STATDS   TITLE 'DISK DATA-SET STATISTICS PROGRAM.'
* PURPOSE :        COMPUTE DATA-SET USEAGE STATISTICS FOR DISK
* ---------        DATA-SETS.
*
* MVS/XA :         MUST BE AMODE AND RMODE 24.
* --------
*
* AUTHOR :         A. BRUCE LELAND
* --------         ADD'S BY : 1. BILL GODFREY
*                                PRC COMPUTER CENTER INC.
*                                MCLEAN, VIRGINIA
*                             2. P.A. MOINIL
*                                COMPUTING CENTRE (TP 361)
*                                J.R.C. - ISPRA ESTABLISHMENT
*                             3. GERHARD POSTPISCHIL, ESP       GP14242
*                                REDID CODE FOR LOCAL MACROS AND
*                                SERVICES, AS THE CBT 454/455 VERSION
*                                FAILED TO ASSEMBLE UNDER MVS.
*                                CHANGED DD NAMES TO IN->SYSIN
*                                OUT->SYSTSPRT/SYSPRINT
*                                TRC->SUMPRINT (PRETTY USELESS?)
*
*
* DESCRIPTION :    THIS PROGRAM FORMATS INFORMATION ON ANY DISK
* -------------    DATA-SET. IT READS THROUGH THE ENTIRE DATA-SET AND
*                  OUTPUTS DISK TRACK USAGE, BLOCKSIZE AND OTHER
*                  STATISTICS.
*
* OPERATION :
* -----------
*        A. THE PROGRAM GETS THE INPUT DATA-SET NAME AND VOLUME SERIAL
*           NUMBER VIA "RDJFCB".
*        B. THE PROGRAM INPUTS THE FORMAT 1 DSCB TO DETERMINE OTHER
*           DATA-SET CHARACTERISTICS.
*        C. THE PROGRAM OPENS THE INPUT FILE, THEN FORMATS AND
*           OUTPUTS DEB AND DSCB INFORMATION.
*        D. IF A PARM OF "LABEL" IS NOT CODED, THE PROGRAM READS
*           EACH BLOCK OF DATA IN THE DATA-SET AND COLLECTS RECORD
*           COUNTS, RECORD SIZE AND TRACK USEAGE STATISTICS.
*        E. ADDITIONAL PROCESSING :
*           1. FOR DSORG=PS, NONE.
*           2. FOR DSORG=DA, NONE.
*           3. FOR DSORG=IS, THE PROGRAM READS THROUGH THE ENTIRE
*              DATA-SET (THERE MAY BE UP TO THREE SEPARATE FILES OF
*              DATA AND REPORTS ON EACH FILE). ALSO, THE PROGRAM
*              INPUTS THE ISAM LABEL (FORMAT TWO DSCB) RECORD AND
*              PROVIDES A DATA-SET PROFILE WHICH INCLUDES DATA-SET
*              REORGANIZATION DATA AND DATA-SET CHARACTERISTICS.
         EJECT
*           4. FOR DSORG=PO, THE PROGRAM COMPARES DIRECTORY TTR'S
*              AGAINST ACTUAL DISK ADDRESSES TO DETERMINE THE TTRS
*              OF "GAS" MEMBERS (THE PROGRAM ALSO OUTPUTS THE FIRST
*              72 CHARACTERS OF ANY GAS MEMBER). STATISTICS ARE
*              MAINTAINED ON THE SIZE OF GAS AND REAL MEMBERS AND
*              THE NUMBER OF ALIAS MEMBERS.
*              IF ANY ALIASES ARE IN THE DATA-SET, A CHECK IS MADE
*              TO SEE THAT CORRESPONDING REAL ENTRIES ALSO EXIST.
*              THE PROGRAM FINDS "GAS" MEMBERS ONLY IF THE DATA-SET
*              HAS NOT BEEN COMPRESSED OUT YET.
*              OPTIONS WHICH MAY BE SELECTED THROUGH THE PARM :
*              1. A PARM OF 'NO' WILL NULLIFY THE GAS TTR REPORT.
*              2. A PARM OF 'FULL' WILL PRODUCE THE LIST OF ALL
*                 DIRECTORY MEMBERS.
*              3. A PARM OF 'GAS(TT.TT.RR)' WILL ADD A MEMBER NAME
*                 TO THE DIRECTORY FOR THE "GAS" MEMBER HAVING THE
*                 SPECIFIED TTR LOCATION (IN HEX, OBTAINED FROM A
*                 PREVIOUS RUN). THIS IS USEFULL IF YOU KNOW WHICH
*                 ONE YOU WANT. ANY USER DATA THAT WAS IN THE
*                 DIRECTORY ENTRY OF A DELETED OR REPLACED MEMBER
*                 CANNOT BE RECOVERED.
*                 A PARM OF 'GAS(ALL)' ALLOWS TO RECOVER ALL "GAS"
*                 MEMBERS IN A DATA-SET.
*              -- NOTE ABOUT LOAD MODULES - THIS PROGRAM CANNOT
*                 RECOVER A "GAS" LOAD MODULE. IT WILL GIVE IT A
*                 MEMBER NAME SO IT WONT BE COMPRESSED OUT, BUT THE
*                 DIRECTORY ENTRY WILL NOT BE A LOAD MODULE TYPE.
*                 TO RECONSTRUCT THE PROPER LOAD MODULE DIRECTORY
*                 WOULD REQUIRE INFORMATION THAT IS NO LONGER IN
*                 THE PARTITIONED DATA-SET.
*              4. A PARM OF 'TEST' MAY BE USED TO OBTAIN MBBCCHHR'S
*                 TRACE (ON 'TRC').
*
* FILE NAMES : DD-NAME 'IN'  - INPUT DATA-SET.
* ------------ DD-NAME 'OUT' - OUTPUT LIST.
*              DD-NAME 'TRC' - TRACE LIST (SEE 'TEST' OPTION).
*
* TSO ACCESS : USE %STATDS COMMAND TO INVOKE THE CLIST DESCRIBED BELOW.
* ------------
*              PROC   1 DSN PARM() DSOUT(*)
*              ALLOC  F(IN) DA(&DSN.) SHR
*              ALLOC  F(OUT) DA(&DSOUT.)
*              STATS  &PARM
*              FREE   F(IN OUT)
*              TSO    END
         EJECT ,
         MACRO ,
&NM      OUTPT  &R0,&R1,&LN=                                    GP14242
         LCLC  &L                                               GP14242
&L       SETC  'L'''                                            GP14242
&NM      MACPARM R0,&R0,OP=L                                    GP14242
         MACPARM R1,&R1                                         GP14242
         MACPARM R15,&LN,NULL=&L&R1                             GP14242
         MACPARM R14,OUTRTN,OP=BAL                              GP14242
         AIF   ('&R1' NE 'WORK').MEND
         MVC   WORK,BLANKS
.MEND    MEND
         SPACE 1
         MACRO
&LBL     TTRMBB &TTR,&MBB=DOUBLE   TTR TO MBBCCHHR CONVERSION MACRO
&LBL     STM   R14,R12,12(R13)
         XR    R0,R0
         ICM   R0,B'1110',&TTR     GET TTR INTO R0
         L     R1,DCBDEBAD         DEB ADDRESS
         LA    R2,&MBB             RESULT ADDRESS
         LR    R3,R13              SAVE SAVEAREA REGISTER
         L     R15,CVTPTR          CVT POINTER
         USING CVT,R15             CVT ADDRESSABILITY
         L     R15,CVTPCNVT        TTRN TO MBBCCHHR ROUTINE ADDRESS
         DROP  R15
         BASR  R14,R15
         LR    R13,R3
         LM    R14,R12,12(R13)
         MEND
         SPACE 1
         MACRO
&LBL     KILL   &N                 GO TO "ABEND WITH DUMP" MACRO
&LBL     LA    R1,&N               SET ABEND CODE
         B     GDMP                GO TO ABEND WITH DUMP.
         MEND
         EJECT
         MACRO
&LBL     HEXES  &SRC,&NUM,&OUT     OUTPUT LOOKS LIKE C1.C2.C3 . . .
         LCLC  &ONE                     FOR NUM SOURCE CHARACTERS
&ONE     SETC  'HX'.'&SYSNDX'
&LBL     XR    R15,R15
         LA    R14,&NUM
         LR    R1,R14
         BCTR  R1,0
         LA    R1,&NUM.(R1,R1)     LAST PERIOD AT (N-1)*3+1
         LA    R1,&OUT.(R1)
         S     R1,=F'3'            OUTPUT START LOCATION
&ONE     LA    R15,&SRC            SOURCE BASE ADDRESS
         XR    R0,R0
         BCTR  R15,0
         IC    R0,0(R14,R15)       INPUT CHARACTER
         LR    R15,R0
         N     R15,=F'15'
         IC    R15,HEXTBL(R15)
         STC   R15,2(,R1)          LOW NIBBLE
         LR    R15,R0
         SRL   R15,4
         IC    R15,HEXTBL(R15)
         STC   R15,1(,R1)          HIGH NIBBLE
         MVI   0(R1),C'.'          SEPARATOR
         S     R1,=F'3'            BACK UP 3 IN OUTPUT ADDRESS
         BCT   R14,&ONE            DO PREVIOUS INPUT CHARACTER
         MVI   3(R1),C' '          BLANK THE LEADING PERIOD
         MEND
         SPACE 2
         PUNCH ' ORDER STATDS(P) '  MAKE DEBUGGING EASIER       GP14242
         SPACE 2
         COPY  ASMOPTS                                          GP14242
         SPACE 1
         ASMPARM LIST=YES    SHOW CURRENT ASSEMBLY OPTIONS      GP14242
         EJECT
         SPACE 1
STATDS   PGMHEAD ZERO31,BASE=(R12,R11,R10,R9),AM=24,RM=24,BNDRY=PAGE,  *
               PARM=R8                                          GP14242
NTEL     EQU   2000 -------------- "SAVETBL" NUMBER OF ELEMENTS -------
NGEL     EQU   2000 -------------- MAX. NUMBER OF "GAS" MEMBERS -------
         SPACE 1
         SERVINIT ,          FIND @SERVICE ROUTINE              GP14242
         SERVLOAD @PRINTER,SUBDSATR                             GP14242
         PRTOPEN SYSPRINT,OPT=(ABEND)  NEED OUTPUT DEVICE       GP14242
         PRTOPEN SUMPRINT,OPT=(NOWTO),DEV=2                     GP14242
         PRTF  HEADER1,TITLE=1                                  GP14242
         PRTF  HEADER2,TITLE=2                                  GP14242
         SPACE 1
         LA    R7,IN              DEFAULT TO SEQUENTIAL         GP14242
         ST@   R7,OCSYSIN,MVI=X'80'    PREPARE OPEN/CLOSE LIST  GP14242
         USING IHADCB,R7
         LA    R1,MYJFCB                                        GP14242
         ST@   R1,EXLIST,MVI=X'87'     PREPARE DCB EXIT LIST    GP14242
         LA    R1,EXLIST                                        GP14242
         STCM  R1,7,DCBEXLSA      SET EXLIST INTO DCB           GP14242
         L     R0,=F'33000'       MIN LENGTH BLOCK              GP14242
         ST    R0,MINBLK                                        GP14242
         ST    R0,MINBLKI                                       GP14242
         LA    R0,255                                           GP14242
         ST    R0,MINR            MIN RECORDS/TRACK             GP14242
         MVI   MINGAS,X'7F'       VERY LARGE NUMBER             GP14242
         MVI   MINREL,X'7F'       VERY LARGE NUMBER             GP14242
         MVI   MINBYTE,X'7F'      VERY LARGE NUMBER             GP14242
         LA    R0,TLG                                           GP14242
         ST    R0,TLC                                           GP14242
         LA    R0,NTEL                                          GP14242
         ST    R0,TLMX                                          GP14242
         MVC   STOWMEM,=C'$GAS0000'    PATTERN FOR GAS MEMBERS  GP14242
         MVI   STOWTTR+3,X'80'         SET ALIAS FLAG           GP14242
         LA    R1,TRCL+6     INIT TRACE POINTERS                GP14242
         ST    R1,TCURPTR                                       GP14242
         ST    R1,TSTRPTR                                       GP14242
         LA    R1,TRCL+4*18  END                                GP14242
         ST    R1,TMAXPTR                                       GP14242
         MVI   FLAGS,GAS     DEFAULT TO PDSGAS MODE             GP14242
         SPACE 1
         PARMLOAD R2,PTR=R8  GET PARM DATA                      GP14242
         LTR   R6,R3         COPY AND TEST LENGTH               GP14242
         BNP   PARMDONE        NO PARM; SKIP OUT                GP14242
         LR    R5,R2         START OF TEXT                      GP14242
         ALR   R6,R5         CHANGE LENGTH TO END ADDRESS       GP14242
         BCTR  R6,0          LAST BYTE                          GP14242
LOOKIN   VLOOK T=VERBTAB,E=(R6),FULL=C'=(',ABBR=3  VERB MATCH   GP14242
         CR    R5,R6         AT LAST BYTE ?                     GP14242
         BNL   PARMDONE                                         GP14242
         LR    R4,R6                                            GP14242
         SR    R4,R5                                            GP14242
         BM    PARMDONE ?                                       GP14242
BADPARM  MVC2  WORK,=C'INVALID PARM:'                           GP14242
         MIN   R4,=A(L'WORK-14)                                 GP14242
         EX    R4,EXMVCPRM   MOVE BAD PARM PORTION              GP14242
         PRTF  WORK,CC=NO                                       GP14242
         MVC   WORK,BLANKS                                      GP14242
         LA    R2,12         SET ERROR CODE                     GP14242
         B     LEAVE           AND GET OUT                      GP14242
EXMVCPRM MVC   WORK+13(*-*),0(R5) COPY BAD TEXT                 GP14242
         SPACE 1
SETGAS   OI    FLAGS,GAS           TURN ON GAS RECORDS (LIST ONLY)
         B     LOOKIN                                           GP14242
SETNO    NI    FLAGS,255-GAS       TURN OFF GAS RECORDS
         B     LOOKIN                                           GP14242
SETGALL  OI    FLAGS,GASALL
         B     LOOKIN                                           GP14242
SETTTR   LR    R4,R6         GET END                            GP14242
         SR    R4,R5         GET RESIDUAL LENGTH - 1            GP14242
         CH    R4,=H'7'      TT.TT.RR - 1                       GP14242
         BL    BADPARM                                          GP14242
         CLI   2(R5),C'.'    GET END                            GP14242
         BNE   BADPARM                                          GP14242
         CLI   5(R5),C'.'                                       GP14242
         BNE   BADPARM                                          GP14242
         MVC   GASTTR(L'GASTTR),0(R1)   SAVE TT.TT.RR
         OI    FLAGS,GASADD
         BAS   R14,HEXCTL
         B     LOOKIN                                           GP14242
SETFUL   OI    FLAGS,FULL          TURN ON FULL LIST
         B     LOOKIN                                           GP14242
SETLAB   OI    FLAGS,LABELS        SET THE "LABELS ONLY" FLAG
         B     LOOKIN                                           GP14242
SETEST   OI    FLAGS,FULL+TEST     TURN ON FULL & TEST LIST
SETU     B     LOOKIN                                           GP14242
         SPACE 1
PARMDONE L     R3,TLMX             ROOM FOR DIRECTORY ENTRIES
         AH    R3,=H'1'
         MH    R3,TLC+2
         LR    R5,R3
         LA    R3,7(,R3)
         SRL   R3,3
         SLL   R3,3
         AL    R3,=A(32776)
         ST    R3,ARLGTH
        GETMAIN R,LV=(R3)
         ST    R1,BUFFER
         STCM  R1,B'0111',CCW3+1
         AL    R1,=A(32776)
         LR    R4,R1
         LA    R14,*
         XR    R15,R15
         ICM   R15,B'1000',BLANKS
         MVCL  R4,R14              SET TO ALL BLANKS
         SL    R1,TLC
         ST    R1,SAVETBL
         B     DONEPARM
         SPACE 1
HEXCTL   MVC   DB,=X'0001030406070205'  CHANGE TT.TT.RR TO TTTTRR..
         TR    DB,0(R5)           RERAANGE                      GP14242
         TRT   0(6,R5),TRTHEX     CHECK FOR VALID HEX           GP14242
         BNZ   BADPARM              FAIL IF NOT HEX             GP14242
         NC    DB,=6X'1F'         STRIP ZONE BITS               GP14242
 TR DB,=X'000A0B0C0D0E0F00000000000000000000102003040506070809' GP14242
         PACK  DB2(4),DB(7)       MAKE HEX TTR                  GP14242
         BR    R14                  RETURN TO CALLER            GP14242
WRONG    PRTF  WRMSG,CC=NO                                      GP14242
         B     ENDALL
         SPACE 1
DONEPARM TM    FLAGS,GASALL        ALL GAS TO RECOVER?
         BZ    NOGALL              NO
         L     R3,GTLGTH           YES, GET RECORDING AREA
         GETMAIN R,LV=(R3)
         ST    R1,GTANK
         LR    R2,R1
         SL    R1,GTSTR+4
         ST    R1,GTSTR
         AL    R1,GTSTR+8
         ST    R1,GTSTR+8
         XR    R15,R15
         MVCL  R2,R14              SET TO ALL ZEROS
NOGALL   BAS   R14,OPENIN          OPEN THE INPUT FILE
         MVC2  WORK,=CL4'DSN='
         MVC   WORK+4(44),JFCBDSNM      DATA-SET NAME
         TRT   WORK,TRTBLANK      FIND NEXT BLANK               GP14242
         MVC2  0(R1),=C',VOL=SER='                              GP14242
         MVC   9(6,R1),JFCBVOLS    ADD THE VOLUME SERIAL        GP14242
         SPACE 1
*------- FORMATS A STRING LIKE "DCB=(RECFM=...)".
         SPACE 1
         LA    R1,WORKAREA   POINT TO WORK SPACE                GP14242
         LA    R0,DS1DSORG   POINT TO ORG//RECFM/BLKS/LRECL     GP14242
         SUBCALL SUBDSATR    CONVERT DCB INFO                   GP14242
         LR    R3,R1         PRESERVE THE ADDRESS               GP14242
         USING DSATR,R3      DECLARE RETURN AREA                GP14242
         LA    R1,WDCB       FORMAT THE DCB INFORMATION         GP14242
         MVC2  0(R1),=C'DCB=(DSORG='                            GP14242
         MVC2  11(R1),DSAORG                                    GP14242
         TRT   WDCB,TRTBLANK                                    GP14242
         MVC2  0(R1),=C',RECFM='                                GP14242
         MVC2  7(R1),DSARECFM                                   GP14242
         TRT   WDCB,TRTBLANK                                    GP14242
         TM    DS1RECFM,DCBRECU   RECFM=U?
         BO    SKIPLEN              YES, NO LRECL MESSAGE       GP14242
         TM    DS1RECFM,DCBRECV+DCBRECSB   VS OR VBS ?          GP14242
         BNO   *+4+4           NO                               GP14242
         OI    SWITCH,FGSPAN DO EXTRA SPANNED PROCESSING        GP14242
         MVC2  0(R1),=C',LRECL='                                GP14242
         MVC2  7(R1),DSALRECL     ADD RECORD LENGTH             GP14242
         LADJ  7(R1),L'DSALRECL   LEFT JUSTIFY                  GP14242
         TRT   WDCB,TRTBLANK                                    GP14242
SKIPLEN  MVC2  0(R1),=C',BLKSIZE='                              GP14242
         MVC2  9(R1),DSABLKSI                                   GP14242
         LADJ  9(R1),L'DSABLKSI   LEFT JUSTIFY                  GP14242
         TRT   WDCB,TRTBLANK                                    GP14242
         MVI   0(R1),C')'
         DROP  R3                                               GP14242
         PRTF  WORK,CC=NO,TITLE=3                               GP14242
         MVC   WORK,BLANKS                                      GP14242
         CLI   DS1EXT1,X'40' LABEL TRACK?                       GP14242
         BNE   SKIPLABX        NO                               GP14242
         PRTL  ' DATA SET HAS A LABEL TRACK'                    GP14242
SKIPLABX TM    FLAGS,GASADD        GAS TO RECOVER?
         BZ    NDCB                NO, BRANCH
         TM    DS1DSORG,DS1DSGPO   PARTITIONED DATA-SET ORG?
         BO    OKPO                YES, BRANCH
         PRTF  NOTPDS,CC=NO                                     GP14242
         B     ENDALL
OKPO     MVC   TEMP(4),DS1LSTAR
        TTRMBB TEMP,MBB=LASTMBB    SAVE THE LAST USED MBBCCHHR
         B     PO
         SPACE 1
NDCB     L     R1,DCBDEBAD
         L     R14,NUMEXT          NUMBER OF EXTENTS
         XR    R0,R0
SUMXTSIZ AH    R0,46(,R1)          TRACKS IN THE EXTENT
         LA    R1,16(,R1)          OFFSET BETWEEN EXTENTS
         BCT   R14,SUMXTSIZ        REPEAT FOR EACH EXTENT
         ST    R0,TOTALTRK         SAVE TRACK TOTAL
         LH    R1,DS1LSTAR         LAST TT USED
         SR    R0,R1               AVAILABLE SPACE + 0.5 TRACKS
         BCTR  R0,0                SUBTRACT ONE MORE
         ST    R0,AVAILTRK
         MVC   TEMP(4),DS1LSTAR
        TTRMBB TEMP,MBB=LASTMBB    SAVE THE LAST USED MBBCCHHR
         OUTPT TOTALTRK,NUMTRKS
         OUTPT AVAILTRK,FRETRKS
         MVC   TEMP(2),=H'0'
         MVC   TEMP+2(2),DS1TRBAL
         OUTPT TEMP,TRBALS         BYTES LEFT ON LAST USED TRACK
         OUTPT NUMEXT,NUMEXTS
         L     R2,DCBDEBAD
         L     R3,NUMEXT
         XR    R1,R1
         IC    R1,24(R2)
         SRL   R1,4
         IC    R1,HEXTBL(R1)
         STC   R1,PROTECT          PROTECTION KEY FOR THIS DATA-SET
         PRTF  PROTECT,CC=NO                                    GP14242
         TM    DS1SCALO,X'C0'      CYLINDER ALLOC?
         BNO   *+L'*+10            NO, BRANCH
         MVC   SCALOS+25(9),=CL9'CYLINDERS'  YES
         B     ALLOCS
         BNZ   *+L'*+10            ABS. TRACKS
         MVC   SCALOS+25(15),=CL15'ABSOLUTE TRACKS'    YES
         B     ALLOCS
         TM    DS1SCALO,X'80'      TRACKS?
         BNO   *+L'*+10            NO, BRANCH
         MVC   SCALOS+25(6),=CL6'TRACKS'     YES
         B     ALLOCS
         MVC   SCALOS+25(6),=CL6'BLOCKS'     BLOCKS
         TM    DS1SCALO,X'01'      ROUND TO CYL?
         BNO   ALLOCS              NO, BRANCH
         MVC   SCALOS+32(12),=CL12'(WITH ROUND)'       YES
ALLOCS   PRTF  SCALOS,CC=NO                                     GP14242
         MVC   TEMP+1(3),DS1SCALO+1     SECONDARY ALLOCATION QUANTITY
         MVI   TEMP,X'00'
         CLC   TEMP,=F'0'          ANY SECONDARIES ALLOWED?
         BNE   SECS                YES, BRANCH
         PRTF  NOSEC,CC=NO         NO                           GP14242
         B     ALLSECS
SECS     OUTPT TEMP,EXTENTS
         SPACE 1
ALLSECS  XR    R4,R4               EXTENT NUMBER
         HEXES DS1LSTAR,3,LSTAR+13                              GP14242
         HEXES LASTMBB,8,LSTAR+24                               GP14242
         PRTF  LSTAR,CC=NO         LAST USED MBBCCHHR           GP14242
         PRTSPACE 1                                             GP14242
         PRTF  EXTHDR,CC=NO                                     GP14242
         PRTF  EXTUND,CC=NO                                     GP14242
         SPACE 1
HEXAGN   CVD   R4,DOUBLE
         LA    R4,1(,R4)
         UNPK  DOUBLE(5),DOUBLE+5(3)
         MVC   WORK+1(2),DOUBLE+3
         OI    WORK+2,X'F0'        EXTENT NUMBER
         CLI   WORK+1,C'0'
         BNE   *+L'*+4
         MVI   WORK+1,C' '
         L     R1,32(R2)           UCB ADDRESS
         MVC   WORK+8(3),13(R1)    UCB
        HEXES  38(R2),4,WORK+14    INITIAL CCHH
        HEXES  42(R2),4,WORK+28    END CCHH
         LH    R1,46(,R2)          NUMBER OF TRACKS IN THIS EXTENT
         CVD   R1,DOUBLE
         UNPK  WORK+41(9),DOUBLE+3(5)
         LA    R1,WORK+40
         LA    R1,1(,R1)
         CLI   1(R1),C'0'          NEXT DIGIT ZERO?
         MVI   0(R1),C' '          REPLACE CURRENT DIGIT WITH BLANK
         BE    *-12                YES, CONTINE BLANKING LEADING ZEROES
         OI    WORK+49,X'F0'       MAKE THE ZONE PRINTABLE
         PRTF  WORK,CC=NO                                       GP14242
         MVC   WORK,BLANKS                                      GP14242
         LA    R2,16(,R2)          NEXT EXTENT DISPLACEMENT
         BCT   R3,HEXAGN
         PRTSPACE 1                                             GP14242
         SPACE 1
         XR    R0,R0
         IC    R0,DS1KEYL
         LTR   R0,R0               KEYLENGTH=0?
         BZ    DSORGTST            YES, BRANCH
         OUTPT (R0),KEYLEN         NO, OUTPUT KEYLEN AND RKP
         MVC   TEMP+2(2),DS1RKP
         OUTPT TEMP,KEYPOS
         PRTSPACE 1                                             GP14242
DSORGTST TM    FLAGS,LABELS        LABEL INFORMATION ONLY DESIRED?
         BO    END3                YES, DO NOT DO ANY READS
         TM    DS1DSORG,DS1DSGPO   PARTITIONED DATA-SET ORGANIZATION?
         BO    PO                  YES, BRANCH. NO -- PS, DA OR IS
INPUTS   BAS   R14,EXCPSTAT        READ EACH BLOCK AND COLLECT STATS
         LTR   R15,R15             END OF FILE?
         BNZ   INPUTX              YES; SKIP OUT OF LOOP        GP14242
         TM    SWITCH,FGSPAN DOING SPANNED PROCESSING ?         GP14242
         BZ    INPUTS
         BAS   R14,SPANNED   GET SPAN COUNTS                    GP14242
         B     INPUTS              CONTINUE READING             GP14242
INPUTX   LR    R2,R15              SAVE FOR LATER
         PRTF  EOFSS,CC=NO                                      GP14242
         TM    DS1DSORG,DS1DSGIS   ISAM?
         BNO   ENDDATA             NO, BRANCH -- END OF DATA-SET
         CLC   MINBLKI,=F'33000'   EVER CHANGED?
         BNE   *+L'*+6             YES, BRANCH
         XR    R14,R14             NO, RESET TO ZERO
         ST    R14,MINBLKI
         OUTPT MINBLKI,MINBLKS
         LA    R14,1
         C     R14,READTOTI
         BH    *+L'*+4
         L     R14,READTOTI        TAKE MAX OF READTOTI AND 1
         L     R1,READTOTI
         SRL   R1,1
         A     R1,BYTECNTI
         XR    R0,R0
         DR    R0,R14              AVG BLK = (BYTECNTI+READTOTI/2) /
         LR    R0,R1                    MAX(READTOTI,1)
         OUTPT (R0),AVGBLKS
         OUTPT MAXBLKI,MAXBLKS
         OUTPT READTOTI,NUMBLKS
         TM    SWITCH,FGSPAN      SPANNED DATA ?                GP14242
         BZ    NOSPAM
         PRTL  '0 SPANNED RECORD DATA:'                         GP14242
         OUTPT NUMSPREC,MSGSPREC  WHOLE RECORDS                 GP14242
         OUTPT NUMSPSTR,MSGSPSTR  START SEGMENTS                GP14242
         OUTPT NUMSPMID,MSGSPMID  MIDDLE SEGMENTS               GP14242
         OUTPT NUMSPEND,MSGSPEND  END SEGMENTS                  GP14242
         OUTPT NUMSPBAD,MSGSPBAD  ERRORS                        GP14242
NOSPAM   PRTSPACE 1                                             GP14242
         MVC   MINBLKI,=F'33000'   RESET
         XR    R14,R14                  THE
         ST    R14,READTOTI                  FILE
         ST    R14,MAXBLKI                        STATISTICS
         ST    R14,BYTECNTI                            VARIABLES
         C     R2,=F'5'            END OF DATA-SET?
         BL    INPUTS              NO, REPORT ON THE NEXT SEGMENT
         SPACE 1
ENDDATA  L     R0,READTOT
         LTR   R0,R0               ANY RECORDS READ?
         BP    ENDPSPO             YES, NORMAL DATA-SET
         PRTF  NULL,CC=NO          NO RECORDS IN THE DATA-SET   GP14242
         B     END3                QUIT
         SPACE 1
PO       XR    R5,R5               CLEAR R5 FOR LATER
         OI    FLAGS,DIRECTOR      TURN ON THE "IN DIRECTORY" FLAG
         TM    FLAGS,GASADD        GAS TO RECOVER?
         BO    NEXTMBR             YES, BRANCH
         CLI   DS1NOBDB,0          LAST DIRECTORY BLOCK IN USE?
         BE    NEXTMBR             NO, BRANCH
         XR    R0,R0
         IC    R0,DS1NOBDB         NUMBER OF BYTES IN USE
         ST    R0,TEMP
         OUTPT TEMP,NOBDBS
         SPACE 1
NEXTMBR  BAS   R14,MEMBERS         GET THE NEXT MEMBER
         B     *+L'*(R15)          BRANCH TABLE
         B     GOODMBR             SUCCESSFUL
         B     MBREOF              EOF IN DIRECTORY
         B     LASTUSED            LAST USED ENTRY FOUND
         B     NOTDIREC            NOT A DIRECTORY RECORD
         SPACE 1
GOODMBR  LA    R2,REALMBR          SET A REAL ENTRY
         NI    SWITCH,255-ALIAS    TURN OFF THE ALIAS BIT FOR NAME
         TM    11(R1),X'80'        ALIAS ENTRY?
         BZ    NOTAL               NO, BRANCH
         LA    R2,ALIASES          YES, AN ALIAS ENTRY
         OI    SWITCH,ALIAS        TURN ON THE ALIAS BIT FOR NAME
NOTAL    L     R14,0(,R2)
         LA    R14,1(,R14)         INCREMENT ALIASES OR REALMBR
         ST    R14,0(,R2)
         LR    R2,R1
        TTRMBB 8(R1),MBB=DOUBLE    CONVERT TTR TO MBBCCHHR
         TM    FLAGS,FULL          FULLY LIST REQUESTED?
         BZ    STTBL               NO, BRANCH
         TM    SWITCH,DIRPR
         BO    PRDIR
         OI    SWITCH,DIRPR
         PRTF  DIRTLA,CC=NO                                     GP14242
         PRTF  DIRTL1,CC=NO                                     GP14242
         PRTF  DIRTL2,CC=NO                                     GP14242
PRDIR    MVC   WORK,BLANKS         CLEAR WORK AREA
         MVC   WORK(8),0(R2)       MOVE MEMBER OR ALIAS NAME
         MVC   WORK+8(3),=C'(M)'   INDICATE MEMBER NAME
         TM    SWITCH,ALIAS        ALIAS ENTRY?
         BZ    *+L'*+4             NO, OK
         MVI   WORK+9,C'A'         INDICATE ALIAS NAME
        HEXES  8(R2),3,WORK+15     TTR
        HEXES  DOUBLE,8,WORK+26    MBBCCHHR
         PRTF  WORK,CC=NO                                       GP14242
         MVC   WORK,BLANKS                                      GP14242
STTBL    L     R1,SAVETBL
TBLINC   AL    R1,TLC
         USING TELDSECT,R1                                      GP14242
         CLI   0(R1),C' '          EMPTY TABLE ENTRY?
         BE    INSERT              YES, BRANCH
         CLC   TLADDR(L'TLADDR),DOUBLE  IS IT THIS ENTRY?
         BNE   TBLINC              NO, CHECK NEXT ENTRY
         TM    SWITCH,ALIAS        ALIAS?
         BO    NEXTMBR             YES, BRANCH
         MVC   TLNAME(L'TLNAME),0(R2)   NO, USE THE REAL MEMBER NAME
         B     NEXTMBR
INSERT   MVC   TLNAME(L'TLNAME),0(R2)   INSERT THE NAME
         MVC   TLADDR(L'TLADDR),DOUBLE  INSERT THE MBBCCHHR
         MVC   TLTTR(L'TLTTR),8(R2)     INSERT THE TTR
         MVI   TLSTAT,0
         TM    SWITCH,ALIAS        ALIAS?
         BZ    *+L'*+4             NO, BRANCH
         OI    TLSTAT,TLALIAS      INDICATE ALIAS NAME
         DROP  R1
         LR    R5,R1               SAVE THE "HIGH-WATER" MARK
         L     R1,REALENT
         LA    R1,1(,R1)           COUNT THE NUMBER OF TABLE ENTRIES
         ST    R1,REALENT
         C     R1,TLMX
         BL    NEXTMBR
         PRTF  MANYENT,CC=NO                                    GP14242
        KILL   1 ------- TOO MANY MEMBER ENTRIES IN THE TABLE *** 1 ***
NOTDIREC XR    R15,R15             SIMULATED END OF FILE (GOOD READ)
MBREOF   L     R1,READTOT          END OF FILE IN DIRECTORY
         ST    R1,USEDBLK          USED COUNT
         ST    R1,ALLOCBLK         ALLOCATED COUNT
         B     PRESORT
         SPACE 1
LASTUSED L     R0,READTOT          END OF USED ENTRIES IN DIRECTORY
         ST    R0,USEDBLK
RPTEXCP  BAS   R14,EXCPSTAT        READ AND COLLECT STATISTICS
         LTR   R15,R15
         BZ    RPTEXCP
         L     R1,READTOT
         ST    R1,ALLOCBLK         ALLOCATED COUNT
PRESORT  XR    R0,R0
         XI    FLAGS,DIRECTOR      TURN OFF THE "IN DIRECTORY" FLAG
         ST    R15,TEMP            SAVE FOR LATER
         ST    R0,READTOT          REINITIALIZE READTOT
         ST    R0,MAXBLK                MAXBLK
         ST    R0,BYTECNT                    BYTECNT
         MVC   MINBLK(4),=F'33000' REINITIALIZE MINBLK
         SPACE 1
*------- SORT THE MEMBER ARRAY BY MBBCCHHR (USES A SHELLSORT).
         SPACE 1
         L     R3,REALENT          NUMBER OF ELEMENTS TO SORT
         LTR   R3,R3
         BNP   SORTED
         MH    R3,TLC+2            GAP IS LENGTH * NO. OF ELEMENTS
         LR    R0,R3               N=GAP
SHELLG   XR    R2,R2               GAP=GAP/2  (DROP FRACTION BITS)
         D     R2,TLC
         SRL   R3,1
         LTR   R3,R3               GAP LESS THAN ONE?
         BZ    SORTED              YES, DONE
         MH    R3,TLC+2
         L     R2,SAVETBL          TABLE BASE ADDRESS
         LR    R4,R3               I=GAP
SHELLI   AL    R4,TLC              I=I+1 (TABLE WIDTH)
         CR    R4,R0               I<=N?
         BH    SHELLG              NO, HALVE THE GAP
         LR    R1,R4               YES, J=I
SHELLJ   SR    R1,R3               J=J-GAP
         BNP   SHELLI              IF J<=0, INCREMENT I
         LA    R15,0(R2,R1)        ADDRESS OF SAVETBL(J)
         LA    R14,0(R3,R1)        JG=J+GAP
         LA    R14,0(R2,R14)       ADDRESS OF SAVETBL(JG)
         CLC   8(8,R15),8(R14)     MBBCCHHR(J) <= MBBCCHHR(JG)
         BNH   SHELLI              YES, INCREMENT I
         XC    0(TLG,R14),0(R15)   INTERCHANGE
         XC    0(TLG,R15),0(R14)        SAVETBL ENTRIES
         XC    0(TLG,R14),0(R15)             J AND JG
         B     SHELLJ              INCREMENT J
         SPACE 1
SORTED   TM    SWITCH,DIRPR
         BZ    LKTBL
         PRTSPACE 1                                             GP14242
         PRTF  DIRTLB,CC=NO                                     GP14242
         PRTF  DIRTL1,CC=NO                                     GP14242
         PRTF  DIRTL2,CC=NO                                     GP14242
         L     R2,SAVETBL
         AL    R2,TLC
         MVC   WORK,BLANKS         CLEAR WORK AREA
         USING TELDSECT,R2                                      GP14242
PSDIR    MVC   WORK(L'TLNAME),TLNAME    MOVE MEMBER NAME
         MVC   WORK+L'TLNAME(3),=C'(M)' INDICATE MEMBER NAME
        HEXES  TLTTR,L'TLTTR,WORK+15    TTR
        HEXES  TLADDR,L'TLADDR,WORK+26  MBBCCHHR
         PRTF  WORK,CC=NO                                       GP14242
         MVC   WORK,BLANKS                                      GP14242
         AL    R2,TLC
         CLI   0(R2),C' '          EMPTY TABLE ENTRY?
         BNE   PSDIR               NO, BRANCH
         DROP  R2
         PRTSPACE 1                                             GP14242
         L     R2,SAVETBL
         B     NULLPDS
LKTBL    L     R2,SAVETBL
         LTR   R5,R5               ANY MEMBERS?
         BZ    NULLPDS             NO, BRANCH
         TM    FLAGS,GASADD        GAS TO RECOVER?
         BO    NULLPDS             YES, BRANCH
         USING TELDSECT,R5                                      GP14242
         MVC   LASTMEMS(L'TLNAME),TLNAME     LAST MEMBER IN THE PDS
         DROP  R5
         PRTF  LASTMEMS,CC=NO                                   GP14242
NULLPDS  L     R15,TEMP            RESTORE THE RETURN CODE
         SPACE 1
NXTREAL  AL    R2,TLC              LAST MEMBER WAS REAL
         USING TELDSECT,R2                                      GP14242
         TM    FLAGS,TEST          TRACE REQUESTED?
         BZ    NXTONE              NO, BRANCH
         XR    R1,R1               TRACE TABLED MBBCCHHR
         BAS   R14,TRACE
NXTONE   L     R0,BYTECNT          LAST MEMBER WAS GAS
         ST    R0,SAVETOT          SAVE FOR LATER
         C     R15,=F'8'           END OF DATA-SET?
         BE    LASTMTCH            YES, BRANCH
         OI    FLAGS,REALONE       SET FLAG FOR ACTUAL MEMBER
NXTTBL   MVC   CURRMBR(L'TLNAME),TLNAME SAVE THE CURRENT MEMBER NAME
         CLC   TLADDR(L'TLADDR),IOBSEEK NEXT MBBCCHHR IN THE TABLE?
         BE    REALRD              YES, BRANCH TO READ LOOP
         BH    NOABE13             NO, HIGHER -- A "GAS" MEMBER
         CLI   TLG(R2),C' '        EMPTY TABLE?
         BE    ABE13               YES, BRANCH
         PRTSPACE 1                NO, INVALID TTR              GP14242
         MVC   INVTTR+9(L'TLNAME),TLNAME SET MEMBER NAME IN MESSAGE
         PRTF  INVTTR,CC=NO                                     GP14242
         AL    R2,TLC              BUMP TABLE POINTER
         TM    FLAGS,TEST          TRACE REQUESTED?
         BZ    NXTTBL              NO, BRANCH
         XR    R1,R1               TRACE TABLED MBBCCHHR
         BAS   R14,TRACE
         B     NXTTBL              TRY WITH NEXT
ABE13    PRTF  TTRTOLOW,CC=NO                                   GP14242
        KILL   2 ------- TABLED TTR < CURRENT TTR *************** 2 ***
NOABE13  L     R1,GASCNT           NO, HIGHER -- A "GAS" MEMBER
         LA    R1,1(,R1)
         ST    R1,GASCNT           ADD ONE TO THE GAS MEMBER COUNT
         XI    FLAGS,REALONE       TURN OFF THE ACTUAL MEMBER FLAG
         TM    FLAGS,GAS           GAS RECORDS DESIRED?
         BNO   GASRD               NO, BRANCH
         L     R0,TRACKNUM         CURRENT TRACK NUMBER (RELATIVE 1)
         BCTR  R0,0                CURRENT TRACK NUMBER (RELATIVE 0)
         L     R1,LASTR            CURRENT RECORD NUMBER
         LA    R1,1(,R1)           NEXT RECORD NUMBER (R OF TTR)
         CLC   CURMBB(7),IOBSEEK   SWITCHED TRACKS?
         BE    *+L'*+6             NO, BRANCH
         LA    R1,1                RECORD ONE
         AR    R0,R1               NEXT RELATIVE TRACK
         STH   R0,TEMP             SAVE RELATIVE TRACK NUMBER
         STC   R1,TEMP+2           SAVE RECORD NUMBER
        HEXES  TEMP,3,GASMEM+21
         TM    FLAGS,GASADD        GAS TO RECOVER?
         BZ    NORCV               NO, BRANCH
         OC    STOWTTR,STOWTTR     ALREADY RESOLVED?
         BNZ   NORCV               YES, BRANCH
         TM    FLAGS,GASALL        ALL GAS TO RECOVER?
         BZ    NOGAST              NO
         L     R1,GTSTR
         LM    R14,R15,GTSTR+4
         BXLE  R1,R14,SVTTR
SWOVG    NOP   NORCV
         OI    SWOVG+1,X'F0'
         PRTF  GOVFLOW,CC=NO                                    GP14242
         B     NORCV
SVTTR    MVC   0(L'STOWTTR,R1),TEMP     SAVE TTR FOR STOW
         ST    R1,GTSTR
         B     NORCV
NOGAST   CLC   GASTTR(L'GASTTR),GASMEM+20    IS THIS THE ONE?
         BNE   NORCV               NO, BRANCH
         MVC   STOWTTR(L'STOWTTR),TEMP  SAVE TTR FOR STOW
NORCV    TM    DS1RECFM,DCBRECU    LRECL=U?
         BNO   DODATA              NO, BRANCH
         PRTF  GASMEM,28,CC=NO      YES, DO NOT DO A DATA LINE  GP14242
         B     GASRD
         SPACE 1
DODATA   PRTSPACE 1                                             GP14242
         PRTF  GASMEM,CC=NO                                     GP14242
         BAS   R14,EXCPSTAT        INPUT THE NEXT REAL BLOCK
         TM    SWITCH,FGSPAN DOING SPANNED PROCESSING ?         GP14242
         BZ    *+8
         BAS   R14,SPANNED   GET SPAN COUNTS                    GP14242
         L     R2,BUFFER     GET BUFFER ADDRESS                 GP14242
         ST    R15,TEMP            SAVE THE RETURN CODE
         L     R15,LS
         MIN   R15,=F'72'                                       GP14242
         LTR   R15,R15
         BNZ   *+L'*+8
         LA    R15,1         IF NULL LENGTH, USE ONE BYTE AND
         MVI   (R2),C' '     BLANK THE FIRST BYTE OF BUFFER
         PRTF  0(R2),(R15),CC=NO                                GP14242
         L     R15,TEMP            RESTORE THE RETURN CODE
         B     GASRDF              SKIP THE FIRST READ (ALREADY DONE)
         SPACE 1
GASRD    BAS   R14,EXCPSTAT        "GAS" MEMBER
         L     R1,GASRDS
         LA    R1,1(,R1)           ADD ONE TO THE GAS RECORD COUNT
         ST    R1,GASRDS
         TM    FLAGS,TEST          TRACE REQUESTED?
         BZ    GASRDF              NO, BRANCH
         LA    R1,IOBSEEK          TRACE NEXT MBBCCHHR
         BAS   R14,TRACE
GASRDF   LTR   R15,R15             (DON'T COUNT THE FIRST ONE)
         BNZ   GASRDE
         CLC   TLADDR(L'TLADDR),IOBSEEK NEXT MBBCCHHR IN THE TABLE?
         BH    GASRD               NO, CONTINUE
*                                  YES, END OF "GAS", THUS
GASRDE   L     R0,BYTECNT               START OF A "REAL"
         S     R0,SAVETOT          GAS BYTES THIS MEMBER
         L     R1,GASBYTE
         AR    R1,R0
         ST    R1,GASBYTE          TOTAL GAS BYTES
         C     R0,MAXGAS
         BNH   *+L'*+4
         ST    R0,MAXGAS           MAX BYTES/GAS MEMBER
         C     R0,MINGAS
         BNL   NXTONE
         ST    R0,MINGAS           MIN BYTES/GAS MEMBER
         B     NXTONE
         SPACE 1
REALRD   BAS   R14,EXCPSTAT        "REAL" MEMBER
         TM    FLAGS,TEST          TRACE REQUESTED?
         BZ    REALRDF             NO, BRANCH
         LA    R1,IOBSEEK          TRACE NEXT MBBCCHHR
         BAS   R14,TRACE
REALRDF  LTR   R15,R15             END OF FILE?
         BNZ   REALRDE             YES, END OF "REAL"
         CLI   TLG(R2),C' '        EMPTY TABLE?
         BE    REALRD              NO, CONTINUE
         CLC   TLADDR+TLG(L'TLADDR),IOBSEEK NEXT MBBCCHHR IN TABLE?
         BH    REALRD              NO, CONTINUE
         PRTSPACE 1                                             GP14242
         MVC   NOMEOF+9(8),CURRMBR SET MEMBER NAME IN MESSAGE
         PRTF  NOMEOF,CC=NO        END OF "REAL", BUT NO EOF    GP14242
         LA    R15,4               SET RETURN CODE
REALRDE  L     R0,BYTECNT
         S     R0,SAVETOT
         L     R1,RELBYTE
         AR    R1,R0
         ST    R1,RELBYTE          TOTAL BYTES FOR REAL MEMBERS
         C     R0,MAXREL
         BNH   *+L'*+4
         ST    R0,MAXREL           MAX BYTES/REAL MEMBER
         C     R0,MINREL
         BNL   NXTREAL
         ST    R0,MINREL           MIN BYTES/REAL MEMBER
         B     NXTREAL
         SPACE 1
LASTMTCH CLI   0(R2),C' '
         BE    ENDPO
         PRTF  NOTFOUND,CC=NO                                   GP14242
        KILL   3 ------- NOT ALL MEMBERS IN THE TABLE FOUND ***** 3 ***
ENDPO    TM    FLAGS,GASADD        GAS TO RECOVER?
         BO    STOW                YES, BRANCH
         DROP  R2
         TM    FLAGS,TEST          TRACE REQUESTED?
         BZ    ENDPSPO             NO, BRANCH
         LNR   R1,R1                                            GP14242
         BAS   R14,TRACE
ENDPSPO  PRTSPACE 1                                             GP14242
         OUTPT MINR,MINRS
         L     R15,TRACKNUM
         SRL   R15,1
         A     R15,ALLR            (ALLR + TRACKNUM/2) / TRACKNUM
         XR    R14,R14
         D     R14,TRACKNUM        AVERAGE RECORDS PER TRACK
         LR    R0,R15
         OUTPT (R0),AVGRS
         OUTPT MAXR,MAXRS
         PRTSPACE 1                                             GP14242
         L     R15,TRACKNUM
         SRL   R15,1
         A     R15,TRKBYTE         (TRKBYTE + TRACKNUM/2) / TRACKNUM
         XR    R14,R14
         D     R14,TRACKNUM        AVERAGE BYTES PER TRACK
         ST    R15,AVGBTRK
         OUTPT MINBYTE,MINBYTES    MIN BYTES/TRACK
         OUTPT AVGBTRK,AVGBYTES    AVG BYTES/TRACK
         OUTPT MAXBYTE,MAXBYTES    MAX BYTES/TRACK
         PRTSPACE 1                                             GP14242
         OUTPT TRKBYTE,TOTBYTS     TOTAL DATA BYTES ON ALL TRACKS
         PRTSPACE 1                                             GP14242
         CLC   MINBLK,=F'33000'    EVER CHANGED?
         BNE   *+L'*+6             YES, BRANCH
         XR    R14,R14             NO, RESET TO ZERO
         ST    R14,MINBLK
         OUTPT MINBLK,MINBLKS
         LA    R14,1
         C     R14,READTOT
         BH    *+L'*+4
         L     R14,READTOT         TAKE MAX OF READTOT AND 1
         L     R1,READTOT
         SRL   R1,1
         A     R1,BYTECNT
         XR    R0,R0
         DR    R0,R14              AVG BLK = (BYTECNT+READTOT/2)
         LR    R0,R1                    MAX(READTOT,1)
         OUTPT (R0),AVGBLKS
         OUTPT MAXBLK,MAXBLKS
         PRTSPACE 1                                             GP14242
         OUTPT READTOT,NUMBLKS
         TM    SWITCH,FGSPAN      SPANNED DATA ?                GP14242
         BZ    NOSPAN
         PRTL  '0 SPANNED RECORD DATA:'                         GP14242
         OUTPT NUMSPREC,MSGSPREC  WHOLE RECORDS                 GP14242
         OUTPT NUMSPSTR,MSGSPSTR  START SEGMENTS                GP14242
         OUTPT NUMSPMID,MSGSPMID  MIDDLE SEGMENTS               GP14242
         OUTPT NUMSPEND,MSGSPEND  END SEGMENTS                  GP14242
         OUTPT NUMSPBAD,MSGSPBAD  ERRORS                        GP14242
NOSPAN   TM    DS1DSORG,DS1DSGPO   DSORG=PO?
         BNO   END3
         OUTPT GASRDS,GASBLKS
         OUTPT EOFS,ENDMBR
         OUTPT GASCNT,GASMBR
         L     R0,GASCNT
         LTR   R0,R0
         BZ    NOGASM
         PRTSPACE 1                                             GP14242
         OUTPT MINGAS,MINGASS      MIN BYTES PER GAS MEMBER
         L     R15,GASCNT
         SRL   R15,1
         A     R15,GASBYTE
         XR    R14,R14
         D     R14,GASCNT
         LR    R0,R15
         OUTPT (R0),AVGGASS        AVG BYTES PER GAS MEMBER
         OUTPT MAXGAS,MAXGASS      MAX BYTES PER GAS MEMBER
         PRTSPACE 1                                             GP14242
         L     R1,GASBYTE
         LA    R15,100
         MR    R0,R15
         A     R1,=F'5'
         D     R0,AVGBTRK    TRKS*100 = (GASBYTE*100+5) / AVGBTRK
         SR    R0,R0                                            GP14242
         D     R0,=F'100'    UNSCALE                            GP14242
         CVD   R1,DB         (DOES THE REQUIRED ROUNDING)       GP14242
         MVC   GASTRK(13),=X'4020206B2020206B2021204B20'        GP14242
         ED    GASTRK(13),DB+3     FORMAT NN,NNN,NNN.N          GP14242
         PRTF  GASTRK,CC=NO        OUTPUT GAS TRACK TOTAL       GP14242
NOGASM   PRTSPACE 1                                             GP14242
         L     R0,EOFS
         S     R0,GASCNT
         BNP   NOREAL
         ST    R0,TEMP
         OUTPT MINREL,MINRELS      MIN BYTES PER REAL MEMBER
         L     R15,TEMP
         SRL   R15,1
         A     R15,RELBYTE
         XR    R14,R14
         D     R14,TEMP
         LR    R0,R15
         OUTPT (R0),AVGRELS        AVG BYTES PER MEMBER
         OUTPT MAXREL,MAXRELS      MAX BYTES PER MEMBER
         B     SOMEARE
NOREAL   PRTF  NOREALS,CC=NO                                    GP14242
SOMEARE  PRTSPACE 1                                             GP14242
         OUTPT ALLOCBLK,ALLBLK
         OUTPT USEDBLK,USEBLK
         L     R1,REALMBR
         A     R1,ALIASES
         ST    R1,MINBLK
         OUTPT MINBLK,USENAME
         L     R1,ALIASES
         LTR   R1,R1
         BZ    END3
         OUTPT ALIASES,ALSNAME
         CLC   REALMBR,REALENT     ANY ALIASES WITHOUT REALS?
         BE    END3                NO, DONE
         PRTSPACE 1                                             GP14242
         BAS   R14,REREAD
         L     R3,ALIASES
TRYAGN   L     R2,SAVETBL
         USING TELDSECT,R2                                      GP14242
         BAS   R14,MEMBERS
         LTR   R15,R15
         BP    END3
         TM    11(R1),X'80'        ALIAS?
         BNO   TRYAGN              NO, GET THE NEXT ENTRY
         MVC   ALNOREAL(8),0(R1)   PREPARE ERROR MESSAGE
        TTRMBB 8(R1),MBB=DOUBLE
REALAGN  AL    R2,TLC
         CLC   TLADDR(L'TLADDR),DOUBLE  CORRESPONDING TABLE ENTRY?
         BNE   REALAGN             NO, TRY THE NEXT ONE
         TM    TLSTAT,TLALIAS      YES, ALIAS (IN THE TABLE)?
         BZ    FOUNDIT             NO, BRANCH
         PRTF  ALNOREAL,CC=NO      YES, ALIAS WITH NO REAL      GP14242
FOUNDIT  BCT   R3,TRYAGN
         DROP  R2
         SPACE 1
END3     CLOSE MF=(E,OCSYSIN)      END POINT IF DSORG=PO, PS OR DA
         TM    DS1DSORG,DS1DSGIS   ISAM DATA-SET?
         BNO   ENDALL              NO, BRANCH
         OI    JFCBTSDM,JFCNWRIT   PREVENT REVERSE MERGE TO JFCB
         MVI   JFCBDSNM,X'04'      SET-UP DSNAME FOR
         MVC   JFCBDSNM+1(43),JFCBDSNM  FORMAT 4 DSCB
         OPEN  (IN,INPUT),TYPE=J,MF=(E,OCSYSIN)  DO OPEN JFCB   GP14242
         TM    DCBOFLGS,DCBOFOPN   OPENED CORRECTLY?
         BZ    ABE40               NO, QUIT
         BAS   R14,REREAD
         MVC   IOBSEEK+3(5),DS1PTRDS    POINTER TO FORMAT 2 DSCB
         BAS   R14,EXCP            READ THE FORMAT 2 DSCB
         LTR   R15,R15
         BZ    NOABE11
         PRTF  NODSCB2,CC=NO       FORMAT 2 DSCB NOT READ CORRECTLY
        KILL   4 ------- READ FMT 2 DSCB ERROR ****************** 4 ***
NOABE11  PRTSPACE 1                                             GP14242
         PRTF  DSCB2,CC=NO                                      GP14242
         XR    R0,R0
         IC    R0,DS2NOLEV
         ST    R0,TEMP
         OUTPT (R0),DSSNOLEV       INDEX LEVELS
         MVC   TEMP+3(1),DS2DVIND
         OUTPT TEMP,DSSDVIND       TRACKS FOR MASTER INDEX
         MVC   TEMP+3(1),DS2CYLOV
         OUTPT TEMP,DSSCYLOV       TRACKS OF CYL. OVERFLOW PER CYLINDER
         MVC   TEMP+2(2),DS2TAGDT
         OUTPT TEMP,DSSTAGDT       TAGGED FOR DELETION
         MVC   TEMP+1(3),DS2RORG3
         OUTPT TEMP,DSSRORG3       OVERFLOW RECORD ACCESSES
         LH    R0,DS2NOBYT
         OUTPT (R0),DSSNOBYT       CORE TO HOLD THE HIGHEST INDEX
         XR    R0,R0
         IC    R0,DS2NOTRK
         OUTPT (R0),DSSNOTRK       TRACKS OCCUPIED BY THE HIGHEST INDEX
         MVC   TEMP(4),DS2PRCTR
         OUTPT TEMP,DSSPRCTR       RECORDS IN THE PRIME AREA
         LH    R0,DS2BYOVL
         OUTPT (R0),DSSBYOVL       BYTES LEFT ON CUR. IND. OVER. TRACK
         LH    R0,DS2RORG2
         OUTPT (R0),DSSRORG2       TRACKS LEFT IN IND. OVERFLOW AREA
         LH    R0,DS2OVRCT
         OUTPT (R0),DSSOVRCT       RECORDS IN THE OVERFLOW AREA
         LH    R0,DS2RORG1
         OUTPT (R0),DSSRORG1       CYLINDER OVERFLOW AREAS (FULL)
         CLOSE MF=(E,OCSYSIN)                                   GP14242
         B     ENDALL
         SPACE 1
STOW     CLOSE MF=(E,OCSYSIN)                                   GP14242
         TM    FLAGS,GASALL        ALL GAS TO RECOVER?
         BO    STGALL              YES
         OC    STOWTTR,STOWTTR     GAS (TTR) ENCOUNTERED?
         BNZ   STOWIT              YES, BRANCH
         PRTF  NTFGAS,CC=NO                                     GP14242
         B     ENDALL
STGALL   CLC   GTSTR,GTANK         ANY GAS FOUND?
         BL    NGDS                NO
         MVC   GTSTR+8,GTSTR       YES, SET POINTERS
         MVC   GTSTR,GTANK
         B     STOWIT
NGDS     PRTF  NGASDS,CC=NO                                     GP14242
         B     ENDALL
STOWIT   OPEN  (PDS,UPDAT),MF=(E,OCSYSIN)                       GP14242
         LA    R3,1                MEMBER NAME SEQUENCE NUMBER
STOWLP   CVD   R3,DOUBLE
         UNPK  STOWMEM+4(4),DOUBLE+5(3)
         OI    STOWMEM+7,C'0'
         L     R2,SAVETBL
         USING TELDSECT,R2                                      GP14242
STOWST   AL    R2,TLC
         CLI   0(R2),C' '          EMPTY TABLE ENTRY?
         BE    STOWOK              YES, BRANCH
         CLC   TLNAME(L'TLNAME),STOWMEM IS IT THIS ENTRY?
         BNE   STOWST              NO, CHECK NEXT ENTRY
         DROP  R2
STOWNX   LA    R3,1(R3)            NEXT SEQUENCE NUMBER
         CL    R3,=F'9999'         MAXIMUM REACHED?
         BNH   STOWLP              NO
         TM    FLAGS,GASALL        YES, ALL GAS TO RECOVER?
         BO    GALLUN              YES
         PRTF  GUNABLE,CC=NO                                    GP14242
         B     STOWEX
GALLUN   PRTF  TOOMUCH,CC=NO                                    GP14242
         B     STOWEX
STOWOK   TM    FLAGS,GASALL        ALL GAS TO RECOVER?
         BZ    *+L'*+10            NO
         L     R1,GTSTR            YES, SET TTR OF GAS
         MVC   STOWTTR(L'STOWTTR),0(R1)
         XR    R15,R15
        STOW   PDS,STOWMEM,A
         MVC   WORK,BLANKS         CLEAR WORK AREA
         MVC2  WORK,=C'MEMBER'                                  GP14242
         MVC   WORK+7(L'STOWMEM),STOWMEM
         MVC   WORK+L'STOWMEM+8(L'STOWMSGA),STOWMSGA
         LTR   R15,R15
         BNZ   STOWERR
         TM    FLAGS,GASALL        ALL GAS TO RECOVER?
         BO    *+L'*+10            YES
         MVC   WORK+L'STOWMEM+L'STOWMSGA+9(L'GASTTR),GASTTR
         B     STOWMS
         UNPK  DOUBLE(2*L'STOWTTR+1),STOWTTR(L'STOWTTR+1)
         TR    DOUBLE(2*L'STOWTTR),HEXTBL-X'F0'
         MVC   WORK+L'STOWMEM+L'STOWMSGA+9(2),DOUBLE
         MVI   WORK+L'STOWMEM+L'STOWMSGA+11,C'.'
         MVC   WORK+L'STOWMEM+L'STOWMSGA+12(2),DOUBLE+2
         MVI   WORK+L'STOWMEM+L'STOWMSGA+14,C'.'
         MVC   WORK+L'STOWMEM+L'STOWMSGA+15(2),DOUBLE+4
         PRTF  WORK,CC=NO                                       GP14242
         MVC   WORK,BLANKS                                      GP14242
         L     R1,TOTGAS
         AH    R1,=H'1'
         ST    R1,TOTGAS
         L     R1,GTSTR
         LM    R14,R15,GTSTR+4
         BXLE  R1,R14,STOWONA
         L     R1,TOTGAS
         CVD   R1,DOUBLE
         MVC   LASTMBB(6),=XL6'402020202120'
         ED    LASTMBB(6),DOUBLE+5
         MVC   ENDGALL(5),LASTMBB+1
         PRTF  ENDGALL,CC=NO                                    GP14242
         B     STOWEX
STOWONA  ST    R1,GTSTR
         B     STOWNX
STOWERR  MVC   WORK+L'STOWMEM+8(L'STOWMSGB),STOWMSGB
         STC   R15,WORK+L'STOWMEM+L'STOWMSGB-9
         SRL   R15,4
         STC   R15,WORK+L'STOWMEM+L'STOWMSGB-10
         NC    WORK+L'STOWMEM+L'STOWMSGB-10(2),=XL2'0F0F'
         TR    WORK+L'STOWMEM+L'STOWMSGB-10(2),HEXTBL
         STC   R0,WORK+L'STOWMEM+L'STOWMSGB-1
         SRL   R0,4
         STC   R0,WORK+L'STOWMEM+L'STOWMSGB-2
         NC    WORK+L'STOWMEM+L'STOWMSGB-2(2),=XL2'0F0F'
         TR    WORK+L'STOWMEM+L'STOWMSGB-2(2),HEXTBL
STOWMS   PRTF  WORK,CC=NO                                       GP14242
         MVC   WORK,BLANKS                                      GP14242
STOWEX   DS    0H
         SPACE 1
ENDALL   CLOSE ,MF=(E,OCSYSIN)                                  GP14242
         XR    R2,R2               RC = 0 -----------------------------
LEAVE    LM    R0,R1,GTLGTH        YES
         LTR   R0,R0         LENGTH SET?                        GP14242
         BZ    NOFGAR                                           GP14242
         LTR   R1,R1         ADDRESS SET?                       GP14242
         BZ    NOFGAR                                           GP14242
        FREEMAIN R,A=(1),LV=(0)
NOFGAR   LM    R0,R1,ARLGTH
         LTR   R0,R0         LENGTH SET?                        GP14242
         BZ    SERVTERM                                         GP14242
         LTR   R1,R1         ADDRESS SET?                       GP14242
         BZ    SERVTERM                                         GP14242
        FREEMAIN R,A=(1),LV=(0)
SERVTERM SERVTERM ,                                             GP14242
         PGMEXIT RC=(R2)                                        GP14242
SETRC    LA    R2,16               RC = 16 ----------------------------
         B     ENDALL                                           GP14242
         EJECT
*------ ISSUES EXCP'S AND GATHERS STATISTICS.
         SPACE 1
EXCPSTAT ST    R14,STATR14
         BAS   R14,EXCP
         B     *+L'*(R15)
         B     R15IS0              GOOD READ
         B     R15IS4              END OF FILE OR END OF MEMBER
         B     R15IS8              END OF DATA-SET
R15IS12  PRTSPACE 1                I/O OR OTHER DISK ERROR      GP14242
         L     R14,INAROW
         LA    R14,1(,R14)         UPDATE NUMBER OF CONSECUTIVE ERRORS
         ST    R14,INAROW
         C     R14,=F'10'          ALLOW ONLY 10 CONSECUTIVE ERRORS
         BL    CONTINUE
         PRTF  TOOMANY,CC=NO       TOO MANY CONSECUTIVE ERRORS  GP14242
        KILL   5 ------- PERMANENT I/O DISK ERROR *************** 5 ***
CONTINUE HEXES SAVECB,1,IOERROR+21 ECB ERROR CODE
        HEXES  CURMBB+3,5,IOERROR+33    CURRENT DISK CCHHR
         PRTF  IOERROR,CC=NO                                    GP14242
         TM    DS1DSORG,DS1DSGPO   PDS?
         BNO   BLKNUM              NO, BRANCH
         TM    FLAGS,DIRECTOR      ERROR IN THE DIRECTORY?
         BO    DIRERR              YES, BRANCH
         TM    FLAGS,REALONE       ACTUAL MEMBER?
         BNO   GASERR              NO, BRANCH
         PRTF  CURRMBR,CC=NO       OUTPUT THE CURRENT MEMBER NAME
         B     R15IS4$
GASERR   PRTF  GASERRS,CC=NO       ERROR IN READING GAS DATA    GP14242
         B     R15IS4$
DIRERR   PRTF  DIRERRS,CC=NO       DIRECTORY I/O ERROR (IGNORE BLOCK)
BLKNUM   OUTPT READTOT,BLKNUMS     READ ERROR MESSAGE
         B     EXCPSTAT+4          CONTINUE -- USING THE NEXT BLOCK
         SPACE 1
R15IS4   MVC   INAROW,=F'0'        END OF MEMBER
R15IS4$  LA    R15,4               TREAT AS END OF MEMBER CONDITION
         L     R1,EOFS
         LA    R1,1(,R1)           END OF MEMBER COUNTER
         ST    R1,EOFS
         CLC   MBBCCHH(7),CURMBB   NEW TRACK
         BNE   NEWTRACK
         L     R1,LASTR
         LA    R1,1(,R1)
         ST    R1,LASTR            UPDATE THE RECORD COUNT
         B     RETURNS
         SPACE 1
R15IS0   MVC   INAROW,=F'0'        GOOD READ
R15IS0$  L     R1,READTOT
         LA    R1,1(,R1)
         ST    R1,READTOT          COUNT OF BLOCKS READ
         L     R1,LS               CURRENT BLKSIZE
         LR    R0,R1               SAVE FOR LATER
         C     R1,MINBLK
         BH    *+L'*+4
         ST    R1,MINBLK           MINIMUM LENGTH BLOCK READ
         C     R1,MAXBLK
         BL    *+L'*+4
         ST    R1,MAXBLK           MAXIMUM LENGTH BLOCK READ
         A     R1,BYTECNT
         ST    R1,BYTECNT          TOTAL NUMBER OF BYTES READ
         TM    DS1DSORG,DS1DSGIS   ISAM DATA-SET?
         BNO   NOFILSUM            NO, BRANCH
         L     R1,READTOTI
         LA    R1,1(,R1)
         ST    R1,READTOTI         COUNT OF BLOCKS READ
         LR    R1,R0               CURRENT BLKSIZE
         C     R1,MINBLKI
         BH    *+L'*+4
         ST    R1,MINBLKI          MINIMUM LENGTH BLOCK READ
         C     R1,MAXBLKI
         BL    *+L'*+4
         ST    R1,MAXBLKI          MAXIMUM LENGTH BLOCK READ
         A     R1,BYTECNTI
         ST    R1,BYTECNTI         TOTAL NUMBER OF BYTES READ
NOFILSUM CLC   MBBCCHH(7),CURMBB   SAME MBBCCHH?
         BNE   NEWTRACK            NO, BRANCH
         A     R0,CURBYTE
         ST    R0,CURBYTE          ACCUMULATE TRACK BYTE COUNT
         L     R1,LASTR
         LA    R1,1(,R1)
         ST    R1,LASTR            ACCUMULATE RECORD COUNT/TRACK
         B     RETURNS
         SPACE 1
NEWTRACK L     R1,TRACKNUM         SWITCH TRACKS --
         LA    R1,1(,R1)
         ST    R1,TRACKNUM         ACTUAL TRACKS USED
R15IS8   MVC   MBBCCHH(7),CURMBB   SAVE CURRENT TRACK NUMBER
         L     R0,CURBYTE
         L     R1,LS
         ST    R1,CURBYTE
         L     R1,TRKBYTE
         AR    R1,R0
         ST    R1,TRKBYTE          COUNT OF ALL CHARACTERS READ
         L     R1,LASTR
         LA    R14,1
         ST    R14,LASTR
         L     R14,ALLR
         AR    R14,R1
         ST    R14,ALLR            COUNT OF ALL READS
         LTR   R0,R0               FIRST ENTRY HERE?
         BNP   NOMINS              YES, BRANCH
         C     R0,MINBYTE
         BNL   *+L'*+4
         ST    R0,MINBYTE          MIN BYTES/TRACK
         C     R1,MINR
         BNL   NOMINS
         ST    R1,MINR             MIN RECORD NUMBER/TRACK
NOMINS   C     R0,MAXBYTE
         BNH   *+L'*+4
         ST    R0,MAXBYTE          MAX BYTES/TRACK
         C     R1,MAXR
         BNH   RETURNS
         ST    R1,MAXR             MAX RECORD NUMBER/TRACK
         SPACE 1
RETURNS  L     R14,STATR14
         BR    R14
         EJECT
*------- MEMBER INPUT ROUTINE.
         SPACE 1
MEMBERS  ST    R14,MEMBR14
         LM    R15,R1,DIRPTRS      PICK UP ADDR, INCR, LIMIT
         LTR   R1,R1               INITIALIZED?
         BNZ   DEBLOCK             YES, BRANCH
         BAS   R14,REREAD          OPEN IN FOR INPUT OF THE DIRECTORY
GETBLK   BAS   R14,EXCPSTAT        GET A DIRECTORY BLOCK
         LTR   R15,R15             END OF FILE?
         BP    MBRDONE             YES, END OF FILE INDICATION
         CLC   LS+2(2),=H'256'     GOOD DIRECTORY BLOCK?
         BNE   NODIRBLK            NO, BRANCH
         LA    R0,2                INCREMENT IS 2 FIRST
         L     R15,BUFFER          START
         LH    R1,0(R15)           LENGTH HALFWORD
         LA    R1,0(R1,R15)        LIMIT
         BCTR  R1,0
DEBLOCK  BXH   R15,R0,GETBLK
         IC    R0,11(,R15)
         N     R0,=F'31'           GET LENGTH BITS
         AR    R0,R0               LENGTH * 2
         A     R0,=F'12'           LENGTH * 2  +  12
         STM   R15,R1,DIRPTRS      SAVE FOR LATER
         CLI   0(R15),X'FF'        LAST MEMBER
         BNE   *+L'*+8             NO, BRANCH
         LA    R15,8               LAST MEMBER USED INDICATION
         B     MBRDONE
         LR    R1,R15              ADDRESS OF MEMBER NAME (RETURNED)
         XR    R15,R15             GOOD READ INDICATION
         B     MBRDONE
NODIRBLK PRTF  EOFSIMS,CC=NO       RECORD LENGTH IS NOT 256     GP14242
         OUTPT LS,BLOCKLEN         OUTPUT CURRENT LRECL
         LA    R15,12              ERROR INDICATION
MBRDONE  L     R14,MEMBR14         RETURN ADDRESS
         BR    R14
         EJECT
*======= EXCP SUBROUTINES (OPENIN, REREAD AND EXCP).
*------- OPEN THE FILE WITH DDNAME "IN".
         SPACE 1
OPENIN   ST    R14,EXCPR14         ENTRY TO OPEN THE FILE
         DEVTYPE DCBDDNAM,DB       CHECK DD CARD                GP14242
         BXH   R15,R15,BADSYN        MISSING                    GP14242
         CLI   DB+2,UCB3DACC       DASD TYPE ?                  GP14242
         BNE   BADSYN                NO; FAIL                   GP14242
         CLI   DB+3,X'05'          2325 ?                       GP14242
         BE    BADSYN                YES; MBBCCHHR CALC WRONG?  GP14242
         MVI   JFCBDSNM,0          OLDER SYSTEMS DON'T SET R15  GP14242
        RDJFCB MF=(E,OCSYSIN)      GET DSN AND VOLUME SERIAL    GP14242
         CLI   JFCBDSNM,C' '       SUCCESSFUL?                  GP14242
         BH    NOABE40               YES, BRANCH                GP14242
ABE40    PRTF  NOTOPEN,CC=NO                                    GP14242
KILL6   KILL   6 ------- RDJFCB, OBTAIN OR OPEN FAILED ********** 6 ***
         SPACE 1
BADSYN   PRTDATA 'SYSIN DD IS MISSING OR UNSUPPORTED DEVICE'    GP14242
         B     KILL6                                            GP14242
         SPACE 1
*PATTERN CAMLST SEARCH,JFCBDSNM,JFCBVOLS,WORKAREA
NOABE40  L     R14,=X'C1000000'    SEARCH                       GP14242
         LA    R15,JFCBDSNM
         LA    R0,JFCBVOLS
         LA    R1,DS1FMTID
         STM   R14,R1,GETDSCB1     COMPLETE THE REQUEST         GP14242
         XC    IECSDSL1(DS1END-IECSDSL1),IECSDSL1  CLEAR        GP14242
         XC    IECSDSL2(DS2END-IECSDSL2),IECSDSL2  CLEAR        GP14242
         XC    IECSDSL3(DS3END-IECSDSL3),IECSDSL3  CLEAR        GP14242
         OBTAIN GETDSCB1           READ IN THE FORMAT 1 DSCB    GP14242
         LTR   R15,R15             SUCCESSFUL?
         BNZ   ABE40               NO, STOP THE PROGRAM
         CLC   DS1PTRDS,ZEROES     ANY MORE ?                   GP14242
         BE    SKIPDSCB              NO                         GP14242
         L     R14,=X'C0800000'    SEEK 2 OR 3                  GP14242
         LA    R15,DS1PTRDS                                     GP14242
         LA    R0,JFCBVOLS                                      GP14242
         LA    R1,IECSDSL2                                      GP14242
         STM   R14,R1,GETDSCB2     COMPLETE THE REQUEST         GP14242
         OBTAIN GETDSCB2           READ IN THE FORMAT 2 DSCB    GP14242
         CLI   DS2FMTID,C'3'       BUT GOT A 3?                 GP14242
         BNE   HAVEFMT2                                         GP14242
         MVC   IECSDSL3(DS2END-IECSDSL2),IECSDSL2  MOVE THE 3   GP14242
         XC    IECSDSL2(DS2END-IECSDSL2),IECSDSL2  CLEAR 2      GP14242
HAVEFMT2 CLC   DS2PTRDS,ZEROES     ANY MORE ?                   GP14242
         BE    SKIPDSCB                                         GP14242
         L     R14,=X'C0800000'    SEEK                         GP14242
         LA    R15,DS2PTRDS                                     GP14242
         LA    R0,JFCBVOLS                                      GP14242
         LA    R1,IECSDSL3                                      GP14242
         STM   R14,R1,GETDSCB2     COMPLETE THE REQUEST         GP14242
         OBTAIN GETDSCB3           READ IN THE FORMAT 3 DSCB    GP14242
SKIPDSCB OPEN  (IN,INPUT),MF=(E,OCSYSIN)                        GP14242
         TM    DCBOFLGS,DCBOFOPN   IN OPEN?
         BZ    ABE40               NO, STOP THE PROGRAM
         B     REREAD+4
         SPACE 1
*------- ENTRY TO REREAD THE OPEN DATA-SET (DDNAME IS "SYSIN").
         SPACE 1
REREAD   ST    R14,EXCPR14
         L     R1,DCBDEBAD
         XR    R14,R14
         IC    R14,16(R1)          NUMBER OF EXTENTS
         ST    R14,NUMEXT          SAVE FOR LATER
         LA    R14,16(R1)          CURRENT EXTENT POINTER - 16
         ST    R14,CUREXT          SAVE FOR LATER
         MVI   IOBSEEK,0           RESET THE EXTENT NUMBER
         LA    R15,4               EARLY EXIT FLAG
         B     FIRSTONE            DO THE INITIAL EXTENT
         SPACE 1
*------- FOR EACH EXTENT, INITIALIZE CCHHR, SECTOR NUMBER
*        AND READ IN THE LENGTH OF THE FIRST RECORD.
         SPACE 1
EACHXTNT XR    R1,R1               AFTER THE FIRST EXTENT --
         IC    R1,IOBSEEK
         LA    R1,1(,R1)
         STC   R1,IOBSEEK          INCREMENT THE EXTENT NUMBER
         C     R1,NUMEXT           PAST LAST EXTENT?
         BNL   EOF$DS              YES, NO END OF DATA-SET RECORDED
         SPACE 1
FIRSTONE ST    R15,EXCPR15         SAVE ENTRY CODE FOR LATER
         L     R1,CUREXT
         LA    R1,16(R1)           CURRENT EXTENT ENTRY
         MVC   IOBSEEK+3(4),6(R1)  INITIAL CCHH
         ST    R1,CUREXT           UPDATE FOR LATER
         MVI   IOBSEEK+7,0
         MVI   SNO,0               SET SECTOR NUMBER=ZERO
         LA    1,CCW1A
         ST    1,CPADDR
        EXCP   IOB
        WAIT   ECB=ECB
         CLI   ECB,X'7F'           GOOD READ?
         BE    NOABE30             YES, BRANCH
         PRTF  FIRSTRD,CC=NO                                    GP14242
        KILL   7 ------- FIRST READ OF EXTENT DID NOT WORK ****** 7 ***
NOABE30  LA    R1,CCW1
         ST    R1,CPADDR
         MVC   CCW3+6(2),DATALN    GET LENGTH FOR NEXT READ
         MVI   IOBSEEK+7,1         SET R=1
         L     R15,EXCPR15
         LTR   R15,R15             EARLY EXIT DESIRED?
         BZ    EXCP+4              NO, ISSUE THE EXCP AGAIN
         L     R14,EXCPR14         RETURN FROM OPENIN, REREAD OR
         BR    R14                 AFTER EOF AND EXTENT VIOLATION
         SPACE 1
*------- ENTRY FOR EACH EXCP TO BE PERFORMED.
         SPACE 1
EXCP     ST    R14,EXCPR14
         CLI   ECB,X'42'           EXTENT VIOLATION LAST TIME?
         BE    EACHXTNT            YES, DO THE NEXT EXTENT
         MVC   CURMBB(8),IOBSEEK   SAVE DISK ADDRESS OF THIS RECORD
         MVC   LS+2(2),DATALN      SAVE BLOCK LENGTH FOR THIS READ
        EXCP   IOB
        WAIT   ECB=ECB
         XR    R15,R15             GOOD READ FLAG
         CLI   ECB,X'42'           EXTENT VIOLATION (NEXT TIME)?
         BE    ENDEXCP             YES, QUIT
         TM    DS1DSORG,DS1DSGIS   ISAM DATA-SET?
         BO    *+L'*+10            YES, IGNORE DS1LSTAR CHECK FOR ISAM
         CLC   LASTMBB(8),CURMBB   PAST THE DS1LSTAR MARKER?
         BNH   EOF$DS              YES, END OF FILE AND DATA-SET
         CLI   ECB,X'7F'           GOOD READ?
         BE    ENDEXCP             YES, QUIT
         SPACE 1
         CLI   ECB,X'41'           PERMANENT ERROR?
         BE    NOABE10             YES, BRANCH
         PRTF  BADERROR,CC=NO                                   GP14242
         B     SOMERR              BAD ERROR, ECB NOT HEX 41, 42 OR 7F
NOABE10  TM    CSW+4,X'01'         ACTUALLY EOF?
         BO    EOF
         CLC   CSW+4(4),=X'00200000'    NULL MEMBER OR DATA-SET?
         BE    EOF                 YES, BRANCH
         PRTF  PERMERR,CC=NO                                    GP14242
SOMERR   LA    R15,12              PERMANENT ERROR -- NOT END OF FILE
         MVC   SAVECB(1),ECB       SAVE ECB CODE FOR LATER
         B     REISSUE
         SPACE 1
EOF      TM    DS1DSORG,DS1DSGPS+DS1DSGDA    DSORG=PS OR DA?
         BNZ   EOF$DS              YES, END OF FILE AND DATA-SET
         LA    R15,4               END OF MEMBER FLAG
         CLC   =H'0',DATALN        NEXT BLOCK LENGTH = 0?
         BNE   ENDEXCP             NO, BRANCH
REISSUE  ST    R15,EXCPR15
         LA    R1,CCW1A            NEED TO REISSUE THE INITIALIZATION
         ST    R1,CPADDR                EXCP TO GET LENGTH AND ADDRESS
        EXCP   IOB                      OF THE NEXT DATA BLOCK
        WAIT   ECB=ECB
         L     R15,EXCPR15         READ ERROR (12) OR END OF MEMBER (4)
         LA    R1,CCW1
         ST    R1,CPADDR
         MVC   CCW3+6(2),DATALN    DATA LENGTH FOR NEXT EXCP
         CLI   ECB,X'42'           EXTENT VIOLATION?
         BE    EACHXTNT            YES, DO NEXT EXTENT -- EARLY EXIT
         CLI   ECB,X'7F'           GOOD READ?
         BE    ENDEXCP             YES, QUIT
         CLI   ECB,X'41'           PERMANENT ERROR?
         BE    NOABE70             YES, CHECK EOF
ABE70    PRTF  BADERROR,CC=NO                                   GP14242
        KILL   8 ------- SOME OTHER BAD ERROR ******************* 8 ***
NOABE70  TM    CSW+4,X'01'         NULL MEMBER (ANOTHER EOF)?
         BO    ENDEXCP
ABE80    PRTF  PERMERR,CC=NO                                    GP14242
        KILL   9 ------- PERMANENT ERROR AFTER EOF ************** 9 ***
EOF$DS   LA    R15,8               END OF FILE AND DATA-SET FLAG
ENDEXCP  L     R14,EXCPR14         RETURN ADDRESS
         BR    R14
         EJECT ,                                                GP14242
*------- ENTRY TO PROCESS SPANNED RECORD INFORMATION            GP14242
         SPACE 1
SPANNED  ST    R14,EXCPR14                                      GP14242
         STM   R1,R4,DB                                         GP14242
         L     R15,BUFFER    GET THE BLOCK                      GP14242
         LH    R0,0(,R15)    GET LENGTH FROM BDW                GP14242
         C     R0,LS         COMPARE TO PHYSICAL SIZE           GP14242
         BNE   SPANBAD         COUNT ERRORS                     GP14242
         CLC   2(2,R15),ZEROES                                  GP14242
         BNE   SPANBAD                                          GP14242
         LR    R1,R15        COPY START                         GP14242
         AR    R1,R0         END + 1                            GP14242
         BCTR  R1,0          LAST BYTE                          GP14242
         LA    R0,4          BDW LENGTH                         GP14242
SPANLOOP BXH   R15,R0,SPANDEX   ALL DONE ?                      GP14242
         CLI   0(R15),X'80'  FLAGGED FOR NO RDW ?               GP14242
         BE    SPANDEX         YES; DONE                        GP14242
         ICM   R0,3,0(R15)   LOAD LENGTH OF THIS RECORD/SEGMENT GP14242
         CL    R0,=F'4'      AT LEAST MINIMUM AND POSITIVE?     GP14242
         BL    SPANBAD       NO; BAD RECORD OR BAD BLOCK        GP14242
         CH    R0,DS1BLKL    VALID LENGTH ?                     GP14242
         BH    SPANBAD       NO                                 GP14242
         LR    R3,R0                                            GP14242
         AR    R3,R15        SET ADDRESS OF LAST BYTE +1        GP14242
         BCTR  R3,0          LAST BYTE                          GP14242
         CR    R3,R1         WILL IT FIT INTO BUFFER ?          GP14242
         BH    SPANBAD       NO; FAIL                           GP14242
         TM    2(R15),255-3  CLEAN RDW ?                        GP14242
         BNZ   SPANBAD                                          GP14242
         TM    3(R15),X'FF'  CLEAN RDW ?                        GP14242
         BNZ   SPANBAD                                          GP14242
         IC    R2,2(,R15)    GET SPAN BITS                      GP14242
         N     R2,=F'3'                                         GP14242
         SLL   R2,2          WORD OFFSET                        GP14242
         LA    R4,1                                             GP14242
         AL    R4,NUMSPREC(R2)    GET SPANNED COUNTERS          GP14242
         ST    R4,NUMSPREC(R2)    AND UPDATE COUNT              GP14242
         B     SPANLOOP                                         GP14242
SPANBAD  LA    R4,1                                             GP14242
         AL    R1,NUMSPBAD   UPDATE ERROR COUNT                 GP14242
         ST    R1,NUMSPBAD   UPDATE ERROR COUNT                 GP14242
SPANDEX  L     R14,EXCPR14                                      GP14242
         LM    R1,R4,DB                                         GP14242
         BR    R14           RETURN TO CALLER                   GP14242
         EJECT ,
*------- OUTPUT ROUTINE.
*        NOTE : R0 NEGATIVE IF NO NUMERIC OUTPUT,
*                  OTHERWISE OUTPUT NUMERIC
*               R1 CONTAINS THE ADDRESS OF THE STRING TO OUTPUT
*               R14 CONTAINS THE RETURN ADDRESS
*               R15 CONTAINS THE LENGTH OF THE STRING
         SPACE 1
OUTRTN   ST    R14,RETOUT
         MIN   R15,=A(L'OUTLINE-13)    NOT TOO LONG             GP14242
         BCTR  R15,0               MACHINE LENGTH
         EX    R15,OUTMOVE         MOVE IN THE STRING
         LA    R1,OUTLINE+13
         LTR   R0,R0               ANY LEADING NUMERICS?
         BM    OUTNOW              NO, BRANCH                   GP14242
         CVD   R0,DB                                            GP14242
         MVC2  OUTLINE,MASKNUM                                  GP14242
         ED    OUTLINE(L'MASKNUM),DB+3  MAKE NICE PRINTABLE #   GP14242
OUTNOW   PRTF  OUTLINE             OUTPUT THE LINE              GP14242
         MVCH  OUTLINE             BLANK IT                     GP14242
         L     R14,RETOUT          RETURN
         BR    R14
OUTMOVE  MVC   OUTLINE+13(*-*),0(R1)    <<EXECUTED>>
MASKNUM  DC    X'402020206B2020206B202120'                      GP14242
         SPACE 1
GDMP     LR    R2,R1         SAVE2CODE                          GP14242
         SERVTERM ,          WRITE LAST MESSAGES                GP14242
         ABEND (R2),DUMP           THAT'S ALL FOLKS             GP14242
         EJECT
*------- TRACE ROUTINE.
*        NOTE : R1 POSITIVE = NEXT MBBCCHHR, ZERO = TABLED MBBCCHHR
*                  AND NEGATIVE = CLOSE TRACE.
*               R15 = CURRENT 'EXCP' CC (ONLY WHEN R1 POSITIVE).
*      OUTPUT : 4 MBBCCHHR'S BY LINE, EACH ONE PRECEEDED BY
*               - A BLANK TO INDICATE A NORMAL DATA BLOCK READ,
*               - THE CHARACTER 'T' FOR A TABLED LOCATION (DIRECTORY),
*               - ANOTHER CHARACTER (GENERALLY '4' FOR AN EOF) TO SAY
*                 SPECIAL CONDITION.
         SPACE 1
TRACE    STM   R14,R3,TRSVREG      SAVE REGISTERS AROUND ROUTINE
         L     R3,TCURPTR          GET CURRENT POINTER
         LTR   R1,R1               WHAT REQUESTED?
         BZ    TR01                TABLED
         BP    TR02                NEXT
         CL    R3,TSTRPTR          FINAL, ANYTHING TO TRACE?
         BE    TR06                NO, BRANCH TO CLOSE
         LR    R2,R1               YES, PASS INDICATOR
         B     TR04                GO OUTPUT LAST INFORMATION
         USING TELDSECT,R2                                      GP14242
TR01     MVI   0(R3),C'T'          INDICATE TABLED ORIGIN
         UNPK  1(9,R3),TLADDR(5)   STORE TABLED MBBCCHHR
         UNPK  9(9,R3),TLADDR+4(5)
         MVI   17(R3),C' '
         TR    1(16,R3),HEXTBL-X'F0'
         B     TR03
         DROP  R2
TR02     MVI   0(R3),C' '
         UNPK  1(9,R3),0(5,R1)     STORE NEXT MBBCCHHR
         UNPK  9(9,R3),4(5,R1)
         MVI   17(R3),C' '
         TR    1(16,R3),HEXTBL-X'F0'
         LTR   R15,R15             EOF ON PREVIOUS MBBCCHHR?
         BZ    TR03                NO, BRANCH
         STC   R15,0(R3)           INDICATE EOF
         OI    0(R3),X'F0'
         TR    0(1,R3),HEXTBL-X'F0'
TR03     LA    R3,18(,R3)          BUMP POINTER
         CL    R3,TMAXPTR          END OF AREA?
         BL    TR08                NO, BRANCH
TR04     DS    0H                  ALREADY OPEN                 GP14242
TR05     PRTF  TRCL,CC=NO,DEV=2    OUTPUT THE LINE              GP14242
         MVCH  TRCL                BLANK IT                     GP14242
         LTR   R2,R2               FINAL CALL?
         BNM   TR07                NO, BRANCH
TR06     NI    FLAGS,255-TEST      OFF TRACE
TR07     L     R3,TSTRPTR          SET STARTING ADDRESS
TR08     ST    R3,TCURPTR          SET NEW CURRENT POINTER
         LM    R14,R3,TRSVREG      RESTORE REGISTERS
         BR    R14                 RETURN
         SPACE 1
         LTORG ,
         EJECT
*------- CONSTANTS AND WORK AREAS.
         SPACE 1
         PRINT &PRTMAC                                          GP14242
SYSPRINT PRTWORK SYSTSPRT,SYSPRINT,TITLE=5                      GP14242
SUMPRINT PRTWORK SUMPRINT                                       GP14242
         SPACE 1
PDS      DCB   DDNAME=SYSIN,DSORG=PO,MACRF=(R)                  GP14242
         SPACE 1
IN       DCB   DDNAME=SYSIN,DSORG=PS,MACRF=E,EXLST=EXLIST-EXLIST
         SPACE 1
CCW1A    CCW   X'31',IOBSEEK+3,X'60',5       SEARCH ID EQUAL (CCHHR)
CCW2A    CCW   X'08',*-8,X'60',1             TIC
CCW3A    CCW   X'92',IOBSEEK+3,X'20',8       MT READ NEXT COUNT
         SPACE 1
CCW0     CCW   X'23',SNO,X'60',1             SET SECTOR
CCW1     CCW   X'31',IOBSEEK+3,X'60',5       SEARCH ID EQUAL (CCHHR)
CCW2     CCW   X'08',*-8,X'60',1             TIC
CCW3     CCW   X'06',*-*,X'60',32767         READ DATA
CCW4     CCW   X'92',IOBSEEK+3,X'60',8       MT READ NEXT COUNT
CCW5     CCW   X'22',SNO,X'20',1             READ SECTOR NUMBER
         SPACE 1
IOB      DC    0F'0',X'C2000000'
         DC    A(ECB)
CSW      DC    2A(0)
CPADDR   DC    A(CCW1)
         DC    A(IN)
         DC    2A(0)
IOBSEEK  DC    2A(0)               NEXT MBBCCHHR ADDRESS
         DC    X'0'                NEXT KEYLENGTH
DATALN   DC    2X'0'               NEXT RECORD LENGTH
SNO      DC    X'0'                NEXT SECTOR NUMBER
CURMBB   DC    CL8' '              CURRENT MBBCCHHR ADDRESS
MBBCCHH  DC    CL7' '              SAVE AREA FOR MBBCCHH
SAVECB   DC    X'0'                SAVE AREA FOR ECB
ECB      DC    F'0'                ECB
         SPACE 1
SAVE     DSECT ,             SAVE AND WORK AREA                 GP14242
ZEROES   DS    D             CONSTANT                           GP14242
DB       DS    2D                                               GP14242
DB2      DS    2D                                               GP14242
DB3      DS    2D                                               GP14242
         SERVDEFS ,          SERVICE ROUTINE AREA               GP14242
DOUBLE   DC    D'0'
LASTMBB  DC    D'0'
DIRPTRS  DC    3A(0)               SAVEAREA FOR R15, R0 AND R1
MEMBR14  DC    A(0)                SAVEAREA FOR RETURN ADDRESS
RETOUT   DC    A(0)                SAVEAREA FOR RETURN ADDRESS
EXLIST   DC    X'87',AL3(MYJFCB)   JFCB BLOCK                   GP14242
OCSYSIN  DC    X'80',AL3(IN-IN)    OPEN/CLOSE LIST              GP14242
STOWMEM  DC    CL8'$GAS0000'       GAS - MEMBER NAME
STOWTTR  DC    XL3'0',X'80'        TTR, ALIAS FLAG
GASTTR   DC    XL8'0'
LS       DC    F'0'                LENGTH OF THE CURRENT RECORD
LASTR    DC    F'0'                CURRENT NUMBER OF RECORDS/TRK
EXCPR14  DC    F'0'                RETURN REGISTER FOR EXCP
EXCPR15  DC    F'0'                EXIT FLAG REGISTER SAVEAREA
STATR14  DC    F'0'                RETURN REGISTER FOR EXCPSTAT
CUREXT   DC    F'0'                ADDRESS OF THE CURRENT EXTENT
NUMEXT   DC    F'0'                TOTAL EXTENTS IN THE DATA-SET
READTOT  DC    F'0'                TOTAL RECORDS READ
MINBLK   DC    F'33000'            MINIMUM LENGTH BLOCK
MAXBLK   DC    F'0'                MAXIMUM LENGTH BLOCK
BYTECNT  DC    F'0'                TOTAL BYTES READ
READTOTI DC    F'0'                TOTAL RECORDS READ (ISAM)
MINBLKI  DC    F'33000'            MINIMUM LENGTH BLOCK (ISAM)
MAXBLKI  DC    F'0'                MAXIMUM LENGTH BLOCK (ISAM)
BYTECNTI DC    F'0'                TOTAL BYTES READ (ISAM)
TEMP     DC    F'0'                WORK STORAGE
GASBYTE  DC    F'0'                TOTAL BYTES IN GAS MEMBERS
RELBYTE  DC    F'0'                TOTAL BYTES IN REAL MEMBERS
MINGAS   DC    X'7F000000'         MIN BYTES IN GAS MEMBERS
MAXGAS   DC    F'0'                MAX BYTES IN GAS MEMBERS
MINREL   DC    X'7F000000'         MIN BYTES IN REAL MEMBERS
MAXREL   DC    F'0'                MAX BYTES IN REAL MEMBERS
SAVETOT  DC    F'0'                STARTING BYTE COUNT HOLD
MAXR     DC    F'0'                MAXIMUM RECORDS/TRACK
MINR     DC    F'255'              MINIMUM RECORDS/TRACK
TRKBYTE  DC    F'0'                SUM OF ALL BLOCKSIZES
INAROW   DC    F'0'                NUMBER OF CONSECUTIVE ERRORS
ALLR     DC    F'0'                SUM OF ALL RECORDS READ
CURBYTE  DC    F'0'                CURRENT TRACK BYTE COUNT
MAXBYTE  DC    F'0'                MAXIMUM BYTES/TRACK
MINBYTE  DC    X'7F000000'         MINIMUM BYTES/TRACK
TRACKNUM DC    F'0'                ACTUAL COUNT OF TRACKS USED
EOFS     DC    F'0'                COUNT OF END OF FILES
GASCNT   DC    F'0'                NUMBER OF "GAS" MEMBERS
GASRDS   DC    F'0'                NUMBER OF "GAS" RECORDS
NUMSPREC DC    F'0'    00    SPANNED WHOLE RECORDS              GP14242
NUMSPSTR DC    F'0'    01    SPANNED START SEGMENTS             GP14242
NUMSPEND DC    F'0'    10    SPANNED END SEGMENTS               GP14242
NUMSPMID DC    F'0'    11    SPANNED MIDDLE SEGMENTS            GP14242
NUMSPBAD DC    F'0'          SPANNED ERRORS                     GP14242
AVGBTRK  DC    F'0'                AVERAGE BYTES PER TRACK
AVAILTRK DC    F'0'                FREE TRACKS
TOTALTRK DC    F'0'                TOTAL TRACKS
ALIASES  DC    F'0'                ALIASES IN THE DIRECTORY
REALMBR  DC    F'0'                REAL ENTRIES (DIRECTORY)
REALENT  DC    F'0'                MEMBER TABLE ENTRIES
ALLOCBLK DC    F'0'                DIRECTORY BLOCKS ALLOCATED
USEDBLK  DC    F'0'                DIRECTORY BLOCKS USED
TOTGAS   DC    F'0'                TOTAL GAS MEMBERS PROCESSED
TLC      DC    A(TLG)
TLMX     DC    A(NTEL)
         SPACE 1
FLAGS    DC    AL1(GAS)            FLAG BYTE
GAS      EQU   X'01'                    GAS RECORDS DESIRED (DEFAULT)
LABELS   EQU   X'02'                    LABELS ONLY (NO DATA READ)
FULL     EQU   X'04'                    FULL INFORMATION LIST
TEST     EQU   X'08'                    TRACE MBBCCHHR'S
GASADD   EQU   X'10'                    GAS RECORD TO BE RECOVERED
GASALL   EQU   X'20'                    ALL GAS RECORDS TO BE RECOVERED
REALONE  EQU   X'40'                    ERROR IN ACTUAL MEMBER FLAG
DIRECTOR EQU   X'80'                    ERROR IN DIRECTORY RECORDS
         SPACE 1
SWITCH   DC    AL1(0)                   WORK SWITCH BYTE
DIRPR    EQU   X'01'
ALIAS    EQU   X'02'
FGSPAN   EQU   X'80'           PRODUCE SPANNED DATA COUNTS      GP14242
MYJFCB   DS    0D                                               GP14242
         IEFJFCBN ,                JFCB MAPPING
         SPACE 1
         IECSDSL1 (1,2,3)          EXPAND DSCBS
         SPACE 1
GETDSCB1 CAMLST SEARCH,JFCBDSNM,JFCBVOLS,DS1FMTID
GETDSCB2 CAMLST SEEK,DS3PTRDS,JFCBVOLS,IECSDSL2
GETDSCB3 CAMLST SEEK,DS3PTRDS,JFCBVOLS,IECSDSL3
WORKAREA DS    0D,148C
         SPACE 1
ARLGTH   DC    F'0'           1/2  I/O AREA AND DIR. ENTRIES LENGTH
BUFFER   DC    A(*-*)         2/2  I/O AREA AND DIR. ENTRIES ADDRESS
GTNEL    EQU   NGEL*L'STOWTTR      GAS RECORDING ELEMENTS AREA
GTLGTH   DC    A(((GTNEL+7)/8)*8)  GAS RECORDING AREA LENGTH
GTANK    DC    A(*-*)              GAS RECORDING AREA ADDRESS
GTSTR    DC    A(*-*,L'STOWTTR,GTNEL-L'STOWTTR)   TO STORE GAS TTR'S
SAVETBL  DC    A(*-*)              BASE ADDR. TO ROOM FOR DIR. ENTRIES
TRSVREG  DC    6F'0'
TCURPTR  DC    A(TRCL+6)
TSTRPTR  DC    A(TRCL+6)
TMAXPTR  DC    A(TRCL+(4*18))
SAVEEND  EQU   *             END OF DYNAMIC R13 STORAGE         GP14242
STATDS   CSECT ,             RESTORE MAIN                       GP14242
         SPACE 1
ALNOREAL DC    CL50'12345678 IS AN ALIAS BUT HAS NO "REAL" ENTRY      '
SCALOS   DC    CL50'DATA-SET IS ALLOCATED IN 123456                   '
IOERROR  DC    CL50'READ ERROR -- ECB = 12, CCHHR = 12.45.78.01.34    '
CURRMBR  DC    CL50'12345678 IS THE MEMBER (END OF MEMBER SIMULATED)  '
LASTMEMS DC    CL50'12345678 IS THE LAST REAL MEMBER IN THE DATA-SET  '
PROTECT  DC    CL50'X IS THE PROTECTION KEY                           '
GASMEM   DC    CL50'"GAS" MEMBER AT TTR TT.TT.RR, DATA :              '
LSTAR    DC    CL50'DS1LSTAR IS TT.TT.RR @ MM.BB.BB.CC.CC.HH.HH.RR'
NOMEOF   DC    CL50'MEMBER : 12345678 (END OF MEMBER MISSING) ++++++++'
INVTTR   DC    CL50'MEMBER : 12345678 (INVALID TTR POINTER) ..........'
ENDGALL  DC    CL50'12345 "GAS" MEMBERS PROCESSED - END               '
GASTRK DC CL60' 12,345,678.9 TRACKS WOULD BE REGAINED BY COMPRESSING '
BLANKS   DC    CL80' '
         SPACE 1
HEXTBL   DC    CL16'0123456789ABCDEF'
WORK     DC    CL132' '                                         GP14242
WDCB     EQU   WORK+68,51,C'C'    DCB FORMAT INFORMATION        GP14242
         DC    CL2' '
OUTLINE  DC    CL90' '
TRCL     DC    CL80' '
         SPACE 1
STOWMSGA DC    C'ADDED TO DIRECTORY FOR TTR'
STOWMSGB DC    C'NOT ADDED TO DIRECTORY : R15=XX - R0=XX'
         SPACE 1
TRTBLANK TRENT FILL=0        PASS ALL                           GP14242
         TRENT ,4,X'00',C' '   EXCEPT NULL AND BLANK            GP14242
         SPACE 1
TRTHEX   TRENT FILL=8        SET ALL INVALID                    GP14242
         TRENT ,0,(X'81',6),      LOWER CASE                    GP14242
         TRENT ,0,(C'A',6),       UPPER CASE                    GP14242
         TRENT ,0,(C'0',10)       DIGITS                        GP14242
         SPACE 1
         DROP  ,                                                GP14242
         EJECT
*------- UNMODIFIED MESSAGES (OUTPT A=I).
         SPACE 1
HEADER1  DC    C'#STATDS : DISK DATA-SET STATISTICS PROGRAM '   GP14242
HEADER2  DC    C' -----------------------------------------'    GP14242
NODSCB2  DC    CL50'ISAM FMT 2 DSCB READ ERROR -----------------------'
DSCB2    DC    CL50'THE FOLLOWING DATA IS FROM THE ISAM LABEL RECORD :'
DSSNOLEV DC    CL50'INDEX LEVELS PRESENT IN THIS DATA-SET             '
DSSDVIND DC    CL50'TRACKS FOR EACH MASTER INDEX                      '
DSSCYLOV DC    CL50'TRACKS OF CYLINDER OVERFLOW AREA ON EACH CYLINDER '
DSSTAGDT DC    CL50'RECORDS TAGGED FOR DELETION                       '
DSSRORG3 DC    CL50'OVERFLOW ACCESSES (NOT COUNTING FIRST IN CHAIN)   '
DSSNOBYT DC    CL50'BYTES NEEDED TO HOLD THE HIGHEST INDEX IN CORE    '
DSSNOTRK DC    CL50'TRACKS OCCUPIED BY THE HIGHEST-LEVEL INDEX        '
DSSPRCTR DC    CL50'RECORDS IN THE PRIME AREA                         '
DSSBYOVL DC    CL50'BYTES LEFT ON CURRENT INDEPENDENT OVERFLOW TRACK  '
DSSRORG2 DC    CL50'TRACKS REMAINING IN THE INDEPENDENT OVERFLOW AREA '
DSSOVRCT DC    CL50'RECORDS IN THE OVERFLOW AREA                      '
DSSRORG1 DC    CL50'CYLINDER OVERFLOW AREAS THAT ARE FULL             '
MINRS    DC    CL50'IS THE MINIMUM NUMBER OF RECORDS PER TRACK        '
AVGRS    DC    CL50'IS THE AVERAGE NUMBER OF RECORDS PER TRACK        '
MAXRS    DC    CL50'IS THE MAXIMUM NUMBER OF RECORDS PER TRACK        '
MINBYTES DC    CL50'IS THE MINIMUM NUMBER OF DATA BYTES PER TRACK     '
AVGBYTES DC    CL50'IS THE AVERAGE NUMBER OF DATA BYTES PER TRACK     '
MAXBYTES DC    CL50'IS THE MAXIMUM NUMBER OF DATA BYTES PER TRACK     '
MINGASS  DC    CL50'IS THE MINIMUM NUMBER OF BYTES IN ANY GAS MEMBER  '
AVGGASS  DC    CL50'IS THE AVERAGE NUMBER OF BYTES IN A GAS MEMBER    '
MAXGASS  DC    CL50'IS THE MAXIMUM NUMBER OF BYTES IN ANY GAS MEMBER  '
MINRELS  DC    CL50'IS THE MINIMUM NUMBER OF BYTES IN ANY REAL MEMBER '
AVGRELS  DC    CL50'IS THE AVERAGE NUMBER OF BYTES IN A REAL MEMBER   '
MAXRELS  DC    CL50'IS THE MAXIMUM NUMBER OF BYTES IN ANY REAL MEMBER '
NOREALS  DC    CL50'NO REAL MEMBERS ARE IN THIS PARTITIONED DATA-SET  '
MINBLKS  DC    CL50'IS THE SHORTEST BLOCK LENGTH                      '
AVGBLKS  DC    CL50'IS THE AVERAGE BLOCK LENGTH                       '
MAXBLKS  DC    CL50'IS THE MAXIMUM BLOCK LENGTH                       '
TOTBYTS  DC    CL50'TOTAL DATA BYTES ARE IN THIS DATA-SET             '
NUMBLKS  DC    CL50'TOTAL DATA BLOCKS ARE RECORDED                    '
GASBLKS  DC    CL50'OF THESE ARE GAS BLOCKS                           '
EOFSS    DC    CL50'END OF FILE                                       '
ENDMBR   DC    CL50'END OF MEMBER (FILE MARKS) ARE RECORDED           '
GASMBR   DC    CL50'OF THESE ARE FOR GAS MEMBERS                      '
ALLBLK   DC    CL50'DIRECTORY BLOCKS ARE ALLOCATED                    '
USEBLK   DC    CL50'OF THESE ARE IN USE                               '
USENAME  DC    CL50'MEMBER NAMES ARE IN THE DIRECTORY                 '
ALSNAME  DC    CL50'OF THESE ARE ALIASES                              '
NUMTRKS  DC    CL50'TRACKS ARE ALLOCATED FOR THIS DATA-SET            '
EXTENTS  DC    CL50'IS THE SECONDARY ALLOCATION QUANTITY              '
NOSEC    DC    CL50'NO SECONDARY ALLOCATION QUANTITY IS IN THE LABEL  '
FRETRKS  DC    CL50'OF THESE ARE NOT IN USE                           '
NUMEXTS  DC    CL50'EXTENTS ARE IN THIS DATA-SET                      '
NOBDBS   DC    CL50'BYTES ARE USED IN LAST PDS DIRECTORY BLOCK        '
TRBALS   DC    CL50'BYTES ARE AVAILABLE ON THE LAST USED TRACK        '
TOOMANY  DC    CL50'TOO MANY CONSECUTIVE I/O ERRORS (10) -------------'
MANYENT  DC    CL50'MEMBER ENTRIES TABLE OVERFLOW --------------------'
NOTOPEN  DC    CL50'RDJFCB, OBTAIN OR OPEN FOR DDNAME "IN" FAILED ----'
FIRSTRD  DC    CL50'FIRST READ OF AN EXTENT DID NOT WORK -------------'
TTRTOLOW DC    CL50'TTR IN MEMBER TABLE .LT. CURRENT TTR -------------'
NOTFOUND DC    CL50'MEMBERS NOT FOUND IN THE MEMBER TABLE ------------'
BADERROR DC    CL50'UNDEFINED READ ERROR -----------------------------'
PERMERR  DC    CL50'UNDEFINED PERMANENT ERROR ------------------------'
EOFSIMS  DC    CL50'BAD DIRECTORY RECORD -- RECORD LENGTH IS NOT 256 -'
BLOCKLEN DC    CL50'IS THE BLOCK LENGTH (END OF DIRECTORY SIMULATED)  '
DIRERRS  DC    CL50'READ ERROR IN THE DIRECTORY ----------------------'
BLKNUMS  DC    CL50'IS THE BLOCK NUMBER (BLOCK IGNORED)               '
GASERRS  DC    CL50'ERROR IN A GAS MEMBER (END OF MEMBER SIMULATED)   '
KEYLEN   DC    CL50'IS THE KEY LENGTH                                 '
KEYPOS   DC    CL50'IS THE RELATIVE KEY POSITION                      '
NULL     DC    CL50'DATA-SET IS EMPTY --------------------------------'
EXTHDR   DC    CL50'EXTENT  UCB  CC.CC.HH.HH   CC.CC.HH.HH   T./TRACKS'
EXTUND   DC    CL50'------  ---  -----------   -----------   ---------'
NOTPDS   DC    CL50'THIS IS NOT A PARTITIONED DATA-SET ... -----------'
NTFGAS   DC    CL50'REQUESTED "GAS" (TTR) NOT FOUND ... --------------'
NGASDS   DC    CL50'NO "GAS" IN THE DATA-SET ... ---------------------'
GOVFLOW  DC    CL50'NUMBER OF "GAS" OVERFLOW (INCOMPLETE PROCESS) ----'
GUNABLE  DC    CL50'UNABLE TO RECOVER THE REQUESTED "GAS" ... --------'
TOOMUCH  DC    CL50'UNABLE TO RECOVER ALL "GAS" IN DATA-SET ... ------'
WRMSG    DC    CL50'PARM=GAS(TT.TT.RR) SYNTAX ERROR ... --------------'
TRCOERR  DC    CL50'TRACE DD COULD NOT BE OPENED ---------------------'
DIRTLA   DC    CL50'DIRECTORY CONTENTS :                              '
DIRTLB   DC    CL50'MEMBERS SORTED BY MBBCCHHR (OR TTR) :             '
DIRTL1   DC    CL50'NAME    M/A   TT.TT.RR   MM.BB.BB.CC.CC.HH.HH.RR  '
DIRTL2   DC    CL50'----    ---   --------   -----------------------  '
MSGSPREC DC    CL50'COMPLETE RECORDS'                           GP14242
MSGSPSTR DC    CL50'BEGIN SEGMENTS'                             GP14242
MSGSPMID DC    CL50'MIDDLE SEGMENTS'                            GP14242
MSGSPEND DC    CL50'END SEGMENTS'
MSGSPBAD DC    CL50'INVALID ENTRIES'
         EJECT
         PRINT NOGEN
VERBTAB  BTAB  'GAS=ALL',SETGALL,BASE=*                         GP14242
         BTAB  'GAS=NO',SETNO                                   GP14242
         BTAB  'GAS=',SETTTR                                    GP14242
         BTAB  'TTR=',SETTTR                                    GP14242
         BTAB  'GAS',SETGAS                                     GP14242
         BTAB  'LABEL',SETLAB                                   GP14242
         BTAB  'TERSE',SETLAB                                   GP14242
         BTAB  'TEST',SETEST                                    GP14242
         BTAB  'DEBUG',SETEST                                   GP14242
         BTAB  'FULL',SETFUL                                    GP14242
         BTAB  'NO',SETNO                                       GP14242
         BTAB  'L',SETLAB                                       GP14242
         BTAB  'T',SETEST                                       GP14242
         BTAB  'F',SETFUL                                       GP14242
         BTAB  'N',SETNO                                        GP14242
         BTAB  'U',SETU                                         GP14242
         BTAB  ' ',LOOKIN    IGNORED                            GP14242
         BTAB  ',',LOOKIN    IGNORED                            GP14242
         BTAB  ';',LOOKIN    IGNORED                            GP14242
         BTAB  ')',LOOKIN    IGNORED                            GP14242
         BTAB  *END                                             GP14242
         SPACE 2
TELDSECT DSECT ,             "SAVETBL" ELEMENT DESCRIPTION      GP14242
         SPACE 1
TLNAME   DS    CL8                 MEMBER NAME
TLADDR   DS    XL8                 MBBCCHHR
TLTTR    DS    XL3                 TTR
TLSTAT   DS    XL1                 STATUS FLAGS
TLALIAS  EQU   X'01'               ALIAS NAME
TLG      EQU   *-TELDSECT    "SAVETBL" ELEMENT LENGTH           GP14242
         SPACE 1
DSATR    MAPDSATR ,          SUBDSATR RETURN MAPPING            GP14242
         CVT   DSECT=YES
         DCBD  DSORG=PS,DEVD=DA
         IEFUCBOB ,                                             GP14242
         SPACE 1
         END
