TRANUTIL TITLE 'T R A N T A B   ***   TRANSLATION TABLE UTILITY'
         COPY  OPTIONGB
         SPACE 1
         SYSPARM LIST=YES
         EJECT ,
         PRINT &PRTSOR
***********************************************************************
*                                                                     *
*                                                                     *
*        COPYRIGHT 1994  EXPERT SYSTEM PROGRAMMING, INC.              *
*        COPYRIGHT 2003  EXPERT SYSTEM PROGRAMMING                    *
*                        176 OLD STAGE COACH ROAD                     *
*                        BRADFORD, VT 05033-8844                      *
*                                                                     *
*                    ALL RIGHTS RESERVED                              *
*                                                                     *
***********************************************************************
*                                                                94291
*   THIS UTILITY ANALYZES TRANSLATION TABLES AND BUILDS ASSEMBLER DC
*   CARD IMAGES FOR TWO-WAY TRANSLATION.                         94291
*                                                                94291
*   COMPLETE TABLE:                                              94291
*   NAME: TABLE NAME-OF-TABLE (OPTIONAL FOR FIRST/ONLY TABLE)    94291
*           THE NAME IS PUNCHED AS THE FIRST NAME FIELD          94291
*           THE CARD FORCES A NEW TABLE DEFINITION               94291
*                                                                94297
*   COMMENT:  ANY CARD WITH * IN COLUMN IS PRINTED/PUNCHED AS-IS.
*   DATA:  FREE FORMAT, MAY BE SEPARATED BY COMMAS, SPACES, AND QUOTES
*          OR USE HEX NOTATION X'NN', USING COLUMNS 1-71         94291
*          AN ASTERISK AND A SEMI-COLON MAY BE USED TO START COMMENTS
*       "FROM" VALUES ARE ASSIGNED AS THE DATA ARE READ (FIRST ENTRY
*       IS VALUE ZERO, NEXT IS 1, ETC.                           94291
*   COMPARE:  THE WORD COMPARE ON A CARD TERMINATES THE CURRENT TABLE
*          AND CAUSES IT TO BE ANALYZED, THEN COMPARED WITH THE PRIOR
*          TABLE. DIFFERENCES ARE PRINTED.                       94297
*                                                                94291
*   EXAMPLE:                                                     94291
*       TABLE ITOE             (ISCII TO EBCDIC)                 94291
*       0,1,2,3,37,2D,2F,16,05,25,0B,0C,0D                       94291
*       X'0E0F'  X'10,11'  '12'  .....                           94291
*                                                                94297
*   OR USE:  PARM=PAIRED - SUPPLY INPUT AND OUTPUT BYTE IN PAIRS 94297
*       OMITTED VALUES DEFAULT TO THEIR OWN VALUE                94297
*                                                                94291
*   INVALID DATA CHARACTERS ARE SHOWN WITH AN 'A' ON THE NEXT    94291
*   LINE; IF TWO CHARACTERS TRANSLATE TO THE SAME VALUE, THE LATER
*   DEFINITION IS FLAGGED WITH >;  WHEN ONE VALUE IS DEFINED TO  94291
*   TRANSLATE TO TWO DIFFERENT VALUES, IT IS SHOWN WITH A <      94291
*   OMITTED TRANSLATIONS ARE PRINTED AFTER THE INPUT             94291
*                                                                94291
TRANUTIL PGMHEAD ZERO12,BASE=R12,PARM=R9,BNDRY=PAGE              94291
         SPACE 1
         SERVINIT ,
         SERVCALL LPALD,=CL8'@PRINTER'
         ST    R0,@PRINTER
         SERVCALL LPALD,=CL8'@INPREAD'
         ST    R0,@INPREAD
         PRTOPEN SYSPRINT,OPT=ABEND
         PRTOPEN SYSPUNCH,OPT=NOWTO,DEV=2  OPTIONAL PUNCH
         PRTF  '#TRANSLATION TABLE UTILITY',TITLE=1
         INPOPEN SYSIN,OPT=ABEND
         LA    R0,256        SET TABLE SIZE                      94297
         STH   R0,INMAX      FOR DEFAULT                         94297
         LA    R9,0(,R9)     CLEAR HIGH                          94297
         LTR   R9,R9         ANY POINTER?                        94297
         BZ    NEWTABLE      NO; USE DEFAULTS                    94297
         L     R9,0(,R9)     LOAD ADDRESS                        94297
         LA    R9,0(,R9)     CLEAR HIGH                          94297
         LTR   R9,R9         ANY ?                               94297
         BZ    NEWTABLE      NO; ASSUME DEFAULTS                 94297
         SLR   R8,R8         CLEAR FOR IC                        94297
         ICM   R8,3,0(R9)    LOAD AND TEST LENGTH                94297
         BNP   NEWTABLE      NONE                                94297
         CH    R8,=Y(L'LINE)  TOO LONG?                          94298
         BH    BADPARM       SO SORRY                            94298
         MVC   LINE,BLANKS   CLEAR LINE FOR PROCESSING           94298
         BCTR  R8,0          SET FOR EXECUTE                     94298
         EX    R8,MVCLINE    MOVE TO LINE                        94298
         LA    R5,LINE       POINT TO LINE                       94298
         LA    R3,LINE+L'LINE-1  END                             94298
PARMLOOK BLOOK T=PARMTAB,R=R3,Y=R3  DO VERB LOOK-UP              94298
         CR    R5,R3         BUFFER EXHAUSTED?                   94298
         BH    NEWTABLE      YES; BEGIN PROCESSING               94298
BADPARM  PRTF  '**** INVALID PARM FIELD ****',CC=NO              94297
MVCLINE  MVC   LINE(0),2(R9)  MOVE USER'S PARM FIELD             94298
         SPACE 1                                                 94298
PARMTAB  BTAB  'PAIRED',SETPAIR,BASE=*   PAIRED INPUT            94298
         BTAB  'PAIRS',SETPAIR   ALTERNATE FORM                  94298
         BTAB  'PAIR',SETPAIR  ALTERNATE FORM?                   94298
         BTAB  'EVEN',SETEVEN                                    94298
         BTAB  '7BIT',SET7BIT  SEVEN BIT INPUT TABLES?           94298
         BTAB  '7-BIT',SET7BIT                                   94298
         BTAB  ',',PARMLOOK  IGNORE BLANKS AND COMMAS            94298
         BTAB  ';',PARMLOOK    AND SEMI-COLONS                   94298
         BTAB  *END                                              94298
         SPACE 1                                                 94297
SETPAIR  OI    PROFG,FGPAIR  INPUT IN INPUT/OUTPUT BYTE FORMAT   94297
         PRTF '>>>> PARM=PAIRED - IN/OUT BYTE PAIRS EXPECTED',CC=NO
         PRTSPACE 1                                              94297
         B     PARMLOOK                                          94298
         SPACE 1                                                 94297
SET7BIT  OI    PROFG,FG7BIT  SET 7-BIT INPUT TABLE               94297
         LA    R0,128        SET TABLE SIZE                      94297
         STH   R0,INMAX      FOR 7-BIT MODE                      94297
         B     PARMLOOK      LOOK FOR MORE                       94298
SETEVEN  OI    PROFG,FGEVEN  PROCEED EVEN WITH ERRORS            94297
         B     PARMLOOK      GO FOR MORE                         94298
         SPACE 1
NEWTABLE LA    R14,TABLE2
         LA    R0,TABLE1
         LA    R1,TABLE1-TABLE2
         LA    R15,0(R1,R1)  DOUBLE OUTPUT LENGTH TO CLEAR
         MVCL  R14,R0        SAVE CURRENT TABLE AND CLEAR
         SLR   R4,R4         CURRENT VALUE
         NI    PROFG,255-FGSET-FGCMP-FGCHNG-FGNRV  RESET FOR NEW TABLE
         MVC   TABLE1,BLANKS  SET DEFAULT NAME
         TM    PROFG,FGPAIR  RUNNING IN PAIRED MODE?             94297
         BZ    INLONCE       NO                                  94297
         MVI   INFLG,2       SET FLAG FOR DEFAULT VALUE          94297
         MVC   INFLG+1(255),INFLG  SET FOR ALL                   94297
         MVC   OUFLG,INFLG   BOTH IN AND OUT                     94297
         LA    R1,255        NUMBER OF NON-ZERO ENTRIES          94297
NEWTDFLT STC   R1,INTAB(R1)  SET BYTE = TO ITSELF                94297
         STC   R1,OUTAB(R1)     DITTO FOR OUTPUT                 94297
         BCT   R1,NEWTDFLT   DO ALL BUT ZERO                     94297
         B     INLONCE       GO TO READ A CARD
         SPACE 1
INLINE   CLC   LINE,BLANKS   ANY ERRORS ?
         BE    INLINED
         PRTLIST ERRLINE     PRINT ERROR LINE
         PRTSPACE 1          AND DOUBLE-SPACE TO NEXT LINE
         OI    RETCH+L'RETCH-1,8  SET MAJOR ERROR
INLONCE  MVC   LINE,BLANKS   CLEAR FOR NEXT TIME
INLINED  INPGET ,            GET NEXT INPUT LINE
         BXH   R15,R15,ENDSYN  QUIT ON EODAD OR ERROR
         LR    R8,R1         COPY CARD ADDRESS
         LA    R7,71(,R8)    LAST COLUMN OF TEXT
         LA    R6,1          BX INCREMENT
         BAL   R9,MAJOR      SCAN FOR MAJOR CONTROL CARDS        94297
         CH    R2,=H'4'      COMPARE REQUEST ?                   94297
         BNH   INPRINT       NOTHING SPECIAL (OR COMPARE)        94297
         TM    PROFG,FGSET   FOR DEFERRED TABLE? (TABLE/CHANGE)  94297
         BNZ   INNOPRT       YES; SKIP PRINT
INPRINT  PRTF  0(R8),80,CC=NO  ECHO THE INPUT
INNOPRT  CLI   0(R8),C'*'    COMMENT ?
         BE    INLINED       YES; GET ANOTHER CARD
         CH    R2,=H'4'      CHECK TYPE OF MAJOR CONTROL CARD    94297
         BL    INDEFS        NONE; CHARACTER DEFINITIONS         94297
         BE    REQCOMP       TABLE COMPARE REQUESTED             94297
         TM    PROFG,FGSET   FIRST OCCURRENCE FOR THIS GROUP ?   94297
         BZ    INTABNB       YES; BEGIN PROCESSING               94297
         INPKEEP ,           RETURN THIS CARD AT NEXT INPGET
         B     ENDTABLE      AND PROCESS THE CURRENT TABLE
         SPACE 1                                                 94297
INTABNB  MVC   TABLE1,DB2    SET NAME OF NEW TABLE               94297
         CH    R2,=H'9'      NORMAL ?                            94298
         BL    INLINED                                           94298
         BE    INTABNP                                           94298
         CH    R2,=H'10'     PAIRED?                             94298
         BH    INTABCH                                           94298
         OI    PROFG,FGPAIR  ASSUME PAIRED MODE                  94298
         B     INLINED                                           94298
INTABNP  NI    PROFG,255-FGPAIR  RESUME UNPAIRED MODE            94298
         B     INLINED                                           94298
INTABCH  CH    R2,=H'12'     WAS THIS TABLE RATHER THAN CHANGE(S) ?
         BL    INLINED       TABLE - ALL SET FOR NEXT?           94297
         OI    PROFG,FGCHNG  CHANGES FORCES TEMPORARY PAIR MODE  94297
         MVC   INTAB,INTAB2  USE PRIOR TABLE                     94297
         MVI   INFLG,2       SHOW DEFAULT, NOT FORCED VALUE      94297
         MVC   INFLG+1(255),INFLG  PROPAGATE ALL                 94297
         MVC   OUTAB,OUTAB2  DITTO                               94297
         MVC   OUFLG,INFLG   DITTO                               94297
         B     INLINED       GO FOR IT                           94297
         SPACE 1
INDEFS   MVC   72(8,R8),BLANKS  CLEAR SEQUENCE FIELD
         TR    0(72,R8),UPPER  MAKE UPPER CASE
         LR    R5,R8         SAVE START
         BCTR  R8,0          ALLOW FOR FIRST BXH
         MVC   LINE,BLANKS
INCHAR   BAL   R9,GETCHAR    GET NEXT HEX BYTE, IF ANY           94297
         B     INLINE        NONE?                               94297
         OI    PROFG,FGSET   SHOW ONE PAIR SET (ATTEMPTED)       94297
         TM    PROFG,FGPAIR+FGCHNG  DOING PAIRED INPUT ?         94297
         BZ    INCHONLY                                          94297
         LR    R4,R3         PRESERVE FIRST BYTE OF PAIR         94297
         BAL   R9,GETCHAR    GET SECOND OF PAIR                  94297
         B     MISSTWO       SECOND BYTE MISSING                 94297
INCHONLY LA    R14,INTAB(R4)  GET POSITION FOR CURRENT VALUE
         LA    R15,OUTAB(R3)  GET POSITION IN OTHER TABLE
         TM    INFLG-INTAB(R14),1  ALREADY DEFINED ?
         BZ    INOK
         MVI   0(R1),C'<'    POINT TO ERROR
         B     INCHAR        SKIP THIS AND PROCESS NEXT
INOK     TM    OUFLG-OUTAB(R15),1  OUTPUT ALREADY DEFINED?
         BZ    OUTOK         NO; PROCEED
         MVI   0(R1),C'>'    POINT TO ERROR
         B     INCHAR        SKIP THIS AND PROCESS NEXT
OUTOK    OI    OUFLG-OUTAB(R15),1  SET DEFINED
         OI    INFLG-INTAB(R14),1  SET DEFINED
         STC   R4,OUTAB-OUTAB(R15)  SET OUTPUT CONVERSION
         STC   R3,INTAB-INTAB(R14)  SET INPUT CONVERSION
         TM    PROFG,FGPAIR+FGCHNG  DOING PAIRED INPUT ?         94297
         BNZ   INCHAR        YES; DON'T INCREMENT IN POINTER     94297
         LA    R4,1(,R4)     READY FOR NEXT BYTE
         B     INCHAR
         SPACE 1                                                 94297
MISSTWO  PRTF  '*** SECOND BYTE OF PAIR MISSING ***',CC=NO       94297
         OI    RETCH+L'RETCH-1,8  MAJOR ERROR                    94297
         B     INLINE        KEEP SCANNING                       94297
         SPACE 1
ENDSYN   OI    PROFG,FGEODAD  REMEMBER END OF INPUT
         TM    PROFG,FGSET   WAS THERE ANY INPUT ?               94297
         BZ    EXIT          NO; JUST GET OUT
         B     ENDTABLE                                          94297
         SPACE 1                                                 94297
REQCOMP  OI    PROFG,FGCMP   SET TABLE COMPARE REQUEST           94297
         SPACE 1                                                 94297
ENDTABLE LA    R8,INFLG      START WITH INPUT TABLE
         LA    R7,INFLG-1    SET START OF TABLE LESS ONE         94297
         AH    R7,INMAX      + SIZE = LAST BYTE                  94297
         SLR   R2,R2         CLEAR ENTIRE BYTE
         LR    R5,R8         SAVE START
CHECKIN  ICM   R2,1,0(R8)    WAS THIS VALUE DEFINED ?
         BNZ   CHECKINX      YES; PROCEED
         LR    R3,R8
         SR    R3,R5         GET OFFSET
         STC   R3,DB         STASH IT
         PRTLIST INOMIT      SIGNAL OMISSION
         OI    RETCH+L'RETCH-1,4
CHECKINX BXLE  R8,R6,CHECKIN   CONTINUE
         LA    R8,OUFLG      START WITH OUTPUT TABLE
         LA    R7,OUFLG+255  END WITH LAST POSITION
         SLR   R2,R2         CLEAR ENTIRE BYTE
         LR    R5,R8         SAVE START
CHECKOUT ICM   R2,1,0(R8)    WAS THIS VALUE DEFINED ?
         BNZ   CHECKOUX      YES; PROCEED
         LR    R3,R8
         SR    R3,R5         GET OFFSET
         STC   R3,DB         STASH IT
         PRTLIST OUTOMIT     SIGNAL OMISSION
         OI    RETCH+L'RETCH-1,4
CHECKOUX BXLE  R8,R6,CHECKOUT  CONTINUE
         SPACE 1
         TM    PROFG,FGEVEN  PROCEED WITH ERRORS?                94297
         BNZ   CHECKOUR      YES                                 94297
         CLC   RETCH,=H'4'   ANY MAJOR ERROR ?                   94298
         BH    EXIT          YES; QUIT NOW
CHECKOUR SLR   R2,R2                                             94297
         SLR   R3,R3                                             94297
         SLR   R4,R4                                             94297
CHECKREV IC    R3,INTAB(R2)  GET OUTPUT VALUE                    94297
         IC    R4,OUTAB(R3)  GET CORRESPONDING INPUT VALUE       94297
         CR    R2,R4         MATCH ?                             94297
         BE    CHECKREW      YES                                 94297
         STC   R2,DB                                             94297
         STC   R3,DB+1                                           94297
         STC   R4,DB+2                                           94297
         PRTLIST NONREV      SHOW NON-REVERSIBLE                 94297
         OI    PROFG,FGNRV   SIGNAL NON-REVERSIBLE TRANSLATION   94298
         OI    RETCH+L'RETCH-1,4                                 94297
CHECKREW LA    R2,1(,R2)                                         94297
         CH    R2,INMAX      DONE ?                              94297
         BL    CHECKREV      GO AGAIN                            94297
         SPACE 1                                                 94298
         TM    PROFG,FGNRV   ANY NON-REVERSIBLE TRANSLATIONS?    94298
         BZ    CHECKDON      DONE?                               94298
         SLR   R2,R2         CLEAR COUNTER                       94298
CHECKALT LA    R8,INTAB      GET INPUT TABLE                     94298
         LA    R7,INTAB+255    AND LAST BYTE                     94298
CHECKALB CLM   R2,1,0(R8)    BYTE IN TABLE ?                     94298
         BE    CHECKALX      YES; TRY NEXT BYTE                  94298
         BXLE  R8,R6,CHECKALB                                    94298
         STC   R2,DB         STASH IT                            94298
         PRTLIST OUTOMIT     SIGNAL OMISSION                     94298
         OI    RETCH+L'RETCH-1,4                                 94298
CHECKALX LA    R2,1(,R2)     SET NEXT TEST VALUE                 94298
         CH    R2,INMAX      DONE YET?                           94298
         BL    CHECKALT      NO; TRY AGAIN                       94298
         SPACE 1
CHECKDON TM    PROFG,FGEVEN  PROCEED WITH ERRORS?                94297
         BNZ   CHECKCMR      YES                                 94297
         CLC   RETCH,=H'4'   ANY MAJOR ERROR ?                   94298
         BH    EXIT          YES; QUIT NOW
CHECKCMR MVC   DB2,TABLE1    SET TABLE NAME
         LA    R8,INTAB      GET INPUT TABLE
         BAL   R9,DUMPTAB    PRINT RESULT
         LA    R8,OUTAB      GET OUTPUT TABLE
         BAL   R9,DUMPTAB    PRINT IT
         TM    PROFG,FGCMP   NEED TO DO A COMPARE ?              94297
         BZ    CHECKCMP      NO                                  94297
         BAL   R9,COMPTAB    PERFORM THE COMPARE                 94297
CHECKCMP PRTSPACE (R8)       FORCE EJECT
         TM    PROFG,FGEVEN  PROCEED WITH ERRORS?                94298
         BNZ   CHECKEOF      YES                                 94298
         ICM   R0,3,RETCH    ANY ERROR AT ALL ?                  94298
         BNZ   EXIT          YES; QUIT                           94298
CHECKEOF TM    PROFG,FGEODAD  REAL END OF INPUT ?
         BZ    NEWTABLE      NO; PROCESS NEXT TABLE
         SPACE 1
EXIT     SERVTERM ,
         LH    R9,RETCH
         PGMEXIT RC=(R9)     RETURN WITH CONDITION              GP03030
         SPACE 2                                                 94297
*   GETCHAR RETRIEVES THE NEXT CHARACTER, IF ANY, FROM THE CURRENT LINE
*        INPUT R8 - CURRENT BYTE, R6/R7 BXLE LINE POINTERS       94297
*       OUTPUT R1 - CURRENT CHARACTER ON INPUT LINE              94297
*              R3 - VALUE OF BYTE                                94297
*       RETURN R9 - NO BYTE FOUND                                94297
*            R9+4 - VALID BYTE FOUND                             94297
*                                                                94297
GETCHAR  BXH   R8,R6,0(R9)   RETURN NO BYTE FOUND                94297
         CLC   =C'X''',0(R8)  POSSIBLE HEX TEXT ?
         BE    GETCHAR       YES; IGNORE IT                      94297
         SLR   R2,R2
         TRT   0(1,R8),VALTAB  TABLE OF VALID/VALUE
         CLM   R2,1,=X'10'   IGNORE THIS CHARACTER ?
         BL    INYBBLE       NO; HAVE FIRST NIBBLE
         BE    GETCHAR       YES; SKIP APOSTROPHE, COMMA, ETC.   94297
         CLM   R2,1,VALTAB+C'*'  COMMENT CHARACTER?              94297
         BE    INLINE        YES; NO MORE ON THIS LINE           94297
         LA    R3,LINE(R8)   GET CURRENT POSITION + LINE
         SR    R3,R5         GET OFFSET FROM START OF CARD
         MVI   0(R3),C'A'    POINT TO ERROR
         B     GETCHAR       BUT CONTINUE                        94297
         SPACE 1
INYBBLE  LR    R3,R2         PRESERVE CURRENT HEX DIGIT
         CH    R4,INMAX      TOO MUCH, TOO SOON?                 94297
         BL    INYBBLED
         PRTF  '***** MORE THAN 256 DEFINITIONS SUPPLIED *****',CC=NO
         OI    RETCH+L'RETCH-1,8
         B     ENDSYN
INYBBLED BXH   R8,R6,DOBYTE  NO MORE INPUT - PROCESS CURRENT
         SLR   R2,R2         CLEAR AGAIN
         TRT   0(1,R8),VALTAB  CHECK VALUE
         LA    R1,LINE-1(R8)  GET CURRENT POSITION + LINE
         CLM   R2,1,=X'10'   VALID CHARACTER?
         BE    DOBYTE        SEPARATOR - ACCEPT SINGLE DIGIT
         BL    TOBYTE        DOUBLE-DIGIT
         BCTR  R8,0          BACKSPACE TO REDO BAD CHARACTER
         B     DOBYTE        BUT PROCESS SINGLE
TOBYTE   SLL   R3,4          MAKE ROOM
         OR    R3,R2         MAKE TRUE VALUE
         LA    R1,LINE(R8)   GET CURRENT POSITION + LINE
DOBYTE   SR    R1,R5         GET OFFSET FROM START OF CARD
         B     4(,R9)        RETURN WITH PARSED BYTE             94297
         SPACE 1                                                 94297
*   MAJOR - SUBROUTINE TO (PRE)CHECK FOR MAJOR CONTROL CARD:     94297
*     TABLE - BEGIN A NEW TABLE DEFINITION                       94297
*     CHANGE(S) - BEGIN NEW TABLE DEFINITION USING PRIOR TABLE   94297
*     COMPARE - COMPARE CURRENT AND PRIOR TABLE FOR DIFFERENCES  94297
*        RETURN:  R2 - 0 NO CONTROL CARD; 4 - COMPARE; 8 - TABLE;
*                     12 - CHANGE(S) CARD                        94297
*                 R3 - SET TO FIRST BYTE AFTER REQUEST           94297
*           TABLE/CHANGE(S) - FIRST OPERAND PARSED INTO DB2      94297
MAJOR    LA    R0,60         SCAN FIRST 60 COLUMNS FOR A NON-BLANK
         SLR   R2,R2         PRESET FOR NO CONTROL CARD          94297
         LR    R3,R8         COPY SCAN POINTER                   94297
MAJORNB  CLI   0(R3),C' '    LEADING BLANK ?                     94297
         BNE   MAJORFB       NO; TEST FOR CONTROL CARD           94297
         BXH   R3,R6,0(R9)   RETURN IF INPUT EXHAUSTED (?)       94297
         BCT   R0,MAJORNB    TRY AGAIN                           94297
         BR    R9            VERY BLANK CARD                     94297
MAJORFB  CLI   0(R3),C'C'    POSSIBLE CONTROL CARD?              94297
         BLR   R9            NO                                  94297
         LA    R1,4          PRESET FOR COMPARE VALUE            94297
         LA    R0,7          AMOUNT TO POINT PAST VERB           94297
         CLC   =C'COMPARE',0(R3)  IS IT A COMPARE REQUEST ?      94297
         BE    MAJORS2       YES; SET RETURN REGISTER            94297
         LA    R1,8          PRESET FOR PLAIN TABLE              94297
         LA    R0,5          POINT PAST VERB                     94297
         CLC   =C'TABLE',0(R3)  IS IT A NEW TABLE DEFINITION?    94297
         BE    MAJORPS       YES; NEED TO PARSE                  94297
         LA    R0,4          POINT PAST VERB                     94297
         CLC   =C'NAME',0(R3)  IS IT A NEW TABLE ALIAS           94297
         BE    MAJORPS       YES; NEED TO PARSE                  94297
         LA    R1,8+1        ONE MORE THAN 'NAME/TABLE'          94298
         LA    R0,8          LENGTH                              94298
         CLC   =C'UNPAIRED',0(R3)  MATCH ?                       94298
         BE    MAJORPS       YES; PARSE NAME                     94298
         LA    R0,6          SET FOR SHORTER FORM                94298
         CLC   0(6,R3),=C'UNPAIRED'   'UNPAIR' ?                 94298
         BE    MAJORPS                                           94298
         LA    R1,8+2        SET FOR PAIRED MODE                 94298
*        LA    R0,6          SET LENGTH                          94298
         CLC   =C'PAIRED',0(R3)  REVERT TO PAIRED MODE?          94298
         BE    MAJORPS                                           94298
         BCTR  R0,0                                              94298
         CLC   =C'PAIRS',0(R3)                                   94298
         BE    MAJORPS                                           94298
         BCTR  R0,0                                              94298
         CLC   0(4,R3),=C'PAIRED'                                94298
         BE    MAJORPS                                           94298
         LA    R1,12         PRESET FOR CHANGE TABLE             94297
*OLD*    TM    PROFG,FGPAIR  PAIRED INPUT ?                      94297
*OLD*    BZR   R9            NO; CHANGES NOT AVAILABLE           94297
         LA    R0,7          POINT PAST VERB                     94297
         CLC   =C'CHANGES',0(R3)  LONG FORM ?                    94297
         BE    MAJORPS       YES; GO TO PARSE NAME               94297
         CLC   0(6,R3),=C'CHANGES'  JUST 'CHANGE' ?              94297
         BNER  R9            NO; RETURN NO MATCH                 94297
         LA    R0,6          POINT PAST                          94297
MAJORPS  MVC   DB2,BLANKS    CLEAR FOR NAME INSERTION            94297
         AR    R3,R0         SPACE PAST VERB                     94297
MAJORLB  CLI   0(R3),C' '    LEADING BLANK ?
         BNE   MAJORSB       NO
         BXLE  R3,R6,MAJORLB  TRY AGAIN
         B     MAJORS2       ALL BLANK - IGNORE; SET COMMAND VALUE
MAJORSB  LA    R14,DB2       POINT TO OUTPUT
         LA    R15,L'DB2     MAKE LENGTH OF NAME
MAJORSM  CLI   0(R3),C' '    TRAILING BLANK ?
         BE    MAJORS2       YES; RETURN                         94297
         MVC   0(1,R14),0(R3)  MOVE ONE BYTE
         AR    R14,R6        ADD ONE
         SR    R15,R6        GET REMAINING LENGTH
         BNP   MAJORS2       NO MORE - SET COMMAND VALUE
         BXLE  R3,R6,MAJORSM  ELSE CHECK NEXT
MAJORS2  LR    R2,R1         RETURN COMMAND BYTE TO CALLER       94297
         BR    R9            AND RETURN                          94297
         SPACE 1
DUMPTAB  LA    R7,255(,R8)   END OF TABLE
         PRTF  COMHEAD,L'COMHEAD,DEV=(1,2)  PRINT HEADER
         LA    R3,HEXTAB     COUNT ZONE
DUMPTABL MVC   LINE,BLANKS   CLEAR ENTIRE LINE
         MVC   LINE(8),DB2   SET TABLE NAME OR BLANKS
         MVC   DB2,BLANKS    MAKE BLANKS FROM NOW ON
         MVC   LINE+9(2),=C'DC'
         MVC   LINE+15(2),=C'X'''
         LA    R5,LINE+17
         LA    R4,16         DO 16 PER LINE
DUMPTABC UNPK  0(3,R5),0(2,R8)  UNPACK
         TR    0(2,R5),HEXTAB-C'0'  MAKE PRINTABLE HEX CHARACTER
         MVI   2(R5),C','    MAKE SEPARATOR
         LA    R5,3(,R5)
         BCT   R4,DUMPTABM   BUMP IF NOT LAST
         BCTR  R5,0
         MVI   0(R5),C''''
         MVC   3(1,R5),0(R3)  MOVE HEX 'TENS' DIGIT
         MVI   4(R5),C'X'    SHOW VARIABLE UNIT
         PRTF  LINE,80,CC=NO,DEV=(1,2)
         LA    R3,1(,R3)     NEXT ZONE
         BXLE  R8,R6,DUMPTABL
         BR    R9
DUMPTABM BXLE  R8,R6,DUMPTABC
         EX    0,*           SHOULD NEVER GET HERE
         SPACE 1                                                 94297
*   COMPTAB - ROUTINE TO COMPARE TO TABLES FOR DIFFERENCES       94297
COMPTAB  CLC   INTAB,INTAB2  INPUT COMPARES?                     94297
         BNE   COMPDIF       NO                                  94297
         CLC   OUTAB,OUTAB2  OUTPUT COMPARES?                    94297
         BNE   COMPDIF       NO                                  94297
         PRTF  '  >>>> TABLES ARE IDENTICAL <<<<',CC=NO          94297
         BR    R9                                                94297
COMPDIF  PRTF  '>>>> TABLES ARE DIFFERENT <<<<',CC=NO            94297
         PRTSPACE 1                                              94297
         SLR   R2,R2         SET LOOP INDEX                      94297
         SLR   R3,R3                                             94297
         SLR   R4,R4                                             94297
COMPLUP  IC    R3,INTAB(R2)  GET CURRENT TABLE                   94297
         IC    R4,INTAB2(R2)  GET PRIOR TABLE                    94297
         CR    R3,R4         MATCH?                              94297
         BE    COMPBMP       YES; DO NEXT                        94297
         STC   R2,DB                                             94297
         STC   R3,DB+1                                           94297
         STC   R4,DB+2                                           94297
         PRTLIST INDIFF                                          94297
COMPBMP  LA    R2,1(,R2)                                         94297
         CH    R2,=H'256'                                        94297
         BL    COMPLUP                                           94297
         PRTSPACE 1                                              94297
         SLR   R2,R2         SET LOOP INDEX                      94297
COM2LUP  IC    R3,OUTAB(R2)  GET CURRENT TABLE                   94297
         IC    R4,OUTAB2(R2)  GET PRIOR TABLE                    94297
         CR    R3,R4         MATCH?                              94297
         BE    COM2BMP       YES; DO NEXT                        94297
         STC   R2,DB                                             94297
         STC   R3,DB+1                                           94297
         STC   R4,DB+2                                           94297
         PRTLIST OUTDIFF                                         94297
COM2BMP  LA    R2,1(,R2)                                         94297
         CH    R2,=H'256'                                        94297
         BL    COM2LUP                                           94297
         PRTSPACE 1                                              94297
         BR    R9                                                94297
         SPACE 2
SYSPRINT PRTWORK SYSPRINT,TITLE=3
SYSPUNCH PUNWORK SYSPUNCH
SYSIN    INPWORK SYSIN,EODAD=ENDSYN
         SPACE 1
ERRLINE  FD    LINE,72,NL,PAD
         FD    'ERRORS FOUND'
         FD    *END
         SPACE 1
INOMIT   FD    'TRANSLATION FROM VALUE',NL
         FD    DB,1,HEX,PAD
         FD    'MISSING'
         FD    *END
OUTOMIT  FD    'NO TRANSLATION TO VALUE',NL
         FD    DB,1,HEX,PAD
         FD    'SUPPLIED'
         FD    *END
         SPACE 1                                                 94297
INDIFF   FD    'INPUT',NL                                        94297
         FDGOTO INDIFFC                                          94297
OUTDIFF  FD    'OUTPUT',NL                                       94297
INDIFFC  FD    'VALUE',PAD                                       94297
         FD    DB,1,HEX,PAD                                      94297
         FD    'TRANSLATED TO'                                   94297
         FD    DB+1,1,HEX,PAD                                    94297
         FD    'BY'                                              94297
         FDCLC TABLE1,BLANKS,BNE=INDIFF1N                        94297
         FD    'CURRENT TABLE',PAD                               94297
         FDGOTO INDIFF1C                                         94297
INDIFF1N FD    TABLE1,PAD,DEB                                    94297
INDIFF1C FD    'AND TO'                                          94297
         FD    DB+2,1,HEX,PAD                                    94297
         FD    'BY'                                              94297
         FDCLC TABLE2,BLANKS,BNE=INDIFF2N                        94297
         FD    'PREVIOUS TABLE',PAD                              94297
         FDGOTO INDIFF2C                                         94297
INDIFF2N FD    TABLE2,PAD                                        94297
INDIFF2C FD    *END                                              94297
         SPACE 1                                                 94297
NONREV   FD    'NON-REVERSIBLE TRANSLATION FROM',NL              94297
         FD    DB,1,HEX,PAD                                      94297
         FD    'TO'                                              94297
         FD    DB+1,1,HEX,PAD                                    94297
         FD    'BACK TO'                                         94297
         FD    DB+2,1,HEX,PAD                                    94297
         FD    *END                                              94297
         SPACE 1
BLANKS   DC    CL80' '
HEXTAB   DC    C'0123456789ABCDEF'
COMHEAD  DC    C'0*                 0  1  2  3  4  5  6  7  8  9  A  B *
                C  D  E  F'
         SPACE 1
UPPER    DC    256AL1(*-UPPER)  NO TRANSLATION TABLE
         TRENT UPPER,(*-UPPER+C' '),(X'81',9),(X'91',9),(X'A2',8)
         SPACE 1
VALTAB   DC    256AL1(X'20')    ALL GARBAGE
         TRENT VALTAB,X'10',C' ',C',',C''''  VALID SEPARATORS
         TRENT ,(*-VALTAB-C'A'+10),(C'A',6)  DEFINE A-F
         TRENT ,(*-VALTAB-C'0'),(C'0',10)    DEFINE 0-9
         TRENT ,X'24',C'*',C';'  DEFINE COMMENTS CHARACTERS      94297
         SPACE 1
SAVE     DSECT ,
DB       DS    D
DB2      DS    D
@SERVICE DS    A
@PRINTER DS    A
@INPREAD DS    A
RETCH    DS    H             RETURN CODE
INMAX    DS    H             MAXIMUM INPUT VALUE (128 - 7-BIT MODE)
PROFG    DS    X
FGEODAD  EQU   X'80'           ON AFTER END-FILE ON SYSIN
FGSET    EQU   X'40'           AT LEAST ONE VALUE SET            94297
FGCMP    EQU   X'20'           TABLE COMPARE REQUESTED           94297
FG7BIT   EQU   X'10'           PROCESSING 7-BIT TABLE            94297
FGNRV    EQU   X'08'           NON-REVERSIBLE TRANSLATION FOUND  94298
FGCHNG   EQU   X'04'           CHANGE CARD: TEMPORARY PAIR MODE  94297
FGPAIR   EQU   X'02'           INPUT IN PAIRS                    94297
FGEVEN   EQU   X'01'           PROCESS EVEN THOUGH ERRORS        94297
LINE     DS    CL80          ERROR POINTER(S)
TABLE2   DS    CL8' '
INTAB2   DS    CL256         INPUT TRANSLATION
INFLG2   DS    CL256
OUTAB2   DS    CL256         OUTPUT TRANSLATION
OUFLG2   DS    CL256
TABLE1   DS    CL8' '
INTAB    DS    CL256         INPUT TRANSLATION
INFLG    DS    CL256
OUTAB    DS    CL256         OUTPUT TRANSLATION
OUFLG    DS    CL256
SAVEEND  EQU   *                                                GP03030
         SPACE 1
         CVT   DSECT=YES
         IHACDE ,
         END   ,
