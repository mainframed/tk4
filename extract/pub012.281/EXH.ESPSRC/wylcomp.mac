WCOM     TITLE 'W Y L C O M P  ***  CONVERT A DATA SET TO EDIT FORMAT'
         PUNCH ' ORDER WYLCOMP(P) '   MAKE DUMPS EASIER         GP03126
         PUNCH ' ALIAS WYLPRESS '                               GP03126
         PUNCH ' SETCODE AC(1)  '                               GP03126
         COPY  OPTIONGB                                          81360
         SPACE 1                                                 81360
         SYSPARM LIST=YES    SET AND LIST GLOBAL OPTIONS         81360
         SPACE 2                                                 81360
***********************************************************************
*                                                                     *
*        COPYRIGHT 1981  EXPERT SYSTEM PROGRAMMING, INC.              *
*        COPYRIGHT 2003  EXPERT SYSTEM PROGRAMMING                    *
*                        176 OLD STAGE COACH ROAD                     *
*                        BRADFORD, VT 05033-8844                      *
*                                                                     *
*                    ALL RIGHTS RESERVED                              *
*                                                                     *
***********************************************************************
         EJECT ,
         LCLB  &LOCO         LOCAL VALIDITY CHECKING            GP03126
&LOCO    SETB  ('&LOCAL' EQ 'PID')                              GP03126
         PRINT &PRTSOR                                           81360
WYLCOMP  PGMHEAD ZERO12,BASE=(R11,R12),PARM=R9,BNDRY=PAGE,   NEW 81360 *
               ENTRY=WYLPRESS,ENTNO=FLAGS                        93218
***********************************************************************
*                                                                     *
*   THIS ROUTINE CONVERTS AN INPUT FILE (CARD FORMAT, EDIT FORMAT,    *
*   PRINT FORMAT, OR EXPANDED NIH FORMAT) TO WYLBUR EDIT FORMAT.      *
*   INPUT IS SUPPLIED ON SYSUT1 (OR SYSIN IF NO SYSUT1), AND          *
*   IS WRITTEN TO SYSUT2 (OR FOR A NON-SPECIFIC ALLOCATION, TO        *
*   THE INSTALLATION DEFINED WYLBUR SAVE VOLUME CONTAINING THE        *
*   MOST FREE SPACE).                                                 *
*                                                                     *
*   WHEN THE DATA SET ALREADY EXISTS, AND IS SEQUENTIAL, THE USER'S   *
*   JCL MUST NOT SPECIFY A MEMBER NAME. WHEN THE DATA SET IS A PDS,   *
*   A MEMBER NAME MUST BE SUPPLIED. WHEN THE DATA SET DOES NOT        *
*   ALREADY EXIST, ONE IS ALLOCATED. FOR A PDS, 1 TRACK IS USED       *
*   FOR DIRECTORY SPACE. PRIMARY ALLOCATION IS AS SPECIFIED ON        *
*   THE PATTERN SYSUT2 DD CARD. THE DSN ON THE DD CARD MAY BE         *
*   IN WYLBUR SHORT FORM, IF SO, IT IS CONVERTED USING THE JOB'S      *
*   ACCOUNT NUMBER AND JOBNAME FOR THE NAME. THE LOCAL NAME IS        *
*   OF THE FORM:        USERID.NAME OR USERID.ACCOUNT.NAME            *
*   SEE THE @SERVICE ROUTINE FOR DETAILS.                             *
*********DSN CONVERSION, OTHER LOCAL CHECKING BYPASSED*********GP03126*
*                                                                87034*
*   ENTRY POINT WYLPRESS REQUIRES A USER VOLUME, AND EXPECTS A        *
*   NON-WYLBUR DSNAME FOR OUTPUT (FOR A WYLBUR DSN, CODE IS AS        *
*   FOR WYLCOMP WITH A USER PACK SPECIFIED).                     87034*
*                                                                     *
*   TO REPLACE AN EXISTING DATA SET OR MEMBER, PARM=REP MUST BE       *
*   USED. FOR AN EXISTING PDS, REASONABLE NON-EDIT FORMATS ARE        *
*   SUPPORTED, AND NUMBERING IS SUPPRESSED.                           *
*                                                                     *
*   BY DEFAULT, NEW OUTPUT IS NUMBERED IN INCREMENTS OF 0.001;        *
*   PARM=INT MAY BE SPECIFIED TO NUMBER BY 1.000                      *
*                                                                     *
*   PARM=NUM MAY BE SPECIFIED TO INDICATE THAT THE LAST          82018*
*   EIGHT BYTES OF EACH INPUT RECORD CONTAIN A SEQUENCE          82018*
*   NUMBER. THE NUMBER MAY BE EITHER AN 8-BYTE INTEGER           82018*
*   OR A WYLBUR EDIT NUMBER (NNNN.NNN)                           82018*
*                                                                     *
*   TO SUPPORT THE NEW WYLBUR, CHANGE THE EQUATES BELOW :             *
*                                                                     *
MAXOUTLN EQU   232           MAXIMUM LENGTH ALLOWED BY ACS/OBS   93218
NEWNIHFG EQU   0             SET TO 128 (PAWF1NIH) FOR NIH SUPPORT
*                                                                     *
***********************************************************************
         SPACE 1                                                 81360
         AIF   (&LOCO).PASSL01                                  GP03126
         MVI   FLAGS,FGNWYL  NOT LOCAL - ALLOW ANY NAME         GP03126
.PASSL01 MVC   SYSUT2(PATDCBLN),PATDCB  MAKE DCB                 81360
         MVC   UT2OPTS(PATDCBDL),PATDCBD  MAKE EXIT LIST OPTIONS 81360
         MVC   COMPLIST(PATCOMPL),PATCOMP  MAKE COMPRESS OPTIONS 81360
         MVC   SYSUT1(PATUT1LN),PATUT1  MAKE SYSUT1/SYSIN OPTIONS
         MVI   @DCBEXIT,5    SET DCB OPEN EXIT                   81360
         LA    R14,INFMJFCB  SET JFCB ADDRESS                    81360
         LA    R15,UT2OPTS   SET OPTIONS FOR @DCBEXIT            81360
         LA    R0,SYSUT2     GET DCB ADDRESS                     81360
         STM   R14,R0,@DCBEXIT+4                                 81360
         MVI   @DCBEXIT+4,X'07'  SET JFCB ADDRESS                81360
         MVI   @DCBEXIT+8,X'80'  SET END OF LIST / @DCBEXIT OPTIONS
         MVI   UT2OPLST,X'8F'   MAKE OPEN OUTPUT LIST            81360
         LA    R1,@DCBEXIT   POINT TO EXIT LIST                  81360
         STCM  R1,7,SYSUT2+DCBEXLSA-IHADCB                       81360
         LA    R1,1                                              81360
         STH   R1,CATCOUNT   MAKE SCRATCH/CATALOG LIST           81360
         MVC   INSTVENT(PATVENTL),PATVENT  MAKE VOLUME TABLE     91169
         MVC   USERVENT(MAPLEN),INSTVENT  MAKE DEFAULT ENTRY     91169
         MVI   OLDVOLS,C' '                                      81360
         MVC   OLDVOLS+1(OLDVOLSX-OLDVOLS-1),OLDVOLS  BLANK IT   81360
         LA    R1,999        SET OUR NORMAL ABEND CODE           81360
         ST    R1,ABENDOPT                                       81360
         SPACE 1                                                 93218
*   CHANGE TO 500 FOR NEW NIH WYLBUR                             82032
         LA    R1,MAXOUTLN   SET MAXIMUM LENGTH SUPPORTED        82032
         ST    R1,OUTMAX     SET MAXIMUM LRECL SUPPORTED         82032
         AIF   (NOT &BUGBEAR).NODUMP                             81360
         OI    ABENDOPT,X'90'  SET ABEND/STEP TERM FOR DEBUGGING 81360
.NODUMP  SPACE 1                                                 81360
         SERVINIT ,          LOAD THE SERVICE ROUTINE            81360
         SPACE 1                                                 93218
         AIF   (NOT &LOCO).SKIPL01                              GP03126
         XC    A$SVCRB(A$SLENRB),A$SVCRB  CLEAR PARMLIST         93218
         LA    R1,CATWORK    GET ERROR MESSAGE                   93218
         ST    R1,A$SERMSG   PASS IT                             93218
         MVC   A$SID,=C'A$RB'  IDENTIFY                          93218
         MVI   A$SFCTN,A$SFVALX   VALIDATE DATA SET/INDEX ACCESS 93218
         MVI   A$SCNTL,A$SAKEY     ACCOUNT PASSED                93218
         MVI   A$SACES,A$XAFRDW  REQUEST READ/WRITE ACCESS       93218
         LA    R1,DS1DSNAM   POINT TO NAME TO BE CHECKED         93218
         ST    R1,A$SPSWD+4  (ZERO IN FIRST FOUR)                93218
         SERVCALL ACGET,A$SACCT  OBTAIN CURRENT ACCOUNT          93218
         BXH   R15,R15,BADCALL  ALLOW ON DISASTROUS FAILURE      93218
         STC   R0,ACCTPRIV   STASH PRIVILEGE FLAGS               93218
.SKIPL01 ANOP  ,                                                GP03126
BADCALL  SERVLOAD @PRINTER,@INPREAD  LOAD REQUIRED MODULES      GP03125
         SERVCALL LPALD,=CL8'@DCBEXIT'  GET THE DCB OPEN EXIT    81360
         STCM  R0,7,@DCBEXIT+1    PUT INTO EXIT LIST             81360
         PRTOPEN SYSPRINT,OPT=(NOWTO,DUMMY)  OK IF NO PRINT FILE 81360
         BXLE  R15,R15,HAVEPRT         OK                        81360
         PRTOPEN CONSOLE,DEV=2   ELSE OPEN THE CONSOLE FILE FOR WTO
HAVEPRT  LA    R9,0(,R9)     CLEAR THE HIGH BYTE OF THE PARM ADDRESS
         LTR   R9,R9         ANY PARM ADDRESS ?                  81360
         BZ    DONEPARM      NO; SKIP PARM PROCESSING            81360
         L     R9,0(,R9)     GET THE PARM POINTER               GP03126
         LA    R9,0(,R9)     CLEAR HIGH BIT                     GP03126
         LTR   R9,R9         ANY ?                              GP03126
         BZ    DONEPARM      NONE; QUIT                          81360
         SLR   R8,R8         CLEAR FOR IC                        81360
         ICM   R8,3,0(R9)    GET THE PARM LENGTH                 81360
PARMLOOP LTR   R8,R8         ANYTHING LEFT TO SCAN ?             81360
         BNP   DONEPARM      NO                                  81360
         LA    R7,1          SET SKIP LENGTH                     81360
         CLI   2(R9),C' '    EMBEDDED BLANK ?                    81360
         BE    PARMSKIP      YES; IGNORE IT                      81360
         CLI   2(R9),C','    OR COMMA ?                          81360
         BE    PARMSKIP      YES; SKIP OVER                      81360
         LA    R7,L'DCINT    GET INTEGER OPTION                  81360
         CLC   DCINT,2(R9)   IS IT INTEGER OPTION ?              81360
         BE    PARMSINT      YES; SET IT                         81360
         LA    R7,3          SET FOR SHORT FORM                  81360
         CLC   DCINT(3),2(R9)  IS IT SHORT FORM ?                81360
         BE    PARMSINT      YES; SET IT                         81360
         LA    R7,L'DCNUM    SET FOR NUMBERED OPTION             81360
         CLC   DCNUM,2(R9)   IS IT ?                             81360
         BE    PARMSNUM      YES; SET IT                         81360
         LA    R7,6          TEST MODERATELY LONG FORM           81360
         CLC   DCNUM(6),2(R9)  IS IT ?                           81360
         BE    PARMSNUM      YES                                 81360
         LA    R7,3          SET FOR SHORT FORM                  81360
         CLC   DCNUM(3),2(R9)  IS IT SHORT FORM ?                81360
         BE    PARMSNUM      YES                                 81360
         LA    R7,L'DCREP    SET FOR REPLACE                     81360
         CLC   DCREP,2(R9)   REPLACE OPTION ?                    81360
         BE    PARMSREP      YES; SET IT                         81360
         LA    R7,3                                              81360
         CLC   DCREP(3),2(R9)  SHORT FORM ?                      81360
         BE    PARMSREP      YES; HONOR IT                       81360
         CLC   DCADD,2(R9)   ADD RATHER THAN REPLACE ?           81360
         BE    PARMSKIP      YES; TREAT AS NO-OP                 81360
         CLC   DCUNN(3),2(R9)  UNNUMBERED ?                      82032
         BE    PARMSUNN      YES; NO-OP                          82032
         LA    R7,L'DCUNN    FULL LENGTH                         82032
         CLC   DCUNN,2(R9)                                       82032
         BE    PARMSUNN                                          82032
         LA    R7,L'DCLONG                                       87179
         CLC   DCLONG,2(R9)  LONG (WYLBUR 7) FORMAT ?            87179
         BE    PARMLONG      YES; SET IT                         87179
         LA    R7,L'DCNIH                                        87179
         CLC   DCNIH,2(R9)   NIH FORMAT ?                        87179
         BE    PARMNIH                                           87179
         LA    R7,L'DCTEST                                       81360
         CLC   DCTEST,2(R9)                                      81360
         BE    PARMDBUG                                          81360
         LA    R7,L'DCBUG    DEBUG MODE ?                        81360
         CLC   DCBUG,2(R9)                                       81360
         BE    PARMDBUG                                          81360
         PRTV  BADPARM,DEV=(1,2)   WRITE ERROR MESSAGE           81360
         OICC  4             SET MINOR ERROR CODE                81360
         B     DONEPARM      AND QUIT                            81360
DCINT    DC    C'INTEGER'    NUMBER IN INTEGER INCREMENTS        81360
DCUNN    DC    0C'UNNUMBERED',C'UN'  UNNUMBERED DUMMY            82032
DCNUM    DC    C'NUMBERED'   INSERT LINE NUMBERS IN COLUMNS 73/80
DCADD    DC    C'ADD'        ADD RATHER THAN REPLACE DSN/MEMBER  81360
DCREP    DC    C'REPLACE'    REPLACE (ADD PERMITTED)             81360
DCBUG    DC    C'DEBUG'                                          81360
DCTEST   DC    C'TEST'                                           81360
DCLONG   DC    C'LONG'       LONG FORMAT (232 BYTE NEW WYLBUR)   87179
DCNIH    DC    C'NIH'        NIH FORMAT (500 BYTE RECORDS)       87179
BADPARM  VCON  '0INVALID PARM FIELD; IGNORED'                    81360
         SPACE 1                                                 81360
PARMSINT LA    R0,1000       SET INCREMENT                       81360
         ST    R0,WCMDELTA   FOR INTEGER NUMBERING               81360
         ST    R0,WCMLINE#   SET FIRST LINE NUMBER               81360
         TM    WCMFG1,WCMF1EDT  NUMBERED OPTION USED ALSO ?      82018
         BNZ   PARMBOTH                                          81360
         B     PARMSKIP      GET NEXT PARM                       81360
         SPACE 1                                                 81360
PARMDBUG OI    ABENDOPT,X'90'  SET ABEND FOR DUMP AND STEP TERMINATION
         B     PARMSKIP      GET ANOTHER PARM                    81360
PARMLONG LA    R0,232        NEW OBS LENGTH                      87179
         B     PARMNIGH                                          87179
PARMNIH  OI    COMPLIST+PAWFG1-PATCOMP,PAWF1NIH  REQUEST NIH FORMAT
         LA    R0,500                                            87179
PARMNIGH ST    R0,OUTMAX     SET DESIRED MAXIMUM                 87179
         B     PARMSKIP                                          87179
         SPACE 1                                                 81360
PARMSNUM OI    WCMFG1,WCMF1EDT  SHOW NUMBERS SUPPLIED BY USER    82018
         MVI   OUTMAXUP+L'OUTMAXUP-1,8  ALLOW 8 MORE FOR LENGTH  87179
         TM    WCMDELTA+3,X'FF'   INTEGER OPTION USED ?          81360
         BNZ   PARMSKIP      NO; CONTINUE                        81360
         SPACE 1                                                 81360
PARMBOTH PRTV  MGDUPRM,DEV=(1,2)                                 81360
         NI    WCMFG1,255-WCMF1EDT  RESET NUMBERED OPTION        82018
         MVI   OUTMAXUP+L'OUTMAXUP-1,0  RESET LENGTH OF LENGTH   87179
         B     PARMSKIP                                          81360
MGDUPRM  VCON  '0PARM=NUM AND INT ARE MUTUALLY EXCLUSIVE; ''NUM'' IGNOR*
               ED'                                               81360
         SPACE 1                                                 82032
PARMSUNN TM    WCMFG1,WCMF1EDT  NUMBERED REQUESTED ?             82032
         BZ    PARMSKIP      NO; UNN IS NO-OP                    82032
         PRTV  MGDUPRU,DEV=(1,2)  WRITE WARNING MESSAGE          82032
         B     PARMSKIP      AND IGNORE                          82032
MGDUPRU  VCON  '0PARM=UNN AND PARM=NUM CONFLICT; ''UNN'' IGNORED'
         SPACE 1                                                 81360
PARMSREP OI    FLAGS,FGREP   SHOW REPLACE PERMITTED              81360
         SPACE 1                                                 81360
PARMSKIP AR    R9,R7         SPACE PAST IT                       81360
         SR    R8,R7         SET RESIDUAL LENGTH                 81360
         BP    PARMLOOP      TRY AGAIN                           81360
         SPACE 1                                                 81360
DONEPARM ESTAE STAEEXIT,CT                                      GP03125
         L     R0,OUTMAX     GET DESIRED MAXIMUM                 87179
         A     R0,OUTMAXUP   PLUS LENGTH ADJUSTMENT              87179
         ST    R0,OUTMAX     STASH IT BACK                       87179
         DEVTYPE SYSUT1,DB   WAS A SYSUT1 DD CARD SUPPLIED ?     81360
         BXH   R15,R15,NOUT1   NO; LOOK FOR SYSIN                81360
         CLI   DB+2,0        DD DUMMY ?                          81360
         BNE   HAVEUT1       NO; ASSUME IT IS USABLE             81360
NOUT1    LM    R14,R1,SYSUT1  GET SYSUT1/SYSIN DDNAMES           81360
         STM   R0,R1,SYSUT1                                      81360
         STM   R14,R15,SYSUT1+8  REVERSE SEQUENCE                81360
HAVEUT1  INPOPEN SYSUT1,OPT=(ABEND)  ABEND (2540) IF NO INPUT    81360
         INPGET ,            GET THE FIRST RECORD                81360
         BXH   R15,R15,INPERR   TOO BAD                          81360
         INPKEEP ,           RETURN IT ON NEXT REQUEST           81360
         DEVTYPE SYSUT2+DCBDDNAM-IHADCB,DB                       81360
         BXH   R15,R15,BADUT2    FAIL IF NO SYSUT2 PRESENT       81360
         TM    DB+2,UCB3DACC+UCB3TAPE  SUPPORTED TYPE ?          81360
         BNM   BADUT2                                            81360
         CLI   DB+2,UCB3TAPE   TAPE OUTPUT ?                     81360
         BNE   *+8           NO                                  81360
         OI    FLAGS,FGTAPOUT  SHOW OUTPUT TO TAPE               81360
         RDJFCB MF=(E,UT2OPLST)  READ THE JFCB FOR THE OUTPUT FILE
         BXLE  R15,R15,HAVEUT2  OK ?                             81360
BADUT2   LA    R3,MGBADUT2                                       81360
         BAS   R9,ABENDMSG   WRITE THE ERROR MESSAGE             81360
MGBADUT2 VCON  '0SYSUT2 DD CARD MISSING OR UNUSABLE'             81360
         SPACE 1                                                 81360
HAVEUT2  TM    FLAGS,FGTAPOUT   TAPE OUTPUT ?                    81360
         BZ    CONVUT2       NO; DO NORMAL WYLBUR NAME PROCESSING
         SERVCALL DSTST,JFCBDSNM  JUST TEST FOR OS NAME          81360
         B     CONVUT2C      GO TO COMMON                        81360
CONVUT2  SERVCALL UCBDK,JFCBVOLS  LOCATE UCB BY VOLSER           81360
         BXH   R15,R15,BADUT2   NOT DISK ?                       81360
         ST    R0,MAPUCB-MAPVENT+USERVENT  SAVE UCB ADDRESS FOR ALLOC
         TM    FLAGS,FGNWYL  NON-WYLBUR DSNAME PERMITTED ?       87034
         BZ    CONVUTW       NO; DO FULL CHECKING                87034
         SERVCALL DSTST,JFCBDSNM  CHECK DSN                      87034
         BXH   R15,R15,BADDSN  UNINDEXED OR INVALID NAME         87034
         CLM   R0,1,=AL1(VRNWYL)  WYLBUR INDEX ?                 87034
         BNE   CONVUT2T      NO; BYPASS SOME CHECKING            87034
         NI    FLAGS,255-FGNWYL  KILL NON-WYLBUR MODE            87034
CONVUTW  SERVCALL DSGET,JFCBDSNM  CHECK (AND CONVERT) NAME       81360
CONVUT2C BXH   R15,R15,BADDSNT SHOW INVALID DATA SET NAME        81360
*        TM    FLAGS,FGTAPOUT   TAPE OUTPUT ?                    81360
*        BNZ   CONVUT2T      YES; GDG PERMITTED                  81360
*        TM    JFCBIND1,JFCGDG  GDG REQUESTED ?                  87034
*        BNZ   BADDSN        YES; NOT SUPPORTED                  87034
CONVUT2T TM    JFCBIND1,JFCPDS  MEMBER NAME SPECIFIED ?          81360
         BZ    UT2NOMEM      NO                                  81360
         CLI   JFCBELNM,C' '    MEMBER NAME REALLY PRESENT ?     81360
         BNH   UT2NOMEM      NO; RESET IT                        81360
         SERVCALL DSMEM,JFCBELNM   VALIDITY CHECK IT             81360
         BXH   R15,R15,BADMEM   SHOW INVALID MEMBER NAME         81360
         CLI   JFCBELNM+7,X'C0'  SVC NAME ?                      81360
         BE    BADMEM        YES; NOT SUPPORTED                  81360
         TM    FLAGS,FGTAPOUT  TAPE OUTPUT ?                     81360
         BNZ   BADMEM        YES; NOT SUPPORTED                  81360
         OI    JFCBIND1,JFCPDS  SHOW MEMBER NAME PRESENT         81360
         B     UT2GOMEM      JOIN COMMON CODE                    81360
BADMEM   LA    R3,MGBADMEM                                       81360
         BAS   R9,ABENDMSG                                       81360
BADDSNT  CH    R15,=H'16'    MESSAGE IN R0 ?                     81360
         BH    BADDSN        NO                                  81360
         LA    R15,15        MAKE MASK                           81360
         NR    R15,R0                                            81360
         MH    R15,=Y(MGDSN2-MGDSN1)                             81360
         LA    R2,MGDSN1(R15)                                    81360
         LA    R3,MGBADDS                                        81360
         BAS   R9,ABENDLST   PRINT ERROR                         81360
MGBADDS  FDOPT NL,CC=C'0'                                        81360
         FDPRT 'DATA SET NAME',PADR                              81360
         FDPRT JFCBDSNM,DEB,PAD                                  81360
         FDPRT 'IS INVALID -',PADR                               81360
         FDPRT 0(R2),L'MGDSN1                                    81360
         FDPRT *END                                              81360
MGDSN1   DC    CL40'PROCESSING ERROR'                            81360
MGDSN2   DC    CL40'PROCESSING ERROR'                            81360
         DC    CL40'INVALID CHARACTER OR SYNTAX'                 81360
         DC    CL40'INVALID ACCOUNT NUMBER'                      81360
         DC    CL40'NAME NOT IN O.S. FORMAT'                     81360
         DC    CL40'NAME NOT VALID FOR OUTPUT VOLUME'            81360
         DC    CL40'NAME HAS TOO FEW INDEX LEVELS'               81360
         DC    CL40'NAME HAS TOO MANY INDEX LEVELS'              81360
         DC    CL40'HIGH-LEVEL INDEX NOT VALID'                  81360
         DC    CL40'NO PASSWORD ENTRY EXISTS FOR THIS NAME'      81360
BADDSN   LA    R3,MGBADDSN                                       81360
         BAS   R9,ABENDLST                                       81360
MGBADDSN FDOPT NL,CC=C'0'                                        81360
         FDPRT JFCBDSNM,DEB                                      81360
         FDPRT 'IS NOT A PERMITTED DATA SET NAME',PADL           81360
         FDPRT *END                                              81360
MGBADMEM VCON  '0MEMBER NAME CONTAINS INVALID CHARACTERS'        81360
         SPACE 2                                                 81360
UT2NOMEM TM    JFCBIND1,JFCGDG  GENERATION DATA GROUP ?          91070
         BNZ   UT2GOMEM      YES; DON'T CLOBBER IT               91070
         XC    JFCBELNM,JFCBELNM  RESET MEMBER NAME FIELD        91070
         NI    JFCBIND1,255-JFCPDS  RESET FLAG                   91070
UT2GOMEM OI    JFCBTSDM,JFCNWRIT  DO NOT WRITE JFCB TO JOBQUEUE  81360
         CLI   JFCDSORG+1,0  FUNNY DSORG ?                       81360
         BNE   BADJFCDC      YES; FAIL                           81360
         CLI   DS1DSORG+1,0       SAM ?                         GP09186
         BNE   BADJFCDC      NO; FAIL                           GP09186
         TM    DS1DSORG,255-JFCORGPS-JFCORGPO-JFCORGU            81360
         BNZ   BADJFCDC                                          81360
         CLI   JFCRECFM,0    ANY RECFM ?                         81360
         BE    UT2JFC3       NO; CONTINUE                        81360
         TM    JFCRECFM,JFCUND   RECFM=U ?                       81360
         BO    UT2JFC3       YES; PROCESS                        81360
         TM    JFCRECFM,JFCFIX   FIXED ?                         81360
         BZ    BADJFCDC      NO; FAIL                            81360
         TM    JFCRECFM,JFCVARD+JFCVAR  RECFM=V OR =D ?          81360
         BZ    UT2JFC3       NEITHER; PROCEED                    81360
BADJFCDC SERVCALL DSFMT,JFCDSORG  FORMAT DSORG/DCB PARAMETERS    81360
         MVC   CATWORK(27),0(R1)  MOVE TO SAVE AREA              81360
         LA    R3,MGBADJFD   SET FOR UNSUPPORTED JFCB DCB ATTRIBUTES
         BAS   R9,ABENDLST                                       81360
MGBADJFD FDOPT NL,CC=C'0'                                        81360
         FDPRT 'SYSUT2 DD HAS UNSUPPORTED FORMAT :'              81360
         FDEXEC DSFORM,DSFORMX                                   81360
         FDPRT *END                                              81360
         SPACE 1                                                 81360
UT2JFC3  DS    0H                                                81360
         EJECT
GOODJFCB MVC   MAPVOL-MAPVENT+USERVENT,JFCBVOLS  SET USER VOLSER 81360
         CLC   MAPVOL-MAPVENT+INSTVENT(5),JFCBVOLS  WYLBUR GROUP ?
         BE    *+8
         OI    FLAGS,FGUSER   INDICATE GOING TO USER VOLUME      81360
         LA    R1,USERVENT   SET POINTER TO SYSUT2 ENTRY         82017
         ST    R1,POINTVOL     FOR TAPE AND NEW DISK DSN         82017
ACCVOLOK DS    0H                                                81360
         EJECT
         TM    FLAGS,FGTAPOUT  TAPE OUTPUT ?                     81360
         BNZ   OPENOUT       YES; GO TO OUTPUT PROCESSING        81360
         LA    R6,OLDVOLS    MAKE LIST OF OLD VOLUME SERIALS     81360
         LA    R5,USERVENT   POINT TO USER VOLUME ENTRY TABLE    81360
         TM    FLAGS,FGUSER  DOING WYLBUR PACKS ONLY ?           81360
         BNZ   *+8           NO; INCLUDE USER                    81360
         LA    R5,INSTVENT   SET FOR INSTALLATION VOLUMES ONLY   81360
         TM    FLAGS,FGNWYL  NON-WYLBUR MODE ?                   87034
         BZ    OBTCHECK      NO; CHECK                           87034
         OI    USERVENT+MAPLEN+MAPUCB-MAPVENT,X'80' SET 1 ONLY   87034
         USING MAPVENT,R5    DECLARE MAPPING                     81360
OBTCHECK MVC   VOLJFCB,MAPVOL   MOVE VOLSER TO SERVCALL PARM     81360
         SERVCALL DSDS1,VOLJFCB  OBTAIN THE FORMAT 1 DSCB        81360
         CH    R15,=H'4'     VOLUME NOT MOUNTED ?                81360
         BE    OBTBUMP       YES; SKIP THIS ENTRY                81360
         CH    R15,=H'8'     ERROR ?                             81360
         BH    OBTBUMP       YES; SKIP THIS VOLUME               81360
         BE    OBTSPACE      DATA SET NOT FOUND                  81360
         MVC   DS1FMTID(DS1END-DS1FMTID),0(R1)  SAVE FORMAT 1    81360
         OI    FLAGS,FGOLD   SET EXISTING DATA SET               81360
         ST    R5,POINTVOL   SET POINTER TO VOLUME               81360
         MVC   0(6,R6),VOLJFCB  SAVE VOLSER FOR MULTIPLE VSN MESSAGE
         LA    R6,6(,R6)     BUMP POINTER                        81360
OBTSPACE SERVCALL UCBDK,VOLJFCB  GET A UCB BY VOLSER             81360
         LTR   R4,R0         FOUND ?                             81360
         BZ    OBTBUMP       NO; SKIP THIS VOLUME                81360
         SERVCALL TIOUA,(R4)  LOOK FOR USABLE DD CARD FOR VOLUME 81360
         LTR   R0,R0                                             81360
         BZ    OBTBUMP       NONE; SKIP THE VOLUME               81360
         TM    FLAGS,FGUSER  DOING USER VOLUME ?                 81360
         BNZ   OBTBUMP       YES; SKIP LSPACE                    81360
         LA    R1,LSPACER    POINT TO WORK AREA                  81360
         LR    R0,R4         SET UCB ADDRESS                     81360
         SVC   78                                                81360
         BXH   R15,R15,OBTBUMP  SKIP IF BAD                      81360
         ST    R4,MAPUCB     SAVE UCB ADDRESS                    81360
         CLC   LSPACER+21(9),PRIORSPC  MORE SPACE THAN BEFORE ?  81360
         BNH   OBTBUMP       NO                                  81360
         ST    R5,POINTUCB   POINT TO UNIT WITH MOST FREE SPACE  81360
         MVC   PRIORSPC,LSPACER+21  SAVE AMOUNT                  81360
OBTBUMP  LA    R5,MAPLEN(,R5)   BUMP TO NEXT ENTRY               81360
         TM    MAPUCB,X'80'  END OF LIST ?                       81360
         BZ    OBTCHECK      NO; TRY NEXT ENTRY                  81360
         CLI   OLDVOLS+6,C' '  DATA SET FOUND ON MORE THAN ONE PACK ?
         BNH   NOTMANY       NO                                  81360
         LA    R3,OLDVOLS    POINT TO FIRST VOLUME               82017
         PRTLIST MGTOOVOL,DEV=(1,2)  WRITE TOO MANY HEADER       82017
         LA    R3,OLDVOLS+L'OLDVOLS    POINT TO VOLUME SERIALS   82017
         LA    R4,PATVENTL/MAPLEN   NUMBER OF ENTRIES            91169
TOOVOLMG CLI   0(R3),C' '    ANY MORE ?                          82017
         BE    TOOVOLAB      NO; QUIT                            82017
         PRTLIST MGTOOVO2,DEV=(1,2)    TACK THE VOLSER ON        82017
         LA    R3,L'OLDVOLS(,R3)  NEXT VOLUME                    82017
         BCT   R4,TOOVOLMG                                       82017
TOOVOLAB BAS   R9,ABEND      AND QUIT                            82017
MGTOOVOL FDPRT 'DATA SET FOUND ON MULTIPLE VOLUMES :',NL,PAD     81360
MGTOOVO2 FDPRT 0(R3),L'OLDVOLS,DEB,PAD                           82017
         FDPRT *END                                              81360
         SPACE 1                                                 81360
NOTMANY  LA    R15,DS1DSNAM  GET DATA SET NAME                   81360
         MVC   DS1DSNAM,JFCBDSNM  MAKE SURE IT IS THERE          81360
         SLR   R0,R0         CLEAR CVOL VOLSER                   81360
         LA    R1,CATWORK    GET CATALOG WORK AREA               81360
         L     R14,PATLOC    GET LOCATE PATTERN                  81360
         STM   R14,R1,DB     BUILD LOCATE CAMLST                 81360
         LOCATE DB           SEE IF CATALOGED                    81360
         BXH   R15,R15,NOTCATLG  SKIP IF CVOL NOT MOUNTED, ETC.  81360
         MVC   JFCBDSNM,DS1DSNAM  COPY DSN IN CASE CHANGED BY CATLG
         MVC   VOLJFCB,CATWORK+6  COPY VOLSER                    81360
         OI    CATFG,FGRECAT   SHOW RECATALOG NEEDED             81360
         CLI   CATWORK+4,UCB3DACC  DISK ENTRY ?                  81360
         BNE   NOTCATLG      NO; TREAT AS UNCATALOGED            81360
         LA    R5,USERVENT   GET TABLE OF VOLUMES                81360
OLDCLOOK CLC   VOLJFCB,MAPVOL  DID WE OBTAIN IT ALREADY ?        81360
         BE    NOTCATLG      YES; IGNORE                         81360
         LA    R5,MAPLEN(,R5)  TRY AGAIN                         81360
         TM    MAPUCB,X'80'  END OF LIST ?                       81360
         BZ    OLDCLOOK      NO; TRY AGAIN                       81360
         SERVCALL DSDS1,VOLJFCB  SEE IF IT EXISTS                81360
         CH    R5,=H'4'      VOLUME NOT MOUNTED - SPECIAL MESSAGE
         BE    BADCATMT      SHOW UNABLE TO MOUNT OLD VOLUME     81360
         BH    NOTCATLG      TREAT AS UNCATALOGED IF NOT FOUND   81360
         MVC   DS1FMTID(DS1END-DS1FMTID),0(R1)  SAVE FORMAT 1    81360
         OI    FLAGS,FGUSER  SET USER VOLUME                     81360
         LA    R5,USERVENT   SET TO USER ENTRY                   81360
         MVC   MAPVOL,VOLJFCB  SET VOLSER                        81360
         ST    R5,POINTVOL   SET POINTER TO VOLUME ENTRY         81360
         SERVCALL UCBDK,VOLJFCB  LOOK FOR UCB                    81360
         LTR   R4,R0         FOUND ?                             81360
         BNZ   BADCATMT      NO; UNABLE TO MOUNT PACK            81360
         ST    R4,MAPUCB     SAVE UCB ADDRESS                    81360
         SERVCALL TIOUA,(R4)  SEE IF USABLE DD CARD              81360
         LTR   R0,R0         ANY FOUND ?                         81360
         BNZ   TESTREP       YES; SET REPLACE REQUIRED           81360
BADCATMT LA    R3,MGCATMT    SHOW UNABLE MOUNT                   81360
         BAS   R9,ABENDLST                                       81360
MGCATMT  FDOPT NL,CC=C'0'                                        81360
         FDPRT 'DATA SET',PADR                                   81360
         FDPRT DS1DSNAM,DEB                                      81360
         FDPRT 'CATALOGED ON',PAD                                81360
         FDPRT VOLJFCB,DEB                                       81360
         FDPRT ', BUT NO DD CARD FOR VOLUME SUPPLIED.'           81360
         FDPRT *END                                              81360
         SPACE 2                                                 81360
*---------------------------------------------------------------------*
*  CHECK FOR PRESENCE OF A DD FOR ALLOCATION OF OLD DATA SET          *
*---------------------------------------------------------------------*
TESTREP  OI    FLAGS,FGOLD   SHOW DATA SET ALREADY EXISTS        81360
NOTCATLG TM    FLAGS,FGOLD   EXISTING DATA SET ?                 81360
         BZ    NEEDALLO      NO; NEED TO ALLOCATE                81360
         L     R5,POINTVOL   GET VOLUME ENTRY BACK               81360
         MVC   VOLJFCB,MAPVOL  REFRESH VOLSER                    81360
         MVC   JFCBVOLS(6),MAPVOL                                81360
         ICM   R4,15,MAPUCB  UCB FOUND ?                         81360
         BZ    BADREPMT      NO; TOO BAD                         81360
         SERVCALL TIOUA,(R4)  DOUBLE-CHECK FOR DD                81360
         LTR   R0,R0         ANY FOUND ?                         81360
         BNZ   REPTEST       YES; PROCEED                        81360
BADREPMT LA    R3,MGREPMT    SHOW UNABLE TO MOUNT                81360
         BAS   R9,ABENDLST                                       81360
MGREPMT  FDOPT NL,CC=C'0'                                        81360
         FDPRT 'DATA SET',PADR                                   81360
         FDPRT DS1DSNAM,DEB                                      81360
         FDPRT 'FOUND ON',PAD                                    81360
MGREPMX  FDPRT VOLJFCB,DEB                                       81360
         FDPRT ', BUT NO DD CARD FOR VOLUME SUPPLIED.'           81360
         FDPRT *END                                              81360
         SPACE 1                                                 81360
REPTEST  TM    DS1DSORG,JFCORGPO  PARTITIONED DATA SET ?         81360
         BNZ   REPPDS        YES; DO SPECIAL MEMBER CODE         81360
*   OMISSION OF MEMBER/NO MEMBER TEST IN JFCB IS INTENTIONAL
*   I.E. USER MAY REPLACE PS BY PO DATA SET                      81360
         TM    FLAGS,FGREP   DID USER USE PARM=REPLACE ?         81360
         BNZ   REPSCR        YES; SCRATCH OLD DATA SET           81360
         LA    R3,MGREPOP    SHOW MISSING REPLACE OPTION         81360
         BAS   R9,ABENDLST                                       81360
MGREPOP  FDEXEC MGREPMT,MGREPMX                                  81360
         FDPRT ', BUT PARM=REPLACE WAS NOT SPECIFIED.'           81360
         FDPRT *END                                              81360
         SPACE 1                                                 81360
REPSCR   DS    0H                                               GP03126
         AIF   (NOT &LOCO).SKIPL03                              GP03126
         OI    A$SACES,A$XADELT+A$XALLOC DELETE+RE-ALLOCATE      93218
         BAL   R9,ACCCHECK   CHECK ACCESS                        93218
.SKIPL03 L     R14,PATSCR    GET SCRATCH PATTERN                 81360
         LA    R15,DS1DSNAM  POINT TO NAME                       81360
         SLR   R0,R0                                             81360
         STH   R0,CATCODE    CLEAR RETURN CODE                   82018
         LA    R1,CATCOUNT   POINT TO VOLUME LIST                81360
         STM   R14,R1,DB     MAKE LIST                           81360
         MVC   CATVOL,MAPVOL  COPY VOLSER                        81360
         L     R1,MAPUCB     GET UCB                             81360
         MVC   CATTYPE,UCBTBYT1-UCBOB(R1)  ADD DEVICE TYPE       81360
         SCRATCH DB          SCRATCH THE OLD COPY                81360
         BXLE  R15,R15,SETSALLO  GO TO ALLOCATE NEW ONE          81360
         LA    R3,MGBADSC                                        81360
         BAS   R9,ABENDLST                                       81360
MGBADSC  FDEXEC MGREPMT,MGREPMX                                  81360
         FDPRT ', BUT REPLACEMENT OF OLD DATA SET FAILED'        81360
         FDPRT *END                                              81360
         SPACE 1                                                 81360
REPPDS   TM    JFCBIND1,JFCPDS  MEMBER SPECIFIED ?               81360
         BNZ   REPPDS2       YES; CHECK DCB PARAMETERS           81360
         LA    R3,MGREPMNM                                       81360
         BAS   R9,ABENDLST                                       81360
MGREPMNM FDEXEC MGREPMT,MGREPMX                                  81360
         FDPRT 'IS A PDS, BUT MEMBER NAME NOT SUPPLIED',PADL     81360
         FDPRT *END                                              81360
         SPACE 1                                                 81360
REPPDS2  CLI   DS1DSORG+1,0  FUNNY DSORG ?                       81360
         BNE   BADREPDC      YES; FAIL                           81360
         TM    DS1DSORG,255-JFCORGPO-JFCORGU                     81360
         BNZ   BADREPDC                                          81360
         CLI   DS1RECFM,JFCUND   RECFM=U ?                       81360
         BE    REPPDS3       YES; PROCESS                        81360
         TM    DS1RECFM,JFCFIX   FIXED ?                         81360
         BZ    BADREPDC      NO; FAIL                            81360
         TM    DS1RECFM,JFCVARD+JFCVAR  RECFM=V OR =D ?          81360
         BZ    REPPDS3       NEITHER; PROCEED                    81360
BADREPDC SERVCALL DSFMT,DS1DSORG  FORMAT DSORG/DCB PARAMETERS    81360
         MVC   CATWORK(27),0(R1)  MOVE TO SAVE AREA              81360
         LA    R3,MGBADDCB                                       81360
         BAS   R9,ABENDLST   SHOW UNSUPPORTED DCB ATTRIBUTES     81360
MGBADDCB FDEXEC MGREPMT,MGREPMX                                  81360
         FDPRT 'HAS UNSUPPORTED FORMAT : ',PADL                  81360
DSFORM   FDPRT 'DSORG=',PADL                                     81360
         FDPRT CATWORK,3,DEB                                     81360
         FDPRT 'RECFM=',PADL                                     81360
         FDPRT CATWORK+3,6,DEB                                   81360
         FDPRT 'LRECL=',PADL                                     81360
         FDPRT CATWORK+22,5,DEB                                  81360
         FDPRT 'BLKSIZE=',PADL                                   81360
DSFORMX  FDPRT CATWORK+17,5,DEB                                  81360
*        FDPRT 'OPTCD=',PADL                                     81360
*SFORMX  FDPRT CATWORK+9,8,DEB,PADR                              81360
         FDPRT *END                                              81360
         SPACE 1                                                 81360
REPPDS3  CLI   JFCRECFM,0    ANY OVERRIDE ?                      81360
         BE    REPPDS3F      NO                                  81360
         CLC   JFCRECFM,DS1RECFM  MATCH ?                        81360
         BNE   BADJFDS1      SHOW JFCB/DS1 MISMATCH              81360
REPPDS3F CLC   JFCBLKSI,DS1BLKL  SAME BLOCKSIZE ?                81360
         BE    REPPDS3B                                          81360
         CLC   JFCBLKSI,ZEROES  OR ZERO ?                        81360
         BNE   BADJFDS1      NO                                  81360
REPPDS3B CLC   JFCLRECL,DS1LRECL                                 81360
         BE    OPENOUT       OK; OPEN FOR OUTPUT                 81360
         CLC   JFCLRECL,ZEROES  OR ZERO ?                        81360
         BE    OPENOUT       OK; OPEN                            81360
BADJFDS1 SERVCALL DSFMT,DS1DSORG                                 81360
         MVC   CATWORK(27),0(R1)  FORMAT DCB ATTRIBUTES          81360
         PRTLIST MGBADCB1,DEV=(1,2)                              81360
         SERVCALL DSFMT,JFCDSORG                                 81360
         MVC   CATWORK(27),0(R1)  FORMAT JFCB DCB ATTRIBUTES     81360
         LA    R3,MGBADCB2   SET FOR INCONSISTENCY               81360
         BAS   R9,ABENDMSG                                       81360
MGBADCB1 FDEXEC MGREPMT,MGREPMX  FORMAT OLD DATA SET INFO        81360
         FDPRT '  ',PAD                                          81360
         FDEXEC DSFORM,DSFORMX                                   81360
         FDPRT *END                                              81360
MGBADCB2 FDPRT 'SYSUT2 ATTRIBUTES CONFLICT : ',NL,PADR           81360
         FDEXEC DSFORM,DSFORMX                                   81360
         FDPRT *END                                              81360
         SPACE 2                                                 81360
SETSALLO OI    FLAGS,FGSCRD  SET SCRATCH FLAG                    81360
NEEDALLO ICM   R5,15,POINTVOL  GET REPLACEMENT VOLUME            81360
         BZ    SPACEUCB      NOT FOUND; USE ONE OF OURS          82018
         TM    FLAGS,FGUSER+FGOLD  USER OR EXISTING DATA SET ?   82018
         BNZ   NEEDALLD      YES; USE IT                         82018
SPACEUCB ICM   R5,15,POINTUCB  ELSE GET VOLUME WITH MOST FREE SPACE
         ST    R5,POINTVOL   SAVE POINTER TO IT                  81360
         BNZ   NEEDALLD      OK                                  81360
BADUCBDD LA    R3,MGUCBDDS                                       81360
         BAS   R9,ABENDMSG                                       81360
MGUCBDDS VCON  '0REQUIRED DD CARDS FOR ALLOCATION NOT SUPPLIED'  81360
         SPACE 1                                                 81360
NEEDALLD TM    JFCBIND1,JFCPDS  DOING A PDS MEMBER ?             81360
         BNZ   NEEDALLP      YES; SPECIAL PROCESSING             81360
         OI    JFCBIND1,JFCRLSE  SET FOR SPACE RELEASE AFTER CLOSE
         XC    JFCBDQTY,JFCBDQTY  CLEAR DIRECTORY BLOCK COUNT    81360
         MVI   DS1DSORG,JFCORGPS  SET FOR FINAL MESSAGE          81360
         B     NEEDALLC      GO TO COMMON                        81360
NEEDALLP MVI   DS1DSORG,JFCORGPO  SET FOR FINAL MESSAGE          81360
         CLC   JFCBDQTY,ZEROES  DIRECTORY COUNT SET ?            81360
         BNE   NEEDALLC      YES; GO TO COMMON                   81360
         L     R4,MAPUCB     GET UCB FOR THIS VOLUME             81360
         SLR   R1,R1                                             81360
         USING UCBOB,R4                                          81360
         IC    R1,UCBTBYT4   GET DISK SUB-TYPE                   81360
         DROP  R4                                                81360
         LA    R15,15                                            81360
         NR    R1,R15        MASK IT                             81360
         LA    R1,PDSDENUM(R1)  GET DEFAULT DIRECTORY BLOCKS     81360
         MVC   JFCBDQTY+L'JFCBDQTY-1(1),0(R1)  SET DE COUNT      81360
NEEDALLC MVI   DS1DSORG+1,0  SET FOR FINAL MESSAGE               81360
         CLC   JFCBPQTY,ZEROES  PRIMARY SPACE SPECIFIED ?        81360
         BNE   NEEDALLS      YES; LEAVE IT ALONE                 81360
         MVC   JFCBPQTY,=AL3(10)  SET 10 TRACK DEFAULT           81360
         NI    JFCBCTRI,255-JFCBSPAC  ZERO REQUEST TYPE          81360
         OI    JFCBCTRI,JFCBTRK   AND SET FOR TRACK ALLOCATION   81360
NEEDALLS CLC   JFCBSQTY,ZEROES    SECONDARY SPECIFIED ?          81360
         BNE   NEEDALLV      YES                                 81360
         MVC   JFCBSQTY,=AL3(10)  SET TEN OF ANYTHING            81360
NEEDALLV MVC   JFCBVOLS(6),MAPVOL  COPY REQUESTED VOLSER         81360
         OI    JFCBIND2,JFCNEW   SET FOR NEW DATA SET            81360
         TM    JFCBCTRI,JFCONTIG+JFCMIXG+JFCALX  SPECIAL REQUEST ?
         BNZ   *+8           YES                                 81360
         OI    JFCBCTRI,JFCONTIG  REQUEST CONTIGUOUS EXTENTS     81360
         AIF   (NOT &LOCO).SKIPL04                              GP03126
         OI    A$SACES,A$XALLOC   REQUEST ALLOCATION             93218
         BAS   R9,ACCCHECK   GET PERMISSION                      93218
.SKIPL04 TIME  TU ,          GET THE DATE                        81360
         SLR   R0,R0         CLEAR TIME                          81360
         STM   R0,R1,DB                                          81360
         CVB   R1,DB         MAKE BINARY YYYYDDD                GP03126
         D     R0,=F'1000'   YYYY/DD                            GP03126
         STCM  R0,3,JFCBCRDT+1  SET DAYS                         81360
         STC   R1,JFCBCRDT   AND YEAR OF ALLOCATION              81360
         SERVCALL ALCDS,INFMJFCB   ALLOCATE THE DATA SET         81360
         STM   R15,R0,DB     SAVE ERROR CODES                    81360
         BXLE  R15,R15,DIDALLOC  SHOW ALLOCATION DONE            81360
         LA    R3,MGBADALC                                       81360
         BAS   R9,ABENDLST                                       82029
MGBADALC FDOPT NL,CC=C'0'                                        81360
         FDPRT 'ALLOCATION OF'                                   81360
         FDPRT JFCBDSNM,DEB,PAD                                  81360
         FDPRT 'ON'                                              81360
         FDPRT JFCBVOLS,6,DEB,PAD                                81360
         FDPRT 'FAILED; ERROR CODE'                              81360
         FDPRT DB,4,I,PAD                                        81360
         FDCLI DB+4,C'0',BL=MGBADALO                             81360
         FDPRT '   SEE MESSAGE DTS',PADL                         81360
         FDPRT DB+4,4                                            81360
MGBADALO FDPRT *END                                              81360
         SPACE 1                                                 81360
DIDALLOC OI    FLAGS,FGALC   SHOW DATA SET ALLOCATED             81360
OPENOUT  TM    FLAGS,FGTAPOUT   TAPE OUTPUT ?                    81360
         BNZ   OPENOUTP      YES; USE SYSUT2                     81360
         AIF   (NOT &LOCO).SKIPL05                              GP03126
         BAS   R9,ACCCHECK   GET PERMISSION                      93218
.SKIPL05 L     R5,POINTVOL   GET VOLUME POINTER                  81360
         SERVCALL UCBDK,MAPVOL  GET THE UCB BACK                 81360
         LTR   R4,R0                                             81360
         BZ    BADUCBDD      PROGRAM ERROR ???                   81360
         USING UCBOB,R4                                          81360
         MVC   JFCBVOLS(6),UCBVOLI  UPDATE THE DISK VOLSER       81360
         MVC   CATVOL,JFCBVOLS  UPDATE THE CATALOG VOLUME ALSO   82018
         MVC   CATTYPE,UCBTBYT1  COPY TYPE FOR LATER CATALOG     81360
         DROP  R4                                                81360
         SERVCALL TIOUA,(R4)  GET THE TIOT ENTRY FOR IT          82018
         LTR   R4,R0                                             81360
         BZ    BADUCBDD      HUH ?                               81360
         MVC   DCBDDNAM-IHADCB+SYSUT2,TIOEDDNM-TIOENTRY(R4)  COPY DDN
OPENOUTP LA    R5,SYSUT2
         USING IHADCB,R5                                         81360
         OPEN  MF=(E,UT2OPLST),TYPE=J  OPEN SYSUT2               81360
         LA    R3,MGBADOPN   SET FOR BAD OPEN                    81360
         TM    DCBOFLGS,DCBOFOPN   WAS THERE AN ERROR IN THE OPEN?
         BZ    ABENDMSG      YES; WRITE MESSAGE                  81360
         MVC   DS1BLKL,DCBBLKSI  UPDATE THE DCB ATTRIBUTES       81360
         MVC   DS1LRECL,DCBLRECL                                 81360
         MVC   DS1RECFM,DCBRECFM                                 81360
         MVC   DS1OPTCD,DCBOPTCD                                 81360
         TM    FLAGS,FGTAPOUT   TAPE OUTPUT ?                    81360
         BZ    OPENGOOD                                          81360
         MVI   DS1DSORG,JFCORGPS                                 81360
         MVI   DS1DSORG+1,0                                      81360
OPENGOOD L     R1,DCBDEBAD   GET THE DEB                         81360
         N     R1,=X'00FFFFFF'  CLEAN IT                        GP03126
         L     R1,DEBBASND-DEBBASIC(,R1)  GET THE UCB            81360
         N     R1,=X'00FFFFFF'  CLEAN IT                        GP03126
         MVC   JFCBVOLS(6),UCBVOLI-UCBOB(R1)  UPDATE THE TAPE VOLSER
         TM    DCBRECFM,DCBRECU   WYLBUR COMPRESS REQUIRED ?     81360
         BO    COMPRESS      YES; DO COMPRESSION                 81360
         MVC   OUTMAX+2(2),DCBLRECL  COPY MAXIMUM LRECL          82032
         MVC   SYSUT1+24(2),DCBLRECL  SET PADDING LENGTH         82032
LOOPASIS INPGET ,            GET AN INPUT RECORD                 81360
         BXH   R15,R15,INPERR  SIGNAL INPUT ERROR                81360
         C     R0,OUTMAX     RECORD TOO LONG ?                   82032
         BNH   LOOPAPUT      NO                                  82032
         INC   OUTOVER,WORK=R15  COUNT TRUNCATION                82032
LOOPAPUT LR    R0,R1         COPY RECORD ADDRESS                 82032
         PUT   SYSUT2        WRITE RECORD IN R0                  81360
         INC   #RECS         COUNT RECORDS                       81360
         B     LOOPASIS      CONTINUE                            81360
         SPACE 2                                                 81360
COMPRESS SLR   R2,R2                                             81360
         ICM   R2,3,DCBBLKSI GET BLOCKSIZE                       81360
         GETMAIN R,LV=(R2)   GET A BUFFER (FOR MOVE MODE)        81360
         ST    R1,WCMBUFAD   SAVE BUFFER ADDRESS                 81360
         STH   R2,WCMBUFMX   SET MAXIMUM BUFFER SIZE             81360
         STCM  R2,12,WCMBUFLN  SET LENGTH IN USE (NONE)          81360
COMPGET  INPGET ,            GET AN INPUT RECORD                 81360
         BXH   R15,R15,INPERR  SIGNAL INPUT ERROR                82032
         C     R0,OUTMAX     RECORD TOO LONG ?                   82032
         BNH   COMPAPUT      NO                                  82032
         INC   OUTOVER,WORK=R15  COUNT TRUNCATION                82032
         L     R0,OUTMAX     TRUNCATE TO MAXIMUM                 82032
COMPAPUT ST    R1,WCMRECAD   SET THE RECORD ADDRESS              82032
         STH   R0,WCMRECLN   SET THE RECORD LENGTH               81360
         INC   #RECS         COUNT INPUT RECORDS                 81360
COMPPRES SERVCALL WCOMP,COMPLIST  COMPRESS THE RECORD            81360
         CH    R15,=H'4'     CHECK RETURN CODE                   81360
         BL    COMPGET       DONE; GET NEXT RECORD               81360
         BH    COMPERR       ERROR                               81360
         STH   R0,DCBLRECL   SET THE BLOCK SIZE                  81360
         L     R0,WCMBUFAD   GET BUFFER ADDRESS BACK             81360
         PUT   SYSUT2        WRITE THE BLOCK                     81360
         INC   #BLOCKS       COUNT OUTPUT BLOCKS                 81360
         B     COMPPRES      COMPRESS CURRENT RECORD AGAIN       81360
MGBADOPN VCON  '0OPEN FOR SYSUT2 FAILED'                         81360
INPERR   LA    R3,MGBADINP                                       81360
         B     COMERRIN                                          82018
MGBADINP FDPRT 'ERROR PROCESSING INPUT FILE',NL                  82018
         FDGOTO MGOUTIN      FORMAT COMMON INPUT INFORMATION     82018
         SPACE 1                                                 81360
COMPERR  CH    R15,=H'8'     SPECIAL ERROR MESSAGE ?             81360
         BNE   COMPERRN      NO                                  81360
         LA    R15,7         MAKE MASK                           81360
         NR    R15,R0        GET MESSAGE NUMBER                  81360
         MH    R15,=Y(MGOUT2-MGOUT1)  GET OFFSET                 81360
         LA    R2,MGOUT1(R15)  POINT TO MESSAGE                  81360
         LA    R3,MGOUTFD    POINT TO MESSAGE LIST               81360
COMERRIN LH    R4,WCMRECLN   GET RECORD LENGTH                   82018
         LA    R0,80         SET MAXIMUM TO BE PRINTED           82018
         CR    R4,R0                                             82018
         BNH   *+6           NOT TOO LONG                        82018
         LR    R4,R0         ELSE TRUNCATE                       82018
         BAS   R9,ABENDLST   PRINT IT                            81360
MGOUTFD  FDOPT NL,CC=C'0'                                        81360
         FDPRT 'UNRECOVERABLE OUTPUT ERROR -',PADR               81360
         FDPRT 0(R2),L'MGOUT1                                    81360
MGOUTIN  FDPRT 'INPUT RECORD',NL,PAD                             82018
         FDPRT #RECS,I,PAD,LEN=6                                 82018
         FDPRT 'LAST/CURR SEQUENCE=',PADL                        82018
         FDPRT WCMLINE#,I,PADR,LEN=9                             82018
         FDPRT ''''                                              82018
         FDPRT WCMRECAD,(R4),*   INDIRECT ADDRESS; REGISTER LEN  82018
         FDPRT ''''                                              82018
         FDPRT *END                                              81360
MGOUT1   DC    CL28'PROCESSING ERROR'                            81360
MGOUT2   DC    CL28'PROCESSING ERROR'                            81360
         DC    CL28'BLOCK NOT IN EDIT FORMAT'                    81360
         DC    CL28'BLOCKSIZE INVALID'                           81360
         DC    CL28'INVALID RECORD LENGTH'                       81360
         DC    CL28'SEQUENCE # BAD OR OVERFLOW'                  81360
         DC    CL28'SEQUENCE NUMBER OUT OF SEQ.'                 82018
         DC    CL28'PROCESSING ERROR'                            81360
COMPERRN LA    R3,MGBADOUT                                       81360
         B     COMERRIN                                          82018
MGBADOUT FDPRT 'ERROR WRITING OUTPUT',NL                         82018
         FDGOTO MGOUTIN      FORMAT INPUT RECORD INFO            82018
         SPACE 2                                                 81360
DSEND    TM    DCBRECFM,DCBRECU  COMPRESSING ?                   81360
         BNO   CLOSEOUT      NO; CLOSE OUTPUT FILE               81360
         SLR   R1,R1                                             81360
         ST    R1,WCMRECAD   SIGNAL CLOSE CALL                   81360
         SERVCALL WCOMP,COMPLIST  SEE IF FINAL WRITE NEEDED      81360
         LTR   R0,R0         ANYTHING TO WRITE ?                 81360
         BNP   CLOSEOUT      NO                                  81360
         STH   R0,DCBLRECL   SET FINAL BLOCK                     81360
         L     R0,WCMBUFAD   GET BUFFER ADDRESS BACK             81360
         PUT   SYSUT2        AND WRITE THE LAST BLOCK            81360
         INC   #BLOCKS       COUNT OUTPUT BLOCKS                 81360
         SPACE 1                                                 81360
CLOSEOUT CLC   #RECS,ZEROES  ANY RECORDS READ ?                  81360
         BNE   CLOSEOUC      YES                                 81360
         LA    R3,MGINPNUL                                       81360
         BAS   R9,ABENDMSG   ABEND WITH ERROR CODE               81360
MGINPNUL VCON  '0INPUT CONTAINS NO DATA'                         81360
CLOSEOUC CLOSE (,DISP),MF=(E,UT2OPLST)  CLOSE THE OUTPUT FILE    81360
         TM    FLAGS,FGTAPOUT  TAPE OUTPUT ?                     81360
         BNZ   SKIPCAT       YES; LET USER CATALOG ON JCL        81360
         L     R14,PATCAT    GET CATALOG PATTERN                 81360
         TM    CATFG,FGRECAT   USE RECATALOG ?                   81360
         BZ    *+8           NO                                  81360
         L     R14,PATRECAT  SET RECATALOG                       81360
         LA    R15,JFCBDSNM  POINT TO DATA SET NAME              81360
         SLR   R0,R0         CLEAR CVOL                          81360
         STH   R0,CATCODE    CLEAR RETURN CODE                   82018
         LA    R1,CATCOUNT   POINT TO CATALOG LIST               82018
         STM   R14,R1,DB     MAKE CATALOG CAMLST                 81360
         CLI   CATTYPE+2,UCB3DACC   DASD DATA SET ?              82018
         BNE   CLOSECAT      NO; ALL DONE ?                      82018
         NI    CATTYPE+1,255-UCBRR-UCBVLPWR-UCBDVPWR  FIX BYTE 2 82018
         CLI   CATTYPE+3,X'0A'    3340 ?                         82018
         BNE   CLOSECAT      NO                                  82018
         NI    CATTYPE+1,255-UCBRPS  RESET RPS BIT ALSO          82018
CLOSECAT CATALOG DB          TRY TO CATALOG                      82018
         STM   R15,R0,DB     SAVE RETURN CODE                    81360
         OI    CATFG,FGCAT   SHOW CATALOGED                      81360
         BXLE  R15,R15,SKIPCAT  ALL DONE                         81360
         XI    CATFG,FGCAT+FGNOCAT  SHOW CATALOG FAILED          81360
         TM    FLAGS,FGALC   WAS THIS DATA SET ALLOCATED ?       81360
         BNZ   SKIPCAT       YES; LEAVE ERROR MESSAGE            81360
         NI    CATFG,255-FGNOCAT  IRRELEVANT                     81360
SKIPCAT  PRTLIST FINALMS1,DEV=(1,2)    WRITE FINAL MESSAGE       81360
         SERVCALL DSFMT,DS1DSORG  FORMAT DCB ATTRIBUTES AGAIN    81360
         MVC   CATWORK(27),0(R1)  COPY                           81360
         PRTLIST FINALMS2    LONGER MESSAGE ON SYSPRINT          81360
         ESTAE 0             CANCEL STAE                         82017
         SERVTERM ,          CLOSE AND FREE EVERYTHING           81360
         L     R9,RETCODE    GET RETURN CODE                     81360
         PGMEXIT RC=(R9)     AND EXIT                            81360
         TITLE 'W Y L C O M P  ***  STAE EXIT / RETRY'           81360
STAEEXIT LM    R11,R12,CSECTADD-STAEEXIT(R15)
         LTCB  R13           GET MY TCB                         GP03126
         L     R13,TCBFSA-TCB(,R13)  GET HIGH SAVE AREA          81360
         L     R13,8(,R13)   GET OUR WORK AREA BACK              81360
         SLR   R15,R15
         XC    CATWORK(128),CATWORK  CLEAR TEMP WORK AREA        81360
         LA    R4,12
         CR    R0,R4         WORK AREA PROVIDED ?
         BE    STAEEXR1      NO; R1 HAS COMPLETION CODE          81360
         MVC   CATWORK(104),0(R1)  SAVE CONTENTS OF WORK AREA    81360
         NC    ABENDOPT,=X'90000000'  LEAVE ONLY THE DUMP/STEP BITS
         OC    ABENDOPT,4(R1)  COPY TRUE ABEND CODE              81360
         ICM   R1,15,4(R1)   LOAD ABEND CODE FROM WORK AREA      81360
STAEEXR1 LA    R1,0(,R1)     CLEAR THE HIGH BYTE                 81360
         LR    R3,R1         PRESERVE THE CODE                   81360
         SRL   R1,12         SYSTEM COMPLETION CODE ?            81360
         SLA   R1,4          SHIFT BACK FOR UNNECESSARY SIGN NYBBLE
         ST    R1,DB+4                                           81360
         BNZ   STAEEXSY      YES; UNPACK                         81360
         CVD   R3,DB         ELSE PACK                           81360
         LR    R1,R3         SIGNAL USER CODE                    81360
STAEEXSY UNPK  ABENDCOD,DB+6(2)                                  81360
         OI    ABENDCOD+3,C'0'                                   81360
         TR    ABENDCOD,HEXTRTAB-C'0'  MAKE EBCDIC               81360
         CR    R1,R3                                             81360
         BE    *+8                                               81360
         MVI   ABENDCOD,C'S'                                     81360
         CR    R0,R4         WORK AREA PRESENT ?                 81360
         BER   R14           NO, EXIT (ABEND SOME MORE)
         TM    FLAGS,FGCURSE ABEND RECURSION ?                   81360
         BNZR  R14           YES; JUST QUIT                      81360
         OI    FLAGS,FGCURSE                                     81360
         LA    R0,RETRY
         LA    R15,4         INDICATE RETRY
         BR    R14
         SPACE 2
RETRY    LM    R11,R12,CSECTADD-RETRY(R15)
         LTCB  R13           GET MY TCB                         GP03126
         L     R13,TCBFSA-TCB(,R13)  GET HIGH SAVE AREA          81360
         L     R13,8(,R13)   GET OUR WORK AREA BACK              81360
         CLC   ZEROES,CATWORK  PSW AND REGISTERS STORED ?        81360
         BE    RETRYDMP      NO                                  81360
         PRTLIST WORKSTAE    PRINT STAE WORK AREA                81360
RETRYDMP TM    FLAGS,FGALC   WAS A DATA SET EXPLICITLY ALLOCATED ?
         BZ    RETRYMSG      NO; JUST ISSUE MESSAGE              81360
         L     R14,PATSCR    GET SCRATCH PATTERN                 81360
         LA    R15,JFCBDSNM  POINT TO NAME                       81360
         SLR   R0,R0                                             81360
         STH   R0,CATCODE    CLEAR RETURN CODE                   82018
         LA    R1,CATCOUNT   POINT TO VOLUME LIST                81360
         STM   R14,R1,DB     MAKE LIST                           81360
         SCRATCH DB          SCRATCH THE OLD COPY                81360
         NI    FLAGS,255-FGALC  RESET                            81360
RETRYMSG LA    R3,MGABEND    SET FOR ABEND MESSAGE               81360
*        BAS   R9,ABEND                                          81360
         SPACE 1                                                 81360
ABENDLST PRTLIST (R3),DEV=(1,2)                                  81360
         B     ABEND                                             81360
         SPACE 1                                                 93218
         AIF   (NOT &LOCO).SKIPL69                              GP03126
ACCCHECK ACCTSVC A$SVCRB,ERR=(R9) CHECK ACCESS; ALLOW IF NO ACCT 93218
         BXLE  R15,R15,0(R9)   ACCESS PERMITTED                  93218
         SPACE 1                                                 93218
ACCDENY  PRTLIST MSGDENY,DEV=(1,2)                               93218
         PRTV  CATWORK,DEV=(1,2),CC=NO                           93218
         B     ABEND                                             93218
         SPACE 1                                                 93218
MSGDENY  FD    'ACCESS TO DATA SET',NL                           93218
         FD    DS1DSNAM,DEB,PAD                                  93218
         FD    'WAS DENIED'                                      93218
         FD    *END                                              93218
.SKIPL69 SPACE 1                                                 81360
ABENDMSG PRTV  (R3),DEV=(1,2)   PRINT ERROR MESSAGE              81360
ABEND    PRTROOM 0,DEV=(1,2) FORCE BUFFER WRITES                 81360
         L     R1,ABENDOPT   GET ABEND OPTIONS AND CODE          81360
         SVC   13            GREETINGS                           81360
         SPACE 1                                                 81360
CSECTADD DC    A(WYLCOMP,WYLCOMP+4096)                           81360
MGABEND  FDOPT NL,CC=C'0'                                        81360
         FDCLC ABENDCOD,U999,4,BE=MGABENDF  DIFFERENT MESSAGE    81360
         FDPRT 'WYLCOMP ABENDED WITH CODE',PADR                  81360
         FDPRT ABENDCOD                                          81360
         FDPRT *END                                              81360
MGABENDF FDPRT 'WYLCOMP TERMINATED DUE TO ERRORS'                81360
         FDPRT *END                                              81360
U999     DC    C'0999'                                           81360
HEXTRTAB DC    C'0123456789ABCDEF'                               81360
         SPACE 1                                                 81360
WORKSTAE FDOPT NL,CC=C'-'                                        81360
         FDPRT 'ESTAE WORK AREA'                                 81360
         FDPRT CATWORK,4,HEX,PAD,RADJ,LEN=15                     81360
         FDPRT CATWORK+4,4,HEX,PAD,LEN=15                        81360
         FDPRT 'PSWS :',PAD                                      81360
         FDPRT CATWORK+8,4,HEX,PAD                               81360
         FDPRT CATWORK+12,4,HEX,PAD,LEN=14                       81360
         FDPRT CATWORK+16,4,HEX,PAD                              81360
         FDPRT CATWORK+20,4,HEX,PAD                              81360
         FDPRT 'REGISTERS 0-7 :',NL,PAD                          81360
         FDPRT CATWORK+24,4,HEX,PAD                              81360
         FDPRT CATWORK+28,4,HEX,PAD                              81360
         FDPRT CATWORK+32,4,HEX,PAD                              81360
         FDPRT CATWORK+36,4,HEX,PAD,LEN=14                       81360
         FDPRT CATWORK+40,4,HEX,PAD                              81360
         FDPRT CATWORK+44,4,HEX,PAD                              81360
         FDPRT CATWORK+48,4,HEX,PAD                              81360
         FDPRT CATWORK+52,4,HEX,PAD                              81360
         FDPRT 'REGISTERS 8-F :',NL,PAD                          81360
         FDPRT CATWORK+56,4,HEX,PAD                              81360
         FDPRT CATWORK+60,4,HEX,PAD                              81360
         FDPRT CATWORK+64,4,HEX,PAD                              81360
         FDPRT CATWORK+68,4,HEX,PAD,LEN=14                       81360
         FDPRT CATWORK+72,4,HEX,PAD                              81360
         FDPRT CATWORK+76,4,HEX,PAD                              81360
         FDPRT CATWORK+80,4,HEX,PAD                              81360
         FDPRT CATWORK+84,4,HEX,PAD                              81360
         FDPRT *END                                              81360
         SPACE 1                                                 81360
FINALMS1 FDOPT NL,CC=C'0'                                        81360
         FDPRT 'DATA SET'                                        81360
         FDPRT JFCBDSNM,DEB,PADL                                 81360
         FDTM  JFCBIND1,JFCPDS,BZ=FINALNMN                       81360
         FDPRT '('                                               81360
         FDPRT JFCBELNM,DEB                                      81360
         FDPRT ')',PADR                                          81360
FINALNMN FDPRT 'ON',PAD                                          81360
         FDPRT JFCBVOLS,6,DEB,PAD                                81360
         FDTM  CATFG,FGCAT+FGNOCAT,BZ=FINALNCT                   81360
         FDPRT 'HAS',PAD                                         81360
         FDTM  CATFG,FGNOCAT,BZ=FINALGCT                         81360
         FDPRT 'NOT',PAD                                         81360
FINALGCT FDPRT 'BEEN',PAD                                        81360
         FDTM  CATFG,FGRECAT,BZ=FINALRCT                         81360
         FDPRT 'RE',PADL                                         81360
FINALRCT FDPRT 'CATALOGED'                                       81360
FINALNCT FDCLC OUTOVER,ZEROES,4,BE=FINALNOV                      82032
         FDPRT '*****',NL,PADR                                   82032
         FDPRT OUTOVER,I                                         82032
         FDPRT 'TRUNCATED TO',PAD
         FDPRT OUTMAX,I                                          82032
         FDPRT 'BYTES *****',PADL                                82032
FINALNOV FDPRT *END                                              82032
FINALMS2 FDPRT #RECS,I,NL                                        81360
         FDCLC #RECS,INTEGER1,4,BE=FINALM1R
         FDPRT 'RECORDS PROCESSED  ',PAD                         81360
         FDGOTO FINALBTS
FINALM1R FDPRT 'RECORD PROCESSED  ',PAD
FINALBTS FDCLC #BLOCKS,ZEROES,4,BE=FINALNOB                      81360
         FDCLC #BLOCKS,INTEGER1,4,BE=FINALN1B                   GP03126
         FDPRT #BLOCKS,I,PAD                                     81360
         FDPRT 'BLOCKS AFTER COMPRESSION',PAD                    81360
         FDGOTO FINALNOB                                        GP03126
FINALN1B FDPRT '1 BLOCK AFTER COMPRESSION',PAD                  GP03126
FINALNOB FDPRT 'DATA SET ATTRIBUTES ARE :',NL,PADR               81360
         FDEXEC DSFORM,DSFORMX                                   81360
         FDPRT *END                                              81360
         TITLE 'W Y L C O M P  ***  DATA AREAS'                  81360
SYSPRINT PRTWORK SYSPRINT,SYSTERM   SYSPRINT MESSAGE FILE        81360
CONSOLE  PRTWORK *CONSOLE    OPERATOR CONSOLE (ONLY IF NO SYSPRINT)
PATUT1   INPWORK SYSUT1,SYSIN,WIDTH=0,FILL=C' ',EODAD=DSEND,EDIT=128
PATUT1LN EQU   *-PATUT1                                          81360
         SPACE 1                                                 81360
*        DIRECTORY BLOCKS (SUBSCRIPTED BY UCBTBYT4)              81360
PDSDENUM DC    AL1(10,10,10,10,10,10,10,10,17,28,28,36,10,28,10,10)
         SPACE 1                                                 81360
PATLOC   CAMLST NAME,1,,3    LOCATE DSN IN CATALOG               81360
         ORG   PATLOC+4                                          81360
PATSCR   CAMLST SCRATCH,1,,3,,OVRD  SCRATCH DSN                  81360
         ORG   PATSCR+4                                          81360
PATCAT   CAMLST CATBX,1,,3   CATALOG WITH INDEX BUILD            81360
         ORG   PATCAT+4                                          81360
PATRECAT CAMLST RECAT,1,,3   RECATALOG EXISTING DATA SET         81360
         ORG   PATRECAT+4                                        81360
PATDCB   DCB   DDNAME=SYSUT2,DSORG=PS,MACRF=(PM),EXLST=2         81360
PATDCBLN EQU   *-PATDCB                                          81360
         SPACE 1                                                 81360
ZEROES   DC    XL8'0'        CONSTANT                            81360
INTEGER1 DC    XL4'1'        CONSTANT                           GP03126
         SPACE 1                                                 81360
PATCOMP  SERVCOMP DSECT=NO,PFX=PAW,LINE#=1,DELTA=1,  INCR=.001         *
               FG1=NEWNIHFG  SET TO PAWF1NIH FOR NEW NIH SUPPORT 82032
PATCOMPL EQU   *-PATCOMP     LENGTH TO MOVE                      81360
         SPACE 1                                                 81360
PATDCBD  DCBEXITD PREFIX=PD  DCB EXIT LIST OPTIONS FOR SYSUT2    81360
         ORG   PDFLAG1                                           81360
         DC    AL1(PD1BLOCK+PD1NOSD+PD1FULLT+PD1TRUNC)           81360
         DC    AL1(PD2TARER)                                     81360
         DC    AL1(PD3BLKTB+PD3BLKWY+PD3DFWYL)                   81360
         ORG   ,                                                 81360
PATDCBDL EQU   *-PATDCBD                                         81360
         SPACE 3
         LTORG
         EJECT
PATVENT  DC    A(0),C'WYL001'  WYLBUR VOLUME TABLE              GP03125
         DC    A(0),C'WYL002'                                   GP03125
         DC    A(0),C'WYL003'                                   GP03125
         DC    0A(0),X'FF'   END OF LIST                         82017
PATVENTL EQU   *-PATVENT                                         81360
         SPACE 2                                                 81360
MAPVENT  DSECT ,             VOLUME CONTROL ENTRY MAPPING        81360
MAPUCB   DS    A             UCB ADDRESS (0 - NO DD / FF - LAST) 81360
MAPVOL   DS    CL6           VOLUME SERIAL                       81360
         DS    0A                                                81360
MAPLEN   EQU   *-MAPUCB      ENTRY LENGTH                        81360
         EJECT
SAVE     DSECT ,             CONTINUATION OF WORK/SAVE AREA      81360
DB       DS    3D            WORK AREA                           81360
         SERVDEFS ,
@DCBEXIT DS    X'05',AL3(0)  DCB OPEN EXIT ROUTINE               81360
         DS    X'07',AL3(INFMJFCB)  OUTPUT JFCB                  81360
         DS    X'80',AL3(UT2OPTS)  DCBEXIT WORK AREA             81360
UT2OPLST DS    X'8F',AL3(SYSUT2)  OPEN LIST                      81360
UT2OPTS  DCBEXITD ,          SET @DCBEXIT OPTIONS                81360
SYSUT1   INPWORK SYSUT1,SYSIN,FILL=C' ',EDIT=128 EODAD=DSEND     81360
#RECS    DC    A(0)          NUMBER OF OUTPUT RECORDS            81360
#BLOCKS  DC    A(0)          NUMBER OF BLOCKS (COMPRESS ONLY)    81360
OUTOVER  DC    A(0)          NUMBER OF EXCESSIVE BLOCKS          82032
OUTMAX   DC    A(133)        MAXIMUM OUTPUT LRECL                82032
OUTMAXUP DC    A(0)          ADJUSTMENT TO OUTMAX                87179
POINTVOL DC    A(0)          POINTER TO VOLUME ENTRY             81360
POINTUCB DC    A(0)          POINTER TO UCB ENTRY FOR VOLUME     81360
CATCOUNT DC    H'1'          NUMBER OF VOLUMES                   81360
CATTYPE  DC    XL4'0'        UNIT TYPE CONVERTED FOR CATALOG     81360
CATVOL   DC    CL6' '        SCRATCH/CATLG VOLSER                81360
CATCODE  DS    F             CATLG/SCRATCH RETURN CODE, SPACER   82018
ABENDOPT DC    A(999)        ABEND 999, NO DUMP                  81360
FLAGS    DC    X'00'         MAJOR PROCESSING FLAGS              81360
FGREP    EQU   X'80'           DATA SET REPLACEMENT PERMITTED    81360
FGTAPOUT EQU   X'40'           OUTPUT IS TO TAPE                 81360
FGCURSE  EQU   X'20'           SET BY STAE EXIT - RECURSION PREVENTION
FGOLD    EQU   X'10'           DATA SET ALREADY EXISTS           81360
FGSCRD   EQU   X'08'           OLD DATA SET WAS SCRATCHED        81360
FGALC    EQU   X'04'           NEW DATA SET WAS ALLOCATED        81360
FGUSER   EQU   X'02'           OUTPUT VOLUME IS NOT INSTALLATION PACK
FGNWYL   EQU   X'01'           DSN NOT A WYL. NAME; USER PACK    87034
CATFG    DC    X'00'         CATALOG PROCESSING FLAG             81360
FGRECAT  EQU   X'80'           USE RECAT RATHER THAN CATBX       81360
FGCAT    EQU   X'40'           CATALOG DONE                      81360
FGNOCAT  EQU   X'20'           CAT/RECAT FAILED                  81360
ACCTPRIV DC    X'00'         PRIVILEGES OF CURRENT ACCOUNT       81360
ABENDCOD DC    0CL4' '       SYSTEM OR USER ABEND CODE           81360
ACCOUNT  DC    CL8' '        CURRENT ACCOUNT NUMBER              81360
ACCTEXT  DC    CL4' '        ACCOUNT IN EXTERNAL (DSN) FORM      82011
OLDVOLS  DC    (PATVENTL/MAPLEN)CL6' '  OLD RESIDENCE VOLUMES    82017
OLDVOLSX EQU   *                                                 81360
LSPACER  DC    CL32' '       LSPACE SVC OUTPUT AREA              81360
PRIORSPC DC    CL9' '        AMOUNT FROM PRIOR LSPACE            81360
         PRINT NOGEN
         CNOP  2,4           ALIGNMENT FOR VOLSER/DSN            81360
VOLJFCB  DC    CL6' '        MAKE VOL/DSN FOR SERVCALL           81360
      IEFJFCBN
         SPACE 1                                                 81360
         AIF   (NOT &LOCO).SKIPL90                              GP03126
A$SVCRB  A$SVCRB DSECT=NO    EXPAND ACCOUNT SVC PARM LIST        93218
.SKIPL90 SPACE 1                                                 81360
         DS    0F            FORCE ALIGNMENT                     81360
         IECSDSL1 1          EXPAND FORMAT 1 DSCB                81360
         SPACE 1                                                 81360
         IECSDSL1 4          EXPAND FORMAT 4 DSCB                81360
         SPACE 1                                                 81360
COMPLIST SERVCOMP DSECT=NO   SET WYLBUR COMPRESS OPTIONS         81360
         SPACE
SYSUT2   DCB   DDNAME=SYSUT2,DSORG=PS,MACRF=(PM),EXLST=@DCBEXIT  81360
         SPACE 1                                                 81360
USERVENT DC    A(0),C'TSO001'  USER VOLUME ENTRY                 93218
INSTVENT DS    0A(0),XL(PATVENTL)  RESERVE SPACE FOR PATVENT     91169
         SPACE 1                                                 81360
CATWORK  DS    35D           CATALOG WORK AREA                   81360
         SPACE 1                                                 81360
SAVEEND  EQU   *             END OF WORK AREA                    81360
         SPACE 2
         PRINT &PRTSYS                                           81360
CVT      DSECT ,                                                 81360
         CVT   DSECT=YES                                         81360
         SPACE 1                                                 81360
         IHACDE ,                                                81360
         SPACE 1                                                 93218
         AIF   (NOT &LOCO).SKIPL95                              GP03126
A$USER   A$USER ,            USER ACCOUNT RECORD                 93218
         SPACE 1                                                 93218
A$INDX   A$INDX ,            INDEX CONTROL RECORD                93218
         SPACE 1                                                 93218
         A$GDA DSECT ,       GLOBAL ACCOUNTING DATA AREA         93218
         SPACE 1                                                 93218
USERCVT  USERCVT ,                                               93218
.SKIPL95 SPACE 1                                                 81360
         IEZDEB ,                                                81360
         SPACE 1                                                 81360
         IKJTCB ,                                                81360
         SPACE 1                                                 81360
MYTIOT   DSECT ,                                                GP03126
         IEFTIOT1 ,                                              81360
         SPACE 1                                                 81360
         DCBD  DEVD=DA,DSORG=PS                                  81360
         SPACE 1                                                 81360
MYUCB    DSECT ,                                                GP03126
         IEFUCBOB ,                                              81360
         AIF   (NOT &SVS).UCBRR                                  81360
UCBRPS   EQU   UCB2OPT3      RPS FEATURE                         81360
UCBRR    EQU   UCB2OPT2      SHARED DASD                         81360
.UCBRR   SPACE 1                                                 81360
         IHAPSA ,                                               GP03126
         END   ,
