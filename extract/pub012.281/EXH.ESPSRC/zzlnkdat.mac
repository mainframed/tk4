ZZLNKDAT TITLE 'Z Z L N K D A T  ***  EXTRACT LOAD MODULE INFORMATION'
***********************************************************************
*                                                                     *
*   THIS MODULE READS THE MEMBER SPECIFIED BY DD INFILE AND RETURNS   *
*   INFORMATION FROM THE CONTROL RECORDS.                             *
*   DD INPDS DEFINES THE PDS, AND IS USED FOR DIRECTORY READS.        *
*                                                                     *
*   THIS PROGRAM IS INVOKED BY TSO COMMAND FILE FINDLIB, FINDLOAD,    *
*   FINDALL, AND ?                                                    *
*                                                                     *
*   1999/12 CHANGES - ADD DESERV SUPPORT, AND BYPASS MEMBER READ      *
*     WHEN POSSIBLE (PDS/E, ETC.)                               GP    *
*                                                                     *
***********************************************************************
         PUNCH ' ORDER ZZLNKDAT(P) '                            GP99348
         SPACE 1
         COPY  OPTIONGB                                         GP99348
         SPACE 1                                                GP99348
         SYSPARM LIST=YES                                       GP99348
         PRINT &PRTSOR                                          GP99350
ZZLNKDAT PGMHEAD ZERO15,BASE=R12,PARM=R9 DEFINE BASE, SAVE AREA, ETC.
         SPACE 1                                                GP99350
         RDJFCB MF=(E,DCBJFCB)  READ THE JFCB                   GP99348
         TM    JFCBIND1,JFCPDS  MEMBER?                         GP99350
         BZ    BADMEM        NOT A PDS MEMBER                   GP99350
         MVC   MSGDSN,JFCBDSNM  MOVE DATASET NAME               GP99350
         MVC   MSGMEM,JFCBELNM    AND MEMBER                    GP99350
         AIF   (NOT &MVSESA).COMESA                             GP99348
         OPEN  MF=(E,PDSLIST)   OPEN FOR DIRECTORY READS        GP99348
         TM    DCBOFLGS-IHADCB+INPDS,DCBOFOPN                   GP99348
         BZ    MEMREAD       MAY FAIL ANYWAY?                   GP99348
         PUSH  USING                                            GP99348
         USING DESN,NAMENTRY                                    GP99348
         LA    R0,L'MSGMEM                                      GP99348
         STH   R0,DESN_LEN                                      GP99348
         MVC   DESN_VAL(L'MSGMEM),MSGMEM                        GP99348
         USING DESL,NAMELIST                                    GP99348
         LA    R0,NAMENTRY                                      GP99348
         ST    R0,DESL_NAME_PTR                                 GP99348
         LA    R2,INPDS      GET DCB ADDRESS                    GP99348
         LA    R3,AREA                                          GP99348
         LA    R4,NAMELIST                                      GP99348
         PUSH  PRINT                                            GP99348
*      DESERV FUNC=GET,AREA=((R3),5*1024),                           *
*            BYPASS_LLA=YES,CONN_INTENT=HOLD,DCB=(R2),               *
*            NAME_LIST=((R4),1)                               GP99348
         PRINT OFF                                              GP99348
         DESERV FUNC=GET,AREA=((R3),5*1024),                           *
               BYPASS_LLA=YES,CONN_INTENT=HOLD,DCB=(R2),               *
               NAME_LIST=((R4),1)                               GP99348
         POP   PRINT                                            GP99348
         STM   R15,R0,DB                                        GP99348
         LTR   R15,R15       DID IT WORK?                       GP99348
         BNZ   MEMREAD       NO; USE OLD CODE                   GP99348
         USING DESB,AREA                                        GP99348
         ICM   R5,15,NAMELIST+8                                 GP99348
         BZ    MEMREAD       IF NOTHING RETURNED, READ MEMBER   GP99348
         USING SMDE,R5       DECLARE IT                         GP99348
         TM    SMDE_FLAG,SMDE_FLAG_ALIAS  HAVE AN ALIAS ?       GP99348
         BZ    *+8           NO                                 GP99348
         MVI   MSGAFG,C'*'   FLAG AS ALIAS                      GP99348
         SPACE 1                                                GP99348
         TM    SMDE_FLAG,SMDE_FLAG_LMOD   HAVE A LOAD MODULE?   GP99348
         BZ    GETUDATA      NO; PROCESS USER DATA              GP99348
         SPACE 1                                                GP99348
         SLR   R6,R6                                            GP99348
         ICM   R6,3,SMDE_PMAR_OFF  GET OFFSET TO PMAR           GP99348
         BZ    MEMREAD       OOPS                               GP99348
         AR    R6,R5         GET PMAR ADDRESS                   GP99348
         USING PMAR,R6       DECLARE IT                         GP99348
         UNPK  DB(9),PMAR_STOR(5)  GET MEMORY REQUIRED          GP99348
         TR    DB,HEXTRTAB   MAKE PRINTABLE                     GP99348
         CLI   PMAR_STOR,0   24-BIT SIZE?                       GP99348
         BE    MEM24         YES                                GP99348
         MVC   MSGSIZE,DB                                       GP99348
         MVI   MSGSIZE+L'MSGSIZE-1,C'K'                         GP99348
         B     MEMCOM                                           GP99348
MEM24    MVC   MSGSIZE,DB+2  SHOW IT                            GP99348
         SPACE 1                                                GP99350
MEMCOM   LA    R7,PMAR_END   POINT TO LS LOADER ATTRIBUTES      GP99348
         USING PMARL,R7                                         GP99348
         CLI   SMDE_LIBTYPE,SMDE_LIBTYPE_PDS ?                  GP99348
         BNZ   MEMPDSE       PROCESS PDSE DATA                  GP99348
*  CHECK HERE FOR SCLM SSI. IF NONE, READ MEMBER.               GP99350
         TM    PMAR_SSI,X'80'  SCLM BIT?                        GP99350
         BZ    MEMREAD       NO; DO THE OLD WAY                 GP99350
         ICM   R14,15,PMAR_SSI  LOAD SSI                        GP99350
         ST    R14,DB2       STORE DATE                         GP99350
         SR    R15,R15                                          GP99350
         NC    DB2(4),=X'000FFFFF'    MASK                      GP99350
         SRL   R14,20        SHIFT DATE OUT                     GP99350
         SRDL  R14,6         MINUTES IN R15                     GP99350
         SRL   R15,32-6      RIGHT-JUSTIFY                      GP99350
         LA    R0,X'1F'      HOUR MASK                          GP99350
         NR    R14,R0                                           GP99350
         CH    R14,=H'24'    HOUR TOO HIGH?                     GP99350
         BH    MEMREAD       IS 24:00 VALID?                    GP99350
         CH    R15,=H'60'    VALID MINUTE?                      GP99350
         BH    MEMREAD                                          GP99350
         MH    R14,=H'100'   MAKE ROOM FOR MINUTES              GP99350
         AR    R14,R15       HHMM                               GP99350
         MH    R14,=H'100'   CHEAT                              GP99350
         CVD   R14,DB        PACK                               GP99350
         MVC   PMARL_TIME,DB+4  MOVE WITH SIGN                  GP99350
         L     R1,DB2        VALIDATE PRESUMED DAY              GP99350
         SLL   R1,4          MAKE ROOM FOR SIGN                 GP99350
         STCM  R1,15,PMARL_DATE  FAKE THE DATE                  GP99350
         OI    PMARL_DATE+L'PMARL_DATE-1,X'0F'  SIGN IT         GP99350
         MVC   PMARL_USER,=CL8'*SCLM*'                          GP99350
         SPACE 1                                                GP99350
MEMPDSE  MVC   MSGUID,PMARL_USER  UID OR JOB NAME               GP99348
         ICM   R2,15,PMARL_DATE   GET DATE                      GP99348
         BZ    MEMREAD       HUH ?                              GP99350
         UNPK  MSGJUL,PMARL_DATE+2(2)  SHOW JULIAN DAY          GP99348
         LR    R1,R2         COPY TO PARAMETER REGISTER         GP99348
         CALL  SUBP2D        GET DAY OF WEEK                    GP99348
         MVC   MSGWEEK,0(R1)  IDENTIFY THE DAY OF THE WEEK      GP99348
         LR    R1,R2         COPY PARM AGAIN                    GP99348
         CALL  SUBP2M        GET MONTH/DAY                      GP99348
         MVC   MSGYR,2(R1)   COPY YEAR                          GP99348
         MVC   MSGMON,4(R1)    AND MONTH                        GP99348
         MVC   MSGDAY,6(R1)      AND DAY                        GP99348
         UNPK  DB(8),PMARL_TIME                                 GP99348
         MVC   MSGTIME(2),DB+2                                  GP99348
         MVC   MSGTIME+3(2),DB+4                                GP99348
         MVI   MSGTIME+2,C'.'                                   GP99348
         B     EODAD         SKIP MEMBER READ                   GP99348
         POP   USING                                            GP99348
         SPACE 1                                                GP99348
         PUSH  USING                                            GP99348
         USING SMDE,R5       DECLARE IT                         GP99348
GETUDATA SR    R6,R6                                            GP99348
         ICM   R6,3,SMDE_USRD_OFF  ANY OFFSET ?                 GP99348
         BZ    EODAD         NO; NOTHING ELSE                   GP99348
         SR    R7,R7                                            GP99348
         ICM   R7,3,SMDE_USRD_LEN                               GP99348
         BZ    EODAD                                            GP99348
         CH    R7,=H'4'      MINIMUM DATE?                      GP99348
         BL    EODAD         NO; IGNORE                         GP99348
         LA    R6,1(R6,R5)   ADDRESS OF DATA (SKIP LENGTH BYTE) GP99348
         USING USERDATA,R6                                      GP99348
         CH    R7,=H'8'      POSSIBLY DATE/TIME?                GP99348
         BH    GETUDATE      NO; TRY SPF                        GP99348
         ICM   R1,15,UDDATE  GET SSI OR DATE                    GP99348
         BAS   R9,DATCHECK   CHECK IT                           GP99348
           B   EODAD         SKIP MEMBER READ IF NOT DATE       GP99348
         B     EODAD         THAT'S ALL FOR THIS ONE            GP99348
         SPACE 1                                                GP99348
GETUDATE CH    R7,=Y(SPFULEN)  CORRECT LENGTH FOR ISPF/PDF ?    GP99348
         BNE   EODAD         NOTHING ELSE NOW                   GP99348
         CLC   SPFCRTDT,SPFMODDT  VALID RANGE ?                 GP99348
         BHR   R9            NO                                 GP99348
         ICM   R1,15,SPFCRTDT  GET PRESUMED CREATION DATE       GP99348
         BAS   R9,DATCHECK   CHECK IT                           GP99348
           B   EODAD         SKIP MEMBER READ IF NOT DATE       GP99348
         MVC   MSGDATE,BLANKS  CLEAR AGAIN                      GP99348
         ICM   R1,15,SPFMODDT  GET PRESUMED CHANGE DATE         GP99348
         BAS   R9,DATCHECK   CHECK IT                           GP99348
           B   EODAD         SKIP MEMBER READ IF NOT DATE       GP99348
         UNPK  DB(5),SPFVERL(3)  PROCESS VERSION AND MOD LEVEL  GP99348
         TR    DB(4),HEXTRTAB                                   GP99348
         MVC   MSGVV(2),DB    SHOW VERSION W/LEADING ZERO       GP99348
         MVI   MSGLL-1,C'.'                                     GP99348
         MVC   MSGLL(2),DB+2  SHOW LEVEL W/LEADING ZERO         GP99348
         UNPK  DB(5),SPFMODTM(3)                                GP99348
         MVC   MSGTIME(2),DB                                    GP99348
         MVI   MSGTIME+2,C':'                                   GP99348
         MVC   MSGTIME+3(2),DB+2                                GP99348
         MVC   MSGUID,SPFUID                                    GP99348
         TM    SPFFLAG,SPFSCLM  SCLM ENTRY?                     GP99350
         BZ    EODAD         NO                                 GP99350
         MVC   MSGDATE(6),=CL6' SCLM '                          GP99350
         B     EODAD         THAT'S ALL FOR THIS ONE            GP99348
         SPACE 1                                                GP99348
FMTINTNS STC   R0,DB                                            GP99348
         NI    DB,X'0F'                                         GP99348
         CLI   DB,X'09'      VALID SIGN ?                       GP99348
         BH    FMTINT        HOPE SO                            GP99348
         SLL   R0,4                                             GP99348
         O     R0,=X'0000000F'                                  GP99348
FMTINT   CVD   R0,DB         MAKE IT PACKED                     GP99348
         MVC   DB2(16),=X'40202020202020202020202020202120'     GP99348
         ED    DB2(16),DB    MAKE PRINTABLE                     GP99348
         MVC   DB(8),=8C'0'                                     GP99348
         OC    DB,DB2+8      ALSO DO WITH LEADING ZEROES        GP99348
         BR    R9                                               GP99348
         SPACE 1                                                GP99348
*  DESIRED DATE FIELD:  00YYDDDF  000YYDDD                      GP99348
*                       01YYDDDF  001YYDDD                      GP99348
*                       19YYDDDF  019YYDDD                      GP99348
*                       20YYDDDF  020YYDDD                      GP99348
*  RETURNS 0(R9) IF INVALID; 4(R9) IF GOOD; DB CONTAINS CCYYDDDF
DATCHECK LA    R15,X'0F'                                        GP99348
         LR    R0,R1                                            GP99348
         NR    R0,R15        RETAIN SIGN ONLY                   GP99348
         CR    R0,R15                                           GP99348
         BE    DATCHNSH                                         GP99348
*        BO    DATCHNSH                                         GP99348
         SR    R0,R0                                            GP99348
         SLDL  R0,4          LEFTIFY IT                         GP99348
         LTR   R0,R0         VALID?                             GP99348
         BNZR  R9            NO; NOT A DATE                     GP99348
         OR    R1,R15        MAKE A SIGN                        GP99348
DATCHNSH ST    R1,DB                                            GP99348
         UNPK  DB2,DB(4)  UNPACK IT FOR DIGIT CHECKS            GP99348
         TRT   DB2,DIGITABL                                     GP99348
         BNZR  R9            NOT NUMERIC; RETURN                GP99348
         CLI   DB,X'21'      CENTURY PLAUSIBLE?                 GP99348
         BHR   R9            NO; RETURN                         GP99348
         CLI   DB,X'01'      LOW VALUE?                         GP99348
         BH    DATCHNPD      NO; PADDING NOT NEEDED             GP99348
         MVI   DB,X'20'      SET FOR FREQUENT ENTRY             GP99348
         BE    DATCHNPD                                         GP99348
         MVI   DB,X'19'      ELSE SET OBSOLETE                  GP99348
DATCHNPD CP    =P'366',DB+2(2)  VALID JULIAN DAY?               GP99348
         BLR   R9                                               GP99348
         UNPK  MSGJUL,DB+2(2)  MAKE JULIAN DAYS PRINTABLE       GP99348
         CALL  SUBP2M        GET MONTH/DAY                      GP99348
         MVC   MSGYR,2(R1)   COPY YEAR                          GP99348
         MVC   MSGMON,4(R1)    AND MONTH                        GP99348
         MVC   MSGDAY,6(R1)      AND DAY                        GP99348
         MVC   MSGDAY-1,C'/' MAKE SEPARATOR                     GP99348
         MVC   MSGYR-1,C'/'  MAKE SEPARATOR                     GP99348
         MVC   MSGJUL-1,C'.' MAKE SEPARATOR                     GP99348
         B     4(,R9)        RETURN ACCEPTED                    GP99348
         POP   USING                                            GP99348
DIGITABL DC    256X'4'       VALID DIGIT = 0; OTHER = 4         GP99348
         ORG   DIGITABL+C'0'                                    GP99348
         DC    10X'0'                                           GP99348
         ORG   ,                                                GP99348
.COMESA  SPACE 1                                                GP99348
MEMREAD  OPEN  MF=(E,DCBLIST)  OPEN THE MEMBER                  GP99348
         TM    DCBOFLGS-IHADCB+INFILE,DCBOFOPN  OPEN?           GP99350
         BNZ   READLOOP      GET MEMBER DATA                    GP99348
BADMEM   TPUT  BADMEMSG,L'BADMEMSG  TELL USER                   GP99350
         ABEND 013                                              GP99350
BADMEMSG DC    C'ZZLNKDAT: INFILE NOT PDS MEMBER OR UNUSABLE'   GP99348
         SPACE 1                                                GP99350
READLOOP GET   INFILE        READ A BLOCK                       GP99350
         LR    R3,R1         PRESERVE THE INPUT ADDRESS         GP99350
         TM    0(R3),X'80'   IDR RECORD?                        GP99350
         BZ    NOTIDR        NO; SEE IF TEXT                    GP99350
         CLC   =C'5695DF108',3(R3)  LINK-EDIT 2  ?              GP99350
         BE    LINKDATE      YES                                GP99350
         CLC   =C'566528408',3(R3)  LINK-EDIT 1  ?              GP99350
         BNE   READLOOP      NO; READ AGAIN                     GP99350
         SPACE 1                                                GP99350
LINKDATE SLR   R2,R2         CLEAR FOR IC                       GP99350
         ICM   R2,7,15(R3)   LOAD (PRESUMED) PACKED DATE        GP99348
         UNPK  MSGJUL,16(2,R3)  MAKE JULIAN DAYS PRINTABLE      GP99350
         LR    R1,R2         COPY TO PARAMETER REGISTER         GP99350
         CALL  SUBP2D        GET DAY OF WEEK                    GP99350
         MVC   MSGWEEK,0(R1)  IDENTIFY THE DAY OF THE WEEK      GP99350
         LR    R1,R2         COPY PARM AGAIN                    GP99350
         CALL  SUBP2M        GET MONTH/DAY                      GP99350
         MVC   MSGYR,2(R1)   COPY YEAR                          GP99350
         MVC   MSGMON,4(R1)    AND MONTH                        GP99350
         MVC   MSGDAY,6(R1)      AND DAY                        GP99350
NOTIDR   TM    0(R3),X'01'   TEXT RECORD NEXT?                  GP96086
         BZ    READLOOP      NO; READ NEXT RECORD               GP96086
         SPACE 1                                                GP99350
         GET   INFILE        READ FIRST TEXT BLOCK              GP99350
         LR    R3,R1         SAVE RECORD ADDRESS                GP99350
*DISLIKE MVC   MSGTIME,=C'-----'                                GP99350
         CLC   =X'47F0F0',0(R3)  IS THIS POSSIBLE LOCAL EXPANSION?
         BNE   EODAD                                            GP96086
         CLI   4(R3),29      NOT TOO LONG ?                     GP99348
         BH    EODAD                                            GP99348
         SR    R0,R0                                            GP99348
         IC    R0,4(,R3)     GET LENGTH                         GP99348
         LR    R5,R3                                            GP99348
         AR    R5,R0         GET LAST BYTE - 4                  GP99348
         BCTR  R5,0          LAST BYTE MINUS 1                  GP99348
         CLI   0(R5),C' '    SPACE ?                            GP99348
         BNE   EODAD         NO                                 GP99348
         CLI   3(R5),C'.'    HH.MM ?                            GP99348
         BNE   EODAD                                            GP99348
         MVC   MSGTIME,1(R5)   IF SO, USE IT                    GP99348
         SPACE 1                                                GP99350
EODAD    LA    R0,ENDMSG-TPUTMSG  GET MESSAGE LENGTH            GP99348
         TPUT  TPUTMSG,(0)   WRITE TO USER                      GP99350
*OS390*  BXLE  R15,R15,ENDJOB                                   GP99350
*DEBUG*  WTO   MF=(E,WTOMSG)  WRITE MESSAGE FOR TESTING         GP99350
         SPACE 2                                                GP99350
ENDJOB   CLOSE MF=(E,DCBLIST)                                   GP99350
         PGMEXIT RC=0                                           GP97310
         SPACE 2                                                GP99348
HEXTAB   DC    C'0123456789ABCDEF'                              GP99348
HEXTRTAB EQU   HEXTAB-C'0'                                      GP99348
BLANKS   DC    CL16' '                                          GP99350
         SPACE 2                                                GP99350
WTOMSG   DC    Y(ENDMSG-*),X'8000'  WTO PREFIX                  GP99350
TPUTMSG  DC    0C' '                                            GP99350
MSGAFG   DC    C' '          ALIAS FLAG (*)                     GP99348
MSGMEM   DC    CL8' ',CL1' '                                    GP99348
MSGMON   DC    CL2'  ',C'/'                                     GP99350
MSGDAY   DC    CL2'  ',C'/'                                     GP99350
MSGYR    DC    CL2'  ',C'.'                                     GP99350
MSGJUL   DC    CL3'   ',C' '                                    GP99350
MSGDATE  EQU   MSGMON,*-MSGMON,C'C'                             GP99350
MSGTIME  DC    CL5' ',CL2' '                                    GP99350
MSGVV    DC    CL5'     '    VERSION VV.LL ??                   GP99348
MSGLL    EQU   MSGVV+3,2,C'C'                                   GP99348
MSGSIZE  DC    CL6' ',C' '   SIZE                               GP99348
MSGUID   DC    CL8' ',C' '   OWNER                              GP99348
MSGDSN   DC    CL44' '                                          GP99350
ENDMSG   DC    X'00000008'   END OF MESSAGE; DESC=0, ROUTCDE=13 GP99350
MSGWEEK  EQU   MSGVV,3,C' '  DAY OF WEEK                        GP99348
         SPACE 1                                                GP99350
DCBLIST  OPEN  (INFILE,(INPUT,REREAD),INPDS,(INPUT,REREAD)),MF=L
PDSLIST  EQU   DCBLIST+4,4,C'A'  OPEN FOR MEMBER TEXT           GP99348
DCBJFCB  OPEN  (INFILE,(INPUT,REREAD)),MF=L                     GP99348
INPDS    DCB   DDNAME=INPDS,DSORG=PO,MACRF=R                    GP99348
INFILE   DCB   DDNAME=INFILE,DSORG=PS,MACRF=GL,EODAD=EODAD,            *
               EXLST=DCBXLIST                                   GP99350
DCBXLIST DC    0A(0),X'87',AL3(INFMJFCB)                        GP99350
MYJFCB   IEFJFCBN ,                                             GP99350
         PRINT &PRTSYS                                          GP99350
SAVE     DSECT ,                                                GP97310
DB       DS    2D                                               GP99348
DB2      DS    2D                                               GP99348
@TRACE   DS    A                                                GP99350
         SPACE 1                                                GP99348
NAMELIST DC    XL(L'DESL_ENTRY)'0'                              GP99348
NAMENTRY DC    XL(L'NAMEHEAD+L'NAMEMEM)'0'                      GP99348
NAMEHEAD DC    Y(8)                                             GP99348
NAMEMEM  DC    CL8' '                                           GP99348
AREA     DC    5XL256'0'     DESERV RETURN DATA                 GP99348
SAVEEND  EQU   *                                                GP97310
         SPACE 1                                                GP99350
         DCBD  DEVD=DA,DSORG=PS                                 GP99350
         SPACE 2                                                GP99348
         AIF   (NOT &MVSESA).PREEMAC                            GP99348
*   DEFINE SPECIAL DIRECTORY MAPPINGS                           GP99348
         SPACE 1                                                GP99348
USERDATA DSECT ,             DEFINE USER DATA                   GP99348
UDDATE   DS    0PL4'0'       SHORT ENTRY DATE/SSI ONLY          GP99348
         SPACE 1                                                GP99348
*        USER DATA FOR SPF ENTRIES                              GP99348
*                                                               GP99348
SPFVERL  DS    X             VERSION NUMBER                     GP99348
SPFMODL  DS    X             MODIFICATION LEVEL                 GP99348
SPFFLAG  DS    X             SCLM FLAG                          GP99350
SPFSCLM  EQU   X'80'           SCLM CONTROLLED MEMBER           GP99350
SPFRSV1  DS    X               RESERVED (ZEROES)                GP99350
SPFCRTDT DS    PL4           CREATION DATE                      GP99348
SPFMODDT DS    PL4           MODIFICATION DATE                  GP99348
SPFMODTM DS    XL2           MODIFICATION TIME (PACKED, NO SIGN)
SPFCURLN DS    XL2           CURRENT LINE COUNT                 GP99348
SPFINILN DS    XL2           INITIAL LINE COUNT                 GP99348
SPFMODLN DS    XL2           NUMBER MODIFIED                    GP99348
SPFUID   DS    CL7           USER ID                            GP99348
         DS    C' '            RESERVED (BLANK)                 GP99348
         DS    CL2' '          RESERVED (BLANK)                 GP99348
SPFULEN  EQU   (*-USERDATA+1)/2*2   LENGTH OF ENTRY             GP99348
         SPACE 1                                                GP99348
*        USER DATA PORTION FOR IEBUPDTX CHAINED ENTRIES         GP99348
*                                                               GP99348
         ORG   USERDATA                                         GP99348
DTXSSI   DS    XL4           SSI OR FF200000                    GP99348
DTXMODNM DS    CL8           PRODUCTION MEMBER NAME             GP99348
DTXID    DS    CL2           LIBRARY ID                         GP99348
DTXMODNO DS    PL2           MODULE NUMBER                      GP99348
DTXVER   DS    PL2           VERSION NUMBER                     GP99348
DTXUDLEN EQU   (*-USERDATA+1)/2   HALF-WORD LENGTH OF SHORT ENTRY
DTXTIMES DS    XL4           (OPTIONAL) TIME STAMP YYDDDHHF     GP99348
DTXUDLTM EQU   (*-USERDATA+1)/2   HALF-WORD LENGTH OF SPECIAL ENTRY
         ORG   ,                                                GP99348
         SPACE 2                                                GP99348
         PRINT &PRTMAC                                          GP99348
         IGWDES ,                                               GP99348
         SPACE 2                                                GP99348
         IGWSMDE ,                                              GP99348
.PREEMAC END   ,                                                GP99348
