J2IN     TITLE 'J E S 2 I N I T  ***  PRE-JES INITIALIZATION'
         COPY  OPTIONGB
         SPACE 1
         SYSPARM LIST=YES
         SPACE 1
         PRINT &PRTSOR
*
*        STANDARD JES2 INVOKES MODULE HASJES20 FROM THE START PROC
*        AT PID, THE PROC SPECIFIES PGM=JES2INIT (THIS MODULE)
*        (REMEMBER TO ZAP JES2INIT INTO IEFSDPPT IN IEFSD060)
*        OR IN X/A, ADD TO SYS1.PARMLIB(SCHED--) MEMBER.         92304
*
*        1) MODULE INVOKES DYNAMASK (REQUIRES SYSIN/SYSPRINT DD)
*              PERMIT CHANGING DEVICE NAME/MASK ENTRIES BEFORE JES
*              EXAMINES THEM THE FIRST TIME.  THIS PERMITS UNIT RECORD
*              DEVICES TO BE REDEFINED, SOMETHING THAT DOES NOT WORK
*              IF DYNAMASK IS INVOKED AS A START-TASK LATER ON.
*
*        2) CALLS JESSTUFF (ALIAS OF IPLSTUFF) TO SCRATCH TEMPORARY
*              DATASETS OFF SYSDA PACKS.
*
*        3) FINALLY TRANSFERS TO HASJES20
*           IF INVOKED WITH PARM=TEST, CHANGES TEXT TO PARM=WARM AND
*              INVOKES HASJESNU INSTEAD.                         89155
*
***********************************************************************
*                                                                     *
*        COPYRIGHT 1986  EXPERT SYSTEM PROGRAMMING, INC.              *
*        COPYRIGHT 2003  EXPERT SYSTEM PROGRAMMING                    *
*                        176 OLD STAGE COACH ROAD                     *
*                        BRADFORD, VT 05033-8844                      *
*                                                                     *
*                    ALL RIGHTS RESERVED                              *
*                                                                     *
***********************************************************************
         SPACE 1
JES2INIT SAVEM ZERO,BASE=R12,PARM=R10                   ADDED ON 86041
         SLR   R7,R7         INDICATE NO USER CVT                91049
         AIF   ('&LOCAL' NE 'PID').NOLOCO                        92304
         L     R1,CVTPTR     GET THE CVT                         91049
         ICM   R7,7,CVTUSER+1-CVTMAP(R1)  ANY EXTENSION ?        91049
         BZ    NOUCVT        NO                                  91049
         USING USERCVT,R7                                        91049
.NOLOCO  SPACE 1
NOUCVT   LA    R10,0(,R10)   CLEAR PARM ADDRESS POINTER          89155
         LTR   R10,R10       ANY ?                               89155
         BZ    DONEPARM      NO                                  89155
         ICM   R9,7,1(R10)   ANY ADDRESS ?                       89155
         BZ    DONEPARM      NO                                  89155
         SLR   R8,R8                                             89155
         ICM   R8,3,0(R9)    ANY TEXT ?                          89155
         CH    R8,=H'4'      LONG ENOUGH ?                       89155
         BL    DONEPARM                                          89155
         CLC   =C'TEST',2(R9)  TEST REQUEST ?                    89155
         BNE   DONEPARM      NO                                  89155
         MVC   2(4,R9),=C'WARM'  REPLACE BY WARM START           89155
         MVC   JES2MOD+6(2),=C'NU'  INVOKE HASJESNU              89155
         SPACE 1                                                 89155
DONEPARM L     R2,PSATOLD-PSA  GET OUR TCB ADDRESS
         L     R1,TCBTIO-TCB(,R2)  GET THE TIOT
         CLC   =C'JES2 ',0(R1)  RUNNING PRIMARY JES ?
         BNE   BYGONES       NO; JUST INVOKE REAL MODULE (?)     89155
         AIF   ('&LOCAL' NE 'PID').ONLY1CE                       92304
         LTR   R7,R7         ANY USER CVT ?                      91049
         BZ    DONELOCO      NO; SKIP                            91049
         TM    UCFLAGS,UCASCRCH  ALREADY DID THIS ?              91049
         BNZ   BYGONES       YES; JUST GO TO JES2                91049
.ONLY1CE SPACE 1                                                 91049
DONELOCO LA    R3,TASKLIST
         LA    R4,(TASKLAST-TASKLIST)/(TASKTWO-TASKLIST)
TASKLOOP CLI   0(R3),C' '    ZAPPED OUT ?
         BNH   TASKSKIP      YES; SKIP
         XC    WAITECB,WAITECB  CLEAR THE ECB AGAIN
         LM    R1,R2,8(R3)   LOAD PARM AND PROCESSING ROUTINE
         BR    R2            INVOKE PROCESSING ROUTINE
ATTAUTH  ATTACH EPLOC=(R3),ECB=WAITECB,RSAPF=YES  INVOKE SEMI-AUTH
         B     ATTCOMM       JOIN COMMON
ATTZERO  ATTACH EPLOC=(R3),KEY=ZERO,ECB=WAITECB  ATTACH
ATTCOMM  ST    R1,ADDRTCB
         WAIT  ECB=WAITECB   WAIT FOR COMPLETION
         DETACH ADDRTCB
TASKSKIP LA    R3,TASKTWO-TASKLIST(,R3)  NEXT TASK
         BCT   R4,TASKLOOP   DO NEXT
         AIF   ('&LOCAL' NE 'PID').CHOCO                         92304
         LTR   R7,R7         ANY USER CVT ?                      91049
         BZ    BYGONES       NO; SKIP                            91049
         MODESET KEY=ZERO    GET PRIVILEGED                      91049
         OI    UCFLAGS,UCASCRCH  SHOW INITIALIZATION DONE        91049
         DROP  R7                                                91049
         MODESET KEY=NZERO   DON'T NEED ?                        91160
.CHOCO   SPACE 1                                                 91049
BYGONES  ENDM  ,             FREEMAIN AND RETURN (INTERCEPTED)
         ORG   *-2           DO NOT RETURN TO CALLER
         DROP  ,
         USING JES2INIT,R15
         XCTL  EPLOC=JES2MOD   GO TO HASP/JES2 INITIALIZATION    89155
         SPACE 1
JES2MOD  DC    CL8'HASJES20' JES2 INITIALIZATION                 89155
ZEROPARM DC    Y(0)          NULL PARM
NULLPARM DC    0A(0),X'80',AL3(ZEROPARM)
WAITECB  DC    F'0'          ATTACH ECB
         SPACE 1
TASKLIST DC    0A(0),CL8'DYNAMASK',A(NULLPARM,ATTAUTH)
TASKTWO  DC    CL8'JESSTUFF',A(NULLPARM,ATTZERO)
         DC    CL8'        ',A(NULLPARM,ATTZERO)  SPARE
         DC    CL8'        ',A(NULLPARM,ATTZERO)  SPARE
TASKLAST EQU   *
         SPACE 1
SAVE     DSECT ,
ADDRTCB  DS    F             SUBTASK TCB ADDRESS
SAVEND   EQU   *             SAVE/WORK
         SPACE 1
         PRINT &PRTSYS
         IHAPSA ,
         IKJTCB ,
         AIF   ('&LOCAL' NE 'PID').NOUCVT                        92304
USERCVT  USERCVT ,                                               91049
         CVT   DSECT=YES                                         91049
.NOUCVT  END   ,
