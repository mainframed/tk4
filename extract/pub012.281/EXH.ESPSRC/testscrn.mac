TSTS     TITLE 'T E S T S C R N  ***  TEST OF @SCREENS ROUTINE'
         MACRO ,
&NM      BOMBTEST ,
&NM      LTR   R15,R15       CHECK RETURN CODE
         BZ    *+8           PROCEED
         BAL   R14,ABEND     BOMB AND SET ADDRESS
         MEND  ,
         SPACE 1
         COPY  OPTIONGB
         SPACE 1
         SYSPARM LIST=YES
         EJECT ,
         PRINT &PRTSOR
TESTSCRN  PGMHEAD ZERO12,BASE=R12,PARM=R9                        87307
         SPACE 1
         SERVINIT ,          LOAD THE SERVICE ROUTINE
         SPACE 1
         SERVCALL LPALD,=CL8'@SCREENS' LOAD THE CRT MANAGEMENT ROUTINE
         ST    R0,@SCREENS   STASH IT
         BOMBTEST ,          CHECK RETURN
         SCROPEN SCRWORK     OPEN THE CRT
         CH    R15,=H'4'     ACCEPTED ?
         BNH   *+8           YES
         BAL   R14,ABEND     ELSE FAIL
*        XC    FDW(FDINWORK-FDW),FDW  CLEAR ALL FIELDS
         LA    R14,UPSCREEN  POINT TO FD/FDIN LIST
         LA    R15,FDINWORK  POINT TO FDIN WORK AREA
         LA    R0,FDINLEN    SET SIZE OF INPUT WORK AREA
         STM   R14,R0,FDWFDA   STASH IN PARAMETER LIST
         MVC   STATLINE,BLANKS
         XC    A$SACCT,A$SACCT  WILL NOT BE PASSED
         XC    A$SPSWD,A$SPSWD   DITTO
RESTART  MVC   CURRUSER,BLANKS  RESTART
         MVI   PROFLAG,0     ELSE CLEAR IT
         MVI   CURRPAGE,1    SET DISPLAY PAGE NUMBER
         TM    PRIVY,VAASYS+VAASUP  SYSTEMS OR TECH SUPPORT ?
         BNZ   PARSCMD       YES
         MVC   STATLINE+10(8),=C'<LOCKED>'
         B     PARSCMD       ANALYZE COMMAND IN BUFFER
ABEND    DC    H'0'          SOCK IT TO ME
DOEXIT   EX    0,*
DOWERR   EX    0,*
DORERR   EX    0,*
SCRWORK  SCRWORK DD3270      OPEN TSO/VTAM BLOCK
         EJECT ,
SHOWANEW MVI   PROFLAG,0     RESET ALL FLAGS
SHOWTEST EX    0,PRECLEAR    CLEAR THE ERROR MESSAGE BUFFER
         TM    PROFLAG,PROFREAD  ANYTHING IN BUFFER ?
         BNZ   CLEARIT       YES; DISPLAY IT
         CLI   CURRUSER,C' '  ANY USERID ?
         BNH   CLEARIT       NO; SOLICIT ONE
SHOW1    MVC   A$SID,=C'A$RB'  EYE-BALL
         MVI   A$SFCTN,A$SFREAD  READ A RECORD
         MVI   A$SACES,X'FF'  ALL ACCESS
         MVI   A$SCNTL,A$SUKEY  USER ENTRY
         XC    A$SKADR,A$SKADR
         LA    R0,MSGTEXT1
         ST    R0,A$SERMSG   ERROR MESSAGE
         MVC   A$SUID,CURRUSER SET KEY
         LA    R1,A$USER     GET RECORD START
         ST    R1,A$SBUFR    SET OUTPUT LOCATION
         LA    R1,A$USIZE    GET RECORD LENGTH
         STH   R1,A$SBUFL    SET BUFFER SIZE
         LA    R1,A$SVCRB    READ THE RECORD
         SVC   249           READ THE RECORD
         BXH   R15,R15,IOERROR  ERROR
         CLC   CURRUSER,A$UUID  CORRECT RECORD ?
         BNE   IOERROR       NO
         OI    PROFLAG,PROFREAD  SHOW TEXT PRESENT
         BAL   R14,RECPACK   FORCE SIGNS IN ALL PACKED FIELDS
CLEARIT  NI    FDWOPT,255-FDWOKEEP  PERMIT INPUT BUFFER ERASURE
         B     RESHOW
         SPACE 1
RESHOW2  DS    0H
RESHOW   SCRINIT FDW         INITIALIZE SCREEN PROCESSING
         BOMBTEST ,
         TM    PRIVY,VAASYS+VAASUP  SYSTEMS OR TECH SUPPORT ?
         BZ    RESHOWNP      NO
         OI    FDWOPT,FDWLOCK  UNLOCK CONDITIONAL INPUT FIELDS
RESHOWNP TM    PROFLAG,PROFREAD  ANYTHING IN BUFFER ?
         BZ    RESHOW3       NO; DON'T TEST IT
         CP    A$UCRTDT,=P'0' DID USER CLEAR THE CHANGE DATE ?
         BNE   RESHOW3       NO
         CLC   =CL8'DEFAULT ',A$UUID  SPECIAL NAME ?
         BE    RESHOW3       YES; LEAVE DATE ALONE
         TIME  TU
         STCM  R1,7,A$UCRTDT  SET TODAY AS THE CHANGE DATE
RESHOW3  SCRLIST FDW         FORMAT THE INFORMATION
         SCRPAGE FDW         DISPLAY THE PAGE
         LTR   R15,R15       TEST RETURN
         BNM   REPLANAL      CHECK INPUT
         CLI   FDWIAID,1     WHAT CONDITION ?
         BL    DOEXIT        NO RESPONSE FROM USER
         BE    DOWERR        WRITE ERROR
         B     DORERR        READ ERROR
REPLANAL LA    R5,FDWICOD    POINT TO CONVERTED CURSOR
         BLOOK T=QUICKIES,R=FDWICOD    LOOK FOR NON-DATA FUNCTIONS
QUICKEND TM    FDWFG,FDWFERR  PREVIOUS TEXT IN ERROR ?
         BNZ   PRECLEAR      YES; REPROCESS THROUGH SCANNER
         ICM   R0,15,FDWSCAL ANY INPUT TEXT ?
         BZ    PRETEXT       NO; LEAVE ERROR MESSAGE INTACT
PRECLEAR XC    MSGTEXT1,MSGTEXT1  CLEAR THE ERROR/RESPONSE MESSAGE
PRETEXT  OI    FDWOPT,FDWOKEEP  KEEP INPUT TEXT
         SCRANAL FDW         ANALYZE THE INPUT
         TM    FDWFG,FDWFERR   ANY TEXT IN ERROR ?
         BNZ   SYNSHOW       YES
         TM    FDWFG,FDWFTXT  ANY TEXT TO PROCESS ?
         BZ    TESTPAGE      NO; JUST REDISPLAY
         SCRMOVE FDW         MOVE TEXT TO MEMORY
         TM    FDWFG,FDWFERR   ANY ERRORS ?
         BNZ   SYNSHOW       YES; REQUEST CORRECTION
PARSCMD  LA    R5,CURRUSER   POINT TO INPUT
RLOOK    BLOOK T=COMTABLE,R=CURRUSER   GO TO COMMAND
         CLI   0(R5),C' '    ANY INPUT ?
         BNE   SHOW1         YES; PROCESS NEW USER ID
TESTPAGE CLI   CURRPAGE,1    DOES USER WISH TO CHANGE PAGE ?
         BH    SWAPPAGE      YES
         MVI   CURRPAGE,1    RESET IN CASE USER ZEROED IT
TESTPAG9 TM    PROFLAG,PROFREAD  ANY TEXT ?
         BNZ   CLEARIT       YES; JUST REDISPLAY
         CLI   CURRUSER,C' '  ANY MEMBER NAME ?
         BNH   CLEARIT       NO
         B     SHOW1         TRY TO FIND IT
CMDINVLD MVC   MSGTEXT1+4(15),=C'Unknown command'
         B     RESHOW
         SPACE 1
PAGE1    MVI   CURRPAGE,1    SET FOR PAGE 1
         B     QUICKEND      CONTINUE SCANNING
PAGE2    MVI   CURRPAGE,2    SET FOR PAGE 2
         B     QUICKEND      CONTINUE
GOHOME   MVC   FDWCUR,FDWCUD  SET CURSOR FROM DEFAULT
         B     QUICKEND      AND CONTINUE PARSING
         SPACE 1
SWAPPAGE CLI   CURRPAGE,2    VALID NEXT PAGE ?
*NOTYET* BE    SWAPPAG2      YES
         CLI   CURRPAGE,9    SPACIAL REQUEST ?
         BE    PAGENINE      YES
BADPAGE# MVC   MSGTEXT1+4(12),=C'INVALID PAGE'
         MVI   CURRPAGE,1    RESET IT TO CORRECT VALUE           87278
         B     RESHOW
SWAPPAG2 EX    0,*           SHOW FULL-SCREEN XCTL
         SPACE 1
PAGENINE TM    PRIVY,VAASYS+VAASUP SYSTEMS OR TECH SUPPORT ?
         BZ    BADPAGE#      NO
         B     TESTPAG9      REDISPLAY WITH PASSWORDS
         SPACE 1
SYNSHOW  MVC   MSGTEXT1+4(13),=C'Invalid input'
         MVC   FDWCUR,FDWICUD  SET CURSOR AT ERROR POSITION
         B     RESHOW2       AND REDISPLAY
         SPACE 1
IOERROR  NI    PROFLAG,255-PROFREAD  NO TEXT
         ICM   R0,3,MSGTEXT1  ANY TEXT ?
         BNZ   CLEARIT       DISPLAY ERROR
         MVC   MSGTEXT1+4(13),=C'Program error'
         B     CLEARIT
         SPACE 1
RECCLEAR XC    A$UCRTDT,A$UCRTDT  CLEAR NEW RECORD
         XC    A$UEXPDT,A$UEXPDT
         SLR   R0,R0
         STCM  R0,3,A$U#FAIL NUMBER OF FAILED LOGONS
         XC    A$UPPSWD,A$UPPSWD
         XC    A$UCHGDT,A$UCHGDT
         XC    A$UTSODT,A$UTSODT
         STCM  R0,3,A$UTSUSE
         XC    A$UWYLDT,A$UWYLDT
         STCM  R0,3,A$UWYUSE
         XC    A$UCICDT,A$UCICDT
         STCM  R0,3,A$UCIUSE
         XC    A$UEXHDT,A$UEXHDT
         STCM  R0,3,A$UEXUSE
         MVC   A$UID,=C'USER'  IDENTIFIER
RECPACK  OI    A$UCRTDT+L'A$UCRTDT-1,X'0F'  POSITIVELY POSITIVE
         OI    A$UEXPDT+L'A$UEXPDT-1,X'0F'
         OI    A$UCHGDT+L'A$UEXPDT-1,X'0F'
         OI    A$UTSODT+L'A$UTSODT-1,X'0F'
         OI    A$UCICDT+L'A$UCICDT-1,X'0F'
         OI    A$UWYLDT+L'A$UWYLDT-1,X'0F'
         OI    A$UEXHDT+L'A$UEXHDT-1,X'0F'
         BR    R14
BLANKS   DC    CL80' '
         EJECT
         PRINT NOGEN
QUICKIES BTAB  *PA1,DOEXIT,BASE=*
         BTAB  *CANCEL,RESHOW2  REDISPLAY SCREEN
         BTAB  *CLEAR,SHOWTEST  REREAD
PFKTAB   BTAB  *PF1,PAGE1
         BTAB  *PF2,PAGE2    CHANGE DISPLAY PAGE
         BTAB  *PF12,GOHOME  CURSOR TO COMMAND FIELD
         BTAB  *END
         SPACE 1
COMTABLE BTAB  'LOGOFF ',DOEXIT,BASE=*
         BTAB  'LOGON ',RESTART
         BTAB  *END
         SPACE 2
         PRINT NOGEN
UPSCREEN FDOPT SBA=(1,1)     START A NEW SCREEN
         FD    '=============== TEST SCREEN ==============='
         FD    STATLINE,PAD,LEN=44     STATUS MESSAGES
         FD    'User',PAD
         FD    A$UUID,DEB,INT,LEN=8  USER ID
         FD    MSGTEXT1+4,39,RADJ,DEB,INTENSE,NL,LEN=39  ERROR MSG
         FD    'User Id',PAD
         FDIN  CURRUSER,UPPER,NULL,DEBR,LEN=8  UP-CASE (MAIN CMD)
         FDTM  PROFLAG,PROFREAD,BZ=ENDFD  SKIP IF NO DATA
         SPACE 1
         FD    'Acct',PADR
         FDINP A$UACCT,PAD,UP,LEN=8 PRIMARY ACCOUNT/SUBACCOUNT
         FD    'CrtDt',PAD
         FDINP A$UCRTDT,DATJ,PAD,LEN=6 DATE CREATED OR LAST CHANGED
         FD    'ExpDt',PAD
         FDINP A$UEXPDT,DATJ,PAD,LEN=6 OR OR LAST DAY ENTRY IS VALID
         FD    '# failed:',PAD
         FDINP A$U#FAIL,I,PAD,RADJ,LEN=5  # OF FAILED LOGON ATTEMPTS
         FDOPT SBA=(*,-8)
         FD    'PAGE'
         FDIN  CURRPAGE,I,PINK,LEN=1
         FDCLI CURRPAGE,9,BNE=NOPSWD1
         FD    'Pswd',PADR,NL
         FDINP A$UPSWD,PAD,UP,LEN=8 CURRENT PASSWORD
         FD    'PrevPswd',PAD
         FDINP A$UPPSWD,PAD,UP,LEN=8 PREVIOUS PASSWORD
         FD    'PsChgDt',PAD
         FDINP A$UCHGDT,DATJ,PAD,LEN=6 DATE OF LAST PASSWORD CHANGE
*A$ULINK  DC    CL8' '        NAME OF EXTENSION (RESERVED)
NOPSWD1  FD    'SubSys',PADR,NL
         FDINP A$USUBFG,HEX,PAD,LEN=2  ALLOWED SUB-SYSTEMS
         FD    'Privy',PAD
         FDINP A$UPRIVY,HEX,PAD,LEN=2 PRIVILEGE FLAGS
         FD    'Admin',PAD
         FDINP A$UADMIN,HEX,PAD,LEN=2 ADMINISTRATIVE PRIVILEGES
         FD    'SaveVol',PADR,NL
         FDINP A$USAVOL,PAD,UP,LEN=6 VOLSER (OR MASK) OF SAVE VOL
         FD    'AccessVols',PAD
         FDINP A$UACVOL,PAD,UP,LEN=6 VOLUMES (MASKS) ALLOWED ON
         FDINP A$UACVOL+6,L'A$UACVOL,PAD,UP,LEN=6 VOLUMES ALLOWED ON
         FDINP A$UACVOL+12,L'A$UACVOL,PAD,UP,LEN=6 VOLUMES ALLOWED ON
         FDINP A$UACVOL+18,L'A$UACVOL,PAD,UP,LEN=6 VOLUMES ALLOWED ON
         FDINP A$UACVOL+24,L'A$UACVOL,PAD,UP,LEN=6 VOLUMES ALLOWED ON
         FD    'AcctFg/#',PADR,NL
         FDINP A$UACCFG,HEX,PAD,LEN=2 FLAG/ACCOUNT ACCESS TABLE
         FDINP A$UACCNO,PAD,UP,LEN=8 ACCOUNT (OPT. SUBACCOUNT)
         FDINP A$UACCFG+9,L'A$UACCFG,HEX,PAD,LEN=2 FLAG ACCESS TABLE
         FDINP A$UACCNO+9,L'A$UACCNO,PAD,UP,LEN=8 ACCT (+SUBACCOUNT)
         FDINP A$UACCFG+18,L'A$UACCFG,HEX,PAD,LEN=2 FLAG ACCESS TABLE
         FDINP A$UACCNO+18,L'A$UACCNO,PAD,UP,LEN=8  ACCT/SUBACCNT
         FDINP A$UACCFG+27,L'A$UACCFG,HEX,PAD,LEN=2  FLAG  ACCESS TABLE
         FDINP A$UACCNO+27,L'A$UACCNO,PAD,UP,LEN=8  ACCT/SUB
         FDINP A$UACCFG+36,L'A$UACCFG,HEX,PAD,LEN=2   FLAG ACCESS TABLE
         FDINP A$UACCNO+36,L'A$UACCNO,UP,PAD,LEN=8    ACCT/SUBACCT
         FD    'PrtNode/Dest',PADR,NL
         FDINP A$UPROUT,PAD,UP,LEN=8 NODE OF DEFAULT PRINTING
         FDINP A$UPROUT+8,8,PAD,UP,LEN=8 DEST. OF DEFAULT PRINTING
         FD    'PunNode/Dest',PAD
         FDINP A$UPUOUT,PAD,UP,LEN=8  NODE OF DEFAULT PUNCHING
         FDINP A$UPUOUT+8,8,PAD,UP,LEN=8 DEST. OF DEFAULT PUNCHING
         FD    'JobCls',PADR,NL
         FDINP A$UJOBCL,PAD,UP,LEN=1 DEFAULT JOB CLASS
         FD    'MsgCls',PAD
         FDINP A$UMSGCL,PAD,UP,LEN=1 DEFAULT MESSAGE CLASS
         FD    'OutCls',PAD
         FDINP A$UOUTCL,PAD,UP,LEN=1 DEFAULT SYSOUT CLASS
         FD    'SSPriv',PAD
         FDINP A$USSFG,2,HEX,LEN=4 SUBSYSTEM PRIVILEGE FLAGS
         FD    'CPU Msk',PAD
         FDINP A$UCPUMK,HEX,PAD,LEN=4
         FDTM  A$USUBFG,A$USFTSO,BZ=ENDFD    SKIP TSO DISPLAY
         FD    ' ',NL
TSOLINE  FD    'TSO AccDt',PADR,NL
         FDINP A$UTSODT,DATJ,PAD,LEN=6 DATE OF LAST LOGON
         FD    '# logons',PAD
         FDINP A$UTSUSE,I,PAD,RADJ,LEN=5     NUMBER OF LOGONS
         FD    'Privy',PAD
         FDINP A$UTSPRV,3,HEX,LEN=6  PRIVILEGE FLAGS
         FD    'Prof',PAD
         FDINP A$UTSPRO,HEX,LEN=2 PROFILE FLAGS
         FD    'Proc',PAD
         FDINP A$UTSPRC,PAD,UP,LEN=8 PROCEDURE NAME
         FDOPT SBA=(*,-5)
         FD    'Cmd',PAD
         FDINP A$UTSCMD,PAD,UP,LEN=78 INITIAL COMMAND
ENDFD    FD    *END
         SPACE 2
SAVE     DSECT ,             DEFINE WORK AREA
@SERVICE DS    A
@SCREENS DS    A
DB       DS    D
CURRUSER DS    CL8           CURRENT USER ID
A$USER   A$USER DSECT=NO
         SPACE 2
         PRINT GEN
A$SVCRB  A$SVCRB DSECT=NO    EXPAND ACCT SVC CALLING SEQUENCE
         SPACE 2
FDW      MAPFDW DSECT=NO     EXPAND SC-ROUTINE CALLING SEQUENCE
         SPACE 1
PROFLAG  DC    X'00'         PROCESSING FLAGS
PROFREAD EQU   X'80'           TEXT HAS BEEN READ
PRONEW   EQU   X'01'           NEW MEMBER TO BE ADDED
CURRPAGE DC    X'00'         DISPLAY PAGE NUMBER
PRIVY    DC    X'00'         PRIVILEGE FLAGS
STATLINE DC    CL48' '       STATUS INDICATORS
MSGTEXT1 DS    CL125         ERROR MESSAGE
         SPACE 1
FDINWORK FDIN  *EXPAND       DEFINE INPUT WORK AREA
FDINLEN  EQU   *-FDINWORK      LENGTH TO CLEAR
SAVEEND  EQU   *             END OF GETMAINED AREA
         SPACE 2
         MAPFIW ,            MAP SINGLE ENTRY IN ABOVE AREA
         SPACE 2
         PRINT NOGEN
CVTDSECT DSECT ,
         CVT   DSECT=YES
         IHACDE ,
         SPACE 2
         END   ,
