DTD2J    TITLE 'S U B D 2 J   **   JULIAN DATE FROM DSCB DATE'
*   THIS SUBROUTINE CONVERTS A DATE FIELD FROM A DSCB TO A FULL
*   YEAR AND JULIAN DAY FORMAT.
*     R1 - POINTS TO DSCB DATE FIELD
*
*   OUTPUT R15 = 0F  IF INVALID DATE PASSED
*          R15 = YYYYDDDF IN HEX
*        R0-R1 = YYYY.DDD IN EBCDIC
*
SUBD2J   START 0
SUBD2J   AMODE ANY
SUBD2J   RMODE ANY
         STM   R14,R12,12(R13)
         LR    R6,R15        AND NEW BASE
         USING SUBD2J,R6
         LA    R4,7+20+8*4(,R13)  POINT INTO USER'S SAVE AREA
         N     R4,=X'7FFFFFF8'  MAKE DOUBLE-WORD BOUNDARY
         USING USERSAVE,R4   MAKE THIS CODE RE-ENTRANT/REFRESHABLE
         LA    R5,DATEBAD    PRESET FOR FAST BRANCHES
         LTR   R1,R1         DID USER SUPPLY A DATE?
         BZR   R5            NO
         SPACE 2
*   TEST FOR VALID DSCB FORMAT DATE - RETURN 0 IF BAD
*
         ICM   R0,7,0(R1)    ALL ZERO ?
         BZR   R5            RETURN ZEROES
         CLC   0(3,R1),BLANKS  REALLY OLD FORMAT ?
         BER   R5            YES; RETURN
         SR    R2,R2
         IC    R2,0(,R1)     LOAD YEAR
         LA    R2,1900(,R2)  ADD CENTURY
         CH    R2,=H'1964'   PLAUSIBLE YEAR ?                   GP05158
         BNL   NOFIX         YES                                GP05158
         LA    R2,100(,R2)   FINAGLE                            GP05158
NOFIX    SR    R3,R3
         ICM   R3,3,1(R1)    LOAD JULIAN DAY
         BNPR  R5            NOT VALID
         CH    R3,=H'366'    IN RANGE ?
         BHR   R5            NO; RETURN 0
         BL    DATEFORM      YES; FORMAT
         EX    R2,EXISLEAP   LEAP YEAR ?
         BNZR  R5            NO; 366 IS INVALID IN NON-LEAP
DATEFORM MH    R2,=H'1000'   MULTIPLY YEAR BY 1000
         AR    R2,R3         ADD DAYS TO YEAR
         CVD   R2,DB         MAKE PACKED
         OI    DB+7,X'0F'    MAKE PREFERRED PLUS SIGN
         ICM   R15,15,DB+4   RELOAD SIGNED, PACKED VALUE
         MVC   PACKWORK,PACKPAT
         ED    PACKWORK,DB+4
         ICM   R0,15,PACKWORK+1  LOAD YEAR
         ICM   R1,15,PACKWORK+5  LOAD .DDD
         SPACE 1
PGMEXIT  LM    R2,R6,28(R13)  RESTORE REST
         BR    R14           RETURN TO CALLER
         SPACE 1
*   DATE FIELD IS BAD OR 0 - RETURN ZERO
DATEBAD  LA    R15,X'0F'     ZERO SIGNED PACKED RETURN
         ICM   R0,15,BLANKS
         ICM   R1,15,EBZERO-3  MAKE CL8'   0'
         B     PGMEXIT       COMMON EXIT
         SPACE 1
EXISLEAP TM    =X'03',*-*    LEAP YEAR ?
BLANKS   DC    CL4' '  1/2
EBZERO   DC    C'0'    2/2   W/BLANKS+1 - ZERO PRINTABLE
PACKPAT  DC    X'40212020204B202020'
         LTORG ,
         SPACE 1
         PRINT NOGEN
         SPACE 1                                                GP04075
USERSAVE DSECT ,             MY WORK AREA MAPPED ON USER'S SAVE AREA
PACKWORK DS    C' YYYY.DDD'
         ORG   PACKWORK+8    OVERLAP A LITTLE
DB       DS    D                                                GP04075
         SPACE 1
         YREGS ,
         END   ,
