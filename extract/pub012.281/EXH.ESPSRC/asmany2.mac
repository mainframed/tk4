ANY2     TITLE 'A S M A N Y 2  ---  MAKE BIG LETTERS FOR STEPSEP'
*
*        USES MODIFIED VERSION OF IEFSD095 TO PRINT BIG LETTERS
*
*        INPUT IN G.P.R. 1 IS POINTER TO:   CL8  NAME TO BE PRINTED
*                            XL1  =0 FIRST TIME, EXTRA EJECT
*                                 =1  SKIP EJECT
*                                 =2 FORCE EJECT                GP97262
*                            =X'10'  PRINT DATE LINE SECOND     GP97262
*                            =X'20'  PRINT DATE LINE THIRD      GP97262
*                            =X'40'  XEROX DUPLEX PROCESSING     90095
*                            =X'80'  PRINT SECOND ITEM          GP97262
*                            CL8  TEXT OF SECOND LINE (ELSE DATE)
*                            CL8  DDNAME FOR PRINTING
*                            AL2  BLKSIZE - IF ZERO WILL BE RETURNED
*                            AL2  LRECL FOR DCB
*                            X   RECFM FOR DCB
*                            X   CHARACTER SPECIFICATION (0=DEFAULT)
*        WHEN THE 80 FLAG IS OFF, PRINTS FIRST BLOCK AND DATE   GP97262
*        WHEN 80 IS ON, BUT 2ND NAME IS BLANK, PRINTS DATE      GP97262
*        WHEN 80 IS ON, AND SECOND BLOCK NON-BLANK, PRINTS      GP97262
*          A LINE WITH DATE AS SECOND (10) OR THIRD BLOCK (20)  GP97262
*
*   NOTE THAT ASMANY NO LONGER CALLS THIS PROGRAM, BUT USES @PRINTER
*   FUNCTION PRTBIG INSTEAD.
*
*   THIS ROUTINE IS LINKED BY STEPSEP, DIPLOMAT, AND MICROSUB.
*
         PRINT NOGEN                                            GP97262
ASMANY2  START 0
         USING ASMANY2,R12                                      GP97262
         STM   R14,R12,12(R13)                                  GP97262
         LR    R12,R15                                          GP97262
         LR    R9,R13                                           GP97262
         LR    R11,R1                                           GP97262
         LA    R13,SAVE                                         GP97262
         ST    R13,8(,R9)                                       GP97262
         ST    R9,4(,R13)                                       GP97262
         LA    R10,PRINCE                                       GP97262
         USING PARMSECT,R11                                     GP97262
         USING IHADCB,R10                                       GP97262
         WXTRN IEFSD095      CONDITIONALLY LINKED
         ICM   R0,15,VIEFSD95   IS BIG LETTER ROUTINE LINKED IN
         BNZ   HAVE95        OH FRABJOUS DAY
         LOAD  EPLOC=NIEFSD95  ELSE LOAD IT
         ST    R0,VIEFSD95   AND SAVE FOR LATER
HAVE95   MVC   DCBDDNAM,DDNAME    COPY DD NAME
         MVC   DCBRECFM,DDRECF    COPY RECFM
         MVC   DCBBLKSI,DDBLKSI   COPY DCB BBLOCKSIZE OR ZERO
         MVC   DCBLRECL,DDLRECL   COPY RECLEN
         MVI   RPAD+1,0      SET LENGTH OVERHAED TO ZERO
         MVC   PAD,=A(CC)    USE CC ADDRESS FOR PUT
         MVC   EJECT(ACC-MCC),ACC  MOVE ASA CONTROL CHARACTERS   90095
         TM    DCBRECFM,X'C0'  TEST RECFM
         BNM   NOTV          U, D, OR ZERO
         TM    DCBRECFM,X'40'  IS RECFM = V ?
         BZ    NOTV          NO
         MVC   PAD,=A(VREC)  USE VREC ADDR. FOR PUT
         MVI   RPAD+1,4      SET FOR LENGTH LONGER BY 4
NOTV     TM    DCBRECFM,X'04'  USE ASA ?
         BO    NOTM          YES
         MVC   EJECT(ACC-MCC),MCC  NO, USE MACHINE CODE          90095
NOTM     DS    0H            BYPASS
         OPEN  MF=(E,DCBLIST)  OPEN PRINT DCB                   GP97262
         TM    DCBOFLGS,DCBOFOPN   OPEN ?                       GP97262
         BZ    CLOSE         NO, SKIP THE GOODIES
         OC    DDBLKSI,DDBLKSI   WAS INPUT SUPPLIED ?
         BNZ   *+10
         MVC   DDBLKSI,DCBBLKSI  NO, WRITE THE BLOCKSIZE BACK
         SLR   R1,R1         REQUEST CURRENT DATE
         L     R15,=V(SUBP2M)  GET DATE ROUTINE
         BASR  R14,R15                                          GP97262
         MVC   DATE(2),2(R1)  MOVE YEAR                         GP97262
         MVC   DATE+3(2),4(R1) MOVE MONTH                       GP97262
         MVC   DATE+6(2),6(R1) MOVE DAY                         GP97262
         MVC   FIRST,BLANKS  CLEAR OUTPUT                       GP97262
         LA    R1,2          SET FOR TWO PRINT BLOCKS           GP97262
         LA    R2,6          AND SIX SEPARATION LINES           GP97262
         MVC   STACK(8),BLOCK1  MOVE MEMBER NAME                GP97262
         MVC   STACK+8(16),BLANKS  CLEAR                        GP97262
         TM    FLAG,FG2LINE  PROCESS SECOND DATA LINE?          GP97262
         BZ    DODATE        NO; USE DATE                       GP97262
         MVC   STACK+8(8),BLOCK2 MOVE SECOND LINE               GP97262
         CLC   STACK+8(8),BLANKS  DID USER SUPPLY ANY?          GP97262
         BNE   TREELINE      YES; CHECK FOR THIRD LINE OPTION   GP97262
DODATE   MVC   STACK+8(8),DATE  MOVE DATE INTO STACK            GP97262
         B     DONEMEM                                          GP97262
TREELINE TM    FLAG,FG3DATE+FG2DATE  APPEND OR INSERT DATE?     GP97262
         BZ    DONEMEM       NO; TWO LINES ONLY                 GP97262
         LA    R1,3          CHANGE TO THREE LINE BLOCKS        GP97262
         LA    R2,5          AND FIVE SEPARATION LINES          GP97262
         MVC   STACK+16(8),DATE  MOVE DATE TO THIRD POSITION    GP97262
         TM    FLAG,FG3DATE  IN THIRD LINE?                     GP97262
         BNZ   DONEMEM       OK; DONE                           GP97262
         MVC   STACK+8(8),DATE                                  GP97262
         MVC   STACK+16(8),BLOCK2 ELSE SWAP                     GP97262
DONEMEM  STM   R1,R2,NUMSTACK   SET STACK COUNTER AND SPACING   GP97262
         SPACE 1                                                 90095
         TM    MYFLAG,MEFIRST  FIRST TIME ?                      90096
         MVI   MYFLAG,MEFIRST  SET FIRST TIME DONE               90096
         BZ    NOTLASER      FIRST CALL, JES DOES SHEET EJECT    90096
         TM    FLAG,FGXEROX  LASER MODE (XEROX 4050/9700...)     90095
         BZ    NOTLASER      NO                                  90095
         MVC   CC,SKIP12     SET SKIP TO CHANNEL 12              90095
         MVC   FIRST(10),=10X'21'  MAKE LOCAL SHEET EJECT        90095
         BAS   R14,PUT       WRITE IT                            90095
         OI    FLAG,FGNOEJE  FORCE EJECT OFF                     90095
         MVC   FIRST,BLANKS  WIPE OUT SHEET FEED REQUEST         90095
         EJECT
NOTLASER LA    R2,2          OUTPUT TWO PAGES                    90095
         TM    FLAG,FGNOEJE  DONE BEFORE ?                      GP97262
         BZ    DOJECT        NO, DO EXTRA EJECT                  90095
         TM    FLAG,FGDOEJE  FORCED EJECT ON ?                   90095
         BZ    NOSKIP        NO, SKIP EJECT
DOJECT   OI    FLAG,FGNOEJE  RESET FLAG FOR FIRST TIME ENTRY    GP97262
         SPACE 2
LOOP     MVC   CC,EJECT      SET UP PAGE EJECT
         BAS   R14,PUT       ... AND ZOOM ....                  GP97262
         SPACE
NOSKIP   TM    FLAG,FGXEROX  XEROX MODE ?                        90095
         BZ    NOSKIPNX      NO                                  90095
         MVC   FIRST(10),=10X'11'  MAKE AUXILIARY TRAY FEED      90095
NOSKIPNX MVC   CC,DOUBLE     DOUBLE SPACE                       GP97262
         LA    R3,5          SKIP 10 LINES                      GP97262
         CLI   NUMSTACK+L'NUMSTACK-1,2  MORE THAN 2 BLOCKS?     GP97262
         BNH   NOSKIPLP                                         GP97262
         LA    R3,1          DO ONLY ONCE FOR TRAY FEED         GP97262
NOSKIPLP BAS   R14,PUT                                           90095
         MVC   FIRST,BLANKS  WIPE OUT TRAY FEED REQUEST          90095
         BCT   R3,NOSKIPLP                                       90095
         LA    R6,STACK      POINT TO STACK                     GP97262
         L     R7,NUMSTACK   GET NUMBER TO DO                   GP97262
NEXTBLK  MVC   MEM,0(R6)     MOVE NAME TO BE PRINTED TO WORK FIELD
         BAS   R9,DOBLOCK    PRINT IT (16 LINES)                GP97262
         CH    R7,=H'1'      LAST TIME THROUGH?                 GP97262
         BNH   NEXTOUT       YES; NO TRAILING SPACES            GP97262
         L     R3,NUMSKIP    SKIP 6 LINES                       GP97262
         BAS   R14,PUT                                          GP97262
         BCT   R3,*-4                                           GP97262
         LA    R6,L'MEM(,R6)  NEXT ENTRY                        GP97262
         BCT   R7,NEXTBLK                                       GP97262
NEXTOUT  BCT   R2,LOOP       DO TWO PAGES                       GP97262
         SPACE 2
CLOSE    CLOSE MF=(E,DCBLIST)  CLOSE PRINT FILE                 GP97262
      FREEPOOL (R10)         AND PACK BATHING SUIT AWAY         GP97262
EXIT     L     R13,4(,R13)                                      GP97262
         LM    R14,R12,12(R13)                                  GP97262
         XR    R15,R15                                          GP97262
         BR    R14                                              GP97262
         SPACE 5
DOBLOCK  MVC   CC,SINGLE     SINGLE SPACE                       GP97262
         MVC   CONTR,CONTP   INITIALIZE CONTROL FLAGS           GP97262
         MVC   CONTR(1),DDCHAR  SET CHARACTER SET               GP00005
         LA    R3,16         16 LINES FOR BIG LETTERS           GP97262
         XR    R4,R4                                            GP97262
         BCTR  R4,0          START WITH COUNT OF -1             GP97262
DOBLOCKL ST    R4,COUNT      SAVE FOR BIGABC                    GP97262
         LA    R1,LIST                                          GP97262
         L     R15,VIEFSD95  GET A LINE OF BIG LETTERS          GP97262
         BASR  R14,R15       GET DATA LINE                      GP97262
         BAS   R14,PUT       AND PRINT IT                       GP97262
         LA    R4,1(,R4)     BUMP TO NEXT LINE                  GP97262
         BCT   R3,DOBLOCKL                                      GP97262
         BR    R9            RETURN TO CALLER                   GP97262
         SPACE 2                                                GP97262
PUT      L     R0,PAD                                           GP97262
         PUT   (R10),(0)     PUT F OR V RECORDS                 GP97262
         ORG   *-2                                              GP97262
         BR    R15           INVOKE PUTTER; RETURN VIA CALLER'S R14
         SPACE 3
         PUSH  USING
         DROP  ,
         USING DCBEX1,R15
         USING IHADCB,R1                                        GP97262
DCBEX1   LH    R0,DCBBLKSI   GET BLOCK SIZE                     GP97262
         LTR   R0,R0                                            GP97262
         BNZ   DCBEX2
         LH    R0,DCBLRECL   USE RECORD LENGTH INSTEAD          GP97262
         AH    R0,RPAD       ALLOW FOR RDW, IF ANY              GP97262
DCBEX2   STH   R0,DCBBLKSI                                      GP97262
         BR    R14                                              GP97262
         POP   USING                                            GP97262
         EJECT
SAVE     DC    18A(0)                                           GP97262
DCBLIST  OPEN  (PRINCE,OUTPUT),MF=L  OPEN/CLOSE LIST            GP97262
VIEFSD95 DC    V(IEFSD095)   WEAK EXTERNAL
NIEFSD95 DC    CL8'IEFSD095'  EXTERNAL NAME
LIST     DC    A(MEM,COUNT,BUFF,X'80000000'+CONTR)
EXLST    DC    X'85',AL3(DCBEX1)
COUNT    DC    F'0'                                             GP97262
VREC     DC    0F'0',AL2(125,0)  RDW - MUST PRECEDE 'CC'
CC       DC    X'00'         CARRIAGE CONTROL CHARACTER         GP97262
FIRST    DC    0CL132' ',CL6' '  INITIALIZED TO BLANKS          GP97262
BUFF     DC    CL128' '      OUTPUT LINE DATA AREA
BLANKS   DC    CL(L'FIRST)' '  FOR BLANKING PRINT LINE          GP97262
CONTR    DC    F'0'                                             GP97262
CONTP    DC    AL2(8,8)      CENTER, RE-WRITE 8 CHARACTERS
DATE     DC    CL8'YY/MM/DD' CURRENT DATE
MEM      DC    CL8' '        MEMBER NAME
NUMSTACK DC    F'2'          NORMALLY - 2 OR 3 LINES            GP97262
NUMSKIP  DC    F'6'          # LINES BETWEEN BLOCKS             GP97262
STACK    DC    3CL8' '       STACK FOR 3-LINE NAMES             GP97262
RPAD     DC    H'0'          LENGTH DIFF. FOR F AND V RECORDS
PAD      DC    A(CC)         F OR V PUT RECORD ADDRESS
MCC      DC    X'891109E3'   MACHINE CODE; EJCT, DBL, SNGLE, CH12
ACC      DC    C'10 C'       EQ. ASA CODE; EJCT, DBL, SNGLE, CH12
EJECT    DC    X'89'
DOUBLE   DC    X'11'
SINGLE   DC    X'09'
SKIP12   DC    X'E3'         CHANNEL 12 SKIP                     90095
MYFLAG   DC    X'00'                                             90096
MEFIRST  EQU   X'01'           SET AFTER FIRST CALL              90096
         SPACE 3
         PRINT NOGEN
PRINCE   DCB   DDNAME=ASMPRINT,DSORG=PS,MACRF=PM,EROPT=ACC,            X
               LRECL=121,RECFM=FBM,EXLST=EXLST
         SPACE 2
PARMSECT DSECT ,
BLOCK1   DS    CL8                                              GP97262
FLAG     DS    X
FGNOEJE  EQU   X'01'           SUPPRESS FIRST EJECT             GP97262
FGDOEJE  EQU   X'02'           FORCE FIRST EJECT                GP97262
FG2DATE  EQU   X'10'           PRINT DATE ALSO; AS SECOND ITEM  GP97262
FG3DATE  EQU   X'20'           PRINT DATE ALSO; AS THIRD ITEM   GP97262
FGXEROX  EQU   X'40'           SPECIAL XEROX PRINTING           GP97262
FG2LINE  EQU   X'80'           PROCESS SECOND BLOCK LINE        GP97262
BLOCK2   DS    CL8           TEXT OF SECOND PRINT LINE          GP97262
DDNAME   DS    CL8           DD NAME TO BE USED
DDBLKSI  DS    AL2           BLKSIZE TO BE USED / RETURNED IF ZERO
DDLRECL  DS    AL2           LRECL TO BE USED
DDRECF   DS    X             RECFM TO BE USED
DDCHAR   DS    X             CHARACTER SET SELECTOR             GP00005
         DS    9X              SPARES                           GP00005
         SPACE 2
         DCBD  DSORG=PS,DEVD=DA
         YREGS ,             MNEMONIC REGISTER DEFINITIONS      GP97262
         END
