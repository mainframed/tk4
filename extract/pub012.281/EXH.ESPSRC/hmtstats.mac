HMTS     TITLE 'H M T S T A T S  ***  HARRIS MOUNT MESSAGES'
*    HMTSTATS ALLOWS OUR LOCALLY ATTACHED (VIA COMTEN FRONT-END) HARRIS
*        MINI (ACTUALLY 2) TO SEND MOUNT, UNMOUNT AND SIMILAR MESSAGES
*        TO THE MAINFRAME OPERATOR.
*
*        THE DATA ARE STORED IN A CHAIN ROOTED IN OUR LOCAL USERCVT.
*        ENTRIES ARE NEVER FREED, BUT MAY BE REPLACED OR EXPANDED.
*
*
*
*        INPUT - A DATA SET ON SYSIN
*
*        OUTPUT - ON OPERATOR'S CONSOLE
*               - ON SYSPRINT IF A DD FOR IT IS PRESENT
*
*        THIS ROUTINE IS RE-ENTRANT
         SPACE 1
         COPY  OPTIONGB      DEFINE GLOBAL ASSEMBLY VARIABLES
         SPACE 1
         SYSPARM LIST=YES    SHOW ASSEMBLY OPTIONS
         EJECT ,
         PRINT &PRTSOR
HMTSTATS PGMHEAD ZERO12,BASE=R12,BNDRY=PAGE,PARM=R9     ADDED ON 92289
         SERVINIT ,          LOAD AND INITIALIZE THE @SERVICE ROUTINE
         SERVCALL LPALD,=CL8'@PRINTER'  GET THE PRINTER SERVICE
         ST    R0,@PRINTER   SAVE IT
         SERVCALL LPALD,=CL8'@INPREAD'   GET INPUT SERVICES
         ST    R0,@INPREAD   SAVE IT
         PRTOPEN SYSPRINT,OPT=ABEND  QUIT IF NO OUTPUT
         L     R8,CVTPTR     GET THE CVT
         ICM   R8,15,CVTUSER-CVTMAP(R8)  GET THE USER EXTENSION
         BZ    NOUSE         NONE
         MVC   ENQLIST(ENQPATL),ENQPAT  MAKE ENQUEUE LIST
         USING USERCVT,R8    DECLARE IT
         SERVCALL ACGET,DB   GET CURRENT ACCOUNT
         BXH   R15,R15,STATLIST  NONE - JUST LIST
         CLC   =C'G036',DB   SPECIAL CUSTOMER ?
         BE    ALLOWUP       YES
         STC   R0,DB         STASH PRIVILEGE FLAGS
         TM    DB,VAASTC+VAASYS+VAAINH  ANY PRIVILEGES ?
         BZ    STATLIST      NO; LIST ONLY
ALLOWUP  INPOPEN SYSIN,OPT=NOWTO  DON'T QUIT IF NO INPUT
         CH    R15,=H'4'     INPUT AVAILABLE ?
         BH    STATLIST      NO; JUST LIST STATUS
         SPACE 1
GETLOOP  INPGET ,            BETTER READ THAN DEAD ?
         BXH   R15,R15,STATLIST  NO MORE (ERROR OR EOF)
         MVC   CARD,0(R1)    SAVE TEXT
         PRTLIST INCARD      DISPLAY THE INPUT CARD
         CLI   CARD,C'*'     COMMENTS CARD ?
         BE    GETLOOP       YES; READ ANOTHER
         CLC   CARD+1(L'CARD-10),CARD  SPACER OR SOMETHING ?
         BE    GETLOOP
         SPACE 1
         ENQ   MF=(E,ENQLIST),RET=HAVE  PROTECT THE CHAIN
         LA    R1,INDRIV     GET INPUT DRIVE
         BAL   R9,FINDDRIV   FIND THE DRIVE ENTRY
         TIME  BIN           GET TIME AND DATE
         USING HARSECT,R7    DECLARE RESULT
         STM   R0,R1,HMTTIME  STASH TIME AND DATE
         MVC   HMTDRIV,INDRIV  COPY DRIVE ADDRESS
         MVC   HMTSTAT,INSTAT  COPY STATUS BYTE
         MVC   HMTSER,INSER  COPY SERIAL
         LA    R0,L'HMTSER   CURRENT FUNCTION - SET LENGTH
         STH   R0,HMTMSLN    SET TEXT LENGTH
         MODESET KEY=ZERO
         MVC   HARTIME(HARSIZE-(HARTIME-HARSECT)),HMTTIME  UPDATE
         NI    HARDOM,255-X'80'  RESET DATA CHANGE FLAG          93205
         MODESET KEY=NZERO   GET NORMAL
         DEQ   MF=(E,ENQLIST),RET=HAVE  RELEASE THE CHAIN
         B     GETLOOP       READ NEXT CARD
         SPACE 1
FINDDRIV LA    R7,UCHARRIS   GET HEAD OF HARRIS CHAIN            93202
FINDLOOP LR    R6,R7         SAVE ANCHOR ADDRESS                 93202
         ICM   R7,15,HARLINK  IS THERE ANOTHER ?
         BZ    FINDMAKE      NO; MAKE ONE
         CLC   INDRIV,HARDRIV  MATCHING DRIVE ?
         BNE   FINDLOOP
         MVC   HMTTIME(HARSIZE-(HARTIME-HARSECT)),HARTIME  COPY LOCAL
         BR    R9            RETURN
FINDMAKE MODESET KEY=ZERO    GET AUTHORIZED
         LA    R0,HARSIZE    GET SIZE NEEDED
         GETMAIN RC,LV=(0),SP=241  GET SPACE IN CSA
         BXH   R15,R15,ABENDE04  NOT ENOUGH CSA
         LR    R7,R1         SAVE ADDRESS
         XC    HARSECT(HARSIZE),HARSECT  CLEAR IT
         ST    R7,HMTLINK-HMTLINK(R6)  LINK IT IN
         MODESET KEY=NZERO
         BR    R9            RETURN TO CALLER
         SPACE 1
NOUSE    PRTF  'USERCVT NOT FOUND - CONTACT SYSTEMS',CC=NO
         ABEND 666
         SPACE 1
ABENDE04 PRTF  '0ALLOCATION OF CSA SPACE FAILED - CONTACT SYSTEMS'
         ABEND 3588
         SPACE 2
STATLIST INPCLOSE ,          FREE INPUT
         PRTSPACE 1          DOUBLE-SPACE
         LA    R7,UCHARRIS   POINT TO CHAIN HEAD
         ICM   R7,15,HARLINK  ANY ?
         BNZ   STATLOOP      YES; GO TO DISPLAY
         PRTF  'NO MOUNT REQUESTS ON CHAIN',CC=NO
         B     EXIT
         SPACE 1
STATLOOP PRTLIST STATLINE    PRINT ONE LINE
         ICM   R7,15,HARLINK  GET NEXT, IF ANY
         BNZ   STATLOOP                                          93202
         SPACE 1
EXIT     SERVTERM ,          CLOSE AND FREE EVERYTHING
         PGMEXIT RC=0        GOOD RETURN
         EJECT
         PRINT &PRTMAC
*        READ ONLY STORAGE
         SPACE 2
SYSPRINT PRTWORK SYSPRINT,SYSTERM,TITLE=5
SYSIN    INPWORK SYSIN,EDIT=128,WIDTH=80
         SPACE 1
INCARD   FD    ' ',NL,LEN=20
         FD    CARD
         FD    *END
         SPACE 1
STATLINE FD    0(R7),4,ADDR,HEX,NL,PAD
         FD    HARDATE,DATE,PAD
         FD    HARTIME,TIME,PAD
         FD    HARDRIV,PAD
         FD    HARSTAT,PAD
         FD    HARSER,PAD
         FD    *END
         SPACE 1
HEADER   FDOPT NL,CC=C'#'     REQUEST AUTO PAGE NUMBERING
         FD    'HARRIS MOUNT STATUS',RADJ,LEN=65
         FD    *END
         SPACE 1
ENQPAT   ENQ   (QNAME,RNAME,E,,SYSTEM),MF=L
ENQPATL  EQU   *-ENQPAT
QNAME    DC    CL8'HARRIS'
RNAME    DC    C'MOUNT'
         SPACE 2
SAVE     DSECT ,             SAVE/WORK SPACE
DB       DS    D             WORK WORDS
@SERVICE DS    A             @SERVICE ADDRESS
@PRINTER DS    A             @PRINTER ADDRESS
@INPREAD DS    A             @INPREAD ADDRESS
CARD     DS    CL80          INPUT BUFFER
         ORG   CARD
INDRIV   DS    CL3           HARRIS DRIVE NUMBER
INSTAT   DS    C             STATUS BYTE
INSER    DS    CL6           SERIAL FOR MOUNT
         ORG   ,
         SPACE 1
ENQLIST  DC    (ENQPATL)X'0'  ENQUEUE LIST
         SPACE 1
USERHMT  USERHMT SECT=C,PFX=HMT  LOCAL COPY OF ENTRY
         SPACE 1
SAVEEND  EQU   *             END OF SAVE/WORK AREA
         SPACE 2
HARSECT  USERHMT SECT=D,PFX=HAR  READ CONTROL BLOCK
         SPACE 1
USERCVT  USERCVT ,           LOCAL EXTENSION
         SPACE 1
         PRINT &PRTSYS       SYSTEMS BLOCKS
         CVT   DSECT=YES
         IHACDE ,            FOR NON-SVC @SERVICE
         END
