XTRA     TITLE 'E X H I B I T  ***  FILE UPDATE UTILITY'
         COPY  OPTIONGB                                          82235
         SPACE 1                                                 82235
         SYSPARM LIST=YES                                        82235
         SPACE 1                                                 82235
*********************************************************************
*                                                                   *
*        THIS VERSION OF EXTRACTS HAS BEEN REWRITTEN FOR MVS AND    *
*        WILL NOT WORK FOR EXHIBIT UNLESS ALL REQUIRED ROUTINES     *
*        (@SERVICE, @PRINTER, ETC.) ARE LOADABLE                    *
*                                                                   *
*********************************************************************
         EJECT ,
*        THIS EXHIBIT UTILITY PROGRAM MAY BE USED TO APPLY
*        UPDATES FROM EXHIBIT DISTRIBUTION TAPES.
*
*        TAPE FORMAT:
*        FILE 1              SOURCE    EXHIBIT SOURCE/MACRO CODE
*        FILE 2              OBJECT    CORRESPONDING OBJECT DECKS
*        FILE 3              SYSMAC    REQUIRED VS AND OS MACROS 90303
*        FILE 4/5              OPERATOR'S AND TECHNICAL GUIDES
*        FILE 6              EXHELP    EXHIBIT HELP DATASET
*        FILE 7              ESPSRC    OPTIONAL UTILITIES/SYSTEM MODS
*        FILE 8              ESPMAC    REQUIRED EXHIBIT+ESP MACROS
*
*        JCL FOR APPLICATION OF UPDATE TAPE
* //     EXEC PGM=EXTRACTS,REGION=150K,TIME=5                    81054
* //STEPLIB DD DISP=SHR,DSN=SYS1.EXHIBIT  LOAD MODULE LIBRARY
* //SYSPRINT DD SYSOUT=A                  PROCESSING MESSAGES
* //TAPE     DD UNIT=3400-3,VOL=SER=EXHIBI,LABEL=(,SL,EXPDT=98000),
* //           DSN=SOURCE,DISP=(OLD,PASS)  DISTRIBUTION TAPE
* //SOURCE   DD DISP=SHR,DSN=SYS1.EXSOURCE  EXHIBIT SOURCE PDS
* //OBJECT   DD DISP=OLD,DSN=SYS1.EXHOBJ    EXHIBIT OBJECT PDS
* //MACROS   DD DISP=SHR,DSN=SYS1.EXSYSMAC  OS/VS MACRO LIBRARY  90303
* //HELP     DD DISP=SHR,DSN=SYS1.EXHELP    EXHIBIT HELP DATASET
* //ESPSRC   DD DISP=SHR,DSN=SYS1.ESPSRC    (OPT.) UTILITY PDS   90303
* //ESPMAC   DD DISP=SHR,DSN=SYS1.ESPMAC    (OPT.) UTILITY PDS   90303
* //*    TEMPORARY FILES FOR SUBSEQUENT IEBUPDTE RUNS
* //UPSOURCE DD DISP=(,PASS),UNIT=SYSDA,DSN=&&SOURCE,
* //             SPACE=(CYL,(5,5),,CONTIG)
* //UPOBJECT DD DISP=(,PASS),UNIT=SYSDA,DSN=&&OBJECT,
* //             SPACE=(CYL,(5,5),,CONTIG)
* //UPMACROS DD DISP=(,PASS),UNIT=SYSDA,DSN=&&MACROS,
* //             SPACE=(CYL,(5,5),,CONTIG)
* //UPHELP   DD DISP=(,PASS),UNIT=SYSDA,DSN=&&HELP,
* //             SPACE=(CYL,(5,5),,CONTIG)
* //UPESPSRC DD DISP=(,PASS),UNIT=SYSDA,DSN=&&ESPSRC,            90303
* //             SPACE=(CYL,(5,5),,CONTIG)
* //UPESPMAC DD DISP=(,PASS),UNIT=SYSDA,DSN=&&ESPMAC,            90303
* //             SPACE=(CYL,(5,5),,CONTIG)                       90303
* //*    FILES TO CONTAIN UPDATED SOURCE FOR USER MODIFIED CODE
*        THESE SHOULD BE RUN AS IEBUPDTE JOBS ALSO AND APPLIED
*        AS NECESSARY
* //RESOURCE DD DISP=(,KEEP),DSN=TEMPSAVE.SOURCE,VOL=SER=...
* //           SPACE=(CYL,(1,5),RLSE,CONTIG)
* //REHELP   DD DISP=(,KEEP),DSN=TEMPSAVE.HELP  ,VOL=SER=...
* //           SPACE=(CYL,(1,5),RLSE,CONTIG)
* //REESPSRC DD DISP=(,KEEP),DSN=TEMPSAVE.ESPSRC  ,VOL=SER=...   90303
* //           SPACE=(CYL,(1,5),RLSE,CONTIG)
* //REESPMAC DD DISP=(,KEEP),DSN=TEMPSAVE.ESPMAC  ,VOL=SER=...   90303
* //           SPACE=(CYL,(1,5),RLSE,CONTIG)                     90303
* //*          IF ONLY THE FIRST STEP IS RUN (WITH PARM=TEST)
* //*          THEN ADDITIONAL CARDS MAY BE INSERTED TO PRINT OUT
* //*          THE MODIFIED INFORMATION (INSTEAD OF THE IEBUPDTE
* //*          SYSPRINT LISTING)
* //PRSOURCE DD SYSOUT=A  (PRSOURCE/PRSYSMAC/PRHELP/PRESPSRC/PRESPMAC)
* //*      COMPRESSION OF FILES TO MINIMIZE X37 ABENDS
* //     EXEC PGM=IEBCOPY,REGION=300K,TIME=15
* //SYSPRINT DD SYSOUT=A
* //SYSUT3   DD UNIT=SYSDA,SPACE=(CYL,3)
* //SYSUT4   DD UNIT=SYSDA,SPACE=(CYL,3)
* //SOURCE   DD DISP=OLD,DSN=SYS1.EXSOURCE
* //OBJECT   DD DISP=OLD,DSN=SYS1.EXHOBJ
* //MACROS   DD DISP=OLD,DSN=SYS1.EXSYSMAC                       90303
* //HELP     DD DISP=OLD,DSN=SYS1.EXHELP
* //ESPSRC   DD DISP=OLD,DSN=SYS1.ESPSRC                         90303
* //ESPMAC   DD DISP=OLD,DSN=SYS1.ESPMAC                         90303
*  COPY I=SOURCE,O=SOURCE
*  COPY I=OBJECT,O=OBJECT
*  COPY I=SYSMAC,O=SYSMAC                                        90303
*  COPY I=HELP,O=HELP
*  COPY I=ESPSRC,O=ESPSRC                                        90303
*  COPY I=ESPMAC,O=ESPMAC                                        90303
* //*     PDS UPDATE RUNS
* //UPSOURCE EXEC PGM=IEBUPDTE,PARM=NEW
* //SYSPRINT DD SYSOUT=A
* //SYSUT2  DD DISP=OLD,DSN=SYS1.EXSOURCE,SPACE=(CYL,(5,5,57),,CONTIG)
* //SYSIN  DD DSN=&&SOURCE,DISP=(OLD,DELETE)
* //UPOBJECT EXEC PGM=IEBUPDTE,PARM=NEW
* //SYSPRINT DD SYSOUT=A
* //SYSUT2  DD DISP=OLD,DSN=SYS1.EXHOBJ,SPACE=(CYL,(5,3,57),,CONTIG)
* //SYSIN  DD DSN=&&OBJECT,DISP=(OLD,DELETE)
* //UPSYSMAC EXEC PGM=IEBUPDTE,PARM=NEW                          90303
* //SYSPRINT DD SYSOUT=A
* //SYSUT2  DD DISP=OLD,DSN=SYS1.EXSYSMAC,SPACE=(CYL,(5,5,57),,CONTIG)
* //SYSIN  DD DSN=&&MACROS,DISP=(OLD,DELETE)
* //UPHELP   EXEC PGM=IEBUPDTE,PARM=NEW
* //SYSPRINT DD SYSOUT=A
* //SYSUT2  DD DISP=OLD,DSN=SYS1.EXHELP,SPACE=(CYL,(5,3,57),,CONTIG)
* //SYSIN  DD DSN=&&HELP,DISP=(OLD,DELETE)
* //UPESPSRC EXEC PGM=IEBUPDTE,PARM=NEW                          90303
* //SYSPRINT DD SYSOUT=A
* //SYSUT2  DD DISP=OLD,DSN=SYS1.ESPSRC,SPACE=(CYL,(5,5,57),,CONTIG)
* //SYSIN  DD DSN=&&ESPSRC,DISP=(OLD,DELETE)                     90303
* //UPESPMAC EXEC PGM=IEBUPDTE,PARM=NEW                          90303
* //SYSPRINT DD SYSOUT=A                                         90303
* //SYSUT2  DD DISP=OLD,DSN=SYS1.ESPMAC,SPACE=(CYL,(5,5,57),,CONTIG)
* //SYSIN  DD DSN=&&ESPMAC,DISP=(OLD,DELETE)                     90303
* //*    LAST STEP TO BE EXECUTED ONLY IF EVERYTHING WORKED
* //FINAL  EXEC PGM=IEHPROGM
* //DISK DD VOL=REF=SYS1.EXSOURCE,DISP=OLD
* //SYSPRINT DD SYSOUT=A
*  SCRATCH MEMBER=$,DSNAME=SYS1.EXSOURCE,VOL=XXXX=VOLSER
*   RENAME MEMBER=$$,DSNAME=SYS1.EXSOURCE,VOL=XXXX=VOLSER,NEWNAME=$
*
*        IN CASE OF X37 ABENDS, TAKE THE APPROPRIATE RECOVERY
*        ACTION AND RERUN THE JOB FROM THE BEGINNING.  DO SAVE THE
*        SOURCE UPDATE LISTING FROM PREVIOUS RUNS.
*        THE CURRENT VERSION SOMETIMES FAILS TO RECOGNIZE WHEN MODULES
*        NEED TO BE REASSEMBLED.  IF MODULES NEED TO BE ASSEMBLED,
*        RUN AN EXHIBIT STAGE 1 WITH A GENMODS CARD FOR EACH MODULE
*        TO BE ASSEMBLED, AND A GENERATE TYPE=ASMON
*        AFTER THE ASSEMBLIES (IF ANY), RUN ANOTHER STAGE 1
*        WITH A GENMODS CARD FOR EACH MODULE FOR WHICH THE OBJECT
*        DECK WAS REPLACED, AND A GENERATE TYPE=LINK.
*        THIS COMPLETES APPLICATION OF THE UPDATE TAPE.
*          NOTE THAT CURRENTLY THERE IS A LIMIT OF 50 GENMODS CARDS
*          PER STAGE1 RUN.
         SPACE 1                                                 81053
*        THE PROGRAM MAY BE USED FOR GENERAL CARD FORMAT PO DATASETS
*        IF ALL DDNAMES CONTAINING 'SOURCE' ARE CHANGED TO 'PDS',
*        //PDS DD DISP=OLD //REPDS DD //UPPDS DD                 81053
*        SUPPORT FOR OTHER LIBRARIES IS NOT PROVIDED.            81053
         EJECT ,
         PRINT &PRTSOR                                           82235
EXTRACTS PGMHEAD ZERO12,BASE=(R11,R12),PARM=R9 ADDED 79208
         SERVINIT ,          LOAD AND INITIALIZE @SERVICE        82235
         SERVCALL LPALD,=CL8'@PRINTER'  LOAD THE PRINTER ROUTINE 82235
         ST    R0,@PRINTER   SAVE IT                             82235
         SPACE 1
         LA    R1,0(,R9)                                         82235
         LTR   R1,R1         ANY PARM POINTER ?
         BZ    NOPARM        NO
         LAT   R1,0(R1),NOPARM
         OC    0(2,R1),0(R1)  ANY LENGTH ?
         BZ    NOPARM        NO
         OI    OPTS,TEST     SET TEST MODE
NOPARM   PRTOPEN SYSPRINT,OPT=(ABEND,WTO)  BOMB IF NO PRINTER    82235
         PRTV  HEADER,TITLE=1  PRINT HEADER                      82235
         MVI   PASS#,0       SET PASS NUMBER/OFFSET
         MVI   PASS,PASSSORE  SET FOR SOURCE FILE
         DEVTYPE =CL8'PDS',DB  SEE IF PDS DD CARD PROVIDED       81053
         BXH   R15,R15,NEXTPASS    NO; RUN EXHIBIT MODE          81053
         CLI   DB+2,X'20'    DISK FILE ?                         81053
         BNE   NEXTPASS      NO; BOMB LATER                      81053
         OI    OPTS,PDS      SET FOR PDS MODE                    81053
         MVC   NAMELIST(8),=CL8'PDS'  PRIME DDNAME LIST          81053
         MVC   LISTJFCB+2(8),=CL8'PDS'                           81053
NEXTPASS MVI   FLAG,0        RESET FLAG
         XC    COUNTS,COUNTS  CLEAR COUNT FIELDS
         BAL   R9,OPENDISK   OPEN AND PREPROCESS SOURCE FILE
         BAL   R9,OPENTAPE   OPEN AND ESTABLISH TAPE INPUT
         SPACE 2
TAPEGET  TM    FLAG,IEBCOPY  UNLOADED FORMAT ?                   81053
         BZ    TAPEGETF      NO; GET NEXT CARD                   81053
         TM    CPFLAG,CPCARD   NEW CARD AVAILABLE ?              81053
         BNZ   TPCARD        YES; UNBLOCK IT                     81053
         TM    CPFLAG,CPBLOK   NEW BLOCK AVAILABLE ?             81053
         BNZ   NEWBLOCK      YES; GET IT                         81053
         GET   TAPE                                              81053
         ST    R1,ADRBLOCK   SAVE CURRENT BLOCK ADDRESS          81053
         USING INREC,R1      DECLARE INPUT RECORD                81053
LOOPPNEW MVC   TRKPCYL,UTRKPCYL  SAVE TRACKS PER CYLINDER        81053
         TM    UDSORG,255-JFCORGPO-JFCORGU  ANY NON-PO DSORG ?   81053
         BNZ   BADBLOCK                                          81053
         CLI   UDSORG+1,0                                        81053
         BNE   BADBLOCK                                          81053
         TM    UDSORG,JFCORGPO                                   81053
         BZ    BADBLOCK                                          81053
         CLC   =H'80',ULRECL   CARD FORMAT ?                     81053
         BNE   BADBLOCK                                          81053
         MVC   PDRECFM,URECFM                                    82235
         CLI   UKEYLN,0                                          81053
         BNE   BADBLOCK                                          81053
         CLI   UUCBTYPE+2,X'20'   DIRECT ACCESS DEVICE ?         81053
         BNE   BADBLOCK      NO; FAIL                            81053
         SPACE 1                                                 81053
         GET   TAPE                                              81053
         ST    R1,ADRBLOCK   SAVE CURRENT BLOCK ADDRESS          81053
         CLI   UNMEXT,0                                          81053
         BE    BADBLOCK                                          81053
         CLI   UNMEXT,16     VALID EXTENT NUMBER ?               81053
         BH    BADBLOCK      NO                                  81053
         MVC   NMEXT+1(1),UNMEXT  SAVE NUMBER OF EXTENTS         81053
         MVC   EXTENTS(16*16),UEXTENTS                           81053
         LM    R7,R9,PDEINIT   GET PDS SAVE AREA POINTERS        81053
         LTR   R7,R7         GETMAIN DONE BEFORE ?               81053
         BP    LOOPBXLE      YES                                 81053
         GETMAIN VU,LA=GETMAD,A=DB   GET STORAGE FOR DIRECTORY   81053
         BXLE  R15,R15,LOOPGOTM                                  81053
         SERVTERM ,          CLOSE AND FREE                      82235
         ABEND 804           INDICATE LOW ON STORAGE             81053
LOOPGOTM L     R7,DB         GET START ADDRESS                   81053
         L     R9,DB+4       GET SIZE                            81053
         SR    R9,R8         BUMP BACK FOR BXLE LOGIC            81053
         SR    R9,R8         BUMP BACK ONE MORE FOR 'FF' ENTRY   81053
         AR    R9,R7         MAKE END ADDRESS                    81053
         STM   R7,R9,PDEINIT  SAVE INITIAL POINTERS              81053
LOOPBXLE STM   R7,R9,PDEBXLE   SET PDS SAVE AREA POINTERS        81053
         USING PDENTRY,R7    DECLARE IT                          81053
         SPACE 1                                                 81053
LOOPPDEG GET   TAPE          GET A PDS RECORD                    81053
         ST    R1,ADRBLOCK   SAVE CURRENT BLOCK ADDRESS          81053
         LH    R0,RDW        GET BLOCK LENGTH                    81053
         LA    R1,4(R1,0)    SKIP RDW                            81053
         SH    R0,*-2        DECREMENT RESIDUAL LENGTH           81053
         BNP   LOOPPDEG      SKIP NULL RECORD                    81053
         USING UWORD,R1                                          81053
LOOPPDEH STM   R0,R1,CURBLOCK  SAVE CURRENT BLOCK ADDRESS        81053
         CLC   ZEROES(5),UCOUNT  PDS RECORD ?                    81053
         BNE   DONEPDER      NO                                  81053
         CLC   =X'080100',UCKLEN    PDS KEY/LENGTH ?             81053
         BNE   DONEPDER      NO                                  81053
         TM    CPFLAG,CPEOF  LOGICAL END-FILE IN DIRECTORY ?     81053
         BNZ   LOOPPDER      YES; SKIP THIS BLOCK                81053
         LA    R3,UPDSLEN                                        81053
         LH    R5,UPDSLEN    GET LENGTH OF ENTRY                 81053
         LA    R5,UPDSLEN-1(R5)  LAST BYTE                       81053
         LA    R4,2          LENGTH OF FIRST SEGMENT             81053
LOOPPDEM BXH   R3,R4,LOOPPDER  DONE; GET NEXT BLOCK              81053
         CLC   =X'FFFFFFFFFFFFFFFF00000000',0(R3)  LAST BLOCK ?  81053
         BNE   LOOPPDEN      NO                                  81053
         OI    CPFLAG,CPEOF  SET LOGICAL END-FILE                81053
LOOPPDER LM    R0,R1,CURBLOCK  GET BLOCK POINTER                 81053
         XR    R2,R2                                             81053
         ICM   R2,3,UDATLEN  GET BLOCK LENGTH                    81053
         LA    R2,UFIXLEN+8(,R2)  ADD OVERHEAD                   81053
         AR    R1,R2                                             81053
         SR    R0,R2                                             81053
         BP    LOOPPDEH      DO NEXT BLOCK                       81053
         B     LOOPPDEG      GET ANOTHER BLOCK                   81053
LOOPPDEN STM   R3,R5,DEBXLE  SET PARMS FOR PDE ANALYZE           82235
         SERVCALL PDSDE,DEBXLE   ANALYZE THE DATA                82235
         LR    R15,R0        COPY EXTENDED RETURN DATA ADDRESS   82235
         MVC   PDRALIS(PDRBLNK),0(R15)  MOVE TO MY WORK          82235
         MVC   PDENTRY(PDEDATA-PDENTRY),0(R3)  COPY BASIC PORTION
         XC    PDEDATA(4),PDEDATA  CLEAR SSI FIELD JUST IN CASE  81053
         NI    PDEFLAG,X'80' CLEAR LENGTH                        82235
         CLI   PDRDATE,C' '  IS DATE PRESENT ?                   82235
         BH    LOOPPDDT      YES; CONVERT IT                     82235
         TM    PDFLAGS,PDFSSI  IS SSI PRESENT ?                  82235
         BZ    LOOPPDSX      NO; CHECK IF DATE REQUIRED          82235
LOOPPDSS PACK  UPD(5),PDRSSI(9)  PACK SSI                        82235
         MVC   PDEDATA(4),UPD  MOVE SSI                          82235
         OI    PDEFLAG,2     SET SSI PRESENT                     82235
LOOPPDSX TM    PASS,PASSSORE+PASSHELP+PASSMODS  SSI REQUIRED ?   82235
         BZ    LOOPPDEX      NO                                  82235
         PRTLIST BADTPSSI    SHOW INVALID/MISSING SSI ON TAPE    82235
         OI    PDEFLAG,2     FORCE DATE FORM OF SSI              82235
         MVC   PDEDATA(4),=X'00075001'  FORCE BAD DATE           82235
         B     LOOPPDEX                                          82235
LOOPPDDT PACK  UPD(5),PDRDATE(6)  PACK THE PRESUMED DATE         82235
         MVC   PDEDATA(4),UPD  MOVE TO NEW DIRECTORY ENTRY       82235
         OI    PDEFLAG,2     SET SSI PRESENT                     82235
LOOPPDEX L     R4,PDINCR     GET MEMBER ENTRY LENGTH             82235
         BXLE  R7,R8,LOOPPDEM  DO ANOTHER MEMBER                 81053
         PRTV  MEMOFLOW      MEMBER TABLE OVERFLOW               82235
         B     CLOSERR       CLOSE WITH ERROR                    81053
         SPACE 1                                                 81053
DONEPDER MVI   PDENTRY,X'FF'   SET FINAL ENTRY                   81053
         MVC   PDENTRY+1(PDLEN-1),PDENTRY  MAKE ALL FF, INC. TTR 81053
         SR    R7,R8         GET LAST MEMBER ENTRY               81053
         ST    R7,PDEBXLE+8  UPDATE IT                           81053
         LR    R3,R1         SAVE BLOCK ADDRESS                  82235
         PRTSPACE 1          MAKE LISTING NEAT AFTER ERRORS (?)  82235
         LR    R1,R3         RESTORE BLOCK ADDRESS               82235
         OI    CPFLAG,CPEOF+CPBLOK  SET END-FILE AND DATA BLOCK  81053
         LM    R3,R5,PDEBXLE   GET DIRECTORY POINTERS            81053
         LR    R2,R3         COPY START ADDRESS                  81053
         CR    R2,R5         ANY ENTRIES ?                       81053
         BH    NOMEMBRS      NONE - EXIT WITH ERROR              81053
         BE    GETDATA       ONLY ONE - SKIP SORT                81053
         LR    R6,R4         LENGTH OF AN ENTRY                  81053
         LR    R7,R5         1ST BXLE                            81053
         SR    R5,R4         NEXT TO LAST ENTRY ADDR FOR         81053
SORTBGIN LA    R3,0(R2,R6)   NEXT ENTRY                          81053
SORTCLC  CLC   PDETTR-PDENTRY(PDLEN,R2),PDETTR-PDENTRY(R3)       81053
         BL    SORTEND                                           81053
         BH    SORTXC        FLIP                                81053
         TM    PDEFLAG-PDENTRY(R2),X'80'  ALIAS ?                81053
         BZ    SORTEND       NO; LEAVE LOW                       81053
         TM    PDEFLAG-PDENTRY(R3),X'80'  SECOND ALIAS ?         81053
         BNZ   SORTEND       NO; LEAVE AS IS                     81053
SORTXC   XC    0(PDLEN,R2),0(R3)                                 81053
         XC    0(PDLEN,R3),0(R2)                                 81053
         XC    0(PDLEN,R2),0(R3)                                 81053
SORTEND  BXLE  R3,R6,SORTCLC                                     81053
         BXLE  R2,R4,SORTBGIN                                    81053
         B     GETDATA2      USE CURRENT BLOCK                   81053
         SPACE 1                                                 81053
GETDATA  GET   TAPE          GET NEXT BLOCK                      81053
         ST    R1,ADRBLOCK   SAVE CURRENT BLOCK ADDRESS          81053
         USING INREC,R1      DECLARE INPUT RECORD                81053
GETDATA2 LH    R0,RDW        GET BLOCK LENGTH                    81053
         LA    R1,4(R1,0)    SKIP RDW                            81053
         SH    R0,*-2        DECREMENT RESIDUAL LENGTH           81053
         BNP   GETDATA       SKIP NULL RECORD                    81053
SETBLOCK STM   R0,R1,CURBLOCK  SAVE CURRENT BLOCK ADDRESS        81053
         USING UWORD,R1                                          81053
         XR    R2,R2                                             81053
         ICM   R2,3,UDATLEN    GET BLOCK DATA LENGTH             81053
         BNZ   DATBLOCK      PROCESS DATA                        81053
         OI    CPFLAG,CPEOF  SET END-FILE ON                     81053
         B     NEWBLOCK      DO NEXT BLOCK                       81053
DATBLOCK LA    R3,UBLOCK                                         81053
         LA    R5,UBLOCK-1(R2)  GET LAST BYTE                    81053
         LA    R4,80         SET CARD LENGTH                     81053
         STM   R3,R5,BLOKBXLE  SET BLOCK POINTERS                81053
         TM    CPFLAG,CPEOF  PRIOR END-FILE ?                    81053
         BZ    TPCARD        NO; RETURN NEXT RECORD              81053
         CLC   UCOUNT,ZEROES   ANY CCHHR ?                       81053
         BE    BADBLOCK      DIRECTORY BLOCK OUT OF SEQUENCE     81053
         BAL   R9,ABSTTR     CONVERT ABSOLUTE TO TTR             81053
         LM    R7,R9,PDEBXLE  GET MEMBER POINTERS                81053
GETBLMEM CLC   TTR,PDETTR    SAME TTR ?                          81053
         BE    MAKEADD       YES; MAKE AN ADD CARD               81053
         CLC   TTR,PDETTR+PDLEN  LOWER THAN NEXT ONE ?           81053
         BL    NEWBLOCK      YES; SKIP THIS BLOCK                81053
         BXLE  R7,R8,GETBLMEM  ELSE TRY AGAIN                    81053
         LR    R5,R1         SAVE BLOCK ADDRESS                  81053
         PRTV  MEMNTFND      SIGNAL MEMBER NOT FOUND             82235
         PRTV  MEMRENIT      RE-INITIALIZE                       82235
         PRTSPACE 1          SKIP A LINE AFTER                   82235
         MVI   CPFLAG,0                                          81053
         LR    R1,R5         RESTORE BLOCK ADDRESS               81053
         SH    R1,=H'4'      BUMP BACK TO RDW                    81053
         C     R1,ADRBLOCK   BLOCK START ADDRESS ?               81053
         BNE   CLOSERR       NO; TAKE A DUMP                     81053
         B     LOOPPNEW      TRY IT AGAIN                        81053
MAKEADD  ST    R7,PDEBXLE    SAVE ADDRESS OF LAST MEMBER USED    81053
         MVC   ADDMEM(8+8+5),BLANKS   CLEAR NAME AND SSI FIELD   81053
         MVC   ADDMEM,PDEMEM  COPY MEMBER NAME                   81053
         LA    R14,ADDMEM+1                                      81053
         LA    R15,7                                             81053
MAKEADDB CLI   0(R14),C' '   TRAILING BLANK ?                    81053
         BE    MAKEADDC                                          81053
         LA    R14,1(,R14)                                       81053
         BCT   R15,MAKEADDB                                      81053
MAKEADDC TM    PDEFLAG,X'1F'-1  ANY SSI ?                        81053
         BZ    PUTCARDM                                          81053
         MVC   0(5,R14),=C',SSI='                                81053
         UNPK  5(9,R14),PDEDATA(5)                               81053
         TR    5(8,R14),HEXTRTAB-C'0'                            81053
         MVI   13(R14),C' '                                      81053
PUTCARDM NI    CPFLAG,255-CPEOF  RESET END-FILE                  81053
         NI    FLAG,255-COPYCON                                  81053
         LA    R1,FAKECARD   GET FAKE ADD CARD                   81053
         OI    CPFLAG,CPCARD   SHOW CARD AVAILABLE               81053
         B     TPDOCARN      GO TO COMMON                        81053
TPCARD   TM    FLAG,COPY+REJECT  IS CARD NEEDED ?                81053
         BZ    NEWBLOCK      NO; SKIP TO NEXT BLOCK              81053
         OI    CPFLAG,CPCARD   SHOW CARD AVAILABLE               81053
         LM    R3,R5,BLOKBXLE  GET BLOCK POINTERS                81053
         LR    R1,R3         SAVE CURRENT CARD ADDRESS           81053
         BXLE  R3,R4,TPDOCARD  SEND CARD TO PROGRAM              81053
         NI    CPFLAG,255-CPCARD  NO MORE CARDS IN THIS BUFFER   81053
         SPACE 1                                                 81053
TPDOCARD STM   R3,R5,BLOKBXLE   SAVE POINTERS FOR NEXT CARD      81053
         CLC   0(2,R1),FAKECARD  IEBUPDTE CONTROL FIELD ?        81053
         BNE   TPDOCARN      NO                                  81053
         MVI   0(R1),C'@'    CHANGE IT FOR LOCAL                 81053
         TM    FLAG,COPYCON  ERROR MESSAGE ALREADY ISSUED ?      81053
         BNZ   TPDOCARN      YES                                 81053
         OI    FLAG,COPYCON  SET ERROR MESSAGE ISSUED            81053
         LR    R5,R1         SAVE OVER PRINT                     81053
         PRTV  MSGCOPYC      ISSUE ERROR MESSAGE                 82235
         LR    R1,R5         RESTORE                             81053
TPDOCARN B     TAPEGETC      GO TO COMMON                        81053
         SPACE 1                                                 81053
NEWBLOCK NI    CPFLAG,255-CPCARD  TURN 'CARD AVAILABLE' OFF      81053
         LM    R0,R1,CURBLOCK  GET BLOCK POINTER                 81053
         USING UWORD,R1                                          81053
         XR    R2,R2                                             81053
         ICM   R2,3,UDATLEN  GET BLOCK LENGTH                    81053
         LA    R2,UFIXLEN(,R2)    ADD OVERHEAD                   81053
         AR    R1,R2                                             81053
         SR    R0,R2                                             81053
         BP    SETBLOCK      DO NEXT BLOCK                       81053
         B     GETDATA       ELSE READ ANOTHER                   81053
         SPACE 1                                                 81053
BADBLOCK PRTV  MSGCOPYB      SHOW BAD BLOCK                      82235
         B     CLOSERR       TAKE A DUMP                         81053
         SPACE 1                                                 81053
NOMEMBRS PRTV  MEMNONE       NO MEMBERS MESSAGE                  82235
CLOSERR  SERVTERM ,          CLOSE AND FREE EVERYTHING           82235
         ABEND 3,DUMP        TAKE A DUMP                         81053
         SPACE 2                                                 81053
         USING UWORD,R1                                          81053
ABSTTR   MVC   TTR+2(1),UCOUNT+4  COPY RECORD NUMBER             81053
         LH    R14,NMEXT     GET NUMBER OF EXTENTS               81053
         LA    R15,EXTENTS   POINT TO FIRST                      81053
         XR    R0,R0         CLEAR TRACK COUNTER                 81053
ABSTTRL  CLC   UCOUNT(4),6(R15)  LOWER THAN EXTENT START ?       81053
         BL    ABSTTRN       YES; INCREMENT                      81053
         CLC   UCOUNT(4),10(R15)  WITHIN EXTENT ?                81053
         BNH   ABSTTRF       YES                                 81053
ABSTTRN  AH    R0,14(,R15)   ADD TRACKS IN THIS EXTENT           81053
         LA    R15,16(,R15)  BUMP                                81053
         BCT   R14,ABSTTRL   TRY NEXT EXTENT                     81053
         MVC   TTR,=X'FFFFFFFFFFFFFFFF00000000'  MAKE FF'S       81053
         BR    R9                                                81053
ABSTTRF  LH    R14,UCOUNT    GET CYLINDER                        81053
         SH    R14,6(,R15)   LESS START CYLINDER                 81053
         MH    R14,TRKPCYL   CONVERT TO TRACKS                   81053
         AH    R14,UCOUNT+2  ADD TRACK NUMBER                    81053
         SH    R14,8(,R15)   LESS START TRACK                    81053
         AR    R14,R0        PLUS PRIOR EXTENT TRACKS            81053
         STH   R14,TTR       SET TT                              81053
         BR    R9            RETURN                              81053
         SPACE 1                                                 81053
TAPEGETF GET   TAPE          GET NEXT TAPE CARD                  81053
TAPEGETC LA    R5,0(,R1)     SAVE CLEANED ADDRESS                81053
         CLC   =C'./',0(R5)  UPDTE CONTROL CARD ?
         BE    CARDCHEK      YES;  SEE WHAT IT IS
         SPACE 2
DISPOSE  TM    FLAG,COPY     COPY THIS MEMBER ?
         BZ    DISPREJ       NO
         PRTF  (R5),80,CC=NO,DEV=(3,4)  UPDATE/UPLIST            82235
DISPLIST TM    FLAG,TESTASM  ASSEMBLY CHECK REQUIRED ?           79230
         BZ    TAPEGET       NO                                  79230
         XR    R9,R9         START WITH SECOND COLUMN            79230
DISPTRT  LA    R9,1(,R9)     GO TO NEXT COLUMN                   79230
         LA    R14,0(R5,R9)  GET NEXT COMPARE COLUMN             79230
         LA    R15,67        SET MAX COLUMNS TO SCAN             79230
         SR    R15,R9        NO. OF COLUMNS TO SCAN-1            79230
         BM    TAPEGET       DONE WITH THIS CARD                 79230
         EX    R15,SCANTRT   LOOK FOR SIGNIFICANT CHARACTER      79230
         BZ    TAPEGET       NONE; NEXT CARD                     79230
         LR    R9,R1         GET HIT COLUMN                      79230
         SR    R9,R5         GET EQUIVALENT OFFSET               79230
         CLC   =C'''&&HASP''',0(R1)  HASP DEPENDENCY ?           79230
         BE    DISPHIT       YES; SET ASSEMBLY                   79230
CLCLOCAL CLC   LOCALE,0(R1)  OR LOCAL= ?                         79230
         BE    DISPHIT                                           79230
         CLC   =C'''&&CWEXEC''',0(R1)  OR EXECUTOR ?             79230
         BNE   DISPTRT       NO; TRY AGAIN                       79230
DISPHIT  NI    FLAG,255-TESTASM  SKIP ADDITIONAL SEARCHES        79230
         MVC   ASMMEM,MEMBER  COPY NAME                          79230
         PRTV  ASMMSG        PRINT MESSAGE                       82235
         B     TAPEGET       GET ANOTHER CARD                    79230
SCANTRT  TRT   0(0,R14),QUOTSCAN  LOOK FOR QUOTED STRING         79230
         SPACE 1
DISPREJ  TM    FLAG,REJECT   COPY REJECTED MEMBER ?
         BZ    TAPEGET       NO                                  79230
         PRTF  0(R5),80,CC=NO,DEV=5  COPY TO REJECT FILE         82235
         B     DISPLIST
         SPACE 2
CARDCHEK LA    R14,70        NUMBER OF COLUMNS TO SCAN
         LA    R15,2(,R5)    START COLUMN
         MVC   MEMBER,BLANKS RESET MEMBER NAME
         XC    NEWSSI,NEWSSI RESET SSI
CARDCHB1 CLI   0(R15),C' '   BLANK ?
         BE    CARDCHB2      YES; NOW FIND NON-BLANK
         LA    R15,1(,R15)
         BCT   R14,CARDCHB1  TRY ANOTHER
         B     CARDCHNO      NOT ADD CARD
CARDCHB2 CLI   0(R15),C' '   NON-BLANK ?
         BNE   CARDCHB3      YES
         LA    R15,1(,R15)
         BCT   R14,CARDCHB2
         B     CARDCHNO
CARDCHB3 CLC   =C'ADD ',0(R15)  ADD CARD ?
         BE    CARDCHAD      YES
         CLC   =C'REPL',0(R15)  REPLACE CARD ?
         BNE   CARDCHNO      INVALID CARD
CARDCHAD LA    R15,4(R15,0)  SKIP TEXT
         SH    R14,*-2       AND ADJUST LENGTH
         BNP   CARDCHNO
         NI    FLAG,255-COPY-REJECT-TESTASM  RESET COPY FLAGS    79230
CARDCHLP CLC   =C'SSI=',0(R15)  CHECK FOR SSI
         BE    CARDCHSS      PROCESS SSI
         CLC   =C'NAME=',0(R15)  LOOK FOR NAME
         BE    CARDCHNM
         LA    R15,1(,R15)
         BCT   R14,CARDCHLP
         CLI   MEMBER,C' '   IF A MEMBER NAME WAS FOUND,
         BH    CARDBSSI       BUT NO SSI, CONTINUE
CARDCHNO MVC   VBMEM,CURRMEM
         MVC   VBCARD,0(R5)
         PRTV  VBADCARD                                          82235
         CLI   MEMBER,C' '   IF A MEMBER NAME WAS FOUND,
         BH    CARDBSSI       BUT NO SSI, CONTINUE
         B     DISPOSE       TREAT AS REGULAR DATA
CARDCHNM LA    R1,MEMBER
         LA    R15,5(R15,0)  SKIP TO FIRST BYTE
         SH    R14,*-2       ADJUST REMAINING LENGTH
         BNP   CARDCHNO      ERROR
CARDCHNL CLI   0(R15),C' '   END OF NAME ?
         BE    CARDCHAL      SEE IF ALL
         CLI   0(R15),C','    ALTERNATE END ?
         BE    CARDCHAL      YES
         CLI   MEMBER+7,C' '  MORE THAN 8 BYTES ?
         BH    CARDCHNO      YES; REJECT
         MVC   0(1,R1),0(R15)  COPY
         LA    R1,1(,R1)
         LA    R15,1(,R15)
         BCT   R14,CARDCHNL
         B     CARDCHNO
CARDCHSS LA    R1,NEWSSI
         LA    R15,4(R15,0)  SKIP TO FIRST BYTE
         SH    R14,*-2
         BNP   CARDCHNO
CARDCHSL CLI   0(R15),C' '   END ?
         BE    CARDCHAL
         CLI   0(R15),C','
         BE    CARDCHAL
         CLI   NEWSSI+7,0    MORE THAN 8 BYTES ?
         BNE   CARDCHNO      YES; ERROR
         CLI   0(R15),C'A'   VALID HEX ?
         BL    CARDCHNO      NO
         CLI   0(R15),C'F'   VALID ?
         BNH   CARDCHSC      YES; COPY
         CLI   0(R15),C'0'   NUMERIC ?
         BL    CARDCHNO
         CLI   0(R15),C'9'
         BH    CARDCHNO
CARDCHSC MVC   0(1,R1),0(R15)
         LA    R1,1(,R1)
         LA    R15,1(,R15)
         BCT   R14,CARDCHSL
         B     CARDCHNO
CARDCHAL CLI   MEMBER,C' '   MEMBER NAME GOTTEN ?
         BNH   CARDCHLP      NO; GET MORE
         CLI   NEWSSI,0      ANY SSI ?
         BE    CARDCHLP      NO; KEEP IT UP
         CLI   NEWSSI+7,0    ALL THERE ?
         BNE   CARDSSIM      YES; PROCESS
CARDBSSI TM    PASS,PASSOBJ+PASSMAC  FUNNY SSI LIBRARIES ?
         BNZ   CARDSSIM      YES; SKIP ERROR MESSAGE
         CLC   =C'OPTION',MEMBER  OPTIONS, OPTIONDF, ETC ?       81054
         BE    CARDSSIM      YES; SKIP MESSAGE                   81054
CARDSSIB MVC   VBSSMEM,MEMBER  IDENTIFY BAD MEMBER               81054
         PRTV  VBSSI                                             82235
CARDSSIM CLC   =C'OPTION',MEMBER  OPTIONS MEMBER ?               81054
         BNE   *+10                                              81054
         MVC   NEWSSI(8),=8C'0'   FAKE SSI AS ALL ZERO           81054
         PACK  DB(5),NEWSSI(9)  PACK SSI                         81054
         UNPK  UPD(9),DB(5)  UNPACK IT
         TR    UPD(8),HEXTRTAB-C'0'  CLEAN IT UP
         MVC   NEWSSI,UPD    AND COPY IT BACK
         MVC   MEMSSI,UPD     ALSO TO MESSAGE
         MVC   CURRMEM,MEMBER  PROPAGATE MEMBER NAME
         MVC   MEMMEM,MEMBER    ALSO TO BLDL
         MVC   MEMSTAT(L'MEMSTAT+1+L'MEMNOTE),MEMSTAT-1  BLANK
         XC    MEMTTR(10),MEMTTR  CLEAR BLDL RESPONSE
         BLDL  SOURCE,BLDLLIST  LOCATE THE MEMBER
         BXLE  R15,R15,CARDCOMP  FOUND
         MVC   MEMSTAT(9),=CL9'NOT FOUND'  SHOW NOT FOUND
         MVC   MEMNOTE(13),=CL13'WILL BE ADDED'
         B     CARDCADD      GO TO ADD MEMBER
CARDCOMP MVC   MEMFLAG(BLDLLISN-MEMFLAG-2),MEMFLAG+2 KILL BLDL   82235
         LA    R1,MEMBER     POINT TO MEMBER                     82235
         ST    R1,DEBXLE     SET FOR CALL                        82235
         MVI   PDRECFM,DCBRECF  SET RECFM=F                      82235
         SERVCALL PDSDE,DEBXLE  EXTRACT DATE AND SSI             82235
         LR    R15,R0        COPY EXTENDED WORK AREA             82235
         MVC   PDRALIS(PDRBLNK),0(R15)  COPY RETURN DATA         82235
         NI    MEMFLAG,X'80'  RESET FLAG                         82235
         XC    MEMDATA(4),MEMDATA  CLEAR SSI                     82235
         TM    PDFLAGS,PDFSSI  SSI PRESENT ?                     82235
         BZ    CARDCMST      NO ?                                82235
         OI    PDEFLAG,2     SHOW SSI PRESENT                    82235
         CLI   PDRDATE,C' '  DATE FORMAT ?                       82235
         BH    CARDCMDT      YES; DO DATE                        82235
         PACK  UPD(5),PDRSSI(9)  PACK RETURNED SSI               82235
         MVC   MEMDATA(4),UPD    COPY IT BACK                    82235
         TM    PASS,PASSSORE+PASSHELP+PASSMODS  GOOD SSI REQUIRED ?
         BNZ   CARDCMST      NO; USE AS IS                       82235
         B     CARDCMSX      ELSE KILL IT                        82235
CARDCMDT PACK  UPD(5),PDRDATE(6)  PACK RETURNED DATE             82235
         MVC   MEMDATA(4),UPD  MOVE BACK TO D.E.                 82235
         B     CARDCMST      AND PROCESS IT                      82235
CARDCMSX XC    MEMDATA,MEMDATA  ELSE CLEAR IT AGAIN
CARDCMST UNPK  UPD(9),MEMDATA(5)
         TR    UPD(8),HEXTRTAB-C'0'  MAKE PRINTABLE
         MVC   OLDSSI,UPD
         MVC   MEMDSSI,UPD
         MVC   MEMDCSSI(3),=C'SSI='  IDENTIFY
         TM    OPTS,DIST     WAS A DISTRIBUTION DATE FOUND ?
         BZ    CARDCOLD      NO
         TM    PASS,PASSSORE+PASSHELP+PASSMODS  SSI VALID ?
         BZ    CARDCOLD      NO
         CLC   DISTSSI(4),MEMDATA   IS SSI REASONABLE ?
         BL    CARDREJ       NO; ASSUME UPDATED BY USER
CARDCOLD CLC   DB(4),MEMDATA  CHECK SSI AGAINST CURRENT
         BE    CARDKEPT      EQUAL - NO CHANGE
         CLC   =C'OPTION',MEMBER  OPTION DEFINITION ?            81056
         BE    CARDREJ       YES; REJECT IT UNCONDITIONALLY      81056
         CLC   DB(4),MEMDATA  RESTORE CONDITION CODE             81056
         BH    CARDCREP      HIGH - REPLACE MEMBER
         TM    PASS,PASSOBJ+PASSMAC  SSI NOT QUITE SIGNIFICANT ?
         BNZ   CARDCREP      YES; REPLACE (ANYWAY?)
CARDREJ  MVC  MEMNOTE(38),=CL38'CHANGED BY USER, COPIED TO REJECT FILE'
         CLI   REJECTS+3,C'-'  AVAILABLE ?                       82235
         BNE   CARDREJP      YES                                 82235
         MVC   MEMNOTE+17(27),=C'BUT REJECT FILE UNAVAILABLE'
CARDREJP OI    FLAG,REJECT+ANYREJ  INDICATE REJECTED
         PRTV  MEMMSG        PRINT WARNING                       82235
         LA    R1,1
         AH    R1,REJ#
         STH   R1,REJ#       MAKE REJECT COUNT
         B     CARDTASM      AND GO COPY TO REJECT               79230
CARDCREP MVC   MEMNOTE(16),=CL16'WILL BE REPLACED'
CARDCADD OI    FLAG,COPY+ANY  SHOW TO BE COPIED
         PRTV  MEMMSG        PRINT MESSAGE                       82235
         LA    R1,1
         AH    R1,ADD#
         STH   R1,ADD#       MAKE ADD/REPLACE COUNT
         PRTLIST UPLHEAD,TITLE=3,DEV=3  MAKE SUB-HEADER          82235
         TM    PASS,PASSSORE  DOING SOURCE PASS ?
         BZ    CARDTASM      NO; JUST COPY                       79230
         CLC   =C'$ ',MEMBER  DISTRIBUTION DATE MEMBER ?
         BNE   CARDCADY      NO
         MVC   0(28,R5),=C'./ ADD NAME=$$,SSI=000YYDDD '
         MVC   28(52,R5),27(R5)  BLANK REST OF CARD              79254
         MVC   19(8,R5),NEWSSI  FIX UNTIL UPDATES ALL DONE
CARDCADY TM    OBJECT+DCBOFLGS-IHADCB,DCBOFOPN  OBJECT OPEN ?    81053
         BZ    CARDONDL      NO; SHOW NOT DELETED
         TM    OPTS,TEST     RUNNING IN TEST MODE ?
         BNZ   CARDTASM      YES; SKIP DELETE                    79230
         STOW  OBJECT,MEMBER,D  DELETE OBJECT DECK
         CH    R15,=H'8'     CHECK RETURN
         BE    CARDTASM      NOT FOUND - IGNORE                  79230
         PRTV  OBJGONE       SHOW DELETED                        82235
         B     CARDTASM                                          79230
CARDONDL TM    OPTS,PDS      SPECIAL RUN ?                       81053
         BNZ   CARDTASM      YES; SKIP MESSAGE                   81053
         PRTV  OBJKEPT       SHOW UNABLE TO DELETE               82235
CARDTASM TM    PASS,PASSSORE+PASSMODS  DOING SOURCE ?            79230
         BZ    DISPOSE       NO; JUST PROCESS                    79230
         TM    PASS,PASSSORE  IS IT SOURCE PASS ?                79230
         BZ    CARDTAST      NO; SET CHECK                       79230
         CLC   MEMBER(2),=C'EXTRACTS' ASSEMBLY (EX...) MODULE ?  79230
         BNE   DISPOSE       NO; JUST COPY                       79230
         CLI   MEMBER+2,C'G'  GEN MACRO ?                        79232
         BE    DISPOSE       YES; SKIP                           79232
         CLI   MEMBER+2,C'H'  NORMAL MODULE ?                    79232
         BNE   CARDTASH      NO; JUST CHECK ONE MORE             79232
         CLI   MEMBER+3,C'B'  SPECIAL MAPPING MACRO ?            79232
         BE    DISPOSE       YES; NO ASSEMBLY                    79232
         CLI   MEMBER+3,C'M'  NORMAL MAPPING MACRO ?             79232
         BE    DISPOSE       DITTO; NO ASM                       79232
         LA    R14,MEMBER+3  POINT TO FIRST VARIABLE POSITION    79232
         LA    R15,5         LOOK AT 5 BYTES                     79232
CARDTASL CLI   0(R14),C'$'   IS IT A HASP DEPENDENT MEMBER ?     79232
         BE    CARDTASI      YES; PUT MESSAGE NOW                79232
         LA    R14,1(,R14)                                       79232
         BCT   R15,CARDTASL                                      79232
         LA    R14,LISTASM   GET MEMBERS ALWAYS TO BE DONE       79232
         LA    R15,(LISTASMX-LISTASM)/5  NUMBER OF ENTRIES       79232
CARDTASP CLC   MEMBER+3(5),0(R14)  SPECIAL ?                     79232
         BE    CARDTASI                                          79232
         LA    R14,5(,R14)                                       79232
         BCT   R15,CARDTASP                                      79232
CARDTASH CLC   =C'EXTRACTS',MEMBER  BUT SKIP EXTRACTS            79230
         BE    DISPOSE                                           79230
CARDTAST OI    FLAG,TESTASM  SET TEST FLAG                       79230
         B     DISPOSE
CARDTASI MVC   ASMMEM,MEMBER   COPY NAME                         79232
         PRTV  ASMMSG        PRINT MESSAGE                       82235
         B     DISPOSE       NO SPECIAL SCANNING                 79232
CARDKEPT LA    R1,1
         AH    R1,KEPT#
         STH   R1,KEPT#      UPDATE MEMBERS UNCHANGED
         B     TAPEGET       LOOK FOR ANOTHER MEMBER
         SPACE 2
TAPEEOF  OC    COUNTS,COUNTS  ANY MEMBER COUNTS ?
         BZ    TAPEEOF1      NO
         PRTLIST ANYMSG      PRINT SUMMARY                       82235
         B     ENDPASS
TAPEEOF1 PRTV  REJMSG                                            82235
ENDPASS  CLOSE (TAPE,LEAVE,SOURCE)                               82235
         PRTCLOSE DEV=(3,4,5)  CLOSE UPDATE/UPLIST/REJECTS       82235
         TM    PASS,PASSSORE   SOURCE PASS ?
         BZ    ENDPASS1
         CLOSE (OBJECT)      CLOSE OBJECT ALSO
ENDPASS1 XR    R1,R1
         IC    R1,PASS
         SRA   R1,1          MAKE NEXT PASS
         STC   R1,PASS
         IC    R1,PASS#
         LA    R1,1(,R1)
         STC   R1,PASS#      MAKE NEXT PASS NUMBER/INDEX
         TM    OPTS,PDS      RANDOM PDS MODE ?                   81053
         BNZ   EXIT          YES; QUIT                           81053
         TM    PASS,DEFPASS  ANY LEFT TO DO ?
         BNZ   NEXTPASS      YES; CONTINUE
EXIT     SERVTERM ,          CLOSE AND FREE EVERYTHING           82235
         CLOSE (TAPE,LEAVE,SOURCE)                               82235
         PRTCLOSE DEV=(3,4,5)  CLOSE UPDATE/UPLIST/REJECTS       82235
         LH    R9,CCODE      SET RETURN CODE                     82235
         PGMEXIT RC=(R9)     RETURN WITH CODE                    82235
         SPACE 2
OPENDISK XR    R2,R2
         IC    R2,PASS#      GET PASS NUMBER
         SLL   R2,3          MAKE INTO INDEX
         LA    R6,NAMELIST(R2)  GET FILE NAME
         MVC   SUBFILE,0(R6) INTO HEADER
         PRTV  SUBHEAD,TITLE=3  MAKE SUB-HEADER                  82235
         LA    R3,ADDRDCB                                        79232
         LA    R4,4          MAKE NUMBER OF DCBS
OPENFREE L     R5,0(,R3)     GET ADRESS OF NEXT DCB
         CLI   0(R3),0       NORMAL DCB ?                        82235
         BE    OPENFRET      YES; CHECK FOR FREEPOOL             82235
         MVC   2(6,R5),0(R6)  COMPLETE DD NAME                   82235
         B     OPENFROQ      SKIP REST OF CODE                   82235
         USING IHADCB,R5                                         82235
OPENFRET TM    DCBBUFCB+3,1  ANY ?                               82235
         BNZ   OPENFROP      NO; SKIP FREEPOOL
         FREEPOOL (R5)       FREE THE BUFFERS
OPENFROP XC    DCBBLKSI(2),DCBBLKSI   CLEAR                      81053
         MVC   DCBDDNAM+2(6),0(R6)  MAKE NAME                    81053
OPENFROQ LA    R3,4(,R3)     POINT TO NEXT DCB ADDRESS           82235
         BCT   R4,OPENFREE   FREE ALL BEFORE OPENING
         TM    DCBBUFCB+3-IHADCB+TAPE,1  TAPE POOL ?
         BNZ   OPENFRON
         FREEPOOL TAPE       FREE TAPE ALSO
OPENFRON MVC   DCBDDNAM-IHADCB+SOURCE(8),0(R6)  MAKE MAIN NAME
         DEVTYPE SOURCE+DCBDDNAM-IHADCB,DB  LOCATE SOURCE
         BXH   R15,R15,OPENSKIP
         CLI   DB+2,X'20'    DISK FILE ?
         BE    OPENDSK       YES; HOPE FOR THE BEST
OPENSKIP PRTV  BADSORE       SHOW MISSING/INVALID DD             82235
         B     ENDPASS       SKIP THIS PASS
OPENDSK  OPEN  (SOURCE)      OPEN DISK PDS
         LA    R3,ADDRDCB+4                                      79232
         LA    R4,3          SET FOR THREE DCBS
         TM    PASS,PASSOBJ+PASSMAC  MACRO/OBJECT PASS ?         79215
         BZ    OPENDSK2      NEITHER                             79215
         BCTR  R4,0          IGNORE REJECT FILE                  79215
         MVI   REJECTS+3,C'-'  SET 'CLOSED'                      82235
         TM    PASS,PASSOBJ  OBJECT PASS ?                       79215
         BZ    OPENDSK2      NO                                  79215
         BCTR  R4,0          SKIP LIST FILE, ALSO                79215
         MVI   UPLIST+3,C'-'                                     82235
OPENDSK2 L     R5,0(,R3)     GET DCB ADDRESS
         CLI   0(R3),0       NORMAL FILE ?                       82235
         BNE   OPENDOL3      NO                                  82235
         DEVTYPE DCBDDNAM,DB                                     82235
         BXH   R15,R15,OPENDSKM                                  82235
         MVC   AUXDD,DCBDDNAM                                    82235
         OPEN  ((R5),OUTPUT)                                     82235
         TM    DCBOFLGS,DCBOFOPN  OPEN ?                         82235
         BNZ   OPENDSK3      YES                                 82235
         B     OPENDSKM      NO; ISSUE WARNING MESSAGE           82235
OPENDOL3 MVC   AUXDD,0(R5)   COPY DDNAME                         82235
         PRTOPEN (R5),DEV==0(R3),OPT=(NOWTO,DUMMY)  TRY TO OPEN  82235
         CH    R15,=H'8'     OPEN ?                              82235
         BL    OPENDSK3      YES                                 82235
         MVI   3(R5),C'-'    SHOW CLOSED                         82235
OPENDSKM PRTV  AUXMSG        DD MISSING                          82235
OPENDSK3 LA    R3,4(,R3)
         BCT   R4,OPENDSK2
         PRTV  HEADER,TITLE=1,DEV=3  PRINT UPDATE TITLE          82235
         DROP  R5                                                81053
         TM    PASS,PASSSORE  SOURCE PASS ?
         BZR   R9            NO; CONTINUE
         MVC   AUXDD,OBJECT+DCBDDNAM-IHADCB                      81053
         DEVTYPE OBJECT+DCBDDNAM-IHADCB,DB  TEST FOR OBJECT      81053
         BXH   R15,R15,OPENOBJN  'OBJECT' DD MISSING             81053
         CLI   DB+2,X'20'    DISK ?                              81053
         BNE   OPENOBJN      NO; TOO BAD                         81053
         OPEN  (OBJECT,OUTPUT)
OPENOBJN TM    OBJECT+DCBOFLGS-IHADCB,DCBOFOPN  OPEN ?           81053
         BNZ   OPENDSK4      YES
         PRTV  AUXMSG                                            82235
OPENDSK4 MVC   MEMBER,=CL8'$'  LOOK FOR PREVIOUS UPDATE
         MVI   MEMTTR,C' '   SHOW MEMBER NOT PRESENT             79232
         MVI   MEMTTR+BLDLLISX,C' '  DITTO FOR OPTIONS           79232
         MVI   BLDLLIST+1,2   SET FOR TWO MEMBERS                79232
         BLDL  SOURCE,BLDLLIST
         MVI   BLDLLIST+1,1  RESET FOR ONE MEMBER ONLY           79232
         CLI   MEMTTR,C' '   WAS $ MEMBER FOUND ?                79232
         BE    OPENDNIN      NO; ISSUE WARNING                   79232
         MVC   MEMFLAG(BLDLLISN-MEMFLAG-2),MEMFLAG+2 KILL BLDL   82235
         LA    R1,MEMBER     POINT TO MEMBER                     82235
         ST    R1,DEBXLE     SET FOR CALL                        82235
         MVI   PDRECFM,DCBRECF  SET RECFM=F                      82235
         SERVCALL PDSDE,DEBXLE  EXTRACT DATE AND SSI             82235
         LR    R15,R0        COPY EXTENDED WORK AREA             82235
         MVC   PDRALIS(PDRBLNK),0(R15)  COPY RETURN DATA         82235
         CLI   PDRDATE,C' '  DATE FORMAT ?                       82235
         BNH   OPENDNIN      NO                                  82235
         PACK  DB(5),PDRDATE(6)  PACK RETURNED DATE              82235
         MVC   MEMDATA(4),DB     COPY IT BACK                    82235
         OI    OPTS,DIST     SHOW DISTRIBUTION DATE AVAILABLE
         UNPK  UPD(9),MEMDATA(5)
         TR    UPD(8),HEXTRTAB-C'0'
         MVC   DISTSSI,MEMDATA   SAVE OLD SSI FOR COMPARES
         MVC   DIMSSI(2),UPD+3         SHOW YEAR
         MVC   DIMSSI+3(3),UPD+5        AND JULIAN DAY
         PRTV  DIMMSG        INDICATE OLD DATA                   82235
         B     OPENDFOP
OPENDNIN PRTV  DINMSG                                            82235
OPENDFOP CLI   MEMOPT+8,C' '   WAS OPTIONS MEMBER FOUND ?        79230
         BE    DISKEOF       NO; SKIP THIS                       79232
         FIND  SOURCE,MEMTTR+BLDLLISX,C  POINT TO IT             79230
         GETBUF SOURCE,R6    GET A BUFFER                        79230
OPENDFOR LH    R4,DCBBLKSI-IHADCB+SOURCE  GET BLOCKSIZE          79230
         LR    R3,R6         RELOAD ONE AND ONLY BUFFER ADDRESS  79232
         READ  RECB,SF,SOURCE,(R3)  READ A BLOCK                 79230
         CHECK RECB          WAIT FOR COMPLETION                 79230
         LA    R5,0(R3,R4)   GET LAST CARD+1                     79230
         LA    R4,80         NOW SET CARD LENGTH                 79232
         SR    R5,R4         GET LAST CARD                       79230
OPENDCLC CLC   =C'&&LOCAL',0(R3)  LOCAL SETC ?                   79230
         BE    OPENDLCL      YES; SAVE IT                        79230
         BXLE  R3,R4,OPENDCLC  ELSE LOOK AGAIN                   79230
         B     OPENDFOR      TRY NEXT BLOCK                      79230
OPENDLCL CLI   0(R3),C''''   START OF LOCAL STRING ?             79230
         BE    OPENDLCC      YES                                 79230
         LA    R3,1(,R3)                                         79230
         BCT   R4,OPENDLCL   ELSE TRY AGAIN                      79230
         B     DISKEOF       SKIP IT IF CARD IS BAD              79230
OPENDLCC MVC   LOCALE,0(R3)  MOVE ENTIRE STRING                  79230
         LA    R4,9          SCAN UP TO 9                        79230
         LA    R3,1(,R3)                                         79230
OPENDLCS CLI   0(R3),C''''   TRAILING QUOTE ?                    79230
         BE    OPENDLCQ      YES                                 79230
         LA    R3,1(,R3)                                         79230
         BCT   R4,OPENDLCS                                       79230
         LA    R4,1          SET FOR MAX LENGTH                  87348
OPENDLCQ LA    R3,10         SET MAX LENGTH                      79230
         SR    R3,R4         GET TRUE LENGTH-1                   79230
         STC   R3,CLCLOCAL+1   PUT LENGTH IN TEST                79230
DISKEOF  BR    R9            FOR NOW
         SPACE 2
OPENTAPE MVC   TAPE(PATTAPE-TAPE),PATTAPE  REFRESH TAPE DCB      81053
         PRTSPACE 1          MAKE LISTING PRETTY                 82235
         XR    R2,R2
         IC    R2,PASS#
         MH    R2,=AL2(LENJFCB)
         LA    R2,LISTJFCB(R2)  GET PROPER TAPE ENTRY
         RDJFCB (TAPE)
         BXH   R15,R15,TAPEBAD
         TM    OPTS,PDS      NON-EXHIBIT MODE ?                  81053
         BNZ   NJFCBMOD      YES; BYPASS JFCB CLOBBER            81053
         MVC   JFCBDSNM(LENJFCB-2),2(R2)  MAKE NEW DSNAME
         MVC   JFCBFLSQ,0(R2)   SET FILE NUMBER
NJFCBMOD OI    JFCBTSDM,JFCVSL+JFCNWRIT                          81053
         OPEN  (TAPE,(INPUT,REREAD)),TYPE=J  OPEN CORRECT FILE
         TM    TAPE+DCBOFLGS-IHADCB,DCBOFOPN                     81053
         BNZR  R9            OK; GO
TAPEBAD  PRTV  BADTAPE                                           82235
GUTEN    SERVTERM ,          CLOSE AND FREE                      82235
         ABEND 666                                               82235
         SPACE 2                                                 81053
         USING TAPEDCBX,R15                                      82235
         USING IHADCB,R1                                         82235
TAPEDCBX TM    DCBRECFM,DCBRECU   DCB RECORD FORMAT SPECIFIED ?  81053
         BO    TAPEBAD       U - NOT SUPPORTED                   81053
         BNZ   *+8           NON-ZERO; CHECK FURTHER             81053
         OI    DCBRECFM,DCBRECF+DCBRECBR  SET FIXED BLOCKED      81053
         TM    DCBRECFM,DCBRECV  VARIABLE ?                      81053
         BNZ   TAPEDCBV      YES; TREAT AS UNLOADED IEBCOPY      81053
         OC    DCBLRECL,DCBLRECL  ANY RECORD LENGTH ?            81053
         BNZ   *+10          YES                                 81053
         MVC   DCBLRECL,=H'80'  DEFAULT TO 80 BYTES              81053
         CLC   =H'80',DCBLRECL  IS IT 80 ?                       81053
         BNE   TAPEBAD       NO                                  81053
         OC    DCBBLKSI,DCBBLKSI  ANY BLOCKSIZE ?                81053
         BNZR  R14           YES; RETURN
         MVC   DCBBLKSI,=H'14400'  DEFAULT SIZE
         BR    R14           RETURN
TAPEDCBV OI    FLAG,IEBCOPY  SET FOR IEBCOPY UNLOAD FORMAT       81053
         MVI   CPFLAG,0      CLEAR IEBCOPY PROCESSING FLAG       81053
         TM    DCBRECFM,DCBRECSB  SPANNED ?                      81053
         BZ    *+8           NO                                  81053
         OI    DCBBFTEK,DCBBFTA  ENABLE VBS INTERFACE            81053
         LH    R2,DCBBLKSI   GET BLOCK SIZE                      81053
         LTR   R2,R2         SPECIFIED ?                         81053
         BP    *+8           YES                                 81053
         LH    R2,=H'32760'  ELSE USE DEFAULT                    81053
         LH    R3,DCBLRECL   GET RECORD LENGTH                   81053
         LTR   R3,R3                                             81053
         BMR   R14                                               81053
         SH    R2,=H'4'                                          81053
         CR    R3,R2                                             81053
         BNLR  R14                                               81053
         STH   R2,DCBLRECL   FIX IEBCOPY LRECL ERROR             81053
         BR    R14                                               81053
         DROP  R1,R15
         EJECT
DB       DC    D'0'          WORK WORD
HEXTRTAB DC    C'0123456789ABCDEF'   HEX TRANSLATE
GETMAD   DC    A(448*PDLEN,1024*PDLEN)  SIZE FOR DIRECTORY       81053
PDEBXLE  DC    A(0,PDLEN,0)                                      81053
PDEINIT  DC    A(0,PDLEN,0)                                      81053
BLOKBXLE DC    A(0,80,0)     DEBLOCK POINTERS                    81053
CURBLOCK DC    A(0,0)        LENGTH/ADDRESS OF CURRENT INPUT BLOCK
ADRBLOCK DC    A(0)          ADDRESS OF CURRENT BLOCK            81053
ZEROES   DC    XL8'0'        HEX ZEROES                          81053
EXTENTS  DC    16XL16'0'     DEB EXTENTS                         81053
ADDRDCB  DC    A(SOURCE),AL1(4),AL3(UPDATE),AL1(8),AL3(UPLIST)   82235
         DC    AL1(16),AL3(REJECTS)  DCB/WORK POINTERS           82235
CURRMEM  DC    CL8' '        CURRENT TAPE MEMBER
NEWSSI   DC    CL8' '        CURRENT TAPE SSI
OLDSSI   DC    CL8' '        CURRENT DISK SSI (UNPACKED)
DISTSSI  DC    XL4'0'        SSI OF PREVIOUS DISTRIBUTION TAPE
CCODE    DC    H'0'          COMPLETION CODE
TRKPCYL  DC    H'19'         TRACKS PER CYLINDER                 81053
NMEXT    DC    H'1'          NUMBER OF DEB EXTENTS PRESENT       81053
COUNTS   DC    0XL6'0'       MEMBER COUNT FIELDS
ADD#     DC    H'0'          NUMBER TO BE ADDED
KEPT#    DC    H'0'          NUMBER UNCHANGED
REJ#     DC    H'0'          NUMBER REJECTED
TTR      DC    XL3'0'        TTR OF CURRENT BLOCK                81053
CPFLAG   DC    X'00'         PROCESSING FLAG                     81053
CPEOF    EQU   X'80'         END-FILE PROCESSED                  81053
CPCARD   EQU   X'08'         CARD AVAILABLE                      81053
CPBLOK   EQU   X'04'         BLOCK AVAILABLE                     81053
PASS     DC    X'0'          PASSFLAG
PASSSORE EQU   X'80'           SOURCE LIBRARY
PASSOBJ  EQU   X'40'           OBJECT LIBRARY
PASSMAC  EQU   X'20'           MACRO LIBRARY
PASSHELP EQU   X'10'           HELP LIBRARY
PASSMODS EQU   X'08'           OPTIONAL SYSTEM MODS
DEFPASS  EQU   PASSSORE+PASSOBJ+PASSMAC+PASSHELP+PASSMODS
FLAG     DC    X'0'          STATUS FLAG
ANY      EQU   X'80'           AT LEAST ONE MEMBER COPIED
ANYREJ   EQU   X'40'           AT LEAST ONE MEMBER REJECTED
COPY     EQU   X'20'           COPY CURRENT TAPE MEMBER
REJECT   EQU   X'10'           MEMBER REJECTED
TESTASM  EQU   X'08'         CHECK FOR ASSEMBLY                  79230
IEBCOPY  EQU   X'04'         TAPE IS IEBCOPY UNLOADED FORMAT     81053
COPYCON  EQU   X'02'         ./ FOUND IN DATA                    81053
PASS#    DC    X'0'          NUMBER OF THIS PASS (ALSO INDEX)
OPTS     DC    X'0'          OTHER OPTIONS
DIST     EQU   X'80'           DISTRIBUTION SSI FOUND
TEST     EQU   X'40'         TEST MODE FLAG
STOW     EQU   X'20'         STOW DIRECTLY - NO UPDTE PROCESSING 81053
PDS      EQU   X'10'         PROCESS A NON-EXHIBIT PDS           81053
LOCALE   DC    CL10' '       SAVE AREA FOR &LOCAL VALUE (QUOTED) 79230
         SPACE 1
SYSPRINT PRTWORK SYSPRINT,TITLE=5                                82235
TAPE     DCB   DDNAME=TAPE,DSORG=PS,MACRF=(GL),EODAD=TAPEEOF,          *
               EXLST=TAPEEX                                      81053
PATTAPE  DCB   DDNAME=TAPE,DSORG=PS,MACRF=(GL),EODAD=TAPEEOF,          *
               EXLST=TAPEEX                                      81053
SOURCE   DCB   DDNAME=SOURCE,DSORG=PO,MACRF=R,BUFNO=1,NCP=1,           *
               RECFM=FB,LRECL=80,EODAD=DISKEOF
OBJECT   DCB   DDNAME=OBJECT,DSORG=PO,MACRF=(R,W),BUFNO=1,NCP=1,       *
               RECFM=FB,LRECL=80,EODAD=DISKEOF
UPDATE   PUNWORK UPSOURCE                                        82235
UPLIST   PRTWORK PRSOURCE,TITLE=5                                82235
REJECTS  PUNWORK REJECTS                                         82235
TAPEEX   DC    X'05',AL3(TAPEDCBX),X'87',AL3(INFMJFCB) TAPE JFCB EXIT
JFCB     IEFJFCBN ,          EXPAND JFCB IN-LINE
         SPACE 2
         LTORG ,
         SPACE 2
HEADER   VCON '#                  EXHIBIT DISTRIBUTION UPDATE PROGRAM'
UPLHEAD  FDPRT MEMMEM,NL                                         82235
         FDPRT 'UPDATE LISTING',PAD                              82235
         FDOPT SBA=(*,-20)                                       82235
         FDPRT 'MEMBER PAGE S####'                               82235
         FDPRT *END                                              82235
VBADCARD VCON  ' INVALID CARD FOUND IN MEMBER ',END=BADCARX      82235
VBMEM    DC    CL8' ',C'; TEXT IS '
VBCARD   DC    CL80' '
         VCON  ,                                                 82235
MEMMSG   VCON  ' ',END=MEMMSGX                                   82235
         DC    C'MEMBER '
MEMMEM   DC    CL8' ',C' SSI '
MEMSSI   DC    CL8' ',C'    DISK MEMBER '
MEMSTAT  DC    0CL12'NOT FOUND'
MEMDCSSI DC    C'SSI',C' '
MEMDSSI  DC    CL8' ',C' '
MEMNOTE  DC    CL50' '
         VCON  ,                                                 82235
BLDLLIST DC    0A(0),Y(1,BLDLLISX)
MEMBER   DC    CL8' '        CURRENT DISK MEMBER
MEMTTR   DC    CL3' '          MEMBER TTR
         DC    0XL2'0'       BLDL CONCAT/LIBRARY FLAGS (CREAMED) 82235
MEMFLAG  DC    X'0'          FLAG/LENGTH/ALIAS
MEMDATA  DC    XL4'0'        SSI
         DC    XL62'0'       JUST IN CASE                        82235
BLDLLISN EQU   *                                                 82235
BLDLLISX EQU   *-MEMBER      LENGTH OF BLDL DATA
MEMOPT   DC    CL(BLDLLISX)'OPTIONS'  SECOND MEMBER SEARCH       79232
OBJKEPT  VCON  ' ***** UNABLE TO DELETE OBJECT DECK *****'       82235
OBJGONE  VCON  '       OBJECT DECK HAS BEEN DELETED'             82235
VBSSI    VCON  '0***** MISSING OR INVALID SSI FOR MEMBER ',END=VBSSIX
VBSSMEM  DC    CL8' '
         VCON  ,                                                 82235
SUBHEAD  VCON  '              ',END=SUBHEADX                     82235
SUBFILE  DC    CL6' ',C' DATASET UPDATE PROCESSING'
         VCON  ,                                                 82235
         SPACE 1                                                 82235
BADTPSSI FDPRT '***** TAPE MEMBER',NL                            82235
         FDPRT 0(R7),8,PAD                                       82235
         FDPRT 'SSI',PAD                                         82235
         FDTM  11(R7),2,BZ=BADTPSSM                              82235
         FDPRT 12(R7),4,HEX,PAD                                  82235
         FDPRT 'INVALID'                                         82235
         FDPRT *END                                              82235
BADTPSSM FDPRT 'MISSING'                                         82235
         FDPRT *END                                              82235
         SPACE 1
NAMELIST DC    CL8'SOURCE'
         DC    CL8'OBJECT'
         DC    CL8'SYSMAC'                                       90303
         DC    CL8'HELP'
         DC    CL8'ESPSRC'                                       90303
         DC    CL8'ESPMAC'                                       90303
LISTJFCB DC    H'1',CL8'SOURCE'
LENJFCB  EQU   *-LISTJFCB
         DC    H'2',CL8'OBJECT'
         DC    H'5',CL8'SYSMAC'                                  90303
         DC    H'6',CL8'EXHELP'
         DC    H'7',CL8'ESPSRC'                                  90303
         DC    H'8',CL8'ESPMAC'                                  90303
         SPACE 1                                                 79232
LISTASM  DC    C'CCDUMCCENQCCSMFCCTASCCUNICXDEB'                 79232
         DC    C'DDA00DDQ00DDR00DIR00DOF00'                      79232
LISTASMX EQU   *                                                 79232
         SPACE 1                                                 79232
ANYMSG   FDOPT NL,CC=C'0'                                        82235
         FDPRT ADD#,I,RADJ,PAD,LEN=18                            82235
         FDPRT 'MEMBERS TO BE ADDED/REPLACED;'                   82235
         FDPRT REJ#,I,RADJ,PAD,LEN=6                             82235
         FDPRT 'MEMBERS MODIFIED BY USER;'                       82235
         FDPRT KEPT#,I,RADJ,PAD,LEN=6                            82235
         FDPRT 'MEMBERS NOT CHANGED.'                            82235
         FDPRT *END                                              82235
REJMSG   VCON  '0*****  NO MEMBERS PROCESSED IN THIS FILE'       82235
BADSORE  VCON  '0***** DISK FILE DD MISSING OR BAD'              82235
AUXMSG   VCON  ' ***** DD CARD FOR ',END=AUXMSGX                 82235
AUXDD    DC    CL8' ',C' MISSING/INVALID; FUNCTION BYPASSED'
         VCON  ,                                                 82235
DIMMSG   VCON  '   PREVIOUS COMPLETED UPDATE RUN FROM',END=DIMMSGX
         DC    C' TAPE DATED '
DIMSSI   DC    C'YY.DDD'
         VCON  ,                                                 82235
DINMSG   VCON  ' ***** PREVIOUS DISTRIBUTION DATE UNAVAILABLE; SOME MEM*
               BERS WITH LOCAL CHANGES MAY GET REPLACED'         82235
BADTAPE  VCON  '0*****  UNABLE TO OPEN TAPE DD'                  82235
ASMMSG   VCON  END=ASMMSGE                                       82235
         DC    CL95' ',C'MEMBER '                                82235
ASMMEM   DC    CL8' ',C' SHOULD BE ASSEMBLED'                    79230
         VCON  ,                                                 82235
MSGCOPYC VCON  ' *** ./ CARD(S) IN DATA CONVERTED TO @/ - MEMBER MUST B*
               E EDITED BY USER AFTER UPDATE'                    82235
MSGCOPYB VCON  '0IEBCOPY LOAD - BLOCK IN ERROR OR OUT OF SEQUENCE'
MEMNONE  VCON  '0IEBCOPY LOAD - NO MEMBERS READ'                 82235
MEMOFLOW VCON '0IEBCOPY LOAD - MEMBER TABLE OVERFLOW - REASSEMBLE'
MEMNTFND VCON '0IEBCOPY LOAD - MEMBER FOR CURRENT BLOCK NOT FOUND'
MEMRENIT VCON  '  TAPE CONCATENATION ASSUMED'                    82235
FAKECARD DC    CL80' '                                           81053
         ORG   FAKECARD                                          81053
         DC    C'./       ADD   NAME='                           81053
ADDMEM   DC    CL8' '                                            81053
         DC    C',SSI=XXXXXXXX'                                  81053
BLANKS   DC    CL40' '
         ORG   ,                                                 81053
         SPACE 1                                                 79230
QUOTSCAN DC    256X'0'       TRT TABLE                           79230
         ORG   QUOTSCAN+C''''                                    79230
         DC    C''''         SEARCH FOR QUOTE                    79230
         ORG   ,                                                 79230
         SPACE 2
SAVE     DSECT ,             SAVE/WORK AREA CONTINUATION         82235
@SERVICE DS    A             SERVICE ROUTINE                     82235
@PRINTER DS    A             PRINTER SERVICES                    82235
         PRINT &PRTMAC                                           82235
DEBXLE   SERVPDS PFX=PD,DSECT=NO,RETURN=YES,VER=1 DE WORK AREA  GP12042
         SPACE 1                                                 82235
UPD      DS    D,D           WORK AREA                           82235
SAVEEND  EQU   *             END OF DYNAMIC AREA                 82235
         SPACE 2                                                 81053
PDENTRY  DSECT ,             MAPPING OF SHORT DIRECTORY BLOCK    81053
PDEMEM   DS    CL8           MEMBER NAME                         81053
PDETTR   DS    XL3           TTR                                 81053
PDEFLAG  DS    X             FLAG/LENGTH BYTE                    81053
PDEDATA  DS    XL4           SSI                                 81053
PDLEN    EQU   *-PDENTRY       LENGTH OF ONE ENTRY               81053
         SPACE 2                                                 81053
INREC    DSECT ,             INPUT RECORD MAPPING                81053
RDW      DS    XL2           RECORD LENGTH                       81053
RDWFG    DS    XL2           FLAGS                               81053
INDC     DS    X             INDICATOR BYTE                      81053
INDCERR  EQU   X'80'         ERROR IN COPY                       81053
UNLID    DS    XL3           IEBCOPY ID BYTES                    81053
UDSORG   DS    XL2           DSORG OF ORIGINAL DATASET           81053
UBLKSI   DS    XL2           BLOCKSIZE                           81053
ULRECL   DS    XL2           RECORD LENGTH                       81053
URECFM   DS    X             RECFM                               81053
UKEYLN   DS    X             KEY LENGTH                          81053
UOPTCD   DS    X                                                 81053
         DS    X             RESERVED                            81053
TBLKSI   DS    XL2           TAPE BLOCKSIZE                      81053
UUCBTYPE DS    XL4           SOURCE UCBTYPE                      81053
UBLKMAX  DS    XL4           MAX DEVICE BLOCKSIZE                81053
UCYLMAX  DS    XL2           CYLINDERS PER PACK                  81053
UTRKPCYL DS    XL2           TRACKS PER CYLINDER                 81053
         DS    XL8           OTHER OVERHEAD CONSTANTS            81053
         SPACE 1                                                 81053
         ORG   INDC                                              81053
UNMEXT   DS    X             NUMBER OF EXTENTS                   81053
         DS    XL15          OTHER DEB DATA                      81053
UEXTENTS DS    XL256         DEB EXTENTS                         81053
         SPACE 1                                                 81053
         ORG   INDC                                              81053
UWORD    DS    XL4           CONTROL WORD ?                      81053
UCOUNT   DS    XL5           COUNT FIELD                         81053
UCKLEN   DS    X             KEY LENGTH                          81053
UDATLEN  DS    XL2           DATA LENGTH OF BLOCK                81053
UBLOCK   DS    0X            BLOCK DATA                          81053
UFIXLEN  EQU   *-UWORD       FIXED OVERHEAD                      81053
UPDSKEY  DS    CL8           DIRECTORY BLOCK KEY                 81053
UPDSLEN  DS    XL2           DATA LENGTH                         81053
UPDSENT  DS    0X            DIRECTORY ENTRIES                   81053
         SPACE 2                                                 81053
         SPACE 2                                                 82235
         PRINT &PRTSYS                                           82235
CVTDSECT DSECT ,                                                 82235
         CVT   DSECT=YES                                         82235
         IHACDE ,                                                82235
         DCBD  DEVD=DA,DSORG=PS                                  82235
         END
