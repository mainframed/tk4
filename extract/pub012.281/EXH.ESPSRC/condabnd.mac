CCAB     TITLE 'C O N D A B N D  ***  CONVERT CC INTO ABEND CODE'
         COPY  OPTIONGB
         SPACE 1
         SYSPARM LIST=YES
         SPACE 1
***********************************************************************
*                                                                     *
*                                                                     *
*        COPYRIGHT 1986  EXPERT SYSTEM PROGRAMMING, INC.              *
*                                                                     *
*        COPYRIGHT 2003  EXPERT SYSTEM PROGRAMMING                    *
*                        176 OLD STAGE COACH ROAD                     *
*                        BRADFORD, VT 05033-8844                      *
*                                                                     *
*                    ALL RIGHTS RESERVED                              *
*                                                                     *
***********************************************************************
         SPACE 1
***********************************************************************
*                                                                     *
*        CONDITIONAL DISPOSITION PARAMETERS ON JCL WORK ONLY WITH     *
*        ABENDS, NOT WITH NON-ZERO COMPLETION CODES.                  *
*                                                                     *
*        THIS PROGRAM WILL INVOKE THE PROGRAM THE USER REALLY WISHES  *
*        TO RUN, AND CHANGE ANY NON-ZERO CONDITION CODE TO AN ABEND.  *
*                                                                     *
*        //STEP  EXEC  PGM=CONDABND,PARM='PGMNAME/PGM-PARAMETERS'     *
*        // ... PGM JCL                                               *
*                                                                     *
*        A USER PROGRAM ABEND IS PASSED THROUGH AS IS.                *
*        CONDITION CODES 1-4095 ARE CONVERTED TO USER ABEND 1-4095.   *
         EJECT ,
         PRINT &PRTSOR
CONDABND PGMHEAD ZERO12,BASE=R12,PARM=R11               ADDED ON 86150
         SPACE 1
         MVC   EPLOC,=CL8' '   CLEAR PROGRAM NAME
         LA    R1,NEWPARM    SET DEFAULT PARM POINTER
         ST    R1,@PARMPTR
         OI    @PARMPTR,X'80'  MAKE END OF LIST
         MVC   ATTLIST(ATTPATL),ATTPAT  MOVE ATTACH PATTERN
         LA    R4,0(,R11)    COPY THE PARM POINTER
         LTR   R4,R4         ANY SUPPLIED ?
         BZ    NOPARM
         L     R4,0(,R4)     GET PARM ADDRESS
         LA    R4,0(,R4)     LOAD PARM ADDRESS
         LTR   R4,R4         VALID ?
         BZ    NOPARM
         SLR   R5,R5
         ICM   R5,3,0(R4)    GET PARM LENGTH
         BNP   NOPARM        NONE                                94231
         BCTR  R5,0          DECREASE FOR EXECUTE
         LA    R1,2+1(R4,R5)   SET END OF PARM (IF NO HIT)       94231
         SLR   R2,R2         SET NO SKIP
         EX    R5,EXTRT      LOOK FOR SPECIAL BYTE
         SR    R1,R4         GET LENGTH+2                        94231
         SH    R1,=H'3'      ALLOW FOR LENGTH OF PARM LENGTH, AND EX
         BM    BADPARM       INVALID PARM
         LA    R0,L'EPLOC-1  SET MAXIMUM PROGRAM NAME LENGTH, LESS 1
         CR    R1,R0         LONGER ?                            94231
         BNH   NOTRUNC       NO                                  94231
         SLR   R2,R2         DON'T SKIP SEPARATOR
         LR    R1,R0         USE ONLY MAX                        94231
NOTRUNC  EX    R1,EXMVC                                          94231
         SR    R5,R1         SET NEW PARM LENGTH                 94231
         LA    R1,3(R1,R4)   NEW PARM START                      94231
         BCT   R2,NOSEPAR    SKIP SEPARATOR ?
         LA    R1,1(,R1)     SET PAST SEPARATOR
         BCTR  R5,0          DECREMENT LENGTH FOR SEPARATOR      94231
NOSEPAR  LTR   R5,R5         NEW PARM LENGTH                     94231
         BNP   NULLPARM      SKIP IF RESIDUAL IS NULL STRING
         STH   R5,NEWPARM    STASH NEW LENGTH
         BCTR  R5,0
         EX    R5,EXMVP      MOVE TO MY PARM AREA
NULLPARM B     PGMTEST
EXTRT    TRT   2(0,R4),TRANTAB  LOOK FOR NON-ALPHAMERICS
EXMVC    MVC   EPLOC(0),2(R4)   MOVE PROGRAM NAME
EXMVP    MVC   NEWTEXT(0),0(R1)  MOVE NEW PARM
NOPARM   LTCB  R2,USE=YES    GET THE TCB
         L     R2,TCBTIO     GET THE TIOT
         MVC   EPLOC,8(R2)   MOVE THE STEPNAME
PGMTEST  CLI   EPLOC,C'0'    NUMERIC START ?
         BNL   BADPARM       NO; ERROR
         CLI   EPLOC,C' '    ANY STEPNAME ?
         BH    AROUND        YES; USE IT
BADPARM  WTO   'INVALID PROGRAM NAME (PARM OR STEPNAME)'
         ABEND 0             QUIT THE HARD WAY
         SPACE 1
AROUND   LA    R1,@PARMPTR
         LA    R2,EPLOC
         LA    R3,ATTECB
         ATTACH EPLOC=(R2),ECB=(R3),SF=(E,ATTLIST)
         ST    R1,ATTTCB     SAVE TCB ADDRESS
         WAIT  ECB=ATTECB    WAIT FOR COMPLETION
         DETACH ATTTCB       DETACH SUBTASK
         SLR   R1,R1         IGNORE HIGH BYTE (PROBLEM WITH TMON)
         ICM   R1,7,ATTECB+1  EXECUTION SUCCESSFUL ?             86152
         BZ    EXIT0         YES
ABEND    SLR   R0,R0         CLEAR MFT MESSAGE PTR
         SVC   13            GREETINGS
         SPACE 1
EXIT0    PGMEXIT RC=0        RETURN WITHOUT ERROR               GP03026
         SPACE 1
         DS    0F            FORCE ALIGNMENT
ATTPAT   ATTACH EPLOC=1,ECB=2,SF=L
ATTPATL  EQU   *-ATTPAT      LENGTH OF PATTERN
         SPACE 1
TRANTAB  DC    256AL1(2)     TERMINATING CHARACTERS
*                                                                94231
*   NON-TERMINATING CHARACTERS (PART OF PROGRAM NAME)            94231
*                                                                94231
         TRENT TRANTAB,0,C'$',C'#',C'@'   NATIONAL CHARACTERS    94231
         TRENT ,0,(C'A',9),(C'J',9),(C'S',8),(C'0',10)           94231
*                                                                94231
*   EXPECTED SEPARATOR CHARACTERS                                94231
*                                                                94231
         TRENT ,1,C' ',C'/',C'.',C',',C';',C':',C'=',C'-'        94231
         SPACE 1
SAVE     DSECT ,             SAVE/WORK AREA
ATTECB   DS    F             ATTACH COMPLETION ECB
ATTTCB   DS    F
EPLOC    DS    CL8           ATTACHED PROGRAM NAME
ATTLIST  ATTACH EPLOC=1,SF=L   ATTACH LIST
@PARMPTR DS    A             POINTER TO PARM
NEWPARM  DS    H
NEWTEXT  DS    CL100         SYSTEM LENGTH
SAVEEND  EQU   *                                                GP03026
         SPACE 2
         PRINT &PRTSYS
CVTDSECT DSECT ,
         CVT   DSECT=YES
         IHAPSA ,
         IHACDE ,
         IKJTCB ,
         END   ,
