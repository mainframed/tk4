TABS     TITLE 'T A B L A B E L  ***  PRINT TAB LABELS'
         PUNCH ' ORDER TABLABEL(P) '
         COPY  OPTIONGB      GLOBAL DEFINITIONS
         SPACE 1
         SYSPARM LIST=YES    GLOBAL SETTINGS
         SPACE 1
***********************************************************************
**                                                                   **
**   THIS PROGRAM READS THE DIRECTORY OF ONE OR MORE PARTITIONED     **
**   DATA SETS (SPECIFIED ON DD SYSLIB WITH OPTIONAL CONCATENATION). **
**                                                                   **
**   ALIAS ENTRIES ARE IGNORED. THE REMAINING MEMBER NAMES ARE       **
**   FORMATTED FOR PRINTING ON AN HP LASERJET 4SI                    **
**   ON SPECIAL TAB LABELS SUPPLIED WITH INDEX TABS FROM STAPLES     **
**                                                                   **
**   OUTPUT ON SYSPRINT (PORTRAIT MODE); OPTIONAL SUMMARY ON SUMPRINT**
**                                                                   **
**   PARM OPTIONS:  DUPLEX (DEFAULT - PRINT SAME DATA ON BOTH SIDES) **
**       SIMPLEX - PRINT ONE SET PER PAGE (PRINTER W/O DUPLEX)       **
**       ONESIDE - DUPLEX OUTPUT, BUT PRINTED ONE SIDE ONLY          **
**       DATE=YYDDD OR YYYYDDD - PRINT ONLY MEMBERS WITH CREATION    **
**                 DATE ON OR AFTER THE SPECIFIED ONE.               **
**       LINE= 1-20 - BEGIN PRINT ON NTH LABEL LINE (TO CONTINUE ON  **
**                 PARTIALLY USED SHEET.                             **
**                                                                   **
**   IF A SYSIN DD CARD IS PRESENT, AND IS NOT A DD DUMMY, THE       **
**   INPUT MEMBER NAMES ARE READ ONE PER CARD, AND SYSLIB IS NOT     **
**   OPTIONAL.                                                       **
**                                                                   **
**                                                                   **
***********************************************************************
         SPACE 2
***********************************************************************
*                                                                     *
*        COPYRIGHT 2004  EXPERT SYSTEM PROGRAMMING                    *
*                        176 OLD STAGE COACH ROAD                     *
*                        BRADFORD, VT 05033-8844                      *
*                                                                     *
*                    ALL RIGHTS RESERVED                              *
*                                                                     *
***********************************************************************
         SPACE 1
         PRINT &PRTSOR       SAVE A TREE
TABLABEL PGMHEAD ZERO31,BASE=(R11,R12),PARM=R2,                        *
               AM=31,RM=24,BNDRY=PAGE
         LA    R14,WRKHEAD   WORKING STORAGE
         L     R0,=A(PATSTART)  PATTERN FOR IT
         LA    R1,PATSTLEN   LENGTH
         LR    R15,R1          DITTO
         MVCL  R14,R0        BUILD CONTROL BLOCKS
         BAS   R14,PAGEPRNT  INITIALIZE PAGE PROCESSING
         SERVINIT ,          INITIALIZE @SERVICE ROUTINE
         SERVLOAD @INPREAD,@PARSER,@PRINTER  LOAD SUPPORT CODE
         SERVLOAD SUBVERB,SUBDSERV
         PRTOPEN SYSPRINT,OPT=(ABEND,WTO)  ABSOLUTELY REQUIRED
         MVI   ERRDEV,1      ALL OUTPUT ON SYSPRINT
         PRTOPEN SUMPRINT,OPT=(NOWTO,DUMMY),DEV=2
         CH    R15,=H'4'     DID IT OPEN ?
         BH    MISSUMPT      NO; USE SYSPRINT ONLY
         MVI   ERRDEV,2      ROUTINE STATUS/ERROR MESSAGES TO SUMPRINT
MISSUMPT PRTLIST /TITLE1,TITLE=1,DEV=2
         MVI   PRINTLIN-1,C' '   FOR FAST CLEARING
         EX    0,PRLINCLR    CLEAR PRINT LINE
         TITLE 'T A B L A B E L  ***  HOUSEKEEPING'
*---------------------------------------------------------------------*
*  PROCESS PARM FIELD, IF ANY                                         *
*---------------------------------------------------------------------*
         LA    R2,0(,R2)     CLEAN
         LTR   R2,R2         ANY ?
         BZ    NOEXPARM
         L     R2,0(,R2)           PARM PTR
         LA    R2,0(,R2)     CLEAN
         LTR   R2,R2         ANY ?
         BZ    NOEXPARM
         LH    R3,0(,R2)     GET PARM LENGTH
         PRTF  2(R2),(R3),CC=NO,DEV=2
         PARSE 2(R2),(R3),OPT=KEYWORD  COMMA/BLANK DELIMITED FIELDS
         BXH   R15,R15,NOEXPARM  NOTHING TO DO
         LA    R4,PAR@TABL   POINT TO RESULT CHAIN
         USING PRSDSECT,R4   DECLARE ENTRY
NEXTPARM ICM   R4,15,PRSLINK GET NEXT VALUE
         BZ    NOEXPARM      ALL DONE
         ICM   R0,15,PRS#TEXT  GET TEXT LENGTH
         BZ    NEXTPARM      SKIP NULL PARM
         OC    PRS$TEXT,BLANKS  UPPER CASE
*---------------------------------------------------------------------*
*   LINE= { 1 - 20 }  ON PAGE 1; START PRINT ON SPECIFIED LINE        *
*                     TO ALLOW RE-USE OF PARTIAL SHEETS               *
*---------------------------------------------------------------------*
         CLC   =C'LINE ',PRSKEYWD  IS IT LINE= ?
         BNE   PARMNLIN      NO; ASSUME FLAG
         ICM   R1,15,PRS#TINT+4  VALID INTEGER ?
         BNP   PARMBAD       NO; FAIL
         CH    R1,=H'1'      AT LEAST MINIMUM ?
         BL    PARMBAD       NO; TOO BAD
         CH    R1,=AL2((PAGE1END-PAGE1)/16)  NOT TOO LARGE ?
         BH    PARMBAD
         BCTR  R1,0          MAKE RELATIVE TO 0
         MH    R1,=H'16'     ONE LINE'S WORTH
         A     R1,NEXTNAME   AND ADJUST
         ST    R1,NEXTNAME   START POINTER
         B     NEXTPARM      AND GET ANOTHER
         SPACE 1
*---------------------------------------------------------------------*
*    DATE=YYDDD   OR  YYYYDDD    SETS DESIRED CREATION DATE           *
*    DATE=YYMMDD  OR  YYYYMMDD   SETS DESIRED CREATION DATE           *
*    MEMBERS CREATED BEFORE THEN, OR HAVING NO DATE, ARE OMITTED      *
*---------------------------------------------------------------------*
PARMNLIN CLC   =C'DATE ',PRSKEYWD  IS IT DATE= ?
         BNE   PARMFLAG      NO; ASSUME FLAG
         ICM   R1,15,PRS#TINT+4  VALID INTEGER ?
         BNP   PARMBAD       NO; FAIL
         CH    R0,=H'8'      YYYYMMDD FORMAT ?
         BE    PARMDAT8      YES; CONVERT TO JULIAN FORM
         BH    PARMBAD       TOO LONG; TOO BAD
         CH    R0,=H'5'      YYDDD FORMAT ?
         BE    PARMDAT5      NO; SET DATE AS IS
         BL    PARMBAD       TOO SHORT; TOO BAD
         CH    R0,=H'7'      YYYYDDD FORMAT ?
         BE    PARMDATE      YES; USE IT AS IS
* YYMMDD
         L     R2,=F'19000000' MAKE LAST CENTURY FOR 65-99
         C     R1,=F'650000'  PRIOR TO 65 ?
         BNL   *+8
         L     R2,=F'20000000' MAKE THIS CENTURY FOR 00-64
         AR    R1,R2         NOW HAVE YYYYMMDD
* YYYYMMDD
PARMDAT8 SR    R0,R0
         D     R0,=F'10000'  ISOLATE YEAR IN R1
         LR    R14,R1        SAVE THE YEAR
         SRDA  R0,32         ADJUST FOR NEXT DIVIDE
         D     R0,=F'100'    SPLIT DAY / MONTH
         LTR   R0,R0         DAY > 0 ?
         BNP   PARMBAD
         CH    R0,=H'31'     DAY < 32 ?
         BH    PARMBAD
         LTR   R1,R1         MONTH > 0 ?
         BNP   PARMBAD
         CH    R1,=H'12'     MONTH < 13 ?
         BH    PARMBAD
         LA    R2,NORMYEAR-2
         EX    R14,EXISLEAP  TEST FOR LEAP YEAR
         BNZ   *+8
         LA    R2,LEAPYEAR-2 ELSE GET LEAP YEAR TABLE
         SLA   R1,1          CONVERT MONTH TO TABLE OFFSET
         LH    R1,0(R1,R2)   LOAD DAYS PRIOR TO MONTH
         AR    R1,R0         DAYS IN THIS MONTH
         MH    R14,=H'1000'  SHIFT YEAR
         AR    R14,R1
         CVD   R14,DB        CONVERT TP PACKED FORM
         B     PARMDATZ
         SPACE 1
* YYDDD
PARMDAT5 L     R2,=F'1900000'  MAKE LAST CENTURY
         C     R1,=F'65000'  WRAPPED ?
         BNL   *+8
         L     R2,=F'2000000'  MAKE THIS CENTURY
         AR    R1,R2         MAKE SEVEN DIGIT DATE
* YYYYDDD
PARMDATE CVD   R1,DB         ALSO MAKE PACKED
PARMDATZ ZAP   JULDATE,DB    AND SAVE IT
         CP    JULDATE+L'JULDATE-2(2),=P'1'  LEGAL ?
         BL    PARMBAD       NO
         CP    JULDATE+L'JULDATE-2(2),=P'366'  LEGAL ?
         BH    PARMBAD       NO
         CP    JULDATE,=P'1965001'  REASONABLE ?
         BL    PARMBAD
         CP    JULDATE,=P'2099365'  REASONABLE ?
         BH    PARMBAD
         PRTLIST /MSGDATE,DEV=2
         OI    PROFLAGS,FGDATE  SHOW DATE SELECTED
         B     NEXTPARM      LOOK FOR MORE
NORMYEAR DC    H'0,31,59,90,120,151,181,212,243,273,304,334,365'
LEAPYEAR DC    H'0,31,60,91,121,152,182,213,244,274,305,335,366'
EXISLEAP TM    =X'03',*-*    MULTIPLE OF FOUR?
         SPACE 1
*---------------------------------------------------------------------*
*    EVERYTHING ELSE MUST BE A SELF-DEFINING KEYWORD (OPTION FLAG)    *
*---------------------------------------------------------------------*
PARMFLAG PARFGSET PARMOPTP,ERR=PARMBAD,DONE=NEXTPARM
         SPACE 1
PARMBAD  PRTLIST /MSGBDPRM,DEV==ERRDEV
         MVICC 4             MINOR ERROR
         B     NEXTPARM      DO ANOTHER
         SPACE 1
         PARKEYBX PARMOPT    BXLE FOR PARM OPTIONS
         DROP  R4
         SPACE 1
*---------------------------------------------------------------------*
*  CHECK 'SYSIN' INPUT DD - IF NOT FOUND OR DD DUMMY, LOOK FOR SYSLIB *
*---------------------------------------------------------------------*
NOEXPARM DEVTYPE SYSIN,DB    LOOK FOR SYSIN
         BXH   R15,R15,OMITSYN     MISSING SYSLIB DD ?
         CLI   DB+2,0        CHECK MAJOR DEVICE TYPE
         BE    OMITSYN       DD DUMMY - LOOK FOR SYSLIB
         TITLE 'T A B L A B E L  ***  PROCESS SYSIN INPUT'
         INPOPEN SYSIN,OPT=(ABEND)   OPEN THE INPUT FILE
SYNLOOP  INPGET ,            GET NEXT RECORD
         BXH   R15,R15,SYNEOF  QUIT ON EOF OR ERROR
         LR    R2,R1         SAVE RECORD ADDRESS
         LR    R3,R0         AND LENGTH
         PRTF  0(R2),(R3),CC=NO,DEV=2  ECHO THE CARD
         PARSE (R2),(R3),OPT=COMMA  COMMA/BLANK DELIMITED FIELDS
         BXH   R15,R15,SYNLOOP  NOTHING TO DO
         LA    R4,PAR@TABL   POINT TO RESULT CHAIN
         USING PRSDSECT,R4   DECLARE ENTRY
         ICM   R4,15,PRSLINK GET NEXT VALUE
         BZ    SYNLOOP       ALL DONE
         ICM   R0,15,PRS#TEXT  GET TEXT LENGTH
         BZ    SYNLOOP       SKIP NULL PARM
         TR    PRS$TEXT,TRNONULL  CONVERT NULL TO BLANK
         SPACE 1
         L     R3,NEXTNAME   POINT TO NEXT NAME SLOT
         LA    R0,PAGE1END   END OF PAGE1
         CR    R3,R0         ROOM FOR IT ?
         BL    SYNSAME       YES; MOVE IT
         BAS   R14,PAGEPRNT  ELSE PRINT PAGE
         LA    R3,PAGE1      NEW SPACE
SYNSAME  MVC   0(8,R3),PRS$TEXT  SAVE CURRENT NAME
         OI    PROFLAGS,FGDATA  DATA ON PAGE
         LA    R3,8(,R3)     NEXT SLOT
         ST    R3,NEXTNAME   SAVE IT
         B     SYNLOOP       LOOK FOR ANOTHER MEMBER TO DO
         DROP  R4
         SPACE 1
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
SYNEOF   DEVTYPE INDS+DCBDDNAM-IHADCB,DB  DO SYSLIB ALSO ?
         BXH   R15,R15,PREEXIT     NO
         CLI   DB+2,UCB3DACC       IS IT DIRECT ACCESS?
         BNE   PREEXIT             BR IF NO -- JUST EXIT
         B     LIBISDA             PROCESS SYSLIB TOO
         TITLE 'T A B L A B E L  ***  PROCESS SYSLIB DD(S)'
*---------------------------------------------------------------------*
*  CHECK 'SYSLIB' INPUT DD - MUST BE PDS OR PDS/E                     *
*---------------------------------------------------------------------*
OMITSYN  DEVTYPE INDS+DCBDDNAM-IHADCB,DB
         BXH   R15,R15,INVDSORG    MISSING SYSLIB DD ?
         CLI   DB+2,UCB3DACC       IS IT DIRECT ACCESS?
         BNE   INVDSORG            BR IF NO -- INVALID DEVTYPE
         SPACE 2
LIBISDA  SERVCALL RJFCB,INDS+DCBDDNAM-IHADCB GET JFCB
         BXH   R15,R15,INVDSORG    BAD DD ?
         MVC   MYJFCB(JFCBLGTH),0(R1)    SAVE IT HERE
         SERVCALL DSDJ1,JFCBDSNM   DSNAME POINTER
         BXH   R15,R15,INVDSORG    BAD DD ?
         MVC   DS1FMTID(96),0(R1)  SAVE DSCB
**  TEST DSORG FOR ACTUAL DATA SET TYPE
         CLI   DS1DSORG+1,0        ANY FUNNIES? (VSAM, ETC.)    GP12045
         BNE   INVDSORG              YES; REJECT                GP12045
         TM    DS1DSORG,DS1DSGPO   IS IT PARTITIONED?
         BO    DSISPO
**  DATA SET IS NEITHER PS NOR PO
INVDSORG PRTL ' ***** SYSLIB DD IS MISSING OR UNUSABLE *****',DEV==ERRD*
               EV
         SERVTERM ,
         ABEND 213,DUMP
         SPACE 2
**  DATA SET IS PO  -- TEST WHETHER MEMBER WAS SPECIFIED
DSISPO   TM    JFCBIND1,JFCPDS  DID USER SPECIFY A MEMBER ?
*              (OLD CODE FAILED ON GDG #)
         BNZ   INVDSORG      YES; DON'T WANT TO PROCESS
         SPACE 2
*---------------------------------------------------------------------*
*  OPEN PDS FILE AND BEGIN PROCESSING                                 *
*---------------------------------------------------------------------*
         OPEN  (INDS,INPUT),MF=(E,OPENLIST)
         SPACE 1
         PUSH  USING
         SUBCALL SUBDSERV,('INIT',ROOTPTR,INDS),VL,MF=(E,CALLPARM)
         LTR   R15,R1        DID IT WORK?
         BNZ   DESERVED      YES; CONTINUE
BADSERV  PRTLIST /MSGBDSRV,DEV==ERRDEV  LET USER KNOW
         ABEND 18,DUMP
         TITLE 'T A B L A B E L  ***  PROCESS NEXT MEMBER'
*---------------------------------------------------------------------*
*   LOOP HERE UNTIL ALL MEMBER NAMES ARE PROCESSED                    *
*---------------------------------------------------------------------*
DESERVED PRTL  '0THE FOLLOWING MEMBERS WILL BE PRINTED:',DEV=2
         PRTSPACE 1,DEV=2
         SPACE 2
MEMBLOOP SUBCALL SUBDSERV,('LOOP',ROOTPTR,MAINBXLE),VL,MF=(E,CALLPARM)
         LTR   R5,R15        SUCCESSFUL ?
         BZ    PREEXIT       NO; ALL DONE
         USING SMDE,R5       TEMPORARY ENTRY DECLARATION
         TM    SMDE_FLAG,SMDE_FLAG_DONE+SMDE_FLAG_ALIAS
         BNZ   MEMBLOOP
*DEFER*  TM    SMDE_FLAG,SMDE_FLAG_PROC  PROCESS ?
*DEFER*  BZ    MEMBLOOP      NO; DO ANOTHER
         OI    SMDE_FLAG,SMDE_FLAG_DONE  FLAG IT AS HAVING BEEN DONE
         LA    R2,CURRMEMB
         LA    R3,L'CURRMEMB
         ICM   R1,8,BLANKS
         MVCL  R2,R0         COPY MEMBER NAME
         CLI   CURRMEMB,C'$' LOCAL DOC OR JCL MEMBER ?
         BE    MEMBLOOP      YES; SKIP
*---------------------------------------------------------------------*
*    CHECK FOR CREATION DATE SELECTION                                *
*---------------------------------------------------------------------*
         TM    PROFLAGS,FGDATE  SCREEN BY DATE ?
         BZ    ACCEPT        NO; JUST PROCESS
         TM    SMDE_FLAG,SMDE_FLAG_LMOD  LOAD MODULE OR OBJECT?
         BNZ   MEMBLOOP      YES; CREATON DATE NOT AVAILABLE
         LH    R7,SMDE_USRD_LEN  LENGTH OF USER DATA
         CH    R7,=H'30'     ISPF/PDF LENGTH ?
         BNE   MEMBLOOP      NO; OTHER PROGRAMS HAVE NO CREDT
         LH    R6,SMDE_USRD_OFF  GET USER DATA OFFSET
         AR    R6,R5         USER DATA
         USING SPFVERL,R6    DECLARE SPF ENTRY
         TM    SPFCRTDT+L'SPFCRTDT-1,X'0F'  VALID SIGN ?
         BNO   MEMBLOOP      NO; IGNORE
         TM    SPFCRTDT,X'FE'  CENTURY SPECIFIED ?
         BNZ   HAVECENT
         AP    SPFCRTDT,=P'1900000'  GIVE IT A SPORTING CHANCE
HAVECENT CP    SPFCRTDT,JULDATE  OLDER THAN CUTOFF ?
         BL    MEMBLOOP      YES; SKIP
         DROP  R6
         SPACE 1
*---------------------------------------------------------------------*
*    MATCHES ALL SELECTION REQUESTS - PROCESS                         *
*---------------------------------------------------------------------*
ACCEPT   INC   NUMPROC       COUNT PROCESSED
         PRTITEM /MSGMEMNM,DEV=2    PRINT MEMBERNAME ONLY
         SPACE 1
         L     R3,NEXTNAME   POINT TO NEXT NAME SLOT
         LA    R0,PAGE1END   END OF PAGE1
         CR    R3,R0         ROOM FOR IT ?
         BL    MOVEMEMB      YES; MOVE IT
         BAS   R14,PAGEPRNT  ELSE PRINT PAGE
         LA    R3,PAGE1      NEW SPACE
MOVEMEMB MVC   0(8,R3),CURRMEMB  SAVE CURRENT MEMBER
         OI    PROFLAGS,FGDATA  DATA ON PAGE
         LA    R3,8(,R3)     NEXT SLOT
         ST    R3,NEXTNAME   SAVE IT
         B     MEMBLOOP      LOOK FOR ANOTHER MEMBER TO DO
         POP   USING
         SPACE 2
*---------------------------------------------------------------------*
*   END OF JOB - CLOSE AND FREE EVERYTHING; RETURN WITH CODE IN R15   *
*---------------------------------------------------------------------*
PREEXIT  BAS   R14,PAGEPRNT  PRINT LAST PAGE, IF ANY
         PRTLIST /FINALS,DEV=2
         SUBCALL SUBDSERV,('END',ROOTPTR),VL,MF=(E,CALLPARM)
*
PGMEXIT  CLOSE MF=(E,OPENLIST)
         SERVTERM ,          CLOSE AND FREE STUFF
         PGMEXIT RC=RETCODE  SET RETURN CODE
         TITLE 'T A B L A B E L  ***  SUBROUTINES -- PAGEPRNT'
***** THE FOLLOWING INSTRUCTION IS EXECUTED MANY PLACES *****
PAGEPRNT STM   R0,R15,SUBSAVE
         TM    PROFLAGS,FGDATA   ANY MEMBER NAMES ON PAGE ?
         BZ    PAGEPRNX      NO; NOTHING TO DO
         INCH  PAGECNT       INCREASE PAGE COUNT
         LA    R4,PAGE1      POINT TO FIRST ONE
         LA    R5,(PAGE1END-PAGE1)/8/2 NUMBER OF PRINT LINES TO DO
         PRTSPACE 4095       GET TOP OF PAGE
         MVI   PRINTLIN,C'-' TRIPLE SPACE
PAGEPRNL MVC   LEFTNAME,0(R4)
         MVC   RIGHTNAM,8(R4)
         PRTF  PRINTLIN
         LA    R4,16(,R4)    NEXT PAIR OF NAMES
         BCT   R5,PAGEPRNL
PRLINCLR MVC   PRINTLIN,PRINTLIN-1   CLEAR THE PRINT LINE
         TM    PROFLAGS,FGSIMPX  JUST ONE PAGE ?
         BNZ   PAGEPRNX      YES; DONE
         PRTSPACE 4095       GET TOP OF PAGE
         TM    PROFLAGS,FG1SIDE  LEAVE BACK BLANK ?
         BZ    PAGEPRND      NO; FILL BACK SIDE
         PRTL  ' '           PUT SOMETHING ON THIS PAGE
         B     PAGEPRNX      AND CLEAN UP
PAGEPRND LA    R4,PAGE1      POINT TO FIRST ONE
         LA    R5,(PAGE1END-PAGE1)/8/2 NUMBER OF PRINT LINES TO DO
         MVI   PRINTLIN,C'-' TRIPLE SPACE
PAGEPRN2 MVC   LEFTNAME,8(R4)
         MVC   RIGHTNAM,0(R4)
         PRTF  PRINTLIN
         LA    R4,16(,R4)    NEXT PAIR OF NAMES
         BCT   R5,PAGEPRN2
         MVC   PRINTLIN,PRINTLIN-1   CLEAR THE PRINT LINE
PAGEPRNX NI    PROFLAGS,255-FGDATA  PAGE NOW EMPTY
         CLRL  PAGE1,PAGE1END-PAGE1,FILL=C' ' CLEAR NAMES
         LA    R1,PAGE1
         ST    R1,NEXTNAME
         LM    R0,R15,SUBSAVE
         BR    R14           RETURN TO CALLER
         SPACE 1
         LTORG ,             FOR PGMHEAD ADDRESSABILITY
         TITLE 'T A B L A B E L  ***  CONSTANTS'
SYSPRINT PRTWORK SYSPRINT,TITLE=1  NO TITLE WRITTEN
SUMPRINT PRTWORK SUMPRINT,TITLE=5
SYSIN    INPWORK SYSIN,EDIT=128
         SPACE 1
DCH1     DC    H'1'          FDCLC OPERAND
BLANKS   DC    CL80' '
         SPACE 1
TITLE1   FDOPT NL,CC=C'#'    AUTO PAGE/DATE/TIME PROCESSING
TITLE1SQ FDOPT SBA=(*,61)
         FDPRT 'TAB LABELS'
         FD    *END
         TITLE 'T A B L A B E L  ***  DSECTS AND UNBASED CONSTANTS'
PARMOPT  PARKEYFG 'DUPLEX',PROFLAGS,0,FGDUPLX,LEN=1
PARMOPT2 PARKEYFG 'DEBUG',PROFLAGS,0,FGDEBUG  DEBUG MODE
         PARKEYFG 'SIMPLEX',PROFLAGS,0,FGSIMPX
         PARKEYFG 'ONESIDED',PROFLAGS,0,FGDUPLX+FG1SIDE
PARMOPTN PARKEYFG 'TEST',PROFLAGS,0,FGDEBUG   DEBUG MODE
         SPACE 2
MSGBDSRV FDPRT '*** DESERV FAILED ***',NL
         FDPRT *END
         SPACE 1
         PUSH  USING
         USING PRSDSECT,R4
MSGBDPRM FDPRT '*** UNSUPPORTED PARM',NL,PADR
         FDCLC PRSKEYWD,BLANKS,BE=MSGBDPRN
         FDPRT PRSKEYWD,DEB
         FDPRT '='
MSGBDPRN FDPRT PRS$TEXT,DEB,PADR
         FDPRT 'IGNORED ***'
         FD    *END
         POP   USING
MSGMEMNM FDPRT CURRMEMB,PADL
         FD    *END
         SPACE 1
MSGDATE  FDPRT 'MODULES CREATED PRIOR TO',NL
         FDPRT JULDATE,D,PAD
         FDPRT 'WILL BE SKIPPED'
         FD    *END
         SPACE 1
FINALS   FDOPT NL,CC=C'0'    DOUBLE-SPACE
         FDCLC PAGECNT,DCH1,BL=FINALSZ,BH=FINALSS
         FDPRT PAGECNT,I,RADJ,LEN=9
         FDPRT 'SHEET IS REQUIRED',PAD
         FDGOTO FINALCM
FINALSS  FDPRT PAGECNT,I,RADJ,LEN=9
         FDPRT 'SHEETS ARE REQUIRED',PAD
FINALCM  FDPRT NUMPROC,I,RADJ,NL,LEN=9
         FDPRT 'MEMBERS PRINTED',PAD
         FD    *END
FINALSZ  FDPRT 'NO MEMBERS WERE FOUND FOR PRINTING'
         FD    *END
         SPACE 2
PATSTART DS    0F     PATTERN DATA TO BE COPIED TO DYNAMIC SAVE AREA
PATINDS  DCB   DSORG=PO,MACRF=R,DDNAME=SYSLIB,BUFNO=2,                 *
               KEYLEN=0,NCP=2
         SPACE 1
PATOPLST OPEN  (PATINDS),MF=L
         SPACE 1
PATSTLEN EQU   *-PATSTART    MOVED TO SAVE AREA
         SPACE 1
TRNONULL DC    C' ',255AL1(*-TRNONULL)
         SPACE 2
SAVE     DSECT ,             NOTE STACK AT BEGINNING
SUBSAVE  DS    18A           SUBROUTINE SAVE AREA
ZEROES   DS    D
DB       DS    D
ROOTPTR  DS    A             START OF DESERV RESPONSE
NEXTNAME DS    A             POINTER TO NEXT PAGE NAME
JULDATE  DS    PL4           PACKED DATE YYYYYDDD
         SERVDEFS ,          EXPAND STUFF FOR @SERVICE, ETC.
         SPACE 1
         MAPPARSE DSECT=NO   PARSE PARAETER AREA
         SPACE 1
NUMPROC  DS    F             NUMBER OF MEMBERS FLAGGED FOR PROCESSING
MAINBXLE DS    4A            BXLE POINTER FOR DIRECTORY LIST
PAGECNT  DS    H             NUMBER OF SHEETS WRITTEN
ERRDEV   DS    X             OUTPUT DEVICE FOR ERROR & STATUS MESSAGES
         SPACE 1
***********************************************************************
**                                                                   **
WRKHEAD  DS    0A     DATA COPIED FROM PATSTART
INDS     DCB   DSORG=PO,MACRF=R,DDNAME=SYSLIB,BUFNO=2,                 *
               KEYLEN=0,NCP=2
         SPACE 1
OPENLIST OPEN  (INDS),MF=L
PGCNT    DC    PL2'0'
CURRMEMB DC    CL8' '
         SPACE 1
         DC    C' '   1/2    PRINT LINE
PRINTLIN DC    CL133' ' 2/2  PRINT LINE WITH ASA CONTROL
LEFTNAME EQU   PRINTLIN+30,8,C'C'
RIGHTNAM EQU   PRINTLIN+49,8,C'C'
         SPACE 1
***  PROFLAGS SET AS FOLLOWS:
PROFLAGS DS    X
FGDEBUG  EQU   X'80'           DEBUG REQUESTED
FGDUPLX  EQU   X'40'           TWO-SIDED PRINTING (SAME DATA)
FGSIMPX  EQU   X'20'           ONE SIDE/PAGE PRINTING
FG1SIDE  EQU   X'10'           ONESIDED PRINTING ON DUPLEX PRINTER
FGDATE   EQU   X'08'           SELECT MEMBERS BY CREATION DATE>=
FGDATA   EQU   X'02'           AT LEAST ONE NAME IN LIST
FGEND    EQU   X'01'           LAST CALL TO PAGEPRNT
         SPACE 1
MYJFCB   DS    0F            LIBRARY JFCB
         IEFJFCBN ,          LIBRARY JFCB
         SPACE 1
         IECSDSL1 1          LIBRARY DSCB
         SPACE 1
PAGE1    DS    40CL8         MEMBER NAMES FOR FRONT PAGE
PAGE1END EQU   *
SAVEEND  EQU   *
         SPACE 1
         MAPPARST ,          MAP PARSE RETURN ENTRIES
         SPACE 1
         PRINT &PRTSYS
MYUCB    DSECT ,
         IEFUCBOB ,
         SPACE 1
         DCBD  DSORG=(PS,PO,DA),DEVD=DA
         EJECT ,
         PUSH  PRINT
         PRINT ON,GEN
         IGWDES ,
         SPACE 2
*  LOCAL FLAGS USED IN SMDE_FLAG:
SMDE_FLAG_PROC EQU X'01'     WANT TO PROCESS THIS ENTRY
SMDE_FLAG_DONE EQU X'10'     THIS ENTRY HAS BEEN PROCESSED (OR SKIPPED)
         IGWSMDE ,
         SPACE 2
         IHAPDS PDSBLDL=NO
         MAPPDS ,            LOCAL MAPPING OF USER DATA IN DIR. ENTRY
         POP   PRINT
         END   ,
