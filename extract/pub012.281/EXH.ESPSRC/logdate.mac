LOGD     TITLE 'L O G D A T E  ***  WRITE DATE TO SYSLOG/HARDCPY'
         COPY  OPTIONGB
         SPACE 1
         SYSPARM LIST=YES
         SPACE 1
         PRINT &PRTSOR
         SPACE 1
***********************************************************************
*                                                                     *
*        LOGDATE (ALMOST CALLED IT LOGWRECK) TO WRITE THE CURRENT     *
*        DATE TO THE CONSOLES, SYSTEM LOG AND HARDCOPY WITH BIG       *
*        LETTERS (8*8).                                               *
*                                                                     *
***********************************************************************
         SPACE 1
LOGDATE  PGMHEAD ZERO12,BASE=R12,PARM=R9,BNDRY=PAGE     ADDED ON 91017
         MVC   MLWTO(PATWTO1L),PATWTO1  MOVE WTO PREFIX
         MVC   WTOEND(PATWTO2L),PATWTO2  MOVE SUFFIX
         STIMER WAIT,BINTVL==A(10*100)  WAIT A BIT FOR DATE CHANGE
         SERVINIT ,                                             GP03265
         SERVLOAD (IEFSD095,@BIGABC)  *B*I*G* LETTER ROUTINE    GP03265
         CALL  SUBDAT,(EBCDATE),VL,MF=(E,PARMLIST)  CURR. DATE  GP06358
         LA    R14,EBCDATE   POINT TO PRINTABLE DATE
         LA    R15,NUMLINE   LINE NUMBER POINTER (1:8)
         LA    R0,OUTLINE    OUTPUT LINE ADDRESS
         LA    R1,FLAGSIZE   POINT TO FLAGS/SIZE ENTRY
         STM   R14,R1,PARMLIST  MAKE PARAMETER LIST
         MVC   FLAGSIZE(4),=AL1(1,4+8,0,L'EBCDATE) 8*8,CENTER,8BYTES
         SPACE 1
         LA    R2,8          LOOP FOR 8 LINES
         LA    R3,WTOLEN1    START WITH SECOND LINE
LINELOOP LA    R0,9
         SR    R0,R2         GET CURRENT LINE NUMBER
         ST    R0,NUMLINE    STASH IT
         L     R15,@BIGABC   GET FORMATTER
         LA    R1,PARMLIST   POINT TO OPTION LIST
         BALR  R14,R15       INVOKE IT
         MVC   WTOLEN1-WTOLEN1(PATLINEL,R3),PATLINE  MOVE LINE DATA
         MVC   WTOLINE1-WTOLEN1(L'WTOLINE1,R3),OUTLINE+1        GP03265
         LA    R3,WTOLINE3-WTOLINE2(,R3)
         BCT   R2,LINELOOP   DO ALL
         SPACE 1
         WTO   MF=(E,MLWTO)  WRITE THE INFORMATION TO THE LOG
         SPACE 1
         SERVTERM ,                                             GP03265
         PGMEXIT RC=0        HOW DO WE DETECT AN ERROR ?
         SPACE 1
PATWTO1  DC    AL2(4+1),X'8040',C' ',X'10004000'  ROUT=2,DESC=4
         DC    X'2000',AL2(10)  LINE TYPE/COUNT
PATWTO1L EQU   *-PATWTO1     LENGTH OF PREFIX
PATWTO2  DC    AL2(4+1),X'3000',C' '  TYPE DE (BLANK SEPARATOR)
PATWTO2L EQU   *-PATWTO2     LENGTH OF SUFFIX
PATLINE  DC    AL2(4+L'WTOLINE2),X'2000'  TYPE D LINE
PATLINEL EQU   *-PATLINE     LENGTH OF LINE PREFIX
         SPACE 1
SAVE     DSECT ,             SAVE AND WORK SECTION
DB       DS    2D            WORK AREA
         SERVDEFS ,          SERVICE ROUTINE ADDRESSES, ETC.    GP03265
PARMLIST DS    4A            IEFSD095'S PARAMETER LIST
EBCDATE  DS    CL8           DATE TO BE FORMATTED (MM/DD/YY)
NUMLINE  DS    F             CURRENT LINE NUMBER OF PATTERN (1-8)
FLAGSIZE DS    X,X,H         SIZE FLAG/OPTIONS/NUMBER CHARS
OUTLINE  DS    CL148         LOTS OF ROOM FOR TEMPORARY OUTPUT
         SPACE 1
MLWTO    DC    AL2(4+1),X'8040',C' ',X'10004000'  ROUT=2,DESC=4
         DC    X'2000',AL2(10)  LINE TYPE/COUNT
WTOLEN1  DC    AL2(4+L'WTOLINE1),X'2000'  TYPE D
WTOLINE1 DC    CL70' '       TEXT
WTOLEN2  DC    AL2(4+L'WTOLINE2),X'2000'  TYPE D
WTOLINE2 DC    CL70' '       TEXT
WTOLEN3  DC    AL2(4+L'WTOLINE3),X'2000'  TYPE D
WTOLINE3 DC    CL70' '       TEXT
WTOLEN4  DC    AL2(4+L'WTOLINE4),X'2000'  TYPE D
WTOLINE4 DC    CL70' '       TEXT
WTOLEN5  DC    AL2(4+L'WTOLINE5),X'2000'  TYPE D
WTOLINE5 DC    CL70' '       TEXT
WTOLEN6  DC    AL2(4+L'WTOLINE6),X'2000'  TYPE D
WTOLINE6 DC    CL70' '       TEXT
WTOLEN7  DC    AL2(4+L'WTOLINE7),X'2000'  TYPE D
WTOLINE7 DC    CL70' '       TEXT
WTOLEN8  DC    AL2(4+L'WTOLINE8),X'2000'  TYPE D
WTOLINE8 DC    CL70' '       TEXT
WTOEND   DC    AL2(4+1),X'3000',C' '  TYPE DE (BLANK SEPARATOR)
SAVEEND  EQU   *             END OF GETMAIN
         SPACE 1
         PRINT &PRTSYS
         END   ,
