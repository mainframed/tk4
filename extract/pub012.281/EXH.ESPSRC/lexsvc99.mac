LEXSVC99 TITLE 'L E X S V C 9 9  ***  INSTALLATION ALLOCATION CHECKING'
         MACRO ,                                                 83107
&L       SAVEX &R1,&R3,&LOC,&TYPE=*                              83107
         AIF   ('&TYPE' EQ 'STM').STM                            83107
         MNOTE 8,'TYPE=&TYPE UNKNOWN - TYPE=STM ASSUMED'         83107
.STM     ANOP  ,                                                 83107
&L       STM   &R1,&R3,&LOC                                      83107
         MODESET EXTKEY=ZERO,WORKREG=0  NEED KEY ZERO FOR SERVICE
         MEND  ,                                                 83107
         SPACE 1                                                 83107
         MACRO
&NM      SETMSG &N
         LCLA  &I,&J
         LCLC  &F
&F       SETC  '8040201008040201'
         AIF   (T'&N NE 'N').FAIL
         AIF   (&N GE 1 AND &N LT 31).OK
.FAIL    MNOTE 8,'SETMSG REQUIRES NUMERIC FROM 1 TO 30; NOT &N'
         MEXIT ,
.OK      ANOP  ,
&I       SETA  31-&N
&J       SETA  &I/8          GET BYTE OFFSET
&I       SETA  2*(&I-&J*8)+1   MAKE SUBSTRING OFFSET
&F       SETC  '&F'(&I,2)
&NM      OI    MSGFLAG+&J,X'&F'  SET MESSAGE FLAG
         MEND  ,
         SPACE 2
         MACRO
&NM      TESTMSG &N
         LCLA  &I,&J
         LCLC  &F
&F       SETC  '8040201008040201'
         AIF   (T'&N NE 'N').FAIL
         AIF   (&N GE 1 AND &N LT 31).OK
.FAIL    MNOTE 8,'TESTMSG REQUIRES NUMERIC FROM 1 TO 30; NOT &N'
         MEXIT ,
.OK      ANOP  ,
&I       SETA  31-&N
&J       SETA  &I/8          GET BYTE OFFSET
&I       SETA  2*(&I-&J*8)+1   MAKE SUBSTRING OFFSET
&F       SETC  '&F'(&I,2)
&NM      TM    MSGFLAG+&J,X'&F'  TEST MESSAGE FLAG
         MEND  ,
         SPACE 2
         MACRO ,
&NM      LOCKEY &KEY,&NOT,&NONE=,&NOTONE=
&NM      LA    R0,&KEY       SET REQUESTED KEY/SUBKEY
         BALS  R14,KEYLOOK   LOOK FOR REQUESTED KEY
         AIF   (T'&NOT EQ 'O').MEND
         BZ    &NOT          KEY NOT FOUND
         AIF   (T'&NOTONE EQ 'O').SOME
         CH    R0,=H'1'      ONE SUBPARAMETER ?
         BNE   &NOTONE       NO
         AGO   .MEND
.SOME    AIF   (T'&NONE EQ 'O').MEND
         CH    R0,=H'1'      AT LEAST ONE SUBPARM ?
         BL    &NONE         NO
.MEND    MEND  ,
         SPACE 2
         COPY  OPTIONGB
         SPACE 1
         SYSPARM LIST=YES
         SPACE 2
***********************************************************************
*                                                                     *
*        COPYRIGHT 1983  EXPERT SYSTEM PROGRAMMING, INC.              *
*        COPYRIGHT 2003  EXPERT SYSTEM PROGRAMMING                    *
*                        176 OLD STAGE COACH ROAD                     *
*                        BRADFORD, VT 05033-8844                      *
*                                                                     *
*                    ALL RIGHTS RESERVED                              *
*                                                                     *
***********************************************************************
         EJECT ,
         PRINT &PRTSOR
*
*        LOCAL ALLOCATION PROCESSING MODULE.
*
*        FOR NEW DATASET ALLOCATION (DASD), THIS MODULE PERFORMS
*        VOLUME CHECKING AND UNIT CONVERSION AS FOLLOWS :
*
*        1) REQUEST NOT DASD ALLOCATION OR NOT NEW - ALLOW
*        2) VOLUME SERIAL OR REFERENCE SPECIFIED -
*              A) USER UNIT - ALLOW UNCHECKED
*              B) NO UNIT - SET DEFAULT OF 'SYSALLDA'
*        3) NO VOLUME SERIAL; NO UNIT - FORCE UNIT OF 'SYSALLDA'
*        4) IF UNIT IS REPLACEABLE ('SYSALLDA'), CHECK THE DSN
*              A) NONE OR TEMPORARY DSN => 'SYSDA'
*              B) WYL. INDEX            => 'WYLBUR'
*              C) OTHER - PERMIT UNCHECKED
*        ALL OTHER VALIDITY CHECKING IS DONE IN A FRONT END TO SVC 32
*        (DASD ALLOCATION). SEE MEMBER MVSALLOC FOR DETAILS.
*
*        THIS MODULE IS INVOKED VIA BRANCH (AFTER USERCVT OR LPA
*        LOOKUP) FROM SVC 99 EXIT MODULE IEFDB401 (THE PROPER EXIT).
*
LEXSVC99 SAVEM ZERO12,BASE=R12,PARM=R11                          83135
         AIF   (NOT &MVSXA).NO31AR                               92300
*   IN X/A THIS IS CALLED IN 31-BIT MODE; UNFORTUNATELY LOCAL FUNCTIONS
*      WON'T HANDLE THAT.                                        92300
LEXSVC99 AMODE 31            CHEAT                               92300
LEXSVC99 RMODE 24            FORCE                               92300
         AM24  ,             REQUIRED FOR LOCAL FUNCTIONS        92300
.NO31AR  XR    R9,R9         SET GOOD RETURN CODE
         LA    R1,1
         ST    R1,MSGFLAG    CLEAR (INITIALIZE) MESSAGE FLAGS
         USING S99RBP,R11    DECLARE THE PARAMETER LIST (INCORRECTLY)
         L     R8,S99RBPTR   GET REQUEST BLOCK
         USING S99RB,R8
         CLI   S99VERB,S99VRBAL   DSN ALLOCATION ?
         BNE   EXIT          NO; EXIT OK
         L     R4,PSATOLD-PSA   GET TCB
         USING TCB,R4
         ICM   R5,15,TCBTCT  IS THERE A TCT ?
         BZ    EXIT          NO; ALLOW
         USING SMFTCT,R5
         ICM   R6,15,TCTJMR  IS THERE A JMR ?
         BZ    EXIT          HUH ?
         USING JMR,R6        DECLARE IT
         MVC   CURACCT,JMRUSEID  COPY ACCOUNT NUMBER
         MVC   CURCLASS,JMRUCOM  COPY SUBMITTER'S CLASS REQUEST
         CLI   CURCLASS,0    ANY FILLED IN ?
         BNE   *+10          YES
         MVC   CURCLASS,JMRCLASS  USE (POSSIBLY) RESET CLASS
         L     R3,PSAAOLD-PSA  GET ASCB
         USING ASCB,R3       DECLARE IT
         LA    R15,FGTSO     SET TSO FLAG
         ICM   R0,15,ASCBTSB  IS THERE A TSB ?
         BNZ   HAVECTGY      YES; WE HAVE CATEGORY
         ICM   R1,15,ASCBCSCB  GET THE CSCB
         BZ    EXIT          NONE ?
         USING CHAIN,R1      DECLARE IT
         SLR   R15,R15
         TM    CHTRKID,CHINITID  INITIATOR ?                     83135
         BO    HAVECTGY      YES; NO SPECIAL FLAGS               83135
         ICM   R2,15,ASCBOUCB  GET OUCB                          83135
         BZ    HAVECTGY      OR GIVE UP                          83135
         AM31  ,             GET HIGH                            92300
         USING OUCB,R2                                           83135
         TM    OUCBYFL,OUCBSTT+OUCBMNT  START OR MOUNT ?         83135
         BZ    HAVECTGY      NEITHER - NO PRIVILEGES SET         83135
         DROP  R2                                                83135
         LA    R15,FGSTC     SET FOR START TASK
         LA    R14,STCNAMES  POINT TO TABLE OF SPECIAL STARTS
         LA    R0,STCNUM     SET NUMBER TO CHECK
LOOPSTCS CLC   CHCLS,0(R14)  MATCHING PROCNAME ?
         BE    LOOKSTCS      YES
         LA    R14,STCLEN(,R14)
         BCT   R0,LOOPSTCS
         B     HAVECTGY      JUST SET STC
LOOKSTCS IC    R0,8(,R14)    GET SPECIAL FLAG(S)
         OR    R15,R0        ADD TO STC FLAG
HAVECTGY STC   R15,ENVFG     SET ENVIRONMENT FLAG(S)
         AM24  ,                                                 92300
         DROP  R1,R3,R4,R5,R6
*DEFER*  SERVICE ACTST,CURACCT  VALIDITY CHECK RUNNING ACCOUNT
*DEFER*  STC   R0,ACTFG      SAVE ACCOUNT PRIVILEGES
LOOKDISP LM    R14,R15,@WKA  GET PROVIDED WORK AREA AND LENGTH
         L     R14,0(,R14)   LOAD WORK AREA ADDRESS              83114
         L     R15,0(,R15)   LOAD WORK AREA LENGTH               83114
         STM   R14,R15,WORKAREA  INITIALIZE FOR MODIFICATION
         XR    R0,R0
         XR    R1,R1
         MVCL  R14,R0        CLEAR IT FOR DEBUGGING AND MVI VS. MVC
         LOCKEY DALDUMMY
         BNZ   EXIT          IGNORE DD DUMMY REQUESTS
         LOCKEY DALTERM
         BNZ   EXIT          IGNORE TERMINAL REQUESTS
         LOCKEY DALSYSOU
         BNZ   EXIT          ***TEMPORARY - LATER WILL BE CHECKED***
         SPACE 1
         LOCKEY DALSTATS,NONE=DISPNEW  CHECK FOR DISP=NEW
         USING S99TUNIT,R1   MAP TEXT UNIT
         TM    S99TUPAR,255-X'04'  OTHER THAN NEW ?
         BNZ   DISPNNEW      YES; DO NOT DO NEW CHECKS
         SPACE 1
*        USER WANTS A NEW DATASET (ALLOCATION REQUIRED)
*              CHECK UNIT, DSN AND VOLSER
DISPNEW  MVI   PSWDSN,C' '   USE PSWD FIELD FOR WORK
         MVC   PSWDSN+1(L'PSWDSN-1+L'PSWPSWD),PSWDSN  BLANK IT OUT
         MVI   DSNFG,FDTEMP  PROVISIONALLY SET TEMPORARY DSN
         LOCKEY DALDSNAM,NEWUNIT,NONE=NEWUNIT
         LH    R2,S99TULNG   GET LENGTH OF SUPPLIED DSN
         SH    R2,=H'1'      SET FOR EXECUTE
         BM    NEWUNIT       NONE- TEMPORARY
         EX    R2,MVCPDSN    MOVE TO WORK FIELD
         CLI   PSWDSN,C'&&'  AMPERSAND FORM OF TEMPORARY DSN ?   83136
         BE    NEWUNIT       YES; TREAT AS TEMPORARY             83136
         CLC   =C'*.',PSWDSN IEHMOVE TEMPORARY ?
         BE    NEWUNIT       YES; DON'T BOTHER CHECKING THE REST
         CLC   PSWDSN(3),DCSYS1  NAME STARTS WITH SYS ?
         BNE   DSNOTMP       NO
         CLC   =C'.T',PSWDSN+8   SYSNNNNN.TNNNNNN ?
         BNE   DSNOTMP       NO
         MVC   DB(5),=6C'0'
         NC    DB(5),PSWDSN+3
         CLC   DB(5),=6C'0'  NUMERIC ZONES ?
         BNE   DSNOTMP       NO
         MVC   DB(6),=6C'0'
         NC    DB(6),PSWDSN+10  CHECK TIME FIELD
         CLC   DB(6),=6C'0'
         BE    NEWUNIT       YES; SET TEMPORARY DSN
DSNOTMP  MVI   DSNFG,0       RESET ALL FLAGS
         SERVICE DSCAT,PSWDSN  VALIDITY CHECK
         CH    R15,=H'4'     VALID NAME ?
         BH    FAILDSN       NO
         BE    SHORTDSN      UNQUALIFIED DSN (E.G. SYSCTLG)
*DEFER*  CH    R0,=Y(VRNWYL)   WYLBUR NAME ?
*DEFER*  BNE   NEWUNIT       NO; CHECK UNIT
*DEFER*  OI    DSNFG,FGWYL   SET FOR WYLBUR NAME
*DEFER*  SERVICE DSWYL,PSWDSN  DO WYLBUR CHECK
*DEFER*  BXH   R15,R15,FAILDSN   INVALID WYLBUR NAME
         B     NEWUNIT       CHECK UNIT
SHORTDSN TM    ACTFG,VAASYS  SYSTEMS ?
         BNZ   NEWUNIT       YES; ALLOW
         OI    DSNFG,FDSHRT  CHECK AGAINST UNIT/VOLSER LATER
         SPACE 1
NEWUNIT  LOCKEY DALVLSER,NOVUNIT,NONE=NOVUNIT                    83135
         B     TESTVOL       SEE IF UNIT WITH VOLSER             83135
NOVUNIT  LOCKEY DALVLRDS,NORUNIT,NONE=NORUNIT                    83135
         B     TESTVOL       SEE IF UNIT WITH REF=DSN            83135
NORUNIT  LOCKEY DALUNIT,NOTUNIT,NONE=NOTUNIT                     83135
         LH    R2,S99TULNG   GET LENGTH
         SH    R2,=H'1'
         BNM   CHKUNIT                                           83114
NOTUNIT  TM    ENVFG,FGTSO   RUNNING TSO ?                       83135
         BZ    DISPNNEW      NO; LEAVE UNIT OFF                  83114
         LA    R3,LTUUNITS   MAKE LOCAL UNIT REPLACEMENT         83114
         BALS  R14,KEYMAKE   MAKE AN ENTRY FOR DEFAULT UNIT      83114
         L     R5,PSATOLD-PSA   GET CURRENT TCB                  83135
         L     R5,TCBJSCB-TCB(,R5)  GET JSCB                     83135
         ICM   R5,15,JSCBPSCB-IEZJSCB(R5)  GET PSCB              83135
         BZ    NOVOLUNT      NONE ?                              83135
         USING PSCB,R5       DECLARE IT                          83135
         CLI   PSCBU,C' '    DO WE HAVE A VOLSER ?               83135
         BNH   COPYPSCB      NO; CHECK FOR GROUP NAME            83135
         STM   R15,R1,DB     SAVE LOCKEY/KEYMAKE REGISTERS       83136
         SERVICE UCBDK,PSCBU   FIND DISK UCB FOR 'GROUP'         83135
         LTR   R3,R0         TEST RETURN                         83135
         LM    R15,R1,DB     RESTORE LOCKEY/KEYMAKE REGISTERS    83136
         BZ    COPYPSCB      NOT UP                              83136
         LOCKEY DALVLSER,NEWVOL,NONE=NEWVOL  DUMMY SEARCH        83135
NEWVOL   LA    R3,LTUVOLSR   SET LOCAL VOLSER                    83135
         BALS  R14,KEYMAKE   MAKE VOLSER ENTRY                   83135
         MVC   S99TUPAR(6),PSCBU     MOVE DEFAULT VOLSER         83135
         B     DISPNNEW      AND QUIT                            83135
         SPACE 1                                                 83135
COPYPSCB CLI   PSCBGPNM,C' '   GROUP NAME PRESENT ?              83135
         BNH   NOVOLUNT      NO                                  83135
         MVC   PSWPSWD(8),PSCBGPNM  COPY USER'S GROUP NAME       83135
         B     CHKUNITP      CHECK IT FOR REPLACEMENT            83135
NOVOLUNT LH    R2,S99TULNG   GET FIELD LENGTH                    83114
         BCTR  R2,0          SET MOVE LENGTH                     83114
CHKUNIT  EX    R2,MVCPSWD    MOVE TO TEMPORARY UNIT FIELD        83114
CHKUNITP CLC   TSOUNIT,PSWPSWD  SPECIAL TSO UNIT ?               83135
         BNE   DISPNNEW      NO
*DEFER*  LA    R2,DCWYLBUR   SET FOR WYLBUR UNIT
*DEFER*  TM    DSNFG,FGWYL   WYLBUR ?
*DEFER*  BNZ   REPUNIT       YES; REPLACE UNIT
         LA    R2,DCSYSDA    ELSE TRY SYSDA
         TM    DSNFG,FDTEMP  TEMPORARY ?
         BNZ   REPUNIT
         LA    R2,DCUNITS    NO OTHER SPECIALS AT PRESENT        83114
REPUNIT  MVC   S99TUPAR(L'TSOUNIT),0(R2)  REPLACE UNITNAME
         B     DISPNNEW      DONE ?
         SPACE 1
EXIT8    LA    R9,8          SET ERROR CODE
         B     EXIT
         SPACE 1
FAILDSN  TM    ENVFG,255-FGSTC-FGTSO  SPECIAL TASK ?
         BNZ   EXIT4         YES; JUST RETURN ERROR
         MVC   ERMWTO(PATWTOL+4),PATWTO  MAKE MESSAGE
         MVC   ERMWTO+PATWTOD-PATWTO(44),PSWDSN
         TM    ENVFG,FGTSO   TSO ?
         BNZ   FAILTSO
         WTO   MF=(E,ERMWTO)
         B     EXIT4
FAILTSO  LA    R2,ERMWTO+4   TEXT PROPER
         TPUT  (R2),PATWTOL-4
EXIT4    LA    R9,4          SET ERROR CODE
         B     EXIT
         SPACE 1
TESTVOL  LOCKEY DALUNIT,MAKEUNIT,NONE=MAKEUNIT  UNIT ALSO ?      83135
         B     DISPNNEW      YES; JUST EXIT                      83135
MAKEUNIT LA    R3,LTUUNITS   MAKE LOCAL UNIT REPLACEMENT         83135
         BALS  R14,KEYMAKE   MAKE AN ENTRY FOR DEFAULT UNIT      83135
         SPACE 1                                                 83135
DISPNNEW SLR   R9,R9         NO ADDITIONAL CHECKING YET          83135
EXIT     DS    0H            REPLACE BY ERROR MESSAGE WRITER
EXITEXIT ENDM  RC=(R9)       RETURN WITH CONDITION CODE SET
         ORG   *-2           OVERLAY THE BR14                    83114
         MODESET EXTKEY=SCHED   RESTORE SCHEDULER KEY 1          83114
         BSM   0,R14         RETURN TO CALLER IN 31-BIT MODE     92300
         EJECT ,                                                 90296
*        SUBROUTINE TO ADD OR REPLACE A TEXTUNIT IN ITS ENTIRETY 83114
*  IN:   R15, R1 AS RETURNED BY LOCKEY FOR THE TEXT UNIT;        83114
*        R3 POINTING TO THE NEW TEXT                             83114
*  OUT:     RETURN IS MADE AS FOR LOCKEY                         83114
KEYMAKE  STM   R14,R6,SAVE14-SAVESPLN(R13)    SAVE SOME          83114
         LH    R2,S99TULNG-S99TUNIT(,R3)         GET USER'S LENGTH
         LA    R2,S99TUPAR-S99TUNIT(,R2)         +FIXED OVERHEAD 83114
         LM    R4,R5,WORKAREA          GET SYSTEM WORK AREA      83114
         LTR   R1,R1         IS THE R15 POINTER VALID ?          83114
         BNZ   KEYMAKR       YES; REPLACEMENT REQUEST            83114
         LA    R0,4          ELSE NEED TO ADD ONE                83114
         LR    R15,R4        COPY AREA START                     83114
         SR    R15,R0        GET LAST ENTRY                      83114
         MVC   4(4,R15),0(R15)  SLIDE LAST ENTRY DOWN            83114
         MVI   0(R15),0      KILL LAST ENTRY BIT                 83114
         AR    R4,R0         UPDATE AREA START                   83114
         SR    R5,R0         UPDATE LENGTH                       83114
KEYMAKR  L     R6,0(,R15)    GET OLD SIGN                        83114
         N     R6,=X'80000000'  LEAVE ONLY THE SIGN              83114
         SR    R5,R2         GET RESIDUAL LENGTH AFTER TEXT      83114
         BM    EXIT4         OUT OF SPACE ?                      83114
         LA    R1,0(R4,R5)   NEW END - START OF TEXT UNIT        83114
         STM   R4,R5,WORKAREA  UPDATE POINTERS                   83114
         BCTR  R2,0          SET EXECUTE LENGTH                  83114
         EX    R2,MVCBACK    MAKE A NEW TEXT UNIT                83114
         OR    R6,R1         UPDATE ADDRESS WITH FLAG BIT        83114
         ST    R6,0(,R15)    STASH POINTER BACK                  83114
         LH    R0,S99TUNUM   SET NUMBER OF PARAMETERS            83114
         L     R14,SAVE14-SAVESPLN(,R13)  JUST IN CASE           83114
         LM    R2,R6,SAVE2-SAVESPLN(R13)  RESTORE THE REST       83114
         BR    R14           RETURN                              83114
MVCBACK  MVC   S99TUNIT(0),0(R3)  MOVE USER'S PATTERN            83114
         SPACE 2
*        SUBROUTINE TO LOCATE TEXT UNIT FOR KEY VALUE IN R0
*              IF NOT FOUND, RETURNS 0 IN R1
*              IF FOUND, RETURNS TEXT UNIT ADDRESS IN R1, NUMBER OF
*              ENTRIES IN R0; ADDRESS OF POINTER IN R15
*
KEYLOOK  L     R15,S99TXTPP  GET FIRST TEXT UNIT POINTER
         USING S99TUPL,R15
KEYLOOP  L     R1,S99TUPTR   GET NEXT TEXT UNIT
         N     R1,=X'7FFFFFFF'  CLEAR LAST ENTRY BIT
         BZ    KEYBUMP       SKIP FILLER ENTRY                   83114
         USING S99TUNIT,R1
         CH    R0,S99TUKEY   MATCHING TEXT KEY ?
         BE    KEYFUND       YES; EXIT WITH MATCH
KEYBUMP  TM    S99TUPTR,S99TUPLN   LAST ENTRY ?                  83114
         LA    R15,4(,R15)   BUMP TO NEXT ONE ANYWAY             83114
         BZ    KEYLOOP       TRY NEXT ENTRY                      83114
         XR    R0,R0         SET NO PARAMETERS                   83114
         XR    R1,R1         SET NO ADDRESS
         BR    R14           RETURN TO CALLER
KEYFUND  LH    R0,S99TUNUM   LOAD NUMBER OF PARAMETERS
         LTR   R1,R1         SET CONDITION CODE FOR CALLER       83115
         BR    R14           RETURN TO CALLER
         DROP  R15
         SPACE 2
MVCPDSN  MVC   PSWDSN(0),S99TUPAR  MOVE SHORT DSN
MVCPSWD  MVC   PSWPSWD(0),S99TUPAR  MOVE SHORT UNIT
LTUVOLSR DC    Y(DALVLSER,1,6),CL6'FAKVOL'  VOLSER TEXT          83135
LTUUNITS DC    Y(DALUNIT,1,L'DCUNITS)   1/2 - TSO DEFAULT        83114
TSOUNIT  DC    CL8'SYSALLDA' INSTALLATION DEFAULT FOR TSO        83135
DCWYLBUR DC    CL8'WYLBUR'
DCSYSDA  DC    CL8'SYSDA'
DCUNITS  DC    CL8'TSO     '  LOCAL TSO STORAGE VOLUME(S)        85321
DCSYS1   DC    C'SYS1'
         SPACE 1
STCNAMES DC    CL8'WYLBUR',AL1(FGWYL)  MOST FREQUENT STC CASE
STCLEN   EQU   *-STCNAMES      LENGTH OF ONE ENTRY
         DC    CL8'NETKERM',AL1(FGWYL)                           90296
         DC    CL8'EXHNET',AL1(FGEXH)                            90296
         DC    CL8'EXHIBIT',AL1(FGEXH)
         DC    CL8'CICSPROD',AL1(FGCICS)                         92338
         DC    CL8'CICSTEST',AL1(FGCICS)
         DC    CL8'WYLTEST ',AL1(FGWYL)                          92338
         DC    CL8'        ',AL1(0)  ZAP ENTRY
         DC    CL8'        ',AL1(0)  ZAP ENTRY
         DC    CL8'        ',AL1(0)  ZAP ENTRY
         DC    CL8'        ',AL1(0)  ZAP ENTRY
STCNUM   EQU   (*-STCNAMES)/STCLEN    NUMBER OF ENTRIES
         SPACE 1
PATWTO   DC    Y(PATWTOL),X'8000'
         DC    C'MSG963E INVALID DATASET NAME: '                 83136
PATWTOD  DC    CL44' '       FILLER
PATWTOX  DC    X'00000020'   DESC=0; ROUT=11
PATWTOL  EQU   PATWTOX-PATWTO
         EJECT ,
         PRINT &PRTMAC
SAVE     DSECT ,             WORK/SAVE AREA
DB       DS    2D            DOUBLE-WORD WORK AREA
@VOLT    DS    A             ADDRESS IF IGPVOLT
WORKAREA DS    A             ADDRESS OF FIRST (CURRENT) AVAILABLE
WORKLEN  DS    A               LENGTH OF REMAINDER
CURACCT  DS    CL8           CURRENT ACCOUNT NUMBER
CURCLASS DS    C             JOB CLASS
ENVFG    DS    X             ENVIRONMENT FLAG
FGSTC    EQU   X'80'           START TASK
FGTSO    EQU   X'40'           TIME-SHARING
FGWYL    EQU   X'20'           WYLBUR ADDRESS SPACE
FGEXH    EQU   X'10'           EXHIBIT
FGCICS   EQU   X'08'           CICS
FGMUSE   EQU   X'04'           OTHER MULTI-USER ADDRESS SPACE
         SPACE 1
ACTFG    DS    X             ACCOUNT PRIVILEGE FLAGS
         SPACE 1
DSNFG    DS    X             DSNAME PROCESSING FLAGS
FDWYL    EQU   X'80'           WYLBUR DSN
FDLIB    EQU   X'40'           LIBPAK DSN
FDTSO    EQU   X'20'           TS (UID.) - RESERVED
FDTEMP   EQU   X'10'           TEMPORARY/SCRATCH DSN
FDSHRT   EQU   X'08'           UNQUALIFIED NAME; OK EXCEPT ON DASD
FDBAD    EQU   X'02'           INVALID NAME
FDUP     EQU   X'01'           RETURN DSN TO 99RP
MSGFLAG  DS    F'1'          MESSAGE FLAGS
         SPACE 1
ERMWTO   DS    CL(PATWTOL+4) ERROR MESSAGE SPACE
         SPACE 1
         USERVOLT ,          EXPAND VOLUME TABLE
SAVE     DSECT ,
         SPACE 1
PSWDSN   DS    CL44          WORKING COPY                        92300
PSWPSWD  DS    CL8           PASSWORD                            92300
         ORG   ,
SAVEND   EQU   *             END OF DYNAMIC WORK AREA
         SPACE 2
         PRINT &PRTSYS
CVTDSECT DSECT ,             JUST IN CASE
         IHAPSA ,            LOW
         IHAASCB ,
         CVT   DSECT=YES
         IHACDE ,            JUST IN CASE
         IKJTCB ,
         IEFTCT ,
         IEFJMR ,
         IEZJSCB ,                                               83135
         IKJPSCB ,                                               83135
         IRAOUCB ,                                               83135
         IEECHAIN ,
         SPACE 2
         PRINT GEN
         IEFZB4D0 ,
         SPACE 2
S99RBP   DSECT ,             REDEFINE IEFDB401 CALLING SEQUENCE
         ORG   S99RBPTR                                          83114
@RBP     DS    A             REQUEST BLOCK POINTER
@WKA     DS    A             WORK AREA ADDRESS POINTER
@WKL     DS    A               POINTER TO LENGTH OF ABOVE AREA
@JBN     DS    A             POINTER TO JOBNAME OR BLANKS
@PGMN    DS    A             POINTER TO PROGRAMMER NAME
@ACCT    DS    A             POINTER TO ACCOUNTING INFO
@STPN    DS    A             POINTER TO STEPNAME
@PRG     DS    A             POINTER TO PROGRAM NAME
@ACCTX   DS    A             POINTER TO EXEC ACCOUNTING
*     NOTE THAT MOST FIELDS ARE BLANK FOR STEP SCHEDULING
         SPACE 2
         IEFZB4D2 ,
         SPACE 2
         END   ,
