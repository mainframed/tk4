SMF      TITLE '***  REPLACEMENT FOR ''IFASMFDP''  ***'
*
*        THIS PROGRAM DUMPS SMF DATASETS TO TAPE FASTER AND
*        MORE CONVENIENTLY THAN IBM'S DUMP.  OUTPUT DCB
*        PARAMETERS HAVE A RECFM OF U; ANY PROGRAM PROCESSING
*        THIS TAPE AS INPUT MUST FORCE A RECFM OF VBS. BLKSIZE
*        AND LRECL ARE CORRECT.
*        IF THIS PROGRAM IS RUN AFTER BOTH DATASETS ARE CLOSED,
*        A GARBAGE RECORD IS WRITTEN TO FORCE AN AUTOMATIC
*        SMF SWITCH.  RECORD TYPE IS 255, SIZE IS 1200 BYTES
*        THIS SIZE MUST BE LARGER THAN ONE SMF BUFFER.
*
*        THE TAPE DSN IS FORCED TO BE Z908.SMF(CPUID).DAILY(GDG)
*        OUR SYS1.PROCLIB PROCEDURE IS:
*   //SMFDUMP  PROC  CPU=H155,GDG=18
*   //SMFDUMP  EXEC  PGM=SMFDUMP,REGION=900K
*   //SYSUDUMP DD  SYSOUT=A
*   //MANX     DD  DISP=OLD,DSN=SYS1.MANX
*   //MANY     DD  DISP=OLD,DSN=SYS1.MANY
*   //GDGDUMMY DD  UNIT=(3400-5,,DEFER),DSN=Z908.SMF&CPU..DAILY(-&GDG),
*   //             DISP=SHR,DCB=DEN=4,LABEL=EXPDT=99364
*   //DUMPTAPE DD  UNIT=AFF=GDGDUMMY,VOL=REF=*.GDGDUMMY,DCB=DEN=4,
*   //             DISP=(,KEEP,DELETE),DSN=Z908.SMF&CPU..DAILY(+1),
*   //             LABEL=EXPDT=99364
*
*        NOTE THAT THE PROGRAM PERFORMS AN SMF SWITCH AND DUMPS
*        BOTH FILES.  ALL THE DATA MAY BE LOST IF THE SYSTEM
*        OR PROGRAM CRASHES AFTER THE FIRST DATASET IS WRITTEN
*        BUT BEFORE THE PROGRAM FINISHES.
         SPACE 2
         PRINT NOGEN
SMFDUMP  START 0           REDONE FOR DUMPING WITH ACTIVE SMF ON 79217
         REGEQU
         STM   R14,R12,12(R13)
         LR    R12,R15
         USING SMFDUMP,R12
         LA    R9,SAVEREGS
         ST    R9,8(,R13)
         ST    R13,4(,R9)
         LR    R13,R9
         LA    R10,2         FOR LATER BCT
         L     R9,CVTPTR     GET CVT POINTER
         USING CVT,R9
         L     R9,CVTSMCA    GET SMCA
         USING SMCABASE,R9
         MVC   CPUID(4),SMCASID
         MVC   DSNAME+8(4),CPUID
         TITLE '***  CHECK THE SMF GARBAGE  ***'
LOCATE   LOCATE GDG0
         CLI   DATAAREA+4,X'80'  TAPE DEVICE ??
         BNE   NOTGDG
         MVC   MASK(4),DATAAREA+2
        RDJFCB MF=(E,JFCBLIST)
         BXLE  R15,R15,JFCBOK
         WTO   '*** SMFDUMP *** - RDJFCB FAILURE, CALL SYSTEMS',       *
               ROUTCDE=1,DESC=1
         ABEND 111,DUMP
         SPACE
         TITLE '***  GET LOTS OF BUFFER SPACE  ***'
JFCBOK   GETMAIN VC,LA=REQUEST,A=GOTTEN
         BXH   R15,R15,NOCORE
         LM    R1,R2,GOTTEN
         LR    R3,R2        GET GETMAINED LENGTH
         FREEMAIN R,LV=(R2),A=(1)
         S     R3,REQUEST
         BP    *+6
NOCORE   SR    R3,R3
         ST    R3,REQUEST
         USING INFMJFCB,R3
         LA    R3,DUMPJFCB
         CLI   JFCBDSNM+24,C'V' GDG ???
         BNE   NOTGDG
         MVC   DSNAME+11(L'DSNAME-11),JFCBDSNM+11
         MVC   VOLSER,JFCBVOLS
DSNSTART LA    R3,MANXJFCB   GET MANX JFCB ADDRESS
         L     R9,CVTPTR
         USING  CVT,R9
         L     R9,CVTSMCA
         USING SMCABASE,R9
         CLI   SMCAXORY,C'Y'   MANY ACTIVE
         BE    *+8
         LA    R3,MANYJFCB
DSNTEST  CLC   =C'SYS1.MAN',JFCBDSNM
         BNE   NOTMANT
*        TM    SMCASWA,SMCADSTR    BOTH DATA SETS FULL ?
*        BNO  OPENT2
*        MVI   SKIPSWIT+1,X'F0'
OPENT2   OPEN  (DUMPTAPE,(OUTPUT))
         TM    DCBOFLGS-IHADCB+DUMPTAPE,DCBOFOPN
         BO    CALLDUMP
         MVC   WTO+26(8),DCBDDNAM-IHADCB+DUMPTAPE
         B     WTO
CALLDUMP MVI   RECTYP,2     REQUEST HEADER RECORD
         BAL   R11,DUMPREC
       MVC    CALLDUMP+4(2),=X'4700'
         LA    R6,MANX
         L    R9,CVTPTR
         USING CVT,R9
         L    R9,CVTSMCA
         USING SMCABASE,R9
         CLI   SMCAXORY,C'Y'
         BE    *+8
         LA    R6,MANY
         MVC   SAVEXORY,SMCAXORY   SAVE FOR SWITCH TEST
         LA    R7,DUMPTAPE
         MVC    WTO2+34(1),DCBDDNAM+3-IHADCB(R6)
         MVC   DCBDDNAM-IHADCB+MANDUMMY,DCBDDNAM-IHADCB(R6)
         MVC   DCBEXLSA-IHADCB+MANDUMMY,DCBEXLSA-IHADCB(R6)
OPENXORY OPEN  ((R6),(INPUT))
         TM    DCBOFLGS-IHADCB(R6),DCBOFOPN
         BO    GETPUT
         MVC   WTO+26(8),DCBDDNAM-IHADCB(R6)
WTO      WTO   '*** SMFDUMP *** - MANX      MISSING ?? RUN ABORTED',   *
               ROUTCDE=1,DESC=1
         ABEND 333
         TITLE '***  DO THE ACTUAL COPY  ***'
*        GET LOCATE - PUT MOVE    CHAIN SCHEDULING
         SPACE
GETPUT   L     R4,DCBGET-IHADCB(R6)  GET GET ROUTINE ADDRESS
         L     R5,DCBPUT-IHADCB(R7)  GET PUT ROUTINE ADDRESS
         LA    R3,GET                PUT RETURN ADDRESS
GET      LR    R1,R6                 MANX DCB ADDRESS
         LR    R15,R4                GET ROUTINE ADDR.
         BALR  R14,R15               RGET A RECORD
         LR    R0,R1                 SAVE BUFFER ADDR.
         MVC   DCBLRECL-IHADCB(2,R7),0(R1)
         LR    R1,R7                 RDUMPTAPE DCB ADDR.
         LR    R15,R5                GET PUT ROUTINE ADDR.
         LR    R14,R3                GET GET ROUTINE ADDRESS
         BR    R15                   PUT; RETURN TO GET
         SPACE
EODADX   CLOSE ((R6))
         FREEPOOL ((R6))
       MVC   0(MANY-MANX,R6),MANDUMMY
         OPEN   ((R6),(OUTPUT))
         CLOSE   ((R6))
         FREEPOOL ((R6))
WTO2     WTO   '*** SMFDUMP *** - SYS1.MANX DUMPED - INITIALIZED',     *
               ROUTCDE=1,DESC=1
SKIPSWIT NOP   BCTR10ST
         CLOSE DUMPTAPE,TYPE=T
         BAL   R9,CATGDG     CATALOG THE TAPE                    79219
         OI    EXIT+1,X'F0'  BUT ONLY ONCE                       79219
         MODESET KEY=ZERO,ENABLE=NO                              79218
         L    R9,CVTPTR
         USING CVT,R9
         L    R9,CVTSMCA
         USING SMCABASE,R9
         CLC   SMCAXORY,SAVEXORY
         BNE   NOMGCR        SWITCHED BY ITSELF
         OI    SMCAPSTA+TDEVSIZE,SMCAPMTY  JUST EMPTIED
         TM    SMCAPSTA+TDEVSIZE,SMCAPNAV  AVAILABLE ?
         BZ    DOMGCR         YES - ISSUE SWITCH
         NI    SMCAPSTA+TDEVSIZE,255-SMCAPNAV
         MODESET KEY=ZERO,ENABLE=YES  SAFE NOW                   79218
         MVI   RECTYP,X'FF'   MAKE A GARBAGE RECORD
         LA    R1,DUMSMFRC   WRITE DUMMY TO FORCE AUTO-SWITCH
         MVC   DUMSMFRC,=H'1200'
         SMFWTM  (1)         I'D RATHER WRITE THAN SWITCH
         MVC   DUMSMFRC,=H'18'
         STIMER WAIT,BINTVL==A(10*60)
         CLC   SMCAXORY,SAVEXORY
         BNE   NOMGCR
DOMGCR   LA    R0,1   MASTER CONSOLE
         LA    R1,SWITCH
         SVC   34
NOMGCR   MODESET KEY=NZERO,ENABLE=YES                            79218
         MVI   SKIPSWIT+1,X'F0'
        LA    R8,5
STIMER   STIMER WAIT,BINTVL==A(10*60)
         L    R9,CVTPTR
         USING CVT,R9
         L    R9,CVTSMCA
         USING SMCABASE,R9
         CLC   SMCAXORY,SAVEXORY
         BNE   BCTR10ST
         BCT   R8,STIMER
         MVC   BOOWTO+33(9),WTO2+26  SHOW ONLY ONE MAN DUMPED
BOOWTO   WTO   '*** SMFDUMP *** ONLY-----SYS1.MAN? DUMPED.',           *
               ROUTCDE=1,DESC=1
         LA    R10,1         DON'T DUMP SECOND ONE
BCTR10ST BCT   R10,DSNSTART
         MVI   RECTYP,3     REQUEST TRAILER RECORD
         BAL   R11,DUMPREC
         CLOSE DUMPTAPE
         SPACE
         SPACE
EXIT     NOP   EXITALL                                           79219
         LA    R9,EXITALL    SET RETURN FROM IN-LINE SUBROUTINE  79219
CATGDG   CATALOG GDGCAT                                          79219
         BXH   R15,R15,BADCAT
         MVC   WTOCAT+36(6),VOLSER
WTOCAT   WTO   '*** SMFDUMP *** - DUMPTAPE (000000) CATALOGED.',       *
               ROUTCDE=1,DESC=1
         BR    R9            RETURN TO CALLER OR NEXT            79219
EXITALL  L     R13,4(,R13)                                       79219
         LM    R14,R12,12(R13)
         SR    R15,R15
         BR    R14
         SPACE 2
BADCAT   MVC   BADCAT2+36(6),VOLSER                              79219
BADCAT2  WTO   '*** SMFDUMP *** - DUMPTAPE (000000) CATALOG FAILED',   *
               ROUTCDE=1,DESC=1                                  79219
         ABEND 555,DUMP
         TITLE '***  WRITE THE DUMP RECORDS / SYNAD EXITS  ***'
*        WRITE SMF HEADER/TRAILER RECORDS
         SPACE
DUMPREC  TIME  BIN
         STM   R0,R1,DOUBLE  SAVE DOUBLE/DATE
         MVC   TIME(8),DOUBLE
         MVI   RESERVE,X'02'  SHOW SVS TYPE SMF
         MVC   DCBLRECL-IHADCB+DUMPTAPE(2),CTLWDS
         PUT   DUMPTAPE,CTLWDS
         BR    R11           RETURN
         SPACE
NOTMANT  WTO   '*** SMFDUMP ***  DSN NOT= SYS1.MAN?',ROUTCDE=1,DESC=1
         ABEND 444,DUMP
NOTGDG   WTO   '*** SMFDUMP *** - DUMPTAPE NOT A GDG MEMBER ???',      *
               ROUTCDE=1,DESC=1
         ABEND 666,DUMP
SYNADX   DS    0H
SYNADY   DS    0H
         TITLE '***  DCBS - JFCB - SAVEAREAS -  ***'
DOUBLE   DC    D'0'
SAVEREGS DC    18F'0'        REGISTER SAVE AREA
REQUEST  DC    A(4*1024,1024*1024)  GETMAIN MINIMUM - MAXIMUM
GOTTEN   DC    2A(0)                GETMAIN RESULTS
         SPACE
MANX     DCB   DDNAME=MANX,DSORG=PS,MACRF=GL,EXLST=MANXEXIT,OPTCD=C,   *
               SYNAD=SYNADX,EODAD=EODADX,RECFM=U
         SPACE
MANY     DCB   DDNAME=MANY,DSORG=PS,MACRF=GL,EXLST=MANYEXIT,OPTCD=C,   *
               SYNAD=SYNADY,EODAD=EODADX,RECFM=U
         SPACE
MANDUMMY DCB   DDNAME=MANY,DSORG=PS,MACRF=PM,EXLST=MANYEXIT,BUFNO=1,   *
               SYNAD=SYNADY,EODAD=EODADX,RECFM=VBS,LRECL=1000,         *
               BLKSIZE=1000
         SPACE
DUMPTAPE DCB   DDNAME=DUMPTAPE,DSORG=PS,MACRF=PM,OPTCD=C,              *
               RECFM=U,EXLST=DUMPEXIT
         SPACE 2
MANXEXIT DS    0F
         DC    X'05',AL3(DCBEXIT)
         DC    X'87',AL3(MANXJFCB)
MANYEXIT DS    0F
         DC    X'05',AL3(DCBEXIT)
         DC    X'87',AL3(MANYJFCB)
DUMPEXIT DS    0F
         DC    X'05',AL3(DCBEXIT)
         DC    X'87',AL3(DUMPJFCB)
         SPACE
JFCBLIST RDJFCB (MANX,,MANY,,DUMPTAPE),MF=L
         SPACE
*        SMF DUMP HEADER/TRAILER RECORD
         SPACE
         DS    0D
CTLWDS   DC    H'22'         BLOCK
         DC    H'0'          AND
DUMSMFRC DC    H'18'         LRECL
ZEROS    DC    H'0'          COUNTS
RESERVE  DC    X'02'         SYSTEM INDICATOR
RECTYP   DC    CL1' '        RECORD TYPE
TIME     DC    CL4' '        TIME OF RECORD
DATE     DC    CL4' '        DATE OF RECORD
CPUID    DC    CL4' '        SYSTEM AND MODEL
SAVEXORY DC   C' '
        SPACE 2
SWITCH  DS    0H
        DC    AL2(L'SWITCHMS+4,0)
SWITCHMS DC   C'I SMF   '
         TITLE '***  CAMLISTS AND LOCATE DATA AREA  ***'
GDG0     CAMLST NAME,DSNAME,,DATAAREA
DSNAME   DC    CL44'Z908.SMFASID.DAILY(0)'
         SPACE 2
GDGCAT   CAMLST CAT,DSNAME,,VOLUME
VOLUME   DC    H'1'
MASK     DC    X'00000000'   DEVTYPE MASK
VOLSER   DC    CL6' '        VOL SER
         DC    H'0'
         SPACE 2
DATAAREA DS    0D
         DC    265C' '
         ORG   DATAAREA
MANXJFCB DC    CL176' '
MANYJFCB DC    CL176' '
DUMPJFCB DC    CL176' '
         ORG
         TITLE '***  DCB EXIT  ***'
DCBEXIT  L     R12,CSECTADD-DCBEXIT(R15)
         USING SMFDUMP,R12
         LR    R9,R1         SAVE DCB ADDR.
         USING IHADCB,R9
         OC    DCBLRECL,DCBLRECL
         BZ    *+10
         MVC   DCBLRECL-IHADCB+MANDUMMY(2),DCBLRECL
         MVI   DCBBUFNO,1
         LH    R4,DCBBLKSI   BLKSIZE ??
         LTR   R4,R4
         BNZ   MVCBLK
         LH    R4,EIGHT
         STH   R4,DCBBLKSI
         B     MVCBLK+6
MVCBLK   MVC   DCBBLKSI-IHADCB+MANDUMMY(2),DCBBLKSI
         L     R3,REQUEST   GETMAINED CORE
         LTR   R3,R3
         BZR   R14
         LA    R4,128(,R4)   + IOB /ICB OVERHEAD
         SR    R2,R2         ZERO FOR DIVIDE
         DR    R2,R4         # BLKS IN R3
         SRA   R3,1          HALF FOR EACH DCB
         CH    R3,*+10
         BNH   *+8
         LA    R3,50         OVER 50 IT SLOWS DOWN AGAIN         79218
         STC   R3,DCBBUFNO   STASH BUFNO IN DCB
         BR    R14           RETURN
CSECTADD DC    A(SMFDUMP)
EIGHT    DC    H'1000'        DEFAULT BLKSIZE
         LTORG
         TITLE '***  MAPPING EXPANSIONS  ***'
         PRINT GEN
JDSECT   DSECT
         IEFJFCBN
CVT      DSECT
         CVT
         IEESMCA
         IHADCB DSORG=PS
SMFDUMP  CSECT
         DC    1200C' '
         END
