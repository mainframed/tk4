LPAP     TITLE 'L P A P R I N T  ***  PRINT LPA/MLPA DATA'       91158
         COPY  OPTIONGB                                          91158
         SPACE 1                                                 91158
         SYSPARM LIST=YES                                        91158
         SPACE 1                                                 91158
***********************************************************************
*                                                                     *
*                                                                     *
*        COPYRIGHT 1991  EXPERT SYSTEM PROGRAMMING, INC.              *
*        COPYRIGHT 2003  EXPERT SYSTEM PROGRAMMING                    *
*                        176 OLD STAGE COACH ROAD                     *
*                        BRADFORD, VT 05033-8844                      *
*                                                                     *
*                    ALL RIGHTS RESERVED                              *
*                                                                     *
*    THIS MODULE IS DERIVED FROM MODULE EXHCCLPA, ALSO COPYRIGHTED    *
*        BY ESP.                                                      *
*                                                                     *
*    THIS MODULE SORTS LPA ENTRIES BY NAME AND CHECKS FOR DUPLICATION *
*        SET 'MAXMEM' HIGH ENOUGH FOR YOUR SYSTEM (# OF ENTRIES)      *
*                                                                     *
***********************************************************************
         EJECT ,                                                 91158
         PRINT &PRTSOR                                           91158
LPAPRINT PGMHEAD ZERO31,BASE=(R11,R12),PARM=R8,BNDRY=PAGE        91158
***********************************************************************
MAXMEM   EQU   3000          LARGEST NUMBER TO PROCESS           91158
***********************************************************************
         SPACE 1                                                 91158
         SERVINIT ,                                              91158
         SERVCALL LPALD,=CL8'@PRINTER'                           91158
         ST    R0,@PRINTER                                       91158
         PRTOPEN SYSPRINT,OPT=ABEND                              91158
         L     R2,CVTPTR                                         91158
         L     R3,CVTSMCA-CVTMAP(,R2)                            91158
         MVC   SYSNAME,16(R3)  COPY SMCA MODEL/ID                91158
         SH    R2,=H'40'                                         91158
         MVC   SYSSP,0(R2)   JES VERSION                         91158
         MVC   SYSMOD,8(R2)  MODIFICATION LEVEL                  91158
         MVC   SYSCPU,32(R2)  CPU TYPE                           91158
         MVC   SYSREL,36(R2)                                     91158
         PRTLIST HEADER1,TITLE=1                                 91158
         PRTLIST HEADER2,TITLE=3                                 91158
         SPACE 1                                                 91158
*              THIS MODULE SCANS THE VARIOUS LPA CHAINS          91158
*              THE SWITCH LPASW IS USED TO TRACK THE TYPE OF ENTRY
*              LPASW         TYPE
*              0             MASTER SCHEDULER LLE CHAIN
*              LPACDE        MLPA  (VIA CVTQLPAQ)
*              LPATSO        TSLPA (VIA TSCVTLPA CDE CHAIN)
*              LPALPDE       PLPA  (VIA CVTLPDIA)
         SLR   R9,R9         PROVISIONALLY ZERO PARM LENGTH      91158
         LA    R8,0(,R8)     CLEAN PARM POINTER                  91158
         LTR   R8,R8         ANY ?                               91158
         BZ    MOVEPARM      NO                                  91158
         ICM   R8,15,0(R8)   GET ADDRESS OF PARM                 91158
         LA    R8,0(,R8)     CLEAN IT                            91158
         LTR   R8,R8         ANY ?                               91158
         BZ    MOVEPARM      NO                                  91158
         ICM   R9,3,0(R8)    LOAD PARM LENGTH                    91158
         LA    R8,2(,R8)     POINT TO TEXT                       91158
MOVEPARM LA    R14,MYPARM    POINT TO MY PARAMETER AREA          91158
         LA    R15,L'MYPARM  MAX LENGTH LESS PAD                 91158
         MVCL  R14,R8        MOVE PARM                           91158
         OC    MYPARML,BLANKS  UPPER CASE EVERYTHING             91158
         SLR   R2,R2         PROVISIONALLY SET ZERO MASK LENGTH
         MVI   LPASW,LPASLOW PROVISIONALLY SET SLOW LOOKUP       78280
         LA    R5,MYPARM     GET INPUT POINTER                   91158
         MVC   MASKNAME,BLANKS
DOSKIP   BLOOK T=OPTAB,R=MYPARM  CHECK FOR INPUT                 91158
         CLI   0(R5),C' '    ANY ?
         BE    STAMASK       NO; JUST STASH MASK ADDRESS
DOMASK   NI    LPASW,255-LPASLOW  SET FOR FAST LOOKUP            78269
         LA    R2,8          ELSE SET FULL MASK LENGTH
         LA    R0,9
         LA    R1,MASKNAME
MASKLOOP CLI   0(R5),C' '    END OF NAME ?
         BE    DOSKIP         YES - STASH LENGTH
         CLI   0(R5),C','
         BE    DOSKIP         YES - STASH LENGTH
         CLI   0(R5),C'-'    MATCH ALL TRAILING BYTES ?          88242
         BE    CHOPMASK      YES                                 88242
         CLI   0(R5),C'*'    NEW-FANGLED TRAILER ?               88242
         BE    CHOPMASK      YES; PROCESS                        88242
         MVC   0(1,R1),0(R5) COPY NEXT MASK CHARACTER
         CLI   0(R1),C'?'    NEW-FANGLED MATCH CHARACTER ?       88242
         BNE   *+8           NO                                  88242
         MVI   0(R1),C'"'    REPLACE BY OLD MATCH BYTE           88242
         CLI   0(R1),C'"'    IS IT A UNIVERSAL MATCH ?           88274
         BNE   *+8
         OI    LPASW,LPASLOW  YES - CAN'T DO FAST LOOKUP
         LA    R1,1(,R1)
         LA    R5,1(,R5)
         BCT   R0,MASKLOOP
BADPARM  PRTF  '0INVALID OR EXCESSIVE PARM FIELD'                91158
         ABEND 101                                               91158
         SPACE 1                                                 88242
CHOPMASK LA    R2,9          FINAGLE A BIT                       88242
         SR    R2,R0         GET MASK LENGTH                     88242
         OI    LPASW,LPASLOW  MASK; CAN'T DO FAST LOOKUP         88242
         SPACE 1
STAMASK  TR    MASKNAME,TRTAB                                    79078
STAMASK2 LR    R3,R2         COPY MASK LENGTH TO DATA LENGTH     79078
         STM   R2,R3,MASKLENS   SAVE BOTH LENGTHS
         LA    R10,TABSTAR   GET TABLE                           91158
         ST    R10,TABLOW                                        91158
         ST    R10,TABHIGH   FAKE ONE ENTRY                      91158
         L     R1,=A(TABEND-TABSTAR)  OFFSET TO LAST VALID       91158
         LA    R1,TABSTAR(R1)  ENTRY                             91158
         ST    R1,TABSTOP                                        91158
         MVI   TABSEQ,C'A'   ASCENDING SEQUENCE                  91158
         MVI   TABLEN,MODSIZE  ENTRY LENGTH                      91158
         MVI   TABCOO,MODNAME-MODULE  OFFSET TO KEY              91158
         MVI   TABCOL,MODALI-MODNAME  LENGTH OF KEY              91158
         USING MODULE,R10    DECLARE ONE MODULE ENTRY            91158
         XC    MODULE(MODSIZE),MODULE                            91158
         SPACE 1
REPEAT   NI    LPASW,LPASLOW+LPAADDR                             79078
         OI    LPASW,LPANADA  SHOW NOT FOUND                     79078
         L     R2,CVTPTR           CVT PTR
         USING CVTMAP,R2                                         91158
         MVI   WHO,C'M'      SHOW SOURCE LOCATION                91158
         L     R5,CVTQLPAQ
         USING CDENTRY,R5                                        91158
LPALOOP  TM    LPASW,LPALPDE
         BO    NEXTLPDE
         USING CDENTRY,R5                                        91158
NEXTCDE  LAT   R5,CDCHAIN,DONECDE,LA=RX                          78269
THISCDE  LR    R2,R5
         DROP  R5
         USING CDENTRY,R2                                        91158
DOCDE    LA    R14,MASKNAME  LOAD MASK ADDRESS
         LM    R15,R0,MASKLENS  LOAD LENGTHS
         LA    R1,CDNAME     POINT TO NAME                       91158
         BAL   R9,CLCMASK    DO MASK TEST
         BNE   LPALOOP       SKIP IF NO MATCH                    89275
LPAMAP2  MVC   MODLOC,WHO    SHOW WHERE IT CAME FROM             91158
         MVC   MODATTR,CDATTR  COPY ATTRIBUTES                   91158
         TM    LPASW,LPALPDE                                     91158
         BO    ISLPDE2
         TM    CDATTR2,CDAUTH+CDSYSLIB  AUTH MODULE+LIBRARY ?    91158
         BO    ISLPDE2       YES; RETAIN                         91158
         NI    MODATTR2,255-(CDAUTH+CDSYSLIB)  NOT AUTHORIZED    91158
ISLPDE2  MVC   MODNAME,CDNAME   MODULE NAME                      91158
         TR    MODNAME,TRTAB   CHANGE HEX'C0' TO '+'             91158
         MVC   MODUSE,CDUSE     USE / RES COUNT                  91158
         MVC   MODENT,CDENTPT   EP ADDRESS                       91158
         TM    LPASW,LPALPDE       PROCESSING PLPA?
         BZ    TSTAL                NO - DO CDE ALIAS PROCESSING
         NI    MODATTR,CDNIP+CDREN+CDSER+CDMIN+CDNLR             91158
         NI    MODATTR2,CDXLE+CDRLC  KEEP ONLY COMMON            91158
         LR    R9,R5               SET UP LPDE PTR FOR ALIAS PROC
         USING LPDE,R9
TSTLPDAL LA    R3,LPDEXTLN         GET EXTENT IF NOT MINOR
         TM    LPDEATTR,LPDEMIN    ALIAS ?
         BZ    TSTLPDXT             NO - CHECK EXTENT DATA
         MVI   MODALI,C'*'   INDICATE ALIAS                      91158
TSTLPDXT TM    LPDEATT2,LPDEXLE  EXTENT DATA PRESENT?           GP05303
         BNZ   NOTALM        YES; USE THEM                      GP05303
         SPACE 1
*              NOTE THAT ONLY R1-R6 AND R10-R15 ARE
*              GUARANTEED ACROSS CALL TO IEAVVMSR.
         SPACE 1
         L     R3,CVTPTR           SET UP R3 FOR IEAVVMSR
         USING CVTMAP,R3                                         91158
         LM    R0,R1,LPDEMJNM      SET UP R0, R1
         L     R7,CVTLPDSR         FIND MAJOR LPDE
         BALR  R14,R7
         B     ISLPDMAJ
*              IF CONTROL ARRIVES HERE, SOMETHING IS WRONG.
*              R14+4 IS THE RETURN FOR "NOT FOUND":
*              IF THE MAJOR DOES NOT EXIST, WHERE DOES THE MINOR
*                 COME FROM?
         MVI   MODLEN,X'80'        FLAG AS FUNNY                 91158
         MVI   MODSTAR,X'80'                                     91158
         LA    R3,LPDEXTLN         ONE LAST TRY TO FIX UP?
         TM    LPDEATT2,LPDEXLE    ALIAS WITH NO REAL NAME?
         BZ    DOIT                 NO - GIVE UP
         B     NOTALM               YES - USE EXTENT FROM MINOR
ISLPDMAJ LR    R9,R0
         B     TSTLPDAL            PICK UP EXTENTS FROM MAJOR
         DROP  R3,R9
.MVTLP4  ANOP  ,
TSTAL    L     R4,CDXLMJP          EXT LIST OR MAJ CDE ADDRESS
         TM    CDATTR,CDMIN  CDNAME = ALIAS ?
         BZ    TSTALXL
         MVI   MODALI,C'*'   INDICATE ALIAS                      91158
TSTALXL  TM    CDATTR2,CDXLE  EXTENT LIST PRESENT ?             GP05303
         BNZ   NOTAL         YES; USE IT                        GP05303
         TM    CDATTR,CDMIN  CDNAME = ALIAS ?
         BZ    DOIT          NO; SKIP EXTENT DATA
         LR    R2,R4
         B     TSTAL
         SPACE 1
NOTAL    LA    R3,8(,R4)     EXT LIST ADDR + 8 MODULE LENGTH
NOTALM   MVC   MODLEN,0(R3)         MODULE LENGTH                91158
         NI    MODLEN,X'7F'      CLEAN                           91158
         MVC   MODSTAR,4(R3)  STARTING ADDRESS                   91158
         NI    MODSTAR,X'7F'                                     91158
         TM    LPASW,LPAADDR  REQUEST TO LOCATE BY ADDRESS ?     79078
         BZ    DOIT          NO; PROCESS                         91158
         LM    R14,R15,0(R3)  GET LENGTH AND ADDRESS             79078
         C     R15,REQADDR   START ?                             88274
         BH    SKIPIT        NOT; SKIP                           88274
         AR    R14,R15                                           79078
         C     R14,REQADDR                                       79078
         BH    FOUNDADD      FOUND; PROCESS IT                   79078
SKIPIT   XC    MODULE(MODSIZE),MODULE  KILL IT                   91158
         B     DOITTOIT      DOIT, BUT DON'T DO IT               79078
FOUNDADD NI    LPASW,255-LPANADA  SHOW ENTRY FOUND               79078
         SPACE 1                                                 88274
DOIT     C     R10,TABSTOP   VALID ADDRESS ?                     91158
         BNH   DOITWELL      YES                                 91158
         PRTF  ' **** TABLE OVERFLOW - ASSEMBLE WITH LARGER ''MAXMEM'''
         OI    CCODE+L'CCODE-1,8  SET ERROR                      91158
         B     LPADONE       QUIT                                91158
DOITWELL ST    R10,TABPARM+4  SET NEW END ADDRESS                91158
         LA    R10,MODSIZE(,R10)  NEXT ENTRY                     91158
DOITTOIT XC    MODULE(MODSIZE),MODULE  CLEAR IT                  91158
         TM    LPASW,LPAFOUND      SINGLE MODULE & FOUND ?
         BO    LPADONE              YES - NO MORE TO DO
         B     LPALOOP             NO  - GET NEXT ELEMENT
         SPACE 2
DONECDE  OI    LPASW,LPALPDE           FLAG DOING LPDE
         MVI   WHO,C'P'      IDENTIFY OWNER                      91158
         L     R3,CVTPTR
         USING CVTMAP,R3                                         91158
         L     R5,CVTLPDIA         GET PLPA (LPDE) DIRECTORY
         USING LPDE,R5
         SPACE 1
         TM    LPASW,LPASLOW       IS NAME A MASK ?
         BO    TRYLPDE              YES - CAN'T DO DIRECT HASH
         LM    R0,R1,MASKNAME      SETUP R0, R1, R3
         L     R7,CVTLPDSR         FIND LPDE
         BALR  R14,R7
         B     INPLPA              +0 - FOUND
         B     LPADONE             +4 - NOT FOUND                91158
INPLPA   LR    R5,R0
         OI    LPASW,LPAFOUND
         SPACE 1
TRYLPDE  TM    LPDENAME,X'FF'  IS PLPA DONE (FF) OR EMPTY SLOT (00)?
         BO    LPADONE              YES                         GP05303
         BM    THISCDE              NEITHER - FORMAT AS CDE     GP05303
NEXTLPDE LA    R5,LPDEXTAD+4
         B     TRYLPDE             TRY NEXT ENTRY
         SPACE 1
LPADONE  TM    LPASW,LPAADDR+LPANADA  ADDR NOT FOUND ?           79078
         BO    CENTFOR       YES; DISPLAY ADRESS                 79078
         CLI   MASKNAME,C' ' ANY MASK SELECTION ?
         BE    OILPA         NO; LEAVE AS IS
CENTFOR  PRTLIST MSGEOS      SPECIFIC NOT FOUND                  91158
         B     EXIT                                              91158
OILPA    OI    TABHIGH,X'80'   INDICATE END ADDRESS              91158
         SERVCALL SORTH,TABPARM  SORT THE TABLE                  91158
         LA    R10,TABSTAR   GET THE FIRST ENTRY                 91158
LOOPRINT CLI   MODNAME,0     EMPTY ENTRY ?                       91158
         BE    LOOPDONE      YES; QUIT                           91158
         LA    R1,NUMMLPA                                        91158
         CLI   MODLOC,C'M'                                       91158
         BE    *+8           YES                                 91158
         LA    R1,NUMPLPA                                        91158
         INC   0(R1)         INCREMENT MODULE COUNT              91158
         CLC   MODNAME,MODNAME+MODSIZE  DUPLICATE ?              91158
         BNE   LOOPRONE      NO                                  91158
         CLC   MODSTAR,MODSTAR+MODSIZE  SAME LOAD ADDRESS ?      91158
         BE    LOOPRONE      YES; NOT DUPLICATION                91158
         PRTROOM 2           ROOM FOR TWO LINES                  91158
         PRTLIST MSGMOD      PRINT THE FIRST ENTRY               91158
         LA    R10,MODSIZE(,R10)  PRINT THE SECOND               91158
         OI    LPASW,LPADUPE  SHOW DUPLICATE                     91158
         CLI   MODALI,C'*'   IS THIS AN ALIAS ?                  91158
         BE    LOOP2ALI      YES                                 91158
         ICM   R14,15,MODLEN  GET MODULE LENGTH                  91158
         INC   NUMWASTE,INC=(R14)  INCREASE STORAGE SIZE         91158
         INC   NUMDUPE       INCREMENT DUPLICATE COUNT           91158
LOOP2ALI LA    R1,NUMMLPA                                        91158
         CLI   MODLOC,C'M'                                       91158
         BE    *+8           YES                                 91158
         LA    R1,NUMPLPA                                        91158
         INC   0(R1)         INCREMENT MODULE COUNT              91158
LOOPRONE PRTLIST MSGMOD      PRINT THE ENTRY                     91158
         NI    LPASW,255-LPADUPE                                 91158
         LA    R10,MODSIZE(,R10)                                 91158
         B     LOOPRINT                                          91158
LOOPDONE PRTROOM 5                                               91158
         PRTSPACE 2                                              91158
         PRTLIST MSGSUM      PRINT SUMMARY                       91158
EXIT     SERVTERM ,                                              91158
         LH    R9,CCODE                                          91158
         PGMEXIT RC=(R9)     RETURN WITH CONDITION               91158
         DROP  R2,R3,R5                                          91158
         SPACE 3
CLCMASK  CMASK ML=R15,L=R0,MA=R14,A=R1,RET=R9
         SPACE 3
DOEP     DS    0H
DOLOC    SLR   R0,R0         ZERO ACCUMULATOR                    79078
         MVC   MASKNAME+1(6),0(R5)  SHOW IN NAME                 79078
         LA    R2,8          MAX LENGTH OF 8 BYTES               91158
         LA    R3,HEXTAB+15  MAKE OFFSET FOR LAST HEX BYTE       91158
         LA    R4,16         MAKE INDEX                          79078
LH1      CLI   0(R5),C'A'    HEX ?                               79078
         BL    LHS           DEFINITELY NOT - STOP SCANNING      79078
         LR    R14,R3        COPY TABLE ADDRESS                  79078
         LR    R15,R4        COPY INDEX                          79078
LH2      CLC   0(1,R14),0(R5)   COMPARE ONE BYTE AGAINST TABLE ? 79078
         BE    LH3           FOUND                               79078
         BCTR  R14,0         PREVIOUS TABLE ENTRY                79078
         BCT   R15,LH2       TRY AGAIN                           79078
         B     BADPARM       NOT FOUND                           91158
         SPACE 1                                                 79078
LH3      BCTR  R15,0         MAKE TRUE VALUE                     79078
         SLL   R0,4          SHIFT LEFT                          79078
         OR    R0,R15        INSERT NEXT DIGIT                   79078
         LA    R5,1(,R5)     NEXT I/P                            79078
         BCT   R2,LH1        TRY NEXT ONE                        79078
LHS      LTR   R0,R0         ANY VALUE GOTTEN ?                  79078
         BNP   BADPARM       NO, TOO BAD                         91158
         CLC   0(8,R5),BLANKS  TRAILING BLANKS ?                 91158
         BNE   BADPARM       NO; TOO BAD                         91158
         OI    LPASW,LPAADDR+LPASLOW  HEX FOUND; NO LOOK-UP      79078
         ST    R0,REQADDR    ELSE SAVE IT                        79078
         SLR   R2,R2         CLEAR MASK LENGTH                   79078
         B     STAMASK2      START PROCESSING                    79078
         SPACE 1                                                 91158
OPTAB    BTAB  'EP=',DOEP,BASE=*
         BTAB  'X=',DOLOC
         BTAB  'A=',DOLOC                                        79078
         BTAB  'MEMBER=',DOMASK                                  91158
         BTAB  'NAME=',DOMASK                                    91158
         BTAB  'MASK=',DOMASK                                    91158
         BTAB  'MODULE=',DOMASK                                  91158
         BTAB  'MOD=',DOMASK                                     91158
         BTAB  'MEM=',DOMASK                                     91158
         BTAB  ',',DOSKIP
         BTAB  *END
         SPACE 2
EOFMSG   DC    C'0** END OF LPA MAP **'                          91158
ZEROES   DC    XL8'0'                                            91158
BLANKS   DC    CL132' '      BLANK CONSTANT                      91158
MSGMOD   FDTM  LPASW,LPADUPE,BZ=MSGMOD1  DUPLICATED MODULE ?     91158
         FD    MODNAME,INTENSE,NL  MAKE DARKER                   91158
         FDGOTO MSGMODC      JOIN COMMON                         91158
MSGMOD1  FD    ' ',NL        COMPENSATE FOR 'INTENSE' ERROR      91158
         FD    MODNAME                                           91158
MSGMODC  FD    MODLOC,PADL                                       91158
         FD    'LPA',PADR                                        91158
         FD    MODALI,PAD,DEB,LEN=1  ALIAS FLAG                  91158
         FD    MODENT,HEX,PAD                                    91158
         FD    MODSTAR,HEX,PAD                                   91158
         FD    MODLEN,HEX,PAD                                    91158
         FDTM  MODATTR,CDREN,BNO=MSGMREUS                        91158
         FD    'RENT',PAD                                        91158
         FDGOTO MSGMOLAY     IF RENT, MUST BE REUS - SO SKIP IT  91158
MSGMREUS FDTM  MODATTR,CDSER,BNO=MSGMOLAY  SERIALLY REUSEABLE ?  91158
         FD    'REUS',PAD                                        91158
MSGMOLAY FDTM  MODATTR2,CDOLY,MSGMREFR                           91158
*WRONG*  FD    'OVERLAY',PAD                                     91158
MSGMREFR DS    0X  FDTM MODATTR,CDREFR,BNO=MSGMAUTH  NONE SUCH   91158
MSGMAUTH FDTM  MODATTR2,CDAUTH,BNO=MSGMNFN                       91158
         FD    'AUTH',PAD                                        91158
MSGMNFN  FDTM  MODATTR,CDNFN,BNO=MSGMDONE                        91158
*WRONG?  FD    '***NON-FUNCTIONAL***',PAD                        91158
MSGMDONE FDTM  LPASW,LPADUPE,BZ=MSGMDUPL                         91158
         FD    '*** EXTRA COPY ***',INTENSE,PAD                  91158
MSGMDUPL FD    *END                                              91158
MSGEOS   FD    '** END OF LPA FOR',NL                            91158
         FD    MASKNAME,DEB,PAD                                  91158
         FD    '**'                                              91158
         FD    *END                                              91158
         SPACE 1                                                 91158
MSGSUM   FD    NUMMLPA,ICM,NL,PAD,RADJ,LEN=15                    91158
         FD    'MODULES IN MLPA'                                 91158
         FD    NUMPLPA,ICM,NL,PAD,RADJ,LEN=15                    91158
         FD    'MODULES IN PLPA'                                 91158
         FDCLC NUMDUPE,ZEROES,4,BE=MSGSUMX                       91158
         FD    NUMDUPE,ICM,NL,PAD,RADJ,LEN=15                    91158
         FD    'EXTRA COPIES WASTING'                            91158
         FD    NUMWASTE,HEX,PADL                                 91158
         FD    '('                                               91158
         FD    NUMWASTE,I                                        91158
         FD    ') BYTES'                                         91158
MSGSUMX  FD    *END                                              91158
         SPACE 2
HEXTAB   DC    C'0123456789ABCDEF'                               91158
HEXTRTAB EQU   HEXTAB-C'0',256,C'C'                              91158
TRTAB    TRTAB CODE=TN,OPT=(SVC,ERR)                             91158
         ORG   TRTAB+C'+'
         DC    X'C0'
         ORG   ,
         SPACE 1                                                 91158
HEADER1  FDOPT NL,CC=C'#'    AUTO PAGE/TIME/DATE                 91158
         FD    'L P A P R I N T  ***  LISTING OF LPA MODULES'    91158
         FD    'ON',PAD                                          91158
         FD    SYSCPU,HEX,DEBZ,DEB,PAD                           91158
         FD    'SYSTEM',PAD                                      91158
         FD    SYSNAME       SMCA ID                             91158
         FD    SYSREL,PAD                                        91158
         AIF   (NOT &MVSSP).NOSPMOD
         FD    SYSSP                                             91158
         FD    SYSMOD,PAD                                        91158
.NOSPMOD FD    *END                                              91158
         SPACE 1                                                 91158
HEADER2  FD    ' MODULE',NL,LEN=10                               91158
         FD    'LOC AL',LEN=6                                    91158
         FD    'ENTRY',RADJ,PAD,LEN=8                            91158
         FD    'START',RADJ,PAD,LEN=8                            91158
         FD    'LENGTH',RADJ,PAD,LEN=8                           91158
         FD    '  ATTRIBUTES'                                    91158
         FD    *END                                              91158
         SPACE 2                                                 91158
         LTORG ,             SPIT IT OUT                         91158
         SPACE 2                                                 91158
SYSPRINT PRTWORK SYSPRINT,TITLE=5                                91158
         SPACE 1                                                 91158
MODULE   DSECT ,             SINGLE ENTRY                        91158
MODNAME  DS    CL8           MODULE NAME (MODIFIED IF SVC)       91158
MODLOC   DS    C             'M' OR 'P' FOR SOURCE LOCATION      91158
MODALI   DS    C             ALIAS FLAG                          91158
MODENT   DS    AL4           ENTRY POINT ADDRESS                 91158
MODSTAR  DS    AL4           MODULE LOAD ADDRESS                 91158
MODEND   DS    AL4           MODULE END ADDRESS                  91158
MODLEN   DS    AL4           LENGTH OF MODULE                    91158
MODUSE0  DS    X             4 BITS OF USE COUNT FROM CDROLL     91158
MODUSE   DS    X             8 BITS OF USE COUNT FROM CDUSE      91158
*                                                 OR FROM LPDEUSE
MODRESP  DS    XL2                 RESPONSIBILITY COUNT          91158
MODATTR  DS    XL2                                               91158
MODATTR2 EQU   MODATTR+1,1,C'X'                                  91158
MODSIZE  EQU   *-MODULE      SIZE OF ONE ENTRY                   91158
         SPACE 1                                                 91158
SAVE     DSECT ,             SAVE AND WORK AREA                  91158
DB       DS    D             WORK WORD                           91158
DB2      DS    D             ANOTHER                             91158
@SERVICE DS    A             @SERVICE ADDRESS                    91158
@PRINTER DS    A             @PRINTER ADDRESS                    91158
MASKNAME DS    CL8
MASKLENS DS    2F
REQADDR  DC    A(0)          ZERO OR REQUESTED ADDRESS           91158
NUMMLPA  DS    F             ENTRIES IN MLPA                     91158
NUMPLPA  DS    F             ENTRIES IN PLPA                     91158
NUMDUPE  DS    F             DUPLICATED COUNT                    91158
NUMWASTE DS    F             SIZE WASTED                         91158
TABPARM  SERVSORT DSECT=NO,PFX=TAB   SORT PARM LIST              91158
TABSTOP  DS    A(TABEND)     LAST AVAILABLE ADDRESS              91158
LPASW    DC    X'00'
LPACDE   EQU   X'80'               PROCESSING CDE S FROM CVTQLPAQ
LPATSO   EQU   X'40'               PROCESSING CDE S FROM TSLPA
LPALPDE  EQU   X'20'               PROCESSING LPDE S
LPASLOW  EQU   X'10'               FULL MODULE NAME NOT SUPPLIED
LPAFOUND EQU   X'08'               SPECIFIED MODULE FOUND
LPANADA  EQU   X'04'         LOCATE BY ADDRESS - NOT FOUND       79078
LPAADDR  EQU   X'02'         LOCATE BY ADDRESS                   79078
LPADUPE  EQU   X'01'           MLPA/PLPA DUPLICATION             91158
SYSNAME  DS    CL4           SMCA SYSTEM ID                      91158
SYSCPU   DS    XL4           CPU TYPE                            91158
SYSSP    DS    CL8           SP VERSION                          91158
SYSREL   DS    CL4           OS MVS VERSION                      91158
SYSMOD   DS    CL8           SYSTEM LEVEL                        91158
CCODE    DS    H             RETURN CODE                         91158
WHO      DS    C             'M' OR 'P'                          91158
MYPARML  DS    0CL132        LOCAL COPY OF EXEC PARM             91158
MYPARM   DS    CL125         PARM WITH STOPPER                   91158
         ORG   MYPARM        RE-USE FOR WORK SPACE               91158
TABSTAR  DS    (MAXMEM)XL(MODSIZE)  ROOM FOR LPA DATA            91158
TABEND   DS    2XL(MODSIZE)  STOPPER + OVERFLOW                  91158
         ORG   ,                                                 91158
SAVEEND  EQU   *             END OF WORK SPACE                   91158
         SPACE 2
         CVT   DSECT=YES                                         91158
         IHACDE ,                                                91158
         IHALLE ,                                                91158
         IHALPDE ,                                               91158
         END   ,                                                 91158
