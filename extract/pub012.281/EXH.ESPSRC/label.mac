TLBL     TITLE '*** INVOKE LABEL SVC 39 FROM OPERATOR CONSOLE ***'
         COPY  OPTIONGB                                          94251
         SPACE 1                                                 94251
         SYSPARM LIST=YES                                        94251
         SPACE 1                                                 94251
***********************************************************************
**                                                                   **
**   AUTHOR:  MICHAEL RADZYMSKI, EXPERT SYSTEM PROGRAMMING, INC.     **
**                                                                   **
**   START JOB FOR WRITING STANDARD LABEL TAPES (DRIVE TYPE AND      **
**     ALLOWABLE DENSITY RANGE DETERMINED BY START PROCEDURE AND     **
**     NOT CHECKED)                                                  **
**                                                                   **
**   //LABEL PROC T=3400-5                                           **
**   //IEFPROC EXEC PGM=LABEL                                        **
**   //STEPLIB DD ...  AS REQUIRED                                   **
**   //IEFRDER DD VOL=SER=TAPLAB,DISP=(,KEEP),UNIT=(&T,,DEFER),      **
**   //           LABEL=(,BLP,EXPDT=98000)                           **
**                                                                   **
***********************************************************************
         PRINT &PRTSOR                                           94251
LABEL    PGMHEAD ZERO,BASE=R12                                   94251
         SPACE 2
         EXTRACT COMMADDR,FIELDS=COMM
         L     R15,COMMADDR            ECB/CIB POINTER
         L     R8,0(,R15)              STOP ECB
         ST    R8,ECBLIST
*            DON'T USE R8 FOR ANYTHING ELSE
         SPACE 2
WTORINCR XC    WTORECB,WTORECB
         WTOR  '*** LABEL ***  INCREMENT VOLSER BY 1 ?  REPLY Y/N.',   *
               INCR,1,WTORECB,ROUTCDE=(1),DESC=(2,7)
         ST    R1,DOMSAVE
         WAIT  1,ECBLIST=ECBLIST
         TM    0(R8),X'40'
         BO    EXIT
         L     R1,DOMSAVE
         DOM   MSG=(1)
         OI    INCR,C' '
         TM    0(R8),X'40'
         BO    EXIT
         SPACE 2
         L     R15,PSATOLD-PSA
         USING TCB,R15
         L     R15,TCBTIO
         USING TIOT1,R15
         LA    R14,TIOENTRY  1ST DD ENTRY
         SLR   R10,R10
         SLR   R11,R11
TIOTLOOP CLC   DDNAME,4(R14)
         BE    GETUCB
TIOTBUMP IC    R11,0(R14)     ENTRY LENGTH
         AR    R14,R11
         CLI   0(R14),X'00'            END OF TABLE ?
         BNE   TIOTLOOP
         B     EXIT
         USING UCBOB,R10
GETUCB   ICM   R10,B'0111',17(R14)      TIOEFSRT UCB ADDRESS
         BZ    TIOTBUMP
         MVC   SAVEUCB,UCBNAME         SAVE FOR MY WTO
         MVI   COMMAND,X'00'           ASSUME CARTRIDGE DRIVE
         TM    UCBTBYT4,UCB3400
         BNO   *+8
         MVI   COMMAND,X'C3'           1600 BPI
WTORVOL  TM    0(R8),X'40'
         BO    EXIT
         SPACE 2
         XC    WTORECB,WTORECB
         MVC   REPLY,BLANKS
         WTOR  '*** LABEL *** END OR VOLSER ?',REPLY,6,WTORECB,        *
               ROUTCDE=(1),DESC=(2,7)
         ST    R1,DOMSAVE      SAVE FOR MSG DELETION
         WAIT  1,ECBLIST=ECBLIST
         TM    0(R8),X'40'
         BO    EXIT
         L     R1,DOMSAVE
         DOM   MSG=(1)          DELETE MESSAGE
         OC    REPLY,BLANKS     UPPER CASE
         CLC   =C'END',REPLY    FINISHED ???
         BE    EXIT
         MVC   VOLSER,REPLY     ELSE ASSUME VOLSER PRESENT
         SPACE 2
         TITLE '*** INVOKE SVC 39 ***'
LABELSVC TM    0(R8),X'40'
         BO    EXIT
         MVC   WTOUCB+38(3),SAVEUCB
         MVC   WTOUCB+28(6),VOLSER
         PUSH PRINT                                              94251
         PRINT GEN
WTOUCB   WTO   '*** LABEL *** MOUNT ?????? ON ???',ROUTCDE=1,DESC=(2,7)
         POP   PRINT                                             94251
         ST    R1,SAVEWTO
         LA    R1,SVCLIST       GET SVC 39 PARAMETER LIST
         MVC   TENBYTES+4(6),VOLSER
         SVC   39
         LTR   R15,R15          GOOD IO ???
         BNZ   WTOERROR
         L     R1,SAVEWTO
         DOM   MSG=(1)
         TM    0(R8),X'40'    OPERATOR WANT TO QUIT ??
         BO    EXIT
         CLI   INCR,C'N'
         BE    WTORVOL           GET NEXT VOLUME FROM OPERATOR
         LA    R15,VOLSER+6
TRLOOP   BCTR  R15,0
         C     R15,=A(VOLSER)
         BL    WTORVOL
         CLI   0(R15),C' '
         BE    TRLOOP
         TM    0(R15),X'F0'
         BNO   WTORVOL
         TR    0(1,R15),TRTAB
         CLI   0(R15),C'0'
         BE    TRLOOP
HALTTEST TM    0(R8),X'40'    OPERATOR WANT TO QUIT ??
         BNO   LABELSVC
         B     EXIT
         SPACE 3
WTOERROR B     *+0(R15)
         B     OPDENIAL           R15=4
         B     IOERROR            R15=8
         B     PIOERROR           R15=12
         SPACE 2
OPDENIAL WTO   '*** LABEL *** OPERATOR DENIED LABEL REQUEST',          *
               ROUTCDE=(1),DESC=(2,7)
         DOM   MSG=(1)
         L     R1,SAVEWTO
         DOM   MSG=(1)
         B     WTORVOL       START ANOTHER ONE
IOERROR  WTO   '*** LABEL *** INVALID PARAMETER LIST,NOTIFY SYSTEMS',  *
               ROUTCDE=(1),DESC=(2,7)
         DOM   MSG=(1)
         L     R1,SAVEWTO
         DOM   MSG=(1)
         ABEND 999,DUMP
PIOERROR WTO   '*** LABEL *** PERMANENT I/O ERROR',ROUTCDE=(1),        *
               DESC=(2,7)
         DOM   MSG=(1)
         L     R1,SAVEWTO
         DOM   MSG=(1)
         B     WTORVOL
         SPACE 2
EXIT     L     R1,DOMSAVE
         DOM   MSG=(1)
         WTO   '*** LABEL *** OPERATOR REQUESTED STOP',ROUTCDE=(1),    *
               DESC=(2,7)
         DOM   MSG=(1)
         PGMEXIT RC=0                                            94251
         TITLE '*** CONSTANTS AND PARAMETER LISTS ***'
         DS    0F
TLABEL   DS    0CL80
VOL1     DC    CL4'VOL1'
VOLSER   DC    CL6'??????'
SECURITY DC    X'00'           MUST BE ZERO RESERVED
         DC    CL69' '
TENBYTES DC    CL10'VOL1??????'
         SPACE 2
         DS    0F
SVCLIST  DS    0CL20
SVCRUNOP DC    X'C00006'       UNLOAD OPTION
SVCUCB   DC    X'00'           UCB SEQUENCE NUMBER IN TIOT
SVCDDPTR DC    A(DDNAME)       TAPE DDNAME POINTER
SVCVOL1P DC    A(VOL1)         VOL1 POINTER
LABELLEN DC    AL2(80)         VOL1 LENGTH
LABELNUM DC    AL1(1)
COMMAND  DC    X'D3'           6250 BPI
SVCHDR1P DC    A(TENBYTES)     VOL1 POINTER
         SPACE 2
COMMADDR DC    F'0'
ECBLIST  DS    0F
         DC    A(0)
         DC    X'80',AL3(WTORECB)
WTORECB  DC    F'0'            WTOR ECB
DOMSAVE  DC    F'0'            SAVE AREA FOR DOM DE DOM DOM
SAVEWTO  DC    F'0'
INCR     DC    C'N'
DUMREPLY DC    C' '
REPLY    DC    CL6' '          WTOR REPLY AREA
BLANKS   DC    CL6' '          FILLER
DDNAME   DC    CL8'IEFRDER '                                     94251
SAVEUCB  DC    CL3' '
TRTAB    DC    256AL1(*-TRTAB)
         ORG   TRTAB+C'0'
         DC    C'1234567890'
         ORG   ,
         SPACE 1                                                 94251
SAVE     DSECT ,                                                 94251
SAVEEND  EQU   *                                                 94251
         SPACE 1                                                 94251
         PRINT &PRTSYS                                           94251
         IHAPSA ,
         IEFUCBOB ,
         IKJTCB LIST=NO
TIOT     DSECT ,
         IEFTIOT1 ,
         END   ,                                                 94251
