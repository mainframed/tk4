IEFU29   TITLE 'I E F U 2 9  ***  SMF SWITCH EXIT FOR MVS'
         COPY  OPTIONGB      GET GLOBAL OPTIONS
         SPACE 1
         SYSPARM LIST=YES    DISPLAY OPTIONS
         EJECT ,
         LCLC  &PGM          NAME OF DUMP PROGRAM
&PGM     SETC  'SMFDUMP '    TRAILING BLANK IF NON-SPECIFIC
*        NO TRAILING BLANK IF PROC HAS DSN PARAMETER (N=)
*
         PRINT &PRTSOR
IEFU29   PGMHEAD ZERO12,BASE=R12,PARM=R9                        GP12043
         SERVINIT ,                                             GP12043
         MVI   RETCH+L'RETCH-1,MSGPRT  PRINT DS-FULL MSG
         ICM   R8,15,0(R9)   LOAD AND TEST ADDRESS
         USING EXITPARM,R8   DECLARE IT
         CLI   SUFFIX,C'Y'   MAJOR DATASETS FULL ?
         BL    GOAWAY        NOT YET; IGNORE
         MVI   RETCH+L'RETCH-1,MSGSUP  SUPPRESS CONSOLE MSG
         L     R3,CVTPTR     GET THE CVT
         USING CVTMAP,R3
         SERVCALL GASJB,PROCNAME    SEE IF RUNNING              GP12043
         LTR   R0,R0         FOUND AN ASCB ?
         BNZ   GOAWAY        YES; NO NEED TO START AGAIN
         MVC   COMMAND,CMDPAT  MOVE START COMMAND
         MVC   COMMAND+DSOFF(L'DSOFF),SUFFIX  COPY SUFFIX
         SLR   R0,R0                   CLEAR REG ZERO FOR SVC 34
         MGCR  COMMAND       ISSUE START COMMAND
         SPACE 1
MSGPRT   EQU   0             PRINT IEE362X OPER. MSG
MSGSUP   EQU   4             SUPPRESS CONSOLE MESSAGE
GOAWAY   SERVTERM ,          CLEAN UP                           GP12043
         L     R9,RETCODE    LOAD THE RETURN CODE               GP12043
         PGMEXIT RC=(R9)     RETURN TO CALLER                   GP12043
         EJECT ,
CMDPAT   DC    Y(CMDPATND-*,0)  RDW
         DC    C'S &PGM,N=0 '
DSOFF    EQU   *-2-CMDPAT,1,C'C'
CMDPATND EQU   *
PROCNAME DC    CL(8+8+8)'&PGM '  MUST MATCH CMDPAT
         SPACE 1
SAVE     DSECT ,             SAVE AND WORK AREA
         SERVDEFS ,                                             GP12043
RETCH    EQU   RETCODE+3,1,C'X'                                 GP12043
         DS    0F
COMMAND  DS    XL(CMDPATND-CMDPAT)
SAVEEND  EQU   *             END OF WORK                        GP12043
         SPACE 1
EXITPARM DSECT ,
SYSDSN   DS    C'SYS1.MAN? '
SUFFIX   EQU   *-2,1,C'C'    OFFSET TO VARIABLE PORTION
         SPACE 1
         PRINT &PRTSYS
         IHAASCB ,
         IHAASVT ,
         CVT   DSECT=YES
         END   ,
