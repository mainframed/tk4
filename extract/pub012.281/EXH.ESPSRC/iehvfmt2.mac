FMT2     TITLE 'DSCB2'
         PRINT NOGEN
         MACRO
&NAME    TAB
         LCLC  &FL,&TL                                           81117
&FL      SETC  'L''&SYSLIST(1)'
         AIF   (N'&SYSLIST(1) LT 2).A
&FL      SETC  '&SYSLIST(1,2)'
.A       ANOP
&TL      SETC  'L''&SYSLIST(2)'
         AIF   (N'&SYSLIST(2) LT 2).B
&TL      SETC  '&SYSLIST(2,2)'
.B       ANOP
&NAME    DC S(&SYSLIST(1,1)),AL2(&FL),S(&SYSLIST(2,1)),AL2(&TL)  81117
         MEND
         EJECT
DSCB2    CSECT
         SAVE  (14,12),,*          SAVE REGS
         LR    12,15               SET BASE
         USING DSCB2,12
         LR    11,13               FREE 13
         LA    13,D2SAVE           GET NEW SAVE
         ST    13,8(11)            CHAIN BACK
         ST    11,D2SAVE+4         CHAIN FORWARD
         LM    10,11,0(1)          GET PARMS
         USING DS2ADDR,10
         USING FMT2,11
         MVC   FM2FMTID,DS2FMTID   USE ACTUAL FORMAT
         CALL  PUTADDR,(CVHTAB2)     DO HEX CONVERSIONS
         CALL  CVD,(CVDTAB2)       DO DEC CONVERSIONS
         MVC   FM2KSCK(2),=C'NOT'
         MVC   FM2ILCM(3),=C'NOT'
         TM    DS2STIND,X'40'  KEY SEQ. CHECK ?
         BZ    DSCB2K        NO
         MVC   FM2KSCK(2),FM2KSCK-1  BLANK OUT
DSCB2K   TM    DS2STIND,X'20'  INTL. LOAD COMPLETE ?
         BZ    DSCB2L        NO
         MVC   FM2ILCM(3),FM2ILCM-1  BLANK OUT
DSCB2L   MVC   FM2STNDB,=CL6'IS NOT'  SET LAST-BLOCK NOT FILL
         TM    DS2STIND,X'02'           IS IT FULL
         BZ    DSCB2A                   NO
         MVC   FM2STNDB,=CL6'  IS  '    YES
DSCB2A   MVC   FM2STNDT,=CL6'IS NOT'    SET LAST-TRACK NOT FULL
         TM    DS2STIND,X'01'           IS IT FULL
         BZ    DSCB2B                   NO
         MVC   FM2STNDT,=CL6'  IS  '    YES
DSCB2B   CALL  PRINT,(DSCB2OUT,DSCB2SIZ)
         L     13,D2SAVE+4         RESTORE 13
         RETURN (14,12),T          RETURN
DSCB2SIZ DC    H'16,17'  -- MOVED CLOSER TO FMT1
D2SAVE   DS    18F
CVHTAB2  TAB   DS2ADLIN,FM2ADLIN
         TAB   DS2LMSAD,FM2LMSAD
         TAB   DS22MIND,FM22MIND
         TAB   DS2L2MEN,FM2L2MEN
         TAB   DS23MIND,FM23MIND
         TAB   DS2L3MIN,FM2L3MIN
         TAB   DS2CYLAD,FM2CYLAD
         TAB   DS2LCYAD,FM2LCYAD
         TAB   DS2LTRAD,FM2LTRAD
         TAB   DS21RCYL,FM21RCYL
         TAB   DS2LTCYL,FM2LTCYL
         TAB   DS2LPRAD,FM2LPRAD
         TAB   DS2LOVAD,FM2LOVAD
         TAB   DS2ADDR,FM2ADDR
         TAB   DS2PTRDS,FM2PTRDS
         TAB   DS2ADHIN,FM2ADHIN
         DC    H'0'
CVDTAB2  TAB   DS2NOLEV,FM2NOLEV
         TAB   DS2DVIND,FM2DVIND
         TAB   DS2HIRIN,FM2HIRIN
         TAB   DS2NOTRK,FM2NOTRK
         TAB   DS2NOBYT,FM2NOBYT
         TAB   DS2HIRTI,FM2HIRTI
         TAB   DS2PRCTR,FM2PRCTR
         TAB   DS2HIRPR,FM2HIRPR
         TAB   DS2RSHTR,FM2RSHTR
         TAB   DS2TAGDT,FM2TAGDT
         TAB   DS2CYLOV,FM2CYLOV
         TAB   DS2RORG1,FM2RORG1
         TAB   DS2OVRCT,FM2OVRCT
         TAB   DS2RORG2,FM2RORG2
         TAB   DS2BYOVL,FM2BYOVL
         TAB   DS2HIROV,FM2HIROV
         TAB   DS2RORG3,FM2RORG3
         DC    H'0'
ZAP      DC    16CL132' '
         ORG   ZAP
DSCB2OUT DC    C'0'          -- MOVED CLOSER TO FMT1
FM2ADDR  DC    CL12' ',C'  FMT='
FM2FMTID DC    C' ',C'       (ISAM ORGANIZATION)  '
         DC    C'  THERE ARE '
FM2NOLEV DC    CL3' ',CL33' INDEX LEVELS'
         DC    CL7' '
         DC    C'DSCB EXTENDED AT '
FM2PTRDS DC    CL12' '
         ORG   DSCB2OUT+132
FM2LINE2 DC    C'      THE NUMBER OF TRACKS DETERMINING MASTER INDEX DEX
               VELOPMENT IS '
FM2DVIND DC    CL3' ',C'  GREATEST R ON HIGH-LEVEL INDEX TRACK IS '
FM2HIRIN DC    CL3' ',CL10' '
         ORG   FM2LINE2+132
FM2LINE3 DC    C'      MBBCCHH OF FIRST TRACK OF(LOWEST LEVEL MASTER INX
               DEX='
FM2ADLIN DC    CL17' ',C' HIGHEST LEVEL MASTER INDEX='
FM2ADHIN DC    CL17' ',CL14')'
         ORG   FM2LINE3+132
FM2LINE4 DC    C'      LAST MASTER INDEX ENTRY CCHHR='
FM2LMSAD DC    CL12' ',C'  HIGHEST-LEVEL INDEX(SIZE IS '
FM2NOTRK DC    CL3' ',C' TRACKS, LENGTH IS '
FM2NOBYT DC    CL5' ',CL45' BYTES)'
         ORG   FM2LINE4+132
FM2LINE5 DC    C'      MASTER INDEX LEVEL 2(MBBCCHH='
FM22MIND DC    CL17' ',C', LAST ACTIVE ENTRY CCHHR='
FM2L2MEN DC    CL12' ',CL50')'
         ORG   FM2LINE5+132
FM2LINE6 DC    C'      MASTER INDEX LEVEL 3(MBBCCHH='
FM23MIND DC    CL17' ',C', LAST ACTIVE ENTRY CCHHR='
FM2L3MIN DC    CL12' ',CL50')'
         ORG   FM2LINE6+132
FM2LINE7 DC    C'      CYLINDER INDEX      (MBBCCHH='
FM2CYLAD DC    CL17' ',C', LAST ACTIVE ENTRY CCHHR='
FM2LCYAD DC    CL12' ',CL50')'
         ORG   FM2LINE7+132
FM2LINE8 DC    C'      TRACK INDEX         (GREATEST R OF UNSHARED TRACX
               K IS '
FM2HIRTI DC    CL3' ',C', LAST NORMAL ENTRY CCHHR='
FM2LTRAD DC    CL12' ',CL35')'
         ORG   FM2LINE8+132
FM2LINE9 DC    C'      PRIME DATA AREA     (CONTAINS '
FM2PRCTR DC    CL10' ',C' RECORDS, FIRST DATA RECORD/CYL HHR='
FM21RCYL DC    CL7' ',C', LAST DATA TRACK/CYL HH='
FM2LTCYL DC    CL4' ',CL15','
         ORG   FM2LINE9+132
FM2LINEA DC    CL27' ',C'GREATEST R OF RECFM=F RECORDS IS '
FM2HIRPR DC    CL3' ',C', GREATEST R OF SHARED TRACK IS '
FM2RSHTR DC    CL3' ',CL35','
         ORG   FM2LINEA+132
FM2LINEB DC    CL27' '
FM2TAGDT DC    CL5' ',C' RECORDS ARE TAGGED FOR DELETION, LAST RECORD MX
               BBCCHHR='
FM2LPRAD DC    CL20' ',CL35','
         ORG   FM2LINEB+132
FM2LINEC DC    CL27' ',C'THE LAST BLOCK '
FM2STNDB DC    C'IS NOT',C' FULL, THE LAST TRACK '
FM2STNDT DC    C'IS NOT',C' FULL, '
FM2KSCK  DC    C'NO',C' KEY SEQUENCE CHECK, INITIAL LOAD '
FM2ILCM  DC    C'NOT',C' COMPLETE)      '
         ORG   FM2LINEC+132
FM2LINED DC    C'      CYLINDER OVERFLOW   ('
FM2RORG1 DC    CL5' ',C' CYLINDER OVERFLOW AREAS ARE FULL, EACH CYLINDEX
               R HAS '
FM2CYLOV DC    CL3' ',CL30' OVERFLOW TRACKS)'
         ORG   FM2LINED+132
FM2LINEE DC    C'      INDEPENDENT OVERFLOW(CONTAINS '
FM2OVRCT DC    CL5' ',C' RECORDS, '
FM2RORG2 DC    CL5' ',C' UNUSED TRACKS, GREATEST R OF RECFM=F RECORDS IX
               S '
FM2HIROV DC    CL3' ',CL25','
         ORG   FM2LINEE+132
FM2LINEF DC    CL27' ',C'LAST RECORD MBBCCHHR='
FM2LOVAD DC    CL20' ',C', LAST TRACK BALANCE IS '
FM2BYOVL DC    CL5' ',CL40')'
         ORG   FM2LINEF+132
FM2LINEG DC    CL27' ',C'THERE HAVE BEEN '
FM2RORG3 DC    CL8' ',C' RANDOM REFERENCES TO OVERFLOW RECORDS OTHER THX
               AN THE FIRST OVERFLOW RECORD'
DS2ADDR  DSECT
FMT2     DSECT
DS2HEX02 DS    X
DS22MIND DS    XL7
DS2L2MEN DS    XL5
DS23MIND DS    XL7
DS2L3MIN DS    XL5
DS2RES1  DS    XL19
DS2FMTID DS    C
DS2NOLEV DS    X
DS2DVIND DS    X
DS21RCYL DS    XL3
DS2LTCYL DS    XL2
DS2CYLOV DS    X
DS2HIRIN DS    X
DS2HIRPR DS    X
DS2HIROV DS    X
DS2RSHTR DS    X
DS2HIRTI DS    X
DS2RES2  DS    X
DS2TAGDT DS    XL2
DS2RORG3 DS    XL3
DS2NOBYT DS    XL2
DS2NOTRK DS    X
DS2PRCTR DS    XL4
DS2STIND DS    B
DS2CYLAD DS    XL7
DS2ADLIN DS    XL7
DS2ADHIN DS    XL7
DS2LPRAD DS    XL8
DS2LTRAD DS    XL5
DS2LCYAD DS    XL5
DS2LMSAD DS    XL5
DS2LOVAD DS    XL8
DS2BYOVL DS    XL2
DS2RORG2 DS    XL2
DS2OVRCT DS    XL2
DS2RORG1 DS    XL2
DS2RES3  DS    XL3
DS2PTRDS DS    XL5
         END
