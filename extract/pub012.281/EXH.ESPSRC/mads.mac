MADS     TITLE 'MADS INSTRUCTION SUBROUTINE'
***********************************************************************
**                                                                   **
**   THIS SUBROUTINE MAY BE CALLED (BY FORTRAN PROGRAMS)             **
**   WHETHER OR NOT THE EXECUTION CPU SUPPORTS THE MADS INSTRUCTION  **
**                                                                   **
**   CALL MADS(N,A,B,C,S,LA,LB)                                      **
**             VALUES AFTER B ARE OPTIONAL:                          **
**             C DEFAULTS TO A                                       **
**             S DEFAULTS TO D'0.0'                                  **
**             LA, LB DEFAULT TO F'8'                                **
**                                                                   **
**   FOR I=1 TO N, A(I) = B(I) * S + C(I)                            **
**                                                                   **
**   SEE MATHEMATICAL ASSISTS, SA22-7094                             **
**                                                                   **
***********************************************************************
**  MAINTENANCE                                                      **
**  2003-05-27  GYP  REPLACED SPIE WITH ESPIE                        **
**                                                                   **
**                                                                   **
***********************************************************************
         SPACE 2
         PRINT NOGEN
MADS     START 0                                        ADDED ON 85197
MADS     AMODE ANY                                              GP03147
MADS     RMODE ANY                                              GP03147
         USING MADS,R15                                         GP03147
         B     AROUND                                           GP03147
         BCON  'MADS &SYSDATE'                                  GP03147
AROUND   BSM   R14,0         PRESERVE CALLER'S AMODE            GP03147
         STM   R14,R12,12(R13)  SAVE ALL REGISTERS
         DROP  R15                                              GP03147
         LR    R12,R15       ESTABLISH LOCAL BASE
         USING MADS,R12
         LR    R10,R1        SAVE PARAMETER ADDRESS
RECURSE  TM    FLAG,FGSIM    DO SIMULATION ?                    GP03147
         BNZ   SIMULATE      YES; CPU LACKS MADS INSTRUCTION
         TM    FLAG,FGNOSPIE  SKIP SPIE ?
         BNZ   SKIPSPIE
         ESPIE SET,EXITSPIE,(1)  TAKE SPIE EXIT FOR 0C1         GP03147
         ST    R1,PREVPICA   SAVE PRIOR REQUEST
         OI    FLAG,FGSPIED  SET TO FREE ON EXIT
SKIPSPIE BAS   R11,GETPARMS  SET-UP REGISTERS                   GP03147
*        MADS  8,4
         DC    X'B2430084'   MADS
         BO    SIMCC12       SET CODE 12 - INVALID INCREMENT
         BNZ   SIMCC8        OTHER PROBLEM
         OI    FLAG,FGNOSPIE  NEED NO SPIE
EXIT0    SR    R0,R0         SET CC                             GP03147
EXIT     TM    FLAG,FGSPIED  DID A SPIE ?
         BZ    EXITED        NO; JUST EXIT
         LR    R2,R0         SAVE R0, JUST IN CASE
         ESPIE RESET,PREVPICA   RESTORE USER'S                  GP03147
         NI    FLAG,255-FGSPIED
         LR    R0,R2         RESTORE CONDITION CODE
EXITED   LM    R14,15,12(R13)  RELOAD RETURN ADDRESS
         LM    R1,R12,24(R13)  RELOAD OTHERS
         BSM   0,R14         RETURN TO CALLER                   GP03147
         SPACE 1
SIMCC8   LA    R0,8          SET RETURN CODE 8
         B     SIMCOM
SIMCC12  LA    R0,12         SET RETURN CODE 12
SIMCOM   OI    FLAG,FGNOSPIE   SET MADS INSTRUCTION AVAILABLE
         LTR   R1,R1         ANY ELEMENTS LEFT TO DO ?
         BNP   EXIT          NO
         LTR   R4,R4         ANY ADDRESS FOR 'C' ?
         BNZ   *+6           YES
         LR    R4,R2         COPY 'A' ADDRESS
         B     SIMLOOP       DO IT THE HARD WAY
         SPACE 1
SIMULATE BAS   R11,GETPARMS  ESTABLISH PARM REGISTERS           GP03147
         LA    R0,4          SET MINOR CC - NO MADS INSTRUCTION
         SPACE 1
SIMLOOP  LD    R2,0(R3)      GET B(I)
         MDR   R2,R0         GET B(I) * S
         AD    R2,0(,R4)     B(I) * S + C(I)
         STD   R2,0(,R2)     A(I)=
         AR    R2,R8         NEXT A(I)
         AR    R3,R9         NEXT B(I)
         AR    R4,R8         NEXT C(I)
         BCT   R1,SIMLOOP    REPEAT
         B     EXIT
         SPACE 2
GETPARMS LM    R1,R7,0(R10)  LOAD ALL OF PRESUMED ADDRESSES
         LA    R0,16         SET MAJOR ERROR - BAD PARM
         ICM   R1,15,0(R1)   LOAD NUMBER OF ELEMENTS
         BZ    EXIT0         NONE - JUST RETURN
         BM    EXIT          INVALID - FAIL REQUEST
         TM    0(R10),X'80'  END OF LIST ?
         BO    EXIT          YES; TOO SHORT
         LTR   R2,R2         IS 'A' ADDRESS END OF LIST ?
*        BM    EXIT          YES; ERROR                         GP03147
*        BZ    EXIT          INVALID                            GP03147
         BNP   EXIT          ERROR OR INVALID                   GP03147
         LTR   R3,R3         IS 'B' VALID ?
         BZ    EXIT          NO
         BM    DFLTC         SET 'C' TO 'A'
         LTR   R4,R4         IS 'C' VALID ?
         BM    DFLTS         DEFAULT SCALAR TO D'0'
         BNZ   *+6           MAYBE ?
         LR    R4,R2         DEFAULT 'C' TO 'A'
         LD    R0,0(,R5)     LOAD SCALAR
         LTR   R5,R5         END OF LIST ?
         BM    DFLTLA        DEFAULT LENGTHS
         L     R8,0(,R6)     LENGTH OF 'A', 'C'
         LTR   R6,R6         END OF LIST ?
         BM    DFLTLB        YES; DEFAULT LENGTH OF B
         L     R9,0(,R7)     LOAD LENGTH OF 'B'
         BR    R11           RETURN TO CALLER
         SPACE 1
DFLTC    LR    R4,R2         DEFAULT 'C' TO 'A'
DFLTS    SDR   R0,R0         SET SCALAR TO 0.0
DFLTLA   LA    R8,8          DEFAULT SEPARATION IS 8 BYTES
DFLTLB   LA    R9,8          -"-
         BR    R11           RETURN TO CALLER
         SPACE 2
         PUSH  USING
         DROP  ,
         USING EXITSPIE,R15
         USING EPIE,R1                                          GP03147
EXITSPIE OI    FLAG,FGSIM    SIMULATE MADS INSTRUCTION
         ICM   R2,8,EPIENXT1  SAVE AMODE                        GP03147
         N     R2,=X'80000000'  ONLY AMODE BIT                  GP03147
         LA    R0,RECURSE-MADS(,R12)                            GP03147
         OR    R2,R0         RECURSE WITH CORRECT AMODE         GP03147
         ST    R2,EPIENXT1   RESUME NEAR BEGINNING              GP03147
         BR    R14           RETURN TO SUPERVISOR
         POP   USING         RESTORE ENVIRONMENT
         SPACE 1
PREVPICA DC    A(0)          CALLER'S PICA
FLAG     DC    X'00'         GLOBAL FLAGS
FGSIM    EQU   X'80'           SIMULATE MADS
FGSPIED  EQU   X'40'           SPIE ISSUED
FGNOSPIE EQU   X'01'           SPIE NOT REQUIRED
         SPACE 1
         PRINT NOGEN
         YREGS ,             DEFINE MNEMONIC REGISTER NAMES     GP03147
         IHAEPIE ,                                              GP03147
         END   ,
