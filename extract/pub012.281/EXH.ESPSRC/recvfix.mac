RECVFIX  TITLE 'R E C V F I X  ***  RECEIVE SINGLE TRANSMIT FILE'
 PUNCH ' ORDER RECVFIX(P) '   *****DEBUG*****
         COPY  OPTIONGB                                          90364
         SPACE 1                                                 90364
         SYSPARM LIST=YES                                        90364
         EJECT ,                                                 90364
         PRINT &PRTSOR                                           90364
RECVFIX  PGMHEAD ZERO12,BASE=R12,PARM=R9,BNDRY=PAGE
         SPACE 1                                                 90364
         SERVINIT ,                                              90364
         SERVLOAD @INPREAD,@PRINTER
         PRTOPEN SYSPRINT,OPT=NOWTO                              90364
         PRTOPEN SYSPUNCH,OPT=NOWTO,DEV=2
         INPOPEN SYSIN       GET THE BAD FILE
         SPACE 1                                                 90364
         L     R3,=A(40000*80)   GET ROOM FOR  LOTS OF CARDS
         GETMAIN RU,LV=(R3),BNDRY=PAGE
         LR    R2,R1         SAVE START
         L     R15,=X'40000000'
         MVCL  R2,R14        CLEAR TO BLANKS
         ST    R1,@CARDS     SAVE START
         SPACE 2
         LR    R4,R1
GETLOOP  INPGET
         BXH   R15,R15,GETDONE    ALL DONE (EOF OR ERROR)
         MVC   0(80,R4),0(R1)     COPY THE CARD IMAGE
         LA    R4,80(,R4)
         B     GETLOOP
         SPACE 1
GETDONE  LR    R7,R4         GET LAST+1 CARD ADDRESS
         SH    R7,=H'82'     SET FOR BXLE
         LA    R6,1          INCREMENT
         L     R5,@CARDS     GET FIRST CARD IMAGE
         TRT   0(256,R5),TABDIGIT  SEQ. IMAGE PREFIX BY X'52C0'
         BNZ   PUTTEST
         LA    R5,255(,R5)
         B     PUTBUMP       TRY AGAIN
         SPACE 1
PUTLOOP  LA    R1,81(,R5)    IF NO STOP
         TRT   0(82,R5),TABDIGIT  SEQUENCE NUMBER ?
PUTTEST  CLI   1(R1),X'C0'   PAIRED X'52C0' ?
         BNE   PUTBUMP       YES
         LA    R5,81(,R1)    ALMOST NEXT IMAGE
         PRTF  2(R1),80,CC=NO,DEV=2  PUNCH THE CARD
PUTBUMP  BXLE  R5,R6,PUTLOOP   DO ANOTHER
         SPACE 1                                                 90364
EXIT     SERVTERM ,                                              90364
EXITCODE PGMEXIT RC=RETCODE  RETURN WITH CONDITION               90364
SYSIN    INPWORK SYSIN,WIDTH=80,EDIT=128
SYSPRINT PRTWORK SYSPRINT,TITLE=5                                90364
SYSPUNCH PUNWORK SYSPUNCH
TABDIGIT DC    256AL1(0)     CHECK FOR DIGITS
         TRENT TABDIGIT,4,X'52'
         SPACE 1                                                 90364
SAVE     DSECT ,                                                 90364
DB       DS    D                                                 90364
DB2      DS    D                                                 90364
         SERVDEFS ,          EXPAND SERVICE ADDRESSES            90364
@CARDS   DS    A
SAVEEND  EQU   *
         SPACE 1                                                 90364
         END   ,                                                 90364
