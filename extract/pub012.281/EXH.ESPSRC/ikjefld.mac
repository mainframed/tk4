IKJEFLD  TITLE 'I K J E F L D  ***  LOGON PRE-PROMPT EXIT'
         COPY  OPTIONGB
         SPACE 1
         SYSPARM LIST=YES
         SPACE 2
***********************************************************************
*                                                                     *
*        COPYRIGHT 1986  EXPERT SYSTEM PROGRAMMING, INC.              *
*        COPYRIGHT 2003  EXPERT SYSTEM PROGRAMMING                    *
*                        176 OLD STAGE COACH ROAD                     *
*                        BRADFORD, VT 05033-8844                      *
*                                                                     *
*                    ALL RIGHTS RESERVED                              *
*                                                                     *
***********************************************************************
         EJECT ,
         PRINT &PRTSOR
*
*        THIS MODULE IS A PREFACE TO THE LOCAL PRE-PROMPT EXIT,
*        WHICH IS LOADED DYNAMICALLY FROM LPALIB.
*
*        THIS SPLIT BETWEEN INVOKATION AND PROCESSING PERMITS SIMPLE
*        TESTING AND REPLACEMENT OF THE INSTALLATION CODE.
*
*        THE ACTUAL PROCESSING MODULE IS NAMED LEXLOGON, AND RESIDES
*        IN SYS1.LPALIB. ANOTHER MODULE IS CALLED WHEN THE USER CHOOSES
*        THE DEBUG() PARAMETER.                                  87270
*                                                                87270
*        CODE DATED 87270, TOGETHER WITH MODS TO IEESB605, PERMITS
*        LOGON TO SECONDARY JES USING ALTJES(SSNM). ADAPTED FROM 87270
*             SCOTT C. DIMOND, SOFTWARE SUPPORT ANALYST          87270
*             BASE OPERATING SYSTEM SUPPORT                      87270
*             SOFTWARE SUPPORT GROUP, (403) 290-7257             87270
*             NOVA, AN ALBERTA COMPANY                           87270
*             P.O. BOX 2535, POSTAL STATION M                    87270
*             CALGARY, ALBERTA, CAN   T2P 2N6                    87270
*                                                                87270
*
*        WORD UCXLOGON IN THE LOCAL USER CVT EXTENSION IS USED AS
*        THE POINTER AND LOAD CONTROL FLAG.
*
         SPACE 1
IKJEFLD  START 0                FIRST AND EARLY VERSION ADDED ON 86146
         STM   R14,R12,12(R13)    SAVE ALL REGISTERS
         LR    R12,R15
         USING IKJEFLD,R12   SET LOCAL BASE
         LR    R7,R1         COPY PARM LIST ADDRESS
         L     R2,0(,R7)     GET SWITCH HEADER
         L     R2,0(,R2)     GET SWITCHES
         TM    1(R2),X'02'   PRIOR ABEND ?
         BNZ   QUITTER       YES; AVOID FURTHER DAMAGE
         SPACE 1                                                 86145
         SERVICE APFON       SET APF ON
         AIF   (&MVSXA).NTOFIXD                                  92300
***********************************************************************
*                                                                     *
*    NTO FIX - FOR TTY LOOKING LIKE 3767, THE FULL-SCREEN             *
*    LOGON IS INVOKED ANYWAY.  THIS CODE REVERTS TO NORMAL LOGON      *
*    EXCEPT FOR 3270-TYPE DEVICES.                                    *
*                                                                     *
***********************************************************************
         SPACE 1                                                 86145
         MODESET KEY=ZERO    GET ACCESS TO TSB
         L     R1,PSAAOLD-PSA  LOAD THE ASCB ADDRESS
         USING ASCB,R1
         ICM   R1,15,ASCBTSB  LOAD AND TEST THE TSB ADDRESS
         BZ    NOFULLSC      NONE; NO FULL-SCREEN LOGON
         USING TSB,R1
         TM    TSBSTAT,TSB3270  3270 TYPE DEVICE ?
         BNZ   DOFULLSC      YES; PERMIT FULL-SCREEN
NOFULLSC OI    2(R2),X'20'   NO FULL-SCREEN PANEL
DOFULLSC MODESET KEY=NZERO   RESET
         DROP  R1
.NTOFIXD SPACE 1                                                 87270
*        SCAN USER'S BUFFER FOR ALTJES(SSNM) OPERAND             87270
*                                                                87270
         L     R5,4(,R7)     ADDRESS OF INPUT BUFFER ADDRESS     87270
         L     R6,0(,R5)     INPUT BUFFER ADDRESS                87270
         LH    R5,6(,R5)     GET CURRENT BUFFER LENGTH           87270
         SH    R5,=H'12'     DON'T SCAN PAST END                 87270
         LA    R5,0(R5,R6)   LAST BYTE IN BUFFER                 87270
ALTLOOK  CLC   =C' ALTJES(',0(R6)  ALTERNATE JES ?               87270
         BE    ALTJESNM      YES                                 87270
         LA    R6,1(,R6)                                         87270
         CR    R6,R5         SCANNED ALL ?                       87270
         BNH   ALTLOOK       NO; LOOK AGAIN                      87270
         B     GETEXIT       YES; NOW CHECK FOR EXIT             87270
ALTJESNM CLI   12(R6),C')'   4-BYTE SUB-SYSTEM NAME ?            87270
         BNE   BADSSNM       NO; IGNORE                          87270
         L     R1,CVTPTR     GET THE CVT                         87270
         ICM   R1,15,CVTJESCT-CVTMAP(R1)  SUBSYSTEM VECTOR TABLE 87270
         BNP   BADSSNM       MAJOR BOOBOO                        87270
         USING JESCT,R1                                          87270
         CLC   JESPJESN,8(R6)  PRIMARY NAME ?                    87270
         BE    GOODSSNM      YES; SKIP LOOP                      87270
EXLOCHSY LA    R1,JESSSCT-(SSCTSCTA-SSCT)  CHEAT                 87270
         USING SSCT,R1                                           87270
EXLOCSLP ICM   R1,15,SSCTSCTA   GET NEXT SSCVT                   87270
         BNP   BADSSNM       USER CHOSE BAD NAME ?               87270
         CLC   SSCTSNAM,8(R6)   DESIRED SUBSYSTEM ?              87270
         BNE   EXLOCSLP      NO                                  87270
         ICM   R0,15,SSCTSSVT  TEST FOR SVT                      87270
         BNP   BADSSNM       NONE ?                              87270
         DROP  R1                                                87270
GOODSSNM L     R4,PSATOLD-PSA  GET OUT TCB                       87270
         L     R4,TCBJSCB-TCB(,R4)  LOAD OUR JSCB                87270
         MODESET KEY=ZERO                                        87270
         USING IEZJSCB,R4                                        87270
         MVC   JSCRSV01,8(R6)  COPY SUB-SYSTEM REQUESTED         87270
         DROP  R4                                                87270
         MODESET KEY=NZERO                                       87270
         MVC   1(12,R6),0(R6)  BLANK OUT THE ACCEPTED PARAMETER  87270
         B     GETEXIT                                           87270
BADSSNM  TPUT  =C'INVALID ALTJES SPECIFICATION',28               87270
         SPACE 1                                                 86145
*        SCAN USER'S BUFFER FOR DEBUG() PARAMETER                87270
*        INVOKE ALTERNATE LOGON EXIT IF FOUND                    87270
*                                                                87270
GETEXIT  LM    R0,R1,MODNAME  SPECIFY EXIT NAME
         L     R5,4(,R7)     GET INPUT BUFFER POINTER            87270
         L     R6,0(,R5)     GET INPUT BUFFER                    87270
         LH    R5,6(,R5)     GET CURRENT LENGTH                  87270
         SH    R5,=H'7'      DON'T OVERRUN BUFFER                87270
         LA    R5,0(R5,R6)   LAST BYTE IN BUFFER                 87270
DEBLOOP  CLC   =C' DEBUG(',0(R6)  DEBUG REQUEST ?                87270
         BE    DEBNAME       YES; GET THE NAME                   87270
         LA    R6,1(,R6)                                         87270
         CR    R6,R5                                             87270
         BNH   DEBLOOP       TRY AGAIN                           87270
         B     GETDFLT       USE DEFAULT NAME                    87270
DEBNAME  CLI   7(R6),C')'    NULL PARM ?                         87270
         ICM   R0,B'1110',=C'TST'  USE DEFAULT NAME              87270
         BE    LOADEX        YES; USE DEFAULT TEST NAME          87270
         CLI   10(R6),C')'   THREE BYTES ?                       87270
         BNE   GETDFLT       NO; USE DEFAULT MODULE              87270
         ICM   R0,B'1110',7(R6)                                  87270
LOADEX   LM    R4,R5,16(R13)  SAVE OLD R15,R0                    87270
         STM   R0,R1,16(R13) SAVE MODULE NAME                    87270
         LOAD  EPLOC=16(R13),ERRET=LOADFAIL                      87270
         LR    R15,R0        COPY ENTRY POINT ADDRESS            87270
         STM   R4,R5,16(R13)  RESTORE OLD R15, R0                87270
         MVC   1(7,R6),0(R6) WIPE BASIC TEXT                     87271
         CLI   10(R6),C')'   FOUR MORE ?                         87271
         BNE   *+10          NO                                  87271
         MVC   8(3,R6),0(R6) WIPE REST                           87271
         B     QUICKGO       INVOKE EXIT                         87270
LOADFAIL TPUT  =C'DEBUG EXIT FAILED',17                          87270
         STM   R4,R5,16(R13) RESTORE OLD R15, R0                 87270
         SPACE 1                                                 87270
*        INVOKE INSTALLATION LOGON EXIT LEXLOGON IF AVAILABLE    87270
*                                                                87270
GETDFLT  LM    R0,R1,MODNAME  SPECIFY EXIT NAME
         L     R3,CVTPTR     GET CVT IN R3 FOR IEAVVMSR
         USING CVTMAP,R3
         LTCB  R5,HAVE=CVT,USE=YES
         ICM   R4,15,CVTUSER  GET USER CVT
         BZ    LOOKLPA       NO PLACE TO STASH ADDRESS
         USING USERCVT,R4    DECLARE IT
         ICM   R15,15,UCXLOGON  ADDRESS ALREADY SET ?
         BP    QUICKGO       YES; USE IT
         BM    QUITTUNG      MODULE NOT FOUND
LOOKLPA  L     R7,CVTLPDSR-CVTMAP(,R3)  IEAVVMSR
         BALR  R14,R7    NOTE THAT R7-R9 ARE CLOBBERED
         B     FOUNDIT       MODULE FOUND
         SPACE 1                                                 87362
         L     R15,CVTQLPAQ  GET MLPA CDE CHAIN                  87362
         USING CDENTRY,R15                                       87362
MLPACMP  ICM   R15,15,CDCHAIN   GET NEXT CDE; ELSE TRY PLPA      87362
         BZ    NONEVER       NO MATCH                            87362
         C     R1,CDNAME+4   MATCH ?                             87362
         BNE   MLPACMP       NO; TRY NEXT                        87362
         C     R0,CDNAME     FULL MATCH ?                        87362
         BNE   MLPACMP       NO; TRY NEXT                        87362
         LR    R0,R15        FINAGLE THE CDE ADDRESS             87362
         SPACE 1
FOUNDIT  LR    R15,R0        COPY LPDE ADDRESS
         USING LPDE,R15
         L     R15,LPDENTP   LOAD ENTRY POINT
         LTR   R4,R4         USERCVT SET ?
         BZ    QUICKGO       NO
         LR    R5,R15        FINAGLE THE EXIT ADDRESS            87362
         MODESET KEY=ZERO    GET PRIVILEGED
         ST    R5,UCXLOGON   SAVE THE EXIT ADDRESS               87362
         MODESET KEY=NZERO   RESTORE KEY
         LR    R15,R5        UNFINAGLE THE EXIT                  87362
QUICKGO  ST    R15,16(,R13)  SAVE EXIT ROUTINE ADDRESS           87270
         SERVICE APFOF       RESET APF
         LM    R14,R12,12(R13)  RESTORE ALL BUT R15
         BR    R15           TRANSPARENT CALL TO EXIT
         SPACE 1
NONEVER  LTR   R4,R4         HAVE A USER CVT ?
         BZ    QUITTUNG      NO; JUST GET OUT
         MODESET KEY=ZERO    GET PRIVILEGED
         OI    UCXLOGON,X'80'  SET UNAVAILABILITY FLAG
         MODESET KEY=NZERO   RESTORE KEY
QUITTUNG SERVICE APFOF       RESET APF
QUITTER  LM    R14,R12,12(R13)  RELOAD EVERYTHING
         SLR   R15,R15       JUST IN CASE
         BR    R14           RETURN AS IS
         SPACE 1
MODNAME  DC    0F'0',CL8'LEXLOGON'   LOCAL EXIT NAME
         SPACE 2
         PRINT &PRTSYS
CVTDSECT DSECT ,
         CVT   DSECT=YES
         IHALPDE ,
         IHACDE ,                                                87362
USERCVT  USERCVT ,
         PRINT NOGEN
         REGEQU ,
         IKJTCB ,
         IEZJSCB ,                                               87270
         IEFJESCT ,          SUB-SYSTEM VECTOR TABLE             87270
         IEFJSCVT ,                                              87270
         IEFJSSIB ,                                              87270
         IKJTSB ,
         IHAASCB ,
         IHAPSA ,
         END   ,
