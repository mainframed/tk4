DPRT     TITLE 'DIPLOMACY GAME - PRINT ROUTINE'
         MACRO
         NATO
         LCLA  &I,&J
         LCLC  &P,&DDN,&X
.*
.*       NATION OUTPUT BLOCK
.*
.LOOP    AIF   (&I GE 8).MEND
&I       SETA  &I+1
&J       SETA  3*&I-2
&P       SETC  'AUSENGFRAGERITARUSTURSYS'(&J,3)
&X       SETC  '001002004008016032064128'(&J,3)
&J       SETA  8*&I-7
&DDN SETC 'AUSTRIA ENGLAND FRANCE  GERMANY ITALY   RUSSIA  TURKEY  SYSP*
               RINT'(&J,8)
.*
&P.BLK   DS    0D
&P.BIT   DC    AL1(&X)
&P.NAT   DC    CL7'&DDN '
&P.DCB   DCB   DDNAME=&DDN,DSORG=PS,MACRF=PL,EROPT=ACC,                *
               RECFM=VBA,LRECL=137,BLKSIZE=689
&P.LCT   DC    H'-99'
&P.PGN   DC    PL2'0'
&P.TIT   DC    0A(0),AL2(137,0),CL133'1'
         ORG   &P.TIT+5
&P.TITS  DC    CL83' '
&P.TLEN  EQU   *-&P.TITS
         DC    CL4' ',4C'*',C' '
         AIF   (&I EQ 8).SPEC
&DDN     SETC  '&DDN'        ELIMINATE TRAILING BLANKS ?
         DC    CL14'&DDN ****'
         AGO   .COM
.SPEC    DC    CL14'FILE COPY ****'
.COM     DC    CL3' '
&P.DAT   DC    C' FALL  1900'
         DC    CL4' ',C'PAGE'
&P.PAG   DC    CL4' '
         ORG
         DS    0D
&P.LEN   EQU   *-&P.BLK
         AGO   .LOOP
.MEND    MEND
         EJECT
*        COMMON PRINT ROUTINE
*
         ENTRY XPRNT,XPROP,XPCLS
         PRINT NOGEN
PRINTOUT START 0
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         USING *,R12
XPRNT    STM   R14,R12,12(R13)
         LR    R12,R15
         LA    R9,XPSAVE
         ST    R9,8(R13)
         ST    R13,4(R9)
         LR    R13,R9
         LR    R6,R1         SAVE RECORD ADDDRESS
         MVC   XPREC,0(R6)   MOVE REQUEST PREFIX TO WORK AREA
         NI    XPLEN,X'7F'   PREVENT NEGATIVE LENGTH ON TEST
         CLC   XPLEN,XP137   TOO LONG ?
         BNH   *+10          NO, OK
         MVC   XPLEN,XP137   SHORTEN IT
         CLC   XPLEN,XP6     TOO SHORT ?
         BNL   *+10          NO, OK
         MVC   XPLEN,XP6     USE MINIMUM
         CLI   XPROUT,0      GENERAL ROUTNG CODE ?
         BNE   *+8           NO, OK
         MVI   XPROUT,X'FF'  CONVERT TO ROUT ALL
         L     R15,=V(DATE)  GET CHECKPOINT ENTRY
         NC    XPROUT(1),12(R15)   ROUTE ONLY TO ACTIVE COUNTRIES
         LH    R1,XPLEN      GET LENGTH
         LA    R2,0(R1,R6)   GET LAST BYTE + ONE TO BE CHECKED
XPBL     BCTR  R2,0          PRECEEDING BYTE
         CLI   0(R2),C' '    TRAILING BLANK ?
         BNE   XPBLI         NO, HAVE CORRECT LENGTH
         BCTR  R1,0          LESS ONE FOR LENGTH
         CH    R1,XP6        SHORTENED TO MINIMUM ?
         BH    XPBL          NO, TRY AGAIN
         BE    XPBLI         OK IF MINIMUM
         LA    R1,6          IF LESS, UP TO MINIMUM
XPBLI    STH   R1,XPLEN      SET NEW LENGTH
         CLI   XPCON,C'$'    TITLE REQUEST ?
         BE    XPTIT         YES, DO IT
         LA    R3,XPCC       GET TABLE OF CONTROL CHARACTERS
         LA    R4,(XPCCE-XPCC)/2   GET NUMBER OF ENTRIES
XPRCL    CLC   XPCON,0(R3)   MATCH ?
         BE    XPRCV         YES, USE IT
         LA    R3,2(R3)      TRY NEXT
         BCT   R4,XPRCL      TRY AGAIN
         MVC   XPCON,0(R3)   NOT FOUD, USE DEFAULT
XPRCV    MVC   XPCT+1(1),1(R3)        COPY NO. OF LINES USED
         LA    R4,AUSBLK     GET FIRST OUTPUT BLOCK ADDRESS
         LA    R5,8          GET NUMBER OF BLOCKS
         USING SYSBLK,R4     DECLARE LAST ONE
XPOUTP   MVC   XPDB,SYSBIT   COPY REQUIRED BIT
         NC    XPDB,XPROUT   IS ROUTING ON FOR THIS ONE ?
         BZ    XPOUTING      NO, SKIP IT
         PUT   SYSDCB        GET A BUFFER
         LH    R3,SYSLCT     GET LINE COUNT
         SH    R3,XPCT       SUBTRACT CURRENT REQUEST SIZE
         STH   R3,SYSLCT     STASH BACK
         BNM   XPOUPUTM      ROOM, PUT IT OUT
         L     R15,=V(DATE)
         MVC   SYSDAT,0(R15)   COPY IT TO TITLE
         MVC   SYSLCT,XPRMAX   MOVE NEW PAGE LINE COUNT
         MVC   SYSPAG,XPMASK   MOVE EDIT MASK
         AP    SYSPGN,XPP1   UP PAGE COUNT
         ED    SYSPAG,SYSPGN    GET PAGE NUMBER
         MVC   0(137,R1),SYSTIT   COPY TITLE
         PUT   SYSDCB        GET ANOTHER BLOCK
         MVC   0(4,R1),XPREC   MOVE PREFIX
         MVI   4(R1),C'-'    FORCE TRIPLE SPACE
         B     XPOUPM
XPOUPUTM MVC   0(5,R1),XPREC    MOVE PREFIX
XPOUPM   XC    2(2,R1),2(R1)   ZERO ROUT, ETC.
         MVC   XPSAVE(2),0(R1)   MOVE LENGTH FIELD TO HALFWORD
         LH    R2,XPSAVE     LOAD LENGTH
         SH    R2,XP6        LENGTH OF DATA PORTION
         EX    R2,XPMVC      MOVE DATA TO BUFFER
XPOUTING LA    R4,SYSLEN(R4)   NEXT BLOCK
         BCT   R5,XPOUTP     DO AGAIN
         SPACE
XPEX     L     R13,4(R13)
         LM    R14,R12,12(R13)
         BR    R14           RETURN
         SPACE 2
XPTIT    CLC   XPLEN,=AL2(SYSTLEN+5)   LONGER THAN TITLE MAX ?
         BNH   *+10          NO, OK
         MVC   XPLEN,=AL2(SYSTLEN+5)   MOVE MAX
         LA    R4,AUSBLK     GET FIRST BLOCK
         LA    R5,8          DO EIGHT TIMES
XPTITL   MVC   XPDB,SYSBIT   COPY BIT
         NC    XPDB,XPROUT   IS THIS ROUTING REQUESTED ?
         BZ    XPTINC        NO, SKIP
         MVI   SYSTITS,C' '  PRE-CLEAR
         MVC   SYSTITS+1(SYSTLEN-1),SYSTITS
         LA    R1,SYSTIT     ADDRESS OF TITLE
         LH    R2,XPLEN      GET LENGTH
         SH    R2,XP6        DATA LENGTH
         EX    R2,XPMVC      MOVE TO TITLE IN CORE
        MVI   SYSLCT,X'FE'  MAKE MINUS TO FORCE EJECT
XPTINC   LA    R4,SYSLEN(R4)   NEXT BLOCK
         BCT   R5,XPTITL     TRY AGAIN
         B     XPEX          EXIT
         SPACE 2
         USING *,R15
XPROP    STM   R14,R12,12(R13)
         L     R12,=A(PRINTOUT)
         USING PRINTOUT,R12
         DROP  R15
         LA    R9,XPSAVE
         ST    R9,8(R13)
         ST    R13,4(R9)
         LR    R13,R9
         LA    R4,AUSBLK     GET FIRST BLOCK
         LA    R5,8          DO FOR EIGHT
XPROL    TM    SYSDCB+48,X'10'   OPEN ALREADY ?
         BO    XPROIN
         MVC   SYSDCB(40),MASDCB   MOVE PREFIX OF PATTERN DCB
         MVC   SYSDCB+48(MASDCBE-MASDCB-48),MASDCB+48  MOVE ALL BUT DDN
         LA    R2,SYSDCB     GET DCB ADDRESS
         OPEN  ((R2),OUTPUT)
         TM    SYSDCB+48,X'10'   OPEN NOW ?
         BO    XPROIN        OK
         ABEND 16            TOO BAD
         SPACE
XPROIN   LA    R4,SYSLEN(R4)   NEXT BLOCK
         BCT   R5,XPROL      TRY NEXT ONE
         B     XPEX          ALL OPEN
         SPACE 2
         USING *,R15
XPCLS    STM   R14,R12,12(R13)
         L     R12,=A(PRINTOUT)
         USING PRINTOUT,R12
         DROP  R15
         LA    R9,XPSAVE
         ST    R9,8(R13)
         ST    R13,4(R9)
         LR    R13,R9
         LA    R4,AUSBLK     GET FIRST CONTROL BLOCK
         LA    R5,8          NO. OF BLOCKS
XPCLL    TM    SYSDCB+48,X'10'  CLOSED ALREADY ?
         BZ    XPCLIN        YES, SKIP
         LA    R2,SYSDCB     GET THIS DCB ADDRESS
         CLOSE ((R2),LEAVE)    CLOSE IT
      FREEPOOL (R2)          FREE BUFFER POOL
XPCLIN   LA    R4,SYSLEN(R4)
         BCT   R5,XPCLL
         B     XPEX          RETURN, ALL CLOSED
         SPACE 3
MASDCB   DCB  DDNAME=DUMMY,DSORG=PS,MACRF=PL,EROPT=ACC,                *
               RECFM=VBA,LRECL=137,BLKSIZE=689
MASDCBE  EQU   *             END OF DCB
XPSAVE   DS    18F
XPMVC    MVC   5(0,R1),5(R6)   EX. MOVE DATA
XP137    DC    H'137'
XP6      DC    H'6'
XPRMAX   DC    H'55'         MAX LIES PER PAGES
XPCT     DC    H'0'          LINE COUNT OF CURR. CONTR. CHAR
XPREC    DS    0XL5          V RECORD PREFIX WORK AREA
XPLEN    DS    H             LENGTH FINAGLER
XPDB     DS    X             WORK BYTE
XPROUT   DS    X             ROUTING CODE
XPCON    DS    X             CARR. CONTROL CHARACTER
XPMASK   DC    X'40202120'   EDIT MASK - PAGE NUMBER
XPP1     DC    P'1'
XPCC     DC    AL1(C' ',1,C'0',2,C'-',3,C'+',0,C'1',255)
XPCCE    DC    C' ',X'1'     DEFAULT IF UNRECOGNIZABLE
         SPACE 3
         LTORG
         SPACE 2
        NATO
         END
