SSEP     TITLE 'S T E P S E P *** PRINTS BIG PAGES BETWEEN STEPS'
         PRINT NOGEN
STEPSEP  START 0
         USING *,R12
         STM   R14,R12,12(R13)
         LR    R12,R15
         LA    R9,SAVE
         ST    R9,8(R13)
         ST    R13,4(R9)
         LR    R13,R9
         SPACE
***********************************************************************
*                                                                     *
*   THIS PROGRAM IS INVOKED VIA // EXEC CARD OR IN CORE               *
*     LINKS TO ASMANY2                                                *
*     INPUT :  PARM FIELD - UP TO TWO 8 BYTE CHARACTER STRINGS        *
*                            IF ONE SUPPLIED, SECOND LINE IS DATE     *
*                           OPTIONAL 17TH CHARACTER: SYSOUT CODE      *
*                              M - MEMO FOR XEROX  (8*8)              *
*                              P - PORTRAIT MODE (USE 8*8 CHARACTERS) *
*                              X - XEROX MODE                         *
*                              OMITTED OR OTHER - NORMAL 12*12 CHARS  *
*                                                                     *
*           NO PARM FIELD - STEPNAME IF NON-BLANK, ELSE JOBNAME       *
*     OUTPUT WILL BE ON SYSPRINT OR SYSOUT DD                         *
*     OUTPUT DCB PARAMETERS MUST BE SUPPLIED IN FULL ON THE DD CARD;  *
*          ELSE THEY WILL BE FORCED TO FBM,121,0 (SYSTEM SUPPLIED)    *
*                                                                     *
***********************************************************************
         XC    LINE1(LINE1LEN),LINE1  CLEAR IT                  GP00005
         MVC   LINE1(LINE1BLK),LINE1-1   BUT SET BLANKS INTO TEXT
         MVI   FLAG,X'20'    RESET THE FLAG (DATE ON 3RD LINE)  GP97262
         LA    R1,0(,R1)     ANYTHING PASSED IN R1 ?
         LTR   R4,R1         CHECK IT
         BZ    DEFAULT       NO; USE  STEP OR JOBNAME
         ICM   R4,7,1(R4)    LOAD ADDRESS                       GP98260
         BZ    DEFAULT       SKIP IF NONE                       GP98260
         LH    R2,0(,R4)     GET LENGTH
         LA    R3,2(,R4)     POINT TO INPUT
         LA    R1,LINE1      POINT TO FIRST OUTPUT LINE
         LA    R5,2          MAX OF TWO LINES
         CH    R2,=H'16'     MORE THAN TWO NAMES WORTH ?         90095
         BNH   LINELOOP      NO                                  90095
         CLI   16(R3),C'M'   XEROX MEMO?                        GP00005
         BE    SETXER                                           GP00005
         CLI   16(R3),C'X'   SYSOUT X (FOR XEROX) ?              90095
         BNE   TESTCHAR      NO                                  90095
SETXER   OI    FLAG,X'40'    SET XEROX DUPLEX MODE               90095
TESTCHAR CLI   16(R3),C'M'   MEMO?                              GP00005
         BE    SET8X8        YES; SMALL                         GP00005
         CLI   16(R3),C'P'   PORTRAIT MODE?                     GP00005
         BNE   LINELOOP      NO                                  90095
SET8X8   OI    CHARSEL,X'01'   SELECT 8*8 CHARACTER SIZE        GP00005
LINELOOP LTR   R2,R2         ANY MORE INPUT ?
         BNP   LINEEND       NO; CHECK IT
         LA    R6,8          SET MAX LENGTH
         CR    R2,R6         LONGER THAN MAX ?
         BNL   *+6           YES; USE ONLY MAX
         LR    R6,R2         ELSE USE REMAINING LENGTH ONLY
         BCTR  R6,0           LESS FOR EXECUTE
         EX    R6,LINEMVC    MOVE TO LINE
         SR    R2,R6         GET REMAINING LENGTH+1
         BCTR  R2,0          REMAINING LENGTH
         LA    R1,LINE2      SET FOR NEXT OUTPUT FIELD
         LA    R3,8(,R3)     AND NEXT INPUT FIELD
         BCT   R5,LINELOOP   TRY FOR SECOND ONE
LINEEND  CLC   LINE1,BLANKS  ANY INPUT ON FIRST LINE ?
         BE    DEFAULT       NO; USE DEFAULT
         CLC   LINE2,BLANKS  ANYTHING ON SECOND LINE
         BE    READJF        NO; USE ONLY FIRST LINE
         OI    FLAG,X'80'     SHOW TWO LINES PRESENT             90095
         B     READJF
         SPACE 1
DEFAULT  MVC   LINE2,BLANKS  RESET LINE 2
         NI    FLAG,255-X'80'   RESET FLAG TO ONE LINE           90095
        EXTRACT SAVE,FIELDS=(TIOT)  GET TIOT ADDRESS LAZEE WAY
         L     R3,SAVE       LOAD IT
         LA    R1,8(,R3)     POINT TO STEPNAME FIELD
         CLC   BLANKS,0(R1)  HAVE A STEPNAME ?
         BNE   *+6           YES; USE IT
         LR    R1,R3         ELSE USE JOB NAME
         MVC   LINE1,0(R1)   MOVE STEP/JOB NAME
         SPACE 1
READJF   MVC   DDNAME,=CL8'SYSPRINT'   SEE IF SYSPRINT IS PRESENT
         XC    DDCB(5),DDCB  CLEAR REQUEST LIST
JFREAD   MVC   DUMDCB+40(8),DDNAME  COPY THE DDNAME TO THE DUMMY DCB
         RDJFCB (DUMDCB)     TRY FOR THE JFCB
         LTR   R15,R15       GOT ONE ?
         BZ    READCHK       OK; CHECK DCB
         CLC   DDNAME,=CL8'SYSOUT'   IF NO SYSPRINT, TRY SYSOUT
         BE    EXIT2         IF NO SYSOUT, EXIT
         MVC   DDNAME,=CL8'SYSOUT'  RETRY WITH SYSOUT
         B     JFREAD
         SPACE 1
READCHK  MVC   RECFM,JFCRECFM      COPY RECORD FORMAT
         MVC   BLKSIZ,JFCBLKSI    AND BLOCK SIZE
         MVC   RECL,JFCLRECL      AND RECORD LENGTH
         OC    DDCB,DDCB     ANY SUPPLIED ?                     GP00005
         BNZ   USE           YES; LET HIM BOMB IF BAD
         MVC   DDCB,DEFDCB   NO; USE DEFAULTS                   GP00005
USE      LA    R1,LINE1      POINT TO PARM LIST
         LINK  EP=ASMANY2    CALL PRINTER
         SR    R15,R15       CLEAR RETURN CODE
         B     EXITC
         SPACE 1
EXIT2    LA    R15,2         SET BAD EXIT CODE
EXITC    L     R13,4(,R13)   REGAIN OLD SAVE AREA
         LM    R0,R12,20(R13)   RELOAD OLD REGISTERS
         L     R14,12(R13)   GET RETURN ADDRESS
         BR    R14           RETURN
         SPACE 1
LINEMVC  MVC   0(0,R1),0(R3)      MOVE INPUT FROM PARM FIELD
         EJECT
SAVE     DC    18A(0)
BLANKS   DC    CL8' '
****** INVOCATION LIST FOR ASMANY2
LINE1    DC    CL8' '        FIRST BIG LINE TEXT; PRECEDED BY 'BLANKS'
FLAG     DC    X'0'          FLAG BYTE IN CRUMMY LOCATION
LINE2    DC    CL8' '        OPTIONAL SECOND LINE
DDNAME   DC    CL8' '        OUTPUT DDNAME
LINE1BLK EQU   *-LINE1                                          GP00005
DDCB     DS    0XL5          DCB STUFF
BLKSIZ   DC    AL2(0)        BLOCKSIZE
RECL     DC    AL2(0)        RECORD LENGTH
RECFM    DC    X'0'          RECORD FORMAT
CHARSEL  DC    X'00'         CHARACTER SELECTOR (0=12*12, 1=8*8)
         DC    9X'00'          SPARES                           GP00005
LINE1LEN EQU   *-LINE1                                          GP00005
         SPACE 1                                                GP00005
DEFDCB   DC    AL2(0,121),X'92' BLKSIZE=605,LRECL=121,RECFM=FBM
         SPACE
EXLIST   DC    0A(0),X'87',AL3(INFMJFCB)  JFCB EXIT LIST
       IEFJFCBN
         SPACE
DUMDCB   DCB   DSORG=PS,MACRF=PL,DDNAME=SYSPRINT,EROPT=ACC,EXLST=EXLIST
         YREGS ,                                                GP00005
         END
