S253     TITLE 'SVC 253 - USER INTERFACE FOR WRITING SMF RECORDS'
         MACRO                                                   83136
&NM      ADCHECK &ADDR                                           83136
         GBLB  &MVS                                              83136
         AIF   (&MVS).DOMVS                                      83136
         GBLC  &MACPLAB                                          83136
&MACPLAB SETC  '&NM'                                             83136
         MACPARM R7,&ADDR    COPY TO VALIDITY CHECK REGISTER     83136
         LR    R9,R4         COPY TCB ADDRESS                    83136
         LR    R11,R3        COPY CVT ADDRESS                    83136
         L     R8,CVT0VL00    GET VALIDITY CHECK ROUTINE         83136
         BALR  R10,R8        INVOKE IT                           83136
         MEXIT ,                                                 83136
.DOMVS   ANOP  ,                                                 83136
&NM      LR    R7,R1         SAVE R1                             83136
         MACPARM R1,&ADDR    LOAD ADDRESS TO BE CHECKED          83136
         L     R15,CVT0VL00  GET ADDRESS VALIDITY-CHECK ROUTINE  83136
         XR    R2,R2         JUST IN CASE                        83136
         BALR  R14,R15       CALL VALIDITY CHECK                 83136
         LR    R1,R7         RESTORE R1                          83136
         MEND  ,                                                 83136
         SPACE 2                                                 83136
         COPY  OPTIONGB                                          83007
         SPACE 2                                                 83007
         SYSPARM LIST=YES                                        83007
         EJECT ,                                                 79311
         PRINT &PRTSOR                                           83007
IGC0025C CSECT ,
         USING CVTMAP,R3                                         83007
         USING SMFMAP,R1                                         79311
         SPACE 2
         BALR  R12,0                   SET BASE
         USING *,R12
         LA    R1,0(,R1)               HI BYTE
         LR    R6,R1                   COPY SMF RECORD POINTER
         N     R6,=X'00FFFFFE'         TAKE OUT '1' BIT          79311
         CR    R1,R6                   HALF-WORD BOUNDARY ?      79311
         BNE   BADALIGN                BRANCH IF NOT
         LA    R11,SMFHEADE            END OF HEADER ADDRESS
         CR    R1,R11                  HEADER WITHIN STORAGE ?   79311
         BNL   BADADDR                 BRANCH IF NOT
         ADCHECK (R1)        CHECK START ADDRESS                 83136
         BNZ   BADADDR       ERROR                               79311
         LR    R9,R1         COPY THE ADDRESS                    83136
         AH    R9,SMFLEN     ADD LENGTH                          83136
         SH    R9,=H'1'      POINT TO LAST BYTE                  83136
         ADCHECK (R9)        INVOKE THE VALIDITY CHECKER         83136
         BNZ   BADADDR       ERROR                               79311
         L     R6,CVTSMCA              GET SMCA ADDRESS
         CLC   SMFSYSID(4),16(R6)      PROPER SYSTEM AND MODEL ID ?
         BNE   BADID                   BRANCH IF NOT
TSTACC   TM    SMFUID,X'C0'            USER FIELD PRESENT ?      92356
         BNO   BADUSER                 BRANCH IF NOT
         TM    SMFDATE+3,X'0F'         GOOD SIGN ZONE ?
         BNO   BADDATE                 BRANCH IF NOT
         XR    R6,R6                                             79311
         ICM   R6,3,SMFSDW             CHECK SDW                 79311
         BNZ   BADLEN                  ERROR IF NOT ZERO         79311
         LH    R8,SMFLEN               GET RECORD LENGTH
         CH    R8,=AL2(SMFRLEN)        LENGTH OK ?               79311
         BNH   BADLEN                  BRANCH IF NOT
         CH    R8,=Y(4*1024)           OVER 4K ?
         BH    BADLEN                  BRANCH IF SO
         LA    R7,128                  ORIGIN OF USER RECORD TYPES
         IC    R6,SMFTYPE              GET RECORD TYPE           79311
         SR    R6,R7                   MAKE RELATIVE TO LENGTH TABLE
         BM    BADTYPE                 BRANCH IF NOT 'USER' TYPE
         LA    R6,TAB(R6)              INDEX INTO LENGTH TABLE
         CLI   0(R6),0       DEFINED TYPE ?                      79311
         BNE   BADTYPE                 BRANCH IF NOT             79311
         LA    R11,0(R8,R1)            FIND END OF RECORD ADDRESS
         CR    R11,R1        WRAP-AROUND ?                       79311
         BNH   BADADDR                 BRANCH IF SO              79311
         SMFWTM (1)                    WRITE THE RECORD
         SVC   3                       RETURN
         SPACE 2
*        CONSTANTS AND STUFF
         SPACE 1
TAB      DC    128AL1(128+*-TAB)  RECORD # IF INVALID, ZERO IF OK
         ORG   TAB+166-128   BLOCKPRINT INTERIM CUMULATIVE RCD   79311
         DC    X'0'          BLOCK PRINT RECORD                  79311
         ORG   ,                       END OF TABLE
         SPACE 2
BADALIGN LA    R1,X'1FD'                                         79311
         B     BIGBOMB                                           79311
         SPACE 1
BADADDR  LA    R1,X'2FD'                                         79311
         B     BIGBOMB                                           79311
         SPACE 1
BADLEN   LA    R1,X'3FD'                                         79311
         B     BIGBOMB                                           79311
         SPACE 1
BADID    LA    R1,X'4FD'                                         79311
         B     BIGBOMB                                           79311
         SPACE 1
BADTYPE  LA    R1,X'5FD'                                         79311
         B     BIGBOMB                                           79311
         SPACE 1
BADUSER  LA    R1,X'6FD'                                         79311
         B     BIGBOMB                                           79311
         SPACE 1
BADDATE  LA    R1,X'7FD'                                         79311
BIGBOMB  SLL   R1,12         MAKE SYSTEM COMPLETION CODE         79311
         O     R1,=X'80000000'         BE NICE - ADD A DUMP      79311
         SVC   13            GREETINGS                           79311
         SPACE 2
         LTORG ,
         SPACE 1
PATCH    DC    5D'0'                   PATCH AREA
         SPACE 2
SMFMAP   DSECT ,                       MAP SMF RECORD HEADER
SMFLEN   DS    Y                       RECORD LENGTH
SMFSDW   DS    Y                       SDW, MUST BE ZERO
         DS    XL1                     RESERVED
SMFTYPE  DS    XL1                     RECORD TYPE
SMFTIME  DS    XL4                     HEADER TIME STAMP
SMFDATE  DS    PL4                     HEADER DATE STAMP
SMFSYSID DS    CL4                     SYSTEM ID AMD MODEL TYPE
SMFJOBNM DS    CL8                     JOBNAME
SMFRTIME DS    CL4                     READER TIME
SMFRDATE DS    CL4                     READER DATE
SMFUID   DS    CL8                     USER ID FIELD
SMFHEADE EQU   *                       END OF SMF HEADER
SMFRLEN  EQU   SMFHEADE-SMFMAP         MINIMUM LENGTH OF RECORD  79311
         SPACE 2
         PRINT &PRTSYS                                           83007
CVTDSECT DSECT ,                                                 83007
         CVT   DSECT=YES                                         83007
         REGEQU ,                                                83007
         END   ,
