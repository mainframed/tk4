SUBDATIM TITLE 'S U B D A T I M  ***  TIME AND DATE CONVERSION ROUTINE'
         PUNCH ' ALIAS TIMDAY '                                 GP03146
         COPY  OPTIONGB
         SPACE 1
         SYSPARM LIST=YES
         SPACE 1
***********************************************************************
**                                                                   **
**  SUBDATIM / ALIAS TIMDAY                                          **
**                                                                   **
**  CALL TIMDAY,(RESULTS),VL                                         **
**  RETURNS CURRENT DATE, TIME, DAY OF WEEK IN VARIOUS FORMATS IN    **
**  USER PROVIDED OUTPUT AREA (SEE MAPPING FOR DSECT USERMAP)        **
**                                                                   **
**  CALL TIMDAY,(RESULTS,UTIME,UDATE),VL                             **
**  RETURNS FORMATTED TIME AND DATE AS PASSED BY USER                **
**                                                                   **
***********************************************************************
**  AUTHOR:  DICK OXLEY, 1969                                        **
**                                                                   **
**  MAINTENANCE                                                      **
**  1982-01-06  GYP  SUPPORT USER PROVIDED DATE/TIME                 **
**  2003-05-26  GYP  ADDED CENTURY TO DATES; Y2K FIXES. REFR, RENT.  **
**                   (MAJOR REWRITE)                                 **
**                                                                   **
***********************************************************************
         PRINT &PRTSOR
SUBDATIM PGMHEAD ZERO12,BASE=R12,PARM=R9                        GP03146
         SPACE 1
         LR    R4,R9                   SAVE CALLING LIST PTR.   GP03146
         L     R10,0(,R4)              GET USERS AREA ADDRESS
         USING USERMAP,R10             MAP FOR USER AREA
         MVI   USERAREA,C' '           CLEAR IT
         MVC   USERAREA+1(L'USERAREA-1),USERAREA  CLEAR ALL
*---------------------------------------------------------------------*
*  CHECK PARM - VL BIT MUST BE ON.                                    *
*  ACCEPT (RESULTS),VL  (DEFAULTS TO CURRENT TIME/DATE)               *
*     AND (RESULTS,UTIME,UDATE),VL                                    *
*---------------------------------------------------------------------*
         TM    0(R4),X'80'             USE SYSTEM DATE AND TIME ?
         BO    SYSTIME                 BRANCH IF SO
         TM    4(R4),X'80'             2-WORD LIST ?             82006
         BO    BADPARM                  YES - LIST SYSUDUMP      82006
         TM    8(R4),X'80'             USER TIME AND DATE ?
         BO    USERTIME                                         GP03146
BADPARM  WTO   '* SUBDATIM * - ERROR IN PARAMETER LIST',         82006 *
               ROUTCDE=11,DESC=7                                 82006
         LA    R9,16         SET ERROR CODE                     GP03146
         B     COMMEXIT      AND QUIT                           GP03146
         SPACE 1
USERTIME LM    R2,R3,4(R4)             POSSIBLE TIME/DATE PTRS.  82006
         L     R0,0(,R2)               LOAD USER TIME FROM USER PTR.
         L     R1,0(,R3)               LOAD USER DATE FROM USER PTR.
         B     COMMTIME                BRANCH TO COMMON CODE    GP03146
         SPACE 1
SYSTIME  TIME  BIN                     GET TIME
COMMTIME ST    R1,REQDATE              SAVE DATE                GP03146
         CLI   REQDATE,C'2'  00,01,02 FOR CENTURY ?             GP03146
         BH    HAVECENT      NO; ASSUME IT'S IN THERE           GP03146
         AP    REQDATE,=P'1900000'  ADD IN THE BASE CENTURY     GP03146
*---------------------------------------------------------------------*
*  CONVERT TIME FROM BINARY 100THS SECONDS TO MINUTES/HOURS AND EDIT  *
*---------------------------------------------------------------------*
HAVECENT SRDL  R0,32         PUT TIME IN LOW WORD               GP03146
         D     R0,=F'100'    TRUNCATE TO SECONDS                GP03146
         SR    R0,R0         CLEAR FOR DIVIDE                   GP03146
         D     R0,=F'60'     TRUNCATE TO MINUTES                GP03146
         SR    R0,R0         CLEAR FOR DIVIDE                   GP03146
         D     R0,=F'60'     SPLIT HOURS AND MINUTES            GP03146
*        STM   R0,R1,REQMINS  SAVE TIME                         GP03146
         MH    R1,=H'100'    HOURS * 100                        GP03146
         AR    R1,R0         PLUS MINUTES                       GP03146
         CVD   R1,DB         CONVERT                            GP03146
         MVC   WORKD(L'EDMTIME),EDMTIME  MOVE TIME MASK         GP03146
         ED    WORKD(L'EDMTIME),DB+5     EDIT TIME              GP03146
         MVC   TIME24HR,WORKD+2   MOVE HH:MM                    GP03146
         SPACE 1                                                GP03146
         LA    R2,=C'MIDN'   SET FOR MIDNIGHT                   GP03146
         LTR   R1,R1         IS IT ?                            GP03146
         BZ    READY12       YES; FORMAT                        GP03146
         LA    R2,=C'A.M.'   ELSE A.M.                          GP03146
         C     R1,=F'1200'   BEFORE NOON ?                      GP03146
         BL    READY12       YES                                GP03146
         LA    R2,=C'NOON'   EXACTLY NOON ?                     GP03146
         BE    READY12                                          GP03146
         LA    R2,=C'P.M.'   IT'S LATER THAN YOU THINK          GP03146
         C     R1,=F'1300'   REACHED 1 P.M. YET ?               GP03146
         BL    READY12       NO; RETAIN AS 12:MM                GP03146
         S     R1,=F'1200'   SUBTRACT 12                        GP03146
READY12  CVD   R1,DB         CONVERT                            GP03146
         MVC   WORKD(L'EDMTIME),EDMTIME  MOVE TIME MASK         GP03146
         ED    WORKD(L'EDMTIME),DB+5     EDIT TIME              GP03146
         MVC   TIME12HR,WORKD+2   MOVE HH:MM                    GP03146
         MVC   TIMEAMPM,0(R2)  MOVE DESCRIPTOR (MIDN/A.M./NOON/P.M.)
         SPACE 1                                                GP03146
*---------------------------------------------------------------------*
*  CONVERT DATE INTO VARIOUS FORMATS: JULIAN, M/D/Y, Y-M-D, WEEKDAY   *
*---------------------------------------------------------------------*
         SPACE 1
         MVC   DB(L'EDMJUL),EDMJUL                              GP03146
         ED    DB(L'EDMJUL),REQDATE      EDIT DATE (JULIAN)     GP03146
         MVC   JULDATE,DB+1            MOVE TO USERS AREA       GP03146
         SPACE 1                                                GP03146
         ZAP   DB,REQDATE         SHIFT DATE TO DOUBLE WORD     GP03146
         CVB   R1,DB         MAKE BINARY                        GP03146
         SR    R0,R0                                            GP03146
         D     R0,=F'1000'    JULIAN DAY IN R0, YEAR IN R1      GP03146
         STM   R0,R1,REQJUL  SAVE THEM                          GP03146
*  N.B.: THIS WORKS UNTIL 2099 ONLY                             GP99167
         SR    R2,R2         SET FOR NO LEAP DAY ADJUSTMENT     GP03146
         EX    R1,EXISLEAP   TEST FOR MULTIPLE OF FOUR          GP99167
         BNZ   NOTALEAP      BRANCH IF NOT A LEAP YEAR          GP03146
         CH    R0,=H'60'     IS THIS ON, BEFORE, OR AFTER LEAP DAY?
         BL    NOTALEAP      BEFORE; TREAT NORMALLY             GP03146
         BH    ONELESS       AFTER; ADJUST DAY                  GP03146
         LA    R2,1          SET LEAP DAY ADJUSTMENT            GP03146
ONELESS  BCTR  R0,0          ONE LESS TO FIX DAYS AFTER LEAP DAY
NOTALEAP LA    R4,TABMON     GET TABLE OF CUMULATIVE DAYS       GP03146
         LA    R5,1          SET MONTH                          GP03146
FINDMON  CH    R0,OFTDAY(,R4)  IN THIS MONTH?                   GP03146
         BNH   FOUNDMON      YES; PROCESS                       GP03146
         SH    R0,OFTDAY(,R4)                                   GP03146
         LA    R4,TABMON2-TABMON(,R4)     INCREMENT             GP03146
         LA    R5,1(,R5)     COUNT MONTHS                       GP03146
         B     FINDMON       AND TRY AGAIN                      GP03146
FOUNDMON AR    R0,R2         ADJUST 1 IF LEAP DAY               GP03146
         ST    R5,REQMON     SAVE FOR LATER                     GP03146
         ST    R0,REQDAY                                        GP03146
*  WE NOW HAVE FULL YEAR IN R1, MONTH IN R5, AND DAY IN R0      GP03146
         CVD   R5,DB         CONVERT MONTH                      GP03146
         MVC   DB(4),=X'40202120'                               GP03146
         ED    DB(4),DB+6                                       GP03146
         MVC   OUTMON,DB+2                                      GP03146
         CVD   R0,DB                                            GP03146
         MVC   DB(4),=X'F0202120'                               GP03146
         ED    DB(4),DB+6                                       GP03146
         MVC   OUTDAY,DB+2                                      GP03146
         SPACE 1                                                GP03146
         MVC   MODAYR(9),OFTMON(R4)                             GP03146
         LOCBYTE MODAYR,MODAYR+9                                GP03146
         MVC   1(2,R15),OUTDAY                                  GP03146
         MVI   3(R15),C','                                      GP03146
         MVC   5(4,R15),OUTYEAR                                 GP03146
         SPACE 1                                                GP03146
         MVC   REGDAY+2(4),=C'/  /'                             GP03146
         MVC   REGDAY(2),OUTMON                                 GP03146
         MVC   REGDAY+3(2),OUTDAY                               GP03146
         MVC   REGDAY+6(2),OUTYEAR+2                            GP03146
         SPACE 1                                                GP03146
         MVC   EURODAY+4(4),=C'-00-'                            GP03146
         MVC   EURODAY(4),OUTYEAR                               GP03146
         OC    EURODAY+5(2),OUTMON                              GP03146
         MVC   EURODAY+8(2),OUTDAY                              GP03146
         SPACE 1                                                GP03146
*---------------------------------------------------------------------*
*  FINALLY - FIND THE DAY OF THE WEEK                                 *
*---------------------------------------------------------------------*
         SPACE 1
         L     R1,REQMON               MONTH                    GP03146
         L     R2,REQDAY               DAY                      GP03146
         L     R3,REQYEAR              YEAR                     GP03146
         CH    R1,=H'2'                JAN. OR FEB. ?
         BH    *+10                    NO
         LA    R1,12(,R1)              YES, USE
         BCTR  R3,0                    PREV. YEAR
         LA    R4,2(,R2)               FINAGLE CONSTANT
         AR    R4,R1                   TWO TIMES
         AR    R4,R1                   THE MONTH
         LA    R7,1(,R1)
         MH    R7,=H'3'
         SR    R6,R6                   3*MONTH/5
         D     R6,=F'5'
         AR    R4,R7
         AR    R4,R3                   +YEAR
         SRA   R3,2                    +YEAR/4
         AR    R4,R3
         SR    R2,R2
         D     R2,=F'25'               -YEAR/100
         SR    R4,R3
         SRA   R3,2
         AR    R4,R3                   +YEAR/400
         LR    R5,R4
         SR    R4,R4                   MONTHS/7
         D     R4,=F'7'
         SPACE 1
*              REG. 4 = 0,SAT.  1,SUN.  2,MON.  ETC.
         SPACE 1
         MH    R4,=Y(TABDAY2-TABDAY)                            GP03146
         LA    R5,TABDAY(R4)                                    GP03146
         MVC   WEEKDAY(9),0(R5)                                 GP03146
         MVC   WEEKDAY+9(2),=C', '                              GP03146
GOODEXIT SR    R9,R9                                            GP03146
COMMEXIT PGMEXIT RC=(R9)                                        GP03146
EXISLEAP TM    =X'03',*-*    MULTIPLE OF FOUR?                  GP99167
         SPACE 2
MOVMONTH MVC   0(0,R4),5(R3)         MOVE MONTH NAME
DAPAT    DC    X'40202120'
EDMJUL   DC    X'F0212020204B202020'                            GP03146
EDMTIME  DC    X'402021207A2020'                                GP03146
CONST2   DC    C', 19XX'
         SPACE 2
TABMON   DC    H'031',CL9'JANUARY'                              GP03146
OFTDAY   EQU   0,2,C'H'                                         GP03146
OFTMON   EQU   2,9,C'C'                                         GP03146
TABMON2  DC    H'028',CL9'FEBRUARY'                             GP03146
         DC    H'031',CL9'MARCH'                                GP03146
         DC    H'030',CL9'APRIL'                                GP03146
         DC    H'031',CL9'MAY'                                  GP03146
         DC    H'030',CL9'JUNE'                                 GP03146
         DC    H'031',CL9'JULY'                                 GP03146
         DC    H'031',CL9'AUGUST'                               GP03146
         DC    H'030',CL9'SEPTEMBER'                            GP03146
         DC    H'031',CL9'OCTOBER'                              GP03146
         DC    H'030',CL9'NOVEMBER'                             GP03146
         DC    H'031',CL9'DECEMBER'                             GP03146
         SPACE 2
TABDAY   DC    CL9' SATURDAY'                                   GP03146
TABDAY2  DC    CL9'   SUNDAY'                                   GP03146
         DC    CL9'   MONDAY'
         DC    CL9'  TUESDAY'
         DC    CL9'WEDNESDAY'
         DC    CL9' THURSDAY'
         DC    CL9'   FRIDAY'
         SPACE 2
USERMAP  DSECT ,                                                GP03146
TIME24HR DS    CL5           HH:MM                              GP03146
         DS    C                                                GP03146
TIME12HR DS    0CL10,CL5,C   HH:MM A.M./NOON/P.M./MIDN          GP03146
TIMEAMPM DS    CL4           A.M./NOON/P.M./MIDN                GP03146
         DS    C                                                GP03146
JULDATE  DS    CL8           YYYYDDD                            GP03146
OUTYEAR  EQU   JULDATE,4,C'C'  FOUR-DIGIT YEAR                  GP03146
         DS    C                                                GP03146
REGDAY   DS    CL8           MM/DD/YY                           GP03146
         DS    C                                                GP03146
EURODAY  DS    CL10          YYYY-MM-DD                         GP03146
         DS    C                                                GP03146
WEEKDAY  DS    CL11          WEEKDAY, (RADJ)                    GP03146
MODAYR   DS    CL19          MONTH DAY, YEAR
USERAREA EQU   USERMAP,*-USERMAP,C'C'                           GP03146
         SPACE 2
SAVE     DSECT ,             DYNAMIC SAVE/WORK AREA             GP03146
DB       DS    D                                                GP03146
WORKD    DS    CL16          EDIT WORK AREA                     GP03146
*EQTIME  DS    F                                                GP03146
*EQMINS  DS    F                                                GP03146
*EQHOUR  DS    F                                                GP03146
REQJUL   DS    F             BINARY DAY                         GP03146
REQYEAR  DS    F             BINARY YEAR                        GP03146
REQDATE  DS    PL4                                              GP03146
REQDAY   DS    F             BINARY DAY OF MONTH                GP03146
REQMON   DS    F             BINARY MONTH                       GP03146
OUTDAY   DS    C'31'         DAY OF MONTH                       GP03146
OUTMON   DS    C'12'         MONTH OF YEAR                      GP03146
SAVEEND  EQU   *                                                GP03146
         END
