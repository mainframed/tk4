SUBDD    TITLE 'S U B D D   **   FIND TIOT, JFCB, DSCB1 FOR DD NAME'
***********************************************************************
**                                                                   **
**   SUBROUTINE SUBDD PERFORMS A TIOT SCAN FOR THE DD NAME           **
**   POINTED TO BY R1. OTHER CONTROLS BLOCKS DEPENDING ON RESULT.    **
**                                   AUTHOR: G. POSTPISCHIL          **
***********************************************************************
**                                                                   **
**  R1 POINTS TO CL8'ddname'                                         **
**                                                                   **
**  RETURN CODES                                                     **
**                                                                   **
**  R15 - 0    REQUESTED NAME FOUND                                  **
**          R0 MAXIMUM BUFFER SIZE NEEDED (OR 0 IF NOT DETERMINED)   **
**          R1 FLAGS                                                 **
**                                                                   **
**  R15 - 4    REQUESTED NAME NOT FOUND                              **
**                                                                   **
**  R15 - 8    REQUESTED NAME NOT USABLE OR NOT SUPPORTED            **
**          R0 REASON CODE                                           **
**                                                                   **
**  R15 - 12   INVALID DD NAME PASSED (BLANK OR NULL)                **
**                                                                   **
***********************************************************************
         COPY  ASMOPTS
         SPACE 1
         ASMPARM LIST=YES
         SPACE 1
SUBDD    PGMHEAD ZERO12,BASE=R12,PARM=R3,AM=24,RM=ANY  ADDED ON GP13055
         CLI   0(R3),C' '    VALID NAME ?
         BNH   SETCC12         NO; REJECT IT
         LR    R8,R3              COPY DDNAME POINTER TO SCRATCH REG.
         LA    R4,DDWATTR         POINTER TO DD ATTRIBUTES
         USING DDATTRIB,R4        DECLARE TABLE
         L     R14,PSATOLD-PSA    GET MY TCB
         L     R9,TCBTIO-TCB(,R14) GET TIOT
         USING TIOT1,R9           DECLARE IT
         LA    R0,TIOENTRY-TIOT1  INCREMENT TO FIRST ENTRY
*---------------------------------------------------------------------*
*   LOOK FOR FIRST (OR ONLY) DD                                       *
*---------------------------------------------------------------------*
DDCFDD1  AR    R9,R0              NEXT ENTRY
         USING TIOENTRY,R9        DECLARE IT
         ICM   R0,1,TIOELNGH      GET ENTRY LENGTH
         BZ    DDCTDONE             TOO BAD
         TM    TIOESTTA,TIOSLTYP  SCRATCHED ENTRY?
         BNZ   DDCFDD1              YES; IGNORE
         CLC   TIOEDDNM,0(R8)     MATCHES USER REQUEST?
         BNE   DDCFDD1              NO; TRY AGAIN
         SR    R7,R7
         ICM   R7,7,TIOEFSRT      LOAD UCB ADDRESS (COULD BE ZERO)
         USING UCBOB,R7
         CLI   TIOELNGH,20        SINGLE UCB ?
         BL    SETCC8               NOT EVEN
*---------------------------------------------------------------------*
* EXAMINE ONE DD ENTRY, AND SET FLAGS AND BUFFER SIZE HIGH-WATER MARK *
*---------------------------------------------------------------------*
         SPACE 1
DDCHECK  LTR   R6,R6              DO WE HAVE A JFCB?
         BZ    SETCC8               NO; SERIOUS PROBLEM
         OI    DDWFLAGS,CWFDD     DD FOUND
         AIF   (NOT &MVSXA).MVSJFCB
         XC    DDWSWA(DDWSWAL),DDWSWA  CLEAR SWA LIST FORM
         LA    R1,DDWSVA          ADDRESS OF JFCB TOKEN
         ST    R1,DDWEPA
         MVC   DDWSVA+4(3),TIOEJFCB    JFCB TOKEN
         SWAREQ FCODE=RL,EPA=DDWEPA,MF=(E,DDWSWA),UNAUTH=YES
         BXH   R15,R15,SETCC8
         L     R6,DDWSVA          LOAD JFCB ADDRESS
         AGO   .COMJFCB
.MVSJFCB SR    R6,R6              FOR AM31
         ICM   R6,7,TIOEJFCB      SHOULD NEVER BE ZERO
         LA    R6,16(,R6)         SKIP QUEUE HEADER
.COMJFCB USING INFMJFCB,R6
         MVC   DDWDSORG,JFCDSORG  SAVE
         MVC   DDWRECFM,JFCRECFM    DCB
         MVC   DDWLRECL,JFCLRECL      ATTRIBUTES
         MVC   DDWBLKSI,JFCBUFSI
         CLC   JFCDSORG,ZEROES    ANY DSORG HERE?
         BE    DDCNOORG             NO
         CLI   JFCDSRG2,0         ANYTHING UNWANTED?
         BNE   BADDSORG             YES; FAIL
         TM    JFCDSRG1,254-JFCORGPS-JFCORGPO  UNSUPPORTED ?
         BNZ   BADDSORG             YES; FAIL
DDCNOORG SR    R5,R5
         ICM   R5,3,JFCBUFSI      ANY BLOCK/BUFFER SIZE ?
         C     R5,DDWBLOCK        COMPARE TO PRIOR VALUE
         BNH   DDCNJBLK             NOT LARGER
         ST    R5,DDWBLOCK        SAVE FOR RETURN
DDCNJBLK LTR   R7,R7         IS THERE A UCB ?
         BZ    DDCSEQ          NO; MUST NOT BE A PDS
         CLI   UCBTBYT3,UCB3DACC  DASD VOLUME ?
         BNE   DDCSEQ               NO; NOT PDS
         L     R14,PATFMT1
         LA    R15,JFCBDSNM  POINT TO DSN
         LA    R0,UCBVOLI    POINT TO SERIAL
         LA    R1,DS1FMTID   POINT TO RETURN
         STM   R14,R1,DDCFMT1
         OBTAIN DDCFMT1      READ DSCB1
         BXH   R15,R15,DDCX1DD
         CLI   DS1FMTID,C'1'      SUCCESSFUL READ ?
         BNE   SETCC8               NO; OOPS
         SPACE 1
         CLC   DDWDSORG,ZEROES
         BNE   *+4+6
         MVC   DDWDSORG,DS1DSORG  SAVE
         CLI   DDWRECFM,0
         BNE   *+4+6
         MVC   DDWRECFM,DS1RECFM    DCB
         CLC   DDWLRECL,ZEROES
         BNE   *+4+6
         MVC   DDWLRECL,DS1LRECL      ATTRIBUTES
         CLC   DDWBLKSI,ZEROES
         BNE   *+4+6
         MVC   DDWBLKSI,DS1BLKL
         SPACE 1
         LTR   R5,R5              DID JFCB HAVE OVERRIDING BUFFER SIZE
         BNZ   DDCUJBLK             YES; IGNORE
         LH    R5,DS1BLKL         GET BLOCK SIZE
         C     R5,DDWBLOCK        COMPARE TO PRIOR VALUE
         BNH   DDCUJBLK             NOT LARGER
         ST    R5,DDWBLOCK        SAVE FOR RETURN
DDCUJBLK CLI   DS1DSORG+1,0       ANYTHING UNPROCESSABLE?
         BNE   BADDSORG
         TM    DS1DSORG,254-DS1DSGPS-DS1DSGPO   UNSUPPORTED?
         BNZ   BADDSORG             YES; TOO BAD
         TM    DS1DSORG,DS1DSGPO
         BZ    DDCSEQ             (CHECK JFCB OVERRIDE DSORG?)
         TM    JFCBIND1,JFCPDS    MEMBER NAME ON DD ?
         BNZ   DDCPMEM              YES; SHOW
         OI    DDWFLAGS,CWFPDS    SET PDS ONLY
         B     DDCX1DD              NO
DDCPMEM  OI    DDWFLAGS,CWFPDQ    SHOW SEQUENTIAL PDS USE
         B     DDCX1DD
*---------------------------------------------------------------------*
*   FOR A CONCATENATION, PROCESS THE NEXT DD                          *
*---------------------------------------------------------------------*
         SPACE 1
DDCSEQ   OI    DDWFLAGS,CWFSEQ    SET FOR SEQUENTIAL ACCESS
DDCX1DD  SR    R0,R0              RESET
         LTR   R8,R8              FIRST TIME FOR DD ?
         BZ    FIND1DD              NO
         MVC   DDWFLAG1,DDWFLAGS  SAVE FLAGS FOR FIRST DD
         NI    DDWFLAGS,255-CWFDD RESET DD FOUND
         SR    R8,R8              SIGNAL FIRST DD DONE
FIND1DD  IC    R0,TIOELNGH        GET ENTRY LENGTH BACK
         AR    R9,R0              NEXT ENTRY
         ICM   R0,1,TIOELNGH      GET NEW ENTRY LENGTH
         BZ    DDCTDONE             ZERO; ALL DONE
         TM    TIOESTTA,TIOSLTYP  SCRATCHED ENTRY?
         BNZ   DDCTDONE             YES; DONE (?)
         CLI   TIOEDDNM,C' '      CONCATENATION?
         BH    DDCTDONE             NO; DONE WITH DD
         LA    R4,DDASIZE(,R4)    NEXT DD ATTRIBUTE ENTRY
         SR    R7,R7
         ICM   R7,7,TIOEFSRT      LOAD UCB ADDRESS (COULD BE ZERO)
         USING UCBOB,7
         CLI   TIOELNGH,20        SINGLE UCB ?
         BL    SETCC8               NOT EVEN
         B     DDCHECK            AND PROCESS IT
         SPACE 1
DDCTDONE CLI   DDWFLAGS+1,0  ANYTHING FOUND ?
         BNE   PGMEXIT         YES; OK
         SETCC 4                       SHOW NOT FOUND
         B     PGMEXIT                 RETURN
         SPACE 1
BADDSORG LA    R0,ERFNORG         BAD DSORG
         B     SETCC8R
SETCC8R  ST    R0,RSNCODE
SETCC8   SETCC 8
         B     PGMEXIT
         SPACE 1
SETCC12  SETCC 12,ERFNODD
*NEXT*   B     PGMEXIT
         SPACE 1
PGMEXIT  SERVTERM ,
         PGMEXIT COPYRET=(RETCODE,12)  RETURN R15-R1
         SPACE 2
PATFMT1  CAMLST SEARCH,*-*,*-*,*-*
         ORG   PATFMT1+4     KEEP FLAGS ONLY
         LTORG ,
         SPACE 2
DDATTRIB DSECT ,
DDWDSORG DS    H             DS ORG FROM JFCB OR DSCB1 (2B)
DDWRECFM DS    X             RECORD FORMAT
         DS    X               POSSIBLE KEY LENGTH ?
DDWLRECL DS    H             RECORD LENGTH
DDWBLKSI DS    H             BLOCK/BUFFER SIZE
DDASIZE  EQU   *-DDATTRIB
         SPACE 2
SAVE     DSECT ,
         SERVDEFS ,          DEFINE RETCODE, ETC.
         SPACE 1
DDCFMT1  CAMLST SEARCH,*-*,*-*,*-*
DDWATTR  DS    16XL8         DS ATTRIBUTES (DSORG,RECFM,X,LRECL,BLKSI)
         AIF   (NOT &MVSXA).COMSWA
DDWEPA   DS    A(DDWSVA)
DDWSWA   SWAREQ FCODE=RL,EPA=DDWEPA,MF=L
DDWSVA   DS    7A                 (IBM LIES ABOUT 4A)
DDWSWAL  EQU   *-DDWSWA           LENGTH TO CLEAR
.COMSWA  SPACE 1
ZEROES   DS    F             CONSTANT
         ORG   RSNCODE         RETURN SIZE IN R0
DDWBLOCK DS    F             MAXIMUM BUFFER SIZE NEEDED
         ORG   RR1CODE         RETURN SIZE IN R1
DDWFLAG1 DS    X             RESULT FLAGS FOR FIRST DD)
DDWFLAGS DS    X             RESULT FLAGS FOR ALL; (EXC, CWFDD FLAG)
CWFDD    EQU   X'80'           FOUND A DD
CWFSEQ   EQU   X'40'           USE IS SEQUENTIAL
CWFPDQ   EQU   X'20'           DS IS PDS WITH MEMBER NAME
CWFPDS   EQU   X'10'           DS IS PDS
CWFBLK   EQU   X'02'           DD HAS FORCED BLKSIZE
ERRFLAGS DS    X             ERROR CONDITIONS
ERFNODD  EQU   X'80'           DD NOT FOUND
ERFNODV  EQU   X'40'           UNSUPPORTED DEVICE
ERFNORG  EQU   X'20'           UNSUPPORTED DSORG
ERFNOPDS EQU   X'10'           MEMBER, BUT NOT PDS
         ORG   ,             END OVERLAY
         SPACE 1
         IECSDSL1 1               MAP FORMAT 1 DSCB
         DS    CL256              EXTRA WORK SPACE
         SPACE 1
SAVEEND  EQU   *
         SPACE 1
         PRINT NOGEN
MYTIOT   DSECT ,
         IEFTIOT1 ,
         SPACE 1
MYJFCB   DSECT ,
         IEFJFCBN ,
         SPACE 1
MYUCB    DSECT ,
         IEFUCBOB ,
         SPACE 1
         IEZJSCB ,
         IKJTCB ,
         IHAPSA ,
         AIF   (NOT &MVSXA).MVSEND
         CVT   DSECT=YES
         IEFJESCT ,
.MVSEND  END   ,
