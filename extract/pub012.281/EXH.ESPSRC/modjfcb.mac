MODJFCB  TITLE 'M O D J F C B  ***  PL/I SUBROUTINE FOR JFCB MOD SVC'
*              THIS PROGRAM WAS WRITTEN BY:
         SPACE 1
*                                  SEYMOUR JEROME METZ
*                                  PINKERTON COMPUTER CONSULTANTS, INC.
*                                  5881 LEESBURG PIKE
*                                  BAILEYS CROSSROADS, VIRGINIA   22041
*                                  703-820-5571
         SPACE 1
*              THIS PROGRAM IS FURNISHED ON AN AS-IS BASIS.
*              IT IS NOT PROPRIETARY, AND MAY BE DISTRIBUTED TO ANY
*                   OTHER INSTALLATION.
*              FEEL FREE TO CONTACT ME IF ANY PROBLEMS OCCUR.
         SPACE 3
*              THIS ROUTINE MODIFIES A JFCB BY CALLING THE MODJFCB SVC.
         SPACE 1
*              IT REQUIRES THREE PARAMETERS:
*              SDV FOR 8 CHARACTER DDNAME
*              SDV FOR 6 CHARACTER VOLUME SERIAL NUMBER
*              HALFWORD FILE SEQUENCE NUMBER
         SPACE 1
*              THE OPTIONAL FOURTH PARAMETER IS A SDV FOR 44 CHAR DSN.
         SPACE 1
*              THE MAXIMUM STRING LENGTH IS NEVER CHECKED, ONLY THE
*              CURRENT LENGTH. IF THE DSN IS A NULL STRING, IT IS
*              IGNORED, OTHERWISE IT MUST BE 44 CHARS.
         SPACE 2
*              THE FOLLOWING ON CONDITIONS MAY OCCUR:
         SPACE 1
*              #ARGS                    LESS THAN THREE ARGUEMENTS
*              #CHARS                   INCORRECT STRING LENGTH
*              #240                     NON-ZERO RETURN CODE FORM SVC
         EJECT
*              ALTERNATE ENTRY MODMEM REQUIRES TWO PARAMETERS:
*              SDV FOR 8 CHARACTER DDNAME
*              SDV FOR 8 CHARACTER MEMBER NAME
         SPACE 1
*              OPTIONAL THIRD AND FOURTH PARAMETERS ARE VOLUME SERIAL
*              AND DSNAME AS ABOVE. BOTH ARE IGNORED IF LENGTH 0.
MOD      TITLE 'PL1LIB REGISTER EQUATES, DSECTS, ETC.'
         IHELIB
SAVE     DSECT
SAVEFLG1 DS    X
SAVEFLG2 DS    X
SAVELEN  DS    H
SAVEDR   DS    A
SAVENEXT DS    A
SAVELR   DS    A
SAVEBR   DS    A
SAVER0   DS    A
SAVERA   DS    A
SAVERB   DS    A
SAVERC   DS    A
SAVERD   DS    A
SAVERE   DS    A
SAVERF   DS    A
SAVERG   DS    A
SAVERH   DS    A
SAVERI   DS    A
SAVERJ   DS    A
SAVEWR   DS    A
SAVEPR   DS    A
         DS    D                        CURRENT FILE
         DS    D                        INVOCATION COUNT
         DS    D                        DISPLAY
         DS    F                        STATEMENT NUMBER
         DS    F                        A(TASK VARIABLE CHAIN)
         DS    F                        ZERO
DDNAME   DS    A
VOLSER   DS    A
MEMBER   DS    0A
FILESEQ  DS    A
DSNAME   DS    A
SAVEND   EQU   *
         TITLE 'MODIFY JFCB FOR MPPL PROGRAN VIA MODJFCB SVC'
MODJFCB  CSECT
MODJFCB GETDSA
         BALR  R2,0
         USING *,R2
         L     R11,=A(MODJFCB)
         DROP  BR
         USING MODJFCB,R11
         LM    R3,R6,0(R1)
         TM    0(R1),X'80'              IS DDNAME LAST ARGUEMENT?
         BO    CZECH                     YES - NO SERIAL - SIGNAL #ARGS
         TM    4(R1),X'80'              IS VOLUME SER LAST ARGUEMENT?
         BO    CZECH                     YES - NO FILE SEQ
         CLC   6(2,R12),=H'8'           IS DDNAME 8 CHARACTERS?
         BNE   DOPE                      NO - SIGNAL #CHARS
         CLC   6(2,R13),=H'6'           IS SERIAL 6 CHARACTERS?
         BNE   DOPE                      NO - SIGNAL #CHARS
         MVC   DDNAME+1(3),1(R3)
         MVI   DDNAME,X'01'
         MVC   VOLSER+1(3),1(R4)
         MVI   VOLSER,X'03'
         ST    R5,FILESEQ
         MVI   FILESEQ,X'82'
TESTDSN  TM    8(R10),X'80'             IS DSNAME PARAMETER PRESENT?
         BO    CALLIT
         OC    6(2,R15),6(R15)          IS DSN A NULL STRING?
         BZ    CALLIT
         CLC   6(2,R15),=H'44'          FULL DSN?
         BNE   DOPE                      NOPE - SIGNAL #CHARS
         NI    FILESEQ,X'7F'            REMOVE LAST FLAG
         MVC   DSNAME+1(3),1(R6)
         MVI   DSNAME,X'86'             MAKE DSN LAST ENTRY
CALLIT   LA    R10,DDNAME               MODJFCB PARAMETER LIST
         SVC   240
         LTR   15,15
         BZ    BYBY
         LA    R10,#240LIST             SIGNAL CONDITION(#240)
DEAR     L     BR,=V(IHEERRD)
         BALR  LR,BR
ABBY     IHEPRV ERR,R10,OP=LA           SIGNAL ERROR
         MVI   0(R10),X'60'
         L     BR,=V(IHEERRB)
         BALR  LR,BR
BYBY   FREEDSA
         EJECT
MODMEM  GETDSA LABEL=BEGMEM
         BALR  R11,0
         USING *,R11
         L     R11,=A(MODJFCB)
         DROP  BR
         USING MODJFCB,R11
         LM    R12,R15,0(R10)
         CLC   6(2,R12),=H'8'           IS DDNAME 8 CHARACRERS?
         BNE   DOPE
         TM    0(R10),X'80'             IS DDNAME LAST PARAMETER?
         BO    CZECH                     YES - RE-MEMBER THE DUMP.
         MVI   DDNAME,X'01'
         MVC   DDNAME+1(3),1(R12)
         CLC   6(2,R13),=H'8'           IS MEMBER NAME 8 CHARACTERS?
         BNE   DOPE
         MVI   MEMBER,X'84'
         MVC   MEMBER+1(3),1(R13)
         MVI   VOLSER,0
         TM    4(R10),X'80'             IS MEMBER THE LAST ARGUEMENT?
         BO    CALLIT
         OC    6(2,R14),6(R14)          IS VOL-SER NULL?
         BZ    TESTDSN                   YES - TRY DSN
         CLC   6(2,R14),=H'6'           IS SERIAL 6 CHARACTERS?
         BNE   DOPE                      NO - DUMP THE CEREAL
         MVC   VOLSER+1(3),1(R14)
         MVI   VOLSER,X'03'
         B     TESTDSN
CZECH    LA    R10,#ARGL                SIGNAL CONDITION(#ARGS)
         B     DEAR
DOPE     LA    R10,#CHARL               SIGNAL CONDITION(#CHARS)
         B     DEAR
         LTORG
         DS    0F
#240LIST DC    X'50',AL3(#240)
#ARGL    DC    X'50',AL3(#ARGS)
#CHARL   DC    X'50',AL3(#CHARS)
         TITLE 'ON CONDITIONS'
#240     CSECT
         DC    AL1(4),C'#240'
#ARGS    CSECT
         DC    AL1(5),C'#ARGS'
#CHARS   CSECT
         DC    AL1(6),C'#CHARS'
         PRINT NOGEN                                            GP03028
         YREGS ,                                                GP03028
         END
