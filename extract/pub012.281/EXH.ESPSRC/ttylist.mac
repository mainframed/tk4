TTYS     TITLE 'T T Y L I S T  ***  TTY CONTROL SEQUENCES'
         COPY  OPTIONGB
         SPACE 1
         SYSPARM LIST=YES
         SPACE 1
***********************************************************************
*                                                                     *
*        COPYRIGHT 1984  EXPERT SYSTEM PROGRAMMING, INC.              *
*        COPYRIGHT 2003  EXPERT SYSTEM PROGRAMMING                    *
*                        176 OLD STAGE COACH ROAD                     *
*                        BRADFORD, VT 05033-8844                      *
*                                                                     *
*                    ALL RIGHTS RESERVED                              *
*                                                                     *
***********************************************************************
         EJECT ,
         PRINT &PRTSOR
*********************************************************************
*                                                                   *
*        THIS PROGRAM CREATES A TABLE OF TTY CONTROL SEQUENCES      *
*        AS DEFINED IN THE LPA RESIDENT IGPVOLT MODULE.             *
*                                                                   *
*********************************************************************
         SPACE 1
TTYLIST  SAVEM ZERO,BASE=R12,PARM=R9                    ADDED ON 84267
         SERVINIT ,          LOAD THE SERVICE ROUTINE
         SERVCALL LPALD,=CL8'IGPVOLT'  GET THE VOLUME TABLE
         LR    R10,R0        SAVE THE ADDRESS
         USING USERVOLT,R10   DECLARE IT
         SERVCALL LPALD,=CL8'@PRINTER'  GET THE PRINT ROUTINE
         ST    R0,@PRINTER   STASH IT
         SPACE 1
         PRTOPEN SYSPRINT,OPT=(ABEND)  NEED OUTPUT FILE
         PRTLIST TITLE1,TITLE=1  MAKE A TITLE
         SPACE 1
         L     R9,VTTTYTAB   GET THE TTY LOOKUP TABLE
         L     R8,0(,R9)     GET THE NULL ENTRY
         SR    R8,R9         GET OFFSET TO NULL ENTRY
         SRL   R8,2          GET NUMBER OF ENTRIES
         SH    R8,=H'2'      ALLOW FOR TERMNAME OF NULL TERMINAL
         BNP   NOTTYS        NONE ?
         SPACE 1
LOOPTERM L     R7,0(,R9)     GET THE TERMINAL ENTRY
         SH    R7,=Y(L'TERMNAME)   GO TO PREFIX
         PRTLIST INTRO,TITLE=4     INTRODUCTORY INFORMATION
         INC   TERMNO        INCREASE THE TERMINAL NUMBER
         LA    R5,DCFEAT     POINT TO FEATURE ENTRY
         LA    R6,TDOPTOFF   GET NUMBER OF ENTRIES
         L     R7,0(,R9)     POINT TO FIRST ENTRY
         LA    R3,0(R6,R7)   OPTION STRING LOCATION
         MVC   DB(4),0(R3)   COPY OPTIONS
         PRTLIST SHOWOPTS    DISPLAY OPTIONS
LOOPFEAT SLR   R2,R2
         ICM   R2,1,0(R7)    GET THE ENTRY OFFSET
         BZ    NONEFEAT      FEATURE NOT PRESENT
         L     R3,0(,R9)     GET THE TABLE BASE
         AR    R3,R2         GET ENTRY LENGTH/ADDRESS
         ICM   R2,1,0(R3)    GET LENGTH
         BZ    NONEFEAT      NOT PRESENT
         ST    R2,DB         SAVE LENGTH FOR FORMATTING
         PRTLIST SHOWFEAT    SHOW FEATURE LENGTH/TEXT
HEXLOOP  PRTLIST SHOWHEX     SHOW HEX TEXT
         LA    R3,1(,R3)
         BCT   R2,HEXLOOP    REPEAT FOR ALL
         B     BUMPFEAT      GO TO NEXT FEATURE
NONEFEAT PRTLIST SHOWNONE    NO FEATURE PRESENT
BUMPFEAT LA    R7,1(,R7)     POINT TO NEXT ENTRY OFFSET
         LA    R5,DCFEAT2-DCFEAT1(,R5)  NEXT TEXT ENTRY
         BCT   R6,LOOPFEAT   DO NEXT FEATURE
         SPACE 1
BUMPTERM LA    R9,4(,R9)     BUMP TO NEXT TERMINAL POINTER
         TITLE 'T T Y L I S T   ***   TTY CONTROL SEQUENCES'
         BCT   R8,LOOPTERM   DO IT
         SPACE 1
         SERVTERM ,          CLOSE AND FREE
         ENDM  RC=0          QUIT
         SPACE 2
NOTTYS   PRTF  'No TTY sequences defined',CC=NO
         SERVTERM ,
         ABEND 666
         SPACE 2
SHOWNONE FDOPT NL,CC=C'0'
         FDPRT DCFEAT1-DCFEAT1(R5),L'DCFEAT,DEB,RADJ,LEN=30
         FDPRT ': none'
         FDPRT *END
         SPACE 1
SHOWFEAT FDOPT NL,CC=C'0'
         FDPRT DCFEAT-DCFEAT(R5),L'DCFEAT,DEB,RADJ,LEN=30
         FDPRT ': L='
         FDPRT DB,4,I,DEB,LEN=2
         FDPRT 'Text=',PADL
         FDPRT 1(R3),(R2),LEN=15
         FDPRT 'Hex=',RADJ,PADL
         FDPRT *END
SHOWHEX  FDOPT IND=62
         FDCLI 1(R3),X'07',BE=SHOWHEXX  SKIP PADDING BYTES
         FDPRT 1(R3),1,HEX
SHOWHEXX FDPRT *END
         SPACE 2
SHOWOPTS FDPRT 'Options (',NL
         FDPRT DB,4,HEX
         FDPRT ')',PADR,LEN=2
         FDOPT IND=20
         FDCLI DB+3,0,BE=SHOWO3X
         FDCLI DB+3,1,BE=SHOWO31
         FDCLI DB+3,2,BE=SHOWO32
         FDCLI DB+3,3,BNE=SHOWO3X
         FDPRT 'Decimal Tabs relative to 0'
         FDGOTO SHOWO3S
SHOWO32  FDPRT 'Decimal Tabs relative to 1'
         FDGOTO SHOWO3S
SHOWO31  FDPRT 'Binary (ASCII) Tabs'
SHOWO3S  FDPRT ' ',LEN=3
SHOWO3X  FDCLI DB+2,0,BE=SHOWO2X
         FDCLI DB+2,1,BNE=SHOWO2X
         FDPRT 'Attribute takes one display position'
SHOWO2X  FDPRT *END          NOTHING ELSE YET
         EJECT ,
SYSPRINT PRTWORK SYSPRINT,SYSTERM,TITLE=5,WIDTH=78
TITLE1   FDOPT NL,CC=C'#'  AUTO PAGE/DATE/TIME
         FDPRT 'TTY Control Sequences by Terminal'
         FDPRT *END
         SPACE 1
INTRO    FDPRT TERMNAME-TERMSECT(R7),L'TERMNAME,DEB,PADR
         FDPRT 'Terminal',PAD
         FDPRT TERMNO,I,DEB,DEBZ       TERMINAL NUMBER
         FDPRT *END
         SPACE 1
         PRINT &PRTMAC
         TTYFLAGS ,
         SPACE 1
DCFEAT   DC    (TDOPTOFF)CL30'?'   OPTION/FEATURE NAME
         ORG   DCFEAT+30*TDOCLT
         DC    CL30'Clear Tabs'
         ORG   DCFEAT+30*TDOCLD
         DC    CL30'Clear Device'
         ORG   DCFEAT+30*TDOCLS
         DC    CL30'Clear Screen'
         ORG   DCFEAT+30*TDOCBRK
         DC    CL30'Plot command BREAK Reset'
         ORG   DCFEAT+30*TDOTEXT
         DC    CL30'Normal Text Rendition'
         ORG   DCFEAT+30*TDOERR
         DC    CL30'Error Message Rendition'
         ORG   DCFEAT+30*TDOHIDE
         DC    CL30'Non-Display Input Field'
         ORG   DCFEAT+30*TDOWARN
         DC    CL30'Warning Message Rendition'
         ORG   DCFEAT+30*TDOW80
         DC    CL30'Narrow Width=80'
         ORG   DCFEAT+30*TDOW132
         DC    CL30'Wide Width=132'
         ORG   DCFEAT+30*TDOTAB1
         DC    CL30'First Tab Sequence'
         ORG   DCFEAT+30*TDOTAB2
         DC    CL30'Second Tab Sequence'
         ORG   DCFEAT+30*TDOPR1N
         DC    CL30'Auxiliary Port On/Device Off'
         ORG   DCFEAT+30*TDOPR1F
         DC    CL30'Reset Device On/Auxiliary Off'
         ORG   DCFEAT+30*TDOPR2N
         DC    CL30'Set Device+Auxiliary On'
         ORG   DCFEAT+30*TDOPR2F
         DC    CL30'Reset Device On/Auxiliary Off'
DCFEAT1  EQU   DCFEAT,L'DCFEAT,C'C'
DCFEAT2  EQU   DCFEAT+L'DCFEAT,L'DCFEAT,C'C'
         ORG   ,
         SPACE 1
TERMSECT DSECT ,             MAPPING OF ONE TERMINAL ENTRY
TERMNAME DS    CL8           OFFSET -8 FROM ENTRY
TERMDISP DS    (TDOPTOFF)AL1(0)  BYTE POINTERS TO OPTIONS
TERMOPTS DS    4AL1(0)       OPTION BITS
         DS    X             VARIABLE NUMBER AND LENGTH OPTIONS
         SPACE 1
         PRINT &PRTSYS
SAVE     DSECT ,             WORK/SAVE AREA
@SERVICE DS    A
@PRINTER DS    A
@IGPVOLT DS    A
TERMNO   DS    F             TERMINAL NUMBER (INDEX)
DB       DS    D
SAVEND   EQU   *             END
         SPACE 1
CVTDSECT DSECT ,
         CVT   DSECT=YES
         IHACDE ,
IGPVOLT  USERVOLT LIST=NO,MAP=
         END   ,
