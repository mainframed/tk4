DMAP     TITLE 'DIPLOMACY - MAP DEFINITION AND PRINTING'
         MACRO
         INIT
         GBLC  &AR(896)
         GBLC  &Z
         GBLC  &CH,&CV,&LH,&LV,&OH,&OV
         GBLA  &BY(400),&BC
         GBLA  &MAX,&XMAX,&YMAX,&XINC
         LCLA  &I
         LCLC  &ZZ
&XMAX    SETA  128
&YMAX    SETA  56
&XINC    SETA  &XMAX/8
&MAX     SETA  &XINC*&YMAX
&Z       SETC  '
&ZZ      SETC  '&Z&Z&Z&Z&Z&Z&Z&Z'
&BC      SETA  1
&LH      SETC  '''-'''
&LV      SETC  '''×'''
&OH      SETC  '''.'''
&OV      SETC  '''.'''
&CH      SETC  '''='''
&CV      SETC  '''"'''
&I       SETA  1
.LOOP    AIF   (&I GT &MAX).LEND
&AR(&I)  SETC  '&ZZ'
&I       SETA  &I+1
         AGO   .LOOP
.LEND    ANOP
         MEND
         SPACE 2
         MACRO
         BYTE  &C,&I,&J
         GBLC  &AR(896)
         GBLC  &Z
         GBLA  &MAX,&XMAX,&YMAX,&XINC
         LCLA  &K,&L,&M,&N
         LCLC  &B,&OB
         AIF   (&I LT 1 OR &I GT &XMAX).MEND
         AIF   (&J LT 0 OR &J GT &YMAX).MEND
         AIF   ('&C' EQ '').MEND
&B       SETC  '&C'
         AIF   (K'&C EQ 1).OK
         AIF   (K'&C NE 2).C3
&B       SETC  '&Z'
         AGO   .OK
.C3      AIF   (K'&C NE 3).MEND
&B       SETC  '&C'(2,1)
.OK      ANOP
&K       SETA  (&I+7)/8+&J*&XINC
&L       SETA  &I-1-((&I-1)/8)*8
         AIF   ('&B' GE 'A' OR '&B' EQ '&Z').WHICH
&OB      SETC  '&AR(&K)'(&L+1,1)
         AIF   ('&OB' GE 'A' OR '&OB' EQ '&Z').WHICH
&B       SETC  '+'
.WHICH   AIF   (&L LT 1).LO
         AIF   (&L EQ 7).HI
&M       SETA  &L+2
&N       SETA  8-&L
&AR(&K)  SETC  '&AR(&K)'(1,&L).'&B'.'&AR(&K)'(&M,&N)
         MEXIT
.LO      ANOP
&AR(&K)  SETC  '&B'.'&AR(&K)'(2,7)
         MEXIT
.HI      ANOP
&AR(&K)  SETC  '&AR(&K)'(1,7).'&B'
.MEND    MEND
         SPACE 2
         MACRO
&PRO     NAME  &I,&J,&E1,&E2,&S1,&S2,&E3,&E4
         GBLA  &BY(400),&BC
         LCLC  &PB
         LCLA  &M,&N
&M       SETA  &I
&N       SETA  1
         AIF   ('&E1' EQ '' OR '&E2' EQ '').LOOP
&BY(&BC) SETA  &E1
&BY(&BC+1) SETA &E2
         AIF   ('&E3' EQ '' OR '&E4' EQ '').UP4
&BY(&BC+2) SETA &E3
&BY(&BC+3) SETA &E4
.UP4     ANOP
&BC      SETA  &BC+4
.LOOP    ANOP
&PB      SETC  '&PRO'(&N,1)
         BYTE  &PB,&M,&J
&N       SETA  &N+1
&M       SETA  &M+1
         AIF   (&N LT 5).LOOP
         AIF   ('&S1' EQ '' OR '&S2' EQ '').MEND
         BYTE  *,&S1,&S2     SUPPLY CENTER
.MEND    MEND
         SPACE 2
         MACRO
         EXPAND
         GBLC  &AR(896),&Z
         GBLA  &BY(400),&BC
         GBLA  &MAX,&XMAX,&YMAX,&XINC
         LCLA  &I,&J,&K,&L
&I       SETA  1
&L       SETA  1
PROVLOC  DS    0F .          START OF PROVINCE LABEL LOCATION TABLE
.PRO     AIF   (&L GE &BC).PROEND
         DC    AL1(&BY(&L),&BY(&L+1),&BY(&L+2),&BY(&L+3))
&L       SETA  &L+4
         AGO   .PRO
.PROEND  ANOP
PROVEND  EQU   * .           END OF PROVINCE ADDRESSES
         SPACE 2
LINEONE  EQU   *
.LOOP    AIF   (&I GT &MAX).LEND
         DS    0H
 DC AL2(137,0),CL133' &AR(&I)&AR(&I+1)&AR(&I+2)&AR(&I+3)&AR(&I+4)&AR(&I*
               +5)&AR(&I+6)&AR(&I+7)&AR(&I+8)&AR(&I+9)&AR(&I+10)&AR(&I+*
               11)&AR(&I+12)&AR(&I+13)&AR(&I+14)&AR(&I+15)'
&I       SETA  &I+&XINC
         AGO   .LOOP
.LEND    ANOP
         SPACE
LASTLINE EQU   *
         MEND
         SPACE 2
         MACRO
         VOCE  &I,&J1,&J2,&P4
         GBLC  &CH,&CV,&LH,&LV,&OH,&OV
         AIF   ('&P4' NE '').FOUR
         LINE  &OV,&I,&J1,&I,&J2
         MEXIT
.FOUR    LINE  &OV,&I,&J1,&J2,&P4
         MEND
         SPACE 2
         MACRO
         VLAN  &I,&J1,&J2,&P4
         GBLC  &CH,&CV,&LH,&LV,&OH,&OV
         AIF   ('&P4' NE '').FOUR
         LINE  &LV,&I,&J1,&I,&J2
         MEXIT
.FOUR    LINE  &LV,&I,&J1,&J2,&P4
         MEND
         SPACE 2
         MACRO
         VCOA  &I,&J1,&J2,&P4
         GBLC  &CH,&CV,&LH,&LV,&OH,&OV
         AIF   ('&P4' NE '').FOUR
         LINE  &CV,&I,&J1,&I,&J2
         MEXIT
.FOUR    LINE  &CV,&I,&J1,&J2,&P4
         MEND
         SPACE 2
         MACRO
         HOCE  &J,&I1,&I2,&P4
         GBLC  &CH,&CV,&LH,&LV,&OH,&OV
         AIF   ('&P4' NE '').FOUR
         LINE  &OH,&I1,&J,&I2,&J
         MEXIT
.FOUR    LINE  &OH,&J,&I1,&I2,&P4
         MEND
         SPACE 2
         MACRO
         HLAN  &J,&I1,&I2,&P4
         GBLC  &CH,&CV,&LH,&LV,&OH,&OV
         AIF   ('&P4' NE '').FOUR
         LINE  &LH,&I1,&J,&I2,&J
         MEXIT
.FOUR    LINE  &LH,&J,&I1,&I2,&P4
         MEND
         SPACE 2
         MACRO
         HCOA  &J,&I1,&I2,&P4
         GBLC  &CH,&CV,&LH,&LV,&OH,&OV
         AIF   ('&P4' NE '').FOUR
         LINE  &CH,&I1,&J,&I2,&J
         MEXIT
.FOUR    LINE  &CH,&J,&I1,&I2,&P4
         MEND
         SPACE 2
         MACRO
         LINE  &B,&I1,&J1,&I2,&J2
         LCLA  &I,&J,&DX,&DY
         LCLA  &M,&N
&DX      SETA  5
&DY      SETA  5
&I       SETA  &I1
&J       SETA  &J1
&M       SETA  1
&N       SETA  1
         AIF   (&I1 EQ &I2).IE
         AIF   (&I1 LT &I2).IH
&N       SETA  &I1-&I2+1
&DX      SETA  4
         AGO   .IE
.IH      ANOP
&DX      SETA  6
&N       SETA  &I2-&I1+1
.IE      AIF   (&J1 EQ &J2).LOOP
         AIF   (&J1 LT &J2).JH
&DY      SETA  4
&N       SETA  &J1-&J2+1
         AGO   .LOOP
.JH      ANOP
&DY      SETA  6
&N       SETA  &J2-&J1+1
.LOOP    BYTE  &B,&I,&J
&I       SETA  &I+&DX-5
&J       SETA  &J+&DY-5
&M       SETA  &M+1
         AIF   (&M LE &N).LOOP
         MEND
         EJECT
         PRINT NOGEN
DIPLMAPS START 0
         USING *,R12
         REGEQU
         STM   R14,R12,12(R13)
         LR    R12,R15
         LA    R9,SAVE
         ST    R9,8(R13)
         ST    R13,4(R9)
         LR    R13,R9
         SPACE
         SR    R3,R3         CLEAR LINE COUNTER
         LA    R7,LINEONE    GET FIRST LINE
PRLOOP   LR    R1,R7         GET LINE ADDRESS
         MVI   BLANKS,C' '
         MVC   BLANKS+1(L'BLANKS-1),BLANKS   CLESR LINE
         OC    4(133,R1),BLANKS   MAKE UPPER CASE
         CALL  XPRNT
         MVC   BLCO,BLANKS
         MVC   BL2,BLANKS    CLEAR OUT
         STC   R3,LCT        STASH FOR CLC
         LA    R3,1(R3)      UP LINE COUNTER TO CURRENT #
         LA    R4,PROVLOC    GET FIRST PROVINCE ENTRY
         L     R5,=V(PROV#BEG)   GET PROVINCE ENTRY TABLE
         USING PROVINCE,R5
LOOPRO   MVC   ENTRY,SPACES
         MVC   WHO,PROCC
         CLC   LCT,1(R4)     THIS LINE ?
         BNE   SEETWO        NO, SEE IF SECONDARY ENTRY
         MVC   LOC1,0(R4)    STASH TO ADDRESS
         TM    PROF,PROFNAV+PROFTWO+PROFSOU   SOUTH COAST OCCUPANCY ?
         BO    SEETWO        YES - SEE IF ON SECOND ONE
         TM    PROF,PROFARM+PROFNAV  OCCUPIED ?
         BNZ   COMFORM       YES - FORMAT IT
         TM    PROF,PROFSUP  SUPPLY CENTER ?
         BZ    LOOPINC       NO, SKIP
         CLI   PROSC,0       CAPTURED ?
         BE    LOOPINC       NO, IGNORE
         MVC   WHO,PROSC     FORMAT CAPTURING COUNTRY
         B     COMFORM
         SPACE
SEETWO   CLC   LCT,3(R4)
         BNE   LOOPINC
         TM    PROF,PROFNAV+PROFTWO+PROFSOU  ON SOUTH COAST ?
         BNO   LOOPINC       NO, IGNORE
         MVC   LOC1,2(R4)    USE SOUTH COAST ADDRESS
         SPACE
COMFORM  MVC   FORCE,PROF    MOVE FORCE
         NI    FORCE,PROFARM+PROFNAV   MASK IT
         TR    FORCE,FTAB    MAKE PRINTABLE
         L     R14,=V(DIPLPROV)   GET STATUS TABLE
         LA    R14,2*STASLEN(R14)   SKIP DUMMY ENTRIES
         USING STASECT,R14
         LA    R15,7         MAX ENTRIES
LOOPSTA  CLC   STABIT,WHO    SAME COUNTRY ?
         BE    FOUNDSTA      YES
         LA    R14,STASLEN(R14)
         BCT   R15,LOOPSTA
         B     LOOPINC       SKIP IF COUNTRY NOT FOUND
FOUNDSTA MVC   NATION,STASNAT   COPY COUNTRY
         LA    R14,ENTRY
         LA    R15,L'ENTRY-1
         LH    R1,LOC
         LA    R1,VWORK+4(R1)   POINT TO ENTRY LOCATION
         CLI   FORCE,C' '
         BE    ENTCO         COUNTRY ONLY
         MVC   VWORK2-VWORK(5,R1),=5C'_'   MAKE UNDER-LINE
         MVI   BLANKS,C'+'   SET FOR OVERPRINTING
         MVI   BL2,C'+'      SET FOR OVERPRINT
         B     ENTMOV
ENTCO    LA    R1,VWORKC-VWORK+1(R1)   SKIP TO COUNTRY LINE
         SH    R15,H2        SHOW NATION ONLY
         MVI   VWORKC+4,C'+'  SIGNAL DATA
ENTMOV   EX    R15,MVC
LOOPINC  LA    R4,4(R4)
         LA    R5,PROLEN(R5)
         C     R4,=A(PROVEND)   DONE ?
         BL    LOOPRO        NO
         CLI   VWORKC+4,C' '   ANY COUNTRY ?
         BE    NOCON         NO
         LA    R1,VWORKC
         CALL  XPRNT         PRINT COUNTRY SINGLE TIME
NOCON    CLI   BLANKS,C' '   ANY DATA TO BE PRINTED ?
         BE    PRINC         NO
         LA    R2,2          DO TWICE
PRILP    LA    R1,VWORK      GET RECORD
         CALL  XPRNT         PUT IT OUT
         LA    R1,VWORK2
         CALL  XPRNT
         BCT   R2,PRILP      REPEAT ONCE
PRINC    LA    R7,138(R7)    NEXT LINE
         C     R7,=A(LASTLINE)
         BL    PRLOOP        DO NEXT LINE
         SPACE
         L     R13,4(R13)
         LM    R14,R12,12(R13)
         SR    R15,R15
         BR    R14
         EJECT
SAVE     DS    18F
VWORK    DC    0H'0',AL2(137,0),0CL133' '
BLANKS   DC    CL133' '
VWORK2   DC    0H'0',AL2(137,0)
BL2      DC    CL133' '
VWORKC   DC    0H'0',AL2(137,0)
BLCO     DC    CL133' '
MVC      MVC   0(0,R1),0(R14)   MOVE AN ENTRY
H2       DC    H'2'
SPACES   DC    CL5' '
LOC      DC    0H'0',X'0'
LOC1     DC    X'0'
LCT      DC    X'0'
WHO      DC    X'0'
ENTRY    DC    0CL5' '
NATION   DC    CL3' '
         DC    C' '
FORCE    DC    C' '
FTAB     DC    C' AF'        UNIT TRANSLATE TABLE
         SPACE
         LTORG
         SPACE 3
         PRINT GEN
         INIT
         SPACE
         VOCE  24,0,7
         VCOA  24,7,20
         HCOA  20,17,24
         VCOA  17,20,23
         HCOA  23,17,36
         VCOA  36,7,23
         HCOA  7,24,36
         HOCE  13,13,24
         VOCE  13,13,28
         HOCE  20,1,13
         HOCE  23,13,17
         HOCE  28,13,17
         VCOA  17,28,37
         HCOA  37,3,17
         VCOA  3,37,49
         HCOA  49,3,17
         VCOA  17,42,49
         VOCE  9,49,52
         VLAN  9,37,45
         HLAN  45,3,9
         HCOA  52,1,49
         VLAN  17,37,40
         VLAN  30,7,23
         HLAN  10,24,30
         HLAN  13,30,36
         HLAN  17,24,30
         HLAN  19,30,36
         VOCE  36,23,25
         VCOA  36,25,28
         HCOA  28,17,36
         VLAN  24,28,34
         HLAN  31,24,39
         HLAN  34,17,39
         VLAN  33,34,36
         VLAN  39,28,34
         HLAN  36,26,52
         VLAN  26,36,40
         HLAN  40,17,30
         VCOA  30,40,42
         HCOA  42,17,30
         HCOA  40,30,48
         VCOA  48,40,48
         HOCE  42,45,48
         VOCE  45,42,52
         HOCE  45,17,45
         VLAN  45,25,34
         VLAN  44,34,36
         HLAN  34,44,63
         VLAN  45,36,40
         HLAN  25,41,90
         HLAN  28,36,45
         VLAN  39,52,55
         VCOA  49,52,55
         VCOA  52,2,10
         VOCE  52,10,13
         VCOA  52,13,19
         HCOA  19,41,52
         VCOA  41,19,25
         HCOA  25,36,41
         HOCE  7,36,52
         HOCE  15,45,52
         VOCE  45,15,19
         VLAN  48,19,25
         VLAN  52,25,31
         HLAN  31,45,52
         VLAN  52,34,40
         HLAN  40,48,58
         HLAN  43,48,58
         HCOA  48,48,58
         HOCE  52,49,58
         HCOA  2,52,121
         VOCE  90,0,2
         VLAN  82,2,4
         HLAN  4,67,98
         VLAN  79,4,7
         VLAN  98,4,7
         HCOA  7,79,98
         VLAN  67,4,10
         HCOA  10,52,67
         VCOA  67,10,13
         HCOA  13,52,67
         VCOA  79,7,15
         HOCE  13,79,90
         VCOA  98,7,10
         HCOA  10,90,98
         VCOA  90,10,13
         HCOA  15,67,79
         VCOA  67,13,17
         HLAN  17,52,59
         HCOA  17,59,67
         VCOA  59,17,19
         VLAN  59,19,25
         HCOA  19,59,90
         VLAN  73,19,25
         VCOA  90,13,19
         VLAN  90,19,25
         VLAN  98,10,22
         HLAN  13,98,118
         HLAN  22,90,98
         VLAN  94,22,29
         VLAN  63,25,34
         HLAN  25,94,121
         HLAN  29,63,98
         VLAN  79,25,29
         VLAN  110,25,34
         HLAN  34,87,110
         VLAN  98,29,34
         VLAN  75,29,32
         HLAN  32,63,87
         VLAN  87,32,48
         VLAN  102,34,37
         VCOA  102,37,45
         HCOA  37,102,117
         HCOA  45,102,117
         VCOA  117,37,45
         HLAN  41,117,121
         VLAN  113,45,53
         HLAN  52,113,121
         HLAN  53,108,113
         VCOA  108,53,55
         HCOA  53,98,108
         VCOA  98,48,53
         VLAN  106,45,50
         HLAN  50,98,113
         HLAN  41,87,102
         HLAN  45,98,102
         VLAN  98,45,48
         HLAN  48,84,90
         HCOA  48,90,98
         VCOA  90,48,52
         HOCE  53,90,98
         VOCE  94,53,55
         HCOA  53,78,90
         VCOA  78,45,53
         HLAN  49,78,84
         HLAN  44,78,84
         VLAN  84,44,49
         HCOA  52,58,67
         VCOA  67,49,52
         HCOA  49,68,70
         HOCE  48,70,78
         VLAN  58,40,48
         VCOA  58,48,52
         VLAN  66,32,36
         HLAN  36,63,75
         HLAN  37,52,63
         VLAN  63,36,40
         VCOA  63,40,44
         HLAN  44,58,63
         HCOA  44,63,67
         HLAN  46,58,63
         VLAN  63,46,49
         HLAN  49,63,67
         VCOA  70,48,49
         HCOA  40,63,70
         HLAN  39,78,87
         VLAN  78,39,45
         HCOA  45,75,78
         HCOA  67,44,70,47
         HCOA  70,40,75,45
         HLAN  75,36,78,39
         HLAN  75,36,79,32
         LINE  '#',53,18,58,18   KIELER CANAL
         LINE  '#',64,14,64,16     DANISH SOUND
         LINE  '#',99,49,102,46    DARDANELLES
         EJECT
XXXX     NAME  45,35         SWITZERLAND
XXXX     NAME  48,35         SWITZERLAND
         SPACE 2
ADRI     NAME  67,42,68,43
AEGE     NAME  93,50,92,51
ARCT     NAME  35,3,35,5
BALT     NAME  73,17,82,17
BARE     NAME  97,1,105,1
BLAC     NAME  107,40,106,42
BOTH     NAME  83,9,82,11
CHAN     NAME  17,25,25,25
EMED     NAME  100,55,99,54
HELG     NAME  48,16,47,17
IONI     NAME  59,54,70,53
IRIS     NAME  17,15,17,17
LYON     NAME  31,43,36,44
MIDA     NAME  5,27,5,30
NATL     NAME  6,6,6,9
NSEA     NAME  41,10,40,13
SKAG     NAME  55,11,60,12
TYRR     NAME  46,49,52,50
WMED     NAME  26,47,32,49
BULG     NAME  90,44,97,43,95,44,92,45
SPAI     NAME  11,44,11,41,13,46,10,47
STPE     NAME  102,7,101,4,100,7,100,9
AFRI     NAME  6,54,17,54
ALBA     NAME  80,46,79,48
ANKA     NAME  108,46,107,48,110,47
APUL     NAME  64,45,65,47
ARME     NAME  116,47,115,50
BELG     NAME  38,26,40,27,43,26
BERL     NAME  63,21,64,23,69,21
BRES     NAME  18,30,18,32,20,31
CLYD     NAME  25,8,25,9
CONS     NAME  102,47,101,48,100,47
DENM     NAME  54,15,61,15,59,15
EDIN     NAME  32,9,31,11,33,10
FINL     NAME  84,5,90,6
GASC     NAME  19,35,20,37
GREE     NAME  82,50,83,51,87,50
HOLL     NAME  43,21,42,23,44,22
KIEL     NAME  50,20,52,22,56,20
LIVE     NAME  25,12,25,15,27,13
LIVO     NAME  92,14,92,18
LOND     NAME  32,20,31,22,33,21
MARS     NAME  30,37,38,39,35,38
NAPL     NAME  59,49,60,51,61,50
NORW     NAME  58,4,58,8,60,6
PICA     NAME  27,29,32,30
PIED     NAME  47,37,47,38
PORT     NAME  5,39,4,43,6,41
PRUS     NAME  77,21,82,23
ROME     NAME  52,45,51,46,50,45
RUMA     NAME  91,35,94,38,94,36
SEVA     NAME  110,36,113,33,107,36
SMYR     NAME  105,51,103,52,102,51
SWED     NAME  71,7,71,13,72,10
SYRI     NAME  115,53,114,54
TRIE     NAME  66,38,72,40,73,38
TUNI     NAME  41,53,42,54,47,53
TUSC     NAME  52,41,51,42
VENI     NAME  54,38,58,39,60,38
WALE     NAME  26,19,25,21
YORK     NAME  32,15,31,17
BOHE     NAME  65,30,67,31
BUDA     NAME  82,35,81,37,79,36
BURG     NAME  41,30,40,32
GALI     NAME  89,32,90,33
MOSC     NAME  105,18,105,22,103,20
MUNI     NAME  56,27,55,31,57,29
PARI     NAME  27,32,28,33,33,32
RUHR     NAME  47,27,46,29
SERB     NAME  82,41,81,42,80,41
SILE     NAME  65,26,69,27
TYRO     NAME  58,35,56,36
UKRA     NAME  102,28,102,31
VIEN     NAME  69,33,70,34,75,33
WARS     NAME  84,26,85,27,89,26
         EJECT
         EXPAND
         SPACE 2
*      MAP INSERT
*
         ORG   LINEONE+5*138+123
INS      DC    CL14'**************'
         ORG   INS+138
         DC    CL14'*            *'
         ORG   INS+2*138
         DC    CL14'* +... OCEAN *'
         ORG   INS+3*138
         DC    CL14'* .          *'
         ORG   INS+4*138
         DC    CL14'*            *'
         ORG   INS+5*138
         DC    CL14'* +---- LAND *'
         ORG   INS+6*138
         DC    CL14'* ×          *'
         ORG   INS+7*138
         DC    CL14'*            *'
         ORG   INS+8*138
         DC    CL14'* +=== COAST *'
         ORG   INS+9*138
         DC    CL14'* "          *'
         ORG   INS+10*138
         DC    CL14'*            *'
         ORG   INS+11*138
         DC    CL14'* ## BRIDGED *'
         ORG   INS+12*138
         DC    CL14'* # WATERWAY *'
         ORG   INS+13*138
         DC    CL14'*            *'
         ORG   INS+14*138
         DC    CL14'*  * SUPPLY  *'
         ORG   INS+15*138
         DC    CL14'*   CENTER   *'
         ORG   INS+16*138
         DC    CL14'*            *'
         ORG   INS+17*138
         DC    CL14'**************'
         EJECT
PROVINCE DSECT
PRONAME  DS    CL4           ABBREVIATED PROVINCE NAME
PRO#     DS    X             PROVINCE NUMBER
PROCC    DS    X             COUNTRY OCCUPYING
PROSC    DS    X             COUNTRY OWNING SUPPLY CENTER
PROF     DS    X             STATUS FLAG
PROFSUP  EQU   128           SUPPLY CENTER
PROFSEA  EQU   64            OCEAN
PROFTWO  EQU   32            2-COAST
PROFLAN  EQU   16            INLAND
PROFSOU  EQU   4             FLEET ON SOUTH COAST
PROFNAV  EQU   2             NAVY OCCUPYING
PROFARM  EQU   1             ARMY OCCUPYING
PROFHOM  DS    X             HOME COUNTRY
         SPACE
PROLAND  DS    XL12          VALID LAND MOVES FROM THIS PROVINCE
PROSEA   DS    0XL12         VALID NAVY MOVES FROM THIS PROVINCE
         DS    XL6           VALID MOVES FROM NORTH/WEST COAST
PROSOU   DS    XL6           VALID MOVES FROM EAST/SOUTH COAST
PROLEN   EQU   *-PROVINCE
         SPACE 2
STASECT  DSECT
STABIT   DS    X             COUNTRY ROUTING CODE
STASNAT  DS    CL7           NAME OF COUNTRY
STAFOR   DS    PL2           NO. OF FORCES
STACON   DS    PL2           NO. OF CONTROLLED SUPPLY CENTERS
STABUIL  DS    PL2           NO. AVAILABLE TO BUILD ON
STASLEN  EQU   *-STASECT
         SPACE 2
         END
