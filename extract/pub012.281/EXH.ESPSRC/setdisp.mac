DISP     TITLE 'SET CLOSE DISPOSITION IN PL/I DECLARE CONTROL BLOCK'
*              THIS PROGRAM WAS WRITTEN BY:
         SPACE 1
*                                  SEYMOUR JEROME METZ
*                                  PINKERTON COMPUTER CONSULTANTS, INC.
*                                  5881 LEESBURG PIKE
*                                  BAILEYS CROSSROADS, VIRGINIA   22041
*                                  703-820-5571
         SPACE 1
*              THIS PROGRAM IS FURNISHED ON AN AS-IS BASIS.
*              IT IS NOT PROPRIETARY, AND MAY BE DISTRIBUTED TO ANY
*                   OTHER INSTALLATION.
*              FEEL FREE TO CONTACT ME IF ANY PROBLEMS OCCUR.
         SPACE 3
*              THE FUNCTION OF THIS ROUTINE IS
*              TO MODIFY THE DISPOSITION FIELD IN A PL/I DCLB.
         SPACE 1
*              THE ONLY PARAMETER IS THE FILE WHOSE DISPOSITION IS
*              TO BE SET.
         SPACE 1
         SPACE 1
*              THE ENTRY POINTS SETRERD, SETLEAV, AND SETDISP SET
*              REREAD, LEAVE, AND DISP AS THE CLOSE DISPOSITION.
         EJECT
DISP     START 0
         USING IHEZDCL,RA
         ENTRY SETRERD
         USING *,BR
SETRERD  STM   LR,PR,OFLR(DR)
         L     RA,0(RA)                 ADDRESS DCLB
         NI    DCLD,255-X'82'           CLEAR LEAVE AND REWIND BITS
         LM    LR,WR,OFLR(DR)
         BR    LR
         SPACE 5
         ENTRY SETLEAV
         USING *,BR
SETLEAV  STM   LR,PR,OFLR(DR)
         L     RA,0(RA)                 ADDRESS DCLB
         NI    DCLD,255-X'82'           CLEAR LEAVE AND REWIND BITS
         OI    DCLD,X'80'               SET LEAVE OPTION
         LM    LR,WR,OFLR(DR)
         BR    LR
         SPACE 5
         ENTRY SETDISP
         USING *,BR
SETDISP  STM   LR,PR,OFLR(DR)
         L     RA,0(RA)                 ADDRESS DCLB
         NI    DCLD,255-X'82'           CLEAR LEAVE AND REWIND BITS
         OI    DCLD,X'02'               SET REWIND OPTION (REWIND)
         LM    LR,WR,OFLR(DR)
         BR    LR
         TITLE 'PL1 REGISTER EQUATES, DSECTS, ETC.'
         IHELIB
         TITLE 'PL1 I/O BLOCK DEFINITIONS'
         IHEZAP
         END
