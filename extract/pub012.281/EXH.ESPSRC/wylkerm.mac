WYLKERM TITLE 'W Y L K E R M  ***  MCP INTERFACE / KERMIT CONTROL TASK'
         MACRO ,                                                 91273
&NM      XHEAD &SAVE=,&EXH=NO,&RPL=NO,&LTORG=NO,&RENT=4*18,&MODE=
         GBLC  &X@SAVE,&X@EXIT,&X@RENT                           91273
         LCLC  &SA                                               91273
         AIF   ('&X@SAVE' EQ '').OLDSAVE                         91273
&X@SAVE  DC    18A(0)        PRIOR XHEAD SAVE AREA               91273
&X@SAVE  SETC  ''                                                91273
.OLDSAVE AIF   ('&LTORG' EQ 'NO').NOLIT                          91273
         LTORG ,                                                 91273
.NOLIT   DROP  ,                                                 91273
&X@EXIT  SETC  '&NM'         EXIT NAME                           91273
&X@RENT  SETC  ''                                                91273
         USING &NM,R12                                           91273
         AIF   ('&MODE' EQ 'OS').DOALL                           91273
&NM      LR    R12,R15       LOAD BASE                           91273
         AGO   .CMALL                                            91273
.DOALL   ANOP  ,                                                 91273
&NM      STM   R14,R12,12(R13)  SAVE IN USER'S SAVE AREA         91273
         LR    R12,R15       SET LOCAL BASE                      91273
.CMALL   L     R11,=A(WORKBASE)  AND COMMON WORK AREA            91273
         USING WORKBASE,R11                                      91273
         LR    R6,R1                                             91273
         AIF   ('&RPL' EQ 'NO').NORPL                            91273
         USING IFGRPL,R6                                         91273
         L     R7,RPLUSFLD   GET NIB (STASHED EARLIER)           91273
         USING MTSWORK,R7    MTSWORK BEGINS WITH NIB             91273
&X@SAVE  SETC  ''                                                91273
.NORPL   AIF   ('&SAVE' EQ 'NONE').NOSAVE                        91273
&SA      SETC  '&SAVE'                                           91273
         AIF   ('&SAVE' NE 'RENT').NORENT                        91273
&SA      SETC  ''                                                91273
&X@RENT  SETC  '&RENT'                                           91273
         LR    R5,R0         SAVE R0 IN LERAD/SYNAD              89131
         GETMAIN R,LV=&RENT                                      91273
         STM   R13,R12,0(R1)                                     91273
         LR    R0,R5         RESTORE R0                          89131
         LR    R13,R1                                            91273
.NORENT  AIF   ('&SA' NE '').DFLT                                91273
&SA      SETC  '&NM'.'ZZZZZZZZ'                                  91273
&SA      SETC  '&SA'(1,4).'SAVE'                                 91273
.DFLT    AIF   ('&SAVE' EQ 'RENT').NDFLT                         91273
         STM   R13,R12,&SA   SAVE RETURN INFO IN OUR SAVE AREA   91273
         LA    R13,&SA                                           91273
.NDFLT   ANOP  ,                                                 91273
&X@SAVE  SETC  '&SA'                                             91273
.NOSAVE  AIF   ('&EXH' NE 'YES' AND '&EXH' NE '').MEND           89328
         AIF   ('&RPL' EQ 'NO').MEND                             91273
.MEND    MEND  ,                                                 91273
         SPACE 1                                                 91273
         MACRO ,                                                 91273
&NM      XEXIT &LTORG=,&MODE=                                    91273
         GBLC  &X@SAVE,&X@EXIT,&X@RENT                           91273
         LCLC  &XN,&CS                                           91273
         AIF   ('&X@SAVE' EQ '').MEND                            91273
&XN      SETC  '&X@EXIT'.'ZZZZZZZZ'                              91273
&XN      SETC  '&XN'(1,4).'EXIT'                                 91273
         AIF   ('&NM' EQ '' OR '&NM' EQ '&XN').NODC              91273
&NM      DS    0H                                                91273
.NODC    AIF   ('&X@RENT' NE '').FRENT                           91273
&XN      LM    R13,R14,&X@SAVE   RESTORE RETURN REGISTERS        91273
         AIF   ('&MODE' NE 'OS').NOS                             91273
         LM    R15,R12,16(R13)  RESTORE ALL REGISTERS            91273
.NOS     BR    R14           RETURN                              91273
&X@SAVE  DC    18A(0)                                            91273
&X@SAVE  SETC  ''                                                91273
         AGO   .LORG                                             91273
.FRENT   ANOP  ,                                                 91273
&XN      LR    R1,R13                                            91273
         LM    R13,R14,0(R1)                                     91273
         FREEMAIN R,LV=&X@RENT,A=(R1)                            91273
         AIF   ('&MODE' NE 'OS').NOS2                            91273
         LM    R15,R12,16(R13)  RESTORE ALL REGISTERS            91273
.NOS2    BR    R14                                               91273
&CS      SETC  '&SYSECT'                                         91273
&X@SAVE  DSECT ,                                                 91273
         DC    18A(0)                                            91273
&X@SAVE  SETC  ''                                                91273
&CS      CSECT ,                                                 91273
.LORG    AIF   ('&LTORG' EQ 'NO').MEND                           91273
         LTORG ,                                                 91273
.MEND    MEND  ,                                                 91273
         SPACE 1                                                 88348
         MACRO ,                                                 88348
&NM      UNPOST &ECB                                             88348
&NM      NI    &ECB,255-X'80'-X'40'                              88348
         MEND  ,                                                 88348
         SPACE 1                                                 91273
         MACRO ,                                                 91273
&NM      BUGWTOR &M=                                             91273
         LCLC  &T                                                91273
         LCLA  &I                                                91273
&T       SETC  'ZZG'                                             91273
&I       SETA  &SYSNDX                                           91273
         CNOP  0,4           MAKE WORD BOUNDARY                  91273
&NM      STM   R0,R15,&T&I.S  SAVE ALL                           91273
         B     &T&I.C                                            91273
&T&I.S   DC    16A(0)        REGISTER SAVE AREA                  91273
&T&I.E   DC    A(0)          ECB                                 91273
&T&I.R   DC    CL4' '        REPLY                               91273
&T&I.C   UNPOST &T&I.E                                           91273
         WTOR  'KER666A DEBUG POINT &M',&T&I.R,4,&T&I.E          91273
         WAIT  ECB=&T&I.E                                        91273
         MEND  ,                                                 91273
         SPACE 1                                                 91273
         MACRO ,                                                 91273
&NM      BUGWTO &M=                                              91273
         LCLC  &T                                                91273
         LCLA  &I                                                91273
&T       SETC  'ZZG'                                             91273
&I       SETA  &SYSNDX                                           91273
         CNOP  0,4           MAKE WORD BOUNDARY                  91273
&NM      STM   R0,R15,&T&I.S  SAVE ALL                           91273
         B     &T&I.C                                            91273
&T&I.S   DC    16A(0)        REGISTER SAVE AREA                  91273
&T&I.C   WTO   'KER666I DEBUG POINT &M'                          91273
         MEND  ,                                                 91273
         SPACE 1                                                 91273
         COPY  OPTIONGB                                          89328
         SPACE 1                                                 91273
         GBLA  &WYLREL       MAJOR WYLBUR RELEASE NUMBER         91273
         GBLA  &SMFTYPE      KERMIT SMF RECORD # OR ZERO         91273
         GBLA  &MINUSER,&MAXUSER  MIN/MAX WORK AREAS IN USE      91273
         GBLB  &ZZ@BLUK      'BLOOK' EXPANSION VARIABLE          89345
         SPACE 1                                                 89328
         SYSPARM LIST=YES                                        89328
&WYLREL  SETA  9             SUPPORT WYLBUR REL. 9               93132
         AIF   ('&LOCAL' NE 'CCSI' OR &JES2REL GT 99).NEWREL     91273
&WYLREL  SETA  5             SUPPORT WYLBUR REL. 5               91273
.NEWREL  SPACE 1                                                 91273
*********** SMF RECORD TYPE FOR SESSION BILLING *************    91273
&SMFTYPE SETA  0   OR 34     FAKE TSO / ZERO FOR NO RECORD       91273
         SPACE 1                                                 91273
&MINUSER SETA  0             NUMBER OF IDLE WORK AREAS NOT FREEMAINED
&MAXUSER SETA  5             MAX. CONCURRENT FILE TRANSFERS (@ 50-80K?)
         SPACE 3                                                 89328
***********************************************************************
*                                                                     *
*        COPYRIGHT 1986, 1989-1991  EXPERT SYSTEM PROGRAMMING, INC.   *
*        COPYRIGHT 2003  EXPERT SYSTEM PROGRAMMING                    *
*                        176 OLD STAGE COACH ROAD                     *
*                        BRADFORD, VT 05033-8844                      *
*                                                                     *
*                    ALL RIGHTS RESERVED                              *
*                                                                     *
***********************************************************************
*                                                                     *
*     CODE IN THIS MODULE FLAGGED WITH 'OBS' IN COLUMNS 66-68         *
*        HAS BEEN COPIED FROM MODULE UEXMGR72 IN THE                  *
*        ASC (FORMER OBS) DISTRIBUTED SOURCE LIBRARY.                 *
*                                                                     *
*                                                                     *
*    THE PROTECTED CODE INCLUDES TRADE SECRETS, AND IS PROPRIETARY    *
*        TO ON-LINE BUSINESS SYSTEMS, INC.                            *
*           SAN FRANCISCO, CALIFORNIA                                 *
*                                                                     *
***********************************************************************
*                                                                91273
*    THIS MODULE CONTAINS MULTI-TASKING SUPPORT FOR THE KERMIT FILE
*    TRANSFER PROGRAM.  THE CONTROL CODE WAS DERIVED FROM PROGRAM
*    EXHABASE, COPYRIGHT 1986 BY EXPERT SYSTEM PROGRAMMING, INC. 91273
*    THE KERMIT CODE IS A HIGHLY MODIFIED VERSION OF VERSION 1 FROM
*    COLUMBIA UNIVERSITY, AND RESIDES IN EXTERNAL MODULE KERMIT, 91273
*    ALIAS KERMNET (KERMIT IS THE TSO ENTRY, KERMNET THE MULTI-TASKING)
         EJECT ,                                                 91273
         PRINT &PRTSOR                                           89328
WYLKERM  SAVEM ZERO12,BASE=R12,PARM=R9                           91273
         L     R11,=A(WORKBASE) GET COMMON WORK AREA             91273
         USING WORKBASE,R11  MAKE SECOND BASE                    91273
         L     R1,=A(SUBINIT)  LOAD INITIALIZATION CODE          90231
         IDENTIFY EPLOC=FROGFACE,ENTRY=(R1)  IDENTIFY THE SUBTASK CODE
         BXLE  R15,R15,CONTOPEN  PROCEED                         89328
IDENFAIL WTO   'KER412E SUBTASK IDENTIFY FAILED',ROUTCDE=1,DESC=1
         ABEND 412,DUMP                                          91273
         SPACE 1                                                 91273
CONTOPEN LA    R9,0(,R9)     CLEAR HIGH BYTE                     91273
PARMDONE LA    R2,=CL8'KERMNET'  POINT TO NORMAL KERMIT          91078
         TM    FLAGS,FGTEST  TEST MODE ?                         91078
         BZ    *+8           NO                                  91078
         LA    R2,=CL8'KERMNEW'  LOAD TEST VERSION               91078
         LOAD  EPLOC=(R2)    GET KERMIT/WYLKERM INTERFACE ENTRY  91078
         ST    R0,@KERMIT    SAVE FOR SUBTASKS                   90206
         AIF   (&SVC@SVC GT 0).NORESVC                           90186
         SERVINIT ,          LOAD THE SERVICE ROUTINE            90186
.NORESVC SPACE 1                                                 91273
         ICM   R6,15,0(R9)   LOAD AND CHECK THE TIB ADDRESS      91273
         BZ    FAILISSU      QUIT                                91273
         USING TIBSTART,R6                                       91273
         LA    R9,MICWORK    GET MIC PARM AREA                   91273
         USING MICPSTRT,R9                                       OBS
         MVC   MICPMCPN,TIBMCPN    FORMAT MIC OPEN PARAMETERS    OBS
         MVC   MICPSSN,TIBNAME                                   OBS
         MVC   MICPEPAD,TIBMEPAD   SET MIC ENTRY POINT ADDRESS   OBS
         SPACE 1                                                 91273
*        ESTABLISH MIC                                           OBS
*                                                                OBS
         MIC   LOAD,MICWORK  MIC ENTRY (NOP - SVC NOT SUPPORTED) 91273
         MIC   OPEN,MICWORK  SET US UP IN BUSINESS               91273
         BXLE  R15,R15,HAVEMIC  BRANCH ON SUCCESSFUL OPEN        91273
FAILISSU WTO   'KER400E WYLKERM MIC INTERFACE FAILURE'           91273
         ABEND 400,DUMP                                          OBS
         SPACE 1                                                 91273
HAVEMIC  WTO   'KER468I WYLKERM READY FOR LOGONS'                91273
         LA    R14,TIBECB                                        91273
         ST    R14,WAITLIST  COMPLETE ECB ADDRESS LIST           91273
         DROP  R9                                                OBS
         SPACE 2                                                 OBS
***********************************************************************
*    LOOK FOR WORK FROM MILTEN CONTROL PROGRAM (MCP)                  *
***********************************************************************
         USING MPRBSTRT,R8                                       91273
GETSOME  ICM   R8,15,QUEUEHED  IS THERE ANY WORK TO BE DONE ?    91273
         BNZ   HANDSOME      YES; PROCEED                        91273
         LA    R1,MICWORK    LOOK AT TIB ECB TO SEE IF WORK THERE
         L     R6,MICPSTIB-MICPSTRT(,R1)                         91273
         TM    TIBECB,X'40'        HAS TIB ECB BEEN POSTED ?     OBS
         BO    READMIC                                           OBS
         WAIT  ECBLIST=WAITLIST                                  91273
         TM    TIBECB,X'40'  MIC AVAILABLE ?                     91273
         BNZ   READMIC       YES; PROCESS IT                     91273
         UNPOST MAINECB      ELSE CHECK FOR WORK TO MCP          91273
***********************************************************************
*    NO MCP REQUEST; SCAN FOR WORK FROM OUR SUBTASKS.                 *
***********************************************************************
         ICM   R7,15,NEXTWORK  IS THERE A NEXT ONE TO BE DONE ?  91273
         BNZ   LOOPWORK      YES; TRY IT                         91273
LOOPHEAD ICM   R7,15,WORKQUE   ELSE START AT HEAD OF QUEUE       91273
         BZ    GETSOME       NOTHING - JUST WAIT FOR MCP         91273
         USING MTSWORK,R7    DECLARE IT                          91273
LOOPWORK TM    MTSWYLRQ,1    ACTION REQUEST ?                    91273
         BZ    BUMPWORK      NO                                  91273
         MVC   NEXTWORK,MTSQLINK  SCHEDULE NEXT ONE              91273
         B     SENDWORK                                          91273
BUMPWORK ICM   R7,15,MTSQLINK  GET NEXT                          91273
         C     R7,NEXTWORK   GONE THROUGH ENTIRE QUEUE ?         91273
         BE    GETSOME       YES; WAIT FOR WORK                  91273
         LTR   R7,R7                                             91273
         BNZ   LOOPWORK                                          91273
         B     LOOPHEAD                                          91273
         SPACE 1                                                 91273
***********************************************************************
*    SEND A SUBTASK REQUEST TO MCP                                    *
***********************************************************************
SENDWORK L     R8,MTS@RTCB   RELOAD RTCB ADDRESS                 91273
         LA    R1,MICWORK    RELOAD MIC PARM AREA                91273
         USING MICPMAP,R1                                        91273
         ST    R7,MPRBSAV1   SAVE MY WORK AREA ADDRESS           91273
         NI    MTSWYLRQ,255-1  RESET ACTION PENDING              91273
         MVC   MPRBRTEC,MTSWYLRQ  SET READ/WRITE                 91273
         SLR   R14,R14       REQUEST DEFAULT SUBSYSTEM           91273
         ICM   R14,8,=X'04'   MAKE LOGON REQUEST FOR UPLEVEL     91273
         CLI   MPRBRTEC,MRRCXCTL  GOING ELSEWHERE ?              91273
         BE    SENDCLR       YES; CLEAR TEXT                     91273
         CLI   MPRBRTEC,MRRCSOFF  GOING AWAY ?                   91273
         BNE   SENDRITE      NO                                  91273
         L     R14,MICPSTIB  GET MY TIB                          91273
         LA    R14,0(,R14)   CLEAR HIGH BYTE                     91273
SENDCLR  L     R15,RTCBRTBI-RTCBSTRT(,R8)                        91273
         USING RTBSTART,R15                                      91273
         XC    RTBCCLR(RTBCCLRL),RTBCCLR  CLEAR                  91287
         MVC   RTBMFL(L'RTBMFL+L'RTBMF2),MTSRTBFL  RESTORE WYLBUR'S
         AIF   ('&LOCAL' NE 'CCSI' OR &WYLREL GT 6).NORF1        91300
         NI    RTBRF,255-RTBRFNID  RESET SUPPRESSION             91303
         TM    MTSFLGT,MTSFXON  RESTORE X-ON ?                   91303
         BZ    *+12          NO                                  91303
         L     R15,RTCBCPA-RTCBSTRT(,R8)  GET CPA                91303
         NI    CPAFLAG-CPASTRT(R15),255-CPADXON  ENABLE X-ON     91303
.NORF1   AIF   (&WYLREL LT 7).NORFCM                             91356
         OI    RTCBDF2-RTCBSTRT(R8),RTCBVTNL  RESTORE NL         91356
.NORFCM  DROP  R15                                               91273
         ST    R14,MPRBPRM1  LOGOFF FROM THIS TIB                91273
         AIF   (&WYLREL LT 7).KEEPSS                             92055
         L     R14,MICPSTIB  GET MY TIB                          92055
         SLR   R15,R15                                           92055
         IC    R15,TIBINDEX-TIBSTART(R14)  GET TIB INDEX         92055
         SLL   R15,2         CONVERT TO OFFSET                   92055
         LA    R15,RTCBRCB-RTCBSTRT(R15,R8)  GET SUBSYS BYTE     92055
         NI    0(R15),X'7F'  RELINQUISH OWNERSHIP                92055
.KEEPSS  SPACE 1                                                 92055
SENDRITE MIC   WRITE,(1),MPRB=(R8),RETURN=(R7)                   91273
         BXH   R15,R15,BADMIC                                    91273
         CLI   MTSWYLRQ,MRRCSOFF  LOGOFF REQUEST ?               91273
         BE    SOFFSENT                                          91273
         CLI   MTSWYLRQ,MRRCXCTL  UPLEVEL ?                      91273
         BNE   GETSOME       NO                                  91273
         SPACE 2                                                 91273
***********************************************************************
*    USER GOING AWAY                                                  *
***********************************************************************
SOFFSENT XC    NEXTWORK,NEXTWORK  INVALIDATE WORK CHAIN          91273
         XC    NIBSYM-NIBST+MTSNIB,NIBSYM-NIBST+MTSNIB FREE MTS  91273
SOFFNOCS LA    R1,WORKQUE-(MTSQLINK-MTSWORK) QUEUE               91273
         DROP  R7                                                91275
         USING MTSWORK,R1                                        91273
SOFFSLOC C     R7,MTSQLINK   IS THIS IT ?                        91273
         BE    SOFFSDEQ      YES; REMOVE FROM QUEUE              91273
         ICM   R1,15,MTSQLINK  POINT TO NEXT ONE                 91273
         BNZ   SOFFSLOC                                          91273
         B     LOOPHEAD      LOOK FOR MORE WORK                  91273
SOFFSDEQ L     R15,MTSQLINK-MTSWORK(,R7)  GET POINTER TO NEXT    91273
         CS    R7,R15,MTSQLINK  UNCHAIN THIS ONE                 91273
         BNZ   SOFFNOCS      DIDN'T TAKE - RUN THE CHAIN AGAIN   91273
         DROP  R1                                                91273
         USING MTSWORK,R7    POINT BACK TO MAIN                  91275
         L     R0,MTV#WA     GET NUMBER OF WORK AREAS IN USE     91273
         CH    R0,MINUSERS   TOO MANY ?                          91273
         BNH   SOFFNZCS      NO; KEEP AND QUEUE ON FREE QUEUE    91273
         CSDEC MTV#WA        ADJUST WORK AREA                    91273
         MODESET MODE=SUP    ENABLE SPKA                         91273
         LA    R1,X'80'      GET KEY 8                           91273
         MODESET KEYREG=1    MAKE UNPRIVILEGED                   91273
         FREEMAIN R,LV=MTWWORKL,SP=0,A=(R7)  FREE                91273
*        MODESET KEY=ZERO,MODE=PROB  RESTORE                     91273
         LA    R1,X'34'      SET OPTIONS                         91280
         SVC   107           MODESET                             91280
         B     LOOPHEAD      LOOK FOR WORK                       91273
         USING MTSWORK,R7                                        91273
SOFFNZCS L     R15,FREEQUE   GET CURRENT FREE ELEMENT            91273
         ST    R15,MTSQLINK  MAKE TENTATIVE LINK                 91273
         CS    R15,R7,FREEQUE  ADD TO QUEUE                      91273
         BNZ   SOFFNZCS      ELSE TRY AGAIN                      91273
         B     LOOPHEAD      LOOK FOR MORE WORK                  91273
         SPACE 2                                                 91273
***********************************************************************
*    PROCESS MCP REQUEST                                              *
***********************************************************************
READMIC  UNPOST TIBECB       RESET POST CODE                     91273
         MIC   READ,MICWORK  GET REQUEST QUEUE FROM MIC          91273
         BXH   R15,R15,BADMIC                                    91273
         ST    R1,QUEUEHED   PLACE THREAD ON WORK HEAD           91273
         B     GETSOME       REMOVE FROM QUEUE AND PROCESS REQUEST
         SPACE 2                                                 91273
***********************************************************************
*    EXAMINE AND PROCESS THE MCP REQUEST                              *
***********************************************************************
         USING MPRBSTRT,R8                                       OBS
HANDSOME MVC   QUEUEHED,MPRBCHN    DEQUEUE THIS MPRB             91273
         IC    R2,MPRBRTEC   GET ROUTING CODE                    91279
         BIX   VAL=(R2),SRL=2, BRANCH VALUE IN R2, MULTIPLES OF 4      *
               BASE=WYLKERM,ERR=BADREQ,   ADDR. BASE; INVALID BR       *
               PFX=,LOC=(LOGON,        PREFIX; LOGON/LOGOFF PROC.      *
               ATTN,     4   ATTENTION/BREAK                           *
               QUIT,     8   NORMAL SHUTDOWN                           *
               DUMP,    12   ABEND/DUMP                                *
               WAKEUP,  16   V7+  HEARTBEAT (IGNORE)                   *
               LOGON,   20   V7+  UPLEVEL (LOGON)                      *
               DOSOME,  24   PROCESS RTCB (SEND/RCV TEXT)        91279 *
               SSATTN)  28   V7+  SUBSYS BREAK (??)              91279
         SPACE 1                                                 91273
WAITLIST DC    A(TIBECB-TIBECB),X'80',AL3(MAINECB)               91273
MAINECB  DC    F'0'          MAIN TASK WORK ECB                  91273
         SPACE 1                                                 91280
***********************************************************************
*    HEARTBEAT - DON'T NEED; DON'T WANT - KICK IT BACK                *
***********************************************************************
WAKEUP   MIC   WRITE,MICWORK,MPRB=(R8),ROUTE=MRRCRETN  GIVE BACK 91280
         B     GETSOME       WAIT FOR MORE                       91280
         EJECT ,                                                 91273
***********************************************************************
*                                                                     *
*    WHERE THE REAL WORK GETS DONE ?                                  *
*                                                                     *
***********************************************************************
         USING MTSWORK,R7                                        91273
         USING RTCBSTRT,R8   MPRB DOUBLES AS RTCB                91273
SSATTN   LR    R2,R8         SAVE MPRB ADDRESS                   91280
         L     R8,MPRBPRM1-MPRBSTRT(,R8)  GET REAL MPRB/RTCB     91280
         MIC   WRITE,MICWORK,MPRB=(R2),ROUTE=MRRCRETN  RETURN    91280
         MVI   MPRBRTEC-MPRBSTRT+RTCBSTRT,MRRCSSBR  SET BREAK    91280
ATTN     DS    0H            USE SAME CODE AS DOSOME             91273
DOSOME   L     R6,RTCBNCE    LOAD NCE                            91273
         USING NCESTART,R6   DECLARE NCE                         91273
         ICM   R7,15,WORKQUE GET WORK QUEUE                      91273
         BZ    GETSOME       NONE; IGNORE                        91273
DOSOCMPO CLC   NCENAME,NIBSYM-NIBST+MTSNIB  DUPLICATE ?          91273
         BE    DOSOMEWK      YES; HURRAH                         91273
         ICM   R7,15,MTSQLINK  TRY NEXT ENTRY                    91273
         BNZ   DOSOCMPO      LOOK AGAIN                          91273
         B     GETSOME       IGNORE ERROR ?                      91273
DOSOMEWK DS    0H            ACCEPT                              91273
         USING MTSWORK,R7    PASSED WORK AREA                    91273
         L     R6,RTCBRTBI                                       91273
         ST    R6,MTS@RTB                                        91273
         USING RTBSTART,R6                                       91273
         ST    R8,MTS@RTCB                                       91273
         CLI   MPRBRTEC-MPRBSTRT+RTCBSTRT,MRRCSSBR  ATTN/BREAK ? 91273
         BNE   DOSOATTN      NO                                  91273
         OI    MTSFLGM,MTSFATTN  SET ATTENTION ON                91273
         OI    RTBSFL,RTBSFATN  SET ATTENTION FLAG               91273
DOSOATTN DS    0H                                                91273
         SPACE 1                                                 91273
***********************************************************************
*                                                                     *
*    CHECK FOR UNUSUAL, BUT EXPECTED, CONDITIONS                      *
*                                                                     *
***********************************************************************
         TM    RTCBAFL,RTCBAFSF  AUTO LOGOFF ?                   91273
         BNZ   DOSOTATA      YES; GO BYE-BYE                     91273
         TM    RTBSFL,RTBSFHU  DISCONNECTED ?                    91273
         BNZ   DOSOTATA      YES                                 91273
         TM    RTBSFL,RTBSFEND  I/O ERROR ?                      91273
         BO    DOSOTATA      ?                                   91273
*    IGNORE IDLE OR BREAK ATTENTION                              91273
         CLI   MTSWYLRQ,MRRCTREA    READ REQUEST ?               91273
         BE    DONEREAD      YES                                 91273
         LA    R2,MTSWECB    LOAD WRITE COMPLETION ECB ADDRESS   91273
         XC    RTBCCLR(RTBCCLRL),RTBCCLR  CLEAR FOR TEXT         91287
DOSOPOST MVI   MTSWYLRQ,0    RESET REQUEST                       91273
         L     R0,=X'3F000000'  GET NORMAL COMPLETION CODE       91273
         POST  (R2),(0)      POST GOOD COMPLETION                91273
         B     GETSOME       GO FOR MORE                         91273
DONEREAD LH    R3,RTBRC     GET READ COUNT                       91273
         ST    R3,MTSINPLN   SAVE FOR READ ROUTINE               91273
         LA    R2,RTBTEXT    RTB INPUT/OUTPUT TEXT BUFFER        91273
         AH    R2,RTBPC      SHIFTED BY PROMPT                   91273
         AH    R2,RTBWC      SHIFTED BY WRITE                    91273
         L     R14,MTSINPAD  GET SUBTASK BUFFER                  91273
         LA    R15,L'MTWBUFIN  SET OUTPUT LENGTH                 91273
         MVCL  R14,R2        COPY TO WORK BUFFER                 91273
         XC    RTBCCLR(RTBCCLRL),RTBCCLR  CLEAR FOR TEXT         91287
         LA    R2,MTSATECB   GET NORMAL ATTENTION ECB            89328
         NI    FLAGS,255-FGREAD  NO LONGER HAVE A READ PENDING   87151
         B     DOSOPOST      POST COMMON ECB; RESET FUNCTION CODE
         SPACE 1                                                 91273
***********************************************************************
*    FORCED DISCONNECTION - ABEND SUBTASK (LOGOFF VIA ETX)            *
***********************************************************************
DOSOTATA OI    MTSFLGT,MTSFGONE  TERMINAL COMING DOWN            91273
         ICM   R2,15,MTSTASK SUB-TASK STILL THERE ?              91273
         BZ    GETSOME       HUH ?  MAJOR ERROR                  91273
         L     R0,=X'00622000'  SET COMPLETION CODE              91273
         OI    MTSFLGT,MTSFXEND  AND END REQUEST                 91273
         POST  MTSATECB,(0)  POST 'ATTENTION' ECB                91273
         POST  MTSCNECB,(0)  POST 'CANCEL' ECB                   91273
         B     GETSOME       AND GET OUT                         91273
         DROP  R6,R7                                             91273
         EJECT ,                                                 OBS
***********************************************************************
*    LOGON (AND AUTO-LOGOFF) ENTRIES                                  *
***********************************************************************
         USING RTCBSTRT,R8   MPRB DOUBLES AS RTCB                91273
LOGON    L     R5,RTCBRTBI   LOAD RTB ADDRESS                    91273
         USING RTBSTART,R5                                       91273
         ICM   R3,15,RTBPC   PRESERVE PROMPT AND READ COUNT      91287
         XC    RTBCCLR(RTBCCLRL),RTBCCLR  CLEAR                  91287
         TM    FLAGS,FGDOWN  GOING BYE-BYE ?                     91273
         BNZ   XLOGNOLO      YES; NO REASON GIVEN TO USER        91273
         XC    NEXTWORK,NEXTWORK  INVALIDATE WORK CHAIN          91273
         USING MTSWORK,R7                                        91273
         L     R6,RTCBNCE    LOAD NCE                            91273
         USING NCESTART,R6   DECLARE NCE                         91273
         ICM   R7,15,WORKQUE GET WORK QUEUE                      91273
         BZ    LOGOLOOK      NONE; LOOK FOR ONE OR GETMAIN       91273
LOGOCMPO CLC   NCENAME,NIBSYM-NIBST+MTSNIB  DUPLICATE ?          91273
         BE    XLOGDUPE      YES; SEE IF LOGOFF OR XCTL          91273
         ICM   R7,15,MTSQLINK  TRY NEXT ENTRY                    91273
         BNZ   LOGOCMPO      LOOK AGAIN                          91273
LOGOLOOK L     R5,RTCBRTBI   RELOAD RTB ADDRESS                  91273
         TM    RTCBAFL,RTCBAFSF  AUTO LOGOFF ?                   91273
         BNZ   XLOGNOLO      YES; GO BYE-BYE                     91273
         TM    RTBSFL,RTBSFHU  DISCONNECTED ?                    91273
         BNZ   XLOGNOLO      YES                                 91273
         TM    RTBSFL,RTBSFEND  I/O ERROR ?                      91273
         BO    XLOGNOLO      ?                                   91273
         CLC   RTCBCLAX,=Y(RTCBCXSC)  SYSTEM CONSOLE ?           91280
         BE    CONSTAT       YES; DISPLAY STATUS                 91280
         SPACE 1                                                 91273
         BAL   R9,WORKINIT   GET OR INITIALIZE A WORK AREA       91273
LOGOHAVE ST    R8,MTS@RTCB   SAVE RTCB/MPRB ADDRESS              91273
         L     R5,RTCBRTBI   RELOAD RTB AFTER WORKINIT           91273
         ST    R5,MTS@RTB    SAVE RTB ADDRESS                    91273
         MVC   NIBSYM-NIBST+MTSNIB,NCENAME  COPY TERMNAME TO NIB 91273
         MVC   MTSRTBFL,RTBMFL  PRESERVE WYLBUR'S FLAG SETTINGS  91273
         NI    RTBMFL,255-RTBMFUC  NOT ONLY UPPER CASE           91273
         OI    RTBMFL,RTBMFNTO  SET SLOWLIST (KERMIT COMPACTS)   91273
         NI    RTBMF2,255-RTBMFAPL  NOT APL/TEXT CHARACTER SET   91273
         OI    RTBMF2,RTBMFSL   SET SLOWLIST                     91273
         AIF   (&WYLREL LT 7).NOVTM                              91279
         OI    RTBMF2,RTBMFIDL  SUPPRESS IDLES                   91279
         MVI   RTBIDLFT,0    BY ZEROING MULTIPLIER               91279
         CLI   NCEFACIL,NCEFVNTO  NTO (3767) DEVICE ?            91280
         BNE   LOGONNTO      NO                                  91280
         OI    MTSTRMTY,MTSFVTAM  YES; SHOW TTY/VTAM INTERFACE   91280
         LA    R0,RTBNTEXT   GET TEXT LENGTH                     91357
         MIN   R0,=Y(240),TYPE=H  USE SHORTER LENGTH             93207
         ST    R0,MTSKMXPK   STASH FOR MAX PACKET                91357
LOGONNTO DS    0H                                                91357
.NOVTM   MVC   MTSUID,=CL8' '  CLEAR POTENTIAL USER ID           87214
         MVI   MTSACCT,C' '  PRE-CLEAR                           91273
         MVC   MTSACCT+1(L'MTSACCT-1),MTSACCT  CLEAR ACCOUNT     91273
         MVC   MTSUID(L'RTCBUID),RTCBUID  MOVE 7/8-BYTE USER ID  91273
         AIF   ('&LOCAL' NE 'CCSI').NOACCT                       91273
         MVC   MTSACCT,RTCBUAC    MOVE ACCOUNT NUMBER            91273
         AGO   .CMACCT                                           91273
.NOACCT  MVC  MTSACCT,RTCBACNO  ADD USER'S ACCOUNT NUMBER        91273
.CMACCT  LR    R4,R3         COPY SAVED COUNTERS                 91287
         LA    R15,X'FFF'    MAKE LENGTH MASK                    91287
         NR    R4,R15        MASK READ LENGTH                    91287
         BZ    XOPEPARM      NO TEXT; SKIP                       91273
         SLR   R15,R15       SET NO DECREMENT                    91315
         SRL   R3,16         ISOLATE PROMPT LENGTH               91287
         LA    R3,RTBTEXT(R3)  INDENT BY PROMPT LENGTH           91287
         CH    R4,=H'7'      AT LEAST SEVEN BYTES ?              91273
         BL    LOGOYCMD      NOT VALID                           91273
         OC    0(7,R3),=CL8' '  UPPER CASE                       91304
         CLC   =C'KERMIT ',0(R3)  NON-RECOMMENDED PREFIX?        91287
         BE    LOGOYPFX      YES                                 91273
         CLC   =C'WYLKERM',0(R3)  DESIGN PREFIX ?                91287
         BNE   LOGOYCMD      NO; PASS AS-IS                      91273
LOGOYPFX LA    R15,7         SET FOR SEVEN SCANNED               91273
LOGOYBMP AR    R3,R15        ADVANCE INPUT POINTER               91294
LOGOYCMD SR    R4,R15        MAKE RESIDUAL                       91273
         BNP   XOPEPARM      NONE                                91273
         CLI   RTBTEXT-RTBTEXT(R3),C' '  LEADING CRUD ?          91303
         BH    LOGOCMLN      NO?                                 91303
         LA    R15,1                                             91303
         B     LOGOYBMP      REMOVE IT                           91303
LOGOCMLN BCTR  R4,0          SET EXECUTE LENGTH                  91273
         EX    R4,XOPEUPRM   COPY USER'S PARM                    91273
         LA    R4,5(,R4)     GET LENGTH WITH LENGTH AND PREFIX   87133
         STH   R4,MTSSTADB+2 STASH LENGTH                        87130
         STCM  R4,IIOO,MTSSTADB+4  CLEAR LENGTH PROCESSED        91294
         LA    R1,MTSSTADB+2 GET PARMLENGTH AND TEXT             87130
         ST    R1,MTS@PARM   SET TEXT POINTER                    87130
         OC    MTSSTADB+6(8),=CL8' '  UPPER CASE                 91304
         CLC   =C'SEND ',MTSSTADB+6  SPECIAL ?                   91303
         BE    LOGOCMD1      YES                                 91303
         CLC   =C'RECEIVE ',MTSSTADB+6  OTHER SPECIAL?           91303
         BE    LOGOCMD1      YES                                 91303
         CLC   =C'SERVER ',MTSSTADB+6 OR ?                       91303
         BNE   XOPEPARM      NO                                  91303
LOGOCMD1 OI    MTSFLGT,MTSFCMD1  EXEC SINGLE COMMAND ONLY        91303
         DROP  R5                                                91273
*****>>>>>---------------------------------------------------<<<<<*****
*    WYLBUR PROPER MAY HAVE TCBS IN KEY 0 OR KEY 8, DEPENDING ON      *
*    INSTALLATION (IEFSDPPT ENTRY).  THE CODE HERE AND IN WORKMAIN    *
*    ENSURES THAT THE KERMIT (KERMNET) SUBTASK GETS CONTROL AND       *
*    RUNS IN KEY 8, PROBLEM STATE.  THIS AVOIDS 30A ABENDS IN         *
*    FREEPOOL AND OTHER IBM SINS FOR KEY ZERO PROGRAMS.               *
*****>>>>>---------------------------------------------------<<<<<*****
XOPEPARM MODESET MODE=SUP    MAKE SURE                           91273
         LA    R1,X'80'      KEY 8                               91273
         MODESET KEYREG=1    FORCE SUBTASK KEY 8                 91273
         LA    R1,MTS@PARM   POINT TO POINTER                    91273
         ATTACH SF=(E,MTSATT) EPLOC=FROGFACE   ATTACH THE SERVICE TASK
         LR    R4,R1         SAVE RETURNED ADDRESS               91273
         LR    R5,R15        SAVE RETURN CODE                    91273
*        MODESET KEY=ZERO,MODE=PROB   GET NORMAL                 91273
         LA    R1,X'34'      SET OPTIONS                         91280
         SVC   107           MODESET                             91280
         BXLE  R5,R5,XOPEATOK  ATTACH TOOK                       91273
         WTO   'KER442E WYLKERM ATTACH FAILED'                   91273
         B     XLOGFAIL      AND QUIT                            91273
XOPEATOK ST    R4,MTSTASK    SAVE THE ADDRESS FOR DETACH         91273
         CSADD MTV#CURS,WK2=R15  ADD NUMBER OF CURRENT SESSIONS  91273
         STMAX R15,MTV#MAXS  SET MAXIMUM CONCURRENT SESSIONS     91273
         CSADD MTV#TOTS      ADD TOTAL SESSIONS SINCE START-UP   91273
         SERVICE CANOF       DISALLOW OPERATOR CANCEL            90285
         B     GETSOME       JOIN COMMON                         91273
XOPEUPRM MVC   MTSSTADB+4+2(0),0(R3)  COPY USER'S PARAMETER      91315
         SPACE 2                                                 91273
***********************************************************************
*    ALLOCATE AND PREPARE SUBTASK WORK AREA                           *
***********************************************************************
WORKINIT LH    R5,=Y(MTWWORKL)  GET LENGTH OF EXTENDED WORK AREA 91273
WORKINCS ICM   R7,15,FREEQUE GET A FREE ELEMENT                  91273
         BZ    WORKMAIN      NONE AVAILABLE; GET A NEW ONE       91273
         L     R15,MTSQLINK  LOAD ITS LINK                       91273
         CS    R7,R15,FREEQUE  TRY TO DEQUEUE IT                 91273
         BNZ   WORKINCS                                          91273
         B     WORKHAVE                                          91273
         SPACE 1                                                 91273
WORKMAIN L     R0,MTV#WA     GET NUMBER OF AREAS GETMAINED       91273
         CH    R0,MAXUSERS   ARE WE BELOW LIMIT ?                91273
         BNH   WORKGETM      YES                                 91273
         L     R5,RTCBRTBI                                       91273
         USING RTBSTART,R5                                       91273
        MVC   RTBTEXT(41),=C'KER413I All work areas in use - try later'
         MVI   RTBTEXT+41,X'15'  MAKE CR                         91273
         MVI   RTBWC+1,42    SET WRITE LENGTH                    91273
         INC   NUMBUSY       COUNT BUSIES                        91280
         B     XLOGNOLO      AND LOG OFF                         91273
*    SUBTASK (NETKERM) WILL RUN IN KEY 8; GETMAIN ACCORDINGLY    91273
*      AVOID 0C4 ON STORAGE/BRANCH (BAL,L AND B) TO MODULE       91280
WORKGETM MODESET MODE=SUP    MAKE SURE SPKA WILL WORK            91273
         LA    R1,X'80'      KEY 8                               91273
         MODESET KEYREG=1  MAKE UNPRIVILEGED                     91273
*        GETMAIN RC,LV=(R5),SP=0,BNDRY=PAGE                      91273
         LR    R0,R5         SET LENGTH                          91280
         LA    R15,4         SET REQUEST TYPE                    91280
         SLR   R1,R1         CLEAR R1 (?)                        91280
         SVC   120           GETMAIN                             91280
         LR    R7,R1         GET WORK AREA ADDRESS               91273
*        MODESET KEY=ZERO,MODE=PROB   BACK TO NORMAL             91273
         LA    R1,X'34'      SET OPTIONS                         91280
         SVC   107           MODESET                             91280
         BXH   R15,R15,XLOG804         ERR. MSG IF STORAGE ^ OBTAINED
         CSADD MTV#WA        COUNT WORK AREAS ALLOCATED          91273
WORKHAVE LR    R4,R7         COPY START ADDRESS                  91273
         L     R14,=A(PATWORK)  GET PATTERNS FOR NIB, RPL, DCBS  91273
         LA    R15,PATSIZE   SIZE TO COPY                        91273
         MVCL  R4,R14        COPY PATTERNS AND ZERO REST         91273
         USING MTSWORK,R7    DECLARE EXTENDED WORK AREA          91273
         LA    R1,MTWBUFF                                        91273
         AH    R1,=Y(MTWBUFIN-MTWBUFF)   INPUT BUFFER ADDRESS    882
         ST    R1,MTSINPAD   MAKE ADDRESS                        91273
         MVI   MTSXPART+2,MTSDSSIZ-MTSXPART-1  SET ORDER SIZE    88244
         MVI   MTSDSSIZ+2,X'40'  SET 3270DS ID                   88243
         MVI   MTSXPART+3,X'0C'  SET CREATE PARTITION REQUEST    88243
         MVI   MTSXPART,X'F3'  SET WSF COMMAND CODE              88243
         LA    R14,ATTPARM   POINT TO DEFAULT PARM               91273
         LA    R15,MTVECT    POINT TO COMMON VECTOR              91273
         STM   R14,R15,MTS@PARM  POINTERS USED TO LINK WORK AREA 91273
         OI    MTS@PARM+4,X'80'  MTS@MTV                         91273
         MVC   MTSPTITL(137),SIMWH137  MOVE TITLE PATTERN        91273
         MVI   MTSPLIN#,X'FF'  NEGATIVE LINE COUNT FORCES PAGE EJECT
         OI    MTSPPAG#+L'MTSPPAG#-1,X'0C'  ZERO PAGE COUNT      91273
         MVC   MTSPVLIN(4),MTSPH137  SET PRINT LINE LENGTH       91273
         MVI   MTSPVLIN+4,C' '  CLEAR PRINT LINE                 91273
         MVC   MTSPVLIN+5(132),MTSPVLIN+4                        91273
         MVC   MTSPVLN2(137),MTSPVLIN  ALSO SECOND LINE          91273
         MVI   MTSFLGV,MTSFVONC   SET FIRST-TIME FLAG            91273
         OI    MTSTRMFG,MTSFWYLB  SHOW WYLBUR SUBSYSTEM          91273
         LA    R0,RTBNTEXT   GET MAXIMUM TEXT BUFFER SIZE        91280
         MIN   R0,=Y(240),TYPE=H  OBS RECEIVE MAXIMUM            93207
         ST    R0,MTSKMXPK   USE *-8 AS MAX PACKET SIZE          91273
         LA    R0,MTSRCEND-MTSRLEN  LENGTH OF SMF RECORD         89337
         STH   R0,MTSRLEN    SET SMF 34 RECORD LENGTH            89337
         MVI   MTSRFLG,2     SET MVS                             89337
         MVI   MTSRCDTY,&SMFTYPE   TYPE 34 (SHORTENED)           91273
         L     R1,CVTPTR                                         89337
         L     R1,CVTSMCA-CVTMAP(R1)                             89337
         MVC   MTSCPUID,16(R1)  SET SYSTEM/MACHINE ID            89337
         TIME  BIN                                               89337
         STCM  R0,15,MTSONTME  SET TIME ON                       89337
         STCM  R1,15,MTSONDTE  SET TIME ON/DATE ON               89337
         STCM  R0,15,MTSSIT  STEP INITIATION                     89337
         STCM  R0,15,MTSAST  ALLOCATION START                    89337
         STCM  R0,15,MTSPPST PROBLEM PROGRAM START               89337
         LA    R0,MTSVARA-MTSSMF  INCORRECT RELOCATE SECT. OFFSET
         STH   R0,MTSRLCT                                        89337
         LA    R0,MTSVARA-MTSVAR  LENGTH OF EXCP SECTION         90017
         STH   R0,MTSVAR                                         89337
         MVC   MTSEXCP(4),=X'80030181'  FAKE BILLING DISK INP.   89337
         MVC   MTSOXCP(4),=X'80030182'  FAKE DISK OUT            89337
         MVI   MTSVARA,4     CPU + NO ACCOUNT                    89337
         L     R1,=A(XPRABEND)  GET DCB ABEND EXIT ADDRESS       91273
         ST@   R1,MTSPDCBX,MVI=X'91'  MAKE DCB ABEND EXIT LIST   89275
         LA    R1,MTSPDCBX   GET EXIT LIST ADDRESS               91273
         STCM  R1,7,DCBEXLSA+MTSPDCB-IHADCB  MAKE DCB EXIT LIST  91273
         ST    R7,MTSSAVER+56  ENSURE WORK BASE FOR DCB ABEND    91273
         AIF   ('&SYSPARM' NE 'DEBUG').NOBUGP                    87040
         LR    R4,R9         SAVE RETURN REGISTER                87040
         BAL   R9,SOUTALOC   ALLOCATE A SYSOUT DD (DDNM=>MTSPRDD)
         LR    R9,R4         RESTORE RETURN                      87040
         BXH   R15,R15,WORKNPRT  ALLOCATION FAILED               87040
         MVC   DCBDDNAM+MTSPDCB-IHADCB,MTSPRDD  MAKE DCB DDNM    87040
         DEVTYPE MTSPRDD,MTSDDB  SEE IF EXHPRINT SUPPLIED        87040
         BXH   R15,R15,WORKNPRT  ALLOC OR PGM ERROR              87040
         LA    R1,MTSPDCB     GET DCB ADDRESS                    87040
         ST@   R1,MTSDDB,MVI=X'8F'  MAKE OPEN,OUTPUT LIST        89275
         OPEN  ,MF=(E,MTSDDB)       OPEN IT                      87040
         TM    MTSPDCB+DCBOFLGS-IHADCB,DCBOFOPN  OPEN NOW ?      87040
         BZ    WORKNPRT      NO                                  87040
         OI    MTSFLGT,MTSFBUG  SET TRACE MODE                   87040
         B     WORKCHEN      JOIN COMMON                         87040
.NOBUGP  ANOP  ,                                                 87040
WORKNPRT MVI   MTSPRDD,C' '  NOT ALLOCATED                       87032
         LA    R1,XPRRET     MAKE DUMMY PUT ROUTINE ADDRESS      87032
         ST    R1,MTSPDCB+48      MAKE NOPS                      87032
WORKCHEN L     R15,WORKQUE   GET CURRENT TOP ELEMENT             91273
         ST    R15,MTSQLINK  MAKE PROVISIONAL LINK               91273
         CS    R15,R7,WORKQUE  TRY TO PLACE IT ON QUEUE          91273
         BNZ   WORKCHEN      NO GOOD; TRY AGAIN                  91273
         BR    R9            RETURN TO CALLER                    91273
         SPACE 1                                                 89337
***********************************************************************
*    PRODUCE STATISTICS FOR SYSTEM CONSOLE CALLER; THEN UPLEVEL       *
***********************************************************************
CONSTAT  L     R5,RTCBRTBI   GET RTB                             91280
         USING RTBSTART,R5   DECLARE IT                          91280
         MVC   RTBTEXT(12),=C'KERMIT info:'                      91280
         LA    R1,RTBTEXT+12                                     91280
         LA    R2,=A(MTV#CURS,MTV#TOTS,MTV#MAXS,NUMBUSY,MTV#FAIL)
         LA    R3,=C'currsess HWMbusyfail'                       91280
         LA    R4,5          DO FIVE COUNTERS                    91280
CONSTATL MVC   0(9,R1),=X'402020202020212040'  MAKE EDIT MASK+BLANK
         L     R15,0(,R2)    LOAD NEXT COUNTER ADDRESS           91280
         L     R0,0(,R15)    GET NEXT COUNTER VALUE              91280
         CVD   R0,INITDDB    PACK IT                             91280
         ED    0(8,R1),INITDDB+4 UNPACK AND EDIT                 91280
         MVC   9(4,R1),0(R3)   MOVE TAG                          91280
         MVI   13(R1),C','   SEPARATE THE FIELDS                 91280
         LA    R1,14(,R1)    NEXT OUTPUT                         91280
         LA    R2,4(,R2)     NEXT ADDRESS                        91280
         LA    R3,4(,R3)     NEXT TAG                            91280
         BCT   R4,CONSTATL   DO NEXT COUNTER                     91280
         LR    R2,R1         SAVE END POSITION                   91280
         BCTR  R1,0          BACKSPACE LAST                      91280
         MVI   0(R1),X'15'   MAKE TRAILING CR                    91280
         LA    R1,RTBTEXT    GET TEXT START                      91280
         SR    R2,R1         GET LENGTH OF TEXT                  91280
         STH   R2,RTBWC      SET WRITE COUNT                     91280
         LA    R1,MICWORK    GET WORK AREA                       91287
         MVI   MPRBRTEC-MPRBSTRT(R8),MRRCXCTL  OLD FORM OF UPLEVEL
         L     R14,MICPSTIB-MICPSTRT(,R1)  GET MY TIB            91287
         L     R14,TIBMILT-TIBSTART(,R14)  GET MILTEN TIB        91287
         ICM   R14,8,=X'04'  REQUEST LOGON                       91287
         B     XLOGNOLX      GO TO UPLEVEL                       91287
         SPACE 1                                                 89337
***********************************************************************
*    LOG-OFF FORCED WHEN NO WORK AREA (MTS) IS AVAILABLE              *
***********************************************************************
XLOG804  WTO   'KER408E REGION TOO SMALL',ROUTCDE=1,DESC=1       91273
         CSADD MTV#FAIL      COUNT FAILURES                      91280
         L     R5,RTCBRTBI   RELOAD RTB                          91273
         USING RTBSTART,R5                                       91273
         MVC   RTBTEXT(24),XLOG804+8   COPY ERROR MESSAGE        91273
         MVI   RTBTEXT+24,X'15'        MAKE CR                   91273
         MVI   RTBWC+1,25    SET WRITE COUNT                     91273
XLOGNOLO LA    R1,MICWORK                                        91273
         USING MICPMAP,R1                                        91273
         MVI   MPRBRTEC-MPRBSTRT(R8),MRRCXCTL  RET TO CALLING SUBSYSTEM
         SLR   R14,R14       REQUEST DEFAULT SUBSYSTEM           91273
         ICM   R14,8,=X'04'  SET LOGON CODE                      91273
XLOGNOLX TM    RTCBAFL,RTCBAFSF  AUTO-LOGOFF ?                   91273
         BZ    XLOGNLOF      NO                                  91273
         MVI   MPRBRTEC-MPRBSTRT(R8),MRRCSOFF  ACKNOWLEDGE SIGN-OFF
         L     R14,MICPSTIB  GET MY TIB                          91273
         LA    R14,0(,R14)   CLEAN IT                            91273
XLOGNLOF ST    R14,MPRBPRM1-MPRBSTRT(,R8)  PASS TO UPLEVEL       91273
         MIC    WRITE,(1),MPRB=(R8)                              91273
         BXH   R15,R15,BADMIC  MIC CALL FAILED                   91273
         B     GETSOME                                           91273
         SPACE 1                                                 91273
***********************************************************************
*    DUPLICATE LOGON - EITHER LOGOFF OR SUBSYSTEM XCTL RETURN         *
***********************************************************************
XLOGDUPE L     R5,RTCBRTBI   RELOAD                              91273
         USING RTBSTART,R5                                       91273
         TM    RTCBAFL,RTCBAFSF  AUTO LOGOFF ?                   91273
         BNZ   LOGOLOFF      YES; GO BYE-BYE                     91273
         TM    RTBSFL,RTBSFHU  DISCONNECTED ?                    91273
         BNZ   LOGOLOFF      YES                                 91273
         TM    RTBSFL,RTBSFEND  I/O ERROR ?                      91273
         BO    LOGOLOFF      ?                                   91273
         ICM   R0,15,MTSTASK STILL HAVE A SUBTASK ?              91273
         BZ    LOGOHAVE      NO; RE-ATTACH                       91273
         B     DOSOME        ELSE TREAT AS ACTION REQUEST        91273
         SPACE 1                                                 91273
***********************************************************************
*    LOG-OFF FORCED BY MCP                                            *
***********************************************************************
XLOGFAIL CSADD MTV#FAIL      COUNT LOGON FAILURES                91273
         XC    MTSUID(24),MTSUID  CLEAR NON-PASSWORD INPUT       91273
         L     R5,RTCBRTBI                                       91273
         USING RTBSTART,R5                                       91273
         MVC   RTBTEXT(21),=C'KER432E ATTACH FAILED'             91273
         MVI   RTBTEXT+21,X'15'  CR                              91273
         MVI   RTBWC+1,21    SET WRITE LENGTH                    91273
         MVI   MTSWYLRQ,MRRCXCTL+1  HAND IT BACK                 91273
         B     SENDWORK      UPLEVEL IT                          91273
         SPACE 1                                                 91273
LOGOLOFF XC    RTBCCLR(RTBCCLRL),RTBCCLR  CLEAR LENGTHS          91287
         OI    MTSFLGT,MTSFGONE  TERMINAL GONE                   91273
         ICM   R1,15,MTSTASK  ATTACHED SUBTASK ?                 91273
         BNZ   DOSOMEWK      USE NORMAL SHUT-DOWN PATH           91273
         MVI   MTSWYLRQ,MRRCSOFF+1  REQUEST LOGOFF               91273
         B     SENDWORK      SEND IT BACK                        91273
         SPACE 2                                                 91273
***********************************************************************
*    SUBSYSTEM SHUTDOWN REQUESTED                                     *
***********************************************************************
QUIT     MIC    WRITE,MICWORK,MPRB=(R8),ROUTE=MRRCRETN           91273
         BXH   R15,R15,BADMIC  MIC CALL FAILED                   91273
         MIC   CLOSE,MICWORK  CLOSE INTERFACE                    91273
         SPACE 1                                                 OBS
         OI    FLAGS,FGDOWN  SHOW SYSTEM GOING DOWN              91273
         LA    R7,WORKQUE-(MTSQLINK-MTSWORK)                     91273
         USING MTSWORK,R7                                        91273
MPOSLOOP ICM   R7,15,MTSQLINK  GET NEXT TASK                     91273
         BZ    MPOSPOST      NONE                                91273
         OI    MTSFLGT,MTSFGONE  SHOW TERMINAL COMING DOWN       91273
         ICM   R2,15,MTSTASK  ANY SUBTASK ?                      91273
         BZ    MPOSLOOP      NO                                  91273
         L     R0,=X'00622000'  SET COMPLETION CODE              91273
         OI    MTSFLGT,MTSFXEND  END REQUEST                     89328
         POST  MTSATECB,(0)  POST ATTENTION                      89328
         POST  MTSCNECB,(0)  POST CANCEL ECB                     91273
         B     MPOSLOOP      TRY ANOTHER TASK                    91273
MPOSPOST L     R1,=A(MAINECB)  GET MAIN TASK ECB                 91273
         POST  (1),0         POST IT                             91273
         DROP  R7                                                89328
MAINQUET ICM   R0,15,WORKQUE  ANYTHING TO DO ?                   91273
         BZ    MAINEXIT      NO; QUIT                            91273
         LA    R2,30         WAIT FOR 30 SECONDS                 87013
MAINQUEX UNPOST TIMEECB                                          88348
         STIMER REAL,EXITTIME,BINTVL==A(100)  WAIT 1 SECOND 30 TIMES
         WAIT  ECB=TIMEECB                                       91273
         ICM   R0,15,WORKQUE  DONE YET ?                         91273
         BZ    MAINEXIT      YES                                 91273
*87321*  ICM   R0,15,MTV#CURS  ANY ACTIVE SUB-TASKS ?            87273
*87321*  BZ    MAINEXIT      NO; MAY HAVE A HUNG TERMINAL ?      87273
         BCT   R2,MAINQUEX                                       87013
         ICM   R1,15,DUMPCODE  GET DUMP OPTION AND ABEND CODE    87263
         SVC   13            TAKE A DIVE                         87263
DUMPCODE DC    X'00522000'   QUIT WITH SYSTEM 522                87263
         SPACE 1                                                 91273
MAINEXIT DS    0H                                                91273
MAINQUIT ENDM  RC=0                                              91273
         SPACE 1                                                 91273
***********************************************************************
*    INVALID MPRB REQUEST CODE RECEIVED - QUIT                        *
***********************************************************************
BADREQ   WTO   'KER410E INVALID REQUEST CODE IN WYLKERM'         91273
         ABEND 410,DUMP                                          91273
         SPACE 1                                                 91273
         PUSH  USING                                             91273
         DROP  ,                                                 91273
         USING EXITTIME,R15                                      91273
EXITTIME POST  TIMEECB,0     POST STIMER COMPLETION              91273
         BR    R14           RETURN TO SYSTEM                    91273
TIMEECB  DC    F'0'                                              91273
         POP   USING                                             91273
         SPACE 5                                                 OBS
***********************************************************************
*    SUBSYSTEM DUMP REQUESTED                                         *
***********************************************************************
DUMP     MIC    WRITE,MICWORK,MPRB=(R8),ROUTE=MRRCRETN           91273
         BXH   R15,R15,BADMIC                                    91273
         MIC   CLOSE,MICWORK  CLOSE INTERFACE                    91273
         ABEND 451,DUMP                                          91273
ATTPARM  DC    H'4,0'        SUBTASK COMMAND BUFFER              91301
         SPACE 1                                                 91273
SIMWH137 DC    H'137,0',C'1'                                     91273
SIMWTITX DC    CL60'     *****     WYLKERM TERMINAL ACTIVITY LOG     ***
               ***     '                                         91273
SIMWTITE DC    C'RUN ON '                                        91273
SIMWTIDT DC    CL6'YY.DDD',C' AT '                               91273
SIMWTITM DC    CL8'HH:MM:SS',CL4' '                              91273
         DC    C'PAGE'                                           91273
SIMWTIPG DC    C'123456',CL38' '                                 91273
SIMWTILE EQU   SIMWTITE-SIMWTITX                                 91273
         ORG   SIMWH137+137                                      91273
         SPACE 2                                                 91273
         DROP  ,                                                 91273
***********************************************************************
*    INITIALIZATION WITHIN SUBTASK                                    *
***********************************************************************
SUBINIT  XHEAD RPL=NO,EXH=NO,SAVE=RENT,MODE=OS,RENT=XSUBSLEN     89328
         USING SAVE,R13      DECLARE EXTENDED SAVE AREA          89328
         L     R11,=A(WORKBASE) GET COMMON WORK AREA             91273
         USING WORKBASE,R11  MAKE SECOND BASE                    91273
         SH    R6,=Y(MTS@PARM-MTSWORK)  GET WORK AREA BACK       89328
         USING MTSWORK,R6    DECLARE IT                          89328
         LTCB  R14           GET CURRENT TCB                     91058
         ICM   R15,15,TCBFSA-TCB(R14)  GET FSA                   91058
         BZ    *+4+6         NONE ?                              91058
         XC    0(4,R15),0(R15)  CLEAR @SERVICE ANCHOR            91058
         OI    MTSSECFG,MTSSECON  ELSE FAKE LOGON COMPLETE       89328
DONESEC  OI    MTSSAVPO,MTSSAMUL  INDICATE MULTIPLE TASKS IN ADDRES
         AIF   ('&LOCAL' NE 'CCSI' OR &WYLREL GT 6).NOCPXON      91303
         L     R15,MTS@RTCB  GET RTCB BACK                       91303
         L     R15,RTCBCPA-RTCBSTRT(,R15)  GET CPA               91303
         TM    CPAFLAG-CPASTRT(R15),CPADXON  X-ON ?              91303
         BNZ   SUBNLOG       NO                                  91303
         OI    CPAFLAG-CPASTRT(R15),CPADXON  RESET IT            91303
         OI    MTSFLGT,MTSFXON  REMEMBER TO TURN IT ON LATER     91303
.NOCPXON SPACE 1                                                 91273
SUBNLOG  LA    R14,MTSTMECB  GET FIRST ECB IN LIST               89328
         LA    R15,MTSATECB  GET SECOND ECB                      89328
         LA    R0,MTSCNECB   GET THIRD                           89328
         LA    R1,MTSWECB    GET I/O ECB LAST                    89328
         STM   R14,R1,MTSTLIST  MAKE LONG ECB LIST               89328
         OI    MTSELIST+8,X'80'   MAKE END OF LIST BIT           89328
         L     R14,=A(GETLINE)  GET MY (LOCAL) GETLINE ROUTINE   89328
         L     R15,=A(PUTLINE)  AND PUTLINE                      89328
         SLR   R0,R0                                             89334
*DEFER*  L     R0,=A(FULLSCRN)  AND FULL-SCREEN PUT              89328
         SLR   R1,R1         NOT DONE YET                        89334
*DEFER*  L     R1,=A(FULLWSF)   AND STRUCTURED FIELD WRITE/RESPONSE
         STM   R14,R1,MTSGETLN  SET PROCESSING ADDRESSES         89328
         SLR   R14,R14                                           89337
*DEFER*  L     R14,=A(LOFFCODE)  SPECIAL LOGOFF EXIT             89337
         L     R15,=A(COMMANDS)  COMMAND PRE-PROCESSING          89345
         L     R0,=A(DDACCESS)   ACCESS CONTROL CHECK            89337
         STM   R14,R0,MTSLOGOF                                   89337
         L     R3,=A(STABLE) GET STAE EXIT ADDRESS               91273
         STAE  (R3),CT,PARAM=(R6),XCTL=YES,MF=(E,MTSSTAXP)       89328
         OI    MTSSAVPO,MTSSAPRM  YES; RUN WITH PROMPTS          89328
         MVI   MTSUCB+UCBTBYT4-UCBOB,X'09'  SET 3277 TYPE        91273
         TM    MTSPDCB+DCBOFLGS-IHADCB,DCBOFOPN  PRT OPEN NOW ?  87040
         BZ    *+8           NO                                  87040
         OI    MTSSIMFG,MTSSIPRT  SET PRINT REQUEST              89328
         SPACE 1                                                 91273
         MVC   MTSPTITX+14(8),NIBSYM-NIBST+MTSNIB  FIX TITLE     91273
         SPACE 1                                                 89328
***********************************************************************
*    INVOKE KERMIT PROPER; USE SPECIAL CALLING PARAMETER LIST AND     *
*    SPECIAL ENTRY POINT KERMNET                                      *
***********************************************************************
XSUBCALL MVC   MTSUIF,MTSUID SET SMF USER ID                     89337
         MVC   MTSUDATA,MTSACCT  SET SMF USER DATA (OUR ACCOUNT) 89337
         MVI   MTSINVSQ,1    STEP 1                              89337
         MVI   MTSPRI,X'4F'  PRTY=4                              89337
         MVC   MTSPRGNM,=CL8'*WYLKERM'  FAKE NAME                89337
         MVC   MTSINVNM,=CL8'*WYLKERM'  FOR BILLING              89337
         LA    R0,64         SET REGION=64K                      89337
         STH   R0,MTSEFRGN                                       89337
         STH   R0,MTSMCRE                                        89337
         MVI   MTSSPK,X'80'  PROTECT KEY                         89337
         L     R14,MTS@PARM  POINT TO CMD BUF                    91294
         LA    R15,MTVECT    POINT TO MTV                        89328
         LA    R0,MTSWORK    POINT TO MTS                        89328
         STM   R14,R0,MTSSPARM  SAVE                             89328
         OI    MTSSPARM+8,X'80'  SET END OF LIST BIT             89328
         LA    R1,MTSSPARM   POINT TO PARM LIST                  89328
         L     R15,@KERMIT   GET KERMNET ENTRY POINT             90206
         BALR  R14,R15       INVOKE KERMIT WITH OVERRIDE LIST    90206
         SPACE 1                                                 89337
         STCM  R15,3,MTSSTAT TERMINATION STATUS                  89337
         TIME  BIN                                               89337
         STCM  R0,15,MTSRCDTS  SET TIME OFF                      89337
         STCM  R1,15,MTSRCDTE  SET DATE OFF                      89337
         CLI   MTSRCDTY,0    INHIBIT SMF WRITE ?                 91273
         BE    XSUBNSMF                                          91273
         MODESET KEY=NZERO                                       91273
         LA    R1,MTSSMF                                         89337
         SMFWTM (1)          WRITE THE RECORD                    89337
         MODESET KEY=ZERO                                        91273
XSUBNSMF XC    MTSUIF,MTSUIF SIGNAL RECORD WRITTEN               89337
         SPACE 1                                                 89328
XSUBEXIT XEXIT MODE=OS                                           89328
XPRRET   BR    R14           RETURN TO CALLER                    91273
         SPACE 1                                                 91273
SCRNDEF  SCRN  (RESTKBY,RESTMDT),SBA,(1,1)                       89328
SCRN40   SCRN  (CHAR40,RESTKBY,RESTMDT)                          89328
SCRN80   SCRN  (CHAR80,RESTKBY,RESTMDT)                          89328
         SPACE 1                                                 91273
DEVMAP   DSECT ,                                                 91273
DEVCCW   DS    A             CCW ADDRESS                         91273
DEVSIZ   DS    0XL10         SIZE VALUES                         91273
DEVPAGE  DS    H             PAGE SIZE                           91273
DEVLINE  DS    H             LINES PER PAGE                      91273
DEVBYTE  DS    H             BYTES PER LINE                      91273
DEVPREF  DS    H             PREFIX LENGTH OF CONTROL BYTES      91273
DEVSUFF  DS    H             SUFFIX CONTROL BYTES                91273
DEVPFX   DS    CL6           PREFIX CONTROL BYTES, IF ANY        91273
         SPACE 1                                                 89328
SAVE     DSECT ,             SAVE / WORK SPACE FOR MAIN TASK     89328
SAVEND   EQU   *             LAST ENTRY                          89328
XSUBSLEN EQU   *-SAVE        LENGTH OF XSUBSAVE SAVE/WORK AREA   89328
WYLKERM  CSECT ,                                                 89328
         DROP  ,                                                 89328
         EJECT ,                                                 89337
*        THIS EXIT IS INVOKED BY KERMIT AFTER DATASET ALLOCATION, BUT
*        PRIOR TO OPENING THE DCB. R1 POINTS TO AN OPEN TYPE (0-IN,
*        80-OUT) AND THE ADDRESS OF THE DDNAME. USER INFO IS IN THE
*        MTS FOR THE SUBTASK.                                    89337
*          A HEX 1 IN THE TYPE INDICATES A DSNAME IS BEING PASSED.
*        HEX 40 IN THE TYPE REQUESTS VOLUME SERIAL ACCESS CHECKING
*                                                                89337
DDACCESS XHEAD SAVE=RENT,EXH=NO,RPL=NO,MODE=OS,RENT=DDACWRKL     89337
         L     R7,PSATOLD-PSA  GET CURRENT TCB                   91273
         L     R7,TCBFSA-TCB(,R7)  LOAD THE SAVE AREA            91273
         L     R7,24(,R7)    LOAD ATTACH'S R1                    91273
         SH    R7,=Y(MTS@PARM-MTSWORK)  GET WORK AREA BACK       91273
         USING MTSWORK,R7    DECLARE IT                          91273
         LM    R9,R10,0(R6)  LOAD OPEN TYPE, DDNAME ADDRESS      89337
         USING DDACWORK,R13  DECLARE WORK AREA                   89337
         TM    3(R6),X'40'   VOLUME SERIAL ?                     91294
         BNZ   DDACVOLS      YES                                 91294
         TM    3(R6),1       IS THIS DSNAME OR DDNAME ENTRY ?    91084
         BZ    DDACDDNM      DDNAME                              91084
         MVC   JFCBDSNM,0(R10)  COPY PASSED PARAMETER            91084
         B     DDACDSN       AND CHECK THE NAME                  91084
DDACDDNM MVC   RDDUMDCB(RDOPLIST-RDDUMDCB),PARDCB  MAKE PHONY DCB
         LA    R2,RDDUMDCB                                       81359
         LA    R3,INFMJFCB   POINT TO NEW JFCB                   89337
         STM   R2,R3,RDOPLIST  MAKE LISTS                        81359
         MVI   RDOPLIST,X'80'  SET END OF LIST BIT FOR DCBS      81359
         MVI   RDEXLIST,X'87'  SET JFCB EXIT ADDRESS             81359
         LA    R2,RDEXLIST                                       81359
         STCM  R2,7,RDDUMDCB+DCBEXLSA-IHADCB  SET EXIT LIST ADDRESS
         MVC   DCBDDNAM-IHADCB+RDDUMDCB,0(R10) COPY USER'S DDNAME
         RDJFCB MF=(E,RDOPLIST)  READ THE JFCB                   81359
         LA    R8,12         SET CONTROL BLOCK ERROR             89337
         BXH   R15,R15,DDACOUCH  EXIT IF NOT READ                89337
DDACDSN  L     R5,MTS@RTCB   GET RTCB BACK                       91294
         USING RTCBSTRT,R5   DECLARE IT                          91294
         MVC   RDOPLIST(8),MTSACCT  COPY ACCOUNT                 91294
         NI    RDOPLIST,X'CF'  MAKE 1-9 INTO A-I                 89337
         CLI   RDOPLIST,X'C0'  SYSTEMS ?                         89337
         BNE   *+8           NO                                  89337
         MVI   RDOPLIST,C'Z'   SYSTEMS SPECIAL                   89337
         CLC   =C'WYL.',JFCBDSNM  OLD STYLE WYLBUR ?             89337
         BE    DDACWOLD      YES                                 89337
         CLC   MTSUID(7),JFCBDSNM  USERID. FORM ?                89337
         BNE   DDACHACC      CHECK ACCT. FORM                    89337
         CLI   JFCBDSNM+7,C'.'  UID. ?                           89337
         BE    DDACPASS      YES; ALLOW                          89337
DDACHACC MVI   RDOPLIST+4,C'.' MAKE INDEX DOT                    89337
         CLC   RDOPLIST(5),JFCBDSNM  ACCOUNT INDEX ?             89337
         BE    DDACPASS                                          89337
         B     DDACFAIL      NO OTHER CHECKS AT THIS TIME        89337
         SPACE 1                                                 89337
DDACWOLD CLC   MTSUID(7),JFCBDSNM+13  WYLBUR USER ?              92080
         BE    DDACPASS      YES; PERMIT                         89337
         AIF   ('&LOCAL' NE 'CCSI').NODTSNM                      91294
         ORG   DDACWOLD      UID 7 NOT ENOUGH                    91294
         AIF   (&WYLREL LT 6).CHKRTCB                            91294
         CLC   MTSUID(7),JFCBDSNM+13  OLD STYLE NAME ?           91294
         BE    DDACPASS      YES; PERMIT                         91294
         AGO   .NODTSNM                                          91294
.CHKRTCB CLC   MTSUID(8),JFCBDSNM+13  REALLY USER'S ID ?         92080
         BE    DDACPASS      YES; PERMIT                         92080
         CLC   RDOPLIST(8),JFCBDSNM+4  SAME EXACT ACCOUNT ?      91294
         BE    DDACPASS      YES; ALLOW                          89337
         TM    RTCWPRIV,RTCWSUB  SUB-ACCOUNT PRIVILEGE ?         91294
         BZ    DDACLIST                                          92080
         CLC   RDOPLIST(4),JFCBDSNM+4  SAME MAJOR ACCOUNT ?      92080
         BE    DDACPASS      YES; ALLOWED WITH SUBACCT PRIVY     92080
DDACLIST LA    R1,RTCWACTS   POINT TO EXTENDED ACCOUNTS          91294
         LA    R0,6          SIX ENTRIES                         91294
         MVC   RDOPLIST(4),JFCBDSNM+4  COPY EXTERNAL ACCOUNT     92080
         CLI   RDOPLIST,C'Z'  SPECIAL ?                          92080
         BNE   *+8           NO                                  92080
         MVI   RDOPLIST,C'0'  SPECIAL                            92080
         OI    RDOPLIST,C'0'  MAKE A-I INTO 1-9                  92080
ACCVOLLP CLC   RDOPLIST(4),0(R1)  MATCHING ACCOUNT ?             92080
         BE    DDACPASS      YES; PERMIT                         91294
         LA    R1,4(,R1)                                         91294
         BCT   R0,ACCVOLLP                                       91294
.NODTSNM SPACE 1                                                 91294
DDACFAIL LA    R8,4          DENY ACCESS                         89337
         TM    3(R6),X'80'   IS THIS AN OUTPUT FILE ?            90050
         BNZ   DDACOUCH      YES; FAIL IT                        90050
         AIF   ('&LOCAL' EQ 'CCSI' AND &WYLREL LT 6).CCSIPRV     91273
         TM    RTCBKIFL,X'30'  SYSTEM OR OPERATOR PRV?           91294
         AGO   .COMPRIV                                          91273
.CCSIPRV TM    RTCWPRIV,RTCWSYS  SYSTEMS ?                       91294
.COMPRIV BNZ   DDACPASS      YES; ALLOW DIAGNOSTIC READS         90050
         CLC   =C'PUBLIC.',JFCBDSNM  PUBLIC WYLBUR FILES ?       91091
         BE    DDACPASS      YES; PERMIT                         91091
         CLC   =C'SUPPORT.',JFCBDSNM  WYL 8 PUBLIC ?             92080
         BE    DDACPASS      YES                                 92080
         CLC   =C'WYL.DTPUBLIC',JFCBDSNM  OLD WYLBUR PUBLIC ?    91091
         BE    DDACPASS      YES                                 91091
         CLC   =C'SYS1.PROCLIB',JFCBDSNM  SYSTEM PROCS ?         91091
         BE    DDACPASS                                          91091
         CLC   =C'SYS1.BATPROC',JFCBDSNM  BATCH PROCS ?          93020
         BE    DDACPASS      YES                                 91091
         CLC   =C'SYS1.USERPROC',JFCBDSNM  USER'S PROCS ?        93020
         BE    DDACPASS      YES                                 91091
         CLC   =C'SYS1.CLIST',JFCBDSNM  LOGON PROCS ?            91091
         BNE   DDACFAIL      NOTHING ELSE, YET                   91091
         B     DDACPASS      ALLOW ACCESS                        91294
         SPACE 1                                                 91294
***********************************************************************
*                                                                     *
*        DASD VOLUME ACCESS CHECK                                     *
*                                                                     *
*        EACH LEGAL VOLUME HAS AN ENTRY IN THE USERVOL TABLE, WHICH   *
*        IS POINTED TO BY THE USER CVT, AND BUILT BY THE LOCAL SMF    *
*        INITIALIZATION ROUTINE A$GINIT (USING THE ACCOUNTING SVC).   *
*                                                                     *
*        THE USERVOL ENTRY HAS A LIST OF ONE TO FOUR ACCOUNT NUMBERS  *
*        ALLOWED TO ACCESS THE PACK. THE ACCOUNT NUMBER CORRESPONDS   *
*        TO THE RACF GROUP (SOMEWHAT). THE ENTRY MAY BE IN THE FORM   *
*        OF A MASK, USING '?' FOR CHARACTER MATCH, AND '*' (AND HEX   *
*        ZERO) FOR A COMPLETE MATCH.                                  *
*                                                                     *
***********************************************************************
         SPACE 1                                                 91294
DDACVOLS LTR   R1,R10        ANY POINTER PASSED ?                91294
         BZ    DDACPASS      NO; PASS WITH TREPIDATION           91294
         CLI   0(R10),C' '   VALID ?                             91294
         BNH   DDACPASS      YES ?                               91294
         AIF   ('&LOCAL' NE 'PID').NOVSCHK                       93095
         L     R6,CVTPTR     GET CVT                             91294
         ICM   R6,15,CVTUSER-CVTMAP(R6)  TEST USER EXTENSION     91294
         BZ    DDACPASS      HUH ?                               91294
         ICM   R6,15,UCLVOLS-USERCVT(R6)  VOLUME TABLE ?         91294
         BZ    DDACPASS      NO ?                                91294
         LM    R15,R1,0(R6)  LOAD VOLSER BXLE TABLE              91294
         SR    R1,R0         ADJUST FOR FIRST TEST               91294
         CR    R15,R1        IS THERE AT LEAST ONE ENTRY ?       91294
         BH    DDACPASS      NO; DISASTER - ALLOW THIS ACCESS    91294
VOLCHLUP BXH   R15,R0,VOLNOACC  VOLUME NOT DEFINED               91294
         USING UVOLSER,R15   DECLARE SINGLE VOLUME ENTRY         91294
         CLC   0(L'UVOLSER,R10),UVOLSER   IS THIS THE CORRECT ENTRY ?
         BH    VOLCHLUP      NOT YET; TRY AGAIN                  91294
         BL    VOLNOACC      NO; FAIL - DEFINITION MISSING       91294
         SPACE 1                                                 91294
VOLCHMOR SLR   R5,R5         (RESTART FOR MULTIPLE ENTRIES)      91294
         ICM   R5,1,UVOL#ENT  GET NUMBER OF ACCOUNT NUMBERS      91294
         BZ    VOLNOACC      NO ACCESS PERMITTED                 91294
         LA    R4,UVOLACT1   POINT TO FIRST ENTRY                91294
         LA    R0,4          SET NUMBER OF ENTRIES               91294
         MIN   (R5),(R0)     USE SMALLER VALUE TO LOOP           91294
         DROP  R15                                               91294
         LR    R3,R15        SAVE DEFINITION ADDRESS             91294
         SPACE 1                                                 91294
         USING UVOLACT1,R4   DECLARE THE ENTRY ADDRESS           91294
VOLACCLP TM    UVOLACF1,A$VAFRDW+A$VALLOC  ALLOCATE/WRITE PRIVY? 91294
         BNO   VOLACCUP      NO; TRY ANOTHER                     91294
         LA    R1,MTSACCT    POINT TO ACCOUNT TO BE CHECKED      91294
         LA    R0,L'MTSACCT    AND ITS LENGTH                    91294
         LA    R14,UVOLACT1  POINT TO MASK                       91294
         CLI   UVOLACT1,0    ANY ENTRY AT ALL ?                  91294
         BE    VOLACCUP      NO; DO NOT TEST                     91294
         LA    R15,L'UVOLACT1  SET MASK LENGTH                   91294
         TM    UVOLACF1,A$VAFSUB  ANY SUB-ACCOUNT ?              91294
         BZ    *+8           YES                                 91294
         LA    R15,4         SET LENGTH FOR MAJOR ACCOUNT, ONLY  91294
         BAL   R9,COMPMASK   CHECK FOR MATCH                     91294
         BE    DDACPASS      MATCH; PERMIT ACCESS                91294
*DEFER*  B     VOLNOACC      NOT AUTHORIZED                      91294
*DEFER BECAUSE THERE MAY BE ANOTHER MATCHING ENTRY IN THE LIST   91294
         SPACE 1                                                 91294
VOLACCUP LA    R4,UVOLACT2   POINT TO NEXT ENTRY IN LIST         91294
         BCT   R5,VOLACCLP   CHECK IT                            91294
         DROP  R4            NO LONGER NEED SINGLE ENTRY         91294
         USING UVOLSER,R3    RESTORE SAVED ADDRESS               91294
         CLI   UVOL#ENT,4    MORE THAN FOUR ENTRIES ?            91294
         BL    VOLNOACC      NO; UNAUTHORIZED REQUEST            91294
*        WHEN MORE THAN 4 MASKS ARE DEFINED FOR A VOLUME, THE NEXT
*        FOUR (MORE OR LESS) ARE PLACED INTO AN ADJACENT ENTRY.  91294
         LM    R0,R1,4(R6)   RELOAD BXLE LENGTH/END ADDRESSES    91294
         LR    R15,R3        COPY CURRENT ENTRY ADDRESS          91294
         BXH   R15,R0,VOLNOACC  NOT AUTHORIZED FOR ACCESS        91294
         CLC   0(L'UVOLSER,R10),UVOLSER-UVOLSER(R15)  CONTINUATION ?
         BE    VOLCHMOR      YES; DO IT                          91294
*NEXT    B     VOLNOACC      ELSE ACCESS IS DENIED               91294
.NOVSCHK SPACE 1                                                 91294
VOLNOACC LA    R8,4          ACCESS DENIED                       91294
         B     DDACOUCH      EXIT WITH ERROR                     91294
         SPACE 1                                                 91294
*        COMPARE ROUTINE WITH MASK OPTION                        91294
*          TO MAKE THE CODE FASTER, MASK LENGTH AND STRING LENGTH
*          ARE DEFINED AS THE SAME REGISTER. TO MAKE IT EASIER TO
*          REMOVE THIS CODE IF NOT DESIRED, ALL CALLERS MAINTAIN 91294
*          BOTH LENGTH REGISTERS (15 AND 0).                     91294
*                                                                91294
COMPMASK CMASK L=R15,MASKEQU='?',MASKEND='*
         SPACE 1                                                 91294
DDACPASS SLR   R8,R8         PERMIT ACCESS                       89337
         SPACE 1                                                 89328
DDACOUCH L     R1,0(,R13)    GET OLD SAVE AREA (OUR STYLE)       89328
         ST    R8,16(,R1)    PASS RETURN CODE                    89328
         XEXIT MODE=OS       RETURN TO CALLER                    89328
         SPACE 1                                                 81359
PARDCB   DCB   DDNAME=ANY,MACRF=(E),DSORG=PS,EXLST=2             81359
         SPACE 1                                                 89337
DDACWORK DSECT ,                                                 89337
         DS    18F           SAVE AREA                           89337
RDDUMDCB DCB   DDNAME=ANY,MACRF=(E),DSORG=PS,EXLST=2             89337
RDOPLIST DS    A                                                 89337
RDEXLIST DS    A                                                 89337
DDACJFCB IEFJFCBN ,          EXPAND AN IN-LINE JFCB              89337
DDACWRKL EQU   *-DDACWORK                                        89337
         AIF   ('&LOCAL' NE 'PID').NOUVOLT                       93095
USERVOLS USERVOLS ,          VOLUME ACCESS TABLE DEFINITION      91294
A$VOLUME A$VSER ,            MATCHING ACCOUNT RECORD             91294
.NOUVOLT ANOP  ,                                                 91294
WYLKERM  CSECT ,                                                 89337
         EJECT ,                                                 89328
*        THIS CODE SERVES AS A REPLACEMENT FOR THE TSO 'GETLINE' 89328
*        FUNCTION, EXCEPT THAT ONLY TERMINAL INPUT IS PROCESSED, 89328
*        AND CURRENTLY 'ASIS' AND 'EDIT' REQUESTS ARE IGNORED.   89328
*                                                                89328
GETLINE  XHEAD SAVE=RENT,EXH=NO,RPL=NO,MODE=OS                   89328
         L     R7,PSATOLD-PSA  GET CURRENT TCB                   91273
         L     R7,TCBFSA-TCB(,R7)  LOAD THE SAVE AREA            91273
         L     R7,24(,R7)    LOAD ATTACH'S R1                    91273
         SH    R7,=Y(MTS@PARM-MTSWORK)  GET WORK AREA BACK       91273
         USING MTSWORK,R7    DECLARE IT                          91273
         LA    R8,28         SET TERMINAL DISCONNECTED           89328
         TM    MTSFLGT,MTSFGONE  SHUTTING DOWN ?                 89328
         BNZ   GETLOUCH      YES; QUIT RAPIDLY                   89328
         LA    R8,20         SET PARAMETER LIST ERROR            89328
         L     R6,12(,R6)    GET CONTROL AREA                    89328
         TM    2(R6),X'80'   GET ?                               89328
         BZ    GETLOUCH      NO; INVALID GTPB OR IOPL            89328
         UNPOST MTSATECB     RESET RESPONSE ECB                  91273
         UNPOST MTSWECB      DON'T WAIT FOR WRITE                89328
         AIF   (&WYLREL LT 7).NOVTMGT                            91308
         L     R5,MTS@RTB    GET RTB                             91308
         USING RTBSTART,R5                                       91308
         L     R15,MTS@RTCB  GET RTCB                            91349
         USING RTCBSTRT,R15                                      91349
         TM    2(R6),X'03'   OTHER THAN GET EDIT ?               91349
         BZ    GETLNTIF      YES                                 91356
         NI    RTCBDF2,255-RTCBVTNL         SUPPRESS NEW LINE    91356
         OI    RTBMF2,RTBMFIDL  SUPPRESS IDLES                   91349
         MVI   RTBIDLFT,0    BY ZEROING MULTIPLIER               91349
         B     GETLNTIM                                          91356
GETLNTIF OI    RTCBDF2,RTCBVTNL         ENABLE NEW LINE          91356
         DROP  R5,R15                                            91349
.NOVTMGT ANOP  ,                                                 91308
GETLNTIM XC    MTSTMECB,MTSTMECB  CLEAR TIMER ECB                89345
         TM    2(R6),X'03'   OTHER THAN GET EDIT ?               91304
         BNZ   GETLTIME      YES                                 91304
         TM    MTSFLGT,MTSFCMD1  ONLY ONE COMMAND ?              91303
         BZ    GETLTIME      NO                                  91303
         NI    MTSFLGT,255-MTSFCMD1  RESET TO PREVENT PROBLEMS   91303
         L     R2,MTSINPAD   GET INPUT BUFFER                    91303
         MVC   0(6,R2),=CL6'WYLBUR'  SET RETURN TO CALLER        91303
         LA    R3,6          SET LENGTH                          91303
         STM   R2,R3,MTSINPAD  SET COMMAND ADDRESS AND LENGTH    91303
         B     GETLONCE      SKIP                                91303
GETLTIME STIMER REAL,STIMEXIT,BINTVL==A(15*60*100)  WAIT 15 MINUTES
         MVI   MTSWYLRQ,MRRCTREA+1  SET FOR READ PENDING         91273
         MODESET KEY=ZERO    SET TO ACCESS MAIN TASK ECB         91273
         L     R1,=A(MAINECB)                                    91273
         POST  (1),0         POST IT FOR ACTION                  91273
         MODESET KEY=NZERO   RESTORE UNPRIVILEGED MODE           91273
         WAIT  ECBLIST=MTSTLIST  WAIT FOR ATTN, INPUT OR TIMER   89328
         SPACE 1                                                 91273
         LA    R8,28         SET DISCONNECTED                    89328
         TM    MTSTMECB,X'40'  TIMER EXPIRED ?                   89345
         BZ    GETLBUF       NO                                  89345
         L     R15,MTS@RTCB  REGAIN RTCB                         91303
         TM    RTCBSFL-RTCBSTRT(R15),RTCBSFNT  NO TIMEOUT ?      91303
         BNZ   GETLNTIM      CORRECT; JUST WAIT SOME MORE        91303
GETLLOST OI    MTSFLGT,MTSFGONE  SET TERMINAL GONE               89345
         B     GETLOUCH      QUIT                                89345
GETLBUF  TTIMER CANCEL       CANCEL TIMER REQUEST                89345
         TM    MTSCNECB,X'40'  CANCEL POSTED ?                   89328
         BNZ   GETLLOST      YES; QUIT NOW                       91127
GETLONCE SLR   R8,R8         SET FOR NORMAL COMPLETION           89328
         LM    R2,R3,MTSINPAD  GET INPUT BUFFER/LENGTH           89328
         LA    R0,4                                              89328
         SR    R2,R0         SPACE TO 4 BEFORE TEXT              89328
         AR    R3,R0         SET LENGTH INCLUDING LENGTH OF LENGTH
         SLL   R3,16         MALIGN                              89328
         STCM  R3,15,0(R2)   MAKE RDW                            89328
         ST    R2,4(,R6)     SET BUFFER POINTER                  89328
         UNPOST MTSATECB     RESET ATTENTION ECB                 89328
         OI    MTSFLGM,MTSFREAD  SHOW LAST I/O WAS READ          89345
         TM    MTSFLGM,MTSFATTN  ATTENTION ?                     89328
         BZ    GETLOUCH      NO                                  89328
         LA    R8,8          SET ATTENTION INTERRUPT             89328
         NI    MTSFLGM,255-MTSFATTN  RESET ATTENTION             89330
         CLC   0(2,R2),=H'4' ANY TEXT READ ?                     91091
         BH    GETLOUCH      YES; LEAVE 'READ' STATUS            89345
         NI    MTSFLGM,255-MTSFREAD  RESET                       89345
         SPACE 1                                                 89328
GETLOUCH L     R1,0(,R13)    GET OLD SAVE AREA (OUR STYLE)       89328
         ST    R8,16(,R1)    PASS RETURN CODE                    89328
         XEXIT MODE=OS       RETURN TO CALLER                    89328
         SPACE 1                                                 89345
*        STIMER EXIT FOR CONDITIONAL WAIT ON INPUT               89345
*                                                                89345
         USING STIMEXIT,R15                                      89345
STIMEXIT L     R1,PSATOLD-PSA  GET CURRENT TCB                   89345
         L     R1,TCBFSA-TCB(,R1)  LOAD THE SAVE AREA            89345
         L     R1,24(,R1)    LOAD ATTACH'S R1                    89345
         SH    R1,=Y(MTS@PARM-MTSWORK)  GET WORK AREA BACK       89345
         POST  MTSTMECB-MTSWORK(R1),0  POST TIME ECB             89345
         BR    R14           RETURN TO OS                        89345
         EJECT ,                                                 89328
*        THIS CODE SERVES AS A REPLACEMENT FOR THE TSO 'PUTLINE' 89328
*        FUNCTION, EXCEPT THAT ONLY TERMINAL INPUT IS PROCESSED, 89328
*        AND CURRENTLY 'ASIS' AND 'EDIT' REQUESTS ARE IGNORED.   89328
*                                                                89328
PUTLINE  XHEAD SAVE=RENT,EXH=NO,RPL=NO,MODE=OS                   89328
         L     R7,PSATOLD-PSA  GET CURRENT TCB                   91273
         L     R7,TCBFSA-TCB(,R7)  LOAD THE SAVE AREA            91273
         L     R7,24(,R7)    LOAD ATTACH'S R1                    91273
         SH    R7,=Y(MTS@PARM-MTSWORK)  GET WORK AREA BACK       91273
         USING MTSWORK,R7    DECLARE IT                          91273
         LA    R8,20         SET TERMINAL DISCONNECTED           89328
         TM    MTSFLGT,MTSFGONE  SHUTTING DOWN ?                 89328
         BNZ   PUTLOUCH      YES; QUIT RAPIDLY                   89328
         LA    R8,12         SET PARAMETER LIST ERROR            89328
         L     R6,12(,R6)    GET CONTROL AREA                    89328
         L     R4,4(,R6)     GET BUFFER POINTER                  89328
         LH    R5,0(,R4)     GET LENGTH                          89328
         LA    R4,4(,R4)     POINT TO TEXT                       89328
         SH    R5,=H'4'      ALLOW FOR RDW LENGTH                89328
         BM    PUTLOUCH      SKIP                                89328
         SLR   R8,R8         SET FOR NO ERROR                    89328
         NI    MTSFLGM,MTSFATTN+MTSFREAD   RESET EXCEPT ATTN     91273
         AIF   (&WYLREL GE 9).PTLSK9A                            93132
         TM    2(R6),X'03'   ASIS, CONTROL OR FULL-SCREEN ?      89328
         BNZ   PUTLRCOM      YES; DON'T MOVE, DON'T ALTER        89328
.PTLSK9A LA    R14,MTWBUFF   GET OUTPUT BUFFER                   89325
*DONT    TM    MTSFLGM,MTSFREAD  LAST I/O WAS READ ?             89345
*NEED    BZ    PUTLNRED      NO                                  89345
*WITH    MVI   0(R14),X'15'   MAKE LEADING CR/LF                 91273
*WYLBUR  LA    R14,1(,R14)   AND BUMP START                      91273
PUTLNRED LR    R15,R5        COPY LENGTH                         89328
         LR    R0,R4         COPY INPUT BUFFER                   89328
         LR    R1,R5         AND LENGTH AGAIN                    89328
         MVCL  R14,R0        COPY TO NEW BUFFER                  89328
         LA    R4,MTWBUFF    SET NEW BUFFER START                89325
         MVI   0(R14),X'15'  MAKE CR/LF                          91273
         LA    R14,1(,R14)   SKIP OVER                           91273
         AIF   (&WYLREL LT 9).PTLSK9B                            93132
         TM    2(R6),X'02'   CONTROL OR FULL-SCREEN ?            93132
         BNZ   PUTLRICR      YES; DONE                           93132
         TM    2(R6),X'01'   AS-IS ?                             93132
         BNZ   PUTLRICS      YES; DON'T ALTER                    93132
.PTLSK9B TM    MTSTRMFG,MTSFAUCR  AUTO CR TERMINAL ?             89328
         BZ    PUTLRINL      NO                                  89328
         CH    R5,MTSCOLS    SAME AS TERMINAL WIDTH ?            89328
         BNE   PUTLRINL      NO; LF NECESSARY                    89328
         BCTR  R14,0         SET LENGTH FOR CR, NO NL            89328
         B     PUTLRICR      DON'T DELETE BOTH                   91091
PUTLRINL TM    MTSTRMFG,MTSFAULF  DOES CR INCLUDE LF FUNCTION ?  89328
         BZ    *+6           NO                                  89328
PUTLRICS BCTR  R14,0         SEND ONLY CR                        89328
PUTLRICR LR    R5,R14        GET LAST BYTE USED +1               89345
         SR    R5,R4         SET BUFFER LENGTH                   89345
PUTLRCOM DS    0H                                                91350
*** ASSUME SSPPIO AND V376IO X-ON CODE WAS CHANGED AS NEEDED     91350
*MODS*   AIF   (&WYLREL GE 7).NOVTXON                            91350
         LA    R14,0(R4,R5)  SPACE TO END                        91273
         BCTR  R14,0         BACKSPACE TO LAST BYTE              91273
         CLI   0(R14),X'14'  (NTO) X-ON ?                        91273
         BNE   PUTLRCON      NO                                  91273
         BCTR  R5,0          USE WYLBUR'S X-ON, IF ANY           91273
.NOVTXON SPACE 1                                                 91350
PUTLRCON L     R1,MTS@RTB    ADDRESS OUR RTB                     91273
         USING RTBSTART,R1                                       91273
         XC    RTBCCLR(RTBCCLRL),RTBCCLR  CLEAR FOR TEXT         91287
         AIF   ('&LOCAL' NE 'CCSI' OR &WYLREL GT 6).NOPCMFX      91300
         NI    RTBRF,255-RTBRFNID  RESET SUPPRESSION             91303
         NI    RTBMF2,255-RTBMFNLF  RESTORE LINE FEED            91303
         TM    2(R6),X'02'   CONTROL OR FULL-SCREEN ?            91303
         BZ    PUTNONTO      NO                                  91300
         OI    RTBRF,RTBRFNID  SUPPRESS IDLES                    91303
         OI    RTBMF2,RTBMFNLF  SUPPRESS LINE FEED AFTER INPUT   91303
         AGO   .DOPCMFX                                          91300
.NOPCMFX AIF   (&WYLREL LT 7).NOVTMPU                            91279
         TM    2(R6),X'02'   CONTROL OR FULL-SCREEN ?            91303
         BZ    PUTNONTO      NO; USE O.B.S. DEFAULTS             91279
         OI    RTBMF2,RTBMFIDL  SUPPRESS IDLES                   91279
         MVI   RTBIDLFT,0    BY ZEROING MULTIPLIER               91279
.DOPCMFX ANOP  ,                                                 91300
.NOVTMPU SPACE 1                                                 91307
PUTNONTO TM    MTSFLGT,MTSFCMD1  ONLY ONE COMMAND ?              91307
         BZ    PUTNOCMD      NO                                  91307
         TM    2(R6),X'02'   CONTROL/FULL-SCREEN ?               91307
         BNZ   PUTNOCMD      YES                                 91307
         TM    2(R6),X'01'   ASIS ?                              91307
         BNZ   PUTLOUCH      SKIP WRITING PROMPT                 91307
PUTNOCMD LA    R0,L'RTBTEXT  SET MAXIMUM OUTPUT LENGTH           91273
         MIN   (R5),(R0)                                         91273
         STH   R5,RTBWC      SET WRITE COUNT                     91273
         TM    2(R6),X'03'   PROMPT OR TRANSPARENT TEXT ?        91308
         BZ    PUTLNOPR      NEITHER                             91308
         STH   R5,RTBPC      SET NON-ZERO PROMPT COUNT           91308
PUTLNOPR BCTR  R5,0          FIX FOR EXECUTE                     91273
         EX    R5,PUTLMOVE   MOVE TEXT                           91273
         UNPOST MTSWECB      CLEAR RESPONSE ECB                  91273
         MVI   MTSWYLRQ,MRRCTWRI+1  SET FOR WRITE REQUEST        91273
         MODESET KEY=ZERO    SET TO ACCESS MAIN TASK ECB         91273
         L     R2,=A(MAINECB)  MAIN TASK ECB                     91273
         POST  (R2),0        GET MAIN TASK TO SEND               91273
         MODESET KEY=NZERO   RESTORE UNPRIVILEGED MODE           91273
         WAIT  ECBLIST=MTSELIST  WAIT FOR COMPLETION             91273
         SPACE 1                                                 91273
         TM    MTSCNECB,X'40'  CANCEL POSTED ?                   91273
         BZ    *+8           NO                                  91273
         OI    MTSFLGT,MTSFGONE  YES; SHUT DOWN                  91273
         TM    MTSFLGT,MTSFBUG  DEBUG MODE ?                     89330
         BZ    PUTLPOST      NO                                  89330
         MVC   MTSPVLIN(4),MTSPH137  SET PRINT LINE LENGTH       89330
         MVI   MTSPVLIN+4,C' '  CLEAR PRINT LINE                 89330
         MVC   MTSPVLIN+5(132),MTSPVLIN+4                        89330
         MVC   MTSPFLIN+1(5),=C'PUTLN'                           89330
         STM   R4,R5,MTSDDB  STASH ADDRESS/LENGTH                89330
         UNPK  MTSPFLIN+7(9),MTSDDB(5)                           89330
         UNPK  MTSPFLIN+16(9),MTSDDB+4(5)  LENGTH                89330
         UNPK  MTSPFLIN+25(5),RPLRTNCD-IFGRPL+MTSWRPL(3)  RET.CD 89330
         UNPK  MTSPFLIN+29(5),RPLFDBK2-IFGRPL+MTSWRPL(3)  FEEDBK 89330
         TR    MTSPFLIN+7(9+9+8),MDBHEX-C'0'  MAKE PRINTABLE     89330
         MVI   MTSPFLIN+15,C' '                                  89330
         MVI   MTSPFLIN+24,C' '                                  89330
         MVI   MTSPFLIN+33,C' '                                  89330
         LTR   R15,R5        CHECK LENGTH                        89330
         BNP   PUTLDEB0                                          89330
         MIN   R15,=Y(130-34),TYPE=H  NOT TOO LONG               89330
         BCTR  R15,0                                             89330
         EX    R15,PUTLMVC   MOVE TEXT                           89330
PUTLDEB0 LA    R1,MTSPVLIN   POINT TO OUTPUT                     89330
         L     R15,=A(XPRNT)                                     89330
         BALR  R14,R15       PRINT THE LINE                      89330
         LTR   R0,R5         COPY AND CHECK LENGTH               89330
         BNP   PUTLPOST                                          89330
         LR    R1,R4         COPY START ADDRESS                  89330
         L     R15,=A(HEXPRINT)                                  89330
         BALR  R14,R15       CALL THE HEX DUMP ROUTINE           89330
PUTLPOST TM    MTSWECB,X'7F' NORMAL RETURN ?                     89328
         BO    PUTLOUCH      YES                                 89328
         LA    R8,16         SET (INCORRECT) ERROR CODE          89328
         SPACE 1                                                 89328
PUTLOUCH L     R1,0(,R13)    GET OLD SAVE AREA (OUR STYLE)       89328
         ST    R8,16(,R1)    PASS RETURN CODE                    89328
         NI    MTSFLGM,255-MTSFREAD  RESET LAST I/O WAS READ     89345
         XEXIT MODE=OS       RETURN TO CALLER                    89328
PUTLMVC  MVC   MTSPFLIN+34(0),0(R4)  MOVE SOME TEXT              89330
PUTLMOVE MVC   RTBTEXT(0),0(R4)  MOVE TEXT                       91273
         EJECT ,                                                 89345
*        THIS EXIT IS INVOKED BY KERMIT PRIOR TO PARSING THE INPUT
*        BUFFER.  IT INTERCEPTS FUNCTIONS DONE HERE, AND SOME DONE IN
*        BOTH (E.G., SIGNOFF).                                   89345
*        ENTRY:  R0/R1  LENGTH/ADDRESS OF INPUT                  89345
*              (MOVED TO R5/R6 BY XHEAD MACRO)                   89345
*        RETURN: R15 =0 PROCESS  =4 SKIP AND READ AGAIN   =8 QUIT
*                                                                89345
COMMANDS XHEAD SAVE=RENT,EXH=NO,RPL=NO,MODE=OS                   89345
         L     R7,PSATOLD-PSA  GET CURRENT TCB                   89345
         L     R7,TCBFSA-TCB(,R7)  LOAD THE SAVE AREA            89345
         L     R7,24(,R7)    LOAD ATTACH'S R1                    89345
         SH    R7,=Y(MTS@PARM-MTSWORK)  GET WORK AREA BACK       89345
         USING MTSWORK,R7    DECLARE IT                          89345
         SWAPR R5,R6         EXCHANGE LENGTH AND ADDRESS         89345
         AR    R6,R5         ADD ADDRESS TO LENGTH               89345
         BCTR  R6,0          LAST BYTE                           89345
         TM    MTSFLGT,MTSFGONE+MTSFXEND+MTSFGOIN  QUIT ?        90231
         BNZ   COMMQUIT      YES                                 90231
         SLR   R8,R8         PRESET FOR KERMIT EXECUTION OF COMMAND
         LA    R4,COMMCMDS   GET CRT COMMAND TABLE               89345
         TM    MTSSIMFG,MTSSICRT  CRT MODE ?                     89328
         BNZ   *+8           YES                                 91273
         LA    R4,COMMCMTY   INCLUDE TTY COMMAND                 91273
&ZZ@BLUK SETB  0             REQUEST LOCAL EXPANSION OF 'BLOOK' CODE
         BLOOK T=(R4),B=(R12),R=R6  SCAN INPUT                   89345
         B     COMMOUCH      RETURN WITH R15=0 (PROCESS COMMAND) 89345
*    CHANGE OF PRINT STATUS REQUESTED                            91273
COMMQPRT TM    MTSSIMFG,MTSSINPR    PRINTER DISABLED ?           89328
         BNZ   COMMNPRT      YES; ISSUE MESSAGE                  89345
         BLOOK T=COMMCMPR,B=(R12),R=R6 FOR ON OR OFF             89345
         B     COMMOUCH      NEITHER ON NOR OFF - LET CALLER DO IT
COMMPRTR OI    MTSFLGT,MTSFBUG  SET TRACE MODE                   91273
COMMPRON CLI   MTSPRDD,C' '  ALLOCATION PERFORMED ?              87032
         BH    COMMPROQ      YES; SEE IF OPEN                    87032
         BAL   R9,SOUTALOC   ALLOCATE A SYSOUT DD (DDNM=>MTSPRDD)
         BXH   R15,R15,COMMNPRT  ALLOCATION FAILED               87032
         MVC   MTSPDCB+48(4),PATPDCB+48  OVERLAY XPRRET          87032
         MVC   DCBDDNAM+MTSPDCB-IHADCB,MTSPRDD  MAKE DCB DDNM    87032
         DEVTYPE MTSPRDD,MTSDDB  SEE IF EXHPRINT SUPPLIED        87032
         BXH   R15,R15,COMMNPRT  ALLOC OR PGM ERROR              87032
COMMPROQ TM    MTSSIMFG,MTSSINPR  PRINTER AVAILABLE ?            89328
         BNZ   COMMNPRT      NO                                  87032
         TM    MTSPDCB+DCBOFLGS-IHADCB,DCBOFOPN  OPEN NOW ?      87032
         BNZ   COMMPRQO      OK                                  87032
*        OPEN  (MTSPDCB,(OUTPUT)),MF=(E,MTSDDB)                  87032
         LA    R1,MTSPDCB     GET DCB ADDRESS                    87032
         ST@   R1,MTSDDB,MVI=X'8F'  MAKE OPEN,OUTPUT LIST        89275
         OPEN  ,MF=(E,MTSDDB)       OPEN IT                      87032
         TM    MTSPDCB+DCBOFLGS-IHADCB,DCBOFOPN  OPEN NOW ?      87032
         BNZ   COMMPRQO      OK                                  87032
COMMNPRT L     R1,=A(XPRRET)    MAKE DUMMY PUT ROUTINE ADDRESS   87032
         ST    R1,MTSPDCB+48      MAKE NOPS                      87032
         OI    MTSSIMFG,MTSSINPR  PRINT NO LONGER AVAILABLE      89328
         NI    MTSFLGT,255-MTSFBUG  RESET TRACE MODE             87032
         NI    MTSSIMFG,255-MTSSIPRT  CANCEL PRINT REQUEST       89328
         LA    R2,COMMNOMG   POINT TO MESSAGE                    91273
         B     COMMSKIP      ISSUE 'NO PRINTER' MESSAGE          91273
COMMPRQO OI    MTSSIMFG,MTSSIPRT    INDICATE PRINT REQUEST       89328
COMMSGOK LA    R2,COMMOKMG     GET OK MESSAGE                    91273
COMMSKIP LA    R8,4          SET FOR COMMAND PROCESSED HERE      89345
         XC    MTSUCBX(6*4),MTSUCBX  BUILD FAKE IOPL             89345
         ST    R2,MTSUCBX+5*4  MAKE LINE ADDRESS                 89345
         LA    R0,MTSUCBX+4*4  MAKE POINTER                      89345
         ST    R0,MTSUCBX+3*4  MAKE POINTER POINTER              89345
         LA    R1,MTSUCBX    POINT TO 'IOPL'                     89345
         L     R15,MTSPUTLN  GET PUT ADDRESS                     89345
         BALR  R14,R15       DISPLAY IT                          89345
         CH    R15,=H'4'     ERROR ?                             89345
         BH    COMMQUIT      YES; QUIT                           89345
         B     COMMOUCH      PUT IT OUT AND GET MORE INPUT       89345
COMMPROF NI    MTSSIMFG,255-MTSSIPRT    TURN PRINTING OFF        89328
         B     COMMSGOK      GO TO OK MESSAGE                    91273
COMMQTTY BLOOK T=COMMCMTS,B=(R12),R=R6   TTY SUB-COMMANDS        89345
         CLI   0(R5),C' '    ANY OPERAND ?                       91273
         BNE   COMMOUCH      YES; LET IT FAIL                    89345
         LA    R2,MSGTTYON                                       91273
         TM    MTSTRMFG,MTSFAUCR  BUT IS IT ?                    91273
         BNZ   COMMSKIP      YES; RETURN WITH MESSAGE            91273
         LA    R2,MSGTTYOF   AUTOCR OFF                          91273
         B     COMMSKIP      NOT ON; RETURN                      91273
COMMYAUT OI    MTSTRMFG,MTSFAUCR  SET AUTO CR                    91273
         B     COMMSGOK      SET OK                              91273
COMMYNAC NI    MTSTRMFG,255-MTSFAUCR  RESET AUTO CR              91273
         B     COMMSGOK                                          91273
COMMYAU2 OI    MTSTRMFG,MTSFAUCR  SET BOTH AUTO CR AND AUTO LF   91273
COMMYAUL OI    MTSTRMFG,MTSFAULF  SET AUTO LF                    91273
         B     COMMSGOK      SET OK                              91273
COMMYNAL NI    MTSTRMFG,255-MTSFAULF  RESET AUTO LF              91273
         B     COMMSGOK                                          91273
COMMYNAU NI    MTSTRMFG,255-MTSFAUCR-MTSFAULF  RESET ALL         91273
         B     COMMSGOK                                          91273
         SPACE 1                                                 91273
COMMDISC OI    MTSFLGT,MTSFGONE  PRETEND TERMINAL WENT BYE-BYE   91273
COMMLOFF XC    MTSSTADB,MTSSTADB  CLEAR FOR CLSDST               89345
         OI    MTSFLGT,MTSFXEND REQUEST TERMINATION              89345
COMMQUIT LA    R8,8          RETURN AND QUIT                     89345
         SPACE 1                                                 89345
COMMOUCH L     R1,0(,R13)    GET OLD SAVE AREA                   89345
         ST    R8,16(,R1)    SET R15 RETURN VALUE                89345
         XEXIT MODE=OS       RETURN TO KERMIT                    89345
         SPACE 1                                                 91273
COMMCMTY BTAB  'TTY',COMMQTTY,BASE=COMMANDS  TTY OPTIONS         89345
COMMCMDS BTAB  'LOGOFF',COMMDISC  TERMINATE SESSION              91273
         BTAB  'SIGNOFF',COMMDISC  TERMINATE CONNECTION          90008
         BTAB  'BYE',COMMDISC   TERMINATE CONNECTION             90008
         BTAB  'WYLBUR',COMMLOFF  RETURN TO WYLBUR               91273
         BTAB  'MILTEN',COMMLOFF                                 91273
         BTAB  'EXIT',COMMLOFF    ALTERNATE FORM                 91273
         BTAB  'PRINT ',COMMQPRT  PRINT REQUEST                  91273
         BTAB  'PRI ',COMMQPRT  PRINT REQUEST                    91273
         BTAB  *END                                              91273
COMMCMPR BTAB  'ON',COMMPRON  SET PRINT ON                       91273
         BTAB  'OFF',COMMPROF   SET OFF                          91273
         BTAB  'TRACE',COMMPRTR  TRACE MODE                      91273
         BTAB  *END                                              91273
COMMCMTS BTAB  'AUTOCR',COMMYAUT  SET AUTOCR                     91273
         BTAB  'AUTOLF',COMMYAUL  SET AUTOLF                     91273
         BTAB  'AUTO',COMMYAU2  BOTH                             91273
         BTAB  'NOAUTOCR',COMMYNAC  OFF                          91273
         BTAB  'NOAUTOLF',COMMYNAL  OFF                          91273
         BTAB  'NOAUTO',COMMYNAU                                 91273
         BTAB  *END                                              91273
COMMOKMG VCON  ' OK  '       PRINTER STARTED                     89345
COMMNOMG VCON  'No printer '  UNAVAILABLE MSG TEXT               89345
MSGTTYON VCON  'TTY AUTOCR set '                                 89345
MSGTTYOF VCON  'TTY no AUTOCR '                                  89345
         EJECT ,                                                 87165
         PUSH  USING                                             87165
         DROP  ,                                                 87165
         USING MTSWORK,R7                                        87165
HEXPRINT STM   R14,R12,MTSSAVER  SAVE CALLER'S REGISTERS         87165
         BALR  R12,0         MAKE LOCAL BASE                     87165
         USING *,R12                                             87165
         TM    MTSSIMFG,MTSSINPR  PRINTER AVAILABLE ?            89328
         BNZ   HEXPEXIT      NO                                  87165
         LTR   R3,R0         COPY AND TEST LENGTH                87165
         BNP   HEXPEXIT      HUH ?  QUIT                         87165
         MVC   MTSPVLIN(4),MTSPH137  SET PRINT LINE LENGTH       87165
         MVI   MTSPVLIN+4,C' '  CLEAR PRINT LINE                 87165
         MVC   MTSPVLIN+5(132),MTSPVLIN+4                        87165
         LA    R6,0(,R1)     COPY AND CLEAN START ADDRESS        87165
         LR    R10,R6        SAVE START ADDRESS                  87165
         AR    R3,R6         MAKE END ADDRESS+1                  87165
         MIN   R3,=X'01000000'  PREVENT OVERFLOW                 87165
HEXPLINE MVI   MTSPFLIN+98,C'*'                                  87165
         MVI   MTSPFLIN+131,C'*'   FRAME EBCDIC                  87165
         LR    R9,R3         GET END ADDRESS                     87165
         SR    R9,R6         LESS CURRENT START                  87165
         BNP   HEXPEXIT      ?                                   87165
         LA    R0,32         MAXIMUM ON ONE LINE                 87165
         CR    R9,R0         TOO MUCH ?                          87165
         BNH   *+6           NO                                  87165
         LR    R9,R0         SET SINGLE LINE SIZE                87165
         BCTR  R9,0          LESS ONE FOR EXECUTE                87165
         EX    R9,HEXPMVC    MOVE THE TEXT PORTION               87165
         LA    R2,MTSPFLIN+8                                     87165
         LR    R1,R6         GET ABSOLUTE ADDRESS                87165
         SR    R1,R10        MINUS START = RELATIVE ADDRESS      87165
         BAL   R9,HEXTR                                          87165
         LA    R2,MTSPFLIN+2                                     87165
         LR    R1,R6         ABSOLUTE ADDRESS                    87165
         BAL   R9,HEXTR                                          87165
         MVC   MTSPFLIN+2(2),MTSPFLIN+20                         87165
         MVC   MTSPFLIN+10(2),MTSPFLIN+20    CLEAR               87165
         LA    R2,MTSPFLIN+18   FIRST HEX OUTPUT ADDRSS          87165
         LR    R1,R6         START INPUT ADDRSS                  87165
         LA    R4,8          DO EIGHT WORDS                      87165
         SPACE 1                                                 87165
HEXPWORD BAL   R9,HEXTV      SHOW HEX                            87165
         LA    R2,10(,R2)                                        87165
         LA    R1,4(,R1)     NEXT WORD                           87165
         CR    R1,R3         DONE ?                              87165
         BNL   HEXPLPRT      YES, JUST PRINT THIS ONE            87165
         BCT   R4,HEXPWORD                                       87165
         SPACE 1                                                 87165
HEXPLPRT MVC   MTSPFLIN+2(53),MTSPFLIN+4  GAP BETWEEN 16 BYTE CHUNKS
         MVC   MTSPFLIN+55(2),MTSPFLIN BLANK THE GAP             87165
         LA    R1,MTSPVLIN                                       87165
         BAL   R14,XPRNT     PRINT THE LINE                      87165
         MVI   MTSPFLIN,C' '                                     87165
         MVC   MTSPFLIN+1(132),MTSPFLIN     CLEAR                87165
HEXPBUMP LA    R6,32(,R6)    NEXT LINE                           87165
         CR    R6,R3         END ?                               87165
         BL    HEXPLINE      DO ANOTHER LINE                     87165
HEXPEXIT LM    R14,R12,MTSSAVER  RELOAD CALLER'S REGISTERS       87165
         BR    R14           RETURN                              87165
         SPACE 1                                                 87165
HEXTR    ST    R1,MTSDDB                                         87165
         B     HEXT+6        SKIP MOVE                           87165
HEXT     MVC   MTSDDB(4),0(R1)   MOVE TO SAFE AREA               87165
         UNPK  MTSSTADB(9),MTSDDB(5)  NO 0CX ON LAST BYTE IN STORAGE
         TR    MTSSTADB(8),HEXTAB-C'0'                           87165
         MVC   0(8,R2),MTSSTADB                                  87165
         BR    R9                                                87165
HEXTV    LR    R15,R3        COPY END ADDRESS                    87165
         SR    R15,R1        LESS CURRENT START                  87165
         CH    R15,=H'4'     FULL WORD TO DO ?                   87165
         BNL   HEXT          YES; USE NORMAL FORMATTING          87165
         BCTR  R15,0         MAKE LENGTH FOR EXECUTE             87165
         LA    R14,1(R15,R15)  DITTO FOR OUTPUT                  87165
         EX    R15,HEXPMVC1  MOVE PARTIAL WORD                   87165
         UNPK  MTSSTADB(9),MTSDDB(5)  NO 0CX ON LAST BYTE IN STORAGE
         TR    MTSSTADB(8),HEXTAB-C'0'                           87165
         EX    R14,HEXPMVC2  MOVE PARTIAL WORD'S TEXT            87165
         BR    R9                                                87165
HEXPMVC  MVC   MTSPFLIN+99(0),0(R6)  MOVE EBCDIC TEXT            87165
HEXPMVC1 MVC   MTSDDB(0),0(R1)  MOVE PARTIAL WORD                87165
HEXPMVC2 MVC   0(0,R2),MTSSTADB  MOVE PARTIAL WORD'S TEXT BACK   87165
         POP   USING                                             87165
         EJECT ,                                                 91273
         PUSH  USING                                             91273
         DROP  ,                                                 88024
         USING MTSWORK,R7    PASSED BY CALLER                    88024
*        PRINT ROUTINE,  (1) HAS ADDRESS OF V FORMAT RECORD      91273
*                                                                91273
XPRNT    TM    MTSFLGT,MTSFGONE  SHUTTING DOWN ?                 87273
         BNZR  R14           YES; SKIP OUT                       87273
         TM    MTSPDCB+DCBOFLGS-IHADCB,DCBOFOPN  LOGIC ERROR ?   87273
         BZR   R14           THAT, OR SUBTASK WENT               87273
         STM   R0,R15,MTSSAVES                                   91273
         BALR  R12,0         MAKE NEW LOCAL BASE                 91273
         USING *,R12         AND DECLARE IT                      91273
         LR    R6,R1                                             91273
         USING VREC,R6                                           91273
         CLC   LEN,MTSPH137      LENGTH EXCEED MAX ?             91273
         BNH   *+10          NO, OK                              91273
         MVC   LEN,MTSPH137      SET TO MAX                      91273
         CLC   LEN,H6        SHORTER THAN MINIMUM ?              91273
         BNL   *+10          NO, OK                              91273
         MVC   LEN,H6        YES, USE MIN                        91273
         BE    *+16                                              91273
         LH    R2,LEN                                            91273
         SH    R2,H5                                             91273
         EX    R2,RECTR      MAKE PRINTABLE                      91273
         MVC   MTSPPRFX,LEN  SAVE ALL                            91273
XPRNTSB  LA    R3,XPCC       GET TABLE OF VALID CARRIAGE CONTROLS
         LA    R4,(XPCCE-XPCC)/2                                 91273
         SLR   R5,R5                                             91273
XPRCL    CLC   CC,0(R3)                                          91273
         BE    XPRCV         FOUND IT                            91273
         LA    R3,2(,R3)                                         91273
         BCT   R4,XPRCL                                          91273
         MVC   MTSPPRCC,0(R3)                                    91273
XPRCV    IC    R5,1(,R3)                                         91273
         TM    MTSSIMFG,MTSSINPR  PRIOR ABEND ?                  89328
         BNZ   XPREX         YES; QUIT                           88024
         LH    R3,MTSPLIN#                                       91273
         SR    R3,R5                                             91273
         STH   R3,MTSPLIN#                                       91273
         BP    XPRPUT                                            91273
         MVI   CC,C'-'       SKIP AFTER TITLE                    91273
         MVC   MTSPLIN#,XPRMAX  RESET THE LINE COUNT             91273
         AP    MTSPPAG#,P1                                       91273
         MVC   MTSPTIPG,PGMASK                                   91273
         ED    MTSPTIPG,MTSPPAG#                                 91273
         TIME  DEC                                               91273
         STM   R0,R1,MTSDDB  SAVE TIME/DATE                      91273
         MVC   MTSPTIDT-1(7),XPRDTM  MOVE DATE MASK              91273
         ED    MTSPTIDT-1(7),MTSDDB+5    EDIT THE DATE           91273
         MVC   MTSPTITM-1(9),XPRTMM   MOVE TIME MASK             91273
         ED    MTSPTITM-1(9),MTSDDB      EDIT THE TIME           91273
         TM    MTSPDCB+DCBOFLGS-IHADCB,DCBOFOPN  LOGIC ERROR ?   87273
         BZ    XPREX         SHUTTING DOWN ?                     87273
         PUT   MTSPDCB,MTSPTITL                                  91273
XPRPUT   TM    MTSSIMFG,MTSSINPR  ABENDED ?                      89328
         BNZ   XPREX         YES - GET OUT                       91273
         MVC   VFG,MTSPH137+2    ZERO FLAG FIELD                 91273
         LH    R1,MTSPPRFX   GET USER SPECIFIED LENGTH           91273
         LA    R2,VREC(R1)   POINT TO LAST BYTE + 1              91273
         SH    R1,H6         MAKE SURE TO LEAVE AT LEAST 6       91273
         BNP   XPRPUTS       NONE TO CHECK                       91273
XPRPUTL  BCTR  R2,0          POINT TO PRECEDING BYTE             91273
         CLI   0(R2),C' '    IS IT A TRAILING BLANK ?            91273
         BNH   XPRPUTS       NO, SCAN DONE                       91273
         BCT   R1,XPRPUTL    TRY THE ONE BEFORE                  91273
XPRPUTS  AH    R1,H6         USE AT LEAST 6 BYTES                91273
         STCM  R1,3,LEN                                          91273
         TM    MTSPDCB+DCBOFLGS-IHADCB,DCBOFOPN  LOGIC ERROR ?   87273
         BZ    XPREX         SHUTTING DOWN ?                     87273
         PUT   MTSPDCB,(R6)   PUT THE RECORD                     91273
         MVC   LEN(5),MTSPPRFX   RESTORE                         91273
         SPACE 1                                                 91273
XPREX    LM    R0,R15,MTSSAVES                                   91273
         BR    R14                                               91273
         SPACE 3                                                 91273
*        RESERVE SPACE ON A PAGE                                 91273
*                                                                91273
XPRES    LTR   R1,R1         IS THIS A VALID REQUEST ?           91273
         BNPR  R14           INVALID - RETURN                    91273
         LH    R15,MTSPLIN#  GET LINES LEFT ON PAGE              91273
         SR    R15,R1        GET REMAINDER AFTER THIS REQUEST    91273
         BNMR  R14           RETURN - FITS ON THIS PAGE          91273
         MVI   MTSPLIN#,X'FF'   ELSE FORCE A PAGE EJECT          91273
         BR    R14           RETURN                              91273
         SPACE 2                                                 91273
*        DATA                                                    91273
*                                                                91273
RECTR    TR    CC(0),PRTAB                                       91273
H5       DC    H'5'                                              91273
H6       DC    H'6'                                              91273
XPRMAX   DC    H'56'   ***** MAX DATA LINES PER PAGE *****       91273
XPRDTM   DC    X'4021204B202020'   JULIAN DATE MASK              91273
XPRTMM   DC    X'4021207A20207A2020'  TIME MASK                  91273
PRTAB    TRTAB CODE=PN,OPT=FOLD                                  91273
         SPACE 2                                                 91273
P1       DC    P'1'                                              91273
XPCC     DC    AL1(C' ',1,C'1',255,C'0',2,C'-',3,C'+',0)         91273
XPCCE    DC    AL1(C' ',1)   DEFAULT FOR BAD CODE                91273
PGMASK   DC    X'402020202120'                                   91273
         POP   USING         PUSHED PRIOR TO BUFPRINT            91273
         EJECT ,                                                 91273
XETXR    XHEAD RPL=NO,EXH=NO,SAVE=RENT                           91273
         MODESET KEY=ZERO    GET BACK INTO 'NORMAL' KEY          91273
         LA    R7,WORKQUE-(MTSQLINK-MTSWORK)  POINT TO QUEUE HEAD
         USING MTSWORK,R7                                        91273
XETXLOOP ICM   R7,15,MTSQLINK  GET NEXT WORK AREA                91273
         BZ    XETXEXIT      HUH ?                               91273
         C     R6,MTSTASK    SAME TCB ?                          91273
         BNE   XETXLOOP      NO; TRY NEXT ONE                    91273
         DETACH MTSTASK      DETACH IT                           91273
         XC    MTSTASK,MTSTASK  SIGNAL SUBTASK GONE              91273
         CSDEC MTV#CURS      DECREASE ACTIVE SESSION COUNT       91273
         LTR   R15,R15       ANY ACTIVE USERS LEFT ?             90231
         BP    XETXNZRO      YES                                 90231
         SERVICE CANON       ALLOW OPERATOR CANCEL AGAIN         90285
         TM    FLAGS,FGQUIT  SOFT SHUT-DOWN ?                    90231
         BZ    XETXNZRO      NO                                  90231
         OI    FLAGS,FGDOWN  SHOW GOING DOWN                     90231
XETXNZRO ICM   R0,15,MTSDDNIO  WAS FINAL DD FREED ?              90050
         BZ    XETXDECT      YES                                 90050
         SERVICE ALCFD,MTSDDNIO  REQUEST DE-ALLOCATION           90050
         BXLE  R15,R15,XETXDECT  WORKED                          90289
XETXDECT ICM   R0,15,MTSDDNCT  CONTROL FILE FREED ?              90050
         BZ    XETXDEAL      YES                                 90050
         SERVICE ALCFD,MTSDDNCT  FREE CONTROL FILE               90050
         BXLE  R15,R15,XETXDEAL  WORKED                          90289
XETXDEAL CLI   MTSUIF,C' '   USER LOGGED ON ?                    89337
         BNH   XETXNSMF      NO; OR ALREADY WROTE SMF            89337
         CLI   MTSRCDTY,0    INHIBIT SMF WRITE ?                 91273
         BE    XETXNSMF                                          91273
         OI    MTSSTI,X'02'  SHOW ABEND                          89337
         OI    MTSSTAT,X'80'   DITTO                             89337
         TIME  BIN                                               89337
         STCM  R0,15,MTSRCDTS  SET TIME OFF                      89337
         STCM  R1,15,MTSRCDTE  SET TIME OFF                      89337
         LA    R1,MTSSMF                                         89337
         SMFWTM (1)          WRITE THE RECORD                    89337
XETXNSMF XC    MTSUIF,MTSUIF SIGNAL RECORD WRITTEN               89337
*    NOTIFY MILTEN THAT TERMINAL IS GONE                         91273
         MVI   MTSWYLRQ,MRRCXCTL+1  RETURN VIA MAIN TASK         91273
         TM    MTSFLGT,MTSFGONE  USER WENT BYE-BYE ?             91273
         BZ    *+8           NO; UPLEVEL                         91273
         MVI   MTSWYLRQ,MRRCSOFF+1  REQUEST LOGOFF               91273
         L     R1,=A(MAINECB)                                    91273
         POST  (R1),0        POST MAIN TASK                      91273
         DROP  R7                                                91273
         SPACE 1                                                 91273
         XEXIT ,                                                 91273
         EJECT ,                                                 91273
         DROP  ,                                                 91273
         SPACE 2                                                 91273
*        DCB ABEND EXIT TO IGNORE TASK 013/B37 TYPE ABENDS       91273
*                                                                91273
         USING XPRABEND,R15                                      91273
XPRABEND MVI   3(R1),0       SET PROVISIONAL ABEND               91273
         LAT   R7,4(R1)      GET DCB ADDRESS FOR MTSPDCB         91273
         SH    R7,=AL2(MTSPDCB-MTSWORK) GET EXTENDED WORK AREA   91273
         USING MTSWORK,R7                                        91273
         OI    MTSSIMFG,MTSSINPR  PRINT NO LONGER AVAILABLE      89328
         NI    MTSSIMFG,255-MTSSIPRT  CANCEL PRINT REQUEST       89328
         CLI   MTSPMG14+25,C'X'  IS THIS AN ABEND RECURSION ?    91273
         BNER  R14           YES - JUST ABEND                    91273
         MVI   3(R1),4       ELSE SET IGNORE CODE                91273
         UNPK  MTSSTADB(7),0(4,R1)  UNPACK ABEND CODE            91273
         TR    MTSSTADB+0(6),HEXTAB-C'0'  MAKE EBCDIC            91273
         MVC   MTSPMG14+4+25(3),MTSSTADB    SET MAJOR ABEND CODE 91273
         MVC   MTSPMG14+4+29(2),MTSSTADB+4   AND SUB-CODE        91273
XPRABMSG WTO   MF=(E,MTSPMG14)   ABEND MESSAGE                   91273
         BR    R14           RETURN                              91273
         DROP  ,                                                 91273
         SPACE 2                                                 91273
         USING STABLE,R15                                        91273
STABLE   CH    R0,=H'12'     WAS A WORK/SAVE AREA GOTTEN ?       91273
         BL    STABOK        YES - CONTINUE                      91273
         SLR   R15,R15       IF NOT; LET TMP HANDLE IT           91273
         BR    R14           GO TO TMP STAI                      91273
STABOK   LR    R12,R15       MAKE NEW BASE REGISTER              91273
         DROP  ,                                                 91273
         USING STABLE,R12    DO DECLARE IT                       91273
         LR    R10,R14       SAVE RETURN ADDRESS, JUST IN CASE   91273
         LR    R6,R1         SAVE INPUT ADDRESS                  91273
         L     R7,0(,R1)     GET WORK AREA ADDRESS FROM STAE PARM
         USING MTSWORK,R7                                        91273
         CLC   =X'5220',5(R6)  CHECK SYSTEM ABEND CODE           91127
         BNE   STABN522      NOT 522                             91127
         MVC   MTSSTAVC(PAT522LN),PAT522MG  MOVE PATTERN         91127
         MVC   MTSSTAMG+33(8),MTSNIB+12  ADD TERM ID             91127
         WTO   MF=(E,MTSSTAVC)  SEND A MESSAGE                   91127
         B     STABEXIT                                          91127
         SPACE 1                                                 91127
STABN522 MVC   MTSSTAVC(PATSTALN),PATSTAMG  MOVE CANNED MESSAGE  91273
         UNPK  MTSSTAMG+59(9),8(5,R6)  CURRENT PSW 1             91273
         UNPK  MTSSTAMG+68(9),12(5,R6) CURR. PSW 2 / NOTE OVERLAP TO DB
         TR    MTSSTAMG+59(17),HEXTAB-C'0'  MAKE EBCDIC          91273
         MVI   MTSSTAMG+67,C' '  PUT SPACER BACK                 91273
         UNPK  MTSSTAMG+36(9),16(5,R6) PROG PSW 1                91273
         UNPK  MTSSTAMG+45(9),20(5,R6) PROG PSW 2                91273
         TR    MTSSTAMG+36(17),HEXTAB-C'0'                       91273
         MVI   MTSSTAMG+44,C' '  SEPARATE                        91273
         MVI   MTSSTAMG+53,C';'                                  91273
         MVC   MTSSTADB(2),5(R6)  MOVE COMPLETION CODE           91273
         NC    MTSSTADB(2),=X'FFF0'  MASK OUT SYSTEM CODE        91273
         BZ    STABUSER      USER CODE                           91273
         UNPK  MTSSTAMG+26(3),MTSSTADB(2)  UNPACK                91273
         OI    MTSSTAMG+28,C'0'  FORCE ZONE                      91273
         TR    MTSSTAMG+26(3),HEXTAB-C'0'  MAKE EBCDIC           91273
         MVI   MTSSTAMG+25,C'S'  IDENTIFY SYSTEM CODE            91273
         B     STABCOM       SKIP USER CODE                      91273
STABUSER LH    R0,6(,R6)     GET USER CODE                       91273
         CVD   R0,MTSSTADB   CONVERT IT                          91273
         OI    MTSSTADB+7,X'0F'  FORCE PLUS SIGN FOR NUMERIC ZONE
         UNPK  MTSSTAMG+25(5),MTSSTADB+5(3)                      91273
         MVI   MTSSTAMG+25,C'U'  IDENTIFY AS USER CODE           91273
         MVI   MTSSTAMG+30,C';'  SEPARATE                        91273
STABCOM  MVI   MTSSTAMG+76,C' '                                  89345
         MVC   MTSSTAMG+77(8),MTSNIB+12  COPY TERM-ID            89345
         L     R5,PSATOLD-PSA  GET THE CURRENT TCB               91273
         USING TCB,R5                                            91273
         LAT   R5,TCBRBP,STABPUT  GET RB CHAIN OR OUT            91273
         B     STABRB2       LOOK AT IT                          91273
         USING RBSECT,R5                                         91273
STABRB1  TM    RBSTAB2,RBTCBNXT  DOES RB POINT TO TCB ?          91273
         BNZ   STABPUT       YES - EXIT                          91273
         LAT   R5,RBLINK,STABPUT  GET NEXT RB                    91273
STABRB2  TM    RBSTAB1,RBFTP   IS THIS A PRB ?                   91273
         BNZ   STABRB1       NO; SKIP IT                         91273
         LAT   R1,RBCDE,STABRB1  GET CDE OR NEXT                 91273
         USING CDENTRY,R1                                        89328
         CLC   =C'KER',CDNAME  MY MODULE NAME PREFIX ?           89328
         BNE   STABRB1       NO; TRY AGAIN                       91273
         MVC   MTSSTAMG+8(8),CDNAME  MOVE NAME TO MESSAGE        91273
         DROP  R1,R5                                             91273
STABPUT  WTO   MF=(E,MTSSTAVC)  SEND MESSAGE                     91273
         MVC   MTSSTADB(7),=CL7'GR 0-7'  IDENTIFY FIRST REGISTER LINE
         MVI   MTSSTADB+43,C' '          GAP BETWEEN FOUR        91273
         LA    R5,2          SET FOR TWO LINES                   91273
STABRLP  LA    R4,MTSSTADB+7 SET FIRST OUTPUT ADDRESS            91273
         LA    R3,2          SET FOR TWO GROUPS OF FOUR          91273
STABRLP2 LA    R2,4          SET FOR FOUR REGISTERS              91273
STABRLP3 UNPK  0(9,R4),24(5,R6)  UNPACK A REGISTER               91273
         TR    0(8,R4),HEXTAB-C'0'     MAKE EBCDIC               91273
         MVI   8(R4),C' '    SEPARATOR                           91273
         LA    R4,9(,R4)     NEXT OUTPUT ADDRESS                 91273
         LA    R6,4(,R6)     NEXT REGISTER                       91273
         BCT   R2,STABRLP3   DO NEXT REGISTER                    91273
         LA    R4,1(,R4)     INTERGROUP GAP                      91273
         BCT   R3,STABRLP2   DO NEXT GROUP                       91273
         LA    R0,79+4                                           91273
         SLL   R0,16                                             91273
         STCM  R0,15,MTSSTADB-4                                  91273
         WTO   MF=(E,MTSSTADB-4)       GIVE TO USER              91273
         MVC   MTSSTADB+3(3),=C'8-F'   IDENTIFY SECOND LINE      91273
         BCT   R5,STABRLP    DO SECOND LINE                      91273
STABEXIT XC    MTSSTADB,MTSSTADB  CLEAR FOR CLSDST               87300
         LR    R14,R10       RELOAD EXIT ADDRESS                 91273
         LA    R15,16        IGNORE STAI TO FORCE DUMP           91273
         BR    R14           RETURN                              91273
         DROP  ,                                                 91273
         SPACE 1                                                 91273
PAT522MG VCON  'KER522E WYLKERM  TIME OUT ON TERMIDXX'           91127
PAT522LN EQU   *-PAT522MG                                        91127
PATSTAMG VCON   'KER513E WYLKERM  ABENDED SXXX; APSW=FF00CCCC FFAAAAAA;*
                PSW=FF00CCCC FFAAAAAA TERMIDXX'   STAE ABEND MESSAGE
PATSTALN EQU   *-PATSTAMG    LENGTH OF PATTERN                   91273
HEXTAB   DC    C'0123456789ABCDEF'  HEX CONVERSION TABLE         91273
         SPACE 2                                                 91273
         LTORG                                                   91273
         SPACE 1                                                 91273
FEAT3767 DC    A(CCWW70)                                         91273
         DC    AL2(1840,23,80,4,0)                               91273
         SCRN  (CHAR80,RESTKBY,RESTMDT),SBA,(1,1)                89328
CCWW70   CCW   X'05',0,X'20',0   ERASE/WRITE                     91273
         CCW   X'01',0,0,0   DUMMY                               91273
CCWW78   CCW   X'0D',0,X'20',0  ERASE/WRITE ALTERNATE            91273
         CCW   X'0D',0,0,0                                       91273
         SPACE 2                                                 91273
VREC     DSECT                                                   91273
LEN      DS    H                                                 91273
VFG      DS    H             PROC. FLAGS                         91273
CC       DS    C                                                 91273
TEXT     DS    CL132                                             91273
         SPACE 2                                                 91273
INSECT   DSECT ,             INPUT BUFFER MAPPING                91273
INPUT    DS    C             SINGLE INPUT CHARACTER AT A TIME    91273
         SPACE 1                                                 91273
OUTSECT  DSECT ,             OUTPUT BUFFER MAPPING               91273
OUTPUT   DS    C             SINGLE BYTE AT A TIME               91273
         EJECT ,                                                 91273
WYLKERM  CSECT ,                                                 89328
WORKBASE DS    0D            COMMON ADDRESSABILITY               91273
         USING WORKBASE,R11                                      91273
         DROP  ,                                                 91273
         USING WORKBASE,R11  DECLARE (CALLER'S) SECOND BASE      91273
         USING MTSWORK,R7                                        91273
         SPACE 1                                                 91273
*        DYNAMIC ALLOCATION ROUTINE FOR PRINT FILES              91273
*        RETURNS ON R9, ZEROES R15 IF OK, PUTS DDNM INTO MTSPRDD 91273
*                                                                91273
SOUTALOC MVC   MTSSTADB(PATALOLN),PATALOC  MOVE ALLOCATE PATTERN 91273
         LA    R14,DYNRB-DYNRBPTR+MTSSTADB                       91273
         STCM  R14,7,DYNRBPTR+1-DYNRBPTR+MTSSTADB                91273
         LA    R14,DYNTXPL-DYNRBPTR+MTSSTADB                     91273
         ST    R14,DYNTXTPP-DYNRBPTR+MTSSTADB                    91273
         LA    R14,TXDDN-DYNRBPTR+MTSSTADB                       91273
         ST    R14,DYNTXPL-DYNRBPTR+MTSSTADB                     91273
         LA    R1,DYNRBPTR-DYNRBPTR+MTSSTADB                     91273
         DYNALLOC ,          ALLOCATE IT                         91273
         BXH   R15,R15,0(R9)  RETURN WITH ERROR                  91273
         MVC   MTSPRDD,TXDDN+6-DYNRBPTR+MTSSTADB  RETURN DDN     91273
         XC    MTSSTADB,MTSSTADB  CLEAR FOR CLSDST               87300
         BR    R9            RETURN                              91273
TXSOUT   DC    Y(24,1,1),C'A'  READ-ONLY TEXT UNITS              91273
TXFREE   DC    Y(28,0)                                           91273
         SPACE 1                                                 91273
*        MOVED PATTERN                                           91273
PATALOC  DS    0A                                                91273
DYNRBPTR DC    X'80',AL3(DYNRB)  REQUEST POINTER                 91273
DYNRB    DC    AL1(20,S99VRBAL,S99NOCNV+S99NOMNT,0) +S99JBSYS FOR DEBUG
         DC    Y(0,0)        ERROR, INFO                         91273
DYNTXTPP DC    A(DYNTXPL)    TEXT UNIT LIST POINTER              91273
         DC    A(0,0)        RESERVED                            91273
DYNTXPL  DC    A(TXDDN,TXFREE),X'80',AL3(TXSOUT)  FREE=CLOSE     91273
TXDDN    DC    Y(85,1,8),CL8' '  RETURNED DDNAME                 91273
PATALOLN EQU   *-PATALOC     LENGTH TO MOVE                      91273
         SPACE 1                                                 91273
SCANPARM MVC   INITDDB,=CL8' '  PRE-BLANK RESULT                 91273
         LA    R15,INITDDB                                       91273
         LA    R0,L'INITDDB                                      91273
SCANPATB CLI   0(R6),C' '    LEADING BLANK ?                     91273
         BE    SCANPASB      YES; SKIP IT                        91273
         CLI   0(R6),C','    OR COMMA ?                          91273
         BNE   SCANPALS      NO; PROCESS                         91273
SCANPASB BXLE  R6,R4,SCANPATB  TRY FOR MORE                      91273
         BR    R14           NOTHING                             91273
SCANPALS ST    R6,INITR6     SAVE START OF SCAN                  87130
SCANPALM CLI   0(R6),C' '    TRAILING BLANK ?                    91273
         BER   R14           YES; RETURN                         91273
         CLI   0(R6),C','    OR COMMA ?                          91273
         BER   R14           YES                                 91273
         MVC   0(1,R15),0(R6)  COPY ONE BYTE                     91273
         AR    R15,R4        UP ONE                              91273
         BXH   R6,R4,0(R14)  RETURN IF EXHAUSTED                 91273
         BCT   R0,SCANPALM   ELSE TRY AGAIN                      91273
         BR    R14           ALL DONE                            91273
         SPACE 1                                                 91273
BADMIC   STM   R0,R15,BADMICSV                                   91273
         WTO   'KRM499E MIC CALL FAILED'                         91273
         ABEND 499,DUMP                                          91273
BADMICSV DC    15F'0'                                            91273
INITDDB  DC    D'0'          WORK AND PARSE AREA                 91273
INITR6   DC    A(0)          START OF PARAMETER                  87130
         LTORG ,                                                 91273
         SPACE 2                                                 91273
         PRINT GEN                                               91273
MTVECT   MAPMTV TYPE=        EXPAND BASIC CONTROL BLOCK          91273
@SCREENS DC    A(0)          ADDRESS OF LOADED @SCREENS RTNE     89334
@SERVICE DC    A(0)          ADDRESS OF @SERVICE IF SVC=0        90186
MYACB    DS    0F            NO ACB - WYLBUR APPLICATION         91273
MYAPPLID DS    0F            NO APPLID                           91273
@KERMIT  EQU   MTV@APPL,4,C'A'  RE-USE SPARE FIELD               91273
NUMBUSY  DC    F'0'          NUMBER OF REJECTED REQUESTS         91280
         PRINT NOGEN                                             91273
WYLKERCV DS    0D                                                91273
         SPACE 1                                                 91273
MICWORK  DC    XL(MICPLEN)'00'     MIC PARAMETER LIST            91273
QUEUEHED DC    A(0)          WORK REQUEST QUEUE HEAD             91273
NEXTWORK DC    A(0)          NEXT WORK ELEMENT TO BE DONE        91273
         SPACE 1                                                 91273
FROGFACE DC    CL8'FROGFACE' SUBTASK INITIALIZATION INTERFACE    91273
MDBHEX   DC    C'0123456789ABCDEF'                               91273
         SPACE 1                                                 91273
         PRINT GEN                                               91273
*        ALL TEXT FROM PATWORK THROUGH PATSIZE IS COPIED TO MTSWORK
*        AT MTSNIB.                                              91273
PATWORK  MAPMTS PFX=PAT,TYPE=,FLAVOR=KERMIT,EPLOC=FROGFACE       89328
PATSIZE  EQU   *-PATWORK     LENGTH TO COPY                      91273
         PRINT NOGEN                                             91273
         SPACE 1                                                 91273
PATPRT   DC    0A(0),X'0000FF88',X'00AF0000',X'00000000',X'00',C'PRT'
         DC    X'1250100B',A(PATPRTX,0,0,0,0,0)                  91273
PATPRTX  DC    5A(0),A(PATPRTD)                                  91273
PATPRTD  DC    2A(0)         DDT                                 91273
         SPACE 1                                                 91273
FREEQUE  DC    A(0,0)        QUEUE HEAD FOR FREE WORK AREAS      91273
WORKQUE  DC    A(0,0)        QUEUE HEAD FOR FULL FUNCTIONS       91273
MAXUSERS DC    Y(&MAXUSER)   MAXIMUM # OF WORK AREAS ALLOCATED   91273
MINUSERS DC    Y(&MINUSER)   # NOT FREEMAINED                    91273
         SPACE 1                                                 91273
FLAGS    DC    X'00'         GLOBAL FLAGS                        91273
FGDOWN   EQU   X'80'           SHUT-DOWN IN PROGRESS             91273
FGQUIT   EQU   X'40'           GRACEFUL SHUT-DOWN                90231
FGSYS    EQU   X'20'           RESTRICT TO SYSTEMS USERS         91273
FGLOGON  EQU   X'10'           REQUIRE LOGON                     87350
FGTEST   EQU   X'08'           TEST MODE (KERMNEW, NOT KERMNET)  91078
FGREAD   EQU   X'02'           RECEIVE ISSUED IF ON              91273
         SPACE 1                                                 91273
INSPFSOH DC    A(INSPFSO1,INSPFSO9)  SOH TYPES                   91273
INSPFSTX DC    A(INSPFST1,INSPFST9)  STX TYPES                   91273
         SPACE 1                                                 91273
INSPFSO1 DC    X'C1F2C2F3C3F4C4F5C5F6C6F7C7F8C8F9C97A'  A-I   2-10
         DC    X'817C82C183C284C385C486C587C688C789C8'  A-I L.C.12-20
         DC    X'D17BD27CD3C1D4C291C9924A934B944C7CF1E07B797B'   91273
         DC    X'D5C3D6C4D7C5D8C6D9C7E2C8E3C9E44AE54BE64C'  O-W  91273
INSPFSO9 DC    X'617B'       /                                   91273
INSPFST1 DC    X'C1F1C2F2C3F3C4F4C5F5C6F6C7F7C8F8C9F9'   A-I     91273
         DC    X'D17AD27BD37CD4C1D5C2D6C3D7C4D8C5D9C6'   J-R     91273
         DC    X'E2C7E3C8E4C9E54AE64BE74C'               S-X     91273
         DC    X'81F182F283F384F485F586F687F788F889F9'   A-I     91273
         DC    X'917A927B937C94C195C296C397C498C599C6'   J-R     91273
         DC    X'A2C7A3C8A4C9A54AA64B'                   S-W     91273
INSPFST9 DC    X'A74C'                                     X     91273
         SPACE 1                                                 91273
INPBUFF  DC    28CL132' '    SPACE FOR MODEL 5 + OVERHEAD        91273
INPBUFLN EQU   *-INPBUFF     SIZE OF BUFFER                      91273
         SPACE 2                                                 91273
         PRINT GEN                                               91273
         MAPMTS MODE=VTAM,FLAVOR=KERMIT    EXPAND A WORK AREA    89328
         PRINT NOGEN                                             91273
         SPACE 1                                                 91273
         ORG   MTSNIB                                            91273
MTS@RTCB DS    A             ADDRESS OF RTCB                     91273
MTS@RTB  DS    A             ADDRESS OF RTB                      91273
MTSWYLRQ DS    X             REQUEST FLAG                        91273
MTSRTBFL DS    XL3           PRESERVE WYLBUR'S RTB FLAGS         91273
         DS    2CL8          NAME/'RECORD'                       91273
MTWERRMG DS    CL42' '       LENGTH/MESSAGE TEXT FOR ERROR       91273
MTSFCMD1 EQU   X'02'           MTSFLGT - RETURN AFTER ONE CMD    91303
MTSFXON  EQU   X'04'           MTSFLGT - MUST SET CPA XON        91303
         ORG   MTSBUFF       REDEFINE SHORTED BUFFERS            91273
MTWBUFF  DS    CL(RTBNTEXT+1)' '  OUTPUT BUFFER+CR +++           91273
MTWBUFIN DS    CL(RTBNTEXT)  INPUT BUFFER                        91273
MTWWORKL EQU   *-MTSWORK     SHORTER GETMAIN                     91273
         SPACE 1                                                 91273
         ISTDNIB ,                                               91273
         SPACE 1                                                 91273
         IFGRPL AM=VTAM                                          91273
         SPACE 1                                                 91273
         PRINT NOGEN                                             91273
         ISTDBIND ,                                              91273
         SPACE 1                                                 91273
         CVT   DSECT=YES                                         91273
         EJECT ,                                                 OBS
         PRINT GEN                                               91273
         MICINTER ,                                              OBS
         EJECT ,                                                 OBS
RTCBMAP  DSECT ,                                                 OBS
         RTCB  ,                                                 OBS
         EJECT ,                                                 OBS
RTBMAP   DSECT ,                                                 OBS
         RTB   ,                                                 OBS
         EJECT ,                                                 OBS
TIBMAP   DSECT ,                                                 OBS
         TIB   ,                                                 OBS
         EJECT ,                                                 OBS
CVMAP    DSECT ,                                                 OBS
         MCV   ,                                                 OBS
         EJECT ,                                                 OBS
         PRINT NOGEN                                             OBS
NCEMAP   DSECT ,                                                 91273
         NCE   ,                                                 91273
         SPACE 1                                                 91273
         DCBD  DSORG=PS,DEVD=DA                                  91273
         IKJTCB ,                                                91273
         IHACDE ,                                                91273
         IHARB ,                                                 91273
         IEFUCBOB ,                                              91273
         IHAPSA ,                                                91273
         IEFZB4D0 ,                                              91273
         IEFZB4D2 ,                                              91273
         AIF   ('&LOCAL' NE 'PID').NOLOM                         93095
         CPA   ,                                                 91303
USERCVT  USERCVT ,                                               91273
USERTCB  USERTCB ,                                               91273
.NOLOM   END   ,                                                 91273
