@CAT     TITLE '@ C A T R E A D  ***  FAST OS CATALOG READ ROUTINE'
         COPY  OPTIONGB                                          82315
         SPACE 2                                                 82315
         SYSPARM LIST=YES                                        82315
         EJECT ,
*        THIS SUBROUTINE IS USED BY PROGRAMS DOING OS CATALOG READS
*          ON THE TTR LEVEL.  ALL CODE NOT FLAGGED WITH A CHANGE DATE
*          WAS TAKEN FROM THE 'COPYCAT' UTILITY PROGRAM DISTRIBUTED
*          VIA SHARE, INC.
*
*        COPYCAT AUTHOR : DOUGLAS E. ENGERT
*                       APPLIED MATHEMATICS DIVISION
*                       ARGONNE NATIONAL LABORATORY
*                          9700 SOUTH CASS AVENUE
*                         ARGONNE, ILLINOIS 60439
*
*   THIS 12-77 VERSION CONTAINS SEVERAL MINOR FIXES FROM ARGONNE
*   ("F00001" & "F00003") AND A MAJOR SPEED ENHANCEMENT TO THIS
*   ALREADY FAST PROGRAM IN THAT THE CATALOG I/O IS NOW DONE AT
*   EXCP LEVEL ("EXCP01").  A FIX FROM UCLA FOR A LITTLE MORE
*   SPEED YET IS ALSO INCLUDED ("CCN").  THE HARDCODED BLKSIZES
*   FOR SYSIN AND SYSPRINT HAVE BEEN CHANGED TO 3200 AND 605.
*                             F. PAJERSKI   NASA/GSFC   01DEC77
*
*        CHANGES/SUBROUTINE CONVERSION BY G. POSTPISCHIL
*        CALLING SEQUENCE :
*
*        R0=0  R1=> DDNAME OF CATALOG DD TO OPEN
*        R0=-1      CLOSE CURRENT CATALOG
*        R0=TTR0  R1=> OUTPUT BUFFER   READ BLOCK AT TTR
*
*        RETURN CODE 0 - REQUEST COMPLETED
*                    4 - UNSPECIFIED ERROR
*
         SPACE 1
@CATREAD START 0                                        ADDED ON 81120
FF       EQU   X'FF' -             ALL BITS ON
         SPACE 1
         YREGS ,                                                GP03129
         EJECT
         PRINT NOGEN                                             81120
         B     BEGIN-*(,R15)                                    GP12338
         DC    AL1(17),CL17'@CVLREAD &SYSDATE'                  GP12338
BEGIN    BSM   R14,0         PRESERVE USER'S AMODE              GP99026
         STM   R14,R12,12(R13)                                   81120
         LR    R12,R15                                           81120
         USING @CATREAD,R12                                     GP12338
         LA    R9,SAVEAREA                                       81120
         ST    R9,8(,R13)                                        81120
         ST    R13,4(,R9)                                        81120
         LR    R13,R9        SET NEW SAVE AREA                   81120
         AM24  WORK=R15      WE'RE OLD-FASHIONED                GP99026
         MVI   RETCODE,0     PROVISIONALLY SET GOOD RETURN CODE  81120
         SPACE 1                                                 81120
         LTR   R0,R0         TEST ENTRY CONDITION                81120
         BM    CATCLOSE      CLOSE CATALOG                       81120
         BP    CATREAD       READ A BLOCK                        81120
         TM    FLAGS,FGOPEN  ALREADY OPEN ?                      81120
         BNZ   EXIT16        YES; SET MAJOR ERROR                81120
         LTR   R3,R1         SAVE INPUT POINTER                  81120
         BZ    EXIT16        ERROR                               81120
         MVC   INDCB(INDCBE-INDCB),PATDCB MOVE PATTERN DCB       EXCP01
         LA    R2,INDCB                                          81120
         USING IHADCB,R2                                         81120
         MVC   DCBDDNAM(8),0(R3)  COPY USER'S DDNAME             81120
         MVC   INVOL,8(R3)   AND USER'S VOLSER                   81120
         DEVTYPE DCBDDNAM,DB  SEE IF DD CARD EXISTS              81120
         BXH   R15,R15,EXIT12  NO; ERROR                         81120
         CLI   DB+2,X'20'    DISK ?                              81120
         BNE   EXIT12        NO; SET ERROR                       81120
         MVI   JFCBDSNM,X'04'   MAKE FORMAT4 DSNAME              81120
         MVC   JFCBDSNM+1(43),JFCBDSNM                           81120
         OBTAIN DSCB1        READ THE VTOC                       81120
         LTR   R15,R15       SEE IF OK                           81120
         BZ    GOTDSCB4      YES                                 81120
         CH    R15,=H'8'     OTHER FORM OF OK ?                  81120
         BNE   EXIT12        NO; RETURN ERROR                    81120
GOTDSCB4 CLI   DS4IDFMT,C'4'  VTOC READ ?                        81120
         BNE   EXIT12                                            81120
         MVC   MAXRECTK+1(1),DS4DEVDB  COPY DIRECTORY BLOCKS     81120
         MVI   JFCBDSNM,C' ' FOR RDJFCB TEST                     \
         RDJFCB MF=(E,INDCBA)   GET THE JFCB                     81120
         CLC   =C'SYSCTLG',JFCBDSNM   SYSCTLG.Vvolser ?         GP12338
         BNE   EXIT12          NO; FAILURE                      GP12338
*NOT SET BXH   R15,R15,EXIT12    QUIT IF ERROR IN READ           81120
         OI    JFCBTSDM,JFCNWRIT+JFCNDCB+JFCNDSCB  NO MERGE/REWRITE
         OBTAIN DSCB1        GET THE DSCB                        81120
         BXLE  R15,R15,HAVE1                                    GP12338
         LA    R14,JFCBDSNM  IF THE USER SCREWED UP...           86195
         LA    R15,L'JFCBDSNM   AND DID NOT SPECIFY THE...       86195
         LA    R0,PATDCB-IHADCB+DCBDDNAM CORRECT DSNAME...       86195
         LA    R1,7                                              86195
         ICM   R1,8,PATDCB-IHADCB+DCBDDNAM+7  USE BLANK IN DDNM  86195
         MVCL  R14,R0        THEN FIX JFCB PROPERLY              86195
         OBTAIN DSCB1        GET THE DSCB                        81120
         BXH   R15,R15,EXIT12   QUIT IF ERROR                    81120
HAVE1    CLI   DS1FMTID,C'1'   READ ?                            81120
         BNE   EXIT12                                            81120
         CLI   DS1LSTAR+2,X'FF'  PROPER CATALOG INITIALIZATION DONE ?
         BNE   EXIT12        NO; QUIT                            81120
         OPEN  MF=(E,INDCBA),TYPE=J   OPEN IT                    86195
         TM    DCBOFLGS,DCBOFOPN   IS IT OPEN ?                  81120
         BZ    EXIT12        NO; QUIT                            81120
         OI    FLAGS,FGOPEN  SET OPEN                            81120
         LA    R6,=AL2(RDCCWE-RDCCW,RDCCW-RDCCWS) SIZES          EXCP01
         LA    R5,ECBLISTR         FIRST WRDSECT                 EXCP01
INE010   L     R4,0(,R5)                                         EXCP01
         USING WRDSECT,R4                                        EXCP01
         LH    R0,MAXRECTK         MAX REC/TRACK                 EXCP01
         MH    R0,0(,R6)           SIZE OF CCW                   EXCP01
         AH    R0,2(,R6)           +FIXED SIZE                   EXCP01
         ST    R0,GETMBUFL   SAVE BUFFER LENGTH FOR FREEMAIN     81120
         GETMAIN R,LV=(0)          GET CCW AREA                  EXCP01
         ST    R1,GETMBUFA   SAVE ADDRESS FOR FREEMAIN           81120
         ST    R1,IOBSTART         SAVE IN IOB                   EXCP01
         XC    0(16,R1),0(R1)      CLEAR FIRST 2 CCWS            EXCP01
         ST    R1,8(,R1) SAVE IN TIC                             EXCP01
         LA    R7,IOBMBBCC+3       CCHH ADDR                     EXCP01
         ST    R7,0(,R1)           SAVE IN CCW                   EXCP01
         OC    0(9,R1),=X'310000004000000508'  SEARCH AND TIC    EXCP01
         LA    R1,16(,R1)          POINT AT VARIABLE SEC         EXCP01
         LA    R8,1                                              EXCP01
         LH    R0,MAXRECTK                                       EXCP01
INE030   MVC   0(8,R1),=X'0E00000000000108' READ KEY+DATA,264    EXCP01
         LA    R1,8(,R1)           NEXT CCW                      EXCP01
         BCT   R0,INE030           LOOP FOR ALL CCWS             EXCP01
         SPACE 1                                                 81120
INE060   L     R0,MINSTOR          LOAD REQUIRED STORAGE         81131
         GETMAIN R,LV=(0)                                        EXCP01
         LR    R2,R1               SAVE FOR LATER                EXCP01
         LA    R3,RBUFSIZE         SIZE OF REC AREA              EXCP01
         MH    R3,MAXRECTK         *NUMBER                       EXCP01
         LA    R3,BUFSIZE(,R3)     +FIXED=BUFFER SIZE            EXCP01
         GETMAIN VC,LA=GETMLA,A=GETMA GET THE BUFFERS            EXCP01
         L     R0,MINSTOR                                        81131
         FREEMAIN R,LV=(0),A=(2)   FREE MIN FREE STORAGE         81131
         LM    R14,R15,GETMA                                     EXCP01
         XR    R0,R0                                             EXCP01
         XR    R1,R1                                             EXCP01
         MVCL  R14,R0        CLEAR THE BUFFER STORAGE            81120
         L     R1,GETMS            SIZE GOTTEN                   EXCP01
         XR    R0,R0                                             EXCP01
         ST    R0,USEBUFQ    RESET THE QUEUE HEADER              81120
         DR    R0,R3               /SIZE OF BUF=#OF BUFFS        EXCP01
         CH    R1,MINBUFFS         MIN # OF BUFFS                EXCP01
         BL    ERRCLOSE      ERROR 12 + CLOSE DCB                81120
         DROP  R2                                                81120
         LA    R5,FREEBUFQ                                       EXCP01
         L     R6,GETMA            POINT AT BUFFER AREA          EXCP01
INE080   ST    R6,0(,R5)           SAVE CHAIN ADDR               EXCP01
         LR    R5,R6                                             EXCP01
         AR    R6,R3               NEXT BUFFER                   EXCP01
         BCT   R1,INE080           LOOP FOR ALL                  EXCP01
         XC    0(4,R5),0(R5)       CLEAR LAST                    EXCP01
         LA    R0,256        GET FIRST TTR                       81120
         XR    R1,R1         SIGNAL TRACK READ ONLY              81120
         B     CATREAD       PRE-READ FIRST BLOCK AND TRACK      81120
         SPACE 2                                                 81120
CATCLOSE TM    FLAGS,FGOPEN  IS THIS THING OPEN ?                81120
         BZ    EXIT12        YES; RETURN ERROR                   81120
         CLOSE MF=(E,INDCBA)   CLOSE DCB                         81120
         LM    R1,R2,GETMBUFA  GET BUFFER ADDRESS/LENGTH         81120
         LTR   R1,R1         ANY ?                               81120
         BZ    CATCLOSB      NO                                  81120
         FREEMAIN R,LV=(R2),A=(1)  FREE IT                       81120
         XC    GETMBUFA(8),GETMBUFA                              81120
CATCLOSB LM    R1,R2,GETMA                                       81120
         LTR   R1,R1         ANY ?                               81120
         BZ    CATCLOSC      NO                                  81120
         FREEMAIN R,LV=(R2),A=(1)  FREE IT                       81120
         XC    GETMA(8),GETMA  CLEAR ADDRESS/SIZE                81120
CATCLOSC MVI   FLAGS,0       CLEAR PROCESSING FLAG               81120
         B     EXIT                                              81120
         SPACE 1                                                 81120
EXIT16   MVI   RETCODE,16                                        81120
         B     EXIT                                              81120
ERRCLOSE CLOSE MF=(E,INDCBA)                                     81120
         MVI   FLAGS,0                                           81120
EXIT12   MVI   RETCODE,12                                        81120
         B     EXIT                                              81120
EXIT8    MVI   RETCODE,8                                         81120
         B     EXIT                                              81120
EXIT4    MVI   RETCODE,4                                         81120
         SPACE 1                                                 81120
EXIT     L     R13,4(,R13)                                       81120
         LH    R15,RETHALF                                       81120
         L     R14,12(,R13)                                      81120
         LM    R0,R12,20(R13)                                    81120
         BSM   0,R14         RETURN                             GP99026
         EJECT
CATREAD  STM   R0,R1,CATCALL   SAVE CALL PARAMETERS              81120
         LA    R3,INDCB                                          81120
         USING IHADCB,R3                                         81120
         SRL   R0,16         MAKE TT                             81120
         LA    R6,USEBUFQ          CHAIN PTR FOR USED BUFFERS    EXCP01
         USING BUFFER,R6                                         EXCP01
RD010    LR    R5,R6                                             EXCP01
         ICM   R6,15,BUFNEXT   TEST FOR ANOTHER BUFFER           81120
         BZ    RD030         NONE; GET ONE THE HARD WAY          81120
         CH    R0,BUFTT            THIS TRACK?                   EXCP01
         BNE   RD010         NO; TRY NEXT BUFFER                 81120
         B     RD140               YES,FOUND IT                  EXCP01
         SPACE 1                                                 81120
RD030    BALS  R14,GETBUF                                        81120
         LA    R4,RECB             POINT AT ECB,IOB ETC          EXCP01
         USING WRDSECT,R4                                        EXCP01
         ST    R6,WRBUF            SAVE BUF ADDR                 EXCP01
         STH   R0,BUFTT            SAVE TT ADDRESS               EXCP01
         SLL   R0,16                                             EXCP01
         AL    R0,=X'00000100'     REC ONE                       EXCP01
         L     R1,DCBDEBAD         DEB                           EXCP01
         LA    R2,IOBMBBCC         SPOT FOR MBBCCHHR             EXCP01
         STM   R9,R13,READSAVE                                   EXCP01
         L     R9,16               CVT                           EXCP01
         L     R15,28(,R9)         CVTPCNVT                      EXCP01
         BALSR R14,R15                                           EXCP01
         PUSH  USING                                            GP97227
         DROP  ,                                                GP97227
         USING *,R14                                             EXCP01
         LM    R9,R13,READSAVE                                   EXCP01
         POP   USING                                            GP97227
         BXH   R15,R15,RD080   BAD TTR ?                         81120
         LH    R1,MAXRECTK                                       81120
         L     R5,IOBSTART         CCWS                          EXCP01
         USING RDCCWS,R5                                         EXCP01
         LA    R2,BUFEND+RBUFKEY-RBUFFER                         EXCP01
         B     RD050                                             EXCP01
RD040    LA    R2,RBUFEND-RBUFFER(,R2) NEXT IN BUFFER            EXCP01
         LA    R5,RDCCWE-RDCCW(,R5)                              EXCP01
RD050    ST    R2,RDCCW                                          EXCP01
         MVI   RDCCW,X'0E'         READ KEY + DATA               EXCP01
         OI    RDCCW+4,X'40'       COMMAND CHAIN                 EXCP01
         BCT   R1,RD040                                          EXCP01
         NI    RDCCW+4,FF-X'40'    OFF LAST COMMAND CHAIN        EXCP01
         ST    R3,IOBDCBPT                                       EXCP01
         EXCP  WRIOB         READ A TRACK                        EXCP01
         WAIT  ECB=WRECB     WAIT FOR IO                         81120
         CLI   WRECB,X'7F'         OK?                           EXCP01
         BE    RD090         YES                                 81120
RD080    MVI   RETCODE,8     SET RETURN CODE 8                   81120
         MVI   BUFTT,X'FF'   SET BAD ADDRESS                     81120
         MVI   BUFFLAG,0     FREE THE BUFFER                     81120
         B     RD195         AND PLACE ON FREE CHAIN             81120
         SPACE 1                                                 81120
RD090    ICM   R4,15,RDAREA                                      81120
         BZ    RD170               NO,BRANCH                     EXCP01
         LH    R1,MAXRECTK                                       81120
         LA    R5,BUFEND                                         EXCP01
         L     R2,DCBDEBAD         DEB                           EXCP01
         USING RBUFFER,R5                                        EXCP01
         MVI   BUFFLAG,X'00'                                     EXCP01
RD100    MVI   RBUFFLAG,X'00'      CLEAR FLAG                    EXCP01
         CLC   RBUFKEY,=XL8'00'    DUMMY REC?                    EXCP01
         BE    RD120               YES,BRANCH                    EXCP01
         OI    RBUFFLAG,BUFNRTR    NOT YET READ                  EXCP01
RD120    LA    R5,RBUFEND                                        EXCP01
         BCT   R1,RD100            LOOP ON ALL RECS              EXCP01
         B     RD145                                             EXCP01
RD140    MVC   BUFNEXT-BUFFER(,R5),BUFNEXT DECHAIN               EXCP01
RD145    MVC   BUFNEXT,USEBUFQ     CHAIN IN AS FIRST             EXCP01
         ST    R6,USEBUFQ                                        EXCP01
RD150    ICM   R4,15,RDAREA                                      81120
         BZ    RD170               JUST READ TRACK               EXCP01
         XR    R1,R1                                             EXCP01
         IC    R1,CATCALL+2   GET R                              81120
         BCTR  R1,0                -1                            EXCP01
         MH    R1,=AL2(RBUFSIZE)                                 EXCP01
         LA    R5,BUFEND(R1)       POINT AT PROPER REC           EXCP01
         NI    RBUFFLAG,FF-BUFNRTR SET READ ONCE                 EXCP01
         MVC   0(256,R4),RBUFDATA  MOVE DATA                     81120
RD170    LH    R1,MAXRECTK                                       81120
         LA    R5,BUFEND                                         EXCP01
         NI    BUFFLAG,FF-BUFRETN-BUFNRTR CLEAR BITS             81120
RD190    OC    BUFFLAG,RBUFFLAG                                  EXCP01
         LA    R5,RBUFEND                                        EXCP01
         BCT   R1,RD190                                          EXCP01
         TM    BUFFLAG,BUFRETN+BUFNRTR ALL DONE WITH BUF?        EXCP01
         BNZ   RD200               NO,BRANCH                     EXCP01
         LTR   R4,R4               JUST READ THE TRACK?          EXCP01
         BZ    RD200               YES,BRANCH                    EXCP01
         MVC   USEBUFQ,BUFNEXT     DECHAIN THIS BUF (FIRST)      EXCP01
RD195    MVC   BUFNEXT,FREEBUFQ  CHAIN ON FREE QUEUE             81120
         ST    R6,FREEBUFQ                                       81120
RD200    B     EXIT                                              81120
READSAVE DC    5A(0)                                             81120
CATCALL  DC    A(0)          1 OF 2 - CALL PARM (TTRF)           81120
RDAREA   DC    A(0) THEN READ TRACK,ELSE READ REC,IF <0 THEN RETAIN
         EJECT                                                   EXCP01
*----------------------------------------------------------------EXCP01
*              GETBUF - GET A BUFFER AND RETURN IN R6            EXCP01
*----------------------------------------------------------------EXCP01
GETBUF   ICM   R6,15,FREEBUFQ      POINT AT FREE BUFS            81120
         BZ    GB010         IF NONE FREE, GET USED ONE          81120
         MVC   FREEBUFQ,BUFNEXT   UNCHAIN THIS                   81120
         BR    R14           RETURN TO CALLER                    81120
         SPACE 1                                                 81120
GB010    LA    R6,USEBUFQ                                        81120
         XR    R0,R0         CLEAR FOR COMPARE                   81120
GB020    LR    R5,R6         SAVE PRIOR BUFFER ADDRESS           81120
         L     R6,BUFNEXT    GET NEXT BUFFER                     81120
         C     R0,BUFNEXT    LAST ON CHAIN ?                     81120
         BNE   GB020         NO; TRY AGAIN                       81120
         ST    R0,BUFNEXT-BUFFER(,R5)  TERMINATE CHAIN           81120
         ST    R0,BUFNEXT    JUST IN CASE - MAKE IT PRETTY       81120
         BR    R14                                               81120
         SPACE 2                                                 81120
DSCB1    CAMLST SEARCH,JFCBDSNM,INVOL,DS1FMTID                   81120
         EJECT
*---------------------------------------------------------------------*
*        CONSTANTS AND DATA AREA
*---------------------------------------------------------------------*
SAVEAREA DS    18F
         PRINT NOGEN
PATDCB   DCB   DDNAME=SYSCTLG,DSORG=PS,MACRF=(E),DEVD=DA,RECFM=F,      *
               EXLST=EXLST                                       86195
EXLST    DC    0A(0),X'87',AL3(JFCBDSNM)                         81120
FLAGS    DC    X'00'         PROCESSING FLAGS
FGOPEN   EQU   X'80'           CATALOG IS OPEN
RETHALF  DC    H'0'                RETURN CODE                   81120
RETCODE  EQU   RETHALF+1,1                                       81120
INDCBA   OPEN  (INDCB,INPUT),MF=L   DCB OPEN LIST                81120
MAXRECTK DC    H'0'                MAX REC/TRK ON ALL UNITS OPEN EXCP01
MINBUFFS DC    H'2'                MIN BUFFS=INDDS+OUTDDS+1      EXCP01
ECBLISTR DC    0A(0),X'80',AL3(RECB)  SHORT ECB LIST             81120
RECB     DC    F'0'                READ ECB                      EXCP01
RIOB     DC    X'C2000000',A(RECB),8F'0' IOB                     EXCP01
         DC    F'0'                                              EXCP01
FREEBUFQ DC    A(0)                FREE BUFFER QUEUE             EXCP01
USEBUFQ  DC    A(0)                IN USE BUFFER QUEUE           EXCP01
MINSTOR  DC    A(12*1024)          12K MIN FREE                  81131
GETMLA   DC    A(8),X'00FFFFF0'    GETMAIN LA LIST               EXCP01
GETMA    DC    A(0)                ADDR OF BUFS FROM GETMAIN     EXCP01
GETMS    DC    A(0)                SIZE OF BUFS FROM GETMAIN     EXCP01
GETMBUFA DC    A(0)          1 OF 2                              81120
GETMBUFL DC    A(0)          2 OF 2                              81120
         SPACE 1                                                 81120
         LTORG ,
         SPACE 2                                                 81120
         PRINT NOGEN                                             81120
DB       DS    0D                                                81120
         IEFJFCBN ,          EXPAND JFCB                         81120
         SPACE 1                                                 81120
         DS    0D                                                81120
         ORG   *-L'JFCBDSNM                                      81120
         IECSDSL1 1          EXPAND FORMAT 1 DSCB                81120
         SPACE 1                                                 81120
         ORG   DS1FMTID                                          81120
         IECSDSL1 4          EXPAND FORMAT 4 DSCB                81120
         ORG   ,                                                 81120
         DC    2D'0'         OVERFLOW FOR OBTAIN                 81120
         SPACE 2
         PRINT NOGEN
INDCB    DCB   DSORG=PS,DDNAME=X,MACRF=(E),DEVD=DA,RECFM=F       EXCP01
INDCBE   EQU   *
         PRINT GEN
INVOL    DS    CL6                 VOLUME THAT THIS DATA SET IS ON
         SPACE 2
*----------------------------------------------------------------EXCP01
*              BUFFER - TRACK BUFFER HEADER                      EXCP01
*----------------------------------------------------------------EXCP01
BUFFER   DSECT                                                   EXCP01
BUFNEXT  DS    A                   NEXT TRACK BUFFER             EXCP01
BUFTT    DS    H                   TT OF THACK                   EXCP01
BUFFLAG  DS    X                   FLAGS                         EXCP01
BUFRETN  EQU   X'80'               RETAIN THIS REC OF BUF        EXCP01
BUFNRTR  EQU   X'20'               INPUT REC NOT YET READ        EXCP01
         DS    X                                                 EXCP01
BUFEND   EQU   *                   END OF HEADER OF BUFFER       EXCP01
BUFSIZE  EQU   BUFEND-BUFFER                                     EXCP01
         SPACE 2                                                 EXCP01
*----------------------------------------------------------------EXCP01
*              RBUFFER - RECORD ENTRIES IN THE TRACK BUFFER      EXCP01
*----------------------------------------------------------------EXCP01
RBUFFER  DSECT                                                   EXCP01
RBUFFLAG DS    X                   FLAGS                         EXCP01
         DS    XL3                                               EXCP01
RBUFKEY  DS    XL8                 KEY                           EXCP01
RBUFDATA DS    XL256               DATA                          EXCP01
RBUFEND  EQU   *                   END OF THIS REC               EXCP01
RBUFSIZE EQU   RBUFEND-RBUFFER                                   EXCP01
         SPACE 2                                                 EXCP01
*----------------------------------------------------------------EXCP01
*              WORK AREA FOR EXCP READS AND WRITES               EXCP01
*----------------------------------------------------------------EXCP01
WRDSECT  DSECT                                                   EXCP01
WRECB    DS    F                   ECB                           EXCP01
WRIOB    DS    0XL40               IOB                           EXCP01
         DC    XL16'00'                                          EXCP01
IOBSTART DC    A(0) -              ADDR OF CHANNEL PROGRAM       EXCP01
IOBDCBPT DC    A(0) -              ADDR OF DCB FOR THIS IOB      EXCP01
         DC    XL8'00'                                           EXCP01
IOBMBBCC DS    XL8                 MBBCCHHR                      EXCP01
WRBUF    DS    F                   ADDR OF CURRENT BUFFER        EXCP01
WREND    EQU   *                                                 EXCP01
         SPACE  2                                                EXCP01
*----------------------------------------------------------------EXCP01
*              THE CCWS USED FOR READ                            EXCP01
*----------------------------------------------------------------EXCP01
RDCCWS   DSECT                                                   EXCP01
         CCW   X'31',IOBMBBCC+3,X'40',5 SEARCH ID EQ             EXCP01
         CCW   X'08',*-8,X'00',0       TIC                       EXCP01
RDCCW    CCW   X'0E',*-*,X'00',264     READ KEY + DATA           EXCP01
RDCCWE   EQU   *                   NEXT RDCCW                    EXCP01
         SPACE 2
         PRINT NOGEN
IHADCB   DCBD  DEVD=(DA),DSORG=(PS)                              EXCP01
         END
