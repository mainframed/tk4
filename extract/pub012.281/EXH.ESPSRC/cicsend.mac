CICND    TITLE 'C I C S E N D  ***  SEND COMMAND TO ALL CICS REGIONS'
         COPY  OPTIONGB                                          91030
         SPACE 1                                                 91030
         SYSPARM LIST=YES                                        91030
         SPACE 1                                                 91030
***********************************************************************
*                                                                     *
*        COPYRIGHT 1991  EXPERT SYSTEM PROGRAMMING, INC.              *
*        COPYRIGHT 2003  EXPERT SYSTEM PROGRAMMING                    *
*                        176 OLD STAGE COACH ROAD                     *
*                        BRADFORD, VT 05033-8844                      *
*                                                                     *
*                    ALL RIGHTS RESERVED                              *
*                                                                     *
*                                                                     *
*        DERIVATIVE WORK BASED ON MODULE EXHCCCIC IN EXHIBIT, AND     *
*              HOB.                                                   *
*                                                                     *
***********************************************************************
         SPACE 1                                                 91030
***********************************************************************
*                                                                     *
*        CICSEND ISSUES A COMMAND TO ALL IDENTIFIED CICS ADDRESS      *
*          SPACES.  THE COMMAND IS SUPPLIED IN THE PARM FIELD:        *
*                                                                     *
*        E.G.,   START CICSEND,PARM='CSMT OPEN,VTAM'                  *
*                                                                     *
*        TO MAKE LIFE EASIER FOR THE OPERATORS, THE PARM FIELD IS     *
*        TRANSLATED TO UPPER CASE BEFORE USE.                         *
*                                                                     *
*        ATTRIBUTES:  RENT, REFR, REUS, AC=1, PAGE                    *
*        REQUIRES @SERVICE AND @PRINTER SUBROUTINES                   *
*                                                                     *
***********************************************************************
         EJECT ,                                                 91030
         PRINT &PRTSOR                                           91030
CICSEND  SAVEM ZERO12,BASE=R12,PARM=R8,BNDRY=PAGE                91030
         SERVINIT ,          AIM TO SERVE                        91030
         SERVCALL LPALD,=CL8'@PRINTER'                           91030
         ST    R0,@PRINTER   LOAD THE PRINT SERVICE              91030
         PRTOPEN SYSPRINT,OPT=NOWTO  NO ERROR IF NO OUTPUT       91030
         PRTOPEN CONSOLE,OPT=NOWTO,DEV=2  ERROR MESSAGES         91030
         LA    R8,0(,R8)     CLEAN PARM POINTER ADDRESS          91030
         LTR   R8,R8         ANY ADDRESS ?                       91030
         BZ    NOPARM        NO                                  91030
         ICM   R8,OIII,1(R8)  LOAD THE POINTER ADDRESS           91030
         BZ    NOPARM                                            91030
         LH    R9,0(,R8)     LOAD TEXT LENGTH                    91030
         BNP   NOPARM                                            91030
         LA    R8,2(,R8)     POINT TO TEXT START                 91030
         LA    R0,COMTEXT    POINT TO COMMAND TEXT               91030
         LA    R1,L'COMTEXT                                      91030
         ICM   R9,IOOO,BLANKS  REQUEST BLANK FILL                91030
         MVCL  R0,R8         MOVE PARM TO TEXT FIELD             91030
         MVC   COMMAND(PATCOML),PATCOM  DO FIX-UP                91030
         OC    COMTEXT,BLANKS  MAKE UPPER CASE THE LAZY WAY      91030
         SPACE 1                                                 91030
         TESTAUTH FCTN=1     IS THE USER AUTHORIZED ?            91030
         BXH   R15,R15,FAILSRB  NO; TOO BAD                      91030
         SERVCALL ACGET,DB   GET THE USER'S PRIVILEGES           91030
         STC   R0,DB+8                                           91030
         TM    DB+8,VAASTC+VAASYS+VAASUP START TASK/SYSTEM/TECH SUP?
         BZ    FAILSRB       NO; UNAUTHORIZED USER               91030
         PRTV  COMMAND,CC=NO       SHOW THE COMMAND TO BE ISSUED 91030
         PRTSPACE 1          AND SPACE A BIT                     91030
         SPACE 1                                                 91030
         LH    R3,=Y(LENWORK) GET CSA GETMAIN SIZE               88025
         SERVCALL SCHIN,(R3)      GET SOME CSA SPACE             91030
         BXH   R15,R15,FAILSRB  TOO BAD                          88025
         SERVCALL SCHMV,CRXCODE,(R3)  MOVE CODE AND DATA TO CSA  91030
         BXH   R15,R15,FAILSRB  BUT HOW ?                        88025
         LR    R7,R0         LOAD CSA BASE                       91030
         USING CRXCODE,R7    DECLARE IT                          91030
         L     R4,CVTPTR     GET THE CVT                         91030
         L     R5,CVTASVT-CVTMAP(,R4)  GET THE ASCVT             91030
         USING ASVT,R5       DECLARE IT                          91030
         L     R6,ASVTMAXU   NUMBER OF ENTRIES TO DO             91030
         LA    R5,ASVTFRST   POINT TO FIRST ENTRY                91030
         DROP  R5                                                91030
NEXTASCB ICM   R4,15,0(R5)   TEST NEXT ENTRY                     91030
         BNP   BUMPASCB      NONE                                91030
         USING ASCB,R4       DECLARE ASCB ENTRY                  91030
         LH    R3,ASCBASID   LOAD ASCB ID                        91030
         SERVCALL GASID,(R3)  VALIDATE IT - PRIME SCHEDULE       91030
         BXH   R15,R15,BUMPASCB  GONE FISHING ?                  91030
         CR    R4,R0         SAME ASCB ?                         91030
         BNE   BUMPASCB      HUH ?                               91030
         LR    R9,R1         SAVE RETURN ADDRESS                 91030
         MODESET KEY=ZERO    GET PRIVIED                         91030
         MVC   HEADJOB(2*8),0(R9)  SAVE JOB AND STEPNAME         91030
         MVI   FUNRET,8      PRESET FOR UNEXECUTED CSA           91030
         XC    ADDRCSA,ADDRCSA  DON'T NEED ?                     91030
         MODESET KEY=NZERO   RESTORE THE STATUS QUO              91030
         ST    R3,ADDRASCB                                       91030
         PRTLIST MSGASCB     DO PRELIMINARY INFORMATION          91030
         O     R3,=X'80000000'  REQUEST 31-BIT MODE              93020
         SERVCALL SCHED,(R3) SCHEDULE THE REQUEST                91030
         BXH   R15,R15,TASKGONE TOO BAD                          91030
         CLI   FUNRET,4      TASK GONE ?                         88025
         BNL   TASKGONE      TOO BAD                             91030
         LM    R14,R15,HEADJOB  GET THE JOB NAME                 91030
         LA    R0,8-1        LOOP, BUT NOT TOO LONG              91030
JUSTLOOP CLM   R15,OOOI,BLANKS  TRAILING BLANK ?                 91030
         BNE   JUSTMAKE      NO; DONE                            91030
         SRDL  R14,8         SHIFT RIGHT                         91030
         ICM   R14,IOOO,BLANKS  INSERT LEADING BLANK             91030
         BCT   R0,JUSTLOOP   DO IT AGAIN                         91030
JUSTMAKE STCM  R14,IIII,COMJOB  MAKE JOB NAME                    91030
         STCM  R15,IIII,COMJOB+4  COMPLETE IT                    91030
         MODESET KEY=ZERO,MODE=SUP  GET PRIVILEGED               91030
         SLR   R0,R0         CONSOLE ID                          91030
         MGCR  COMMAND       ISSUE THE REQUEST                   91030
         MODESET KEY=NZERO,MODE=PROB  GET NORMAL                 91030
         INCH  NUMCOMM       COUNT COMMANDS ISSUED               91030
         PRTLIST MSGNORM                                         91030
         B     BUMPASCB                                          91030
TASKGONE PRTLIST MSGNOCSA                                        91030
         B     BUMPASCB                                          91030
         SPACE 1                                                 91030
BUMPASCB LA    R5,4(,R5)     NEXT ASCVT ENTRY                    91030
         BCT   R6,NEXTASCB   TRY IT                              91030
         SERVCALL SCHFR      FREE THE CSA                        91030
         PRTLIST MSGEND,DEV=(1,2)                                91030
         SPACE 1                                                 91030
         SERVTERM ,          CLOSE AND FREE EVERYTHING           91030
         LH    R9,RETCH      GET RETURN CODE                     91030
         ENDM  RC=(R9)       RETURN WITH CODE                    91030
         SPACE 1                                                 91030
FAILSRB  PRTL  ' ***** SCHEDULE INTERFACE FAILED *****',DEV=(1,2)
         PRTCLOSE ,          CLOSE PRINTER ONLY                  91030
         ABEND 047,DUMP      QUIT NASTILY                        91030
         SPACE 1                                                 91030
NOPARM   PRTL ' ***** PARM FIELD REQUIRED, BUT MISSING *****',DEV=(1,2)
         SERVTERM ,          CLOSE                               91030
         ABEND 035,DUMP      NO DUMP                             91030
         SPACE 1                                                 91030
SYSPRINT PRTWORK SYSPRINT,TITLE=5                                91030
CONSOLE  PRTWORK *USER       ERROR MESSAGES ONLY (IF NO SYSPRINT)
         SPACE 1                                                 91030
PATCOM   DC    Y(COMMANDL,0),C'F JOB-NAME,'                      91030
PATCOML  EQU   *-PATCOM      LENGTH OF PATTERN                   91030
BLANKS   DC    CL101' '      LAZY OR                             91030
         SPACE 1                                                 91030
MSGASCB  FD    'ASID=',NL                                        91030
         FD    ASCBASID,I,LEN=6                                  91030
         FD    ' ASCB=',PADL                                     91030
         FD    ADDRASCB,HEX                                      91030
         FD    ' JOB ',PAD                                       91030
         FD    HEADJOB                                           91030
         FD    HEADJOB+8,L'HEADJOB,PAD,DEB,LEN=8                 91030
         FD    *END                                              91030
         SPACE 1                                                 91030
MSGNOCSA FD    '  NO CSA FOUND'                                  91030
         FD    *END                                              91030
         SPACE 1                                                 91030
MSGNORM  FD    '  CSA='                                          91030
         FD    ADDRCSA,HEX                                       91030
         FD    '  COMMAND ISSUED'                                91030
         FD    *END                                              91030
         SPACE 1                                                 91030
MSGEND   FDCLC NUMCOMM,ZEROES,2,BE=MSGENDNO                      91030
         FD    NUMCOMM,I,RADJ,NL,LEN=6                           91030
         FDGOTO MSGENDCM                                         91030
MSGENDNO FD    '***** NO '                                       91030
MSGENDCM FD    'CICS REGION(S) PROCESSED',PAD                    91030
         FD    *END                                              91030
         SPACE 1                                                 91030
         LTORG ,                                                 91030
         SPACE 1                                                 91030
SAVE     DSECT ,                                                 91030
DB       DS    2D            WORK WORDS                          91030
ZEROES   DS    A                                                 91030
@SERVICE DS    A             @SERVICE POINTER                    91030
@PRINTER DS    A             @PRINTER POINTER                    91030
ADDRASCB DS    A             CURRENT ASCB ADDRESS                91030
NUMCOMM  DS    H             NUMBER OF COMMANDS ISSUED           91030
RETCH    DS    H             RETURN CODE                         91030
COMMAND  DS    Y(COMMANDL,0),C'F '                               91030
COMJOB   DS    CL8' ',C','   TARGET JOB NAME                     91030
COMTEXT  DS    CL101' '      COMMAND TEXT; L=1+MAX PARM          91030
COMMANDL EQU   *-COMMAND                                         91030
SAVEND   EQU   *                                                 91030
         TITLE 'C I C S E N D  ***  SRB CODE - DATA COLLECTION'  91030
CICSEND  CSECT ,
         CRMHEAD ,           DECLARE CSA CODE                    88025
         LR    R11,R14       SAVE RETURN ADDRESS                 88025
         L     R2,PSAAOLD-PSA  GET CURRENT ASCB                  88025
         L     R2,ASCBRCTP-ASCB(,R2)  GET TOP TCB                88025
         USING TCB,R2                                            91030
CTCBLOOP LAT   R2,TCBTCB,FINDFAIX,LA=34  NEXT TCB OR ERROR MESSAGE
         C     R2,TCBJSTCB   IS THIS A JOB-STEP TCB ?            91030
         BNE   CTCBLOOP      NO; IGNORE                          91030
         LAT   R1,TCBTIO,CTCBLOOP,LA=34   SKIP IF NO TIOT        93020
         CRMSPIE CTCBLOOP    RECOVER FROM BAD CONTROL BLOCK      88025
         CLC   0(8,R1),HEADJOB  LOW ENOUGH ON CHAIN YET ?
         BNE   CTCBLOOP
         TM    TCBFLGS1,TCBFA+TCBFE  ENDING ?
         BNZ   CTCBLOOP
         CLC   =C'STARTING',16(R1)   IS IT START TASK ?
         BE    CTCBLOOP      YES, SKIP IT
         AIF   (&MVSSP).AFCX                                     91030
         LAT   R4,TCBFSA,CTCBLOOP,LA=0
         L     R7,TCBGRS13   PROVISIONALLY LOAD R13 POINTER
HFNDRB   L     R1,TCBRBP .   GET TOP RB
         USING RBSECT,R1                                         91030
HNXTRB   TM    RBSTAB1,RBFTP WHAT RB TYPE ?
         BZ    HNXTRBN .     PRB, CHECK IF TOP AND CICS
         L     R7,RBGRS13    LOAD R13 VALUE
HNXTRBI  TM    RBSTAB2,RBTCBNXT LAST RB ?
         BNZ   CTCBLOOP      YES; TRY ANOTHER TCB
HNXTRBX  LAT   R1,RBLINK,CTCBLOOP NEXT RB OR ANOTHER TCB         88025
         B     HNXTRB .      CHECK IT
HNXTRBN  TM    RBSTAB2,RBTCBNXT  OLDEST PRB ?
         BZ    HNXTRBX       NO
         LAT   R15,RBCDE,CTCBLOOP GET CDE OR SKIP TCB
         USING CDENTRY,R15                                       91030
         CLC   =CL8'DFHSIP',CDNAME  NEW-FANGLED NAME ?           88025
         BE    HNXTUSE       YES; USE TCB REGS                   88025
         CLC   =CL8'DFHDCSA',CDNAME   CICS TOP MODULE NAME ?
         BNE   CTCBLOOP      NO; SKIP
HNXTUSE  LA    R7,0(,R7)
         LR    R1,R7
         SRL   R1,2
         SLL   R1,2
         CR    R1,R7         WORD BOUNDARY ?
         BNE   LOCALT        NO                                  81056
         C     R4,4(,R7)     BACK POINTER ?
         BE    LOCOUT        YES; FOUND                          88025
LOCALT   L     R7,8(,R4)     TCBFSA => USER SAVE AREA            81056
         LA    R7,0(,R7)                                         81056
         SH    R7,LOCH8+2    BUMP BACK                           81056
         BNP   CTCBLOOP      TOO LOW                             81056
         CLC   =C'STORAGE',0(R7)  CSA PREFIX ?                   81056
         BNE   CTCBLOOP      NO                                  81056
LOCH8    LA    R7,8(R7,0)    USE IT                              81056
         B     LOCOUT        TRY IT                              81056
         AGO   .COMDROP                                          91030
.AFCX    LAT   R15,TCBEXT2,CTCBLOOP,LA=34Z  EXTENSION ?          93020
         USING TCBXTNT2,R15                                      91030
         LAT   R1,TCBCAUF,CTCBLOOP,LA=34  EXTENSION PRESENT ?    93020
         USING AFCX,R1                                           91030
         CLC   =C'AFCX',AFCXID  CORRECT BLOCK ?                  91030
         BNE   CTCBLOOP      NO                                  91030
         LAT   R7,AFCXCSA,,LOCOUT,LA=34  CONTINUE IF PRESENT     93020
.COMDROP DROP  R15,R1,R2                                         88025
FINDFAIX CRMSPIE 0           RESET EXIT                          88025
         MVI   FUNRET,4      SHOW CSA NOT FOUND                  88025
         BR    R11           RETURN TO INTERFACE CODE            88025
         SPACE 1                                                 88025
LOCOUT   CRMSPIE 0           NO ERROR RECOVERY FROM HERE ON      88025
         ST    R7,ADDRCSA    SAVE CSA ADDRESS                    88025
PROCEXIT MVI   FUNRET,0      CLEAR THE RETURN CODE               88025
         BR    R11           RETURN TO CONTROL CODE              88025
         SPACE 1                                                 88025
         LTORG ,                                                 88025
         SPACE 2
ADDRCSA  DC    A(0)          CSA ADDRESS                         88025
HEADJOB  DC    2CL8' '       REQUESTED JOB'S NAME AND STEP       91030
FLAG     DC    X'00'         FLAG BITS
FUNRET   DC    X'00'         LOGICAL RETURN CODE                 88025
         SPACE 1                                                 88025
         ORG   ,                                                 88025
LENWORK  EQU   *-CRXCODE     SIZE OF WORK AREA                   88025
         SPACE 2
         PRINT &PRTSYS                                           88025
         IHAPSA ,                                                88025
         IHAASCB ,                                               88025
         IHAASVT ,                                               91030
         CVT   DSECT=YES                                         91030
         IKJRB ,                                                 91030
         IKJTCB ,                                                91030
         IHACDE ,                                                91030
         SPACE 1                                                 91030
AFCX     DSECT ,             LOCAL AFCX EXPANSION                91030
AFCXID   DC    C'AFCX'                                           91030
         DS    2H            ?                                   91030
AFCXCSA  DS    A             CSA ADDRESS                         91030
AFCXPGM  DS    A             CODE                                91030
         END
