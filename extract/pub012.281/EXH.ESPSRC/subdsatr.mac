SUBDSATR TITLE 'S U B D S A T R  ***  FORMAT DCB/DSCB ATTRIBUTES'
         COPY  OPTIONGB
         SPACE 1
         SYSPARM LIST=YES
         SPACE 2
*    FORMAT DSORG/OPTCD/RECFM/LRECL/BLKSIZE
*    INPUT R0 POINTS TO JFCB OR DSCB1 DSORG FIELD
*    USER'S R1 POINTS TO OUTPUT FIELD :
*      CL3 DSORG CL6 RECFM CL8 OPTCD CL5 BLKL CL5 LRECL
         SPACE 1
SUBDSATR START 0
         USING SUBDSATR,R15  SET BY CALLER
         B     BEGIN
         DC    AL1(BEGIN-*),C'SUBDSATR &SYSDATE'
BEGIN    STM   R14,R8,12(R13)
         LTR   R7,R0         ANY INPUT?
         BZR   R14           NO; TOO BAD - RETURN R15 NON-ZERO
         LTR   R8,R1         ANY OUTPUT?
         BZR   R14           NO; TOO BAD - RETURN R15 NON-ZERO
         LA    R6,10*4+20(,R13)  USE SAVE AREA FOR WORK
         N     R6,=X'7FFFFFF8'   USE DOUBLE-WORD BOUNDARY
         USING WORKSECT,R6   RE-USE SAVE AREA
         USING FMTDSECT,R8   USER'S OUTPUT AREA
         MVI   FMTDSORG,C' '
         MVC   FMTDSORG+1(FMTORGLN-1),FMTDSORG  CLEAR RETURN AREA
         USING DS1DSORG,R7   USE COMMON DSCB1/JFCB MAPPING
         SPACE 1
         SR    R0,R0
         MVI   FMTLRECL+L'FMTLRECL-1,C'X'  PRESET FOR X
         ICM   R0,3,DS1LRECL   GET THE RECORD LENGTH
         BM    DSATRRLN      SKIP LRECL=X
         CVD   R0,DB         MAKE PACKED
         MVC   FMTLRECL(5),=X'2020202120'  MAKE EDIT MASK
         ED    FMTLRECL-1(6),DB+5  EDIT RECORD LENGTH
         SPACE 1
DSATRRLN ICM   R0,3,DS1BLKL  GET BLOCK SIZE
         CVD   R0,DB
         MVC   FMTBLKL(5),=X'2020202120'  MAKE EDIT MASK
         ED    FMTBLKL-1(6),DB+5
         SPACE 1
         LA    R1,PATDSORG   POINT TO LIST OF DSORGS
         LA    R2,16         LOOP THROUGH 16 BITS
         SR    R3,R3
*  LOADING BACKWARDS, SO 'U' ATTRIBUTE LAST
         ICM   R3,2,DS1DSORG     FIRST DSORG SECOND
         ICM   R3,4,DS1DSORG+1   SECOND DSORG FIRST
         SLL   R3,7          ADJUST FOR BXLE
         OR    R3,R2         MAKE NON-ZERO LOW BYTE
         LA    R4,FMTDSORG   SET OUTPUT FIELD
         MVI   FMTDSORG,C'?'   PRESET IF ALL BITS ARE OFF
DSATROGL BXH   R3,R3,DSATROGN  SKIP IF BIT OFF
         MVC   0(2,R4),0(R1)   MOVE ABBREVIATION
         LA    R4,FMTDSORG+2   SET SECOND POSITION (TWO BITS AT MOST?)
DSATROGN LA    R1,2(,R1)     SKIP TO NEXT ABBREVIATION
         BCT   R2,DSATROGL   TRY AGAIN
         SPACE 1
         LA    R4,FMTRECFM   POINT TO OUTPUT FIELD
         TM    DS1DSORG+1,DS1DSGTQ  TCAM MESSY QUEUE ?           83100
         BZ    *+10          NO                                  83100
         MVC   DB+3(2),PATRECMQ  YES; ADJUST RECFM               83100
         MVI   FMTRECFM,C'?'  PRESET IF NOTHING FOUND
         MVC   DB(6),PATRECFM+4  COPY MINOR OPTIONS
         SR    R1,R1
         IC    R1,DS1RECFM   GET RECORD FORMAT
         SRA   R1,6          ISOLATE MAJOR TYPE
         BP    DSATRFMD      OK
         MVI   DB,C'D'       CHANGE FOR POSSIBLE D FORMAT (?)
         B     DSATRFMB
DSATRFMD LA    R1,PATRECFM(R1)  POINT TO MAJOR TYPE
         MVC   0(1,R4),0(R1)  MOVE IT
         LA    R4,1(,R4)     UP OUTPUT ADDRESS
DSATRFMB IC    R1,DS1RECFM   GET FULL FORMAT AGAIN
         LA    R2,5          SET FOR 5 ADDITIONAL OPTIONS
         LA    R3,DB         POINT TO ABBREVIATIONS
         SLL   R1,25         ADJUST FOR BXLE
         OR    R1,R2         MAKE LOW NON-ZERO
DSATRFML BXH   R1,R1,DSATRFMN
         MVC   0(1,R4),0(R3)  MOVE ABBREVIATION
         LA    R4,1(,R4)
DSATRFMN LA    R3,1(,R3)
         BCT   R2,DSATRFML   DO ALL
         SPACE 1
         LA    R3,PATOPTPS   SET QSAM/BSAM COMMON
         TM    DS1DSORG,DS1DSGPO+DS1DSGPS  PS/PO ?
         BNZ   DSATROPT      YES; USE IT
         LA    R3,PATOPTDA   SET DA
         TM    DS1DSORG,DS1DSGDA  IS IT BDAM ?
         BNZ   DSATROPT      YES; USE IT
         LA    R3,PATOPTIS   SET ISAM
         TM    DS1DSORG,DS1DSGIS  IS IT ISAM ?
         BZ    DSATREX       NO; DO COMMON EXIT CODE
DSATROPT LA    R4,FMTOPTCD   POINT TO OUTPUT FIELD
         SR    R1,R1
         IC    R1,DS1OPTCD   GET OPTIONS
         LA    R2,8          SET FOR EIGHT OPTIONS
         SLL   R1,23         ADJUST FOR BXLE
         OR    R1,R2         MAKE LOW NON-ZERO
DSATROPL BXH   R1,R1,DSATROPN
         MVC   0(1,R4),0(R3)  MOVE ABBREVIATION
         LA    R4,1(,R4)
DSATROPN LA    R3,1(,R3)
         BCT   R2,DSATROPL   DO ALL
DSATREX  LM    R14,R8,12(R13)  RELOAD ALTERED REGISTERS
         SR    R15,R15       SET GOOD RETURN
         BR    R14           RETURN TO USER
         SPACE 1             RETURN OK
PATDSORG DC    C'GSTXTQ? AMTR? ? ISPSDACXCQMQPOU '
PATRECMQ DC    C'GR'         TCAM MESSAGE QUEUE                  83100
PATRECFM DC    C' VFUTBSAM '
PATOPTPS DC    C'WUCHQZTJ'   PS/PO COMMON OPTCD
PATOPTDA DC    C'WOEFA84R'   DA OPTCD - 2 RESERVED
PATOPTIS DC    C'WUMIY4LR'   IS OPTCD - 1 RESERVED, U NOT USED
         SPACE 1
WORKSECT DSECT ,             USER'S SAVE AREA
DB       DS    D             CONVERSION WORD
         SPACE 1
FMTDSECT DSECT ,             MAP OF USER'S AREA
FMTDSORG DC    CL3' '        DSATR - DSORG
FMTRECFM DC    CL6' '        DSATR - RECFM
FMTOPTCD DC    CL8' '        DSATR - OPTCD
FMTBLKL  DC    CL5' '        DSATR - BLKSIZE
FMTLRECL DC    CL5' '        DSATR - LRECL
FMTORGLN EQU   *-FMTDSORG    LENGTH OF LIST
         SPACE 1
         PRINT &PRTSYS
         IECSDSL1 1
         SPACE 1
         YREGS ,
         END   ,
