STOW     TITLE 'S T O W U P D T  ***  STOW IEBUPDTE ADD FORMAT'
         COPY  OPTIONGB
         SPACE 1
         LCLA  &WIDTH
&WIDTH   SETA  133
         SPACE 1
         SYSPARM LIST=YES
         SPACE 2
*        MEMBERS ARE DESIGNATED BY './  NAME=' STARTING IN COLUMN 1.
*          OTHER ./ CONTROL CARDS ARE IGNORED.
*
*            REGION - 80K FOR 5K INPUT BLOCK AND 3K OUTPUT
*            PARM:  NONE OR 'MOD' - ADD NEW MEMBERS ONLY
*                   'REP' OR 'NEW' - ADD/REPLACE EVERYTHING
*
*        DD: INTAPE - SEQUENTIAL FILES IN COMPRESSED OR UNCOMPRESSED
*              FORMAT; MAY BE CONCATENATED.  UNLIKE CONCATENATION IS
*              SUPPORTED.  FOR UNLABELLED TAPES, AT A MINIMUM THE
*              RECORD FORMAT (F OR V) MUST BE SPECIFIED; LRECL DEFAULTS
*              TO 80 FOR F, 84 FOR V; BLOCKSIZE DEFAULTS TO 5120 FOR F,
*              5380 FOR V.
*            OUTPDS - PARTITIONED DATASET.  IN OUR CASE, ONE FULL
*              DOUBLE-DENSITY 3330 WITH 195 DIRECTORY ENTRIES FOR ALL
*              MODS TAPES (SINCE 1974). THERE STILL IS ROOM LEFT...
*        ATTRIBUTES:  NON-ANYTHING
*        EXTERNAL REFERENCES, MACROS AND STUFF: ALL ESP FACILITIES
*        COMPLAINTS AND QUESTIONS SHOULD BE ADDRESSED TO:
*              CIRCULAR FILE
*        AUTHOR:  G. POSTPISCHIL (ESP)
         EJECT ,
         PRINT NOGEN
STOWUPDT PGMHEAD ZERO12,BASE=R12,PARM=R9    MOVED FROM WYLBUR ON 86290
         SERVINIT ,          ESTABLISH @SERVICE ROUTINE
         SERVCALL LPALD,=CL8'@INPREAD'  GET THE GENERALIZED INPUT
         ST    R0,@INPREAD   SAVE THE ADDRESS
         SERVCALL LPALD,=C'@PRINTER'   GET THE PRINTER ROUTINE
         ST    R0,@PRINTER
         PRTOPEN SYSPRINT
         SPACE 1
         LA    R1,0(,R9)     DON'T TRUST NOBODY
         LTR   R1,R1         ANYTHING ?
         BNP   NOPARM
         LAT   R1,0(R1),NOPARM  GET ADDRESS
         LH    R2,0(,R1)     GET PRESUMED LENGTH
         LTR   R2,R2         ANY ?
         BNP   NOPARM
         CLI   2(R1),C'M'    'MOD' ?
         BE    NOPARM        YES - ADD ONLY
         CLI   2(R1),C'N'    'NEW' ?
         BE    PARMREP
         CLI   2(R1),C'R'    REPLACE OPTION ?
         BNE   ABEND16
PARMREP  OI    FLAG,REPSW    MAKE ADD STOW INTO REPLACE STOW
         SPACE 1
NOPARM   INPOPEN INFILE      OPEN THE INPUT FILE
         CH    R15,=H'4'     DID IT OPEN ?
         BH    ABEND16       NO
         OPEN  (OUT,(OUTPUT))
         TM    OUT+48,X'10'
         BNZ   OPENOK
ABEND16  ABEND 16            GREETINGS
         SPACE 1
OPENOK   LH    R6,OUT+DCBLRECL-IHADCB  GET OUTPUT LRECL
         LH    R3,OUT+DCBBLKSI-IHADCB  GET OUTPUT BLOCK SIZE
         SLR   R2,R2
         DR    R2,R6         GET NUMBER OF RECORDS THAT FIT
         MR    R2,R6         GET TRUNCATED BLOCK SIZE
         STH   R3,OUT+DCBBLKSI-IHADCB
         LR    R2,R6         COPY LRECL FOR BXLE
         GETMAIN R,LV=(R3)   GET STORAGE FOR BUFFER
         ST    R1,BLOKMAIN   SAVE BLOCK ADDRESS
         ST    R3,BLOKMAIN+4   AND LENGTH
         LR    R5,R1         SAVE FOR REST OF RUN
         LR    R4,R5         COPY START ADDRESS
         AR    R3,R5         GET LAST BYTE+1 OF BUFFER
         SR    R3,R2         GET LAST RECORD ADDRESS
         STM   R2,R4,BLOCKS  SAVE BLOCK INFO
         SPACE 1
GETINP   INPGET ,            GET A RECORD
         CH    R15,=H'4'     EOF OR ERROR ?
         BE    EOF           END FILE
         BH    ABEND8        ERROR
         LR    R7,R1         SAVE THE RECORD ADDRESS
         CLC   =C'./',0(R7)  CONTROL CARD ?
         BNE   NONCON        NO; PROCESS
         CLC   =C' ADD NAME=',2(R7)  MEMBER NAME RECORD ?
         BE    STOWIT        YES - STOW THE PREVIOUS MEMBER
         B     GETINP        NO - SKIP
NONCON   TM    FLAG,SKIPSW   IN SKIP MODE ?
         BNZ   GETINP        YES - SKIP IT
NEWMEM   LR    R1,R2         COPY RECORD LENGTH
         BCTR  R1,0          ADJUST FOR EXECUTE
         EX    R1,EXMOVE     COPY ONE RECORD
         INC   NUMREC        COUNT RECORDS STASHED
         BXLE  R4,R2,GETINP  LOOP BACK; NOT END OF BLOCK
         LA    R9,GETINP     SET RESTART ADDRESS
DOWRITE  LR    R1,R4         COPY CURRENT POSITION
         SR    R1,R5         GET BLOCK LENGTH
         LR    R4,R5         REFRESH THE BLOCK POINTER
         BNPR  R9            RETURN IF BLOCK EMPTY
         STH   R1,OUT+DCBBLKSI-IHADCB  SET BLOCKSIZE INTO DCB
         WRITE FECB,SF,OUT,(R5),'S'   WRITE FULL OR LAST BLOCK
         CHECK FECB          WAIT FOR GOOD COMPLETION
         LAT   R1,TTR,,(R9)  SKIP IF TTR SET
         NOTE  OUT           GET TTR
         ST    R1,TTR        STASH IT
         BR    R9            RETURN TO CALLER
EXMOVE   MVC   0(0,R4),0(R7)  MOVE A RECORD
         SPACE 1
EOF      LA    R7,=13X'FF'   POINT TO END-FILE MEMBER NAME
STOWIT   BAL   R9,DOWRITE    WRITE LAST RECORD
         NI    FLAG,255-SKIPSW  TURN OFF MEMBER SKIP FLAG
         CLI   MEMBER,C' '   PREVIOUS MEMBER STASHED ?
         BNH   RESET         NO PREVIOUS ONE; SKIP
         STOW  OUT,MEMBER,R  REPLACE
         CH    R15,=H'8'     ERROR ?
         BNH   STOWSHOW      NO
         PRTLIST MGBADSTO    SHOW STOW ERROR
ABEND8   ABEND 8,DUMP        ERROR IN DIRECTORY PROCESSING
         SPACE 1
STOWSHOW PRTLIST MGGUDSTO    GOOD STOW
RESET    MVC   MEMBER(8),12(R7)  LOAD NEW MEMBER NAME
         XC    TTR(8),TTR    RESET TTR AND COUNT
         CLI   MEMBER,X'FF'  END-FILE ?
         BE    GETOUT        YES; TERMINATE
         TM    FLAG,REPSW    REPLACE - UNCONDITIONAL STOW
         BNZ   GETINP        YES - PROCESS IT
         MVC   BLDLNAME(8),12(R7)  MOVE NAME TO BLDL LIST
         BLDL  OUT,BLDLIST  DOES IT EXIST ?
         CH    R15,=H'4'
         BE    GETINP        NO; ADD IT IN
         BH    ABEND8        MAYBE - I/O ERROR
         MVI   MEMBER,C' '   INDICATE PREVIOUS MEMBER STOWED
REPSKIP  OI    FLAG,SKIPSW   SET TO SKIP THIS MEMBER
         PRTLIST MGDUPMEM    INDICATE DUPLICATE MEMBER
         B     GETINP        AND GET ANOTHER CARD
         SPACE 1
GETOUT   CLOSE (OUT)
         PGMEXIT RC=0
         SPACE 2
MGBADSTO FDPRT '***** STOW FAILED FOR MEMBER',NL
         FDPRT MEMBER,PAD,DEB
         FDPRT '*****'
         FDPRT *END
         SPACE 1
MGDUPMEM FDPRT '***** MEMBER',NL
         FDPRT MEMBER,PAD,DEB
         FDPRT 'ALREADY EXISTS, AND REPLACE OPTION NOT REQUESTED *****'
         FDPRT *END
         SPACE 1
MGGUDSTO FDPRT ' ',LEN=6,NL
         FDPRT MEMBER,PAD
         FDPRT 'STOWED AT TTR'
         FDPRT TTR,2,HEX,PADL
         FDPRT '.'
         FDPRT TTR+2,1,HEX,PADR
         FDPRT 'WITH'
         FDPRT NUMREC,ICM,RADJ,PADR,LEN=13
         FDPRT 'RECORDS'
         FDPRT *END
         EJECT
SAVE     DSECT ,             WORK AREA GOTTEN BY SAVEM
@SERVICE DS    A
@INPREAD DS    A
@PRINTER DS    A
BLOCKS   DC    3F'0'   1/2   BLOCK POINTERS LEN/END/CURRENT
BLOKMAIN DC    2A(0)   2/2   BUFFER GETMAIN ADDRESS/LENGTH
MEMBER   DC    CL8' '        STOW - MEMBER NAME
TTR      DC    A(0)    1/2   STOW - TTR
NUMREC   DC    A(0)    2/2   RECORD COUNT OF MEMBER
SAVEEND  EQU   *               LENGTH OF GOTTEN STORAGE
STOWUPDT CSECT ,
         SPACE 1
*        FLAG SETTINGS
ONESW    EQU   X'01'         FIRST ENTRY TO DCBEXIT IF OFF
REPSW    EQU   X'02'         REPLACE ALL MEMBERS (ELSE ADD ONLY)
SKIPSW   EQU   X'04'         SKIPPING FOR MEMBER
COMPSW   EQU   X'08'         DATA IS COMPRESSED
CONCSW   EQU   X'10'         CONCATENATION - REISSUE READ
FAILSW   EQU   X'80'         ERROR IN DCB EXIT
FLAG     DC    AL1(SKIPSW)   INIT - SCAN FOR MEMBER
         SPACE 1
INFILE  INPWORK INTAPE,SYSUT1,EODAD=EOF,WIDTH=&WIDTH,FILL=C' ',EDIT=128
OUT      DCB   DDNAME=SYSUT2,DSORG=PO,MACRF=W,RECFM=FB,LRECL=&WIDTH
SYSPRINT PRTWORK SYSPRINT,SYSTERM
         SPACE 1
BLDLIST  DC    0A(0),AL2(1,74)
BLDLNAME DC    CL74' '
         SPACE 2
         PRINT &PRTSYS
CVTDSECT DSECT ,
         CVT   DSECT=YES
         IHACDE ,
         DCBD  DSORG=PS,DEVD=DA
         END
