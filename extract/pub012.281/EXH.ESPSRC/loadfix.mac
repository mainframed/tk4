LFIX     TITLE 'L O A D F I X  ***  LOAD, PAGEFIX AND EXECUTE'
         COPY  OPTIONGB      DEFINE GLOBALS                      94236
         SPACE 1                                                 94236
         SYSPARM LIST=YES    SET AND SHOW GLOBALS                94236
         EJECT ,
         PRINT &PRTSOR                                           94236
LOADFIX  START 0                                        ADDED ON 79296
*
*        THIS PROGRAM LOADS ANOTHER PROGRAM THE NAME OF WHICH
*          IS SPECIFIED IN THE STEPNAME FIELD
*        THE LOADED PROGRAM'S ENTRY POINT - 16 HAS A CL4 CONSTANT OF
*          THE FORM --V-, FOLLOWED BY A POINTER TO A LIST OF ADDRESSES
*          TO BE FIXED - A(START,END,START,END),X'80',AL3(START,END)
*
         USING *,R12
         STM   R14,R12,12(R13)
         LR    R12,R15       MAKE BASE
COMMON   LA    R0,WORKLEN    GET WORK AREA
         GETMAIN R,LV=(0)
         ST    R13,4(,R1)    LINK SAVE AREAS
         ST    R1,8(,R13)
         LR    R13,R1        MAKE NEW SAVE/WORK AREA
         USING WORK,R13
         XC    WORKXC(XCLEN),WORKXC
         STC   R2,FLAG       SAVE FLAG
         MVI   BLDL+1,1
         MVI   BLDL+3,58     INIT BLDL
         SPACE 1
         L     R11,CVTPTR    GET CVT
         NUSE  CVT,R11
         L     R11,CVTTCBP   GET TCB POINTER
         L     R11,4(,R11)   GET TCB
         NUSE  TCB,R11       DECLARE TCB
         L     R10,TCBJSCB   GET JSCB
         NUSE  JSCB,R10       DECLARE IT
         TM    JSCBOPTS,JSCBAUTH  AUTHORIZED ?
         BZ    BOONAUTH       NO; ERROR
         L     R1,TCBTIO     GET TIOT
         CLI   8(R1),C' '    PROGRAM NAME SPECIFIED ?
         BNH   BOONPGM        NO; ERROR
         MVC   BLDLNAME,8(R1)  COPY IT
         BLDL  0,BLDL        LOCATE IT
         BXH   R15,R15,BOONPGM  NOT FOUND
         TM    BLDL+26,2     MODULE EXECUTABLE ?
         BZ    BOONPGM        NO; SKIP
         NI    BLDL+26,255-X'10'  TURN OFF TEST ATTRIBUTE
         NI    BLDL+27,255-4  TURN OFF SYMBOL TABLE
*        LPA IS NOT TESTED (INTENTIONALLY)
         MODESET KEY=ZERO    GET PRIVILEGED TO RESET APF IF NECESSARY
         LOAD  DE=BLDLNAME   LOAD THE PROGRAM (TURNS APF OFF)
         LR    R6,R0         COPY LOAD ADDRESS
         LA    R6,0(,R6)     CLEAN IT UP
         LR    R4,R6         COPY ENTRY ADDRESS
         XR    R5,R5
         ICM   R5,7,BLDL+33  GET ENTRY POINT OFFSET
         SR    R4,R5         GET LOAD ADDRESS
         ICM   R5,7,BLDL+28  COPY MODULE LENGTH
         AR    R5,R4         MAKE END ADDRESS+1
         LR    R8,R6         COPY ENTRY ADDRESS
         LA    R0,4          DECREMENT
         SR    R8,R0         BACKSPACE
         LA    R2,3          LOOP MAX OF THREE TIMES
POINTLUP SR    R8,R0         GET PREVIOUS WORD
         CR    R8,R4         IN PROGRAM ?
         BL    BOONLIST      LIST NOT FOUND
         CLI   0(R8),C' '    EBCDIC VALUE ?
         BNH   POINTLES      NO; TRY AGAIN
         CLI   0(R8),C'A'
         BL    POINTLES
         CLI   2(R8),C'V'    'V' ?
         BE    POINTFND      YES; POINTER FOUND
POINTLES BCT   R2,POINTLUP   TRY AGAIN
         B     BOONLIST      LIST POINTER NOT FOUND
POINTFND ICM   R8,7,5(R8)    GET FIX LIST
         BZ    ENDFIX        SKIP FIX IF NO LIST AT ALL
         CR    R8,R4         VALID ?
         BL    BOONLIST      NO
         CR    R8,R5
         BNL   BOONLIST
         N     R4,=X'FFFFF000'   PUT START ADDRESS ON PAGE BOUND
         LA    R5,4095(,R5)
         N     R5,=X'FFFFF000'   PUT END ADDRESS ON PAGE BOUND
         LA    R7,20         MAX PAGES TO BE FIXED
         LA    R9,1          SET FLAG FOR WARNING MESSAGE
FIXENT   LM    R2,R3,0(R8)   GET NEXT ENTRIES
         LA    R2,0(,R2)
         LA    R3,0(,R3)     CLEAN UP
         CR    R3,R2         VALID RANGE ?
         BL    WARNING        NO; ISSUE WARNING
         CR    R3,R4         IN PROGRAM ?
         BL    WARNING
         CR    R2,R5
         BNL   WARNING
FIXLOOP  LTR   R7,R7         EXHAUSTED ?
         BNP   TOOMANY       SIGNAL TOO MANY
         BCTR  R7,0
         MVI   ECB,0         JUST IN CASE
         PGFIX R,A=(R2),ECB=(R13),LONG=Y  I NEED A FIX
         LA    R1,4
         SR    R15,R1
         BM    FIXBUMP       DONE; FIX NEXT PAGE
         BZ    FIXEND        ERROR ?
         CR    R15,R1
         BH    FIXEND        ERROR
         WAIT  ECB=ECB       WAIT FOR FIX
FIXBUMP  LA    R2,4092(R1,R2)  SKIP TO NEXT PAGE
         CR    R2,R3         TO BE FIXED ALSO ?
         BNL   FIXEND        NO; GET ANOTHER LIST ENTRY
         CR    R2,R5         STILL IN MODULE ?
         BL    FIXLOOP       YES; GET IT
WARNING  BCT   R9,FIXEND     SKIP IF PREVIOUSLY PRINTED
         WTO   'INVALID FIXLIST ENTRY',ROUTCDE=11  WTP
FIXEND   TM    0(R8),X'80'   END OF LIST ?
         BNZ   ENDFIX        YES
         TM    4(R8),X'80'   DOUBLE-CHECK
         BNZ   ENDFIX
         LA    R8,8(,R8)     GET NEXT ENTRY
         CR    R8,R5         DOUBLE-CHECK
         BL    FIXENT        DO NEXT ENTRY
         B     ENDFIX
TOOMANY  WTO   'PAGEFIX LIMIT REACHED',ROUTCDE=11
ENDFIX   TM    FLAG,KAPF     RETAIN AUTHORIZATION ?
         BZ    APFRESET      NO; RESET
         LAT   R9,TCBTCT,APFSET  SET IF NO TCT
         NUSE  TCT,R9
         LAT   R7,TCTJMR,APFSET  SET APF IF NO JMR
         CLI   20(R7),C'0'
         BNE   APFRESET
         CLI   21(R7),C'9'
         BL    APFRESET      NO; RESET
         CLC   =C'04001',22(R7)  AUTHORIZED ACCT/SUB ?
         BNE   APFRESET
APFSET   OI    JSCBOPTS,JSCBAUTH  SET AUTHORIZATION
         LAT   R7,TCBJLB,APFQUIT  GET STEPLIB DCB OR OUT
         NUSE  DEB,R7
         OI    DEBFLGS1,2    SET DEB APF AUTHORIZATION
*  ABOVE REQUIRES LOCAL MOD TO IEAVLK01)
         B     APFQUIT
APFRESET NI    JSCBOPTS,255-JSCBAUTH  SHOULD BE OFF ALREADY
APFQUIT  MODESET KEY=NZERO   GET USER'S KEY BACK
         LA    R0,WORKLEN    GET WORK LENGTH BACK
         LR    R1,R13        COPY GETMAIN ADDRESS
         L     R13,4(,R13)   GET SAVE AREA BACK
         FREEMAIN R,LV=(0),A=(1)  FREE UP
         LR    R15,R6        GET USER PROGRAM'S ENTRY POINT
         LM    R0,R12,20(R13)  RESTORE REGISTERS
         L     R14,12(,R13)  REFRESH RETURN ADDRESS
         BR    R15           INVOKE USER PROGRAM
         SPACE 2
BOONAUTH WTO   'UNAUTHORIZED',ROUTCDE=11
         B     SOCK
BOONPGM  WTO   'PROGRAM OMITTED OR INVALID',ROUTCDE=11
         B     SOCK
BOONLIST WTO   'FIXLIST NOT FOUND',ROUTCDE=11
SOCK     EX    0,*           SOCK IT TOO ME
         SPACE 2
         DROP  R12
         USING *,R15
         ENTRY LOADFIXA      ENTRY TO RETAIN AUTHORIZATION
LOADFIXA STM   R14,R12,12(R13)
         LA    R2,LOADFIXA-LOADFIX
         LR    R12,R15
         SR    R12,R2        MAKE NEW BASE
         LA    R2,KAPF       SET FLAG TO RETAIN APF
         B     COMMON-LOADFIX(,R12)  ENTER COMMON CODE
         SPACE 2
WORK     DSECT ,
ECB      DS    16F'0'        OS SAVE AREA
WORKXC   EQU   *             START OF CLEARED AREA
BLDL     DS    Y(1,58)       BLDL COUNT/LENGTH
BLDLNAME DS    CL8           PROGRAM NAME
         DS    XL50          DIRECTORY INFO
FLAG     DS    X             LOADFIX/LOADFIXA FLAG
KAPF     EQU   1             KEEP APF AUTHORIZATION
         DS    0D
XCLEN    EQU   *-WORKXC      LENGTH TO CLEAR
WORKLEN  EQU   *-WORK
         SPACE 1
         PRINT &PRTSYS                                           94236
         CVT   DSECT=YES                                         94236
         IKJTCB ,                                                94236
         IEFTCT ,                                                94236
         IEZDEB ,                                                94236
         IEZJSCB ,                                               94236
         YREGS ,                                                 94236
         END
