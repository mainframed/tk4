ZZLNKREF TITLE 'Z Z L N K R E F  ***  EXTRACT LOAD MODULE INFORMATION'
***********************************************************************
*                                                                     *
*   THIS MODULE READS THE MEMBER SPECIFIED BY DD INFILE AND RETURNS   *
*   INFORMATION FROM THE CONTROL RECORDS.                             *
*                                                                     *
*   THIS PROGRAM IS INVOKED BY TSO COMMAND FILE FINDLIB               *
*                                                                     *
***********************************************************************
*   MAINTENANCE:                                                      *
*   2004-03-15  GYP  MADE RE-ENTRANT                                  *
*   2012-02-19  GYP  CHANGED TO SUPPORT MVS 3.8 ASSEMBLY              *
***********************************************************************
         MACRO ,                                                GP04075
&NM      RENT  &PFX=DYN      DEFINE PATTERN AND DYNAMIC WORK    GP04075
         LCLC  &P                                               GP04075
&P       SETC  '&PFX'        MAKE EASIER TO READ                GP04075
&P.WORK  DS    0D                                               GP04075
&NM      MACPARM MODE=LBL                                       GP04075
&P.WTO   DC    Y(&P.WND-*),X'8000'  WTO PREFIX                  GP04075
&P.TPUT  DC    0C' '                                            GP04075
&P.WEEK  DC    CL3' ',C' '   DAY OF WEEK                        GP04075
&P.MON   DC    CL2'  ',C'/'                                     GP04075
&P.DAY   DC    CL2'  ',C'/'                                     GP04075
&P.YR    DC    CL2'  ',C'.'                                     GP04075
&P.JUL   DC    CL3'   ',C' '                                    GP04075
&P.TIME  DC    CL5' ',CL2' '                                    GP04075
&P.MEM   DC    CL8' ',CL2' '                                    GP04075
&P.DSN   DC    CL44' '                                          GP04075
&P.WND   DC    X'00000008'   END OF MESSAGE; DESC=0, ROUTCDE=13 GP04075
         SPACE 1                                                GP04075
&P.OLIST OPEN  (&P.DCB,(INPUT,REREAD)),MF=L                     GP04075
&P.DCB   DCB   DDNAME=INFILE,DSORG=PS,MACRF=GL,EODAD=EODAD,            *
               EXLST=&P.XLIST                                   GP04075
&P.XLIST DC    0A(0),X'87',AL3(INFMJFCB-INFMJFCB)               GP04075
&P.WLEN  EQU   *-&P.WORK                                        GP04075
         MEND  ,                                                GP04075
         SPACE 1
         PUNCH ' ORDER ZZLNKREF(P) '
         SPACE 1
         COPY  OPTIONGB
         SPACE 1
         SYSPARM LIST=YES
         SPACE 1
         PRINT &PRTSOR
ZZLNKREF PGMHEAD ZERO,BASE=R12,PARM=R9,AM=31,RM=24
         SPACE 1                                                GP04075
         LA    R1,PATWLEN                                       GP04075
         LR    R15,R1                                           GP04075
         LA    R14,DYNWORK                                      GP04075
         LA    R0,PATWORK                                       GP04075
         MVCL  R14,R0        INITIALIZE                         GP04075
*OLD*    USING IHADCB,DYNDCB                                    GP04075
         LA    R10,DYNDCB                                       GP12050
         USING IHADCB,R10    DECLARE OLD-FASHIONED DCB MAPPING  GP12050
         LA    R0,DYNXLIST                                      GP04075
         STCM  R0,7,DCBEXLSA                                    GP04075
         LA    R0,INFMJFCB                                      GP04075
         STCM  R0,7,DYNXLIST+1                                  GP04075
         LA    R0,DYNDCB                                        GP04075
         STCM  R0,7,DYNOLIST+1                                  GP04075
         SPACE 1
         RDJFCB MF=(E,DYNOLIST) READ THE JFCB                   GP04075
         TM    JFCBIND1,JFCPDS  MEMBER?
         BZ    BADMEM        NOT A PDS MEMBER
         MVC   DYNDSN,JFCBDSNM  MOVE DATASET NAME               GP04075
         MVC   DYNMEM,JFCBELNM    AND MEMBER                    GP04075
         SPACE 1
         OPEN  MF=(E,DYNOLIST) OPEN THE MEMBER                  GP04075
         TM    DCBOFLGS,DCBOFOPN  OPEN?                         GP04075
         BNZ   READLOOP      GET MEMBER DATA
BADMEM   TPUT  BADMEMSG,L'BADMEMSG  TELL USER
         MVICC 8                                                GP04075
         B     PGMEXIT                                          GP04075
BADMEMSG DC    C'ZZLNKREF: INFILE NOT PDS MEMBER OR UNUSABLE'
         SPACE 1
READLOOP GET   DYNDCB        READ A BLOCK
         LR    R3,R1         PRESERVE THE INPUT ADDRESS
         TM    0(R3),X'80'   IDR RECORD?
         BZ    NOTIDR        NO; SEE IF TEXT
         CLC   =C'5695DF108',3(R3)  LINK-EDIT 2  ?
         BE    LINKDATE      YES
         CLC   =C'566528408',3(R3)  LINK-EDIT 1  ?
         BNE   READLOOP      NO; READ AGAIN
         SPACE 1
LINKDATE SLR   R2,R2         CLEAR FOR IC
         ICM   R2,7,15(R3)   LOAD (PRESUMED) PACKED DATE
         UNPK  DYNJUL,16(2,R3)  MAKE JULIAN DAYS PRINTABLE      GP04075
         LR    R1,R2         COPY TO PARAMETER REGISTER
         CALL  SUBP2D        GET DAY OF WEEK
         MVC   DYNWEEK,0(R1)  IDENTIFY THE DAY OF THE WEEK      GP04075
         LR    R1,R2         COPY PARM AGAIN
         CALL  SUBP2M        GET MONTH/DAY
         MVC   DYNYR,2(R1)   COPY YEAR                          GP04075
         MVC   DYNMON,4(R1)    AND MONTH                        GP04075
         MVC   DYNDAY,6(R1)      AND DAY                        GP04075
NOTIDR   TM    0(R3),X'01'   TEXT RECORD NEXT?                  GP96086
         BZ    READLOOP      NO; READ NEXT RECORD               GP96086
         SPACE 1
         GET   DYNDCB        READ FIRST TEXT BLOCK
         LR    R3,R1         SAVE RECORD ADDRESS
         MVC   DYNTIME,=C'-----'                                GP04075
         CLC   =X'47F0F0',0(R3)  IS THIS POSSIBLE ID EXPANSION?
         BNE   EODAD                                            GP96086
         CLC   =C'TIME',92(R3)    WITH ASSEMBLY TIME?
         BNE   EODAD
         MVC   DYNTIME,97(R3)   IF SO, USE IT                   GP04075
         SPACE 1
EODAD    LA    R0,DYNWND-DYNTPUT  GET MESSAGE LENGTH            GP04075
         TPUT  DYNTPUT,(0)   WRITE TO USER                      GP04075
         BXLE  R15,R15,PGMEXIT                                  GP04075
*PROD*   WTO   MF=(E,WTOMSG)  WRITE MESSAGE FOR TESTING
         SPACE 2
PGMEXIT  CLOSE MF=(E,DYNOLIST)                                  GP04075
         PGMEXIT RC=RETCODE                                     GP04075
         SPACE 2
         RENT  PFX=PAT       DEFINE REENTRANT WORK PATTERNS     GP04075
         PRINT &PRTSYS
SAVE     DSECT ,                                                GP97310
DB       DS    D                                                GP04075
         SERVDEFS ,                                             GP04075
         PRINT &PRTMAC                                          GP04075
         RENT  PFX=DYN       EXPAND WORK AREA                   GP04075
         PRINT &PRTSYS                                          GP04075
MYJFCB   IEFJFCBN ,                                             GP04075
SAVEEND  EQU   *                                                GP97310
         SPACE 1
         DCBD  DEVD=DA,DSORG=PS
         END   ,
