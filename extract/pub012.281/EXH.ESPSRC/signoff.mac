SIGN     TITLE 'TSO SIGN-OFF COMMAND PROCESSOR'
         COPY  OPTIONGB
         SPACE 1
         SYSPARM LIST=YES
         SPACE 2
***********************************************************************
*                                                                     *
*                                                                     *
*        COPYRIGHT 1998  EXPERT SYSTEM PROGRAMMING                    *
*                        176 OLD STAGE COACH ROAD                     *
*                        BRADFORD, VT 05033-8844                      *
*                                                                     *
*                    ALL RIGHTS RESERVED                              *
*                                                                     *
***********************************************************************
         SPACE 2
*********************************************************************
*
*        SIGNOFF (ALIAS SIGNOUT, SIGN)  ALLOWS A TSO USER WHO HAS
*              LOGGED ON USING LOCAL FACILITIES (NON-UADS), TO RESET
*              THE GLOBAL LOGON.
*
*        LOCAL ON-LINE USER DATA CONTROL BLOCK (LOUD).
*
*********************************************************************
         EJECT ,
         PRINT &PRTSOR
SIGNOFF  PGMHEAD ZERO,BASE=R12,PARM=R9                  ADDED ON 88002
         SERVINIT ,          LOAD AND INITIALIZE @SERVICE
         MVC   A$SID,=C'A$RB'  MAKE BLOCK IDENTIFIER
         MVC   A$SFCTN(3),=AL1(A$SFLOUD,A$STSO,A$SBUILD)  GET LOUD
         LA    R1,MSGAREA
         ST    R1,A$SERMSG   STASH IN CALLING LIST
         TESTAUTH FCTN=1     CALLER AUTHORIZED ?
         BXLE  R15,R15,HAVEAUTH  YES
         OI    FLAG,FGAUTH   SET UNAUTHORIZED CALLER
         SERVICE  APFON      AND AUTHORIZE HERE
HAVEAUTH L     R4,PSAAOLD-PSA  GET THE ASCB ADDRESS
         USING ASCB,R4       DECLARE IT
         ICM   R3,15,ASCBTSB    RUNNING UNDER TSO ?
         BZ    BOMB666       NO; REALLY BEASTLY
         USING TSB,R3        DECLARE IT
         MODESET KEY=ZERO
         MVC   A$SACCT,TSBTRMID  MOVE THE TERMINAL NAME
         MODESET KEY=NZERO   GET BACK TO REALITY
         SPACE 1
         LA    R1,A$SVCRB    GET SVC PARAMETER LIST
         SVC   249           GET A LOUD
         BXH   R15,R15,BOMB666  FAILED
         MVC   A$SACCT,=CL8'TSO'  IDENTIFY SUB-SYSTEM
         MVC   A$SFCTN(2),=AL1(A$SFLOGF,0)  REQUEST COMPLETE LOGOFF
         XC    A$SPSWD,A$SPSWD  MAKE SURE
         LA    R1,A$SVCRB
         SVC   249           COMPLETE LOGOFF
         B     DOMGCR        ALTERNATIVE LOGOFF
         MODESET KEY=ZERO
         ICM   R1,15,A$SBUFR GET LOUD
         BZ    NOTSO
         MVI   A$LFLG1-A$LOUD(R1),0     LOG THE USER OFF
         MVI   A$LFLG2-A$LOUD(R1),A$LFLOG  DISCONNECT LINE       90008
NOTSO    OI    TSBSTAT,TSBDISC  FAKE A DISCONNECT
         MODESET KEY=NZERO
         ABEND 0,,STEP       AND QUIT
DOMGCR   L     R1,PSATOLD-PSA  GET MY TCB
         L     R1,TCBTIO-TCB(,R1)  GET THE TIOT
         MVC   MSGAREA(PATCMDLN),PATCMD
         MVC   MSGAREA+PATUSER-PATCMD(7),0(R1)  MOVE USER ID
         MODESET KEY=ZERO                                        88002
         SLR   R0,R0         JUST IN CASE
         LA    R1,MSGAREA
         SVC   34            ISSUE MGCR
         STIMER WAIT,,BINTVL==A(100)  WAIT-A-BIT
         MODESET KEY=NZERO                                       88002
         B     DOMGCR        IN CASE OF MULTIPLE LOGONS
*
         SPACE 1
EXIT     BAL   R9,TERMINAT
         SERVTERM ,
         L     R9,RETCODE    GET RETURN CODE
         PGMEXIT RC=(R9)     QUIT WITH RETURN CODE 0 OR 4
         SPACE 1
TERMINAT TM    FLAG,FGAUTH   WAS USER AUTHORIZED AT START ?
         BZR   R9            YES; RETURN
         SERVICE  APFOF      ELSE RESET IT
         BR    R9            AND RETURN
         SPACE 1
BOMB666  SLR   R0,R0
         ICM   R0,3,MSGAREA  ERROR MESSAGE ?
         BZ    BOMBOUT       NO
         SH    R0,=H'4'
         TPUT  MSGAREA+4,(R0)  TELL USER
BOMBOUT  ABEND 666
         SPACE 1
PATCMD   DC    Y(PATCMDND-*,0),C'CANCEL U='
PATUSER  DC    CL8' '
PATCMDND EQU   *
PATCMDLN EQU   PATCMDND-PATCMD
         SPACE 1
SAVE     DSECT ,
DB       DS    D
         SERVDEFS ,                                             GP04061
FLAG     DS    X             PROCESSING FLAG
FGAUTH   EQU   X'80'           CALLER IS NOT AUTHORIZED (ALREADY)
         SPACE 1
         PRINT &PRTMAC
A$SVCRB  A$SVCRB DSECT=NO    ACCOUNT SVC PARAMETER LIST
         SPACE 1
MSGAREA  DS    CL125
SAVEEND  EQU   *
         SPACE 1
         PRINT &PRTSYS
         IHAPSA ,
         IHAASCB ,
         IKJTCB ,
         IKJTSB ,
         SPACE 1
         PRINT &PRTMAC
A$LOUD   A$LOUD ,
         SPACE 1
         END   ,
