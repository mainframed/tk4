INDX     TITLE 'I E H V T O C  --- DO VTOC TOC'
         PRINT NOGEN
VTOCINDX START 0
         USING *,12
         STM   14,12,12(13)
         LR    12,15
         LR    9,13
         LA    13,SAVE
         ST    13,8(9)
         ST    9,4(13)
         L     9,=V(VTOCCOMM)   GET COMMON AREA
         USING VTOCCOMM,9
         L     11,WKADDR     ADDRESS OF 'OBTAIN' WORK AREA
         L     10,F4ADDR     ADDRESS OF FORMAT 4 FOR THIS VOLUME
         SPACE
         LA    7,EXIT        SET BR REG. FOR EXIT, NO FREEMAIN
      GETMAIN  VC,LA=GTMLIST,A=GTMAD
         LTR   15,15
         BNZ   NOSTOR        PUT OUT 'NO STORAGE' MESSAGE        80340
      FREEMAIN V,A=GTMAD     FREE OBTAINED STORAGE               80340
         L     3,GTMAD+4               GET MAX LENGTH RETURNED
         S     3,GTMLIST               SUBTRACT MINIMUM REQUIRED
         SR    2,2                     OR SYSTEM OVERHEAD
         STM   2,3,GTMLIST2            AND STASH FOR NEW REQUEST
      GETMAIN  VC,LA=GTMLIST2,A=GTMAD   GET ALL BUT MIN. STORAGE 80340
         LTR   15,15
         BNZ   NOSTOR        NO ??? WHAT HAPPENED ?              80340
         L     3,GTMAD                 GET START ADDRESS
         A     3,GTMAD+4               ADD LENGTH
         S     3,=A(2*LISTLEN)   SUBTRACT TWO ENTRIES
         ST    3,LISTEND               STASH AS END OF LIST
         LA    7,FREEXIT     SET BR REG FOR FREE STORAGE EXIT    80340
         CLC   =A(2*LISTLEN),GTMAD+4  AT LEAST 2 ENTRIES ?
         BH    NOSTOR        NO, PUT 'NO STORAGE' MESSAGE, AND EXIT
         MVC   CURR,GTMAD              INITIALIZE TABLE POINTER
         USING TAB,8
         XC    LLIMIT,LLIMIT           SET PAGING COUNTER TO MINIMUM
         LH    2,CYLTRK      GET MAX TRACKS PER CYLINDER
         BCTR  2,0           - 1
         STH   2,HITRK       STASH HIGHEST TRACK NO.
         XC    CULINE,CULINE FORCE SUBTITLE FIRST TIME IN
         SPACE 3
LOOPER   MVI   SPILL,0                 ZERO 'NEW PAGE' REQUIRED FLAG
         XC    SEQ,SEQ                 ZERO DUPL. EXTENT SEQUENCE COUNT
         MVI   FM5SW+1,X'70'           MAKE FMT 5 TEST A 'BNE'
         XC    CCHHR,CCHHR             ZERO FOR FIRST READ
         XC    135(5,11),135(11)   ZERO CONTINUATION COUNTER
         B     FMT4                    DO INITIAL ENTRIES INTO TABLE
         SPACE 2
NEXT     CALL  OBTAINIT,(CCHHR)   GET NEXT SEQ. VTOC ENTRY
         B     PRINTIT                 ALL DONE ON THIS SCAN, GO TO PRT
         B     IOERR                   BOO !!!
         CLI   44(11),C'1'             FMT 1 ?
         BE    FMT1                    YES, DO IT
         CLI   44(11),C'5'             FMT 5 ?
FM5SW    BNE   NEXT                    BNE OR B AFTER FIRST TIME
         MVI   FM5SW+1,X'F0'    MAKE 'B'
         B     FMT5                    DO FMT 5 FIRST TIME ONLY
         SPACE 2
MORE     OC    135(5,11),135(11)
         BZ    NEXT                    NO CONTINUATIONS
         CALL  OBTAINS                 GET CONTINUATION
         B     IOERR                   BOO !!
         CLI   44(11),C'3'             FMT 3 ?
         BE    FMT3                    YES
         CLI   44(11),C'5'             FMT 5 ?
         BE    FMT5
         B     MORE
         SPACE 2
IOERR    CALL  PRINT,(ERRMSG,LINE1)   PRINT ERROR MSG
         B     FREEXIT                 AND EXIT
         SPACE 2
NOSTOR   CALL  PRINT,(NOSTORMS,LINE1)   PUT 'NO STORAGE AVAIL.'  80340
         BR    7             GO TO APPROPRIATE EXIT
         EJECT
*        DO INITIAL TABLE ENTRIES
*
FMT4     LA    1,NEWENT                SET ENTRY ADDRESS FOR 'ADDIN'
         XC    NEWENT(LISTLEN),NEWENT
         TM    VOLABEL+1,1             CHECK FOR LABEL TRACK ALLOC ?
         BZ    FMT4A                   YES, NEED NO ENTRY
         MVC   NEWDSN+32(12),=C'VOLUME LABEL'
         LH    2,SEQ
         LA    2,1(2)
         STH   2,SEQ
         STH   2,NEWLOW+4
         MVI   NEWXSQ,1      MAKE EXTENT NO. 1
         BAL   7,ADDIN                 PLACE INTO TABLE
FMT4A    TM    VOLIPL+1,1              IS THERE AN IPL TRACK ?
         BZ    FMT4B                   NOT AS SUCH
         MVC   NEWDSN+32(12),=C'    IPL DATA'
         LH    2,SEQ
         LA    2,1(2)
         STH   2,SEQ
         STH   2,NEWLOW+4
         MVI   NEWXSQ,1      MAKE EXTENT NO. 1
         MVC   NEWLOW+2(2),LINE1       SET CYL=0, TRK=1
         MVC   NEWHIGH+2(2),LINE1
         BAL   7,ADDIN                 ADD IT IN
FMT4B    MVC   NEWDSN+20(24),=C'VOLUME TABLE OF CONTENTS'
         MVC   NEWXFG(6),105-44(10)  COPY DS4VTOCE FSCCHH (LOW)
         MVC   NEWHIGH(4),111-44(10)   CCHH (HIGH)
         LH    2,SEQ
         LA    2,1(2)
         STH   2,SEQ
         STH   2,NEWLOW+4
         IC    2,NEWXSQ      GET SEQ. NO OF EXTENT (ALWAYS 0 ???)
         LA    2,1(2)
         STC   2,NEWXSQ      AND STASH BACK
         BAL   7,ADDIN
         B     MORE
         EJECT
*        PROCESS FORMAT 1 AND FORMAT 3 EXTENTS
*
FMT1     MVC   NEWDSN,0(11)            COPY DSN
         LA    1,NEWENT                SET ENTRY ADDRESS
         LA    5,105(11)               POINT TO FIRST EXTENT ENTRY
         LA    6,3                     SET FOR THREE ENTRIES
         B     FMT31                   GO TO COMMON RTNE
         SPACE 2
FMT3     MVC   44(90,11),45(11)        MAKE CONTIGUOUS
         LA    1,NEWENT
         LA    5,4(11)                 FIRST ENTRY
         LA    6,13                    SET FOR 13 ENTRIES
         SPACE
FMT31    OC    0(10,5),0(5)            L&ST ENTRY DONE ?
         BZ    MORE                    YES
         MVC   NEWXFG(6),0(5)          MOVE FLAGS, SEQ, LOW CCHH
         MVC   NEWHIGH(4),6(5)         MOVE HIGH CCHH
         LH    2,SEQ
         LA    2,1(2)
         STH   2,SEQ
         STH   2,NEWLOW+4
         IC    2,NEWXSQ      GET EXTENT SEQUENCE #
         LA    2,1(2)        CONVERT TO LOGICAL EXTENT #
         STC   2,NEWXSQ      AND STASH BACK (0-15) TRANSL TO (1-16)
         BAL   7,ADDIN
         LA    5,10(5)                 POINT TO NEXT EXTENT ENTRY
         BCT   6,FMT31                 REPEAT IF MORE
         B     MORE
         EJECT
*        PROCESS FORMAT 5 ENTRIES
*
FMT5     MVC   44(90,11),45(11)        MAKE ENTRIES CONTIGUOUS
         LA    5,4(11)                 POINT TO FIRST ENTRY
         LA    1,NEWENT                FOR 'ADDIN'
         XC    NEWDSN(46),NEWDSN       ZERO
         MVC   NEWDSN+35(9),=C'AVAILABLE'
         LA    6,26                    SET FOR 26 ENTRIES
         SPACE
FMT5LP   OC    2(3,5),2(5)        ZERO ENTRY ?
         BZ    MORE                    ES, DONE
         MVC   SAVE(4),0(5)            PUT ON HALF-WORD BOUNDARY
         SR    2,2
         LH    3,SAVE                  START ADDR.
         LH    4,CYLTRK                NO. OF TRACKS PER CYLINDER
         DR    2,4                     CONVERT ADDR. TO CC/HH
         STH   3,NEWLOW                START CYLINDER
         STH   2,NEWLOW+2              START TRACK
         IC    2,4(5)                  NO. OF TRACKS
         LH    3,SAVE+2                NO. OF CYLINDERS
         MH    3,CYLTRK                CONVERT TO TRACKS
         AR    3,2                     TOTAL TRACKS
         SH    3,LINE1                 -1
         AH    3,SAVE                  + START ADDRESS = END ADDRESS
         SR    2,2
         DR    2,4                     CONV. END REL. TRK TO CC/HH
         STH   3,NEWHIGH               END CYLINDER
         STH   2,NEWHIGH+2             END TRACK
         LH    2,SEQ
         LA    2,1(2)
         STH   2,SEQ
         STH   2,NEWLOW+4
         BAL   7,ADDIN                 ENTER IN TABLE
         LA    5,5(5)
         BCT   6,FMT5LP
         B     MORE
         EJECT
*        THIS SECTION WILL PRINT THE CONTENTS OF ONE TABLE LOAD.
*        UNLESS THIS IS THE LAST LOAD OF THE TABLE, THE LAST TWO
*        ENTRIES WILL NOT BE PRINTED TO PERMIT CHECKS FOR OVERLAP
*        AND MISSING TRACKS.
*
PRINTIT  BAL   7,SORT                  SORT THE TABLE IN ORDER
         L     8,GTMAD                 GET TABLE START ADDRESS
         L     4,CURR                  GET CURRENTLY AVAILABLE SLOT
         LR    6,8           SET FOR FIRST TIME
         TM    SPILL,1                 IS THIS THE LAST LOAD ?
         BZ    PRINTLP                 YES, PRINT ENTIRE TABLE
         SH    4,SORTINC+2             SUBTRACT ONE ENTRY
         CR    4,8                     LOWER ?
         BH    *+6
         LR    4,8                     NO ENTRIES OR ONLY ONE
         SPACE
PRINTLP  CLC   LLIMIT,LOW    DONE ALREADY ?
         BNL   PRINTINC      YES, SKIP
         CR    8,6           FIRST TIME THROUGH ?
         BE    PRINT8        YES, PRINT IT
PRINTLQ  LA    R1,NEWENT     FOR WORK AREA                       81117
         USING NEWENT,6      FOR ADDR. OF OLD, NOT WORK AREA
         MVC   ALO,NEWLOW    COPY LOW CCHHR
         MVC   AHI,NEWHIGH
         MVC   BLO,LOW       MOVE NEW LOW
         MVC   BHI,HIGH      MOVE NEW HIGH
         CLI   NEWXFG,X'80'  IS THIS A SPLIT EXTENT ENDING ?
         BNE   NSP
         CLI   XFG,X'80'     IS NEW EXTENT ALSO SPLIT ?
         BE    SPSP          YES, DO SPECIAL CHECK
SPN      CLC   AHIT,HITRK    SPLIT. IS LAST TRACK IN CYLINDER ?
         BE    NSP           YES, TREAT AS UNSPLIT
         DROP  6
         XC    NEWENT(LISTLEN),NEWENT  ZERO WORK AREA
         MVC   NEWLOW(2),ALO COPY LOW CUL
         LH    2,AHIT        COPY HIGH TRACK
         LA    2,1(2)        ADD ONE TO HIGH
         STH   2,NEWLOW+2    AND STORE TRACK PAST THIS ONE
         MVC   NEWHIGH(2),AHI
         MVC   NEWHIGH+2(2),HITRK  SET FOR LAST TRACK IN CYL
         MVC   NEWDSN+14(30),=C'AVAILABLE SPLIT CYLINDER SPACE'
         MVI   NEWXFG,X'8F'  SET FOR FREE SPLIT SPACE
         BAL   7,PRINTLN     PRINT THE MISSING SPACE LINE
         MVC   AHI,NEWHIGH
         USING NEWENT,6
NSP      CLI   XFG,X'80'     IS NEW EXTENT SPLIT ?
         BNE   NN            NO
         CLC   LOW+2(2),ZERO FIRST TRACK ?
         BE    NN            YES, TREAT UNSPLIT
         DROP  6
         XC    NEWENT(LISTLEN),NEWENT
         MVC   NEWLOW(2),BLO
         MVC   NEWLOW+2(2),ZERO
         MVC   NEWHIGH(2),BHI
         LH    2,BLOT        GET START TRACK
         BCTR  2,0           - 1
         STH   2,NEWHIGH+2   TRACK OPEN
         MVC   BLO,NEWHIGH
         MVI   NEWXFG,X'8F'  SET FOR FREE SPLIT SPACE
         MVC   NEWDSN+14(30),=C'AVAILABLE SPLIT CYLINDER SPACE'
         BAL   7,PRINTLN     PRINT THE LINE
         B     NN
         SPACE 2
SPSP     CLC   ALOC,BLOC     SAME SYLINDER ?
         BNE   SPN           NO, PROCESS AS ADJACENT, RATHER THAN OVERL
         CLC   AHIC,BHIC     SAME END CYLINDER ?
         BNE   SPN           NO, BUT HOW ???????
         LH    2,AHIT
         LA    2,1(2)
         CH    2,BLOT        END + 1 SAME AS START OF SECOND ?
         BE    PRINT8        YES, NO MISSING SPACE
         BL    SPSPFREE      FREE INBETWEEN SPACE
         XC    NEWENT(LISTLEN),NEWENT   OVERLAPPING ALLOC
         MVI   NEWXFG,X'80'  SET FOR SPLIT
         MVC   NEWDSN+13(31),=C'OVERLAPPED SPLIT CYLINDER SPACE'
         MVC   NEWLOW(2),ALOC
         MVC   NEWHIGH(2),AHIC
         MVC   NEWLOW+2(2),BLOT
         MVC   NEWHIGH+2(2),AHIT
         BAL   7,PRINTLN     PRINT ERROR MSG
         B     PRINT8        AND PROCESS
         SPACE
SPSPFREE XC    NEWDSN(LISTLEN),NEWDSN
         MVI   NEWXFG,X'80'
         MVC   NEWDSN+14(30),=C'AVAILABLE SPLIT CYLINDER SPACE'
         MVC   NEWLOW(2),ALOC
         MVC   NEWHIGH(2),AHIC
         LH    2,AHIT
         LA    2,1(2)
         STH   2,NEWLOW+2
         LH    2,BLOT
         BCTR  2,0
         STH   2,NEWHIGH+2
         BAL   7,PRINTLN     ACCOUNT FOR MISSING SPACE
         B     PRINT8        PROCESS NEW ONE
         SPACE
NN       XC    NEWENT(LISTLEN),NEWENT
         CLC   AHI,BLO       OVERLOP ?
         BNL   NNOV          YES
         LH    2,AHIT
         LH    3,AHIC
         LA    2,1(2)
         CH    2,CYLTRK
         BL    *+10
         SR    2,2
         LA    3,1(3)
         STH   2,NEWLOW+2
         STH   3,NEWLOW
         SLL   3,16
         OR    2,3
         C     2,BLO
         BE    PRINT8        NO GAP
         LH    2,BLOT
         LH    3,BLOC
         SH    2,LINE1
         BNM   *+10
         LH    2,HITRK       GET HIGH TRACK IN THIS CYL
         BCTR  3,0
         STH   3,NEWHIGH
         STH   2,NEWHIGH+2
         MVC   NEWDSN+27(17),=C'UNACCOUNTED FOR :'
         BAL   7,PRINTLN
         B     PRINT8
NNOV     MVC   NEWLOW(4),BLO
         MVC   NEWHIGH(4),AHI
         MVC   NEWDSN+20(24),=C'OVERLAPPING ALLOCATION :'
         BAL   7,PRINTLN     TELL
         SPACE 3
PRINT8   LR    1,8           SET PARAMETER REGISTER
         TM    XFG,XFGLAST   IS THIS LAST ENTRY + 1 ?            81117
         BO    PREND         YES, SKIP PRINTING OF FAKE DATA     81117
         BAL   7,PRINTLN     PRINT THE LINE, AND ERROR MSG IF ANY
         MVC   LLIMIT,LOW    SET NEW LLIMIT
         SPACE
PRINTINC LR    6,8           SAVE FOR NEXT CHECK
         LA    8,LISTLEN(8)  SPACE TO NEXT ENTRY
         CR    8,4                     DONE ?
         BL    PRINTLP                 NO
         CLI   SPILL,SPILT   IS THERE ANOTHER TABLE LOAD ?       81117
         BE    LOOPER          YES; GO TO READ SOME MORE         81117
         BH    PREND           NO; RECURSION ENTRY; QUIT         81117
         LR    8,6           TO USE USING
         L     3,VOLTOT      GET TOTAL TRACKS ON DEVICE
         SH    3,VOLATOT     SUBTRACT ALTERNATE TRACKS
         BCTR  3,0           GET RELATIVE TRACK NO
         SR    2,2
         LH    4,CYLTRK      MAKE FULLWORD
         DR    2,4           CONVERT TO TRACK/CYLINDER
         CH    3,HIGH        DID LAST ENTRY END ON LAST CYLINDER ?
         BNE   GAP           NO, PUT ERROR
         CLC   HIGH+2(2),HITRK  IS END TRACK LAST IN CYLINDER ?
         BE    PREND         YES, OK
         SPACE
GAP      L     8,GTMAD       GET FIRST TABLE ENTRY FOR WORK SPACE
         CR    8,6           IS FIRST TABLE ENTRY CURRENT ONE ?
         BNE   *+8           NO, OK
         LA    8,LISTLEN(8)  YES, USE NEXT ONE FOR WORKING IN
         XC    TAB(LISTLEN),TAB  CLEAR IT
         LA    3,1(3)        ADDR. OF FIRST ALTERNATE CYLINDER
         STH   3,LOW         MAKE LOW OF FAKE EXTENT ENTRY
         STH   R3,HIGH       SET HIGH, ALSO                      81117
         MVI   XFG,XFGLAST   SET FAKE FLAG                       81117
         OI    SPILL,2       SET LAST TIME + 1 FLAG
         B     PRINTLQ       GO TO PROCESS MISSING / OVERLAPPED SPACE
         SPACE 2
PREND    EQU   *
         SPACE 2
FREEXIT FREEMAIN V,A=GTMAD   FREE THE TABLE STORAGE              80340
         SPACE
EXIT     L     13,4(13)
         LM    14,12,12(13)            RESTORE
         SR    15,15
         BR    14
         EJECT
*        THIS ROUTINE TAKES A SINGLE TABLE ENTRY OR DUMMY ENTRY
*        AT THE ADDRESS IN REG. 1 AND FORMATS FOR PRINTING.
*
PRINTLN  STM   1,8,RTSAVE    SAVE REGISTERS
         LR    8,1
         MVC   RDSN(L'RDSN+L'REXSQ+10),RDSN-1   BLANK DSNAME FIELD, ETC
         MVC   RDSN2(L'RDSN+L'REXSQ+10),RDSN2-1  BLANK CONT. FIELD
         CLI   XFG,X'80'               SPLIT CYLINDER ENTRY ?
         BE    SPLITP                  YES, DIFFERENT SPACE CALC.
         CLI   XFG,X'8F'     SPECIAL SPLIT SPACE ?
         BE    SPLITP        YES; DO IT
         LH    2,LOW                   START CYLINDER
         MH    2,CYLTRK                TO TRACKS
         AH    2,LOW+2                 PLUS TRACKS
         LH    3,HIGH                  END CYLINDER
         MH    3,CYLTRK                TO TRKS
         AH    3,HIGH+2                PLUS TRACKS
         SR    3,2                     DIFFERENCE
         AH    3,LINE1                 ADD 1 FOR NO. OF TRACKS
         STH   3,SIZE                  EXTENT SIZE
         B     PR1
         SPACE
SPLITP   LH    3,HIGH                  GET END CYLINDER
         SH    3,LOW                   - LOW CYLINDER
         AH    3,LINE1                 +1 = TOTAL CYLINDERS
         LH    5,HIGH+2                END TRACK
         SH    5,LOW+2                 - LOW TRACK
         AH    5,LINE1                 +1 = TOTLA TRACKS PER CYLINDER
         MR    2,5                     TOTAL TRACKS IN EXTENT
         STH   3,SIZE                  STASH EXTENT SIZE
         CLI   XFG,X'8F'     MY FLAG FOR AVAIL. SPLIT CYL SPACE ?
         BNE   PR1           NO, GO ON
         L     1,VOLMISS     GET UNACCOUNTED FOR TRACKS
         SR    1,3           SUBTRACT SIZE OF SPLIT FREE SPACE
         ST    1,VOLMISS     STASH BACK
         A     3,VOLFREE     GET FREE TRACKS
         ST    3,VOLFREE     AND STASH BACK CORRECTED NO.
         SPACE
PR1      MVI   RTTRKFG,C' '  SET EYE-CATCHER OFF
         CLC   DSN+35(9),=C'AVAILABLE'  IS IT AN AVAILABALE SPACE ?
         BNE   PR1A          NO
         MVI   RTTRKFG,C'*'  YES, SET EYE-CATCHER
PR1A     CALL  CVD,(CVDLIST)    DO DECIMAL ENTRIES
         CALL  PUTADDR,(CVHLIST)       DO HEX ENTRIWES
         LA    1,EXTFG                 GET TYPE OF EXTENT
         LA    2,EXTAB                 GET TYPE TABLE
         LA    3,EXTAB-EXTFG           GET NO. OF ENTRIES
PR2      CLC   0(1,1),XFG              IS TABLE ENTRY SAME AS DATA ?
         BE    PR3                     YES, USE IT
         LA    1,1(1)                  NO, BUMP
         LA    2,10(2)                 BUMP
         BCT   3,PR2                   TRY AGAIN
         LA    2,RDSN                  NOT IN TABLE, FAKE BLANKS
PR3      MVC   REXTYP,0(2)             MOVE TYPE OF EXTENT
         MVC   LINE(4),LINE2           SET FOR OVERPRINTING OF DSNAME
         LA    1,RDSN                  SET ADDRESS OF DSN ENTRY
         CLI   XFG,X'80'               SPLIT ?
         BNE   *+8                     NO
         LA    1,2(1)                  YES, BUMP 2
         CLI   XSQ,1         FIRST EXTENT OR ZERO ?
         BL    PR4S          LOW, DO SPECIAL
         BE    *+14                    YES
         LA    1,4(1)                  NO, BUMP ANOTHER 4
         MVC   LINE(4),LINE1           AND SET FOR NO OVERPRINTING
         OC    DSN,RDSN                CONVERT TO UPPER CASE
         MVC   0(L'DSN,1),DSN          MOVE DS NAME
         MVC   RDSN2(L'RDSN),RDSN      MOVE FOR OVERPRINT
         LA    2,RDSN+L'RDSN-2         SET LAST TWO BYTES OF DSN
PR4      CLC   0(2,2),EXTAB            2 BLANKS ?
         BNE   PR5                     NO, FINISHED
         MVI   1(2),C'.'               PUT IN A PERIOD
         SH    2,LINE2                 - 2
         CR    2,1                     BUMPED PRIOR TO DSN START ?
         BH    PR4                     NO, TRY AGAIN
         SPACE
PR5      LH    1,CULINE      GET CURRNT LINE COUNT
         SH    1,LINE1       SUBTRACT FOR CURRENT LINE
         STH   1,CULINE      STASH BACK
         BP    PR6           ROOM, PRINT IT
         CALL  PRINT,(ZERO,LEJCT)   FORCE PAGE EJECT
         CALL  PRINT,(SUBTITLE,L1S2)
         MVI   RECORD,C'0'   FORCE DOUBLE SKIP ON NEXT ONE
         LH    1,MAXLINE     GET MAX LINES / PAGE
         SH    1,=H'6'       USE SHORTER PAGE THAN VTOC LIST
         STH   1,CULINE      SET CURRENT COUNT LEFT
PR6      CALL  PRINT,(RECORD,LINE)   PRINT ONE OR TWO DATA LINES
         MVI   RECORD,C' '   RESTORE DOUBLE SKIP FLAG SET BY SUBTITLE
         LM    1,8,RTSAVE    RESTORE
         BR    7             RETURN
         SPACE 2
PR4S     LA    1,RDSN+L'REXSQ+L'RDSN-44+5   OFFSET SPECIAL MESSAGE
         OC    DSN,RDSN      CONVERT TO UPPER CASE
         MVC   0(L'DSN,1),DSN  MOVE DSN NAME
         MVC   RDSN2-RDSN(L'DSN,1),0(1)  COPY FOR OVERPRINTING
         B     PR5           AND GO TO PRINT IT
         EJECT
*        SORT THE TABLE BY STARTING CCHH AND SEQUENCE NO.
*
SORT     STM   1,8,RTSAVE              DO ANYWAY
         L     1,GTMAD                 START ADDRESS
         L     4,CURR                  LAST ENTRY + 1
         S     4,=A(2*LISTLEN)   NEXT TO LAST ENTRY
         CR    1,4                     < 2 ENTRIES ?
         BH    SORTEX                  YES, LEAVE AS IS
         LR    3,1                     START
         SPACE
SORTLP   CLC   LOW-TAB(10,3),LOW-TAB+LISTLEN(3)  IN ORDER ?
         BNH   SORTINC                 YES
         LR    2,3                     NO, DO BACKWARDS
         B     SORT2
         SPACE
SORT1    CLC   LOW-TAB(10,2),LOW-TAB+LISTLEN(2)  THIS ONE IN ORDER ?
         BNH   SORTINC                 FLIPPED INTO SEQUENCE
SORT2    XC    0(LISTLEN,2),LISTLEN(2)
         XC    LISTLEN(LISTLEN,2),0(2)
         XC    0(LISTLEN,2),LISTLEN(2)  FLIP THE TWO ENTRIES
         SH    2,SORTINC+2             BUMP DOWN
         CR    2,1                     ALL DOWN ?
         BNL   SORT1                   NO, KEEP GOING BACK
         SPACE
SORTINC  LA    3,LISTLEN(3,0)  BUMP TO NEXT ENTRY / USE AS H'LISTLEN'
         CR    3,4                     FINISHED ?
         BNH   SORTLP                  NO, KEEP GOING UP
         SPACE
SORTEX   LM    1,8,RTSAVE
         BR    7
         EJECT
*        THIS ROUTINE ADDS AN ENTRY TO THE TABLE, PROVIDING THE
*        NEW ENTRY IS HIGHER THAN THE CUTOFF LIMIT, AND DOES NOT
*        EXCEED THE HIGHEST VALUE IN THE TABLE.
*        IF AN EXISTING ENTRY IN THE TABLE IS LOWER THAN THE LIMIT,
*        IT IS REPLACED. ELSE THE EXISTING HIGHEST ENTRY IS THROWN OUT
*        AND REPLACED BY THE NEW REQUEST
*
ADDIN    STM   1,8,RTSAVE              SAVE ANYWAY
         USING NEWENT,1                ADDRESS OF DESIRED ENTRY IN 1
         CLC   LLIMIT,NEWLOW           LOWER THAN LAST PRINTED ONE ?
         BNL   ADDEX                   DID ALREADY; IGNORE
         L     8,CURR                  ADR OF LAST + 1
         CLC   CURR,LISTEND            IS TABLE FULL
         BNH   ADDB                    NO, ADD AT END
         XC    HLIMIT,HLIMIT           ZERO HIGH COUNTER
         L     8,GTMAD                 GET FIRST ENTRY
ADDA     CLC   LLIMIT,LOW              LOWER THAN LAST PRINTED ONE ?
         BNL   ADD8                    YES, OVERLAY THIS ENTRY
         CLC   HLIMIT,LOW              IS THIS HIGHER THAN LAST ?
         BNL   *+14                    NO
         MVC   HLIMIT,LOW              YES, SAVE
         ST    8,HADD                  REMEMBER WHERE
         LA    8,LISTLEN(8)            BUMP TO NEXT ONE
         C     8,CURR                  DONE ?
         BL    ADDA                    NO, KEEP ON
         OI    SPILL,1                 SET FOR TABLE OVERFLOW
         CLC   HLIMIT,NEWLOW           HIGHERTHAN HIGHEST IN TABLE ?
         BNH   ADDEX                   HIGHER, SKIP IT
         L     8,HADD                  REPLACE PREVIOUS HIGHES ONE
ADD8     MVC   TAB(LISTLEN),NEWENT     PUT ENTRY INTO TABLE
ADDEX    LM    1,8,RTSAVE              RESTORE
         BR    7                       RETURN
         SPACE
ADDB     LA    2,LISTLEN(8)            GET NEW END OF TABLE
         ST    2,CURR                  AND STASH
         B     ADD8                    MAKE ENTRY
         DROP  1
         EJECT
*        DATA AREA
ZERO     DC    D'0'          ZEROES
SAVE     DS    18F
RTSAVE   DS    8F
GTMLIST  DC    A(4096,524288)          MIN/MAX ACCEPTABLE STORAGE
GTMLIST2 DC    A(0,0)                  ACTUAL STORAGE GOTTEN     80340
GTMAD    DC    2F'0'                   RETURN FROM GETMAIN
LISTEND  DS    F                       END OF TABLE
CURR     DS    F                       LOGICAL END OF TABLE
HADD     DS    F
LINE1    DC    0F'0',AL2(1,1)  PRINT 1 LINE, ADD 1 LINE
LINE2    DC    AL2(2,1)                OVERPRINT 1 LINE, ADD 1 LINE
LINE     DS    F                       ACTUAL LINE REQUEST
LEJCT    DC    AL2(0,50)     FORCE EJECT
SEQ      DS    H                       SEQUENCE COUNTER
SIZE     DS    H
L1S2     DC    H'1,2'        PRINT 1 LINE, SET SKIP FOR 2
CULINE   DC    H'0'          CURRENT LINES LEFT ON PAGE
         SPACE 2
ALO      DC    0F'0'
ALOC     DS    CL2
ALOT     DS    CL2
AHI      DS    0CL4
AHIC     DS    CL2
AHIT     DS    CL2
BLO      DS    0CL4
BLOC     DS    CL2
BLOT     DS    CL2
BHI      DS    0CL4
BHIC     DS    CL2
BHIT     DS    CL2
LLIMIT   DC    XL6'0'                  CCHHSQ - LAST PROCESSED CC/HH/SQ
HLIMIT   DC    XL6'0'
CCHHR    DC    XL5'0'                  PARAMETER FOR 'OBTAIN'
SPILL    DC    X'0'                    TABLE OVERFLOW FLAG
SPILT    EQU   1             0 - NO SPILL; 1 - SPILL; 2 - FAKE   81117
         SPACE
RECORD   DC    CL132' '
         ORG   RECORD+2
RDSN     DC    CL50' '                 DS NAME
REXSQ    DC    CL5' '        EXT. NO
         DC    C' '
REXTYP   DC    CL10' '       TYPE OF EXTENT
         DC    CL10' '
RLCYL    DC    CL3' ',C'/'
RLTRK    DC    CL3' ',C' ('
RLHEX    DC    CL9' ',CL9')'
RHCYL    DC    CL3' ',C'/'
RHTRK    DC    CL3' ',C' ('
RHHEX    DC    CL9' ',CL3')'
RTTRK    DC    CL5' '
RTTRKFG  DC    C' '          SET TO '*' IF AVAILABLE SPACE
         ORG
REC2     DC    CL132'+'                FOR OVERPRINTING DSNAMES
         ORG   RDSN-RECORD+REC2
RDSN2    DC    CL50' '
         ORG
SUBTITLE DC    CL132'-'      TRIPLE SKIP FOR SUB-HEADER
         ORG   SUBTITLE+10
         DC    C'DATA SET NAME'
         DC    CL31' '
         DC    C'EXT    TYPE'
         DC    CL9' '
         DC    C'LOW CYL TRK   CC   HH     HIGH CYL TRK   CC   HH'
         DC    C'    TRACKS'
         ORG
HITRK    DC    H'0'          SET TO HIGH TRACK NO. IN CYLINDER
CVDLIST  DC    S(XSQ),H'1',S(REXSQ),AL2(L'REXSQ)  FOR CALL CVD
         DC    S(LOW),H'2',S(RLCYL),H'3'
         DC    S(LOW+2),H'2',S(RLTRK),H'3'
         DC    S(HIGH),H'2',S(RHCYL),H'3'
         DC    S(HIGH+2),H'2',S(RHTRK),H'3'
         DC    S(SIZE),H'2',S(RTTRK),H'5'
         DC    H'0'                    END
CVHLIST  DC    S(LOW),H'4',S(RLHEX),H'9'
         DC    S(HIGH),H'4',S(RHHEX),H'9',H'0'
         SPACE
EXTFG    DC    AL1(1,2,4,X'40',X'80',X'81')
EXTAB    DC    CL10'   DATA'
         DC    CL10' OVERFLOW'
         DC    CL10'INDEX AREA'
         DC    CL10'USER LABEL'
         DC    CL10'SPLIT CYL.'
         DC    CL10'CYL BOUND.'
*
ERRMSG   DC    CL132'0     ''OBTAIN'' ERROR   --   INDEX TERMINATED'
NOSTORMS DC    CL132'0     INSUFFICIENT STORAGE FOR INDEX. RERUN IN LAR*
               GER REGION'                                       80340
         SPACE 2
         LTORG
         SPACE 5
TAB      DSECT
DSN      DS    CL44
XFG      DS    X
XFGLAST  EQU   X'FF'         LAST ENTRY FLAG                     81117
XSQ      DS    X
LOW      DS    CL6                     CCHHSQ / START CYL, TRK, SEQ #
HIGH     DS    CL4                     END CCHH
         DS    0H                      FORCE BOUNDARY
LISTLEN  EQU   *-TAB                   LENGTH OF ONE TABLE ENTRY
         SPACE 3
VTOCINDX CSECT
NEWENT   DS    0D
NEWDSN   DS    CL44
NEWXFG   DS    X
NEWXSQ   DS    X
NEWLOW   DS    CL6
NEWHIGH  DS    CL4
         SPACE 3
VTOCCOMM DSECT
VOLFREE  DS    F
VOLABEL  DS    H
VOLIPL   DS    H
CYLTRK   DS    H
DS5FLG   DS    X
         DS    X
F4ADDR   DS    F
WKADDR   DS    F
VOLTOT   DC    F'0'          TOTAL TRASKS ON VOLUME
VOLMISS  DC    F'0'          TRACKS NOT ACCOUNTED FOR
VOLUSE   DC    F'0'          TRACKS IN USE
VOLATOT  DC    H'0'          TOTAL ALTERNATE TRACKS ON VOLUME
VOLAUSE  DC    H'0'          ALTERNATE TRACKS IN USE
VOLAFREE DC    H'0'          ALT. TRAKCS FREE
MAXLINE  DS    H             MAX LINES PER PAGE
LCNT     DS    F             CURRENT LINES LEFT
PAGE     DS    F             CURRENT PAGE NO.
         SPACE 1                                                 81117
R0       EQU   0                                                 81117
R1       EQU   1                                                 81117
R2       EQU   2                                                 81117
R3       EQU   3                                                 81117
R4       EQU   4                                                 81117
R5       EQU   5                                                 81117
R6       EQU   6                                                 81117
R7       EQU   7                                                 81117
R8       EQU   8                                                 81117
R9       EQU   9                                                 81117
R10      EQU    10                                               81117
R11      EQU    11                                               81117
R12      EQU    12                                               81117
R13      EQU    13                                               81117
R14      EQU    14                                               81117
R15      EQU    15                                               81117
         END
