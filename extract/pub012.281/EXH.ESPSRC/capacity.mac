CAPCY    TITLE 'TEST OF TRACK CAPACITY CODE'
         PUNCH '  ORDER CAPACITY(P) '  EASIER DUMPS
         PUNCH ' ALIAS CAPACARD '
         SPACE 1
         COPY  OPTIONGB      DEFINE GLOBALS
         SPACE 2
         SYSPARM LIST=YES    SET GLOBALS
         EJECT ,
         PRINT &PRTSOR
CAPACITY PGMHEAD ZERO12,BASE=R12,PARM=R9,                              *
               ENTRY=CAPACARD,ENTNO=INCBLOCK                    GP99125
         L     R3,INCBLOCK   ENTRY NUMBER (0 - CAPACITY; 1 - CAPACARD)
         MH    R3,=H'79'     FINAGLE                            GP99125
         LA    R3,1(,R3)     CAPACITY=1  CAPACARD=80            GP99125
         ST    R3,INCBLOCK   UPDATE INCREMENT                   GP99125
         SERVINIT ,          LOAD/INITIALIZE FOR SERVICE CALLS
         SERVCALL LPALD,=CL8'@PRINTER'  LOAD THE PRINTER ROUTINE
         ST    R0,@PRINTER   SAVE IT
         PRTOPEN SYSPRINT,OPT=ABEND  BOMB IF NOT SUPPLIED
         MVC   GENMATCH,BLANKS  CLER FOR MOVE                   GP99125
         LA    R9,0(,R9)     CLEAN                              GP99125
         LTR   R9,R9         PARM POINTER POINTER?              GP99125
         BZ    NOPARM                                           GP99125
         L     R9,0(,R9)                                        GP99125
         LTR   R9,R9         PARM POINTER?                      GP99125
         BZ    NOPARM                                           GP99125
         LH    R8,0(,R9)     GET LENGTH                         GP99125
         LTR   R8,R8         ANY PARM AT ALL ?                  GP99125
         BZ    NOPARM        NONE IS ACCEPTABLE                 GP99125
         CH    R8,=H'2'      VALID?                             GP99125
         BL    BADPARM       NO; IGNORE                         GP99125
         BE    HEXPARM       EXPECT HEX DEVICE TYPE             GP99125
         CH    R8,=H'4'      ELSE 4-8 FOR GENERIC               GP99125
         BL    BADPARM                                          GP99125
         CH    R8,=H'8'      NOT TOO LONG                       GP99125
         BH    BADPARM                                          GP99125
         BCTR  R8,0          SET EXECUTE LENGTH                 GP99125
         EX    R8,EXMVGEN    MOVE GENERIC                       GP99125
         B     NOPARM                                           GP99125
EXMVGEN  OC    GENMATCH(0),2(R9)                                GP99125
         SPACE 1                                                GP99125
HEXPARM  XC    GENMATCH,GENMATCH  CLEAR MATCH                   GP99125
         MVC   DB(2),2(R9)   COPY PARM                          GP99125
         TRT   DB(2),TRTHEX  VALID HEX?                         GP99125
         BNZ   BADPARM       NO                                 GP99125
         TR    DB(2),TRHEX   MAKE HEX VALUES                    GP99125
         PACK  DB+4(2),DB(3)  PACK                              GP99125
         MVC   GENMATCH+7(1),DB+4   MOVE HEX TYPE               GP99125
         B     NOPARM                                           GP99125
         SPACE 1                                                GP99125
BADPARM  PRTV  MSGBDPRM                                         GP99125
         OI    CCODE+L'CCODE-1,8   SET ERROR CODE               GP99125
         B     NOPARM                                           GP99125
MSGBDPRM VCON  ' ***INVALID PARM FIELD IGNORED***'              GP99125
         SPACE 1                                                GP99125
NOPARM   LA    R5,1          SET FOR LOWEST DESIRED DEVICE (2311)
         CLI   GENMATCH,0    HEX TYPE REQUESTED?                GP99125
         BNE   BIGLOOP       NO                                 GP99125
         IC    R5,GENMATCH+7  SET IT                            GP99125
BIGLOOP  SERVCALL DVTBL,(R5)  GET DEVICE TABLE ADDRESS
         CH    R15,=H'8'     VALID DEVICE ?
         BNL   BIGBUMP       NO; SKIP UNSUPPORTED DEVICE
         ST    R0,STARDCTA   SET TABLE ADDRESS
         LR    R8,R0         COPY DEVICE ADDRESS
         USING DVCCYL,R8     DECLARE DEVICE MAPPING
         ICM   R0,3,DVCTRKLN GET DEVICE MAXIMUM TRACK SIZE
         STH   R0,MAXSIZE+2  SAVE IT
         MVI   STARFLGS,STARFUNC+STARMAXS+STARLOC  GET MAXSIZE
         SERVCALL DVCAP,STARLING  GET MAXIMUM SIZE
         MVC   MAXRECD+2(2),STARBAL  SAVE MAXIMUM RECORD
         XC    DB,DB
         STC   R5,DB+7
         MVI   DB+6,X'20'    FAKE DASD UCB
         LA    R1,DB+6-(UCBTBYT3-UCBOB)  SET FAKE UCB ADDRESS
         SERVCALL UCBGN,(R1) GET GENERIC
         CLI   GENMATCH,C' '  MATCH BY GENERIC?                 GP99125
         BNH   NOMATCH                                          GP99125
         CLC   GENMATCH,0(R1)  MATCHES?                         GP99125
         BNE   BIGBUMP       NO; GET ANOTHER                    GP99125
NOMATCH  MVC   TYPE,0(R1)    MOVE RESPONSE - GENERIC TYPE       GP99125
         XR    R6,R6         SET KEY LENGTH
         LA    R0,1          SET NORMAL KEY INCREMENT
         ST    R0,KEYMIN     SET MINIMUM KEY
         LA    R1,255        IF NOT MODULO, USE 255 FOR MAX KEY
         TM    DVCFLAGS,X'10'  DVCMODU   MODULO DEVICE ?        GP12046
         BZ    KEYINIT       NO; BEGIN PROCESSING
         ICM   R0,1,DVCOVNK  GET AND TEST MODULUS                84320
         BNZ   *+8           OK; OLD FORMAT                      84320
         ICM   R0,3,DVCMOD1  GET MODULUS FROM NEW RECORD         84320
         XR    R14,R14       CLEAR FOR DIVIDE
         LH    R15,DVCTOL    GET KEY/DATA ADJUSTMENT
         DR    R14,R0        GET REMAINDER
         LR    R1,R0         COPY MODULUS
         BCTR  R1,0          MAXIMUM KEY IN SAME CYCLE AS MINIMUM
         SR    R1,R14        LESS REMAINDER
         BP    KEYINIT       OK
         LR    R1,R0         USE MODULUS AS LIMIT
KEYINIT  ST    R0,KEYINC     SET KEY INCREMENT
         LA    R14,255       MAX KEY                             84320
         CR    R1,R14        EXCEEDS MAX ?                       84320
         BNH   *+6           NO                                  84320
         LR    R1,R14        TRUNCATE TO MAX                     84320
         ST    R1,KEYMAX     SET MAXIMUM KEY
KEYLOOP  L     R7,INCBLOCK   SET DATA LENGTH (START AT 0 OR 80)
         STC   R6,DB         STASH KEY LENGTH FOR FORMATTING
         PRTLIST HEADER,TITLE=1
         PRTLIST SUBHEAD,TITLE=3
         LTR   R6,R6         ANY KEY ?
         BNZ   KEYDONE       YES; NOT FIRST TIME FOR DEVICE
         PRTLIST DEVINFO     PRINT INTERESTING STATISTICS
         PRTSPACE 2
*                     NOTE: STARLAST OBSOLETE (ISAM ONLY)
KEYDONE  MVI   STARFLGS,STARFUNC+STARLOC  CAPACITY   (+STARLAST)
         STC   R6,STARKL     SET KEY LENGTH
         STH   R7,STARDL
         LTR   R7,R7         DATA LENGTH = 0 ?
         BNZ   *+8           NO
         OI    STARDL+L'STARDL-1,1  MAKE = 1
         MVI   STARR,1       RECORD NUMBER IS ALWAYS 1          GP99133
         SERVCALL DVCAP,STARLING  INITIAL CALL
         CH    R15,=H'4'     VALID?                             GP99133
         BNL   BIGBUMP       NO; SKIP PROBLEM DEVICE            GP99133
         LTR   R0,R0         ANY ?
         BNP   KEYBUMP       NO ???
         ST    R7,MINBLOCK
         ST    R7,MAXBLOCK
         MIN   R0,=F'255'    BUT NOT TOO LARGE                  GP99125
         ST    R0,NUMBLOCK
         A     R7,INCBLOCK                                      GP99125
DATALOOP STH   R7,STARDL     SET DATA LENGTH
         MVI   STARR,1       RECORD NUMBER IS ALWAYS 1          GP99133
         SERVCALL DVCAP,STARLING  GET BLOCKS PER TRACK
*TEST*   TRKCALC MF=(E,STARLING)
         CH    R15,=H'4'     VALID?                             GP99133
         BNL   KEYPOST       NO; DONE WITH THIS TYPE            GP99133
         LTR   R0,R0         ANY ?
         BNP   KEYPOST       NO; BUMP KEY LENGTH                GP99133
         C     R0,NUMBLOCK   SAME NUMBER OF BLOCKS PER TRACK ?
         BE    DATASAME      YES; UPDATE MAXBLOCK
         LR    R9,R0         SAVE SIZE
         BAL   R11,LINEPRT   PRINT RECORD LINE
         ST    R7,MINBLOCK   SET FOR NEXT TIME
         ST    R7,MAXBLOCK
         ST    R9,NUMBLOCK
         B     DATABUMP
DATASAME ST    R7,MAXBLOCK   UPDATE MAXIMUM BLOCK FOR THIS BLK/TRK
DATABUMP A     R7,INCBLOCK   UP DATA LENGTH                     GP99125
         C     R7,=F'65535'  OVER MAX ?
         BNH   DATALOOP      NO; TRY AGAIN
KEYPOST  BAL   R11,LINEPRT   PRINT FINAL LINE
KEYBUMP  LTR   R6,R6         KEY LENGTH=0 ?
         BP    KEYNZERO      NO
         L     R6,KEYMIN     USE MINIMUM KEY
         B     KEYLOOP
KEYNZERO TM    DVCFLAGS,DVCMODU  MODULO DEVICE ?
         BZ    BIGBUMP       NO; DONE WITH KEYS
         LM    R14,R15,KEYMIN   GET MINIMUM/MAXIMUM KEY LENGTH
         L     R6,KEYINC     GET INCREMENT (MODULUS)
         LA    R14,1(,R15)   NEW MINIMUM IS PRIOR MAX +1
         CH    R14,=H'255'   LARGER THAN MAX ?
         BH    BIGBUMP       YES; QUIT
         AR    R15,R6        UP MAXIMUM
         CH    R15,=H'255'   GREATER THAN MAX ?
         BNH   *+8           NO
         LA    R15,255       ELSE SET MAXIMUM
         STM   R14,R15,KEYMIN   SET NEW MINIMUM AND MAXIMUM KEY
         LR    R6,R14        USE NEW MINIMUM AS SIZE
         B     KEYLOOP       DO AGAIN
BIGBUMP  LA    R5,1(,R5)     GET NEXT DEVICE TYPE
         CLI   GENMATCH,0    SPECIFIC DEVICE BY HEX TYPE?       GP99133
         BE    CHECKSUM      YES; SKIP LOOP                     GP99133
         CH    R5,=Y(DVCTYPMK)  REACHED MAXIMUM ?
         BNH   BIGLOOP       NO; DO AGAIN
*OLD*    CLI   GENMATCH,C' ' SPECIFIC DEVICE ?                  GP99134
*OLD*    BNE   GOODEXIT      YES; SKIP SUMMARY                  GP99134
CHECKSUM CLI   INCBLOCK+L'INCBLOCK-1,1  CARD ENTRY?             GP99134
         BH    GOODEXIT      YES; SKIP IMPROPER SUMMARY         GP99134
         SPACE 2
         PRTOPEN SUMPRINT,OPT=NOWTO,DEV=2  OPTIONAL SUMMARY     GP99134
         PRTLIST SUMHEAD,TITLE=1,DEV=(1,2)                      GP99134
         PRTLIST SUMSUB,TITLE=3,DEV=(1,2)                       GP99134
         LA    R5,1          SET FOR LOWEST DEVICE AGAIN
SUMLOOP  SERVCALL DVTBL,(R5)   DEFINED DEVICE ?
         CH    R15,=H'8'     DEFINED ?
         BNL   SUMBUMP       NO; KICK OUT
         LR    R15,R5        COPY DEVICE TYPE                   GP99134
         SLL   R15,3         MAKE SUBSCRIPT                     GP99134
         L     R15,WYLSIZES(R15)  POINT TO SAVED VALUES         GP99134
         LTR   R15,R15       ANYTHING STASHED?                  GP99134
         BZ    SUMBUMP       NO; DO ANOTHER DEVICE              GP99134
         ST    R0,STARDCTA   STASH IN TABLE
         LR    R8,R0         COPY ADDRESS
         ST    R5,DB+4
         MVI   DB+6,X'20'
         LA    R1,DB+6-(UCBTBYT3-UCBOB)
         SERVCALL UCBGN,(R1)   GET GENERIC AGAIN
         MVC   TYPE,0(R1)
         MVI   STARFLGS,STARFUNC+STARLOC  SET CAPACITY, DVCT    GP99134
         MVC   STARRKDD,=X'01,08,0100'  R=1, KL=08, DL=256      GP99134
         SERVCALL DVCAP,STARLING
         ST    R0,PDSTRK     SAVE D.E.S PER TRACK
         MVC   STARRKDD,=X'01,2C,0060'  R=1, KL=44, DL=96       GP99134
         SERVCALL DVCAP,STARLING
         ST    R0,DSCBSTK    SAVE DSCB PER TRACK
         LR    R15,R5        COPY DEVICE TYPE
         SLL   R15,3         MAKE SUBSCRIPT
         LA    R15,WYLSIZES(R15)  POINT TO SAVED VALUES
         MVC   MAXWYL(8),0(R15)  COPY
         XR    R14,R14
         L     R15,MAXWYL    GET MAXIMUM WYLBUR BLOCK
         LA    R0,80         GET CARD SIZE
         DR    R14,R0        GET CARDS PER WYLBUR BLOCK
         MR    R14,R0        GET SIZE
         ST    R15,MAXCARD
         PRTLIST SUMLINE,DEV=(1,2)     PRINT                    GP99134
SUMBUMP  LA    R5,1(,R5)     SET NEXT VALUE
         CH    R5,=Y(DVCTYPMK)  DONE ?
         BNH   SUMLOOP       NO; DO MORE
GOODEXIT SERVTERM ,          CLOSE AND FREE EVERYTHING          GP99133
         LH    R9,CCODE      SET RETURN CODE                    GP99125
         PGMEXIT RC=(R9)     RETURN WITH CODE                   GP99125
         SPACE 1
LINEPRT  LTR   R6,R6         ANY KEY LENGTH ?
         BZ    LINEPRTX      NO; JUST PRINT
         TM    DVCFLAGS,DVCMODU  MODULO DEVICE ?
         BNZ   LINEPRTX      YES; JUST PRINT
         LM    R14,R15,MINBLOCK  GET BLOCK SIZES
         AR    R14,R6
         AR    R15,R6        ADJUST FOR KEY LENGTH
         STM   R14,R15,MINBLOCK
LINEPRTX LTR   R6,R6         ANY KEY LENGTH ?
         BNZ   LINEPRTP      YES; SKIP STATISTICS
         LR    R15,R5        COPY DEVICE TYPE
         SLL   R15,3         *8 = INDEX INTO RECSIZES AND WYLSIZES
         L     R14,MAXBLOCK  GET CURRENT MAX BLOCK
         CH    R14,=H'32760' LARGER THAN OS MAXIMUM ?
         BH    LINEPRTP      YES; DO NOT SAVE
         ST    R14,RECSIZES(R15)  UPDATE
         CH    R14,=H'4100'  EXCEEDS PREFERRED WYLBUR MAXIMUM ?
         BH    LINEPRTP      YES
         ST    R14,WYLSIZES(R15)  UPDATE IT ALSO
LINEPRTP PRTLIST RECLINE     PRINT ONE ENTRY
         BR    R11           RETURN TO CALLER
         SPACE 2
SUMPRINT PRTWORK SUMPRINT,TITLE=5   DEV=2 OPTIONAL SUMMARY      GP99134
SYSPRINT PRTWORK SYSPRINT,SYSTERM,TITLE=5                       GP99134
         SPACE 1
HEADER   FDOPT NL,CC=C'#'    AUTO TIME/PAGE/DATE
         FDPRT 'DEVICE CAPACITY FOR',NL
         FDPRT TYPE,PAD,DEB
         FDSPACE 15
         FDCLI DB,0,BE=HEADERNK   UNKEYED BLOCKS
         FDTM  DVCFLAGS,DVCMODU,BZ=HEADERKD
         FDPRT 'KEY LENGTH'
         FDPRT KEYMIN,I,PAD
         FDCLC KEYMIN,KEYMAX,BE=HEADERNQ                        GP97241
         FDPRT 'TO'
         FDPRT KEYMAX,I,PADL
HEADERNQ FDPRT *END
HEADERNK FDPRT 'WITHOUT KEYS'
         FDPRT *END
HEADERKD FDPRT 'WITH KEYS'
         FDPRT *END
         SPACE 1
SUBHEAD  FDPRT 'BLOCKS PER TRACK',NL,LEN=20
         FDPRT 'MIN   BLOCKSIZE   MAX'
         FDPRT *END
         SPACE 1
RECLINE  FDPRT NUMBLOCK,I,RADJ,NL,LEN=11
         FDPRT MINBLOCK,I,PADR,RADJ,LEN=14
         FDPRT MAXBLOCK,I,PADR,RADJ,LEN=15
         FDPRT *END
         SPACE 1
DEVINFO  FDPRT 'DEVICE CONSTANTS FOR',NL
         FDPRT TYPE,PAD
         FDPRT 'UCB TYPE',PAD
         FDPRT DB+6,2,HEX,PAD
         FDPRT MAXSIZE,I,RADJ,PADR,NL,LEN=8
         FDPRT 'BYTES PER TRACK,'
         FDPRT DVCTRK,I,RADJ,PADR,LEN=5
         FDPRT 'TRACKS PER CYLINDER,'
         FDPRT DVCCYL,I,RADJ,PADR,LEN=8
         FDPRT 'CYLINDERS,'
         FDPRT DVCALT,I,RADJ,PADR,LEN=8
         FDPRT 'ALTERNATE TRACKS.'
         FDTM  DVCFLAGS,DVCMODU,BO=DEVINFOM  MODULO DEVICE
         FDPRT 'OVERHEAD:',NL
         FDTM  DVCFLAGS,DVC2BOV,BO=DEVINFOH  TWO-BYTE OVERHEAD
         FDPRT DVCOVNLB,I,RADJ,PADR,LEN=8
         FDPRT 'BYTES FOR NOT-LAST BLOCK,'
         FDPRT DVCOVLB,I,RADJ,PADR,LEN=8
         FDPRT 'BYTES FOR LAST BLOCK,'
         FDGOTO DEVINFOK     DO UNKEYED DECREMENT
DEVINFOH FDPRT DVCOVHD,I,RADJ,PADR,LEN=8
         FDPRT 'BYTES PER KEYED BLOCK,'
DEVINFOK FDPRT DVCOVNK,I,RADJ,PADR,LEN=8
         FDPRT 'BYTES LESS FOR UNKEYED BLOCK.'
         FDSPACE 5
         FDTM  DVCFLAGS,DVCFTOL,BO=DEVINFOT
         FDPRT 'NO TOLERANCE ADJUSTMENT REQUIRED.'
         FDGOTO DEVINFOR
DEVINFOT FDPRT 'TOLERANCE ADJUSTMENT IS'
         FDPRT DVCTOL,I,PADL
         FDPRT '/512 (EXCEPT LAST BLOCK).'
         FDGOTO DEVINFOR
DEVINFOM FDPRT 'KEY/DATA SIZES ARE ADJUSTED MODULO',NL
         FDCLI DVCOVNK,0,BE=DEVINFON                             84320
         FDPRT DVCOVNK,I,PADL                                    84320
         FDGOTO DEVINFOO                                         84320
DEVINFON FDPRT DVCMOD1,I,PADL                                    84320
DEVINFOO FDPRT '. KEY AND DATA OVERHEAD IS',PADR                 84320
         FDPRT DVCTOL,I,LEN=5
         FDPRT 'BLOCK OVERHEAD IS'
         FDPRT DVCOVHD,I,PADL
DEVINFOR FDCLI DB+7,9,BL=DEVINFOX
         FDPRT 'RPS CONSTANTS ARE:',NL
         FDPRT DVCOVR0,I,RADJ,PADR,LEN=8
         FDPRT 'BYTES RECORD 0 OVERHEAD,'
         FDPRT DVCSECT,I,RADJ,PADR,LEN=5
         FDPRT 'SECTORS ON FULL TRACK,'
         AIF   (NOT &MVSESA).NOTESA                             GP97241
         FDPRT DVCBPSEC,I,RADJ,PADR,LEN=5                       GP97241
         FDPRT 'BYTES/SECTOR,'                                  GP97241
.NOTESA  AIF   (&MVSXA).XASECT                                   94220
         FDPRT DVCSECTD,I,RADJ,PADR,LEN=5
         AGO   .CMSECT                                           94220
.XASECT  FDPRT DVCDSECT,I,RADJ,PADR,LEN=5                        94220
.CMSECT  FDPRT 'DATA SECTORS.'
DEVINFOX FDPRT *END
         SPACE 1
SUMHEAD  FDOPT NL,CC=C'#'    NEW TITLE
         FDPRT 'FREQUENTLY USED VALUES'
         FDPRT *END
         SPACE 1
SUMSUB   FDPRT 'DEVICE',NL,LEN=10
         FDPRT 'CYLINDERS',LEN=10
         FDPRT 'TRKS/CYL',LEN=10
         FDPRT 'PDS/TRACK',LEN=10
         FDPRT 'DSCBS/TRK',LEN=20
         FDPRT 'LKED/SAS',LEN=10
         FDPRT '  WYLBUR',LEN=10
         FDPRT 'CARD FORMAT'
         FDPRT *END
         SPACE 1
SUMLINE  FDPRT TYPE,NL
         FDPRT DVCCYL,I,RADJ,LEN=10
         FDPRT DVCTRK,I,RADJ,LEN=10
         FDPRT PDSTRK,I,RADJ,LEN=10
         FDPRT DSCBSTK,I,RADJ,LEN=10
         FDPRT MAXRECD,I,RADJ,LEN=20
         FDPRT MAXWYL,I,RADJ,LEN=10
         FDPRT MAXCARD,I,RADJ,LEN=10
         FDPRT *END
         SPACE 2
BLANKS   DC    CL8' '        CONSTANT                           GP99125
TRTHEX   DC    256X'FF'      MAKE ALL VALUES INVALID            GP99125
         TRENT TRTHEX,0,(C'A',6),(C'0',10)  VALIDATE HEX        GP99125
         TRENT TRTHEX,0,(X'81',6)  ALSO LOWER CASE              GP99125
TRHEX    DC    256X'00'      MAKE ALL VALUES IRRELEVANT         GP99125
         TRENT TRHEX,(*-TRHEX+10-X'81'),(X'81',6)  DEFINE LOWER A-F
         TRENT ,(*-TRHEX+10-C'A'),(C'A',6)  DEFINE UPPER A-F    GP99125
         TRENT ,(*-TRHEX-C'0'),(C'0',10)  DEFINE DIGITS         GP99125
         SPACE 2                                                GP99125
SAVE     DSECT ,             CONTINUATION OF WORK/SAVE AREA
@SERVICE DS    A
@PRINTER DS    A
         PRINT &PRTMAC
STARLING SERVCALC MF=D       EXPAND MAPPING
DB       DS    D
GENMATCH DS    CL8' '        DESIRED GENERIC TYPE, OR HEX DEVTYPE
TYPE     DC    CL8' '        GENERIC TYPE
CCODE    DS    H             RETURN CODE                        GP99125
MAXWYL   DS    0F
MAXSIZE  DS    F             DEVICE TRACK LENGTH
MAXRECD  DS    F             MAXIMUM BLOCK
INCBLOCK DS    F             BLOCK START/INCREMENT              GP99125
PDSTRK   DS    0F            PDS DIRECTORY ENTRIES PE TRACK
MINBLOCK DS    F             MINIMUM BLOCKSIZE FOR CURRENT BLOCKS/TRACK
DSCBSTK  DS    0F            DSCBS PE TRACK
MAXBLOCK DS    F             MAXIMUM BLOCKSIZE
MAXCARD  DS    0F            MAXIMUM CARD BLOCKSIZE < WYLBUR
NUMBLOCK DS    F             BLOCKS PER TRACK FOR ABOVE SIZES
KEYINC   DS    F             INCREMENT FOR KEY
KEYMIN   DS    F             MINIMUM KEY
KEYMAX   DS    F             MAXIMUM KEY IN MODULUS CYCLE
WYLSIZES DS    F             WYLBUR SIZE; INDEXED BY DEVICE TYPE
RECSIZES DS    F             MAXIMUM RECORD SIZE; INDEXED
         DS    30F           REMAINDER OF INTERLEAVED ARRAYS
SAVEEND  EQU   *                                                GP99125
         SPACE 2
         PRINT &PRTSYS
CVTDSECT DSECT ,
         CVT   DSECT=YES
         IHACDE ,
         IEFUCBOB ,
         SPACE 1
         PRINT GEN
         IHADVCT ,           MAP DEVICE CONSTANTS
DVCMODU  EQU   X'10'           MODULO DEVICE                    GP12063
DVCMOD1  EQU   DVCBPSEC+2,2,C'A'  MODULUS                       GP12063
         END   ,
