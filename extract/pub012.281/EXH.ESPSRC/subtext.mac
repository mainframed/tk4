STEXT    TITLE 'S U B T E X T  ***  CONVERT BIT FLAGS TO TEXT'
***********************************************************************
**                                                                   **
**  This subroutine accepts three or four parameters:                **
**   Address of the output area (no length check performed)          **
**   Address of a flag byte                                          **
**   Address of a table (bit, text string) as created by FLGTAB      **
**   (Optional) Address of options:                                  **
**   nn: Maximum text length (0->256)                                **
**   n0: Length of separator (blank padded)  default is 0            **
**   0n: Length of byte mask to be tested    default is 1            **
**   nn: Type of separator (see SEPTABLE)    default is 1 (comma)    **
**   xx: Flags 01 - prepend separator before first hit               **
**                                                                   **
**  Code adapted from CONVFLAG in EXHASCRN                           **
**  Code is serially reusable, not reentrant nor refreshable.        **
**                                                                   **
***********************************************************************
         SPACE 2
SUBTEXT  START 0
         STM   R14,R12,12(R13)
         USING SUBTEXT,R15   LIVE DANGEROUSLY
         XC    WORKAREA(WORKLEN),WORKAREA
         STM   R0,R15,CALLREGS
         MVCH  SEPTXT        BLANK SEPARATOR
         LA    R10,DEFOPTS   POINT TO DEFAULTS
         LM    R7,R9,0(R1)   LOAD REQUIRED ADDRESSES
         LTR   R9,R9         VL BIT SET?
         BM    NOOPTS          NO; USE DEFAULT
         L     R10,12(,R1)   LOAD OPTIONS POINTER
NOOPTS   DS    0H              DEFER
         USING PARM1,R7
         USING PARM2,R8
         USING FGTENT,R9
         USING PARM4,R10
         ZICM  R12,UTLEN     GET TEXT LENGTH
         BP    UTLENOK
         LA    R12,256       SET TO MAX
UTLENOK  SH    R12,=H'1'     EXEC LENGTH
         BM    SUBERR
         LA    R14,FLAGTEXT(R12)
         ST    R14,MAXOUT    SAVE FOR LATER COMPARES
         MVI   FLAGTEXT,C' '
         SH    R12,=H'1'     EXEC LENGTH FOR MOVE
         BM    SHORTOUT
         EX    R12,EXBLANK   BLANK FOR SPACING
SHORTOUT IC    R3,UMLEN
         SRL   R3,4          ISOLATE SEP LEN
         N     R3,=X'0000000F'  MASK
         ST    R3,SEPLEN     SAVE FOR LATER
         IC    R3,USTYP      GET SEPARATOR TYPE
         LA    R3,SEPTABLE(R3)    GET SEPARATOR CHARACTER
         MVC   SEPTXT(1),0(R3)    SAVE IT
         IC    R3,UMLEN
         N     R3,=X'0000000F'  MASK
         BZ    SUBERR             TOO BAD
         BCTR  R3,0          MAKE EXECUTE LENGTH
         EX    R3,EXMVCDB3   MOVE TO MY WORK SPACE
         TM    UFLAGS,1      PREPEND SEPARATOR?
         BZ    *+8             NO
         OI    FLAGS,FGSEP   SHOW SEPARATOR NEEDED
         LA    R6,FLAGTEXT   START OF OUTPUT
         SR    R5,R5         CLEAR TEXT LENGTH FLAG
FFGLOOP  ICM   R5,1,FGTLEN   END OF TABLE ?
         BM    SUBEXIT       YES; ALL DONE
         EX    R3,EXZERMSK   IS THIS A NULL MASK ?
         BNE   FFGTEST       NO; TEST FOR MATCH
         TM    FLAGS,FGHIT   DID WE HAVE A PRIOR MATCH ?
         BZ    FFGMOVE       NO; MOVE THIS ONE
         NI    FLAGS,255-FGHIT  RESET HIT FLAG
         B     FFGNEXT       AND BUMP
         SPACE 1
FFGTEST  MVC   CBWORK,CB         MOVE COPIED FLAGS TO TEST AREA
         EX    R3,EXANDMSK   AND MASK BITS
         EX    R3,EXCLCMSK   IS THIS A MATCH ?
         BNE   FFGNEXT       NO, TRY NEXT ONE
         OI    FLAGS,FGHIT   SHOW MATCH
         SPACE 1
FFGMOVE  TM    FLAGS,FGSEP   DO WE NEED A SEPARATOR ?
         BZ    FFGMOVES      NO; BUT NEXT TIME
         L     R2,SEPLEN     LOAD SEPARATOR LENGTH
         SH    R2,=H'1'        PREPARE FOR MOVE
         BM    FFGMOVES      NO SEPARATOR WANTED
         LA    R12,1(R2,R6)  LENGTH AFTER SEP
         C     R12,MAXOUT    WILL IT FIT ?
         BH    SUBTEND         NO
         LA    R14,SEPTXT
         EX    R2,EXMVCR14   MOVE SEPARATOR
         LA    R6,1(R2,R6)     INCREMENT OUTPUT LENGTH
FFGMOVES OI    FLAGS,FGSEP   NEED A SEPARATOR NEXT TIME
         LA    R12,1(R5,R6)
         C     R12,MAXOUT    WILL IT FIT ?
         BNH   FFGMOVER      YES
SUBTEND  OICC  4             SET WARNING - TRUNCATION
SUBEXIT  DS    0H
TRIMLOOP BCTR  R6,0
         CLI   0(R6),C' '    TRAILING BLANK?
         BE    TRIMLOOP        NO
         LA    R6,1(,R6)     NEXT AVAILABLE
         LA    R14,FLAGTEXT
         SR    R6,R14
         BP    SETOUT
         SLR   R6,R6
SETOUT   ST    R6,OUTLEN
         SPACE 1
         LM    R14,R12,12(R13)
         LM    R15,R0,RETCODE
         BR    R14
         SPACE 1
FFGMOVER LA    R14,FGTTEXT   POINT TO SOURCE
         EX    R5,EXMVCR14   MOVE TO OUTPUT AREA
         MVC   CBWORK,=16X'FF'   PREPARE TO COMPLIMENT
         EX    R3,EXEXODB        COMPLEMENT TEST BITS
         NC    CB,CBWORK         AND TURN OFF
         LA    R6,1(R5,R6)   NEXT OUTPUT POSITION
         SPACE 1
FFGNEXT  LA    R9,3(R3,R9)   POINT TO NEXT INPUT TABLE POSITION
         AR    R9,R5           PLUS TEXT LENGTH
         B     FFGLOOP       TEST FOR END OR NEXT
         SPACE 1
SUBERR   LM    R14,R12,12(R13)
         SLR   R0,R0         ZERO LENGTH
         LA    R15,8         SET ERROR
         BR    R14
         SPACE 1
EXMVCDB3 MVC   CB(0),FLAGS   MOVE USER'S VALUE FOR MODIFICATION
EXZERMSK CLC   FGTMASK(0),=XL16'0'   NULL MASK ?
EXANDMSK NC    CBWORK(0),FGTMASK AND USER'S MASK
EXCLCMSK CLC   CBWORK(0),FGTMASK COMPARE RESULT TO USER'S MASK
EXMVCR14 MVC   0(0,R6),0(R14) MOVE FROM TABLE
EXEXODB  XC    CBWORK(0),FGTMASK COMPLEMENT MASK BITS
EXBLANK  MVC   FLAGTEXT+1(*-*),FLAGTEXT   BLANK OUTPUT AREA
SEPTABLE DC    C' ,;-'
         SPACE 1
         LTORG ,
         SPACE 1
WORKAREA DS    0D            LATER DYNAMIC?
CB       DS    XL16          WORK SPACE
CBWORK   DS    XL16          WORK SPACE
CALLREGS DS    16A           COPY OF USERS REGISTERS
MAXOUT   DS    A             LAST OUTPUT BYTE
RETCODE  DS    F    1/2
OUTLEN   DS    F    2/2      LENGTH OF TEXT IN WORK AREA
SEPLEN   DS    F             ITEM SEPARATOR LENGTH (0-3)
SEPTXT   DS    CL8           ITEM SEPARATOR
FLAGS    DS    X
FGHIT    EQU   X'80'
FGSEP    EQU   X'01'         PLACE SEPARATOR BETWEEN ITEMS
WORKEND  EQU   *
WORKLEN  EQU   WORKEND-WORKAREA
         SPACE 1
FGTENT   DSECT ,
FGTLEN   DS    X             LENGTH-1 OF TEXT
FGTMASK  DS    X             MASK OF LENGTH (R3) {LATER (R3)-1}
FGTTEXT  DS    0C            VARIABLE LENGTH TEXT
         SPACE 1
PARM1    DSECT ,
FLAGTEXT DS    CL256         VARIABLE LENGTH 0-255
         SPACE 2
PARM2    DSECT ,
UFLAG    DS    X             VARIABLE LENGTH 1-15
         SPACE 2
PARM3    DSECT ,
UTABLE   DS    A             FLAG=>TEXT TABLE
         SPACE 2
PARM4    DSECT ,
UOPTS    DS    0XL4
UTLEN    DS    X             MAX LENGTH OF OUTPUT
USLEN    DS    0XL.4         LENGTH OF SEPARATOR
UMLEN    DS    XL.4          LENGTH OF MASK
USTYP    DS    X             TYPE OF SEPARATOR (0-SPACE, 1-COMMA...)
UFLAGS   DS    X             OPTION FLAGS
SUBTEXT  CSECT ,
DEFOPTS  DC    0A(0),AL1(255,X'01',1,0)
         SPACE 1
         YREGS ,
         END   ,
