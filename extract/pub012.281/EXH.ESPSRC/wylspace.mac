WSPC     TITLE 'W Y L S P A C E  ***  WYLBUR DASD SPACE REPORT'
         COPY  OPTIONGB
         SPACE 1
         SYSPARM LIST=YES
         SPACE 2
***********************************************************************
*                                                                     *
*        COPYRIGHT 1984, 1986  EXPERT SYSTEM PROGRAMMING, INC.        *
*        COPYRIGHT 2003  EXPERT SYSTEM PROGRAMMING                    *
*                        176 OLD STAGE COACH ROAD                     *
*                        BRADFORD, VT 05033-8844                      *
*                                                                     *
*                    ALL RIGHTS RESERVED                              *
*                                                                     *
***********************************************************************
*                                                                     *
*    THIS CODE LOOKS FOR DATA SETS OF THE FORM WYL.acct, IT HAS BEEN  *
*    CHANGED (AIF) TO ASSEMBLE CORRECTLY, BUT WON'T BE USEFUL AS IS.  *
*                                                                     *
***********************************************************************
         EJECT ,
         PRINT &PRTSOR
WYLSPACE PGMHEAD ZERO31,BASE=(R11,R12),PARM=R9,BNDRY=PAGE       GP12050
         SPACE 1
*    WYLSPACE IS INTENDED TO PROVIDE RAPID AND INEXPENSIVE DATASET
*    USAGE INFORMATION.  QUICK RETRIEVAL IS PERFORMED WITH BUFFERED
*    FULL-TRACK EXCP.
*
*    MACROS - LOCAL SYSTEM MACRO LIBRARY REQUIRED
*    EXTERNAL REFERENCES - @SERVICE, @PRINTER, @VOLREAD ROUTINES
*    REFRESHABLE, RE-ENTRANT, AUTHORIZED
*
*    DISKS TO BE SEARCHED ARE SPECIFIED WITH ANY DDNAME OTHER
*      THAN ONES STARTING WITH JOB, STEP, PGM OR SYS
*    THE PRESENCE OF A VOLMOUNT DD CARD CAUSES AUTOMATIC UCB LOOP
*    MODE FOR (LOCAL) PERMANENTLY RESIDENT WYLBUR VOLUMES.
         SPACE 1
         SERVINIT ,          LOAD THE SERVICE ROUTINE
         SERVLOAD @PRINTER   LOAD THE PRINTER ROUTINE           GP12050
         PRTOPEN SYSPRINT,OPT=(ABEND,DUMMY),DEV=1  OPEN PRINT FILE
         PRTOPEN SYSOUT,OPT=(NOWTO),DEV=2  DEBUG TRACE           90182
         VOLREAD LOAD        LOAD THE VOLUME READ ROUTINE
         PRTLIST TITLE,TITLE=1   MAKE PRELIMINARY TITLE
         PRTLIST TITLE3,TITLE=3    AND SUB-TITLE
         PRTLIST BUGTIT,TITLE=1,DEV=2                            90182
         TIME  ,             GET THE DATE
         STCM  R1,7,CURDATED  STASH IT
         LA    R3,CURDATED   POINT TO IT
         BAL   R14,PAKDATE   CONVERT TO DSCB FORM (NOTE: DB=0)
         MVC   TABWHO-TABDSECT+SUBOHD,=CL8'OVERHEAD'             88043
         MVC   TABWHO-TABDSECT+SUBINH,=CL8'IN-HOUSE'             88043
         MVC   TABWHO-TABDSECT+SUBCOM,=CL8'OUTHOUSE'             88043
         SPACE 1
INITTIOT SERVCALL TIODD,=CL8'VOLMOUNT'  DYNAMIC MOUNTING ?
         LTR   R0,R0         ANY DD FOUND ?
         BZ    LOOKTIOT      NO; SKIP AUTOMATIC MODE
         OI    POPS,PAUTO    SET FOR UCB LOOP MODE
         B     AUTOLOOK      START UCB SCAN
         SPACE 1
LOOPTIOT VOLREAD TCLOSE      CLOSE PRIOR VOLUME
         TM    POPS,PAUTO    ARE WE IN UCB LOOKUP MODE ?
         BZ    LOOKTIOT      NO; GET NEXT TIOT ENTRY
AUTOLOOK L     R1,STARUCBA   GET FIRST/NEXT UCB ADDRESS
         SERVCALL UCBUM,(R1)   GET NEXT UCB
         LTR   R4,R0         WAS A UCB FOUND ?
         BZ    AUTOEND       NO; END OF CHAIN
         STCM  R4,7,STARUCBA+1  SAVE IT
         USING UCBOB,R4      DECLARE IT
         CLI   UCBTBYT3,UCB3DACC  DASD DEVICE ?
         BNE   AUTOLOOK      NO; TRY AGAIN
         TM    UCBSTAT,UCBONLI  ON-LINE ?
         BZ    AUTOLOOK      NO
         TM    UCBSTAT,UCBCHGS+UCBUNLD  COMING OFF ?
         BNZ   AUTOLOOK      EITHER; SKIP IT
         ICM   R0,3,UCBVOLI  DOS OR BAD VTOC ?                  GP13227
         BZ    AUTOLOOK        YES; IGNORE IT                   GP13227
         AIF   ('&LOCAL' NE 'CCSI').NOTEST                      GP12050
         SERVCALL VSNFG,UCBVOLI  GET PRIVILEGE BITS
         STH   R0,VOLFLAGS   STASH FLAGS
         TM    VOLFLAGS+1,VF2DSNA+VF2DSNT  CONTROLLED ?          92344
         BZ    AUTOLOOK      NO; SKIP IT
.NOTEST  SERVCALL TIOUA,(R4)  SEE IF USER DD CARD
         LTR   R0,R0
         BNZ   AUTOLOOK      YES; DO LATER ON
         MVC   VOLDDNM,=CL8'VOLMOUNT'
         B     LOOKCOMM      GO TO COMMON CODE
AUTOEND  NI    POPS,255-PAUTO  RESET AUTO MODE
         DROP  R4
LOOKTIOT L     R1,ADDRTIOT   GET PRIOR ENTRY ADDRESS OR 0
         SERVCALL TIOLK      LOOK FOR PLAIN TIOT ENTRIES
         LTR   R2,R0         ANY MORE TO DO ?
         BZ    ALLDONE       NO; QUIT
         ST    R2,ADDRTIOT   SET FOR NEXT CALL
         LAT   R4,16(R2),LOOKTIOT     SKIP DUMMY UCB
         CLC   =C'SYS0',4(R2)  DYNAMIC DD ?                      88234
         BE    LOOKTIOT      YES; ASSUME THE WORST AND SKIP      88234
         USING UCBOB,R4
         CLI   UCBTBYT3,UCB3DACC       IS IT DASD ?
         BNE   LOOKTIOT      IGNORE NON-DA
         SERVCALL TIODD,4(R2)   LOCATE TIOT ENTRY BY DDNAME
         CR    R2,R0         IF NOT EQUAL, DUPLICATE DDNAME
         BNE   LOOKTIOT      WHICH SHOULD BE IGNORED
         CLC   =CL8'VOLMOUNT',4(R2)  GENERIC REQUEST CARD ?
         BE    LOOKTIOT      YES; ALREADY PROCESSED
         MVC   VOLDDNM,4(R2)  SAVE DDNAME
         SERVCALL VSNFG,UCBVOLI   GET ATTRIBUTE FLAGS FOR VOLUME
         STH   R0,VOLFLAGS   SAVE FOR LATER TEST
         STCM  R4,7,STARUCBA+1  SAVE UCB FOR EXTENT FORMATTING
LOOKCOMM SERVCALL UCBGN,(R4)   GET GENERIC NAME
         MVC   VOLTYPE,0(R1)   STASH GENERIC
         MVC   VOLUNIT,UCBNAME   SAVE UCB NAME
         MVC   VOLSER,UCBVOLI   SAVE VOL-SER
         DROP  R4            DONE WITH UCB
         XC    SUBTOT(NONWYL-SUBTOT),SUBTOT
         MVC   SUBTOT(6),VOLSER
         MVC   SUBTOT+6(2),BLANKS  CLEAR REST
         SPACE 1
         VOLREAD OPEN,VOLSER   INITIALIZE VOLUME PROCESSING
         BXH   R15,R15,SKIPVOL   IGNORE IF BAD
         VOLREAD DSCB        READ THE FORMAT 4
         BXH   R15,R15,SKIPVOL   ERROR
         B     NEXTDSC4      JOIN NORMAL PROCESSING
         SPACE 1
NEXTDSCB VOLREAD DSCB        GET ANOTHER DSCB
         CH    R15,=H'4'     DID WE GET ONE ?
         BE    DOTOTALS      NO; END OF VOLUME
         BH    DSCBIOER      NO; I/O ERROR
NEXTDSC4 LR    R1,R0         COPY DSCB ADDRESS
         CLI   DS1FMTID-DS1DSNAM(R1),C'1'   REALLY A FORMAT 1 ?
         BNE   NEXTDSCB      IF NOT, SKIP
         MVC   DS1DSNAM(LENDSCB1),DS1DSNAM-DS1DSNAM(R1)  COPY
         SERVCALL DSFMT,DS1DSORG   FORMAT COMMON FIELDS
         MVC   DSORG(LENFMT),0(R1)  MOVE RESULTS
         SERVCALL WYLDX,DS1DSNAM  VALID WYLBUR INDEX ?
         BXH   R15,R15,NONWYLB   NO; SKIP
         SERVCALL ACTST,DS1DSNAM+4  POINT TO ACCOUNT
         CH    R15,=H'8'     LEGITIMATE ACCOUNT ?
         BL    HAVEACCT      YES                                 88043
         SPACE 1                                                 88043
NONWYLB  CLI   DS1DSNAM+4,C'.'  POSSIBLE ACCOUNT INDEX ?         88043
         BNE   NONLIB        NO; NOT LIBPAK FORMAT               88043
         SERVCALL ACTST,DS1DSNAM,LEN=4  CHECK IF ACCOUNT         90182
         CH    R15,=H'8'     VALID ?                             88043
         BL    HAVEACCT      YES; PROCESS                        88043
         SPACE 1                                                 88043
NONLIB   SERVCALL DSTSO,DS1DSNAM  CHECK FOR TSO NAME             88043
         BXH   R15,R15,NONTSO                                    88043
         SERVCALL ACTST,8(R1)  PASS RETURNED ACCOUNT             88043
         CH    R15,=H'8'     VALID ?                             88043
         BL    HAVEACCT      YES                                 88043
NONTSO   SLR   R3,R3
         BCTR  R3,0          SET TO -1 FOR INDEX TO NONWYL
         LM    R4,R5,=CL8' NON-WYL'
         PRTLIST BUGDSN,DEV=2                                    90182
         B     DONWYLB                                           88043
         SPACE 1                                                 88043
HAVEACCT SRDL  R0,16+32      PUT ACCOUNT OFFSET INTO R1          88043
         LR    R3,R1         SAVE IT                             88043
         SERVCALL ACCON,(R3)  CONVERT TO INTERNAL EBCDIC FORM    88043
         LR    R4,R0         SAVE RETURN VALUE                   88043
         ICM   R5,15,BLANKS  BLANK SECOND WORD                   88043
DONWYLB  MH    R3,=Y(ACTABLEN)  MAKE FULL OFFSET
         LA    R10,ACTAB(R3)  GET TABLE ENTRY
         USING TABDSECT,R10   DECLARE ENTRY
         STM   R4,R5,TABWHO
         LA    R1,SUBTOT
         BAL   R9,VECTSUB    SUBTRACT STARTING VALUES
         INC   TABNUM        COUNT DATASETS
         LA    R14,IECSDSL1  POINT TO FORMAT 1
         XR    R15,R15       CLEAR FORMAT 3 POINTER
         STM   R14,R15,STARUCBA+4   MAKE CALLING LIST
         CLI   DS1NOEPV,3    MORE THAN THREE EXTENTS ?
         BL    FMTRACK       NO; DONE
         BH    FMTRD         YES; READ EXTENSION
         CLI   DS1EXT1,X'40'   LABEL TRACK ?
         BNE   FMTRACK       NO; NO FMT3 EXPECTED
FMTRD    VOLREAD DSC3        GET CHAINED DSCB
         BXH   R15,R15,FMT3ERR  NO; SIGNAL ERROR
         LR    R1,R0         LOAD THE DSCB ADDRESS
         USING IECSDSL3,R1
         CLI   DS3FMTID,C'3'           REALLY A FORMAT 3 ?
         BNE   FMTRD                   BRANCH IF FORMAT 2
         ST    R1,STARUCBA+8  PLACE ADDRESS IN CALLING SEQUENCE
         DROP  R1
         B     FMTRACK       DONE
         SPACE 1
FMT3ERR  OI    POPS,FGFMT3   SHOW ERROR GETTING FORMAT 3 DSCB
FMTRACK  SERVCALL DVEXT,STARUCBA  GET EXTENT SIZE IN TRACKS
         BXLE  R15,R15,FMTRACKS  SKIP IF NO ERROR
         OI    POPS,FGFMT3   SHOW ERROR GETTING EXTENTS
FMTRACKS LR    R7,R0         COPY TOTAL TRACKS
         LA    R9,TABPDS     SET PARTITIONED
         CLC   =C'PO',DSORG  IS IT ?
         BE    FMTRACKL      YES; DO LSTAR
         LA    R9,TABSEQ     ELSE COUNT AS SEQUENTIAL
         CLC   =C'PS',DSORG  IS IT REALLY SEQUENTIAL
         BNE   FMTRACKT      NO
FMTRACKL SLR   R8,R8         CLEAR FOR ICM
         ICM   R8,7,DS1LSTAR  GET TRACKS FROM LSTAR             GP09186
         BE    FMTRACKC      NO                                 GP09186
         SRL   R8,8          00TT                               GP09186
         LA    R8,1(,R8)     ELSE INCREASE TRACK COUNT
FMTRACKC CR    R8,R7         MORE THAN 100% USED ?
         BNH   *+6           NO
FMTRACKT LR    R8,R7         SET 100% FULL
         A     R8,TABSEQU-TABSEQ(,R9)  ADD TRACKS IN USE
         ST    R8,TABSEQU-TABSEQ(,R9)  AND SET
         INC   TABSEQ-TABSEQ(R9),INC=(R7)  COUNT TRACKS ALLOCATED
LISTVTOC LA    R3,DS1CREDT   POINT TO CREATION DATE
         BAL   R14,DS1DATE   FIX DATE
         LA    R3,DS1REFD    POINT TO REFERENCE DATE
         BAL   R14,DS1DATE   CONVERT DSCB DATE
         CLC   DS1CREDT(3),DS1REFD  GET LARGER VALUE
         BNH   *+8           REFD LARGER
         LA    R3,DS1CREDT   POINT TO CREATION DATE
         CLC   CURDATED,0(R3)  USED IN LAST YEAR ?
         BH    ACCUMSUB      YES
         INC   TABOLD        COUNT NUMBER UNUSED
         INC   TABWAS,INC=(R7)  AND SPACE
ACCUMSUB LA    R1,SUBTOT
         BAL   R9,VECTADD    SUBTRACT STARTING VALUES
         B     NEXTDSCB      GET ANOTHER
         SPACE 1
*        DO VOLUME TOTALS
         SPACE 1
DOTOTALS LA    R10,SUBTOT    POINT TO VOLUME TOTALS
         PRTLIST ACTLINE     PRINT VOLUME TOTALS
         B     LOOPTIOT                GO GET A NEW VOLUME
         SPACE 1
*        FORMAT 4 OBTAIN ERROR ROUTINE
         SPACE 1
SKIPVOL  SRL   R15,1         COMPENSATE FOR BXH
         ST    R15,DB        SAVE CODE FOR FORMATTING
         PRTLIST OBT4MSG     VOLUME SKIPPED MESSAGE
         OICC  8             SET NOT SO MINOR ERROR             GP12050
         B     LOOPTIOT                GET NEXT VOLUME
         SPACE 1
DSCBIOER SRL   R15,1         ADJUST FOR BXH
         ST    R15,DB        SAVE FOR FORMATTING
         PRTLIST IOERMSG
         OICC  8             SET NOT SO MINOR ERROR             GP12050
         B     LOOPTIOT
         SPACE 1
ALLDONE  PRTSPACE 2          SPACE A BIT
         L     R7,=F'36001'  NUMBER OF ENTRIES TO PROCESS
         LA    R10,NONWYL    POINT TO FIRST ONE
         B     ALLDPRNT      SKIP DOUBLING FIRST ENTRY           88043
         SPACE 1                                                 88043
ALLDLOOP ICM   R0,15,TABNUM  NUMBER OF DATASETS                  88043
         BZ    ALLDBUMP      NONE; SKIP                          88043
         LA    R1,NONWYL
         BAL   R9,VECTADD    ADD TO GRAND TOTAL
ALLDPRNT PRTLIST ACTLINE     PRINT ONE LINE                      88043
         LA    R1,SUBCOM     POINT TO COMMERCIAL                 88043
         CLC   TABWHO(4),=C'8000'  SPECIAL ?                     88043
         BH    ALLDSUB       NO; COMMERCIAL                      88043
         BE    ALLDINH       IN-HOUSE BILLABLE                   88043
         CLC   TABWHO(4),=C'6500'  COMMERCIAL RANGE ?            88043
         BH    ALLDSUB       YES; PROCESS                        88043
         LA    R1,SUBOHD     MAKE OVERHEAD                       88043
         CLI   TABWHO,C'1'                                       88043
         BL    ALLDSUB                                           88043
         CLI   TABWHO,C'6'                                       88043
         BNL   ALLDSUB                                           88043
ALLDINH  LA    R1,SUBINH                                         88043
ALLDSUB  BAL   R9,VECTADD    SUM BY BILLING STATUS               88043
ALLDBUMP LA    R10,ACTABLEN(,R10)  SPACE TO NEXT
         BCT   R7,ALLDLOOP   REPEAT
         LA    R10,NONWYL
         PRTLIST ACTLINT     PRINT RUN TOTALS
         LA    R10,SUBOHD                                        88043
         LA    R2,3                                              88043
         PRTSPACE 2                                              88043
ALLDSTUF PRTLIST ACTLINE                                         88043
         LA    R10,ACTABLEN(,R10)                                88043
         BCT   R2,ALLDSTUF                                       88043
         SPACE 1
ENDOJOB  SERVTERM ,          CLOSE AND FREE EVERYTHING
         L     R9,RETCODE    LOAD COMPLETION CODE               GP12050
         PGMEXIT RC=(R9)     QUIT                               GP12050
         SPACE 1
BADPARM  PRTV  BADPARMG      SHOW BAD PARM LIST
         SERVTERM ,          CLOSE PRINT FILE
         ABEND 666           AND QUIT
         SPACE 1
VECTADD  LA    R1,L'TABWHO(,R1)
         LA    R15,L'TABWHO(,R10)
         LA    R14,(ACTABLEN-L'TABWHO)/4
VECTADDL L     R0,0(,R1)
         A     R0,0(,R15)
         ST    R0,0(,R1)
         LA    R1,4(,R1)
         LA    R15,4(,R15)
         BCT   R14,VECTADDL
         BR    R9
         SPACE 1
VECTSUB  LA    R1,L'TABWHO(,R1)
         LA    R15,L'TABWHO(,R10)
         LA    R14,(ACTABLEN-L'TABWHO)/4
VECTSUBL L     R0,0(,R1)
         S     R0,0(,R15)
         ST    R0,0(,R1)
         LA    R1,4(,R1)
         LA    R15,4(,R15)
         BCT   R14,VECTSUBL
         BR    R9
         SPACE 1
*        ROUTINES FOR DATE CONVERSION
         SPACE 1
DS1DATE  XC    DB,DB         DSCB FORMAT DATE CONVERSION
         CLC   BLANKS(3),0(R3)   OLD, OLD, OLD DSCB FIELD ?
         BE    DS1DATEN      YES; SET ZERO
         ICM   R0,3,1(R3)    OR ELSE ZERO ?
         BZ    DS1DATEN      YES; INVALID
         CLI   0(R3),99      VALID YEAR ?
         BH    PAKDATE       NO; FAIL
         CLC   1(2,R3),=H'366'
         BNHR  R14           LOOKS ACCEPTABLE; RETURN AS IS
PAKDATE  TM    2(R3),X'0F'   PACKED FORMAT ?
         BNO   DS1DATEN      NO; SET ZERO
         MVC   DB+5(3),0(R3)  MOVE IT
         CVB   R0,DB         GET YEAR AND DATE
         SRDA  R0,32         SHIFT AND CLEAR R0
         D     R0,=F'1000'   ISOLATE DAY AND YEAR
         STC   R1,DB         SET YEAR
         STCM  R0,3,DB+1     AND DAY
DS1DATEN MVC   0(3,R3),DB    RETURN CORRECTED VALUE
         BR    R14
         EJECT
*        CONSTANTS AND STUFF
         SPACE 1
ZEROES   DC    D'0'          CONSTANT
BLANKS   DC    CL8' '        BLANKS
SYSPRINT PRTWORK SYSPRINT,TITLE=5
SYSOUT   PRTWORK SYSOUT,TITLE=5                                  90182
PDCTEST  DC    0CL4'TEST',C'TES'
PDCTST   DC    C'TST'        ALTERNATE TEST MODE
AST30    DC    C'******************************'
         SPACE 2
         LTORG
         EJECT
*        MESSAGES - HEADINGS, ETC.
         SPACE 1
         PRINT NOGEN
TITLE    FDOPT NL,CC=C'#'    AUTOMATIC DATE/TIME/PAGE OPTION
         FDSPACE 4
TITLEVT  FDPRT 'WYLBUR DATA SPACE ACCOUNTING'
         FDPRT *END
         SPACE 1
TITLE3   FDPRT ' USE',NL,LEN=L'TABWHO
         FDPRT '# OF DATASETS',RADJ,LEN=14
         FDPRT '^PDS SPACE',RADJ,LEN=14
         FDPRT '^PDS USED',RADJ,LEN=14
         FDPRT 'PDS SPACE',RADJ,LEN=14
         FDPRT 'PDS USED',RADJ,LEN=14
         FDPRT 'NUMBER',RADJ,LEN=20
         FDPRT ' NOT USED IN ONE YEAR'
         FDPRT *END
         SPACE 1
OBT4MSG  FDPRT AST30,NL,LEN=10
         FDPRT 'VOLUME',PAD
         FDPRT VOLSER,DEB,PAD
         FDPRT 'SKIPPED DUE TO FORMAT 4 PROCESSING ERROR.',PAD
OBTCOM   FDPRT 'CONDITION CODE=',PADL
         FDPRT DB,4,I,PADR
OBTCOMAS FDPRT AST30,LEN=10
OBTCOMX  FDPRT *END
         SPACE 1
OBT3MSG  FDPRT AST30,NL,PADR,LEN=10
         FDPRT 'ERROR PROCESSING EXTENTS; TRACK COUNT INCORRECT.'
         FDGOTO OBTCOMAS
         SPACE 1
IOERMSG  FDOPT NL,CC=C'0'    DOUBLE-SPACE
         FDPRT AST30,PADR,LEN=10
         FDPRT 'VOLUME SKIPPED DUE TO OBTAIN ERROR DURING SEQUENTIAL VT*
               OC SEARCH.',PADR
         FDGOTO OBTCOM
         SPACE 1
ACTLINT  FDPRT ' ',NL
         FDPRT 'TOTAL',RADJ,PAD,NL,LEN=L'TABWHO
         FDGOTO ACTLINEC
ACTLINE  FDPRT TABWHO,NL,PAD
ACTLINEC FDPRT TABNUM,ICM,RADJ,PAD,LEN=13
         FDPRT TABSEQ,ICM,RADJ,PAD,LEN=13
         FDPRT TABSEQU,ICM,RADJ,PAD,LEN=13
         FDPRT TABPDS,ICM,RADJ,PAD,LEN=13
         FDPRT TABPDSU,ICM,RADJ,PAD,LEN=13
         FDCLC TABOLD,ZEROES,4,BE=ACTLINEZ
         FDPRT TABOLD,ICM,RADJ,PAD,LEN=19
         FDPRT TABWAS,ICM,RADJ,LEN=13
ACTLINEZ FDPRT *END
         SPACE 1                                                 90182
BUGTIT   FDOPT NL,CC=C'#'    AUTOMATIC TIME/DATE/PAGE            90182
         FDPRT 'UNBILLABLE DATASETS'                             90182
         FDPRT *END                                              90182
BUGDSN   FDPRT VOLSER,PADR,DEB,NL,LEN=6                          90182
         FDPRT DS1DSNAM                                          90182
         FDPRT *END                                              90182
         SPACE 1
BADPARMG VCON  '-******* INCORRECT PARM FIELD *******'
         SPACE 1
TABDSECT DSECT ,
TABWHO   DS    CL8           ACCOUNT OR OTHER ID
TABSEQ   DS    F             SEQUENTIAL TRACKS
TABSEQU  DS    F             SEQUENTIAL TRACKS USED
TABPDS   DS    F             PARTITIONED TRACKS
TABPDSU  DS    F             PARTITIONED TRACKS USED
TABNUM   DS    F             NUMBER OF DATASETS
TABOLD   DS    F             NUMBER OF DATASETS NOT USED
TABWAS   DS    F             TRACKS NOT USED IN ONE YEAR
ACTABLEN EQU   *-TABDSECT    LENGTH OF ONE ENTRY
         SPACE 2
         PRINT &PRTSYS
SAVE     DSECT ,             SAVE/WORK AREA
DB       DC    D'0'
         SERVDEFS ,          SERVICE ROUTINE ADDRESSES, ETC.    GP12050
@VOLREAD DC    A(0)
ADDRTIOT DC    A(0)
         SPACE 1
         SERVCALC MF=D       EXPAND MAPPING FOR TRKCALC
         SPACE 1
ALLTRACK DC    F'0'          TRACKS LISTED FOR THIS RUN
ALLDSNS  DC    F'0'          NUMBER OF DATASETS IN THIS RUN
CURDATED DC    PL3'0'        SAME DAY LAST YEAR
         SPACE 1
POPS     DC    AL1(0)        PROCESS OPTIONS
PTEST    EQU   8               RUNNING IN TEST MODE ONLY
PAUTO    EQU   16              AUTOMATIC VOLUME RETRIEVAL (UCB LOOP)
FGFMT3   EQU   64              ERROR GETTING DSCB 3
VOLFLAGS DC    H'0'          VOLUME ATTRIBUTE FLAGS
VOLSER   DC    CL6' '        CURRENT VOLUME SERIAL
VOLUNIT  DC    CL3' '        CURRENT UCB NAME
VOLDDNM  DC    CL8' '        CURRENT DD NAME
VOLTYPE  DC    CL8' '        DEVICE TYPE
DSNAME   DC    CL44'DSNAME'  CURRENT DSN
ALODATE  DC    C'00/00/00'
LASTUSE  DC    C'00/00/00'
DSORG    DC    CL3' '   1/5  DSORG
RECFM    DC    CL6' '   2/5  RECFM
OPTCD    DC    CL8' '   3/5  OPTCD
BLKSIZE  DC    CL5' '   4/5  BLKSIZE
LRECL    DC    CL5' '   5/5  LRECL
LENFMT   EQU   *-DSORG       LENGTH TO MOVE
ALLOCTYP DC    CL3' '
         DS    0D
         IECSDSL1 1          FORMAT 1
CCHHFMT1 DS    XL5           ADDRESS OF FORMAT 1 DSCB
LENDSCB1 EQU   *-DS1DSNAM    LENGTH TO MOVE
         SPACE 1
SUBOHD   DS    (ACTABLEN/4)F'0'  OVERHEAD BILLING                88043
SUBINH   DS    (ACTABLEN/4)F'0'  IN-HOUSE BILLING                88043
SUBCOM   DS    (ACTABLEN/4)F'0'  COMMERCIAL BILLING              88043
SUBTOT   DS    (ACTABLEN/4)F'0'  VOLUME TOTALS
NONWYL   DS    (ACTABLEN/4)F'0'  NON-WYLBUR (AND SUMMARY)
ACTAB    DS    36000XL(ACTABLEN)
SAVEEND  EQU   *                                                GP12050
         EJECT ,
         PRINT &PRTSYS
CVTDSECT DSECT ,
         CVT   DSECT=YES
         SPACE 1
         IHACDE ,
         SPACE 1
         IKJTCB ,
         SPACE 1
         IEZJSCB ,
         SPACE 1
         IEFUCBOB ,
         AIF   (&MVS).MVSUCB
UCBRR    EQU   X'20'         DEVICE SHARABLE
UCBRPS   EQU   X'10'         DEVICE HAS RPS
         AIF   (&SVS).MVSUCB
UCBVLPWR EQU   X'02'         DEVICE REQUIRES ALTERNATE POWER
UCBDVPWR EQU   X'01'         DEVICE HAS ALTERNATE POWER
.MVSUCB  SPACE 1
         DCBD  DEVD=DA,DSORG=PS
         SPACE 1
DSCB3    DSECT ,
         IECSDSL1 3          FORMAT 3
         SPACE 1
         AIF   ('&LOCAL' NE 'CCSI').BIGEND                      GP12050
USERVOLT DSECT ,
VOLTUSER USERVOLT MAP=NO     GET FLAGS ONLY
         SPACE 1
.BIGEND  END   ,
