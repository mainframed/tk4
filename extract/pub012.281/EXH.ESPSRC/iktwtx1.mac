IKTWTX1  TITLE 'VTAM TWX OUTPUT PROCESSING USER EXIT ROUTINE'
IKTWTX1  CSECT
***********************************************************************
*                                                                     *
*                                                                     *
*   NAME:  IKTWTX1 IN LOAD MODULE IKTIOM01                            *
*                                                                     *
*   PURPOSE:  ACF/VTAM R2 TWX DEVICE TPUT EDIT EXIT                   *
*                                                                     *
*   SUPPORT:  STEVE KUEKES                                            *
*                                                                     *
*   PRODUCT:  GRAPH                                                   *
*                                                                     *
*   TYPE:  SYSTEM                                                     *
*                                                                     *
*   ENTRY POINT: IKTWTX1                                              *
*                                                                     *
*     REGISTER USAGE:                                                 *
*            R1  - PARAMETER LIST PASSED (SEE PARM DSECT)             *
*            R3  - CURRENT BUFFER ADDRESS                             *
*            R4  - CURRENT BUFFER LENGTH                              *
*            R5  - LENGTH OF EACH TRANSLATE                           *
*            R12 - BASE REGISTER                                      *
*            R13 - POINTER TO SAVE AREA OF CALLER                     *
*            R14 - RETURN ADDRESS                                     *
*            R15 - RETURN CODE (0 = TSO MUST EDIT MESSAGE AFTER ME    *
*                               4 = NO TSO EDITING DONE)              *
*                                                                     *
*     METHOD OF OPERATION:  IF THE BUFFER IS NOT FROM A CONTROL       *
*            TPUT THEN TELL TSO THAT I AM THRU EDITING AND IT IS      *
*            TO FURTHER EDIT THE MESSAGE.  IN THIS CASE THIS EXIT     *
*            DOES NOT MODIFY THE BUFFER IN ANY WAY.  IF, HOWEVER,     *
*            THE BUFFER IS FROM A CONTROL TPUT, THEN WE TRANSLATE     *
*            OUT SPECIFIC EBCDIC CHARACTERS(ONES THAT HAVE NO ASCII   *
*            EQUIVALENT).  THESE SPECIFIC CHARACTERS(SEE TRANSLATE    *
*            TABLE) MEAN SPECIAL THINGS TO NCP AND SHOULD NOT BE      *
*            SENT.  UNLESS OF COURSE YOU WANT NCP TO DO SPECIAL       *
*            THINGS FOR YOU.                                          *
*            THIS EXIT IS REENTRANT, REFRESHABLE, AND REUSABLE.       *
*            BEWARE NO SVCS FROM THIS CODE.  ALL SUPERVISOR SERVICES  *
*            MUST BE VIA "BRANCH ENTRY".                              *
*            FOR DOCUMENTATION SEE ACF/VTAM REL. 2 PLM (LY38-3032)    *
*                                                                     *
*     WRITTEN BY: STEVEN F. KUEKES                                    *
*                 SAS INSTITUTE INC.                                  *
*                 BOX 8000                                            *
*                 CARY, NC  27511    USA                              *
*                 (919)467-8000                                       *
*                                                                     *
*        MINOR SPELLING AND EFFICIENCY CORRECTIONS FLAGGED:      86005
*              BY G. POSTPISCHIL, ESP                            86005
*                                                                     *
*   END                                                               *
*                                                                     *
***********************************************************************
         EJECT
         SAVE  (14,12),T,IKTWTX1_&SYSDATE SAVE THE CALLERS REGS
         LR    R12,R15                 GET ENTRY ADDRESS FOR BASE
         USING IKTWTX1,R12             TELL ASSEMBLER R12 BASE
         USING PARMS,R1                GET BASE FOR PARAMETERS
         SLR   R15,R15       SET PROVISIONAL RETURN CODE         86005
         TM    OPACFLG,X'20'           IS IT A CONTROL TPUT B'XX10XXXX'
         BZ    NOTCNTRL                BRANCH IF NOT
         TM    OPACFLG,X'10'           IS IT STILL A CONTROL TPUT?
         BO    NOTCNTRL                BRANCH IF NOT
         L     R3,OPACBUFA             GET ADDRESS OF TPUT BUFFER
         LH    R4,OPACBUFL             GET LENGTH OF TPUT BUFFER
         LA    R6,256        SET MAXIMUM LENGTH TRANSLATABLE     86005
         LTR   R4,R4                   DO WHILE(R4,GT,0)
         BNP   FINTR                   IF LE GO TO FINTRT
TRLOOP   LR    R5,R6         PROVISIONALLY SET FOR MAXIMUM MOVE  86005
         CR    R4,R6         IS THIS > 1 TR ?                    86005
         BNL   *+6           YES; USE TR MAXIMUM                 86005
         LR    R5,R4         ELSE USE USER'S RESIDUAL LENGTH
         BCTR  R5,0                    -1 FOR EXECUTE
         EX    R5,TRINST               DO THE TR
         AR    R3,R6         GET NEXT TR ADDRESS                 86005
         SR    R4,R6         GET LENGTH REMAINING                86005
         BP    TRLOOP        LOOP TO TR AGAIN                    86005
FINTR    LA    R15,4                   WHEN FINISHED SAY "NO EDITING"
NOTCNTRL RETURN (14,12),T,RC=(15)      RESTORE THE CALLER'S REGS 86005
         BR    R14                     RETURN
         SPACE 3
TRINST   TR    0(1,R3),TRTABLE         EXECUTED INSTRUCTION
         SPACE 1
*        LEAVE ALL THE CHARACTERS ALONE BUT THE FOLLOWING:
*
*        CHANGE X'38' TO X'07'
*        CHANGE X'DF' TO X'07'
*
*                0 1 2 3 4 5 6 7 8 9 A B C D E F
*               ----------------------------------+
TRTABLE  DC    X'000102030405060708090A0B0C0D0E0F' × 0
         DC    X'101112131415161718191A1B1C1D1E1F' × 1
         DC    X'202122232425262728292A2B2C2D2E2F' × 2
         DC    X'303132333435363707393A3B3C3D3E3F' × 3
         DC    X'404142434445464748494A4B4C4D4E4F' × 4
         DC    X'505152535455565758595A5B5C5D5E5F' × 5
         DC    X'606162636465666768696A6B6C6D6E6F' × 6
         DC    X'707172737475767778797A7B7C7D7E7F' × 7
         DC    X'808182838485868788898A8B8C8D8E8F' × 8
         DC    X'909192939495969798999A9B9C9D9E9F' × 9
         DC    X'A0A1A2A3A4A5A6A7A8A9AAABACADAEAF' × A
         DC    X'B0B1B2B3B4B5B6B7B8B9BABBBCBDBEBF' × B
         DC    X'C0C1C2C3C4C5C6C7C8C9CACBCCCDCECF' × C
         DC    X'D0D1D2D3D4D5D6D7D8D9DADBDCDDDE07' × D
         DC    X'E0E1E2E3E4E5E6E7E8E9EAEBECEDEEEF' × E
         DC    X'F0F1F2F3F4F5F6F7F8F9FAFBFCFDFEFF' × F
         SPACE 3
PARMS    DSECT
*
*        THIS DSECT DESCRIBES THE PARAMETER LIST PASSED TO
*        THE EXIT ROUTINE BY IKTWTTYO
*        SEE ACF/VTAM PLM FOR DETAILS
*
OPACBUFA DS    A             ADDRESS OF TPUT BUFFER
OPACBUFL DS    H             TPUT BUFFER LENGTH
         DS    H             RESERVED
         DS    B             RESERVED
OPACFLG  DS    B             FLAG IDENTIFYING TYPE OF TPUT
*                            WHEN B'XX10XXXX' TPUT IS CONTROL
         PRINT NOGEN                                             86005
         REGEQU ,                                                86005
         END   ,                                                 86005
