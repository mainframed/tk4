IEFACTRT TITLE 'I E F A C T R T  ***  STEP/JOB TERMINATION EXIT' 83143
         COPY  OPTIONGB                                          83143
         SPACE 1                                                 83143
         SYSPARM LIST=YES                                        83143
         EJECT ,                                                 83143
         PRINT &PRTSOR                                           83143
IEFACTRT PGMHEAD ZERO12,BASE=R11,PARM=R1,AM=31,RM=31            GP04042
         CH    R0,=H'16'     ENTRY FOR TYPE 30 OR 32 RECORD?    GP04044
         BH    RETURN        YES; IGNORE                        GP04044
         LR    R10,R0                                           GP04042
         LR    R5,R1                                            GP04042
         USING PARMSECT,R5   DECLARE LIST                        83143
         SPACE 2
***********************************************************************
**                                                                   **
**   R4    BASE FOR SMF RECORD                                       **
**   R5    POINTS TO EXIT PARAMETER LIST (R1)                        **
**   R10   ENTRY CODE (R0)                                           **
**   R11   PROGRAM BASE                                              **
**   R12   REQUIRED FOR IEFYS CALLS - LCT ADDRESS                    **
**   R13   SAVE AREA POINTER AND POINTER TO 45F FOR IEFYS            **
**                                                                   **
***********************************************************************
         SPACE 1                                                 83143
         USING IEFLCT,R12    PASSED BY INITIATOR                 83143
         SPACE 1                                                 83143
         AIF   ('&LOCAL' NE 'PID').NOEXACT                      GP04044
*---------------------------------------------------------------------*
*    IF A NEW ACCOUNTING ROUTINE HAS BEEN WRITTEN, LINK TO IT         *
*    DYNAMICALLY. EXTERNAL NAME IS LEXACTEX IN SYS1.LPALIB.      83143*
*    PARAMETERS ARE IN R13-R1 AS DOCUMENTED; IN ADDITION, R2     83143*
*    IS SET TO THE ADDRESS OF IEFYS FOR MESSAGE WRITING.         83143*
*    ANY CODE RETURNED IN R15 BY IT IS PASSED BACK.              83143*
*---------------------------------------------------------------------*
         PUSH  USING                                            GP04044
         LM    R0,R1,MODNAME  SPECIFY EXIT NAME                  83143
         L     R3,CVTPTR     GET CVT IN R3 FOR IEAVVMSR          83143
         ICM   R4,15,CVTUSER-CVTMAP(R3)  GET USER CVT            83143
         BZ    NOUSCVT                                           83143
         USING USERCVT,R4    DECLARE IT                          83143
         ICM   R15,15,UCXACTEX  ADDRESS ALREADY SET ?            83143
         BP    QUICKGO       YES; USE IT                         83143
         BM    USEOLDY       PREVIOUSLY NOT FOUND - USE OLD CODE 83143
NOUSCVT  L     R7,CVTLPDSR-CVTMAP(,R3)  IEAVVMSR                 83143
         BASR  R14,R7    NOTE THAT R6-R9 ARE CLOBBERED           83143
         B     FOUNDIT       MODULE FOUND                        83143
         LTR   R4,R4         USERCVT SET ?                       83143
         BZ    USEOLDY       NO; VERY INEFFICIENT                83143
         MVI   UCXACTEX,X'80'  INDICATE MODULE NOT IN LPA        83143
         B     USEOLDY       USE OLD CODE                        83143
FOUNDIT  LR    R15,R0        COPY LPDE ADDRESS                   83143
         USING LPDE,R15                                          83143
         L     R15,LPDENTP   LOAD ENTRY POINT                    83143
         DROP  R15                                               83143
         LTR   R4,R4         USERCVT SET ?                       83143
         BZ    QUICKGO       NO                                  83143
         ST    R15,UCXACTEX  SAVE THE EXIT ADDRESS               83143
QUICKGO  LR    R1,R5         RESTORE R1                          83143
         LR    R0,R10        RESTORE R0                          83143
         L     R2,YAD        PASS IEFYS ADDRESS                  83143
         BASR  R14,R15       CALL NEW ROUTINE                    83143
         LR    R9,R15        SAVE RETURN CODE                    83143
         B     EXITS         EXIT WITH PASSED RETURN CODE        83143
         SPACE 1                                                 83143
MODNAME  DC    0F'0',CL8'LEXACTEX'   LOCAL EXIT NAME             83143
         POP   USING                                             83143
.NOEXACT SPACE 1                                                GP04044
USEOLDY  L     R4,EXTRDW     POINT TO SMF RECORD                 83143
         MVI   MSG1,C' '          CLEAR OUTPUT AREA                 *
         MVC   MSG1+1(M1LEN-1),MSG1                             GP04044
         LA    R0,M1LEN+2                                       GP04044
         STH   R0,VMSG1      BUILD RDW                          GP04044
         MVI   MSG2,C' '          CLEAR OUTPUT AREA                 *
         MVC   MSG2+1(M2LEN-1),MSG2                             GP04044
         LA    R0,M2LEN+2                                       GP04044
         STH   R0,VMSG2      BUILD RDW                          GP04044
         MVC   WTOAREA(4+M1LEN),VMSG1 INITIALIZE WTO/LOG MSG     83143
         SRA   R10,4              CHECK TERMINATION TYPE            *
         BNZ   JOBTRM              VALUE IS 16
         TITLE 'I E F A C T R T  ***  STEP TERMINATION FORMATTING'
         PUSH  USING                                            GP04044
         MVC   MSG1,=C'MSG374I'                                 GP04044
         MVC   MSG2,=C'MSG374I'                                 GP04044
         USING SMFRCD4,R4    DECLARE FIXED LENGTH RECORD         83143
         TM    SMF4STI,X'10'  STEP CANCELLED BY IEFUSI ?         83143
         BZ    STPNFAIL      NO                                  83143
         LA    R1,SPACER                                         83143
         BAS   R9,YAK        WRITE SPACER                        83143
         LA    R1,STEPFAIL                                       83143
         BAS   R9,YAK        WRITE ERROR MESSAGE                 83143
         SPACE 1
STPNFAIL MVC   WK1,SMF4SIT   STEP INIT TIME                      83143
         BAS   R7,TIMECVT                                           S
         MVC   M1SIT(8),TIME                                        S
         MVC   WK1,SMF4TME   STEP TERM TIME                      83143
         BAS   R7,TIMECVT                                           S
         MVC   M1SET(8),TIME                                        S
         ICM   R7,15,SMF4TME  TIME ENDED                         83143
         ICM   R8,15,SMF4SIT   TIME STARTED                      83143
         SR    R7,R8         END MINUS START                     83143
         BNM   NOT00AM       OK IF NOT NEGATIVE                  83143
         A     R7,=F'8640000'   ELSE ADD 24 HOURS (BUT COULD BE 48...
NOT00AM  AL    R7,=F'50'         ROUND                           83143
         XR    R6,R6              CLEAR WORK                        S
         D     R6,=F'100'         CONVERT TO SECONDS                S
         CVD   R7,PAKT                                              S
         MVC   M1ELPS-1(6),=XL6'402020202120'                       S
         ED    M1ELPS-1(6),PAKT+5      ELAPSED TIME                 S
         SPACE 1
         L     R6,EXTSTPTT   GET STEP CPU TIME ADDRESS           83143
         SR    R7,R7                                                S
         ICM   R7,7,SMF4SETM-SMF4SETM(R6)                           S
         CVD   R7,PAKT                                              S
         MVC   M1TCB-1(9),=XL9'4020202020214B2020'                  S
         ED    M1TCB-1(9),PAKT+4  TCB (STEP CPU) TIME               S
         SPACE 1
         SLR   R7,R7                                                S
         ICM   R7,7,SMF4SRBT    CPU TIME UNDER SRBS                 S
         CVD   R7,PAKT                                              S
         MVC   M1SRB-1(9),=XL9'4020202020214B2020'                  S
         ED    M1SRB-1(9),PAKT+4  SRB (STEP) TIME                   S
         SPACE 1
         MVC   M1JN(8),SMF4JBN    JOBNAME                        83143
         MVC   M1SN(8),SMF4STMN   STEP NAME                      83143
         MVC   M1PN(8),SMF4PGMN   PROGRAM NAME                   83143
         SPACE 1
         LH    R3,SMF4RLCT   GET OFFSET TO PAGING/SWAP COUNTS    83143
         LA    R3,4(R4,R3)   GET ADDRESS OF PAGING SECTION       83143
         PUSH  USING                                            GP04042
         DROP  R4                                               GP04042
         USING SMF4PGIN,R3                                       83143
         ICM   R6,15,SMF4NSW  GET NUMBER OF SWAPS                83143
         C     R6,=F'9999999'                                       S
         BH    SWAPOFLO           TOO MUCH TO PRINT                 S
         CVD   R6,PAKT                                              S
         MVC   WORK10(10),=XL10'40202020202020202120'               S
         ED    WORK10(10),PAKT+3                                    S
         MVC   M1SWAPS(8),WORK10+2                                  S
         B     NOOFLO                                               S
SWAPOFLO MVC   M1SWAPS(8),=8C'*'                                    S
         SPACE 1
NOOFLO   L     R7,PSATOLD-PSA          LOAD TCB ADDRESS          83143
         ICM   R7,15,TCBUSER-TCB(R7)   GET USER TCB OR OUT       83143
         BZ    NOOTCBU       SKIP IF NONE ?                      83143
         A     R6,UTJP-USERTCB(,R7)  ADD PREVIOUS COUNT          83143
         ST    R6,UTJP-USERTCB(,R7)  SET JOB TOTAL               83143
         NI    UTF1-USERTCB(R7),255-TUFDDR  RESET DDR TAKEN      83143
         SPACE 1                                                 83143
NOOTCBU  LA    R14,SMF4PGIN   POINT TO PAGE IN - FIRST OF SIX     83143
         LA    R15,M2PI      FIRST OUTPUT LOCATION               83143
         LA    R0,(SMF4VPO-SMF4PGIN)/4+1  NUMBER OF COUNTERS      83143
FPAGLOOP L     R7,SMF4PGIN-SMF4PGIN(,R14)  GET NEXT COUNTER      83143
         CVD   R7,PAKT                                              S
         MVC   M2PI-M2PI(8,R15),=XL8'4020202020202120'           83143
         ED    M2PI-M2PI(8,R15),PAKT+4                           83143
         LA    R14,SMF4PGOT-SMF4PGIN(,R14)  NEXT COUNTER          83143
         LA    R15,M2PO-M2PI(,R15)  NEXT OUTPUT FIELD            83143
         BCT   R0,FPAGLOOP   REPEAT FOR ALL                      83143
         MVC   M2PLBL,=CL4'PAGE'                                GP04044
         MVC   M2SLBL,=CL4'SWAP'                                GP04044
         MVC   M2VLBL,=CL4'VIO '                                GP04044
         POP   USING                                            GP04042
         SPACE 1
         LH    R7,SMF4SYST   GET SYSTEM STORAGE                  83143
         AH    R7,SMF4H0ST   ADD PRIVATE STORAGE                 83143
         CVD   R7,PAKT                                              S
         MVC   M2SU-1(6),=XL6'402020202120'                         S
         ED    M2SU-1(6),PAKT+5                                     S
         MVI   M2SU+5,C'K'                                          S
         MVI   M2SU-1,C'V'                                          S
         TM    SMF4RIN,1     V=R ?                               83143
         BZ    *+8            NO                                 83143
         MVI   M2SU-1,C'R'                                          S
         SPACE 1
         XR    R6,R6                                             83143
         IC    R6,SMF4STN    GET STEP NUMBER                     83143
         CVD   R6,PAKT                                              S
         MVC   M1SNO-1(4),=X'40202120'                           83143
         ED    M1SNO-1(4),PAKT+6                                 83143
         SPACE 1
         UNPK  M1CC(5),SMF4SCC(3)      COMPLETION CODE           83143
         MVI   M1CC+4,C' '                                          S
         TR    M1CC+1(3),HEXTRTAB                                83143
         MVI   M1CC,C'S'                                            S
         TM    SMF4STI,FLUSHED                                   83143
         BZ    NOTFLUSH                                             S
         MVC   M1CC(4),=CL4'NXEQ'                                   S
         B     WTOIT                                                S
NOTFLUSH TM    SMF4STI,ABENDED                                   83143
         BO    BADEND                                               S
         LA    R0,C'R'       SET FOR R(ETURN) CODE               83143
         B     COMPDEC       FORMAT COMPLETION CODE IN DECIMAL   83143
BADEND   TM    SMF4SCC,X'80'   USER OR SYSTEM ABEND ?            83143
         MVI   M1CC,C'S'                                            S
         BZ    WTOIT         SYSTEM                                 S
         LA    R0,C'U'       USER                                83143
COMPDEC  ICM   R7,3,SMF4SCC  GET THE CODE (PLUS FLAGS)           83143
         LA    R14,X'FFF'    MAKE MASK                           83143
         NR    R7,R14        REMOVE FLAGS                        83143
         CVD   R7,PAKT                                           83143
         MVC   WORK10(6),=X'402020202120'  MAKE MASK             83143
         ED    WORK10(6),PAKT+5   EDIT                           83143
         STC   R0,M1CC-1     SET TYPE (R/U)                      83143
         MVC   M1CC(4),WORK10+2                                  83143
         B     WTOIT
         POP   USING                                             83143
         TITLE 'I E F A C T R T  ***  JOB TERMINATION FORMATTING'
         PUSH  USING                                            GP04044
         USING SMFRCD5,R4    DECLARE FIXED LENGTH RECORD         83143
JOBTRM   MVC   MSG1,=C'MSG376I'                                 GP04044
         MVC   MSG2,=C'MSG376I'                                 GP04044
         TM    SMF5JBTI,X'20'   JOB CANCELLED BY IEFUJI ?        83143
         BZ    JOBNFAIL      NO                                  83143
         LA    R1,SPACER                                         83143
         BAS   R9,YAK        WRITE SPACER                        83143
         LA    R1,JOBFAIL                                        83143
         BAS   R9,YAK        WRITE ERROR MESSAGE                 83143
JOBNFAIL OI    BITS,X'01'    SET JOB (VS. STEP) FLAG                J
         L     R8,PSATOLD-PSA   GET THE TCB                      83143
         ICM   R8,15,TCBUSER-TCB(R8)   EXTENSION ADDED ?         83143
         BZ    PRJYOLD       NO; SKIP PRIORITY BUSINESS          83143
         AIF   ('&LOCAL' NE 'PID').NOCVTUS                      GP04044
         L     R9,CVTPTR     GET CVT                             83143
         ICM   R9,15,CVTUSER-CVTMAP(R9)  GET THE CVT EXTENSION   83143
         BZ    PRJYOLD       NO; SKIP PRIORITY BUSINESS          83143
         USING USERTCB,R8                                        83143
         USING USERCVT,R9                                        83143
         ICM   R9,15,UCLVOLT  GET VOLUME TABLE AND OTHER STUFF   83143
         BZ    PRJYOLD       NO; SKIP PRIORITY BUSINESS          83143
         USING USERVOLT,R9   DECLARE IT                          83143
         CLC   DEMOACCT,SMF5UIF  SPECIAL BENCHMARK RUN ?         82122
         BNE   *+10          NO                                  82122
         MVC   UTPY,DEMOPRTY   SET FAKE PRIORITY                 82122
         L     R6,APPTURN0   PROVISIONALLY SET PRTY=0            83143
         CLI   UTPY,0        REALLY PRTY=0 REQUEST ?             83143
         BE    PRJYGOT       YES; WE HAVE A CHEAP-SKATE          83143
         XR    R6,R6                                             83143
         MVC   PAKT(4),SMF5JIT     GET JOB INITIATION TIME       83143
         L     R7,PAKT       LOAD IT                             83143
         MVC   PAKT(4),SMF5RST    NOW GET READER START TIME      83143
         S     R7,PAKT       GET ELAPSED TIME SINCE JOB GOT INTO SYSTEM
         BNM   *+8           SKIP IF PLUS OR ZERO (SHORT JOB ?)  83143
         A     R7,=A(24*60*60*100)    ADD 24 HOURS, WENT IN YESTERDAY
         D     R6,=A(60*100)   CONVERT TO MINUTES                83143
         LPR   R7,R7         JUST IN CASE                        83143
         CLI   SMF5JICL,C'A'   AT LEAST CLASS A JOB ?            83143
         BL    NOPTYAGE      NO; NO AGING                        83143
         CLI   SMF5JICL,C'E'   NO HIGHER THAN CLASS E ?          83143
         BNH   DOPTYAGE      YES; JOBS A-E ARE AGED              83143
NOPTYAGE XR    R7,R7         NO RATE REDUCTION FOR KLUNKERS      83143
DOPTYAGE L     R6,APPTURN    GET PRIORITY, ELAPSED TIME TABLE    83143
         L     R3,APPTURNE   GET END OF TABLE                    83143
         SR    R3,R6         GET SIZE                            83143
         SRA   R3,2          /4 TO GET ENTRIES                   83143
PRJYCHK  CLC   UTPY,0(R6)      MATCHING PRIORITY ?               83143
         BL    PRJYINC       NO, MUST BE LOWER                   83143
         CH    R7,2(,R6)     DID JOB START SOON ENOUGH ?         83143
         BNH   PRJYGOT       YES, CHARGE FULL RATE               83143
PRJYINC  LA    R6,4(,R6)     ELSE CHARGE LOWER RATE              83143
         BCT   R3,PRJYCHK    CHECK ALL                           83143
PRJYGOT  MVC   UTJF,1(R6)    STASH PRTY FACTOR INDEX             83143
         MVC   SMF5JPTY,0(R6)  **UNTIL JSI FIXED**               79148
.NOCVTUS SPACE 2                                                GP04044
PRJYOLD  MVC   M1JN(8),SMF5JBN         JOB NAME                  83143
         MVC   WK1,SMF5JIT   JOB/STEP INIT TIME                  83143
         BAS   R7,TIMECVT                                           J
         MVC   M2SIT(8),TIME                                        J
         MVC   WK1,SMF5TME   JOB/STEP TERM TIME                  83143
         BAS   R7,TIMECVT                                           J
         MVC   M2SET(8),TIME                                        J
         ICM   R7,15,SMF5TME    TIME ENDED                       83143
         ICM   R8,15,SMF5JIT   TIME STARTED                      83143
         SR    R7,R8         END MINUS START                     83143
         BNM   NOT00AM1           NO WRAP-AROUND                 83143
         A     R7,=F'8640000'     YES..ADD 24 HOURS (OR 72 ?     83143
NOT00AM1 AL    R7,=F'50'          ROUND                          83143
         XR    R6,R6              CLEAR WORK                        J
         D     R6,=F'100'         CONVERT TO SECONDS                J
         CVD   R7,PAKT                                              J
         MVC   M2ELPS-1(6),=XL6'402020202120'                       J
         ED    M2ELPS-1(6),PAKT+5      ELAPSED TIME                 J
         SPACE 1
         SLR   R7,R7                                                J
         ICM   R7,7,SMF5JCPU   TCB CPU TIME                      83143
         CVD   R7,PAKT                                              J
         MVC   M2TCB-1(9),=XL9'4020202020214B2020'                  J
         ED    M2TCB-1(9),PAKT+4                                    J
         SLR   R7,R7                                                J
         ICM   R7,7,SMF5SRBT           SRB TIME                  83143
         CVD   R7,PAKT                                              J
         MVC   M2SRB-1(9),=XL9'4020202020214B2020'                  J
         ED    M2SRB-1(9),PAKT+4                                    J
         L     R7,SMF5TTAT        TOTAL TRANS. ACTIVE TIME       83143
         SLL   R7,10                                                J
         SLR   R6,R6                                                J
         D     R6,=F'1000'                                          J
         CVD   R7,PAKT                                              J
         MVC   M2TTAT(8),=XL8'4020202020202120'                     J
         ED    M2TTAT(8),PAKT+4                                     J
         SPACE 1
         MVI   M2SWAPS-1,C' '          CLEAR SMALL AMOUNT           J
         L     R7,PSATOLD-PSA                                    83143
         ICM   R7,15,TCBUSER-TCB(R7)                             83143
         BZ    SOFLO1        SKIP IF NONE                        83143
         MVI   UTF1-USERTCB(R7),0   RESET PRIVILEGE BITS         83143
         XC    UTMF-USERTCB(L'UTMF+L'UTMG,R7),UTMF-USERTCB(R7)   83143
         L     R6,UTJP-USERTCB(,R7)  TOTAL SWAPS                 83143
         C     R6,=F'9999999'     TOO MUCH TO PRINT ??              J
         BH    SOFLO                                                J
         CVD   R6,PAKT                                              J
         MVC   WORK10(10),=XL10'40202020202020202120'               J
         ED    WORK10(10),PAKT+3                                    J
         MVC   M2SWAPS(8),WORK10+2                                  J
         B     SOFLO1                                               J
SOFLO    MVC   M2SWAPS(8),=8C'*'                                    J
         SPACE 1
SOFLO1   L     R6,SMF5TJS         SERVICE UNITS                  83143
         CVD   R6,PAKT                                              J
         MVC   M2SVUN(8),=XL8'4020202020202120'                     J
         ED    M2SVUN(8),PAKT+4                                     J
         SLR   R7,R7                                             83143
         ICM   R7,3,SMF5PGNO    PERFORMANCE GROUP                83143
         CVD   R7,PAKT                                              J
         MVC   M2PGNO-1(4),=XL4'40202120'                           J
         ED    M2PGNO-1(4),PAKT+6                                   J
*        B     WTOIT                                                J
         POP   USING                                             83143
         TITLE 'I E F A C T R T  ***  WTO PROCESSING'            83143
WTOIT    MVI   WTOAREA+2,X'80'   SET ROUTING/DESCRIPTORS PRESENT 83143
         LA    R1,55+4       POS. OF EOJ                        GP04044
         STH   R1,WTOAREA    BUILD RDW                          GP04044
         LA    R1,WTOAREA(R1)  END OF TEXT                      GP04044
         MVC   0(4,R1),=XL4'00004020'   ROUTCDE=(2,11)          GP04044
         MVC   WTOAREA+4(32),MSG1                                   *
         MVC   WTOAREA+37(11),=CL11'------------'                83143
         MVC   WTOAREA+50(5),M1CC-1                              83143
         TM    BITS,X'01'     IS THIS JOB TERM
         BZ    NJT            NO
         MVC   WTOAREA+51(4),=CL4' EOJ'
NJT      WTO   MF=(E,WTOAREA)                                    83143
         TITLE 'I E F A C T R T  ***  WTP PROCESSING'            83143
NOWTO    LA    R1,VMSG1      SET FOR FIRST MESSAGE               83143
         BAS   R9,YAK        WRITE THE MESSAGE
         TM    BITS,X'01'         IS THIS AN ENTRY FROM JOBTERM ?
         BO    RETURN             YES..DON'T DO MSG2             83143
         LA    R1,VMSG2      SET FOR SECOND MESSAGE              83143
         BAS   R9,YAK        WRITE THE MESSAGE
         LA    R1,SPACER                                         83143
         BAS   R9,YAK        SEPARATE THE STEPS                  83143
*        B     RETURN
         SPACE 1                                                 83143
RETURN   SLR   R9,R9         SET RETURN CODE                     83143
EXITS    PGMEXIT RC=(R9)     FREE AND RETURN                    GP04042
         SPACE 2                                                 83143
*        OUTPUT ROUTINE LINKING TO IEFYS                         83143
*                                                                83143
YAK      LH    R14,0(,R1)    GET LENGTH OF MESSAGE               83143
         SH    R14,H4        SUBTRACT V REC LEN                  83143
         ST    R14,LCTPARM2    STORE IN PARAMETER LIST           83143
         LA    R15,4(R1,0)   GET ADDRESS OF TEXT                 83143
H4       EQU   *-2           CONSTANT                            83143
         ST    R15,LCTPARM1  STASH FOR YS                        83143
YAKMILK  L     R15,YAD       GET ADDRESS OF YS                   83143
         BASR  R14,R15       PRINT THE LINE                      83143
         BR    R9            RETURN TO CALLER                    83143
         TITLE 'I E F A C T R T  ***  TIME CONVERSION ROUTINE'   83143
TIMECVT  DS    0H
         L     R9,WK1                                               *
         SR    R8,R8                                                *
         D     R8,=F'100'         TO SECONDS                        *
         SR    R8,R8                                                *
         D     R8,=F'3600'        HOURS                             *
         CVD   R9,PAKT                                              *
         UNPK  HOUR,PAKT                                            *
         LR    R9,R8                                                *
         SR    R8,R8                                                *
         D     R8,=F'60'          MIN                               *
         CVD   R9,PAKT                                              *
         UNPK  MINUTE,PAKT                                          *
         CVD   R8,PAKT            SEC                               *
         UNPK  SECOND,PAKT                                          *
         OC    TIME(8),=8X'F0'                                      *
         MVI   COL1,C'.'                                            *
         MVI   COL2,C'.'                                            *
         BR    R7                 RETURN                            *
         TITLE 'I E F A C T R T  ***  EQUATES AND WORK AREAS'    83143
YAD      DC    V(IEFYS)      INITIATOR SMB WRITER ROUTINE        83143
DEMOACCT DC    CL8'6500DEMO'   BENCHMARK AND DEMOS               83143
DEMOPRTY DC    AL1(1)        PRIORITY FOR DEMO BILLING           83143
*                                                                83143
SPACER   VCON  ' '           BLANK LINE                         GP04044
JOBFAIL  VCON  'MSG634E ACCOUNT FIELD INCORRECT'                GP04044
STEPFAIL VCON  'MSG635E PGM= INVALID FOR CLASS OR ACCOUNT'      GP04044
         SPACE 1                                                 83143
TRTABHEX DC    C'0123456789ABCDEF'                               83143
HEXTRTAB EQU   TRTABHEX-C'0'                                     83143
         SPACE 2                                                 83143
FLUSHED  EQU   X'01'                                                *
ABENDED  EQU   X'02'                                                *
         SPACE 2                                                 83143
         LTORG ,                                                 83143
         TITLE 'I E F A C T R T  ***  DSECTS'                    83143
PARMSECT DSECT ,             PARM LIST PASSED BY INITIATOR       83143
EXTJMR   DS    A             IEFJMR ADDRESS                      83143
EXTJN    DS    A                  JOBNAME EBCDIC                 83143
EXTPGMR  DS    A                  PROGRAMMER NAME                83143
EXTJOBTT DS    A                  JOB CPU (TCB) TIME BYTE 4 =#JCARD
EXTJACT  DS    A                  JOB ACCT DATA                  83143
EXTSTPTT DS    A                  STEP CPU (TCB) TIME -BYTE 4= EXEC
EXTSACT  DS    A                  STEP ACCT INFO                 83143
EXTTFLG1 DS    A                  FLAGS                          83143
EXTCC    DS    A                  COMPLETION CODE                83143
EXTRDW   DS    A                  POINTER TO SMF RECORD          83143
         SPACE 2                                                 83143
SAVE     DSECT ,                                                 83143
FORYS    DS    (45-18)F           FIELD FOR IEFYS                83143
         ORG   FORYS                                             83143
WK1      DS    F                                                    *
PAKT     DS    D                                                    *
TIME     DS    0CL8                                                 *
HOUR     DS    CL2                                                  *
COL1     DS    CL1                                                  *
MINUTE   DS    CL2                                                  *
COL2     DS    CL1                                                  *
SECOND   DS    CL2                                                  *
         DS    CL2                                                  *
WORK10   DS    CL10                                                 *
         ORG   ,                                                 83143
BITS     DS    CL1           1-JOB / 0-STEP FLAG                    *
WTOAREA  VCON  END=WTOEND                                       GP04044
         DC    128C'*'                                          GP04044
         VCON  *END                                             GP04044
         DC    XL4'0'        DESCRIPTOR/ROUTING CODES           GP04044
ENDWTO   EQU   *                                                    *
         EJECT                                                      *
VMSG1    VCON  END=M1END          V-FORMAT HEADER               GP04044
MSG1     DS    C'MSG374I'                                       GP04044
         DS    CL1                                                  *
M1JN     DS    CL8,C              JOBNAME                           *
M2PGNO   DS    0CL3               PERFORMANCE GROUP NUMBER
M1SNO    DS    CL3,C              STEP NUMBER                       *
M2SVUN   DS    0CL8               SERVICE UNITS
M1SN     DS    CL8,C              STEPNAME                          *
M2TTAT   DS    0CL8           TOTAL TRANS ACTIVE TIME
M1PN     DS    CL8,C              PROGRAM NAME                      *
M2SIT    DS    0CL8                                                 *
M1SIT    DS    CL8,C              STEP INIT                         *
M2SET    DS    0CL8                                                 *
M1SET    DS    CL8,C              STEP TERM                         *
M2TCB    DS    0CL8                                                 *
M1TCB    DS    CL8,C              TCB (STEP) TIME                   *
M2SRB    DS    0CL8                                                 *
M1SRB    DS    CL8,C              SRB TIME (FOR STEP)               *
M2ELPS   DS    0CL5
M1ELPS   DS    CL5,C              ELAPSED TIME                      *
M2SWAPS  DS    0CL8                                                 *
M1SWAPS  DS    CL8,C              SWAPS THIS STEP                   *
         DS    CL1            REGION REQUEST(REAL OR VIRTUAL)
M2SU     DS    CL5,CL3            STORAGE USED                      *
M1CC     DS    CL4                COMPLETION CODE                   *
         VCON *END                                              GP04044
M1LEN    EQU   M1END-MSG1                                       GP04044
         DS    CL1           EDITING OVERFLOW                       *
         EJECT
VMSG2    VCON  END=M2END                                        GP04044
MSG2     DS    C'MSG374I'         PAGE INFO FOR SYSOUT              *
         DS    CL1                                              GP04044
M2PI     DS    CL8,C              PAGE INS                          *
M2PO     DS    CL8,C              PAGE OUTS                         *
M2PLBL   DS    CL4,C                                            GP04044
M2#SWAP  DS    CL8,C         NUMBER OF SWAPS                     83143
M2SI     DS    CL8,C              PAGES SWAPPED IN                  *
M2SO     DS    CL8,C              PAGES SWAPPED OUT                 *
M2SLBL   DS    CL4,C                                            GP04044
M2VI     DS    CL8,C              VIO PAGES IN                      *
M2VO     DS    CL8,C              VIO PAGES OUT                     *
M2VLBL   DS    CL4,C                                            GP04044
         VCON  *END                                             GP04044
M2LEN    EQU   M2END-MSG2                                       GP04044
         DS    CL2                                                  *
SAVEEND  EQU   *                                                GP04042
         SPACE 1                                                 83143
         PRINT &PRTMAC                                           83143
         EJECT
SMFFOUR  DSECT ,
         IFASMFR (4)
         EJECT
SMFFIVE  DSECT ,                                                 83143
         IFASMFR (5)
         EJECT ,                                                 83143
         PRINT &PRTSYS                                           83143
CVTDSECT DSECT ,                                                 83143
         CVT   DSECT=YES                                         83143
         IHAPSA ,                                                83143
         IHALPDE ,                                               83143
         IKJTCB ,                                                83143
IEFLCT   DSECT ,                                                 83143
         IEFALLCT ,                                              83143
USERTCB  USERTCB ,                                               83143
         AIF   ('&LOCAL' NE 'PID').BIGEND                       GP04044
USERCVT  USERCVT ,                                               83143
         USERVOLT LIST=NO                                        83143
.BIGEND  END   ,                                                GP04044
