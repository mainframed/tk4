AUCAG    TITLE 'I S T A U C A G  ***  VTAM ACCOUNTING EXIT'      92012
*    FROM CBT TAPE FILE 70 - AUTHOR FRANK NASH, GENERAL DYNAMICS 92012
***********************************************************************
**  VTAM EXIT (ACCOUNTING) ROUTINE                                   **
**     THIS ROUTINE IS CALLED EACH TIME A SESSION IS ESTABLISHED     **
**   OR TERMINATED.  A SEPARATE INVOCATION IS MADE FOR EACH SESSION. **
**   THE PURPOSE OF THIS ROUTINE IS TO KEEP STATISTICS IN ORDER      **
**   TO APPROPRIATLY CHARGE USERS OF APPLICATION PROGRAMS AND LU'S.  **
**  INITIAL REGISTER CONTENTS                                        **
**   REGISTER 0  -  POSITIVE IF SESSION HAS BEEN ESTABLISHED;        **
**                  NEGATIVE IF SESSION HAS BEEN TERMINATED          **
**   REGISTER 7  -  POINTER TO A DOUBLEWORD CONTAINING THE NAME      **
**                  OF THE PRIMARY LOGICAL UNIT (LU)                 **
**   REGISTER 11 -  POINTER TO A DOUBLEWORD CONTAINING THE NAME      **
**                  OF THE SECONDARY LOGICAL UNIT                    **
**   REGISTER 13 -  ADDRESS OF THE SAVE AREA FOR USE BY THIS ROUTINE **
**   REGISTER 14 -  RETURN ADDRESS                                   **
**   REGISTER 15 -  ADDRESS OF THE ENTRY POINT TO THIS ROUTINE       **
**                                                                   **
**    REFERENCE: ADV COMM FUNCTION FOR VTAM - INSTALLATION           **
***********************************************************************
         SPACE 1                                                 92012
         PRINT NOGEN                                             92012
ISTAUCAG START 0                                                 92012
         SAVE  (14,12),,*
         LR    R12,R15       GET BASE                            92012
         USING ISTAUCAG,R12      SETUP ADDRESSING FOR CSECT
         MODESET  KEY=ZERO   GET REALLY PRIVILEGED
*  DO GETMAIN FOR WORK AREA SPACE FOR SMF RECORD AND SAVE AREA
         GETMAIN  R,LV=SAVEND-SAVE   STORAGE FOR WORK AREA       92012
         ST    R13,4(,R1)        SET BACKWARD PTR
         ST    R1,8(,R13)        SET FORWARD PTR
         LR    R13,R1            PUT ADDR OF SAVE AREA IN R13
         L     R1,4(,R13)    RETRIEVE CALLER'S PARM ADDRESS
         L     R0,20(,R1)        RESTORE R0
         L     R1,24(,R1)        RESTORE R1
         USING SAVEAREA,R13      SETUP ADDRESSING FOR DSECT
*  FIRST SIX BYTES OF RECORD ARE FIXED
         XC    VACRDW(VACRLEN),VACRDW  CLEAR UNUSED FIELDS       92058
SETCONST MVI   VACRDW+1,VACRLEN  SET RECORD LENGTH               92012
         MVI   VACFLG,2      SET MVS FLAG                        92012
         MVI   VACTYPE,X'CD' SET RECORD TYPE 205                 92012
*  SET THE CONNECT/DISCONNECT FLAG
SETDFLG  LTR   R0,R0         TEST FOR CONNECT/DISCONNECT         92012
         BNM   HIGH          IF ZERO/PLUS, THEN CONNECT          92012
         MVI   VACONOFF,C'1'   DISCONNECT                        92012
         B     SETLUS
HIGH     MVI   VACONOFF,C'0'   CONNECT                           92012
SETLUS   MVC   VACAPPL,0(R7)  GET APPLICATION NAME               92012
         MVC   VACTERM,0(R11)  GET LOGICAL UNIT/TERMINAL NAME    92012
*  CALL TIME SVC TO GET TIME AND DATE
         TIME  BIN               GET TIME AND DATE FOR WRITE
         STCM  R0,15,VACTME                                      92012
         STCM  R1,15,VACDTE                                      92012
         L     R4,16             GET ADDRESS OF CVT
         L     R4,196(,R4)       GET ADDRESS OF SMCA
         MVC   VACSID,16(R4)  GET SYSTEM ID                      92012
*  CALL SMF SVC TO WRITE A SMF RECORD
         SMFWTM VACREC       WRITE VTAM ACCOUNTING RECORD        92012
         LTR   R15,R15           TEST FOR ZERO RETURN CODE
         BZ    EXIT1
         WTO   MF=(E,WTO1)       ERROR - WRITE WTO
*  DO CLEANUP BEFORE EXITING
EXIT1    LR    R1,R13            RESTORE R1 FOR FREEMAIN
         L     R13,4(,R13)       RESTORE R13
         FREEMAIN R,LV=SAVEND-SAVE,A=(1) FREE WORK AREA          92012
         MODESET  KEY=NZERO
         RETURN   (14,12),RC=0   RETURN TO VTAM
*  CONSTANT DATA
WTO1     WTO   'MSG252E  ERROR IN ISTAUCAG',ROUTCDE=(1,2,8,10),MF=L
         SPACE 1                                                 92012
*  DSECT TO DEFINE WORK AREAS FOR GETMAIN SPACE
SAVE     DSECT ,             SAVE AND WORK AREA                  92012
SAVEAREA DS    18F
VACREC   DS    0Y                                                92012
VACRDW   DS    2Y            RECORD DESCRIPTOR WORD              92012
VACFLG   DS    BL1
VACTYPE  DS    BL1           RECORD TYPE                         92012
VACTME   DS    BL4           TIME (BINARY)                       92012
VACDTE   DS    PL4           DATE (PACKED DECIMAL)               92012
VACSID   DS    CL4           SYSTEM ID                           92012
VACJBN   DS    CL8           JOBNAME - LEAVE ROOM FOR STANDARD   92012
VACRST   DS    BL4           READER START        JOB STAMP       92012
VACRSD   DS    BL4           READER START                        92012
VACUIF   DS    CL8           ACCOUNT                             92058
*           REF: MVS SYS MNGT FACILITIES
VACONOFF DS    CL1           LOGON/LOGOFF FLAG                   92012
VACAPPL  DS    CL8           APPLICATION ID                      92012
VACTERM  DS    CL8           TERMINAL ID                         92012
VACRLEN  EQU   *-VACRDW      LENGTH OF RECORD                    92012
SAVEND   EQU   *             END OF SAVE AREA                    92012
         SPACE 1                                                 92012
         PRINT NOGEN                                             92012
         REGEQU ,            REGISTER NAMES                      92012
         END
