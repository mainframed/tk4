J041     TITLE '@ S R V J 0 4 1  ***  JES2 4.1 SERVICE ROUTINES'
         MACRO ,
&L       SAVEX &R1,&R3,&LOC,&TYPE=*
&L       SWAPR R14,R15       @SERVICE IS SCREWY
         AIF   ('&TYPE' EQ 'STM').STM
         MNOTE 8,'TYPE=&TYPE UNKNOWN - TYPE=STM ASSUMED'
         AGO   .STM
.STM     ANOP  ,
         STM   &R1,&R3,&LOC
         ORG   &SYSECT+2                                         90274
         DC    X'E0'         MAKE 47F0E020 - BRANCH AROUND ID    90274
         ORG   ,                                                 90274
         MEND  ,
         SPACE 2
         COPY  OPTIONGB
         SPACE 1
         GBLB  &MAPONCE      REGEQU EXPANSION FLAG
&MAPONCE SETB  1             JES2 IS FUSSY
         SPACE 1
         SYSPARM LIST=YES    ASSEMBLE WITH 4.1 OPTIONS/LIBRARIES
         SPACE 1                                                 86251
         PRINT OFF,NOGEN     COPY $GLOBAL                        86251
*PRE-MVS COPY  $GLOBAL       COPY HASP DEFINITIONS               86251
         COPY  $HASPGEN                                          86251
.COPYGBL TITLE '@ S R V J 0 4 1  ***  JES2 4.1 SERVICE ROUTINES'
         PRINT ON                                                90274
         SPACE 1
***********************************************************************
*                                                                     *
*                                                                     *
*        COPYRIGHT 1990  EXPERT SYSTEM PROGRAMMING, INC.              *
*        COPYRIGHT 2003  EXPERT SYSTEM PROGRAMMING                    *
*                        176 OLD STAGE COACH ROAD                     *
*                        BRADFORD, VT 05033-8844                      *
*                                                                     *
*                    ALL RIGHTS RESERVED                              *
*                                                                     *
***********************************************************************
         SPACE 1
         LCLA  &QINC                                             90274
         LCLB  &LOCO
&LOCO    SETB  ('&LOCAL' EQ 'CCSI')  SHORT-CUT FOR LOCAL TESTS
         SPACE 2
***********************************************************************
*                                                                     *
*        @SRVJ041 CONTAINS ALL JES2 RELATED CODE FOR THE @SERVICE     *
*        ROUTINE.                                                     *
*                                                                     *
*        THE CODE HAS BEEN SPLIT OFF FROM @SERVICE, IN ORDER TO       *
*        PROVIDE FOR MULTIPLE JES2 SUB-SYSTEMS RUNNING ON THE SAME    *
*        MACHINE, AND TO AFFORD IEFUJV, IEFUJI AND IEFUSI SOME DEGREE *
*        OF INDEPENDENCE FROM SUB-SYSTEM CONTROL BLOCKS.              *
*                                                                     *
***********************************************************************
*                                                                     *
*        TO MAKE LIFE INTERESTING, THIS ROUTINE IS CALLED WITH THE    *
*        ENTRY POINT ADDRESS IN R14 AND THE RETURN ADDRESS IN R15     *
*                                                                     *
***********************************************************************
*   FUNCTION REQUEST IS IN R0: 0000NNMM                               *
*     nn10 : ACGET                                                    *
*     000B : JES2 INTERFACE INFORMATION                               *
*     010B : LOCATE JOB INFORMATION                                   *
*     020B : MODIFY JOB STATUS/CLASS                                  *
***********************************************************************
         SPACE 2
         PRINT &PRTSOR
@SRVJ041 SAVEM ZERO12,BASE=R12,PARM=R1,PFX=SAWE         ADDED ON 90273
         LR    R6,R0         COPY ENTRY R0
         LR    R7,R1         COPY ENTRY R1
         STM   R0,R1,CALLR0  SAVE ENTRY CODES
         ST    R1,RETR1      PROVISIONALLY LEAVE R1 UNCHANGED    90274
         LA    R10,EXIT      POINT TO GOOD EXIT
         SRL   R0,4          SHIFT 10->n1  0B->n0
         N     R0,=A(15)     ELIMINATE n NYBBLE
         BIX   VAL=(R0),ERR=BADENTRY,PFX=,LOC=(GROUP0,GROUP1)
         SPACE 1
BADENTRY MVI   RC,16         SET RETURN CODE 16
         MVI   RV,VRPARM     INVALID PARM/ENTRY CODE
         B     EXIT          AND EXIT WITH ERROR
         SPACE 1
BADCHAR  MVI   RV,VRSYNT     SYNTAX ERROR OR BAD CHARACTERS IN PARM
BADRET8  MVI   RC,8          SET RETURN CODE 8
         B     EXIT
         SPACE 1
BADRET4  MVI   RC,4          SET RETURN CODE 4
*        B     EXIT
         SPACE 1
ENTEXIT  DS    0H            NORMAL RETURN                       90274
EXIT     ENDM  COPYRET=(RETCH-SAWE(R1),12),PFX=SAWE  RETURN R15,R0,R1
         TITLE '@ S R V J 0 4 1  ***  ACCOUNT NUMBER PROCESSING'
GROUP1   BIX   VAL=CALLMODE,ERR=BADENTRY,PFX=ENT,LOC=(ACGET)    GP06284
         SPACE 2
*        ENTRY CODE 16 - GET ACCOUNT NUMBER OF CURRENT USER
*        RETURN R15 - ALWAYS ZERO (UNLESS R1 SUPPLIED AS ZERO)   81151
*        0(8,R1) - UPDATED WITH CURRENT ACCOUNT NUMBER (OR DEFAULT)
*        R0 - HIGH TWO BYTES - BINARY ACCOUNT EQUIVALENT         81151
*             LOW BYTE - PRIVILEGES (MAPPED BY VAA FLAGS)        81151
*
*        NOTE:  CCSI ACCOUNT NUMBERS ARE FOUR BYTES, PLUS A FOUR BYTE
*          REQUIRED SUBACCOUNT. FIRST BYTE IS NUMERIC (ON JOB CARD
*          AND FOR INTERNAL PROCESSING); OR EQUIVALENT ALPHA IN  81151
*          DATASET NAMES (E.G. // JOB (0904,1234 AND WYL.Z904.LIB).
*          LEADING ZERO IS CONVERTED TO Z, 1-9 TO A-I.           81151
*        THE SECOND BYTE IS ALPHANUMERIC, THE THIRD AND FOURTH BYTES
*          ARE NUMERIC.  THE SUBACCOUNT MUST BE ALPHANUMERIC.    81151
*        A LOCAL MODIFICATION TO IEFUJV SAVES THE 8-BYTE NUMBER  81151
*          (FOR A VALID JOB) IN JMRUSEID.                        81151
*                                                                81151
*        MODE=0  GET CURRENT ACCOUNT FROM JMR / RETURN PRIVY FLAGS
*                WHEN THERE IS NO JMR, LOCATES $JCT ACCOUNT.     83254
*                SUPPLIED BY LOCAL HASPRDR CHANGE.               83254
         SPACE 1                                                 81151
ENTACGET LTR   R1,R7         ANY PARM ADDRESS ?
         BZ    BADENTRY      NO; FAIL
         AIF   (&LOCO).N1L1D                                     81151
         B     EXIT          ACCOUNT FUNCTIONS NOT SUPPLIED      81151
         AGO   .N1L1C                                            81151
.N1L1D   IC    R0,CALLMODE   GET SUB-FUNCTION                    81151
         BIX   ERR=BADENTRY,PFX=ENT,LOC=(GETAC)
ENTGETAC LA    R10,EXIT      JUST IN CASE - SET THE RETURN ADDRESS
GETACCT  LTCB  R2            GET CURRENT TCB                     86314
         USING TCB,R2                                            81137
         LAT   R2,TCBTCT,ACCTGNON  START TASK IF NO TCT
         USING SMFTCT,R2                                         81137
         LAT   R2,TCTJMR,ACCTGNON  TRY DEFAULT IF NO JMR         85021
         USING JMR,R2
         CLI   JMRUSEID,C'A'  WAS ACCOUNT NUMBER FILLED IN ?     85021
         BNL   ACCTGGOT      PRESUMABLY - USE IT                 85021
ACCTGNON LA    R2,ACCT#DEF-(JMRUSEID-JMR)  POINT TO DEFAULT ACCOUNT
         AIF   (NOT &MVS).NJESJCT                                83254
         LTCB  R15           GET TCB BACK                        83254
         ICM   R15,7,TCBJSCBB-TCB(R15)  GET JSCB                 83254
         BZ    ACCTJNON      OR SKIP                             83254
         USING IEZJSCB,R15                                       83254
         ICM   R15,15,JSCBSSIB  GET SUBSYSTEM INFORMATION BLOCK  83254
         BZ    ACCTJNON      OR OUT                              83254
         USING SSIBEGIN,R15                                      83254
         CLC   EXLOCNMS,SSIBSSNM  UNDER MASTER ?                 90287
         BE    ACCTJNON      YES; NO INFORMATION AVAILABLE       90287
         ICM   R15,15,SSIBSUSE  GET SJB ADDRESS                  83254
         BZ    ACCTJNON                                          83254
         USING SJBDSECT,R15                                      83254
         CLC   =C'SJB ',SJBID   REALLY SJB ?                     83254
         BNE   ACCTJNON      NO                                  83254
         ICM   R15,15,SJBJCT   GET $JCT                          83254
         BZ    ACCTJNON                                          83254
         USING JCTDSECT,R15                                      83254
         LA    R2,JCTACCTN-(JMRUSEID-JMR)  FAKE POINTER          83254
         CLI   JCTJOBID,C'S'   START JOB ?                       85097
         BNE   ACCTGGOT      NO; GO TO MOVER                     85097
         DROP  R15                                               83254
.NJESJCT SPACE 1                                                 83254
ACCTJNON OI    RV,VAASTC     SET START TASK FLAG                 83254
         MVC   0(8,R1),JMRUSEID  MOVE CANNED ACCOUNT NUMBER      82018
         B     ACCTPRIM      JOIN COMMON CODE                    82018
         SPACE 1                                                 82018
ACCTGGOT MVC   0(8,R1),JMRUSEID   SEND ACCOUNT NUMBER TO USER
         SPACE 1
ACCTCOMM DS    0H            WAS LA R10,EXIT                     86314
ACCTPRIV NI    RV,255-VAASTC  MAKE SURE IT'S CLEAN               81215
         L     R15,PSAAOLD-PSA  GET ASCB ADDRESS                 85097
         ICM   R15,15,ASCBCSCB-ASCB(R15)  GET CSCB               85097
         BZ    ACCTPRIS      SET START TASK IF NO CSCB           85097
         USING CHAIN,R15     DECLARE IT                          85097
         CLI   CHVCD,X'0C'   MOUNT ?                             85097
         BE    ACCTPRIS      YES; TREAT AS START JOB             85097
         CLI   CHVCD,X'04'   START COMMAND ?                     85097
         BNE   ACCTPRIM      NO                                  85097
         TM    CHTRKID,CHINITID  INITIATOR ?                     85097
         BO    ACCTPRIM      YES; TREAT AS JOB                   85097
ACCTPRIS OI    RV,VAASTC     SET START TASK FLAG                 85097
ACCTPRIM CLI   0(R1),C'0'    SPECIAL ACCOUNT ?                   82018
         BNH   ACCTPRSY      YES; CHECK FOR SYSTEMS PRIVILEGES   81215
         OI    RV,VAAINH     PROVISIONALLY SET IN-HOUSE          81215
         CLC   =C'8000',0(R1)    IN-HOUSE ODD-BALL ?             82052
         BE    ACCTPRCM      YES                                 82052
         CLC   0(2,R1),=C'61'  IN-HOUSE LOWER RANGE ?            81215
         BL    ACCTPRCM      YES                                 81215
         NI    RV,255-VAAINH                                     81215
         OI    RV,VAAUSER    ELSE SET USER                       81215
         CLC   0(4,R1),=C'6500'  USER ?                          81215
         BH    ACCTPRCM      YES                                 81215
         NI    RV,255-VAAUSER-VAAINH  RESET                      81215
ACCTPRSY CLC   ACCT#SYS(3),0(R1)   SPECIAL ACCOUNT ?             81215
         BNE   ACCTPROH      NO; JUST SET OVERHEAD               81215
         CLI   3(R1),C'4'    SPECIAL RANGE ?
         BL    ACCTPROH      NO; OVERHEAD                        81215
         BE    ACCTGSYS      CHECK FOR SYSTEMS
         CLI   3(R1),C'8'
         BNH   ACCTPRSU      IN RANGE - TECHNICAL SUPPORT, ETC.  81215
         CLI   4(R1),C'0'    ANOTHER SPECIAL ?                   81215
         BE    ACCTPROH      YES; JUST SET OVERHEAD              81215
         LA    R1,4(,R1)     ACCT 0909 IS SPECIAL; REAL ACCT IN NEXT 4
         B     ACCTPRIV      TRY IT AGAIN WITH LOWER NUMBER      81215
ACCTPRSU TM    3(R1),1       905,907 ?                           86314
         BNZ   ACCTPROH      YES - JUST PLAIN OVERHEAD           86314
         OI    RV,VAASUP     SET TECHNICAL SUPPORT, ETC.         81215
         B     ACCTPROH      AND GO TO SET OVERHEAD ALSO         81215
ACCTGSYS CLC   ACCT#SUB(3),4(R1)  PROPER SUB-ACCOUNT ?           81151
         BNE   ACCTPROH      NO; SET OVERHEAD                    81215
         OI    RV,VAASYS     SET SYSTEM PRIVILEGES
ACCTPROH OI    RV,VAAOHD     SET OVERHEAD ACCOUNT                81215
ACCTPRCM BR    R10           RETURN TO CALLER                    81215
         DROP  R2
.N1L1C   TITLE '@ S R V J 0 4 1  ***  JES2 LOOK-UP FUNCTIONS'
GROUP0   BIX   VAL=CALLMODE,ERR=BADENTRY,PFX=ENT,                      *
               LOC=(JESVC,JESVC,JESVC)                          GP06284
         SPACE 2                                                 90273
ENTJESVC LTR   R1,R7         USER PASSED PARAMETER LIST ?
         BZ    BADENTRY      NO; ERROR
         SERVINIT ,          GET THE @SERVICE ADDRESS
         MVC   EXWJ2NAM,0(R7)  MOVE SUBSYSTEM NAME
         CLI   EXWJ2NAM,C' '   SUB-SYSTEM NAME SET ?             85070
         BH    *+10          YES                                GP06284
         MVC   EXWJ2NAM,EXLOCNAM  NO; MOVE DEFAULT               85070
         L     R6,CVTPTR     GET THE CVT                         85070
         USING CVTMAP,R6                                         85070
         ICM   R5,15,CVTJESCT  SUBSYSTEM VECTOR TABLE            85070
         BNP   BADRET4       MAJOR BOOBOO                        85070
         USING JESCT,R5      DECLARE IT                          85070
         CLI   EXWJ2NAM,C' '   SUBSYSTEM DEFAULTED ?             85070
         BH    EXLOCHSY      NO                                  87270
         MVC   EXWJ2NAM(L'JESPJESN),JESPJESN  COPY PRIMARY NAME  85070
         L     R4,PSATOLD-PSA  GET MY TCB BACK                   87270
         L     R4,TCBJSCB-TCB(,R4)  GET THE JSCB                 87270
         ICM   R4,15,JSCBSSIB-IEZJSCB(R4)  TEST SSIB             87270
         BZ    EXLOCHSY      LEAVE PRIMARY                       87270
         CLI   SSIBSSNM-SSIB(R4),C' '  SUBSYSTEM PRESENT ?       87270
         BNH   EXLOCHSY      NO; KEEP PRIMARY                    87270
         CLC   EXLOCNMS,SSIBSSNM-SSIB(R4)  RUNNING ALONE ?       87270
         BE    EXLOCHSY      YES; KEEP PRIMARY                   87270
         MVC   EXWJ2NAM(4),SSIBSSNM-SSIB(R4) USE STARTER'S NAME  87270
EXLOCHSY LA    R5,JESSSCT-(SSCTSCTA-SSCT)  CHEAT                 87270
         USING SSCT,R5                                           85070
EXLOCSLP ICM   R5,15,SSCTSCTA   GET NEXT SSCVT                   85070
         BNP   BADRET4       USER CHOSE BAD NAME ?               85070
         CLC   SSCTSNAM,EXWJ2NAM  DESIRED SUBSYSTEM ?            85070
         BNE   EXLOCSLP      NO                                  85070
         ICM   R4,15,SSCTSSVT  TEST FOR SVT                      85070
         BNP   BADRET4       NONE ?                              85070
         L     R15,CVTASVT   GET THE ASCB VECTOR TABLE           85070
         USING ASVT,R15                                          85070
         L     R0,ASVTMAXU   GET HIGHEST VALID ASID              85070
         SLL   R0,2          *4 FOR FAST CHECK                   85070
         SLR   R1,R1         SET INDEX FOR NEXT ASID             85070
EXLOCSAD LA    R1,4(,R1)     GET NEXT ASID INDEX                 85070
         CR    R1,R0         VALID ?                             85070
         BH    BADRET4       NO; QUIT                            85070
EXLOCSOM LA    R3,ASVTFRST(R1)  POINT TO ASCB POINTER            85070
         ICM   R3,15,0(R3)   LOAD AND TEST                       85070
         BNP   EXLOCSAD      SKIP UNUSED/FREE SLOT               85070
         USING ASCB,R3                                           85070
         ICM   R2,15,ASCBJBNI  JOB CSCB ?                        85070
         BNZ   EXLOCSAD      YES; SKIP                           85070
         ICM   R2,15,ASCBCSCB  GET THE START CSCB                85070
         BZ    EXLOCSAD                                          85070
         USING CHAIN,R2      DECLARE THE CSCB                    85070
         CLI   CHVCD,X'04'   START CODE ?                        85070
         BNE   EXLOCSAD      NO                                  85070
         CLC   CHCLS,EXWJ2NAM   MATCHING ID ?                    85070
         BNE   EXLOCSAD      NO                                  85070
         LH    R0,ASCBASID   GET AS ID                           85070
         CH    R0,CHASID     JUST IN CASE                        85070
         BNE   EXLOCSAD                                          85070
         STM   R3,R4,RETR0   RETURN ASCB/$SVT ADDRESSES          85070
         STM   R3,R4,EXWJ2AS# SAVE FOR SCHEDULE                  90274
         MVC   0(8,R7),EXWJ2NAM  RETURN SUB-SYSTEM NAME
         USING SSVT,R4       DECLARE THE $SVT                    85070
         ICM   R2,15,$SVPIT  INITIATOR POINTER ?                 85070
         BZ    BADRET4       NO ?                                85070
         C     R3,$SVPOSTE+4  SAME ASCB IN $SVT                  85070
         BNE   BADRET4       NO                                  85070
         CLC   $SVQLOKE,EXWJ2NAM  RIGHT EXPANSION ?              87270
         BNE   BADRET4       NO; ERROR ?                         85070
         IC    R0,CALLMODE   GET FUNCTION CODE                   90274
         BIX   ERR=BADENTRY,PFX=ENT,                                   *
               LOC=(J2INF,LOJOB,MDJOB)                           90288
EXLOCNAM DC    CL8' JES    '  INITIALIZATION PARAMETER FOR SSID  87270
EXLOCNMS DC    C'MSTR'                                           87270
         SPACE 2                                                 90274
***********************************************************************
*                                                                     *
*        ENTJ2INF LOCATES THE $SVT AND $HCT AND RETURNS INFORMATION   *
*        NOT DEPENDENT ON JES2 MAPPINGS.                              *
*                                                                     *
***********************************************************************
         SPACE 1                                                 90274
ENTJ2INF SERVCALL SSSET,EXWJ2NAM  SPECIFY SUBSYSTEM FOR SCHEDULE 90288
         BXH   R15,R15,BADRET4  HUH ?  SHOULD NEVER HAPPEN       90288
         USING SERVJES,R7    DECLARE USER'S PARAMETER LIST       90288
         LR    R15,R0        COPY ASCB ADDRESS                   90288
         LH    R0,ASCBASID-ASCB(,R15)  GET THE ASID FOR JES      90288
         STM   R0,R1,EXWJ2AS#   SAVE ASID AND $SVT ADDRESS       90288
         SERVCALL SCHIN,CRXSIZE  GET CSA SPACE FOR OUR CODE      90288
         BXH   R15,R15,SCHEDERR  ERROR ?                         90288
         LR    R6,R0         COPY USER ADDRESS                   90288
         SERVCALL SCHMV,CRXCODE,CRXSIZE  INITIALIZE CSA CONTENTS 90288
         BXH   R15,R15,SCHEDERR                                  90288
         MODESET KEY=ZERO    UNLOCK CSA ACCESS                   90288
         MVC   ADDRSVT-CRXCODE(L'ADDRSVT,R6),EXWJ2SVT  PROPAGATE SVT
         MVI   FUNCODE-CRXCODE(R6),FUNINIT  REQUEST INFORMATION  90288
         MODESET KEY=NZERO   RESTORE NORMAL KEY                  90274
         L     R1,EXWJ2AS#   GET JES2 ASID                       90274
         SERVCALL SCHED,(R1) SCHEDULE THE SRB                    86251
         MVC   SERVJES(COPYINF),COPYJES-CRXCODE(R6) RTRN USER'S REQ.
         BXH   R15,R15,SCHEDERR                                  90288
         MVC   RC,REQCODE-CRXCODE(R6)  PROPAGATE RETURN CODE     90288
         B     POSTJES       RETURN VIA COMMON CLEAN-UP          90288
         EJECT ,                                                 90274
***********************************************************************
*                                                                     *
*        ENTLOJOB LOCATES A JOB AND RETURNS JOB INFORMATION IN A DATA *
*        AREA MAPPED BY SERVJES OPT=LOJOB                             *
*                                                                     *
***********************************************************************
         SPACE 1                                                 90274
ENTMDJOB DS    0H            SHARE COMMON LOOK-UP                90288
ENTLOJOB SERVCALL SSSET,EXWJ2NAM  SPECIFY SUBSYSTEM FOR SCHEDULE 90274
         BXH   R15,R15,BADRET4  HUH ?  SHOULD NEVER HAPPEN       90274
         USING SERVJES,R7    DECLARE USER'S PARAMETER LIST       90274
         LR    R15,R0        COPY ASCB ADDRESS                   86251
         LH    R0,ASCBASID-ASCB(,R15)  GET THE ASID FOR JES      86251
         STM   R0,R1,EXWJ2AS#   SAVE ASID AND $SVT ADDRESS       90274
         SERVCALL SCHIN,CRXSIZE  GET CSA SPACE FOR OUR CODE      86251
         BXH   R15,R15,SCHEDERR  ERROR ?                         86251
         LR    R6,R0         COPY USER ADDRESS                   86251
         SERVCALL SCHMV,CRXCODE,CRXSIZE  INITIALIZE CSA CONTENTS 86251
         BXH   R15,R15,SCHEDERR                                  86251
         SPACE 1                                                 90281
         CLC   =C'* ',SJ1JNAM  USER WANT INFORMATION ON SELF ?   90281
         BNE   LOJNOSLF      NO                                  90281
         LTCB  R2,USE=YES    GET TCB                             90281
         L     R1,TCBTIO     GET TIOT                            90281
         MVC   SJ1JNAM,0(R1) COPY TIOT JOB NAME                  90281
         MVI   SJ1QNAM,C'X'  SET FOR EXECUTION QUEUE             90281
         ICM   R2,7,TCBJSCBB GET JSCB                            90288
         BZ    LOJNOSLF      OR SKIP                             90288
         USING IEZJSCB,R2                                        90288
         ICM   R2,15,JSCBSSIB  GET SUBSYSTEM INFORMATION BLOCK   90288
         BZ    LOJNOSLF      OR OUT                              90288
         USING SSIBEGIN,R2                                       90288
         ICM   R2,15,SSIBSUSE  GET SJB ADDRESS                   90288
         BZ    LOJNOSLF                                          90288
         USING SJBDSECT,R2                                       90288
         CLC   =C'SJB ',SJBID   REALLY SJB ?                     90288
         BNE   LOJNOSLF      NO                                  90288
         MVC   SJ1JQOF,SJBJQOFF  COPY JQE OFFSET                 90288
         DROP  R2                                                90281
LOJNOSLF MODESET KEY=ZERO    UNLOCK CSA ACCESS                   90274
         MVC   ADDRSVT-CRXCODE(L'ADDRSVT,R6),EXWJ2SVT  PROPAGATE SVT
         MVC   COPYJES-CRXCODE(COPYLEN,R6),SERVJES  COPY USER'S REQ.
         CLI   CALLMODE,2    MDJOB ENTRY ?                       90288
         BNE   *+8           NO                                  90288
         MVI   FUNCODE-CRXCODE(R6),FUNMOD  SET MODIFY FUNCTION   90288
         MODESET KEY=NZERO   RESTORE NORMAL KEY                  90274
         L     R1,EXWJ2AS#   GET JES2 ASID                       90274
         SERVCALL SCHED,(R1) SCHEDULE THE SRB                    86251
         MVC   SERVJES(COPYLEN),COPYJES-CRXCODE(R6) RTRN USER'S REQ.
         BXH   R15,R15,SCHEDERR                                  86251
         MVC   RC,REQCODE-CRXCODE(R6)  PROPAGATE RETURN CODE     90274
         B     POSTJES       RETURN VIA COMMON CLEAN-UP          90274
         SPACE 2                                                 86251
SCHEDERR WTO   'MSG881E ERROR IN SUB-SYSTEM INTERFACE',                *
               ROUTCDE=1,DESC=3  CONSIDER 1,1 ?                  89309
         LA    R10,BADENTRY  SET FOR MAJOR ERROR                 90274
POSTJES  SERVCALL SCHFR      FREE THE WORK AREA                  90274
         BR    R10           AND RETURN                          90274
         SPACE 1                                                 90274
         PRINT &PRTMAC                                           90274
SERVJES  SERVJES OPT=(J2INF,LOJOB)  USER'S CALLING PARAMETERS    90288
         PRINT &PRTSOR                                           90274
         SPACE 1                                                 86251
@SRVJ041 CSECT ,                                                 86251
         LTORG ,                                                 90274
         SPACE 3                                                 90274
***********************************************************************
*                                                                     *
*        CODE EXECUTED IN CSA (SRB MODE IN JES2 ADDRESS SPACE)        *
*                                                                     *
***********************************************************************
         CRMHEAD ,                                               86251
         MVI   REQCODE-CRXCODE(R13),0  CLEAR THE RETURN CODE     90274
         L     R7,ADDRSVT    GET THE $SVT                        90288
         USING $SVDSECT,R7   DECLARE IT                          90288
         L     R8,$SVHCT     GET THE $HCT ADDRESS                86251
         USING HCTDSECT,R8                                       86251
         LA    R5,COPYJES    POINT TO COPIED USER DATA           90274
         USING SERVJES,R5    BUT DECLARE DSECT                   90274
         CLI   FUNCODE,FUNINIT  INFORMATION CALL ?               90288
         BNE   CRXLOJOB      NO; JOB LOCATION/MODIFICATION       90288
         MVC   SJ2VERS,$VERSION  COPY $HCT VERSION               90288
         MVC   SJ2TON,$SVTOSYS  SYSTEM NUMBER                    90288
         MVC   SJ2TOQ,$SVTOQUL  SYSTEM NUMBER - EBCDIC           90288
         MVC   SJ2MSYS,$SVNOSYS  MAXIMUM SYSTEM NUMBER           90288
         MVC   SJ2MRJE,$SVROUT  MAXIMUM REMOTE NUMBER            90288
         MVC   SJ2COMC,$CCOMCHR  OS COMMAND CHARACTER            90288
         MVC   SJ2COMR,$RCOMCHR  READER COMMAND CHARACTER        90288
         B     CRXDONE       ALL DONE ?                          90288
         SPACE 1                                                 90288
CRXLOJOB ICM   R6,15,SJ1JQOF DID USER PASS A NON-ZERO OFFSET ?   90288
         CLI   SJ1QNAM,C'I'  SCAN INPUT QUEUES ONLY ?            90274
         BE    CRXQINP       INPUT QUEUES ONLY                   90281
         CLI   SJ1QNAM,C'X'  EXECUTION ONLY ?                    90281
         BNE   HSCANALL      NO; SCAN ALL                        86251
CRXQINP  L     R11,$QINDEXA  GET ADDRESS OF QUEUE INDEX TABLE    86251
         SLR   R9,R9           CLEAR FOR IC                      86251
         IC    R9,X'40'(,R11)  GET FIRST INPUT-TYPE QUEUE        90287
         LA    R9,$JQHEADS(R9)  GET QUEUE HEADER ADDRESS         86251
         LA    R11,36+5      SET FOR NUMBER OF QUEUES TO SCAN    90287
         B     HSCANCOM      GO TO COMMON                        86251
HSCANALL LA    R9,$JQHEADS+&QINC   POINT TO FIRST QUEUE          86251
         LA    R11,$JQTYPES-1  SET FOR NUMBER OF QUEUES EXC PURG 86251
HSCANCOM LM    R2,R3,SJ1JNAM  LOAD NAME TO BE FOUND              90274
         SLR   R0,R0                                             90274
         ICM   R0,3,SJ1J###  GET BINARY JOB NUMBER               90274
         CLI   SJ1JNAM,C' '  DID USER PASS A NAME ?              90274
         BH    HSQPTR        YES; PROCEED WITH SCAN LOOP         90274
         SLR   R3,R3         SET QUICK SIGNAL                    90274
&QINC    SETA  2                                                 86251
HSQPTR   LTJQEP R7,0(R9)      TEST QUEUE HEAD                    86251
         BNZ   HSQNJQE       PROCESS THE JQE                     86251
HSQPBUMP LA    R9,&QINC.(,R9)     POINT TO NEXT QUEUE HEAD       86251
         BCT   R11,HSQPTR    TRY THE NEXT QUEUE                  86251
         MVI   REQCODE-CRXCODE(R13),8  SET RETURN CODE 8 - NO MATCH
         CRMEXIT 0           RETURN TO CALLER                    86251
         SPACE 1                                                 86251
         USING JQEDSECT,R7   DECLARE THE JQE MAPPING             86251
HSQNEXT  LTJQEP R7,JQECHAIN   GET NEXT JQE                       86251
         BZ    HSQPBUMP      END OF QUEUE                        86251
HSQNJQE  LTR   R6,R6         WAS A JQE OFFSET PASSED ?           90288
         BZ    HSQNJQE@      NO                                  90288
         CR    R6,R7         IS THIS IS ?                        90288
         BE    HSQNJQE@      YES; USE IT                         90288
         AL    R7,$JOBQPTR   RELOCATE                            90288
         B     HSQNEXT       AND TRY AGAIN                       90288
HSQNJQE@ AL    R7,$JOBQPTR   RELOCATE                            86251
         LTR   R3,R3         ANY NAME TO TEST ?                  90274
         BZ    CRXNJTST      NO                                  90274
         CLM   R3,15,JQEJNAME+4  MATCHING NAME ?                 90189
         BNE   HSQNEXT       NO                                  86251
         CLM   R2,15,JQEJNAME  MATCHING NAME ?                   90189
         BNE   HSQNEXT       NO                                  86251
CRXNJTST LTR   R0,R0         JOB NUMBER TEST ?                   90274
         BZ    CRXNNTST      NO; DON'T TEST JOB NUMBER           90274
         CH    R0,JQEJOBNO   MATCHING NUMBER ?                   90274
         BNE   HSQNEXT       NO; TRY ANOTHER                     90274
CRXNNTST DS    0H                                                90274
         AIF   (NOT &LOCO).NOACCT1                               90274
         CLI   SJ1ACCT,C' '  DID USER PASS AN ACCOUNT ?          90274
         BNH   CRXNATST      NO                                  90274
         CLC   SJ1ACCT,JQEPACCT  SAME ACCOUNT ?                  90274
         BNE   HSQNEXT       NO; IGNORE                          86251
CRXNATST DS    0H                                                90274
.NOACCT1 CLI   SJ1QNAM,C'I'  LOOKING FOR JOB ON ANY OLD QUEUE ?  90274
         BE    CRXQINP2      YES; CHECK ON INPUT                 90281
         CLI   SJ1QNAM,C'X'  IN EXECUTION ?                      90281
         BNE   RETJOB        YES; RETURN WITH IT                 90274
         TM    JQEFLAGS,QUEBUSY  BUSY ?                          90281
         BZ    HSQNEXT       NO; IGNORE                          90281
CRXQINP2 TM    JQETYPE,$XEQ  EXECUTING OR AWAITING EXECUTION ?   86251
         BZ    HSQNEXT       NO; IGNORE                          86251
         SPACE 1                                                 90274
***********************************************************************
*                                                                     *
*        MATCHING JOB LOCATED - RETURN DATA                           *
*                                                                     *
***********************************************************************
         SPACE 1                                                 90274
RETJOB   CLI   FUNCODE,FUNMOD  CHANGE JQE INFORMATION ?          90288
         BNE   RETJOB2       NO                                  90288
         AIF   (NOT &LOCO).NOLFGS                                90288
         MVC   JQEFLAG3,SJ1LFG1  UPDATE LOCAL FLAGS              90288
         MVC   JQEFLAG4,SJ1LFG2    DITTO                         90288
.NOLFGS  MVC   LOOKDB-CRXCODE(1,R13),JQEFLAGS  GET PRIMARY FLAG BYTE
         NI    LOOKDB-CRXCODE(R13),QUEBUSY  LEAVE ONLY SYSTEM ID 90288
         MVC   LOOKDB+1-CRXCODE(1,R13),SJ1FLGS  USER'S REQUEST   90288
         NI    LOOKDB+1-CRXCODE(R13),255-QUEBUSY GET USER'S FLAGS
         OC    LOOKDB-CRXCODE(1,R13),LOOKDB+1 MERGE              90288
         MVC   JQEFLAGS,LOOKDB  RESET                            90288
         SPACE 1                                                 90288
RETJOB2  MVC   SJ1JNAM,JQEJNAME  RETURN NAME                     90274
         MVC   SJ1JNUM,JNUMASK  MAKE MASK                        90274
         SLR   R0,R0         CLEAR HIGH BYTES                    90288
         ICM   R0,3,JQEJOBNO GET BINARY JOB NUMBER               90274
         STH   R0,SJ1J###    RETURN TO USER                      90274
         SRDL  R0,32         PREPARE FOR DIVIDE                  90288
         LA    R2,JOBTYPE    GET PREFIX LIST                     90288
         D     R0,=F'10000'  GET JOB TYPE                        90288
         CVD   R0,LOOKDB-CRXCODE(R13)  MAKE PACKED               90274
         LA    R0,3          GET MASK AND MULTIPLIER             90288
         NR    R1,R0         MASK QUOTIENT                       90288
         MR    R0,R0         MAKE OFFSET INTO LIST               90288
         AR    R1,R2         GET LIST ENTRY ADDRESS              90288
         ED    SJ1JNUM+2(6),LOOKDB+5  MAKE PRETTY                90274
         MVC   SJ1JNUM(3),0(R1)  MOVE JOB/TSU/STC                90287
         MVC   SJ1PTY,JQEPRIO  CURRENT PRIORITY                  90287
         MVC   SJ1CLS,JQETYPE  COPY QUEUE TYPE                   90274
         MVC   SJ1FLGS,JQEFLAGS  AND HOLD/BUSY FLAGS             90288
         MVC   SJ1FLG2,JQEFLAG2  AND MORE FLAGS                  90288
         LR    R15,R9        GET CURRENT QUEUE HEAD              86251
         LA    R4,$JQHEADS   POINT TO QUEUE HEAD                 86251
         SR    R15,R4        GET QUEUE OFFSET                    86251
         SRL   R15,&QINC/2   GET FINAGLED NUMBER                 86251
         STCM  R15,3,SJ1QUE                                      90288
         LR    R15,R7        COPY JQE ADDRESS                    90288
         SL    R15,$JOBQPTR  GET JQE OFFSET                      90288
         ST    R15,SJ1JQOF   RETURN OFFSET                       90288
         TM    JQETYPE,X'40' EXEC/INPUT QUEUE ?                  90288
         BZ    RETQNAME      NO; DO NORMALLY                     90288
         LA    R1,QNAMES+4   SET FOR INPUT                       90288
         TM    JQEFLAGS,QUEBUSY  IN EXECUTION ?                  90288
         BZ    *+8           NO                                  90288
         LA    R1,=C'XEQ'    SET FOR EXECUTION                   90288
         MVC   SJ1QNAM(3),0(R1)                                  90288
         MVC   SJ1QNAM+3(1),JQETYPE                              90288
         OI    SJ1QNAM+3,X'C0'  MAKE COMPLETE QUEUE NAME         90288
         B     RETQNAMD      DONE                                90288
RETQNAME SLR   R15,R15                                           90288
         IC    R15,JQETYPE                                       90288
         LA    R1,QNAMES     POINT TO QUEUE NAME LIST            90288
         SLL   R15,24-1      LEFT ADJUST (SAVE ONE)              90288
         LA    R0,8          TEST EIGHT BITS                     90288
         OR    R15,R8        MAKE LOW NON-ZERO                   90288
RETQNAML BXLE  R15,R15,RETQNAMV  SHIFT AND TEST NEXT BIT         90288
         LA    R1,4(,R1)     POSITION TO NEXT NAME               90288
         BCT   R0,RETQNAML   TRY AGAIN                           90288
RETQNAMV MVC   SJ1QNAM,0(R1) MOVE NAME OF QUEUE                  90288
RETQNAMD DS    0H            DONE                                90288
         AIF   (NOT &LOCO).NOACCT2                               90274
         MVC   SJ1ACCT,JQEPACCT  RETURN ACCT (USER IS AUTHORIZED)
         MVC   SJ1OCLS,JQEOCLAS  ORIGINAL JOB CLASS              90287
         MVC   SJ1OPTY,JQEOPRTY  ORIGINAL PRIORITY               90287
         MVC   SJ1LFG1,JQEFLAG3  AND MORE FLAGS                  90288
         MVC   SJ1LFG2,JQEFLAG4  AND MORE FLAGS                  90288
.NOACCT2 SPACE 1                                                 90288
CRXDONE  CRMEXIT 0           RETURN TO CALLER                    90288
         SPACE 1                                                 90274
JOBTYPE  DC    C'JOBSTCTSU'  TYPE OF 'JOB'                       90287
QNAMES   DC    C'DUMYINP RDR XMITSET RCV OUT HCPYPURG'           90288
         AIF   (NOT &LOCO).NOFET                                 90288
         ORG   QNAMES+16                                         90288
         DC    CL4'FET '     WYLBUR FETCH                        90288
         ORG   ,                                                 90288
.NOFET   ANOP  ,                                                 90288
JNUMASK  DC    X'D1D6402020202120'                               90274
         SPACE 1                                                 86251
LOOKDB   DS    D             WORK AREA                           86251
ADDRSVT  DC    A(0)          $SVT ADDRESS                        86251
LOOKQUE  DC    X'00'         RETURN - QUEUE NUMBER               86251
FUNCODE  DC    X'00'         FUNCTION REQUESTED                  86251
FUNGET   EQU   0               GET INFO ON A JOB                 86251
FUNINIT  EQU   4               INITIALIZE (GET CURRENT JOB)      86251
FUNMOD   EQU   8               MODIFY LOCATED JOB                90288
REQCODE  DC    X'00'         COMPLETION CODE                     86251
         PRINT &PRTMAC                                           90274
COPYJES  SERVJES OPT=LOJOB,DSECT=NO,PFX=CJ                       90274
COPYLEN  EQU   *-COPYJES                                         90274
         ORG   COPYJES                                           90288
COPYINFO SERVJES OPT=J2INF,DSECT=NO,PFX=CI                       90288
COPYINF  EQU   *-COPYINFO    LENGTH OF INFORMATION SECTION       90288
         ORG   ,                                                 90288
         PRINT &PRTSYS                                           90274
         SPACE 1                                                 86251
         LTORG ,                                                 86251
         CRMEND ,            LENGTH OF CSA WORK AREA             86251
         TITLE '@ S R V J 0 4 1  ***  DATA AND WORK AREA'
ACCT#DEF DC    C'0Z04001S'   DEFAULT ACCOUNT FOR START JOBS      81151
ACCT#SYS DC    C'0904'       SYSTEMS ACCOUNT                     81151
ACCT#FLP DC    C'0909'       SPECIAL BYPASS NUMBER               81151
ACCT#SUB DC    C'0010'       DEFAULT SUBACCOUNT FOR SHORT ACCT CHECK
SAWE     DSECT ,
DB       DS    2D            WORK SPACE
@SERVICE DS    A             ADDRESS OF @SERVICE ROUTINE
RETCH    DC    0XL8'0'       RETURN VALUES
RETR15   DC    0A(0),XL3'0'                                      81165
RC       DC    X'0'          RETURN CODE
RETR0    DC    0A(0),XL3'0'                                      81165
RV       DC    X'00'         RETURN FLAGS
RETR1    DC    A(0)          ZERO OR MODIFIED R1 RETURN          81165
CALLR0   DC    A(0)          CALLER'S R0 (COPIED)
CALLMODE EQU   CALLR0+2,1,C'X'  FUNCTION MODIFIER CODE
CALLR1   DC    A(0)          CALLER'S R1 (COPIED)
EXWJ2ASC DC    F'0'          JES2 ASCB                           85070
EXWJ2NAM DC    CL8'JES2    ' JES2 START NAME+CL4                 85070
EXWJ2AS# DS    A             SUBSYSTEM ASID                      90274
EXWJ2SVT DS    A             SUBSYSTEM $SVT                      90274
SAWEND   EQU   *
         PRINT &PRTSYS
         SPACE 1                                                 81137
CVT      DSECT ,             JUST IN CASE                        81137
         CVT   DSECT=YES                                         81137
         IHAASVT ,                                               85070
         IHAASCB ,
         IHASRB ,                                                85070
         IHASDWA ,                                               85070
         IEFJESCT ,                                              85070
         IEFJSCVT ,                                              85070
CSCB     DSECT ,                                                 85070
         IEECHAIN ,                                              85070
         SPACE 1                                                 81137
         IKJTCB ,                                                81137
         SPACE 1                                                 81151
         IHACDE ,                                                81151
         SPACE 1                                                 81145
IEFTIOT1 DSECT ,                                                 81145
         IEFTIOT1 ,                                              81145
         SPACE 1                                                 81137
         IEFTCT ,                                                81137
         SPACE 1                                                 81137
         IEFJMR ,
         SPACE 1                                                 85202
*        IEZIOB ,            NO-OP - DEFINED IN $BUFFER          85202
         SPACE 1                                                 81200
         IEFJFCBN ,                                              81200
         SPACE 2                                                 81359
         DCBD  DSORG=PS                                          81359
         SPACE 1                                                 81359
         IEZJSCB ,                                               83254
         SPACE 1                                                 83254
         AIF   (NOT &MVS).BIGEND                                 83100
         IHAPSA ,                                                83100
         SPACE 1                                                 83100
@SRVJ041 CSECT ,                                                 83254
         IEZDEB ,            FOR SP1.3.3/5 JES                   84316
         IFGRPL ,                                                90154
@SRVJ041 CSECT ,                                                 85014
.COMSJB  SPACE 1                                                 83254
         $SJB  ,                                                 83254
         $SVT  ,                                                 85070
.COMBUFF $BUFFER ,           HASP CAN'T DO ANYTHING RIGHT ?      83254
         $JCT  ,                                                 83254
         $HCT  ,                                                 90274
         $JQE  ,                                                 90274
         IEFJSSIB ,                                              83254
         SPACE 1                                                 83254
.BIGEND  END   ,                                                 84087
