SUBDSCB3 TITLE 'S U B D S C B 3  ***  SEEK FOR DSCB BY CCHHR'
***********************************************************************
**                                                                   **
**   SUBROUTINE SUBDSCB3 DOES A VTOC SEEK FOR THE REQUESTED CCHHR    **
**   ON THE VOLUME, AND RETURNS THE RESULT IN THE USER'S WORK AREA.  **
**                                                                   **
**                                   AUTHOR: G. POSTPISCHIL          **
***********************************************************************
**
*   R1 POINTS TO PARAMETER LIST
*   0     CL5 - CCHHR
*   4     CL6 - POINTER TO VOLSER
*   8     POINTER TO RETURN WORK AREA (140 BYTES)
*
*   RETURN CODES AS DOCUMENTED FOR OBTAIN SEEK
*
*
SUBDSCB3 START 0
         STM   R14,R12,12(R13)
         LR    R8,R15        SET NEW BASE
         USING SUBDSCB3,R8
         LA    R7,20+9*4(,R13)  POINT INTO USER'S SAVE AREA (R9-R12)
         USING USERSAVE,R7   MAKE THIS CODE RE-ENTRANT/REFRESHABLE
         USING PRMDSECT,R1   DECLARE THE PARM LIST
         LM    R4,R6,PRM@CHR  LOAD DSN AND WORK AREA ADDRESSES
         LA    R6,0(,R6)      KILL VL BIT
         USING WRKDSECT,R6
         MVC   DYNOBT(4),PATOBT   INITIALIZE OBTAIN AREA
         STM   R4,R6,DYNOBT+4   STASH CCHHR, VOLSER, WORK ADDRESSES
         OBTAIN DYNOBT       LOOK FOR IT
         LM    R1,R8,24(R13)  RELOAD SOME REGISTERS
         L     R14,12(,R13)  RELOAD RETURN ADDRESS
         BR    R14           RETURN
         SPACE 2
PATOBT   CAMLST SEEK,1-1,2-2,3-3
         LTORG ,
         SPACE 1
         PRINT NOGEN
         SPACE 1
USERSAVE DSECT ,             MY WORK AREA MAPPED ON USER'S SAVE AREA
DYNOBT   CAMLST SEEK,1-1,2-2,3-3
         SPACE 1
PRMDSECT DSECT ,             CALLER'S PARMS
PRM@CHR  DS    A             REQUESTED DATA SET NAME
PRM@VOL  DS    A             REQUESTED VOLUME SERIAL
PRM@WORK DS    A             ADDRESS OF WORK AREA FOR RETURN DATA
         SPACE 1
WRKDSECT DSECT ,             CALLER'S PARMS
WRKDSCB  DS    CL140         RETURN DATA
WRKSIZE  EQU   *-WRKDSECT
         SPACE 1
         YREGS ,
         END   ,
