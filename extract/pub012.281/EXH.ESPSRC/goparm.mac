GOPARM   TITLE 'RETRIEVE EXEC CARD PARM FIELD FOR FORTRAN AND COBOL'
         COPY  OPTIONGB
         SPACE 1
         SYSPARM LIST=YES
         SPACE 1
***********************************************************************
**                                                                   **
**  GOPARM IS A SUBROUTINE THAT RETRIEVES THE EXEC CARD PARM FIELD   **
**    (NO LONGER REQUIRED FOR COBOL VS AND COBOL II)                 **
**                                                                   **
**  PARM 1 MUST SPECIFY MAX. LENGTH OF OUTPUT FIELD PROCESSABLE      **
**  PARM 2 SPECIFIES OUTPUT FIELD PARM IS TO BE COPIED TO            **
**                                                                   **
**  INCORRECT CALL WILL RESULT IN A USER ABEND CODE OF 69            **
**                                                                   **
**  SAMPLE FORTRAN CALL:                                             **
**  INTEGER PARM1                                                    **
**  DIMENSION PARM2(5)                                               **
**  PARM1=20                                                         **
**  CALL GOPARM(PARM1,PARM2)                                         **
**  IF (PARM1.EQ.0)  GO TO ... NO INPUT                              **
**                                                                   **
***********************************************************************
**  AUTHOR:  DICK OXLEY, A.M.S. 1969                                 **
**                                                                   **
**  MAINTENANCE                                                      **
**  2003-05-26  GYP  MNEMONIC REGISTERS, OTHER CLEANUP. USE MVCL     **
**                                                                   **
**                                                                   **
***********************************************************************
         PRINT &PRTSOR
GOPARM   START 0
         USING *,R15                                            GP03146
         B     AROUND
         BCON  'GOPARM &SYSDATE'
AROUND   STM   R14,R12,12(R13)  SAVE REGISTERS
         TM    0(R1),X'80'   ONLY ONE PARAMETER ?
         BNZ   BOO           YES, ABEND CALLER
         LM    R2,R3,0(R1)   GET ADDRESS OF LENGTH, ADDRESS OF OUTPUT
         LA    R2,0(,R2)     TAKE OUT HIGH BYTE
         LTR   R2,R2         WAS THERE AN INPUT ADDRESS (COBOL) ?
         BZ    BOO           NO, ABEND
         L     R9,0(,R2)     GET MAXIMUM DESIRED LENGTH         GP03146
         LTR   R9,R9                                            GP03146
         BNP   BOO           ABEND IF NOT AT LEAST 1
         LA    R0,144        MAXIMUM WE WANT TO PROCESS         GP03146
         CR    R9,R0         MAX PROCESSABLE IS 144             GP03146
         BNH   *+6           OK, DOESN'T EXCEED                 GP03146
         LR    R9,R0         USE 144 AS MAXIMUM                 GP03146
         SPACE 1                                                GP03146
         TM    4(R1),X'80'   2 AND ONLY TWO PARAMETERS ?
         BZ    BOO           NO, COMPLAIN
         LA    R8,0(,R3)     TAKE OUT HIGH BYTE                 GP03146
         LTR   R8,R8         HAVE OUTPUT ADDRESS                GP03146
         BZ    BOO           NO, ABEND
         SPACE 1                                                GP03146
         LTCB  R5,USE=YES    GET MY TCB                         GP03146
         L     R5,TCBJSTCB   GET THE JOB STEP TCB               GP03146
         L     R5,TCBFSA     GET HIGH-LEVEL SAVE AREA           GP03146
         L     R5,20+1*4(,R5)  GET ORIGINAL R1                  GP03146
         DROP  R5                                               GP03146
         LA    R4,2(,R5)     GET ADDRESS OF PARM FIELD          GP03146
         LH    R5,0(,R5)     GET LENGTH OF PARM FIELD           GP03146
         MIN   R5,(R9)       LONGER THAN USER WILL ACCEPT ?     GP03146
         ST    R5,0(,R2)     GIVE LENGTH TO USER                GP03146
         ICM   R5,8,=C' '    USE BLANK PADDING                  GP03146
         MVCL  R8,R4         COPY TO USER WITH BLANK FILL       GP03146
         SPACE 1
EXIT     LM    R14,R12,12(R13) RELOAD THE REGISTERS
         SR    R15,R15
         BR    R14           RETURN TO CALLER
         SPACE 2
BOO      WTO   'PARM ROUTINE - CALL PARAMETERS INVALID',ROUTCDE=11
         ABEND 99,DUMP       ABEND THE CALLER
         SPACE 1
         PRINT &PRTSYS                                          GP03146
         IHAPSA ,                                               GP03146
         IKJTCB ,                                               GP03146
         YREGS  ,                                               GP03146
         END
