SETCODE  TITLE 'S E T C O D E  ***  SET RC OR ABEND CODE FROM PARM'
 PUNCH ' ORDER SETCODE(P) '   *****DEBUG*****
***********************************************************************
**                                                                   **
**   PROGRAM SETCODE IS USED TO SET A SPECIFIC PROGRAM COMPLETION OR **
**   ABEND CODE. IT IS USEFUL TO CONTROL JOB FLOW WITH COND= TESTS.  **
**                                                                   **
**   PARM=nnnn     NUMERIC 0 TO 4095   PRODUCES STEP COMPLETION CODE **
**                                                                   **
**   PARM=Unnnn    NUMERIC 0 TO 4095   PRODUCES USER ABEND nnnn      **
**                                                                   **
**   PARM=Sxxx     HEX 000 TO FFF      PRODUCES SYSTEM ABEND xxx     **
**                                                                   **
**   Omitted or invalid parameter results in return code 16.         **
**                                                                   **
***********************************************************************
         COPY  OPTIONGB
         SPACE 1
         SYSPARM LIST=YES
         EJECT ,
         PRINT &PRTSOR
SETCODE  PGMHEAD ZERO12,BASE=R12,PARM=R9,BNDRY=PAGE         NEW GP11158
         MVICC 16            PROVISIONAL BAD PARM ADDRESS
         SPACE 1
         SERVINIT ,
         SERVLOAD @PARSER
         SPACE 1
         PARMLOAD R8,PTR=R9  LOAD PARM ADDRESS/LENGTH INTO R8,R9
         LTR   R9,R9         ANY TEXT AT ALL?
         BNP   PGMEXIT         NO; QUIT WITH RC=16
         STM   R8,R9,DB      SAVE FOR RESTART
         SPACE 1
         PARSE (R8),(R9),OPT=COMMA  IGNORE BLANKS AND COMMAS
         BXH   R15,R15,PGMEXIT      NOTHING FOUND
         ICM   R3,15,PAR@TABL     ANY TEXT RETURNED?
         BZ    PGMEXIT              NO; FAIL
         USING PRSDSECT,R3        DECLARE RETURN AREA
         ICM   R2,15,PRS#TEXT     ANY TEXT AT ALL?
         BNP   PGMEXIT
         TM    PRSFLAGS,PRSFGINT  INTEGER FOUND ?
         BNZ   SETRC                YES; SET RETURN CODE
         OI    PRS$TEXT,C' '      MAKE UPPER CASE
         CLI   PRS$TEXT,C'U'      USER ABEND ?
         BE    UABEND               YES
         CLI   PRS$TEXT,C'S'      SYSTEM ABEND ?
         BE    SABEND               YES
         CLI   PRS$TEXT,C'X'      SYSTEM ABEND ?
         BE    SABEND               YES
         SPACE 1
*---------------------------------------------------------------------*
*   SYSTEM ABEND WITH CODE 0-FFF                                      *
*---------------------------------------------------------------------*
SABEND   CH    R2,=H'4'           GOOD LENGTH?
         BH    PGMEXIT              NO
         MVC   WORK(4),PRS$TEXT+1 EXTRACT USER CODE +
         OC    WORK,=CL4' '       UPPER CASE THE HEX DIGITS
         LA    R8,WORK
         LR    R9,R2              FULL LENGTH
         BCTR  R9,0               LESS 'S'
         PARSE (R8),(R9),OPT=COMMA  IGNORE BLANKS AND COMMAS
         BXH   R15,R15,PGMEXIT      NOTHING FOUND
         ICM   R3,15,PAR@TABL     ANY TEXT RETURNED?
         BZ    PGMEXIT              NO; FAIL
         USING PRSDSECT,R3        DECLARE RETURN AREA
         ICM   R2,15,PRS#TEXT     ANY TEXT AT ALL?
         BNP   PGMEXIT
         TM    PRSFLAGS,PRSFGHEX  HEX VALUE FOUND ?
         BZ    PGMEXIT              NO; FAIL
         ICM   R0,15,PRS#THEX     MORE THAN FOUR BYTES ?
         BNZ   PGMEXIT              YES; TOO BAD
         ICM   R15,15,PRSLINK     GET NEXT ENTRY (CLEAR ABEND SUBCODE)
         BNZ   PGMEXIT               TOO MANY; FAIL
         L     R1,PRS#THEX+4
         CH    R1,=H'4095'        VALID AMOUNT ?
         BH    PGMEXIT              NO
         SLL   R1,12              SHIFT TO SYSTEM FIELD
         SVC   13                 SYSTEM ABEND
         SPACE 1
*---------------------------------------------------------------------*
*   USER ABEND WITH CODE 0-4095                                       *
*---------------------------------------------------------------------*
UABEND   CH    R2,=H'5'           GOOD LENGTH?
         BH    PGMEXIT              NO
         MVC   WORK(4),PRS$TEXT+1 EXTRACT USER CODE +
         LA    R8,WORK
         LR    R9,R2              FULL LENGTH
         BCTR  R9,0               LESS 'U'
         PARSE (R8),(R9),OPT=COMMA  IGNORE BLANKS AND COMMAS
         BXH   R15,R15,PGMEXIT      NOTHING FOUND
         ICM   R3,15,PAR@TABL     ANY TEXT RETURNED?
         BZ    PGMEXIT              NO; FAIL
         USING PRSDSECT,R3        DECLARE RETURN AREA
         ICM   R2,15,PRS#TEXT     ANY TEXT AT ALL?
         BNP   PGMEXIT
         TM    PRSFLAGS,PRSFGINT  INTEGER FOUND ?
         BZ    PGMEXIT              NO; FAIL
         ICM   R0,15,PRS#TINT     MORE THAN FOUR BYTES ?
         BNZ   PGMEXIT              YES; TOO BAD
         ICM   R15,15,PRSLINK     GET NEXT ENTRY (CLEAR ABEND SUBCODE)
         BNZ   PGMEXIT               TOO MANY; FAIL
         L     R1,PRS#TINT+4
         CH    R1,=H'4095'        VALID AMOUNT ?
         BH    PGMEXIT              NO
         SVC   13                 USER ABEND
         SPACE 1
*---------------------------------------------------------------------*
*   SIMPLE STEP/PROGRAM COMPLETION CODE                               *
*---------------------------------------------------------------------*
SETRC    CH    R2,=H'4'           GOOD LENGTH?
         BH    PGMEXIT              NO
         ICM   R0,15,PRS#TINT     MORE THAN FOUR BYTES ?
         BNZ   PGMEXIT              YES; TOO BAD
         ICM   R15,15,PRSLINK     GET NEXT ENTRY
         BNZ   PGMEXIT              TOO MANY; FAIL
         L     R1,PRS#TINT+4
         CH    R1,=H'4095'        VALID AMOUNT ?
         BH    PGMEXIT              NO
         ST    R1,RETCODE         SET RETURN CODE AND RETURN
         SPACE 1
PGMEXIT  PARSE OPT=CLOSE     FREE GETMAIN
         SERVTERM ,
         PGMEXIT RC=RETCODE  RETURN WITH CONDITION
         SPACE 1
SAVE     DSECT ,
DB       DS    D
         SERVDEFS ,          EXPAND SERVICE ADDRESSES
WORK     DS    CL4           STORAGE FOR U/S OPERAND
         MAPPARSE DSECT=NO   PARSER WORK AREA
SAVEEND  EQU   *
         SPACE 1
         MAPPARST ,          DEFINE PARSE RESULTS
         END   ,
