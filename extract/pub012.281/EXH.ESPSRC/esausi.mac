FUSI     TITLE 'SMF STEP INITIATION EXIT'
         COPY  OPTIONGB                                          81348
         SPACE 1                                                 81348
         SYSPARM LIST=YES    SET AND LIST LOCAL OPTIONS          81348
         EJECT ,
         PRINT &PRTSOR                                           81348
IEFUSI   PGMHEAD BASE=R12,PARM=R9,AM=31,RM=31                   GP04042
         SPACE 1
         L     R6,0(,R9)     GET JMR ADDRESS                     81348
         L     R8,12(,R9)    LOAD ACCT                           81348
         USING JMR,R6
         USING ACTJNFLD,R8
         SPACE 1                                                 83128
SETOS    L     R5,LCTSCTAD-IEFALLCT(,R10)  GET SCT FROM LCT
COMMON   L     R7,CVTPTR     CVT PTR
         USING CVTMAP,R7                                         81348
         L     R9,CVTTCBP    TCB AREA POINTER
         L     R9,4(,R9)     CURRENT TCB
         USING TCB,R9                                            81348
         ICM   R10,15,TCBUSER   USERTCB EXTENSION PRESENT ?      83128
         BZ    EXIT          NO; HOW DID WE GET HERE ?           83128
         USING USERTCB,R10                                       83128
         XC    UTSB(UTSL),UTSB   CLEAR STEP WORK AREA
         USING IEFASCTB,R5
NOUSE    TM    UTF1,TUFUJI   DID UJI SET ANY FLAGS ?             83128
         BO    NORESET
         TM    UTF1,TUFPASS  DID PREVIOUS STEP SET PASS ?        83128
         BNO   NORESET       NO
         NI    UTF1,255-TUFPASS  IF SO, TURN IT OFF              83128
NORESET  CLI   ACTJNFLD,1    AT LEAST ONE SUB-FIELD ?
         BL    SPECIAL       NO, SKIP IT
         LA    R1,ACTJNFLD+2  POINT TO ACCOUNT FIELD
         TM    UTF1,TUFZERO  SPECIFIED KEY ZERO ?                83128
         BO    SETKEY
         CLI   ACTJNFLD+1,4  4 CHAR. STEP ACCT. FIELD 1?
         BL    SPECIAL       NO, CHECK FOR SPECIAL PROGRAMS
         TIME  BIN
         SLL   R1,2
         XR    R2,R2
         XR    R3,R3
         LA    R4,8
LOOP     XR    R0,R0
         SLDL  R0,4
         AR    R3,R0
         BCT   R4,LOOP
         D     R2,=F'11'
         STC   R2,1(,R13)
         CLI   1(R13),9
         BH    LOOP1
         OI    1(R13),X'F0'
         MVI   0(R13),C'F'
         B     LOOP2
LOOP1    MVC   0(2,R13),=C'C1'
LOOP2    CLC   ACTJNFLD+2(2),0(R13)       1ST PART = ?
         BE    SETTCB        YES
         CLC   ACTJNFLD+4(2),0(R13)       2ND PART = ?
         BNE   INIT          NO
SETKEY   OI    SCTSSTAT,SCTKEY0   SET FOR KEY ZERO
         B     INIT
SETTCB   OI    UTF1,TUFPASS  SET FOR PASSWORD OVERRIDE           83128
         SPACE 1                                                 79148
INIT     CLI   JMRUCOM,0     DID MIKE FORGET TO FILL IT IN ?     83128
         BNE   *+10          NO; SHOULD BE OK ?                  83128
         MVC   JMRUCOM(1),JMRCLASS  USE CLASS                    83128
         AIF   ('&LOCAL' NE 'PID').NOLO1                        GP04042
         CLI   JMRUCOM,X'D0' START JOB ?                         83128
         BE    EXIT          YES; NO CHECKING                    83128
         ICM   R7,15,CVTUSER   GET USER EXTENSION                83128
         USING USERCVT,R7                                        83128
         ICM   R7,15,UCLVOLT   GET RESIDENT VOLUME TABLE         83128
         BNZ   HAVEVOLT                                          83128
TESTEXEC LOOKLPA EP=IGPVOLT  SEE IF VOLUME TABLE IS LOADED       81348
         BZ    EXIT          NO; BYPASS CHECKS                   81348
         USING USERVOLT,R7                                       81348
         LR    R7,R0         LOAD IT                             81348
HAVEVOLT CLI   JMRUSEID,C'0'   PRIVILEGED ACCOUNT ?              83128
         BNE   ANOTPRIV      NO                                  81348
         CLI   JMRUSEID+2,C'0'  0X0X ?                           81348
         BNE   ANOTPRIV      NO                                  81348
         CLI   JMRUSEID+1,C'9'  090X ?                           81348
         BE    APOSPRIV                                          81348
         CLI   JMRUSEID+1,C'Z'  SPECIAL START JOB NUMBER ?       81348
         BNE   ANOTPRIV                                          81348
APOSPRIV CLI   JMRUSEID+3,C'4'                                   81348
         BL    ANOTPRIV                                          81348
         BE    EXIT          SYSTEMS SYSTEMS                     81348
         CLI   JMRUSEID+3,C'9'                                   81348
         BL    PROGLOOK      SEMI-PRIVILEGED                     81348
         SPACE 1                                                 81348
ANOTPRIV L     R14,VTPGMRAC  GET TABLE OF RESTRICTED PROGRAMS    81348
ANOTPRIL CLI   0(R14),C'-'   VALID ENTRY ?                       81348
         BNH   PROGLOOK      NO; LOOK FOR PROGRAM NAMES          81348
         CLC   0(8,R14),SCTPGMNM  PRIVILEGED PROGRAM ?           81348
         BE    SOMEFAIL      YES; TOO BAD                        81348
         LA    R14,12(,R14)  TRY NEXT ENTRY                      81348
         B     ANOTPRIL                                          81348
         SPACE 1                                                 81348
PROGLOOK L     R14,VTPGMCLS  PROGRAMS RESTRICTED TO SPEC. CLASSES
PROGLOOP CLI   0(R14),C'-'   END OF LIST ?                       81348
         BNH   CLASLOOK      YES; LOOK FOR SPECIAL CLASSES       81348
         CLC   0(8,R14),SCTPGMNM  PRIVILEGED PROGRAM ?           81348
         BNE   PROGLUMP      NO                                  81348
         LA    R15,8(,R14)   POINT TO FIRST CLASS ENTRY          81348
         LA    R0,4          CHECK FOUR CLASSES                  81348
PROGCLOP CLC   JMRUCOM(1),0(R15)  MATCHING CLASS ?               81348
         BE    EXIT          YES; NO FURTHER CHECKS              81348
         LA    R15,1(,R15)   TRY NEXT CLASS ENTRY                81348
         BCT   R0,PROGCLOP   CHECK IT                            81348
         B     SOMEFAIL      CLASS NOT FOUND - FAIL IT           81348
PROGLUMP LA    R14,12(,R14)  TRY NEXT ENTRY                      81348
         B     PROGLOOP                                          81348
         SPACE 1                                                 81348
CLASLOOK L     R14,VTCLSPGM  GET CLASSES RESTRICTED TO SPEC. PROGRAMS
CLASLOOP CLI   0(R14),C'-'   END OF LIST ?                       81348
         BNH   EXIT          YES; NO CLASS RESTRICTION           81348
         LA    R15,8(,R14)   POINT TO CLASS LIST                 81348
         LA    R0,4          NUMBER OF ENTRIES                   81348
CLASCLOP CLC   JMRUCOM(1),0(R15)  MATCHING CLASS ?               81348
         BE    CLASCLAK      YES; GO TO PROGRAM NAME CHECK       81348
         LA    R15,1(,R15)                                       81348
         BCT   R0,CLASCLOP   TRY NEXT CLASS ENTRY                81348
         LA    R14,12(,R14)  TRY NEXT LIST ENTRY                 81348
         B     CLASLOOP                                          81348
         SPACE 1                                                 81348
*        A CLASS ENTRY HAS BEEN FOUND; CHECK REMAINDER OF TABLE  81348
*        FOR MATCHING CLASS/PROGRAM ENTRY.  FAIL IF NOT FOUND    81348
CLASCLAK CLC   0(8,R14),SCTPGMNM  MATCHING PROGRAM NAME ?        81348
         BE    EXIT          YES; GOOD                           81348
CLASLUMP LA    R14,12(,R14)  GO TO NEXT LIST ENTRY               81348
         CLI   0(R14),C'-'   END OF LIST ?                       81348
         BNH   SOMEFAIL      YES; FAIL IT                        81348
         LA    R15,8(,R14)   POINT TO CLASS ENTRIES              81348
         LA    R0,4          AND NUMBER                          81348
CLASCLAT CLC   JMRUCOM(1),0(R15)  MATCHING CLASS ?               81348
         BE    CLASCLAK      YES; COMPARE PROGRAM NAME           81348
         LA    R15,1(,R15)                                       81348
         BCT   R0,CLASCLAT   TRY NEXT CLASS                      81348
         B     CLASLUMP      ELSE TRY NEXT ENTRY                 81348
         SPACE 1                                                 81348
SOMEFAIL LA   R9,4           SET ERROR RETURN CODE               81348
         B     EXITCOM       RETURN WITH ERROR                   81348
.NOLO1   SPACE 1                                                 81348
EXIT     XR    R9,R9         CLEAR RETURN CODE                   81348
EXITCOM  PGMEXIT RC=(R9)     RETURN WITH APPROPRIATE CODE       GP04042
         SPACE 2
SPECIAL  CLC   =C'JOB',SCTPGMNM   SPECIAL PROGRAM ?
         BNE   INIT          NO
         CLI   SCTPGMNM+3,C'0'    SPECIFIC KEY 0 REQUEST ?
         BE    SETKEY        YES; DO IT
         CLC   =C'CANCL',SCTPGMNM+3  IS IT SPECIAL PROGRAM ?
         BE    SETKEY
         CLC   =C'RELSE',SCTPGMNM+3  DITTO ?
         BE    SETKEY
         B     INIT
         EJECT ,
SAVE     DSECT ,             DYNAMIC SAVE AND WORK AREA          81348
SAVEEND  EQU   *             VERY SHORT                          81348
         SPACE 2                                                 81348
IEFALLCT DSECT ,
         IEFALLCT ,
         SPACE 2
ACTJNFLD DSECT ,             JCT LOOKS BAD WITH HASP BLOCKS
         DS    X
         EJECT
IEFASCTB DSECT ,
         IEFASCTB ,
         EJECT ,
         IEFJMR ,
         SPACE 2
         PRINT &PRTSYS                                           81348
CVTDSECT DSECT ,
         CVT   DSECT=YES
         SPACE 1                                                 81348
         IHACDE ,                                                81348
         SPACE 1                                                 81348
         IKJTCB ,                                                81348
         SPACE 1                                                 83128
         PRINT &PRTMAC                                           81348
USERTCB  USERTCB ,                                              GP04042
         AIF   ('&LOCAL' NE 'PID').BIGEND                       GP04042
         SPACE 2                                                 81348
IGPVOLT  USERVOLT ,          EXPAND VOLUME TABLE                 81348
         SPACE 2                                                 83128
USERCVT  USERCVT ,           (ALMOST) RESIDENT VECTOR TABLE      83128
.BIGEND  SPACE 1                                                 83218
         END
