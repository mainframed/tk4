SPFC     TITLE 'S P F C O P Y  ***  TSO/SPF IEBCOPY INTERFACE'
         COPY  OPTIONGB
         SPACE 2
         SYSPARM LIST=YES
         SPACE 1
***********************************************************************
*                                                                     *
*        COPYRIGHT 1986  EXPERT SYSTEM PROGRAMMING, INC.              *
*        COPYRIGHT 2003  EXPERT SYSTEM PROGRAMMING                    *
*                        176 OLD STAGE COACH ROAD                     *
*                        BRADFORD, VT 05033-8844                      *
*                                                                     *
*                    ALL RIGHTS RESERVED                              *
*                                                                     *
***********************************************************************
         EJECT ,
*        IEBCOPY INTERFACE FROM ISPF - SEE ISPF INSTALLATION MANUAL
*        FOR ORIGINAL CODE. THIS VERSION LOOSELY BASED ON IBM'S.
*
         PRINT &PRTSOR
SPFCOPY  PGMHEAD ZERO,BASE=R12,PARM=R2                           86033
         SERVINIT ,          INITIALIZE SERVICES
         STAX  DEFER=YES     KEEP USER FROM INTERRUPTING
         LA    R3,40         SET PROVISIONAL ERROR CODE
         BXH   R15,R15,RETURN  RETURN WITH ERROR
         ESTAE EXITSTAE      RECOVER
         LA    R3,44         SET PROVISIONAL ERROR
         BXH   R15,R15,STAXOFF  QUIT IF ERROR
         SERVICE APFON       SET US AUTHORIZED
         SERVICE CANOF       SET US NON-CANCELLABLE
         LR    R1,R2         GET USER'S PARAMETER REGISTER BACK
         LINK  EP=IEBCOPY    INVOKE IBM'S COPY
         LR    R3,R15        PRESERVE RETURN CODE
         SERVICE CANON       MAKE CANCELLABLE AGAIN
         SERVICE APFOF       SET UNAUTHORIZED
STAXOFF  STAX  DEFER=NO      LET USER IN
RETURN   SERVTERM ,          TERMINATE SERVICES
         PGMEXIT RC=(R3)     RETURN WITH CC
         SPACE 2
         USING EXITSTAE,R12
EXITSTAE LR    R12,R15       SAVE BASE
         LR    R9,R14        PRESERVE RETURN
         SERVICE CANON       MAKE CANCELLABLE
         SERVICE APFOF       TURN APF OFF
         STAX  DEFER=NO
         BR    R9            RETURN TO CALLER
         SPACE 2
SAVE     DSECT ,
         SERVDEFS ,
SAVEEND  EQU   *
         SPACE 2
         PRINT &PRTSYS
CVTDSECT DSECT ,
         CVT   DSECT=YES
         IHACDE ,
         END   ,
