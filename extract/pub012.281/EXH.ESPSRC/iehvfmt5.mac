FMT5     TITLE 'DSCB5'
         PRINT NOGEN
         LCLC  &PP
         LCLA  &P
 ACTR 200
DSCB5    CSECT
         SAVE  (14,12),T,*         SAVE REGS
         LR    12,15               SET BASE REG
         USING DSCB5,12            TELL ASSEMBLER
         LA    9,DSCB5OUT          OUTPUT AREA BASE
         USING DSCB5OUT,9
         LR    11,13               FREE 13
         LA    13,D5SAVE           SET 13 TO SAVE
         ST    11,D5SAVE+4         SAVE PTR TO LAST
         ST    13,8(11)            CHAIN IT TO THIS
         LM    10,11,0(1)          GET PARM PTRS
         USING DS5ADDR,10
         USING FMT5,11
         MVC   DSCB5FMT,DS5FMTID   USE DSCB FMT ID AS IS
         CALL  PUTADDR,(CVHTAB5)
         L     6,=V(VTOCCOMM)  LOAD COMMON ADDRESS
         USING VTOCCOMM,6
         TM    DS5FLG,X'81'  INDEXED VTOC ?                      90248
         BNO   NOTINDX       NO                                  90248
         L     15,16         GET CVT POINTER                     90248
         ICM   15,15,328(15)  VS1/VS2 COMMON EXTENSION           90248
         BZ    SETBAD        NONE ?                              90248
         ICM   15,15,12(15)  CVAF TABLE ?                        90248
         BNZ   HAVE0         YES; @OBTAINS WILL FAKE FMT5S       90248
NOTINDX  TM    DS5FLG,X'9C'  ANY ERRORS IN VTOC ?                90248
         BZ    HAVE0         NO
SETBAD   MVC   DSCBAD(19),=C'CONTENTS UNRELIABLE'                90248
         B     HAVE0+6
HAVE0    MVC   DSCBAD(19),DSCBAD-1   BLANK ERROR PORTION
         SPACE 2
*        AVOID AN ABEND ????? CHECK FOR NO AVAILABLE ENTRIES
*
         CLC   DS5EXT01+2(3),=XL3'0'   ANY SPACE AT ALL ?
         BNZ   HAVE1         YES
         LA    4,DSCB5NOR    USE SHORT LENGTH
         MVC   DSCBNO(18),=C'NO SPACE AVAILABLE'  MAKE MSG
         B     VARLEN        PUT OUT SHORT RECORD
HAVE1    MVC   DSCBNO(18),DSCBNO-1    BLANK OUT 'NO SPACE' FIELD
         CALL  CVD,(CVDTAB5)       DO DEC CONVERSIONS
         LA    4,DSCB5SIZ    FOR FULL DSCB LIST
         CLC   DS5EXT14+2(3),=XL3'0'  IS SECOND SET ZERO ?
         BNE   VARLEN        NO, PRINT FULL
         LA    4,DSCB5SHT    YES, PRINT SHORT LIST
VARLEN   CALL  PRINT,(DSCB5OUT,(4))
         SPACE 2
         MVC   FMT5+44(90),FMT5+45   MAKE DSCB ENTRIES CONTIGUOUS
         LA    1,26          NO. OF ENTRIES
         LA    2,FMT5+4      GET FIRST ENTRY ADDRESS
         SPACE
AMOUNT   CLC   2(3,2),=XL3'0'   ZERO ENTRY
         BE    ALLDONE       YES, FINISHED
         MVC   D5SAVE(4),0(2)  MOCE TO BOUNDARY
         SR    3,3
         IC    3,4(2)        NO. OF TRACKS
         LH    5,D5SAVE+2    NO. OF CYLINDERS
         MH    5,CYLTRK      CONVERT TO TRACKS
         AR    5,3           TOTAL TRACKS IN FREE EXTENT
         LR    3,5           STASH BACK
         A     5,VOLFREE     ADD FREE SPACE
         ST    5,VOLFREE     STORE BACK
         CLC   0(2,2),=XL3'0'  TRACK ZERO ?????????
         BNE   *+8           NO
         MVI   VOLABEL+1,0   SET 'ACCOUNTED' FOR FLAG
         CLC   0(2,2),DSCB5NOR  =H'1' ?
         BH    AMINC         NO
         AH    3,D5SAVE      END TRACK + 1
         BCTR  3,0           END TRACK
         STH   3,D5SAVE
         CLC   0(2,2),D5SAVE   EXTENT INCLUDES TRACK 1 ?
         BL    AMINC         NO
         MVI   VOLIPL+1,0    SET 'ACCOUNTED' FOR FLAG
AMINC    LA    2,5(2)        BUMP
         BCT   1,AMOUNT      DO NEXT FREE EXTENT
ALLDONE  L     13,D5SAVE+4         RESTORE 13
         RETURN (14,12),T          RETURN
D5SAVE   DS    18F
DSCB5NOR DC    H'1,3'        COUNTS FOR 'NO SPACE' AVAILABLE
DSCB5SIZ DC    H'7',H'11'
DSCB5SHT DC    H'4,7'
CVHTAB5  DC    S(DS5ADDR),H'5',S(DSCB5ADR),H'12'
         DC    S(DS5PTRDS),H'5',S(DSCB5EXT),H'12'
         DC    H'0'
&P       SETA  101
CVDTAB5  DS    0H
.CVDL5   ANOP
&PP      SETC  '&P'(2,2)
         DC    S(DS5EXT&PP),H'2',S(RTK5&PP),H'5'
         DC    S(DS5EXT&PP+2),H'2',S(CYL5&PP),H'5'
         DC    S(DS5EXT&PP+4),H'1',S(TRK5&PP),H'3'
&P       SETA  &P+1
         AIF   (&P LT 127).CVDL5
         DC    H'0'
DSCB5OUT DC    0F'0',C'-'
DSCB5ADR DC    CL12' ',C'  FMT='
DSCB5FMT DC    C' ',CL120'       (DASD AVAILABLE EXTENTS)'
DSCBAD   EQU   DSCB5FMT+37
DSCBNO   EQU   DSCB5FMT+60   'NO SPACE AVAILABLE' MESSAGE SLOT
         ORG   DSCB5OUT+X'67'
         DC    C'DSCB EXTENDED AT '
DSCB5EXT DC    CL12' '
         ORG   DSCB5OUT+132
DSCB5LN2 DC    C'0     AVAILABLE CYLS - '
&P       SETA  101
.E5A     ANOP
&PP      SETC  '&P'(2,2)
CYL5&PP  DC    CL5' ',C'   '
&P       SETA  &P+1
         AIF   (&P LE 113).E5A
         DC    CL15' '
         ORG   DSCB5LN2+132
DSCB5LN3 DC    C'      AVAILABLE TRKS - ',C'  '
&P       SETA  101
.E5B     ANOP
&PP      SETC  '&P'(2,2)
TRK5&PP  DC    CL3' ',CL5' '
&P       SETA  &P+1
         AIF   (&P LE 113).E5B
         DC    CL15' '
         ORG   DSCB5LN3+132
DSCB5LN4 DC    C'      AT ABS TRACK   - '
&P       SETA  101
.E5C     ANOP
&PP      SETC  '&P'(2,2)
RTK5&PP  DC    CL5' ',C'   '
&P       SETA  &P+1
         AIF   (&P LE 113).E5C
         DC    CL15' '
         ORG   DSCB5LN4+132
DSCB5LN5 DC    C'0     AVAILABLE CYLS - '
&P       SETA  114
.E5D     ANOP
&PP      SETC  '&P'(2,2)
CYL5&PP  DC    CL5' ',C'   '
&P       SETA  &P+1
         AIF   (&P LE 126).E5D
         DC    CL15' '
         ORG   DSCB5LN5+132
DSCB5LN6 DC    C'      AVAILABLE TRKS - ',C'  '
&P       SETA  114
.E5E     ANOP
&PP      SETC  '&P'(2,2)
TRK5&PP  DC    CL3' ',CL5' '
&P       SETA  &P+1
         AIF   (&P LE 126).E5E
         DC    CL15' '
         ORG   DSCB5LN6+132
DSCB5LN7 DC    C'      AT ABS TRACK   - '
&P       SETA  114
.E5F     ANOP
&PP      SETC  '&P'(2,2)
RTK5&PP  DC    CL5' ',C'   '
&P       SETA  &P+1
         AIF   (&P LE 126).E5F
         DC    CL15' '
FMT5     DSECT
DS5KEYID DC    XL4'05'
&P       SETA  101
.D1      ANOP
&PP      SETC  '&P'(2,2)
DS5EXT&PP  DS  CL5
&P       SETA  &P+1
         AIF   (&P EQ 127).DEXIT
         AIF   (&P NE 109).D1
DS5FMTID DS    C
         AGO   .D1
.DEXIT   ANOP
DS5PTRDS DS    CL5
         SPACE 2
VTOCCOMM DSECT
VOLFREE  DC    F'0'          FREE TRACKS ON VOLUME
VOLABEL  DC    H'0'          LABEL TRACK FLAG
VOLIPL   DC    H'0'          IPL TRACK FLAG
CYLTRK   DC    H'0'          TRACKS/CYLINDER
DS5FLG   DC    X'0'          FMT4 CONTAMINATED INDICATOR
DS5ADDR  DSECT
         END
