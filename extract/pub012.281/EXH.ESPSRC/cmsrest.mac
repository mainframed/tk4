CMSR     TITLE 'C M S R E S T  ***  RESTORE (SOME) CMS DUMP TAPES'
         COPY  OPTIONGB
         SPACE 1
         SYSPARM LIST=YES
         SPACE 1
***********************************************************************
*                                                                     *
*        COPYRIGHT 2010       EXPERT SYSTEM PROGRAMMING               *
*                        176 OLD STAGE COACH ROAD                     *
*                        BRADFORD, VT 05033-8844                      *
*                                                                     *
*                    ALL RIGHTS RESERVED                              *
*                                                                     *
***********************************************************************
         SPACE 2
*   THIS PROGRAM IS INTENDED FOR RELOADING CMS DUMP TAPES WITH        *
*   FIXED FORMAT.  THE AUTHOR HAS NEVER SEEN THE RELEVANT CMS         *
*   DOCUMENTATION, AND MAKES NO WARRANTY THAT THE PROGRAM WILL        *
*   DO ANYTHING USEFUL, AND NOT DO ANY HARM.                          *
*                                                                     *
*   REQUIRED DD CARDS:                                                *
*                                                                     *
*    INPUT - INPUT FILE(S), RECFM FB,BLKSIZE<=32720                   *
*                                                                     *
*    OUTPUT - SEQUENTIAL FILE WITH ./ ADD CARDS                       *
*           TO THE CMS NAME, MINUS THE LAST LEVEL, AND THE UNIT       *
*           AND VOLUME ARE USED FOR NEW ALLOCATIONS.                  *
*                                                                     *
***********************************************************************
*                                                                     *
*   THE PROGRAM READS THROUGH THE INPUT FILE(S), BUILDS ./ ADD CARDS  *
*   AS APPROPRIATE.                                                   *
*                                                                     *
***********************************************************************
         SPACE 2
         PRINT &PRTSOR
CMSREST  PGMHEAD ZERO31,BASE=(R11,R12),PARM=R9,BNDRY=PAGE
         SPACE 1
         SERVINIT ,          LOAD THE SERVICE ROUTINE
         MVI   BLANKS,C' '   INITIALIZE
         MVC   BLANKS+1(L'BLANKS-1),BLANKS  COMPLETELY
         MVC   PREVNAME,BLANKS
         SERVLOAD @PRINTER,@INPREAD  LOAD SERVICE ROUTINES
         PRTOPEN SYSPRINT    OPEN THE PRINT FILE
         PRTV  HEADER,TITLE=1  MAKE A TITLE
         PRTOPEN SYSPUNCH,DEV=2
         SPACE 1
         INPOPEN SYSIN
         CH    R15,=H'8'     OPEN ?
         BL    GETLOOP       GO TO PROCESS INPUT
         PRTL  '*** UNABLE TO OPEN INPUT FILE ***',CC=NO
         B     EXIT16
         SPACE 1
GETLOOP  INPGET ,
         CH    R15,=H'4'
         BE    EODAD
         BH    PROCERR
         LR    R10,R1
         USING INPDSECT,R10  DECLARE INPUT RECORD
         CLC   CMSFILE(4),INPID    CORRECT RECORD?
         BNE   PROCERR         NO; REJECT
         CLC   CMSFILE,INPID    CORRECT RECORD?
         BNE   GETLOOP         NO; IGNORE MSN, ETC.
         CLC   =C'CNTRL ',INPTYPE
         BE    GETLOOP       SKIP UNWANTED STUFF
         CLC   =C'MAP ',INPTYPE
         BE    GETLOOP       SKIP UNWANTED STUFF
         CLC   =C'EXEC ',INPTYPE
         BE    GETLOOP       SKIP UNWANTED STUFF
         CLC   =C'TEXT ',INPTYPE
         BE    GETLOOP       SKIP UNWANTED STUFF
         CLC   =C'AUXSF ',INPTYPE
         BE    GETLOOP       SKIP UNWANTED STUFF
         CLC   =C'MODULE ',INPTYPE
         BE    GETLOOP       SKIP UNWANTED STUFF
         CLI   INPTYPE+1,C'0'  PATCHES?
         BNL   GETLOOP       SKIP UNWANTED STUFF
         CLC   INPNAME,PREVNAME   SAME MEMBER/FILE NAME ?
         BE    NOBREAK         NO
         ICM   R0,15,NUMFULL GET SIZE FILLED
         BZ    NUBREAK         EXPECTED
         OC    OUTREC(10),OUTREC  FILLER ?
         BZ    NUBREAK
         PRTDATA 'Record not terminated:',OUTREC
         OICC  8
*NEXT    B     NUBREAK
         SPACE 1
NUBREAK  MVC   PREVNAME,INPNAME   REMEMBER THE NAME
         XC    NUMFULL,NUMFULL    SIZE USED IN OUTPUT
         MVC   OUTREC,BLANKS CLEAR OUTPUT
         PRTDATA 'New file:',INPNAME,(INPTYPE,PAD)
         PRTDATA './       ADD   NAME=',INPNAME,DEV=2
         SPACE 1
NOBREAK  L     R2,NUMFULL    GET CURRENT USED COUNT
         LA    R1,OUTREC(R2) GET NEW POSITION
         LA    R3,L'INPTEXT(R2)  NEW LENGTH
         MVC   0(L'INPTEXT,R1),INPTEXT MOVE TEXTICLE
TESTMORE CH    R3,=H'80'
         BL    SAVESIZE
         PRTF  OUTREC,80,CC=NO,DEV=2    PUNCH ONE CARD IMAGE
         MVC   OUTREC,OUTREC+80   SLIDE REMAINDER AND BLANKS
         SH    R3,=H'80'     NEW SIZE
         BP    TESTMORE
         MVC   OUTREC,BLANKS ENSURE CLARITY
SAVESIZE ST    R3,NUMFULL
         B     GETLOOP       LOOP
         SPACE 1
PROCERR  PRTLIST PROCSNAP    DISPLAY THE BAD RECORD
         INC   NUMBAD,WORK=R1  COUNT THE BAD RECORDS
         CH    R1,=H'5'      MORE THAN FIVE ?
         BNH   GETLOOP       NO; KEEP GOING
         SERVTERM ,
         ABEND 001,DUMP      QUIT NASTILY
         SPACE 2
EODAD    ICM   R0,15,NUMFULL ANYTHING LEFT ?
         BZ    ENDOK
EODAT    PRTL  '0*** PREMATURE END - EXPECTING DATA ***'
         SPACE 1             CATALOG, ETC.
EXIT16   MVICC 16
         B     ENDOK
ENDOK    DS    0H
         SERVTERM ,          CLOSE AND FREE REST
         L     R9,RETCODE    GET RETURN CODE                    GP12063
         PGMEXIT RC=(R9)     RETURN TO SYSTEM                   GP12063
         SPACE 1
         LTORG ,
         SPACE 1
SYSPRINT PRTWORK SYSPRINT,SYSTERM,TITLE=5
SYSPUNCH PUNWORK SYSPUNCH,OUTPUT
SYSIN    INPWORK SYSIN,INPUT,EDIT=128,EODAD=EODAD
         SPACE 1
HEADER   VCON  '#C M S R E S T'
HEADP1   VCON  '     Entry Name   / Type',END=HEADP1ND
         DC    C'     Command  Tp Format'
         DC    C'   Records     Date   Time'
HEADP1ND VCON  *END
HEADP2   VCON  '     Entry  PDS    ',END=HEADP2ND
         DC    C'    Format'
         DC    C'      Members   Records'
HEADP2ND VCON  *END
HEADP3   VCON  '                Processing messages'
         SPACE 1
PROCSNAP FDOPT CC=C'0'
         FDOPT '*** INVALID CMS RECORD READ ***',NL
         FDSNAP INBUF,64,DUAL,NOABS,OFFSET,BASE=INBUF
         FDPRT *END
CMSFILE  DC    0CL5' '               CMS COMMON FILE HEADER      (5)
CMSHEAD  DC    0CL4' ',X'02',C'CMS',C'F'  CMS COMMON BLOCK HEADER(4)
         SPACE 1
TRNAME   DC    256C'$'       DSN/MEMBER CONVERSION TABLE
         TRENT TRNAME,C' ',C' ',0      ZERO OR BLANK IS BLANK
         TRENT ,C'.',C'.',C'@',C'@',C'#',C'#'  NATIONALS
         TRENT ,(*-TRNAME+C' '),(X'81',9)      a-i
         TRENT ,(*-TRNAME+C' '),(X'91',9)      j-r
         TRENT ,(*-TRNAME+C' '),(X'A2',8)      s-z
         TRENT ,(*-TRNAME),(C'A',9)            A-I
         TRENT ,(*-TRNAME),(C'J',9)            J-R
         TRENT ,(*-TRNAME),(C'S',8)            S-Z
         TRENT ,(*-TRNAME),(C'0',10)           0-9
         SPACE 1
SAVE     DSECT ,             SAVE AND WORK AREA
DB       DS    D
ZEROES   DS    D             CONSTANT
         SERVDEFS ,          SUPPORT VALUES                     GP12063
NUMFULL  DS    F             BYTES IN BUFFER
NUMBAD   DS    F             NUMBER OF BAD RECORDS
NUMOUT   DS    F             NUMBER OF GOOD RECORDS
PREVNAME DS    CL8           CURRENT FILE NAME
OUTREC   DS    CL160   1/2   TWO CARDS WORTH
BLANKS   DS    CL160   2/2   CONSTANT BLANKS
FLAGS    DS    X             PROCESSING FLAGS
FGCONT   EQU   X'20'           RECORD SPLIT BETWEEN BLOCKS
FGDEBUG  EQU   X'01'           PRINT DEBUG INFO IF ON
SAVEEND  EQU   *
         SPACE 1
INPDSECT DSECT ,             INPUT
INBUF    DS    0X            INPUT BYTE
INPID    DS    XL5           X'02','CMS','F'
INPTEXT  DS    CL50          TEXT
         DS    XL2           X'0001'
INPNAME  DS    CL8           CMS ENTITY NAME
INPTYPE  DS    CL8           CMS FILE TYPE EXTENSION
INPQUAL  DS    CL2           A1
INPSEQ   DS    CL5           SEQUENCE NUMBER
         SPACE 1
         END   ,
