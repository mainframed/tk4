PDSGAS  TITLE 'P D S G A S  ***  DISCOVER AND RECOVER REPLACED MEMBERS'
***********************************************************************
****     FROM FILE 316 OF THE CONNECTICUT BANK & TRUST MODS TAPE ****
*                                                                     *
*         WRITTEN BY. BILL GODFREY, PRC COMPUTER CENTER INC.          *
*                                   MCLEAN, VIRGINIA                  *
*                                                                     *
*          MOST OF THE CODE IS TAKEN FROM THE 'STATS' PROGRAM         *
*          BY A. BRUCE LELAND, FOUND ON THE CBT MODS TAPE.            *
*                                                                     *
*         DATE WRITTEN: MARCH 11 1981.  VERSION 1.0                   *
*                                                                     *
*         DATE UPDATED: NOVEMBER 27, 2009 - MISC. FIXES   G.P.        *
*                                                                     *
*         DESCRIPTION:  THIS PROGRAM READS A PDS AND FINDS "GAS"      *
*          MEMBERS, I.E. MEMBERS THAT HAVE BEEN DELETED BUT HAVE      *
*          NOT BEEN COMPRESSED OUT YET.                               *
*                                                                     *
*          DDNAMES REQUIRED:                                          *
*            SYSLIB    -  INPUT PDS                                   *
*            SYSPRINT  -  OUTPUT MESSAGES                             *
*                                                                     *
*          IF PARM=ADD IS SPECIFIED, THE PROGRAM WILL ADD A MEMBER    *
*          NAME TO THE DIRECTORY FOR EACH "GAS" MEMBER.               *
*                                                                     *
*          THE ADDED MEMBER NAMES ALL BEGIN WITH '$GAS', FOLLOWED     *
*          BY A 4 DIGIT SEQUENCE NUMBER.                              *
*                                                                     *
*          IF PARM=TTR(TT.TT.RR) IS SPECIFIED, THE PROGRAM WILL ADD   *
*          A MEMBER NAME TO THE DIRECTORY FOR THE "GAS" MEMBER        *
*          HAVING THE SPECIFIED TTR LOCATION (IN HEX, OBTAINED FROM   *
*          A PREVIOUS RUN). THIS IS USEFUL IF PARM=ADD CAUSES THE     *
*          DIRECTORY TO FILL UP, OR IF YOU KNOW WHICH ONE YOU WANT.   *
*                                                                     *
*          FOR VERY LARGE PDS'S YOU MAY WANT TO SPECIFY PARM=ADD      *
*          THE FIRST TIME, BECAUSE THIS PROGRAM READS EVERY BLOCK     *
*          IN THE PDS.  IT MAY BE QUICKER TO DELETE ALL THE           *
*          GENERATED MEMBER NAMES THAN TO RUN IT A SECOND TIME.       *
*                                                                     *
*          ANY USER DATA THAT WAS IN THE DIRECTORY ENTRY OF           *
*          A DELETED OR REPLACED MEMBER CANNOT BE RECOVERED.          *
*                                                                     *
*          A NOTE ABOUT LOAD MODULES.  THIS PROGRAM CANNOT RECOVER    *
*          A "GAS" LOAD MODULE.  IT WILL GIVE IT A MEMBER NAME SO     *
*          IT WONT BE COMPRESSED OUT, BUT THE DIRECTORY ENTRY WILL    *
*          NOT BE A LOAD MODULE TYPE.  TO RECONSTRUCT THE PROPER      *
*          LOAD MODULE DIRECTORY WOULD REQUIRE INFORMATION THAT       *
*          IS NO LONGER IN THE PDS.                                   *
*                                                                     *
*$DOC$*****************************************************************
         EJECT
***********************************************************************
*                                                                     *
*         HOW CODE FROM 'STATS' WAS MODIFIED.                         *
*          CODE FOR NON-PDS DATA SETS WAS REMOVED.                    *
*          CODE FOR MOST INFO AND STATISTICS WAS REMOVED.             *
*          CODE TO SAVE GAS TTR'S FOR STOW WAS ADDED.                 *
*          STOW ROUTINE WAS ADDED, TO RECOVER GAS MEMBERS.            *
*          THE BUFFER AND VERY LARGE TABLES HAVE BEEN MOVED           *
*          TO A GETMAINED AREA SO THE LOAD MODULE DOESNT TAKE         *
*          UP SO MUCH SPACE. ITS NOW ABOUT 7K INSTEAD OF 72K.         *
*          PARM FIELD IS TOTALLY DIFFERENT. CBUF SUPPORTED.           *
*          DDNAMES ARE DIFFERENT.  THE PRINT FILE IS FBA 121.         *
*                                                                     *
*         WHAT THIS PROGRAM NEEDS.                                    *
*          IF PARM = TTR(TT.TT.RR) IS SPECIFIED, THE PROGRAM          *
*          SHOULD JUST BLINDLY ADD THE MEMBER INSTEAD OF READING      *
*          THE WHOLE PDS AGAIN.                                       *
*          THE PROGRAM SHOULD HAVE A PARM=DELETE OPTION WHICH         *
*          WOULD DELETE ALL $GAS MEMBER NAMES (AFTER YOU'VE           *
*          RENAMED THE ONE YOU WANT TO KEEP).   (DONE - G.P.)         *
*                                                                     *
*$DOC$*****************************************************************
*   MAINTENANCE:                                                      *
* 2008-08-31 GYP  FIXED BUFFER SIZE FOR 'MODERN' DASD (32767)         *
*                 ADDED SYSTEM MAPPING MACROS                         *
*                 ADDED MY OWN FUNCTIONS & MACROS                     *
*                 SYSPRINT DFLT IS FBA,133 BUT V AND >120 SUPPORTED   *
*                 FIXED WIDOWED ALIAS PROCESSING & ALIAS NAME WITH    *
*                  NATIONAL CHARACTER (MOVED FLAG AS X'40' TO BYTE 7) *
* 2009-11-27 GYP  ADDED ERROR RETURN CODES FOR FAILED ADD REQUEST;    *
*                  CHANGED MESSAGES, ADDED TTR=nnnnnn PARM            *
***********************************************************************
         SPACE 3
         MACRO
&LBL     OUTPT   &R0,&R1,&LN=       OUTPUT MACRO
         GBLC  &MACPLAB                                         GP08245
         LCLC  &L                                               GP08245
&L       SETC  'L'''                                            GP08245
&LBL     MACPARM R15,&LN,NULL=&L&R1    LOAD STRING LENGTH       GP08245
         MACPARM R0,&R0,OP=L,NULL=MINUS1    COUNT (UNLESS -1)   GP08245
         MACPARM R1,&R1        STRING TO FOLLOW                 GP08245
         MACPARM R14,OUTRTN,OP=BAL   OUTPUT ROUTINE             GP08245
         AIF     ('&R1' NE 'WORK').MEND
         MVC     WORK(80),BLANKS
.MEND    MEND
         SPACE 2
         MACRO
&LBL     TTRMBB  &TTR,&MBB=DOUBLE  TTR TO MBBCCHHR CONVERSION MACRO
&LBL     STM     R14,R12,12(R13)
         L       R1,&TTR           TTRN ACTUALLY
         SLDL    R0,24             SHIFT TTR INTO R0
         SLL     R0,8              ADJUST
         L       R1,DCBDEBAD       DEB ADDRESS
         LA      R2,&MBB           RESULT ADDRESS
         LR      R3,R13            SAVE SAVEAREA REGISTER
         L       R15,16            CVT POINTER
         L       R15,28(,R15)      TTRN->MBBCCHHR ROUTINE ADDRESS
         BALR    R14,R15
         LR      R13,R3
         LM      R14,R12,12(R13)
         MEND
         SPACE 2
         MACRO
&LBL     MBBTTR  &MBB,&TTR=TEMPTTR MBBCCHHR TO TTR CONVERSION MACRO
&LBL     L       R0,TRACKNUM       CURRENT TRACK NUMBER (RELATIVE 1)
         BCTR    R0,0              CURRENT TRACK NUMBER (RELATIVE 0)
         CLC     CURMBB(7),&MBB    CHANGED TRACKS?
         BE      *+12              NO                           GP10026
           AL    R0,=F'1'          BUMP TRACK NUMBER            GP10026
           MVI   LASTR+L'LASTR-1,1  RESET RECORD NUMBER         GP10026
         STH     R0,&TTR           SAVE RELATIVE TRACK NUMBER
         IC      R1,7+&MBB         GET RECORD NUMBER            GP10026
         STC     R1,&TTR+2         SAVE RECORD NUMBER
         MEND
         SPACE 2
         MACRO
&LBL     HEXES   &SRC,&NUM,&OUT     OUTPUT LOOKS LIKE C1.C2.C3 . . .
         GBLC  &MACPLAB                                         GP08245
&LBL     MACPARM R0,&NUM     LOAD BYTE COUNT                    GP08245
         MACPARM R15,&SRC    LOAD SOURCE ADDRESS                GP08245
         MACPARM R1,&OUT     LOAD OUTPUT ADDRESS                GP08245
         MACPARM R14,SUBHEX,OP=BAL  SUBROUTINE                  GP08245
         MEND
         SPACE 1
         PUNCH ' ORDER PDSGAS(P) '     EASIER DEBUGGING         GP08245
         PUNCH '  SETCODE AC(1)  '     PRIVILEGED               GP08245
         SPACE 1                                                GP08245
         COPY  OPTIONGB                                         GP08245
         SPACE 1                                                GP08245
         SYSPARM LIST=YES                                       GP08245
         EJECT
         PRINT &PRTSOR
         EXTRN SUBSTRIP      DEBLANKER                          GP09331
PDSGAS   PGMHEAD ZERO31,BASE=(R10,R11,R12),PARM=R2,BNDRY=PAGE   GP08245
         MVC   MINBLK,=F'100000'  > 32K                         GP08245
         SPACE 1                                                GP08245
         SERVINIT ,          FIND @SERVICE ROUTINE              GP08245
         SERVLOAD @PRINTER   GET THE PRINT ROUTINE              GP08245
         PRTOPEN SYSPRINT,OPT=(WTO,ABEND)  FAIL IF NO DD        GP08245
         PRTF  OUTHEAD,TITLE=1    SET TITLE PAGE W/PAGE NUMBERS GP08245
         SPACE 1
         PARMLOAD R8,PTR=R2        EXTRACT PARM ADDR/LEN        GP09331
         STM   R8,R9,DB2           SAVE FOR CALL                GP09331
*   FOR TSO, NEED TO STRIP LEADING AND TRAILING BLANKS          GP09331
         SUBCALL SUBSTRIP,(DB2,DB2+4),VL,MF=(E,CALLPARM)        GP09331
         L     R9,DB2+4            GET TRIMMED LENGTH           GP09331
         CH    R9,=H'1'            IS PARM LENGTH ZERO, ONE, OR GP09331
         BL    PARMX               YES, BRANCH
         BH    PARM3               HIGHER                       GP09331
         CLI   0(R8),C'U'          PARM=U SAME AS NO PARM       GP09331
         BE    PARMX                                            GP09331
         B     PARMAB
PARM3    CH    R9,=H'3'            IS PARM LENGTH 3             GP09331
         BNE   PARM6
         CLC   =C'ADD',0(R8)       PARM=ADD                     GP09331
         BNE   PARMAB
         OI    FLAGS,GASADD
         B     PARMX
PARM6    CH    R9,=H'6'            IS PARM LENGTH 6             GP09331
         BNE   PARM10                                           GP09331
         CLC   =C'DELETE',0(R8)    PARM=DELETE                  GP09331
         BNE   PARMAB
         OI    FLAGS,FGDELETE      DELETE ALL $GASNNNN NAMES    GP08245
         B     PARMX               OH, GOODY                    GP08245
PARM10   CH    R9,=H'10'           NEW-FANGLED ?                GP09331
         BNE   PARM13                                           GP09331
         CLC   =C'TTR=',0(R8)      SHORT TTR ?                  GP09331
         BNE   PARMAB              NO; ERROR                    GP09331
         MVC   GASTTR,=X'0001000203000405'                      GP09331
         TR    GASTTR,4(R8)        CH NNNNNN -> NN NN NN        GP09331
         MVI   GASTTR+2,C'.'                                    GP09331
         MVI   GASTTR+5,C'.'                                    GP09331
         B     PARMDOTT                                         GP09331
PARM13   CH    R9,=H'13'           IS PARM LENGTH 13            GP09331
         BNE   PARMAB
         CLC   =C'TTR(',0(R8)      PARM=TTR(NN.NN.NN)           GP09331
         BNE   PARMAB
         CLI   06(R8),C'.'                                      GP09331
         BNE   PARMAB                                           GP09331
         CLI   09(R8),C'.'                                      GP09331
         BNE   PARMAB                                           GP09331
         CLI   12(R8),C')'                                      GP09331
         BNE   PARMAB
         MVC   GASTTR,4(R8)        SAVE NN.NN.NN                GP09331
PARMDOTT MVC   DOUBLE(2),GASTTR         NN                      GP09331
         MVC   DOUBLE+2(2),GASTTR+3        NN                   GP09331
         MVC   DOUBLE+4(2),GASTTR+6           NN                GP09331
         TRT   DOUBLE(6),TRHEXES   VALID HEX ?                  GP09331
         BNZ   PARMX               NO; FAIL                     GP09331
         NC    DOUBLE(6),=6X'1F'   STRIP                        GP09331
         TR    DOUBLE(6),HEXINTAB  CONVERT TO HEX               GP09331
         PACK  GASHTTR(4),DOUBLE(7)    PACK                     GP09331
         OI    FLAGS,GASADD+FGNOMAT   PRESET UNMATCHED          GP09331
         B     PARMX
*                  1 2 3 4 5 6 . 8 . . . . . . .10
HEXINTAB DC   X'000A0B0C0D0E0F00000000000000000000010203040506070809'
         SPACE 1
PARMAB   PRTL  '*** Invalid PARM field ***'                     GP09331
         SERVTERM ,                                             GP09331
         ABEND 1,DUMP                                           GP08245
         SPACE 2
PARMX    LA    R7,BUFFERP    SPACE PAST WORK STUFF              GP08245
         USING BUFFERP,R7
         LA    R1,BUFFER     BUFFER IS OFFSET                   GP08245
         STCM  R1,7,CCW3+1   SET BUFFER ADDRESS                 GP08245
         LR    R8,R7
         A     R8,=A(SAVETBP-BUFFERP)                           GP08245
         USING SAVETBP,R8
         SPACE 2
         LA    R9,LIB
         USING IHADCB,R9
         BAL   R14,OPENIN           OPEN THE INPUT FILE
         SPACE
         MVC   TEMP(4),DS1LSTAR
         HEXES TEMP,3,BUFFER      TEMP LSTAR FORMAT             GP08245
         TTRMBB TEMP,MBB=LASTMBB  SAVE THE LAST USED MBBCCHHR
         PRTLIST HEADER2,TITLE=2    MAKE SUBTITLE               GP08245
         TM    DS1DSORG,DS1DSGPO  PARTITIONED DATA SET ORGANIZATION?
         BO    DSNORGPO           YES, BRANCH
         PRTF  NOTPDS,CC=NO                                     GP08245
         SERVTERM ,                                             GP09331
         ABEND 4,DUMP                                           GP08245
         SPACE 1
HEADER2  FDOPT NL            DEFINE SECOND HEADER LINE          GP08245
         FDPRT ' ',LEN=20                                       GP08245
         FDPRT JFCBDSNM                                         GP08245
         FDPRT JFCBVOLS,6,PAD                                   GP08245
         FDPRT 'DS1LSTAR',RADJ,LEN=11                           GP08245
         FDPRT BUFFER,8,PAD                                     GP08245
         FDPRT *END                                             GP08245
         SPACE
DSNORGPO OI    FLAGS,DIRECTOR       TURN ON THE "IN DIRECTORY" FLAG
         ICM   R0,7,DS1LSTAR  EMPTY OR PDS/E                    GP08245
         BNZ   TESTADEL       NO; PROCEED                       GP08245
         PRTF  MSGBDPDS,CC=NO                                   GP08245
         SERVTERM ,                                             GP09331
         ABEND 4,DUMP                                           GP08245
         SPACE 1
TESTADEL TM    FLAGS,FGDELETE     DELETE FUNCTION REQUESTED ?   GP08245
         BZ    NEXTMBR       NO; JUMP INTO ADD/SCAN LOOP        GP08245
         LA    R5,SAVETBL    LOAD MEMBER TABLE ADDRESS          GP08245
         SR    R4,R4         KEEP COUNT                         GP08245
         SPACE 1
DELDIRED BAL   R14,GETNUMEM  GET NEXT DIRECTORY MEMBER          GP08245
         B     *+4(R15)      BRANCH BY RESPONSE                 GP08245
          B    DELDIRNM      CHECK NAME                         GP08245
          B    MBREOF        LOGICAL END                        GP08245
          B    MBREOF        PHYSICAL END                       GP08245
          B    NOTDIREC      DIRECTORY HOSED                    GP08245
         SPACE 1
DELDIRNM CLC   =C'$GAS',0(R1)   GAS MEMBER ?                    GP08245
         BH    DELDIRED      NOT YET                            GP08245
         BL    MBREOF        PASSED LAST ONE - STOP READING     GP08245
         MVC   0(12,R5),0(R1)  PRESERVE MEMBER NAME AND TTR     GP08245
         LA    R5,16(,R5)    NEXT TABLE SLOT                    GP08245
         INC   (R4)          UP COUNTER                         GP08245
         B     DELDIRED      AND LOOK FOR ANOTHER               GP08245
         SPACE 1
NEXTMBR  BAL   R14,GETNUMEM         GET THE NEXT MEMBER         GP08245
         B     *+4(R15)             BRANCH TABLE
         B     GOODMBR              SUCCESSFUL
         B     MBREOF               EOF IN DIRECTORY
         B     LASTUSED             LAST USED ENTRY FOUND
         B     NOTDIREC             NOT A DIRECTORY RECORD
         SPACE
GOODMBR  LA    R2,ALIASES
         TM    11(R1),X'80'          ALIAS ENTRY?
         BO    GOODALS               YES, BRANCH
         LA    R2,REALMBR            NO, A REAL ENTRY
         NI    7(R1),255-C' '        TURN OFF THE CASE BIT      GP08245
GOODALS  INC   0(,R2),WORK=R14       INCREMENT ALIASES OR MAIN  GP08245
         LR    R2,R1
         TTRMBB 8(R1),MBB=DOUBLE     CONVERT TTR TO MBBCCHHR
         LA    R5,SAVETBL-16
TBLINC   LA    R5,16(,R5)
         TM    0(R5),255-X'40'       EMPTY TABLE ENTRY?         GP08245
         BZ    INSERT                YES, BRANCH                GP08245
         CLC   8(8,R5),DOUBLE        IS IT THIS ENTRY?
         BNE   TBLINC                NO, CHECK NEXT ENTRY
         TM    11(R2),X'80'          ALIAS?
         BZ    UPMEMNAM              NO; SAVE MAIN MEMBER
         CLI   0(R5),C' '           WAS MEMBER NAME STORED ?    GP08245
         BH    NEXTMBR               YES; ELSE WIDOWED ALIAS    GP08245
UPMEMNAM MVC   0(8,R5),0(R2)         NO, USE THE REAL MEMBER NAME
         B     NEXTMBR
INSERT   MVC   0(8,R5),0(R2)        INSERT THE NAME
         MVC   8(8,R5),DOUBLE       INSERT THE MBBCCHHR
         INC   REALENT,WORK=R1      NUMBER OF TABLE ENTRIES     GP08245
         C     R1,=A((SAVETBND-SAVETBL)/16) REACHED MAX ?       GP08245
         BL    NEXTMBR
         PRTF  MANYENT,CC=NO                                    GP08245
         SERVTERM ,                                             GP09331
         ABEND 99,DUMP         ** TOO MANY MEMBER ENTRIES IN THE TABLE
         SPACE
NOTDIREC SR    R15,R15               SIMULATED END OF FILE (GOOD READ)
         B     MBREOF                END OF FILE IN DIRECTORY
         SPACE
LASTUSED DS    0H                    END OF USED ENTRIES IN DIRECTORY
RPTEXCP  BAL   R14,EXCPSTAT          READ AND COLLECT STATISTICS
         LTR   R15,R15
         BZ    RPTEXCP               LOOP IF NOT EOF
MBREOF   SR    R0,R0
         ZI    FLAGS,DIRECTOR        TURN OFF THE "IN DIRECTORY" FLAG
         ST    R15,TEMP              SAVE FOR LATER
         ST    R0,READTOT            REINITIALIZE READTOT
         ST    R0,MAXBLK                                 MAXBLK
         ST    R0,BYTECNT                                      BYTECNT
         MVC   MINBLK,=F'100000'  > 32K                         GP08245
         TM    FLAGS,FGDELETE  DELETE REQUEST ?                 GP08245
         BNZ   DIRCLOSE      YES; LEAVE SORTED BY NAME          GP08245
*
*     SORT THE MEMBER ARRAY BY MBBCCHHR  (USES A SHELLSORT)
*
         L     R2,REALENT           NUMBER OF ELEMENTS TO SORT
         CH    R2,=H'1'             NEED TO SORT ?              GP08245
         BNH   SORTED               NO                          GP08245
         LA    R1,SAVETBL                                       GP08245
         STM   R1,R2,SHELSORT                                   GP08245
         SERVCALL SORTH,SHELSORT    SORT BY MBBCCHHR            GP08245
         SPACE
SORTED   LA    R5,SAVETBL-16
         CLI   16(R5),C' '          ANY MEMBERS?                GP08245
*WHY?*   BH    NOTNULL       YES; SKIP                          GP08245
NOTNULL  L     R14,REALENT   GET ENTRY COUNT                    GP08245
         SLL   R14,4         *16 IS OFFSET                      GP08245
         LA    R14,16(R14,R5)       POINT PAST LAST ENTRY       GP08245
         MVC   0(16,R14),=X'4BC4C9D940C5D5C4FFFFFFFFFFFFFFFF'   GP08245
         L     R15,TEMP             RESTORE THE RETURN CODE
*
NXTREAL  LA    R5,16(,R5)           LAST MEMBER WAS REAL
NXTONE   L     R0,BYTECNT            LAST MEMBER WAS GAS
         ST    R0,SAVETOT            SAVE FOR LATER
         C     R15,=F'8'             END OF DATA SET?
         BE    LASTMTCH              YES, BRANCH
         OI    FLAGS,REALONE         SET FLAG FOR ACTUAL MEMBER
         MVC   CURRMBR(8),0(R5)      SAVE THE CURRENT MEMBER NAME
         CLC   8(8,R5),IOBSEEK       NEXT MBBCCHHR IN THE TABLE?
         BE    REAL                  YES, BRANCH TO READ LOOP
         BH    GAS                   NO, HIGHER -- A "GAS" MEMBER
         PRTF  TTRTOLOW,CC=NO                                   GP08245
         SERVTERM ,                                             GP09331
         ABEND 13,DUMP       **  TABLED TTR < CURRENT TTR  **   GP08245
         SPACE 1
SHELSORT SERVSORT DSECT=NO,LEN=16,COO=8,COL=8                   GP08245
         SPACE
GAS      XI    FLAGS,REALONE         TURN OFF THE ACTUAL MEMBER FLAG
         MBBTTR IOBSEEK,TEMPTTR      CONVERT MBBCCHHR TO TTRN   GP08245
         INC   GASCNT        ADD ONE TO THE GAS MEMBER COUNT    GP08245
         HEXES TEMPTTR,3,GASMTTR     INSERT TTR                 GP08245
         CLI   GASTTR,0              DID USER WANT A SPECIFIC TTR
         BE    GASSAVE               NO, SAVE THEM ALL
         CLC   GASTTR,GASMTTR        THIS THE ONE ?             GP08245
         BNE   GASSAVEX              NO, BRANCH
GASSAVE  INC   ADDCNT,WORK=R1  COUNT CACHED TTRS                GP08245
         C     R1,=A(SAVEGAS#)    HIT LIMIT ?                   GP08245
         BL    GASSAVED      NOT YET                            GP08245
         PRTF  MSGNOGAS,CC=NO                                   GP08245
         L     R1,=A(SAVEGAS#)    RESTORE                       GP08245
         OI    FLAGS,FGLEND  SHOW LOGICAL END                   GP08245
         LA    R15,8         DITTO                              GP08245
         TM    FLAGS,GASADD  ADD ALL ?                          GP08245
         BNZ   GASSAVED      YES; PROCESS WHAT WE HAVE SO FAR   GP08245
         SERVTERM ,                                             GP09331
         ABEND 69,DUMP       RUDE AWAKENING                     GP08245
         SPACE 1                                                GP08245
GASSAVED BCTR  R1,0                  OFFSET = (NUMBER-1)*3
         MH    R1,=H'3'              GET OFFSET INTO TTR TABLE
         LA    R1,SAVEGAS(R1)        POINT TO NEXT SLOT IN TTR TABLE
         MVC   0(3,R1),TEMPTTR       SAVE TTR FOR STOW          GP08245
         SPACE 1
GASSAVEX PRTF  GASMEM,CC=NO                                     GP08245
*PRINT   TM    DS1RECFM,X'C0'        RECFM=U?
*ANYWAY  BO    GASRD                                            GP08245
         BAL   R14,EXCPSTAT          INPUT THE NEXT REAL BLOCK
         ST    R15,TEMP              SAVE THE RETURN CODE
         L     R4,LS                                            GP08245
         MIN   R4,=F'80'             NO MORE THAN CARD IMAGE    GP08245
         BCTR  R4,0          EXECUTE LENGTH                     GP08245
         EX    R4,EXBUFMVC   MOVE TEXT TO BUFFER                GP08245
         PRTF  OUTLINE       PRINT IT                           GP08245
         L     R14,=A(TRTPRINT)   ALL PRINTABLE                 GP08245
         EX    R4,EXBUFTRT   IS IT PRINTABLE ?                  GP08245
         BZ    TESTMAC       YES; NOW CHECK FOR MACRO           GP08245
         L     R14,=A(TRZONES)  ZONE TRANSLATE                  GP08245
         EX    R4,EXBUFTRN   MAKE PRINTABLE                     GP08245
         PRTF  OUTLINE       PRINT IT                           GP08245
         EX    R4,EXBUFMVC   MOVE TEXT TO BUFFER                GP08245
         L     R14,=A(TRNUMBS)  NUMERICS                        GP08245
         EX    R4,EXBUFTRN   MAKE PRINTABLE                     GP08245
         PRTF  OUTLINE       PRINT IT                           GP08245
         MVC   OUTLINE,OUTLINE-1  CLEAR PRINT LINE              GP08245
         B     SKIPMAC                                          GP08245
EXBUFMVC MVC   OUTLINE+20(0),BUFFER    MOVE TEXT                GP08245
EXBUFTRT TRT   OUTLINE+20(0),0(R14)    TEST TEXT                GP08245
EXBUFTRN TR    OUTLINE+20(0),0(R14)    CONVERT TEXT             GP08245
         SPACE 1
TESTMAC  MVC   OUTLINE,OUTLINE-1  CLEAR PRINT LINE              GP08245
         CH    R4,=H'159'    AT LEAST TWO CARDS ?               GP06288
         BL    SKIPMAC       NO                                 GP06288
         L     R14,=A(TRTMAC)    GET TRT WITH ONLY 'M' STOPPER  GP08245
         TRT   BUFFER+1(20),0(R14)  LOOK FOR AN 'M'             GP08245
         CLM   R2,1,C'M'(R14)    FOUND IT ?                     GP08245
         BNE   SKIPMAC       NO                                 GP08245
         BCTR  R1,0          TRT STOPPER 'M' - 1                GP08245
         CLC   =C' MACRO ',0(R1)   IS IT A MACRO ?              GP08245
         BNE   SKIPMAC       NO; GET OUT                        GP08245
NAMEMAC  PRTF  BUFFER+80,80,CC=NO    SHOW MACRO NAME            GP08245
SKIPMAC  L     R15,TEMP              RESTORE THE RETURN CODE
         B     GASRDF                SKIP THE FIRST READ (ALREADY DONE)
         SPACE
GASRD    BAL   R14,EXCPSTAT          "GAS" MEMBER
         INC   GASRDS        ADD ONE TO THE GAS RECORD COUNT    GP08245
GASRDF   TM    FLAGS,FGLEND  LOGICAL END ?                      GP08245
         BNZ   ENDPSPO       QUIT READING                       GP08245
GASRDFT  LTR   R15,R15               (DON'T COUNT THE FIRST ONE)
         BZ    GASRD                 BRANCH IF NOT EOF
         L     R0,BYTECNT
         S     R0,SAVETOT            GAS BYTES THIS MEMBER
         B     NXTONE
         SPACE
REAL     DS    0H
REALRD   BAL   R14,EXCPSTAT          "REAL" MEMBER
REALRDF  LTR   R15,R15               END OF FILE?
         BZ    REALRD                NO, CONTINUE LOOPING
         B     NXTREAL
         SPACE
LASTMTCH CLI   0(R5),X'40'                                      GP08245
         BNH   ENDPSPO                                          GP08245
         CLI   0(R5),X'4B'   DUMMY ENTRY ?                      GP08245
         BE    ENDPSPO       YES; ALL IS FINE AND DANDY         GP08245
         TM    FLAGS,FGLEND  LOGICAL END ?                      GP08245
         BNZ   ENDPSPO       YES; QUIT WITHOUT ERROR            GP08245
         PRTF  NOTFOUND,CC=NO                                   GP08245
         SERVTERM ,                                             GP09331
         ABEND 19,DUMP    **  ALL MEMBERS IN THE TABLE NOT FOUND  **
ENDPSPO  ICM   R1,15,GASCNT                                     GP09331
         BNZ   GASCOUNT
         PRTF  NOGAS,CC=NO                                      GP08245
         TM    FLAGS,GASADD       ADD RUN ?                     GP09331
         BZ    GASCOUNX           NO                            GP09331
         OICC  4                  MINOR ERROR ?                 GP09331
         B     GASCOUNX
GASCOUNT OUTPT GASCNT,GASMBR
GASCOUNX DS    0H
DIRCLOSE CLOSE LIB
         TM    FLAGS,FGDELETE     DELETION RUN ?                GP08245
         BNZ   STOWDEL            YES; CHECK IT                 GP08245
         TM    FLAGS,GASADD
         BZ    PGMEXIT
         BAL   R14,STOW
         TM    FLAGS,FGNOMAT       UNMATCHED REQUEST ?          GP09331
         BZ    PGMEXIT             NONE                         GP09331
         PRTDATA '*** ADD REQUEST FOR TTR',(GASTTR,8,X,PAD),'NOT MATCHE*
               D ***'
         OICC  8                   LEVEL 8 ERROR                GP09331
PGMEXIT  SERVTERM ,          CLEAN UP SERVICE ROUTINES          GP08245
         PGMEXIT RC=RETCODE                                     GP08245
         EJECT
STOW     ICM   R1,15,ADDCNT        GET NUMBER OF TTR'S TO STOW  GP09331
         BZR   R14                 NO, RETURN                   GP09331
         ST    R14,STOWR
         MH    R1,=H'3'            GET OFFSET TO LAST TTR
         LA    R3,SAVEGAS-3(R1)    POINT TO LAST GAS TTR        GP09331
         LA    R2,SAVEGAS          POINT TO FIRST GAS TTR
         LA    R4,1                MEMBER NAME SEQUENCE NUMBER
         OPEN  (PDS,UPDAT)
STOWLOOP CR    R2,R3               ARE WE PAST THE LAST GAS TTR
         BH    STOWEXIT            YES, STOP
         CLC   GASHTTR,0(R2)       MATCHES SPECIFIC REQUEST ?   GP09331
         BNE   *+8                 NO                           GP09331
         ZI    FLAGS,FGNOMAT       SHOW TTR MATCHED             GP09331
         CVD   R4,STOWCVD
         OI    STOWCVD+7,X'0F'
         UNPK  STOWMEM+4(4),STOWCVD+5(3)
         MVC   STOWTTR(3),0(R2)    MOVE TTR FROM TABLE
         STOW  PDS,STOWMEM,A
         STM   R15,R0,DB2    SAVE THE RETURN CODE               GP08245
         MVC   OUTLINE,OUTLINE-1
         MVC   OUTLINE+1(6),=C'MEMBER'
         MVC   OUTLINE+8(8),STOWMEM
         MVC   OUTLINE+17(L'STOWMSGA),STOWMSGA
         BIX   VAL=(R15),SRL=2,BASE=*,ERR=STOWER16,PFX=STOW,           *
               LOC=(MSG0,ERR4,,ER12,,)                          GP08245
STOWMSG0 MVC   TEMPTTR,STOWTTR
         HEXES TEMPTTR,3,OUTLINE+17+L'STOWMSGA                  GP08245
STOWMSG  PRTF  OUTLINE                                          GP08245
         LA    R4,1(,R4)           NEXT SEQUENCE NUMBER
         LA    R2,3(,R2)           NEXT TTR
         B     STOWLOOP
STOWERR4 MVC   OUTLINE+17(L'STOWMSGB),STOWMSGB
         SH    R2,=H'3'            TRY SAME TTR AGAIN W/NEW NAME
         B     STOWMSG
STOWER12 MVC   OUTLINE+17(L'STOWMSGC),STOWMSGC
         B     STOWERRX
STOWER16 MVC   OUTLINE+17(L'STOWMSGD),STOWMSGD
         HEXES DB2+3,1,OUTLINE+17+L'STOWMSGD+1   RETURN CODE    GP08245
         HEXES DB2+7,1,OUTLINE+17+L'STOWMSGD+1+3+1  REASON      GP08245
STOWERRX PRTF  OUTLINE                                          GP08245
         OICC  16                  SET ERROR                    GP09331
STOWEXIT CLOSE (PDS)
         L     R14,STOWR
         BR    R14
STOWR    DC    A(0)
STOWCVD  DC    D'0'
STOWMEM  DC    CL8'$GAS0000'
STOWTTR  DC    XL3'00',X'80'       TTR, ALIAS FLAG
STOWMSGA DC    C'ADDED TO DIRECTORY FOR TTR '
STOWMSGB DC    C'NOT ADDED TO DIRECTORY - ALREADY EXISTS'
STOWMSGC DC    C'NOT ADDED TO DIRECTORY - DIRECTORY FULL'
STOWMSGD DC    C'NOT ADDED TO DIRECTORY - UNKNOWN ERROR'
         SPACE 1
PDS      DCB   DDNAME=SYSLIB,DSORG=PO,MACRF=(R)
         EJECT
*---------------------------------------------------------------------*
*   DELETE OLD GASBAGS. R5 POINT TO LAST ENTRY +1; R4 HAS COUNT       *
*---------------------------------------------------------------------*
STOWDEL  LTR   R4,R4         NUMBER OF TTRS TO DELETE           GP08245
         BP    STOWOP        SOME; OPEN                         GP08245
         PRTL  'NO $GAS---- MEMBERS FOUND',CC=NO                GP08245
         B     PGMEXIT       GET OUT                            GP08245
         SPACE 1                                                GP08245
STOWOP   OPEN  (PDS,UPDAT)   OPEN FOR OUTPUT                    GP08245
STOWDELP SH    R5,=H'16'     PRIOR MEMBER                       GP08245
         MVC   STOWMEM(8),0(R5)    MOVE MEMBER NAME             GP08245
         MVC   STOWTTR(3),8(R5)    MOVE TTR FROM TABLE          GP08245
         STOW  PDS,STOWMEM,D       DELETE                       GP08245
         STM   R15,R0,DB2    SAVE THE RETURN CODE               GP08245
         MVC   OUTLINE,OUTLINE-1
         MVC   OUTLINE+1(6),=C'MEMBER'
         MVC   OUTLINE+8(8),STOWMEM
         MVC   OUTLINE+17(L'DELEMSGA),DELEMSGA                  GP08245
         HEXES 8(R5),3,OUTLINE+17+3                             GP08245
         ICM   R0,15,DB2     STOW WORKED ?                      GP08245
         BNZ   STOWDEBD                                         GP08245
         PRTF  OUTLINE                                          GP08245
         BCT   R4,STOWDELP   DO ANOTHER                         GP08245
         B     STOWDELX      ALL DONE                           GP08245
         SPACE 1                                                GP08245
STOWDEBD MVC   OUTLINE+17+12(L'DELEMSGD),DELEMSGD               GP08245
         HEXES DB2+3,1,OUTLINE+17+L'DELEMSGD+13  RETURN CODE    GP08245
         HEXES DB2+7,1,OUTLINE+17+L'DELEMSGD+16     REASON      GP08245
         PRTF  OUTLINE                                          GP08245
STOWDELX CLOSE (PDS)                                            GP08245
         B     PGMEXIT                                          GP08245
         SPACE 1                                                GP08245
DELEMSGA DC    C'AT TT.TT.RR DELETED'                           GP08245
DELEMSGD DC    C'FAILED. RCD/RSN:'                              GP08245
         EJECT
EXCPSTAT ST    R14,STATR14    ISSUES EXCP'S AND GATHERS STATISTICS
         BAL   R14,EXCP
         B     *+4(R15)
         B     R15IS0             GOOD READ
         B     R15IS4             END OF FILE OR END OF MEMBER
         B     R15IS8             END OF DATA SET
R15IS12  PRTSPACE 1               I/O OR OTHER DISK ERROR       GP08245
         INC   INAROW,WORK=R14    UPDATE NUMBER OF CONSECUTIVE ERRORS
         C     R14,=F'10'         ALLOW ONLY 10 CONSECUTIVE ERRORS
         BL    CONTINUE
         PRTF  TOOMANY,CC=NO      TOO MANY CONSECUTIVE ERRORS   GP08245
         SERVTERM ,                                             GP09331
         ABEND 999,DUMP
CONTINUE HEXES SAVECB,1,IOERROR+16    ECB ERROR CODE            GP08245
         HEXES CURMBB+3,5,IOERROR+26  CURRENT DISK CCHHR        GP08245
         PRTF  IOERROR,CC=NO                                    GP08245
         TM    DS1DSORG,DS1DSGPO  PDS?                          GP08245
         BNO   BLKNUM             NO, BRANCH
         TM    FLAGS,DIRECTOR     ERROR IN THE DIRECTORY?
         BO    DIRERR             YES, BRANCH
         TM    FLAGS,REALONE      ACTUAL MEMBER?
         BNO   GASERR             NO, BRANCH
         OI    CURRMBR+7,X'40'    MAKE PRINTABLE                GP08245
         PRTF  CURRMBR,CC=NO      OUTPUT THE CURRENT MEMBER NAME
         B     R15IS4$
GASERR   PRTF  GASERRS,CC=NO      ERROR IN READING GAS DATA
         B     R15IS4$
DIRERR   PRTF  DIRERRS,CC=NO      DIRECTORY I/O ERROR (IGNORE BLOCK)
BLKNUM   OUTPT READTOT,BLKNUMS    READ ERROR MESSAGE
         B     EXCPSTAT+4         CONTINUE--USING THE NEXT BLOCK
*
R15IS8   DS    0H                 END OF DATA SET
         B     TRKNOINC           FINISH UP PROCESSING
*
R15IS4   MVC   INAROW,=F'0'       END OF MEMBER
R15IS4$  LA    R15,4              TREAT AS AN END OF MEMBER CONDITION
         INC   EOFS               END OF MEMBER COUNTER         GP08245
         CLC   MBBCCHH(7),CURMBB  NEW TRACK
         BNE   RETURNS
         INC   LASTR              UPDATE THE RECORD COUNT
         B     RETURNS
*
R15IS0   MVC   INAROW,=F'0'       GOOD READ
R15IS0$  L     R1,READTOT
         LA    R1,1(,R1)
         ST    R1,READTOT         COUNT OF BLOCKS READ
         L     R1,LS              CURRENT BLKSIZE
         LR    R0,R1              SAVE FOR LATER
         A     R1,BYTECNT
         ST    R1,BYTECNT         TOTAL NUMBER OF BYTES READ
NOFILSUM CLC   MBBCCHH(7),CURMBB  SAME MBBCCHH?
         BNE   NEWTRACK           NO, BRANCH
         A     R0,CURBYTE
         ST    R0,CURBYTE         ACCUMULATE TRACK BYTE COUNT
         INC   LASTR              ACCUMULATE RECORD COUNT/TRACK
         B     RETURNS
*
NEWTRACK INC   TRACKNUM           SWITCH TRACKS --
TRKNOINC MVC   MBBCCHH(7),CURMBB  SAVE CURRENT TRACK NUMBER
         L     R0,CURBYTE
         L     R1,LS
         ST    R1,CURBYTE
         INC   LASTR
*
RETURNS  L     R14,STATR14
         BR    R14
         EJECT
*
*   MEMBER INPUT ROUTINE
*
*
GETNUMEM ST    R14,MEMBR14
         LM    R15,R1,DIRPTRS     PICK UP ADDR, INCR, LIMIT
         LTR   R1,R1              INITIALIZED?
         BNZ   DEBLOCK            YES, BRANCH
         BAL   R14,REREAD         OPEN IN FOR INPUT OF THE DIRECTORY
GETBLK   BAL   R14,EXCPSTAT       GET A DIRECTORY BLOCK
         LTR   R15,R15            END OF FILE?
         BP    MBRDONE            YES, END OF FILE INDICATION
         CLC   LS+2(2),=H'256'    GOOD DIRECTORY BLOCK?
         BNE   NODIRBLK           NO, BRANCH
         LA    R0,2               INCREMENT IS 2 FIRST
         LH    R1,BUFFER          LENGTH HALFWORD
         LA    R1,BUFFER-1(R1)    LIMIT
         LA    R15,BUFFER         START
DEBLOCK  BXH   R15,R0,GETBLK
         IC    R14,11(,R15)
         N     R14,=F'31'          GET LENGTH BITS
         LA    R0,12(R14,R14)     LENGTH * 2  +  12             GP08245
         STM   R15,R1,DIRPTRS     SAVE FOR LATER
         LR    R1,R15        RETURN ENTRY ADDRESS               GP08245
         SR    R15,R15            GOOD READ INDICATION
         CLI   0(R1),X'FF'   LAST MEMBER                        GP08245
         BNE   MBRDONE
         LA    R15,8              LAST MEMBER USED INDICATION
         B     MBRDONE
NODIRBLK PRTF  EOFSIMS,CC=NO      RECORD LENGTH IS NOT 256      GP08245
         OUTPT LS,BLOCKLEN        OUTPUT CURRENT LRECL
         LA    R15,12             ERROR INDICATION
MBRDONE  L     R14,MEMBR14        RETURN ADDRESS
         BR    R14
DIRPTRS  DC    3A(0)              SAVEAREA FOR R15, R0 AND R1
MEMBR14  DC    A(0)               SAVEAREA FOR RETURN ADDRESS
         EJECT
*
*   EXCP SUBROUTINES  (OPENIN, REREAD AND EXCP)
*
*
*   OPENS THE FILE WITH DDNAME IN R9
*
OPENIN   ST    R14,EXCPR14                  ENTRY TO OPEN THE IN FILE
         SERVCALL RJFCB,LIB+DCBDDNAM-IHADCB GET DSN AND VOLUME  GP08245
         LTR   R15,R15                      SUCCESSFUL?
         BZ    NOABE40                      YES, BRANCH
ABE40    PRTF  NOTOPEN,CC=NO                                    GP08245
         SERVTERM ,                                             GP09331
         ABEND 40,DUMP             ** RDJFCB, OBTAIN OR OPEN FAILED **
NOABE40  MVC   INFMJFCB(JFCBLGTH),0(R1)  SAVE THE JFCB
         SERVCALL DSDJ1,INFMJFCB         READ IN THE FORMAT 1 DSCB
         LTR   R15,R15                      SUCCESSFUL?
         BNZ   ABE40                        NO, STOP THE PROGRAM
         MVC   DS1FMTID(DS1END-DS1FMTID),0(R1)  SAVE RETURN     GP08245
         OPEN  (LIB,INPUT)
         TM    DCBOFLGS,DCBOFOPN            IN OPEN?            GP08245
         BZ    ABE40                        NO, STOP THE PROGRAM
         B     REREAD+4
*
*
*   ENTRY TO REREAD THE OPEN DATA SET (DDNAME IS IN)
*
REREAD   ST    R14,EXCPR14
         L     R1,DCBDEBAD
         SR    R14,R14
         IC    R14,16(R1)                   NUMBER OF EXTENTS
         ST    R14,NUMEXT                   SAVE FOR LATER
         LA    R14,16(R1)                   CURRENT EXTENT POINTER - 16
         ST    R14,CUREXT                   SAVE FOR LATER
         MVI   IOBSEEK,X'00'                RESET THE EXTENT NUMBER
         LA    R15,4                        EARLY EXIT FLAG
         B     FIRSTONE                     DO THE INITIAL EXTENT
*
*
*
*    FOR EACH EXTENT, INITIALIZE CCHHR, SECTOR NUMBER AND READ IN THE
*        LENGTH OF THE FIRST RECORD.
*
EACHXTNT SR    R1,R1              AFTER THE FIRST EXTENT --
         IC    R1,IOBSEEK
         LA    R1,1(,R1)
         STC   R1,IOBSEEK         INCREMENT THE EXTENT NUMBER
         C     R1,NUMEXT          PAST LAST EXTENT?
         BNL   EOF$DS             YES, NO END OF DATA SET RECORDED
*
FIRSTONE ST    R15,EXCPR15                  SAVE ENTRY CODE FOR LATER
         L     R1,CUREXT
         LA    R1,16(R1)                    CURRENT EXTENT ENTRY
         MVC   IOBSEEK+3(4),6(R1)           INITIAL CCHH
         ST    R1,CUREXT                    UPDATE FOR LATER
         MVI   IOBSEEK+7,0
         MVI   SNO,0                        SET SECTOR NUMBER=ZERO
         LA    1,CCW1A
         ST    1,CPADDR
         EXCP  IOB
         WAIT  ECB=ECB
         CLI   ECB,X'7F'                    GOOD READ?
         BE    NOABE30                      YES, BRANCH
         PRTF  FIRSTRD,CC=NO
         SERVTERM ,                                             GP09331
         ABEND 30,DUMP     **  FIRST READ OF EXTENT DID NOT WORK  **
NOABE30  LA    R1,CCW1
         ST    R1,CPADDR
         MVC   CCW3+6(2),DATALN             GET LENGTH FOR NEXT READ
         MVI   IOBSEEK+7,1                  SET R=1
         L     R15,EXCPR15
         LTR   R15,R15                      EARLY EXIT DESIRED?
         BZ    EXCP+4                       NO, ISSUE THE EXCP AGAIN
         L     R14,EXCPR14          RETURN FROM OPENIN, REREAD OR
         BR    R14                      AFTER EOF AND EXTENT VIOLATION
*
*   ENTRY FOR EACH EXCP TO BE PERFORMED
*
EXCP     ST    R14,EXCPR14
         CLI   ECB,X'42'                    EXTENT VIOLATION LAST TIME?
         BE    EACHXTNT                     YES, DO THE NEXT EXTENT
         MVC   CURMBB(8),IOBSEEK  SAVE THE DISK ADDRESS OF THIS RECORD
         MVC   LS+2(2),DATALN     SAVE THE BLOCK LENGTH FOR THIS READ
         EXCP  IOB
         WAIT  ECB=ECB
         SR    R15,R15            GOOD READ FLAG
         CLI   ECB,X'42'          EXTENT VIOLATION (NEXT TIME)?
         BE    ENDEXCP            YES, QUIT
         TM    DS1DSORG,DS1DSGIS  ISAM DATA SET?                GP08245
         BO    *+14               YES, IGNORE DS1LSTAR CHECK FOR ISAM
         CLC   LASTMBB(8),CURMBB  PAST THE DS1LSTAR MARKER?
         BNH   EOF$DS             YES, END OF FILE AND DATA SET
         CLI   ECB,X'7F'          GOOD READ?
         BE    ENDEXCP            YES, QUIT
*
         CLI   ECB,X'41'          PERMANENT ERROR?
         BE    NOABE10            YES, BRANCH
         PRTF  BADERROR,CC=NO
         B     SOMERR         **  BAD ERROR; ECB NOT HEX 41, 42 OR 7F
NOABE10  TM    CSW+4,X'01'        ACTUALLY EOF?
         BO    EOF
         CLC   CSW+4(4),=X'00200000'  NULL MEMBER OR DATA SET?
         BE    EOF                    YES, BRANCH
         PRTF  PERMERR,CC=NO                                    GP08245
SOMERR   LA    R15,12         **  PERMANENT ERROR--NOT END OF FILE  **
         MVC   SAVECB(1),ECB      SAVE ECB CODE FOR LATER
         B     REISSUE
*
EOF      TM    DS1DSORG,DS1DSGPS+DS1DSGDA   PS OR DA?           GP08245
         BNZ   EOF$DS             YES, END OF FILE AND DATA SET
         LA    R15,4              END OF MEMBER FLAG
         CLC   =H'0',DATALN       NEXT BLOCK LENGTH = 0?
         BNE   ENDEXCP            NO, BRANCH
REISSUE  ST    R15,EXCPR15
         LA    R1,CCW1A           NEED TO REISSUE THE INITIALIZATION
         ST    R1,CPADDR                EXCP TO GET LENGTH AND ADDRESS
         EXCP  IOB                      OF THE NEXT DATA BLOCK
         WAIT  ECB=ECB
         L     R15,EXCPR15        READ ERROR (12) OR END OF MEMBER (4)
         LA    R1,CCW1
         ST    R1,CPADDR
         MVC   CCW3+6(2),DATALN   DATALENGTH FOR NEXT EXCP
         CLI   ECB,X'42'          EXTENT VIOLATION?
         BE    EACHXTNT           YES, DO THE NEXT EXTENT -- EARLY EXIT
         CLI   ECB,X'7F'          GOOD READ?
         BE    ENDEXCP            YES, QUIT
         CLI   ECB,X'41'          PERMANENT ERROR?
         BE    NOABE70            YES, CHECK EOF
ABE70    PRTF  BADERROR,CC=NO                                   GP08245
         SERVTERM ,                                             GP09331
         ABEND 70,DUMP        **  SOME OTHER BAD ERROR  **
NOABE70  TM    CSW+4,X'01'        NULL MEMBER (ANOTHER EOF)?
         BO    ENDEXCP
ABE80    PRTF  PERMERR,CC=NO                                    GP08245
         SERVTERM ,                                             GP09331
         ABEND 80,DUMP        **  PERMANENT ERROR AFTER EOF  **
EOF$DS   LA    R15,8              END OF FILE AND DATA SET FLAG
ENDEXCP  L     R14,EXCPR14        RETURN ADDRESS
         BR    R14
         EJECT
*
*   OUTPUT ROUTINE
*      NOTE: R0 NEGATIVE IF NO NUMERIC OUTPUT; OTHERWISE OUTPUT NUMERIC
*            R1 CONTAINS THE ADDRESS OF THE STRING TO OUTPUT
*            R14 CONTAINS THE RETURN ADDRESS
*            R15 CONTAINS THE LENGTH OF THE STRING
*
OUTRTN   ST    R14,RETOUT
         LA    R14,OUTLINE+1  NORMAL TEXT POSITION              GP08245
         BCTR  R15,0                  MACHINE LENGTH
         LTR   R0,R0                  ANY LEADING NUMERICS?
         BM    OUTSTMT                NO, BRANCH
         LA    R14,OUTLINE+10   MAKE ROOM FOR NUMBER            GP08245
         CVD   R0,DOUBLE
         MVC   OUTLINE+1(8),=X'4020202020202120'                GP08245
         ED    OUTLINE+1(8),DOUBLE+4  8 DIGITS MAX              GP08245
OUTSTMT  EX    R15,EXMVCOUT                                     GP08245
         PRTF  OUTLINE                                          GP08245
         MVC   OUTLINE,OUTLINE-1      BLANK IT
         L     R14,RETOUT             RETURN
         BR    R14
EXMVCOUT MVC   0(0,R14),0(R1)   <<EXECUTED>>                    GP08245
RETOUT   DC    A(0)                   SAVEAREA FOR RETURN ADDRESS
         DC    C' '
OUTLINE  DC    CL121' '
         DC    CL10' '
OUTHEAD  DC    C'#--- PDSGAS --- LOST MEMBER FINDER ---'
         EJECT ,
*---------------------------------------------------------------------*
*   SUBROUTINE TO PRODUCE PERIOD DELIMITED HEX STRINGS                *
*   I/P IN R15, COUNT IN R0, OUTPUT ADDRESS IN R1                     *
*     O/P LENGTH IS 3*(R0)-1   i.e. X'1234' -> C'12.34'               *
*---------------------------------------------------------------------*
SUBHEX   STM   R14,R1,12(R13)    SAVE A LITTLE                  GP08245
         LTR   R0,R0         VALID COUNT ?                      GP08245
         BNP   SUBHEXEX      NO; EXIT                           GP08245
SUBHEXLP UNPK  DOUBLE(3),0(2,R15)  GET FIRST BYTE               GP08245
         TR    DOUBLE(2),HEXTRTAB  MAKE PRINTABLE               GP08245
         MVC   0(2,R1),DOUBLE      MOVE TEXT                    GP08245
         CH    R0,=H'1'      LAST BYTE ?                        GP08245
         BNH   *+4+4         YES                                GP08245
         MVI   2(R1),C'.'    SEPARATOR                          GP08245
         LA    R15,1(,R15)   ADVANCE INPUT                      GP08245
         LA    R1,3(,R1)     ADVANCE OUTPUT                     GP08245
         BCT   R0,SUBHEXLP   DO ANOTHER                         GP08245
SUBHEXEX LM    R14,R1,12(R13)    SAVE A LITTLE                  GP08245
         BR    R14
         LTORG
         EJECT
MINUS1   DC    F'-1'                      NO NUMERIC OUTPUT FLAG
CCW1A    CCW   X'31',IOBSEEK+3,X'60',5      SEARCH ID EQUAL (CCHHR)
CCW2A    CCW   X'08',*-8,X'60',1            TIC
CCW3A    CCW   X'92',IOBSEEK+3,X'20',8      MT READ NEXT COUNT
*
CCW0     CCW   X'23',SNO,X'60',1            SET SECTOR
CCW1     CCW   X'31',IOBSEEK+3,X'60',5      SEARCH ID EQUAL (CCHHR)
CCW2     CCW   X'08',*-8,X'60',1            TIC
CCW3     CCW   X'06',*-*,X'60',32767        READ DATA           GP08245
CCW4     CCW   X'92',IOBSEEK+3,X'60',8      MT READ NEXT COUNT
CCW5     CCW   X'22',SNO,X'20',1            READ SECTOR NUMBER
*
         DS    0F
IOB      DC    X'C2000000'
         DC    A(ECB)
CSW      DC    2A(0)
CPADDR   DC    A(CCW1)
         DC    A(LIB)
         DC    2A(0)
IOBSEEK  DC    XL8'0'         NEXT MBBCCHHR ADDRESS             GP08245
KEYLN    DS    X              NEXT KEYLENGTH
DATALN   DS    2X             NEXT RECORD LENGTH
SNO      DC    X'00'          NEXT SECTOR NUMBER
*
CURMBB   DC    CL8' '         CURRENT MBBCCHHR ADDRESS
MBBCCHH  DC    CL7' '         SAVE AREA FOR MBBCCHH
*
ECB      DC    F'0'           ECB
LS       DC    F'0'           LENGTH OF THE CURRENT RECORD
*
GASMBR   DC    C'"GAS" MEMBERS FOUND'
NOGAS    DC    C'NO "GAS" MEMBERS FOUND'
IOERROR  DC    C'READ ERROR--ECB=12; CCHHR=12.45.78.01.34'
TOOMANY  DC    C'TOO MANY CONSECUTIVE I/O ERRORS (10)'
MANYENT1 DC    C'MORE THAN '                                    GP08245
         EBCDIG ((SAVETBND-SAVETBL)/16),5                       GP08245
         DC    C' ENTRIES IN THE MEMBER TABLE'                  GP08245
MANYENT  EQU   MANYENT1,*-MANYENT1,C'C'                         GP08245
MSGNOG1  DC    C'REACHED '                                      GP08245
         EBCDIG SAVEGAS#,5                                      GP08245
         DC    C' ENTRIES IN THE GAS TTR TABLE'                 GP08245
MSGNOGAS EQU   MSGNOG1,*-MSGNOG1,C'C'                           GP08245
NOTOPEN  DC    C'RDJFCB, OBTAIN OR OPEN FOR DDNAME SYSLIB FAILED'
FIRSTRD  DC    C'FIRST READ OF AN EXTENT WAS NOT SUCCESSFUL'
TTRTOLOW DC    C'TTR IN MEMBER TABLE IS LESS THAN THE CURRENT TTR'
NOTFOUND DC    C'SOME MEMBERS IN THE MEMBER TABLE WERE NOT FOUND'
BADERROR DC    C'UNDEFINED READ ERROR'
PERMERR  DC    C'UNDEFINED PERMANENT ERROR'
EOFSIMS  DC    C'BAD DIRECTORY RECORD -- RECORD LENGTH IS NOT 256'
BLOCKLEN DC    C'IS THE BLOCK LENGTH  (END OF DIRECTORY SIMULATED)'
DIRERRS  DC    C'READ ERROR IN THE DIRECTORY'
BLKNUMS  DC    C'IS THE BLOCK NUMBER  (BLOCK IGNORED)'
GASERRS  DC    C'ERROR IN A GAS MEMBER  (END OF MEMBER SIMULATED)'
CURRMBR  DC    C'12345678 IS THE MEMBER  (END OF MEMBER SIMULATED)'
*GASMEM   DC    C'"GAS" MEMBER AT TTR TT.TT.RR, DATA:'
GASMEM   DC    C'                "GAS" MEMBER AT TTR TT.TT.RR, DATA:'
GASMTTR  EQU   GASMEM+20+16,8,C'C'  INSERTION POINT             GP08245
NOTPDS   DC    C'SYSLIB DATA SET IS NOT PARTITIONED'
MSGBDPDS DC    C'SYSLIB DATA SET IS EMPTY, A PDS/E, OR HFS'     GP08245
BLANKS   DC    CL256' '
HEXTBL   DC    CL16'0123456789ABCDEF'
HEXTRTAB EQU   HEXTBL-C'0'                                      GP08245
*
         PRINT &PRTSYS                                          GP08245
SYSPRINT PRTWORK SYSPRINT,TITLE=3
LIB      DCB   DDNAME=SYSLIB,DSORG=PS,MACRF=E                   GP08245
         SPACE 2
SAVE     DSECT ,             SAVE AND WORK AREA                 GP08245
DOUBLE   DS    D
DB2      DS    D                                                GP08245
LASTMBB  DS    D
         SERVDEFS ,          EXPAND SERVICE STUFF               GP08245
GASTTR   DC    XL8'00'       0 OR 'TT.TT.RR' FROM PARM
GASHTTR  DC    XL3'0',X'00'  GASTTR IN HEX (PLUS PACK OVERRUN)  GP09331
TEMPTTR  DC    XL3'0'        TTR HOLDER                         GP08245
FLAGS    DC    X'00'                      FLAG BYTE
GASADD   EQU   X'80'                      GAS RECORDS TO BE RECOVERED
FGNOMAT  EQU   X'40'         SPECIFIC REQUEST NOT MATCHED       GP09331
FGDELETE EQU   X'10'         GAS NAMES TO BE DELETED            GP08245
FGLEND   EQU   X'08'         GAS TTR TABLE FULL; RUN AGAIN?     GP08245
REALONE  EQU   X'04'                      ERROR IN ACTUAL MEMBER FLAG
DIRECTOR EQU   X'02'                      ERROR IN DIRECTORY RECORDS
SAVECB   DC    X'00'                      SAVE AREA FOR ECB
LASTR    DC    F'0'                       CURRENT NUMBER OF RECORDS/TRK
EXCPR14  DC    F'0'                       RETURN REGISTER FOR EXCP
EXCPR15  DC    F'0'                       EXIT FLAG REGISTER SAVEAREA
STATR14  DC    F'0'                       RETURN REGISTER FOR EXCPSTAT
CUREXT   DC    F'0'                       ADDRESS OF THE CURRENT EXTENT
NUMEXT   DC    F'0'                       TOTAL EXTENTS IN THE DATA SET
READTOT  DC    F'0'                       TOTAL RECORDS READ
MINBLK   DC    F'33000'                   MINIMUM LENGTH BLOCK
MAXBLK   DC    F'0'                       MAXIMUM LENGTH BLOCK
BYTECNT  DC    F'0'                       TOTAL BYTES READ
TEMP     DC    F'0'                       WORK STORAGE
GASBYTE  DC    F'0'                       TOTAL BYTES IN GAS MEMBERS
RELBYTE  DC    F'0'                       TOTAL BYTES IN REAL MEMBERS
SAVETOT  DC    F'0'                       STARTING BYTE COUNT HOLD
TRKBYTE  DC    F'0'                       SUM OF ALL BLOCKSIZES
INAROW   DC    F'0'                       NUMBER OF CONSECUTIVE ERRORS
CURBYTE  DC    F'0'                       CURRENT TRACK BYTE COUNT
TRACKNUM DC    F'0'                       ACTUAL COUNT OF TRACKS USED
EOFS     DC    F'0'                       COUNT OF END OF FILES
GASCNT   DC    F'0'                       NUMBER OF "GAS" MEMBERS
ADDCNT   DC    F'0'                       NUMBER OF "GAS" TTR'S SAVED
GASRDS   DC    F'0'                       NUMBER OF "GAS" RECORDS
ALIASES  DC    F'0'                       ALIASES IN THE DIRECTORY
REALMBR  DC    F'0'                       REAL ENTRIES (DIRECTORY)
REALENT  DC    F'0'                       MEMBER TABLE ENTRIES
WORK     DC    CL80' '
         SPACE 2
*---------------------------------------------------------------------*
*   SYSTEM CONTROL BLOCKS                                             *
*---------------------------------------------------------------------*
         IEFJFCBN ,          SYSLIB JFCB
         SPACE 1
         IECSDSL1 1          FORMAT 1 DSCB
         SPACE 4
*---------------------------------------------------------------------*
*   BUFFERS AND TABLES                                                *
*---------------------------------------------------------------------*
         DS    0D
SAVEGAS  DS    800XL3               ROOM FOR 800 ENTRIES        GP08245
SAVEGAS# EQU   (*-SAVEGAS)/3      NUMBER SUPPORTED              GP08245
         SPACE
BUFFERP  DS    D                  ALLOW REFERENCE TO BUFFER-1
BUFFER   DS    128XL256      32K BUFFER                         GP08245
*
SAVETBP  DS    2D                   ALLOW REFERENCE TO SAVETBL-16
SAVETBL  DC    64CL256' '           ROOM FOR 1000 ENTRIES       GP08245
         DC    64CL256' '           ROOM FOR 1000 ENTRIES       GP08245
         DC    64CL256' '           ROOM FOR 1000 ENTRIES       GP08245
SAVETBND DS    CL16' '       (FIX FOR TRAILING DELETES)         GP08245
SAVEEND  EQU   *
         SPACE
PDSGAS   CSECT ,
TRTMAC   DC    256AL1(0)     SKIP EVERYTHING                    GP08245
         TRENT TRTMAC,4,C'M'      STOP ON M                     GP08245
         SPACE 1
TRTPRINT DC    256AL1(8)     SET ALL UNPRINTABLE                GP08245
         TRENT TRTPRINT,0,(C'A',9),(C'J',9),(C'S',8),(C'0',10)  GP08245
         TRENT TRTPRINT,0,(C'a',9),(C'j',9),(C's',8)  LOWER     GP08245
         TRENT ,0,C' ',C'.',C'-',C'/',C'@',C'#',C'$',C'*'       GP08245
         TRENT ,0,C''''                                         GP08245
         SPACE 1
TRZONES  DC    16C'0',16C'1',16C'2',16C'3',16C'4'               GP08245
         DC    16C'5',16C'6',16C'7',16C'8',16C'9'               GP08245
         DC    16C'A',16C'B',16C'C',16C'D',16C'E'               GP08245
         DC    16C'F'                                           GP08245
         SPACE 1
TRNUMBS  DC    16C'0123456789ABCDEF'                            GP08245
         SPACE 1
TRHEXES  DC    256X'8'       ALL INVALID                        GP09331
         TRENT TRHEXES,0,(X'81',6),(C'A',6)  a-f  A-F           GP09331
         TRENT ,0,(C'0',10)                  0-9                GP09331
         PRINT NOGEN
         DCBD  DSORG=PS,DEVD=DA
         END
