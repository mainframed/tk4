SUBCMPRS TITLE 'S U B C M P R S  ***  COMPRESS A PDS'
         MACRO  ,
&NM      MVD   &FLD,&EF      DEFINE FIELD FOR MVC
         LCLC  &HB,&L
&L       SETC  'L'''
         AIF   ('&EF' EQ '').NOTEND
&HB      SETC  '128+'
.NOTEND  ANOP  ,
&NM      DC    AL1(&HB&L&FLD-1),AL2(&FLD-TEXTBASE)
         MEND  ,
         MACRO  ,                                               GP13203
&NM      CLRDD &FLD,&F2                                         GP13203
         GBLC  &MACPLAB                                         GP13203
         LCLA  &I,&L                                            GP13203
         LCLC  &DD                                              GP13203
&L       SETA  N'&SYSLIST                                       GP13203
&MACPLAB SETC  '&NM'                                            GP13203
.LOOP    AIF   (&I GE &L).MEND                                  GP13203
&I       SETA  &I+1                                             GP13203
&DD      SETC  '&SYSLIST(&I)'                                   GP13203
         AIF   ('&DD' EQ '').LOOP      JUST IN CASE             GP14278
         MACPARM &DD,BLANKS,OP=MVC                              GP13203
         AGO   .LOOP                                            GP13203
.MEND    MEND  ,                                                GP13203
         COPY  OPTIONGB
         SPACE 1
         SYSPARM LIST=YES
         SPACE 1
***********************************************************************
*                                                                     *
*   QUICK AND DIRTY COMPRESS INTERFACE; SPLIT OFF FROM SUBCOPY        *
*                         REWRITTEN BY GERHARD POSTPISCHIL 2005-02-24 *
*                                                                     *
***********************************************************************
*                                                                     *
*   INPUT: R1 - ADDRESS LIST:                                         *
*               'CMP'        CMP,DSN,SER,PSWD,MSGDDN(RETURNED)        *
*         R14 - RETURN                                                *
*                                                                     *
*  OUTPUT R15 - 0  SUCCESSFUL                                         *
*               4  MINOR ERROR (E.G., TRUNCATED RECORDS)              *
*               8  OPEN FAILURE                                       *
*              12  ALLOCATION FAILURE                                 *
*              16  OTHER ERROR                                        *
*                                                                     *
*  FOR ERRORS, R1 WILL POINT TO A BCON-FORMAT ERROR MESSAGE           *
*  R0/R1 WILL CONTAIN THE DDNAME FOR THE IEBCOPY SYSPRINT FILE.       *
*  >>>>> THE USER MUST CALL 'UNA' TO FREE THIS FILE LATER <<<<<       *
*  ALL OTHER FILES ARE UNALLOCATED ON RETURN                          *
***********************************************************************
         PRINT &PRTSOR
SUBCMPRS PGMHEAD ZERO,BASE=(R12,R11),AM=31,RM=24,BNDRY=PAGE,PARM=R1
* FUNCTION \ PARM:                 R3  = INPUT DSN
*
*  CMP (COMPRESS) OUTDSN OUTVOL OUTPSW MSGDDN
*
*  UNA (UNALL)    MSGDDN   (or any DDN)
         SPACE 1
         CLI   MSGDDN,C' '   DID USER FREE IT ?                 GP14278
         BNH   CMPCLR          YES                              GP14278
         MVC   UNDDNX,MSGDDN REQUEST FREE                       GP14278
         BAL   R14,FREEDD    FREE IT                            GP14278
CMPCLR   CLRDD SINDDN,UT1DDN,UT3DDN,UT4DDN,MSGDDN  CLEAR ALL    GP14278
         MVC   CVDSN,BLANKS                                     GP11358
         MVC   CVSER,BLANKS                                     GP11358
         L     R2,0(,R1)     LOOK AT THE FUNCTION CODE
         SR    R9,R9         CLEAR REASON CODE
         SR    R10,R10       CLEAR MESSAGE ADDRESS
         CLC   =C'UNA',0(R2)  UNALLOCATE ONLY ?
         BE    JUSTUNA       YES; HANDLE DIFFERENTLY
         CLC   =C'CMP',0(R2) COMPRESS SINGLE PDS ?              GP13296
         BE    ALLCMP        YES                                GP13296
         LA    R8,16         MAJOR BOOBOO
         LA    R10,MSGBDFUN  INVALID FUNCTION
         B     RETERROR      QUIT WITH MAJOR ERROR
         SPACE 1
*---------------------------------------------------------------------*
*   NORMAL TERMINATION, CLEANUP, AND RETURN                           *
*---------------------------------------------------------------------*
CMPERROR STM   R8,R10,RETCODE     SET ERROR LEVEL AND DDN       GP13203
PGMEXIT  LA    R5,=A(SINDDN,UT1DDN,UT3DDN,UT4DDN)  ALLOCATED DDS
         LA    R6,4          CURRENTLY FOUR NAMES MAX
PGMEXILP L     R1,0(,R5)     GET NEXT DD NAME ADDRESS
         CLI   0(R1),C' '    WAS IT ALLOCATED ?
         BNH   PGMEXIBP      NO; BUMP
         MVC   UNDDNX,0(R1)  MOVE TO UNALLOCATE LIST
         MVI   0(R1),C' '    INDICATE IT'S FREE, NOW            GP14278
         BAL   R14,FREEDD    FREE IT                            GP14278
PGMEXIBP LA    R5,4(,R5)     NEXT DD POINTER
         BCT   R6,PGMEXILP   TRY AGAIN
PGMEXIX  PGMEXIT COPYRET=(RETCODE,12)  RETURN R15 THROUGH R1
         SPACE 1
JUSTUNA  LA    R3,UNAMVC     POINT TO DDNAME MOVER
         BAL   R14,MOVEDATA  MOVE OUTPUT FIELDS
         MVC   UNDDNX,MSGDDN REQUEST FREE                       GP14278
         BAL   R14,FREEDD    FREE IT                            GP14278
         LTR   R15,R15                                          GP14278
         BNZ   CMPERROR      RETURN WITH ERROR MESSAGE          GP14278
         B     PGMEXIX         OR GOOD RETURN                   GP14278
         SPACE 1
FREEDD   STM   R14,R1,DB     SAVE RETURN AND PARM
         SLR   R9,R9         FOR QUICK RETURN                   GP14278
         SLR   R15,R15                                          GP14278
         CLI   UNDDNX,C' '   ALREADY FREED ?                    GP14278
         BNHR  R14             YES; IMMEDIATE RETURN            GP14278
         LA    R4,UNTU1      POINT TO MESSAGE UNALLOCATION      GP14278
         LA    R3,0          OPTIONAL PARAMETERS (NONE)         GP14278
         BAL   R14,BUILDTUS  BUILD TEXT UNIT LIST               GP14278
         LA    R8,12         PRESET FOR ALLOCATION FAILURE      GP14278
         LA    R10,MSGUNALL  UNALLOCATION FAILED                GP14278
         LA    R1,MYRBPTR                                       GP14278
         DYNALLOC ,                                             GP14278
         L     R9,S99RSC-S99RB+MYRB  COPY REASON CODE           GP14278
         LM    R14,R1,DB                                        GP14278
         BR    R14                                              GP14278
         SPACE 1
*---------------------------------------------------------------------*
*   ERROR RETURN AND CLEANUP                                          *
*---------------------------------------------------------------------*
RETERROR STM   R8,R10,RETCODE SET ERROR LEVEL AND MESSAGE ADDRESS
         B     PGMEXIT       YES; JUST RETURN
CPYPARM  HCON  'RC4NOREP'
         SPACE 1
***********************************************************************
*   ALLOCATE AND INVOKE IEBCOPY FOR PDS COMPRESS                      *
*     CALLING SEQUENCE HAS DSN, SER, PSWD                             *
***********************************************************************
         SPACE 1
ALLCMP   L     R15,16(,R1)   GET MESSAGE DD NAME RETURN AREA    GP13203
         ST    R15,@MSGDDN   SAVE FOR LATER                     GP13203
         MVC   0(8,R15),BLANKS    CLEAR RETURN DD NAME          GP13203
         LA    R3,CMPMVC     POINT TO PARM MOVER                GP13296
         BAL   R14,MOVEDATA  MOVE OUTPUT FIELDS                 GP13296
         LA    R4,CMPTU2     POINT TO OUTPUT ALLOCATION LIST    GP13296
         LA    R3,2          2 OPTIONAL PARAMETERS (VS, PSWD)   GP13296
         BAL   R14,BUILDTUS  BUILD TEXT UNIT LIST               GP13296
         LA    R8,12         PRESET FOR ALLOCATION FAILURE      GP13296
         LA    R10,MSGALLIN  INPUT ALLOCATION FAILED            GP13296
         LA    R1,MYRBPTR                                       GP13296
         DYNALLOC ,                                             GP13296
         MVC   UT1DDN,WKDDRET                                   GP14278
         L     R9,S99RSC-S99RB+MYRB  COPY REASON CODE           GP13296
         LTR   R15,R15                                          GP13296
         BNZ   CMPERROR      RETURN WITH ERROR MESSAGE          GP13296
         SPACE 1                                                GP13296
*  SYSIN DD DUMMY
         SR    R3,R3         NO CONDITIONAL TEXT                GP13296
         LA    R4,TEMPDU1    SYSIN DUMMY                        GP13296
         BAL   R14,BUILDTUS  BUILD TEXT UNITS                   GP13296
         LA    R10,MSGSYSDU  SYSPRINT ALLOCATION FAILED         GP13296
         LA    R1,MYRBPTR                                       GP13296
         DYNALLOC ,                                             GP13296
         MVC   SINDDN,WKDDRET                                   GP14278
         L     R9,S99RSC-S99RB+MYRB  COPY REASON CODE           GP13296
         LTR   R15,R15                                          GP13296
         BNZ   CMPERROR                                         GP13296
         SPACE 1                                                GP13296
*  SYSPRINT DD (disk file)
         SR    R3,R3         NO CONDITIONAL TEXT                GP13296
         LA    R4,MSGTU2     SAVE FOR BROWSE                    GP13203
         BAL   R14,BUILDTUS  BUILD TEXT UNITS                   GP13296
         LA    R10,MSGPRTOU  SYSPRINT ALLOCATION FAILED         GP13296
         LA    R1,MYRBPTR                                       GP13296
         DYNALLOC ,                                             GP13296
         MVC   MSGDDN,WKDDRET                                   GP14278
         L     R14,@MSGDDN   GET USER'S DD NAME ADDRESS         GP13203
         MVC   0(8,R14),MSGDDN    RETURN DD NAME                GP13203
         L     R9,S99RSC-S99RB+MYRB  COPY REASON CODE           GP13296
         LTR   R15,R15                                          GP13296
         BNZ   CMPERROR                                         GP13296
         SPACE 1                                                GP13296
*  SYSUT3   DD (disk file)
         SR    R3,R3         NO CONDITIONAL TEXT                GP13296
         LA    R4,TEMPTU1    SAVE FOR BROWSE                    GP13203
         BAL   R14,BUILDTUS  BUILD TEXT UNITS                   GP13296
         LA    R10,MSGBDWRK  SYSUT3   ALLOCATION FAILED         GP13296
         LA    R1,MYRBPTR                                       GP13296
         DYNALLOC ,                                             GP13296
         MVC   UT3DDN,WKDDRET                                   GP14278
         L     R9,S99RSC-S99RB+MYRB  COPY REASON CODE           GP13296
         LTR   R15,R15                                          GP13296
         BNZ   CMPERROR                                         GP13296
         SPACE 1                                                GP13296
*  SYSUT4   DD (disk file)
         SR    R3,R3         NO CONDITIONAL TEXT                GP13296
         LA    R4,TEMPTU1    SAVE FOR BROWSE                    GP13203
         BAL   R14,BUILDTUS  BUILD TEXT UNITS                   GP13296
         LA    R10,MSGBDWRK  SYSUT4   ALLOCATION FAILED         GP13296
         LA    R1,MYRBPTR                                       GP13296
         DYNALLOC ,                                             GP13296
         MVC   UT4DDN,WKDDRET                                   GP14278
         L     R9,S99RSC-S99RB+MYRB  COPY REASON CODE           GP13296
         LTR   R15,R15                                          GP13296
         BNZ   CMPERROR                                         GP13296
         SPACE 1                                                GP13296
         LA    R0,CPYDDND-CPYDDNM-2  LENGTH OF DDNAME LIST      GP13296
         STH   R0,CPYDDNM    SET LIST LENGTH                    GP13296
         LA    R14,CPYPARM   GET PARM OPTION LIST               GP13296
         LA    R15,CPYDDNM   GET DD NAME LIST                   GP13296
         STM   R14,R15,CPY@PARM                                 GP13296
         OI    CPY@PARM+4,X'80'  COMPLETE LIST                  GP13296
         MVC   UT2DDN,UT1DDN      OUTPUT SAME AS INPUT          GP13296
         LTCB  R3,USE=YES                                       GP13296
         LR    R5,R3         SET NON-ZERO FLAG                  GP13296
         TM    TCBPKF,TCBFLAG  NON-ZERO PROTECT KEY ?           GP13296
         BNZ   USEPKF        YES; USE AS IS                     GP13296
         OI    TCBPKF,X'80'  SET KEY 8                          GP13296
         SR    R5,R5         SET KEY ZERO FLAG                  GP13296
USEPKF   LA    R8,20         ERROR WITHOUT NORMAL RETURN CODE   GP13296
         LA    R10,MSGATTCY  IEBCOPY FAILED                     GP13296
         LA    R2,CPYECB                                        GP13296
         LA    R1,CPY@PARM   POINT TO PARM LIST                 GP13296
         ATTACH EP=IEBCOPY,ECB=(R2),SZERO=NO  WAIT ON IEBCOPY   GP13296
         LTR   R5,R5         DID WE CHANGE TCB PROTECT KEY ?    GP13296
         BNZ   DONEPKF       NO                                 GP13296
         NI    TCBPKF,255-TCBFLAG  RESET KEY                    GP13296
         DROP  R3                                               GP13296
DONEPKF  LTR   R9,R15                                           GP13296
         BNZ   CMPERROR      FAILED ?                           GP13296
         ST    R1,CPYTCB                                        GP13296
         WAIT  ECB=(R2)                                         GP13296
         LA    R1,CPYTCB     GET TCB POINTER                    GP13296
         DETACH (1)          AND GET RID OF IT                  GP13296
         LA    R10,MSGEXECY  IEBCOPY FAILED                     GP13296
         ICM   R9,7,CPYECB+1  GET RETURN CODE                   GP13296
         BNZ   CMPERROR                                         GP13296
         B     PGMEXIT       AND RETURN                         GP13296
         EJECT ,
*---------------------------------------------------------------------*
*   MOVE INPUT DATA TO TEXT UNITS - MVD CHAIN IN R3; PARM IN R1       *
*---------------------------------------------------------------------*
MOVEDATA STM   R14,R12,12(R13)    SAVE FOR DEBUGGING (R14-R1 RETURNED)
         SR    R5,R5         CLEAR FOR IC
MOVEDATL LTR   R1,R1         LAST CALL PARAMETER PROCESSED ?
         BM    MOVEDATX      YES; RETURN
         LA    R1,4(,R1)     ADVANCE TO NEXT ONE
         IC    R5,0(,R3)     GET LENGTH AND FLAG BYTE
         SR    R6,R6
         ICM   R6,3,1(R3)    LOAD DESTINATION DISPLACEMENT
         LA    R6,TEXTBASE(R6)  MAKE ADDRESS
         N     R5,=X'0000007F'  REMOVE END OF LIST BIT
         L     R7,0(,R1)     LOAD USER'S PARAMETER ADDRESS
         LA    R7,0(,R7)     CLEAN FOR TEST
         LTR   R7,R7         ANY ?
         BZ    MOVEDATS      NO; SKIP (OR RETURN ERROR?)
         EX    R5,EXINPMVC   MOVE DATA TO TEXT
MOVEDATS TM    0(R3),X'80'   LAST LIST ENTRY ?
         BNZ   MOVEDATX      YES; RETURN
         LA    R3,3(,R3)     NEXT LIST
         B     MOVEDATL      AND TEST PARM END
MOVEDATX LM    R2,R12,28(R13)  R1 UPDATED
         BR    R14
EXINPMVC MVC   0(0,R6),0(R7)  MOVE DATA TO TEXT
         EJECT
*---------------------------------------------------------------------*
*   BUILD TEXT UNIT POINTERS                                          *
*---------------------------------------------------------------------*
BUILDTUS XC    TUWORK,TUWORK  CLEAR RB AND TEXT UNIT WORK AREA
         LA    R2,MYRB       POINT TO REQUEST BLOCK
         ST    R2,MYRBPTR+S99RBPTR-S99RBP    BUILD POINTER
         OI    MYRBPTR,S99RBPND    END BIT
         USING S99RB,R2      DECLARE BRIEFLY
         MVI   S99RBLN,S99RBEND-S99RB  BLOCK LENGTH
         MVI   S99VERB,S99VRBAL    ALLOCATE
         LA    R1,NEWTUS     TU POINTER START
         ST    R1,S99TXTPP   SET INTO RB
         OI    S99FLAG1,S99NOCNV                                GP13203
         LA    R1,NEWTUS-4   POINT TO NEW OUTPUT LESS ONE
         DROP  R2
BUILDTUL LH    R2,0(,R4)     GET OFFSET
         N     R2,=X'00007FFF'  KILL END OF LIST BIT
         LA    R2,TEXTBASE(R2)  ADDRESS OF TEXT UNIT
         LTR   R3,R3         NEED TO TEST TEXT PARM ?
         BNP   BUILDTUU      NO; UNCONDITIONAL
         CLI   6(R2),C'*'    PARAMETER MASK (ENTIRE PDS)
         BE    BUILDTUB      YES; BUMP TO NEXT ONE
         CLI   6(R2),C' '    PARAMETER PRESENT ?
         BNH   BUILDTUB      NO; BUMP TO NEXT ONE
BUILDTUU ST    R2,4(,R1)     FINAGLE
         LA    R1,4(,R1)     ADVANCE
BUILDTUB BCTR  R3,0          DECREMENT TEST COUNTER
         TM    0(R4),X'80'   LAST ITEM ?
         LA    R4,2(,R4)     BUMP ANYWAY
         BZ    BUILDTUL      LOOP
         OI    0(R1),X'80'   SET LAST TU
         BR    R14
         SPACE 1
@MSGDDN  DC    A(0)          USER'S MESSAGE DD NAME             GP13203
BLANKS   DC    CL44' '       CONSTANT                           GP11358
DB       DS    2D                                               GP14278
         LTORG ,                                                GP11358
         EJECT
*---------------------------------------------------------------------*
*   ERROR MESSAGES                                                    *
*---------------------------------------------------------------------*
MSGALLIN BCON  'INPUT ALLOCATION FAILED'
MSGALLOU BCON  'OUTPUT ALLOCATION FAILED'
MSGUNALL BCON  'DD UNALLOCATION FAILED'                         GP14278
MSGPRTOU BCON  'SYSPRINT TEMP. ALLOCATION FAILED'
MSGBDWRK BCON  'SYSUTn WORK SPACE ALLOCATION FAILED'            GP14278
MSGSYSDU BCON  'SYSIN DUMMY ALLOCATION FAILED'
MSGALLSP BCON  'SPACE ALLOCATION FAILED'
MSGATTCY BCON  'IEBCOPY ATTACH FAILED'
MSGEXECY BCON  'IEBCOPY FAILED'
MSGOPNIN BCON  'INPUT OPEN FAILED'
MSGOPNOU BCON  'OUTPUT OPEN FAILED'
MSGABEND BCON  'COPY REQUEST ABENDED XXXXX'
MSGBDFUN BCON  'INVALID FUNCTION REQUESTED'
MSGBADID BCON  'COPY TO SAME DATA SET FAILED'                   GP11358
         SPACE 1
*---------------------------------------------------------------------*
*   OPEN LIST AND DCB PATTERNS                                        *
*---------------------------------------------------------------------*
CMPTU2   DC    AL2(OUTDSN-TEXTBASE)         COMPRESS LIST       GP13296
         DC    AL2(OUTVOL-TEXTBASE)                             GP13296
         DC    AL2(OUPASS-TEXTBASE)                             GP13296
         DC    AL2(FREECLOS-TEXTBASE)                           GP13296
         DC    AL2(DISPOLD-TEXTBASE)                            GP13296
         DC    AL2(WKDDNAME-TEXTBASE)                           GP13296
         DC    AL2(DISPKEEP+X'8000'-TEXTBASE)                   GP13296
         SPACE 1
MSGTU2   DC    AL2(DISPNEW-TEXTBASE)                            GP13203
         DC    AL2(DISPNDEL-TEXTBASE)                           GP13203
         DC    AL2(DISPCDEL-TEXTBASE)                           GP13203
         DC    AL2(SPCTRK-TEXTBASE)                             GP13203
         DC    AL2(SPCTRKS-TEXTBASE)                            GP13203
         DC    AL2(SPCSECS-TEXTBASE)                            GP13203
         DC    AL2(WKDDNAME+X'8000'-TEXTBASE)                   GP13203
         SPACE 1
TEMPDU1  DC    AL2(WKDDNAME-TEXTBASE)  ALLOCATE A DUMMY (SYSIN)
         DC    AL2(WRKDUMMY-TEXTBASE+X'8000')
         SPACE 1
TEMPTU1  DC    AL2(WKDDNAME-TEXTBASE)  ALLOCATE A TEMPORARY
         DC    AL2(DISPNEW-TEXTBASE)
         DC    AL2(DISPCDEL-TEXTBASE)
         DC    AL2(SPCCYL-TEXTBASE)
         DC    AL2(SPCCYLS-TEXTBASE)
         DC    AL2(SPCSECS-TEXTBASE+X'8000')
         SPACE 1
***********************************************************************
*   MOVE LISTS, IN PARM SEQUENCE                                      *
***********************************************************************
CMPMVC   MVD   CVDSN         OUTPUT DSN                         GP13296
         MVD   CVSER                VOLSER                      GP13296
         MVD   CVPSW,*END           PASSWORD                    GP13296
         SPACE 1
UNAMVC   MVD   UT1DDN,*END   UNALLOCATION DD NAME
         SPACE 1
*---------------------------------------------------------------------*
*   UNALLOCATION LISTS                                                *
*---------------------------------------------------------------------*
UNTU1    DC    AL2(UNDDNM-TEXTBASE)
         DC    AL2(UNHOLD-TEXTBASE)    RELEASE SYSOUT HOLD STATUS
         DC    AL2(UNALL+X'8000'-TEXTBASE)
         SPACE 2
***********************************************************************
*   INPUT TEXT UNITS                                                  *
***********************************************************************
TEXTBASE DS    0H            START OF TEXT UNITS
DISPSHR  DC    Y(DALSTATS,1,1),X'08'   DISP=SHR
DISPOLD  DC    Y(DALSTATS,1,1),X'01'   DISP=OLD
DISPMOD  DC    Y(DALSTATS,1,1),X'02'   DISP=MOD                 GP06269
DISPKEEP DC    Y(DALNDISP,1,1),X'08'   DISP=,KEEP
FREECLOS DC    Y(DALCLOSE,0)      FREE ON CLOSE
         SPACE 1
***********************************************************************
*   OUTPUT DATA SET TEXT UNITS                                        *
***********************************************************************
OUTDSN   DC    Y(DALDSNAM,1,44)   OUTPUT DSN
CVDSN    DC    CL44' '
OUTVOL   DC    Y(DALVLSER,1,6)    OUTPUT SERIAL
CVSER    DC    CL6' '
OUPASS   DC    Y(DALPASSW,1,8)    PASSWORD
CVPSW    DC    CL8' '
         SPACE 1
***********************************************************************
*   OUTPUT DATA SET TEXT UNITS WITH SPACE ALLOCATION                  *
***********************************************************************
DISPNEW  DC    Y(DALSTATS,1,1),X'04'  DISP=NEW
DISPCAT  DC    Y(DALNDISP,1,1),X'02'  ,CATLG
DISPNDEL DC    Y(DALNDISP,1,1),X'04'  ,DELETE                   GP13302
DISPCDEL DC    Y(DALCDISP,1,1),X'04'  ,,DELETE
DSARECFM DC    Y(DALRECFM,1,1)
CVRECFM  DC    X'00'
DSALRECL DC    Y(DALLRECL,1,2)
CVLRECL  DC    XL2'00'
DSABLKSI DC    Y(DALBLKSZ,1,2)
CVBLKL   DC    XL2'00'
SPCTYPE  DC    Y(DALTRK,0)  OVERLAYED BY CYL OR TRK
CVSPTYP  DC    X'00'
SPCPRIM  DC    Y(DALPRIME,1,3)
CVSPRIM  DC    XL3'00'
SPCSEC   DC    Y(DALSECND,1,3)
CVSPSEC  DC    XL3'00'
SPCRELS  DC    Y(DALRLSE,0)
SPCDES   DC    Y(DALDIR,1,3)
CVDES    DC    XL3'00'
ORGPO    DC    Y(DALDSORG,1,2),X'0200'  DSORG=PO
         SPACE 1
***********************************************************************
*   TEMPORARY WORK DATA SETS (SYSIN/SYSOUT/etc.)                      *
***********************************************************************
WKDDNAME DC    Y(DALRTDDN,1,8)  RETURN DD NAME                  GP14278
WKDDRET  DC    CL8' '                                           GP14278
SPCTRK   DC    Y(DALTRK,0)
SPCTRKS  DC    Y(DALPRIME,1,3)
         DC    XL3'10'
SPCSECS  DC    Y(DALSECND,1,3)
         DC    XL3'10'
SPCCYL   DC    Y(DALCYL,0)                                      GP14278
SPCCYLS  DC    Y(DALPRIME,1,3)                                  GP14278
         DC    XL3'5'                                           GP14278
SPCSECC  DC    Y(DALSECND,1,3)                                  GP14278
         DC    XL3'5'                                           GP14278
PRTRECFM DC    Y(DALRECFM,1,1),X'94'  FBA
PRTLRECL DC    Y(DALLRECL,1,2,121)    LRECL=79
PRTBLKSI DC    Y(DALBLKSZ,1,2,1210)   BLKSIZE=6162
WRKDUMMY DC    Y(DALDUMMY,0)  DD DUMMY
         SPACE 1
***********************************************************************
*   SYSOUT TEXT UNITS                                                 *
***********************************************************************
OUTOUT   DC    Y(DALSYSOU,1,1)
OVCLS    DC    CL1'A'
OUTFORM  DC    Y(DALSFMNO,1,4)
OVFRM    DC    CL4' '
OUTCOP   DC    Y(DALCOPYS,1,1)
OVCOP    DC    AL1(1)        COPY COUNT
OUTDEST  DC    Y(DALSUSER,1,8)
OVDEST   DC    CL8' '
OUTWTR   DC    Y(DALSPGNM,1,8)
OVWTR    DC    CL8' '
*UTSPIN  DC    Y(DALSPIN,1,1),X'80'  IMMEDIATE SPIN
         SPACE 1
***********************************************************************
*   UNALLOCATION (FOR CASES WHERE OPEN FAILS)                         *
***********************************************************************
UNDDNM   DC    Y(DUNDDNAM,1,8)
UNDDNX   DC    CL8' '
UNALL    DC    Y(DUNUNALC,0)
UNHOLD   DC    Y(DUNOVSNH,0)
         SPACE 1
***********************************************************************
*   IEBCOPY INVOCATION PARAMETERS                                     *
***********************************************************************
CPY@PARM DS    A(CPYPARM,CPYDDNM+X'80000000')
CPYDDNM  DS    AL2(CPYDDND-*)
         DS    4XL8'0'       RESERVED
SINDDN   DS    CL8'SYSIN'
MSGDDN   DS    CL8'SYSPRINT'
         DS    1XL8'0'       RESERVED
UT1DDN   DS    CL8'SYSUT1'   INPUT PDS
UT2DDN   DS    CL8'SYSUT2'   OUTPUT PDS
UT3DDN   DS    CL8'SYSUT3'   WORK DS
UT4DDN   DS    CL8'SYSUT4'   WORK DS
CPYDDND  EQU   *             END OF DD NAME LIST
         EJECT
*---------------------------------------------------------------------*
*   DYNAMIC STORAGE                                                   *
*---------------------------------------------------------------------*
SAVE     DSECT
         SERVDEFS ,          COMMON GOODIES
         SPACE 1
MYRBPTR  DC    A(0)          RB POINTER
MYRB     DC    5A(0)         REQUEST BLOCK
NEWTUS   DC    32A(0)        TEXT UNIT POINTERS                 GP11358
TUWORK   EQU   MYRBPTR,*-MYRBPTR,C'A'  WORK AREA FOR CLEARING
         SPACE 1
OUTMAXLN DC    F'0'          MAXIMUM OUTPUT LRECL
OUTADJST DC    F'0'          4 FOR V AND D
INADJUST DC    F'0'          0-FF; 4-VF; 1/5 IF CC-> ^CC
OUTFREC  DC    X'00'         80 F; 40 V; 00 U
PROFLAGS DC    X'00'         PROCESSING FLAGS
PFOUT    EQU   X'80'           CALLED FOR O/P ALLOCATION ONLY
PFCOPY   EQU   X'40'           SHOW SOME TYPE OF COPY           GP11358
PFINT    EQU   X'20'           BYPASS INTEGRITY                 GP11358
         SPACE 1
CPYTCB   DS    A             TCB ADDRESS FOR DETACH
CPYECB   DS    A             ECB ADDRESS FOR ATTACH
         SPACE 1
SAVEEND  EQU   *
         SPACE 1
         DCBD  DEVD=DA,DSORG=PS
         SPACE 1                                                GP06343
         IKJTCB ,                                               GP06343
         SPACE 1                                                GP06343
         IHAPSA ,                                               GP06343
         SPACE 1
         IEFZB4D0 ,          DEFINE REQUEST BLOCK
         IEFZB4D2 ,          DEFINE TEXT UNIT VALUES
         END
