LOOKDD   TITLE 'L O O K D D  ***  DDNAME LOCATION ROUTINE'
         COPY  OPTIONGB
         SPACE 1
         SYSPARM LIST=YES
         SPACE 1
***********************************************************************
**                                                                   **
**  THIS SUBROUTINE MAY BE USED BY FORTRAN OR COBOL PROGRAMS         **
**  TO DETERMINE THE PRESENCE OR ABSENCE OF A DD CARD,               **
**  AND TO GET THE DEVICE TYPE (OF THE FIRST UCB).                   **
**                                                                   **
**  FORTRAN FUNCTION:  I=LOOKDD('DDNAME  ') 8-BYTES                  **
**     -"-  SUBRTNE.:  CALL LOOKDD('DDNAME  ',I)                     **
**  COBOL: CALL LOOKDD USING DDNAME, I                               **
**                                                                   **
**       I RETURNED:         -1 DDCARD NOT FOUND                     **
**         HEX 0000           0 DD DUMMY                             **
**             0101         257 DD TERM                              **
**             0102         258 SYSIN/SYSOUT (SPOOLED)               **
**             08XX        2048+ UNIT RECORD TYPES                   **
**             10XX        4096+ GRAPHICS/LOCAL COMMUNICATIONS       **
**             20XX        8192+ DIRECT ACCESS DEVICES               **
**             40XX       16384+ COMMUNICATIONS EQUIPMENT            **
**             41XX       16640+ CHANNEL/CHANNEL ADAPTER             **
**             80XX       32768+ TAPE DRIVES                         **
**                                                                   **
***********************************************************************
         SPACE 2
         PRINT &PRTSOR
LOOKDD   PGMHEAD ZERO12,BASE=R12,PARM=R9,AM=31,RM=ANY   ADDED ON 85198
         L     R7,PSATOLD-PSA  GET OUR TCB ADDRESS
         LM    R2,R3,0(R9)  LOAD CALLING PARAMETERS             GP03146
         SLR   R0,R0
         BCTR  R0,0          PROVISIONALLY SET BAD TYPE
         ICM   R7,15,TCBTIO-TCB(R7)  ANY TIOT ?
         BZ    EXIT          NO
         SLR   R5,R5         CLEAR FOR IC
         USING TIOT1,R7      DECLARE THE TIOT BASE
LOOKTIOT ICM   R5,1,TIOELNGH  LAST ENTRY ?
         BZ    EXIT          YES; DDNAME NOT FOUND
         CLC   TIOEDDNM,0(R2)  MATCHES CALLER'S REQUEST ?
         BE    HAVEDD        YES; CHECK TYPE, ETC.
         AR    R7,R5         SPACE TO NEXT ENTRY
         B     LOOKTIOT      AND TRY AGAIN
         SPACE 1
HAVEDD   SLR   R0,R0         CLEAR R0 AGAIN
         ICM   R4,7,TIOEFSRT  GET FIRST UCB ADDRESS
         BZ    NULLUCB       NONE ?
*DEFER*  SERVICE SWAAB,(R4)  GET UCB ADDRESS
         LR    R4,R1
         USING UCBOB,R4      DECLARE IT
         ICM   R0,3,UCBTBYT3  LOAD MAJOR AND MINOR DEVICE TYPE
         B     EXIT
         SPACE 1
NULLUCB  LA    R0,X'103'     SET FOR TCAM QUEUE
         TM    TIOELINK,TIOEQNAM  QUEUE ?
         BNZ   EXIT          YES
         BCTR  R0,0
         TM    TIOELINK,TIOESYIN+TIOESSDS+TIOTREM  SPOOLED ?
         BNZ   EXIT          YES
         BCTR  R0,0
         TM    TIOELINK,TIOTTERM  TSO TERMINAL ?
         BNZ   EXIT
         SLR   R0,R0         REAL DD DUMMY ?
EXIT     LTR   R2,R2         END OF PARM LIST AT NAME ?
         BM    FUNCTION      YES; FUNCTION CALL
         ST    R0,0(,R3)     SAVE IN USER'S WORD
         SPACE 1
FUNCTION ST    R0,RSNCODE    SAVE IN REASON CODE
         PGMEXIT COPYRET=(RETCODE,8)  RETURN R15, R0
         SPACE 2
SAVE     DSECT ,
         SERVDEFS ,          DEFINE R15-RETCODE, R0-RSNCODE
SAVEEND  EQU   *
         SPACE 2
         PRINT &PRTSYS
         IHAPSA ,
         IKJTCB ,
MYTIOT   DSECT ,
         IEFTIOT1 ,
MYUCB    DSECT ,
         IEFUCBOB ,
         END
