SUBP2N    TITLE 'S U B P 2 N   **   DATE CONVERSION SUBROUTINE'
***********************************************************************
*                                                                     *
*   THIS SUBROUTINE PROVIDES DATE INFORMATION IN CANONICAL FORM       *
*   FOR THE PURPOSE OF CALCULATING ELAPSED DAYS BETWEEN TWO DATES     *
*                                                                     *
*   NO ALLOWANCE IS MADE FOR LEAP CENTURIES, SO THE NUMBERS ARE GOOD  *
*   BETWEEN MARCH 1, 1900 AND FEBRUARY 28, 2100 ONLY.                 *
*                                                                     *
*   WHEN R0=0                                                         *
*   INPUT R1 = PACKED DATE (00YYDDDS/01YYDDDS OR CCYYDDDS)            *
*            = 0 : USE CURRENT DATE                                   *
*                                                                     *
*   WHEN R0=4                                                         *
*   INPUT R1 = UNSIGNED HEX DATE CCYYMMDD OR 00YYMMDD                 *
*        (00YY BECOMES 19YY IF YY>80, ELSE 20YY)                      *
*                                                                     *
*   OUTPUT R1 = 0 IF INVALID DATA PASSED                              *
*          R1 = NUMBER OF DAYS SINCE BASE DATE (BINARY)               *
*          R0 = DAY OF WEEK (0 SUNDAY, 1 MONDAY,...,6 SATURDAY)       *
*         R15 = FULL DATE USED, IN UNSIGNED HEX (CCYYMMDD)            *
*                                                                     *
***********************************************************************
*                                                                     *
*   WHEN R0=8                                                         *
*   INPUT R1 = CANONICAL DAY (BINARY) FROM PRIOR CALL                 *
*     OUT R1 = PACKED DATE CCYYDDDS                                   *
*                                                                     *
***********************************************************************
SUBP2N   START 0
SUBP2N   AMODE ANY                                              GP00162
SUBP2N   RMODE ANY                                              GP00162
         USING SUBP2N,R15    TEMPORARY BASE
         STM   R14,R12,12(R13)
         LR    R12,R15       AND NEW BASE
         DROP  R15
         USING SUBP2N,R12
         ST    R13,LOCSAVE+4  MAKE BACK LINK
         LA    R9,LOCSAVE
         ST    R9,8(,R13)    MAKE DOWN LINK
         LR    R13,R9        MAKE LOCAL SAVE
         CH    R0,=H'4'      WHAT FUNCTION IS REQUESTED ?
         BE    NOTPACK       NOT IN PACKED FORMAT
         BH    INVERSE       RETURN DATE FROM NUMBER
         L     R15,=V(SUBP2M)    CONVERT PACKED DAY IN R1 TO HEX
         BASR  R14,R15
         LTR   R15,R15       WAS IT VALID?
         BZ    EXITBAD       NO; OOPS
         LR    R1,R0         PUT IT INTO THE EXPECTED REGISTER
         SPACE 1
NOTPACK  ST    R1,DB         STASH UNSIGNED HEX FORM
         CLI   DB,1          IS THERE A CENTURY VALUE?
         BH    HAVECENT
         BE    CENT21        YES; NEXT
         CLI   DB+1,X'80'    END OF 20?
         BL    CENT21        NO; ASSUME 21
         MVI   DB,X'19'      MAKE THIS CENTURY
         B     HAVECENT      CONTINUE
CENT21   MVI   DB,X'20'      ACTUALLY 2000 IS 20TH CENTURY
HAVECENT MVO   PKYEAR,DB(2)  COPY YEAR
         MVO   PKMON,DB+2(1)  COPY MONTH
         MVO   PKDAY,DB+3(1)  AND DAY
         CVB   R1,PKDAY      DAY TO BINARY
         LTR   R1,R1         NON-ZERO?
         BNP   EXITBAD       TOO BAD
         CH    R1,=H'31'     PLAUSIBLE?
         BH    EXITBAD
         CVB   R3,PKMON      MONTH TO BINARY
         LTR   R3,R3         NON-ZERO?
         BNP   EXITBAD       TOO BAD
         CH    R3,=H'12'     PLAUSIBLE?
         BH    EXITBAD
         CVB   R5,PKYEAR     YEAR TO BINARY
         LA    R3,1(,R3)     ADD FINAGLE FACTOR TO MONTH
         CH    R3,=H'3'      JANUARY/FEBRUARY?
         BH    IMZELLER      NO; LEAVE AS IS
         LA    R3,12(,R3)    ELSE FAKE AS BEING
         BCTR  R5,0          IN THE PREVIOUS YEAR
IMZELLER M     R2,=F'153'    CONVERT MONTH
         D     R2,=F'5'      INTO DAY INCREMENT
         M     R4,=F'1461'   CONVERT YEAR
         D     R4,=F'4'      INTO DAY INCREMENT
         AR    R1,R3         ADD MONTH'S WORTH TO DAY
         AR    R1,R5         ADD YEAR'S WORTH
         LA    R15,5         ADJUST TO MAKE SUNDAY = 0
         ALR   R15,R1        ADD DAYS
         SLR   R14,R14
         D     R14,=F'7'     GET DAY OF WEEK
         LR    R0,R14          INTO RETURN R0 (SUN=0)
         L     R15,DB        RETURN ADJUSTED RESULT
EXITALL  L     R13,4(,R13)   GET CALLER'S SAVE AREA
         STM   R15,R1,16(R13)   RETURN RESULT DAY OF WEEK, DAY
         LM    R14,R12,12(R13)  RESTORE ALL REGISTERS
         BR    R14           RETURN TO CALLER
         SPACE 1
EXITBAD  SLR   R15,R15       SET ERROR CODE
         LR    R0,R15
         LR    R1,R15        DITTO
         B     EXITALL       AND RETURN
         SPACE 1
*  INVERSE - BINARY VALUE IN R1 (FROM PRIOR CALL?) TO BE CONVERTED
*    TO A DATE (IN CCYYDDDS PACKED FORMAT) AND RETURNED IN R1
*  ASSUMPTIONS - LOTS OF FINAGLES - LAST DAY OF LEAP YEAR
*    IS SCALED TO BE MULTIPLE OF 1461.
*
*        YA=428%365
*  PARM1=PARM1-428
*  R=PARM1//1461
*  IF R=0 THEN LP=1
*         ELSE LP=0
*  Q=PARM1%1461
*  QJ=(1-LP)*(R-1)%365
*  RJ=(1-LP)*((R-1)//365)+1+LP*365
*  Y=Q*4+QJ+YA-LP
*  SAY "  "PARM1":   YEAR "Y"   DAY "RJ
*
INVERSE  LR    R15,R1        COPY THE PASSED VALUE
         SLR   R14,R14       CLEAR FOR DIVIDE
         S     R15,=F'428'   SCALE TO MAKE LAST DAY OF LEAP YEAR 0
         BNP   EXITBAD       TOO SMALL
         D     R14,=F'1461'  CONVERT BACK TO GROUPS OF 4 YEARS
         SLA   R15,2         CONVERT GROUP TO YEARS
         LA    R2,365        PRESET FOR LEAP YEAR (DAYS-1)
         LTR   R14,R14       LAST DAY OF LEAP YEAR?
         BZ    INVLEAP       YES; ALL SET
         LR    R3,R14        COPY REMAINDER
         BCTR  R3,0          FINAGLE
         SLR   R2,R2         CLEAR FOR DIVIDE
         D     R2,=F'365'    SPLIT INTO YEARS AND DAYS
         LA    R15,1(R3,R15) ADD QUOTIENT AND FINAGLE CONSTANT
INVLEAP  MH    R15,=H'1000'  SCALE YEARS
         LA    R15,1(R2,R15) ADD DAYS TO YEARS AND FINAGLE
         CVD   R15,DB        CONVERT TO PACKED FORM
         L     R1,DB+4       LOAD RESULT
         B     EXITALL       AND RETURN
         SPACE 1
DB       DC    D'0'          DOUBLE-WORD WORK AREA
PKYEAR   DC    PL8'0'        YEAR, INCLUDING CENTURY
PKMON    DC    PL8'0'        MONTH
PKDAY    DC    PL8'0'        DAY IN MONTH
LOCSAVE  DC    18F'0'        LOCAL SAVE AREA
         PRINT NOGEN
         SPACE 1
         YREGS ,
         END   ,
