SUBP2W   TITLE 'S U B P 2 W   **   DATE CONVERSION SUBROUTINE'
***********************************************************************
*                                                                     *
*   THIS SUBROUTINE PROVIDES WEEK OF MONTH AND WEEK OF YEAR           *
*                                                                     *
*   WHEN R0=0                                                         *
*   INPUT R1 = PACKED DATE (00YYDDDS/01YYDDDS OR CCYYDDDS)            *
*                                                                     *
*   OUTPUT R1 = 0 IF INVALID DATA PASSED                              *
*          R0 = 00 / WEEK OF YEAR / WEEK OF MONTH / 9 IF LAST WEEK    *
*          R1 = 00 / DAY OF WEEK / DAY OF MONTH / 99 UNLESS LAST DAY  *
*         R15 = POINTER TO MAPSBP2W RETURN AREA (MOSTLY TEXT)         *
*                                                                     *
***********************************************************************
SUBP2W   START 0
SUBP2W   AMODE ANY                                              GP00162
SUBP2W   RMODE ANY                                              GP00162
         USING SUBP2W,R15    TEMPORARY BASE
         STM   R14,R12,12(R13)
         LR    R12,R15       AND NEW BASE
         DROP  R15
         USING SUBP2W,R12
         ST    R13,LOCSAVE+4  MAKE BACK LINK
         LA    R9,LOCSAVE
         ST    R9,8(,R13)    MAKE DOWN LINK
         LR    R13,R9        MAKE LOCAL SAVE
         XC    RETVARS(CLEARLEN),RETVARS  CLEAR VALUES          GP02322
         MVI   RETVWOL,9     PRESET LAST WEEK OF MONTH FLAG     GP02322
         MVI   RETVDOL,99    PRESET LAST DAY OF MONTH FLAG      GP02322
         MVI   MONTHL+5,C'L'  RESET LAST WEEK OF MONTH          GP02322
         MVC   MONDH1+3(2),=C'  '  RESET DAY OF MONTH           GP02322
         MVC   MONDHL+3(2),=C'32'  RESET LAST DAY OF MONTH      GP02322
         LTR   R1,R1         ANY DATE PASSED ?
         BNZ   HAVEDATE      YES
         TIME  ,             GET TODAY'S DATE AS DEFAULT
         SPACE 1
HAVEDATE ST    R1,DB+4       STASH UNSIGNED HEX FORM            GP02322
         CLI   DB+4,X'19'    IS THERE A CENTURY VALUE?          GP02322
         BL    HAVECENT      NO; RETAIN IT                      GP02322
         SP    DB+4(4),=P'1900000' CHANGE TO BASE               GP02322
         SPACE 1                                                GP02322
HAVECENT L     R5,DB+4       GET DATE FROM 'TIME SVC' OR PARM   GP02322
         ZAP   JULDATE,DB+4(4)  SAVE RUN DATE                    89212
         AP    JULDATE,=P'1900000'  WITH PROPER CENTURY         GP02322
         SR    R0,R0         CLEAR FOR DIVIDE                   GP02322
         CVB   R1,DB         MAKE BINARY DATE                   GP02322
         D     R0,=F'1000'   SPLIT INTO DAY/YEAR                GP02322
         ST    R0,BINDAY                                        GP02322
         ST    R1,BINYEAR                                       GP02322
         LA    R1,6(,R1)     ADD FINAGLE FACTOR                  80105
         LA    R9,1(,R1)     SAVE YEAR + SECOND FINAGLE          80105
         SRA   R9,2          GET NUMBER OF LEAP YEARS SINCE 1900 80105
         MH    R1,=H'365'    DAYS SINCE 1900                     80105
         AR    R1,R9         ADD LEAP DAYS                       80105
         A     R1,BINDAY     ADD DAYS IN THIS YEAR              GP02322
         LA    R1,6(,R1)     ADD ANOTHER FINAGLE                 80105
         SLR   R0,R0                                             80105
         D     R0,=F'7'      GET DAY OF WEEK                     80105
         STC   R0,RETVDOW    RETURN TO CALLER                   GP02322
         MH    R0,=AL2(MONDAY-SUNDAY)    MAKE OFFSET INTO TABLE  80105
         LA    R1,SUNDAY     GET TABLE OF DAYS                   80105
         AR    R1,R0         GET TODAY'S ENTRY                   80105
         MVC   WEEKDAY(MONDAY-SUNDAY),0(R1)    SAVE IT           80105
         ST    R5,DB+4       STASH DATE AGAIN                    80105
         TM    DB+5,X'01'    ODD YEAR ?                          80105
         BNZ   NOTLEAP       YES; NOT A LEAP YEAR                80105
         TM    DB+5,X'12'    MULTIPLE OF FOUR ?                  80105
         BM    NOTLEAP       NO; NOT LEAP YEAR (FOR A WHILE)     80105
         MVI   MONFEB+1,29      MAKE LONG FEBRUARY               80105
NOTLEAP  L     R1,BINDAY     GET JULIAN DAYS                    GP02322
         SLR   R0,R0                                             80105
         LA    R1,6(,R1)                                         80105
         D     R0,=F'7'      GET WEEK OF YEAR                    80105
         STC   R1,RETVWOY    PUT IN RETURN                      GP02322
         CVD   R1,DB         MAKE PACKED                         80105
         UNPK  DB(3),DB+6(2)                                     80105
         OI    DB+2,C'0'     MAKE ZONE                           80105
         MVC   WEEKNO+4(2),DB+1  COPY WEEK NUMBER                80105
         CLI   DB+1,C'0'     EARLY WEEK ?                        80105
         BNE   WK2DIGS       NO                                  80105
         MVC   WEEKNO+4(1),DB+2                                  80105
         MVI   WEEKNO+5,C' '                                     80105
WK2DIGS  L     R1,BINDAY     GET DAY AGAIN                      GP02322
         LA    R14,MONTAB                                        80105
         LA    R15,11                                            80105
MONTHLOP CH    R1,0(,R14)    IN THIS MONTH ?                     80105
         BNH   MONTHDON      YES                                 80105
         SH    R1,0(,R14)                                        80105
         LA    R14,2(,R14)                                       80105
         BCT   R15,MONTHLOP                                      80105
MONTHDON LH    R15,0(,R14)   GET DAYS IN THIS MONTH              80105
         LR    R14,R1        SAVE DAY IN THIS MONTH              80105
         STC   R1,RETVDOM    RETURN DAY OF MONTH                GP02322
         SLR   R0,R0                                             80105
         LA    R1,6(,R1)                                         80105
         D     R0,=F'7'                                          80105
         STC   R1,RETVWOM    RETURN WEEK OF MONTH               GP02322
         STC   R1,MONTH1+5                                       80105
         OI    MONTH1+5,C'0'                                     80105
         LR    R1,R14        COPY DAY OF MONTH AGAIN             84062
         SLR   R0,R0         CLEAR FOR DIVIDE                    84062
         D     R0,=F'10'     GET DAY/TENS                        84062
         STC   R1,MONDH1+3   STASH TENS                          84062
         STC   R0,MONDH1+4     AND UNITS                         84062
         OI    MONDH1+3,C'0'  MAKE EBCDIC NUMERIC                84062
         OI    MONDH1+4,C'0'                                     84062
         CR    R14,R15       LAST DAY OF MONTH ?                 84062
         BNE   MONNLAST      NO                                  84062
         MVC   MONDHL,MONDH1  COPY LAST DAY                      84062
         MVC   RETVDOL,RETVDOM  RETURN LAST DAY/MONTH INDICATOR GP02322
MONNLAST SH    R15,=H'6'                                         80105
         CR    R14,R15       LAST SEVEN DAYS OF MONTH ?          80105
         BL    WEEKNLST      YES                                 80105
         MVC   MONTHL,MONTH1  ELSE KILL LAST WEEK OF MONTH WORD  80105
         MVC   RETVWOL,RETVWOM  RETURN LAST DAY/MONTH INDICATOR GP02322
WEEKNLST LA    R15,RETPARMS  POINT TO RETURNED VARIABLES        GP02322
         LM    R0,R1,RETVARS RETURN NUMERIC RESULTS             GP02322
EXITALL  L     R13,4(,R13)   GET CALLER'S SAVE AREA
         STM   R15,R1,16(R13)   RETURN RESULT DAY OF WEEK, DAY
         LM    R14,R12,12(R13)  RESTORE ALL REGISTERS
         BR    R14           RETURN TO CALLER
         SPACE 1
EXITBAD  SLR   R15,R15       SET ERROR CODE
         LR    R0,R15
         LR    R1,R15        DITTO
         B     EXITALL       AND RETURN
         SPACE 1                                                 80105
SUNDAY   DC    X'05',CL9'SUNDAY'                                 80105
MONDAY   DC    X'05',CL9'MONDAY'                                 80105
         DC    X'06',CL9'TUESDAY'                                80105
         DC    X'08',CL9'WEDNESDAY'                              80105
         DC    X'07',CL9'THURSDAY'                               80105
         DC    X'05',CL9'FRIDAY'                                 80105
         DC    X'07',CL9'SATURDAY'                               80105
         SPACE 1
MONTAB   DC    H'31'         DAYS PER MONTH                      80105
MONFEB   DC    H'28'         SET TO 28 OR 29                     80105
         DC    H'31,30,31,30,31,31,30,31,30'                     80105
MONDEC   DC    H'31'         DECEMBER                            80105
         SPACE 1
LOCSAVE  DC    18F'0'        LOCAL SAVE AREA
*   NOTE - AREA FROM RETVARS TO BINDAY CLEARED AT ENTRY         GP02322
RETVARS  DC    D'0'                                             GP02322
RETVWOY  EQU   RETVARS+1,1,C'X'  WEEK OF YEAR                   GP02322
RETVWOM  EQU   RETVARS+2,1,C'X'  WEEK OF MONTH                  GP02322
RETVWOL  EQU   RETVARS+3,1,C'X'  WEEK OF MONTH IF LAST, ELSE 9  GP02322
RETVDOW  EQU   RETVARS+5,1,C'X'  DAY OF WEEK                    GP02322
RETVDOM  EQU   RETVARS+6,1,C'X'  DAY OF MONTH                   GP02322
RETVDOL  EQU   RETVARS+7,1,C'X'  DAY OF MONTH IF LAST, ELSE 99  GP02322
DB       DC    D'0'          DOUBLE-WORD WORK AREA
BINYEAR  DC    F'0'          BINARY YEAR                        GP02322
BINDAY   DC    F'0'          BINARY JULIAN DAY                  GP02322
CLEARLEN EQU   *-RETVARS     SIZE OT CLEAR                      GP02322
*   NOTE - VARIABLES REFERENCED BY CALLER (HOB, ETC.)           GP02322
RETPARMS MAPSBP2W ,          RETURN PARAMETER LIST              GP02322
*   END OF EXTERNALLY REFERENCED DATA                           GP02322
         PRINT NOGEN
         SPACE 1
         YREGS ,
         END   ,
