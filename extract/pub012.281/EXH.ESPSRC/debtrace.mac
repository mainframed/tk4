DEBTRC   TITLE 'D E B T R A C E  ***  QUICK AND DIRTY DEBUG OUTPUT'
***********************************************************************
*                                                                     *
*                                                                     *
*   THIS ROUTINE, DRIVEN BY THE DBT MACRO, PRODUCES DEBUG             *
*   OUTPUT FOR ONE OR MORE OF THE OPTIONAL PARAMETERS:                *
*                                                                     *
*   CODE IS EXTREMELY NOT RE-ENTRANT, REUSEABLE, NOR REFRESHABLE.     *
*   CALLER MAY BE AM24 OR 31; ADDRESSES CALCULATED ACCORDINGLY.       *
*   MODULE OPERATES IN AM31; ACCESS REGISTER MODE SUPPORTED.          *
*                                                                     *
*   LABEL DBT TAG,REGS=,TEXT=,HEX=                                    *
*         "TAG" IS AN OPTIONAL 1-8 BYTE NAME (NO QUOTES)              *
*         "REGS=" PRODUCES REGISTERS 0-15 IN HEX                      *
*         "REGS=NO" PRODUCES NO REGISTERS (DEFAULT)                   *
*         "REGS=SHORT"  PRODUCES REGISTERS 14-1 ONLY                  *
*                                                                     *
*         "TEXT=NAME" PRODUCES EBCDIC TEXT USING L'NAME AS A LENGTH   *
*         "TEXT=(NAME,LEN)" PRODUCES TEXT OF LENGTH "LEN"             *
*             THE "NAME" IN THE SECOND FORM MAY BE IN REGISTER OR     *
*             BASE/DISPLACEMENT FORM:  TEXT=((R2),16)                 *
*         "CTEXT=... PRODUCES EBCDIC IF PRINTABLE, ELSE HEX           *
*                                                                     *
*         "HEX= " USES THE SAME SYNTAX AS TEXT=, BUT THE SPECIFIED    *
*             LENGTH IS THAT OF THE INPUT, AND THE OUTPUT WILL BE     *
*             TWICE THE INPUT LENGTH (PLUS SPACERS)                   *
*                                                                     *
*         "PACK=" USES THE SAME SYNTAX AS TEXT=, BUT THE SPECIFIED    *
*             LENGTH IS THAT OF THE INPUT, AND THE OUTPUT WILL BE     *
*             TWICE THE INPUT LENGTH. A LENGTH OF 0 IS VALID.         *
*                                                                     *
*         'LIST=(OP1,(OP2,LN2),(OP3,,TP3)...)                         *
*             PROCESSES A LIST OF ITEMS. EACH ITEM HAS THREE FIELDS:  *
*             NAME OF ITEM TO BE PROCESSED (NO DEFAULT)               *
*             LENGTH (DEFAULT IS L'NAME EXCEPT PACKED DECIMAL, L=0)   *
*             TYPE: HEX (DEFAULT), TEXT, CTEXT, PACKED                *
*                                                                     *
*   ALL NAME FIELDS, WHETHER IN A KEYWORD OR THE LIST OPERAND, MAY    *
*   BE FULLWORD POINTERS:   /NAME DISPLAYS THE DATA AT THE LOCATION   *
*   FOUND IN FIELD NAME. NOTE THAT THIS FORM REQUIRES A LENGTH.       *
*   THE FORM NAME% CAUSES 24-BIT ADDRESSING RESOLUTION WHEN IN 31-BIT *
*   MODE. BOTH / AND % MAY BE USED FOR THE SAME NAME FIELD.           *
*                                                                     *
*   THE CALLER MAY SET DBT@UDCB TO AN OPEN DCB ADDRESS, OR A PRINT    *
*   ADDRESS (EXORPRNT, @PRINTER) AND SET DBTFGMOD ACCORDINGLY.        *
*                                                                     *
*   WHEN THE PRODUCTION MODE FLAG IS ON, PROCESSING WILL BE BYPASSED  *
*   UNLESS A DEBTRACE DD IS SUPPLIED (WTO - DD DUMMY)                 *
*                                                                     *
*   INPUT:  R15 - BASE ADDRESS            (RETURN VIA PR TO BAKR)     *
*           R0  - WORK AREA IN CALLER'S PROGRAM                       *
*           R1  - CALLER'S REQUEST:                                   *
*     ONE OR MORE REQUEST UNITS OF 1 TO 4 HALF-WORDS EACH:            *
*    X'0000'                       NO-OP (USED TO CARRY END-LIST BIT) *
*    X'0100' CL8'NAME    '         FIELD LABEL                        *
*    X'0200'                       ID: CALLER'S PSW, ASCB, ASID       *
*    X'030M' X'R1R2'               REGISTERS R1 TO R2 (FP/CTL/ACR/GP) *
*    X'040M' S(ADDR)               ADDRESS INFO: MODULE+OFFSET        *
*    X'05NN'            (TO BE DONE) SYSTEM INFORMATION, TYPE NN      *
*    X'060M' S(ADDR)               PROCESS SPECIFIED LIST (PUSH/POP)  *
*    X'070M' S(ADDR) S(LEN) LO LL  DUMP A CHAIN (LO-LINK OFFSET/LEN)  *
*    X'080M' S(ADDR) S(LEN)        TEXT STRING                        *
*    X'090M' S(ADDR) S(LEN)        TEXT IF PRINTABLE; HEX OTHERWISE   *
*    X'0A0M' S(ADDR) S(LEN)        FORMAT HEXADECIMAL                 *
*    X'0B0M' S(ADDR) S(LEN)        FORMAT PACKED DECIMAL (0<=L<=16)   *
*    X'0C0M' S(ADDR) S(LEN)        FORMAT INTEGER        (0<=L<=8)    *
*    X'0D0M' S(ADDR) S(LEN)        FORMAT ABSOLUTE INTEG.(0<=L<=8)    *
*    X'0E0M' S(ADDR) S(LEN)        FORMAT HEX FLOATING POINT (0<L<=8) *
*    X'0F0M' S(ADDR) S(LEN)        FORMAT BIN FLOATING POINT (0<L<=8) *
*    X'100M' S(ADDR) S(LEN) S(TBL) SHOW FLAGS AS TEXT        (0<L<=8) *
*                                                                     *
*    X'8XXX' END OF LIST BIT IN LAST ITEM                             *
*         M  ADDR PROCESSING FLAGS (OR'D)                             *
*            01 - INDIRECT ADDRESS          01 - GENERAL REGISTERS    *
*            02 - FORCE VALUE TO 24 BITS    02 - ACCESS REGISTERS     *
*            04 - FORCE VALUE TO 31 BITS    04 - CONTROL REGISTERS    *
*            08 - FORCE VALUE TO 64 BITS    08 - FLOATING POINT REGS  *
*                                                                     *
*        NN  REQUEST INDEX (FOR SYSINFO - NOT DONE YET)               *
*                                                                     *
*   THIS ROUTINE MAY BE CALLED IN 24-BIT AND 31-BIT ADDRESSING MODE,  *
*   AND MAY RESIDE ABOVE THE LINE WHEN LINKED WITH A 31-BIT MODE PGM. *
*   ALL ADDRESS EXPRESSIONS ARE RESOLVED IN THE CALLER'S MODE.        *
*   ENTERED IN PRIMARY MODE, BUT SUPPORTS ACCESS REGISTER MODE FOR    *
*   DATA ACCESS (USING R2 ONLY). AC MODE CONTROLLED BY DBWOPSW FLAGS  *
*                                                                     *
***********************************************************************
*                                                                     *
*  2000/07/15  GYP  ADDED SUPPORT FOR ACCESS REGISTER MODE            *
*                                                                     *
*  2003/01/01  GYP  MAJOR REWRITE OF OLD DEBTRACE; ADD SUBDEBTR STUB; *
*                   CALLER USING BAKR; RETURN VIA PR.                 *
*                                                                     *
***********************************************************************
         MACRO ,
&NM      STOMP &TYPE,&RESTART,&DATA=(R2),&LEN=(R3)
         GBLC  &ZZSTACT(10),&ZZSTINT(10),&ZZSTAXX
         GBLB  &ZZSTACF
         GBLA  &ZZSTACM
         LCLA  &I,&J
         AIF   ('&TYPE' NE 'RESET').SETUP
&NM      MVI   DBWSPIPF,0    RESET ACTION
         AIF   ('&ZZSTAXX' EQ '').MEND
         XC    DBWSPIBY,DBWSPIBY  RESET RECOVERY ADDRESS
&ZZSTAXX SETC  ''
         MEXIT ,
.SETUP   AIF   (&ZZSTACF).NODEF
&ZZSTACF SETB  1
&ZZSTACT(1)  SETC 'PARM'
&ZZSTINT(1)  SETC 'P'
STFPARM  EQU   1             PROCESSING PARM
.*
&ZZSTACT(2)  SETC 'DATA'
&ZZSTINT(2)  SETC 'D'
STFDATA  EQU   2             PROCESSING DATA
.*
&ZZSTACT(3)  SETC 'CONV'
&ZZSTINT(3)  SETC 'D'
STFCONV  EQU   3             PROCESSING DATA FORMATTING
.*
&ZZSTACT(4)  SETC 'WRITE'
&ZZSTINT(4)  SETC ' '
STFRITE  EQU   4             WRITING OUTPUT RECORD
&ZZSTACM SETA 4              NUMBER DEFINED
.NODEF   AIF   (&I GE &ZZSTACM).NOGOOD
&I       SETA  &I+1
         AIF   ('&TYPE' NE '&ZZSTACT(&I)').NODEF
&NM      MVI   DBWSPIPF,&I   SAVE ACTION INDEX
         ST    R9,DBWSPIPM   SAVE PARM ADDRESS
         AIF   ('&ZZSTINT(&I)' NE 'D').MEND  NO DATA
         AIF   ('&DATA(1)' NE 'R2' OR '&LEN(1)' NE 'R3').SPECDAT
         STM   R2,R3,DBWSPIAD  SAVE ADDRESS/LEN OF DATA
         AGO   .DOREST
.SPECDAT MACPARM R0,&DATA
         ST    R0,DBWSPIAD   DATA ADDRESS
         MACPARM R0,&LEN
         ST    R0,DBWSPILN   DATA LENGTH
.DOREST  AIF   ('&RESTART' EQ '').MEND
         MACPARM R0,&RESTART   SET RESTART ACTION
&ZZSTAXX SETC  '&RESTART'    REMEMBER EXIT SET
         ST    R0,DBWSPIBY   RECOVERY EXIT
         MEXIT ,
.NOGOOD  MNOTE 8,'STOMP: UNDEFINED ACTION &TYPE'
.MEND    MEND  ,
         SPACE 2
         MACRO ,
&NM      REQTP &ADDR,&SAD,&LEN
         LCLC  &RQA,&RQL
&RQA     SETC  '&SAD'
&RQL     SETC  '&LEN'
         AIF   ('&RQA' NE '').HAVEAD
&RQA     SETC  '1'           DEFAULT ADDRESS RESOLUTION
.HAVEAD  AIF   ('&RQL' NE '').HAVELN
&RQL     SETC  '6'           DEFAULT ENTRY LENGTH (TYPE/ADDR/LEN)
.HAVELN  ANOP  ,
&NM      DC    Y(&ADDR-DEBTRACE),AL1(&RQA,&RQL)
         MEND  ,
         SPACE 2
         COPY  OPTIONGB      DEFINE GLOBALS
         SPACE 1
         SYSPARM LIST=YES    DEFINE GLOBAL SETTINGS
         SPACE 1
DEBTRACE CSECT ,
         USING DEBTRACE,R15  DECLARE
         B     AROUND        BRANCH AROUND CPR AND ENTRY VECTOR
         DC    AL1(L'OWNTEXT+L'IDTEXT)
IDTEXT   DC    C'DEBTRACE &SYSDATE'
OWNTEXT  DC    C' COPYRIGHT 1995,2003 EXPERT SYSTEM PROGRAMMING'
         SPACE 1                                                GP03009
*  NOTE - CALLER SHOULD BE IN PRIMARY MODE                      GP03009
AROUND   ARMODE 0            SET PRIMARY MODE                   GP00197
         LR    R11,R15       LOCAL BASE                         GP03009
         LA    R12,2048
         LA    R12,2048(R12,R11)  GET TO SECOND BASE
         DROP  R15                                              GP03009
         USING DEBTRACE,R11,R12                                 GP03009
         AM31  WORK=R15      GET HIGH                           GP99029
         LAM   R0,R15,=16F'0'  CLEAR ALL ACCESS REGISTERS       GP00197
         LR    R9,R1         PRESERVE USER'S PARAMETER LIST ADR GP03009
         USING REQWORK,R9    DECLARE MAPPING OF REQUESTS        GP95235
         LR    R8,R0         KEEP OLD WORK AREA ADDRESSABLE     GP95235
         USING DBUSECT,R8    DECLARE OLD AREA                   GP99113
         L     R10,DBU@WORK  GET MY WORK AREA
         USING DBWSECT,R10   DECLARE WORK AREA
         LA    R13,DBWSUBSV  POINT TO NEW SAVE AREA             GP95235
         MVC   4(4,R13),=C'F1SA'  SHOW NO HIGHER S.A.           GP03009
         NI    DBWFLAGS,DBWFGGBL RESET ALL BUT GLOBAL FLAGS     GP99113
         MVI   DBWADMSK,X'FF'  RESTORE AM64 MASK
         SR    R15,R15                                          GP99116
         LA    R0,DBWESPSW   POINT TO ESPIE WORK AREA
         LA    R1,DBWSPISZ   AND LOAD ITS SIZE
         MVCL  R0,R14        CLEAR IT
         TM    DBWOPSW+3,X'01'  64 (128?) BIT ADDRESSING MODE?
         BNZ   DONAMASK      YES; SKIP AROUND
         TM    DBWOPSW+4,X'80'  CALLER IN AM31?                 GP03009
         BZ    SETAMASK      NO                                 GP99116
         LA    R15,X'7F'     MAKE MASK FOR AMODE 31             GP99116
SETAMASK STC   R15,DBWADMSK                                     GP99116
DONAMASK TM    DBUFLAG,DBUFLSPI  BYPASS ESPIE ?
         BNZ   SKIPESPI
         TESTAUTH FCTN=0,RBLEVEL=1,STATE=YES,KEY=YES  MODE/KEY TEST
         BXLE  R15,R15,SKIPESPI  IF EITHER/BOTH, SKIP EPSIE
         ESPIE SET,SPEXIT,(1,2,3,4,5,6,7,8,9,10,11,15),                *
               PARAM=(R8),MF=(E,DBWESPIE)  PASS DBU ADDRESS
         ST    R1,DBWTKSPI   SAVE HIGHER EPSIE, IF ANY          GP02353
         OI    DBWFLAGS,DBWFGSPI SIGNAL ESPIE SUCCESSFUL
SKIPESPI LA    R6,DBTEXITS   SET FOR QUICK EXIT (LABEL ONLY)
         TM    DBUFLAG,DBUFLON  DEBUG ON ?                      GP03003
         BZR   R6            NO; JUST TAKE QUICK RETURN         GP99113
         TM    DBWFLAGS,DBWFGCAN PERMANENT DISABLE?             GP00162
         BNZR  R6            YES; JUST TAKE QUICK RETURN        GP00162
         SPACE 1                                                GP99113
         CLI   DBUFGMOD,DBUF@LCL   IS THIS THE FIRST CALL ?     GP99113
         BNE   DBTNONCE      NO                                 GP99113
         SPACE 1                                                GP00162
         MVI   DBUFGMOD,DBUF@WTO   FIRST TIME CHECKED; DEFAULT  GP99113
         DEVTYPE DBWPRDCB+DCBDDNAM-IHADCB,DBWDB  USER HAS DEBTRACE DD?
         BXH   R15,R15,DBTBYPED   SEE WHETHER TO BYPASS?        GP99113
         CLI   DBWDB+2,0     SOMETHING I CAN USE?               GP99113
         BE    DBTNONCE      NO; USE WTO                        GP99113
         OPEN  MF=(E,DBWOPDCB) MODE=31   OPEN IT                GP03009
         TM    DBWPRDCB+DCBOFLGS-IHADCB,DCBOFOPN OPEN NOW?
         BZ    DBTNONCE
         MVI   DBUFGMOD,DBUF@DCB  SIGNAL OPEN DCB               GP99113
         LA    R0,DBWPRDCB   GET DCB ADDRESS                    GP99113
         ST    R0,DBU@UDCB   AND SET IT                         GP99113
         CLC   DCBLRECL-IHADCB+DBWPRDCB,=H'132'                 GP99113
         BL    DBTNONCE                                         GP99113
         OI    DBUFLAG,DBUFLWID  SET WIDE MODE                  GP99113
         B     DBTNONCE                                         GP99113
DBTBYPED TM    DBUFLAG,DBUFLPRO  PRODUCTION MODE?               GP99113
         BZ    DBTNONCE      NO; USE WTO                        GP99113
         OI    DBWFLAGS,DBWFGCAN DISABLE COMPLETELY             GP00162
         B     DBTEXITS      PROD & NO DD - BYPASS              GP99113
DBTNONCE TM    DBUFLAG,DBUFLPUT  USE TPUT UNDER TSO ?           GP04052
         BZ    DBTNOTSO      NO                                 GP04052
         L     R14,PSAAOLD-PSA  LOAD MY ASCB ADDRESS            GP04052
         ICM   R14,15,ASCBTSB-ASCB(R14)  IS THERE A TSB ?       GP04052
         BNZ   DBTNOTSO      YES; USE TPUT                      GP04052
         NI    DBUFLAG,255-DBUFLPUT  RESET TPUT; USE WTO        GP04052
*   COUNT(3) - SKIP FIRST N CALLS                               GP95235
DBTNOTSO ICM   R14,15,DBUCNT3 LOAD SKIP COUNT                   GP95235
         BNP   DBCNTC        LIMIT REACHED - PROCESS            GP95235
         BCTR  R14,0         DECREMENT                          GP95235
         ST    R14,DBUCNT3   SAVE FOR NEXT TIME                 GP95235
         BR    R6            AND SKIP CALL                      GP95235
DBCNTC   ICM   R14,15,DBUCNT2   LOAD COUNTER                    GP95235
         BNP   DBCNTL        BAD - PROCESS CALL                 GP95235
         BCT   R14,DBCNTN    NON-ZERO; SAVE AND SKIP            GP95235
         MVC   DBUCNT2,DBUCNT2I  REFRESH COUNTER                GP95235
         B     DBCNTL        AND GO                             GP95235
DBCNTN   ST    R14,DBUCNT2      UPDATE COUNTER                  GP95235
         BR    R6            AND EXIT                           GP95235
DBCNTL   ICM   R14,15,DBUCNT1    LOAD LIMIT COUNT               GP95235
         BZ    DBTRACNM      NO COUNT, NO LIMIT                 GP95235
         BNPR  R6            IGNORE IF REACHED                  GP95235
         BCT   R14,DBTRACNM  DECREMENT                          GP95235
         BCTR  R14,0         MAKE NEGATIVE FOR NEXT TIME        GP95235
DBTRACNM ST    R14,DBUCNT1       SAVE FOR NEXT TIME             GP95235
         MVC   DBWMGHED,=CL7'MSG666 '   DEBUG HEADER
         MVI   DBWMGLBL-1,C' ' RESET
         MVC   DBWMGLBL(DBWMGRD2-DBWMGLBL),DBWMGLBL-1 BLANK THE LINE
         SR    R14,R14
         IC    R14,DBWOPSW+2  GET AS×CC×PM BYTE
         SRL   R14,6         ISOLATE ADDRESS SPACE CONTROL BITS
         LA    R14,=X'00,02,01,03'(R14)  POINT TO SAC EQUIVALENT
         MVC   DBWACRMD,0(R14)  SAVE SAC EQUIVALENT FLAG BITS
         TM    DBUFLAG,DBUFLWTO  IS WTO FORCED?                 GP99113
         BNZ   INITSHRT      YES; NARROW MODE                   GP99113
         TM    DBUFLAG,DBUFLWID  WIDE MODE REQUESTED?           GP99113
         BZ    INITSHRT      NO                                 GP99113
         OI    DBWFLAGS,DBWFGWID SET WIDE MODE                  GP99113
         LA    R0,DBWMGRD2-DBWMGRDW WIDE SIZE                   GP99113
         STH   R0,DBWMGRDW   SET WIDE                           GP99113
         TM    DBUFLAG,DBUFLTCB  INCLUDE TCB IN MESSAGE?        GP95235
         BZ    DBTNTCB       NO                                 GP95235
         TM    DBWFLAGS,DBWFGBOT DUAL MODE REQUEST?             GP00162
         BNZ   DBTNTCB       YES; NO ROOM FOR IT                GP00162
         UNPK  DBWMGTC2(9),PSATOLD-PSA(5)                        94011
         TR    DBWMGTC2,TABHEXTR                                 94011
         MVI   DBWMGTC2+L'DBWMGTC2,C' '                          94011
         B     DBTNTCB       DONE                               GP99113
         SPACE 1                                                GP99113
INITSHRT LA    R0,DBWMGRDC-DBWMGRDW SMALL SIZE                  GP99113
         STH   R0,DBWMGRDW   SET NARROW                         GP99113
         NI    DBWFLAGS,255-DBUFLWID  RESET WIDE MODE
         MVC   DBWMGRCC,DBWMGRD2 SET ROUTER/DESCRIPTOR CODES
         TM    DBUFLAG,DBUFLTCB  INCLUDE TCB IN MESSAGE?        GP95235
         BZ    DBTNTCB       NO                                 GP95235
         UNPK  DBWMGTCB(9),PSATOLD-PSA(5)                        94011
         TR    DBWMGTCB,TABHEXTR                                 94011
         MVI   DBWMGTCB+L'DBWMGTCB,C' '                          94011
         SPACE 1                                                GP99113
DBTNTCB  MVC   DBWSVRDW,DBWMGRDW SAVE CORRECT LENGTH AND FLAGS
         SPACE 1                                                GP99113
***********************************************************************
**                                                                   **
**  LOOP TO PROCESS CURRENT REQUEST (POINTED TO BY R9)               **
**                                                                   **
***********************************************************************
         SPACE 1                                                GP99113
LOOPREQ  STOMP PARM          CATCH INVALID PARM ADDRESS
         NI    DBWFLAGS,255-DBWFGNEG RESET NEGATIVE
         IC    R15,REQTYPE   LOAD NEXT REQUEST                  GP97265
         LA    R0,RQTMSK     LOAD END-LIST MASK                 GP02364
         NR    R15,R0        IGNORE END-LIST BIT                GP02364
         CH    R15,=Y((PROCEND-PROCONE)/(PROCTWO-PROCONE))      GP97265
         BH    DBTBADPR      TOO HIGH, TOO BAD
         SLL   R15,2         MAKE PROCESSOR ROUTINE OFFSET      GP97265
         LA    R15,PROCONE(R15)  REQUEST ENTRY
         LH    R7,0(,R15)    LOAD PROCESSING ROUTINE OFFSET
         SR    R14,R14
         IC    R14,3(,R15)   LOAD INSTRUCTION LENGTH
         STH   R14,DBWPRMLN  LENGTH OF CURRENT PARM ITEM
         BCTR  R14,0         LESS ONE
         IC    R0,REQTYPE(R14)  MAKE SURE IT'S ADDRESSABLE
         CLI   2(R15),0      ADDRESS (PRE)PROCESSING?
         BE    DEBTRACE(R7)  NO; INVOKE IT DIRECTLY             GP97265
         B     RESADDR                                          GP97265
         SPACE 1                                                GP97265
*    REQUEST DEFINITIONS
*              SUBRTN   A L   A IS ADDRESS RESOLUTION FLAG (0,1,2)
PROCONE  REQTP DBTPRINT,0,2  NOP
PROCTWO  REQTP FORMNAME,0,10 NAME/LABEL (2/CL8)
         REQTP FORMIDNT,0,2  CALLER ID
         REQTP FORMREGS,0,4  REGISTERS
         REQTP FORMADDR,1,4  ADDRESS INFORMATION
         REQTP FORMINFO,0,2  SYSTEM INFORMATION
         REQTP EXECLIST,1,4  EXECUTE ANOTHER PARM LIST
         REQTP PROCHAIN,1,8  CHAIN (ADDR/LEN/LNKOFF/LNKLEN)
         REQTP FORMTEXT,1    TEXT
         REQTP FORMCHEX,1    TEXT IF PRINTABLE, ELSE HEX
         REQTP FORMHEXT,1    HEX
         REQTP FORMPACK,1    PACKED DECIMAL
         REQTP FORMINTG,1    INTEGER
         REQTP FORMAINT,1    ABSOLUTE INTEGER
         REQTP FORMHEXT,1    HEX FLOATING POINT - TO BE DONE
         REQTP FORMHEXT,1    IEEE FLOATING POINT - TO BE DONE
         REQTP FORMFLAG,3,8  FLAG FORMATTING
PROCEND  EQU   *                                                GP97265
         SPACE 1                                                GP99113
***********************************************************************
**                                                                   **
**  PROCESS ADDRESS, LENGTH, AND FLAG OPTION FOR COMMON FUNCTIONS    **
**                                                                   **
***********************************************************************
         SPACE 1                                                GP99113
RESADDR  STOMP DATA          BUT NOTHING YET SET YET
         SR    R14,R14                                          GP00197
         SR    R2,R2                                            GP00199
         SAR   R2,R2         ALSO CLEAR ACCESS REGISTER
         IC    R14,REQADDR   GET S(ADDR)                        GP00197
         SRL   R14,4         DELETE LOW BITS OF OFFSET          GP00197
         SLA   R14,2         *4 => INDEX INTO DBWSVUSR          GP00197
         BZ    RESADDR0      R0 = 0
         L     R2,DBWSVUSR(R14) GET USER'S REGISTER             GP00197
         L     R14,DBWSVACR(R14)  GET ACCESS REGISTER VALUE     GP00197
         SAR   R2,R14        ALL USER ADDRESS REFERENCES VIA R2 GP00197
RESADDR0 ICM   R14,2,DBWACRMD  SET ACR MODE
         SAC   0(R14)        GET INTO USER'S MODE
         LA    R0,4095       MAKE MASK
         ICM   R14,3,REQADDR  GET LOW PORTION OF ADDRESS        GP95235
         NR    R0,R14        MASK OFFSET OF ADDRESS             GP95235
         ALR   R2,R0         MAKE EFFECTIVE ADDRESS
         TM    REQFG,RFA64   64-BIT ?
         BNZ   NOTBADST      YES; DO NOTHING
         TM    REQFG,RFA24   FORCED 24-BIT ADDRESSING MODE?     GP96101
         BZ    *+8           NO                                 GP96101
         N     R2,=X'00FFFFFF'  FORCE 24-BIT ADDRESS            GP96101
         TM    REQFG,RFA31   FORCED 31-BIT ADDRESSING MODE?     GP99116
         BZ    NOTBAD31      NO
         N     R2,=X'7FFFFFFF'  FORCE 31 BIT
         B     NOTBADST
NOTBAD31 N     R2,DBWADMSK   ELSE MASK ACCORDING TO CALLER'S MODE
NOTBADST ST    R2,DBWSPIAD   SAVE FOR ESPIE
         TM    REQFG,RFIAD   INDIRECT ADDRESSING?               GP96101
         BZ    NOTIADRQ      NO                                 GP96101
         MVC   DBWSPILN,=F'4'  SET INDIRECT LENGTH
         L     R2,0(,R2)     LOAD ADDRESS FROM POINTER          GP96101
         TM    REQFG,RFA64   64-BIT ?
         BNZ   NOTIADST      YES; DO NOTHING
         TM    REQFG,RFA24   FORCED 24-BIT ADDRESSING MODE?     GP96101
         BZ    *+8           NO                                 GP96101
         N     R2,=X'00FFFFFF'  FORCE 24-BIT ADDRESS            GP96101
         TM    REQFG,RFA31   FORCED 31-BIT ADDRESSING MODE?     GP99116
         BZ    NOTIADST      NO
         N     R2,=X'7FFFFFFF'  FORCE 31 BIT
         B     NOTIADST
NOTIAD31 N     R2,DBWADMSK   ELSE MASK ACCORDING TO CALLER'S MODE
NOTIADST ST    R2,DBWSPIAD   SAVE FOR ESPIE
NOTIADRQ SLR   R3,R3                                            GP96101
         IC    R3,REQLEN     GET S(LEN)
         SRL   R3,4          DELETE LOW BITS OF OFFSET
         SLA   R3,2          *4 => INDEX INTO DBWSVUSR          GP03003
         BZ    *+8           R0 = 0
         L     R3,DBWSVUSR(R3) GET USER'S REGISTER              GP03003
         LA    R0,4095       MAKE MASK
         ICM   R14,3,REQLEN  GET LOW PORTION OF LENGTH          GP95235
         NR    R0,R14        MASK OFFSET OF LENGTH              GP95235
         ALR   R3,R0         MAKE EFFECTIVE LENGTH
         ST    R3,DBWSPILN   SAVE FOR ESPIE
         CLI   2(R15),2      MORE THAN ADDR/LEN FIELDS ?
         BNH   DEBTRACE(R7)  NO; GO TO PROCESSOR NOW
*---------------------------------------------------------------------*
*  RESOLVE TABLE ADDRESS FOR FLAG FORMATTING                          *
*    NOTE THAT TABLE MUST BE IN THE PRIMARY ADDRESS SPACE             *
*---------------------------------------------------------------------*
         SR    R14,R14
         SR    R4,R4
         SAR   R4,R4         ALSO CLEAR ACCESS REGISTER
         IC    R14,REQFGADR  GET S(ADDR)
         SRL   R14,4         DELETE LOW BITS OF OFFSET
         SLA   R14,2         *4 => INDEX INTO DBWSVUSR
         BZ    RESTABR0      R0 = 0
         L     R4,DBWSVUSR(R14) GET USER'S REGISTER
         L     R14,DBWSVACR(R14)  GET ACCESS REGISTER VALUE
         SAR   R4,R14        ALL USER ADDRESS REFERENCES VIA R4
RESTABR0 LA    R0,4095       MAKE MASK
         ICM   R14,3,REQFGADR GET LOW PORTION OF ADDRESS
         NR    R0,R14        MASK OFFSET OF ADDRESS
         ALR   R4,R0         MAKE EFFECTIVE ADDRESS
         N     R4,DBWADMSK   USE MASK IN USER'S AMODE
         B     DEBTRACE(R7)  GO TO PROCESSING ROUTINE
         SPACE 1                                                GP99113
***********************************************************************
**                                                                   **
**  REQ 01 - IDENTIFY (NEXT) PRINT LINE WITH A NAME                  **
**           (DOES NOT FORCE PRINTING; OVERLAYS PRIOR NAME)          **
***********************************************************************
         SPACE 1                                                GP99113
FORMNAME SAC   0
         MVC   DBWMGLBL,REQNAME MOVE USER'S LABEL TO PRINT LINE
         TM    REQTYPE,RQTEOL  END OF REQUEST?
         BNZ   DBTNOMOR      YES; SHOULD NEVER HAPPEN ?
         LR    R14,R9        COPY REQUEST ADDRESS
         AH    R14,DBWPRMLN  POINT TO NEXT ENTRY
         CLI   REQTYPE-REQTYPE(R14),RTNAME   FOLLOWED BY ANOTHER ID ?
         BE    DBTNOMOR      YES; FORCE PRINTING OF THIS ONE
         CLI   REQTYPE-REQTYPE(R14),RTNAME+RQTEOL   ANOTHER ID ?
         BE    DBTNOMOR      YES; FORCE PRINTING OF THIS ONE
         LR    R9,R14        POSITION TO NEXT ENTRY
         B     LOOPREQ       AND PROCESS NEXT REQUEST WITH THIS LABEL
         SPACE 1                                                GP99113
***********************************************************************
**                                                                   **
**  REQ 02 - PROCESS CALLER ID (PSW, TCB, OTHER.....)                **
**                                                                   **
***********************************************************************
         PUSH  USING
FORMIDNT SAC   0             GET INTO PRIMARY MODE
         MVC   DBWMGTXT(4),=C'PSW='
         INHEX DBWMGTXT+4,DBWOPSW,4,MAKE=DEB LEFT HALF OF PSW
         INHEX DBWMGTXT+13,DBWOPSW+4,4 RIGHT HALF OF PSW
         LTCB  R3,USE=R3     GET CURRENT TCB
         ST    R3,DBWDB
         MVC   DBWMGTXT+25(4),=C'TCB='
         INHEX DBWMGTXT+29,DBWDB,4 SHOW TCB ADDRESS
         CL    R3,TCBJSTCB   IN JOBSTEP ?
         BNE   FORMIDAS      NO; GET ASCB
         MVC   DBWMGTXT+23(2),=C'JS'
FORMIDAS L     R3,PSAAOLD-PSA  GET MY ASCB
         ST    R3,DBWDB
         MVC   DBWMGTXT+39(5),=C'ASCB='
         INHEX DBWMGTXT+44,DBWDB,4 SHOW ASCB ADDRESS
         USING ASCB,R3
         INHEX DBWMGTXT+54,ASCBASID,2  AND HEX ASCB ID
         B     DBTNOMOR      ALL FOR NOW
         POP   USING
         SPACE 1                                                GP99113
***********************************************************************
**                                                                   **
**  REQ 03 - PROCESS REGISTER REQUESTS                               **
**           REGISTER GROUPS SELECTED BY REQUEST FLAG                **
**                                                                   **
***********************************************************************
         SPACE 1                                                GP99113
FORMREGS CLI   REQFG,0       REGISTERS SPECIFIED ?
         BE    DOREGGP       NO; REQUEST FOR GENERAL ONLY
*---------------------------------------------------------------------*
*   GENERAL PURPOSE REGISTERS                                         *
*---------------------------------------------------------------------*
         TM    REQFG,RRGPR   DO GENERAL REGISTERS ?
         BZ    DOREGAC       NO; CHECK ACR
DOREGGP  LA    R7,DBWSVUSR   POINT TO R0                        GP96101
         IC    R4,REQRG1     GET START REGISTER                 GP97261
         IC    R5,REQRG2     GET END REGISTER                   GP97261
         MVI   DBWMGFLG,C'R'-X'40' AS REGISTER                  GP97265
         LA    R0,DOREGAC
         ST    R0,DBWCHAIN   RETURN AFTER FORMATTING
         TM    DBWOPSW+3,X'01'  AM64 ?
         BNZ   REGLONG       YES; DO 64-BIT REGISTERS
         B     REGSUB        ELSE DO 32-BIT REGISTERS
         SPACE 1
*---------------------------------------------------------------------*
*   ACCESS CONTROL REGISTERS                                          *
*---------------------------------------------------------------------*
DOREGAC  SAC   0
         TM    REQFG,RRACR   DO ACCESS REGISTERS ?
         BZ    DOREGCT       NO; CHECK CTL
         LA    R7,DBWSVACR   POINT TO R0                        GP96101
         IC    R4,REQRG1     GET START REGISTER                 GP97261
         IC    R5,REQRG2     GET END REGISTER                   GP97261
         MVI   DBWMGFLG,C'A'-X'40' AS REGISTER                  GP97265
         LA    R0,DOREGCT
         ST    R0,DBWCHAIN   RETURN AFTER FORMATTING
         B     REGSUB        DO COMMON
         SPACE 1
*---------------------------------------------------------------------*
*   CONTROL REGISTERS - NOT SET                                       *
*---------------------------------------------------------------------*
DOREGCT  SAC   0
         TM    REQFG,RRCTL   DO CONTROL REGISTERS ?
         BZ    DOREGFP       NO; CHECK FLOAT
         LA    R7,DBWSVCCR   POINT TO R0                        GP96101
         IC    R4,REQRG1     GET START REGISTER                 GP97261
         IC    R5,REQRG2     GET END REGISTER                   GP97261
         MVI   DBWMGFLG,C'C'-X'40' AS REGISTER                  GP97265
         LA    R0,DOREGFP
         ST    R0,DBWCHAIN   RETURN AFTER FORMATTING
         TM    DBWOPSW+3,X'01'  AM64 ?
         BNZ   REGLONG       YES; DO 64-BIT REGISTERS
         B     REGSUB        ELSE DO 32-BIT REGISTERS
         SPACE 1
*---------------------------------------------------------------------*
*   FLOATING POINT REGISTERS - HEX                                    *
*---------------------------------------------------------------------*
DOREGFP  SAC   0
         TM    REQFG,RRFPR   DO FLOATING REGISTERS ?
         BZ    DOREGIE       NO; CHECK IEEE
         LA    R7,DBWSVFPR   POINT TO R0                        GP96101
         IC    R4,REQRG1     GET START REGISTER                 GP97261
         IC    R5,REQRG2     GET END REGISTER                   GP97261
         MVI   DBWMGFLG,C'F'-X'40' AS REGISTER                  GP97265
         LA    R0,DOREGIE
         ST    R0,DBWCHAIN   RETURN AFTER FORMATTING
         B     REGLONG       DO COMMON
         SPACE 1
*---------------------------------------------------------------------*
*   FLOATING POINT REGISTERS - BINARY (IEEE)                          *
*---------------------------------------------------------------------*
DOREGIE  SAC   0
         TM    REQFG,RRIEE   DO FLOATING REGISTERS ?
         BZ    DBTPRINT      NO; DONE
         LA    R7,DBWSVFPR   POINT TO R0                        GP96101
         IC    R4,REQRG1     GET START REGISTER                 GP97261
         IC    R5,REQRG2     GET END REGISTER                   GP97261
         MVI   DBWMGFLG,C'B'-X'40' AS REGISTER                  GP97265
         B     REGLONG       DO COMMON
         SPACE 1
*---------------------------------------------------------------------*
*                                                                     *
*  COMREGS - COMMON 32-BIT REGISTER FORMATTING                        *
*                                                                     *
*     4 - START OFFSET (REGISTER NUMBER * 4)                          *
*     5 - END OFFSET   (REGISTER NUMBER * 4)                          *
*     7 - ADDRESS OF REGISTER SAVE AREA                               *
*                                                                     *
*---------------------------------------------------------------------*
REGSUB   STOMP DATA,DATA=(R7),LEN=64
         LA    R0,15         MAKE MASK                          GP97261
         NR    R4,R0         KEEP ONLY LOW NIBBLE (START REG)   GP96101
         NR    R5,R0           END REGISTER                     GP96101
         LR    R1,R4         COPY FOR TEST
         OR    R1,R5         IS LOW SAME AS HIGH AND = 0 ?      GP96101
         BNZ   DOREGWRP      NO                                 GP96101
         BCTR  R5,0          MAKE RANGE OF 15 REGISTERS         GP96101
         NR    R5,R0         CLEAN IT UP                        GP96101
DOREGWRP SLDL  R4,2          CONVERT REGISTER NUMBER TO OFFSET  GP96101
COMREGS  BASR  R6,0          RETURN POINT FOR SECOND LINE
         SAC   0             OUR WORK AREA IN PRIMARY
         STOMP DATA,DATA=(R7),LEN=64
         LR    R1,R4
         SRL   R1,2          CONVERT LOW REGISTER TO INDEX
         LA    R1,TABHEXTB(R1)   POINT TO PRINTABLE EQUIVALENT  GP97265
         MVC   DBWMGFLG+1(1),0(R1) IDENTIFY IT                  GP97265
         LA    R2,DBWMGTXT   POINT TO OUTPUT AREA
         LA    R15,1         ONE GROUP PER LINE
         TM    DBWFLAGS,DBWFGWID WIDE ?
         BZ    COMREGGP      NO
         LA    R15,2         TWO GROUPS PER LINE
COMREGGP LA    R14,4         FOUR ENTRIES PER GROUP
         SR    R3,R3         END OF REQUEST FLAG TO DBTPRINT
COMREGLP L     R0,0(R4,R7)   GET REGISTER CONTENTS
         ST    R0,DBWDB2     STASH
         UNPK  0(9,R2),DBWDB2(5) UNPACK
         TR    0(8,R2),TABHEXTR  MAKE PRINTABLE
         MVI   8(R2),C' '    CLEAR GARBAGE BYTE
         LA    R2,9(,R2)     INCREMENT
         CR    R4,R5         DONE ?
         BE    DBTPRINT      YES; QUIT
         LA    R4,4(,R4)     GO TO NEXT REGISTER
         N     R4,=X'0000003C'  WRAP-AROUND
         BCT   R14,COMREGLP  DO NEXT REGISTER
         LA    R2,1(,R2)     MAKE GROUP SEPARATOR
         BCT   R15,COMREGGP  DO NEXT GROUP
         LA    R3,1          REQUEST RETURN AFTER LINE PRINT
         B     DBTPRINT
         SPACE 1
*---------------------------------------------------------------------*
*                                                                     *
*  LONGREGS - COMMON REGISTER FORMATTING FOR 64-BIT ENTRIES           *
*                                                                     *
*     4 - START OFFSET (REGISTER NUMBER * 4)                          *
*     5 - END OFFSET   (REGISTER NUMBER * 4)                          *
*     7 - ADDRESS OF REGISTER SAVE AREA                               *
*                                                                     *
*---------------------------------------------------------------------*
REGLONG  STOMP DATA,DATA=(R7),LEN=128
         LA    R0,15         MAKE MASK                          GP97261
         NR    R4,R0         KEEP ONLY LOW NIBBLE (START REG)   GP96101
         NR    R5,R0           END REGISTER                     GP96101
         LR    R1,R4         COPY FOR TEST
         OR    R1,R5         IS LOW SAME AS HIGH AND = 0 ?      GP96101
         BNZ   REGLNWRP      NO                                 GP96101
         BCTR  R5,0          MAKE RANGE OF 15 REGISTERS         GP96101
         NR    R5,R0         CLEAN IT UP                        GP96101
REGLNWRP SLDL  R4,3          CONVERT REGISTER NUMBER TO OFFSET  GP96101
LONGREGS BASR  R6,0          RETURN POINT FOR SECOND LINE
         SAC   0             OUR WORK AREA IN PRIMARY
         STOMP DATA,DATA=(R7),LEN=128
         LR    R1,R4
         SRL   R1,3          CONVERT LOW REGISTER TO INDEX
         LA    R1,TABHEXTB(R1)   POINT TO PRINTABLE EQUIVALENT  GP97265
         MVC   DBWMGFLG+1(1),0(R1) IDENTIFY IT                  GP97265
         LA    R2,DBWMGTXT   POINT TO OUTPUT AREA
         LA    R15,1         ONE GROUP PER LINE
         TM    DBWFLAGS,DBWFGWID WIDE ?
         BZ    LONGRGGP      NO
         LA    R15,2         TWO GROUPS PER LINE
LONGRGGP LA    R14,2         TWO ENTRIES PER GROUP
         SR    R3,R3         END OF REQUEST FLAG TO DBTPRINT
LONGRGLP LD    R0,0(R4,R7)   GET REGISTER CONTENTS
         STD   R0,DBWDB2     STASH
         UNPK  0(9,R2),DBWDB2(5) UNPACK
         TR    0(8,R2),TABHEXTR  MAKE PRINTABLE
         MVI   8(R2),C'-'    SHOW SPLIT
         UNPK  9(9,R2),DBWDB2+4(5) UNPACK
         TR    9(8,R2),TABHEXTR  MAKE PRINTABLE
         MVI   17(R2),C' '   CLEAR GARBAGE BYTE
         LA    R2,18(,R2)     INCREMENT
         CR    R4,R5         DONE ?
         BE    DBTPRINT      YES; QUIT
         LA    R4,8(,R4)     GO TO NEXT REGISTER
         N     R4,=X'00000078'  WRAP-AROUND
         BCT   R14,LONGRGLP  DO NEXT REGISTER
         LA    R2,1(,R2)     MAKE GROUP SEPARATOR
         BCT   R15,LONGRGGP  DO NEXT GROUP
         B     DBTMORE
         SPACE 1                                                GP99113
***********************************************************************
**                                                                   **
**  REQ 04 - PROCESS ADDRESS REQUEST - SHOW ADDRESS & MODULE         **
**                                                                   **
***********************************************************************
FORMADDR SAC   0             GET INTO PRIMARY MODE
         MVC   DBWMGTXT(L'ADDMSG),ADDMSG
         INHEX DBWMGTXT+10,REQADDR,2   GET S FORM OF ADDRESS
         MVC   DBWMGTXT+9(1),DBWMGTXT+10
         MVI   DBWMGTXT+8,C'('
         MVI   DBWMGTXT+10,C')' PRETTIFY BASE REGISTER
         INHEX DBWMGTXT+15,DBWSPIAD,4  USER'S PASSED ADDRESS
         N     R2,DBWADMSK   KILL AMODE BIT (ALREADY DONE?)
         ST    R2,DBWDB2     MAKE ARGUMENT
         CSVQUERY INADDR=DBWDB2,SEARCH=JPALPA,OUTEPNM=DBWMGTXT+32,     *
               OUTLOADPT=DBWDB,OUTLENGTH=DBWDB+4,MF=(E,DBWPK16)
         BXLE  R15,R15,ADDLOAD
         NUCLKUP BYADDR,ADDR=(R2),NAME=DBWMGTXT+32
         BXH   R15,R15,ADDNONE   TOO BAD
         NUCLKUP BYNAME,NAME=DBWMGTXT+32,ADDR=(R3)
         BXH   R15,R15,ADDNONE   NOT FOUND (UNEXPECTED)
         ST    R3,DBWDB      SAVE IT
ADDLOAD  L     R3,DBWDB      GET START (LOAD) ADDRESS
         N     R3,DBWADMSK   CLEAN IT, JUST IN CASE
         SR    R2,R3         GET OFFSET
         BNP   ADDNOOFF      NO OFFSET
         MVI   DBWMGTXT+40,C'+'
         ST    R2,DBWDB
         INHEX DBWMGTXT+41,DBWDB,4 SHOW OFFSET
         LADJ  DBWMGTXT+41,8,MASK=C'0'
         B     DBTNOMOR      ALL FOR NOW
         SPACE 1
ADDNONE  MVC   DBWMGTXT+32(6),=C'*NONE*'
ADDNOOFF B     DBTNOMOR      ALL FOR NOW
ADDMSG   DC    C'ADDRESS=(X)XXX XXXXXXXX  MODULE' XXXXXXXX+XXXXXXXX'
         SPACE 1                                                GP99113
***********************************************************************
**                                                                   **
**  REQ 05 - SHOW SYSTEM INFORMATION BY SUBCODE                      **
**                                                                   **
***********************************************************************
FORMINFO SAC   0             GET INTO PRIMARY MODE
         MVC   DBWMGTXT(21),=C'SYS INFO NOT DONE YET'
         B     DBTNOMOR      ALL FOR NOW
         SPACE 1                                                GP99113
***********************************************************************
**                                                                   **
**  REQ 06 - EXECUTE A REMOTE PARM LIST  (ADDRESS IN R2)             **
**                                                                   **
***********************************************************************
EXECLIST ICM   R4,15,DBWEXECZ  GET LAST ELEMENT ON STACK
         BNZ   EXECLSNO
         LTR   R2,R2         ANY ADDRESS ?
         BZ    DBTNEXT       NO; TREAT AS NO-OP
         TM    REQTYPE,RQTEOL  IS THIS THE LAST ENTRY ?
         BNZ   EXECLSNP      YES; NO PUSH, JUST GOTO
         LA    R14,4
         LNR   R14,R14       NEGATIVE INCREMENT
         LA    R1,DBWEXECZ-4
         LA    R15,DBWEXECS-4
EXECLSP  L     R0,0(,R1)
         ST    R0,4(,R1)     PUSH DOWN ONE
         BXH   R1,R14,EXECLSP
         LR    R1,R9         COPY CURRENT REQUEST POINTER
         AH    R1,DBWPRMLN   POINT TO NEXT REQUEST
         ST    R1,DBWEXECS   PUSH ON STACK
EXECLSNP LR    R9,R2         GET NEW REQUEST LIST
         B     LOOPREQ       AND PROCESS IT
EXECLSNO MVC   DBWMGTXT(32),=C'EXEC STACK FULL; REQUEST IGNORED'
         B     DBTNOMOR      ALL FOR NOW
         SPACE 1                                                GP99113
***********************************************************************
**                                                                   **
**  REQ 07 - CHAIN CHASING - FORMAT EACH ELEMENT IN HEX              **
**                                                                   **
***********************************************************************
PROCHAIN ST    R9,DBWCHNPT   PRESERVE CHAIN POINTER
         XC    DBWCHNCT,DBWCHNCT  CLEAR COUNTER
CHAINTST STM   R2,R3,DBWCHNAD
         LA    R0,CHAINLUP   SET RETURN ADDRESS AFTER HEX
         ST    R0,DBWCHAIN   COME BACK HERE
         INCH  DBWCHNCT      COUNT ELEMENTS ON CHAIN
         CLC   DBWMGLBL(8),DBWMGLBL-1 IS THERE A LABEL ?
         BNE   CHAINTNM      YES; KEEP IT
         MVC   DBWMGLBL(3),=C'EL#'
         INHEX DBWMGLBL+4,DBWCHNCT,2 SHOW BLOCK NUMBER
CHAINTNM LTR   R2,R2         ANY MORE TO DO ?
         BNZ   COMHEXTD      DUAL HEX
         MVC   DBWMGTXT(18),=C'** END OF CHAIN **'
         SR    R3,R3         NO RETURN
         ST    R3,DBWCHAIN   NO MORE ELEMENTS
         B     DBTPRINT      ALL FOR NOW
CHAINLUP LM    R2,R3,DBWCHNAD  RESTORE PRIOR ELEMENT'S ADDRESS/LENGTH
         L     R5,DBWCHNPT   GET ORIGINAL REQUEST POINTER
         IC    R4,REQLKOFF-REQWORK(R5)   GET POINTER OFFSET
         AR    R2,R4         LOCATION OF POINTER
         CLI   REQLKLEN-REQWORK(R5),3  IS POINTER LENGTH 3 (AM24)?
         BE    CHAINE24      YES
         L     R2,0(,R2)     GET NEXT CHAIN ENTRY
         N     R2,DBWADMSK   JUST IN CASE
         B     CHAINTST      DO IT
CHAINE24 ICM   R2,7,0(R2)    GET NEXT CHAIN ENTRY
         ICM   R2,8,=X'00'   ENSURE CLEAR HIGH
         B     CHAINTST      DO IT
         SPACE 1                                                GP97265
***********************************************************************
**                                                                   **
**  REQ 11 - FORMAT PACKED DECIMAL                                   **
**           PERFORM VALIDITY CHECK. MAKE SURE LENGTH                **
**           MATCHES USER'S REQUEST IF NON-ZERO                      **
**                                                                   **
***********************************************************************
         SPACE 1                                                GP99113
FORMPACK STOMP CONV
         MVI   DBWMGFLG,C'P'-X'40' SHOW PACKED
         MVI   DBWMGFLG+1,C'L' PROV. SET BAD LENGTH
COMPACK  LA    R4,16         SET MAXIMUM LENGTH                 GP96081
         CR    R3,R4         IS USER'S LENGTH VALID?            GP96081
         BH    COMFMHEX      NO; FORMAT HEX WITH 'L' FLAG       GP96081
         MVI   DBWMGFLG+1,C'P' FLAG BAD PACKED (PRELIM)
         LR    R5,R2         SAVE OVER TRT                      GP00197
         TRT   0(16,R2),TRTPACK  CHECK FOR VALID PACKED DECIMAL GP00197
         LR    R0,R2         SAVE INSERTION                     GP00197
         LR    R2,R5         PUT IT BACK                        GP00197
         BZ    COMFMHEX      NO SIGN; INVALID                   GP00197
         CLM   R0,1,TRTPACK+X'0F'  TERMINATED BY VALID SIGN?    GP00197
         BNE   COMFMHEX      NO                                 GP00197
         LA    R5,1(,R1)     GET END ADDRESS + 1                GP00197
         SR    R5,R2         LESS START= LENGTH                 GP00197
         LR    R15,R1        SAVE SIGN LOCATION                 GP00197
         SPACE 1                                                GP96081
*  COPY NUMBER TO MAXIMUM LENGTH PACKED WORK AREA AND CHECK SIGN
*    NOTE THAT SIGN IS STILL IN R15                             GP96081
         LTR   R3,R3         DID USER SPECIFY AN INPUT LENGTH?  GP96081
         BZ    COMPACK0      NO; USE OURS                       GP96081
         MVI   DBWMGFLG+1,C'L' PROVISIONALLY FLAG BAD LENGTH AGAIN
         CR    R3,R5         SAME AS USER'S ?                   GP96081
         BNE   COMFMHEX      NO; FORMAT AS HEX                  GP96081
COMPACK0 BCTR  R5,0          EXECUTE LENGTH                     GP96081
         XC    DBWPK16,DBWPK16 WORK SPACE
         LA    R7,DBWPK16+L'DBWPK16-1                           GP96081
         SR    R7,R5         LOCATION TO MOVE TO                GP96081
         EX    R5,COMPACKM   MOVE TO WORK SPACE                 GP96081
         MVC   DBWMGTXT-1(L'DBTPAMSK),DBTPAMSK                  GP96081
         LA    R1,DBWMGTXT-1+L'DBTPAMSK-1 POINT TO SIGNIFICANCE
         EDMK  DBWMGTXT-1(L'DBTPAMSK),DBWPK16 FORMAT
         BCTR  R1,0          INSIGNIFICANT LOCATION             GP96081
         MVC   0(1,R1),0(R15)  SET HEX SIGN (0A-0F)             GP00197
         NI    0(R1),X'0F'   STRIP ZONES
         TR    0(1,R1),COMPACKS-X'0A'   PRINTABLE SIGN
         MVI   DBWMGFLG+1,C' ' RESET ERROR INDICATOR
         CLI   0(R1),C'?'    UNUSUAL SIGN?                      GP96081
         BNE   DBTNOMOR      PRINT THIS LINE                    GP96081
         MVI   DBWMGTXT-1,C' '                                  GP96081
         MVC   DBWMGTXT,DBWMGTXT-1 CLEAR TEXT PORTION AGAIN     GP96081
         MVI   DBWMGFLG+1,C'S' SHOW BAD SIGN AND DO AS HEX      GP97265
         B     COMFMHEX                                         GP97266
         SPACE 1
COMPACKM MVC   0(0,R7),0(R2)  RIGHT ADJUST USER'S NUMBER        GP96081
*    PACKED SIGN ABCDEF
COMPACKS DC    C'??+-? '     CONVERT SIGN                       GP96081
DBTPAMSB DC    C' ',X'206B',9X'2020206B',X'202120'  BLANK FILL
DBTPAMSK EQU   DBTPAMSB,*-DBTPAMSB,C'X'
         SPACE 1                                                GP97265
***********************************************************************
**                                                                   **
**  REQ 12 - FORMAT SIGNED INTEGER                                   **
**                                                                   **
***********************************************************************
         SPACE 1
FORMINTG STOMP DATA          SET FOR EPSIE
         MVI   DBWMGFLG,C'I'-X'40' FLAG INTEGER
         MVI   DBWMGFLG+1,C'L' PROV. FLAG BAD LENGTH
         TM    0(R2),X'80'   IS INPUT POSITIVE?
         BZ    FORMAINT      YES; USE SAME CODE AS ABSOLUTE
         OI    DBWFLAGS,DBWFGNEG INDICATE IT'S NEGATIVE
         XC    DBWDB2,DBWDB2 CLEAR
         LTR   R3,R3         LOAD AND TEST INPUT LENGTH
         BNP   DBTPRINT      INVALID
         CH    R3,=Y(L'DBWDB2) NOT TOO LONG?
         BH    COMFMHEX      TOO LONG; DO AS HEX
         LA    R15,DBWDB2+L'DBWDB2 POINT PAST HOLD FIELD
         SR    R15,R3        MAKE DESTINATION ADDRESS
         BCTR  R3,0          MAKE EXECUTE LENGTH
         EX    R3,MOV12F     MOVE TO WORK AREA
         EX    R3,CMP12F     MAKE ONE'S COMPLEMENT
         SAC   0             DONE WITH USER'S ADDRESSING
         LM    R14,R15,DBWDB2 LOAD IT
         AL    R15,=F'1'     MAKE TWO'S COMPLEMENT
         BC    12,FORMINTC   NO CARRY
         AL    R14,=F'1'     PROPAGATE CARRY
FORMINTC STM   R14,R15,DBWDB2 STASH IT BACK
         NI    DBWDB2,X'7F'  KILL OVERFLOW BIT
         B     COMAINT       COMMON INTEGER PROCESSING
         SPACE 1
***********************************************************************
**                                                                   **
**  REQ 13 - FORMAT UNSIGNED INTEGER                                 **
**                                                                   **
***********************************************************************
         SPACE 1
FORMAINT MVI   DBWMGFLG+1,C'L' PROV. SET BAD LENGTH
         MVI   DBWMGFLG,C'I'-X'40' FLAG INTEGER
         STOMP DATA
         XC    DBWDB2,DBWDB2 CLEAR FOR MOVE
         LTR   R3,R3         LOAD AND TEST INPUT LENGTH
         BNP   DBTPRINT      INVALID
         CH    R3,=Y(L'DBWDB2) NOT TOO LONG?
         BH    COMFMHEX      TOO LONG - FORMAT AS HEX
         LA    R15,DBWDB2+L'DBWDB2 POINT PAST HOLD FIELD
         SR    R15,R3        MAKE DESTINATION ADDRESS
         BCTR  R3,0          MAKE EXECUTE LENGTH
         EX    R3,MOV12F     MOVE TO WORK AREA
         SAC   0             DONE WITH USER'S ADDRESSING
COMAINT  ZAP   DBWPK16A,=P'0' CLEAR HOLD FIELD
         MVI   DBWMGFLG+1,C' ' RESET ERROR FLAG
COMIHLN  LM    R14,R15,DBWDB2 LOAD ENTIRE VALUE
         LA    R0,3          LOOP FOR THREE TIMES 24 BITS (ARBITRARY)
         LA    R4,PSCALE     POINT TO SCALE FACTORS
COMILY   LR    R5,R15        COPY LOW ORDER BITS
         N     R5,=X'00FFFFFF'  MASK ONLY WANTED BITS
         SRDL  R14,24        SHIFT FOR NEXT TIME
         CVD   R5,DBWDB      CONVERT TO DECIMAL
         ZAP   DBWPK16B,0(L'PSCALE,R4) COPY MULTIPLIER
         MP    DBWPK16B,DBWDB SCALE
         AP    DBWPK16A,DBWPK16B ACCUMULATE
         LA    R4,L'PSCALE(,R4)  NEXT MULTIPLIER
         BCT   R0,COMILY     REPEAT
         LA    R2,DBWPK16A   POINT TO INTERMEDIATE FIELD
         LA    R3,L'DBWPK16A LENGTH
         TM    DBWFLAGS,DBWFGNEG IS AMOUNT NEGATIVE?
         BZ    COMPACK       NO
         OI    DBWPK16A+L'DBWPK16A-1,X'0D' MAKE NEGATIVE
         B     COMPACK       JOIN COMMON CODE
MOV12F   MVC   0(0,R15),0(R2)  MOVE INPUT
CMP12F   XC    0(0,R15),=8X'FF'   GET ONE'S COMPLEMENT
PSCALE   DC    PL8'1,16777216,281474976710656'
         SPACE 1
***********************************************************************
**                                                                   **
**  REQ 09 - CONDITIONAL TEXT - IF PRINTABLE, TEXT, ELSE HEX         **
**                                                                   **
***********************************************************************
         SPACE 1                                                GP99113
FORMCHEX MVI   DBWMGFLG,C'C'-X'40'                              GP97266
COMTCHEX LTR   R15,R3        COPY LENGTH                        GP96101
         BNP   FORMTEXT      INVALID; GET OUT                   GP96101
         BCTR  R15,0         SET FOR EXECUTE                    GP96101
         LR    R4,R2         SAVE                               GP96101
         EX    R15,CHEXTRT   IS THIS A PRINTABLE TEXT STRING?   GP96101
         LR    R2,R4         RESTORE                            GP96101
         BZ    FORMTEXT      YES; PRINT AS TEXT                 GP96101
         OI    DBWFLAGS,DBWFGBOT ENABLE DUAL HEX/TEXT DISPLAY   GP00162
         B     FORMHEXT      ELSE FORMAT IN HEX                 GP96101
CHEXTRT  TRT   0(0,R2),TRTCHAR  CHECK FOR PRINTABLES            GP96101
         SPACE 1                                                GP99113
***********************************************************************
**                                                                   **
**  REQ 10 - FORMAT HEXADECIMAL TEXT                                 **
**                                                                   **
***********************************************************************
         SPACE 1                                                GP96101
FORMHEXT MVI   DBWMGFLG+1,C'H'-X'40' MAKE LOWER CASE H          GP97266
COMHEXTD OI    DBWFLAGS,DBWFGBOT ENABLE DUAL HEX/TEXT DISPLAY   GP00162
COMFMHEX BASR  R6,(0)        SET RETURN ADDRESS                  92271
         STOMP CONV
         SLR   R4,R4
         LA    R1,DBWMGTXT   POINT TO TEXT
         LA    R5,16         MAX INPUT CHARACTERS ON LINE        92093
         TM    DBWFLAGS,DBWFGWID DOING WIDE?                    GP99113
         BZ    COMFMHX3      NO                                 GP99113
         LA    R5,32         PRINT TWO GROUPS                   GP99113
         TM    DBWFLAGS,DBWFGBOT DUAL MODE REQUEST?             GP00162
         BZ    COMFMHX3      NO                                 GP00162
         LTR   R3,R3         IS LENGTH VALID ?                  GP00162
         BNP   COMFMHX3      NO                                 GP00162
         LA    R15,L'DBWMGHTX MAX LENGTH ON ONE LINE            GP00162
         MIN   (R15),(R3)    USE SHORTER                        GP00162
         BCTR  R15,0         SET FOR EXECUTE                    GP00162
         EX    R15,EXMVCHTX  MOVE TO OUTPUT LINE                GP00162
         EX    R15,EXTRHTX   AND MAKE IT PRINTABLE              GP00162
         MVI   DBWMGHTX-1,C'*' FRAME THE TEXT                   GP00162
         MVI   DBWMGHTX+L'DBWMGHTX,C'*'                         GP00162
COMFMHX3 LTR   R3,R3         VALID LENGTH?                      GP96081
         BP    COMFMHXL      YES                                GP96081
         MVC   DBWMGTXT(3),=C'L=?'  MAKE AN ERROR MESSAGE       GP96081
COMFMHXL LTR   R3,R3         ANY MORE TO DO ?
         BNP   DBTPRINT
         MVC   DBWDB(1),0(R2) MOVE SINGLE BYTE                  GP98222
         UNPK  0(3,R1),DBWDB(2) UNPACK ONE BYTE (NO 0C4)        GP98222
         TR    0(2,R1),TABHEXTR   MAKE IT PRINTABLE             GP97265
         MVI   2(R1),C' '    BLANK NEXT
         LA    R2,1(,R2)
         LA    R1,2(,R1)
         BCTR  R3,0          ADJUST RESIDUAL COUNT
         LA    R4,1(,R4)     ADD ONE TO COUNT DONE
         LA    R15,3
         NR    R15,R4        END OF A WORD ?
         BNZ   *+8           NO
         LA    R1,1(,R1)     LEAVE A GAP BETWEEN WORDS
         CR    R4,R5         DONE ONE LINE ?                     92093
         BNL   DBTPRINT      YES; PROCESS IT
         CH    R4,=H'16'     DONE ONE GROUP?                    GP99113
         BNE   COMFMHXL      NO; NOT YET, OR NEVER              GP99113
         LA    R1,1(,R1)     LEAVE A LARGER GAP BETWEEN GROUPS  GP99113
         B     COMFMHXL      SEE IF MORE TO DO
EXMVCHTX MVC   DBWMGHTX(0),0(R2) COPY USER'S TEXT               GP00162
EXTRHTX  TR    DBWMGHTX(0),TRTABTN  LIMIT TO PRINTABLE STUFF    GP00162
         SPACE 1                                                GP97266
***********************************************************************
**                                                                   **
**  REQ 08 - FORMAT PRINTABLE TEXT (UNPRINTABLES ARE BLANKED)        **
**                                                                   **
***********************************************************************
FORMTEXT MVI   DBWMGFLG+1,C'T'-X'40' L.C. T                     GP97266
COMFTEXT LA    R4,L'DBWMGTXT SET MAXIMUM TEXT PRINTABLE
         TM    DBWFLAGS,DBWFGWID DOING WIDE?                    GP99113
         BZ    COMFTXT3      NO                                 GP99113
         LA    R4,2*(1+L'DBWMGTXT) WIDER IS BETTER?             GP99113
COMFTXT3 LTR   R3,R3         VALID LENGTH?                      GP96081
         BP    COMFTXTL      YES                                GP96081
         MVC   DBWMGTXT(3),=C'L=?'  MAKE AN ERROR MESSAGE       GP96081
COMFTXTL BASR  R6,(0)        SET RETURN POINT                    92271
         STOMP CONV
         LTR   R3,R3         ANY MORE ?
         BNP   DBTPRINT      NO; EXIT
         LR    R15,R4        SET TEXT LENGTH
         CR    R3,R15        IS REQUEST LONGER ?
         BNL   *+6           YES
         LR    R15,R3        ELSE USE SHORTER
         BCTR  R15,0         SET EXECUTE LENGTH
         EX    R15,DBTFTMVC  MOVE TO LINE
         EX    R15,DBTFTTR   MAKE PRINTABLE                     GP00162
         LA    R1,DBWMGTXT+1(R15) GET END OF LINE+1             GP00162
         MVI   0(R1),C'<'    SHOW WHERE TEXT ENDS               GP00162
         AR    R2,R4         UP TEXT ADDRESS
         SR    R3,R4         SET RESIDUAL PRINT LENGTH
         B     DBTPRINT      PRINT IT
DBTFTMVC MVC   DBWMGTXT(0),0(R2) MOVE USER'S TEXT
DBTFTTR  TR    DBWMGTXT(0),TRTABTN LIMIT TO PRINTABLES          GP00162
         SPACE 1                                                GP99113
***********************************************************************
**                                                                   **
**  REQ 16 - PROCESS FLAG REQUEST - SHOW VARIABLE, THEN TEXT         **
**                                                                   **
***********************************************************************
         PUSH  USING
FORMFLAG MVI   DBWMGFLG,C'F'-X'40'
         STOMP DATA
         MVI   DBWMGFLG+1,C'L' SET BAD LENGTH
         LTR   R3,R3         TEST LENGTH
         BNP   DBTPRINT      NOTHING USABLE - QUIT
         CH    R3,=Y(L'DBWDB3) NOT TOO LONG ?
         BH    COMFMHEX      TOO LONG, TOO BAD - DO AS HEX
         BCTR  R3,0          MAKE EXECUTE LENGTH
         EX    R3,EXMVCDB3   MOVE TO MY WORK SPACE
         MVI   DBWMGFLG+1,C' ' RESET BAD LENGTH
         LA    R5,1(R3,R3)   TEXT LENGTH
         INHEX DBWPK16,DBWDB3,4 UNPACK LEFT HALF
         INHEX DBWPK16+8,DBWDB3+4,4 UNPACK RIGHT HALF
         EX    R5,EXMVCTXT   MOVE TO DBWMGTXT
         SPACE 1
         LA    R5,DBWMGTXT+3(R5) START OF OUTPUT
         USING FGTENT,R4     MAP INPUT TABLE
         SR    R6,R6         CLEAR TEXT LENGTH FLAG
FFGLOOP  ICM   R6,1,FGTLEN   END OF TABLE ?
         BM    DBTNOMOR      YES; ALL DONE
         EX    R3,EXZERMSK   IS THIS A NULL MASK ?
         BNZ   FFGTEST       NO; TEST FOR MATCH
         TM    DBWFLAGS,DBWFGNEG DID WE HAVE A PRIOR MATCH ?
         BZ    FFGMOVE       NO; MOVE THIS ONE
         NI    DBWFLAGS,255-DBWFGNEG RESET HIT FLAG
         B     FFGNEXT       AND BUMP
         SPACE 1
FFGTEST  MVC   DBWDB,DBWDB3  MOVE COPIED FLAGS TO TEST AREA
         EX    R3,EXANDMSK   AND MASK BITS
         EX    R3,EXCLCMSK   IS THIS A MATCH ?
         BNE   FFGNEXT       NO, TRY NEXT ONE
         OI    DBWFLAGS,DBWFGNEG SHOW MATCH
         SPACE 1
FFGMOVE  LA    R0,DBWMGRD2-1-1   END OF TEXT AREA
         SR    R0,R6
         CR    R5,R0         WILL IT FIT ?
         BNH   FFGMOVER
         MVI   DBWMGFLG+1,C'L' SHOW BAD LENGTH
         B     DBTNOMOR      AND QUIT THIS ONE
FFGMOVER LAE   R14,FGTTEXT(R3)  POINT TO TEXT
         EX    R6,EXMVCR14   MOVE TO OUTPUT AREA
         CPYA  R14,R1        CLEAR ACR14 AGAIN
         MVC   DBWDB,=8X'FF' PREPARE TO COMPLIMENT
         EX    R3,EXEXODB    COMPLEMENT TEST BITS
         NC    DBWDB3,DBWDB  AND TURN OFF
         LA    R5,2(R6,R5)   NEXT OUTPUT POSITION
         SPACE 1
FFGNEXT  LA    R4,3(R3,R4)   POINT TO NEXT INPUT TABLE POSITION
         AR    R4,R6           PLUS TEXT LENGTH
         B     FFGLOOP       TEST FOR END OR NEXT
         SPACE 1
EXEXODB  XC    DBWDB(0),FGTMASK COMPLEMENT MASK BITS
EXMVCTXT MVC   DBWMGTXT(0),DBWPK16 MOVE FROM WORK AREA
EXMVCR14 MVC   DBWMGTXT-DBWMGTXT(0,R5),0(R14) MOVE FROM TABLE
EXMVCDB3 MVC   DBWDB3(0),0(R2) MOVE USER'S VALUE
EXANDMSK NC    DBWDB(0),FGTMASK AND USER'S MASK
EXCLCMSK CLC   DBWDB(0),FGTMASK COMPARE RESULT TO USER'S MASK
EXZERMSK OC    FGTMASK(0),FGTMASK  NULL MASK ?
         POP   USING
         SPACE 1
FGTENT   DSECT ,
FGTLEN   DS    X             LENGTH-1 OF TEXT
FGTMASK  DS    X             MASK OF LENGTH (R3) {LATER (R3)-1}
FGTTEXT  DS    0C            VARIABLE LENGTH TEXT
         SPACE 1                                                GP99113
DEBTRACE CSECT ,
***********************************************************************
**                                                                   **
**  UNRECOGNIZED REQUEST CODE - SHOW AND TERMINATE                   **
**                                                                   **
***********************************************************************
DBTBADPR SAC   0             GET INTO PRIMARY MODE
         BAS   R6,DBTMORE    PRINT CURRENT LINE IF NOT EMPTY
         SAC   0             AND RESTORE PRIMARY MODE
         MVC   DBWMGLBL(L'SPEMSG03),SPEMSG03
         MVC   DBWMGLBL+L'SPEMSG03+1(15),=C'UNKNOWN REQUEST'
         INHEX DBWMGLBL+18,DBWSPIPM,4  SHOW PARM LIST ADDRESS
         INHEX DBWMGLBL+31,REQTYPE,1   PRIMARY REQUEST
         INHEX DBWMGLBL+34,REQFG,1     REQUEST MODIFIER FLAG
*  NOTE THAT OTHER FIELDS ARE IRRELEVANT
         LA    R0,DBTEXITS   POINT TO FINAL EXIT
         ST    R0,DBWCHAIN   AND DON'T RETURN
         B     DBTNOMOR
         SPACE 1                                                GP99113
***********************************************************************
**                                                                   **
**  ESPIE EXIT WAS ENTERED UNEXECTEDLY                               **
**    PRINT BASIC DIAGNOSTIC DATA AND CONTINUE/RETURN                **
**                                                                   **
***********************************************************************
SPEMRITE SAC   0             GET INTO PRIMARY MODE
         MVC   DBWESPPF(1),DBWSPIPF  PRESERVE FLAG OVER PRINT
         BAS   R6,DBTMORE    PRINT CURRENT LINE IF NOT EMPTY
         SAC   0             AND RESTORE PRIMARY MODE
         MVC   DBWMGHED(L'SPEMSG01),SPEMSG01
         INHEX DBWMGHED+22,DBWESPSW,4   SHOW PSW LEFT
         INHEX DBWMGHED+31,DBWESPSW+4,4 SHOW PSW RIGHT
         INHEX DBWMGHED+44,DBWESPIN,2   SHOW INTERRUPT CODE
         INHEX DBWMGHED+54,DBWESPIN+2,2 SHOW INTERRUPT CODE
         INHEX DBWMGHED+59,DBWESPIN+4,4 TRANSLATION ADDR
         BAS   R6,DBTMORE
         SAC   0             AND RESTORE PRIMARY MODE
         IC    R0,DBWESPPF   GET PROGRESS FLAG
         BIX   PFX=SPEM,ERR=SPEMLAST,  BRANCH BY TYPE                  *
               LOC=(,PARM,DATA,CONV)
         SPACE 1
SPEMPARM MVC   DBWMGLBL(L'SPEMSG02),SPEMSG02
         INHEX DBWMGLBL+26,DBWSPIPM,4   SHOW PARM LIST ADDRESS
         B     SPEMLAST
         SPACE 1
SPEMCONV DS    0H            SAME FOR NOW
SPEMDATA MVC   DBWMGLBL(L'SPEMSG03),SPEMSG03
         INHEX DBWMGLBL+18,DBWSPIPM,4   SHOW PARM LIST ADDRESS
         INHEX DBWMGLBL+31,REQTYPE,1    PRIMARY REQUEST
         INHEX DBWMGLBL+34,REQFG,1      REQUEST MODIFIER FLAG
         LH    R5,DBWPRMLN   GET REQUEST LENGTH
         SH    R5,=H'2'      LENGTH TWO ?
         BNP   SPEMDEND
         INHEX DBWMGLBL+37,REQADDR,2    DATA ADDRESS
         SH    R5,=H'2'      LENGTH FOUR ?
         BNP   SPEMDEND
         INHEX DBWMGLBL+42,REQLEN,2     DATA LENGTH
         SH    R5,=H'2'      LENGTH SIX ?
         BNP   SPEMDEND
         INHEX DBWMGLBL+47,REQLKOFF,2   TABLE ADDRESS/CHAIN OFFSET
SPEMDEND BAS   R6,DBTMORE
         SAC   0             AND RESTORE PRIMARY MODE
         MVC   DBWMGLBL(L'SPEMSG04),SPEMSG04
         INHEX DBWMGLBL+13,DBWSPIAD,4   SHOW DATA ADDRESS
         INHEX DBWMGLBL+30,DBWSPILN,4   SHOW DATA LENGTH
         B     SPEMLAST
         SPACE 1
SPEMLAST BAS   R6,DBTMORE
         ARMODE 0            AND RESTORE PRIMARY MODE
         MVC   DBWMGLBL,=CL8'REGS@ERR'
         LA    R7,DBWESPGR   POINT TO REGISTERS
         SR    R4,R4         START WITH R0
         LA    R5,15         THROUGH R15
         CLI   DBWESPPF,STFPARM  BAD PARM ?
         BH    REGSUB        NO; CONTINUE IF ONLY BAD DATA
         LA    R0,DBTEXITS   POINT TO FINAL EXIT
         ST    R0,DBWCHAIN   AND DON'T RETURN
         B     REGSUB        FORMAT REGISTERS
         SPACE 1
SPEMSG01 DC    C'PROGRAM INTERRUPT PSW=XXXXXXXX XXXXXXXX ILC=XXXX CODE=*
               XXXX XXXXXXXX'
SPEMSG02 DC    C'INVALID PARM LIST ADDRESS XXXXXXXX'
SPEMSG03 DC    C'PARM LIST ADDRESS XXXXXXXX REQ=XX XX XXXX XXXX'
SPEMSG04 DC    C'DATA ADDRESS XXXXXXXX; LENGTH XXXXXXXX '
         SPACE 1                                                GP99113
***********************************************************************
**                                                                   **
**  PROCESS THE CURRENT RECORD PER USER'S REQUEST                    **
**    SKIP COMPLETELY BLANK LINES                                    **
**                                                                   **
***********************************************************************
         SPACE 1                                                GP99113
DBTNOMOR SR    R3,R3         SET FLAG NOT TO RETURN
         B     DBTPRINT
         SPACE 1                                                GP99113
DBTMORE  LA    R3,1          SET FLAG TO RETURN TO CALLER
DBTPRINT SAC   0             RESET AR MODE                      GP00197
         CLC   DBWMGLBL(DBWMGRDC-DBWMGLBL),DBWMGLBL-1 ANYTHING 2 PRINT?
         BE    DONPRINT      NO; SKIP WHEN NO DATA              GP99113
         STOMP WRITE         NO RECOVERY ON 0CX DURING OUTPUT
         TM    DBUFLAG,DBUFLPUT  USE TPUT RATHER THAN WTO?      GP04052
         BNZ   DOTPUT        YES                                GP04052
         TM    DBUFLAG,DBUFLWTO  USE WTO RATHER THAN PRINT?     GP95240
         BNZ   DOWTO         YES                                GP95240
         IC    R0,DBUFGMOD   HOW TO PRINT?                      GP99113
         BIX   BASE=DEBTRACE,ERR=DOWTO,                                *
               LOC=(DOWTO,DOWTO,     0,1 - WTO                         *
               DOPUT,                2 - PUT TO OPEN DCB               *
               DOEXIT,               3 - CALL USER EXIT                *
               DOXPRINT,             4 - OLD EXORPRNT/XPRINT I/F       *
               DO@PRINT)             5 - NEW @PRINTER INTERFACE GP99113
         SPACE 1                                                GP99113
***********************************************************************
**                                                                   **
**  PRINT RECORD TO ANY DCB OPEN FOR QSAM OUTPUT                     **
**    (CURRENTLY DATA MODE IS NOT SUPPORTED)                         **
**                                                                   **
***********************************************************************
         SPACE 1                                                GP99113
DOPUT    LA    R1,DBWMGRDW   GET OUTPUT RECORD                  GP99113
         BAS   R14,PRTDEB    GO TO PUT ROUTINE                  GP99113
         B     DONPRINT      JOIN COMMON                        GP99113
         SPACE 1                                                GP99113
***********************************************************************
**                                                                   **
**  PASS RECORD TO SPECIFIED USER EXIT                               **
**                                                                   **
***********************************************************************
         SPACE 1                                                GP99113
DOEXIT   LA    R1,DBWMGRDW   GET OUTPUT RECORD                  GP99113
         L     R15,DBU@UDCB  GET USER EXIT                      GP99113
         SYNCH (R15),RESTORE=YES,AMODE=DEFINED  CALL USER EXIT  GP99113
         B     DONPRINT      JOIN COMMON                        GP99113
         SPACE 1                                                GP99113
***********************************************************************
**                                                                   **
**  CALL EXORPRNT (XPRINT) TO PROCESS THE RECORD                     **
**                                                                   **
***********************************************************************
         SPACE 1                                                GP99113
DOXPRINT LA    R1,DBWMGRDW   GET OUTPUT RECORD                  GP99113
         L     R15,DBU@UDCB  GET USER EXIT                      GP99113
         BASR  R14,R15       ANCIENT PRINT ROUTINE (CIRCA 1969) GP99113
         B     DONPRINT      JOIN COMMON                        GP99113
         SPACE 1                                                GP99113
***********************************************************************
**                                                                   **
**  WRITE RECORD WITH TPUT                                           **
**    NOTE THAT WE DO SKIP OVER THE CARRIAGE CONTROL CHARACTER       **
**                                                                   **
***********************************************************************
         SPACE 1                                                GP04052
DOTPUT   LH    R0,DBWMGRDW                                      GP04052
         SH    R0,=H'5'      LENGTH WITHOUT RDW AND CC          GP04052
         BNP   DONPRINT      NO SPACERS                         GP04052
         LA    R1,DBWMGRDW+5 POINT TO TEXT AFTER CC             GP04052
         TPUT  (1),(0),EDIT  SHOW USER                          GP04052
         B     DONPRINT      JOIN COMMON                        GP04052
         SPACE 1                                                GP99113
***********************************************************************
**                                                                   **
**  PASS THE RECORD TO @PRINTER (USE PRTV TYPE ENTRY)                **
**                                                                   **
***********************************************************************
         SPACE 1                                                GP99113
DO@PRINT PRTV  DBWMGRDW,DEV==DBUF@DEV PRINT ON REQUESTED FILE   GP95235
         BXLE  R15,R15,DONPRINT IF IT FAILS, USE WTO            G922845
         SPACE 1                                                GP99113
***********************************************************************
**                                                                   **
**  WRITE RECORD WITH WTO                                            **
**    NOTE THAT WE DON'T REMOVE THE CARRIAGE CONTROL CHARACTER       **
**                                                                   **
***********************************************************************
         SPACE 1                                                GP99113
DOWTO    WTO   MF=(E,DBWMGRDW) WRITE TO CONSOLE                  95067
         SPACE 1                                                GP99113
***********************************************************************
**                                                                   **
**  DONE PRINTING.                                                   **
**                                                                   **
**  1) WHEN R3 IS NON-ZERO (RESIDUAL INPUT LENGTH), CONTINUE PROCESS **
**                                                                   **
**  2) BUMP ITEM POINTER (R9) AND FORMAT IT IF VALID                 **
**                                                                   **
**  OTHERWISE CLEAN UP AND RETURN TO CALLER                          **
**                                                                   **
***********************************************************************
         SPACE 1                                                GP99113
DONPRINT STOMP RESET         NO ERROR RECOVERY
         MVC   DBWMGRDW(5),DBWSVRDW REFRESH RDW/FLAGS           GP99113
         MVI   DBWMGLBL-1,C' ' JUST IN CASE
         LA    R15,DBWMGRDC-DBWMGLBL SHORT BLANK
         TM    DBWFLAGS,DBWFGWID WIDE OUTPUT ?
         BZ    DONPRINTB     NO
         LA    R15,DBWMGRD2-DBWMGLBL WIDE BLANK
DONPRINTB EX   R15,EXBLANK  BLANK TEXT AREA
         MVC   DBWMGHED,DBWMGLBL CLEAR MESSAGE NUMBER            93357
         MVI   DBWMGHED+5,C'+' INDICATE CONTINUATION             93357
         SPACE 1                                                GP99113
         LTR   R3,R3         ANY MORE TO DO ?                   GP95235
         BNP   DBTCHAIN      NO; DONE                           GP00197
         LAM   R5,R1,=16F'0'  CLEAN UNWANTED ACCESS REGISTERS
         ICM   R14,2,DBWACRMD  SET ACR MODE
         SAC   0(R14)        GET INTO USER'S MODE
         BR    R6            AND RESTART                        GP00197
EXBLANK  MVC   DBWMGLBL(0),DBWMGLBL-1 BLANK IT
         SPACE 1
DBTCHAIN ICM   R15,15,DBWCHAIN  ADDITIONAL DATA FOR THIS ?
         BZ    DBTNEXT       NO; TRY NEXT ENTRY
         XC    DBWCHAIN,DBWCHAIN  PREVENT LOOPING
         MVC   DBWMGHED,DBWMGLBL CLEAR MESSAGE NUMBER
         BR    R15           AND CONTINUE
         SPACE 1
DBTNEXT  TM    REQTYPE,RQTEOL  END OF LIST FLAG ?               GP02364
         BNZ   DBTEXITP      YES; STOP NOW                      GP02364
         AH    R9,DBWPRMLN   SKIP TO NEXT OPTION ENTRY
         B     LOOPREQ       AND DO IT
DBTEXITP ICM   R9,15,DBWEXECS  IS THERE A RECURSION ENTRY ?
         BZ    DBTEXITS      NO; QUIT
         MVC   DBWEXECS(DBWEXECZ-DBWEXECS),DBWEXECS+4  POP THE STACK
         XC    DBWEXECZ,DBWEXECZ  CLEAR LAST ENTRY
         B     LOOPREQ       RESUME FROM OLD ADDRESS
DBTEXITS TM    DBUFLAG,DBUFLEND  DID WE HAVE A CLOSE CALL ?     GP98222
         BZ    DBTEXITZ      NO                                 GP98222
         TM    DBWPRDCB+DCBOFLGS-IHADCB,DCBOFOPN  OPEN ?
         BZ    DBTEXITZ
         CLOSE MF=(E,DBWOPDCB) MODE=31  CLOSE THE DCB           GP03009
         FREEPOOL DBWPRDCB   FREE STORAGE
DBTEXITZ TM    DBWFLAGS,DBWFGSPI NEED TO RESET ESPIE ?
         BZ    DBTESKIP      NOT IF I DIDN'T ISSUE ONE
         ESPIE RESET,DBWTKSPI   RESET USER'S ESPIE REQUEST
DBTESKIP LAM   R0,R1,DBWSVACR     RESTORE USER'S ACCESS REGS
         LM    R0,R1,DBWSVUSR       AND PRIOR SAVED ONES
         LAM   R14,R15,DBWSVACR+14*4  RESTORE USER'S ACCESS REGS
         LM    R14,R15,DBWSVUSR+14*4    AND PRIOR SAVED ONES
         PR    ,             AND RETURN TO USER                 GP00197
         SPACE 2
***********************************************************************
**                                                                   **
**  PRTDEB :  SUBROUTINE TO WRITE THE CURRENT OUTPUT RECORD          **
**                                                                   **
***********************************************************************
         SPACE 1                                                GP99113
*  PRTDEB - PROCESS ONE PRINT LINE; TEXT ADDR IN R1             GP99113
*    CONTROL CHARACTERS, IF NEEDED, SET TO SINGLE SPACE         GP99113
*    RETURNS ON R14                                             GP99113
         PUSH  USING                                            GP99113
PRTDEB   STM   R14,R5,DBWPRTSV  SAVE REGISTERS                  GP99113
         L     R4,DBU@UDCB   LOAD DCB                           GP99113
         USING IHADCB,R4                                        GP99113
         LR    R2,R1         PRESERVE RECORD ADDRESS            GP99113
         LH    R3,0(,R2)     LOAD LENGTH                        GP99113
         SR    R15,R15                                          GP99113
         ICM   R15,3,DBWMXOLN  GET MAXIMUM TEXT LENGTH          GP99113
         BNZ   PRTNFRST                                         GP99113
         SPACE 1                                                GP99113
*---------------------------------------------------------------------*
*                                                                     *
*  SET DBWRCFMX AS A BRANCH INDEX:                                    *
*    0 IF RECFM IS FIXED OR UNDEFINED   8 IF VARIABLE OR D (ANSI VAR) *
*    4 IS ADDED WHEN THE DCB HAS CARRIAGE CONTROLS                    *
*                                                                     *
*---------------------------------------------------------------------*
         SR    R5,R5         SET FOR FB
         TM    DCBRECFM,DCBRECCC  ANY CONTROL CHARACTERS?       GP99113
         BZ    PRT1NCC                                          GP99113
         MVI   DBWCCHAR,C' ' ASA CODE - SINGLE SPACE            GP99113
         LA    R5,4          ADD ADJUSTMENT FOR CC
         TM    DCBRECFM,DCBRECCA  ASA ?                         GP99113
         BNZ   PRT1NCC                                          GP99113
         MVI   DBWCCHAR,X'09' MACHINE CODE - SINGLE SPACE       GP99113
PRT1NCC  LH    R15,DCBLRECL  GET MAXIMUM RECORD LENGTH          GP99113
         STH   R15,DBWMXOLN  SAVE IT                            GP99113
         TM    DCBRECFM,DCBRECU  RECFM=U ?                      GP99113
         BO    PRT1RECU      YES; PROCESS                       GP99113
         BM    PRTQRECF      MIXED; F OR V                      GP99113
         TM    DCBRECFM,DCBRECD  RECFM=D?                       GP99113
         BNZ   PRT1RECV      YES; TREAT AS V                    GP99113
PRT1RECU MVC   DBWMXOLN,DCBBLKSI  RECFM=U USES REC=BLK          GP99113
         B     PRT1RECF      SET FLAF FOR F                     GP99113
PRTQRECF TM    DCBRECFM,DCBRECF  IS IT FIXED?                   GP99113
         BNZ   PRT1RECF      YES                                GP99113
PRT1RECV LA    R5,8(,R5)     BRANCH ON V/D
         B     PRT1SETX                                         GP99113
PRT1RECF DS    0H            BRANCH ON F/U
PRT1SETX STC   R5,DBWRCFMX   SAVE PROCESSING BRANCH INDEX
         SPACE 1                                                GP99113
PRTNFRST MVC   DBWMGRDW+4(1),DBWCCHAR SET APPROPRIATE CARRIAGE CONTROL
         TM    DCBMACF2,DCBMRLCP  PUT LOCATE MODE?              GP99119
         BNZ   PRTLOC                                           GP99113
         SPACE 1                                                GP99113
PRTMOV   SR    R5,R5
         IC    R5,DBWRCFMX   GET PROCESSING INDEX
         B     *+4(R5)       BRANCH BY RECFM
         B     PRTMOVFB        FIXED
         B     PRTMOVFC        FIXED WITH CARRIAGE CONTROL
         B     PRTMOVVB        VARIABLE
         B     PRTMOVVC        VARIABLE WITH CARRIAGE CONTROL
PRTMOVFC LA    R2,4(,R2)     SKIP RDW
         SH    R3,=H'4'      ADJUST LENGTH
         B     PRTMOVFP      COMMON PUTTER
PRTMOVFB LA    R2,5(,R2)
         SH    R3,=H'5'      ADJUST LENGTH
         B     PRTMOVFP      COMMON PUTTER
PRTMOVVB LA    R2,1(,R2)     ADJUST FOR CC
         SH    R3,=H'1'      ADJUST LENGTH
PRTMOVVC MIN   R3,DBWMXOLN,TYPE=H  NOT TOO LONG
         STH   R3,0(,R2)     MAKE NEW RDW
         STCM  R3,12,2(R2)   AND ZERO FLAGS
PRTMOVFP PUT   (R4),(R2)     PUT THE RECORD
         B     PRINTSEX      GO TO EXIT                         GP99113
         SPACE 1                                                GP99113
PRTLOC   SR    R5,R5
         IC    R5,DBWRCFMX   GET PROCESSING INDEX
         B     *+4(R5)       BRANCH BY RECFM
         B     PRTLOCFB        FIXED
         B     PRTLOCFC        FIXED WITH CARRIAGE CONTROL
         B     PRTLOCVB        VARIABLE
         B     PRTLOCVC        VARIABLE WITH CARRIAGE CONTROL
PRTLOCFC LA    R2,4(,R2)     SKIP RDW
         SH    R3,=H'4'      ADJUST LENGTH
         B     PRTLOCFP      COMMON PUTTER
PRTLOCFB LA    R2,5(,R2)
         SH    R3,=H'5'      ADJUST LENGTH
         B     PRTLOCFP      COMMON PUTTER
PRTLOCVB LA    R2,1(,R2)     ADJUST FOR CC
         SH    R3,=H'1'      ADJUST LENGTH
PRTLOCVC MIN   R3,DBWMXOLN,TYPE=H  NOT TOO LONG
         STH   R3,0(,R2)     MAKE NEW RDW
         STCM  R3,12,2(R2)   AND ZERO FLAGS
         STH   R3,DCBLRECL   GET ONLY WHAT'S NEEDED
PRTLOCFP PUT   (R4)          GET A RECORD
         LR    R14,R1        SAVE RECORD ADDRESS
         LH    R15,DBWMXOLN  GET ORIGINAL LENGTH
         ICM   R3,8,=C' '    PAD WITH BLANKS
         MVCL  R14,R2        MOVE TO BUFFER
         B     PRINTSEX
PRINTSEX LM    R14,R5,DBWPRTSV  RESTORE                         GP99113
         BSM   0,R14         RETURN IN CALLER'S ADDRESSING MODE GP99113
         SPACE 1                                                GP96109
         POP   USING                                            GP99113
         SPACE 2
TABHEXTB DC    C'0123456789ABCDEF'   1/2                        GP97265
TABHEXTR EQU   TABHEXTB-C'0'         2/2                        GP97265
         SPACE 1
PATCHWK  DC    30S(*)        MINI-PATCH                         GP95235
         SPACE 1                                                GP96101
*   THIS TABLE DEFINES ALL PN/P11 CHARACTERS PLUS LOWER CASE    GP99113
*                                                               GP96101
TRTCHAR  TRTAB CODE=TN,FILL=X'FF'  0 FOR VALID CHARS            GP00162
TRTABTN  TRTAB CODE=TN,FILL=X'40'  VALID CHARS                  GP00162
         SPACE 1                                                GP00197
TRTPACK  DC    256X'08'      EVERYTHING INVALID                 GP00197
         ORG   TRTPACK+X'00'                                    GP00197
*   ZONE +       0 1 2 3 4 5 6 7 8 9 A B C D E F                GP00197
         DC    X'0,0,0,0,0,0,0,0,0,0,8,8,4,4,8,4'  0-           GP00197
         DC    X'0,0,0,0,0,0,0,0,0,0,8,8,4,4,8,4'  1-           GP00197
         DC    X'0,0,0,0,0,0,0,0,0,0,8,8,4,4,8,4'  2-           GP00197
         DC    X'0,0,0,0,0,0,0,0,0,0,8,8,4,4,8,4'  3-           GP00197
         DC    X'0,0,0,0,0,0,0,0,0,0,8,8,4,4,8,4'  4-           GP00197
         DC    X'0,0,0,0,0,0,0,0,0,0,8,8,4,4,8,4'  5-           GP00197
         DC    X'0,0,0,0,0,0,0,0,0,0,8,8,4,4,8,4'  6-           GP00197
         DC    X'0,0,0,0,0,0,0,0,0,0,8,8,4,4,8,4'  7-           GP00197
         DC    X'0,0,0,0,0,0,0,0,0,0,8,8,4,4,8,4'  8-           GP00197
         DC    X'0,0,0,0,0,0,0,0,0,0,8,8,4,4,8,4'  9-           GP00197
         ORG   ,                                                GP00197
***********************************************************************
*                                                                     *
*   ESPIE EXIT.  CHECK WHETHER THIS IS ANYWHERE IN OUR CODE, AND      *
*   WHETHER THE CURRENT PROGRAM HAS REQUESTED RECOVERY.               *
*                                                                     *
*   RECOVERY CONSISTS OF RELOADING ALL REGISTERS, AND GOING TO USER'S *
*   SPECIFIED RESTART ADDRESS; RESTART REQUEST IS CLEARED.            *
*                                                                     *
***********************************************************************
         SPACE 1
         PUSH  USING         JUST IN CASE
         DROP  ,             DON'T TRUST NOBODY
SPEXIT   ARMODE 0            SET PRIMARY
         LR    R11,R15       SET NEW BASE
         USING SPEXIT,R11
         AM31  WORK=R15      GET HIGH
         LR    R6,R1         PRESERVE EPIE ADDRESS
         USING EPIE,R6       DECLARE IT
         L     R8,EPIEPARM   GET USER WORK ADDRESS
         USING DBUSECT,R8    DECLARE IT
         L     R10,DBU@WORK  GET MY WORK AREA BACK
         USING DBWSECT,R10   DECLARE IT
         STM   R14,R2,DBWPK16  TEMP SAVE
         ICM   R3,15,DBWSPIBY  DID PROGRAM SET A RECOVERY ADDRESS
         BNZ   SPEXCOMM      YES; JUST RETURN THERE
         SPACE 1
         MVC   DBWESPSW,EPIEPSW  SAVE PSW
*AM64*   MVC   DBWESPSW+8,EPIEPSW+8  ???
*AM64*   MVC   DBWESPGH(16*4),EPIEG  ???
*AM64*   MVC   DBWESPAH(16*4),EPIEA  ???
         MVC   DBWESPGR(16*4),EPIEGPR  SAVE (LOW) GENERAL REGISTERS
         MVC   DBWESPAR(16*4),EPIEAR   SAVE (LOW) ACCESS REGISTERS
         MVC   DBWESPIN(EPIEAR-EPIEINT),EPIEINT  INT. DATA
         L     R3,=A(SPEMRITE)  UNRECOVERABLE ERROR - WRITE DIAG DATA
         CLI   DBWESPPF,STFRITE  ERROR DURING WRITE ?
         BNE   SPEXCOMM      NO; CONTINUE
         WTO   'DEBTRACE-ERROR DURING OUTPUT PROCESSING'
         LH    R2,EPIEINT+2  GET INTERRUPT CODE
         ABEND X'C00',DUMP,,SYSTEM,REASON=(R2)  GREETINGS
         SPACE 1
SPEXCOMM XC    DBWSPIBY,DBWSPIBY  RESET IT
         ST    R3,EPIEPSW+4  SET RESTART ADDRESS AND MODE
         OI    EPIEPSW+4,EPIEMOD1  SET AM31
         LM    R14,R2,DBWPK16  TEMP SAVE
         SR    R15,R15
         BR    R14           RETURN TO RECOVERY VIA SYSTEM
         SPACE 1
         LTORG ,
         SPACE 1
         IHAEPIE ,           MAP EPIE
         SPACE 1                                                GP95235
REQWORK  DSECT ,             INPUT REQUEST LIST MAPPING         GP95235
REQTYPE  DS    X             REQUEST TYPE                       GP95235
RTNAME   EQU   1               LINE LABEL
RTREGS   EQU   3               REGISTER LISTING                 GP96101
RTTEXT   EQU   8               TEXT STRING                      GP95235
RTCHEX   EQU   9               PRINTABLE TEXT, ELSE HEX         GP96101
RTHEX    EQU   10              HEXADECIMAL                      GP95235
RTPACK   EQU   11              PACKED DECIMAL                   GP96081
RQTEOL   EQU   X'80'           END OF LIST FLAG                 GP02364
RQTMSK   EQU   X'7F'           END OF LIST MASK                 GP02364
REQFG    DC    AL1(0)        OPTION FLAGS                       GP95235
RFIAD    EQU   1               INDIRECT ADDRESS SUPPLIED        GP96101
RFA24    EQU   2               FORCE 24-BIT MODE ADDRESSING     GP96101
RFA31    EQU   4               FORCE 31-BIT ADDRESSING MODE     GP96101
RFA64    EQU   8               FORCE 64-BIT MODE                GP96101
.*  REGISTER REQUESTS
RRGPR    EQU   1             GENERAL REGISTER
RRACR    EQU   2             ACCESS  REGISTER
RRCTL    EQU   4             CONTROL REGISTER
RRFPR    EQU   8             FLOATING REGISTER (HEX)
RRIEE    EQU   16            FLOATING REGISTER (IEEE)
.*   LABEL/NAME REQUEST ONLY;  END OF SHORT REQUESTS
REQNAME  DS    0CL8          VARIABLE NAME FOR LABEL TYPE       GP95235
REQADDR  DS    S             ADDRESS OF VARIABLE                GP95235
REQRG1   EQU   REQADDR+0,1,C'X'  FIRST OF REGISTER LIST
REQRG2   EQU   REQADDR+1,1,C'X'  LAST  OF REGISTER LIST
REQLEN   DS    Y             LENGTH OF VARIABLE                 GP95235
.*   REQUEST FOR CHAIN
REQLKOFF DS    X             OFFSET TO LINK
REQLKLEN DS    X             LENGTH OF LINK
.*   REQUEST FOR FLAG FORMATTING
REQFGADR EQU   REQLKOFF,2,C'S'  ADDRESS OF TABLE
.*   END OF DATA FORMATTING REQUESTS
         SPACE 1                                                GP95235
DBUSECT  DSECT ,                                                GP95235
         DBT MODE=D,CALL=TRC,PFX=DBU    MAP USER'S AREA         GP03003
@PRINTER EQU   DBU@UDCB,4,C'A'  REDEFINED FOR PRTV MACRO        GP99113
DBWSECT  DSECT ,                                                GP03003
         DBT MODE=DW,CALL=TRC,PFX=DBW   EXPAND MY WORK AREA     GP03003
         PRINT NOGEN                                            GP95235
         YREGS ,                                                GP95235
         IHAPSA ,                                               GP95235
         DCBD  DEVD=DA,DSORG=PS                                 GP95235
         IEFUCBOB ,                                             GP99113
         IKJTCB ,
         CVT   DSECT=YES,LIST=NO
         IHAASCB ,
         END   ,
