SYNCU    TITLE 'S Y N C U S E R  ***  SYNCHRONIZE UADS/BDCST'    93055
         COPY  OPTIONGB
         SPACE 1
         SYSPARM LIST=YES
         SPACE 2
***********************************************************************
*                                                                     *
*  This program uses local accounting/security facilities.            *
*  The local user data base is compared to SYS1.UADS, entries are     *
*  updated, and a TSO SYNC is performed.                              *
*                                                                     *
***********************************************************************
*                                                                     *
*        COPYRIGHT 1993  EXPERT SYSTEM PROGRAMMING, INC.              *
*        COPYRIGHT 2003  EXPERT SYSTEM PROGRAMMING                    *
*                        176 OLD STAGE COACH ROAD                     *
*                        BRADFORD, VT 05033-8844                      *
*                                                                     *
*                    ALL RIGHTS RESERVED                              *
*                                                                     *
***********************************************************************
         EJECT ,
         PRINT &PRTSOR
SYNCUSER SAVEM ZERO15,BASE=R12,PARM=R5,BNDRY=PAGE                93055
         MVC   BLDLIST(4),=Y(1,L'DIRBLOCK)  MAKE BLDL REQUEST    93055
         MVC   UADS(UADSLEN),PATUADS  MAKE UADS DCB              93055
         LA    R1,UADS                                           93055
         ST@   R1,@UADS,MVI=X'80'  MAKE OPEN LIST FOR UADS       93055
         SERVINIT ,          LOAD @SERVICE ROUTINE
         SERVCALL LPALD,=CL8'@PRINTER'  GET PRINTER ROUTINE
         ST    R0,@PRINTER
         SERVCALL LPALD,=CL8'@VOLREAD'  VOLUME READER
         ST    R0,@VOLREAD
         SERVCALL ACGET,DB,ERR=ABEND69  GET ACCOUNT              93055
         STC   R0,DB         SAVE ACCOUNT'S PRIVILEGES
         TM    DB,VAASYS+VAASUP+VAASTC  PRIVILEGED CALLER ?      93055
         BNZ   NOPARM        YES                                 93055
UNPRIVY  WTO   '*** INVALID OR UNAUTHORIZED ACCOUNT ***'         93055
ABEND69  ABEND 69
         SPACE 1
NOPARM   PRTOPEN SUMPRINT,OPT=(ABEND)
         PRTLIST TITLE,TITLE=1                                   93055
         SERVICE APFON        *****DEBUG*****                    93055
         SPACE 2
GETJFCB  LA    R2,=CL8'UADS'                                     93055
         SERVCALL RJFCB,(R2)  LOOK FOR UADS                      93055
         BXH   R15,R15,MISSJFCB                                  93055
         LA    R2,=CL8'ACCOUNT'                                  93055
         SERVCALL RJFCB,(R2)  LOOK FOR THE ACCOUNT DATASET       93055
         BXLE  R15,R15,HAVEJFCB
MISSJFCB PRTLIST NOJFCB                                          93055
BOMB     SERVTERM ,          CLOSE AND FREE
         CLOSE MF=(E,@UADS)                                      93055
         ABEND 666
NOJFCB   FD    '****',NL,PAD                                     93055
         FD    0(R2),8,PAD,DEB  DDNAME                           93055
         FD    'MISSING OR BAD ****'                             93055
         FD    *END                                              93055
HAVEJFCB MVC   INFMJFCB(JFCBLGTH),0(R1)  SAVE IN MY STORAGE
         OPEN  MF=(E,@UADS)  OPEN SYS1.UADS                      93055
         L     R0,=A(200*1024)  SET DESIRED BUFFER POOL SIZE
         VOLREAD OPEN,JFCBVOLS,SIZE=(R0)  OPEN THE INDICATED VOLUME
         BXH   R15,R15,BADVOL
         VOLREAD SEARCH,JFCBDSNM  LOOK FOR THE DATASET
         BXLE  R15,R15,USERREAD  START PROCESSING                93055
BADVOL   PRTLIST MSGBADVL                                        93055
         B     BOMB
MSGBADVL FDPRT '**** UNABLE TO OPEN DATASET',NL
         FDPRT JFCBDSNM,DEB,PAD
         FDPRT 'ON VOLUME',PAD
         FDPRT JFCBVOLS,6,PAD
         FDPRT *END
         SPACE 3                                                 88006
*        NON-SORT SECTION FOR PLAIN USER LISTING                 88006
*                                                                88006
USERREAD VOLREAD GETDE       GET A DIRECTORY ENTRY               88006
         CH    R15,=H'4'     REALLY BAD ?                        89016
         BH    BADVOL        YES; GIVE AN ERROR                  89016
         BXLE  R15,R15,USERREED  OK                              88006
         XC    DB,DB         CLEAR RETURN CODE                   88006
         B     GETOUT        AND QUIT                            88006
USERREED LR    R1,R0                                             88006
         ST    R1,PDNEXT     STASH START                         88006
         MVC   DIRBLOCK,0(R1)  COPY IT                           88006
         SERVCALL PDSDE,DEBXLE  DECODE IT                        88006
         LR    R15,R0                                            88006
         MVC   PDRALIS(PDRBLNK),0(R15)  COPY RETURN              88006
         VOLREAD READ        READ THE FIRST (AND ONLY) BLOCK     88006
         BXH   R15,R15,USBADMEM                                  89016
         AH    R0,=H'8'      SKIP THE COUNT FIELD                88006
         LA    R1,A$USIZE    LENGTH OF RECORD                    93055
         LR    R15,R1        COPY LENGTH                         88006
         LA    R14,A$USER    MY COPY                             93055
         MVCL  R14,R0        COPY FROM I/O BUFFER                88006
         CLC   PDS2NAME,A$UUID  CORRECT KEY                      93055
         BNE   USBADMEM      NO                                  89016
         CLC   =C'USER',A$UID   USER-ID ENTRY ?                  88006
         BNE   USERREAD      NO; SKIP IT                         93055
         TM    A$USUBFG,A$USFTSO  TSO USER ?                     93055
         BZ    USERREAD      NO; SKIP CICS AND WYLBUR            93055
         LA    R1,PDS2NAME   POINT TO USER ID                    93055
         LA    R0,7          SET MAXIMUM TSO LENGTH              93055
LENLOOP  CLI   0(R1),C' '    TRAILING BLANK ?                    93055
         BE    LENFOUND      YES                                 93055
         LA    R1,1(,R1)                                         93055
         BCT   R0,LENLOOP                                        93055
LENFOUND MVI   0(R1),C'0'    MAKE TRAILING ZERO                  93055
         BLDL  UADS,BLDLIST  FIND THE MATCHING UADS ENTRY        93055
         BXLE  R15,R15,USERREAD  FOUND; TRY AGAIN                93055
         CLI   A$UACCT,C'A'  VALID ACCOUNT ?                     93055
         BL    USBADMEM      NO                                  93055
         CLI   A$UTSPRC,C'$'  VALID PROCEDURE NAME ?             93055
         BL    USBADMEM      NO                                  93055
         XC    A$UPSWD,A$UUID  MAKE EBCDIC                       93055
         TM    RETCODE+L'RETCODE-1,4  FIRST TIME ?               93055
         BNZ   HAVEPUN       NO                                  93055
         PRTOPEN ADDFILE,DEV=4   OPEN CONTROL OUTPUT FILE        93055
         OI    RETCODE+L'RETCODE-1,4  SIGNAL WORK TO DO          93055
         PRTF  ' ACCOUNT ',CC=NO,DEV=4                           93055
HAVEPUN  PRTLIST ADDMEM,DEV=4      ADD A MEMBER                  93055
         MVC   A$UPSWD,=CL8'PASSWORD'                            93055
         PRTLIST ADDMEM,DEV=1     LIST ADDITION                  93055
         B     USERREAD                                          88006
         SPACE 1                                                 93055
ADDMEM   FD    ' ADD (',NL                                       93055
         FD    A$UUID                                            93055
         FD    A$UPSWD,PAD                                       93055
         FD    A$UACCT,PAD                                       93055
         FD    A$UTSPRC      PROC NAME                           93055
         FD    ') NOLIM JCL UNIT(TSO)'                           93055
         FD    *END                                              93055
         SPACE 1                                                 93055
GETOUT   LH    R9,RETCODE                                        93055
         TM    RETCODE+L'RETCODE-1,4  ANY CARDS WRITTEN ?        93055
         BZ    ALLDONE       NO                                  93055
         PRTF  ' SYNC ',CC=NO,DEV=4  REQUEST BROADCAST SYNCHRONIZATION
         PRTF  ' END  ',CC=NO,DEV=4  LEAVE ACCOUNT COMMAND       93055
ALLDONE  CLOSE MF=(E,@UADS)  CLOSE UADS                          93055
         SERVTERM ,          FREE AND CLOSE
         ENDM  RC=(R9)
         SPACE 1                                                 89016
USBADMEM PRTLIST MSGBADMM    INDICATE PROBLEM                    93055
         OI    RETCODE+L'RETCODE-1,8                             89016
         B     USERREAD      PROCESS ANOTHER                     93055
MSGBADMM FD    '***** RECORD',NL                                 93055
         FD    PDS2NAME,PAD,DEB                                  93055
         FD    'CONTAINS INVALID KEY OR DATA *****'              93055
         FD    *END                                              93055
         SPACE 1
         LTORG ,
         EJECT ,
SUMPRINT PRTWORK SUMPRINT,SYSPRINT,TITLE=3       DEV=1           93055
ADDFILE  PUNWORK SYSPUNCH                        DEV=4           93055
PATUADS  DCB   DDNAME=UADS,MACRF=(R),DSORG=PO                    93055
         SPACE 1
TITLE    FDOPT NL,CC=C'#'    AUTO PAGE NUMBERING, ETC.
         FDPRT 'ACCOUNT STATUS',RADJ,LEN=25
         FDPRT *END
         SPACE 1
DETSUB2  FD    JFCBDSNM,RADJ,DEB,NL,LEN=23   SYS1.A$ACCT OR A$BKUP
         FD    JFCBVOLS,PAD,LEN=6         VOLSER (LIBPAK)
         FD    *END
         SPACE 2
SAVE     DSECT ,
DB       DS    2D
@SERVICE DS    A
@PRINTER DS    A
@VOLREAD DS    A
RETCODE  DS    H
ACCTFG   DS    C             EBCDIC STATUS FLAG
FLAGS    DS    X             OPTION FLAGS                        88006
         SPACE 1                                                 93055
@UADS    DS    A             OPENLIST FOR UADS                   93055
UADS     DCB   DDNAME=UADS,MACRF=(R),DSORG=PO                    93055
UADSLEN  EQU   *-UADS                                            93055
         SPACE 1
         PRINT &PRTSYS
         IEFJFCBN ,          INCLUDE A JFCB EXPANSION
         SPACE 1
DEBXLE   SERVPDS PFX=PD,DSECT=NO,RETURN=YES
BLDLIST  DS    0F,Y(1,76)  BLDL PREFIX                           93055
DIRBLOCK DS    XL76'0'   MAXIMAL ENTRY
         ORG   DIRBLOCK
         IHAPDS PDSBLDL=NO,DSECT=NO
OLDKEY   EQU   DIRBLOCK      RE-USE AS PREVIOUS KEY FIELD
         ORG   ,
         PUSH  PRINT                                             93055
         PRINT GEN,DATA                                          93055
A$USER   A$USER DSECT=NO
         POP   PRINT                                             93055
SAVEND   EQU   *
         SPACE 2
         PRINT &PRTSYS
         CVT   DSECT=YES
         IHACDE ,
         IHAPSA ,
         IKJTCB ,
         SPACE 1
         DCBD  DEVD=DA,DSORG=PS
         SPACE 1
         IEFUCBOB ,
         SPACE 1
         END   ,
