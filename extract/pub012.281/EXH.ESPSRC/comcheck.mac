CMTCK    TITLE 'C O M C H E C K  ***  CHECK COMTEN FRONT-END STATUS'
         MACRO ,
&NM      NSSRU &PFX=NSS,&DSECT=YES
         LCLC  &P
&P       SETC  'NSS'
         AIF   ('&PFX' EQ '').DFP
&P       SETC  '&PFX'
.DFP     AIF   ('&DSECT' EQ 'NO').NOD
         AIF   ('&NM' EQ '').DFDS
&NM      DSECT ,
         AGO   .COM
.DFDS    ANOP  ,
&PFX.DSECT DSECT ,
         AGO   .COM
.NOD     AIF   ('&NM' EQ '').COM
&NM      DS    0F            ALIGN
.COM     ANOP  ,
&P.TH1   DC    X'00'         TH1
&P.TH2   DC    X'00'         TH2
&P.SITE  DC    CL3' '        SITE
&P.SS#   DC    X'00'         SUBSYSTEM (ILD,NETRTR,RIL,OND)
&P.SNF   DC    XL2'00'       TH7, TH8
&P.TRS   DC    Y(&P.RUND-&P.RH1)  SIZE OF RH+RU
&P.RH1   DC    X'00'         REQUEST/RESPONSE HEADER 1
&P.RH2   DC    X'00'
&P.RH3   DC    X'00'
&P.RHU   DC    XL5'00'       RESPONSE/REQUEST UNIT
&P.HID1  DC    X'00'         HOST ID 1
&P.HID2  DC    X'00'         HOST ID 2
&P.CMD   DC    X'00'         SUBCOMMAND
&P.SCN   DC    X'00'         -"- REPEAT COUNT
&P.LNG   DC    XL2'00'
&P.ER1   DC    X'00'         ERROR 1
&P.ER2   DC    X'00'         ERROR 2
&P.DSN   DC    CL8' '        DSN
&P.DATA  DC    XL128'00'     RU DATA
&P.RUND  EQU   *             END OF PACKET
         MEND  ,
         COPY  OPTIONGB      GLOBALS
         SPACE 1
         SYSPARM LIST=YES    SET AND LIST GLOBALS
         EJECT ,
         PRINT &PRTSOR
COMCHECK SAVEM ZERO12,BASE=R12,BNDRY=PAGE,PARM=R9                87296
         MVC   COMTEN(PATDCBLN),PATDCB  BUILD DCB                87296
         LA    R8,COMTEN     WE HAVE REGISTERS TO SPARE
         USING IHADCB,R8
         LA    R7,MYIOB
         USING IOBSTDRD,R7
         LA    R0,IOECB
         ST    R0,IOBECBPT
         ST    R8,IOBDCBPT   DCB POINTER
         ST    R8,OPENLIST                                       87296
         OI    OPENLIST,X'80'                                    87296
         MVI   REQTH1,X'1C'  INITIALIZE TRANSMISSION HEADER
         MVI   REQTRS+L'REQTRS-1,8  SET MINIMUM LENGTH
         MVI   REQRH1,X'43'  ONLY SEGMENT; DATAFLOW
         MVI   REQCMD,X'04'  SITE ID
         MVI   REQHID1,1     SET HOST ONE ?
         MVC   REQSITE,=3X'EF'
         LA    R14,TIMECB
         LA    R15,IOECB
         STM   R14,R15,ECBLIST
         OI    ECBLIST+4,X'80'
         MVC   STIMEXIT(STIMPATN-STIMPAT),STIMPAT
         LA    R0,PYWACKET   GET INCOMING PACKET
         LM    R1,R2,PATCCW  GET CCW PATTERN                     87298
         OR    R1,R0
         STM   R1,R2,READCCW
         LA    R1,MYPACKET
         STM   R1,R2,WRITECCW
         MVI   WRITECCW,1
         SERVINIT ,          INITIALIZE SERVICES
*DEFER*  SERVCALL LPALD,=CL8'@PRINTER'  LOAD THE PRINTER
*DEFER*  ST    R0,@PRINTER   SAVE THE ADDRESS
*DEFER*  PRTOPEN SYSPRINT    OPEN AN OUTPUT FILE
         LA    R10,16        SET ERROR CODE
         SPACE 1                                                 87296
         LA    R9,0(,R9)     CLEAN PARM POINTER                  87296
         LTR   R9,R9         ANY PARM ?                          87296
         BZ    DONEPARM      NO                                  87296
         ICM   R9,7,1(R9)    ANY ADDRESS ?                       87296
         BZ    DONEPARM      NO                                  87296
         SLR   R2,R2                                             87296
         ICM   R2,3,0(R9)    ANY LENGTH ?                        87296
         BNP   DONEPARM                                          87296
         MIN   R2,=Y(L'DCBDDNAM),TYPE=H  TRUNCATE                87296
         BCTR  R2,0          SET FOR EXECUTE                     87296
         MVC   DCBDDNAM,=CL8' '  MAKE BLANKS                     87296
         EX    R2,DDNMVC     MOVE NAME                           87296
DONEPARM MVC   DDNAME,DCBDDNAM  ALSO SAVE                        87296
         SPACE 1
         OPEN  MF=(E,OPENLIST)  OPEN THE UTILITY SUB-CHANNEL     87296
         LA    R1,MSG013     PRELOAD FOR OPEN ERROR MESSAGE      87296
         TM    DCBOFLGS,DCBOFOPN
         BZ    TATTLE        TOO BAD                             87296
         STCM  R7,7,DCBIOBAA PUT IOB ADDRESS INTO DCB FOR DUMPS
         SPACE 1
         SRL   R10,1         CHANGE ERROR CODE TO 8
         LA    R14,DB+8+3    POINT TO WRITE BYTE                 87298
         LA    R15,1         SET I/O LENGTH                      87298
         LA    R0,5          SET DATA BYTE                       87298
         ICM   R14,8,=X'9'   MAKE POOL REQUEST                   87298
         ICM   R15,8,READCCW+4  ADD SLI, JUST IN CASE            87298
         STM   R14,R0,DB     MAKE CCW AND DATA BYTE              87298
         LA    R1,DB         PASS CCW ADDRESS TO I/O             87298
         BAL   R9,EXCP1      ISSUE SPECIAL WRITE
         LA    R1,WRITECCW   WRITE SITE REQUEST
         BAL   R9,EXCP1
         LA    R1,READCCW    POINT TO READ CCW
         BAL   R9,EXCP1      READ A RESPONSE UNIT
         LA    R1,GOODBYE                                        93022
         BAL   R9,EXCP1      CLOSE THE INTERFACE                 93022
         SLR   R10,R10       SET GOOD COMPLETION
         SPACE 1
GETOUT   SERVTERM ,          CLOSE AND FREE
         CLOSE MF=(E,OPENLIST)                                   87296
         ENDM  RC=(R10)      END WITH CC
         SPACE 1                                                 87296
TATTLE   SLR   R15,R15                                           87296
         ICM   R15,3,0(R1)   GET MESSAGE LENGTH                  87296
         LA    R15,3(,R15)   ALLOW FOR ROUTING AND DESCRIPTORS   87296
         EX    R15,MVCWTO                                        87296
         LA    R14,WTO                                           87296
TATTLOOP CLC   =C'XXXXXXXX',0(R14)  DDNAME INSERTION ?           87296
         BNE   TATTBUMP      NO                                  87296
         MVC   0(L'DCBDDNAM,R14),DDNAME  REPLACE IT              87296
TATTBUMP LA    R14,1(,R14)                                       87296
         BCT   R15,TATTLOOP                                      87296
         WTO   MF=(E,WTO)    WRITE TO OPERATOR                   87296
         B     GETOUT        AND QUIT                            87296
         SPACE 1
EXCP1    ST    R1,IOBSTART   SET CCW START
         XC    IOBFLAG1(4),IOBFLAG1  CLEAR FLAGS AND SENSE
         OI    IOBFLAG1,IOBCMDCH+IOBUNREL
         EXCP  MYIOB         I/O
         XC    TIMECB,TIMECB
         STIMER REAL,STIMEXIT,BINTVL==A(10*100)  SET FOR 10 SEC.
         WAIT  ECBLIST=ECBLIST
         TM    TIMECB,X'40'  TIMER EXPIRED ?
         BNZ   TIMEOUT       YES
         TTIMER CANCEL       CANCEL THE REQUEST
TIMEOUT  TM    IOECB,X'40'   DID I/O COMPLETE
         BZ    TIMEBOMB      NO; QUIT
         TM    IOBSIOCC,X'30'  CC=3 ?
         BO    BOXDOWN       YES; NEED TO RELOAD OR FLIP CHANNEL
         CLI   IOECB,X'7F'   NORMAL COMPLETION ?
  BR R9  BER   R9            YES; RETURN
         LA    R1,MSG001                                         87296
         B     TATTLE        WRITE MESSAGE AND QUIT              87296
MSG001   WTO   'CMT001E XXXXXXXX I/O ERROR',ROUTCDE=1,DESC=1,MF=L
         SPACE 1
BOXDOWN  DS    0H            FOR NOW
TIMEBOMB LA    R1,MSG000                                         87296
         B     TATTLE        WRITE MESSAGE AND QUIT              87296
MSG000   WTO   'CMT000E XXXXXXXX IS NOT RESPONDING TO I/O',            *
               ROUTCDE=1,DESC=1,MF=L                             87296
         SPACE 1
         PUSH  USING
         DROP  ,
         USING STIMPAT,R15
STIMPAT  POST  TIMECB-STIMEXIT(,R15),0
         BR    R14
STIMPATN EQU   *
         POP   USING
         SPACE 1
MSG013   WTO   'CMT013E UNABLE TO OPEN XXXXXXXX DCB',                  *
               ROUTCDE=1,DESC=1,MF=L                             87296
PATCCW   DC    0A(0),X'02',AL3(0),X'20',AL3(REQRUND-REQTH1)      87298
GOODBYE  CCW   X'09',HANGUP,X'20',1   RESET THE CONNECTION       93022
HANGUP   DC    X'FF'         BYE-BYE                             93022
         SPACE 1
DDNMVC   MVC   DCBDDNAM(0),2(R9)  MOVE DDNAME FROM PARM          87296
MVCWTO   MVC   WTO(0),0(R1)  MOVE CALLER'S WTO                   87296
SYSPRINT PRTWORK SYSPRINT,SYSTERM,TITLE=5
PATDCB   DCB   DDNAME=COMTEN,DSORG=PS,MACRF=(E)  COMTEN SUBCHANNEL
PATDCBLN EQU   *-PATDCB                                          87296
         SPACE 1
         IEZIOB ,
SAVE     DSECT ,             SAVE AND WORK AREA
@SERVICE DS    A
@PRINTER DS    A
DB       DS    2D
READCCW  CCW   2,PYWACKET,X'20',162  READ A PACKET
WRITECCW CCW   1,MYPACKET,X'20',162
DDNAME   DS    CL8           DDNAME                              87296
COMTEN   DCB   DDNAME=COMTEN,DSORG=PS,MACRF=(E)  COMTEN SUBCHANNEL
OPENLIST DS    A             COMTEN DCB OPEN LIST                87296
         DS    D             IOB PADDING ?
MYIOB    DS    ((IOBEXTEN-IOBSTDRD+8)/4)F'0'  IOB
         DS    9D            ?????                               87298
         SPACE 1
STIMEXIT DC    (STIMPATN-STIMPAT)X'00'  STIMER EXIT
ECBLIST  DC    A(0),X'80',AL3(0)
TIMECB   DS    F
IOECB    DS    F
MYPACKET NSSRU DSECT=NO,PFX=REQ OUTGOING PACKET
PYWACKET NSSRU DSECT=NO,PFX=RSP INCOMING PACKET
WTO      DS    XL132         ROOM FOR WTO                        87296
SAVEND   EQU   *
         SPACE 1
         PRINT &PRTSYS
CVTDSECT DSECT ,
         CVT   DSECT=YES
         IHACDE ,
         DCBD  DEVD=DA,DSORG=PS
         END   ,
