FUJI     TITLE 'SMF JOB INITIATION EXIT'
         COPY  OPTIONGB                                          83128
         SPACE 1                                                 83128
         SYSPARM LIST=YES                                        83128
         EJECT ,
         PRINT &PRTSOR                                           83128
IEFUJI   PGMHEAD ZERO,NOEQU,BASE=R12,PARM=R3,AM=31,RM=31        GP04042
         SPACE 1                                                 83128
*              R1 POINTS TO A PARAMETER LIST:
*        0     JOB NAME IN JMR
*        4     PROGRAMMER NAME IN ACT
*        8     JOB PRIORITY (CHANGE NOT PROPAGATED)
*        12    1ST JOB ACCOUNTING FIELD IN ACT
         SPACE 1
         L     R6,0(,R3)     GET JMR ADRESS                      83128
         USING JMR,R6
         L     R8,12(,R3)    GET ACCT FIELDS                     83128
         USING ACTJNFLD,R8   DECLARE THEM
         L     R4,CVTPTR     GET CVT
         USING CVTMAP,R4     DECLARE IT                          83128
         L     R5,CVTTCBP
         L     R5,4(,R5)     GET CURRENT TCB
         USING TCB,R5        DECLARE TCB                         83128
         MVI   CT,X'0C'      =P'0' INTO RETRY COUNTER
         MVI   RCVAL,4-4*&MVS   KILL NON-HASP EXCEPT IN MVS      83129
         ICM   R10,15,TCBUSER   IS THERE A USABLE EXTENSION ?    83128
         BP    NOMAIN        YES; INITIALIZE FOR NEW JOB         83128
         USING USERTCB,R10   DECLARE IT
         STORAGE OBTAIN,LENGTH=UTLN,SP=243-8*&MVS  MAKE USERTCB
         LR    R10,R1                                            83128
         XC    0(UTLN,R1),0(R1)   CLEAR IT                       83128
         ST    R1,TCBUSER    STASH IT                            83128
         L     R2,TCBTIO                                         83128
         MVC   UTID,0(R2)    SET INIT NAME                       83128
NOMAIN   MVI   UTF1,0        ZERO ALL FLAGS                      83128
         XC    UTSB(UTJL+UTSL),UTSB  CLEAR JOB/STEP RELATED FIELDS
         SPACE 1                                                 83128
         L     R3,SAVE13-SAVE(,R13)  GET HIGHER SAVE AREA        83136
         L     R3,SAVE10-SAVE(R3)  GET CALLER'S R10             GP04043
         N     R3,=X'00FFFFFF'  24-BIT ADDRESS                  GP04043
         BZ    PTYOK         NONE; SKIP                          83128
         USING LCTDSECT,R3                                      GP04043
         L     R7,LCTJCTAD   GET THE OS JCT                     GP04043
         L     R3,LCTJSCBP-1  POINTER TO JOB JSCB               GP04043
         N     R3,=X'00FFFFFF'  24-BIT ADDRESS                  GP04043
         BZ    PTYOS         NONE; SKIP                          83128
*   NOTE: THIS KLUDGE DISCOVERED FROM IEFSMFIE LISTING. USER JSCB
*        ADDRESS DOES NOT APPEAR TO BE OTHERWISE AVAILABLE ?     83136
         USING IEZJSCB,R3    DECLARE IT                          83128
         ICM   R3,15,JSCBSSIB  GET THE SUB-SYSTEM BLOCK          83128
         BZ    PTYOS         HUH ?                               83128
         USING SSIBEGIN,R3   DECLARE THE SSIB                    83128
         ICM   R0,15,SSIBSUSE  CHECK THE USER FIELD              83129
         BZ    PTYOS         NONE ?                              83128
         MVC   UTID,SSIBJBID   SET TASK TYPE AND NUMBER          83129
         LR    R3,R0         SET SJB ADDRESS                     83129
         USING SJB,R3        SORRY - WE RUN JES2                GP04042
         CLC   =CL4'SJB ',SJBID  DOUBLE-CHECK                    83128
         BNE   PTYOS                                             83128
         ICM   R3,15,SJBJCT  GET JES JCT                         83128
         BZ    PTYOS                                             83128
         USING JCTSTART,R3                                      GP04042
         CLI   JCTJCLAS,C'A'  VALID CLASS SPECIFIED ?            83128
         BL    *+10          NO ?                                83128
         MVC   JMRUCOM(1),JCTJCLAS  COPY SUBMITTED JOB CLASS     83128
         XR    R0,R0                                             83129
         IC    R0,JCTIPRIO   GET INITIATION PRIORITY             83129
         SRL   R0,4          DELETE SUB-PRIORITY BITS            83129
         STC   R0,UTPY       SAVE PROVISIONAL BILLING PRIORITY   83129
         B     PTYCHK        SKIP JCT PRIORITY                   83128
         USING IEFJCT,R7     PASSED BY CALLER ?                  83128
PTYOS    LTR   R7,R7         IS THERE A JCT ?                   GP04043
         BZ    PTYOK         NO; CAN USE                        GP04043
         MVN   UTPY,JCTJPRTY  USE OS JOB PRIORITY               GP04043
PTYCHK   TM    JCTJPRTY,X'08'  PRTY <= 7 ?                       79049
         BNZ   PTYOK         NO, LEAVE AS IS
         MVN   JCTJPRTY(1),HUH+15  USE APG PRTY=7                79049
PTYOK    OI    UTMF,X'80'    ENABLE 'TELL' MESSAGES
         LTR   R8,R8         HAVE AN ACT POINTER ?              GP04043
         BZ    BYEBYE        NO; SKIP THIS                      GP04043
         CLI   ACTJNFLD,3                                       GP04043
         BL    BYEBYE                                           GP04043
         LA    R14,JMRUSEID                                     GP04043
         LA    R15,L'JMRUSEID                                   GP04043
         LA    R4,ACTJNFLD+1                                    GP04043
         SR    R2,R2                                            GP04043
         IC    R2,0(,R4)                                        GP04043
         LA    R0,1(,R4)     POINT TO TEXT                      GP04043
         LR    R1,R2         COPY LENGTH                        GP04043
         MVCL  R14,R0        SAVE ACCOUNT IN JMRUSEID FOR SORTING
         LA    R4,1(R4,R2)                                      GP04043
         IC    R2,0(,R4)                                        GP04043
         LA    R0,1(,R4)     POINT TO TEXT                      GP04043
         LR    R1,R2         COPY LENGTH                        GP04043
         MVCL  R14,R0        SAVE SUB-ACCOUNT IN JMRUSEID       GP04043
         CLI   ACTJNFLD,2                                       GP04043
         BNH   BYEBYE                                           GP04043
         LA    R3,1(R4,R2)                                      GP04043
         CLI   0(R3),1
         BL    BYEBYE        NO THIRD FIELD - SKIP
         CLI   0(R3),4
         BNE   CONDKILL      KILL UNLESS SPECIAL
         MVC   REPLY,=4C'0'  MOVE ZONES TO WORK FIELD
         NC    REPLY,1(R3)   MASK OUT NUMERIC ZONE ONLY
         CLC   REPLY,=4C'0'  ALL NUMERIC ?
         BE    BYEBYE        YES; HASP EXEC TIME FIELD
         SPACE 2
         CLC   CTIME,1(R3)   REQUEST FOR TIME EXTENSION ?
         BE    UTLEX         YES, SET FLAGS FOR UTL
         CLC   CPASS,1(R3)    REQUEST FOR PASSWORD OVERRIDE ?
         BE    NOPASS        YES, GRANT IT; WAS ALREADY CHECKED BY UJV
         CLC   CZERO,1(R3)    REQUEST FOR KEY ZERO ?
         BE    KEY0          YES, GRANT IT; CHECKED BY UJV
         TIME  BIN
         SLL   R1,6                     SHIFT 2 + ZONE OF 0
         ST    R1,WFULL
         UNPK  WCL7,WFULL
         XR    R0,R0                    CALCULATE SOME OF THE FIDGETS,
         LA    R2,WCL7                    MOD 11
         LA    R14,7
         XR    R15,R15                  CLEAR SUM WORD
         NI    0(R2),R15     RESTRICTIVE ZONING CLAWS
         IC    R0,0(,R2)
         AR    R15,R0
         LA    R2,1(,R2)
         BCT   R14,*-14
         D     R14,=F'11'
         SLL   R14,1
         LA    R2,HUH(R14)   PRESERVE R2 TILL CHECKS
         CLC   WTORCAN+21(4),1(R3)     LAZY WISE GUY ?
         BE    TCONS         YES
         AP    CT,P2         PREVENT RETRY
TESTIT   CLC   1(2,R3),0(R2)    OK ?
         BE    NOPASS        YES
         CLC   3(2,R3),0(R2)   OK ?
         BE    KEY0          YES, GRANT IT
         CP    CT,P1         RETRY TIME ?
         BNH   TCONT         YES, TRY AGAIN
CONDKILL MVC   RC+1(1),RCVAL  SET CONDITIONAL KILL
BYEBYE   LH    R9,RC         GET RETURN CODE                     83128
         PGMEXIT RC=(R9)     RETURN WITH CODE                   GP04042
         SPACE 2
TCONS    MVC   WTOR(WTORLEN),WTORCAN  MOVE CANNED MSG TO DYN. AREA
         LA    R0,REPLY
         LA    R1,ECB
         STM   R0,R1,WTOR    SET REPLY AND ECB ADDRESS
         MVI   WTOR,4         SET REPLY LENGTH
TCONT    XC    REPLY(8),REPLY  CLEAR REPLY AND ECB
         AP    CT,P1         UP RETRY COUNTER
         WTOR  MF=(E,WTOR)
         WAIT  ECB=ECB
         OC    REPLY,BLANKS
         LA    R3,REPLY-1
         B     TESTIT        CHECK REPLY
         SPACE
NOPASS   OI    UTF1,TUFUJI+TUFPASS  SET PASSWORD OVERRIDE        83128
         B     BYEBYE        EXIT
         SPACE
KEY0     OI    UTF1,TUFUJI+TUFZERO  LET USER RUN LOOSE           83128
         B     BYEBYE
         SPACE
UTLEX    OI    UTF1,TUFJOBEX+TUFSTPEX  ALLOW IEFUTL TIME EXT     83128
         B     BYEBYE
         SPACE 2
WTORCAN  WTOR  'DTS666I ''CONS'' ?',HUH,4,HUH,MF=L
CPASS    DC    C'PASS'
CZERO    DC    C'ZERO'
CTIME    DC    C'TIME'
P1       DC    P'1'
P2       DC    P'2'
BLANKS   DC    CL4' '
HUH      DC    C'F0F1F2F3F4F5F6F7F8F9C1'
         SPACE 1                                                 83128
SAVE     DSECT ,                                                 83128
         DC    X'0'
RCVAL    DC    X'04'         RETURN CODE FOR BAD FIELD
RC       DC    H'0'          RETURN CODE FIELD
REPLY    DC    CL4' '        REPLY INPUT FOR 'CONSOLE' OPTION
ECB      DC    F'0'          REPLY ECB
WTOR     WTOR  'DTS666I ''CONS'' ?',REPLY,4,ECB,MF=L
WTORLEN  EQU   *-WTOR
WFULL    DS    F
WCL7     DS    CL7
CT       DS    PL1           RETRY COUNTER
SAVEEND  EQU   *             END OF GETMAIN                     GP04042
         EJECT ,
         PRINT &PRTMAC                                           83128
USERTCB  USERTCB ,
         EJECT ,
         IEFJMR ,
         EJECT ,
         PRINT &PRTSYS                                           83128
USERCVT  USERCVT ,
         SPACE 2
CVTDSECT DSECT ,
         CVT   DSECT=YES
         SPACE 1                                                GP04043
LCTDSECT DSECT ,                                                GP04043
         IEFALLCT ,          LIFE-OF-TASK CONTROL BLOCK         GP04043
         SPACE 2
IEFJCT   DSECT ,             DON'T USE MACRO - HASP CONFLICTS
         DS    7X            SPACER
JCTJPRTY DS    X             JOB PRIORITY IN LOW DIGIT
JCTJBNAM DS    CL8          MAKE NAME DIFFERENT FROM $JCT
         SPACE 2
ACTJNFLD DSECT ,             DON'T USE JCT - HASP MAPPING CONFLICT
         DS    X             THAT'S ALL...
         SPACE 1
         IKJTCB ,                                                83128
         IEZJSCB ,                                               83128
         SPACE 1                                                GP04042
IEFUJI   CSECT ,             JUST IN CASE                        83128
         $MODULE ASVT,REQCBS=ALL,SYSP=(NOGEN,NOGEN,NODATA,NOGEN,NOGEN),*
               TITLE='J E S 2  C O N T R O L  B L O C K S',            *
               MGCRE,              GENERATE MVS MGCRE DSECT            *
               RPL,                GENERATE MVS RPL DSECT              *
               SSOB,               GENERATE MVS SSOB DSECT             *
               $BLDMSGL,           GENERATE HASP BLDMSGL DSECT         *
               $BUFFER,            GENERATE HASP BUFFER DSECT          *
               $CADDR,             GENERATE HASP CADDR DSECT           *
               $CAT,               GENERATE HASP CAT DSECT             *
               $CMB,               GENERATE HASP CMB DSECT             *
               $DCT,               GENERATE HASP DCT DSECT             *
               $ERPL,              GENERATE HASP ERPL DSECT            *
               $EST,               GENERATE HASP EST DSECT             *
               $FSSCB,             GENERATE HASP FSSCB DSECT           *
               $HASB,              GENERATE HASP HASB DSECT            *
               $HASPEQU,           GENERATE HASP EQUATES               *
               $HCCT,              GENERATE HASP HCCT DSECT            *
               $HCT,               GENERATE HASP HCT DSECT             *
               $HFAME,             GENERATE HASP HFAME DSECT           *
               $IOT,               GENERATE HASP IOT DSECT             *
               $JCT,               GENERATE HASP JCT DSECT             *
               $JOE,               GENERATE HASP JOE DSECT             *
               $JQE,               GENERATE HASP JQE DSECT             *
               $KIT,               GENERATE HASP KIT DSECT             *
               $MIT,               GENERATE HASP MIT DSECT             *
               $MITETBL,           GENERATE HASP MTE DSECT             *
               $NHD,               GENERATE HASP NHD DSECTS            *
               $NIT,               GENERATE HASP NIT DSECT             *
               $NWR,               GENERATE HASP NWR DSECT             *
               $PADDR,             GENERATE HASP PADDR DSECT           *
               $PARMLST,           GENERATE HASP PARMLST DSECT         *
               $PCE,               GENERATE HASP PCE DSECT             *
               $PDDB,              GENERATE HASP PDDB DSECT            *
               $PIT,               GENERATE HASP PIT DSECT             *
               $PSV,               GENERATE HASP PSV DSECT             *
               $QGET,              GENERATE HASP QGET PARM LIST        *
               $QSE,               GENERATE HASP QSE DSECT             *
               $RDRWORK,           GENERATE HASP RDRWORK DSECT         *
               $SAFINFO,           GENERATE HASP SAFINFO DSECT         *
               $SCAT,              GENERATE HASP SCAT DSECT            *
               $SJB,               GENERATE HASP SJB DSECT             *
               $SQD,               GENERATE HASP SQD DSECT             *
               $TAB,               GENERATE HASP TAB DSECT             *
               $TGB,               GENERATE HASP TGB DSECT             *
               $TQE,               GENERATE HASP TQE DSECT             *
               $XECB,              GENERATE HASP XECB DSECT            *
               $XEQWORK,           GENERATE HASP XEQWORK DSECT         *
               $XIT                GENERATE HASP XIT DSECT      GP04042
         IEFJSSIB ,                                              83128
.BIGEND  END   ,
