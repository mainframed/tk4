CHKASVT  TITLE 'C H K A S V T  ***  TEST ASVT AVAILABILITY'     GP04127
         MACRO ,                                                GP12162
&NM      INT   &OUT,&IN                                         GP12162
&NM      MACPARM PACKED,&IN,OP=CVD,OPR=CVD,MODE=REV             GP12162
         MVC   &OUT.(6),=X'402020202120'                        GP12162
         ED    &OUT.(6),PACKED+5                                GP12162
         MEND  ,                                                GP12162
         SPACE 1
         PUNCH ' ORDER CHKASVT(P)'  EASIER DUMPS                GP04127
         SPACE 1
         COPY  OPTIONGB                                         GP05162
         SPACE 1
         SYSPARM LIST=YES                                       GP05162
         SPACE 1
*   FROM CBT 464, FILE 473                                      GP04127
*        DISPLAYS THE MAXIMUM AND AVAILABLE NUMBER OF ASID SLOTS.
*        SYS1.CMDLIB(CHKASVT)
CHKASVT  PGMHEAD ZERO,BASE=(R12)                                GP12162
         SERVINIT ,                                             GP12162
         SERVLOAD SUBTPUT                                       GP12162
         SPACE 1
         L     R1,CVTPTR                  GET CVT               GP12162
         USING CVT,R1
         L     R1,CVTASVT                 GET ASVT
         USING ASVT,R1
         L     R2,ASVTMAXU                MAX ASIDS
         LR    R4,R2                      SAVE MAX ASIDS IN REG4
         INT   MSGSUM+10,(R2)     EDIT TO DISPLAY               GP12162
         SLR   R2,R2
         AIF   (&MVSESA).ASCNUM                                 GP05162
         L     R0,ASVTMAXU   GET NUMBER TO TEST                 GP05162
         LA    R3,ASVTFRST   POINT JUST BEFORE FIRST ONE        GP05162
LOOPASCB LA    R3,4(,R3)     POINT TO NEXT SLOT                 GP05162
         ICM   R15,15,0(R3)  TEST THE ASCB ADDRESS              GP05162
         BP    BUMPASCB      NOT AVAILABLE; SKIP                GP05162
         LA    R2,1(,R2)     UP THE COUNT                       GP05162
BUMPASCB BCT   R0,LOOPASCB   DO ALL                             GP05162
         AGO   .ASCCOM                                          GP05162
.ASCNUM  L     R2,ASVTAAV                 GET # AVAILABLE ASIDS
.ASCCOM  INT   MSGSUM+35,(R2)     DISPLAY                       GP12162
         SLR   R2,R2                    CLEAR REG 2(COUNTER)
         LA    R3,ASVTENTY              GO TO 1ST ENTRY ADDR
         LR    R5,R3                 SAVE ADDR FOR COMPARISON
         ICM   R5,8,=X'80'           SET HIGHORDER
         BCTR  R4,0                 REDUCE COUNTER BY ONE
         LA    R6,MSGTWO+58         ADDRESS OF LAST ZZZZZ       GP12162
         LA    R9,MSGTWO+20         ADDRESS OF FRST ZZZZZ       GP12162
         USING CHEAT,R9
         LA    R7,1                 SET REG7 TO ASID=1
         MVC   MSGTWO+21(7*6-1),MSGTWO+20  BLANK INSERTS        GP12162
ASVTLOOP C     R5,0(,R3)            ENTRY = TO MASTER ASID ENTRY ADDR?
*BUG*    BNE   PASSCNT              NO, DON'T COUNT IT AS UNAVAILABLE
         INT   OUTNUM,(R7)   SHOW ID                            GP12162
         LA    R9,6(,R9)            BUMP TO NEXT ZZZZZ IN WTO2
         CR    R9,R6
         BNH   CONTINUE             HAVEN'T FILLED IN ALL ZZZZZ'S YET
         BAL   R10,WTORTN           DISPLAY NON-REUSEABLE ASID NUMBERS
         LA    R9,MSGTWO+20         REINITIALIZE REG9 TO 1ST ZZZZZ ADDR
         MVC   MSGTWO+21(7*6-1),MSGTWO+20  BLANK INSERTS        GP12162
CONTINUE DS    0H
         LA    R2,1(,R2)                BUMP COUNTER
PASSCNT  LA    R3,4(,R3)               GET NEXT ASVT ENTRY
         LA    R7,1(,R7)               BUMP COUNT TO NEXT ASID NUMBER
         BCT   R4,ASVTLOOP             LOOP THRU ENTRIES
         CLI   MSGTWO+25,C' '    ANY DATA FILLED IN?            GP12162
         BE    CONTINU2                 NOPE, BYPASS WTO2
         BAL   R10,WTORTN           DISPLAY NON-REUSEABLE ASID NUMBERS
CONTINU2 INT   MSGSUM+58,(R2)     DISPLAY                       GP12162
         #TPUT MSGSUM                                           GP12162
         SERVTERM ,                                             GP12162
         PGMEXIT RC=RETCODE                                     GP12162
         SPACE 1
WTORTN   #TPUT MSGTWO                                           GP12162
         BR    R10
         SPACE 1
MSGSUM   DC    C'ASID MAX = 12345; ASIDS AVAILABLE = 12345; NON-REUSABL*
               ES = 12345',C' '
MSGTWO   DC    C'NON-REUSEABLE ASIDS: ZZZZZ ZZZZZ ZZZZZ ZZZZZ ZZZZZ ZZZ*
               ZZ ZZZZZ',C' '
         SPACE 1
CHEAT    DSECT ,                                                GP12162
OUTNUM   DS    CL6           FAKE OUTPUT FIELD FOR INT MACRO    GP12162
         SPACE 1
SAVE     DSECT ,                                                GP12162
PACKED   DS    D
         SERVDEFS ,                                             GP12162
SAVEEND  EQU   *                                                GP12162
         SPACE 1
         CVT   DSECT=YES
         IHAASVT
         END   CHKASVT
