DSDEL    TITLE 'S U B D S D E L  ***  DATA SET DELETION'
         COPY  OPTIONGB
         SPACE 1
         SYSPARM LIST=YES
         EJECT ,
***********************************************************************
**                                                                   **
**   Subroutine SUBDSDEL deletes and uncatalogs a data set.          **
**   Calling parameters are (use VL option):                         **
**     DSN - address of data set name (non-VSAM)                     **
**     SER - CL6 field with volume serial                            **
**           CL6 field with * - as below                             **
**           addr omitted or blank - catalog lookup and delete,      **
**               multivolume and MIGRATed supported.                 **
**                                                                   **
**     Return codes: R15  reason R0                                  **
**                     0          0  scratch and uncatalog complete  **
**                     0          4  already scratched; uncataloged  **
**                     0          8  scratched, but uncatalog failed **
**                                     or catalog for different DS   **
**                     4          4  not scratched, not cataloged    **
**                     4          8  scratched, but uncatalog failed **
**                     8          4  not cataloged, no serial given  **
**                     8          8  not scratched, not uncataloged  **
**                     8         12  HDELETE failed                  **
**                                                                   **
***********************************************************************
         PRINT &PRTSOR
         EXTRN SUBUCBLK
SUBDSDEL PGMHEAD ZERO12,BASE=R12,PARM=R9
         SPACE 1
         MVI   WORKBLK,C' '  CLEAR BLANK AREA
         MVC   WORKBLK+1(WORKBLKN-1),WORKBLK     CLEAR ALL
         SPACE 1
         LM    R14,R0,PATUNCAT
         ALR   R15,R13       RELOCATE TRUE DSN
         STM   R14,R0,CAMUNCAT
         SPACE 1
         LM    R14,R0,PATDELIX
         ALR   R15,R13       RELOCATE INDEX
         STM   R14,R0,CAMDELIX
         SPACE 1
         LM    R14,R1,PATDSCB1
         ALR   R15,R13       RELOCATE DSNAME
         ALR   R0,R13        RELOCATE VOLSER
         ALR   R1,R13        RELOCATE WORK AREA
         STM   R14,R1,CAMDSCB1  COMPLETE OBTAIN REQUEST
         SPACE 1
         LM    R14,R1,PATLOCAT
         ALR   R15,R13       RELOCATE USER'S DSN
         ALR   R1,R13        RELOCATE WORK AREA
         STM   R14,R1,CAMLOCAT
         SPACE 1
         LM    R14,R1,PATSCRAT
         ALR   R15,R13       RELOCATE TRUE DSN
         ALR   R1,R13        RELOCATE WORK AREA
         STM   R14,R1,CAMSCRAT
         SPACE 2
         MVI   CURRFG,FGSCR+FGUNC   DEFAULT-SCRATCH AND UNCATALOG
         SPACE 1
         L     R1,0(,R9)     GET DSN POINTER
         MVC   TRUENAME,0(R1)
         LTR   R1,R1
         BM    NOVOL
         L     R1,4(,R9)     LOAD SERIAL POINTER
         MVC   SERIAL,0(R1)  COPY IT
         CLI   SERIAL,C'*'   MULTI-VOLUME REQUEST ?
         BNE   NOVOL         NO
         OI    CURRFG,FGMULTI   INDICATE MULTI-VOLUME REQUEST
         MVC   SERIAL,BLANKS
         SPACE 2
***********************************************************************
*                                                                     *
*    CHECK WHETHER THE DATASET IS IN THE CATALOG; IF IT IS,           *
*    KEEP THE UNCATALOG BIT ONLY IF THE CATALOG ENTRY MATCHES         *
*    PRECISELY.                                                       *
*      NOTE A MULTI-VOLUME CATALOG ENTRY WILL NEVER MATCH,            *
*    EVEN IF THE USER SPECIFIED ONE OF THE VOLUMES IN THE LIST.       *
*                                                                     *
***********************************************************************
NOVOL    LOCATE CAMLOCAT     IS IT CATALOGED ?
         BXLE  R15,R15,HAVECAT   YES
         CLC   SERIAL,BLANKS  DID USER SPECIFY A VOLUME ?
         BNE   CATFAKE       YES; BUILD A FAKE CATALOG ENTRY
LACKVOL  MVICC ,4
         B     POSTPATE      TOO BAD
HAVECAT  CLC   =C'MIGRAT',CATVOL  MIGRATED DATASET?             GP99062
         BE    EXAMNDEV      YES; BYPASS DEVICE TYPE CHECK      GP99062
         TM    CURRFG,FGMULTI  DO MULTI-VOLUME SCRATCH ?
         BNZ   GOSCRAT       YES; SKIP MOUNTING, ETC.
         CLC   =H'1',CATVOL#  ONE VOLUME ENTRY ?
         BNE   CATFATE       NO; TREAT AS UNCATALOGED
         CLI   CATDEV+2,UCB3DACC  DASD DEVICE ?
         BNE   CATFATE       NO; TREAT AS UNCATALOGED
EXAMNDEV CLC   SERIAL,BLANKS  DID USER SPECIFY A VOLUME ?
         BNE   EXAMYLOC      YES; LOOK AT IT
         MVC   SERIAL,CATVOL  USE IT
EXAMYLOC CLC   CATVOL,SERIAL   SAME VOLSER  ?
         BE    GOSCRAT       YES; RETAIN UNCAT REQUEST
CATFATE  MVICC ,8            CATALOGED ELSEWHERE
         SPACE 1
***********************************************************************
*    ENTRY NOT CATALOGED, OR CATALOGED ELSEWHERE; BUILD A FAKE ENTRY  *
***********************************************************************
CATFAKE  SR    R1,R1
         NI    CURRFG,255-FGUNC  DON'T DELETE OTHER CATALOG ENTRY
LOOPUCB  SUBCALL SUBUCBLK    LOOK THROUGH UCBS
         LTR   R1,R1         ALL DONE ?
         BZ    LACKVOL       TOO BAD
         USING UCBOB,R1
         CLI   UCBTBYT3,UCB3DACC   DASD?
         BNE   LOOPUCB       NO; IGNORE
         TM    UCBSTAT,UCBONLI     ONLINE ?
         BZ    LOOPUCB
         CLC   UCBVOLI,SERIAL     PROPER ENTRY?
         BNE   LOOPUCB
*   BUILD A FAKE, SINGLE VOLUME CATALOG ENTRY
         MVC   CATVOL#,=H'1'      FAKE A CATALOG ENTRY
         MVC   CATVOL,SERIAL      COPY USER'S SERIAL
         MVC   CATDEV,UCBTBYT1    MAY NEED AM31 IN XA +
         DROP  R1
         NI    CATDEV+1,255-UCBVLPWR-UCBDVPWR-UCBRR   CLEAN
         CLI   CATDEV+2,X'0A'     3340?
         BNE   GOSCRAT
         NI    CATDEV+1,255-UCBRPS
         B     GOSCRAT
         SPACE 1
***********************************************************************
*                                                                     *
*    SCRATCH THE DATA SET (FROM ALL PACKS IF USER REQUESTED SER=*)    *
*    IF THE DATASET IS NOT FOUND, SET THE CODE TO 1 AND CONTINUE.     *
*    ANY OTHER ERROR INHIBITS THE UNCATALOGING.                       *
*                                                                     *
***********************************************************************
         SPACE 1
GOSCRAT  CLC   =C'MIGRAT',CATVOL
         BE    HSMDS         HDELETE OR FAIL
         SLR   R0,R0         PACK IS MOUNTED - DON'T NEED UCB
         SCRATCH CAMSCRAT    SCRATCH FROM DASD
         LTR   R15,R15       CHECK RETURN CODE
         BZ    POSTSCRT      SCRATCHED, NOW CHECK IF UNCATLG NEEDED
         MVICC ,4            ELSE SET FOR BAD SCRATCH
         CLC   =H'1',VOLSEQ  NOT FOUND ?
         BE    POSTSCRT      OK; NOW CHECK IF UNCATLG NEEDED
         B     POSTPATE      NOT MOUNTED ?
         SPACE 1
***********************************************************************
*                                                                     *
*    MIGRATED DATA SET:                                               *
*    FOR AN EXISTER REQUEST, WE ARE FINISHED.                         *
*    FOR DELETER, INVOKE HSM SERVICES TO DELETE THE DATA SET.         *
*                                                                     *
***********************************************************************
HSMDS    DS    0H                                               GP99062
         AIF   (&MVSXA).HSM  HAVE HSM UNDER THIS ?              GP05032
         MVICC ,12           SET BIG ERROR                      GP05032
         AGO   .NOHSM                                           GP05032
.HSM   ARCHDEL DSN=TRUENAME,WKAREA=HSMWORK,WAIT=YES,PURGE=YES   GP99062
         LTR   R15,R15       DID IT TAKE?                       GP99062
         BZ    POSTLIST                                         GP99062
         MVICC ,12           SHOW BAD MIGRATE DELETION          GP99062
.NOHSM   B     POSTPATE      SET RC=8 AND MAKE ERROR MESSAGE    GP99062
         SPACE 1
***********************************************************************
*                                                                     *
*    THE SCRATCH WAS SUCCESSFUL, OR THE DATASET WAS NOT FOUND.        *
*    IF UNCATALOGING IS APPROPRIATE, DO SO.                           *
*                                                                     *
***********************************************************************
POSTSCRT TM    CURRFG,FGUNC  UNCATALOG REQUIRED ?
         BZ    PGMEXIT       NO; NO MESSAGE, EITHER
         CATALOG CAMUNCAT    REMOVE IT FROM CATLG
         LTR   R15,R15
         BZ    CATGONE       IT'S GONE; WONDERFUL?
         MVICC ,8            SHOW CATALOG ERROR
         B     POSTFOUR      IS IT GONE ?
         SPACE 1
***********************************************************************
*                                                                     *
*    UNCATALOG WAS SUCCESSFUL - NOW CLEAN UP AND DELETE THE           *
*    HIGH LEVEL INDEX ALSO.  IGNORE THE RETURN CODE.                  *
*                                                                     *
***********************************************************************
CATGONE  LOCBYTE TRUENAME,END=TRUENAME+L'TRUENAME,BYTE=C'.'
         SR    R15,R14       LENGTH TO PERIOD
         CH    R15,=Y(L'INDEX)  VALID LENGTH ?
         BH    PGMEXIT       NO
         SH    R15,=H'1'     MAKE EXECUTE LENGTH
         BM    PGMEXIT       ?
         EX    R15,MVCINX    MOVE INDEX NAME
         INDEX CAMDELIX      RELEASE THE INDEX
         B     PGMEXIT       GO TO CLEANUP
         SPACE 2
***********************************************************************
*                                                                     *
*    THE REQUEST HAS BEEN PROCESSED (SUCCESSFULLY OR OTHERWISE);      *
*    NOW WE CLEAN UP AND ISSUE MESSAGES, SET RETURN CODE, ETC.        *
*                                                                     *
***********************************************************************
POSTPATE MVICC 8             SET ERROR
         B     PGMEXIT       AND QUIT THIS REQUEST
         SPACE 1
POSTFOUR MVICC 4             SET LESSER ERROR
         SPACE 1
PGMEXIT  PGMEXIT COPYRET=(RETCODE,8)  RETURN R15, R0
         SPACE 1
MVCVOL   MVC   SERIAL(0),0(R1)  VOLUME SERIAL
MVCINX   MVC   INDEX(0),0(R1)  HIGH-LEVEL INDEX
         SPACE 2
PATUNCAT CAMLST UCATDX,TRUENAME-SAVE  UNCATALOG
PATDELIX CAMLST DLTX,INDEX-SAVE   DELETE HIGH-LEVEL INDEX (IF EMPTY)
PATDSCB1 CAMLST SEARCH,TRUENAME-SAVE,SERIAL-SAVE,OTHWORK-SAVE
PATLOCAT CAMLST NAME,TRUENAME-SAVE,,CATWORK-SAVE  LOCATE CATALOG
PATSCRAT CAMLST SCRATCH,TRUENAME-SAVE,,CATWORK-SAVE,,OVRD  SCRATCH DS
BLANKS   DC    CL80' '       CONSTANCE
         SPACE 1
SAVE     DSECT ,
DB       DS    2D            WORK WORDS
         SERVDEFS ,          SERVICE EXPANSIONS
CAMUNCAT CAMLST UCATDX,TRUENAME  UNCATALOG
CAMDELIX CAMLST DLTX,INDEX   DELETE HIGH-LEVEL INDEX (IF EMPTY)
CAMDSCB1 CAMLST SEARCH,TRUENAME,SERIAL,OTHWORK
CAMLOCAT CAMLST NAME,TRUENAME,,CATWORK  LOCATE CATALOG ENTRIES
CAMSCRAT CAMLST SCRATCH,TRUENAME,,CATWORK,,OVRD  SCRATCH DATASET
         SPACE 1
WORKBLK  DS    0C            START OF BLANKING
TRUENAME DC    CL44' '       NAME AFTER CATALOG LOOKUP
SERIAL   DC    CL6' '        USER'S VOLUME SERIAL
INDEX    DC    CL8' ',C' '   HIGH-LEVEL INDEX OF TRUE NAME + DLTX BLK
WORKBLKN EQU   *-WORKBLK     LENGTH TO MAKE BLANK
         SPACE 1
WORKCLR  DS    0D            START OF ZEROING
CATWORK  DS    35D'0'        CATALOG WORK/RETURN AREA
CATVOL#  EQU   CATWORK,2,C'H'  NUMBER OF VOLUMES
CATDEV   EQU   CATWORK+2,4,C'X'  CATALOG DEVICE TYPE
CATVOL   EQU   CATWORK+6,6,C'C'  VOLUME SERIAL
VOLSEQ   EQU   CATWORK+12,2,C'H'  SCRATCH RETURN CODE
OTHWORK  DS    35D'0'        LOCATE/SCRATCH WORK AREA
         SPACE 1
CURRFG   DS    X             FLAGS FOR THIS DSN
FGDEBUG  EQU   X'80'           DEBUG MODE (SNAPS, ETC.)
FGMULTI  EQU   X'20'           ACCEPT MULTI-VOLUME SCRATCH
FGSCR    EQU   X'10'           SCRATCH DATASET
FGUNC    EQU   X'08'           UNCATALOG DATASET
FGTEST   EQU   X'04'           TEST MODE (NO SCRATCH/DELETE)
WORKCLRN EQU   *-WORKCLR     LENGTH TO ZERO
         SPACE 1
HSMWORK  DC    XL200'00'     HSM WORD AREA                      GP99062
SAVEEND  EQU   *
         SPACE 1
         PRINT NOGEN
         IEFUCBOB ,
         END ,
