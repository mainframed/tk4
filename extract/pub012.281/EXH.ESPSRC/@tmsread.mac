@TMS TITLE '@ T M S R E A D  ***  UCC-1 TMC FULL-TRACK READ ROUTINE'
         COPY  OPTIONGB                                          82314
         SPACE 1                                                 82314
         SYSPARM LIST=YES                                        82314
         SPACE 2                                                 82314
         SPACE 2
*        CALLING SEQUENCE :
*              LA    R0,REQUEST TYPE
*              LA    R1,PARAMETER ADDRESS OR ZERO
*              L     R15,=V(@TMSREAD)
*              BALR  R14,R15
*        FUNCTION CODES (R0) ARE :
*              0     R1=0    FREEMAIN AND CLOSE
*                    R15=0 FOR ALL RETURNS
*              1     R1=0    OPEN
*              1     R1=BXLE ADDRESS  OPEN AND BUILD DISK TABLE ALSO
*                    R15=0 OPENED
*                        4 NO TMC DD CARD FOUND
*                        8 OPEN OR GETMAIN ERROR
*
*              2     R1=0    READ NEXT RECORD (VOLUME ONLY)
*                    R1=BUFFER ADDRESS (MOVE MODE)
*              3     R1 AS ABOVE   READ NEXT RECORD (VOL OR DSNB)
*                    R15=0  READ; R0 HAS BUFFER ADDRESS (LOCATE MODE)
*                        4  END-FILE (OR DSNB IN VOL MODE)
*                        8  I/O ERROR
*                       12  NOT OPEN
*                       16  OTHER ERROR
*
         SPACE 1
@TMSREAD START 0                                        ADDED ON 81123
         PRINT &PRTSOR
         USING @TMSREAD,R12
         STM   R14,R12,12(R13)
         LR    R12,R15       SET BASE
         LA    R9,SAVEAREA
         ST    R13,4(,R9)
         ST    R9,8(,R13)
         LR    R13,R9        SAVE AREA LINKAGE COMPLETE
         SPACE 1
         STM   R0,R1,REQR0   SAVE ENTRY VALUES (AGAIN)
         XC    RETCH(8),RETCH  CLEAR RETURN PARMS
         LA    R2,3          GET HIGH ENTRY PARM
         CR    R0,R2         VALID CALL ?
         BH    EXIT16        NO; FATAL ERROR
         BE    GOGET         GET A RECORD
         LA    R2,1
         CR    R0,R2
         BH    GOGET         READ ENTRY - VOLUME RECORDS ONLY
         BE    GOOPEN        OPEN REQUEST
         LTR   R0,R0         VALID CLOSE REQUEST ?
         BM    EXIT16        NO; FAIL
         LA    R4,CURBUFF-(BUFLINK-BUFSECT)  POINT TO FIRST BUFFER
         USING BUFSECT,R4
DOCLBUFL ICM   R4,15,BUFLINK  GET NEXT BUFFER
         BZ    DOCLOSED      DONE
         TM    BUFLINK,BFPEND  READ PENDING ?
         BZ    DOCLBUFX      NO; DO NEXT
         WAIT  ECB=BUFECB    WAIT FOR COMPLETION
DOCLBUFX MVI   BUFLINK,0     RESET THE FLAGS
         CLC   CURBUFF+1(3),BUFLINK+1  DONE ALL ?
         BNE   DOCLBUFL      TRY NEXT
DOCLOSED MVI   FLAGS,FGEOM+FGNOPEN  SET UNAVAILABLE              81137
         XC    CURBUFF,CURBUFF
         LM    R2,R3,#ADR    GET GOTTEN STORAGE
         LTR   R2,R2         ANY GOTTEN ?
         BZ    DOCLOSE       NO
         LTR   R3,R3         ANY SIZE ?
         BZ    DOCLOSE       NO
         FREEMAIN R,LV=(R3),A=(R2)  RELEASE STORAGE
DOCLOSE  XC    #ADR(8),#ADR  SHOW FREED
         TM    @TMCDCB+DCBOFLGS-IHADCB,DCBOFOPN  OPENED ?
         BZ    EXIT          NO; JUST EXIT
         CLOSE (@TMCDCB)     CLOSE IT
EXIT     L     R13,4(,R13)
         LM    R15,R0,RETCH  GET RETURN VALUES
         L     R14,12(,R13)
         LM    R1,R12,24(R13)  RESTORE EVERYTHING
         BR    R14           RETURN
         SPACE 1
EXIT16   MVI   RETCC,16      SET RETURN CODE
         B     EXIT
EXIT12   MVI   RETCC,12      SET I/O ERROR RETURN
         B     EXIT
EXIT8    MVI   RETCC,8       SET ERROR
         B     EXIT
EXIT4    MVI   RETCC,4       SET EOF OR MINOR ERROR
         B     EXIT
         SPACE 1
GOGET    TM    FLAGS,FGEOF+FGNOPEN  AVAILABLE FOR PROCESSING ?
         BNZ   EXIT12        NO; TAKE MAJOR ERROR EXIT
         TM    FLAGS,FGONE   FIRST TIME ?
         BNZ   NORECONE      YES; GET FIRST BUFFER
REGET    LM    R3,R5,RECBXLE  GET RECORD VALUES FROM LAST READ
         BXH   R3,R4,NOREC   BRANCH IF OUT OF RECORDS
         ST    R3,RECBXLE    REPLACE CURRENT ADDRESS
         L     R15,CURBUFF   GET CURRENT BUFFER                  81137
         CLC   0(4,R3),BUFSEEK+3-BUFSECT(R15)  SAME ADDRESS ?    81137
         BNE   NOREC         NO; SKIP SHORT TRACK                81137
         ICM   R0,3,6(R3)    CHECK DATA LENGTH                   81137
         BZ    SETEOF        TREAT END-FILE AS END               81137
         LA    R3,8(,R3)     SKIP OVER COUNT FIELD
         USING TMRECORD,R3   DECLARE THE TMC RECORD
         CLI   REQR0+3,3     VOL OR DSNB ?                       93025
         BH    NOTABLE       EVERYTHING                          93025
         CLC   =C'TMSCTL#',0(R3)  CONTROL RECORD ?               93025
         BE    REGET         YES; SKIP AND GET NEXT              93025
NOTABLE  CLI   0(R3),X'FF'   DSNB RECORD ?
         BNE   NODSNB
         CLI   REQR0+3,2     TMC ONLY ?
         BE    SETEOF        YES; RETURN END-FILE CODE
NODSNB   ICM   R0,7,REQR1+1  DID USER SUPPLY RECORD ADDRESS ?    92366
         BZ    NOMOVE        NO; LOCATE MODE                     92366
         LA    R1,TMSRSIZE   SET SIZE OF RECORD                  92366
         LR    R2,R3         COPY SOURCE START                   92366
         LR    R3,R1         SOURCE LENGTH                       92366
         MVCL  R0,R2         MOVE TO USER                        92366
         B     EXIT          AND EXIT OK
NOMOVE   L     R1,4(,R13)    GET USER'S SAVE AREA
         ST    R3,24(,R1)    RETURN VALUE IN R1
         B     EXIT          AND EXIT OK
         SPACE 1
NOREC    L     R4,CURBUFF    GET CURRENT BUFFER
         USING BUFSECT,R4    DECLARE IT
         MVI   BUFLINK,0     RESET THE FLAGS
         MVC   CURBUFF,BUFLINK  GET NEXT BUFFER
         BAL   R9,READBUFL   SCHEDULE READ FOR THIS ONE
NORECONE NI    FLAGS,255-FGONE  RESET FIRST-TIME SWITCH
         L     R4,CURBUFF    GET BUFFER ADDRESS BACK
         TM    BUFLINK,BFPEND  IS IT PENDING ?
         BZ    NORECNW       NO
         WAIT  ECB=BUFECB    WAIT FOR COMPLETION
         XI    BUFLINK,BFPEND+BFREAD  SET READ COMPLETE
         CLI   BUFECB,X'7F'  OK ?
         BE    NORECNW       YES
         TM    FLAGS,FGEOE   PRIOR END ?                         81137
         BNZ   SETEOF        YES; IGNORE THIS ERROR, TREAT A END 81137
         CLC   NODATCSW(4),BUFCSW+4  GOT COUNT ONLY ?            92366
         BNE   NORECSR       NO; SET ERROR                       81137
         OI    FLAGS,FGEOE   SET DEFERRED END                    81137
         OI    BUFLINK,BFEOF  SET END IN THIS ONE                81137
         B     NORECNF       AND PROCESS DATA (IF ANY)           81137
NORECSR  OI    BUFLINK,BFERR  SET ERROR FLAG ON                  81137
NORECNW  TM    BUFLINK,BFEOF  IS EOF ON ?
         BZ    NORECNF       NO
SETEOF   OI    FLAGS,FGEOM   SET GLOBAL EOF                      81137
         B     EXIT4         TAKE EOF EXIT
NORECNF  LM    R14,R0,BUFBXLE  GET DATA POINTERS
         SR    R14,R15       ADJUST FOR FIRST BXLE
         STM   R14,R0,RECBXLE  SET FOR RECORD PROCESSING
         TM    BUFLINK,BFERR  I/O ERROR ?
         BZ    REGET         NO; PROCESS FIRST BLOCK
         B     EXIT8         TAKE I/O ERROR EXIT
         DROP  R4
         SPACE 2
GOOPEN   TM    FLAGS,FGNOPEN  OPEN ALREADY ?
         BZ    EXIT          YES; IGNORE THIS REQUEST
         TM    @TMCDCB+DCBOFLGS-IHADCB,DCBOFOPN  WAS OPENED ALREADY ?
         BNZ   GOOGET        YES; SEE IF PRIOR GETMAIN FAILED
         DEVTYPE @TMCDCB+DCBDDNAM-IHADCB,DB  SEE IF DD PROVIDED
         BXH   R15,R15,EXIT4   EXIT WITH 'NO DD' INDICATION
         CLI   DB+2,X'20'    DISK DD ?
         BNE   EXIT4         NO; RETURN 'NO DD' ANYWAY
         NI    DB+3,X'7F'    MAKE SURE?                          87075
         SLR   R3,R3                                             87075
         IC    R3,DB+3       LOAD THE DEVICE SUB-TYPE            87075
         TRKCALC FUNCTN=TRKCAP,TYPE=(R3),R=1,K=0,DD==Y(TMSRSIZE),      *
               REGSAVE=YES,MF=(E,CALCPARM)  GET RECORDS/TRACK    92366
         LTR   R0,R0         ANY AT ALL ?                        87075
         BZ    EXIT8         NO; UNPROCESSABLE DEVICE TYPE       87075
         STH   R0,RECPTRK    ELSE STASH RECORDS/TRACK            87075
         OPEN  (@TMCDCB,INPUT)  OPEN FOR INPUT
         TM    @TMCDCB+DCBOFLGS-IHADCB,DCBOFOPN  OPEN NOW ?
         BZ    EXIT8         NO; RETURN OPEN ERROR
GOOGET   ICM   R1,15,#ADR    GETMAIN DONE ALREADY ?
         BNZ   GOONGET       YES ?
         LH    R0,=AL2(8*1024)  SET FOR 8K OVERHEAD
         LA    R3,TMSRSIZE+8+8    GET BLOCK+COUNT+CCW SIZE       92366
         MH    R3,RECPTRK    TIMES RECORDS PER TRACK
         LA    R3,BUFLEN+8(,R3)  PLUS BUFFER OVERHEAD            93026
         N     R3,=X'7FFFFFF8'  ROUND TO DOUBLE-WORD BOUNDARY    93026
         LR    R4,R3         SAVE TILL LATER
         LR    R2,R3         SET FOR SINGLE BUFFER
         AR    R2,R0         PLUS OVERHEAD
         LA    R15,16        SET FOR 16 BUFFERS (DEFAULT)
         CLI   REQR1,0       USER SPECIFIED BUFNO ?
         BE    *+8           NO
         IC    R15,REQR1     GET USER NUMBER
         STH   R15,DB        CHEAT
         MH    R3,DB         GET USER'S SIZE
         AR    R3,R0         PLUS OVERHEAD
         STM   R2,R3,#SIZE   SET REQUESTED SIZE
         GETMAIN VC,LA=#SIZE,A=#ADR  GET 1 TO N BUFFERS
         BXLE  R15,R15,GOOGOT  PROCESS IF GOTTEN
GOOGETFL XC    #ADR(8),#ADR  JUST IN CASE
         B     EXIT8         SET FOR OPEN/GETMAIN FAILURE
         SPACE 1
GOOGOT   LH    R0,=AL2(8*1024)  GET SYSTEM OVERHEAD AGAIN
         LM    R14,R15,#ADR  GET GOTTEN STORAGE
         SR    R15,R0        ALLOW FOR CHUNK GOING BACK
         XR    R3,R3         ZERO LENGTH
         MVCL  R14,R2        CLEAR GOTTEN STORAGE                92366
         LM    R2,R3,#ADR    GET START ADDRESS AND SIZE
         SR    R3,R0         REMOVE CHUNK RESERVED FOR SYSTEM
         XR    R14,R14
         LR    R15,R3        GET SIZE OF GOTTEN STORAGE
         DR    R14,R4        GET NUMBER OF BLOCKS
         MR    R14,R4        GET SIZE OF COMPLETE BLOCKS
         LA    R15,7(,R15)   ROUND TO DOUBLE-WORD                92366
         N     R15,=X'7FFFFFF8'                                  92366
         LM    R0,R1,#ADR    RELOAD GETMAIN SIZES                92366
         LR    R14,R0        RELOAD START ADDRESS                92366
         STM   R14,R15,#ADR    SAVE ADDRESS/SIZE TO FREE ON CLOSE
         LA    R2,0(R14,R15)   DATA END/START FREE
         AR    R0,R1         END ADDRESS GETMAINED               92366
         SR    R0,R2         LESS ADDRESS GIVES SIZE TO FREE     92366
         FREEMAIN R,LV=(0),A=(R2)  FREE CHUNK FOR SYSTEM
GOONGET  LM    R2,R3,#ADR    GET ADDRESS POINTERS BACK
         ST    R2,CURBUFF    SET CURRENT BUFFER ADDRESS
         AR    R3,R2         SET END ADDRESS +1
         SH    R3,=H'8'        MAKE AN INSURANCE BET             92366
         XC    CURTTR,CURTTR   JUST IN CASE
         USING BUFSECT,R2    DECLARE THE BUFFER
INITBUFF LA    R15,0(R2,R4)  GET ADDRESS OF NEXT BUFFER
         CR    R15,R3        DOING LAST BUFFER ?
         BL    *+8           NO
         L     R15,#ADR      GET FIRST BUFFER
         ST    R15,BUFLINK   SET LINK TO NEXT BUFFER
         MVI   BUFIOB,X'42'  SET UNRELATED; CMD CHAINING
         LA    R14,BUFECB    GET ECB ADDRESS
         ST    R14,BUFECB@
         LA    R14,BUFCCW    SET FIRST CCW ADDRESS
         ST    R14,BUFCCW+8  SAVE ADDRESS FOR TIC
         ST    R14,BUFSTART    SET INTO START
         ST    R14,BUFRESTR    AND RESTART FIELD
         LA    R14,@TMCDCB   GET DCB ADDRESS
         ST    R14,BUFDCB@
         LA    R14,BUFSEEK+3   POINT TO SEEK ADDRESS
         LA    R15,5         SET HARDWARE LENGTH
         STM   R14,R15,BUFCCW
         MVI   BUFCCW,X'31'   SET SEARCH ID EQUAL
         MVI   BUFCCW+4,X'40'  SET CMD CHAIN
         MVI   BUFCCW+8,8    COMPLETE TIC
         LH    R14,RECPTRK   GET BLOCKS ON TRACK
         SLL   R14,3         CHANGE TO CCW LENGTH
         LA    R14,BUFCCWS(R14)  ADDRESS OF FIRST COUNT FIELD
         ST    R14,BUFBXLE   SET START OF DATA
         ICM   R14,8,=X'1E'  MAKE READ COUNT/KEY/DATA OPCODE
         LH    R0,RECPTRK    GET LOOP COUNTER
         LA    R1,BUFCCWS    GET FIRST ADDRESS
         LA    R5,TMSRSIZE+8   GET DATA ADDRESS INCREMENT        92366
         LR    R15,R5        COPY LENGTH                         92366
         ICM   R15,8,=X'60'  MAKE COMMAND CHAIN + SLI FOR EOF    93026
         LA    R6,8          GET CCW INCREMENT
INITCCWS STM   R14,R15,0(R1)  BUILD CCW
         ALR   R14,R5        SPACE TO NEXT DATA RECORD
         AR    R1,R6         AND NEXT CCW
         BCT   R0,INITCCWS   DO ALL
         SLR   R14,R5
         SR    R1,R6         BACK-UP
         MVI   4(R1),0       RESET CMD CHAINING
         LA    R6,0(,R14)    CLEAN HIGH BYTE
         STM   R5,R6,BUFBXLE+4  COMPLETE BXLE PARM
         AR    R2,R4         SPACE TO NEXT BUFFER
         CR    R2,R3         DONE ?
         BL    INITBUFF      NO; DO NEXT
         MVI   FLAGS,FGONE   RESET NOT OPEN/EOF FLAGS
         DROP  R2
         LA    R9,EXIT       SET FOR NORMAL EXIT / PRIME BUFFERS
         SPACE 2
READBUF  L     R4,CURBUFF    GET CURRENT BUFFER ADDRESS
         USING BUFSECT,R4
READBUFL TM    BUFLINK,BFREAD+BFEOF+BFPEND  ANYTHING DONE ?
         BNZ   READBUFN      NO; SKIP
         TM    FLAGS,FGEOE   END OF DATASET ?
         BNZ   READBEOF      YES; QUIT NOW
         L     R15,CVTPTR    GET CVT
         USING CVTMAP,R15    DECLARE IT
         L     R15,CVTPCNVT  GET CONVERT ROUTINE
         DROP  R15
         L     R0,CURTTR     GET TTR
         L     R1,@TMCDCB+DCBDEBAD-IHADCB  GET DEB ADDRESS
         LA    R2,BUFSEEK    GET SEEK ADDRESS
         L     R3,BUFBXLE    GET DATA ADDRESS FOR SAVE AREA
         STM   R9,R13,0(R3)  SAVE VOLATILE REGISTERS
         BALR  R14,R15       CONVERT TTR
         LM    R9,R13,0(R3)  RESTORE REGISTERS
         L     R0,CURTTR     GET CURRENT TTR
         AL    R0,=X'00010000'  UP BY ONE
         ST    R0,CURTTR     SET FOR NEXT READ
         BXLE  R15,R15,READBUFI  OK
READBEOF OI    FLAGS,FGEOE   SET EOE ON LAST TTR CONVERT
         MVI   BUFLINK,BFEOF   SET EOF (INVALID TTR)
         B     READBUFN
READBUFI LA    R1,BUFIOB     POINT TO IOB
         XC    BUFECB,BUFECB  MAKE IT PRETTY
         EXCP  (1)           READ A TRACK
         MVI   BUFLINK,BFPEND  SHOW READ PENDING
READBUFN L     R4,BUFLINK    GET NEXT BUFFER
         CLM   R4,7,CURBUFF+1  DONE ALL ?
         BNE   READBUFL      NO; DO AGAIN
         BR    R9            RETURN TO CALLER
         EJECT ,
         PRINT &PRTSYS                                           82314
SAVEAREA DC    9D'0'
DB       DC    D'0'          WORK AREA
#SIZE    DC    A(08*1024,160*1024)     GETMAIN MIN/MAX
#ADR     DC    A(0,0)
REQR0    DC    A(0)          USER'S R0
REQR1    DC    A(0)          USER'S R1
RETCH    DC    0XL8'0'       RETURNED R15/R0
         DC    XL3'0'
RETCC    DC    X'00'
RETV     DC    A(0)
RECBXLE  DC    3A(0)         CURRENT BUFFER'S BXLE VALUES
CURBUFF  DC    A(0)          CURRENT BUFFER ADDRESS
CURTTR   DC    A(0)          TTR OF LAST EXCP
NODATCSW DC    X'0D00',AL2(TMSRSIZE)  NO DATA CSW ?              93026
@TMCDCB  DCB   MACRF=E,DDNAME=TMC
*              RECFM=F,LRECL=TMSRSIZE,BLKSIZE=TMSRSIZE,KEYLEN=0  92366
CALCPARM TRKCALC MF=L        TRKCALC WORK AREA                   87075
RECPTRK  DC    H'0'          WILL BE NUMBER OF RECORDS/TRACK
FGNOPEN  EQU   X'80'         NOT OPEN
FGEOF    EQU   X'40'         END-FILE RETURNED TO USER
FGEOE    EQU   X'20'         END-FILE IN BUFFER READ
FGEOM    EQU   FGEOF+FGEOE   POSITIVE END                        81137
FGONE    EQU   X'01'         FIRST TIME FLAG
FLAGS    DC    AL1(FGNOPEN)  PROCESSING FLAG
         SPACE 1
BUFSECT  DSECT ,             BUFFER HEADER
BUFLINK  DS    A             ADDRESS OF NEXT BUFFER
BFPEND   EQU   X'80'         READ PENDING
BFREAD   EQU   X'40'         READ COMPLETED
BFERR    EQU   X'20'         I/O ERROR
BFEOF    EQU   X'10'         END OF EXTENTS/FILE
BUFTTR   DS    A             TTR OF CURRENT TRACK
BUFBXLE  DS    3A            DATA AREA BXLE POINTERS
BUFECB   DS    A             I/O ECB
BUFIOB   DS    0D,X'42',AL3  UNRELATED; CMD CHAINING
BUFECB@  DS    A             ADDRESS OF BUFECB
BUFCSW   DS    A,A           CSW
BUFSTART DS    A             IOB START CCW @
BUFDCB@  DS    A             DCB ADDRESS
BUFRESTR DS    A,A           IOB RESTART CCW @
BUFSEEK  DS    XL8           SEEK ADDRESS
BUFCCW   DS    2D            SEEK/TIC + READ CKD CCWS
BUFCCWS  DS    D             VARIABLE NUMBER OF CCWS+DATA
BUFLEN   EQU   *-BUFSECT     FIXED BUFFER OVERHEAD
         SPACE 1
*
         DCBD  DSORG=PS,DEVD=DA
         SPACE 2
         CVT   DSECT=YES
         SPACE 2
         REGEQU
         SPACE 2
         PRINT &PRTMAC                                           82314
         TMSREC ,
TMSRSIZE EQU   *-TMRECORD    SIZE OF TMS RECORD                  92366
         SPACE 1
         END
