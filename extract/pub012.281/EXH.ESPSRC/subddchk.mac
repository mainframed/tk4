SUBDDCHK TITLE 'S U B D D C H K  *** DD/DSN VERIFICATION, LOOKUP, ETC.'
         PUNCH ' ORDER SUBDDCHK(P) '  MAKE DUMPS EASIER
         PUNCH ' ALIAS SUBDJCHK    '
         SPACE 1
         COPY  ASMOPTS
         SPACE 1
         ASMPARM LIST=YES
         SPACE 1
***********************************************************************
**                                                                   **
**  THIS MODULE IS A FRONT END TO SUBDSCHK, ALLOWING DSN/MEMBER      **
**  VALIDATION BY PASSING A DD NAME OR A JFCB.                       **
**                                                                   **
**  COPYRIGHT 2014      EXPERT SYSTEM PROGRAMMING                    **
**                                                                   **
**  AUTHOR: G. POSTPISCHIL                                           **
**                                                                   **
**  DEPENDENCIES:  ANY AMODE, ANY RMODE                              **
**                 CODE IS REENTRANT AND REFRESHABLE                 **
**                                                                   **
**                                                                   **
**  RETURNS R15 = 0   WHEN NAME IS VALID                             **
**              = 8   WHEN NAME IS NOT VALID                         **
**              = 20  ERROR IN PARM LIST                             **
**                                                                   **
**    R15 = 8 RETURN CARRIES A REASON CODE IN R0:                    **
**                                                                   **
**                1   NAME LENGTH IS NON-POSITIVE                    **
**                2   NAME EXCEEDS 44 CHARACTERS                     **
**                3   INDEX LENGTH = 0                               **
**                4   INDEX LEVEL MORE THAN 8 CHARACTERS             **
**                5   NAME HAS TRAILING PERIOD                       **
**                6   FIRST CHARACTER OF LEVEL IS INVALID            **
**                7   OTHER CHARACTER IS INVALID                     **
**                8   NAME HAS NO INDEX LEVELS                       **
**                9   INVALID GDG OR MEMBER SPECIFICATION            **
**               10   NEGATIVE RESPONSE                              **
**               11   DS NOT CATALOGED                               **
**               12   VOLUME NOT AVAILABLE                           **
**               13   DSCB 1 NOT LOCATED                             **
**               14   MEMBER NOT FOUND IN PDS                        **
**               15   ERROR ALLOCATING/READING DIRECTORY             **
**               16   ERROR FINDING JFCB OR DD                       **
**                                                                   **
**  INVOCATION:  CALL SUBDDCHK,('fun',WORKAREA,->DDN,PARM2)          **
**               CALL SUBDJCHK,('fun',WORKAREA,->JFCB,PARM2)         **
**                                                                   **
***********************************************************************
**                                                                   **
**  MAINTENANCE:                                                     **
**                                                                   **
**  GYP  2014-07-28   NEW                                            **
**                                                                   **
***********************************************************************
         SPACE 1
         PRINT &PRTSOR
SUBDDCHK PGMHEAD ZERO12,BASE=R12,AM=31,RM=24,PARM=R1,                  *
               ENTRY=SUBDJCHK,ENTNO=ENTFLAG
         ST    R1,RETCODE+8  RETURN R1 = R1 EXCEPT INDEX FUNCTION
         LA    R0,16         PRESET FOR JFCB NOT FOUND
         ST    R0,RSNCODE
         MVI   TRTBLANK+X'00',4   STOP ON NULL
         MVI   TRTBLANK+X'40',4   STOP ON BLANK
         LR    R10,R1
         LA    R2,CALLPARM
         LA    R0,4               MAX SUPPORTED PARMS
COPYPARM MVC   0(4,R2),0(R1)      COPY ONE WORD
         TM    0(R2),X'80'        WAS VL BIT ON ?
         BNZ   COPYDONE
         LA    R2,4(,R2)
         LA    R1,4(,R1)
         BCT   R0,COPYPARM
         SPACE 1
*   THIRD PARAMETER IS DD NAME. WE COPY LIST TO CALLPARM AND
*     REPLACE THIS BY A POINTER TO DSN OR DSN(MEMBER)
*
COPYDONE L     R8,CALLPARM+8     LOAD DD NAME POINTER
         LR    R6,R8             COPY FOR DJ ENTRY
         CLI   ENTFLAG,1         SUBDJCHK ENTRY ?
         BE    HAVEJFCB            YES; USE USER'S "JFCB"
         L     R14,PSATOLD-PSA    GET MY TCB
         L     R9,TCBTIO-TCB(,R14) GET TIOT
         USING TIOT1,R9           DECLARE IT
         LA    R0,TIOENTRY-TIOT1  INCREMENT TO FIRST ENTRY
*---------------------------------------------------------------------*
*   LOOK FOR FIRST (OR ONLY) DD                                       *
*---------------------------------------------------------------------*
DDCFDD1  AR    R9,R0              NEXT ENTRY
         USING TIOENTRY,R9        DECLARE IT
         ICM   R0,1,TIOELNGH      GET ENTRY LENGTH
         BZ    SETCC8               NO MATCH
         TM    TIOESTTA,TIOSLTYP  SCRATCHED ENTRY?
         BNZ   DDCFDD1              YES; IGNORE
         CLC   TIOEDDNM,0(R8)     MATCHES USER REQUEST?
         BNE   DDCFDD1              NO; TRY AGAIN
         CLI   TIOELNGH,20        SINGLE UCB ?
         BL    SETCC8               NOT EVEN
*---------------------------------------------------------------------*
*   EXAMINE DD ENTRY; PROCESS JFCB                                    *
*---------------------------------------------------------------------*
         SPACE 1
         SLR   R6,R6              CLEAR FOR JFCB
         AIF   (NOT &MVSXA).MVSJFCB
         XC    DDWSWA(DDWSWAL),DDWSWA  CLEAR SWA LIST FORM
         LA    R1,DDWSVA          ADDRESS OF JFCB TOKEN
         ST    R1,DDWEPA
         MVC   DDWSVA+4(3),TIOEJFCB    JFCB TOKEN
         SWAREQ FCODE=RL,EPA=DDWEPA,MF=(E,DDWSWA),UNAUTH=YES
         BXH   R15,R15,SETCC8
         L     R6,DDWSVA          LOAD JFCB ADDRESS
         AGO   .COMJFCB
.MVSJFCB SR    R6,R6              FOR AM31
         ICM   R6,7,TIOEJFCB      SHOULD NEVER BE ZERO
         LA    R6,16(,R6)         SKIP QUEUE HEADER
.COMJFCB USING INFMJFCB,R6
         SPACE 1
HAVEJFCB MVI   OPERAND,C' '
         MVC   OPERAND+1(L'OPERAND-1),OPERAND    BLANK DSN PARAMETER
         OC    OPERAND(L'JFCBDSNM),JFCBDSNM      COPY DSN
         TRT   OPERAND,TRTBLANK                  FIND ENDING BLANK
         CLI   JFCBELNM,C' '                     MEMBER NAME ?
         BH    ADDMEM
         TM    JFCBIND1,JFCPDS+JFCGDG            HAVE MEMBER/GDG NAME?
         BZ    GOCALL                              NO
ADDMEM   MVI   0(R1),C'('                        FRAME IT
         OC    1(L'JFCBELNM,R1),JFCBELNM         COPY MEMBER/GDG
         TRT   0(10,R1),TRTBLANK                 FIND NEW END
         MVI   0(R1),C')'                        SET END
GOCALL   LA    R2,OPERAND
         ST    R2,CALLPARM+8      MAKE NEW CALLING SEQUENCE
         LA    R1,CALLPARM
         LINK  EP=SUBDSCHK        CALL DSN CHECKER
         STM   R15,R0,RETCODE     COPY THE RETURN CODES
         B     PGMEXIT                 RETURN
         SPACE 1
SETCC8R  ST    R0,RSNCODE
SETCC8   SETCC 8
*NEXT*   B     PGMEXIT
         SPACE 1
PGMEXIT  SERVTERM ,
         PGMEXIT COPYRET=(RETCODE,12)  RETURN R15-R1
         SPACE 2
         LTORG ,
         SPACE 2
SAVE     DSECT ,
         SERVDEFS ,          DEFINE RETCODE, ETC.
         SPACE 1
         AIF   (NOT &MVSXA).COMSWA
DDWEPA   DS    A(DDWSVA)
DDWSWA   SWAREQ FCODE=RL,EPA=DDWEPA,MF=L
DDWSVA   DS    7A                 (IBM LIES ABOUT 4A)
DDWSWAL  EQU   *-DDWSWA           LENGTH TO CLEAR
.COMSWA  SPACE 1
OPERAND  DS    CL64          ROOM FOR DSN AND (MEMBER) OR (GDG)
TRTBLANK TRENT FILL=0        ALLOW ALL
         TRENT ,4,C' ',X'00'      STOP ON BLANK OR NULL
         SPACE 1
ENTFLAG  DS    X             0 FOR DDCHK, 1 FOR DJCHK
SAVEEND  EQU   *
         SPACE 1
         PRINT NOGEN
MYTIOT   DSECT ,
         IEFTIOT1 ,
         SPACE 1
MYJFCB   DSECT ,
         IEFJFCBN ,
         SPACE 1
         IEZJSCB ,
         IKJTCB ,
         IHAPSA ,
         AIF   (NOT &MVSXA).MVSEND
         CVT   DSECT=YES
         IEFJESCT ,
.MVSEND  END   ,
