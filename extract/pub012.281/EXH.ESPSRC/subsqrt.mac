SQRT     TITLE 'S U B S Q R T  ***  DOUBLE PRECISION SQUARE ROOT'
*    THIS CODE WAS ADAPTED FROM A DISASSEMBLED COPY OF IHCLSQRT,
*    THE FORTRAN G LIBRARY ROUTINE.
*
*    THE VALUE IS PASSED TO THE ROUTINE IN FPR 0 (DOUBLE)
*    RETURN IS IN FPR 0 WHEN R15=0
*      AND INVALID VALUE SETS 0 INTO FPR 0, AND 4 INTO R15
*
*  ROUTINE IS SERIALLY REUSABLE, BUT NEITHER REENTRANT NOR REFRESHABLE
*
SUBSQRT  START 0
         USING SUBSQRT,R15
         B     BEGIN
         DC    AL1(17),CL17'SUBSQRT &SYSDATE'
BEGIN    STM   R14,R15,12(R13)
         STD   FR2,SAVEF2    SAVE OTHER REGISTERS
         STD   FR4,SAVEF4
         LTDR  FR4,FR0
         BM    NEGATORY      CALLER IS IMAGINING THINGS
         BZ    GOODEXIT      NOTHING TO DO IF ZERO
         STE   FR4,WORKWORD
         L     R0,WORKWORD
         AL    R0,WORKTWO
         SRDL  R0,25
         STC   R0,WORKWORD
         STC   R0,WORKTREE
         LE    FR2,WORKWORD
         AE    FR2,WORKTREE
         ME    FR2,WORKFOUR
         LTR   R1,R1
         BNL   AROUND
         AER   FR2,FR2
         AER   FR2,FR2
AROUND   DER   FR4,FR2
         AUR   FR4,FR2
         HER   FR4,FR4
         LER   FR2,FR0
         DER   FR2,FR4
         AUR   FR2,FR4
         HER   FR2,FR2
         LDR   FR4,FR0
         DDR   FR4,FR2
         AWR   FR4,FR2
         HDR   FR4,FR4
         DDR   FR0,FR4
         SDR   FR0,FR4
         HER   FR0,FR0
         SU    FR0,WORKTREE
         AU    FR0,WORKTREE
         ADR   FR0,FR4
         LD    FR2,SAVEF2    RESTORE OTHERS
         LD    FR4,SAVEF4
GOODEXIT SLR   R15,R15       SET GOOD RETURN
         L     R14,12(,R13)
         MVI   12(R13),X'FF'
         BR    R14
         SPACE 1
NEGATORY SDR   FR0,FR0       SET (INCORRECT) VALUE OF 0
         LA    R15,4         SET BAD RETURN
         L     R14,12(,R13)
         MVI   12(R13),X'FF'
         BR    R14
         SPACE 1
SAVEF2   DC    D'0'
SAVEF4   DC    D'0'
WORKWORD DC    F'0'
WORKTWO  DC    X'31000000'
WORKTREE DC    X'00423A2A'
WORKFOUR DC    X'48385F07'
         SPACE 1
         PRINT ON,NOGEN
         YREGS ,
FR0      EQU   0             FLOATING POINT REGISTER
FR2      EQU   2             FLOATING POINT REGISTER
FR4      EQU   4             FLOATING POINT REGISTER
FR6      EQU   6             FLOATING POINT REGISTER
         END   ,
