TBAK     TITLE 'T M S B A C K S  ***  TMS DISK/TAPE BACKUP LISTING'
         COPY  OPTIONGB                                          81179
         SPACE 2                                                 81179
         SYSPARM LIST=YES                                        81179
         EJECT ,
*        THIS MODULE READS THE TMC AND SELECTS ENTRIES BY
*        DATASET NAME (BACKUP.VOLSER ) FOR BACKUP LISTINGS.
*
         PRINT &PRTSOR                                           81179
TMSBACKS SAVEM ZERO12,BASE=R12,PARM=R9                           90316
         SERVINIT ,          LOAD THE @SERVICE ROUTINE           90316
         SPACE 1                                                 90316
*        ALL CODE RELATED TO DISK ENTRIES IN THE TMC (X00NNN) HAS
*        BEEN REMOVED.  USE DISKLIST PROGRAM FOR DASD ENTRIES.   90316
         SPACE 1                                                 90316
         MVC   OUT,OUT-1     GET SOME BLANKS
         SERVCALL LPALD,=CL8'@TMSREAD'  LOAD TMSREAD CODE        93169
         ST    R0,@TMSREAD   SAVE IT                             93169
         SERVCALL LPALD,=CL8'@PRINTER'  LOAD PRINTER CODE        90316
         ST    R0,@PRINTER   SAVE IT                             81179
         PRTOPEN SYSPRINT,OPT=ABEND   QUIT IF MISSING OR DUMMY   81179
         PRTLIST HEADER,TITLE=1                                  81179
         PRTV  SUBHEAD,TITLE=3,CC=NO                             81179
         SPACE 1
ACTII    TIME  BIN
         STCM  R1,15,CURDAT  SAVE CURRENT DATE                   93169
         OI    CURDAT+L'CURDAT-1,X'0F'  MAKE GOOD DATE           93169
         AP    NEXTWEEK,CURDAT
         CP    NEXTWEEK+L'NEXTWEEK-2(2),=P'366'  OVERFLOW ?      93169
         BL    DATEOK        NO
         BH    DATEYEAR      BUMP A YEAR
         TM    CURDAT+L'CURDAT-3,X'01'   ODD YEAR ?              93169
         BNZ   DATEYEAR      YES; NOT LEAP
         TM    CURDAT+L'CURDAT-3,X'12'   MULTIPLE OF FOUR ?      93169
         BNM   DATEOK        YES; LEAP YEAR (FOR A WHILE)
DATEYEAR AP    NEXTWEEK,=P'635'  REDO FOR NEXT YEAR
DATEOK   READTMC OPEN,BUFNO=2,TEST=NO,EXT=YES  USE TWO TRACK BUFFERS
         BXH   R15,R15,ABEND251  USE TMC SVC IF FAILED           81179
         GETMAIN VC,LA=GTMLIST,A=DB   CHECK REGION SIZE
         BXLE  R15,R15,GOTSTOR  STORAGE OK
         ABEND 804
GOTSTOR  FREEMAIN V,A=DB     FREE IT
         L     R1,DB+4
         SH    R1,=AL2(8*1024)  LEAVE SOME FREE
         ST    R1,SORTSIZE   SET SIZE FOR SORT
         LINK  EP=SORT,PARAM=(SORTPARM),VL=1  GO TO SORT
         LR    R3,R15        SAVE RETURN CODE
         SPACE 1
         SERVTERM ,          CLOSE THE PRINT FILE                90316
         ENDM  RC=(R3)       RETURN WITH APPROPRIATE CODE        90316
         SPACE 2
ABEND251 ABEND 251                                               81179
         EJECT ,
         DROP  ,
E15      STM   R14,R12,12(R13)  SAVE
         USING E15,R15                                           81179
         L     R12,=A(TMSBACKS)  RESTORE BASE
         DROP  R15
         USING TMSBACKS,R12
         LR    R9,R13
         LA    R13,SORTSAVE
         ST    R13,8(,R9)
         ST    R9,4(,R13)    LINK SAVE AREAS
         SPACE 1
LOOP     READTMC VOL,TEST=YES          READ VOLUME RECORDS; TEST CC
         BH    ABEND251      I/O ERROR ?                         81179
         BE    ACTIII        ALL DONE                            81179
         LR    R5,R1         SAVE ADDRESS
         USING TMRECORD,R5   DECLARE IT
         CLI   TMVOLSER,C'$'  VALID ?                            90316
         BL    LOOP          NO; SKIP
         TM    TMFLAG2,F2ETMS  EXPIRED ?                         93169
         BNZ   LOOP          YES; SKIP
         TM    TMFLAG1,F1DELET  DELETED FROM LIB ?               93169
         BNZ   LOOP          YES; SKIP
         CLC   =C'BACKUP.',TMDSN   POSSIBLE BACKUP VOLUME ?
         BNE   LOOP          NO MATCH; SKIP
         CLI   TMDSN+13,C' '  JUST RIGHT LENGTH ?
         BNE   LOOP          NO; SKIP IT
         CLI   TMDSN+12,C' '  6-BYTE VOLSER ?
         BNE   HAVESIX       YES
         CLC   =C'TMC ',TMDSN+7  SPECIAL ?
         BE    HAVESIX       YES; FAKE IT
         CLC   =C'AUDIT ',TMDSN+7  OTHER SPECIAL ?
         BNE   LOOP          NO; IGNORE
HAVESIX  MVC   OUT,OUT-1     BLANK THE RECORD
         CLC   TMVOLSEQ,=H'1'   CONTINUATION VOLUME ?
         BH    LOOP          YES; SKIP
         MVC   VEXPDT-1(7),=X'4021204B202020'
         ED    VEXPDT-1(7),TMEXPDT+L'TMEXPDT-3                   93169
         CP    TMEXPDT,CURDAT  EXPIRED ?
         BL    LOOP          YES
         BH    NOTTODAY
         MVC   VEXPDT(7),=C'*TODAY*'
         B     MOVEVOL
NOTTODAY CP    TMEXPDT,NEXTWEEK  WITHIN A WEEK ?
         BNH   SETSOON       YES; SET FLAGS                      81067
         CLC   =X'9800',TMEXPDT+L'TMEXPDT-3  FUNNY ENTRY ?       93169
         BNE   MOVEVOL       NO                                  81067
SETSOON  MVI   VEXPDT-1,C'*'  FLAG IT                            81067
         MVI   VEXPDT+L'VEXPDT,C'*'
MOVEVOL  MVC   VVOLSER,TMVOLSER  VOLUME SERIAL
         MVC   VDSN,TMDSN    COPY DSNAME                         81067
         CLI   TMNXTVOL,C' '  CONTINUED ?
         BNH   NOTMULT       NO
         MVC   VNXTVOL,TMNXTVOL  SHOW NEXT VOLSER
         CLC   =C'FDR',PIDPGMNM  NORMAL BACKUP ?
         BE    NOTMULT       YES; OK
         CLC   =C'IEHDAS',PIDPGMNM  ALTERNATE BACKUP ?
         BE    NOTMULT       ALSO OK
         CLC   =C'TMS',PIDPGMNM  AUDIT/TMC ?
         BNE   BADMULT       NO; UNUSABLE
         CLI   TMDSN+12,C' '  REALLY AUDIT/TMC ?
         BE    NOTMULT       YES; SHOULD BE OK ?
BADMULT  MVI   VDSN+14,C'?'
         MVC   VDSN+15(28),VDSN+14
NOTMULT  MVC   VCRTDT-1(7),=X'4021204B202020'
         ED    VCRTDT-1(7),TMCRTDT+L'TMCRTDT-3                   93169
         LA    R1,TABDEN
         LA    R0,(TABDENS-TABDEN)/2
DENSLOOP CLC   TMDEN,0(R1)
         BNE   DENSBUMP
         MVC   VDEN,1(R1)
         B     DENSDONE
DENSBUMP LA    R1,2(,R1)
         BCT   R0,DENSLOOP
DENSDONE LA    R1,TABLAB
         LA    R0,(TABLABS-TABLAB)/4
LABSLOOP CLC   TMLTYPE,0(R1)
         BNE   LABSBUMP
         MVC   VLTYPE,1(R1)
         B     LABSDONE
LABSBUMP LA    R1,4(,R1)
         BCT   R0,LABSLOOP
LABSDONE LH    R0,TM#DSNBS
         AH    R0,=H'1'
         CVD   R0,DB         CONVERT TO PACKED                   81179
         MVC   V#DSNBS-1(6),CONMASK  INITIALIZE EDIT MASK        81179
         ED    V#DSNBS-1(6),DB+5                                 81179
         UNPK  DB(5),TMCRUNI(3)   UNPACK HEX DRIVE ADDRESS       81179
         TR    DB+1(3),HEXTAB-C'0'  MAKE EBCDIC                  81179
         MVC   VCRUNI,DB+1   MOVE TO PRINT LINE                  81179
         MVI   VCRUNI-1,C' '
         MVC   VJOBNM,TMJOBNM
         MVC   VSTPNAM,TMSTPNAM
         MVC   VPGMNM,PIDPGMNM
         MVC   VACCT,PIDACCT   ACCOUNT
         MVC   VSUB(4),PIDSUB
         ICM   R0,15,TMBLKCNT   GET BLOCK COUNT
         BZ    FAILBACK      FAILED ?
         CVD   R0,DB         CONVERT TO PACKED                   81179
         MVC   VBLOCK-1(6),CONMASK  INITIALIZE EDIT MASK         81179
         ED    VBLOCK-1(6),DB+5                                  81179
         B     COMBACK
FAILBACK CLI   TMNXTVOL,C' '   CONTINUED ?
         BH    COMBACK       YES; COUNT NOT AVAILABLE
         MVC   VBLOCK,=C'EMPTY'
         MVI   VDSN+14,C'?'
         MVC   VDSN+15(28),VDSN+14
COMBACK  EQU   *
         LA    R1,VOUT       GET RECORD ADDRESS
         LA    R15,12        SET INSERT CODE
         B     E15OUT
         SPACE 1
ACTIII   READTMC CLOSE,TEST=NO         CLOSE THE TMC             81179
         LA    R15,8         SET END OF E15 CODE
E15OUT   L     R13,4(,R13)   GET PRIOR SAVE AREA
         L     R14,12(,R13)  GET RETURN ADDRESS
         LM    R2,R12,28(R13)  RESTORE OTHER REGISTERS
         BR    R14           RETURN
         SPACE 2
         DROP  ,
         DS    0H            JUST IN CASE
         USING *,R15
E35      STM   R14,R12,12(R13)  SAVE
         L     R12,=A(TMSBACKS)  RESTORE BASE
         DROP  R15
         USING TMSBACKS,R12
         LR    R9,R13
         LA    R13,SORTSAVE
         ST    R13,8(,R9)
         ST    R9,4(,R13)    LINK SAVE AREAS
         SPACE 1
         LAT   R1,0(R1),ACTIII   QUIT IF ALL RECORDS PROCESSED   90316
         CLC   PREVOL,VDSN+7-VOUT(R1)  BREAK ?
         BE    PUTOUT        NO
         MVC   PREVOL,VDSN+7-VOUT(R1)  SAVE CURRENT
         MVI   4(R1),C'0'    DOUBLE-SPACE
PUTOUT   PRTV  (R1)          PRINT RECORD                        81179
         LA    R15,4         SET 'DELETE' RETURN CODE
         B     E15OUT
         EJECT ,
HEXTAB   DC    C'0123456789ABCDEF'                               81179
CONMASK  DC    X'402020202120'  EDIT MASK                        81179
DB       DC    D'0'
@SERVICE DC    A(0)          ADDRESS OF @SERVICE ROUTINE         90316
@PRINTER DC    A(0)          ADDRESS OF PRINTER SERVICE ROUTINE  81179
@TMSREAD DC    A(0)          ADDRESS OF TMC READER               93169
SYSPRINT PRTWORK SYSPRINT,TITLE=5,WIDTH=132                      81179
GTMLIST  DC    A(32*1024,512*1024)
SAVEAREA DC    18F'0'        SAVE AREA
SORTSAVE DC    18F'0'        SORT-EXIT SAVE AREA
         SPACE 1
         DC    0F'0',H'0'    ALIGN PARM LIST
SORTPARM DC    AL2(SPE-SPS)  LENGTH
SPS      DC    A(CSORT,CSORTE,CREC,CRECE,E15,E35)
SORTSIZE DC    A(12288)      SET DYNAMICALLY
         DC    X'FF00',C'AP' ALL MESSAGES TO SYSOUT
SPE      EQU   *
CSORT    DC    C' SORT FIELDS=('
         DC    AL1((VDSN+7-VOUT+1)/100+240)      HUNDREDS
         DC    AL1(((VDSN+7-VOUT+1)/10-(VDSN+7-VOUT+1)/100*10)+240) TNS
         DC    AL1((VDSN+7-VOUT+1)-((VDSN+7-VOUT+1)/10*10)+240) UNITS
         DC    C',6,A,'
         DC    AL1((VCRTDT-VOUT+1)/100+240)      HUNDREDS
         DC    AL1(((VCRTDT-VOUT+1)/10-(VCRTDT-VOUT+1)/100*10)+240) TNS
         DC    AL1((VCRTDT-VOUT+1)-((VCRTDT-VOUT+1)/10*10)+240) UNITS
         DC    C',6,D),FORMAT=CH'
CSORTE   DC    0C' '
CREC     DC    C' RECORD TYPE=F,LENGTH=137'
CRECE    DC    C' '
CURDAT   DC    PL4'0'
NEXTWEEK DC    PL4'7'        ADD VALUE FOR NEXT WEEK             93169
PREVOL   DC    XL6'FFFFFFFFFFFF'  PRIOR VOLUME SERIAL IN TAPE LIST
TABDEN   DC    X'D3',C'4'
         DC    X'C3',C'3'
         DC    X'CB',C'2'
         DC    X'83',C'2'
         DC    X'E3',C'C'    3480                                90316
         DC    X'E7',C'C'    3480 COMPACTED                      90316
TABDENS  EQU   *
TABLAB   DC    X'01',CL3'NL'
         DC    X'02',CL3'SL'
         DC    X'04',CL3'NSL'
         DC    X'0A',CL3'SUL'
         DC    X'10',CL3'BLP'
         DC    X'20',CL3'LTM'
         DC    X'40',CL3'AL1'                                    90316
         DC    X'48',CL3'AU1'                                    93169
         DC    X'4A',CL3'AU1'                                    90316
         DC    X'C0',CL3'AL3'                                    90316
         DC    X'CA',CL3'AU3'                                    90316
TABLABS  EQU   *
         SPACE 1                                                 81179
BACKPACK DC    CL7'MONTHLY'   TYPE OF BACKUP SERVICE             81179
BACKPAK2 DC    CL7'WEEKLY'                                       81179
         DC    CL7'DAILY'                                        81179
BACKPAND DC    CL7' NONE'                                        81179
         SPACE 1                                                 81179
VOUT     DC    Y(137,0),C' '
OUT      DS    CL132         NOTE BLANK AT OUT-1
         ORG   OUT
VACCT    DC    CL4' '        ACCOUNT
VSUB     DC    CL4' ',C' '   SUB-ACCOUNT
VVOLSER  DC    CL6' ',C' '
         DC    CL2' '
VNXTVOL  DC    CL6' ',CL4' '
VCRTDT   DC    CL6' ',C' '
VEXPDT   DC    CL6' ',C' '
VDEN     DC    C' ',C' '
VLTYPE   DC    CL3' ',C' '
V#DSNBS  DC    CL5' ',C' '
VCRUNI   DC    CL3' ',C' '
VJOBNM   DC    CL8' ',C' '
VSTPNAM  DC    CL8' ',C' '
VPGMNM   DC    CL8' ',C' '
VBLOCK   DC    CL5' ',C' '   BLOCK COUNT
VDSN     DC    CL44' '
         SPACE 1
         ORG   OUT           REDEFINE FOR DISK RECORD
DPTYPE   DC    CL6' ',C' '   DISK TYPE 2314/3330-1...
DPSEQ    DC    CL5' ',C' '   DP SEQUENCE NUMBER
DPVOL    DC    CL6' ',C' '   VOLUME SERIAL
DPDATE   DC    CL6' ',CL2' '   ENTRY DATE
DPEXPD   DC    CL6' ',CL2' '   EXPIRATION DATE                   81067
DPACCT   DC    CL4' ',C','   ACCOUNT
DPSUBA   DC    CL4' ',CL2' '  SUB-ACCOUNT
DPCHG    DC    CL4' ',CL3' '  BILLING STATUS
DPOWN    DC    CL4' ',CL2' '   OWNERSHIP                         81179
DPSERV   DC    CL7' ',C' '   BACKUP SERVICE                      81179
DPRES    DC    CL8' ',CL2' '   PERM/RESIDENT                     81179
DPMAN    DC    CL44' '       MANUFACTURER/SERIAL
         ORG   ,
         SPACE 1
HEADER   FDOPT NL,CC=C'#'    AUTOMATIC DATE/TIME/PAGE OPTION     81278
         FDPRT '       TAPE LIBRARY LISTING FOR BACKUP TAPES'    81278
         FDPRT *END                                              81278
         SPACE 1                                                 81179
SUBHEAD  VCON  END=SUBHEND                                       81179
SUBSUB   DC    C'ACCOUNT  '
         DC    C'VOLUME CONTINUED  CREATED EXPIRE DN/LB FILES '
         DC    C'UNIT   JOB    STEP    PROGRAM BLOCKS'
         DC    C'      DATASET NAME'
         VCON  ,                                                 81179
         SPACE 1                                                 81179
         LTORG ,
         SPACE 1                                                 90316
SAVE     DSECT ,             SAVE/WORK AREA                      90316
SAVEND   EQU   *             END OF WORK AREA                    90316
         SPACE 1
         PRINT &PRTMAC                                           90316
TMRECORD TMSREC ,                                                90316
         SPACE 1
         PRINT &PRTSYS                                           90316
         CVT   DSECT=YES                                         81179
         SPACE 1                                                 81179
         IHACDE ,                                                81179
         END
