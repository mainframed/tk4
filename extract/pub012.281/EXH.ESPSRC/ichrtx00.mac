ICH      TITLE 'I C H R T X 0 0  ***  S.A.F. INSTALLATION EXIT'
         MACRO                                                   93263
&NM      XTAB  &STRING                                           93263
         GBLC   TABMBAS,&TABMOPT                                GP12043
         LCLC  &TEXT                                             88211
         LCLA  &I,&J,&K,&L                                       93263
&K       SETA  K'&STRING-1                                       93263
         AIF   ('&STRING' EQ '').PARMA                           93263
         AIF   ('&STRING'(1,1) NE '''').COMLEN                   93263
&I       SETA  2                                                 93263
&J       SETA  &K-1                                              93263
&K       SETA  &J                                                93263
.LOOP    AIF   ('&STRING'(&I,2) EQ '''''').SK2                   93263
         AIF   ('&STRING'(&I,2) EQ '&&').SK2                     93263
&I       SETA  &I+1                                              93263
         AGO   .INC                                              93263
.SK2     ANOP                                                    93263
&I       SETA  &I+2                                              93263
&K       SETA  &K-1                                              93263
.INC     AIF   (&I LE &J).LOOP                                   93263
&K       SETA  &K-1                                              93263
         AIF   (&K GE 0).COMLEN                                  93263
&K       SETA  0                                                 93263
.PARMA   MNOTE 8,'MISSING OR CONFLICTING PARAMETERS'             93263
         MEXIT                                                   93263
.COMLEN  ANOP  ,                                                 93263
         AIF   ('&STRING' EQ '*END').LAST                        93263
         AIF   ('&STRING'(1,1) EQ '*').SPEC                      93263
         AIF   ('&STRING'(1,1) EQ '''').TOAP                     93263
&NM      DC    AL1(&K),C'&STRING'                                93263
         MEXIT                                                   93263
.TOAP    ANOP                                                    93263
&NM      DC    AL1(&K),C&STRING                                  93263
         MEXIT                                                   93263
.SPEC    AIF   ('&STRING' EQ '*').PARMA                          93263
&I       SETA  0                                                 93263
.SPHL    AIF   (&I GE &K).SPHEXS                                 93263
&I       SETA  &I+1                                              93263
&J       SETA  1                                                 93263
.SPHLD   AIF   ('&TEXT'(&I,1) EQ '0123456789ABCDEF'(&J,1)).SPHL  93263
&J       SETA  &J+1                                              93263
         AIF   (&J GT 16).PARMA                                  93263
         AGO   .SPHLD                                            93263
.SPHEXS  ANOP                                                    93263
&K       SETA  (&K-1)/2                                          93263
&NM      DC    AL1(&K,0),X'&TEXT'                                93263
         MEXIT                                                   93263
.LAST    ANOP                                                    93263
&TABMOPT SETC  ''                                                93263
&NM      DC    X'FF' .       END OF TABLE                        93263
         MEND                                                    93263
         COPY  OPTIONGB
         SPACE 1
         SYSPARM LIST=YES
         SPACE 2
***********************************************************************
*                                                                     *
*                                                                     *
*        COPYRIGHT 1990  EXPERT SYSTEM PROGRAMMING, INC.              *
*        COPYRIGHT 2003  EXPERT SYSTEM PROGRAMMING                    *
*                        176 OLD STAGE COACH ROAD                     *
*                        BRADFORD, VT 05033-8844                      *
*                                                                     *
*                    ALL RIGHTS RESERVED                              *
*                                                                     *
***********************************************************************
         EJECT ,
         PRINT &PRTSOR
ICHRTX00 START 0                                           ADDED 90260
         STM   R14,R12,12(R13)
         LR    R12,R15
         USING ICHRTX00,R12  DECLARE BASE
         LM    R7,R8,0(R1)   LOAD PARM LIST
         USING SAFP,R7       DECLARE THE PARAMETER LIST
         ST    R8,8(,R13)    USE WORK AREA AS SAVE AREA
         ST    R13,4(,R8)    LINK BOTH WAYS
         LR    R13,R8        MAKE NEW SAVE AREA
         USING SAVE,R13      DECLARE IT
         XC    WORKSAVE(SAVEND-WORKSAVE),WORKSAVE  JUST IN CASE
         SPACE 1
         NOP   EXIT0  **********DEBUG********ZAP FOR TESTING**********
         SPACE 1
***********************************************************************
*                                                                     *
*    DUE TO OUR SERVICE BUREAU ENVIRONMENT, NO FULL-FEATURED          *
*    SECURITY PACKAGE IS DESIRABLE.                                   *
*                                                                     *
*    ALL TAPE ACCESS IS CONTROLLED WITH LOCAL CODE IN OUR T.M.S.      *
*                                                                     *
*    DATASET ACCESS IS CONTROLLED BY HIGH-LEVEL INDEX, BUT ONLY       *
*    FOR THOSE USERS WHO CHOSE TO HAVE THEIR DATA PROTECTED BY        *
*    REGISTERING THEIR INDICES. (DONE AS PART OF ACCOUNTING CODE)     *
*                                                                     *
*    THE ONLY STRICT ACCESS CONTROLS ARE FOR DASD VOLUMES; FOR        *
*    MOST VOLUMES, ALL REQUIRED DATA ARE IN MEMORY, AND THE CHECK     *
*    TAKES AN ACCEPTABLE AMOUNT OF OVERHEAD.                          *
*      WE PRIMARILY WANT TO KEEP USERS FROM CREAMING VITAL            *
*    SYSTEM AND USER PACKS !!!!!                                      *
*                                                                     *
***********************************************************************
         SPACE 1
ICHRTX00 RMODE 24   MASTER SCHEDULER INITIALIZATION FAILS ABOVE LINE
ICHRTX00 AMODE ANY           ALL PROCESSING USES 31-BIT ADDRESSES
         SPACE 1
         CLC   SAFPREQT,=AL2(SAFPFAU) FRACHECK FUNCTION ?        90264
         BE    SETUP         YES, CONTINUE
         CLC   SAFPREQT,=AL2(SAFPAU)     OR RACHECK ?            90264
         BNE   EXITPASS      NEITHER; PASS ON TO RACF ?
         SPACE 1
SETUP    LR    R8,R7         COPY PARAMETER ADDRESS
         A     R8,SAFPRACP   + OFFSET TO COPIED PARAMETER LIST
         USING ACHKLIST,R8   DECLARE IT
         CLI   ACHKLENG,2    LONG ENOUGH ?                       93193
         BNH   EXIT0         HAPPENS DURING 2.4 MASTER SCHEDULER INIT
         TM    ACHKFLG3,255-ACHKVOL  ANYTHING I DON'T HANDLE ?
         BNZ   EXITPASS      YES; PASS THROUGH
         TM    ACHKFLG1,ACHK31IN  DATA IN 31-BIT FORM ?
         BNZ   SETUP31       YES
         SLR   R11,R11
         ICM   R11,7,ACHKENT  GET ENTITY ADDRESS
         SLR   R10,R10
         ICM   R10,7,ACHKVOLS  AND VOLUME SERIAL
         SLR   R9,R9
         ICM   R9,7,ACHKCLN  GET CLASS NAME
         B     SETUPCM       JOIN COMMON CODE
         SPACE 1
SETUP31  L     R11,ACHKEN31  GET ENTITY ADDRESS
         L     R10,ACHKVS31  GET VOLUME SERIAL
         L     R9,ACHKCL31   GET CLASS NAME
         SPACE 1
SETUPCM  LA    R9,0(,R9)     CLEAR HIGH BIT/BYTE
         LTR   R9,R9         ANY ADDRESS
         BZ    EXITPASS      NO; FALL THROUGH
         CLI   0(R9),7       LENGTH=7 ?
         BNE   EXITPASS      NO
         CLC   =C'DASDVOL',1(R9)  DIRECT ACCESS ?
         BE    TESTDASD      TEST VOLUME SERIAL ACCESS
         CLC   =C'DATASET',1(R9)  DATASET NAME ?
         BNE   EXITPASS      NO; NO OTHER TYPES TESTED YET
         SPACE 1
***********************************************************************
*                                                                     *
*    DATASET ACCESS CHECKS                                            *
*                                                                     *
*    EACH REGISTERED INDEX HAS EITHER AN INDEX ENTRY, OR A USERID     *
*    ENTRY IN THE SYSTEM. UNREGISTERED ACCESS IS ALLOWED UNRESTRICTED *
*                                                                     *
***********************************************************************
         SPACE 1
TESTDSN  BALS  R9,COMSET     DO COMMON SETUP AND TESTING         91191
         TM    ACHKFLG3,ACHKTAPE  IS THIS A TAPE ENTRY ?         93193
         BNZ   EXIT0         YES; CHECKED IN TMS EXIT(S)         93193
         LTR   R1,R10        WAS A VOLUME SERIAL SPECIFIED ?     90264
         BNP   TESTDSN2      NO; JUST LOOK AT DSN                90264
         MVC   VOLSER,0(R1)   PASS SERIAL TO BE CHECKED          93249
         BALS  R9,VOLSLOOK   LOOK FOR VOLUME SERIAL ENTRY        93249
         LTR   R15,R15       FOUND ?                             93249
         BZ    EXIT0         NO ENTRY, NO CHECKING               93249
         TM    DB+4,A$V1STOR+A$V1JOBI  SPECIAL STORAGE PACK ?    93249
         BO    EXIT0         YES; NO CHECKING                    93249
         TM    DB+4,A$V1STOR+A$V1JOBF  SPECIAL (OLD) STORAGE PACK ?
         BO    EXIT0                                             93249
         TM    DB+4,A$V1SYSP+A$V1SYSG+A$V1STOR  ANY RESTRICTIONS ?
         BNZ   TESTDSN2      YES; TEST DSN                       93193
         TM    DB+5,A$V2ALLO+A$V2DSNA+A$V2DUIX+A$V2DSN1+A$V2DSNT ?
         BZ    EXIT0         NO NAMING CONTROLS                  93193
TESTDSN2 LTR   R1,R11        REALLY HAVE A DSNAME ?              93193
         BZ    EXIT0         ?                                   93193
         CLI   0(R11),C'*'   IEHMOVE TEMPORARY ?                 93193
         BE    EXIT0         YES; NO CHECK                       93193
         CLC   =C'SYS',0(R11)  POSSIBLE TEMPORARY ?              93193
         BNE   TESTDSN3      NO                                  93193
         CLC   =C'.T',8(R11)  TEMP ?                             93193
         BNE   TESTDSN3                                          93193
         MVC   DB(5),=6C'0'                                      93193
         NC    DB(5),1(R11)  SYSNNNNN ?                          93193
         CLC   DB(5),=6C'0'                                      93193
         BNE   TESTDSN3                                          93193
         MVC   DB(6),=6C'0'                                      93193
         NC    DB(6),10(R11)                                     93193
         CLC   DB(6),=6C'0'                                      93193
         BE    EXIT0         TEMPORARY; ALLOW (?)                93193
TESTDSN3 MVC   A$SPSWD,=8C' '  BLANK INTENDED INDEX              93193
         LA    R0,8          MAX TO CHECK                        93193
*@ DSN2  LR    R1,R11        COPY DSN ADDRESS                    93193
         LA    R2,A$SPSWD                                        93193
TESTDSNL CLI   0(R1),C'.'    END OF INDEX ?                      93193
         BE    TESTDSN4                                          93193
         CLI   0(R1),C' '    END OF NAME ?                       93193
         BE    TESTDSN4                                          93193
         MVC   0(1,R2),0(R1)                                     93193
         LA    R1,1(,R1)                                         93193
         LA    R2,1(,R2)                                         93193
         BCT   R0,TESTDSNL                                       93193
         CLI   0(R1),C'.'    FULL INDEX ?                        93193
         BE    TESTDSN4                                          93193
         CLI   0(R1),C' '    NAME ?                              93193
         BNE   EXIT0         ALLOW ACCESS TO DOS NAMES           93193
TESTDSN4 ACCTSVC A$SVCRB,ERR=EXIT0  EXEC ACCT SVC IF POSSIBLE    93193
         CH    R15,=H'4'     0-OK   4-NOT RESTRICTED             93193
         BNH   EXIT0         ACCEPT AS IS (FOR NOW)              90264
         SPACE 1                                                 93263
*   AT THIS POINT CHECK FOR EXCEPTIONAL DATASETS.  THIS CHECK    93263
*   FUNCTION SHOULD BE MOVED TO ACCTSVC EVENTUALLY?              93263
*                                                                93263
         LA    R2,EXCDSNWT   WRITE-ACCESS EXCEPTIONS             93263
         TM    A$SACES,A$VADELT+A$VALLOC  DESTRUCTIVE CONTROL ?  93263
         BNZ   ASVCFAIL      YES; NO EXCEPTIONS                  93263
         TM    A$SACES,A$VAFRDW  READ/WRITE ?                    93263
         BO    EXCCHECK      YES; CHECK WRITE TABLE              93263
         TM    A$SACES,A$VAFRD  READ ONLY (EXTRANEOUS CHECK)     93263
         BZ    ASVCFAIL      NO; INVALID REQUEST                 93263
         LA    R2,EXCDSNRD   POINT TO EXCEPTION TABLE            93263
EXCCHECK SLR   R3,R3         CLEAR FOR IC                        93263
         LA    R4,44         SET LENGTH OF DSN                   93263
         LA    R1,0(R4,R11)  POINT PAST LAST BYTE                93263
EXCCLDSN BCTR  R1,0          GET PRIOR BYTE                      93263
         CLI   0(R1),C' '    IS IT BLANK ?                       93263
         BNE   EXCCLOOP      NO; R4 HAS LENGTH                   93263
         BCT   R4,EXCCLDSN   TRY PRIOR BYTE                      93263
         B     ASVCFAIL      NULL DSN ?                          93263
EXCCLOOP ICM   R3,1,0(R2)    GET LENGTH BYTE OF NEXT ENTRY       93263
         BM    ASVCFAIL      END OF TABLE - FAIL                 93263
         LA    R15,1(,R3)    MASK LENGTH                         93263
         LA    R14,1(,R2)    MASK LOCATION                       93263
         LR    R0,R4         LENGTH OF DSN                       93263
         LR    R1,R11        DSN ADDRESS                         93263
         CR    R0,R15        IS DSN AT LEAST AS LONG AS THE MASK?
         BL    EXCCBUMP      NO; SKIP TEST                       93263
         BALS  R9,COMPMASK   COMPARE                             93263
         BE    EXIT0         MATCH; PERMIT ACCESS                93263
EXCCBUMP LA    R2,2(R2,R3)   SKIP LENGTH AND DSN                 93263
         B     EXCCLOOP      NO MATCH                            93263
         SPACE 1                                                 93263
ASVCFAIL LM    R0,R1,A$SACCT  SAVE ACCOUNT                       93193
         MVC   WTOLIST(PAT958L),PAT958  MAKE MESSAGE PATTERN     93193
         STCM  R0,15,WTOLIST+OFF958A  SET ACCOUNT                93193
         STCM  R1,15,WTOLIST+OFF958A+4  SET ACCOUNT              93193
         MVC   WTOLIST+OFF958V(L'VOLSER),VOLSER  IF ANY          93193
         MVC   WTOLIST+OFF958D(44),0(R11)  DATASET NAME          93193
         B     FAILMSG       ISSUE MESSAGE                       93193
         SPACE 1
***********************************************************************
*                                                                     *
*    DASD ACCESS CHECKS                                               *
*                                                                     *
*    EACH LEGAL VOLUME HAS AN ENTRY IN THE USERVOL TABLE, WHICH       *
*    IS POINTED TO BY THE USER CVT, AND BUILT BY THE LOCAL SMF        *
*    INITIALIZATION ROUTINE A$GINIT (USING THE ACCOUNTING SVC).       *
*                                                                     *
*    THE USERVOL ENTRY HAS A LIST OF ONE TO FOUR ACCOUNT NUMBERS      *
*    ALLOWED TO ACCESS THE PACK. THE ACCOUNT NUMBER CORRESPONDS       *
*    TO THE RACF GROUP (SOMEWHAT). THE ENTRY MAY BE IN THE FORM       *
*    OF A MASK, USING '?' FOR CHARACTER MATCH, AND '*' (AND HEX       *
*    ZERO) FOR A COMPLETE MATCH.                                      *
*                                                                     *
***********************************************************************
         SPACE 1
TESTDASD BALS  R9,COMSET     GET ACCOUNT(GROUP) AND USER ID, ETC.
      B EXIT0 ***********UNTIL LATER, ALLIGATOR*************     93242
         LTR   R1,R11        IS THERE AN ENTITY ADDRESS ?
         BZ    DASDNENT      NO
         BALS  R9,VOLCHECK   CHECK SERIAL                        91191
         SPACE 1
DASDNENT LTR   R1,R10        IS THERE A VOLUME SERIAL ?
         BZ    DASDNSER      NO
         BALS  R9,VOLCHECK   CHECK SERIAL                        91191
         SPACE 1
DASDNSER ICM   R1,15,ACHKOVOL  IS THERE AN OLD SERIAL
         BZ    EXIT0         NO; DID WE CHECK ANYTHING ?
         BALS  R9,VOLCHECK   CHECK THE SERIAL                    91191
         B     EXIT0         PASSED - BYPASS RACF
         SPACE 1
VOLCHECK CLC   VOLSER,0(R1)  ALREADY CHECKED ONCE ?
         BER   R9            YES; SO WHY DO IT AGAIN
         MVC   VOLSER,0(R1)  MOVE VOLUME SERIAL
         CLI   VOLSER,C' '   VALID ?
         BNHR  R9            YES ?
         ST    R9,DB         SAVE RETURN ADDRESS
         BALS  R9,VOLSLOOK   FIND VOLUME SERIAL ENTRY            93249
         LTR   R15,R15       ANY ?                               93249
         BZ    VOLUNDEF      NO; PERMIT ACCESS                   93249
         USING UVOLSER,R15   DECLARE SINGLE VOLUME ENTRY
         SPACE 1
VOLCHMOR SLR   R5,R5         (RESTART FOR MULTIPLE ENTRIES)
         ICM   R5,1,UVOL#ENT  GET NUMBER OF ACCOUNT NUMBERS
         BZ    VOLNOACC      NO ACCESS PERMITTED
         LA    R4,UVOLACT1   POINT TO FIRST ENTRY
         LA    R0,4          SET NUMBER OF ENTRIES
         MIN   (R5),(R0)     USE SMALLER VALUE TO LOOP
         DROP  R15
         LR    R3,R15        SAVE DEFINITION ADDRESS
         SPACE 1
         USING UVOLACT1,R4   DECLARE THE ENTRY ADDRESS
VOLACCLP LA    R1,A$SACCT    POINT TO ACCOUNT TO BE CHECKED      93193
         LA    R0,L'A$SACCT    AND ITS LENGTH                    93193
         LA    R14,UVOLACT1  POINT TO MASK
         CLI   UVOLACT1,0    ANY ENTRY AT ALL ?
         BE    VOLACCUP      NO; DO NOT TEST
         LA    R15,L'UVOLACT1  SET MASK LENGTH
         TM    UVOLACF1,A$VAFSUB  ANY SUB-ACCOUNT ?
         BZ    *+8           YES
         LA    R15,4         SET LENGTH FOR MAJOR ACCOUNT, ONLY
         LR    R0,R15        JUST IN CASE EXPANSION CHANGED      93263
         BALS  R9,COMPMASK   CHECK FOR MATCH                     91191
         BNE   VOLACCUP      NOT MATCHED
         SPACE 1
*        ACCOUNT MATCH - CHECK ACCESS AUTHORIZATION
*
         MVC   DB+4(1),UVOLACF1  COPY AUTHORIZATION MASK
         NC    DB+4(1),A$SACES   MASK WITH REQUEST TYPE          93193
         CLC   DB+4(1),A$SACES   ALLOWED ?                       93193
         BE    VOLPASS       YES
*DEFER*  B     VOLNOAUT      NOT AUTHORIZED
*DEFER BECAUSE THERE MAY BE ANOTHER MATCHING ENTRY IN THE LIST
         SPACE 1
VOLACCUP LA    R4,UVOLACT2   POINT TO NEXT ENTRY IN LIST
         BCT   R5,VOLACCLP   CHECK IT
         DROP  R4            NO LONGER NEED SINGLE ENTRY
         USING UVOLSER,R3    RESTORE SAVED ADDRESS
         CLI   UVOL#ENT,4    MORE THAN FOUR ENTRIES ?
         BL    VOLNOACC      NO; UNAUTHORIZED REQUEST
*        WHEN MORE THAN 4 MASKS ARE DEFINED FOR A VOLUME, THE NEXT
*        FOUR (MORE OR LESS) ARE PLACED INTO AN ADJACENT ENTRY.
         LM    R0,R1,4(R6)   RELOAD BXLE LENGTH/END ADDRESSES
         LR    R15,R3        COPY CURRENT ENTRY ADDRESS
         BXH   R15,R0,VOLNOACC  NOT AUTHORIZED FOR ACCESS
         CLC   VOLSER,UVOLSER-UVOLSER(R15)  CONTINUATION ?
         BE    VOLCHMOR      YES; DO IT
         B     VOLNOACC      ELSE ACCESS IS DENIED
         DROP  R3                                                93252
VOLPASS  L     R9,DB         RESTORE RETURN ADDRESS
         BR    R9            RETURN - VOLUME ACCESS PERMITTED
         SPACE 1                                                 93249
VOLSLOOK LM    R15,R1,0(R6)  LOAD VOLSER BXLE TABLE
         USING UVOLSER,R15   DECLARE ENTRY                       93252
         SR    R1,R0         ADJUST FOR FIRST TEST
         CR    R15,R1        IS THERE AT LEAST ONE ENTRY ?
         BH    EXIT0         NO; DISASTER - ALLOW THIS ACCESS
VOLSLOOP BXH   R15,R0,VOLSLOPS  VOLUME NOT DEFINED               93252
         CLC   VOLSER,UVOLSER  IS THIS THE CORRECT ENTRY ?
         BH    VOLSLOOP      NOT YET; TRY AGAIN
         BL    VOLSLOPS      NO; FAIL - DEFINITION MISSING
         MVC   DB+4(2),UVOLVOLT  COPY ATTRIBUTE FLAGS            93193
         BR    R9            RETURN OK                           93249
VOLSLOPS SLR   R15,R15       NO MATCH                            93249
         ST    R15,DB+4      CLEAR FLAGS                         93252
         BR    R9                                                93249
         SPACE 1
VOLNOACC DS    0H
VOLNOAUT LM    R0,R1,A$SACCT  PRESERVE ACCOUNT BEFORE OVERLAY    93193
         MVC   WTOLIST(PAT956L),PAT956  MOVE MESSAGE PATTERN
         STM   R0,R1,WTOLIST+OFF956A  SET ACCOUNT                93193
         MVC   WTOLIST+OFF956V(L'VOLSER),VOLSER
         B     FAILMSG       ISSUE MESSAGE AND FAIL
         SPACE 1
VOLUNDEF XC    DB+4(4),DB+4  CLEAR RETURN FLAGS                  93193
         B     VOLPASS       **ALLOW ALL ACCESS TO UNREGISTERED VOL**
*OLUNDEF MVC   WTOLIST(PAT957L),PAT957  MOVE MESSAGE PATTERN
         MVC   WTOLIST+OFF957V(L'VOLSER),VOLSER
*        B     FAILMSG
         SPACE 1
FAILMSG  SLR   R0,R0                                             93252
         IC    R0,WRKUCMID   GET UCM ID                          93252
         LA    R1,WTOLIST    POINT TO MESSAGE                    93252
         CLM   R0,1,=X'FF'   TSO ?                               93252
         BNE   FAILMSGW      NO; ISSUE WTO                       93252
         CLM   R1,8,=X'00'   IS IT BELOW THE LINE ?              93252
         BNE   FAILMSGW      NO; CAN'T TPUT ABOVE THE LINE       93252
         LH    R0,0(,R1)     GET WTO LENGTH                      93252
         SH    R0,*+6        MAKE TRUE MESSAGE LENGTH            93252
         LA    R1,4(R1,0)    POINT TO TEXT; MAKE H'4' CONSTANT   93252
         SVC   93            TPUT                                93252
         B     FAILMSGC                                          93252
FAILMSGW WTO   MF=(E,(1))    WTO                                 93252
FAILMSGC TM    PRIVIES,VAASYS+VAASUP+VAASTC  SPECIAL CALLER ?    92338
         BNZ   EXIT0         ALLOW (WITH GRAVE MISGIVINGS)
 B EXIT0 *****DEBUG*****                                         93193
         B     EXITFAIL      AND FAIL THE REQUEST
         SPACE 1
PAT956   WTO   'MSG956E VOL-SER XXXXXX UNAUTHORIZED FOR XXXXXXXX',     *
               MF=L
PAT956L  EQU   *-PAT956
OFF956V  EQU   20,6,C'C'     OFFSET TO VOLUME SERIAL IN MESSAGE
OFF956A  EQU   44,8,C'C'     OFFSET TO ACCOUNT NUMBER
         SPACE 1
PAT957   WTO   'MSG957E VOL-SER XXXXXX NOT DEFINED - UNAUTH.ACCESSS',  *
               MF=L
PAT957L  EQU   *-PAT957
OFF957V  EQU   20,6,C'C'     OFFSET TO VOLUME SERIAL IN MESSAGE
         SPACE 1
PAT958   WTO   'MSG958E UNAUTH.ACCESS: XXXXXXXX YYYYYY Z23456789Z234567*
               89Z23456789Z23456789Z234',MF=L                    93193
PAT958L  EQU   *-PAT958
OFF958A  EQU   27,8,C'C'     OFFSET TO ACCOUNT                   93193
OFF958V  EQU   36,6,C'C'     OFFSET TO VOLUME SERIAL             93193
OFF958D  EQU   43,44,C'C'    OFFSET TO DATASET NAME              93193
         SPACE 1
EXITPASS SLR   R15,R15       NOTHING DOING HERE - PASS TO RACF
         L     R1,CVTPTR
         ICM   R0,15,CVTRAC-CVTMAP(R1)  RACF UP ?
         BZ    EXITBACK      NO; TAKE FASTER RETURN
         B     EXITCOM
         SPACE 1
EXITFAIL LA    R15,4         SET TO FAIL
         NOP   EXITBACK      RETURN WITH ERROR
         SPACE 1
EXIT0    SLR   R15,R15
EXITBACK LA    R15,200(,R15)  BYPASS RACF FUNCTIONS
EXITCOM  L     R13,4(,R13)   RELOAD OLD SAVE AREA
         LM    R0,R12,20(R13)  RELOAD MOST REGISTERS
         L     R14,12(,R13)  RELOAD RETURN
         RET31 R14           RETURN IN CALLER'S MODE             91191
         EJECT ,
***********************************************************************
*                                                                     *
*        COMMON SETUP AND INITIALIZATION                              *
*                                                                     *
***********************************************************************
         SPACE 1
COMSET   DS    0H                                                93242
*WRACK*  ICM   R6,15,ACHKACEE  IS THERE AN ACEE ?
*OFF*    BZ    COMGET        NO; GET CALLER'S ACCOUNT AND PRIVIES
         XC    A$SVCRB(A$SLENRB),A$SVCRB  CLEAR PARMLIST         92331
*NONE*   LA    R1,A$SMSG     GET ERROR MESSAGE                   92332
*NONE*   ST    R1,A$SERMSG   PASS IT                             92332
*NONE*   XC    A$SMSG(4),A$SMSG  CLEAR PARTIAL MESSAGE AREA      92332
         MVC   A$SID,=C'A$RB'  IDENTIFY                          92331
         MVI   A$SFCTN,A$SFVALX    VALIDATE DATASET/INDEX ACCESS 92331
         MVI   A$SCNTL,A$SAKEY     ACCOUNT PASSED                93193
         LTCB  R14,USE=YES   GET MY TCB                          93193
         L     R15,PSAAOLD-PSA  GET THE ASCB                     93193
         ICM   R15,15,ASCBTSB-ASCB(R15)  TSO ?                   93193
         BZ    COMGETB                                           93193
         L     R15,TCBTIO    GET THE TIOT                        93193
         MVC   A$SUID,0(R15)  JOBNAME IS USER ID                 93193
         OI    A$SCNTL,A$SUKEY                                   93193
         MVI   WRKUCMID,255  SET TSO FLAG
COMGETB  ICM   R15,15,TCBUSER  EXTENSION ?                       93193
         BNZ   COMGETU       YES                                 93193
COMGETO  ICM   R14,15,TCBOTC OLDER ?                             93193
         BZ    COMGETJ                                           93193
         ICM   R15,15,TCBUSER  ANY ?                             93193
         BNZ   COMGETU                                           93193
         B     COMGETO       CLIMB THE LADDER                    93193
COMGETJ  ICM   R14,15,TCBJSTCB  MOMMY                            93193
         BZ    COMGETM       HUH ?                               93193
         ICM   R15,15,TCBUSER                                    93193
         BZ    COMGETM                                           93193
         USING USERTCB,R1                                        93193
COMGETU  ICM   R1,15,UTA@    ANY ACCOUNT ?                       93193
         BZ    COMGETM       NO                                  93193
         MVC   A$SACCT,0(R1)  MOVE IT                            93193
         CLI   UTUD,C' '     IS THERE A USER ID ?                93193
         BNH   COMGETAP      NO                                  93193
         MVC   A$SUID,UTUD   PROPAGATE                           93193
         OI    A$SCNTL,A$SUKEY  SHOW UID SUPPLIED                93193
COMGETAP LA    R1,A$SACCT    POINT TO OUTPUT                     93242
         SERVICE ACTST       TEST ACCOUNT AND GET PRIVILEGES     93242
         B     COMGET0                                           93193
         SPACE 1
COMGETM  LA    R1,A$SACCT    OBTAIN CURRENT ACCOUNT              93242
         SERVICE ACGET       GET ACCOUNT AND PRIVILEGES          93242
         BXH   R15,R15,EXIT0  ALLOW ON DISASTROUS FAILURE        92338
COMGET0  STC   R0,PRIVIES    STASH PRIVILEGE FLAGS
         TM    PRIVIES,VAASTC  IS THIS A START TASK ?
         BNZ   EXIT0         YES; NO RACF, NO FURTHER CHECKING
         SPACE 1
COMCOM   SLR   R15,R15       PREPARE FOR IC
         ICM   R15,8,ACHKFLG2  GET ACCESS FLAGS                  93193
         SRL   R15,1         PUT INTO SECOND BIT
         LA    R0,8          EIGHT BITS TO CONVERT
         OR    R15,R0        MAKE NON-ZERO LOW BIT
         LA    R1,=AL1(A$VADELT,0,0,0,A$VALLOC,A$VAFRDW,A$VAFRD,0)
COMACCLP BXH   R15,R15,COMACCUP  SKIP IF NOT REQUESTED
         OC    A$SACES,0(R1)  MAKE REQUEST BIT                   93193
COMACCUP LA    R1,1(,R1)     POINT TO NEXT FLAG
         BCT   R0,COMACCLP   CONVERT ALL FLAGS
         L     R6,CVTPTR     GET THE CVT
         ICM   R6,15,CVTUSER-CVTMAP(R6)  GET THE USER EXTENSION  91191
         BZ    EXIT0         ***DISASTER***ALLOW
         ICM   R6,15,UCLVOLS-USERCVT(R6)  GET THE VOLUME PTR     91191
         BZ    EXIT0         ***DISASTER***ALLOW
         BR    R9            RETURN TO CALLER
         SPACE 1
*        COMPARE ROUTINE WITH MASK OPTION
*          TO MAKE THE CODE FASTER, MASK LENGTH AND STRING LENGTH
*          ARE DEFINED AS THE SAME REGISTER. TO MAKE IT EASIER TO
*          REMOVE THIS CODE IF NOT DESIRED, ALL CALLERS MAINTAIN
*          BOTH LENGTH REGISTERS (15 AND 0).
*
         PRINT &PRTMAC
COMPMASK CMASK L=R15,MASKEQU='?',MASKEND='*
         PRINT &PRTSYS
         EJECT ,                                                 93263
         PUSH  PRINT                                             93263
*PROD*   PRINT ON,GEN,DATA   *****DEBUG*****                     93263
*   TABLE OF EXCEPTIONAL DATASETS                                93263
*                                                                93263
EXCDSNRD XTAB  WYL90.PROD    WYLBUR PRODUCTION LIBRARY           93263
         XTAB  WYL90.LOAD    WYLBUR DISTRIBUTION LIBRARY         93263
         XTAB  WYL90.LIB     WYLBUR TEST LIBRARY                 93263
         XTAB  SUPPORT.*     ALL CUSTOMER SUPPORT                93263
         SPACE 1                                                 93263
EXCDSNWT XTAB  SYS1.UADS     TMP                                 93263
         XTAB  SYS1.BRODCAST NEWS/MSGS                           93263
         XTAB  CATALOG.*     VSAM CATALOGS ON USER PACKS         93263
         XTAB  *END                                              93263
         POP   PRINT                                             93263
         EJECT ,
SAVE     DSECT ,
         DS    18F           SAVE AREA FOR LOWER LEVEL
         SPACE 1
WORKSAVE DS    0D            START OF WORK SPACE
PRIVIES  DS    X             CALLER'S PRIVILEGE FLAGS
WRKUCMID DS    X             0 FOR WTO, 255 FOR TPUT
VOLSER   DS    CL6           VOL-SER TO BE CHECKED
DB       DS    D             WORK SPACE
         DS    13A           SPARE SPACE SUPPLIED BY CALLER
         SPACE 1
         ORG   DB            RE-USE FOR FAILURE MESSAGES
WTOLIST  DS    CL(PAT956L)
         ORG   WTOLIST
         DS    CL(PAT957L)
         SPACE 1                                                 93193
         ORG   DB+8                                              93193
A$SVCRB  A$SVCRB DSECT=NO                                        93193
         ORG   ,
SAVEND   EQU   *             LIMIT IS 150 BYTES                  90260
         ORG   SAVE+150-(SAVEND-SAVE)  NOT TOO BIG ?
         ORG   ,
         SPACE 1
         PRINT &PRTMAC
         ICHSAFP DSECT=YES   MAP THE CALLING PARAMETER LIST
         SPACE 1                                                 90260
         ICHACHKL ,          MAP RACHECK CALLING PARAMETERS      90260
         SPACE 1
USERCVT  USERCVT ,
         SPACE 1
USERVOLS USERVOLS ,
         SPACE 1
A$VOLUME A$VSER ,            EXPAND ACCOUNT RECORD FOR VOL-SER
         SPACE 1
         PRINT &PRTSYS
         YREGS ,             DEFINE SYMBOLIC REGISTERS          GP02237
         CVT   DSECT=YES
         IHAPSA ,
         IHAASCB ,
         IKJTCB ,                                                93193
         SPACE 1                                                 93193
USERTCB  USERTCB ,                                               93193
         A$GDA DSECT                                             93193
         END   ,
