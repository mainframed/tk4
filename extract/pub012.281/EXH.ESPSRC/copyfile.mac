COPF     TITLE 'C O P Y F I L E  ***   BETTER GENER ?'           93015
*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*
*   PSUTDUMP ** SUPER GENER UTILITY PROGRAM     CBT FILE 132        *
*                                                                   *
*   WE HAVE HAD A PROBLEM THAT HAS ONLY OCCURRED TWICE THIS YEAR    *
*   WHEN EXECUTING THIS UTILITY. IT GIVES A FALSE 'COPYFILE20'      *
*   SAYING THAT THE DD NUMBERING IS UNEVEN.  IT ONLY HAPPENS        *
*   DURING TAPE DRIVE ALLOCATION TIME WHEN A DRIVE IS NOT AVAILABLE *
*   AND THE OPERATOR REPLIES 'HOLD' AND 'WAIT',  WHEN THE ALLOCATION*
*   BECOMES AVAILABLE THIS UTILITY ABENDS.  ALL WE DO IS RESTART    *
*   WITHOUT ANY CHANGES AND IT RUNS OK.  IF ANYONE FINDS A FIX      *
*   BEFORE I DO PLEASE SEND IN A FIX AND A NOTE TO ME.              *
*   AGAIN THIS RARELY HAPPENS AND THIS UTILITY IS WIDELY USED WHERE *
*   I WORK, SO IT'S VERY RELIABLE                                   *
*                                                                   *
*                GEORGE RAMAS                                       *
*                250 MONTECITO AVENUE                               *
*                OKLAND, CALIFORNIA   94610                         *
*                                                                   *
*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*
*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*
*   PRODUCTION SERVICES UTILITY DUMP                                *
*                                                                   *
*   FOR INFORMATION CALL: GEORGE RAMAS                              *
*                         OAKLAND, CALIFORNIA 94610                 *
*                         (415)-763-0540                            *
*           STARTING 1990 (510)                                     *
*                                                                   *
*   THIS UTILITY IS SUPPORTED BY OPERATIONS SUPPORT SERVICES        *
*                                                                   *
*   ***IMPORTANT NOTE***                                            *
*                                                                   *
*   WHEN YOU ARE ASSEMBLING THIS SOURCE, YOU MUST HAVE THE IBM      *
*   MACRO 'IEFJFCBN' AND 'IEFJFCBX' IN YOUR MACLIB.  THESE ARE IBM  *
*   COPYWRITTEN MACROS AND CANNOT BE COPIED TO A NON IBM SHOP.      *
*   UNDER PENALTY OF THE BIG BLUE BOX.  ALSO NEED MACRO 'IEFJFCBE'  *
*                                                                   *
*   COPYFILE IS A SUPER IEBGENER. IT WILL ALLOW YOU TO HAVE         *
*   UP TO 99 INPUTS AND 99 OUTPUTS.  IT CAN BE USED FOR BACKUP      *
*   PURPOSES, PRINTING OR COPY DATA SETS OR ANY COMBINATION         *
*   TOGETHER. FOR RESTART PURPOSES, YOUR SHOULD KEEP YOUR PRINT     *
*   AND BACKUP STEPS SEPARATE.                                      *
*                                                                   *
*   COPYFILE WILL ACCEPT A PARM OF 'CHK'. IF PRESENT, THIS          *
*   PARM WILL PREVENT THE OPENING OF OUTPUT DATASETS IF THE         *
*   INPUT DATASET IS EMPTY.  DO NOT USE THIS PARAMETER WHEN         *
*   STACKING OUTPUT DATA SETS.  THIS IS TO BE USED ONLY WHEN        *
*   CREATING SEPARATE AND INDIVIDUAL OUTPUTXX TAPE DATA SETS.       *
*   WITH THE UNIT PARAMETER OF 'UNIT=(TAPE,,DEFER)' SHOULD BE USED. *
*                                                                   *
*   IF DCB INFORMATION IS NOT SPECIFIED FOR OUTPUTXX IT WILL BE     *
*   COPIED FROM INPUTXX.  ALL RECFM MUST BE IDENTICAL IN THE FIRST  *
*   CHARACTER (IE, 'F'BA , 'V'B, 'F'BM OR 'U' FOR ALL OUTPUTXX.     *
*   YOU CANNOT HAVE A FB GOING TO A VB OR VISA VERSA.               *
*                                                                   *
*   YOU CAN SHORTEN THOUGH AN LRECL ON YOUR INPUTTXX IF YOUR        *
*   SPECIFY THE SHORTEN LRECL ON THE DCB FOR OUTPUTXX.              *
*   THIS CAN BE USED ONLY WHEN A SMALLER PORTION OF A RECORD        *
*   INSTEAD THE ENTIRE LRECL.                                       *
*                                                                   *
*   THIS PROGRAM WILL ALSO DISPLAY IN YOUR SYSOUT SECTION AT THE    *
*   END OF YOUR LISTING THE FOLLOWING INFORMATION:                  *
*                                                                   *
*   EXECUTING JOB NAME                                              *
*   PROC STEP                                                       *
*   JOB STEP                                                        *
*   DDNAME (INPUTXX OUTPUTXX)                                       *
*   INPUTXX RECORD COUNT (SHOWS SEPARATE RECORD COUNTS FOR ALL      *
*                         CANCATENATED DATA SETS) DISK OR TAPE(S)   *
*                                                                   *
*   OUTPUTXX RECORD COUNT (WILL DISPLAY A SEPARATE RECORD COUNT     *
*                          IF OUTPUT IS TAPE, THEN A FINAL TOTAL)   *
*                                                                   *
*   RECFM-LRECL-BLKSIZE FOR INPUTXX AND OUTPUTXX DD                 *
*                                                                   *
*   WHEN MULTIPLE OUTPUT FILES ARE CREATED (REEL 1, 2, 3),          *
*   RECORDS COUNTS FOR EACH SEPARATE TAPE WILL BE DISPLAYED.        *
*   THIS WILL SHOW OUTPUTXX STATISTICS FOR UP TO 20 OUTPUT TAPES    *
*   AND A GRAND TOTAL FOR THE OUTPUT FILE WILL SHOW A FINAL TOTAL   *
*   NUMBER OF RECORDS TOTALLY WRITTEN TO YOUR OUTPUT DD.            *
*                                                                   *
*   USE THE FOLLOWING CODING RULES TO CREATE YOUR JCL.              *
*                                                                   *
*   WHEN DCB IS NOT SPECIFIED, IT WILL BE COPIED FROM INPUT         *
*                                                                   *
*     USE WHEN CREATING BACKUP FILES                                *
*                                                                   *
*     //BACKUP  EXEC PGM=COPYFILE                                   *
*     //*                                                           *
*     //INPUT01    DD  DSN=FNBP.TRAN1025.CHECKING,                  *
*     //               DISP=OLD                                     *
*     //*                                                           *
*     //INPUT02    DD  DSN=FNBP.DEPS2510.CHECKING,                  *
*     //               DISP=OLD                                     *
*                                                                   *
*     //OUTPUT01   DD  DSN=FNBP.BKUP4505.CHECKING(+1),              *
*     //               DISP=(,CATLG,DELETE),                        *
*     //               UNIT=TAPE,                                   *
*     //               VOL=(,RETAIN),                               *
*     //               LABEL=(1,SL,RETPD=30),                       *
*     //               DCB=(FNBDSCB,RECFM=FB,LRECL=80,BLKSIZE=9440) *
*     //*                                                           *
*     //OUTPUT02   DD  DSN=FNBP.DEPS4504.CHECKING,                  *
*     //               DISP=(,CATLG,DELETE),                        *
*     //               UNIT=AFF=OUTPUT01,                           *
*     //               VOL=REF=*.OUTPUT01,                          *
*     //               LABEL=(2,SL,RETPD=30),                       *
*     //               DCB=(FNBDSCB,RECFM=FB,LRECL=80,BLKSIZE=9440) *
*     //*                                                           *
*     //SYSOUT     DD  SYSOUT=*                                     *
*     //SYSUDUMP   DD  DUMMY   **USE ONLY WHEN NECESSARY**          *
*                                                                   *
*      THIS STEP WILL CREATE AN OUTPUT ONLY IF THERE IS INPUT DATA. *
*      DO NOT STACK FILES WHEN USING PARM VALUE, LABEL ERRORS MAY   *
*      OCCUR IF DATA IS MISSING BETWEEN LABEL FILES.                *
*                                                                   *
*     //CHECK   EXEC PGM=COPYFILE,PARM='CHK'                        *
*     //*                                                           *
*     //INPUT01    DD  DSN=FNBP.TRAN1025.CHECKING,                  *
*     //               DISP=OLD                                     *
*     //*                                                           *
*     //INPUT02    DD  DSN=FNBP.DEPS2510.CHECKING,                  *
*     //               DISP=OLD                                     *
*                                                                   *
*     //OUTPUT01   DD  DSN=FNBP.BKUP6505.SAVINGS(+1),               *
*     //               DISP=(,CATLG,DELETE),                        *
*     //               UNIT=(TAPE80,,DEFER),                        *
*     //               LABEL=(1,SL,RETPD=30),                       *
*     //               DCB=(FNBDSCB,RECFM=FB,LRECL=80,BLKSIZE=9440) *
*     //*                                                           *
*     //OUTPUT02   DD  DSN=FNBP.DEPS4504.SLIPS(+1),                 *
*     //               DISP=(,CATLG,DELETE),                        *
*     //               UNIT=(TAPE80,,DEFER),                        *
*     //               LABEL=(1,SL,RETPD=30),                       *
*     //               DCB=(FNBDSCB,RECFM=FB,LRECL=80,BLKSIZE=9440) *
*     //*                                                           *
*     //SYSOUT     DD  SYSOUT=*                                     *
*     //SYSUDUMP   DD  DUMMY   **USE ONLY WHEN NECESSARY**          *
*                                                                   *
*     USE WHEN COPYING INPUT DATA TO DISK (TAPE AND DISK CAN BE     *
*                                          MIXED)                   *
*                                                                   *
*     //COPYDSK EXEC PGM=COPYFILE                                   *
*     //*                                                           *
*     //INPUT01    DD  DSN=FNBP.HOSP5015.PATIENTS,                  *
*     //               DISP=OLD                                     *
*     //*                                                           *
*     //INPUT02    DD  DSN=FNBP.PAYM6025.LATE,                      *
*     //               DISP=OLD                                     *
*                                                                   *
*     //OUTPUT01   DD  DSN=FNBP.HOSP5015.PATIENTS(+1),              *
*     //               DISP=(,CATLG,DELETE),                        *
*     //               UNIT=SYSDA,                                  *
*     //               SPACE=(TRK,(3,1),RLSE),                      *
*     //               DCB=(FNBDSCB,RECFM=FB,LRECL=80,BLKSIZE=9440) *
*     //*                                                           *
*     //OUTPUT02   DD  DSN=FNBP.PAYM6025.LATE(+1),                  *
*     //               DISP=(,CATLG,DELETE),                        *
*     //               UNIT=SYSDA,                                  *
*     //               SPACE=(TRK,(3,1),RLSE),                      *
*     //               DCB=(FNBDSCB,RECFM=FB,LRECL=80,BLKSIZE=9440) *
*     //*                                                           *
*     //SYSOUT     DD  SYSOUT=*                                     *
*     //SYSUDUMP   DD  DUMMY   **USE ONLY WHEN NECESSARY**          *
*                                                                   *
*                                                                   *
*     USE WHEN SENDING DATA TO PRINT                                *
*                                                                   *
*     //PRINT1 EXEC PGM=COPYFILE                                    *
*     //*                                                           *
*     //INPUT01    DD  DSN=FNBP.CHEK3005.REGISTER,                  *
*     //               DISP=OLD                                     *
*     //*                                                           *
*     //INPUT02    DD  DSN=FNBP.HOSP3005.PATIENTS,                  *
*     //               DISP=OLD                                     *
*     //*                                                           *
*     //INPUT03    DD  DSN=FNBP.BANK3005.LOANS,                     *
*     //               DISP=OLD                                     *
*     //*                                                           *
*     //OUTPUT01   DD  SYSOUT=(C,,8011),                            *
*     //               DCB=BLKSIZE=133                              *
*     //*                                                           *
*     //OUTPUT02   DD  SYSOUT=(C,,8011),                            *
*     //               DCB=BLKSIZE=133                              *
*     //*                                                           *
*     //OUTPUT03   DD  SYSOUT=(C,,8011),                            *
*     //               DCB=BLKSIZE=133                              *
*                                                                   *
* >>OR //   DCB=(RECFM=FBA,LRECL=133,BLKSIZE=9310)                  *
*                                                                   *
*     //SYSOUT     DD  SYSOUT=*                                     *
*     //SYSUDUMP   DD  DUMMY   **USE ONLY WHEN NECESSARY**          *
*                                                                   *
*                                                                   *
*      COPYFILE ERROR MESSAGE:                                      *
*                                                                   *
*      COPYFILE10 ** NUMBERING SEQUENCE IS NOT CORRECT FOR INPUTXX  *
*                     AND OUTPUTXX CHECK NUMBERING OR SPELLING      *
*                                                                   *
*      COPYFILE20 ** YOU HAVE DUPLICATE DD NUMBERS IN YOUR INPUTXX  *
*                     OR OUTPUTXX STATEMENTS                        *
*                                                                   *
*      COPYFILE30 ** ARE THERE EQUAL NUMBERING DD STATEMENTS FOR    *
*                     INPUTXX AND OUTPUTXX OR MISSPELLED DD         *
*                     STATEMENTS.                                   *
*                                                                   *
*      COPYFILE40 ** LRECL SPECIFIED ON OUTPUT DOES NOT MATCH       *
*                      INPUT DCB                                    *
*                                                                   *
*      COPYFILE60 ** INVALID PARM VALUE SPECIFIED                   *
*                                                                   *
*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*
*        SOURCE MODIFICATION ON 02/07/80 TO ALLOW FOR INPUT(TAPE)
*          TO OUTPUT(DASD,UNIT RECORD, OR TAPE)
*
*    SOURCE MODIFICATION ON 12/13/88 TO DISPLAY RECORD COUNTS
*    SOURCE MODIFICATION ON 05/08/89 TO COPY DCB FROM INPUT TO OUTPUT
*
*
*        REGISTER ASSIGNMENT
*        0 - STANDARD LINKAGE
*        1 - STANDARD LINKAGE
*        2 - TRT MAY DESTROY THE CONTENTS OF THIS REG
*        3 - INITIALIZATION AND WORKINK REG
*        4 - "               "    "      "
*        5 - INPUT DCB
*        6 - OUTPUT DCB
*        7 - OUTPUT DD TABLE
*        8 - INPUT DD TABLE
*        9 -
*       10 - SUBROUTINE CALL/RETURN
*       11 - FIRST BASE
*       12 - SECOND BASE
         SPACE 2                                                 93015
         COPY  OPTIONGB      COPY GLOBAL OPTIONS                 93015
         SPACE 1                                                 93015
         SYSPARM LIST=YES    SET AND LIST OPTIONS                93015
         EJECT ,                                                 93015
         PRINT &PRTSOR       SAVE A BUSH                         93015
COPYFILE PGMHEAD ZERO15,BASE=(R11,R12),PARM=R9,BNDRY=PAGE        93015
         BAL   R10,SETPARM
         SERVINIT ,          LOAD @SERVICE ROUTINE               93015
         SERVCALL LPALD,=CL8'@PRINTER'  GET THE PRINTER ROUTINE  93015
         ST    R0,@PRINTER                                       93015
         USING  IHADCB,R5
         LA    R8,INTABLE          LOAD ADDR OF INPUT DD TABLE.
         LA    R7,OUTABLE          LOAD ADDR OF OUTPUT DD TABLE.
         LA    R5,INPUT            LOAD ADDR OF INPUT DCB.
         LA    R6,OUTPUT           LOAD ADDR OF OUTPUT DCB.
         L     R3,16               -> TO CVT.
         L     R3,0(R3)            -> TO TCB ->'R IN CVT.
         L     R3,4(R3)            -> TO TCB.
         L     R3,12(R3)           -> TO TIOT.
         ST    R3,TIOTPTR          -> TO TIOT.
         SPACE 1
         PRTOPEN SYSOUT,OPT=ABEND
         MVC   IPCLR,=CL133' '    BLANK STATUS LINES             93015
         MVC   OPCLR,=CL133' '                                   93015
         BAL   R10,DSPTITLE
*
         LA    R4,24               SO NEXT INST WILL -> TO DD ENTRY.
SCAN     LA    R3,0(R4,R3)         -> TO NEXT OR FIRST DD ENTRY.
         LTR   R4,R4               SET COND CODE.
         BZ    SORTIT              ALL DD'S CHECKED - GO SORT THEM.
         IC    R4,0(R3)            NEXT TIME WILL -> TO NEXT ENTRY.
         CLC   4(5,R3),DDNAME(R5)  IS IT AN INPUT DD ?
         BNE   SCAN2               NO - GO SEE IF IT'S AN OUTPUT DD.
         CLI   11(R3),C' '         INPUT DD NAME LENGTH OF 7 ?
         BNE   SCAN                NO GO GET NEXT DD ENTRY.
         TRT   9(2,R3),NUMTAB      IS INPUT DD NUMBER NUMERIC ?
         BC    R7,SCAN             NO GO GET NEXT DD ENTRY.
         MVC   0(2,R8),9(R3)       YES PUT IT IN THE INPUT TABLE.
         LA    R8,2(R8)            -> TO NEXT SPACE IN INPUT DD TABLE.
         B     SCAN                GO GET NEXT DD ENTRY.
SCAN2    CLC   4(6,R3),DDNAME(R6)  IS IT AN OUTPUT DD ?
         BNE   SCAN                NO GO GET NEXT DD ENTRY.
         TRT   10(2,R3),NUMTAB     IS OUTPUT DD NUMBER NUMERIC ?
         BC    R7,SCAN             NO GO GET NEXT DD ENTRY.
         MVC   0(2,R7),10(R3)      YES PUT IT IN THE OUTPUT DD TABLE.
         LA    R7,2(R7)            -> TO NEXT SPACE IN OUTPUT DD TABLE.
         B     SCAN                GO GET NEXT DD ENTRY.
*********************************************************************
*
SORTIT   SR    R3,3                ZERO IN REG 3.
         LA    R2,INTABLE          ADDR OF INPUT DD TABLE IN REG 2.
         SH    R8,=H'2'            -> REG 8 TO LAST ENTRY IN TABLE.
         LR    R4,R2               SAVE ->'R TO 1ST ENTRY IN TABLE.
         BAL   R10,CHECK           GO SORT INPUT TABLE.
         SR    R3,R3               RESET REG 3 TO ZERO.
         LA    R2,OUTABLE          ADDR OF OUTPUT DD TABEL IN REG 2.
         LR    R8,R7               ADDR OF LAST ENTRY NOT USED OF
*                                  OF OUTPUT DD TABLE FROM REG 7 - 8.
         SH    R8,=H'2'            -> REG 8 TO LAST ENTRY IN TABLE.
         LR    R4,R2               SAVE ->'R TO 1ST ENTRY IN TABLE.
         BAL   R10,CHECK           GO SORT OUTPUT DD TABLE.
         CLC   INTABLE,OUTABLE     IS THERE AN INPUT DD FOR EACH OUTPUT
*                                  DD AND CONVERSLY.
         BE    DOIT                YES THEN CONTINUE PROCESSING.
        WTO    '*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*',  X
               'COPYFILE30 DD NUMBERING IS UNEQUAL, CHECK DD  ',       X
               '           NUMBERING, POSSIBLE DUPLICATE NUMBERS',     X
               '           OR THERE IS NOT A ONE FOR ONE NUMBERING',   X
               '*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*',  X
               ROUTCDE=(2)
         ABEND 30,DUMP,STEP          NO THEN ABEND WITH USER ABEND 30.
DOIT     DS    0H                                                93015
         MVC   DDNAME+5(2,5),0(4)  SET UP FIRST DD TO BE DUMPED.
         MVC   DDNAME+6(2,6),0(4)  SET UP FIRST DD TO BE DUMPED TO.
RUN      DS    0H
         MVC   DDNAME+5(2,5),0(4)  SET UP  DD TO BE DUMPED.
         MVC   DDNAME+6(2,6),0(4)  SET UP  DD TO BE DUMPED TO.
*
         BAL   R10,INITCTR
         OPEN  (INPUT,(INPUT))
         RDJFCB (INPUT)          GET INFO FOR 1ST FILE
         MVC   INJFCB,INFMJFCB
         MVC   IRECFM,JFCRECFM
         MVC   ILRECL,JFCLRECL
         MVC   IBLKSI,JFCBLKSI
RDJFOUT  DS    0H
         RDJFCB (OUTPUT)
         MVC   ORECFM,JFCRECFM
         MVC   OLRECL,JFCLRECL
         MVC   OBLKSI,JFCBLKSI
         CLC   ORECFM,IRECFM      OUTPUT RECFM SAME AS INPUT?
         BE    SAME01
         CLI   ORECFM,B'0'        RECFM UNSPECIFIED?
         BE    DUPRECFM           YES
*
*   COMPARE HI 2 BITS
*
         IC    R14,IRECFM    GET INPUT RECORD FORMAT             93015
         IC    R15,ORECFM    GET OUTPUT FORMAT                   93015
         LA    R0,JFCVAR+JFCFIX  MAKE MASK FOR MAJOR OPTION      93015
         NR    R14,R0                                            93015
         NR    R15,R0                                            93015
         CR    R14,R15       SAME MAJOR OPTION ?                 93015
         BE    SAME01             YES, LEAVE ALONE
        WTO    '*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*',  X
               'COPYFILE40 RECFM INCOMPATIBLE                 ',       X
               '*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*',  X
               ROUTCDE=(2)
         ABEND 40,DUMP,STEP          NO THEN ABEND WITH USER ABEND 40.
DUPRECFM DS    0H
         MVC   JFCRECFM,IRECFM    FORCE SAME AS INPUT
         MVC   ORECFM,JFCRECFM
SAME01   DS    0H
         CLC   JFCLRECL,=H'0'     OUTPUT LRECL UNSPECIFIED?
         BNE   SAME02
         MVC   JFCLRECL,ILRECL    YES, FORCE SAME AS INPUT
         MVC   OLRECL,JFCLRECL
SAME02   DS    0H
         CLC   JFCBLKSI,=H'0'     OUTPUT BLKSI UNSPECIFIED?
         BNE   SAME03
         MVC   JFCBLKSI,IBLKSI    YES, FORCE SAME AS INPUT
         MVC   OBLKSI,JFCBLKSI
SAME03   DS    0H
         MVC   OUTJFCB,INFMJFCB   SAVE OUTPUT JFCB
         BAL   R14,OPFORMAT
         MVC   OPVOL,=C'*NONE*'
         MVC   INFMJFCB(176),INJFCB RESTORE JFCB FROM INPUT
*
GET      GET   INPUT               READ INPUT RECORD.
         ST    R1,IPRCD
*
CHKDCB   DS    0H
         CLI   DCBSW,C'2'          TAKEN DCB EXIT?
         BNE   CHKOPEN             NO
         MVI   DCBSW,C'1'          RESET
         BAL   R10,DSPIPCNT        DISPLAY INPUT COUNTS
         RDJFCB (INPUT)            GET INFO FOR NEXT FILE
         B     GET                 REDO LAST READ
*
CHKOPEN  DS    0H
         CLI   OPENSW,C'Y'         IS OUTPUT OPEN?
         BE    CHKEOV
*
         MVI   OPENSW,C'Y'
         MVC   INFMJFCB(176),OUTJFCB
         OPEN  (OUTPUT,(OUTPUT)),TYPE=J
         RDJFCB (OUTPUT)         GET INFO FOR OUTPUT FILE
         BAL   R14,OPFORMAT      FORMAT OUTPUT JFCB INFO
         MVC   INFMJFCB(176),INJFCB RESTORE INPUT JFCB
*
CHKEOV   DS    0H
         CLI   EOVSW,C'Y'          VOLUME CHANGE?
         BNE   PUT                 NO
         MVI   EOVSW,C'N'          RESET
         BAL   R10,DSPIPCNT        DISPLAY INPUT COUNTS
         BAL   R10,INCRVOL         POINT TO NEXT VOL SER
         SPACE 1
PUT      L     R3,IPRCD
         TM    ORECFM,JFCVAR+JFCFIX  UNDEFINED ?                 93015
         BM    PUTVARF       NO                                  93015
         MVC   OUTPUT+LRECL(2),INPUT+LRECL  COPY LENGTH          93015
         B     PUTCOM                                            93015
PUTVARF  TM    ORECFM,JFCVAR       IS OUTPUT VARIABLE?
         BZ    PUTCOM              NO
         CLC   0(2,R3),OLRECL      IS LRECL < MAX?
         BNH   PUTCOM              YES
         MVC   0(2,R3),OLRECL      NO, TRUNCATE
PUTCOM   PUT   OUTPUT,(R3)         WRITE RECORD
INCRCTR  DS    0H
         AP    IRCDCTR,=P'1'       INCR INPUT  COUNTER
         AP    ORCDCTR,=P'1'       INCR OUTPUT COUNTER
         B     GET                 GO GET ANOTHER ONE.
*******************************************************************
*
EODAD    DS    0H
         BAL   R10,DSPIPCNT
         CLOSE (INPUT)
         CLI   OPENSW,C'Y'   IS OUTPUT FILE OPEN?
         BE    CLOSEOP
         CLI   OSPARM,C'C'   SHOULD IT BE?
         BE    FREEPOOL      NO, 'CHK' PARM PRESENT
         MVI   OPENSW,C'Y'
         MVC   INFMJFCB(176),OUTJFCB  RESTORE OUTPUT JFCB
         OPEN  (OUTPUT,(OUTPUT)),TYPE=J
         RDJFCB (OUTPUT)         GET INFO FOR OUTPUT FILE
         BAL   R14,OPFORMAT      FORMAT OUTPUT JFCB INFO
CLOSEOP  DS    0H
         CLOSE (OUTPUT)
         FREEPOOL OUTPUT
FREEPOOL DS    0H
         BAL   R10,DSPOPCNT
         FREEPOOL INPUT
         LA    R4,2(R4)       POINT TO NEXT ENTRY IN TABLE.
         CR    R4,R7          AT END OF TABLE ?
         BL    RUN            NO GO DUMP NEXT FILE.
*
*                 E O J
*
         SERVTERM ,          CLOSE AND FREE EVERYTHING
         LH    R9,RETCH      LOAD RETURN CODE                    93015
         PGMEXIT RC=(R9)     RETURN WITH CONDITION CODE          93015
         SPACE 2                                                 93015
********************************************************************
*
SORT     LA    R2,2(2)        INCR TO NEXT 2 ENTRIES IN TABLE.
         USING SORTWORK,R2
CHECK    CR    R2,R8          AT END OF UNSORTED TABLE ?
         BE    TABLEND        YES GO SEE IF WE'RE DONE.
         BL    COMPARE        NO CHECK SEQ.
         WTO   '*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*',      X
               'COPYFILE10 NUMBERING SEQUENCE NOT CORRECT, CHECK',     X
               '           NUMBERING SEQUENCE OR DD NAME SPELLING',    X
               '*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*',      X
               ROUTCDE=(2)
         ABEND 10,DUMP,STEP   DD STATMENT MISSING FOR INPUT OR OUTPUT.
COMPARE  CLC   LOW,NEXT       NO ARE THESE 2 ITEMS IN SEQ ?
         BL    SORT           YES GO TO NEXT ONE.
         BH    SWAP           NOT IN SEQ GO SWAP THEM.
        WTO    '*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*',  X
               'COPYFILE20   DUPLICATE INPUT OR OUTPUT DD NUMBERING',  X
               '           CHECK INPUT AND OUTPUT SEQUENCE NUMBERS',   X
               '*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*',  X
               ROUTCDE=(2)
*                             CANT HAVE 2 DD NAMES THE SAME
         ABEND 20,DUMP,STEP   WHAT ARE YOU TRYING TO DO ???
SWAP     LH    R3,LOW         SAVE LOW OUT OF SEQ ITEM IN REG 3.
         MVC   LOW,NEXT       MOVE LOW ITEM DOWN IN TABLE.
         STH   R3,NEXT        PUT HIGH ITEM WHERE LOW ITEM WAS.
         B     SORT           GO TO CHECK NEXT ONE.
TABLEND  CH    R3,=H'0'       IS WHOLE TABLE IN SEQ ?
         BER   R10            YES RETURN
         SH    R8,=H'2'       NO REDUCE SEARCH TABLE SIZE.
         LR    R2,4           RESET REG 2 TO -> TO TOP OF TABLE.
         SR    R3,3           RESET REG 3 TO ZERO.
         B     CHECK          GO BACK THROUGH AGAIN.
         SPACE 2                                                 93015
         USING IPDCBEX,R15   DECLARE DCB EXIT                    93015
IPDCBEX  CLI   DCBSW,C'0'          1ST TIME?
         BNE   SETRERD             NO, SET REREAD SW
         OI    DCBOFLGS,X'08'      SET FOR UNLIKE ATTRIBUTES
         MVI   DCBSW,C'1'          SET TO NOT 1ST TIME
         BR    R14           RETURN                              93015
SETRERD  MVI   DCBSW,C'2'          SET TO REDO LAST READ
         BR    R14           RETURN                              93015
         DROP  R15                                               93015
DCBSW    DC    C'0'
         EJECT ,                                                 93015
*        COPY USER HEADER AND TRAILER LABELS (IF SUL/AUL).       93015
*        NOTE THAT END-OF-VOLUME LABELS ARE NOT HANDLED.         93015
         PUSH  USING                                             93015
         USING XINPLABS,R12  INPUT LABEL ROUTINE                 93015
XINPUHDS LA    R10,NUMUHDS   POINT TO INPUT LABEL FIELD          93015
         B     XINPLABB-XINPUHDS(,R15)  GO TO COMMON             93015
XINPUTLS LA    R10,NUMUTLS   POINT TO INPUT TRAILERS             93015
         SLR   R15,R15       NO, THANKS!                         93015
*TAPE*   TM    4(R1),X'80'   END-OF-FILE ?                       93015
*TAPE*   BZR   R14           NO; IGNORE END-OF-VOLUME            93015
XINPLABB BALR  R12,0         SET LOCAL BASE                      93015
XINPLABS SLR   R15,R15       QUIT THIS GROUP                     93015
         LM    R3,R6,0(R1)   GET PARAMETERS                      93015
         LA    R3,0(,R3)     CLEAN BUFFER ADDRESS                93015
         LTR   R3,R3         ANY ?                               93015
         BZR   R14           NO; SKIP THIS GROUP                 93015
         LA    R15,4         CONTINUE PROCESSING                 93015
         USING LABSECT,R10   DECLARE LABEL AREA                  93015
         L     R7,NUMLABS    GET CURRENT LABEL COUNT             93015
         LR    R8,R7         COPY                                93015
         MH    R8,=H'80'     CONVERT TO OFFSET                   93015
         LA    R8,USERLABS(R8)  POINT TO IT                      93015
         LA    R7,1(,R7)     GET NEW LABEL COUNT                 93015
         ST    R7,NUMLABM    STASH MAXIMUM                       93015
         CH    R7,=H'8'      LEGAL ?                             93015
         BHR   R14           NO; JUST RETURN                     93015
         ST    R7,NUMLABS    SAVE                                93015
         MVC   0(80,R8),0(R3)  SAVE THIS LABEL                   93015
         BR    R14                                               93015
         POP   USING                                             93015
         SPACE 2                                                 93015
         PUSH  USING                                             93015
         USING XOUTLABS,R12                                      93015
XOUTUHDS LA    R10,NUMUHDS   POINT TO INPUT LABEL FIELD          93015
         B     XOUTLABB-XOUTUHDS(,R15)  GO TO COMMON             93015
XOUTUTLS LA    R10,NUMUTLS   POINT TO INPUT TRAILERS             93015
XOUTLABB BALR  R12,0         SET LOCAL BASE                      93015
XOUTLABS SLR   R15,R15       QUIT THIS GROUP                     93015
         LM    R3,R6,0(R1)   GET PARAMETERS                      93015
         SLR   R15,R15       I'M SICK OF THIS                    93015
         LA    R3,0(,R3)     CLEAN BUFFER ADDRESS                93015
         LTR   R3,R3         CHECK IT                            93015
         BZR   R14           NONE; QUIT                          93015
*TAPE*   TM    4(R1),X'80'   END-OF-FILE ?                       93015
*TAPE*   BZR   R14           NO; NO END-OF-VOLUME PROCESSING     93015
         USING LABSECT,R10   DECLARE LABEL AREA                  93015
         ICM   R7,15,NUMLABS ARE THERE ANY LABELS TO DO ?        93015
         BZR   R14           NO; SO JUST GET OUT                 93015
         LA    R8,8          SET LABEL MAX                       93015
         MIN   R8,NUMLABM    BUT NOT TOO LARGE                   93015
         SR    R8,R7         GET INDEX OF NEXT                   93015
         MH    R8,=H'80'     GET OFFSET                          93015
         LA    R8,USERLABS(R8)  POINT TO CORRECT LABEL           93015
         MVC   0(80,R3),0(R8)  MOVE LABEL TO OUTPUT AREA         93015
         BCT   R7,XOUTLABM   GET NEW LABEL COUNT                 93015
         L     R1,NUMLABM                                        93015
         MIN   R1,=F'8'      TRUNCATE                            93015
         ST    R1,DB         SAVE                                93015
         LR    R9,R14        SAVE RETURN ADDRESS                 93015
         PRTLIST MSGLABS     PRINT LABELS DONE                   93015
         LR    R14,R9        RESTORE RETURN ADDRESS              93015
XOUTLABM ST    R7,NUMLABS    SET RESIDUAL COUNT                  93015
         LA    R15,8         WRITE LABEL AND RETURN              93015
         BR    R14                                               93015
         SPACE 1                                                 93015
MSGLABS  FDPRT DB,4,I,RADJ,NL,LEN=15                             93015
         FDCLC DB,NUMLABM,4,BE=MSGLABQ                           93015
         FDPRT 'OF',PAD                                          93015
         FDPRT NUMLABM,I,PAD                                     93015
MSGLABQ  FDPRT USERLABS,3,PAD                                    93015
         FDCLC NUMLABM,DCF1,BE=MSGLAB1                           93015
         FDPRT 'LABELS WRITTEN'                                  93015
         FDPRT *END                                              93015
MSGLAB1  FDPRT 'LABEL WRITTEN'                                   93015
         FDPRT *END                                              93015
         POP   USING                                             93015
         SPACE 1                                                 93015
IPEOVEX  MVI   EOVSW,C'Y'          INDICATE NEW VOLUME
         BR    R14
EOVSW    DC    C'N'
         SPACE 1
INCRVOL  INC   IVOLPTR,INC=6  POINT TO NEXT VOLUME SERIAL        93015
         BR    R10
         SPACE 2                                                 93015
         USING OPEOVEX,R15                                       93015
OPEOVEX  CLC   OVOLCTR,=F'21'      LUMP ALL VOLS > 20 INTO 21
         BNLR  R14           RETURN                              93015
         L     R10,OVOLCTR
         LA    R10,1(R10)          INCREMENT VOLSER COUNTER
         ST    R10,OVOLCTR         SAVE VOL COUNTER
         L     R10,OCNTPTR         GET POINTER TO COUNT TABLE
         ZAP   0(8,R10),ORCDCTR    STORE LAST VOL'S RECORD COUNT
         AP    OTOTCNT,ORCDCTR     ADD TO TOTAL COUNT
         ZAP   ORCDCTR,=P'0'
         LA    R10,8(R10)          POINT TO NEXT COUNT ENTRY
         ST    R10,OCNTPTR
         BR    R14
         DROP  R15                                               93015
OVOLCTR  DS    F                 NUMBER OF O/P VOLUMES
OCNTPTR  DS    F                 NBR OF RECORD COUNTS IN OCNTTBL
OTOTCNT  DS    PL8'0'
OCNTTBL  DS    21PL8             MAX OF 21 VOLUMES
*
DSPIPCNT DS    0H
         STM   R2,R10,DSPSVA
         IC    R0,INJFCB+JFCBLTYP-INFMJFCB   GET LABEL TYPE      93015
         N     R0,=F'127'    KILL DS SEQUENCE BIT                93015
         LA    R1,LABTAB     POINT TO LABEL TABLE                93015
         LA    R2,LABTAB2-LABTAB  SET INCREMENT                  93015
         LA    R3,LABTABX    SET LAST ENTRY                      93015
DSPIPLLP CLM   R0,1,0(R1)    MATCHING LABEL ?                    93015
         BNE   DSPIPLUP      NO                                  93015
         MVC   IPLBL,1(R1)   MOVE IT                             93015
         B     DSPIPLDN                                          93015
DSPIPLUP BXLE  R1,R2,DSPIPLLP  TRY AGAIN                         93015
DSPIPLDN MVI   IPCODE,C'A'   SET ASCII/ISCII                     93015
         TM    INJFCB+JFCBLTYP-INFMJFCB,JFCBAL  IS IT ASCII ?    93015
         BNZ   DSPIPCDN      YES                                 93015
         TM    INPUT+DCBOPTCD-IHADCB,DCBOPTQ  ASCII TRANSLATE ?  93015
         BNZ   DSPIPCDN                                          93015
         MVI   IPCODE,C'E'   SET EBCDIC                          93015
DSPIPCDN IC    R0,INPUT+DCBDEN-IHADCB  LOAD TAPE DENSITY         93015
         LA    R1,DENTAB     POINT TO LABEL TABLE                93015
         LA    R2,DENTAB2-DENTAB  SET INCREMENT                  93015
         LA    R3,DENTABX    SET LAST ENTRY                      93015
DSPIPDLP CLM   R0,1,0(R1)    MATCHING LABEL ?                    93015
         BNE   DSPIPDUP      NO                                  93015
         MVC   IPDEN,1(R1)   MOVE IT                             93015
         B     DSPIPDDN                                          93015
DSPIPDUP BXLE  R1,R2,DSPIPDLP  TRY AGAIN                         93015
DSPIPDDN SLR   R0,R0                                             93015
         ICM   R0,3,INJFCB+JFCBFLSQ-INFMJFCB  GET FILE NUMBER    93015
         BZ    DSPIPFDN                                          93015
         CVD   R0,CVDWRK                                         93015
         MVC   ZSUPPWK,ZSUPP                                     93015
         ED    ZSUPPWK,CVDWRK                                    93015
         MVC   IPFILE,ZSUPP4                                     93015
DSPIPFDN MVC   ZSUPPWK,ZSUPP
         ED    ZSUPPWK,IRCDCTR
         MVC   IPCNT,ZSUPP8
         AP    IRCDTOT,IRCDCTR  MAKE CUMULATIVE COUNT            93015
         MVC   ZSUPPWK,ZSUPP                                     93015
         ED    ZSUPPWK,IRCDTOT                                   93015
         MVC   IPTOT,ZSUPP8                                      93015
         ZAP   IRCDCTR,=P'0'
*
         CLC   IVOLPTR,=F'120'     MORE THAN 20 VOLUMES?
         BL    CHK5VOLS            NO
         MVC   IPVOL,=6C'*'        YES
         B     MOVERECL
*
CHK5VOLS DS    0H
         CLC   IVOLPTR,=F'30'     MORE THAN 5 VOLUMES?
         BL    MOVEVOL            NO
* GET VOL SER FROM JFCB EXTENSION
         SLR   R1,R1                                             93015
         ICM   R1,7,JFCBEXAD GET EXTENSION ADDRESS               93015
         SERVCALL SWARL,(R1) COPY EXTENSION TO LOCAL AREA        93015
         LA    R10,4(,R1)    POINT TO VOL SER LIST               93015
         A     R10,IVOLPTR        ADD VOL POINTER
         S     R10,=F'30'         SUBTRACT LENGTH OF 1ST 5 VOLS
         MVC   IPVOL,0(R10)
         B     MOVERECL
*
MOVEVOL  DS    0H
         LA    R10,JFCBVOLS
         A     R10,IVOLPTR
         MVC   IPVOL,0(R10)
*
MOVERECL DS    0H
         LH    R0,JFCLRECL         LOAD LRECL
         CVD   R0,CVDWRK
         MVC   ZSUPPWK,ZSUPP
         ED    ZSUPPWK,CVDWRK
         MVC   IPLRECL,ZSUPP5
*
         LH    R0,JFCBLKSI         LOAD BLKSIZE
         CVD   R0,CVDWRK
         MVC   ZSUPPWK,ZSUPP
         ED    ZSUPPWK,CVDWRK
         MVC   IPBLK,ZSUPP7
*
         MVC   RECFMWK,JFCRECFM
         BAL   R10,SETRECFM
         MVC   IPRECFM,RECFMSET
*
         MVC   IPDSN,JFCBDSNM
*
         PRTLIST IPMSG       PRINT INPUT INFORMATION             93015
         MVC   IPCLR+L'IPDDNAM(L'IPCLR-L'IPDDNAM),=CL133' '  CLEAR DATA
         ZAP   IRCDCTR,=P'0'
*
         LM    R2,R10,DSPSVA
         BR    R10
         SPACE 1                                                 93015
OPFORMAT LH    R0,JFCLRECL         LOAD LRECL
         CVD   R0,CVDWRK
         MVC   ZSUPPWK,ZSUPP
         ED    ZSUPPWK,CVDWRK
         MVC   OPLRECL,ZSUPP5
*
         LH    R0,JFCBLKSI         LOAD BLKSIZE
         CVD   R0,CVDWRK
         MVC   ZSUPPWK,ZSUPP
         ED    ZSUPPWK,CVDWRK
         MVC   OPBLK,ZSUPP7
*
         MVC   RECFMWK,JFCRECFM
         BAL   R10,SETRECFM
         MVC   OPRECFM,RECFMSET
         MVC   OPDSN,JFCBDSNM
         SPACE 1                                                 93015
         STM   R14,R12,12(R13)  SAVE REGISTERS                   93015
         IC    R0,OUTJFCB+JFCBLTYP-INFMJFCB   GET LABEL TYPE     93015
         N     R0,=F'127'    KILL DS SEQUENCE BIT                93015
         LA    R1,LABTAB     POINT TO LABEL TABLE                93015
         LA    R2,LABTAB2-LABTAB  SET INCREMENT                  93015
         LA    R3,LABTABX    SET LAST ENTRY                      93015
DSPOPLLP CLM   R0,1,0(R1)    MATCHING LABEL ?                    93015
         BNE   DSPOPLUP      NO                                  93015
         MVC   OPLBL,1(R1)   MOVE IT                             93015
         B     DSPOPLDN                                          93015
DSPOPLUP BXLE  R1,R2,DSPOPLLP  TRY AGAIN                         93015
DSPOPLDN MVI   OPCODE,C'A'   SET ASCII/ISCII                     93015
         TM    OUTJFCB+JFCBLTYP-INFMJFCB,JFCBAL  IS IT ASCII ?   93015
         BNZ   DSPOPCDN      YES                                 93015
         TM    OUTPUT+DCBOPTCD-IHADCB,DCBOPTQ  ASCII TRANSLATE ? 93015
         BNZ   DSPOPCDN                                          93015
         MVI   OPCODE,C'E'   SET EBCDIC                          93015
DSPOPCDN IC    R0,OUTPUT+DCBDEN-IHADCB  LOAD TAPE DENSITY        93015
         LA    R1,DENTAB     POINT TO LABEL TABLE                93015
         LA    R2,DENTAB2-DENTAB  SET INCREMENT                  93015
         LA    R3,DENTABX    SET LAST ENTRY                      93015
DSPOPDLP CLM   R0,1,0(R1)    MATCHING LABEL ?                    93015
         BNE   DSPOPDUP      NO                                  93015
         MVC   OPDEN,1(R1)   MOVE IT                             93015
         B     DSPOPDDN                                          93015
DSPOPDUP BXLE  R1,R2,DSPOPDLP  TRY AGAIN                         93015
DSPOPDDN SLR   R0,R0                                             93015
         ICM   R0,3,OUTJFCB+JFCBFLSQ-INFMJFCB  GET FILE NUMBER   93015
         BZ    DSPOPFDN                                          93015
         CVD   R0,CVDWRK                                         93015
         MVC   ZSUPPWK,ZSUPP                                     93015
         ED    ZSUPPWK,CVDWRK                                    93015
         MVC   OPFILE,ZSUPP4                                     93015
DSPOPFDN LM    R14,R12,12(R13)  RESTORE REGS                     93015
         BR    R14
         SPACE 1                                                 93015
DSPOPCNT DS    0H
         STM   R2,R10,DSPSVA
*
         RDJFCB (OUTPUT)         GET INFO FOR OUTPUT FILE
         L     R10,OCNTPTR         GET POINTER TO COUNT TABLE
         ZAP   0(8,R10),ORCDCTR    STORE LAST VOL'S RECORD COUNT
         AP    OTOTCNT,ORCDCTR     ADD TO TOTAL COUNT
         SPACE 1                                                 93015
         SLR   R7,R7         VOL COUNTER                         93015
         LA    R8,JFCBVOLS         VOLUME LIST
         LA    R9,OCNTTBL          TABLE OF COUNTS BY VOLSER
*
PUT1VOL  DS    0H
         MVC   ZSUPPWK,ZSUPP
         ED    ZSUPPWK,0(R9)       RECORD COUNT
         MVC   OPCNT,ZSUPP8
         MVC   ZSUPPWK,ZSUPP                                     93015
         ED    ZSUPPWK,OTOTCNT     TOTAL RECORD COUNT            93015
         MVC   OPTOT,ZSUPP8                                      93015
         CLI   OPENSW,C'Y'   WAS OUTPUT OPENED?                  93015
         BNE   UNOPENED                                          93015
         C     R7,=F'20'           PAST VOL NUMBER 20?
         BNL   OPVOL21
         C     R7,=F'5'            PAST VOL NUMBER 5?
         BNL   OPVOL6
         MVC   OPVOL,0(R8)         VOL SER
         B     PUTOPMSG
OPVOL6   DS    0H
* GET VOL SER FROM JFCB EXTENSION
         SLR   R1,R1                                             93015
         ICM   R1,7,JFCBEXAD GET EXTENSION ADDRESS               93015
         SERVCALL SWARL,(R1) COPY EXTENSION TO LOCAL AREA        93015
         LA    R10,4(,R1)    POINT TO VOL SER LIST               93015
         AR    R10,R8             ADD VOL POINTER
         S     R10,=A(JFCBVOLS+30) SUBTRACT LENGTH OF 1ST 5 VOLS
         MVC   OPVOL,0(R10)
         B     PUTOPMSG
OPVOL21  DS    0H
         MVC   OPVOL,=C'*MORE*'
         B     PUTOPMSG
UNOPENED DS    0H
         MVC   OPVOL,=C'*NONE*'
PUTOPMSG PRTLIST OPMSG                                           93015
         MVC   OPDDGRP,=9C' '
         C     R7,OVOLCTR
         BE    CHKOPTOT
         LA    R7,1(R7)              NEXT VOL NUMBER
         LA    R8,6(R8)              NEXT VOL SER
         LA    R9,8(R9)              NEXT RECORD COUNT
         B     PUT1VOL
CHKOPTOT MVC   OPCLR,=CL133' '  BLANK
         CLC   OVOLCTR,=F'0'
         BE    CLROPMSG
         MVC   OPCNT,=8C'-'
         PRTLIST OPTOTCT
         MVC   ZSUPPWK,ZSUPP
         ED    ZSUPPWK,OTOTCNT     TOTAL RECORD COUNT
         MVC   OPCNT,ZSUPP8
         PRTLIST OPTOTCT
CLROPMSG PRTROOM 3
         PRTSPACE 1                                              93015
         LM    R2,R10,DSPSVA
         BR    R10
DSPSVA   DS    11F
         SPACE 1
FINDTIOT L     R3,TIOTPTR          ADDRESS OF TIOT
         LH    R4,TIOTWK           SO NEXT INST WILL -> TO DD ENTRY.
         LA    R3,0(R4,R3)         -> TO NEXT OR FIRST DD ENTRY.
         BR    R10
         SPACE 1
SETRECFM MVC   RECFMSET,=CL5' '
         TM    RECFMWK,JFCVAR+JFCFIX  UNDEFINED ?                93015
         BNM   FMUND                                             93015
         TM    RECFMWK,JFCFIX FIXED?                             93015
         BNZ   FMFIX
         MVI   RECFM1,C'V'
         B     CHKBLK
FMUND    MVI   RECFM1,C'U'
         B     CHKBLK
FMFIX    MVI   RECFM1,C'F'
CHKBLK   TM    RECFMWK,B'00010000' BLOCKED?                      93015
         BZ    CHKASA
         MVI   RECFM2,C'B'
CHKASA   TM    RECFMWK,B'00000100' ASA?                          93015
         BNZ   FMASA
         TM    RECFMWK,B'00000010' MACHINE?                      93015
         BZ    SETRECFX
         MVI   RECFM3,C'M'
         B     SETRECFX
FMASA    MVI   RECFM3,C'A'
SETRECFX BR    R10
*
DSPTITLE MVC   JOBNAME,0(R3)
         CLI   16(R3),C' '   IF SPACE, NO NAME ON PROC OR NOT A PROC
         BE    NOTPROC
         MVC   PROCSTEP,8(R3)
         MVC   JOBSTEP,16(R3)
         B     DSPSTEP
*
NOTPROC  DS    0H
         MVC   PROCSTEP,=CL8'*NONE*'
         MVC   JOBSTEP,08(R3)
*
DSPSTEP  PRTF  TITLMSG,133,TITLE=1
         PRTF  STEPMSG,133,TITLE=3                               93015
         PRTLIST HDGMSG1,TITLE=5                                 93015
         PRTLIST HDGMSG2,TITLE=6                                 93015
         BR    R10
         SPACE 1
SETPARM  MVI   OSPARM,C' '
         L     R15,0(,R9)    PUT IN R9 BY SAVEM (WAS R1)         93015
         SLR   R0,R0                                             93015
         ICM   R0,3,0(R15)   LOAD AND TEST PARM LENGTH           93015
         BZR   R10           NONE; KEEP AS IS                    93015
         CH    R0,=H'3'      EXPECTED PARM ?                     93015
         BNE   BADPARM
         CLC   =C'CHK',2(R15)
         BNE   BADPARM
         MVI   OSPARM,C'C'                                       93015
         MVC   TITLMODE,=C'(CHECK MODE)'
         BR    R10           RETURN TO CALLER                    93015
BADPARM WTO    '*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*',  X
               'COPYFILE60 INVALID PARM VALUE SPECIFIED       ',       X
               '*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*',  X
               ROUTCDE=(2)
         ABEND 60,DUMP,STEP
         SPACE 2                                                 93015
INITCTR  MVI   OPENSW,C'N'         OUTPUT OPEN SWITCH
         ZAP   IRCDCTR,=P'0'       ZERO RECORD COUNTER
         ZAP   IRCDTOT,=P'0'       ZERO INPUT TOTALS             93015
         ZAP   ORCDCTR,=P'0'       ZERO RECORD COUNTER
         ZAP   OTOTCNT,=P'0'       ZERO TOTAL COUNTER
         MVI   DCBSW,C'0'          SET TO 1ST TIME
         MVI   EOVSW,C'N'
         MVC   IVOLPTR,=F'0'
         MVC   OVOLCTR,=F'0'
         MVC   OCNTPTR,=A(OCNTTBL)
         MVC   IPDDNAM,DDNAME(R5)
         MVI   IPCOLON,C':'
         MVC   OPDDNAM,DDNAME(R6)
         MVI   OPCOLON,C':'
         XC    NUMUHDS(L'NUMUHDS+L'NUMUHDM),NUMUHDS  CLEAR LABEL COUNT
         XC    NUMUTLS(L'NUMUTLS+L'NUMUTLM),NUMUTLS  CLEAR LABEL COUNT
         BR    R10
*
WRITSOUT LR    R1,R0
         PRTF  (R1),133      PRINT A LINE                        93015
         BR    R10
*-------------------------------------------------
*
*
DCF1     DC    F'1'          CONSTANT                            93015
         SPACE 1                                                 93015
LABTAB   DC    AL1(JFCSL),CL3'SL'                                93015
LABTAB2  DC    AL1(JFCNL),CL3'NL'                                93015
         DC    AL1(JFCSUL),CL3'SUL'                              93015
         DC    AL1(JFCBAL),CL3'AL'                               93015
         DC    AL1(JFCBAL+8),CL3'AUL'                            93015
         DC    AL1(JFCBLTM),CL3'LTM'                             93015
         DC    AL1(JFCBLP),CL3'BLP'                              93015
LABTABX  DC    AL1(JFCNSL),CL3'NSL'                              93015
         SPACE 1                                                 93015
DENTAB   DC    X'E7',CL4'38KC'                                   93015
DENTAB2  DC    X'E3',CL4'38K '                                   93015
         DC    X'D3',CL4'6250'                                   93015
         DC    X'C3',CL4'1600'                                   93015
*OLD*    DC    X'43',CL4' 556'                                   93015
*OLDER*  DC    X'03',CL4' 200'                                   93015
DENTABX  DC    X'83',CL4' 800'                                   93015
         SPACE 1                                                 93015
NUMTAB   DC    X'F00102030405060708090A0B0C0D0E0F'
         DC    X'101112131415161718191A1B1C1D1E1F'
         DC    X'202122232425262728292A2B2C2D2E2F'
         DC    X'303132333435363738393A3B3C3D3E3F'
         DC    X'404142434445464748494A4B4C4D4E4F'
         DC    X'505152535455565758595A5B5C5D5E5F'
         DC    X'606162636465666768696A6B6C6D6E6F'
         DC    X'707172737475767778797A7B7C7D7E7F'
         DC    X'808182838485868788898A8B8C8D8E8F'
         DC    X'909192939495969798999A9B9C9D9E9F'
         DC    X'A0A1A2A3A4A5A6A7A8A9AAABACADAEAF'
         DC    X'B0B1B2B3B4B5B6B7B8B9BABBBCBDBEBF'
         DC    X'C0C1C2C3C4C5C6C7C8C9CACBCCCDCECF'
         DC    X'D0D1D2D3D4D5D6D7D8D9DADBDCDDDEDF'
         DC    X'E0E1E2E3E4E5E6E7E8E9EAEBECEDEEEF'
         DC    X'00000000000000000000FAFBFCFDFEFF'
*
INPUT    DCB   DSORG=PS,MACRF=GL,DDNAME=INPUT,EODAD=EODAD,EXLST=IEXLST
OUTPUT   DCB   DSORG=PS,MACRF=PM,DDNAME=OUTPUT,EXLST=OEXLST
SYSOUT   PRTWORK SYSPRINT,SYSOUT,TITLE=7                         93015
*
IEXLST   DS    0F
         DC    X'01',AL3(XINPUHDS)  INPUT HEADER LABELS          93015
         DC    X'03',AL3(XINPUTLS)  INPUT TRAILER LABELS         93015
*FAILS*  DC    X'0C',AL3(0)  DEFER TRAILER PROCESSING UNTIL CLOSE
         DC    X'05',AL3(IPDCBEX)
         DC    X'06',AL3(IPEOVEX)
         DC    X'87',AL3(INFMJFCB)
*
OEXLST   DS    0F
         DC    X'02',AL3(XOUTUHDS)  OUTPUT HEADER LABELS         93015
         DC    X'04',AL3(XOUTUTLS)  OUTPUT TRAILER LABELS        93015
         DC    X'06',AL3(OPEOVEX)
         DC    X'87',AL3(INFMJFCB)
*
*   FILE STATISTICS MESSAGES
*
IRCDCTR  DC    PL8'0'
IRCDTOT  DC    PL8'0'                                            93015
ORCDCTR  DC    PL8'0'
ZSUPP    DC    C' ',13X'20',X'2120'
*
TITLMSG  DC    C'#'          AUTO TIME + DATE + PAGE #           93015
         DC    132C'-'
         ORG   TITLMSG+46                                        93015
         DC    C'  C O P Y F I L E  '                            93015
         DS    CL5
TITLMODE DS    CL12
         ORG   ,
*
STEPMSG  DC    CL133' '
         ORG   STEPMSG+1
         DC    C'  JOB: '
JOBNAME  DS    CL8
         DC    C'  PROC STEP: '
PROCSTEP DS    CL8
         DC    C'  JOB STEP: '
JOBSTEP  DS    CL8
         ORG
*
HDGMSG1  FD    '  DDNAME',NL,PAD,LEN=9                           93015
         FD    '   COUNT',PAD,LEN=8                              93015
         FD    '  TOTALS',PAD,LEN=8                              93015
         FD    'DSNAME',PAD,LEN=44                               93015
         FD    'VOLSER',PAD                                      93015
         FD    'LBL',PAD                                         93015
         FD    ' DEN',PAD                                        93015
         FD    'C',PAD                                           93015
         FD    'FILE',PAD                                        93015
         FD    'RECFM',PAD                                       93015
         FD    'LRECL',PAD                                       93015
         FD    'BLKSIZE'                                         93015
         FD    *END                                              93015
         SPACE 1
HDGMSG2  FD    '---------',NL,PAD,LEN=9                          93015
         FD    '--------',PAD,LEN=8                              93015
         FD    '--------',PADL,LEN=8                             93015
         FD    ' '           FDREPT FAILS WITH PAD               93015
         FDREPT 44,C'-'                                          93015
         FD    '------',PAD                                      93015
         FD    '---',PAD                                         93015
         FD    '----',PAD                                        93015
         FD    '-',PAD                                           93015
         FD    '----',PAD                                        93015
         FD    '-----',PAD                                       93015
         FD    '-----',PAD                                       93015
         FD    '-------'                                         93015
         FD    *END                                              93015
         SPACE 1
IPMSG    FD    IPDDNAM,NL,RADJ,DEB,LEN=8                         93015
         FD    ':'                                               93015
         FD    IPCNT,PAD                                         93015
         FD    IPTOT,PAD                                         93015
         FD    IPDSN,PAD                                         93015
         FD    IPVOL,PAD                                         93015
         FD    IPLBL,PAD                                         93015
         FD    IPDEN,PAD                                         93015
         FD    IPCODE,PAD                                        93015
         FD    IPFILE,PAD                                        93015
         FD    IPRECFM,PAD                                       93015
         FD    IPLRECL,PAD                                       93015
         FD    IPBLK,PAD                                         93015
         FD    *END                                              93015
         SPACE 1                                                 93015
OPMSG    FD    OPDDGRP,NL,RADJ                                   93015
         FD    OPCNT,PAD                                         93015
         FD    OPTOT,PAD                                         93015
         FD    OPDSN,PAD                                         93015
         FD    OPVOL,PAD                                         93015
         FD    OPLBL,PAD                                         93015
         FD    OPDEN,PAD                                         93015
         FD    OPCODE,PAD                                        93015
         FD    OPFILE,PAD                                        93015
         FD    OPRECFM,PAD                                       93015
         FD    OPLRECL,PAD                                       93015
         FD    OPBLK,PAD                                         93015
         FD    *END                                              93015
         SPACE 1                                                 93015
OPTOTCT  FD    OPCNT,RADJ,NL,LEN=18                              93015
         FD    *END                                              93015
         SPACE 1                                                 93015
*
         IEFJFCBN
*
         LTORG
         EJECT ,
SAVE     DSECT ,             SAVE/WORK AREA
CVDWRK   DS    0D  1/2                                           93015
DB       DS    2D  2/2       WORK AREA                           93015
@SERVICE DS    A             SERVICE ROUTINE                     93015
@PRINTER DS    A             PRINTER SERVICE                     93015
IPRCD    DS    F                   ADDRESS OF INPUT RECORD
IVOLPTR  DS    F
TIOTPTR  DS    F                ADDRESS OF TIOT
RETCH    DS    H             RETURN CODE                         93015
TIOTWK   DS    H
ILRECL   DS    H                   INPUT LRECL
IBLKSI   DS    H                   INPUT BLKSIZE
OLRECL   DS    H                   OUTPUT LRECL
OBLKSI   DS    H                   OUTPUT BLKSIZE
IRECFM   DS    B                   INPUT RECFM
ORECFM   DS    B                   OUTPUT RECFM
OSPARM   DS    CL1
OPENSW   DC    C'N'
RECFMWK  DS    CL1
RECFMSET DS    0CL5
         DS    CL2
RECFM1   DS    CL1
RECFM2   DS    CL1
RECFM3   DS    CL1
         SPACE 1                                                 93015
ZSUPPWK  DS    0CL16,CL8                                         93015
ZSUPP8   DS    0CL8,C                                            93015
ZSUPP7   DS    0CL7,C                                            93015
ZSUPP6   DS    0CL6,C                                            93015
ZSUPP5   DS    0CL5,C                                            93015
ZSUPP4   DS    0CL4,C                                            93015
ZSUPP3   DS    CL3
         SPACE 1                                                 93015
IPDDNAM  DS    CL8
IPCOLON  DC    C':'
IPCNT    DS    CL8
IPTOT    DS    CL8                                               93015
IPDSN    DS    CL44
IPVOL    DS    CL6
IPLBL    DS    CL3                                               93015
IPCODE   DS    C                                                 93015
IPFILE   DS    CL4                                               93015
IPDEN    DS    CL4                                               93015
IPRECFM  DS    CL5
IPLRECL  DS    CL5
IPBLK    DS    CL7
IPCLR    EQU   IPDDNAM,*-IPDDNAM,C'C'                            93015
OPDDGRP  DS    0CL9
OPDDNAM  DS    CL8
OPCOLON  DC    C':'
OPCNT    DS    CL8
OPTOT    DS    CL8                                               93015
OPDSN    DS    CL44
OPVOL    DS    CL6
OPLBL    DS    CL3                                               93015
OPCODE   DS    C                                                 93015
OPFILE   DS    CL4                                               93015
OPDEN    DS    CL4                                               93015
OPRECFM  DS    CL5
OPLRECL  DS    CL5
OPBLK    DS    CL7
OPCLR    EQU   OPDDGRP,*-OPDDGRP,C'C'  LENGTH TO BLANK           93015
INTABLE  DC    CL200' '
OUTABLE  DC    CL200' '
INJFCB   DS    CL176
OUTJFCB  DS    CL176
         SPACE 1                                                 93015
*        HEADER/TRAILER LABEL SAVE AREA                          93015
NUMUHDS  DC    F'0'          NUMBER OF USER HEADERS              93015
NUMUHDM  DC    F'0'          MAXIMUM NUMBER IN THIS GROUP        93015
USERUHDS DC    8CL80' '      HEADER AREA                         93015
NUMUTLS  DC    F'0'          NUMBER OF USER TRAILERS             93015
NUMUTLM  DC    F'0'          MAXIMUM NUMBER IN THIS GROUP        93015
USERUTLS DC    8CL80' '      TRAILER AREA                        93015
SAVEEND  EQU   *                                                 93015
         SPACE 1                                                 93015
LABSECT  DSECT ,                                                 93015
NUMLABS  DC    F'0'          NUMBER OF LABELS                    93015
NUMLABM  DC    F'0'          MAXIMUM NUMBER IN THIS GROUP        93015
USERLABS DC    8CL80' '      LABEL AREA                          93015
SORTWORK DSECT
LOW      DS    CL2
NEXT     DS    CL2
         DSECT
DCBS     DCBD  DSORG=PS,DEVD=TA
LRECL    EQU   DCBLRECL-IHADCB
BLKSIZE  EQU   DCBBLKSI-IHADCB
RECFM    EQU   DCBRECFM-IHADCB
DDNAME   EQU   DCBDDNAM-IHADCB
         CVT   LIST=NO,DSECT=YES                                 94220
         IHACDE ,                                                94220
         END   ,
