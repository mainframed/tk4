NOSS     TITLE 'CANCEL FORTRAN ESPIE AND ESTAE'                 GP03147
***********************************************************************
**                                                                   **
**  THIS SUBROUTINE IS PROVIDED TO ALLOW CANCELLATION OF THE         **
**  FORTRAN STAE AND SPIE INSTRUCTIONS. IT IS INTENDED FOR USE       **
**  WHEN THE INVOKATION OF THE FORTRAN ERROR ROUTINES OBSCURES       **
**  EITHER THE ABEND OR DATA NECESSARY FOR PROBLEM DIAGNOSIS.        **
**                                                                   **
**  THIS MAY ALSO BE HELPFUL FOR NEWER COBOL PROGRAMS AND CEE USERS. **
**                                                                   **
***********************************************************************
**  MAINTENANCE                                                      **
**  2003-05-27  GYP  CHANGED SPIE TO ESPIE; MINOR STUFF.             **
**                                                                   **
***********************************************************************
         SPACE 2
NOSTAE   START 0                                        ADDED ON 85198
NOSTAE   AMODE ANY                                              GP03147
NOSTAE   RMODE ANY                                              GP03147
         USING NOSTAE,R15                                       GP03147
         B     AROUND                                           GP03147
         BCON  'NOSTAE &SYSDATE'                                GP03147
AROUND   BSM   R14,0         SAVE CALLER'S AMODE                GP03147
         STM   R14,R12,12(R13)  SAVE ALL REGISTERS
         LR    R12,R15       SET BASE
         DROP  R15                                              GP03147
         USING NOSTAE,R12
         STAE  0             CANCEL ANY PRIOR STAE (OLD FORTRAN)
         ESTAE 0             CANCEL ANY ESTAE (NEW FORTRAN)
         B     EXIT          RETURN
         SPACE 2
         ENTRY NOSPIE
NOSPIE   BSM   R14,0         PRESERVE AMODE                     GP03147
         STM   R14,R12,12(R13)                                  GP03147
         LR    R12,R15       SET BASE
         USING NOSPIE,R12
         TM    FLAG,FGSPIED  DID WE CANCEL SPIE ALREADY ?
         BNZ   EXIT          YES; JUST RETURN
         ESPIE TEST,STATSPIE  STATUS OF PRIOR REQUESTS          GP03147
         CH    R15,=H'4'     0-ESPIE  4-SPIE  8-NONE            GP03147
         BH    EXIT          JUST RETURN IF NONE                GP03147
         ESPIE RESET,PREVPICA  CANCEL CURRENT SPIE              GP03147
         ST    R1,PREVPICA   SAVE PRIOR REQUEST
         OI    FLAG,FGSPIED  SET FLAG
         B     EXIT          AND RETURN
         SPACE 2
         ENTRY RESPIE
RESPIE   BSM   R14,0         PRESERVE AMODE                     GP03147
         STM   R14,R12,12(R13)                                  GP03147
         LR    R12,R15
         USING RESPIE,R12                                       GP03147
         TM    FLAG,FGSPIED  DID SPIE CANCEL ?
         BZ    EXIT          NO; IGNORE
         ESPIE RESET,PREVPICA  RESTORE PRIOR ENVIRONMENT        GP03147
         NI    FLAG,255-FGSPIED
         DROP  R12           CALLED WITH DIFFERENT BASES        GP03147
EXIT     LM    R14,R12,12(R13)
         BSM   0,R14         RETURN TO CALLER                   GP03147
         SPACE 1
PREVPICA DC    A(0)          ADDRESS OF FORTRAN'S PICA
STATSPIE DC    4A(0)         RESULT FROM ESPIE TEST             GP03147
FLAG     DC    X'00'
FGSPIED  EQU   X'80'         ON IF SPIE CANCELLED
         SPACE 1
         PRINT NOGEN
         YREGS ,             EXPAND MNEMONIC REGISER EQUATES    GP03147
         END
