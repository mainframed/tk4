SUBTIODD TITLE 'S U B T I O D D   **   FIND TIOT ENTRY FOR DD NAME'
***********************************************************************
**                                                                   **
**   SUBROUTINE SUBTIODD PERFORMS A TIOT SCAN FOR THE DD NAME        **
**   POINTED TO BY R1.                                               **
**                                   AUTHOR: G. POSTPISCHIL          **
***********************************************************************
**
*   R1 POINTS CL8'ddname'
*
*   RETURN CODES
*
*   R1 -  0    REQUESTED NAME NOT FOUND
*        addr  ADDRESS OF TIOENTRY FOR DD
*
*
SUBTIODD START 0                                       ADDED ON GP09217
         B     BEGIN-SUBTIODD(,R15)
         BCON  'SUBTIODD &SYSDATE'
BEGIN    STM   R14,R12,12(R13)
         LR    R12,R15       AND NEW BASE
         USING SUBTIODD,R12
         LR    R5,R1                   SAVE CALLER'S R1
         CLI   0(R5),C' '              VALID NAME ?
         BNH   NOMATCH                 NO; REJECT IT
         L     R3,PSATOLD-PSA          GET MY TCB
         L     R1,TCBTIO-TCB(,R3)      GET TIOT
         USING TIOT1,R1                DECLARE IT
         LA    R1,TIOENTRY             SPACE TO FIRST ENTRY
         USING TIOENTRY,R1             DECLARE IT
         SR    R2,R2                   CLEAR FOR LENGTH IC
LOOPTIOT ICM   R2,1,TIOELNGH           GET ENTRY LENGTH
         BZ    NOMATCH                 TOO BAD
         CLC   TIOEDDNM,0(R5)          MATCHES USER REQUEST?
         BE    FOUNDIT                 R1 SET
         AR    R1,R2                   NEXT ENTRY
         B     LOOPTIOT
         SPACE 1
NOMATCH  LA    R15,8                   SHOW NOT FOUND
         SR    R1,R1                   DITTO
         B     PGMEXIT                 RETURN
         SPACE 1
FOUNDIT  SR    R15,R15       SET RETURN CODE TO ZERO - ADDRESS IN R1
*  N.B. IN POST-MVS, NEED SWA LOOKUP
         SR    R0,R0
         ICM   R0,7,TIOEJFCB      EXTRA BONUS
         AH    R0,=H'16'          SKIP QUEUE HEADER
PGMEXIT  LM    R2,R12,28(R13)  RELOAD MANY REGISTERS
         L     R14,12(,R13)  RELOAD RETURN ADDRESS
         BR    R14           RETURN
         SPACE 2
         LTORG ,
         SPACE 1
         PRINT NOGEN
MYTIOT   DSECT ,
         IEFTIOT1 ,
         SPACE 1
         YREGS ,
         IEZJSCB ,
         IKJTCB ,
         IHAPSA ,
         END   ,
