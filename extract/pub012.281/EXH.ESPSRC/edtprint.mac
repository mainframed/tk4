EDTPRINT TITLE 'E D T P R I N T  ***  ELIGIBLE DEVICE TABLE PRINTOUT'
         PUNCH ' ORDER EDTPRINT(P) '  MAKE DUMPS EASIER
         COPY  OPTIONGB
         SPACE 1
         SYSPARM LIST=YES
         EJECT ,
***********************************************************************
*                                                                     *
*    EDTPRINT DISPLAYS THE CURRENT ELIGIBLE DEVICE TABLE. THE         *
*    TABLE IS ASSUMED TO BE FORMAT 2. TO FORMAT OLDER VERSIONS OF THE *
*    EDT, SEE MEMBER OLDCCEDT IN THE EXHIBIT SOURCE.                  *
*                                                                     *
*    FORMAT 3 SUPPORTED PROVIDED 1999/02/05 FF.   G. POSTPISCHIL 99036
*    WILL HANDLE STATIC EDT OR DYNAMIC HCD/EDT                        *
*                                                                     *
***********************************************************************
         PRINT &PRTSOR
EDTPRINT PGMHEAD ZERO15,BASE=(R12),PARM=R5,AM=31,RM=24
         SERVINIT ,          INITIALIZE THE SERVICE ROUTINE
         SERVLOAD @PRINTER
         PRTOPEN SYSPRINT,OPT=(ABEND,WTO)
         SPACE 1
         LA    R1,COMPMASK   POINT TO MASK                       93049
         ST    R1,COMPADDR   SET ADDRESS POINTER                 93049
         LA    R0,VIOLET
         LA    R1,AQUA
         STM   R0,R1,ULTRA   INIT
         LA    R1,MARINE
         ST    R1,INFRA+4
         MVC   AQUA,=X'10A0'    ATTRIBUTES
         MVI   MARINE,X'0A'     ATTRIBUTES
*DEFER*  CLI   0(R5),C' '    ANY FOUND ?                         93049
*DEFER*  BE    RESTART       NO                                  93049
*DEFER*  NI    FLAGS,255-FGALL  TURN UNIVERSAL MATCH OFF         93049
*DEFER*  LA    R0,1          CONSTANT                            93049
*DEFER*  LA    R2,8          MAXIMUM INPUT LENGTH                93049
*DEFER*  LR    R14,R2        SAVE LENGTH                         93049
MOVEMASK CLI   0(R5),C' '    END OF MASK ?                       93049
*DEFER*  BE    MOVEEND       YES                                 93049
*DEFER*  MVC   0(1,R1),0(R5)  COPY A BYTE                        93049
*DEFER*  AR    R1,R0         UP POINTER                          93049
*DEFER*  AR    R5,R0           DITTO                             93049
*DEFER*  BCT   R2,MOVEMASK                                       93049
MOVEEND  SR    R14,R2        GET LENGTH                          93049
*DEFER*  ST    R14,COMPLEN    AND STORE                          93049
         SPACE 1                                                 93049
RESTART  DS    0H
         AIF   (&MVSESA).SKPLOAD                                GP99036
         LOAD  EPLOC==CL8'IEFEB4UV'
         ST    R0,@EB4UV     REMEMBER WHERE                      93049
.SKPLOAD L     R14,CVTPTR    GET THE CVT                        GP99036
         USING CVTMAP,R14
         ICM   R10,15,CVTJESCT  GET THE JES VT
         BZ    LOCFAIL       NONE ?
         AIF   (NOT &MVSESA).PREESA                             GP99036
         ICM   R1,15,JESDACA-JESCT(R10)  GET THE DACA POINTER   GP99036
         BZ    LOCFAIL       OOPS?                              GP99036
         ICM   R1,15,X'60'(R1)  GET THE EDTL                    GP99036
         BZ    LOCFAIL       IN THE MIDDLE OF A SWITCH?         GP99036
         ICM   R10,15,X'10'(R1)  GET STATIC EDT POINTER         GP99036
         BNZ   HAVESEDT      HAVE IT                            GP99036
         ICM   R10,15,X'14'(R1)  GET DYNAMIC EDT POINTER        GP99036
         BZ    LOCFAIL       HAVE IT                            GP99036
         USING EDT,R10                                          GP99036
HAVESEDT ICM   R1,15,EDT@UPL  GET THE UCB TABLE                 GP99036
         ICM   R3,15,4(R1)   GET UCB COUNT                      GP99036
         LA    R1,16-4(,R1)  OFFSET FOR INDEX                   GP99036
         ST    R1,@UPLP      SAVE UCB LOOKUP TABLE+OFFSET       GP99036
         ST    R3,#UPLP      SAVE COUNT                         GP99036
         BZ    LOCFAIL                                          GP99036
         LA    R3,2(,R3)                                        GP99036
         SLA   R3,2          MAKE GETMAIN LENGTH                GP99036
         STORAGE OBTAIN,LENGTH=(R3)
         LR    R2,R1
         STM   R2,R3,MYMAIN  SAVE MY STORAGE ADDRESSES          GP99036
         AGO   .COMESA                                          GP99036
.PREESA  L     R3,JESAUCBS-JESCT(,R10)  GET NUMBER OF UCBS IN SYSTEM
         ST    R3,#UPLP      SAVE COUNT                         GP99036
         LA    R3,2(,R3)                                         93048
         SLA   R3,2          MAKE GETMAIN LENGTH                 93048
         STORAGE OBTAIN,LENGTH=(R3)
         LR    R2,R1
         STM   R2,R3,MYMAIN  SAVE MY STORAGE ADDRESSES           93048
         L     R1,JESUPLP-JESCT(,R10)  GET UCB LOOKUP TABLE      93048
         LA    R1,16-4(,R1)  OFFSET FOR INDEX                    93048
         ST    R1,@UPLP      SAVE UCB LOOKUP TABLE+OFFSET        93048
         ICM   R10,15,JESEDT-JESCT(R10)  GET EDT ADDRESS
         BZ    LOCFAIL       HUH ?
         USING EDT,R10       DECLARE IT                         GP99036
.COMESA  ST    R10,@EDT      SAVE FOR LATER                     GP99036
         L     R7,EDTLKUP    GET LOOKUP TABLE                    93048
         USING LKPPFX,R7     DECLARE PREFIX                      93048
         PRTLIST UPSCREEN,TITLE=1
         PRTLIST HEADER2,TITLE=2
         PRTLIST HEADER3,TITLE=3
         SPACE 1
         LH    R6,LKPINX     GET POINTER TO FIRST ENTRY          93048
         B     FIRSTLKP      PROCESS IT                          93048
         PUSH  USING
         DROP  R7
         USING LKPSECT,R6    DECLARE IT                          93048
LOOPLKP  MVC   VIOLET,LKPNAME  MOVE GROUP NAME                   93048
         AIF   (NOT &MVSESA).OLDCALL                            GP99036
         L     R4,MYMAIN     GET UCB LIST ADDRESS               GP99036
         ST    R4,INFRA                                         GP99036
         MVC   0(4,R4),MYMAIN+4  LIST SIZE                      GP99036
         MVC   4(4,R4),#UPLP  PRESET NUMBER OF UCBS THAT WILL FIT
*OLD*    EDTINFO RTNUCBA,DEVTYPE=LKPDEVT,UCBALIST=INFRA         GP99036
         EDTINFO RTNDEVN,DEVTYPE=LKPDEVT,DEVNLIST=INFRA,               *
               DYNAMIC=YES,RANGE=ALL,MF=(E,MYINFO)              GP03120
         LTR   R15,R15       DID IT WORK?                       GP99036
         BZ    HAVELIST      YES                                GP99036
         STM   R15,R0,DB     SAVE FOR FORMATTING                GP99036
         PRTLIST ERREDT      SHOW THE ERROR                     GP99036
         B     NEXTLKP       AND SKIP                           GP99036
         AGO   .COMCALL                                         GP99036
.OLDCALL LA    R1,ULTRA      POINT TO PARAMETER LIST             93048
         L     R15,@EB4UV    GET SERVICE ROUTINE                 93049
         BALR  R14,R15       INVOKE IT                           93049
.COMCALL ICM   R4,15,INFRA   ANYTHING RETURNED ?                 93048
         BZ    NEXTLKP                                           93048
         SLR   R5,R5         CLEAR                               93048
         ICM   R5,7,1(R4)    LOAD LENGTH                         93048
         LM    R0,R1,MYMAIN  GET MY STORAGE                      93048
         MIN   R1,(R5)       MOVE SHORTER                        93048
         MVCL  R0,R4         MOVE TO MY STORAGE                  93048
         L     R4,INFRA      RELOAD LIST START                   93048
         L     R0,0(,R4)     GET SUBPOOL AND LENGTH              93048
         STORAGE RELEASE,ADDR=(4),LENGTH=(0)
HAVELIST DS    0H                                               GP99036
         XC    INFRA,INFRA   CLEAR FOR DEBUGGING                 93048
         LM    R14,R15,COMPADDR  GET COMPARE MASK                93049
         LA    R1,LKPNAME    POINT TO NAME                       93049
         BAS   R9,CLCMASK    SEE IF TO BE DONE                   93049
         BNE   *+8           SET NO MATCH                        89275
         OI    FLAGS,FMATCH+FOUND  SHOW MATCH                    93049
         UNPK  WORKL8(9),LKPDEVT(5)  UNPACK HEX DEVICE MASK      93049
         TR    WORKL8,TRTOPRT-C'0'  MAKE EBCDIC                  93049
         LM    R14,R15,COMPADDR  GET COMPARE MASK STUFF          93049
         LA    R1,WORKL8     POINT TO CONVERTED MASK             93049
         BAS   R9,CLCMASK    CHECK FOR MATCH                     93049
         BNE   *+8           NO MATCH                            89275
         OI    FLAGS,FMATCH+FOUND  SHOW MATCH                    93049
         TM    FLAGS,FGALL+FMATCH  ALREADY HAVE A MATCH ?        93049
         BNZ   CONVDEV       YES; JUST CONVERT                   93049
         CLI   COMPLEN+3,3   SHORT ENOUGH FOR UCB MATCH ?        93049
         BH    NEXTLKP       NO; NO POSSIBLE MATCH ON UNIT       93049
         SPACE 1                                                 93048
*   CONVERT RETURNED UCB ADDRESSES TO BINARY. REPLACE UCB ADDRESSES BY
*     HEXADECIMAL UCB NAMES. IF NOT IN SYSTEMS MODE, SORT THEM.  93048
CONVDEV  L     R4,MYMAIN     SET COPIED ADDRESS                  93048
         ICM   R5,15,4(R4)   GET NUMBER OF UCBS                  93048
         BZ    SKIPSORT      SKIP REPLACEMENT AND SORT PHASE     93048
NEXTCON  ICM   R3,15,8(R4)   GET UCB ADDRESS                     93048
         BZ    BUMPCON       NONE ?                              93048
         AIF   (NOT &MVSESA).DEVDEF                             GP99036
         TM    FLAGS,FGALL+FMATCH  PRIOR MATCH ?                GP99036
         BNZ   NAME2HEX      YES; NO NEED TO DO IT AGAIN        GP99036
         LM    R14,R15,COMPADDR  GET COMPARE MASK STUFF         GP99036
         LA    R1,8(,R4)     POINT TO UNIT ADDRESS              GP99036
         BAS   R9,CLCMASK    CHECK FOR MATCH                    GP99036
         BNE   *+8           NO MATCH                           GP99036
         OI    FLAGS,FMATCH+FOUND  SHOW MATCH                   GP99036
NAME2HEX MVC   DB(4),TRTOFILL  MAKE MASK                        GP99036
         NC    DB(4),8(R4)   STRIP ZONES OF EBCDIC DEVICE NAME  GP99036
         TR    DB(4),TRTOFAKE  MAKE DOUBLE-SPACED HEX           GP99036
         PACK  8(3,R4),DB(5) PACK                               GP99036
         B     PASTUCB       FOR NOW - DON'T HAVE UCB ADDRESS   GP99036
.DEVDEF  USING UCBOB,R3      DECLARE UCB MAPPING
         TM    FLAGS,FGALL+FMATCH  PRIOR MATCH ?                 93049
         BNZ   MASKDEV       YES; NO NEED TO DO IT AGAIN         93049
         LM    R14,R15,COMPADDR  GET COMPARE MASK STUFF          93049
         LA    R1,UCBNAME    POINT TO UNIT ADDRESS               93049
         BAS   R9,CLCMASK    CHECK FOR MATCH                     93049
         BNE   *+8           NO MATCH                            89275
         OI    FLAGS,FMATCH+FOUND  SHOW MATCH                    93049
MASKDEV  MVC   DB(3),TRTOFILL  MAKE MASK                         93048
         NC    DB(3),UCBNAME  STRIP ZONES OF EBCDIC UCB NAME     93048
         TR    DB(3),TRTOFAKE  MAKE DOUBLE-SPACED HEX            93048
         PACK  8(3,R4),DB(4) PACK                                93048
PASTUCB  LH    R1,8(,R4)     GET HEX ADDRESS                     93048
         LA    R1,1(,R1)     MAKE NEXT ADDRESS                   93048
         STH   R1,10(,R4)    ALSO SAVE IN LIST                   93048
BUMPCON  LA    R4,4(,R4)     SKIP TO NEXT ADDRESS ENTRY          93048
         BCT   R5,NEXTCON                                        93048
         DROP  R3            NO LONGER HAVE UCB                  93048
         TM    FLAGS,FGSYS   SYSTEMS DISPLAY MODE ?              93049
         BNZ   SKIPSORT      YES; SKIP SORT                      93048
         L     R4,MYMAIN     RELOAD START OF LIST                93048
         L     R5,4(,R4)     LOAD NUMBER OF ENTRIES              93048
         BAS   R14,MSORT     CALL THE SORT                       93048
         NOP   0             PROCEED IF IT FAILED                93048
SKIPSORT TM    FLAGS,FGALL+FMATCH  ANY MATCH ?                   93049
         BZ    NEXTLKP       NO; SKIP THIS                       93049
         L     R4,MYMAIN     RESTORE LIST HEAD                   93048
         PRTLIST LOOKENT     FORMAT ONE LOOKUP ENTRY
         NI    FLAGS,255-FMATCH  RESET ENTRY MATCHED             93049
         ICM   R5,15,4(R4)   GET NUMBER OF ENTRIES               93048
         BZ    NEXTLKP                                           93048
         MVC   WORKUCB,BLANKS  CLEAR UCB RANGE                   93048
NEXTDEV  ICM   R3,15,8(R4)   GET FIRST DEV                       93048
         BZ    BUMPDEV       HUH ?                               93048
         UNPK  DB(5),8(3,R4)  UNPACK HEX UCB NAME                93048
         TR    DB+1(3),TRTOPRT-C'0'  MAKE EBCDIC AGAIN           93048
         CLI   WORKUCB+1,C' ' STACKED REQUEST ?                 GP99036
         BE    NONEDEV       NO; JUST ADD THIS ONE               93048
         CLC   NUCB,8(R4)    MATCHES EXPECTED ?                  93048
         BNE   MAKEDEV       NO; DISPLAY                         93048
*OLD*    TM    MARINE+1,X'70'  VIO, TP OR WHAT-NOT ?             93048
*OLD*    BNZ   CRAMDEV       YES; DON'T SPLIT                    93048
         TM    LKPDEVT+2,UCB3DACC+UCB3TAPE  TAPE OR DISK?       GP99036
         BM    TESTDACC      LOOSER TEST FOR TAPE AND DISK      GP99036
         TM    8+1(R4),X'0F'  STRING CONTROL ADDRESS ?          GP99036
         BZ    MAKEDEV       YES; START NEW RANGE                93048
TESTDACC TM    8+1(R4),X'1F'  STRING CONTROL ADDRESS ?          GP99036
         BZ    MAKEDEV       YES; START NEW RANGE                93048
CRAMDEV  MVI   LUCB-1,C'-'   SHOW RANGE                          93048
         CLI   DB,C'0'       THREE-DIGIT FORM?                  GP99036
         BE    CRAMDEV3                                         GP99036
         MVC   LUCB,DB       ADD AS END OF RANGE                GP99036
         B     CRAMDEVC                                         GP99036
CRAMDEV3 MVC   LUCB(3),DB+1  MOVE ONLY SIGNIFICANT DIGITS       GP99036
CRAMDEVC MVC   NUCB,10(R4)   ANTICIPATED NEXT UCB                93048
         B     BUMPDEV       LOOK AT NEXT                        93048
         SPACE 1                                                GP99036
MAKEDEV  PRTLIST PRTUCN      PRINT ONE OR RANGE                  93048
         MVC   WORKUCB,BLANKS  CLEAR AGAIN                       93048
NONEDEV  MVC   PUCB,DB       MAKE START OF RANGE                 93048
         CLI   PUCB,C'0'     LEADING FOUR-DIGIT ZERO?           GP99036
         BNE   *+8           NO                                 GP99036
         MVI   PUCB,C' '     MAKE MORE COMFORTABLE THREE-DIGIT  GP99036
         MVC   NUCB,10(R4)   SAVE NEXT                           93048
*DEFER*  MVZ   PRTUCC+3(1),NUCB  MOVE COLOR SELECTION            93049
BUMPDEV  LA    R4,4(,R4)     BUMP DOWN THE LIST                  93048
         BCT   R5,NEXTDEV                                        93048
         CLI   WORKUCB+1,C' '  STACKED ENTRY ?                  GP99036
         BE    NEXTLKP       NO                                  93048
         PRTLIST PRTUCN      YES; WRITE IT OUT                   93048
NEXTLKP  LH    R6,LKPNXT     GET POINTER TO NEXT ENTRY           93048
FIRSTLKP SH    R6,=H'1'      MAKE RELATIVE                       93048
         BM    DONELKP                                           93048
         MH    R6,=Y(LKPLNG)  CONVERT TO OFFSET                  93048
         POP   USING
         LA    R6,LKPSECT(R6)  POINT TO NEXT ENTRY               93048
         B     LOOPLKP       DO NEXT                             93048
         SPACE 1                                                 93048
DONELKP  TM    FLAGS,FGALL+FOUND  ALL OR SOME DISPLAYED ?        93049
         BNZ   PGMEXIT       YES                                 93049
         PRTLIST NOMATCH     EXPLAIN
         OICC  4,0           SET WARNING CODE
         B     PGMEXIT       RETURN
         SPACE 1
LOCFAIL  PRTL  ' *** UNABLE TO LOCATE THE EDT ***'
         MVICC 8,8           SET RETURN CODE 8, REASON 8
         SPACE 1
PGMEXIT  SERVTERM ,          CLOSE AND FREE STUFF
         ICM   R0,15,@EB4UV  EDTINFO MODULE LOADED ?
         BZ    PGMEXITZ
         DELETE EPLOC==CL8'IEFEB4UV'
PGMEXITZ PGMEXIT COPYRET=(RETCODE,8)
         SPACE 2                                                 93049
CLCMASK  CMASK ML=R15,MA=R14,L=R15,A=R1,RET=R9   MASK TEST       93049
         SPACE 1                                                 93048
MSORT    MSORT ADDR=8(R4),SIZE=4,OFF=0,COMPL=2,NUM=(R5),RET=R14  93048
         SPACE 1
SYSPRINT PRTWORK SYSPRINT,SYSTERM,TITLE=5
         SPACE 1
UPSCREEN FDOPT NL,CC=C'#'    DEFINE TITLE
         FD    'Eligible Device Table (EDT)',RADJ,YELLOW,LEN=70  93049
         FD    @EDT,HEX,PAD,WHITE                                93049
         FD    *END
HEADER2  FD    'Module',BLUE,NL                                 GP99036
         FD    EDTCREAT,PAD,YELLOW                              GP99036
         FD    'on',PAD,BLUE                                    GP99036
         FD    EDTDATE,PAD,WHITE                                GP99036
         FD    'at',PAD,BLUE                                    GP99036
         FD    EDTTIME,WHITE                                    GP99036
         FD    *END
         SPACE 1
PRTPFX   FDTM  FLAGS,FGSYS,BZ=HEADER3                            93049
         FDFD  EDTLKUP       UNITNAME/GENERIC W/DEVTYPE         GP99037
         FDFD  EDTGNRC       GENERICS                           GP99037
         FDFD  EDTGRP        GROUPS                             GP99037
         FDFD  EDTDEVT       DEVICES                            GP99037
         FDFD  EDTGMS                                           GP99037
         FDFD  EDTGPP                                           GP99037
         FDFD  EDTPREF                                          GP99037
         FDFD  EDTTAPE                                          GP99037
         FDFD  EDT@UPL       UNIT ADDRESS (FIXED)               GP99037
         FDFD  EDT@CMPG                                         GP99037
         FD    *END
         SPACE 1
HEADER3  FD    'Dev.Name',NL,WHITE                               93048
         FD    '  Type',YELLOW,LEN=6                             93048
         FD    '      Units',GREEN
HEADER3X FD    *END
         SPACE 1
         PUSH  PRINT
         DROP  R7
         USING LKPSECT,R6                                        93048
LOOKENT  FD    LKPNAME,NL,WHITE
         FD    LKPDEVT,HEX,PAD,YELLOW
         FDTM  FLAGS,FGSYS,BZ=LOOKENTX                           93049
         FD    'Mask=',PADL,BLUE
         FD    LKPMSKPT,HEX,GREEN
         FD    'Altmask=',PADL,BLUE
         FD    LKPALTPT,HEX,GREEN
         FD    'GNX=',PADL,BLUE                                  93048
         FD    LKPGNRC1,HEX,GREEN
         FD    'Num=',PADL,BLUE
         FD    LKPGNRC#,I,DEB,GREEN,LEN=6                        93048
         FD    'Flags=',PADL,BLUE
         FD    LKPFLAGS,2,HEX,GREEN
LOOKENTX FD    *END
         POP   PRINT
         SPACE 1
PRTUCN   FDOPT IND=6                                             93048
PRTUCC   FD    WORKUCB,PAD,DEB,GREEN                             93049
         FD    *END
         SPACE 1                                                 93049
NOMATCH  FDOPT NL                                                93049
         FD    '     No entry found',NL,INT,YELLOW               93049
         FDCLC COMPLEN,ZEROES,4,BE=NOMATCHX                      93049
         FD    'for',PAD,INT,YELLOW                              93049
         FD    COMPMASK,WHITE,INT                                93049
NOMATCHX FD    *END                                              93049
         SPACE 1                                                GP99036
ERREDT   FDEXEC LOOKENT,LOOKENTX  MAKE GROUP INFO               GP99036
         FD    '    EDTINFO error. RC=',YELLOW                  GP99036
         FD    DB,4,I,DEB,RED                                   GP99036
         FD    '  Reason=',BLUE                                 GP99036
         FD    DB+4,4,I,DEB,GREEN                               GP99036
         FD    *END                                             GP99036
         EJECT ,
TRTOFAKE DC    X'00FAFBFCFDFEFF00'     1/3
TRTOFILL DC    8X'1F'        MASK      2/3                 1/1
TRTOPRT  DC    C'0123456789'           3/3       1/2
         DC    C'ABCDEF'                         2/2
HEXTRTAB EQU   TRTOPRT-C'0',256,C'C'             3/2
BLANKS   DC    CL80' '
         SPACE 1
         PRINT &PRTSYS
MYUCB    DSECT ,
         IEFUCBOB ,          NEED FOR L' EXPANSIONS
         SPACE 2
SAVE     DSECT ,
DB       DS    D             WORK AREA
DB2      DS    D             WORK AREA
         SERVDEFS ,          DEFINE SERVICE, PRINTER, DEBUG, ETC.
         SPACE 1
@EB4UV   DS    A             ADDRESS OF IEFEB4UV
@EDT     DC    A(0)          ADDRESS OF ELIGIBLE DEVICE TABLE
@UPLP    DC    A(0)          ADDRESS OF UCB LOOKUP TABLE         93048
#UPLP    DC    F'0'          MAXIMUM NUMBER OF UCBS             GP99036
COMPADDR DC    A(COMPMASK)   MASK ADDRESS                        93049
COMPLEN  DC    A(0)          COMPARE LENGTH                      93049
COMPMASK DC    CL8' '        TYPE/NAME/UCB MASK                  93049
ULTRA    DC    A(VIOLET,AQUA)  CALLING PARAMETER LIST            93048
VIOLET   DC    CL8' '        NAME REQUESTED                      93048
INFRA    DC    F'0'          RETURNED LIST                       93048
         DC    A(MARINE)                                         93048
         DC    AL1(1),AL3(0)  USE SUBPOOL 1 FOR ALL REQUESTS     93048
AQUA     DC    X'10A0'       RETURN UNIT ADDRESSES AND ATTRIBUTES
MARINE   DC    X'0A',XL9'0'  ATTRIBUTES                          93048
MYMAIN   DC    A(0,0)        ADDRESS AND LENGTH OF MY GETMAIN    93048
WORKL8   DC    CL8' ',C' '   TEMP WORK AREA                      93049
         SPACE 1
         EDTINFO MF=(L,MYINFO)
         SPACE 1
PREVTYPE DS    XL4           PREVIOUSLY DISPLAYED GEN/UCB TYPE
         SPACE 1
FLAGS    DC    X'00'         PROCESSING OPTIONS                  93049
FGSYS    EQU   X'80'           DISPLAY SYSTEMS STUFF
FGALL    EQU   X'10'           DISPLAY ALL ENTRIES               93049
FMATCH   EQU   X'08'           DISPLAY THIS ENTRY                93049
FOUND    EQU   X'01'           AT LEAST ONE ENTRY DISPLAYED      93049
         SPACE 1
MYUNIT   DS    CL8
         DS    0F
MYATTR   DS    XL10
ZEROES   DS    XL8
         SPACE 1
NUCB     DC    H'0'          ANTICIPATED NEXT UCB                93048
PUCB     DC    CL(L'UCBNAME+&MVSESA)' '                         GP99036
         DC    C'-'          RANGE INDICATOR
LUCB     DC    CL(L'UCBNAME+&MVSESA)' '                         GP99036
WORKUCB  EQU   PUCB,*-PUCB,C'C'
SAVEEND  EQU   *             END OF GETMAIN
         SPACE 1
         PRINT &PRTSYS
         SPACE 2
EDT      DSECT ,                   ELIGIBLE DEVICES TABLE      DM JUN83
EDTNAME  DS    CL3                   EDT NAME                  SS FEB88
EDTLEVEL DS    C                     LEVEL                     SS FEB88
EDTCREAT DS    0CL8                EDT CREATION ID             SS FEB88
EDTCBNAM DS    CL6                 EDT CB NAME                 SS FEB88
EDTIDNUM DS    CL2                 EDT VER ID                  SS FEB88
EDTDATE  DS    CL8                   CREATION DATE (MM/DD/YY)  SS FEB88
EDTTIME  DS    CL5                     TIME (HH.MM)            SS FEB88
         DS    XL3                                             SS FEB88
EDTLKUP  DS    A                   POINTER TO LOOK-UP SECTION  DM JUN83
EDTGNRC  DS    A                   POINTER TO GENERIC SECTION  DM JUN83
EDTGRP   DS    A                   POINTER TO GROUP SECTION    DM JUN83
EDTDEVT  DS    0A                                               GP99037
EDTUCB   DS    A                   POINTER TO UCB SECTION      DM JUN83
EDTGMS   DS    0A                                               GP99037
EDTMSK   DS    A                   POINTER TO MASK SECTION     DM JUN83
EDTGPP   DS    0A                                               GP99037
EDTGRPT  DS    A                   POINTER TO GROUP POINTERS   DM JUN83
EDTPREF  DS    A                   POINTER TO PREFERENCE TABLE DM JUN83
EDTTAPE  DS    A                   POINTER TO TAPE DEVICE EL   SS FEB88
* THE FOLLOWING ADDED TO HEADER FOR DYNAMSK2                   SS FEB88
EDTGENE  DS    A                   MVSCP GENERATED EDT ADDRESS SS FEB88
EDTSIZE  DS    A                   SUBPOOL AND SIZE OF EDT     SS FEB88
EDT@UPL  EQU   EDT+X'6C',4,C'A'   ADDRESS OF UCB ADDRESS TABLE  GP99036
EDT@CMPG EQU   EDT+X'7C',4,C'A'   ADDRESS OF ? (ALL ZEROES)     GP99036
EDTHDRLX EQU   *-EDT               LENGTH OF THE HEADER        DM JUN83
         SPACE 2                                               DM JUN83
* EACH OF THE FOLLOWING SECTIONS IS PREFIXED BY                SS FEB88
*   A HEADER  - MOST ARE 12 BYTES LONG EXCEPT FOR LOOKUP HDR   SS FEB88
*   FIRST 8 BYTES ARE HEADER ID                                SS FEB88
*   NEXT  4 BYTES ARE THE NUMBER OF ENTRIES                    SS FEB88
*   NEXT  4 BYTES ARE FOR LOOKUP SECTION ONLY                  SS FEB88
*         2 BYTES ARE FOR LOOKUP INDEX TO FIRST ENTRY          SS FEB88
*         2 BYTES ARE RESERVED                                 SS FEB88
         SPACE ,                                               DM JUN83
LKPPFX   DSECT ,                                                 93048
LKPHEAD  DS    CL8           HEADER                              93048
LKPNUM   DS    F             NUMBER OF ENTRIES                   93048
LKPINX   DS    H,H           INDEX TO FIRST ENTRY                93048
LKPSECT  DS    0F                  LOOK-UP SECTION             DM JUN83
LKPNAME  DS    CL8                 UNITNAME                    DM JUN83
LKPDEVT  DS    XL4                 DEVICE TYPE (AS IN CATALOG) DM JUN83
LKPMSKPT DS    A                   INDEX   TO MASK ENTRY       SS FEB88
LKPALTPT DS    A                   ALTERNATE MASK INDEX        SS FEB88
LKPGNRC# DS    F                   NUMBER OF GENERICS          DM JUN83
LKPFLAGS DS    XL2                 FLAGS                       DM JUN83
LKPVIO   EQU   X'80'               ELIGIBLE FOR VIO            SS JAN84
LKPAGMSK EQU   X'40'               GENERIC HAS ALT MASK        SS JAN84
LKPMGEN  EQU   X'20'               ALLOC OF MUTL GEN OK        SS JAN84
LKPGENR  EQU   X'10'               THIS IS A GENERIC: FULL DEV SS JAN84
LKPESOTR EQU   X'08'               THIS IS A ESOTERIC          SS JAN84
LKPGGERN EQU   X'04'               THIS IS A GENERATED GEN     SS JAN84
LKPGESOR EQU   X'08'               THIS IS A GENERATED ESOTR   SS FEB88
LKPAFFIX DS    H                   AFFINITY INDEX              SS FEB88
LKPGNRC1 DS    H                   INDEX   TO FIRST GENERIC    SS FEB88
LKPNXT   DS    H                   INDEX TO NEXT ENTRY         SS FEB88
         DS    (20*&MVSESA)X  DON'T KNOW YET                    GP99036
LKPLNG   EQU   *-LKPSECT           LENGTH OF SECTION           DM JUN83
         SPACE ,                                               DM JUN83
GNRCSECT DSECT ,                   GENERIC SECTION             DM JUN83
GNRCDEVT DS    XL4                 DEVICE TYPE (AS IN CATALOG) DM JUN83
GNRCGRP# DS    F                   NUMBER OF GROUPS            DM JUN83
GNRCGRP1 DS    H                   INDEX   TO FIRST GROUP      SS FEB88
GNRCNXT  DS    H                   INDEX TO NEXT ENTRY         SS FEB88
GNRCLNG  EQU   *-GNRCSECT          LENGTH OF SECTION           DM JUN83
         SPACE ,                                               DM JUN83
GRPTSECT DSECT                     GROUP POINTER SEC           SS JAN84
GRPTPTR  DS    H                   A GROUP INDEX               SS FEB88
GRPTNXT  DS    H                   INDEX TO NEXT ENTRY         SS FEB88
GRPTLNG  EQU   *-GRPTSECT          LENGTH OF SECTION           SS FEB88
         SPACE ,                                               SS JAN84
GRPSECT  DSECT ,                   GROUP SECTION               DM JUN83
GRPID    DS    H                   GROUP ID NUMBER             SS FEB88
GRPDEV#  DS    H                   NUMBER OF DEVICES           SS FEB88
GRPUCB1  DS    A                   INDEX   TO FIRST UCB        SS FEB88
GRPNXT   DS    H                   INDEX TO NEXT ENTRY         SS FEB88
         DS    H                   RESERVED                    SS FEB88
GRPLNGX  EQU   *-GRPSECT           LENGTH OF SECTION XA SECT   DM JUN83
         SPACE ,                                               DM JUN83
MSKSECT  DSECT ,                   GROUP MASK SECTION          DM JUN83
MSKMASK  DS    X                   GROUP MASK (LENGTH VARIABLE)DM JUN83
MSKLNG   EQU   *-MSKSECT           LENGTH OF SECTION           DM JUN83
         SPACE ,                                               DM JUN83
UCBSECT  DSECT ,                   UCB POINTER SECTION         DM JUN83
DEV#     DS    CL3                 DEVICE NUMBER IN EBCDIC     DM NOV83
         DS    C                   EXTRA BYTE                  SS FEB88
DEVNXT   DS    F                   INDEX TO NEXT ENTRY         SS FEB88
UCBLNGX  EQU   *-UCBSECT           LENGTH OF SECTION XA SECT   DM JUN83
         SPACE 2
         PRINT &PRTSYS
         IEFJESCT ,
         SPACE 1
         IHACDE ,
         SPACE 1
         IHAPSA ,
         SPACE 1
         IKJTCB ,
         SPACE 1
         IEZJSCB ,
         SPACE 1
DSECTJCT DSECT ,
         IEFAJCTB ,
         SPACE 1
         CVT   DSECT=YES
         END
