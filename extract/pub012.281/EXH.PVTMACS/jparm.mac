         MACRO
         JPARM &FIXED=,                 FIXED PARAMETER OPTIONS        X
               &DEFAULT=,               DEFAULT PARAMETER OPTIONS      X
               &VERSION=0               VERSION IDENTIFICATION
         LCLA  &I,&J,&L,&X,&Y,&Z                               @AZ13738
         LCLB  &A(25),&B(25),&C(25),&D(25),&F,&N               @AZ13738
         LCLC  &K,&S                                           @AZ13738
.* GENERATES THE CODE IN IFOX0J WHICH CONTAINS THE BIT STRINGS
.* REPRESENTING OPTIONS IN EFFECT
&X       SETA  55                                 LINECNT=60
&Z       SETA  X'200000'                DEFAULT ASMSIZE=2M     @AZ13738
&B(1)    SETB  1
&B(2)    SETB  1
&B(3)    SETB  1
&B(4)    SETB  1
&B(5)    SETB  1
&B(6)    SETB  1
&B(7)    SETB  1
&B(8)    SETB  1
&B(9)    SETB  1
&B(10)   SETB  1
&B(11)   SETB  1
&B(12)   SETB  1
&B(13)   SETB  1
&B(14)   SETB  1
&B(15)   SETB  1
&B(16)   SETB  1
&B(17)   SETB  1
&B(18)   SETB  1
&B(19)   SETB  1
&B(20)   SETB  1
&B(21)   SETB  1
&B(22)   SETB  1
&B(23)   SETB  1
&B(24)   SETB  1
&B(25)   SETB  1
&D(1)    SETB  1                                  LIST
&D(2)    SETB  1                                  XREF
&D(3)    SETB  1                                  ESD
&D(4)    SETB  1                                  RLD
&D(5)    SETB  1                                  DECK
&D(8)    SETB  1                                  STAT
&D(10)   SETB  1                                  ALGN
&D(12)   SETB  1                                  ALOGIC
&D(18)   SETB  1                        STMT
&D(19)   SETB  1
&D(20)   SETB  1                   XREF(SHORT)
&D(25)   SETB  1                        YFLAG
.P1      ANOP
&I       SETA  &I+1                               INCREMENT INDEX
         AIF   (&I GT N'&FIXED).D1                FINIS
         AIF   (K'&FIXED(&I) EQ 0).P1             NULL SUB PARAMETER
         AIF   (K'&FIXED(&I) GT 2).E4             INVALID SUB PARAMETER
.E5      MNOTE 4,'* * * INVALID OPERAND -- &FIXED(&I)'
         AGO   .P1
.E4      AIF   ('&FIXED(&I)'(1,2) NE 'NO').P2     PREFIX 'NO'
&N       SETB  1                                  PREFIX 'NO'
&K       SETC  '&FIXED(&I)'(3,K'&FIXED(&I)-2)     KEYWORD
         AGO   .P3                                SKIP
.P2      AIF   (K'&FIXED(&I) GT 8).X1             KEYWORD
&N       SETB  0                                  NO PREFIX 'NO'
&K       SETC  '&FIXED(&I)'                       KEYWORD
.P3      ANOP
&J       SETA  1                                  'LIST'
         AIF   ('&K' EQ 'LIST').S1                LIST/NOLIST
&J       SETA  2                                  'XREF'
         AIF   ('&K' EQ 'XREF').S1                XREF/NOXREF
&J       SETA  3                                  'ESD'
         AIF   ('&K' EQ 'ESD').S1                 ESD/NOESD
&J       SETA  4                                  'RLD'
         AIF   ('&K' EQ 'RLD').S1                 RLD/NORLD
&J       SETA  5                                  'DECK'
         AIF   ('&K' EQ 'DECK').S1                DECK/NODECK
&J       SETA  6                                  'LOAD'
         AIF   ('&K' EQ 'LOAD').S1                LOAD/NOLOAD
&J       SETA  7                                  'TEST'
         AIF   ('&K' EQ 'TEST').S1                TEST/NOTEST
&J       SETA  8                                  'STAT'
         AIF   ('&K' EQ 'STAT').S1                STAT/NOSTAT
&J       SETA  9                                  'RENT'
         AIF   ('&K' EQ 'RENT').S1                RENT/NORENT
&J       SETA  10                                 'ALGN'
         AIF   ('&K' EQ 'ALGN').S1                ALGN/NOALGN
&J       SETA  11                                 'SYSMAC'
         AIF   ('&K' EQ 'SYSMAC').S1              SYSMAC/NOSYSMAC
&J       SETA  12                                 'ALOGIC'
         AIF   ('&K' EQ 'ALOGIC').S1              ALOGIC/NOALOGIC
&J       SETA  13                                 'MLOGIC'
         AIF   ('&K' EQ 'MLOGIC').S1              MLOGIC/NOMLOGIC
&J       SETA  14                                 'CALLS'
         AIF   ('&K' EQ 'CALLS').S1               CALLS/NOCALLS
&J       SETA  18                                 'TERM'
         AIF   ('&K' EQ 'TERM').S1                TERM/NOTERM
&J       SETA  19                                 'STMT'
         AIF   ('&K' EQ 'STMT').S1                STMT/NOSTMT
&J       SETA  20                                 'NUM'
         AIF   ('&K' EQ 'NUM').S1                 NUM/NONUM
&J       SETA  25                       'YFLAG'                @AZ13738
         AIF   ('&K' EQ 'YFLAG').S1     YFLAG/NOYFLAG          @AZ13738
         AIF   (NOT &F).E5                        INVALID KEYWORD
         AGO   .E7
.S1      AIF   (&F).S2
         AIF   (NOT &A(&J)).S4
.ED      MNOTE 4,'* * * INCOMPATIBLE OPTION -- &FIXED(&I)'
         AGO   .P1
.S4      ANOP
&A(&J)   SETB  1
&B(&J)   SETB  (NOT &N)
&C(&J)   SETB  (NOT &N)
&D(&J)   SETB  (NOT &N)
         AGO   .P1
.S2      AIF   (NOT &A(&J)).S3
.EC      MNOTE 4,'* * * INCOMPATIBLE OPTION -- &DEFAULT(&L)'
         AGO   .D1
.S3      ANOP
&A(&J)   SETB  1
&D(&J)   SETB  (NOT &N)                           DEFAULT OPTION
.D1      ANOP
&F       SETB  1                                  DEFAULT SCAN
&L       SETA  &L+1                               INCREMENT INDEX
         AIF   (&L GT N'&DEFAULT).G1              END OF SCAN
         AIF   (K'&DEFAULT(&L) EQ 0).D1           NULL SUB OPERAND
         AIF   (K'&DEFAULT(&L) GT 2).E6           INVALID SUB PARAMETER
.E7      MNOTE 4,'* * * INVALID OPERAND -- &DEFAULT(&L)'
         AGO   .D1
.E6      AIF   ('&DEFAULT(&L)'(1,2) NE 'NO').D2   PREFIX 'NO'
&N       SETB  1                                  PREFIX 'NO'
&K       SETC  '&DEFAULT(&L)'(3,K'&DEFAULT(&L)-2) KEYWORD
         AGO   .P3
.D2      AIF   (K'&DEFAULT(&L) GT 8).K1           KEYWORD
&N       SETB  0                                  NO PREFIX
&K       SETC  '&DEFAULT(&L)'                     KEYWORD
         AGO   .P3
.K1      AIF   ('&DEFAULT(&L)'(1,8) NE 'LINECNT=').K11
&K       SETC  '&DEFAULT(&L)'(9,K'&DEFAULT(&L)-8) VALUE
         AGO   .Y1
.K11     AIF  ('&DEFAULT(&L)'(1,9) NE 'WORKSIZE=').K2          @AZ13738
&S       SETC  '&DEFAULT(&L)'(10,K'&DEFAULT(&L)-9) VALUE       @AZ13738
         AGO   .SIZE                                           @AZ13738
.K2      AIF   ('&DEFAULT(&L)'(1,9) NE 'MSGLEVEL=').E7
&K       SETC  '&DEFAULT(&L)'(10,K'&DEFAULT(&L)-9)
         AGO   .Z1
.X1      AIF   ('&FIXED(&I)'(1,8) NE 'LINECNT=').X11           @AZ13738
&K       SETC  '&FIXED(&I)'(9,K'&FIXED(&I)-8)     VALUE
         AGO   .Y1
.X11     AIF   ('&FIXED(&I)'(1,9) NE 'WORKSIZE=').X2           @AZ13738
&S       SETC  '&FIXED(&I)'(10,K'&FIXED(&I)-9) VALUE           @AZ13738
         AGO   .SIZE                                           @AZ13738
.X2      AIF   ('&FIXED(&I)'(1,9) NE 'MSGLEVEL=').E5
&K       SETC  '&FIXED(&I)'(10,K'&FIXED(&I)-9)    VALUE
.Z1      ANOP
&Y       SETA  &K                                 VALUE
         AIF   (&Y LT 256).EF
         MNOTE 4,'* * * INVALID VALUE -- &Y'
&Y       SETA  0
         AIF   (&F).D1
         AGO   .P1
.EF      AIF   (&A(24) AND &F).EC
         AIF   (&A(24)).ED
&A(24)   SETB  1
         AIF   (&F).D1
&D(24)   SETB  1
         AGO   .P1
.Y1      ANOP
&X       SETA  &K-5                               VALUE
         AIF   (&X GT 0 AND &X LT 32768).EA       INVALID VALUE
.EB      MNOTE 4,'* * * INVALID VALUE -- &X'
&X       SETA  60
         AIF   (&F).D1
         AGO   .P1
.EA      AIF   (&A(23) AND &F).EC                 REDUNDANT
         AIF   (&A(23)).ED
&A(23)   SETB  1                                  INDICATE USED
         AIF   (&F).D1
&D(23)   SETB  1                                  FIXED
         AGO   .P1
.SIZE    ANOP                                                  @AZ13738
&Z       SETA  &S                                              @AZ13738
         AIF   (&Z GT X'8000' AND &Z LT X'A00000').SIZE1       @AZ13738
         MNOTE 4,'* * * INVALID VALUE -- &Z'                   @AZ13738
&Z       SETA  X'200000'                                       @AZ13738
.SIZE1   AIF   (&F).D1                                         @AZ13738
         AGO   .P1                                             @AZ13738
.G1      ANOP
IFOX0J00 CSECT
         DC    AL1(&VERSION,&Y),H'&X'               , MSGLEVEL, LINECNT
         DC    B'&D(1)&D(2)&D(3)&D(4)&D(5)&D(6)&D(7)&D(8)&D(9)&D(10)&D(X
               11)&D(12)&D(13)&D(14)&D(15)&D(16)&D(17)&D(18)&D(19)&D(20X
               )&D(21)&D(22)&D(23)&D(24)&D(25)0000000' DEFAULT @OX09734
         DC    B'&C(1)&C(2)&C(3)&C(4)&C(5)&C(6)&C(7)&C(8)&C(9)&C(10)&C(X
               11)&C(12)&C(13)&C(14)&C(15)&C(16)&C(17)&C(18)&C(19)&C(20X
               )&C(21)&C(22)&C(23)&C(24)&C(25)0000000'   FIXED TO ON
         DC    B'&B(1)&B(2)&B(3)&B(4)&B(5)&B(6)&B(7)&B(8)&B(9)&B(10)&B(X
               11)&B(12)&B(13)&B(14)&B(15)&B(16)&B(17)&B(18)&B(19)&B(20X
               )&B(21)&B(22)&B(23)&B(24)&B(25)1111111'   FIXED TO OFF
         DC    F'&Z'                                           @AZ13738
         MEND
