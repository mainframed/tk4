 /********************************************************************/
 /*                                                          @Y30LB38*/
 /*     PROCEDURE NAME - VOLEXIT                             @Y30LB38*/
 /*                                                          @Y30LB38*/
 /*     FUNCTION - REMOVES A VOLUME FROM THE MSF.            @Y30LB38*/
 /*                IF THE VOLUME IS EJECTED SUCCESSFULLY,    @Y30LB38*/
 /*                ISSUES MESSAGE IDC0085I TO SYSPRINT AND   @Y30LB38*/
 /*                MESSAGE IDC083E TO THE OPERATOR.  IF THE  @Y30LB38*/
 /*                VOLUME IS NOT EJECTED SUCCESSFULLY, AN    @Y30LB38*/
 /*                ERROR MESSAGE IS ISSUED VIA SSCMSG        @Y30LB38*/
 /*                PROCEDURE AND MESSAGE IDC2084I IS ISSUED  @Y30LB38*/
 /*                TO SYSPRINT.                              @Y30LB38*/
 /*                                                          @Y30LB38*/
 /*     INPUT - THE FOLLOWING POSITIONAL PARAMETERS ARE      @Y30LB38*/
 /*             PASSED AS INPUT:                             @Y30LB38*/
 /*             ARG1 = CSN OF FIRST VOLUME CARTRIDGE         @Y30LB38*/
 /*             ARG2 = VOLUME SERIAL NUMBER                  @Y30LB38*/
 /*             ARG3 = VVI ERROR FLAG                        @Y30LB38*/
 /*             ARG4 = RETAIN RECORD FLAG                    @Y30LB38*/
 /*             ARG5 = VOLUME STORAGE LOCATION               @Y30LB38*/
 /*                                                          @Y30LB38*/
 /*     OUTPUT - RETURN CODE IN REG15                        @Y30LB38*/
 /*              REG15 = 00 CARTRIDGES EJECTED SUCCESSFULLY  @Y30LB38*/
 /*              REG15 = 04 CARTRIDGES NOT EJECTED           @Y30LB38*/
 /*                         SUCCESSFULLY                     @Y30LB38*/
 /*              REG15 = 08 SECOND CARTRIDGE NOT EJECTED     @Y30LB38*/
 /*                         SUCCESSFULLY                     @Y30LB38*/
 /*                                                          @Y30LB38*/
 /********************************************************************/
 VOLEXIT:
   PROCEDURE(VECSN1,                   /* CART SERIAL NUMBER @Y30LB38*/
             VEVOLUME,                 /* VOL SERIAL NUMBER  @Y30LB38*/
             VEVVIERR,                 /* VVI ERROR FLAG     @Y30LB38*/
             VERECFLG,                 /* RETAIN RECORD FLAG @Y30LB38*/
             VEVOLLOC)                 /* VOL STORAGE LOC    @Y30LB38*/
             OPTIONS(NOSAVE,NOSAVEAREA);                   /*@Y30LB38*/
   @EJECT;                                                 /*@Y30LB38*/
   %INCLUDE SYSLIB(IEZMCRTE);                              /*@Y30LB38*/
 @EJECT;                                                   /*@Y30LB38*/
     /* DECLARATIONS FOR VOLEXIT                             @Y30LB38*/
   DCL VEDDSTRU POINTER(31);           /* DARGLIST POINTER   @Y30LB38*/
   DCL MCLSTPTR POINTER(31);           /* BASE PTR FOR MCLIST@Y30LB38*/
   DCL MCRTEPTR POINTER(31);           /* PTR FOR MCLIST EXT @Y30LB38*/
   DCL VERETADR POINTER(31);           /* RETURN ADDR        @Y30LB38*/
   DCL VEMSGPTR POINTER(31);           /* MESSAGE PTR AREA   @Y30LB38*/
   DCL VEWTOPTR POINTER(31);           /* WTO MSG POINTER    @Y30LB38*/
   DCL VECOND FIXED(15);               /* CONDITION CODE     @Y30LB38*/
   DCL VEWTOLEN FIXED(15);             /* LENGTH OF WTO MSG  @Y30LB38*/
   DCL VERESNCD FIXED(31);             /* RSN CODE FOR USSC  @Y30LB38*/
   DCL VEECB CHAR (1);                 /* ECB RETURN AREA    @Y30LB38*/
     /* PARM LIST FOR UPRINT MACRO WITH MULTIPLE INSERTS     @Y30LB38*/
   DCL VELIST CHAR(LENGTH(DARGLIST)+(4*LENGTH(DARGARY)));  /*@Y30LB38*/
     /* POSITIONAL PARMS FOR INPUT PARMAMETERS               @Y30LB38*/
   DCL VECSN1 CHAR(12);                /* CART SERIAL NUM    @Y30LB38*/
   DCL VEVOLUME CHAR(6);               /* VOL SER NUMBER     @Y30LB38*/
   DCL VEVVIERR BIT(1);                /* VVI ERROR FLAG     @Y30LB38*/
   DCL VERECFLG BIT(1);                /* RETAIN REC FLAG    @Y30LB38*/
   DCL VEVOLLOC CHAR(30);              /* VOL LOCATION       @Y30LB38*/
   DCL VEWTOMSG CHAR(72);              /* UWTO MSG AREA      @Y30LB38*/
     /* INVOKE LIST FORM OF MCLIST MACRO                     @Y30LB38*/
    ? ICBMCRT MF(L,MCLIST,BASED(MCLSTPTR));                /*@Y30LB38*/
   @EJECT;
     /* DECLARE WORK AREA FOR MC LIST AND EXTENSION LIST     @Y30LB38*/
   DCL VEMCLIST CHAR(LENGTH(MCLIST));                      /*@Y30LB38*/
   DCL VEEXLIST CHAR(LENGTH(MCRTE));                       /*@Y30LB38*/
     /* DECLARE WORK AREA FOR JOBNAME AND STEPNAME USED BY   @Y30LB38
        USYSINFO MACRO                                       @Y30LB38*/
   DCL 1 VESYSIN CHAR(16),
     2 VEJOBNAM CHAR(8),
     2 VESTEPNM CHAR(8);                                   /*@Y30LB38*/
     /* DECLARE ROUTING AND DESCRIPTOR CODE AREAS FOR UWTO   @Y30LB38*/
   DCL 1 VEROUTCD BIT(16),
     2 VECD1 BIT(1),
     2 * BIT(15);                                          /*@Y30LB38*/
   DCL 1 VEDESCCD BIT(16),
     2 * BIT(2),
     2 VEDESC3 BIT(1),
     2 * BIT(12);                                          /*@Y30LB38*/
     /* DECLARE WORK AREA FOR UDUMP MACRO                    @Y30LB38*/
   DCL VEDMPSTG CHAR(17);                                  /*@Y30LB38*/
     /* DECLARE STATIC MODULE ID FOR MESSAGES USED BY        @Y30LB38
        COMMON FSR'S                                         @Y30LB38*/
   DCL VECMOMSG CHAR(3) CONSTANT('CM0');                   /*@Y30LB38*/
   DCL MSG0085I PTR(8) CONSTANT(21);   /* MSG ENTRY NUMBER   @Y30LB38*/
   DCL MSG2084I PTR(8) CONSTANT(22);   /* MSG ENTRY NUMBER   @Y30LB38*/
   DCL MSG083E PTR(8) CONSTANT(23);    /* MSG ENTRY NUMBER   @Y30LB38*/
     /* VOLEXIT CONSTANTS                                    @Y30LB38*/
   DCL VEECBLEN FIXED(31) CONSTANT(4); /* LENGTH OF ECB      @Y30LB38*/
 @EJECT;
     /* SAVE RETURN ADDRESS AND INITIALIZE PARAMETER LISTS   @Y30LB38*/
   VERETADR = REG14;                                       /*@Y30LB38*/
   UTRACE = 'VESR';                                        /*@Y30LB38*/
   VELIST = ''B;                                           /*@Y30LB38*/
   VEDDSTRU = ADDR(VELIST);                                /*@Y30LB38*/
   MCLSTPTR = ADDR(VEMCLIST);                              /*@Y30LB38*/
   MCRTEPTR = ADDR(VEEXLIST);                              /*@Y30LB38*/
   VEWTOPTR = ADDR(VEWTOMSG);                              /*@Y30LB38*/
   MCLIST = ''B;                                           /*@Y30LB38*/
   MCRTE = ''B;                                            /*@Y30LB38*/
   VECOND = '0'X;                                          /*@Y30LB38*/
     /* INITIALIZE MCLIST FOR USSC MACRO                     @Y30LB38*/
   MCRLNGTH = MCRLEN;                                      /*@Y30LB38*/
   MCRTCSN = VECSN1;                                       /*@Y30LB38*/
   MCRTTRAN = ON;                                          /*@Y30LB38*/
   IF VERECFLG = ON THEN
     MCRREC = ON;                                          /*@Y30LB38*/
   MCRLOC = VEVOLLOC;                                      /*@Y30LB38*/
   MCRTVEXT = MCRTEPTR;                                    /*@Y30LB38*/
   MCRSPLOC = ON;                                          /*@Y30LB38*/
     /* TAKE UDUMP OF MOVE VOLUME PARAMETER LIST (MCLIST)    @Y30LB38*/
   ? DMPSTART LIST(VEDMPSTG);                              /*@Y30LB38*/
   ? DMPITEM NAME(MCLIST) LENGTH(LENGTH(MCLIST)) TYPE(H)
     ADDRESS(MCLSTPTR);                                    /*@Y30LB38*/
   ? DMPEND;                                               /*@Y30LB38*/
   UDUMP (GDTTBL,'VEBM',ADDR(VEDMPSTG));                   /*@Y30LB38*/
     /*  ISSUE USSC MACRO TO EJECT THE VOLUME                @Y30LB38*/
   USSC(GDTTBL,MOVE,MCLSTPTR,VERESNCD,VEECB,VEMSGPTR);     /*@Y30LB38*/
   USAVERC;                                                /*@Y30LB38*/
     /* SET UP ADDRESSABILITY TO MESSAGE AREA FROM USSC      @Y30LB38*/
   VEMSGPTR = VEMSGPTR+VEECBLEN;                           /*@Y30LB38*/
   @EJECT;                                                 /*@Y30LB38*/
     /*  IF CARTRIDGE WERE SUCCESSFULLY EJECTED; ISSUE THE   @Y30LB38
         APPROPRIATE MESSAGE                                 @Y30LB38*/
   IF TESTRC = LASTCC00 × TESTRC = LASTCC12 THEN
     DO;                                                   /*@Y30LB38*/
     /*  IF AN ERROR OCCURRES UPDATING INVENTORY, SET A      @Y30LB38
         FLAG ON FOR THE CALLER                              @Y30LB38*/
       IF TESTRC = LASTCC12 THEN
         VEVVIERR = ON;                                    /*@Y30LB38*/
     /* ISSUE USYSINFO MACRO TO OBTAIN JOBNAME AND STEPNAME  @Y30LB38
        TO BE USED IN MESSAGE IDC083E                        @Y30LB38*/
       USYSINFO (GDTTBL,NAMES,ADDR(VESYSIN));              /*@Y30LB38*/
     /* INITIIALIZE DARGLIST WITH MULTIPLE INSERTS           @Y30LB38
        FOR MESSAGE IDC083E                                  @Y30LB38*/
       VEDDSTRU -> DARGSMOD = VECMOMSG;                    /*@Y30LB38*/
       VEDDSTRU -> DARGSENT = MSG083E;                     /*@Y30LB38*/
       VEDDSTRU -> DARGDBP =ADDR(VEMSGPTR->CMCSN1);        /*@Y30LB38*/
       VEDDSTRU -> DARGILP = LENGTH(CMCSN1);               /*@Y30LB38*/
       VEDDSTRU -> DARGRETP = VEWTOPTR;                    /*@Y30LB38*/
       VEDDSTRU -> DARGRETL = LENGTH(VEWTOMSG);            /*@Y30LB38*/
     /* IF BOTH CARTRIDGES WERE EJECTED, SET DARGLIST        @Y30LB38
        FOR FIVE INSERTS.  IF NOT USE FOUR INSERTS.          @Y30LB38
      IF FIVE INSERTS ARE USED, THEY WILL BE CSN1, CSN2,     @Y30LB38
       VOLUME SERIAL NUMBER, JOB NAME AND STEP NAME.         @Y30LB38
       IF FOUR INSERTS ARE USED CSN2 WILL BE OMMITTED.       @Y30LB38*/
       IF VEMSGPTR -> CMNUMCRT = 'F2'X THEN                /*@Y30LB38*/
         DO;                                               /*@Y30LB38*/
           VEDDSTRU -> DARGCNT = '4'X;                     /*@Y30LB38*/
           VEDDSTRU -> DARGINS(1) = '1'X;                  /*@Y30LB38*/
           VEDDSTRU -> DARGINL(1)=LENGTH(CMCSN2);          /*@Y30LB38*/
           VEDDSTRU -> DARGDTM(1) =ADDR(VEMSGPTR->CMCSN2); /*@Y30LB38*/
           VEDDSTRU -> DARGINS(2) = '2'X;                  /*@Y30LB38*/
           VEDDSTRU -> DARGINL(2) = LENGTH(VEVOLUME);      /*@Y30LB38*/
           VEDDSTRU -> DARGDTM(2) = ADDR(VEVOLUME);        /*@Y30LB38*/
           VEDDSTRU -> DARGINS(3) = '3'X;                  /*@Y30LB38*/
           VEDDSTRU -> DARGINL(3) = LENGTH(VEJOBNAM);      /*@Y30LB38*/
           VEDDSTRU -> DARGDTM(3) = ADDR(VEJOBNAM);        /*@Y30LB38*/
           VEDDSTRU -> DARGINS(4) = '4'X;                  /*@Y30LB38*/
           VEDDSTRU -> DARGINL(4) = LENGTH(VESTEPNM);      /*@Y30LB38*/
           VEDDSTRU -> DARGDTM(4) = ADDR(VESTEPNM);        /*@Y30LB38*/
         END;                                              /*@Y30LB38*/
       ELSE
         DO;                                               /*@Y30LB38*/
           VEDDSTRU -> DARGCNT = '3'X;                     /*@Y30LB38*/
           VEDDSTRU -> DARGINS(1) = '2'X;                  /*@Y30LB38*/
           VEDDSTRU -> DARGINL(1) = LENGTH(VEVOLUME);      /*@Y30LB38*/
           VEDDSTRU -> DARGDTM(1) = ADDR(VEVOLUME);        /*@Y30LB38*/
           VEDDSTRU -> DARGINS(2) = '3'X;                  /*@Y30LB38*/
           VEDDSTRU -> DARGINL(2) = LENGTH(VEJOBNAM);      /*@Y30LB38*/
           VEDDSTRU -> DARGDTM(2) = ADDR(VEJOBNAM);        /*@Y30LB38*/
           VEDDSTRU -> DARGINS(3) = '4'X;                  /*@Y30LB38*/
           VEDDSTRU -> DARGINL(3) = LENGTH(VESTEPNM);      /*@Y30LB38*/
           VEDDSTRU -> DARGDTM(3) = ADDR(VESTEPNM);        /*@Y30LB38*/
         END;                                              /*@Y30LB38*/
     /*  ISSUE UPRINT MACRO TO RETRIEVE MESSAGE IDC0083I     @Y30LB38*/
       UPRINT (GDTTBL,PRTFILE,VEDDSTRU);                   /*@Y30LB38*/
     /* SET MESSAGE LENGTH AND ROUTING AND DESCRIPTOR CODES  @Y30LB38
        FOR UWTO MACRO.  ISSUE UWTO MACRO TO WRITE MESSAGE
        IDC2083E TO THE SYSTEM OPERATOR.                     @Y30LB38*/
       VEWTOLEN = LENGTH(VEWTOMSG);                        /*@Y30LB38*/
       VEROUTCD = ''B;                                     /*@Y30LB38*/
       VECD1 = ON;                                         /*@Y30LB38*/
       VEDESCCD = ''B;                                     /*@Y30LB38*/
       VEDESC3 = ON;                                       /*@Y30LB38*/
       UWTO (GDTTBL,VEWTOLEN,VEWTOPTR,VEROUTCD,VEDESCCD);  /*@Y30LB38*/
   @EJECT;                                                 /*@Y30LB38*/
     /* TEST IF BOTH SERIAL NUMBERS WERE SPECIFIED?  IF      @Y30LB38
        YES, PUT BOTH CSN'S IN MESSAGE IDC0085I AND
        PRINT THE MESSAGE.  IF NO, USE ONLY THE FIRST CSN.   @Y30LB38*/
       VELIST = ''B;                                       /*@Y30LB38*/
       VEDDSTRU -> DARGSMOD = VECMOMSG;                    /*@Y30LB38*/
       VEDDSTRU -> DARGSENT = MSG0085I;                    /*@Y30LB38*/
       VEDDSTRU -> DARGDBP =ADDR(VEMSGPTR->CMCSN1);        /*@Y30LB38*/
       VEDDSTRU -> DARGILP = LENGTH(CMCSN1);               /*@Y30LB38*/
       IF VEMSGPTR -> CMNUMCRT = 'F2'X THEN                /*@Y30LB38*/
         DO;                                               /*@Y30LB38*/
           VEDDSTRU -> DARGCNT = '1'X;                     /*@Y30LB38*/
           VEDDSTRU -> DARGINS(1) = '1'X;                  /*@Y30LB38*/
           VEDDSTRU -> DARGINL(1)=LENGTH(CMCSN2);          /*@Y30LB38*/
           VEDDSTRU -> DARGDTM(1) =ADDR(VEMSGPTR->CMCSN2); /*@Y30LB38*/
         END;                                              /*@Y30LB38*/
       UPRINT (GDTTBL,PRTFILE,VEDDSTRU);                   /*@Y30LB38*/
     /* ISSUE UFSPACE MACRO TO FREE MESSAGE AREA SPACE       @Y30LB38
        FROM THE USSC MACRO                                  @Y30LB38*/
       VEMSGPTR = VEMSGPTR-VEECBLEN;                      /*@Y30LB38*/
       UFSPACE (GDTTBL,VEMSGPTR);                          /*@Y30LB38*/
     END;                                                  /*@Y30LB38*/
   @EJECT;                                                 /*@Y30LB38*/
   ELSE
     DO;                                                   /*@Y30LB38*/
     /* THE RETURN CODE FROM THE USSC MACRO WAS NOT 0 OR 12. @Y30LB38
        A RETURN CODE OF 8 IS CHECKED TO DETERMINE IF USSC
        COULD NOT GET STORAGE.  IF YES, MESSAGE IDC2084I
        IS WRITTEN AND RETURN TO CALLER WITH CONDITION
        CODE 4.                                              @Y30LB38*/
       IF TESTRC = LASTCC08 THEN
         DO;                                               /*@Y30LB38*/
           VELIST = ''B;                                   /*@Y30LB38*/
           VEDDSTRU -> DARGSMOD = VECMOMSG;                /*@Y30LB38*/
           VEDDSTRU -> DARGSENT = MSG2084I;                /*@Y30LB38*/
           UPRINT (GDTTBL,PRTFILE,VEDDSTRU);               /*@Y30LB38*/
           VECOND = LASTCC04;                              /*@Y30LB38*/
         END;                                              /*@Y30LB38*/
       ELSE
         DO;                                               /*@Y30LB38*/
     /* IF THE RETURN CODE WAS NOT 8, TEST IF THE REASON     @Y30LB38
        CODE WAS '4C'X INDICATING THAT THE FIRST CARTRIDGE
        WAS EJECTED BUT THE SECOND WAS NOT.  IF YES,
        ISSUE UPRINT AND UWTO MACROS TO WRITE MESSAGE
        IDC083E TO THE SYSTEM OPERATOR AND ISSUE UPRINT
        MACRO TO WRITE MESSAGE IDC0085I TO THE USER.         @Y30LB38
        ISSUE USYSINFO MACRO TO OBTAIN JOBNAME AND STEPNAME  @Y30LB38
        TO BE USED IN MESSAGE IDC083E                        @Y30LB38*/
           IF VERESNCD = '4C'X THEN
             DO;                                           /*@Y30LB38*/
               USYSINFO (GDTTBL,NAMES,ADDR(VESYSIN));      /*@Y30LB38*/
               VEDDSTRU -> DARGSMOD = VECMOMSG;            /*@Y30LB38*/
               VEDDSTRU -> DARGSENT = MSG083E;             /*@Y30LB38*/
               VEDDSTRU -> DARGDBP=ADDR(VECSN1);           /*@Y30LB38*/
               VEDDSTRU ->DARGILP=LENGTH(VECSN1);          /*@Y30LB38*/
               VEDDSTRU -> DARGRETP = VEWTOPTR;            /*@Y30LB38*/
               VEDDSTRU -> DARGRETL = LENGTH(VEWTOMSG);    /*@Y30LB38*/
               VEDDSTRU -> DARGCNT = '3'X;                 /*@Y30LB38*/
               VEDDSTRU -> DARGINS(1) = '2'X;              /*@Y30LB38*/
               VEDDSTRU -> DARGINL(1) = LENGTH(VEVOLUME);  /*@Y30LB38*/
               VEDDSTRU -> DARGDTM(1) = ADDR(VEVOLUME);    /*@Y30LB38*/
               VEDDSTRU -> DARGINS(2) = '3'X;              /*@Y30LB38*/
               VEDDSTRU -> DARGINL(2) = LENGTH(VEJOBNAM);  /*@Y30LB38*/
               VEDDSTRU -> DARGDTM(2) = ADDR(VEJOBNAM);    /*@Y30LB38*/
               VEDDSTRU -> DARGINS(3) = '4'X;              /*@Y30LB38*/
               VEDDSTRU -> DARGINL(3) = LENGTH(VESTEPNM);  /*@Y30LB38*/
               VEDDSTRU -> DARGDTM(3) = ADDR(VESTEPNM);    /*@Y30LB38*/
               UPRINT (GDTTBL,PRTFILE,VEDDSTRU);           /*@Y30LB38*/
     /* SET MESSAGE LENGTH AND ROUTING AND DESCRIPTOR CODES  @Y30LB38
        FOR UWTO MACRO.  ISSUE UWTO MACRO TO WRITE MESSAGE
        IDC083E TO THE SYSTEM OPERATOR.                      @Y30LB38*/
               VEWTOLEN = LENGTH(VEWTOMSG);                /*@Y30LB38*/
               VEROUTCD = ''B;                             /*@Y30LB38*/
               VECD1 = ON;                                 /*@Y30LB38*/
               VEDESCCD = ''B;                             /*@Y30LB38*/
               VEDESC3 = ON;                               /*@Y30LB38*/
          UWTO(GDTTBL,VEWTOLEN,VEWTOPTR,VEROUTCD,VEDESCCD);/*@Y30LB38*/
     /* ISSUE UPRINT MACRO TO PRINT MESSAGE IDC0085I         @Y30LB38*/
               VELIST = ''B;                               /*@Y30LB38*/
               VEDDSTRU -> DARGSMOD = VECMOMSG;            /*@Y30LB38*/
               VEDDSTRU -> DARGSENT = MSG0085I;            /*@Y30LB38*/
               VEDDSTRU -> DARGDBP =ADDR(VECSN1);          /*@Y30LB38*/
               VEDDSTRU ->DARGILP=LENGTH(VECSN1);          /*@Y30LB38*/
               UPRINT (GDTTBL,PRTFILE,VEDDSTRU);           /*@Y30LB38*/
     /* THE SSCMSG ROUTINE IS CALLED TO WRITE THE MSS        @Y30LB38
        MESSAGE EXPLAINING THE SECOND CARTRIDGE
        WAS NOT EJECTED.                                     @Y30LB38*/
               CALL SSCMSG(VERESNCD);                      /*@Y30LB38*/
               VECOND = LASTCC08;                          /*@Y30LB38*/
             END;                                          /*@Y30LB38*/
   @EJECT;
           ELSE
             DO;                                           /*@Y30LB38*/
     /* RETURN CODE FROM USSC IS 4 INDICATING THAT           @Y30LB38
        THE CARTRIDGES WERE NOT EJECTED.  THE SSCMSG
        ROUTINE IS CALLED TO WRITE THE MSS MESSAGE
        EXPLAINING THE ERROR.  MESSAGE IDC2048I IS
        WRITTEN TO THE USER WITH THE UPRINT MACRO.           @Y30LB38*/
               CALL SSCMSG(VERESNCD);                      /*@Y30LB38*/
               VEDDSTRU -> DARGSMOD = VECMOMSG;            /*@Y30LB38*/
               VEDDSTRU -> DARGSENT = MSG2084I;            /*@Y30LB38*/
               UPRINT (GDTTBL,PRTFILE,VEDDSTRU);           /*@Y30LB38*/
               VECOND = LASTCC04;                          /*@Y30LB38*/
             END;                                          /*@Y30LB38*/
         END;                                              /*@Y30LB38*/
     END;                                                  /*@Y30LB38*/
     /* RESTORE RETURN ADDRESS AND RETURN TO CALLER          @Y30LB38*/
   REG14 = VERETADR;                                       /*@Y30LB38*/
   RETURN CODE(VECOND);                                    /*@Y30LB38*/
 END VOLEXIT;                                              /*@Y30LB38*/
