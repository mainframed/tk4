         MACRO
&NAME    PSEUDO  &TMP=,                                                X
               &PSEUDO=,                                               X
               &MF=
         GBLA  &$PSEUDO(5)
.*
.*       THIS MACRO SETS UP THE PSEUDO RETRY ROUTINE CONTROL BLOCK USED
.*       BY IKJEHSIR TO SUPPLY THE RETRY ROUTINE WITH ADDRESSABILITY.
.*
.*       &TMP IS THE ADDRESS OF THE WORD CONTAINING REG 1 AS THE TMP
.*             PASSED  TO THE CP.  IF IT IS REG FORM, THE REGISTER
.*             MUST CONTAIN REG 1 AS PASSED BY THE TMP.
.*       &PSEUDO IS THE NAME OF THE PSEUDO MF=L FORM OF PSEUDO
.*
         AIF   ('&MF' EQ 'E').MFE  EXECUTE FORM
.*
.*             LIST FORM
.*
.* CHECK TO SEE IF WE HAVE BEEN THRU ONCE, IF SO NEED A NAME
.*
         AIF   (&$PSEUDO(5) NE 0 AND T'&NAME EQ 'O').SEV0
&$PSEUDO(5) SETA &$PSEUDO(5)+1  SET TIMES THRU SW
         SPACE 1
&NAME  DS  0F   FULLWORD ALIGNMENT
         SPACE 1
         AIF   (T'&NAME NE 'O').HISPS  USE HIS NAME
$PSEUDO   DC    X'980FF008'    LM R0,R15,8(R15) LOAD REGS
         AGO   .DC47
.HISPS   DC    X'980FF008'  LM R0,R15,8(R15) LOAD REGS
.DC47    DC    X'47F0F000'    B  0(R15)  BR TO RETRY
         SPACE 1
.*
.* TO AVOID DUPLICATE NAMES, CHECK 1ST TIME THRU SW
.*
         AIF   (&$PSEUDO(5) GT 1).DSIT   ONLY GEN ONCE
*        THE FOLLOWING 16 DC'S ARE A STORAGE AREA FOR THE
*        RETRY ROUTINE'S REGISTERS.  THE SLOTS FOR R0, R1, R2,
*        AND R15 WILL BE SET UP BY THE STAE EXIT ROUTINE
*        AS FOLLOWS:
*              R0    THE CODE GIVEN TO THE EXIT ROUTINE BY STAE
*              R1    THE ABEND CODE, RIGHT JUSTIFIED
*              R2    THE ENTRY NUMBER IN A CODE LIST
*              R15   THE ADDRESS OF THE RETRY ROUTINE
*
*        IF THE ENTRY IS FOR A 33E ABEND, R13 AND R14 WILL BE
*        SET UP AS FOLLOWS:
*              R13   POINTER TO A SAVE AREA
*              R14   RETURN ADDRESS TO IKJEHSIR
*
         SPACE 1
.*
.* IF THE USER OF THE MACRO USES IT TWICE, THE SECOND TIME
.* IT IS USED, A DS IS GENERATED FOR THE DC'S THAT FOLLOW
.*
$RETRY0  DC    F'0'           STORAGE FOR R0
$RETRY1  DC    F'0'           STORAGE FOR R1
$RETRY2  DC    F'0'                STORAGE FOR R2
$RETRYS  DC    10F'0'              THE REST OF THE REGS
$RETRY13 DC    F'0'               STORAGE FOR R13
$RETRY14 DC    F'0'               STORAGE FOR R14
$RETRY15 DC    F'0'           STORAGE FOR R15
         AGO   .DSIT1 BYPASS
.DSIT    DS    16F STORAGE FOR REGISTERS
         SPACE 1
         AGO   .DSIT3     BYPASS COMMENTS
.DSIT1   ANOP
         SPACE 1
*        THE FOLLOWING WORD IS REQUIRED SO THAT THE PUTLINE
*        ROUTINE CAN BE CALLED BY THE STAE EXIT ROUTINE.
*        IT IS REGISTER 1 AS GIVEN TO THE CP BY THE TMP.
         SPACE 1
$TMPR1   DC    F'0'           REG 1 FROM TMP
         AGO   .DSIT4      BYPASS
.DSIT3   DS    F         STORAGE FOR TMP REG 1
.DSIT4   ANOP
         SPACE 1
         MEXIT
.*
.*             EXECUTE FORM
.*
.*       IF WE HAVE ALREADY GENNED THE TWO DC'S, DONT DO IT AGAIN
.*
.MFE     AIF   (&$PSEUDO(1) EQ 1).NO
.*
.*       NEED A NAME FOR THE BRANCH INSTRUCTION
.*
         AIF   (T'&NAME EQ 'O').NONAM
         SPACE 1
&$PSEUDO(1) SETA 1
         B     &NAME               BR AROUND INSTRUCTIONS
         SPACE 1
$PINSTR  DC    X'980FF008'    LM R0,R15,8(R15)
         DC    X'47F0F000'    B  0(R15)
.NO      SPACE 1
.*
.*       IF HE HAS PASSED ME A NAME FOR THE PSEUDO MF=L, USE IT
.*             IF NOT, WE WILL USE MY GENERATED NAME
.*
         AIF   (T'&PSEUDO EQ 'O').MYNAME
.*
.* CHECK TO SEE IF IN A REGISTER
.*
         AIF   ('&PSEUDO'(1,1) EQ '(').HISREG
&NAME    MVC   &PSEUDO.(8),$PINSTR  MOVE INSTRUCTIONS
         STM   0,15,&PSEUDO+8     STORE REGS
.*             IF NO TMP PARM, GET OUT
         AIF   (T'&TMP EQ 'O').END
         AIF   ('&TMP'(1,1) EQ '(').HISREGN
         L     14,&TMP            GET TMP REG1
         ST    14,&PSEUDO+72      STORE IN LIST
         AGO   .END
.HISREG  ANOP
&NAME    MVC   0(8,&PSEUDO(1)),$PINSTR  MOVE INSTRUCTIONS
         STM   0,15,8(&PSEUDO(1))  STORE REGS
         AIF   (T'&TMP EQ 'O').END
         AIF   ('&TMP'(1,1) EQ '(').HISREGT
         L     14,&TMP            GET TMP REG 1
         ST    14,72(&PSEUDO(1))  STORE IN LIST
         AGO   .END
.HISREGN ST    &TMP(1),&PSEUDO+72  STORE IN LIST
         AGO   .END
.HISREGT ST &TMP(1),72(&PSEUDO(1)) STORE IN LIST
         AGO   .END
.MYNAME  ANOP
&NAME    MVC   $PSEUDO(8),$PINSTR MOVE INSTRUCTIONS
         STM   0,15,$RETRY0    STORE ALL REGS
         AIF   (T'&TMP EQ 'O').END
         AIF   ('&TMP'(1,1) EQ '(').RTMP
         L     14,&TMP        GET TMP REG 1
         ST    14,$TMPR1      STORE IN LIST
.END     SPACE 1
         MEXIT
.RTMP    ST    &TMP(1),$TMPR1 STORE TMP REG 1
         AGO   .END
.NONAM   MNOTE 8,'NAME FIELD OMITTED'
         SPACE 1
         MEXIT
.SEV0    MNOTE *,'PSEUDO MACRO USED MORE THAN ONCE AND NAME OMITTED'
         SPACE 1
         MEXIT
         MEND
