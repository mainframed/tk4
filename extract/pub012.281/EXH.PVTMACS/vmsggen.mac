         MACRO
&NAME    VMSGGEN &SYS=,&TBLNO=,&COMPID=,&MCSDFLT=,&RTGDFLT=,&DSCDFLT=, X
               &MLWDFLT=
         GBLC    &VSYS,&VCID
         GBLA    &VTBL,&VMCS,&VRTG,&VDSC,&VMLW,&MMCNT1,&MMCNT2,&MMCNT3
&VCID    SETC    'IST'
&MMCNT1  SETA    0
&MMCNT3  SETA    0
&VTBL    SETA    11
&VMCS    SETA    0
&VRTG    SETA    8
&VDSC    SETA    5
&VMLW    SETA    0
.VMCKSYS ANOP
         AIF     ('&SYS' EQ '').VMTERM     IS THERE A SYS PARMETER
         AIF     ('&SYS' EQ 'EDOS').VMEDOS
         AIF     ('&SYS' EQ 'VS1').VMVS1
         AIF     ('&SYS' EQ 'VS2').VMVS2
.VMTERM  ANOP
         MNOTE 8,'SYS=(&SYS) NOT VALID PARMETER ABORT PROCESSING'
&VSYS    SETC    'ABORT'
         AGO     .VMEND
.VMEDOS  ANOP
&VSYS    SETC    'E'
         AGO     .VMCKTBL
.VMVS1   ANOP
&VSYS    SETC    'A'
         AGO     .VMCKTBL
.VMVS2   ANOP
&VSYS    SETC    'M'
         AGO     .VMCKTBL
.VMCKTBL ANOP
         AIF     ('&TBLNO' EQ '').VMCKCID   IS THERE A TABLE PARMETER
&VTBL    SETA    &TBLNO+1
.VMCKCID ANOP
&MMCNT2  SETA  &VTBL
         AIF     ('&COMPID' EQ '').VMCKMCS   IS THERE A  COMPONENT ID
&VCID    SETC    '&COMPID'
.VMCKMCS ANOP
         AIF     ('&MCSDFLT' EQ '').VMCKRTG   IS THERE A MCS PARMETER
&VMCS    SETA    &MCSDFLT
.VMCKRTG ANOP
         AIF     ('&RTGDFLT' EQ '').VMCKDSC   IS THERE A RTG PARMETER
&VRTG    SETA    &RTGDFLT
.VMCKDSC ANOP
         AIF     ('&DSCDFLT' EQ '').VMCKMLW   IS THERE A DSC PARMETER
&VDSC    SETA    &DSCDFLT
.VMCKMLW ANOP
         AIF     ('&MLWDFLT' EQ '').VMEND   IS THERE A MLW PARMETER
&VMLW    SETA    &MLWDFLT
.VMEND   ANOP
         MEND
