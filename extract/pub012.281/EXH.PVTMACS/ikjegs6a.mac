         MACRO
&LABEL   IKJEGS6A &CALLER,&PARAM=O
         LCLA  &A
         LCLC  &I
         AIF   ('&CALLER' EQ 'CNTSUP').C1
         AIF   ('&CALLER' NE 'OVLYSUP').E2
.*       OVERLAY SUPERVISOR SECTION.                                  *
.O1      ANOP
         SR    1,1 -          INDICATE CALL BY OVERLAY SUPERVISOR
         AIF   ('&PARAM' EQ 'O').S1
         MNOTE *,'PARAM OPERAND IGNORED'
         AGO   .S1
.*       CONTENTS SUPERVISOR SECTION.                                 *
.C1      ANOP
         AIF   ('&LABEL' EQ '').C15
&LABEL   EQU   *
.C15     ANOP
         AIF   (N'&PARAM NE 1).E3
         AIF   ('&PARAM(1)'(1,1) EQ '(' OR '&PARAM(1)' EQ 'O').C2
         LA    1,&PARAM(1) -  LOAD DCB POINTER
.C175    ANOP
&I       SETC  '1'
         AGO   .C3
.C2      ANOP
         AIF   ('&PARAM(1)' EQ '(1)' OR '&PARAM(1)' EQ 'O').C175
.C25     ANOP
&A       SETA  K'&PARAM(1)
         AIF   (&A LT 3 OR &A GT 10).E3
         AIF   ('&PARAM(1)'(&A,1) NE ')').E3
&I       SETC  '&PARAM(1)'(2,&A-2)
.C3      ANOP
         LNR   1,&I -         INDICATE CALL BY CONTENTS SUPERVISOR
.S1      ANOP
         SVC   61 -           ISSUE CALL TO TSO TEST
         MEXIT
.E1      MNOTE 12,'INVALID NUMBER OF MACRO OPERANDS'
         MEXIT
.E2      MNOTE 12,'INVALID POSITIONAL OPERAND'
         MEXIT
.E3      MNOTE 12,'INVALID PARAM OPERAND'
         MEXIT
         MEND
