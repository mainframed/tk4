         MACRO
&NAME    IEEDEFOP &KWD,&RTN=1,&FIRST=NO,&LAST=NO,&NOX=NO,&MEMBER=YES,  X
               &REPLY=YES,&SUBOP=,&BLDRTN=1,&SETSMF=YES
         AGO  .LABEL1
***** START OF SPECIFICATIONS *****************************************
*                                                                     *
* MACRO-NAME =IEEDEFOP                                                *
*                                                                     *
* DESCRIPTIVE-NAME = DEFINE OPTIONS                                   *
*                                                                     *
* FUNCTION = EXPAND IEEDEFOP MACROS IN IEEMB837                       *
*                                                                     *
* NOTES = NONE                                                        *
*                                                                     *
* MODULE-TYPE = BAL EXECUTABLE MACRO                                  *
*                                                                     *
* CHANGE ACTIVITY =                                                   *
*                                                                     *
* A000000-999999                                              @G741PJK*
* JBB1226 CHANGE TO ADD BIT ALLOWING SETSMF TO CHANGE         @G383P26*
*                                                                     *
* METHOD-OF-INVOCATION = SEE BELOW                                    *
*                                                                     *
*    LABEL  IEEDEFOP  KEYWORD,OPTIONS                                 *
*                                                                     *
*    WHERE  LABEL       - NAME OF THIS TABLE                          *
*           KEYWORD     - ACCEPTABLE OPTION OR SUBOPTION              *
*           RTN=N       - N IS ELEMENT NO. IN LSTRTN ARRAY IN         *
*                         IEEMB832 AND IEEMB833                       *
*           BLDRTN=M    - M IS ELEMENT NO. IN BLDRTN ARRAY IN         *
*                         IEEMB821                                    *
*           FIRST=YES   - FIRST KEYWORD IN THIS TABLE                 *
*           LAST=YES    - LAST KEYWORD IN THIS TABLE                  *
*           NOX=YES     - NEGATIVE OF KEYWWRD IS ACCEPTABLE           *
*           SUBOP=LABEL - LABEL OF TABLE OF SUPOPTIONS FOR THIS       *
*                         KEYWORD                                     *
*           MEMBER=YES  -                                             *
*           REPLY=YES   -                                             *
*           SETSMF=NO   - NOT ALLOWED TO CHANGE KEYWORD WITH SETSMF   *
*                                                             @G383P26*
***** END OF SPECIFICATIONS *******************************************
.LABEL1  ANOP
         LCLA  &KWDLEN
         LCLB  &MEMBB,&RPLYB,&NOXB,&LASTB,&SETSMFB            @G383P26
&KWDLEN  SETA  K'&KWD
         AIF   ('&FIRST' NE 'YES').NOTFRST
         AIF   ('&NAME'  NE '').GOTNAME
         MNOTE *,'FIRST=YES SPECIFIED....NAME REQUIRED ON IEEDEFOP'
.GOTNAME ANOP
         ENTRY &NAME     MAKE TABLE EXTERNALLY ADDRESSABLE
.NOTFRST ANOP
         AIF   ('&SUBOP' EQ '').NOSUBOP
&NAME    DC    AL4(&SUBOP.)      SUBOPTION TABLE ADDRESS
         AGO   .TSTMEMB
.NOSUBOP ANOP
&NAME    DC    AL4(0)            NO SUBOPTIONS TABLE SPECIFIED
.TSTMEMB ANOP
         AIF   ('&MEMBER' EQ 'YES').MEMBOK
&MEMBB   SETB  0
         AGO   .TSTRPLY
.MEMBOK  ANOP
&MEMBB   SETB  1
.TSTRPLY ANOP
         AIF   ('&REPLY' EQ 'YES').RPLYOK
&RPLYB   SETB  0
         AGO   .TSTNOX
.RPLYOK  ANOP
&RPLYB   SETB  1
.TSTNOX  ANOP
         AIF   ('&NOX' EQ 'YES').NOXOK
&NOXB    SETB  0
         AGO   .TSTSETS                                        @G383P26
.NOXOK   ANOP
&NOXB    SETB  1
.TSTSETS ANOP                                                  @G383P26
         AIF   ('&SETSMF' EQ 'YES').SETSMFO                    @G383P26
&SETSMFB SETB  0                                               @G383P26
         AGO   .TSTLAST                                        @G383P26
.SETSMFO ANOP                                                  @G383P26
&SETSMFB SETB  1                                               @G383P26
.TSTLAST ANOP
         AIF   ('&LAST' EQ 'YES').LASTOK
&LASTB   SETB  0
         AGO   .SETFLGS
.LASTOK  ANOP
&LASTB   SETB  1
.SETFLGS ANOP
         DC    BL1'&MEMBB&RPLYB&NOXB&LASTB.&SETSMFB.000' FLAGS -
*                                  X'80' - VALID FROM MEMBER
*                                  X'40' - VALID ON OPERATOR REPLY
*                                  X'20' - NOXXX VALID
*                                  X'10' - END OF OP TABLE
*                                  X'08' - SETSMF CAN CHANGE   @G383P26
         DC    1X'00'            RESERVED
         DC    AL2(&RTN)         PROCESSING ROUTINE NUMBER
*                                   CORRESPONDS TO SUBROUTINES IN
*                                   IEEMB832 AND IEEMB833
         DC    AL2(&BLDRTN)      BUILD ROUTINE NUMBER
*                                   USED BY INPUT MERGE ROUTINES
.SETLEN  ANOP
         DC    AL2(&KWDLEN)      KEYWORD LENGTH
         DC    CL&KWDLEN.'&KWD'  KEYWORD
         MEND
