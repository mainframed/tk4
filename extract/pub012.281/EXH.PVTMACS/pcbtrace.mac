         MACRO
&LABEL   PCBTRACE &PCBPTR=PCBPTR,&ID=
.*
.**********************************************************************
.*                                                                    *
.*01* MACRO-NAME = PCBTRACE                                           *
.*                                                                    *
.*01* DESCRIPTIVE-NAME = PCBTRACE TRACE POINT CODE GENERATOR          *
.*                                                                    *
.*01* COPYRIGHT: 5740-XYN COPYRIGHT IBM CORP. 1981,                   *
.*               LICENSED MATERIAL PROGRAM, PROPERTY OF IBM,          *
.*               REFER TO COPYRIGHT INSTRUCTIONS FORM NUMBER          *
.*               G120-2083.                                           *
.*                                                                    *
.*01* STATUS = OS/VS2 JBB1326                                         *
.*                                                                    *
.*01* FUNCTION = GENERATE CODE FOR A PCBTRACE TRACE POINT             *
.*                                                                    *
.*01* MODULE-TYPE = MACRO                                             *
.*                                                                    *
.*02* PROCESSOR = ASSEMBLER                                           *
.*                                                                    *
.*02* MACRO SIZE = 21 LOC                                             *
.*                                                                    *
.*01* MACROS = IHBERMAC                                               *
.*                                                                    *
.*01* CHANGE-ACTIVITY =                                               *
.*                                                                    *
.*  $L0=EXTREAL, JBB1326, 800512, PD18:                               *
.*                                                                    *
.*01* MNOTES = IHB001 ID OPERAND REQUIRED, NOT SPECIFIED.             *
.*             IHB002 INVALID ID OPERAND SPECIFIED-&ID                *
.*                                                                    *
.**********************************************************************
         LCLC  &L
         AIF   (T'&ID EQ 'O').NOID
         AIF   (K'&ID NE 2 OR '&ID' EQ '00').BADID
         AIF   ('&ID'(1,1) LT 'A' OR '&ID'(1,1) GT '9').BADID
         AIF   ('&ID'(1,1) GT 'F' AND '&ID'(1,1) LT '0').BADID
         AIF   ('&ID'(2,1) LT 'A' OR '&ID'(2,1) GT '9').BADID
         AIF   ('&ID'(2,1) GT 'F' AND '&ID'(2,1) LT '0').BADID
&L       SETC  'PCBTRC'.'&ID'
         ENTRY &L
&L       DS    0H
&LABEL   MVC   PCBEND-PCB+1(1,&PCBPTR),PCBEND-PCB+2(&PCBPTR)
*                         PUSH DOWN THE TRACE ENTRY STACK CONTENTS
*                         (THE NTH ENTRY BECOMES THE N-1TH ENTRY,
*                         AND THE OLDEST ENTRY POSITION IS DROPPED).
         MVI   PCBEND-PCB+2(&PCBPTR),X'&ID'  SET NEWEST STACK ENTRY
         ORG   &L         WHEN TRACE IS INACTIVE...
         B     *+10       THIS BRANCH INSTRUCTION OVERLAYS THE 'MVC'.
*                         AT NIP TIME, WHEN 'PVTTRXLN' IS NOT EQUAL
*                         ZERO, 'IEAVPCB' ACTIVATES THE TRACE BY OVER-
*                         LAYING THIS BRANCH INSTRUCTION AND THE LAST
*                         BYTE OF THE 'MVI' WITH THEIR PROPER
*                         ACTIVATION VALUES.
         ORG   &L+10      ORG FORWARD TO END OF GENERATED CODE
         MEXIT
.NOID    IHBERMAC 1006,ID
         MEXIT
.BADID   IHBERMAC 1001,ID,&ID
         MEND
