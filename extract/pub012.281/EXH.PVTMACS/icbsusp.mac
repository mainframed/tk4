* %/*  START OF SPECIFICATIONS****                             @Y30LB02
.*                                                             @Y30LB02
.*01   MODULE-NAME = ICBSUSP                                   @Y30LB02
.*                                                             @Y30LB02
.*01   DESCRIPTIVE-NAME = SUSPEND MACRO                        @Y30LB02
.*                                                             @Y30LB02
.*01   COPYRIGHT = NONE                                        @Y30LB02
.*                                                             @Y30LB02
.*01   STATUS = OS/VS1 RELEASE 4, OS/VS2 RELEASE 3             @Y30LB02
.*                                                             @Y30LB02
.*01   FUNCTION = REQUEST THE MSC TO STOP MSS PROCESSING       @Y30LB02
.*                                                             @Y30LB02
.*01   NOTES = AS FOLLOWS:                                     @Y30LB02
.*                                                             @Y30LB02
.*02     CODE = MACRO IS BILINGUALLY CODED                     @Y30LB02
.*                                                             @Y30LB02
.*02     RESTRICTIONS = THE FOLLOWING PREFIXES SHOULD BE       @Y30LB02
.*                      AVOIDED IN THE USER'S INVOCATION:      @Y30LB02
.*                      ICB AND SU                             @Y30LB02
.*                                                             @Y30LB02
.*02     REGISTER-CONVENTIONS = FOLLOWS NORMAL USE OF REGISTERS@Y30LB02
.*                              BY MACROS: R0,R1,R14, AND R15  @Y30LB02
.*                                                             @Y30LB02
.*01   MODULE-TYPE = MACRO                                     @Y30LB02
.*                                                             @Y30LB02
.*02     PROCESSOR = PLS2 AND ASSEMBLER                        @Y30LB02
.*                                                             @Y30LB02
.*02     ATTRIBUTES = REUSABLE                                 @Y30LB02
.*                                                             @Y30LB02
.*02     ENTRY-POINT = ICBSUSP                                 @Y30LB02
.*                                                             @Y30LB02
.*02     PURPOSE = A SUSPEND REQUEST WILL BE ISSUED            @Y30LB02
.*       WHENEVER THE CALLER WANTS TO QUIESCE MSS              @Y30LB02
.*       PROCESSING FOR A POWER DOWN OR TO INFORM MSS          @Y30LB02
.*       THAT THE HOST INTERFACE WILL NO LONGER                @Y30LB02
.*       ACCESS MSS OR TO PREPARE TO SWAP TO THE ALTERNATE MSC @ZD00155
.*                                                             @Y30LB02
.*02     LINKAGE = SVC  126                                    @Y30LB02
.*                                                             @Y30LB02
.*01  INPUT                                                    @Y30LB02
.*                                                             @Y30LB02
.*                                                             @Y30LB02
.* MACRO SYNTAX                                                @Y30LB02
.*                                                             @Y30LB02
.*                                                             @Y30LB02
.*   LIST FORM                                                 @Y30LB02
.*                                                             @Y30LB02
.* VERB    REQUIRED PARMS        OPTIONAL PARMS                @Y30LB02
.*                                                             @Y30LB02
.*ICBSUSP  MF=L                  MF=(L,MAP) - BAL              @Y30LB02
.*         MF(L,NAME/(R)         MF(L,NAME/(R),ATTR)-PLS       @Y30LB02
.*                                                             @Y30LB02
.*                                                             @Y30LB02
.*   STANDARD FORM                                             @Y30LB02
.*                                                             @Y30LB02
.* VERB    REQUIRED PARMS        OPTIONAL PARMS                @Y30LB02
.*                                                             @Y30LB02
.*ICBSUSP  FLAGS=ADDR/(R)                                      @Y30LB02
.*                                                             @Y30LB02
.* EXECUTE FORM                                                @Y30LB02
.*                                                             @Y30LB02
.* VERB    REQUIRED PARMS        OPTIONAL PARMS                @Y30LB02
.*                                                             @Y30LB02
.*ICBSUSP  MF(E,NAME/(R))       MF(E,NAME/(R),COMPLETE)-PLS    @Y30LB02
.*                              FLAGS=ADDR/(R)                 @Y30LB02
.*                                                             @Y30LB02
.*                                                             @Y30LB02
.*   KEYWORD PARAMETERS:                                       @Y30LB02
.*                                                             @Y30LB02
.*   MF  -  MACRO FORMAT: THIS PARAMETER DESCRIBES THE         @Y30LB02
.*          MACRO FORMAT AND MAY GIVE THE ADDRESS OR           @Y30LB02
.*          ADDRESS IN A REGISTER WHERE THE REQUEST BLOCK      @Y30LB02
.*          IS STORED.                                         @Y30LB02
.*                                                             @Y30LB02
.*          L - LIST FORM: THE EXPANSION OF THE L-FORM         @Y30LB02
.*              MACRO WILL RESULT IN A REQUEST BLOCK WITH      @Y30LB02
.*              THE ONLY NON-ZERO FIELDS BEING THE OPCODE      @Y30LB02
.*              AND LENGTH FIELDS.                             @Y30LB02
.*                                                             @Y30LB02
.*          (1) 'MAP' - IF MAP IS SPECIFIED WITH THE           @Y30LB02
.*              ASSEMBLER MACRO CALL A DSECT EQUIVALENT TO     @Y30LB02
.*              A MAPPING MACRO IS PRODUCED.                   @Y30LB02
.*                                                             @Y30LB02
.*          (2) NAME - THE MACRO INVOKER MUST SPECIFY A        @Y30LB02
.*              NAME FOR HIS STRUCTURE WHEN INVOKING THE       @Y30LB02
.*              PLS LIST FORM.                                 @Y30LB02
.*                                                             @Y30LB02
.*          (3) ATTRIBUTE - THE MACRO WILL PLACE ANY PLS       @Y30LB02
.*              ATTRIBUTE SPECIFIED IN THE MACRO INVOCATION    @Y30LB02
.*              IN AN UNINITIALIZED REQUEST BLOCK.  ATTRI-     @Y30LB02
.*              BUTE EXAMPLE:                                  @Y30LB02
.*                                                             @Y30LB02
.*              CHAR, FIXED, STATIC LOCAL, NONLOCAL,           @Y30LB02
.*              BASED(*), ETC.                                 @Y30LB02
.*                                                             @Y30LB02
.*       NULL - STANDARD FORM: THE EXPANSION OF THIS FORM      @Y30LB02
.*              CONSISTS OF A REQUEST BLOCK, A LINKAGE         @Y30LB02
.*              INSTRUCTION, AND, POSSIBLY, PARAMETER          @Y30LB02
.*              BINDING INSTRUCTION.                           @Y30LB02
.*                                                             @Y30LB02
.*          E - EXECUTABLE FORM: THE EXPANSION OF THIS FORM    @Y30LB02
.*              CONSISTS OF A LINKAGE INSTRUCTION AND          @Y30LB02
.*              POSSIBLY, PARAMETER BINDING INSTRUCTIONS.      @Y30LB02
.*                                                             @Y30LB02
.*              SUB-PARAMETERS                                 @Y30LB02
.*                                                             @Y30LB02
.*              (1)  AN E MUST BE SPECIFIED IN THE FIRST       @Y30LB02
.*                   SUB-PARAMETER POSITION.  THIS INDICATES   @Y30LB02
.*                   THAT THE MACRO IS IN EXECUTABLE FORM.     @Y30LB02
.*                                                             @Y30LB02
.*              (2)  A NAME OR A REGISTER MUST BE SPECIFIED    @Y30LB02
.*                   IN THE SECOND SUB-PARAMETER.              @Y30LB02
.*                                                             @Y30LB02
.*              (3)  'COMPLETE' - COMPLETE WILL CAUSE THE      @Y30LB02
.*                   MACRO TO ZERO OUT THE REQUEST BLOCK.      @Y30LB02
.*                   AFTER ZEROING THE REQUEST BLOCK THE       @Y30LB02
.*                   FUNCTION OPCODE AND LENGTH ARE PLACED     @Y30LB02
.*                   IN THE REQUEST BLOCK.                     @Y30LB02
.*                                                             @Y30LB02
.*   FLAGS - THE 2 BYTE FIELD WHICH INDICATES FUNCTION TO      @Y30LB02
.*   BE PERFORMED.  THE FORMAT OF THE FLAGS FIELD IS AS        @Y30LB02
.*   FOLLOWS:                                                  @Y30LB02
.*                                                             @Y30LB02
.*     ----------------------------------------------          @ZD00155
.*     × X × X × X ×     BITS 3-15 --- ALL ZEROS    ×          @ZD00155
.*     ----------------------------------------------          @ZD00155
.*                                                             @Y30LB02
.*   BIT 0------------'1'B     IMMEDIATE SHUTDOWN              @Y30LB02
.*                    '0'B     NO FUNCTION                     @Y30LB02
.*                                                             @Y30LB02
.*   BIT 1------------'1'B     RECONFIGURATION SHUTDOWN        @Y30LB02
.*                    '0'B     NO FUNCTION                     @Y30LB02
.*                                                             @Y30LB02
.*   BIT 2------------'1'B     PREPARE TO SWAP MSCS            @ZD00155
.*                    '0'B     NO FUNCTION                     @Y30LB02
.*                                                             @Y30LB02
.*   BITS 3-15------------    ×NOT USED - ALWAYS ZERO×         @Y30LB02
.*                                                             @Y30LB02
.*      IMMEDIATE SHUTDOWN:                                    @Y30LB02
.*                                                             @Y30LB02
.*      THIS FUNCTION IS PERFORMED FOR MSC ACTIVITY ON BEHALF  @Y30LB02
.*      OF THE INTERFACE ISSUING THE ORDER ONLY.               @Y30LB02
.*      IF THE ISSUING INTERFACE IS NOT THAT OF THE PRIMARY    @Y30LB02
.*      HOST PROCESSOR, THE INTERFACE IS DEINITIALIZED AND NO  @Y30LB02
.*      FURTHER ACTION IS TAKEN BY THE MSC FOR THIS COMMAND.   @Y30LB02
.*      USE OF THIS ATTRIBUTE CAUSES ALL STAGING/DESTAGING     @Y30LB02
.*      IN PROGRESS TO BE COMPLETED. IF TRACE IS ON, THE TRACE @Y30LB02
.*      END RECORD IS WRITTEN.                                 @Y30LB02
.*      ALL CARTRIDGES ARE RETURNED TO THEIR CELLS             @Y30LB02
.*      AND THE INTERFACE IS DEINITIALIZED.                    @Y30LB02
.*                                                             @Y30LB02
.*      RECONFIGURATION SHUTDOWN:                              @Y30LB02
.*                                                             @Y30LB02
.*      THIS FUNCTION IS PERFORMED ONLY WHEN ISSUED BY THE     @Y30LB02
.*      PRIMARY HOST CPU PROCESSOR.                            @Y30LB02
.*      USE OF THIS ATTRIBUTE CAUSES ALL MOUNTED VOLUMES IN    @Y30LB02
.*      MSS TO BE DEMOUNTED AND ALL SCHEDULED WORK IN MSS      @Y30LB02
.*      COMPLETED. IF TRACE IS ON, THE TRACE END RECORD IS     @Y30LB02
.*      WRITTEN.  THE INTERFACE IS DEINITIALIZED.              @Y30LB02
.*                                                             @Y30LB02
.*      PREPARE TO SWAP:                                       @ZD00155
.*                                                             @ZD00155
.*      THIS IS USED TO PREPARE THE MSF AND PROCESSORS WHICH   @ZD00155
.*      ARE USING THE MSF FOR A SWAP TO THE ALTERNATE MSC,     @ZD00155
.*      AT OPERATOR REQUEST.                                   @ZD00155
.*      A PREPARE MSC SWAP WILL ONLY BE EXECUTED WHEN RECEIVED @ZD00155
.*      BY THE MSC FROM THE PRIMARY HOST.                      @ZD00155
.*      USE OF THIS ATTRIBUTE CAUSES THE STAGE/DESTAGE         @ZD00155
.*      ACTIVITY CURRENTLY IN PROGRESS TO BE COMPLETED.        @ZD00155
.*      IF TRACE IS ON, THE TRACE END RECORD IS WRITTEN.       @ZD00155
.*      A DUMMY MESSAGE IS WRITTEN TO ANY INTERFACE OTHER      @ZD00155
.*      THAN THE PRIMARY TO CONVEY THE INFORMATION THAT        @ZD00155
.*      THE SWAP IS TO TAKE PLACE.                             @ZD00155
.*      NO INTERFACE IS DEINITIALIZED.                         @ZD00155
.*                                                             @Y30LB02
.*   BITS 0, 1 AND 2 ARE MUTUALLY EXCLUSIVE                    @ZD00155
.*                                                             @Y30LB02
.*                                                             @Y30LB02
.*01  EXIT-NORMAL = VALID INVOCATION                           @Y30LB02
.*                                                             @Y30LB02
.*02     CONDITION = RETURN CODE = 0                           @Y30LB02
.*                                                             @Y30LB02
.*02     OUTPUT = REQUESTED ORDER GENERATED FOR THE SVC        @Y30LB02
.*                                                             @Y30LB02
.*01  EXIT-ERROR = INVALID INVOCATION                          @Y30LB02
.*                                                             @Y30LB02
.*02     CONDITION = RETURN CODE = 12                          @Y30LB02
.*                                                             @Y30LB02
.*02     OUTPUT = ERROR MESSAGE AS A COMMENT AT POINT OF       @Y30LB02
.*                INVOCATION                                   @Y30LB02
.*                                                             @Y30LB02
.*01  EXTERNAL REFERENCES = NONE                               @Y30LB02
.*                                                             @Y30LB02
.*01  TABLES = NONE                                            @Y30LB02
.*                                                             @Y30LB02
.*01  MESSAGES = AS FOLLOWS:                                   @Y30LB02
.*      ICB602I:  A NAME FOR THE REQUEST BLOCK MUST BE         @Y30LB02
.*                SPECIFIED IN MF(2)                           @Y30LB02
.*      ICB604I:  WARNING-ONLY THE MF PARAMETER IS VALID FOR   @Y30LB02
.*                THE LIST FORM                                @Y30LB02
.*      ICB607I:  FLAGS MUST BE SPECIFIED FOR STANDARD         @Y30LB02
.*                FORM MACRO                                   @Y30LB02
.*      ICB612I:  MF(2) MUST CONTAIN REQ BLK NAME              @Y30LB02
.*                IF EXEC MACRO FORM REQUESTED                 @Y30LB02
.*      ICB615I:  INVALID MF PARAMETER SPECIFIED, MUST         @Y30LB02
.*                BE E, L, OR NULL                             @Y30LB02
.*                                                             @Y30LB02
.*01  CONTROL-BLOCKS = NONE                                    @Y30LB02
.*                                                             @Y30LB02
.*01  INNER MACROS = NONE                                      @Y30LB02
.*                                                             @Y30LB02
.*01  SYSTEM MACRO = NONE                                      @Y30LB02
.*                                                             @Y30LB02
.*01  CHANGE-ACTIVITY = S/D X04AC02 AND Y30LB02                @Y30LB02
 .*                         @ZD00155                           @ZD00155
.*                                                             @Y30LB02
.**** END OF SPECIFICATIONS ***                                @Y30LB02
         MACRO                                                 @Y30LB02
&SYMBOL  ICBSUSP &MF=,&FLAGS=                                  @Y30LB02
         LCLB  &ERSW              ERROR SWITCH                 @Y30LB02
         LCLC  &SUSP              SUSPEND MACRO INSTR INDX     @Y30LB02
&SUSP    SETC 'SU'.'&SYSNDX'      ASSIGN CHARACTER VALUE       @Y30LB02
.*                                                             @Y30LB02
.*       DETERMINATION OF DESIRED MACRO FORM                   @Y30LB02
.*                                                             @Y30LB02
         AIF  ('&MF(1)' NE 'L').EFORM       LIST FORM?         @Y30LB02
         AIF  ('&FLAGS' EQ '').LISTOK                          @Y30LB02
.WARN    MNOTE 4,'  ICB604I - WARNING-ONLY THE MF PARAMETER '  @Y30LB02
         MNOTE 4,'  IS VALID FOR THE LIST FORM '               @Y30LB02
.*                                                             @Y30LB02
.LISTOK  ANOP                                                  @Y30LB02
         AIF   ('&MF(2)' EQ 'MAP').LISTDS   DSECT REQUESTED?   @Y30LB02
*  MACMEAN  MSSC SUSPEND MACRO--INITIALIZED LIST FORM          @Y30LB02
*  VS1-4/VS2-3 VERSION--06/09/74   LEVEL 00                    @Y30LB02
&SYMBOL  DS   0F                                               @Y30LB02
SUSPLEN  DC   F'8'                LENGTH OF SUSPEND REQBLK     @Y30LB02
SUSPOPC  DC   XL1'22'             SUSPEND  FUNCTION OPCODE     @Y30LB02
SUSP001  DC   XL1'00'             1-BYTE RESERVED AREA         @Y30LB02
SUSPFLG  DC   XL2'0000'           2-BYTE FLAGS AREA            @Y30LB02
         AGO  .FINISH                                          @Y30LB02
.*                                                             @Y30LB02
.LISTDS  ANOP                                                  @Y30LB02
*  MACMEAN  MSSC SUSPEND MACRO--UNINITIALIZED LIST FORM        @Y30LB02
*  VS1-4/VS2-3 VERSION--06/09/74   LEVEL 00                    @Y30LB02
ICBSUSP  DSECT                                                 @Y30LB02
SUSPLEN  DS   F                   LEN SUSPEND REQBLK=8         @Y30LB02
SUSPOPC  DS   XL1                 SUSPEND OPCODE 22 HEX        @Y30LB02
SUSP001  DS   XL1                 1-BYTE RESERVED AREA         @Y30LB02
SUSPFLG  DS   XL2                 2-BYTE FLAGS AREA            @Y30LB02
         AGO  .FINISH                                          @Y30LB02
.*                                                             @Y30LB02
.*       THE FOLLOWING PROCESSING IS ACCOMPLISHED FOR THE      @Y30LB02
.*       EXECUTABLE MACRO FORM                                 @Y30LB02
.*                                                             @Y30LB02
.EFORM   AIF  ('&MF(1)' NE 'E').SFORM       EXECUTE FORM?      @Y30LB02
         AIF  ('&MF(2)' EQ '').EXFAIL       MF(2) NULL?        @Y30LB02
.*                                                             @Y30LB02
*  MACMEAN  MSSC SUSPEND MACRO--EXECUTE FORM                   @Y30LB02
*  VS1-4/VS2-3 VERSION--06/09/74   LEVEL 00                    @Y30LB02
&SYMBOL  IHBINNRA &MF(2)           LD ADDR OF REQUEST BLK      @Y30LB02
.*                                 USING IHBINNRA MACRO        @Y30LB02
         LA    0,8                 LENGTH OF REQ BLOCK         @Y30LB02
         ST    0,0(1)              STORE LENGTH IN BLK         @Y30LB02
         MVI   4(1),X'22'          STORE OPCODE                @Y30LB02
         AGO  .CKSW1               BR TO CK ERROR SWITCH       @Y30LB02
.EXFAIL  ANOP                                                  @Y30LB02
         MNOTE 12,'  ICB612I - MF(2) MUST CONTAIN REQ '        @Y30LB02
         MNOTE 12,'  BLK NAME IF EXEC MACRO FORM REQUESTED'    @Y30LB02
&ERSW    SETB 1                   SET ERROR SWITCH TO 1        @Y30LB02
.*                                                             @Y30LB02
.CKSW1   AIF  (&ERSW).FINISH      IF ERROR-END PROCESSING      @Y30LB02
         AGO  .COMMON                                          @Y30LB02
.*                                                             @Y30LB02
.*       PROCESSING OF THE STANDARD FORM MACRO                 @Y30LB02
.*                                                             @Y30LB02
.SFORM   AIF  ('&MF(1)' NE '').BADFORM      STANDARD FORM?     @Y30LB02
         AIF  ('&FLAGS' NE '').CKSW2        FLAGS SPECIFIED?   @Y30LB02
&ERSW    SETB 1                   SET ERROR SWITCH TO 1        @Y30LB02
         MNOTE 12,'  ICB607I - FLAGS MUST BE SPECIFIED '       @Y30LB02
         MNOTE 12,'  FOR STANDARD FORM MACRO '                 @Y30LB02
         AGO  .CKSW2                                           @Y30LB02
.*                                                             @Y30LB02
.BADFORM ANOP                                                  @Y30LB02
&ERSW    SETB 1                   SET ERROR SWITCH TO 1        @Y30LB02
         MNOTE 12,'  ICB615I - INVALID MF PARAMETER '          @Y30LB02
         MNOTE 12,'  SPECIFIED, MUST BE L, E, OR NULL '        @Y30LB02
.*                                                             @Y30LB02
.*       THE FOLLOWING CODE CHECKS TO DETERMINE IF THE ERROR   @Y30LB02
.*       SWITCH IS ON.  IF YES, PROCESSING IS TERMINATED, OTHER@Y30LB02
.*       WISE STANDARD FORM MACRO PROCESSING WILL BE CON-      @Y30LB02
.*       TINUED                                                @Y30LB02
.*                                                             @Y30LB02
.CKSW2   AIF  (&ERSW).FINISH      IF YES-END PROCESSING        @Y30LB02
*  MACMEAN  MSSC SUSPEND MACRO--STANDARD FORM                  @Y30LB02
*  VS1-4/VS2-3 VERSION--06/09/74   LEVEL 00                    @Y30LB02
         CNOP 0,4                                              @Y30LB02
&SYMBOL  BAL  1,&SUSP             BRANCH AROUND REQ BLOCK      @Y30LB02
SUSPPTR  DC   A(&SUSP-*)          LENGTH OF REQUEST BLOCK      @Y30LB02
SUSPOPC  DC   XL1'22'             SUSPEND FUNCTION OPCODE      @Y30LB02
SUSP001  DC   XL1'00'             1-BYTE RESERVED AREA         @Y30LB02
SUSPFLG  DC   XL2'0000'           2-BYTE FLAGS AREA            @Y30LB02
&SUSP    EQU  *                                                @Y30LB02
.*                                                             @Y30LB02
.*       THE FOLLOWING CODE CHECKS FOR VALID PARAMETERS IF     @Y30LB02
.*       STANDARD FOR EXECUTE FORM MACRO IS SPECIFIED.         @Y30LB02
.*       IF PARAMETERS ARE SPECIFIED, THEY WILL BE STORED      @Y30LB02
.*       IN THE REQUEST BLOCK.                                 @Y30LB02
.*                                                             @Y30LB02
.COMMON  AIF  ('&FLAGS' EQ '').INVOKE       FLAGS SPECIFIED?   @Y30LB02
         AIF  ('&FLAGS'(1,1) EQ '(').FLREG  FLAGS IN REG-FORM? @Y30LB02
         MVC  6(2,1),&FLAGS       PUT FLAGS IN REQ BLK         @Y30LB02
         AGO  .INVOKE             BRANCH TO CK TIME/DATE       @Y30LB02
.*                                                             @Y30LB02
.FLREG   ANOP                                                  @Y30LB02
         MVC  6(2,1),0(&FLAGS(1)) REGFORM,FLAGS IN BLOCK       @Y30LB02
.*                                                             @Y30LB02
.INVOKE  ANOP                                                  @Y30LB02
         SVC  126                                              @Y30LB02
.FINISH  MEND                                                  @Y30LB02
**/
*/*                                                          @Y30LB02*/
*   ICBSUSP: MACRO KEYS(MF,FLAGS);                         /*@Y30LB02*/
*/*                                                          @Y30LB02*/
*/*                     EXTERNAL DECLARES                    @Y30LB02*/
*/*********************************************************  @Y30LB02*/
*/*      SUSPTEST IS USED TO DETERMINE WHETHER THE STANDARD  @Y30LB02*/
*/*      FORM MACRO HAS BEEN INVOKED MORE THAN ONCE BY THE   @Y30LB02*/
*/*      SAME CALLER.                                        @Y30LB02*/
*/*********************************************************  @Y30LB02*/
*/*      SUTESTR1 WILL BE USED TO DETERMINE IF IT IS NECES-  @Y30LB02*/
*/*      SARY TO DECLARE R1 OR WHETHER IT IS ALREADY DCLD    @Y30LB02*/
*/*********************************************************  @Y30LB02*/
*/*                                                          @Y30LB02*/
*   DCL  SUTESTR1  FIXED  EXT;    /*REG1 DECLARE COUNTER     @Y30LB02*/
*   DCL  SUSPTEST  FIXED  EXT;    /*STANDARD INVOC COUNTER   @Y30LB02*/
*/*                                                          @Y30LB02*/
*/*      INTERNAL DECLARES                                   @Y30LB02*/
*   DCL  SUPOP    CHAR;      /*DEFINE SUSPEND OPCODE         @Y30LB02*/
*   DCL  SUPLEN   CHAR;      /*DECLARE LENGTH OF ORDER       @Y30LB02*/
*   DCL  ERSW     FIXED;     /*DECLARE ERROR COND SWITCH     @Y30LB02*/
*   DCL  RETCODE  FIXED;     /*DECLARE RETURN CODE AREA      @Y30LB02*/
*   DCL  CTR      FIXED;     /*DECLARE CTR FOR LIST FORM     @Y30LB02*/
*        ERSW=0;             /*INITIALIZE ERROR SWITCH       @Y30LB02*/
*        RETCODE=0;          /*INITIALIZE RETURN CODE        @Y30LB02*/
*        SUPLEN='8';         /*INITIALIZE LENGTH FIELD       @Y30LB02*/
*        SUPOP='22';         /*INITIALIZE OPCODE FIELD       @Y30LB02*/
*        CTR=0;              /*INITIALIZE COUNTER            @Y30LB02*/
*/********************************************************   @Y30LB02*/
*/*      CHECK FOR VALID MACRO FORM--E INDICATES EXECUTE     @Y30LB02*/
*/*      FORM, L INDICATES LIST FORM AND IF THE MF PARA-     @Y30LB02*/
*/*      METER IS NULL THE STANDARD FORM MACRO IS INVOKED    @Y30LB02*/
*/********************************************************   @Y30LB02*/
*        IF SUTESTR1=0 THEN  /*REGISTER ONE DECLARED?        @Y30LB02*/
*             DO;            /*NO,DECLARE IT                 @Y30LB02*/
*                  ANS('DCL SUSPR1 REG(1) PTR ;/*PTR'××
*                  ' TO REQUEST BLOCK @Y30LB02*/')SKIP;
*                  SUTESTR1=SUTESTR1+1; /*INCREM REG CTR     @Y30LB02*/
*             END;               /*                          @Y30LB02*/
*   IF MF(1) ^= 'L' THEN         /*LIST FORM REQUEST?        @Y30LB02*/
*        GO TO EFORM;            /*NO,CK FOR EXECUTE         @Y30LB02*/
*   IF MF(2) = '' THEN           /*LIST NAME SPECIFIED?      @Y30LB02*/
*        DO;                     /*NO,PRINT ERROR MSG        @Y30LB02*/
*             ERSW=1;            /*SET ERROR SWITCH          @Y30LB02*/
*             RETCODE=12;        /*SET RETURN CODE           @Y30LB02*/
*        ANS MSG('  ICB602I -  A NAME FOR THE '××
*        'REQUEST BLOCK MUST BE SPECIFIED IN MF(2)')SKIP;  /*@Y30LB02*/
*             GO TO FINISH;      /*BRANCH TO END PROCESS     @Y30LB02*/
*        END;                 /*                             @Y30LB02*/
*/*                                                          @Y30LB02*/
*   IF FLAGS ^= '' THEN      /*IS THE FLAGS AREA NULL        @Y30LB02*/
*        CTR=CTR+1;          /*INCREMENT PARAMETER COUNTER   @Y30LB02*/
*   IF CTR > 0 THEN          /*INCREMENT CTR > ZERO?         @Y30LB02*/
*        DO;                 /*GIVE USER WARNING MSG         @Y30LB02*/
*             RETCODE=4;     /*SET RETURN CODE TO 4          @Y30LB02*/
*             ANS MSG('  ICB604I -  WARNING-ONLY'××
*                 ' THE MF PARAMETER IS VALID FOR THE'××
*                 ' LIST FORM  ')SKIP;                     /*@Y30LB02*/
*        END;                  /*                            @Y30LB02*/
*    IF MF(3) ^= '' THEN       /*PLS ATTRIBUTE SPECIFIED     @Y30LB02*/
*        GO TO ATTRLST;        /*BRCH,PROCESS ATTRIBUTE      @Y30LB02*/
*/*                                                          @Y30LB02*/
*   ANS('/*******************************************'××
*       '************** @Y30LB02*/')SKIP;
*   ANS('/*    MACMEAN  MSSC SUSPEND MACRO           '××
*       '               @Y30LB02*/')SKIP;
*   ANS('/*VS1-4/VS2-3 VERSION--06/09/74 LEVEL 00    '××
*       '               @Y30LB02*/')SKIP;
*   ANS('/*THIS IS THE INITIALIZED LIST FORM         '××
*       '               @Y30LB02*/')SKIP;
*   ANS('/*******************************************'××
*       '************** @Y30LB02*/')SKIP;
*             ANS('DCL 1 '××MF(2)××' STATIC LOCAL,'××
*             ' /*SUSPEND REQUEST BLOCK @Y30LB02*/')SKIP;
*             ANS('    2  SUSPLEN  FIXED(31)INIT('××QUOTE
*             ('08')××'X),/*LENGTH OF SUSPEND REQ BLK @Y30LB02*/')SKIP;
*             ANS('    2  SUSPOP  CHAR(1)INIT('××QUOTE('22')××
*             'X),/*SUSPEND OPCODE @Y30LB02*/')SKIP;
*             ANS('    2  *      CHAR(1)INIT('××QUOTE('00')××
*             'X),/*RESERVED AND ZERO @Y30LB02*/')SKIP;
*             ANS('    2  SUSPFLG BIT(16)INIT('××QUOTE
*             ('0000000000000000')××'B),/*TWO BYTE ATTRI'××
*             'BUTE AREA @Y30LB02*/')SKIP;
*             ANS('       3  SUPIMED BIT(1),/*SUSPEND'××
*             ' IMMEDIATE ATTRIBUTE @Y30LB02*/')SKIP;
*             ANS('       3  SUPRCON BIT(1),/*SUSPEND'××
*             ' RECONFIGURATION ATTRIBUTE @Y30LB02*/')SKIP;
*             ANS('       3  SUPPRSW BIT(1),/*SUSPEND'××
*             ' PREPARE TO SWAP MSCS ATTRIBUTE @ZD00155*/')SKIP;
*             ANS('       3  *      BIT(13);/*RESERVED AND'××
*             ' MUST BE ZERO @ZD00155*/')SKIP;
*             GO TO FINISH;    /*BRANCH TO END PROCESSING    @Y30LB02*/
*/*                                                          @Y30LB02*/
*ATTRLST:
*   ANS('/*******************************************'××
*       '************** @Y30LB02*/')SKIP;
*   ANS('/*     MACMEAN  MSSC SUSPEND MACRO          '××
*       '               @Y30LB02*/')SKIP;
*   ANS('/*VS1-4/VS2-3 VERSION--06/09/74 LEVEL 00    '××
*       '               @Y30LB02*/')SKIP;
*   ANS('/*THIS IS THE UNINITIALIZED LIST FORM       '××
*       '               @Y30LB02*/')SKIP;
*   ANS('/*******************************************'××
*       '************** @Y30LB02*/')SKIP;
*             ANS('DCL 1 '××MF(2)××' '××MF(3)××','××
*             ' /*SUSPEND REQUEST BLOCK @Y30LB02*/')SKIP;
*             ANS('    2  SUSPLEN  FIXED(31),/*LENG'××
*             'TH OF SUSPEND REQBLK = 8 BYTES @Y30LB02*/')SKIP;
*             ANS('    2  SUSPOP  CHAR(1),'××
*             '/*SUSPEND OPCODE = 22 HEX @Y30LB02*/')SKIP;
*             ANS('    2  *      CHAR(1),'××
*             '/*RESERVED AND ZERO @Y30LB02*/')SKIP;
*             ANS('    2  SUSPFLG BIT(16),'××
*             '/*TWO BYTE ATTRIBUTE AREA @Y30LB02*/')SKIP;
*             ANS('       3  SUPIMED BIT(1),/*SUSPEND'××
*             ' IMMEDIATE ATTRIBUTE @Y30LB02*/')SKIP;
*             ANS('       3  SUPRCON BIT(1),/*SUSPEND'××
*             ' RECONFIGURATION ATTRIBUTE @Y30LB02*/')SKIP;
*             ANS('       3  SUPPRSW BIT(1),/*SUSPEND'××
*             ' PREPARE TO SWAP MSCS ATTRIBUTE @ZD00155*/')SKIP;
*             ANS('       3  *      BIT(13);/*RESERVED AND'××
*             ' MUST BE ZERO @ZD00155*/')SKIP;
*             GO TO FINISH;   /*BRANCH TO END PROCESSING     @Y30LB02*/
*/*                                                          @Y30LB02*/
*/*      PROCESSING THE EXECUTE MACRO FORM                   @Y30LB02*/
*/*                                                          @Y30LB02*/
*EFORM:
*   IF MF(1) ^= 'E' THEN      /*EXECUTE FORM?                @Y30LB02*/
*        GO TO SFORM;         /*NO,CK FOR STANDARD?          @Y30LB02*/
*   IF MF(2) = '' THEN        /*YES,NAME SPECIFIED?          @Y30LB02*/
*        DO;                  /*NO,PRINT ERROR MSG           @Y30LB02*/
*             ERSW=1;         /*SET ERROR SWITCH             @Y30LB02*/
*             RETCODE=12;     /*SET RETURN CODE              @Y30LB02*/
*             ANS MSG('  ICB612I - MF(2) MUST '××
*                'CONTAIN REQ BLK NAME IF '××
*                'EXEC MACRO FORM REQUESTED ')SKIP;        /*@Y30LB02*/
*             GO TO CKSW;     /*BRANCH TO CK ERROR SWITCH    @Y30LB02*/
*        END;                 /*                             @Y30LB02*/
*/*                                                          @Y30LB02*/
*   ELSE                       /*REQUEST TO ZERO BLOCK?      @Y30LB02*/
*   ANS('/*******************************************'××
*       '************* @Y30LB02*/')SKIP;
*   ANS('/*     MACMEAN  MSSC SUSPEND MACRO          '××
*       '              @Y30LB02*/')SKIP;
*   ANS('/*VS1-4/VS2-3 VERSION-- 06/09/74 LEVEL 00   '××
*       '              @Y30LB02*/')SKIP;
*   ANS('/*THIS IS THE OUTPUT FOR THE EXECUTE FORM.  '××
*       '              @Y30LB02*/')SKIP;
*   ANS('/*THE LIST FORM OF THIS MACRO SHOULD BE     '××
*       '              @Y30LB02*/')SKIP;
*   ANS('/*INVOKED FIRST IN ORDER TO SET UP NECESSARY'××
*       '              @Y30LB02*/')SKIP;
*   ANS('/*DECLARES.  IF THE COMPLETE ARGUMENT IS    '××
*       '              @Y30LB02*/')SKIP;
*   ANS('/*SPECIFIED THE REQ BLOCK WILL BE ZEROED.   '××
*       '              @Y30LB02*/')SKIP;
*   ANS('/*******************************************'××
*       '************* @Y30LB02*/')SKIP;
*    IF MF(3) = 'COMPLETE' THEN   /*COMPLETE SPECIFIED?      @Y30LB02*/
*      DO;                        /*YES,ZERO BLOCK           @Y30LB02*/
*        ANS(' '××MF(2)××'='××MF(2)××'&&'××MF(2)××';'××    /*@Y30LB02*/
*        '/*ZERO OUT REQUEST BLOCK @Y30LB02*/')SKIP;
*      END;                  /*                              @Y30LB02*/
*/*                                                          @Y30LB02*/
*        ANS('RFY(SUSPR1)RSTD;/*RESTRICT REG1 @Y30LB02*/')SKIP;
*        ANS('SUSPR1=ADDR('××MF(2)××');/*R1 POINTER TO'××
*        ' REQUEST BLOCK @Y30LB02*/')SKIP;
*        ANS('SUSPLEN= '××QUOTE(SUPLEN)××'X;/*LENGTH '××
*        'OF SUSPEND REQUEST @Y30LB02*/')SKIP;
*        ANS('SUSPOP= '××QUOTE(SUPOP)××'X;/*SUSPEND'××
*        ' OPCODE @Y30LB02*/')SKIP;
*        GO TO COMPROC;    /*BRANCH,CK PARAMETERS            @Y30LB02*/
*/*                                                          @Y30LB02*/
*CKSW:
*   IF ERSW=1 THEN         /*ERROR SWITCH SET?               @Y30LB02*/
*        GO TO FINISH;     /*BRANCH TO END PROCESSING        @Y30LB02*/
*   ELSE                   /*CONTINUE PROCESSING             @Y30LB02*/
*        GO TO COMPROC;    /*BRANCH,CK PARAMETERS            @Y30LB02*/
*/*                                                          @Y30LB02*/
*SFORM:
*   IF MF(1) ^= '' THEN    /*STANDARD FORM?                  @Y30LB02*/
*     DO;                  /*NO,PRINT ERROR MESSAGE          @Y30LB02*/
*        ERSW=1;           /*SET ERROR SWITCH                @Y30LB02*/
*        RETCODE=12;       /*SET RETURN CODE TO 12           @Y30LB02*/
*        ANS MSG('  ICB615I - INVALID MF PARAMETER '××
*             'SPECIFIED, MUST BE L, E, OR NULL')SKIP;     /*@Y30LB02*/
*        GO TO CKSW2;      /*BRANCH,CHECK ERROR SWITCH       @Y30LB02*/
*     END;                 /*                                @Y30LB02*/
*/*                                                          @Y30LB02*/
*   IF FLAGS = '' THEN     /*FLAGS PARAMETER SPECIFIED?      @Y30LB02*/
*     DO;                  /*NO,PRINT ERROR MESSAGE          @Y30LB02*/
*        ERSW=1;           /*SET ERROR SWITCH TO ONE         @Y30LB02*/
*        RETCODE=12;       /*SET RETURN CODE TO 12           @Y30LB02*/
*        ANS MSG('  ICB607I - FLAGS MUST BE SPECIFIED '××
*        'FOR STANDARD FORM MACRO')SKIP;                   /*@Y30LB02*/
*     END;                 /*                                @Y30LB02*/
*/*                                                          @Y30LB02*/
*CKSW2:
*/*                                                          @Y30LB02*/
*   IF ERSW = 1 THEN       /*ERROR SWITCH EQUAL ONE?         @Y30LB02*/
*        GO TO FINISH;     /*BRANCH TO END PROCESSING        @Y30LB02*/
*/*                                                          @Y30LB02*/
*   IF SUSPTEST = 1 THEN  /*STD FORM REQUESTED BEFORE?       @Y30LB02*/
*     DO;                 /*YES,DON'T PUT OUT DECLARES       @Y30LB02*/
*   ANS('/*******************************************'××
*       '************** @Y30LB02*/')SKIP;
*   ANS('/*     MACMEAN  MSSC SUSPEND MACRO          '××
*       '               @Y30LB02*/')SKIP;
*   ANS('/*VS1-4/VS2-3 VERSION--06/09/74 LEVEL 00    '××
*       '               @Y30LB02*/')SKIP;
*   ANS('/*THIS IS THE OUTPUT FOR A SECONDARY        '××
*       '               @Y30LB02*/')SKIP;
*   ANS('/*INVOCATION OF THE STANDARD FORM MACRO     '××
*       '               @Y30LB02*/')SKIP;
*   ANS('/*******************************************'××
*       '************** @Y30LB02*/')SKIP;
*        ANS('RFY(SUSPR1)RSTD;/*RESTRICT REG1 @Y30LB02*/')SKIP;
*        ANS('SUSPR1=ADDR(SUSPLEN);/*R1 PTR TO BLK @Y30LB02*/')SKIP;
*        ANS('SUSPLEN= '××QUOTE(SUPLEN)××'X;/*LENGTH '××
*        'OF SUSPEND REQUEST @Y30LB02*/')SKIP;
*        ANS('SUSPOP='××QUOTE(SUPOP)××'X;/*SUSPEND'××
*        ' OPCODE @Y30LB02*/')SKIP;
*        GO TO COMPROC;       /*BRANCH,CK PARAMETERS         @Y30LB02*/
*     END;                    /*                             @Y30LB02*/
*/*                                                          @Y30LB02*/
*   ELSE                      /*STD FORM 1ST TIME            @Y30LB02*/
*     DO;                     /*PRINT DECLARES               @Y30LB02*/
*   ANS('/*******************************************'××
*       '************** @Y30LB02*/')SKIP;
*   ANS('/*     MACMEAN  MSSC SUSPEND MACRO          '××
*       '               @Y30LB02*/')SKIP;
*   ANS('/*VS1-4/VS2-3 VERSION--06/09/74 LEVEL 00    '××
*       '               @Y30LB02*/')SKIP;
*   ANS('/*INVOCATION OF THE STANDARD FORM MACRO     '××
*       '               @Y30LB02*/')SKIP;
*   ANS('/*******************************************'××
*       '*********** @Y30LB02*/')SKIP;
*        ANS('RFY(SUSPR1)RSTD;/*RESTRICT REG1 @Y30LB02*/')SKIP;
*        ANS('SUSPR1=ADDR(SUSPLEN);/*R1 PTR  '××
*        'TO BLOCK @Y30LB02*/')SKIP;
*        ANS('DCL 1 SUSPLST  STATIC LOCAL,'××
*        ' /*SUSPEND REQUEST BLOCK @Y30LB02*/')SKIP;
*        ANS('    2  SUSPLEN  FIXED(31)INIT('××QUOTE
*        ('08')××'X),/*LENGTH OF SUSPEND REQ BLK @Y30LB02*/')SKIP;
*        ANS('    2  SUSPOP  CHAR(1)INIT('××QUOTE('22')××
*        'X),/*SUSPEND OPCODE @Y30LB02*/')SKIP;
*        ANS('    2  *      CHAR(1)INIT('××QUOTE('00')××
*        'X),/*RESERVED AND ZERO @Y30LB02*/')SKIP;
*        ANS('    2  SUSPFLG BIT(16)INIT('××QUOTE
*        ('0000000000000000')××'B),/*TWO BYTE ATTRI'××
*        'BUTE AREA @Y30LB02*/')SKIP;
*        ANS('       3  SUPIMED BIT(1),/*SUSPEND'××
*        ' IMMEDIATE ATTRIBUTE @Y30LB02*/')SKIP;
*        ANS('       3  SUPRCON BIT(1),/*SUSPEND'××
*        ' RECONFIGURATION ATTRIBUTE @Y30LB02*/')SKIP;
*        ANS('       3  SUPPRSW BIT(1),/*SUSPEND'××
*        ' PREPARE TO SWAP MSCS ATTRIBUTE @ZD00155*/')SKIP;
*        ANS('       3  *      BIT(13);/*RESERVED AND'××
*        ' MUST BE ZERO @ZD00155*/')SKIP;
*        SUSPTEST=SUSPTEST+1;  /*INCREM STD FORM CTR         @Y30LB02*/
*        GO TO COMPROC;        /*BRANCH TO CK PARAMETERS     @Y30LB02*/
*     END;                     /*                            @Y30LB02*/
*/*                                                          @Y30LB02*/
*/*********************************************************  @Y30LB02*/
*/*      COMMON PROCESSING FOR EXECUTE AND STANDARD FORMS    @Y30LB02*/
*/*      THE FOLLOWING CODE WILL CHECK TO DETERMINE WHAT     @Y30LB02*/
*/*      PARAMETERS ARE SPECIFIED AND PLACE THE VALUE IN     @Y30LB02*/
*/*      THE REQUEST BLOCK                                   @Y30LB02*/
*/*********************************************************  @Y30LB02*/
*COMPROC:
*   IF FLAGS ^= '' THEN   /*FLAGS SPECIFIED?                 @Y30LB02*/
*        ANS('SUSPFLG= '××FLAGS××';/*VALUE OF FLAGS '××
*        'PARAMETER @Y30LB02*/')SKIP;
*/*                                                          @Y30LB02*/
*/********************************************************   @Y30LB02*/
*/*      THE FOLLOWING CODE WILL INVOKE SVC 126.             @Y30LB02*/
*/*      THE RETURN CODE IS SET AND PROCESSING ENDS.         @Y30LB02*/
*/********************************************************   @Y30LB02*/
*        ANS('SVC (126);/*INVOKE  SVC 126 @Y30LB02*/')SKIP;
*        ANS('RFY  SUSPR1 UNRSTD;/*UNRESTRICT REG 1 @Y30LB02*/')SKIP;
*/*                                                          @Y30LB02*/
*FINISH:
*        RETURN CODE(RETCODE);                             /*@Y30LB02*/
*        %END;                                             /*@Y30LB02*/
