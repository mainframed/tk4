          MACRO
          GENTAB
          GBLC  &ERCD(255)          GLOBAL ARRAY OF MSG NAMES
          LCLA  &I
          LCLC  &LQ
.* THIS MACRO GENERATES A SEQUENCED INDEX TO THE ERROR MESSAGES WHICH
.* CONTAINS THE HALF-WORD ADDRESS AND THE LENGTH OF EACH MESSAGE.  THIS
.* ALLOWS THE ERROR CODE TO BE USED TO INDEX THE MESSAGE INDEX.  THIS
.* MACRO MUST BE CALLED EXACTLY ONCE AFTER ALL CALLS TO GENERR.
&LQ       SETC  'L'''
MTABLE    DS    0F                       INDEX OF ERROR MSGS & LENGTHS
MADDR     DS    H                            MESSAGE ADDRESS
MLN       DS    H                            MESSAGE LENGTH
          ORG   MTABLE                   TO START AT THE TOP OF TABLE
          DC    Y(S0-ERRMSGS)            INDEX TO ERR0 MESSAGE
          DC    Y(L'S0-1)                MOVE LENGTH OF ERR0 MESSAGE
          ORG   MTABLE+4                 TO LEAVE SPACE FOR ERR0 ENTRY
.LOOP     ANOP
&I        SETA  &I+1
          AIF   (&I GT 255).END          CHECK FOR MAXIMUM SIZE
          AIF   ('&ERCD(&I)' NE '').C1   FOR UNDEFINED ERROR MESSAGES
          DC    F'0'                     UNDEFINED ERROR MESSAGE
          AGO   .LOOP
.C1       ANOP
          DC    Y(&ERCD(&I)-ERRMSGS)     ADDRESS OF MSG FOR THIS CODE
          DC    Y(&LQ&ERCD(&I).-1)       LENGTH OF MSG FOR THIS CODE
          AGO   .LOOP
.END      MEND
