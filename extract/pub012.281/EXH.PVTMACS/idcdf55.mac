 /********************************************************************/
 /*                                                          @Z40LB35*/
 /* PROCEDURE NAME - DATES  (ACCESS METHOD SERVICES)         @Z40LB35*/
 /*                                                          @Z40LB35*/
 /* FUNCTION - THIS COMMON INCLUDED PROCEDURE PROVIDES       @Z40LB35*/
 /*            THE AMS SERVICES WITH THE BASIC FUNCTION OF   @Z40LB35*/
 /*            CONVERTING A ONE VALUE DATE SUCH AS 99999 TO  @Z40LB35*/
 /*            '00 99 0999' OR OBTAINING THE CURRENT DATE AND@Z40LB35*/
 /*            ADJUSTING IT BY EITHER ADDING OR SUBTRACTING  @Z40LB35*/
 /*            DAYS TO THAT DATE.  THE DATE WILL BE ADJUSTED @Z40LB35*/
 /*            FOR ANY LEAP YEARS.  THE DATE WILL ALSO BE    @Z40LB35*/
 /*            ADJUSTED TO FIT WITHIN THE RANGE 0 TO 99999.  @Z40LB35*/
 /*                                                          @Z40LB35*/
 /* ENTRY POINT - DATES                                      @Z40LB35*/
 /*                                                          @Z40LB35*/
 /*      LINKAGE - 'CALL DATES (DATESAGL)'                   @Z40LB35*/
 /*                '%INCLUDE SYSLIB(IDCDF55)'                @Z40LB35*/
 /*                                                          @Z40LB35*/
 /* INPUT  - ADDRESS OF ARGUMENT LIST (DATESAGL)             @Z40LB35*/
 /*                                                          @Z40LB35*/
 /* OUTPUT - A RETURN CODE IN REGISTER 15                    @Z40LB35*/
 /*          (0) - DATES SUCCESSFUL                          @Z40LB35*/
 /*                                                          @Z40LB35*/
 /*        - A CONVERTED DATE (00 YY DDDD) IN BINARY         @Z40LB35*/
 /*                                                          @Z40LB35*/
 /* EXIT NORMAL - RETURNS TO CALLER WITH CONDITION           @Z40LB35*/
 /*               CODE ZERO                                  @Z40LB35*/
 /*                                                          @Z40LB35*/
 /* EXIT ERROR  - NONE                                       @Z40LB35*/
 /*                                                          @Z40LB35*/
 /* CHANGE ACTIVITY - NONE                                   @Z40LB35*/
 /*                                                          @Z40LB35*/
 /********************************************************************/
 DATES: PROC;                                              /*@Z40LB35*/
             /*DECLARATION FOR DATES PROCEDURE               @Z40LB35*/
 DCL     TIMEVAR CHAR(8);           /*TIME VARIABLE - UTIME  @Z40LB35*/
                                    /*USED IN UTIME CALL     @Z40LB35*/
 DCL     1 DATEVAR CHAR(4),         /*DATE VARIABLE - UTIME  @Z40LB35*/
           2 *     CHAR(1),         /*FILLER - RESERVED      @Z40LB35*/
           2 DATEPACK CHAR(3);      /*DATE RETURN - PACKED   @Z40LB35*/
                                    /*DECIMAL FORMAT         @Z40LB35*/
             /*THE FOLLOWING STRUCTURE IS THE DATE RETURNED  @Z40LB35*/
 DCL     1 CONVDATE BDY(WORD) BASED(DATRETRN),
           2 CONVYEAR FIXED(15),    /*YEAR - RANGE 0 - 99    @Z40LB35*/
           2 CONVDAYS FIXED(15);    /*DAYS - RANGE 0 - 999   @Z40LB35*/
             /*THE FOLLOWING DECLARES ARE INTERNAL FIELDS    @Z40LB35*/
 DCL     DATEBIN FIXED(31);         /*USED TO UNPACK DATE    @Z40LB35*/
 DCL     QUITSW  BIT(1);            /*USED TO CONTROL LOOPS  @Z40LB35*/
 DCL     ADJDAYS FIXED(31);         /*USED TO ADJUST DAYS    @Z40LB35*/
             /*THE FOLLOWING ARE STATIC CONSTANTS            @Z40LB35*/
 DCL     REGYEAR  FIXED(15) INIT(365);                     /*@Z40LB35*/
                                    /*NUM DAYS IN REG YEAR   @Z40LB35*/
 DCL     LEAPYEAR FIXED(15) INIT(366);                     /*@Z40LB35*/
                                    /*NUM DAYS IN LEAP YEAR  @Z40LB35*/
 DCL     MAXYEAR  FIXED(15) INIT(99);                      /*@Z40LB35*/
                                    /*HIGHEST YEAR           @Z40LB35*/
 DCL     MAXDAYS  FIXED(15) INIT(999);                     /*@Z40LB35*/
                                    /*HIGHEST DAYS           @Z40LB35*/
 DCL     MINYEAR  FIXED(15) INIT(0);                       /*@Z40LB35*/
                                    /*LOWEST YEAR            @Z40LB35*/
 DCL     MINDAYS  FIXED(15) INIT(0);                       /*@Z40LB35*/
                                    /*LOWEST DAYS            @Z40LB35*/
 DCL     NOREMAIN FIXED(15) INIT(0);                       /*@Z40LB35*/
                                    /*NO REMAINDER AFTER DIV @Z40LB35*/
 @EJECT;                                                   /*@Z40LB35*/
             /* THIS IS THE START OF THIS PROCEDURE          @Z40LB35*/
             /* HERE INITIALIZATION TAKES PLACE.             @Z40LB35*/
             /* THE PARM LIST HEADER IS INITIALIZED,         @Z40LB35*/
             /* THE TRACE POINT 'DTST' IS RECORDED, AND      @Z40LB35*/
             /* THE DATE IS SET TO ZERO.                     @Z40LB35*/
         UTRACE = 'DTST';                                  /*@Z40LB35*/
         DATHEAD = 'DATESAGL';                             /*@Z40LB35*/
         CONVDATE = 0;                                     /*@Z40LB35*/
             /* DETERMINE IF CALLER WANTS THE CURRENT DATE   @Z40LB35*/
             /* ADJUSTED WITH DAYS ADD OR SUBTRACTED.        @Z40LB35*/
         IF DATDAYOP = ON THEN
           DO;                                             /*@Z40LB35*/
             /* IF CALLER WANTS TO ADJUST CURRENT DATE,      @Z40LB35*/
             /* ISSUE UTIME TO RETRIEVE CURRENT DATE,        @Z40LB35*/
             /* CONVERT THE DATE FROM PACKED DECIMAL TO      @Z40LB35*/
             /* BINARY, AND SEPARATE THE YEARS FROM THE      @Z40LB35*/
             /* DAYS.                                        @Z40LB35*/
             ADJDAYS = DATDAYS;                            /*@Z40LB35*/
             UTIME (GDTTBL,TIMEVAR,DATEVAR);               /*@Z40LB35*/
             CVB(DATEBIN,DATEPACK);                        /*@Z40LB35*/
             CONVYEAR = DATEBIN/1000;                      /*@Z40LB35*/
             CONVDAYS = DATEBIN - (CONVYEAR*1000);         /*@Z40LB35*/
             /*INITIALIZE QUITSW OFF FOR LOOP CONTROL        @Z40LB35*/
             QUITSW = OFF;                                 /*@Z40LB35*/
             /*DETERMINE IF CALLER WANTS DAYS ADDED TO       @Z40LB35*/
             /*CURRENT DATE.                                 @Z40LB35*/
             IF DATADD = ON THEN
             /*IF CALLER WANTS DAYS ADDED, LOOP THOUGH       @Z40LB35*/
             /*THE FOLLOWING DO LOOP UNTIL QUITSW IS SET     @Z40LB35*/
               DO WHILE QUITSW = OFF;                      /*@Z40LB35*/
             /*TEST FOR LEAP YEAR - EVERY FOUTH YEAR IS LEAP @Z40LB35*/
             /*YEAR.  IF NO REMAINDER AFTER DIVIDING BY FOUR @Z40LB35*/
             /*THEN THE YEAR BEING PROCESSED IS A LEAP YEAR  @Z40LB35*/
                 IF CONVYEAR//4 = NOREMAIN THEN
             /*DETERMINE IF THE DAYS BEING ADDED CAN BE      @Z40LB35*/
             /*ADDED TO CURRENT YEAR BEING PROCESSED WITHOUT @Z40LB35*/
             /*AN OVERFLOW INTO THE NEXT YEAR.               @Z40LB35*/
                   IF (ADJDAYS + CONVDAYS) > LEAPYEAR THEN
             /*SUBTRACT LEAP YEAR DAYS (366) FROM DAYS       @Z40LB35*/
             /*AND INCREMENT YEAR BY ONE                     @Z40LB35*/
                     DO;                                   /*@Z40LB35*/
                       ADJDAYS = ADJDAYS - LEAPYEAR;       /*@Z40LB35*/
                       CONVYEAR = CONVYEAR + 1;            /*@Z40LB35*/
                     END;                                  /*@Z40LB35*/
                   ELSE
             /*INCREMENT DAYS AND SET QUITSW TO FALL OUT     @Z40LB35*/
             /*OF DO LOOP                                    @Z40LB35*/
                     DO;                                   /*@Z40LB35*/
                       CONVDAYS = ADJDAYS + CONVDAYS;      /*@Z40LB35*/
                       QUITSW = ON;                        /*@Z40LB35*/
                     END;                                  /*@Z40LB35*/
                 ELSE
             /*DETERMINE IF THE DAYS BEING ADDED CAN BE      @Z40LB35*/
             /*ADDED TO CURRENT YEAR BEING PROCESSED WITHOUT @Z40LB35*/
             /*AN OVERFLOW INTO THE NEXT YEAR.               @Z40LB35*/
                   IF (ADJDAYS + CONVDAYS) > REGYEAR THEN
             /*SUBTRACT REG YEAR DAYS (365) FROM DAYS        @Z40LB35*/
             /*AND INCREMENT YEAR BY ONE                     @Z40LB35*/
                     DO;                                   /*@Z40LB35*/
                       ADJDAYS = ADJDAYS - REGYEAR;        /*@Z40LB35*/
                       CONVYEAR = CONVYEAR + 1;            /*@Z40LB35*/
                     END;                                  /*@Z40LB35*/
                   ELSE
             /*INCREMENT DAYS AND SET QUITSW TO FALL OUT     @Z40LB35*/
             /*OF DO LOOP                                    @Z40LB35*/
                     DO;                                   /*@Z40LB35*/
                       CONVDAYS = CONVDAYS + ADJDAYS;      /*@Z40LB35*/
                       QUITSW = ON;                        /*@Z40LB35*/
                     END;                                  /*@Z40LB35*/
               END;                                        /*@Z40LB35*/
 @EJECT;                                                   /*@Z40LB35*/
             /*IF DAYS ARE TO BE SUBTRACTED FROM CURRENT,    @Z40LB35*/
             /*DO THE FOLLOWING DO GROUP                     @Z40LB35*/
             ELSE
             /*DO THE FOLLOWING WHILE THE QUITSW IS NOT SET  @Z40LB35*/
               DO WHILE QUITSW = OFF;                      /*@Z40LB35*/
             /*TEST FOR LEAP YEAR - EVERY FOUTH YEAR IS LEAP @Z40LB35*/
             /*YEAR.  IF NO REMAINDER AFTER DIVIDING BY FOUR @Z40LB35*/
             /*THEN THE YEAR BEING PROCESSED IS A LEAP YEAR  @Z40LB35*/
                 IF (CONVYEAR-1)//4 = NOREMAIN THEN
             /*TEST TO SEE IF THE YEAR CAN BE DECREMENTED    @Z40LB35*/
                   IF ADJDAYS >= LEAPYEAR THEN
             /*SUBTRACT LEAP YEAR DAYS (366) FROM DAYS       @Z40LB35*/
             /*AND DECREMENT YEAR BY ONE                     @Z40LB35*/
                     DO;                                   /*@Z40LB35*/
                       ADJDAYS = ADJDAYS - LEAPYEAR;       /*@Z40LB35*/
                       CONVYEAR = CONVYEAR - 1;            /*@Z40LB35*/
                     END;                                  /*@Z40LB35*/
                   ELSE
             /*IF NO FULL YEAR, TEST FOR OVERFLOW            @Z40LB35*/
                     IF (CONVDAYS-ADJDAYS) <= 0 THEN
             /*IF YEAR RUNS BACK INTO LAST YEAR,             @Z40LB35*/
             /*ADJUST DAYS, SUBTRACT YEAR AND SET QUITSW     @Z40LB35*/
                       DO;                                 /*@Z40LB35*/
                         CONVDAYS = CONVDAYS + LEAPYEAR - ADJDAYS;
                         CONVYEAR = CONVYEAR - 1;          /*@Z40LB35*/
                         QUITSW = ON;                      /*@Z40LB35*/
                       END;                                /*@Z40LB35*/
                     ELSE
             /*IF YEAR DOES NOT RUNS BACK INTO LAST YEAR,    @Z40LB35*/
             /*ADJUST DAYS AND SET QUITSW                    @Z40LB35*/
                       DO;                                 /*@Z40LB35*/
                         CONVDAYS = CONVDAYS - ADJDAYS;    /*@Z40LB35*/
                         QUITSW = ON;                      /*@Z40LB35*/
                       END;                                /*@Z40LB35*/
             /*ELSE THIS IS A NORMAL YEAR(NON-LEAP YEAR)     @Z40LB35*/
                 ELSE
             /*TEST TO SEE IF THE YEAR CAN BE DECREMENTED    @Z40LB35*/
                   IF ADJDAYS >= REGYEAR THEN
             /*SUBTRACT REG YEAR DAYS (365) FROM DAYS        @Z40LB35*/
             /*AND DECREMENT YEAR BY ONE                     @Z40LB35*/
                     DO;                                   /*@Z40LB35*/
                       ADJDAYS = ADJDAYS - REGYEAR;        /*@Z40LB35*/
                       CONVYEAR = CONVYEAR - 1;            /*@Z40LB35*/
                     END;                                  /*@Z40LB35*/
                   ELSE
             /*IF NO FULL YEAR, TEST FOR OVERFLOW            @Z40LB35*/
                     IF (CONVDAYS-ADJDAYS) <= 0 THEN
             /*IF YEAR RUNS BACK INTO LAST YEAR,             @Z40LB35*/
             /*ADJUST DAYS, SUBTRACT YEAR AND SET QUITSW     @Z40LB35*/
                       DO;                                 /*@Z40LB35*/
                         CONVDAYS = CONVDAYS + REGYEAR - ADJDAYS;
                         CONVYEAR = CONVYEAR - 1;          /*@Z40LB35*/
                         QUITSW = ON;                      /*@Z40LB35*/
                       END;                                /*@Z40LB35*/
                     ELSE
             /*IF YEAR DOES NOT RUNS BACK INTO LAST YEAR,    @Z40LB35*/
             /*ADJUST DAYS AND SET QUITSW                    @Z40LB35*/
                       DO;                                 /*@Z40LB35*/
                         CONVDAYS = CONVDAYS - ADJDAYS;    /*@Z40LB35*/
                         QUITSW = ON;                      /*@Z40LB35*/
                       END;                                /*@Z40LB35*/
               END;                                        /*@Z40LB35*/
           END;                                            /*@Z40LB35*/
 @EJECT;                                                   /*@Z40LB35*/
             /*DETERMINE IF THE CALLER WANTS THE DATE        @Z40LB35*/
             /*CONVERTED                                     @Z40LB35*/
         IF DATDTEOP= ON THEN
             /*IF CALLER WANTS DATE CONVERTED, CONVERT THE   @Z40LB35*/
             /*DATE BY SEPARATING THE YEARS FROM THE DAYS    @Z40LB35*/
           DO;                                             /*@Z40LB35*/
             CONVYEAR = DATDATE/1000;                      /*@Z40LB35*/
             CONVDAYS = DATDATE - (CONVYEAR * 1000);       /*@Z40LB35*/
           END;                                            /*@Z40LB35*/
             /*HERE THE DETERMINATION WILL BE MADE TO SEE    @Z40LB35*/
             /*IF THE LIMITS ARE VIOLATED                    @Z40LB35*/
             /*IF SO, FORCE THE DATE TO FALL WITHIN THE      @Z40LB35*/
             /*LIMITS (0 99999)                              @Z40LB35*/
         IF CONVYEAR > MAXYEAR THEN
           DO;                                             /*@Z40LB35*/
             CONVYEAR = MAXYEAR;                           /*@Z40LB35*/
             CONVDAYS = MAXDAYS;                           /*@Z40LB35*/
           END;                                            /*@Z40LB35*/
         ELSE
           IF CONVYEAR < MINYEAR × CONVDAYS < MINDAYS THEN
             DO;                                           /*@Z40LB35*/
               CONVYEAR = MINYEAR;                         /*@Z40LB35*/
               CONVDAYS = MINDAYS;                         /*@Z40LB35*/
             END;                                          /*@Z40LB35*/
             /*RETURN TO CALLER WITH RETURN CODE ZERO        @Z40LB35*/
         UDUMP (GDTTBL,'DTND');                            /*@Z40LB35*/
         RETURN CODE(RCOK);                                /*@Z40LB35*/
         END DATES;                                        /*@Z40LB35*/
