         MACRO
&MFRNAME MFROUTER &NAME,&ENTRY=BRANCH,&CVTREG=
         LCLA  &CODE
         LCLC  &ACTIVEF
         LCLC  &REG
         AGO   .BEGIN
* /* MACCOMP Y-2 SC1CQ/GAM20                                         */
*/* MACSTAT Y-2 73271/Y02150                                         */
*/**** START OF SPECIFICATIONS ***************************************/
*/*                                                                  */
*/* MODULE-NAME = MFROUTER                                           */
*/*                                                                  */
*/* DESCRIPTIVE-NAME = MF/1 ROUTER CALL                              */
*/*                                                                  */
*/* COPYRIGHT = NONE                                                 */
*/*                                                                  */
*/* STATUS = RELEASE 2 / VERSION 0                                   */
*/*                                                                  */
*/* FUNCTION = MFROUTER PROVIDES AUTHORIZED CONTROL PROGRAMS WITH    */
*/*            A WELL-DEFINED LINKAGE TO MF/1 MEASUREMENT            */
*/*            GATHERING ROUTINES WHICH ARE RESIDENT IN MAIN         */
*/*            STORAGE ONLY WHEN MF/1 IS ACTIVE.                     */
*/*                                                                  */
*/* NOTES = SEE BELOW                                                */
*/*                                                                  */
*/*    DEPENDENCIES = THE USER MUST PROVIDE A CVT MAPPING MACRO      */
*/*                   IN EITHER PL/S OR BAL AND A CODE BASE REGISTER */
*/*                                                                  */
*/*    RESTRICTIONS = THE USER MUST BE DISABLED, IN SUPERVISOR STATE */
*/*                   AND KEY.                                       */
*/*                                                                  */
*/*    REGISTER-CONVENTIONS = REGISTER 0 IS LOADED WITH AN MFROUTER  */
*/*                           CODE, REGISTER 14 CONTAINS THE RETURN  */
*/*                           ADDRESS, AND REGISTER 15 CONTAINS THE  */
*/*                           ENTRY POINT ADDRESS.  ALL OTHER        */
*/*                           REGISTERS ARE TRANSPARENT.             */
*/*                                                                  */
*/* MODULE-TYPE = BAL EXECUTABLE MACRO                               */
*/*                                                                  */
*/* CHANGE-ACTIVITY =                                                */
*/*                                                                  */
*/* A000000-999999                                             Y02150*/
*/*                                                                  */
*/* METHOD OF INVOCATION = SEE BELOW                                 */
*/*                                                                  */
*/*    BAL - MFROUTER A,ENTRY=B,CVTREG=C                             */
*/*          WHERE A - MFROUTER ENTRY TYPE (REQUIRED):               */
*/*                    TIMER                                         */
*/*                    MF1TCH                                        */
*/*                B - MUST BE 'BRANCH' OR OMITTED                   */
*/*                C - ANY REGISTER SYMBOL CONTAINING THE ADDRESS    */
*/*                    OF THE CVT.  IF THIS PARAMETER IS OMITTED,    */
*/*                    THE CVT ADDRESS WILL BE LOADED INTO REGISTER  */
*/*                    15 FROM THE PSA                               */
*/*                                                                  */
*/***** END OF SPECIFICATIONS ****************************************/
.BEGIN   ANOP
*/* MACDATE Y-2 73271                                                */
&ACTIVEF SETC  '80'
&CODE    SETA  0 TIMER CODE
&REG     SETC  '15'
         AIF   ('&NAME' EQ 'TIMER').NAMEOK CHECK 1ST PARAMETER
&CODE    SETA  8 MF1TCH CODE
         AIF   ('&NAME' EQ 'MF1TCH').NAMEOK ALTERNATE 1ST PARAMETER
         MNOTE 8,'INVALID PARAMETER (&NAME)'
         MEXIT
.NAMEOK  AIF   ('&ENTRY' EQ 'BRANCH').ENTRYOK CHECK 2ND PARAMETER
         MNOTE 8,'INVALID PARAMETER (&ENTRY)'
         MEXIT
.ENTRYOK ANOP
         AIF ('&CVTREG' NE '').REGNUM . CVT BASE REGISTER SPECIFIED?
         L     15,CVTPTR(,0) .    SET UP CVT BASE REGISTER
         AGO   .REGOK
.REGNUM  ANOP
&REG     SETC  '&CVTREG'
.REGOK   ANOP
&MFRNAME TM    CVTMFRTR-CVTMAP(&REG),X'&ACTIVEF' . MF/1 ACTIVE?
         BZ    IRB&SYSNDX .       NO
         LA    0,&CODE(,0) .      YES, SAVE CODE FOR IRBMFEVT
         L     15,CVTMFRTR-CVTMAP(,&REG) GET IRBMFEVT ROUTINE
         BALR  14,15                        PASS CONTROL TO IRBMFEVT
IRB&SYSNDX DS  0H
         MEND
