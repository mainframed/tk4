* %/*
         MACRO
&NAME    ENFREQ &ACTION=,&CODE=,&TYPE=,&QUAL=,&QMASK=,                 *
               &EXIT=,&PARM=,&DTOKEN=,&STOKEN=,&FREEPRM=,&MF=,         *
               &RELATED=
.**********************************************************************
.*                                                                    *
.* MACRO  NAME - ENFREQ                                               *
.*                                                                    *
.* DESCRIPTION - MACRO FOR EVENT NOTIFICATION (ENF) REQUESTS          *
.*                                                                    *
.* COPYRIGHT -                                                        *
.*             5740-XYN COPYRIGHT IBM CORP 1981,                      *
.*             LICENSED MATERIAL - PROGRAM,                           *
.*             PROPERTY OF IBM,                                       *
.*             REFER TO COPYRIGHT INSTRUCTIONS FORM                   *
.*             NUMBER G120-2083                                       *
.*                                                                    *
.* STATUS - OS/VS2 JBB1326                                            *
.*                                                                    *
.* FUNCTION -                                                         *
.*      THE ENFREQ MACRO IS USED TO GENERATE THE EVENT                *
.*      PARAMETER LIST AND/OR INVOKE THE ENF SERVICE ROUTINE.         *
.*                                                                    *
.* OPERATION -                                                        *
.*      . VERIFY THE INPUT PARAMETERS SPECIFIED BY THE USER           *
.*                                                                    *
.*      . BUILD THE EVENT PARAMETER LIST (LIST AND NORMAL             *
.*        FORM)                                                       *
.*                                                                    *
.*      . GENERATE INSTRUCTIONS TO MODIFY THE EVENT PARAMETER         *
.*        LIST (NORMAL AND EXECUTE FORM)                              *
.*                                                                    *
.*      . GENERATE INSTRUCTION TO INVOKE THE ENF SERVICE              *
.*        ROUTINE (NORMAL AND EXECUTE FORM)                           *
.*                                                                    *
.* NOTES - N/A                                                        *
.*                                                                    *
.* DEPENDENCIES -                                                     *
.*       TO USE THE ENFREQ MACRO INSTRUCTION, THE CALLER MUST         *
.*            1. DECLARE STORAGE FOR A 4 BYTE POINTER ENFPTR          *
.*            2. DEFINE CVTPTR (EQUAL TO LOCATION 16)                 *
.*            3. INCLUDE THE CVT, IEFENFCT, AND IEFENFPM              *
.*               MAPPPING MACROS                                      *
.*                                                                    *
.* MODULE TYPE - MACRO                                                *
.*                                                                    *
.* ENTRY POINT - N/A                                                  *
.*                                                                    *
.* INPUT - THE FOLLOWING MAY BE SPECIFIED AS PARAMETERS TO ENFREQ     *
.*        . &ACTION - ACTION REQUESTED FOR ENF PROCESSING             *
.*                    (VALID TYPES : SIGNAL, LISTEN, AND DELETE)      *
.*        . &CODE - THE EVENT CODE                                    *
.*        . &DTOKEN - TOKEN FOR LISTEN AND DELETE REQUEST             *
.*        . &STOKEN - TOKEN FOR SIGNAL REQUEST                        *
.*        . &TYPE - REQUESTED ENF TO PROCESS THE REQUEST              *
.*                  SYNCHRONOUSLY(SYNC) OR ASYNCHRONOUSLY(ASYNC)      *
.*        . &QUAL - QUALIFIER (THE MEANING OF THE QUALIFIER IS        *
.*                  DETERMINED BY THE SIGNALLERS/LISTENERS OF         *
.*                  A GIVEN EVENT)                                    *
.*        . &QMASK - MASK TO DETERMINE WHICH BYTES IN THE             *
.*                   QUALIFIER ARE TO BE USED IN COMPARING            *
.*                   QUALIFIERS DURING SIGNAL PROCESSING.             *
.*        . &EXIT - ADDRESS OF SIGNALLER'S OR LISTENER'S              *
.*                  EXIT ROUTINE                                      *
.*        . &FREEPRM - FREEMAIN PARAMETERS USED TO FREE THE           *
.*                     SIGNALLER'S PARAMETER LIST                     *
.*        . &PARM -  ADDRESS OF SIGNALLER'S  PARAMETER LIST           *
.*        . &RELATED - USED BY THE CALLER TO SPECIFY                  *
.*                     INFORMATION USED TO SELF-DOCUMENT THE          *
.*                     VARIOUS ENFREQ REQUESTS                        *
.*                                                                    *
.* OUTPUT - THE OUTPUT FROM THE ENFREQ MACRO IS AN EVENT              *
.*          PARAMETER LIST (FOR LIST AND NORMAL FORM) AND/OR          *
.*          INSTRUCTIONS TO MODIFY THE EVENT PARAMETER LIST           *
.*          AND INVOKE THE ENF SERVICE ROUTINE (IEFENFFX).            *
.*                                                                    *
.* EXIT,NORMAL                                                        *
.*   CONDITION - NO ERRORS DETECTED                                   *
.*   OUTPUT    - NO MNOTE ISSUED. SEVERITY CODE = 0.                  *
.*                                                                    *
.* EXIT,NORMAL                                                        *
.*   CONDITION - ERROR CONDITION DETECTED BY THE ENFREQ MACRO         *
.*   OUTPUT    - INFORMATION MNOTES WITH SEVERITY CODE 4              *
.*               ISSUED WHEN PARAMETER SPECIFIED IS                   *
.*               INCORRECT. THE SPECIFIED PARAMETER IS IGNORED        *
.*               AND THE DEFAULT VALUE FOR THAT PARAMETER             *
.*               IS ASSUMED.                                          *
.*                                                                    *
.* EXIT, ERROR                                                        *
.*   CONDITION - ERROR CONDITION DETECTED BY THE ENFREQ MACRO         *
.*   OUTPUT    - ERROR MNOTES WITH SEVERITY CODE 8 (SEE               *
.*               MESSAGES) ISSUED WHEN CERTAIN REQUIRED               *
.*               PARAMETER IS MISSING OR INCORRECTLY SPECIFIED.       *
.*               THE MACRO WILL ATTEMPT TO SCAN FOR ADDITIONAL        *
.*               ERRORS.                                              *
.*                                                                    *
.* EXIT, ERROR                                                        *
.*   CONDITION - SEVERE ERROR DETECTED BY THE ENFREQ MACRO            *
.*   OUTPUT    - ERROR MNOTES WITH SEVERITY CODE 12 (SEE              *
.*               MESSAGES) ISSUED WHEN CERTAIN REQUIRED               *
.*               PARAMETER IS MISSING OR INCORRECTLY SPECIFIED.       *
.*               THE MACRO EXPANSION WILL BE TERMINATED AS            *
.*               A RESULT OF THE ERROR.                               *
.*                                                                    *
.*                                                                    *
.* EXTERNAL REFERENCES - N/A                                          *
.*                                                                    *
.* TABLES - N/A                                                       *
.*                                                                    *
.* MACROS - CVT                                                       *
.*          IEFENFCT                                                  *
.*          IEFENFPM                                                  *
.*                                                                    *
.* CHANGE ACTIVITY -                                                  *
.*   $D0= DCR#1   JBB1326  800604  PD42: IMPLICIT FLAG - NEW CODE     *
.*   $P1= PMM0114 JBB1326  800604  PD42: ADD KEYWORD RETCODE FOR      *
.*                                         ENFREQP                    *
.*   $P2= PMM0135 JBB1326  800604  PD42: CHECK FOR ACTION PARAMETER   *
.*                                         IF DTOKEN IS SPECIFIED IN  *
.*                                         EXECUTE FORM               *
.*   $P3= PMM0335 JBB1326  810420  PD42: CORRECT CHANGE ACTIVITY      *
.*                                         LINE FLAGS AND COPYRIGHT   *
.*                                                                    *
.* MESSAGES - (MNOTES)                                                *
.*   - INFORMATIONAL MNOTES :                                         *
.*       MNOTE   04,'QUAL NOT ALLOWED FOR DELETE REQUEST - IGNORED'   *
.*       MNOTE   04,'QMASK NOT ALLOWED FOR SIGNAL REQUEST - IGNORED'  *
.*       MNOTE   04,'QMASK NOT ALLOWED FOR DELETE REQUEST - IGNORED'  *
.*       MNOTE   04,'EXIT NOT ALLOWED FOR DELETE REQUEST - IGNORED'   *
.*       MNOTE   04,'PARM NOT ALLOWED FOR LISTEN REQUEST - IGNORED'   *
.*       MNOTE   04,'PARM NOT ALLOWED FOR DELETE REQUEST - IGNORED'   *
.*       MNOTE   04,'FREEPRM NOT ALLOWED FOR LISTEN REQUEST- IGNORED' *
.*       MNOTE   04,'FREEPRM NOT ALLOWED FOR DELETE REQUEST- IGNORED' *
.*       MNOTE   04,'STOKEN NOT ALLOWED FOR DELETE REQUEST - IGNORED' *
.*       MNOTE   04,'STOKEN NOT ALLOWED FOR LISTEN REQUEST - IGNORED' *
.*       MNOTE   04,'DTOKEN NOT ALLOWED IN LIST FORM - IGNORED'       *
.*       MNOTE   04,'DTOKEN NOT ALLOWED FOR SIGNAL REQUEST - IGNORED' *
.*                                                                    *
.*   - ERROR MNOTES :                                                 *
.*       MNOTE   08,'PARM REQUIRED WITH FREEPRM - FREEPRM IGNORED'    *
.*       MNOTE   08,'DTOKEN REQUIRED FOR DELETE REQUEST'              *
.*       MNOTE   08,'ACTION REQUIRED WITH DTOKEN - DTOKEN IGNORED'    *
.*                                                                @P2A*
.*       MNOTE   08,'INVALID TYPE : TYPE=&TYPE IGNORED'               *
.*       MNOTE   08,'INVALID QMASK VALUE : &QMASK(&I) IGNORED'        *
.*       MNOTE   08,'DUPLICATE QMASK VALUE : &QMASK(&I)'              *
.*       MNOTE   08,'MORE THAN &MAXQMSK QMASK VALUES - QMASK IGNORED' *
.*       MNOTE   08,'EXTRANEOUS QMASK INFO - QMASK=(ALL) ASSUMED'     *
.*       MNOTE   08,'EXTRANEOUS QMASK INFO - QMASK=(NONE) ASSUMED'    *
.*       MNOTE   08,'CODE PARAMETER REQUIRED'                         *
.*       MNOTE   08,'EXIT REQUIRED FOR LISTEN REQUEST'                *
.*       MNOTE   08,'LENGTH REQUIRED IN FREEPRM - FREEPRM IGNORED'    *
.*       MNOTE   08,'SUBPOOL REQUIRED IN FREEPRM - FREEPRM IGNORED'   *
.*       MNOTE   08,'SUBPOOL AND LENGTH REQUIRED - FREEPRM IGNORED'   *
.*       MNOTE   08,'MORE THAN &MAXF FREEPRM VALUES -FREEPRM IGNORED' *
.*       MNOTE   08,'INVALID FREEPRM KEY VALUE - FREEPRM IGNORED'     *
.*       MNOTE   08,'KEY IN REGISTER NOT ALLOWED FOR LIST FORM'       *
.*       MNOTE   08,'LENGTH IN REGISTER NOT ALLOWED FOR LIST FORM'    *
.*       MNOTE   08,'SUBPOOL IN REGISTER NOT ALLOWED FOR LIST FORM'   *
.*                                                                    *
.*       MNOTE   12,'INVALID ACTION : ACTION=&ACTION-MACRO TERMINATED'*
.*       MNOTE   12,'INVALID MF PARAMETER : MF=&MF - MACRO TERMINATED'*
.*       MNOTE   12,'ACTION REQUIRED - MACRO TERMINATED'              *
.*       MNOTE   12,'ERROR IN ENFREQ MACRO - MACRO TERMINATED'        *
.*                                                                    *
.*                                                                    *
.* A 000000-999999                                                @D0A*
.**********************************************************************
.*-------------------------------------------------------------------*
.*   DECLARATIONS FOR TEMPORARY VALUES FOR EVENT PARAMETER LIST      *
.*-------------------------------------------------------------------*
         LCLA    &LEN,&ACT             WORD 1 : LENGTH,ACTION
         LCLC    &CDE                  WORD 2 : CODE
         LCLC    &MSK                  WORD 3 : QMASK
         LCLA    &FLG                  WORD 3 : FLAGS
         LCLC    &FREEKEY              WORD 3 : KEY FOR FREEPRM
         LCLC    &FREESP               WORD 3 : SUBPOOL FOR FREEPRM
         LCLC    &QUALIF               WORD 4 : QUALIFIER
         LCLC    &EXTADR               WORD 5 : EXIT ADDRESS
         LCLC    &PARMS                WORD 6 : SIGNAL PARMS
         LCLC    &TOK                  WORD 7 : TOKEN
         LCLC    &FREELEN              WORD 8 : LENGTH FOR FREEPRM
.*-------------------------------------------------------------------*
.*   DECLARATIONS FOR LOCAL VARIABLES                                *
.*-------------------------------------------------------------------*
         LCLB    &NORMAL,&LIST,&EXECUTE FORM OF MACRO CALL
         LCLC    &GNAME                LABEL GENERATED
         LCLC    &REGC                 REGISTER IN CHARACTERS
         LCLA    &N,&I                 FOR PROCESSING QMASK
         LCLB    &QBYTE(4),&ERRMASK    FOR PROCESSING QMASK
         LCLC    &INX                  FOR PROCESSING QMASK
         LCLB    &DONECDE              CODE ALREADY BEEN PROCESSED
         LCLB    &DONEXIT              EXIT ALREADY BEEN PROCESSED
         LCLB    &DONEPRM              PARM ALREADY BEEN PROCESSED
         LCLB    &DONETYP              TYPE ALREADY BEEN PROCESSED
         LCLB    &DONEACT              ACTION ALREADY BEEN PROCESSED
         LCLB    &DONEMSK              QMASK ALREADY BEEN PROCESSED
         LCLB    &DONESP               FREEMAIN : SUBPOOL PROCESSED
         LCLB    &DONELEN              FREEMAIN : LENGTH PROCESSED
         LCLB    &DONEKEY              FREEMAIN : KEY PROCESSED
         LCLB    &DONEFFG              ENFPFREE FLAG HAS BEEN SET UP
         LCLB    &DONEFRP              FREEPRM PROCESSING COMPLETED
         LCLA    &MAXF                 MAXIMUM NUMBER OF FREEPRM ATTR
         LCLA    &MINF                 MINIMUM NUMBER OF FREEPRM ATTR
         LCLA    &MAXQMSK              MAXIMUM NUMBER OF QMASK VALUES
         LCLA    &NPARAMS,&MAXBR,&EXTRA FOR ACTR CALCULATION
.*-------------------------------------------------------------------*
.*   DECLARATIONS FOR DEBUGGING                                      *
.*-------------------------------------------------------------------*
         LCLB    &STOPLOP              DEBUG : PREVENT LOOPING
.*-------------------------------------------------------------------*
.*   INITIALIZE LOCAL VARIABLES                                      *
.*-------------------------------------------------------------------*
&LEN     SETA    32                    LENGTH OF PARAMETER LIST
&ACT     SETA    0                     INVALID ACTION
&CDE     SETC    '0'                   INVALID CODE
&FLG     SETA    X'00'                 DEFAULT VALUES FOR FLAGS
&MSK     SETC    '00000000'            NOT SPECIFIED BY CALLER
&FREEKEY SETC    '0'                   NOT SPECIFIED BY CALLER
&FREESP  SETC    '0'                   NOT SPECIFIED BY CALLER
&QUALIF  SETC    '0'                   NOT SPECIFIED BY CALLER
&EXTADR  SETC    '0'                   NOT SPECIFIED BY CALLER
&PARMS   SETC    '0'                   NOT SPECIFIED BY CALLER
&TOK     SETC    '0'                   NOT SPECIFIED BY CALLER
&FREELEN SETC    '0'                   NOT SPECIFIED BY CALLER
.*-------------------------------------------------------------------*
.*                                                                   *
.*       MINIMUM/MAXIMUM NUMBER OF VALUES ALLOWED IN FREEPRM AND     *
.*       QMASK                                                       *
.*-------------------------------------------------------------------*
&MAXF    SETA    3                     (SUBPOOL,LENGTH,KEY)
&MINF    SETA    2                     (SUBPOOL,LENGTH)
&MAXQMSK SETA    4                     UP TO 4 QMASK VALUES ALLOWED
.*-------------------------------------------------------------------*
.*                                                                   *
.*       SET UP ACTR VALUE                                           *
.*                                                                   *
.*-------------------------------------------------------------------*
&NPARAMS SETA    12                    NUMBER OF MACRO PARAMETERS
&MAXBR   SETA    20                    MAX BRANCHES/PARAM (QMASK USED)
&EXTRA   SETA    100                   EXTRA BRANCHES
         ACTR    &NPARAMS*&MAXBR+&EXTRA
.*-------------------------------------------------------------------*
.*       DETERMINE WHICH FORM OF THE MACRO IS REQUESTED              *
.*-------------------------------------------------------------------*
         AIF     ('&MF' EQ 'L').LISTFM MF=L SPECIFIED
&GNAME   SETC    'IHB'.'&SYSNDX'       INITIALIZE LABEL
         USING   ENFPM,1               MAP ENFPM OVER AREA
         AIF     ('&MF' EQ '').NORMFM  MF NOT SPECIFIED
         AIF     (N'&MF NE 2).BADMF1   EXECUTE FORM HAS 2 PARMS
         AIF     ('&MF(1)' EQ 'E').EXECFM  MF=(E,LIST)
.BADMF1  MNOTE   12,'INVALID MF PARAMETER : MF=&MF - MACRO TERMINATED'
         AGO     .ERREXIT
.*********************************************************************
.*                                                                   *
.*       BEGIN PROCESSING PARAMETERS FOR NORMAL FORM                 *
.*                                                                   *
.*********************************************************************
.NORMFM  ANOP                          BEGIN NORMAL FORM
&NORMAL  SETB    1                     NORMAL FORM REQUESTED
.*-------------------------------------------------------------------*
.*       THE FOLLOWING PARAMETERS WILL BE PUT INTO THE EVENT         *
.*       PARAMETER LIST IF SPECIFIED  (EXCEPT WHEN SPECIFIED         *
.*       IN REGISTER) :                                              *
.*          1. ACTION                                                *
.*          2. CODE                                                  *
.*          3. TYPE                                                  *
.*          4. EXIT                                                  *
.*          5. PARM                                                  *
.*          6. QMASK                                                 *
.*          7. ENFPFREE FOR FREEPRM                                  *
.*-------------------------------------------------------------------*
.*-------------------------------------------------------------------*
.*       PROCESS THE ACTION PARAMETER - NORMAL FORM                  *
.*-------------------------------------------------------------------*
         AIF     ('&ACTION' EQ 'SIGNAL').SIGACT0 ACTION=SIGNAL
         AIF     ('&ACTION' EQ 'DELETE').DELACT0 ACTION=DELETE
         AIF     ('&ACTION' EQ 'LISTEN').LISACT0 ACTION=LISTEN
         AIF     ('&ACTION' EQ '').NOACT0        ACTION NOT SPECIFIED
         MNOTE   12,'INVALID ACTION : ACTION=&ACTION-MACRO TERMINATED'
         AGO     .ERREXIT
.NOACT0  ANOP
         MNOTE   12,'ACTION REQUIRED - MACRO TERMINATED'
         AGO     .ERREXIT
.LISACT0 ANOP
&ACT     SETA    X'02'                 SET ACTION CODE TO LISTEN
         AGO     .ENDACT0
.SIGACT0 ANOP
&ACT     SETA    X'01'                 SET ACTION CODE TO SIGNAL
         AGO     .ENDACT0
.DELACT0 ANOP
&ACT     SETA    X'03'                 SET ACTION CODE TO DELETE
.ENDACT0 ANOP
&DONEACT SETB    1                     ACTION PROCESSING COMPLETED
.*-------------------------------------------------------------------*
.*       PROCESS THE CODE PARAMETER - NORMAL FORM (NOT REGISTER)     *
.*-------------------------------------------------------------------*
         AIF     ('&CODE' NE '').CHCODE MUST SPECIFY EVENT CODE
         MNOTE   08,'CODE PARAMETER REQUIRED'
         AGO     .DONECDE
.CHCODE  ANOP
         AIF     ('&CODE'(1,1) EQ '(').ENDCDE0  SKIP : CODE IN REG
&CDE     SETC    '&CODE'               SAVE FOR EVENT PARM LIST
.DONECDE ANOP
&DONECDE SETB    1                     NO NEED TO GENERATE CODE
.ENDCDE0 ANOP
.*-------------------------------------------------------------------*
.*       PROCESS THE TYPE PARAMETER - NORMAL FORM                    *
.*-------------------------------------------------------------------*
         AIF     ('&TYPE' EQ '').SYNC0        USE DEFAULT = SYNC
         AIF     ('&TYPE' EQ 'ASYNC').ASYNC0  TYPE=ASYNC
         AIF     ('&TYPE' NE 'SYNC').BADSYN0  EXPECT TYPE=SYNC
.SYNC0   ANOP
&FLG     SETA    X'00'+&FLG            SYNCHRONOUS CALLER
         AGO     .ENDTYPE
.ASYNC0  ANOP
&FLG     SETA    X'80'+&FLG            ASYNCHRONOUS CALLER
         AGO     .ENDTYPE
.BADSYN0 MNOTE   08,'INVALID TYPE : TYPE=&TYPE IGNORED'
         AGO     .SYNC0                USE DEFAULT : TYPE=SYNC
.ENDTYPE ANOP
&DONETYP SETB    1                     TYPE PROCESSING COMPLETED
.*-------------------------------------------------------------------*
.*       PROCESS THE EXIT PARAMETER - NORMAL FORM (NOT REGISTER)     *
.*-------------------------------------------------------------------*
         AIF     ('&EXIT' EQ '').NULXIT0
         AIF     ('&ACTION' EQ 'DELETE').BADXIT0
         AIF     ('&EXIT'(1,1) EQ '(').ENDXIT0 EXIT ADDR IN REG
&EXTADR  SETC    '&EXIT'               SAVE EXIT ROUTINE ADDR
         AGO     .DONEXIT
.NULXIT0 AIF     ('&ACTION' NE 'LISTEN').DONEXIT
         MNOTE   08,'EXIT REQUIRED FOR LISTEN REQUEST'
         AGO     .DONEXIT
.BADXIT0 MNOTE   04,'EXIT NOT ALLOWED FOR DELETE REQUEST - IGNORED'
.DONEXIT ANOP
&DONEXIT SETB    1                     NO NEED TO GENERATE CODE
.ENDXIT0 ANOP
.*-------------------------------------------------------------------*
.*       PROCESS THE PARM PARAMETER - NORMAL FORM (NOT REGISTER)     *
.*-------------------------------------------------------------------*
         AIF     ('&PARM' EQ '').DONEPRM
         AIF     ('&ACTION' EQ '' OR '&ACTION' EQ 'SIGNAL').SETPRM0
         MNOTE   04,'PARM NOT ALLOWED FOR &ACTION REQUEST - IGNORED'
         AGO     .DONEPRM
.SETPRM0 ANOP
         AIF     ('&PARM'(1,1) EQ '(').ENDPRM0
&PARMS   SETC    '&PARM'               SAVE USER PARM LIST
.DONEPRM ANOP
&DONEPRM SETB    1                     NO NEED TO GENERATE CODE
.ENDPRM0 ANOP
.*-------------------------------------------------------------------*
.*       PROCESS THE QMASK PARAMETER - NORMAL FORM                   *
.*-------------------------------------------------------------------*
         AIF     ('&QMASK' EQ '').ENDQMK0 QMASK NOT SPECIFIED
         AGO     .GETMASK              GET QMASK
.INITMSK ANOP                          QMASK SHOULD BE INITIALIZED
.ENDQMK0 ANOP
&DONEMSK SETB    1                     QMASK PROCESSING COMPLETED
.*-------------------------------------------------------------------*
.*       PROCESS THE FREEPRM PARAMETER - NORMAL FORM (SET ENFPFREE)  *
.*-------------------------------------------------------------------*
         AIF     ('&FREEPRM' EQ '').ENDFPN
         AIF     ('&ACTION' NE 'SIGNAL').BADFPM0
         AIF     ('&PARM' NE '').FREEPRO PROCESS FREEPRM AS IN LIST
.*                                     FORM. RETURN POINT = ENDFRPN
         MNOTE   08,'PARM REQUIRED WITH FREEPRM - FREEPRM IGNORED'
         AGO     .ENDFPN
.BADFPM0 MNOTE   04,'FREEPRM NOT ALLOWED FOR &ACTION REQUEST- IGNORED'
.ENDFPN  ANOP
&DONEFRP SETB    1                     ENFPFREE PROCESSING COMPLETED
.ENDFRPN ANOP                          END OF FREEPRM PROCESSING
.*-------------------------------------------------------------------*
.*    END PARAMETER LIST PROCESSING (NOT REGISTER) FOR NORMAL FORM   *
.*                                                                   *
.*-------------------------------------------------------------------*
         CNOP    0,4                   ALIGN ON HALFWORD BOUNDARY
&NAME    BAL     1,&GNAME.A            BRANCH AROUND PARM LIST
         AGO     .GENLIST              GO GENERATE PARM LIST
.*********************************************************************
.*                                                                   *
.*       MF=L SPECIFIED : BEGIN PARAMETER PROCESSING FOR LIST FORM   *
.*                                                                   *
.*********************************************************************
.LISTFM  ANOP                          BEGIN LIST FORM
&LIST    SETB    1                     LIST FORM REQUESTED
.*-------------------------------------------------------------------*
.*       PROCESS THE ACTION  PARAMETER - LIST FORM                   *
.*-------------------------------------------------------------------*
         AIF     ('&ACTION' EQ '').ENDACTN   ACTION NOT SPECIFIED
         AIF     ('&ACTION' EQ 'SIGNAL').SIGNAL
         AIF     ('&ACTION' EQ 'DELETE').DELETE
         AIF     ('&ACTION' EQ 'LISTEN').LISTEN
         MNOTE   12,'INVALID ACTION : ACTION=&ACTION-MACRO TERMINATED'
         AGO     .ERREXIT
.SIGNAL  ANOP
&ACT     SETA    X'01'                 ACTION=SIGNAL (ENFPSIG)
         AGO     .ENDACTN
.LISTEN  ANOP
&ACT     SETA    X'02'                 ACTION=LISTEN (ENFPLIS)
         AGO     .ENDACTN
.DELETE  ANOP
&ACT     SETA    X'03'                 ACTION=DELETE (ENFPDEL)
.ENDACTN ANOP
.*-------------------------------------------------------------------*
.*       PROCESS THE CODE PARAMETER - LIST FORM                      *
.*-------------------------------------------------------------------*
         AIF     ('&CODE' EQ '').CDEEND CODE NOT SPECIFIED
&CDE     SETC    '&CODE'               SAVE EVENT CODE
.CDEEND  ANOP
.*-------------------------------------------------------------------*
.*       PROCESS THE DTOKEN PARAMETER - LIST FORM                    *
.*-------------------------------------------------------------------*
         AIF     ('&DTOKEN' EQ '').ENDDTOK  DTOKEN NOT SPECIFIED
         MNOTE   04,'DTOKEN NOT ALLOWED IN LIST FORM - IGNORED'
.ENDDTOK ANOP
.*-------------------------------------------------------------------*
.*       PROCESS THE STOKEN PARAMETER - LIST FORM                    *
.*-------------------------------------------------------------------*
         AIF     ('&STOKEN' EQ '').ENDSTOK  STOKEN NOT SPECIFIED
         AIF     ('&ACTION' NE '' AND '&ACTION' NE 'SIGNAL').BADSTK1
&TOK     SETC    '&STOKEN'              SAVE STOKEN FOR LATER
         AGO     .ENDSTOK
.BADSTK1 MNOTE   04,'STOKEN NOT ALLOWED FOR &ACTION REQUEST - IGNORED'
.ENDSTOK ANOP
.*-------------------------------------------------------------------*
.*       PROCESS THE TYPE PARAMETER - LIST FORM                      *
.*-------------------------------------------------------------------*
         AIF     ('&TYPE' EQ '').ENDSYN TYPE NOT SPECIFIED
         AIF     ('&TYPE' EQ 'ASYNC').ASYN   TYPE=ASYNC SPECIFIED
         AIF     ('&TYPE' NE 'SYNC').BADSYN1 TYPE=SYNC  SPECIFIED
&FLG     SETA    X'00'+&FLG            SYNCHRONOUS CALLER
         AGO     .ENDSYN
.ASYN    ANOP
&FLG     SETA    X'80'+&FLG            ASYNCHRONOUS CALLER(ENFPASN)
         AGO     .ENDSYN
.BADSYN1 MNOTE   08,'INVALID TYPE : TYPE=&TYPE IGNORED'
.ENDSYN  ANOP
.*-------------------------------------------------------------------*
.*       PROCESS THE QUAL   PARAMETER  - LIST FORM                   *
.*-------------------------------------------------------------------*
         AIF     ('&QUAL' EQ '').ENDQUL  QUALIFIER NOT SPECIFIED
         AIF     ('&ACTION' EQ 'DELETE').BADQUL1
&QUALIF  SETC    '&QUAL'               SAVE QUALIFER FOR LATER
         AGO     .ENDQUL
.BADQUL1 MNOTE   04,'QUAL NOT ALLOWED FOR DELETE REQUEST - IGNORED'
.ENDQUL  ANOP
.*-------------------------------------------------------------------*
.*       PROCESS THE QMASK PARAMETER - (ALL FORMS)                   *
.*-------------------------------------------------------------------*
.GETMASK ANOP                          GENERATE QMASK
&ERRMASK SETB    1                     INITIALIZE ERRMASK
         AIF     ('&QMASK' EQ '').ENDQMSK QMASK NOT SPECIFIED
         AIF     ('&ACTION' NE 'LISTEN').BADMASK
.BEGINSL ANOP                          BEGIN PROCESS SUBLIST
&N       SETA    N'&QMASK              NUMBER OF ENTRIES
         AIF     (&N GT &MAXQMSK).BADMSK2 ALLOW UP TO &MAXQMSK VALUES
&I       SETA    1
.LOOP    AIF     (&I GT &N).ENDLOOP    ALL ENTRIES PROCESSED
         AIF     ('&QMASK(&I)' EQ 'ALL').ALL  QMASK=(ALL)
         AIF     ('&QMASK(&I)' EQ 'NONE').NOQMASK QMASK=(NONE)
         AIF     (K'&QMASK(&I) NE 5).BADMSK BYTEX EXPECTED
         AIF     ('&QMASK(&I)'(1,4) NE 'BYTE').BADMSK
&INX     SETC    '&QMASK(&I)'(5,1)     EXTRACT LAST DIGIT
         AIF     (('&INX' LT '1') OR ('&INX' GT '4')).BADMSK
         AIF     (&QBYTE(&INX) NE 0).DUPMSK
&QBYTE(&INX) SETB 1                    SET CORRESPONDING QBYTE
.NEXT    ANOP
&I       SETA    &I+1                  INCREMENT COUNTER
         AGO     .LOOP                 PROCESS ANOTHER ENTRY
.BADMSK  ANOP
         MNOTE   08,'INVALID QMASK VALUE : &QMASK(&I) IGNORED'
         AGO     .NEXT                 SKIP THIS BYTE
.DUPMSK  ANOP
         MNOTE   08,'DUPLICATE QMASK VALUE : &QMASK(&I)'
         AGO     .NEXT
.BADMASK ANOP
         AIF     ('&ACTION' EQ '' AND (NOT &NORMAL)).BEGINSL
.*                                     MAY SPECIFY IN LIST OR
.*                                     EXECUTE FORM
         MNOTE   04,'QMASK NOT ALLOWED FOR &ACTION REQUEST - IGNORED'
         AGO     .ENDQMSK              IGNORE THE QMASK
.BADMSK2 ANOP
         MNOTE   08,'MORE THAN &MAXQMSK QMASK VALUES - QMASK IGNORED'
         AGO     .ENDQMSK
.NOQMASK ANOP
         AIF     (&N EQ 1).SETNOQM
         MNOTE   08,'EXTRANEOUS QMASK INFO - QMASK=(NONE) ASSUMED'
.SETNOQM ANOP
&MSK     SETC    '00000000'            RESET MSK TO ZERO
         AGO     .RETMSK
.ALL     ANOP
         AIF     (&N EQ 1).SETALL
         MNOTE   08,'EXTRANEOUS QMASK INFO - QMASK=(ALL) ASSUMED'
.SETALL  ANOP
&MSK     SETC    '00001111'            SET MSK TO ALL BYTES
         AGO     .RETMSK
.ENDLOOP ANOP
&MSK     SETC    '0000'.'&QBYTE(1)'.'&QBYTE(2)'.'&QBYTE(3)'.'&QBYTE(4)'
.RETMSK  ANOP
&ERRMASK SETB    0                     NO ERROR FOUND
.ENDQMSK ANOP
.*
         AIF     (&EXECUTE).MVQMASK    RETURN TO EXECUTE FORM
         AIF     (&NORMAL).INITMSK     RETURN TO NORMAL FORM
.*-------------------------------------------------------------------*
.*       PROCESS THE EXIT PARAMETER - LIST FORM                      *
.*-------------------------------------------------------------------*
         AIF     ('&EXIT' EQ '').ENDXIT  EXIT NOT SPECIFIED
         AIF     ('&ACTION' EQ 'DELETE').BADXIT1
&EXTADR  SETC    '&EXIT'               SAVE EXIT ADDR FOR LATER
         AGO     .ENDXIT
.BADXIT1 MNOTE   04,'EXIT NOT ALLOWED FOR &ACTION REQUEST - IGNORED'
.ENDXIT  ANOP
.*-------------------------------------------------------------------*
.*       PROCESS THE FREEPRM PARAMETER - LIST & NORMAL (NO REG) FORM *
.*-------------------------------------------------------------------*
         AIF     ('&FREEPRM' EQ '').DONEFRP
         AIF     ('&ACTION' NE '' AND '&ACTION' NE 'SIGNAL').BADFPM1
.FREEPRO ANOP
&N       SETA    N'&FREEPRM            NO OF FREEPRM VALUES SPECIFIED
         AIF     (&N GT &MAXF).BADFRV0      TOO MANY FREEPRM VALUES
         AIF     (&N GE &MINF).SPPROC       CORRECT NO OF VALUES
         MNOTE   08,'SUBPOOL AND LENGTH REQUIRED - FREEPRM IGNORED'
         AGO     .DONEFRP
.BADFRV0 ANOP
         MNOTE   08,'MORE THAN &MAXF FREEPRM VALUES - FREEPRM IGNORED'
         AGO     .DONEFRP
.BADFPM1 MNOTE   04,'FREEPRM NOT ALLOWED FOR &ACTION REQUEST- IGNORED'
         AGO     .DONEFRP
.*  -------------------------------------------   *
.*       SUBPOOL = &FREEPRM(1)                    *
.*  -------------------------------------------   *
.SPPROC  ANOP                          BEGIN SUBPOOL PROCESSING
         AIF     ('&FREEPRM(1)' EQ '').MISSP0  MISSING SUBPOOL NUMBER
         AIF     ('&FREEPRM(1)'(1,1) NE '(').SETFRSP
         AIF     (&NORMAL).ENDSP       SUBPOOL VALUE IN REGISTER
         MNOTE   08,'SUBPOOL IN REGISTER NOT ALLOWED FOR LIST FORM'
         AGO     .DONEFRP
.MISSP0  ANOP
         MNOTE   08,'SUBPOOL REQUIRED IN FREEPRM - FREEPRM IGNORED'
         AGO     .DONEFRP
.SETFRSP ANOP                          SAVE SUBPOOL VALUE
&FREESP  SETC    '&FREEPRM(1)'
&DONESP  SETB    1                     NO NEED TO GENERATE CODE
.ENDSP   ANOP                          END OF SUBPOOL PROCESSING
.*  -------------------------------------------   *
.*       LENGTH = &FREEPRM(2)                     *
.*  -------------------------------------------   *
         AIF     ('&FREEPRM(2)' EQ '').MISLEN0
         AIF     ('&FREEPRM(2)'(1,1) NE '(').SETFLEN
         AIF     (&NORMAL).ENDFLEN     GENERATE CODE LATER ON
         MNOTE   08,'LENGTH IN REGISTER NOT ALLOWED FOR LIST FORM'
         AGO     .DONEFRP
.MISLEN0 ANOP
         MNOTE   08,'LENGTH REQUIRED IN FREEPRM - FREEPRM IGNORED'
         AGO     .DONEFRP
.SETFLEN ANOP                          SAVE LENGTH VALUE
&FREELEN SETC    '&FREEPRM(2)'
&DONELEN SETB    1                     NO NEED TO GENERATE CODE
.ENDFLEN ANOP                          END OF LENGTH PROCESSING
.*  -------------------------------------------   *
.*       KEY = &FREEPRM(3)                        *
.*  -------------------------------------------   *
         AIF     (&N NE 3).DEFKEY0     KEY NOT SPECIFIED
         AIF     ('&FREEPRM(3)' EQ '').DEFKEY0
         AIF     ('&FREEPRM(3)'(1,1) NE '(').SETFKEY
         AIF     (&NORMAL).ENDFKEY     KEY VALUE IN REGISTER
         MNOTE   08,'KEY IN REGISTER NOT ALLOWED FOR LIST FORM'
         AGO     .DONEFRP
.SETFKEY ANOP
         AIF     (T'&FREEPRM(3) NE 'N').GOODKEY   EQUATED VALUE
         AIF     (&FREEPRM(3) LE 15).GOODKEY      KEY = 0 TO 15
         MNOTE   08,'INVALID FREEPRM KEY VALUE - FREEPRM IGNORED'
&FREELEN SETC    '0'                   RESET LENGTH VALUE TO 0
&FREESP  SETC    '0'                   RESET SUBPOOL VALUE TO 0
         AGO     .DONEFRP
.DEFKEY0 ANOP
&FREEKEY SETC    '0'                   DEFAULT TO KEY 0
         AGO     .SETDKEY
.GOODKEY ANOP
&FREEKEY SETC    '&FREEPRM(3)'         SAVE KEY VALUE FOR EPL
.SETDKEY ANOP                          KEY IN EPL
&DONEKEY SETB    1                     KEY ATTRIBUTE PROCESSED
.ENDFKEY ANOP                          COMPLETE KEY PROCESSING
.*  -------------------------------------------   *
.*       SET ENFPFREE                             *
.*  -------------------------------------------   *
&FLG     SETA    &FLG+X'08'            FLAG (ENFPFREE) TO FREEMAIN
&DONEFFG SETB    1                     ENFPFREE IS SET UP
.*
         AIF     (NOT (&DONEKEY AND &DONESP AND &DONELEN)).ENDFREE
.DONEFRP ANOP
&DONEFRP SETB    1                     NO NEED TO GENERATE CODE
.ENDFREE ANOP
         AIF     (&NORMAL).ENDFRPN     RETURN TO NORMAL FORM
.*-------------------------------------------------------------------*
.*       PROCESS THE PARM PARAMETER - LIST FORM                      *
.*-------------------------------------------------------------------*
         AIF     ('&PARM' EQ '').ENDPRM
         AIF     ('&ACTION' NE '' AND '&ACTION' NE 'SIGNAL').BADPRM1
&PARMS   SETC    '&PARM'
         AGO     .ENDPRM
.BADPRM1 MNOTE   04,'PARM NOT ALLOWED FOR &ACTION REQUEST - IGNORED'
.ENDPRM  ANOP
.*********************************************************************
.*                                                                   *
.*       GENERATE PARAMETER LIST                                     *
.*                                                                   *
.*********************************************************************
.GENLIST ANOP
         AIF     (&STOPLOP).ENFBUG * DEBUG : BUG IN ENF
&STOPLOP SETB    1                 * DEBUG : SHOULD DO THIS ONLY ONCE
         AIF     (&NORMAL).EPLIST
&NAME    DS      0F                    START OF ENF PARAMETER LIST
.EPLIST  ANOP                          EVENT PARAMETER LIST
         DC      AL2(&LEN)             LENGTH OF ENF PARAMETER LIST
         DC      AL2(&ACT)             REQUESTED ENF ACTION
         DC      AL4(&CDE)             EVENT CODE
         DC      AL1(&FLG)             FLAG FIELD
         DC      AL1(B'&MSK')          MASK FOR COMPARING QUALIFIERS
         DC      AL1(&FREEKEY*16)      KEY FOR FREEPRM
         DC      AL1(&FREESP)          SUBPOOL FOR FREEPRM
         DC      AL4(&QUALIF)          QUALIFIER
         DC      AL4(&EXTADR)          EXIT ROUTINE ADDRESS
         DC      AL4(&PARMS)           ADDRESS OF SIGNALLERS PARAMETER
         DC      AL4(&TOK)             TOKEN
         DC      AL4(&FREELEN)         LENGTH FOR FREEPRM
         AIF     (&LIST).NORMEND       LIST FORM PROCESSING COMPLETED
&GNAME.A DS      0H                    END OF ENF PARAMETER LIST
         AGO     .PARMCHK              CHECK THE OTHER PARAMETERS
.*                                     FOR NORMAL FORM
.*********************************************************************
.*                                                                   *
.*       MF = (E,PARMLIST) SPECIFIED : PARAMETER PROCESSING FOR      *
.*                                     EXECUTE FORM                  *
.*                                                                   *
.*********************************************************************
.EXECFM  ANOP                          BEGIN EXECUTE FORM
&EXECUTE SETB    1                     EXECUTE FORM REQUESTED
         AIF     ('&NAME' EQ '').SKIPLB NO LABEL SPECIFIED
&NAME    DS      0H
.SKIPLB  ANOP
         AIF     ('&MF(2)' EQ '(1)').ENDLDPM    ADDR IN REG 1
         AIF     ('&MF(2)'(1,1) EQ '(').LOADREG ADDR IN REGISTER
         AIF     (T'&MF(2) EQ 'N').BADMF1
         LA      1,&MF(2)              LOAD ADDR OF PARM LIST
         AGO     .ENDLDPM
.LOADREG ANOP
&REGC    SETC    '&MF(2)'(2,K'&MF(2)-2) EXTRACT REG FROM SUBLIST
         LR      1,&REGC               MOVE ADDR INTO REG 1
.ENDLDPM ANOP                          PARM ADDR NOW IN REG 1
.*********************************************************************
.*                                                                   *
.*       CODE GENERATION FOR EXECUTE FORM OR NORMAL FORM (WHEN       *
.*       PARAMETERS ARE SPECIFIED IN REGISTERS, AND FOR STOKEN,      *
.*       DTOKEN, QUAL AND FREEPRM PARAMETER PROCESSING)              *
.*                                                                   *
.*********************************************************************
.PARMCHK ANOP
.*-------------------------------------------------------------------*
.*       PROCESS THE ACTION PARAMETER - EXECUTE FORM                 *
.*-------------------------------------------------------------------*
         AIF     (&DONEACT).ENDACT     ACTION IN PARM LIST ALREADY
         AIF     ('&ACTION' EQ '').ENDACT        ACTION NOT SPECIFIED
         AIF     ('&ACTION' EQ 'SIGNAL').SIGACT  ACTION=SIGNAL
         AIF     ('&ACTION' EQ 'DELETE').DELACT  ACTION=DELETE
         AIF     ('&ACTION' EQ 'LISTEN').LISACT  ACTION=LISTEN
         MNOTE   12,'INVALID ACTION : ACTION=&ACTION-MACRO TERMINATED'
         AGO     .ERREXIT
.LISACT  ANOP
         MVI     ENFPACT2,ENFPLIS      SET ACTION CODE TO LISTEN
         AGO     .ENDACT
.SIGACT  ANOP
         MVI     ENFPACT2,ENFPSIG      SET ACTION CODE TO SIGNAL
         AGO     .ENDACT
.DELACT  ANOP
         MVI     ENFPACT2,ENFPDEL      SET ACTION CODE TO DELETE
.ENDACT  ANOP
.*-------------------------------------------------------------------*
.*       PROCESS THE CODE PARAMETER - NORMAL(REG) OR EXECUTE FORM    *
.*-------------------------------------------------------------------*
         AIF     (&DONECDE).ENDCODE    ALREADY DONE EARLIER
         AIF     ('&CODE' EQ '').ENDCODE
         AIF     ('&CODE'(1,1) EQ '(').SETCDER  CODE IN REGISTER
         LA      14,&CODE              EVENT CODE INTO REG 14
         ST      14,ENFPCODE           STORE INTO ENF PARM LIST
         AGO     .ENDCODE
.LDADR   ANOP
&REGC    SETC    '&CODE'(3,K'&CODE-4)  EXTRACT REGISTER NUMBER
         MVC     ENFPCODE,0(&REGC)     MOVE EVENT CODE FROM REG
         AGO     .ENDCODE
.SETCDER ANOP
         AIF     ('&CODE'(1,2) EQ '((').LDADR  ADDR OF CODE IN REG
&REGC    SETC    '&CODE(1)'            SAVE REGISTER NUMBER
         ST      &REGC,ENFPCODE        MOVE EVENT CODE FROM REG
.ENDCODE ANOP
.*-------------------------------------------------------------------*
.*       PROCESS THE DTOKEN PARAMETER - NORMAL OR EXECUTE FORM       *
.*-------------------------------------------------------------------*
         AIF     ('&DTOKEN' NE '').DTOKN DTOKEN IS SPECIFIED
         AIF     (&EXECUTE).ENDDTKN    DTOKEN SPECIFIED IN LIST FORM
         AIF     ('&ACTION' NE 'DELETE').ENDDTKN
         MNOTE   08,'DTOKEN REQUIRED FOR DELETE REQUEST'
         AGO     .ENDDTKN
.DTOKN   ANOP
         AIF     ('&ACTION' EQ '').MISACTD ACTION NOT SPECIFIED    @P2A
         AIF     ('&ACTION' EQ 'SIGNAL').BADDTK2
         AIF     ('&ACTION' EQ 'LISTEN').ENDDTKN RESTORE DTOKEN AFTER
.*                                               RETURN FROM ENF
         AIF     ('&DTOKEN'(1,1) EQ '(').SETTKNR DTOKEN IN REG
         MVC     ENFPTOK,&DTOKEN       DTOKEN
         AGO     .ENDDTKN
.BADDTK2 MNOTE   04,'DTOKEN NOT ALLOWED FOR SIGNAL REQUEST - IGNORED'
         AGO     .ENDDTKN
.MISACTD MNOTE   08,'ACTION REQUIRED WITH DTOKEN - DTOKEN IGNORED' @P2A
         AGO     .ENDDTKN                                          @P2A
.SETTKNR ANOP
&REGC    SETC    '&DTOKEN(1)'          EXTRACT REGISTER NUMBER
         ST      &REGC,ENFPTOK         MOVE DTOKEN FROM REG
.ENDDTKN ANOP
.*-------------------------------------------------------------------*
.*       PROCESS THE STOKEN PARAMETER - NORMAL OR EXECUTE FORM       *
.*-------------------------------------------------------------------*
         AIF     ('&STOKEN' EQ '').ENDSTKN
         AIF     ('&ACTION' NE '' AND '&ACTION' NE 'SIGNAL').BADSTK2
         AIF     ('&STOKEN'(1,1) EQ '(').STOKR
         MVC     ENFPTOK,&STOKEN       STOKEN
         AGO     .ENDSTKN
.BADSTK2 ANOP
         MNOTE   04,'STOKEN NOT ALLOWED FOR &ACTION REQUEST - IGNORED'
         AGO     .ENDSTKN
.STOKR   ANOP
&REGC    SETC    '&STOKEN(1)'          EXTRACT REG NUMBER
         ST      &REGC,ENFPTOK         STORE STOKEN FROM REG
.ENDSTKN ANOP
.*-------------------------------------------------------------------*
.*       PROCESS THE TYPE PARAMETER - EXECUTE FORM                   *
.*-------------------------------------------------------------------*
         AIF     (&DONETYP).ENDSYNC   TYPE IN PARM LIST ALREADY
         AIF     ('&TYPE' EQ '').ENDSYNC      TYPE NOT SPECIFIED
         AIF     ('&TYPE' EQ 'ASYNC').ASYNC   TYPE=ASYNC
         AIF     ('&TYPE' NE 'SYNC').BADSYN2  EXPECT TYPE=SYNC
         NI      ENFPFLG,255-ENFPASN   SYNCHRONOUS CALLER
         AGO     .ENDSYNC
.ASYNC   OI      ENFPFLG,ENFPASN       ASYNCHRONOUS CALLER
         AGO     .ENDSYNC
.BADSYN2 MNOTE   08,'INVALID TYPE : TYPE=&TYPE IGNORED'
.ENDSYNC ANOP
.*-------------------------------------------------------------------*
.*       PROCESS THE QUAL PARAMETER -  NORMAL OR EXECUTE FORM        *
.*-------------------------------------------------------------------*
         AIF     ('&QUAL' EQ '').ENDQUAL
         AIF     ('&ACTION' EQ 'DELETE').BADQUL2
         AIF     ('&QUAL'(1,1) EQ '(').SETQULR  QUALIFIER IN REG
         MVC     ENFPQUAL,&QUAL        QUALIFIER
         AGO     .ENDQUAL
.SETQULR ANOP
&REGC    SETC    '&QUAL(1)'            GET REGISTER NUMBER
         ST      &REGC,ENFPQUAL        MOVE QUALIFIER FROM REG
         AGO     .ENDQUAL
.BADQUL2 MNOTE   04,'QUAL NOT ALLOWED FOR DELETE REQUEST - IGNORED'
.ENDQUAL ANOP
.*-------------------------------------------------------------------*
.*       PROCESS THE QMASK PARAMETER - EXECUTE FORM                  *
.*-------------------------------------------------------------------*
         AIF     (&DONEMSK).ENDQMK     ALREADY PUT IN PARM LIST
         AIF     ('&QMASK' EQ '').ENDQMK  QMASK NOT SPECIFIED
         AGO     .GETMASK              GET QMASK
.MVQMASK ANOP
         AIF     (&ERRMASK).ENDQMK     ERROR : DONT GENERATE CODE
         MVI     ENFPQMSK,B'&MSK'      SET UP QMASK
.ENDQMK  ANOP
.*-------------------------------------------------------------------*
.*       PROCESS THE EXIT PARAMETER - NORMAL(REG) OR EXECUTE FORM    *
.*-------------------------------------------------------------------*
         AIF     (&DONEXIT).ENDEXIT    EXIT IN PARM LIST ALREADY
         AIF     ('&EXIT' EQ '').ENDEXIT
         AIF     ('&ACTION' EQ 'DELETE').BADXIT2
         AIF     ('&EXIT'(1,1) EQ '(').SETXITR EXIT ADDR IN REG
         LA      15,&EXIT              LOAD EXIT ROUTINE ADDR
         ST      15,ENFPEADR           SAVE EXIT ROUTINE ADDR
         AGO     .ENDEXIT
.SETXITR ANOP
&REGC    SETC    '&EXIT(1)'            GET REGISTER NUMBER
         ST      &REGC,ENFPEADR        SAVE EXIT ROUTINE ADDR
         AGO     .ENDEXIT
.BADXIT2 MNOTE   04,'EXIT NOT ALLOWED FOR &ACTION REQUEST - IGNORED'
.ENDEXIT ANOP
.*-------------------------------------------------------------------*
.*       PROCESS THE FREEPRM PARAMETER - NORMAL (REG) & EXECUTE FORM *
.*-------------------------------------------------------------------*
         AIF     (&DONEFRP).ENDFRPE    FREEPRM ALREADY PROCESSED
         AIF     ('&FREEPRM' EQ '').ENDFRPE NOT SPECIFIED IN EXEC FORM
         AIF     ('&ACTION' NE '' AND '&ACTION' NE 'SIGNAL').BADFPM2
&N       SETA    N'&FREEPRM            NO OF FREEPRM VALUES SPECIFIED
         AIF     (&N GT &MAXF).BADFRV1      TOO MANY FREEPRM VALUES
         AIF     (&N GE &MINF).SPPROC1      CORRECT NO OF VALUES
         MNOTE   08,'SUBPOOL AND LENGTH REQUIRED - FREEPRM IGNORED'
         AGO     .ENDFRPE
.BADFRV1 ANOP
         MNOTE   08,'MORE THAN &MAXF FREEPRM VALUES - FREEPRM IGNORED'
         AGO     .ENDFRPE
.BADFPM2 MNOTE   04,'FREEPRM NOT ALLOWED FOR &ACTION REQUEST- IGNORED'
         AGO     .ENDFRPE
.*  -------------------------------------------   *
.*       SUBPOOL = &FREEPRM(1)                    *
.*  -------------------------------------------   *
.SPPROC1 ANOP                          BEGIN SUBPOOL PROCESSING
         AIF     (&DONESP).ENDSP1
         AIF     ('&FREEPRM(1)' EQ '').MISSP1
         AIF     ('&FREEPRM(1)'(1,1) NE '(').SETFRS1
&REGC    SETC    '&FREEPRM(1)'(2,K'&FREEPRM(1)-2)
         STC     &REGC,ENFPFSPL        SAVE SUBPOOL NUMBER
         AGO     .ENDSP1
.MISSP1  ANOP
         MNOTE   08,'SUBPOOL REQUIRED IN FREEPRM - FREEPRM IGNORED'
         AGO     .ENDFRPE
.SETFRS1 ANOP                          SAVE SUBPOOL VALUE
         LA      15,&FREEPRM(1)        GET SUBPOOL NUMBER
         STC     15,ENFPFSPL           SAVE SUBPOOL NUMBER
.ENDSP1  ANOP                          END OF SUBPOOL PROCESSING
.*  -------------------------------------------   *
.*       LENGTH = &FREEPRM(2)                     *
.*  -------------------------------------------   *
         AIF     (&DONELEN).ENDFLN1
         AIF     ('&FREEPRM(2)' EQ '').MISLEN1
         AIF     ('&FREEPRM(2)'(1,1) NE '(').SETFLN1
&REGC    SETC    '&FREEPRM(2)'(2,K'&FREEPRM(2)-2)
         ST      &REGC,ENFPFLEN        SAVE LENGTH VALUE
         AGO     .ENDFLN1
.MISLEN1 ANOP
         MNOTE   08,'LENGTH REQUIRED IN FREEPRM - FREEPRM IGNORED'
         AGO     .ENDFRPE
.SETFLN1 ANOP                          SAVE LENGTH VALUE
         AIF     (T'&FREEPRM(2) EQ 'N').NUMLEN
         B       &GNAME.C              BRANCH AROUND LENGTH VALUE
&GNAME.B DC      A(&FREEPRM(2))        DEFINE LENGTH VALUE
&GNAME.C MVC     ENFPFLEN,&GNAME.B     MOVE LENGTH VALUE
         AGO     .ENDFLN1
.NUMLEN  ANOP                          LENGTH VALUE IS A CONSTANT
         AIF     (&FREEPRM(2) LT 4096).MVABSL
         MVC     ENFPFLEN,=F'&FREEPRM(2)' MOVE LENGTH VALUE
         AGO     .ENDFLN1
.MVABSL  ANOP
         LA      15,&FREEPRM(2)        LOAD LENGTH VALUE
         ST      15,ENFPFLEN           SAVE LENGTH VALUE
.ENDFLN1 ANOP                          END OF LENGTH VALUE PROCESSING
.*  -------------------------------------------   *
.*       KEY = &FREEPRM(3)                        *
.*  -------------------------------------------   *
         AIF     (&DONEKEY).ENDFKY1
         AIF     (&N NE 3).DEFKEY1     KEY NOT SPECIFIED
         AIF     ('&FREEPRM(3)' EQ '').DEFKEY1
         AIF     ('&FREEPRM(3)'(1,1) NE '(').SETFKY1
&REGC    SETC    '&FREEPRM(3)'(2,K'&FREEPRM(3)-2)
         STC     &REGC,ENFPFKEY        SAVE KEY NUMBER
         AGO     .ENDFKY1
.SETFKY1 ANOP
         AIF     (T'&FREEPRM(3) NE 'N').GOODKY1 EQUATED VALUE
         AIF     (&FREEPRM(3) LE 15).GOODKY1     KEY= 0 TO 15
         MNOTE   08,'INVALID KEY VALUE &FREEPRM(3), FREEPRM IGNORED'
         AGO     .ENDFRPE
.DEFKEY1 ANOP
         MVI     ENFPFKEY,X'00'        DEFAULT TO KEY 0
         AGO     .ENDFKY1
.GOODKY1 ANOP
         LA      15,&FREEPRM(3)        LOAD STORAGE KEY
         SLL     15,4(0)               LOAD STORAGE KEY
         STC     15,ENFPFKEY           SAVE STORAGE KEY FOR FREEMAIN
.ENDFKY1 ANOP                          COMPLETE KEY PROCESSING
.*  -------------------------------------------   *
.*       SET ENFPFREE                             *
.*  -------------------------------------------   *
         AIF     (&DONEFFG).ENDFRPE    ENFPFREE ALREADY SET UP
         OI      ENFPFLG,ENFPFREE      FREE SIGNAL PARAM LIST
.*
.ENDFRPE ANOP
.*-------------------------------------------------------------------*
.*       PROCESS THE PARM PARAMETER - NORMAL(REG) OR EXECUTE FORM    *
.*-------------------------------------------------------------------*
         AIF     (&DONEPRM).ENDPARM   PARM ALREADY IN PARM LIST
         AIF     ('&PARM' EQ '').ENDPARM
         AIF     ('&ACTION' EQ '' OR '&ACTION' EQ 'SIGNAL').SETPARM
.BADPRM2 MNOTE   04,'PARM NOT ALLOWED FOR &ACTION REQUEST - IGNORED'
         AGO     .ENDPARM
.SETPARM ANOP
         AIF     ('&PARM'(1,1) EQ '(').SETPRMR
         LA      15,&PARM              GET PTR TO USER PARM LIST
         ST      15,ENFPSPRM           SAVE PTR TO USER PARM LIST
         AGO     .ENDPARM
.SETPRMR ANOP
&REGC    SETC    '&PARM(1)'
         ST      &REGC,ENFPSPRM        PUT PARM INTO ENF PARM LIST
.ENDPARM ANOP
.*-------------------------------------------------------------------*
.*       ALL SPECIFIED PARAMETERS ARE PROCESSED. CALL IEFENFFX       *
.*-------------------------------------------------------------------*
         ST      1,ENFPTR              STORE ADDR OF PARM LIST
         LA      1,ENFPTR              LOAD ADDR OF ENFPTR
         L       15,CVTPTR             ADDRESS CHAINING : CVT
         USING   CVT,15                ADDRESSIBILITY TO CVT
         L       15,CVTENFCT           ADDRESS OF ENFCT
         DROP    15                    RELEASE CVT BASE REGISTER
         USING   ENFCT,15              ADDRESSIBILITY TO ENFCT
         L       15,ENFCFMOD           ADDRESS OF ENFFX
         DROP    15                    RELEASE ENFCT BASE REGISTER
         BALR    14,15                 INVOKE IEFENFFX
.*------------------------------------------------------------------*
.*       RETURN TOKEN TO LISTENER - NORMAL OR EXECUTE FORM          *
.*------------------------------------------------------------------*
         AIF     ('&ACTION' NE 'LISTEN').NRSSTKN
         AIF     ('&DTOKEN' EQ '').NRSSTKN
         AIF     ('&DTOKEN'(1,1) EQ '(').RSTTOKR
         AIF     ('&TYPE' EQ 'ASYNC').NRSSTKN
         L       1,ENFPTR              RESTORE POINTER TO EPL
         MVC     &DTOKEN.(4),ENFPTOK   SAVE TOKEN FOR DELETE
         AGO     .NRSSTKN
.RSTTOKR ANOP
&REGC    SETC    '&DTOKEN(1)'          GET REG NUMBER
         L       1,ENFPTR              RESTORE POINTER TO EPL
         L       &REGC,ENFPTOK         RETURN TOKEN IN REG
.NRSSTKN ANOP                          END OF RESTORING DTOKEN
         AGO     .END                  NORMAL TERMINATION
.*------------------------------------------------------------------*
.*       ABNORMAL TERMINATION                                       *
.*------------------------------------------------------------------*
.ENFBUG  ANOP
         MNOTE   12,'ERROR IN ENFREQ MACRO - MACRO TERMINATED'
.ERREXIT ANOP
.*------------------------------------------------------------------*
.*       NORMAL TERMINATION                                         *
.*------------------------------------------------------------------*
.END     ANOP
         AIF     (&LIST).NORMEND       NO USING DONE
         DROP    1                     RELEASE BASE REGISTER
.NORMEND ANOP                          NORMAL TERMINATION (LIST FORM)
         MEND
* */ ENFREQ : MACRO
*             KEYS(ACTION,CODE,TYPE,QUAL,QMASK,EXIT,PARM,
*                  DTOKEN,STOKEN,FREEPRM,MF,RELATED,RETCODE); /* @P1A*/
*             ANS('?'MACLABEL'ENFREQP 'MACLISTMACKEYS';');
*   %END ENFREQ;
