* %/*******************************************************************
*                                                                     *
*01* MACRO-NAME = CMSET                                               *
*                                                                     *
*01* DESCRIPTIVE-NAME = CROSS MEMORY SET                              *
*                                                                     *
*01* COPYRIGHT: 5740-XYN COPYRIGHT IBM CORP. 1980,                    *
*               LICENSED MATERIAL PROGRAM, PROPERTY OF IBM,           *
*               REFER TO COPYRIGHT INSTRUCTIONS FORM NUMBER           *
*               G120-2083.                                            *
*                                                                     *
*01* STATUS = OS/VS2 JBB1226                                          *
*                                                                     *
*01* FUNCTION = TO ESTABLISH ADDRESSABILTY TO A SPECIFIED ADDRESS     *
*               SPACE AND TO RETURN TO THE ORIGINAL ADDRESS SPACE.    *
*                                                                     *
*01* NOTES = PROGRAMS USING CMSET MUST INCLUDE THE IHAPSA AND         *
*            IHASVT MAPPING MACROS.                                   *
*            THE PLS VERSION REQUIRES THE EPILOG MACRO.               *
*                                                                     *
*01* MODULE-TYPE = MACRO                                              *
*                                                                     *
*02* PROCESSOR = ASSEMBLER/PLS (BILINGUAL)                            *
*                                                                     *
*02* MACRO SIZE = 450 LOC                                             *
*                                                                     *
*01* MACROS = IHBERMAC                                                *
*                                                                     *
*01* CHANGE-ACTIVITY = @G38DPXT - CROSS MEMORY SUPPORT                *
*                                                                     *
*01* MNOTES = SEE THE MNOTES SECTION OF THE MACRO FOR LIST            *
*              OF MNOTES ISSUED                                       *
*                                                                     *
*01* PLS MESSAGES = SEE PLS SECTION OF CODE FOR MESSAGES ISSUED       *
*                                                                     *
***********************************************************************
* A-000000-999999                                              @G38DPXT
         MACRO
&LABEL   CMSET &REQUEST,&ASCB=,&XMSAVE=,&CHKAUTH=,                     X
               &CHKICMA=YES,&RELATED=
.*
.*
*        MACDATE = 06/03/80
.*
         AIF   (T'&LABEL EQ 'O').NOLABEL   CHECK IF A LABEL WAS
.*                                     GIVEN, IF YES GENERATE LABEL
&LABEL   EQU   *                       .BRANCH LABEL
.*
.NOLABEL ANOP
.*
         AIF   (T'&REQUEST EQ 'O').MNNREQ   IF REQUEST NOT SPECIFIED
.*                                     THEN ISSUE MNOTE AND EXIT
         AIF   (N'&SYSLIST GT 1).MNEXTRA   IF MORE THAN ONE POSITIONAL
.*                                     PARAMETER SPECIFIED THEN ISSUE
.*                                     MNOTE AND EXIT
         AIF   ('&REQUEST' EQ 'SET').SET   IF REQUEST IS 'SET' GO TO
.*                                     SET SECTION OF MACRO
         AIF   ('&REQUEST' EQ 'RESET').RESET   IF REQUEST IS 'RESET'
.*                                     GO TO RESET SECTION OF MACRO
         AIF   ('&REQUEST' EQ 'SSARTO').SSARTO IF REQUEST IS 'SSARTO'
.*                                     GO TO SSARTO SECTION OF MACRO
         AIF   ('&REQUEST' EQ 'SSARBACK').SSARBCK   IF REQUEST IS
.*                                     'SSARBACK' GO TO SSARBCK
         AGO   .MNINVR                 REQUEST NOT VALID, GO ISSUE
.*                                     MNOTE AND EXIT
.*
.**********************************************************************
.*                                                                    *
.*   SET:  THIS SECTION OF THE MACRO PERFORMS THE VALIDITY CHECKING   *
.*       AND CODE GENERATION FOR THE 'SET' OPTION                     *
.*                                                                    *
.**********************************************************************
.*
.SET     ANOP
         AIF   (T'&ASCB EQ 'O').MNNASCB ASCB KEYWORD REQUIRED ON SET,
.*                                     ISSUE MNOTE AND EXIT
         AIF   ('&CHKICMA' NE 'YES' AND '&CHKICMA' NE 'NO').MNICMA
.*                                     IF CHKICMA NOT EQUAL TO 'YES'
.*                                     OR 'NO' THEN ISSUE MNOTE AND
.*                                     EXIT
         AIF   (T'&CHKAUTH NE 'O').MNAUTHI  IF CHKAUTH IS SPECIFIED
.*                                     ISSUE MNOTE AND EXIT
.*
         AIF   ('&ASCB'(1,1) EQ '(').SETAREG   IF FIRST CHARACTER OF
.*                                     ASCB OPERAND IS AN OPEN PAREN
.*                                     THEN ASCB ADDRESS IS ALREADY
.*                                     IN A REGISTER, BRANCH TO HANDLE
         L     1,&ASCB                 .LOAD ASCB ADDRESS INTO R1
         AGO   .SETCSAV                GO CHECK THE SAVE AREA OPERAND
.*
.SETAREG ANOP                          ASCB ADDRESS IS IN A REGISTER
         AIF   (N'&ASCB NE 1).MNASCBI  IF NUMBER OF ELEMENTS IN ASCB
.*                                     SUBLIST IS NOT ONE, ISSUE AN
.*                                     MNOTE AND EXIT
         AIF   ('&ASCB(1)' EQ '1').SETCSAV  IF ASCB ADDRESS IS ALREADY
.*                                     IN R1 THEN GO CHECK SAVE AREA
         LR    1,&ASCB(1)              .LOAD ASCB ADDRESS INTO R1
.*
.SETCSAV ANOP                          CHECK THE SAVE AREA OPERAND
         AIF   (T'&XMSAVE NE 'O').SETSAVE   IF SAVE AREA IS SPECIFIED
.*                                     GO CHECK IT
         SLR   0,0                     .INDICATE NO XM SAVE AREA
         AGO   .SETBALR                GO GENERATE LINKAGE CODE
.*
.SETSAVE ANOP
         AIF   ('&XMSAVE'(1,1) EQ '(').SETRSAV   IF FIRST CHARACTER OF
.*                                     XMSAVE OPERAND IS AN OPEN PAREN
.*                                     THEN XMSAVE ADDRESS IS ALREADY
.*                                     IN A REGISTER, BRANCH TO HANDLE
         LA    0,&XMSAVE               .GET XMSAVE ADDRESS IN R0
         AGO   .SETBALR                GO GENERATE LINKAGE CODE
.*
.SETRSAV ANOP                          XMSAVE ADDRESS IS IN A REGISTER
         AIF   (N'&XMSAVE NE 1).MNSAVI IF NUMBER OF ELEMENTS IN XMSAVE
.*                                     SUBLIST IS NOT ONE, THEN ISSUE
.*                                     MNOTE AND EXIT
         AIF   ('&XMSAVE(1)' EQ '0').SETBALR   IF XMSAVE ADDRESS IS
.*                                     IN R0 THEN GO GENERATE LINKAGE
         LR    0,&XMSAVE(1)            .GET XMSAVE ADDRESS IN R0
.*
.SETBALR ANOP                          ENTRY LINKAGE CODE
         L     15,PSASVT-PSA(,0)       .SVT ADDRESS
.*
.**********************************************************************
.*                                                                    *
.*  THE HIGH ORDER BIT OF THE REGISTER 1 IS USED TO INDICATE          *
.*  WHETHER OR NOT THE ICMA INDICATOR IS TO BE CHECKED.               *
.*  IF BIT IS ON THEN ICMA IS TO BE CHECKED.  A MASK IN THE           *
.*  SVT IS USED TO SET THIS BIT INSTEAD OF AN INLINE MASK             *
.*  BECAUSE OF PERFORMANCE, DO NOT WANT TO BRANCH AROUND THE MASK.    *
.*                                                                    *
.**********************************************************************
.*
         AIF   ('&CHKICMA' EQ 'NO').SETNCHK
         O     1,SVTCMCKM-SVT(,15)     .ICMA IS TO BE CHECKED
         AGO   .SETGO                  GO FINISH LINKAGE CODE
.*
.SETNCHK ANOP
         LA    1,0(0,1)                .ICMA IS NOT TO BE CHECKED
.*
.SETGO   ANOP
         L     15,SVTCMST1-SVT(,15)    .CMSET SET EPA
         BALR  14,15                   .CALL THE CMSET ROUTINE
         MEXIT
.*
.**********************************************************************
.*                                                                    *
.*   RESET:  THIS SECTION OF THE MACRO PERFORMS THE VALIDITY CHECKING *
.*       AND CODE GENERATION FOR THE 'RESET' OPTION                   *
.*                                                                    *
.**********************************************************************
.*
.RESET   ANOP
         AIF   (T'&XMSAVE EQ 'O').MNNXSAV   IF XMSAVE NOT SPECIFIED
.*                                     ISSUE MNOTE AND EXIT
         AIF   (T'&ASCB NE 'O').MNASCB   IF ASCB SPECIFIED ISSUE MNOTE
         AIF   ('&CHKICMA' NE 'YES').MNCHKI IF CHKICMA IS NOT THE
.*                                     DEFAULT ISSUE MNOTE AND EXIT
         AIF   (T'&CHKAUTH EQ 'O').MNNAUTH IF CHKAUTH IS NOT SPECIFIED
.*                                     THEN ISSUE MNOTE AND EXIT
         AIF   ('&CHKAUTH' NE 'YES' AND '&CHKAUTH' NE 'NO').MNAUTH
.*                                     IF CHKAUTH IS NOT 'YES' OR 'NO'
.*                                     THEN ISSUE MNOTE AND EXIT
.*
         AIF   ('&XMSAVE'(1,1) EQ '(').RERSAV   IF SAVE AREA ADDRESS
.*                                     ALREADY IN A REGISTER BRANCH
         LA    1,&XMSAVE               .GET XMSAVE ADDRESS IN R1
         AGO   .REBALR
.RERSAV  ANOP
         AIF   (N'&XMSAVE NE 1).MNSAVI IF NUMBER OF ELEMENTS IN
.*                                     XMSAVE SUBLIST IS NOT ONE THEN
.*                                     ISSUE MNOTE AND EXIT
         AIF   ('&XMSAVE(1)' EQ '1').REBALR    IF SAVE AREA ADDRESS
.*                                     ALREADY IN R1 THEN BRANCH
         LR    1,&XMSAVE(1)            .GET XMSAVE ADDRESS IN R1
.*
.REBALR  ANOP
         L     15,PSASVT-PSA(,0)       .SVT ADDRESS
         AIF   ('&CHKAUTH' EQ 'NO').RENAUTH   IF NO AUTHORIZATION TEST
.*                                     THEN BRANCH
         L     15,SVTCMRT1-SVT(,15)    .CMSET RESET/AUTH CHECK EPA
         AGO   .REGO
.RENAUTH ANOP
         L     15,SVTCMRT2-SVT(,15)    .CMSET RESET/NO AUTH CHECK EPA
.REGO    ANOP
         BALR  14,15                   .CALL CMSET ROUTINE
         MEXIT
.*
.**********************************************************************
.*                                                                    *
.*   SSARTO:  THIS SECTION OF THE MACRO PERFORMS THE VALIDITY         *
.*       CHECKING AND CODE GENERATION FOR THE 'SSARTO' OPTION         *
.*                                                                    *
.**********************************************************************
.*
.SSARTO  ANOP
         AIF   (T'&RELATED EQ 'O').MNNREL   IF RELATED OPERAND NOT
.*                                     SPECIFIED, ISSUE MNOTE AND EXIT
         AIF   (T'&ASCB EQ 'O').MNNASCB     IF NO ASCB ADDRESS IS
.*                                     SPECIFIED ISSUE MNOTE AND EXIT
         AIF   (T'&XMSAVE NE 'O').MNXMSAV   IF XMSAVE IS SPECIFIED
.*                                     THEN ISSUE MNOTE AND EXIT
         AIF   ('&CHKICMA' NE 'YES').MNCHKI IF CHKICMA IS NOT DEFAULT
.*                                     VALUE ISSUE MNOTE AND EXIT
         AIF   (T'&CHKAUTH NE 'O').MNAUTHI IF CHKAUTH IS SPECIFIED
.*                                     VALUE ISSUE MNOTE AND EXIT
.*
         AIF   ('&ASCB'(1,1) EQ '(').TOREGA   IS THE ASCB ADDRESS
.*                                     ALREADY IN A REGISTER, IF IT
.*                                     IS THEN BRANCH
         L     1,&ASCB                 .LOAD ASCB ADDRESS INTO R1
         AGO   .TOBALR
.TOREGA  ANOP
         AIF   (N'&ASCB NE 1).MNASCBI  IF MORE THAN ONE SUBLIST
.*                                     ELEMENT ISSUE MNOTE AND EXIT
         AIF   ('&ASCB(1)' EQ '1').TOBALR  IF ALREADY IN REG ONE
.*                                     DO NOT LOAD IT INTO REG ONE
         LR    1,&ASCB(1)              .LOAD ASCB ADDRESS INTO R1
.*
.TOBALR  ANOP
         L     15,PSASVT-PSA(,0)       .SVT ADDRESS
         L     15,SVTCMSTR-SVT(,15)    .CMSET SSARTO EPA
         BALR  14,15                   .CALL CMSET ROUTINE
*
*        REGISTER ONE NOW CONTAINS A TOKEN WHICH
*        IS REQUIRED FOR THE CMSET SSARBACK.
*
         MEXIT
.*
.**********************************************************************
.*                                                                    *
.*   SSARBACK:  THIS SECTION OF THE MACRO PERFORMS THE VALIDITY       *
.*       CHECKING AND CODE GENERATION FOR THE 'SSARBACK' OPTION       *
.*                                                                    *
.**********************************************************************
.*
.SSARBCK ANOP
         AIF   (T'&RELATED EQ 'O').MNNREL   IF RELATED OPERAND NOT
.*                                     SPECIFIED, ISSUE MNOTE AND EXIT
         AIF   (T'&ASCB NE 'O').MNASCB      IF ASCB ADDRESS IS
.*                                     SPECIFIED ISSUE MNOTE AND EXIT
         AIF   (T'&XMSAVE NE 'O').MNXMSAV   IF XMSAVE IS SPECIFIED
.*                                     THEN ISSUE MNOTE AND EXIT
         AIF   ('&CHKICMA' NE 'YES').MNCHKI IF CHKICMA IS NOT DEFAULT
.*                                     VALUE ISSUE MNOTE AND EXIT
         AIF   (T'&CHKAUTH NE 'O').MNAUTHI IF CHKAUTH IS SPECIFIED
.*                                     VALUE ISSUE MNOTE AND EXIT
*
*        REGISTER ONE MUST NOW CONTAIN THE TOKEN WHICH
*        WAS RETURNED BY THE CMSET SSARTO MACRO.
*
         L     15,PSASVT-PSA(,0)       .SVT ADDRESS
         L     15,SVTCMSBR-SVT(,15)    .CMSET SSARBACK EPA
         BALR  14,15                   .CALL CMSET ROUTINE
         MEXIT
.*
.**********************************************************************
.*                                                                    *
.*    THIS SECTION OF THE MACRO ISSUES ALL THE MNOTES USED IN         *
.*       BY MACRO.                                                    *
.*                                                                    *
.**********************************************************************
.*
.*
.MNASCB  ANOP
.*  IHB280 ASCB INVALID WITH RESET/SSARBACK
         IHBERMAC 1020,ASCB,&REQUEST
         MEXIT
.*
.MNASCBI ANOP
.*  IHB249 INVALID ASCB PARAMETER SPECIFIED
         IHBERMAC 1014,ASCB
         MEXIT
.*
.MNAUTH  ANOP
.*  IHB002 INVALID CHKAUTH OPERAND SPECIFIED - XXX
         IHBERMAC 1001,CHKAUTH,&CHKAUTH
         MEXIT
.*
.MNAUTHI ANOP
.*  IHB280 CHKAUTH INVALID WITH SET/SSARTO/SSARBACK
         IHBERMAC 1020,CHKAUTH,&REQUEST
         MEXIT
.*
.MNCHKI  ANOP
.*  IHB280 CHKICMA INVALID WITH RESET/SSARTO/SSARBACK
         IHBERMAC 1020,CHKICMA,&REQUEST
         MEXIT
.*
.MNEXTRA ANOP
.*  IHB027 EXCESSIVE POSITIONAL PARAMETERS SPECIFIED
         IHBERMAC 238
         MEXIT
.*
.MNICMA  ANOP
.*  IHB002 INVALID CHKICMA OPERAND SPECIFIED - XXX
         IHBERMAC 1001,CHKICMA,&CHKICMA
         MEXIT
.*
.MNINVR  ANOP
.*  IHB002 INVALID REQUEST OPERAND SPECIFIED - XXX
         IHBERMAC 1001,REQUEST,&REQUEST
         MEXIT
.*
.MNNASCB ANOP
.*  IHB246 SET/SSARTO OPERAND SPECIFIED WITHOUT ASCB OPERAND
         IHBERMAC 1010,&REQUEST,ASCB
         MEXIT
.MNNAUTH ANOP
.*  IHB001 CHKAUTH OPERAND REQUIRED, NOT SPECIFIED
         IHBERMAC 1006,CHKAUTH
         MEXIT
.*
.MNNREL  ANOP
.*  IHB001 RELATED OPERAND REQUIRED, NOT SPECIFIED
         IHBERMAC 1006,RELATED
         MEXIT
.*
.MNNREQ  ANOP
.*  IHB001 REQUEST OPERAND REQUIRED, NOT SPECIFIED
         IHBERMAC 1006,REQUEST
         MEXIT
.*
.MNNXSAV ANOP
.*  IHB246 RESET OPERAND SPECIFIED WITHOUT XMSAVE OPERAND
         IHBERMAC 1010,&REQUEST,XMSAVE
         MEXIT
.*
.MNSAVI  ANOP
.*  IHB249 INVALID XMSAVE PARAMETER SPECIFIED
         IHBERMAC 1014,XMSAVE
         MEXIT
.*
.MNXMSAV ANOP
.*  IHB280 XMSAVE INVALID WITH SSARTO/SSARBACK
         IHBERMAC 1020,XMSAVE,&REQUEST
         MEXIT
.*
         MEND
* */
* CMSET:
*       MACRO KEYS(ASCB,XMSAVE,CHKAUTH,CHKICMA,RELATED);
*
* DCL EHAREGS CHAR EXT;                /* REGS FOR EPILOG            */
* DCL RC FIXED;                        /* RETURN CODE FROM MACRO     */
* DCL CMT CHAR;                        /* COMMENT FOR DO-END STMTS   */
*
* RC=12;                               /* INITIALIZE BAD RETURN CODE */
* CMT=' '××MACNAME××' '××MACLIST××' '××MACKEYS××
*       ' MACDATE(06/03/80)';          /* INITIALIZE MACRO COMMENT   */
* IF MACLABEL^='' THEN                 /* LABEL ON MACRO?            */
*   ANS(MACLABEL) COL(MACLMAR);        /* YES GENERATE LABEL         */
* ANS('DO; '××COMMENT(CMT)) COL(MACCOL);  /* GENERATE DO AND COMMENT */
* IF NUMBER(MACLIST)=0 THEN            /* TYPE SPECIFIED?            */
*   DO;
*     ANS MSG('AHA0013 ''MACLIST(1)'' IS REQUIRED BUT IS NOT '××
*             'SUPPLIED, NO TEXT IS GENERATED');
*     GOTO LHA0400;
*   END;
* IF NUMBER(MACLIST)>1 THEN            /* MORE THAN ONE TYPE?        */
*   DO;
*     ANS MSG('AHA0033 ''MACLIST'' IS TOO LONG, '××
*             'NO TEXT IS GENERATED');
*     GOTO LHA0400;
*   END;
* IF MACLIST(1)^='SET' THEN            /* VALID OPTION SPECIFIED?    */
*   IF MACLIST(1)^='RESET' THEN
*     IF MACLIST(1)^='SSARTO' THEN
*       IF MACLIST(1)^='SSARBACK' THEN
*         DO;
*           ANS MSG('AHA0023 CMSET OPTION '''××MACLIST(1)××''' IS '××
*                   'INVALID, NO TEXT IS GENERATED') COL(MACCOL);
*           GOTO LHA0400;
*         END;
* IF NUMBER(ASCB)>1 THEN               /* MORE THAN ONE ASCB?        */
*   DO;
*     ANS MSG('AHA0033 ''ASCB'' IS TOO LONG, '××
*             'NO TEXT IS GENERATED');
*     GOTO LHA0400;
*   END;
* IF NUMBER(XMSAVE)>1 THEN             /* MORE THAN ONE XMSAVE?      */
*   DO;
*     ANS MSG('AHA0033 ''XMSAVE'' IS TOO LONG, '××
*             'NO TEXT IS GENERATED');
*     GOTO LHA0400;
*   END;
* IF NUMBER(CHKAUTH)>1 THEN            /* MORE THAN ONE CHKAUTH?     */
*   DO;
*     ANS MSG('AHA0033 ''CHKAUTH'' IS TOO LONG, '××
*             'NO TEXT IS GENERATED');
*     GOTO LHA0400;
*   END;
* IF NUMBER(CHKICMA)>1 THEN            /* MORE THAN ONE CHKICMA?     */
*   DO;
*     ANS MSG('AHA0033 ''CHKICMA'' IS TOO LONG, '××
*             'NO TEXT IS GENERATED');
*     GOTO LHA0400;
*   END;
*  IF CHKICMA(1)^='' THEN
*    IF MACLIST(1)^='SET' THEN         /*  CHKICMA MAY ONLY BE
*                                      SPECIFIED ON A SET            */
*      DO;
*        ANS MSG('AHA0043 ''CHKICMA'' IS INVALID WITH '××MACLIST(1)××
*                ' OPTION, NO TEXT IS GENERATED');
*        GOTO LHA0400;
*      END;
* IF CHKAUTH(1)^='' THEN
*   IF MACLIST(1)^='RESET' THEN        /*  CHKAUTH MAY ONLY BE
*                                      SPECIFIED ON A RESET          */
*      DO;
*        ANS MSG('AHA0043 ''CHKAUTH'' IS INVALID WITH '××MACLIST(1)××
*                ' OPTION, NO TEXT IS GENERATED');
*        GOTO LHA0400;
*      END;
*/********************************************************************/
*/*    GENERATE REGISTER RESPECIFY                                   */
*/********************************************************************/
* ANS('RFY (GPR00F,GPR01F,GPR14F,GPR15F) RSTD;  /* RESTRICT '××
*     'REGISTERS USED BY CMSET */') COL(MACCOL+2);
* IF MACLIST(1)='SET' THEN
*   DO;
*/********************************************************************/
*/*    SET PROCESSING                                                */
*/********************************************************************/
*     IF NUMBER(ASCB)=0 THEN           /* ASCB REQUIRED ON SET       */
*       DO;
*         ANS MSG('AHA0013 ''ASCB'' KEYWORD REQUIRED WITH SET '××
*                 'OPTION, NO TEXT IS GENERATED');
*         GOTO LHA0300;
*       END;
*     IF CHKICMA(1)^='' THEN           /* CHKICMA A VALID VALUE?     */
*       IF CHKICMA(1)^='YES' THEN
*         IF CHKICMA(1)^='NO' THEN
*           DO;
*             ANS MSG('AHA0043 CHKICMA MUST BE ''YES'' OR ''NO'', '××
*                     'NO TEXT IS GENERATED');
*             GOTO LHA0300;
*           END;
*     ANS('GPR01F='××ASCB(1)××'; /* PUT ASCB ADDRESS IN '××
*         'REGISTER 1 */') COL(MACCOL+2);
*     IF CHKICMA(1)='NO' THEN
*         ANS('GPR01F=GPR01F&''7FFFFFFF''X; /* ICMA IS NOT TO BE '××
*             'CHECKED */') COL(MACCOL+2);
*       ELSE
*         ANS('GPR01F=GPR01F×''80000000''X; /* ICMA IS TO BE '××
*             'CHECKED */') COL(MACCOL+2);
*     IF XMSAVE(1)='' THEN
*         ANS('GPR00F=0; /* INDICATE NO XMSAVE AREA */') COL(MACCOL+2);
*       ELSE
*         ANS('GPR00F=ADDR('××XMSAVE(1)××'); /* PUT XMSAVE ADDRESS '××
*             'REGISTER 0  */') COL(MACCOL+2);
*     ANS('CALL IEAVCMS1; /* CALL CMSET SET ROUTINE */') COL(MACCOL+2);
*     GOTO LHA0200;
*   END;                               /* END OF SET PROCESSING      */
* IF MACLIST(1)='RESET' THEN
*   DO;
*/********************************************************************/
*/*    RESET PROCESSING                                              */
*/********************************************************************/
*     IF NUMBER(XMSAVE)=0 THEN         /* XMSAVE REQUIRED FOR RESET  */
*       DO;
*         ANS MSG('AHA0013 ''XMSAVE'' KEYWORD REQUIRED WITH '××
*                  'RESET OPTION, NO TEXT IS GENERATED') COL(MACCOL);
*         GOTO LHA0300;
*       END;
*     IF NUMBER(CHKAUTH)=0 THEN        /* CHKAUTH REQUIRED FOR RESET */
*       DO;
*         ANS MSG('AHA0013 ''CHKAUTH'' KEYWORD REQUIRED WITH '××
*                  'RESET OPTION, NO TEXT IS GENERATED') COL(MACCOL);
*         GOTO LHA0300;
*       END;
*     IF NUMBER(ASCB)^=0 THEN          /* ASCB NOT ALLOWED ON RESET  */
*       DO;
*         ANS MSG('AHA0023 ''ASCB'' IS INVALID WITH RESET OPTION, '××
*                 'NO TEXT IS GENERATED') COL(MACCOL);
*         GOTO LHA0300;
*       END;
*     IF CHKAUTH(1)^='YES' THEN
*       IF CHKAUTH(1)^='NO' THEN
*         DO;
*           ANS MSG('AHA0043 ''CHKAUTH'' MUST BE ''YES'' '××
*                 'OR ''NO'', NO TEXT IS GENERATED') COL(MACCOL);
*           GOTO LHA0300;
*         END;
*     ANS('GPR01F=ADDR('××XMSAVE(1)××'); /* PUT XMSAVE ADDRESS '××
*          'IN REGISTER 1  */') COL(MACCOL+2);
*     IF CHKAUTH(1)='YES' THEN
*       ANS('CALL IEAVCMR1; /* CALL CMSET RESET/AUTH CHECK */')
*           COL(MACCOL+2);
*     ELSE
*       ANS('CALL IEAVCMR2; /* CALL CMSET RESET/NO AUTH CHECK */')
*           COL(MACCOL+2);
*     GOTO LHA0200;                    /* END OF RESET PROCESSING    */
*   END;
*
*
* IF NUMBER(RELATED)=0 THEN            /* RELATED REQUIRED FOR
*                                         SSARTO AND SSARBACK        */
*   DO;
*     ANS MSG('AHA0013 ''RELATED'' KEYWORD REQUIRED WITH '××
*         MACLIST(1)××' OPTION, NO TEXT IS GENERATED') COL(MACCOL);
*     GOTO LHA0300;
*   END;
* IF NUMBER(XMSAVE)^=0 THEN            /* XMSAVE NOT ALLOWED ON
*                                         SSARTO AND SSARBACK        */
*   DO;
*     ANS MSG('AHA0023 ''XMSAVE'' KEYWORD IS INVALID WITH '××
*         MACLIST(1)××' OPTION, NO TEXT IS GENERATED') COL(MACCOL);
*     GOTO LHA0300;
*   END;
* IF MACLIST(1)='SSARTO' THEN
*   DO;
*/********************************************************************/
*/*    SSARTO PROCESSING                                             */
*/********************************************************************/
*     IF NUMBER(ASCB)=0 THEN           /* ASCB REQUIRED FOR SSARTO   */
*       DO;
*         ANS MSG('AHA0013 ''ASCB'' KEYWORD REQUIRED WITH SSARTO '××
*                 'OPTION, NO TEXT IS GENERATED') COL(MACCOL);
*         GOTO LHA0300;
*       END;
*     ANS('GPR01F='××ASCB(1)××'; /* PUT ASCB ADDRESS INTO '××
*         'REGISTER 1 */') COL(MACCOL+2);
*     ANS('CALL IEAVCMST;  /* CALL CMSET SSARTO ROUTINE */')
*         COL(MACCOL+2);
*     ANS('/* REGISTER ONE NOW CONTAINS A TOKEN WHICH IS '××
*         'REQUIRED FOR THE CMSET SSARBACK */') COL(10);
*     GOTO LHA0200;
*   END;
* IF MACLIST(1)='SSARBACK' THEN
*   DO;
*/********************************************************************/
*/*    SSARBACK PROCESSING                                           */
*/********************************************************************/
*     IF NUMBER(ASCB)^=0 THEN        /* ASCB NOT ALLOWED ON SSARBACK */
*       DO;
*         ANS MSG('AHA0023 ''ASCB'' KEYWORD IS INVALID WITH '××
*             'SSARBACK OPTION, NO TEXT IS GENERATED') COL(MACCOL);
*         GOTO LHA0300;
*       END;
*     ANS('/* REGISTER ONE MUST NOW CONTAIN THE TOKEN WHICH '××
*         'WAS RETURNED BY THE CMSET SSARTO ROUTINE */') COL(10);
*     ANS('CALL IEAVCMSB; /* CALL CMSET SSARBACK ROUTINE */')
*         COL(MACCOL+2);
*     GOTO LHA0200;                    /* END OF SSARBACK PROCESSING */
*   END;
*LHA0200:
* RC=0;                                /* SET GOOD RETURN CODE      */
*LHA0300:
* ANS('RFY (GPR00F,GPR01F,GPR14F,GPR15F) UNRSTD; '××
*     '/* UNRESTRICT REGISTERS USED BY CMSET */') COL(MACCOL);
* IF INDEX(EHAREGS,'00F')=0 THEN
*   EHAREGS=EHAREGS××'00F';
* IF INDEX(EHAREGS,'01F')=0 THEN
*   EHAREGS=EHAREGS××'01F';
* IF INDEX(EHAREGS,'14F')=0 THEN
*   EHAREGS=EHAREGS××'14F';
* IF INDEX(EHAREGS,'15F')=0 THEN
*   EHAREGS=EHAREGS××'15F';
*LHA0400:
* ANS('END; '××COMMENT(CMT)) COL(MACCOL);
* RETURN CODE(RC);
* % END CMSET;
