         MACRO
&NAME    ENDSUB
.**********************************************************************
.*                                                                    *
.*   MODULE NAME = ENDSUB                                             *
.*                                                                    *
.*   DESCRIPTIVE NAME = END SUBLIST DEFINITION                        *
.*                                                                    *
.*   COPYRIGHT = 5740-XYN COPYRIGHT IBM CORP. 1982,1983,              *
.*                LICENSED MATERIAL - PROGRAM,                        *
.*                PROPERTY OF IBM, REFER TO COPYRIGHT                 *
.*                INSTRUCTIONS FORM G120-2083                         *
.*                                                                    *
.*   STATUS = OS/VS2 JBB1329                                          *
.*                                                                    *
.*   FUNCTION = ENDSUB MACRO SPECIFIES THE END OF THE SUBLIST         *
.*              DEFINITION SPECIFIED BY THE JDSUB MACRO.              *
.*                                                                    *
.*     NOTES = NONE                                                   *
.*                                                                    *
.*      DEPENDENCIES = NONE                                           *
.*                                                                    *
.*      RESTRICTIONS = THE ENDSUB MACRO CAN ONLY FOLLOW JDPARM,       *
.*                     PROVIDING THAT IT  WAS DEFINED  AS BEING       *
.*                     USED (DEFINED) AS A SUBLIST MACRO.             *
.*                                                                    *
.*   ENTRY POINT = N/A                                                *
.*                                                                    *
.*   MODULE TYPE = MACRO                                              *
.*                                                                    *
.*     PROCESSOR = ASSEMBLER                                          *
.*                                                                    *
.*     INPUT = THE FOLLOWING MAY BE SPECIFIED AS PARAMETERS TO ENDSUB *
.*                                                                    *
.*           PARAMETER          DESCRIPTION                           *
.*                                                                    *
.*           - &NAME            ONLY USED AS A LABEL ON THE EXPANSION.*
.*                                                                    *
.*     OUTPUT = THERE IS NO OUTPUT FOR THIS MACRO.                    *
.*                                                                    *
.*   EXIT NORMAL =                                                    *
.*   CONDITION-NO ERRORS DETECTED                                     *
.*   OUTPUT    -NO MNOTE ISSUED. SEVERITY CODE=0.                     *
.*                                                                    *
.*   EXIT ERROR =                                                     *
.*   CONDITION-SEVERE ERROR DETECTED BY THE ENDSUB MACRO.             *
.*   OUTPUT    -ERROR MNOTES WITH SEVERITY CODE 12                    *
.*              ISSUED WHEN MACRO IS NOT INVOKED IN THE CORRECT       *
.*              ORDER (SEE RESTRICTIONS AND MESSAGES)                 *
.*                                                                    *
.*   EXIT ERROR =                                                     *
.*   CONDITION-SEVERE ERROR DETECTED BY THE ENDSUB MACRO.             *
.*   OUTPUT    -ERROR MNOTES WITH SEVERITY CODE 16                    *
.*              ISSUED WHEN JDTMAC MACRO IS NOT INVOKED FIRST TO      *
.*              START THE JDTS. (SEE RESTRICTIONS AND MESSAGES)       *
.*                                                                    *
.*   EXTERNAL REFERENCES = N/A                                        *
.*                                                                    *
.*   TABLES = NONE                                                    *
.*                                                                    *
.*   MESSAGES = (MNOTES)                                              *
.*                                                                    *
.*   ERROR MNOTES =                                                   *
.*                                                                    *
.*    MNOTE 12,'ENDSUB MACRO DID NOT FOLLOW A SUBLIST JDPARM'         *
.*                                                                    *
.*    MNOTE 16,'JDTMAC MACRO MUST BE FIRST MACRO SPECIFIED BUT IS NOT'*
.*                                                                    *
.*   CHANGE ACTIVITY = H0,H1                                       @H1C
.*  $H0= EXTJCL  JBB1328 810315 PDC5: SUPPORT FOR EXTENDED JCL        *
.*  $H1= EXTJCL  JBB1329 820611 PDC5: SUPPORT FOR EXTENDED JCL     @H1A
.*                                                                    *
.**********************************************************************
.* A 000000-999999                                                 @H0A
.* A GLOBAL BIT THAT IS ORIGINALLY SET IN JDSUB, TO BE RESET HERE. @H1A
.*--------------------------------------------------------------------*
.*  GLOBALS USED IN THIS MACRO THAT MAY BE ACTIVE OR BE MADE ACTIVE   *
.*  RESPECTIVELY: JDTMAC, JDVERB, JDKEY, JDPARM, JDSUB, ENDSUB AND    *
.*                ENDJDT.                                             *
.*--------------------------------------------------------------------*
         GBLB  &JACT,&VACT,&KACT,&PACT,&SPACT,&EPACT,&EJACT
.*--------------------------------------------------------------------*
.*  GLOBAL BIT THAT MAY HAVE BEEN SET IN THE JDSUB MACRO TO           *
.*  INDICATE THE PRECEEDING JDPARM MACROS WERE LIST ITEMS.            *
.*--------------------------------------------------------------------*
         GBLB  &L3
&L3      SETB  0                 RESET THE ITEM LIST GLOBAL BIT    @H1A
.*--------------------------------------------------------------------*
.*       CHECK TO SEE IF ENDSUB MACRO IS SPECIFIED IN THE RIGHT ORDER *
.*       ONLY AFTER A JDPARM THAT IS SPECIFIED AS A SUBLIST IS VALID  *
.*--------------------------------------------------------------------*
         AIF   (&PACT EQ 1 AND &SPACT EQ 1).CONT    SUBLIST ACTIVE
.*--------------------------------------------------------------------*
.*       IF NONE OF THE FOLLOWING BITS ARE ON THEN THIS MUST BE THE   *
.*       FIRST MACRO INVOKED IN THE JDT DEFINITIONS.                  *
.*--------------------------------------------------------------------*
         AIF   (&JACT EQ 0 AND &VACT EQ 0 AND &KACT EQ 0 AND &PACT EQ 0+
                AND &SPACT EQ 0 AND &EPACT EQ 0 AND &EJACT EQ 0).NOJDT
.*--------------------------------------------------------------------*
.*      ENDSUB MACRO DID NOT FOLLOW A SUBLIST-> MNOTE IT.             *
.*--------------------------------------------------------------------*
         MNOTE 12,'ENDSUB MACRO DID NOT FOLLOW A SUBLIST JDPARM'
         AGO   .RESET
.*--------------------------------------------------------------------*
.*  JDTMAC MUST BE THE FIRST MACRO SPECIFIED, BUT IS NOT-> MNOTE IT.  *
.*--------------------------------------------------------------------*
.NOJDT   ANOP
       MNOTE 16,'JDTMAC MACRO MUST BE FIRST MACRO SPECIFIED BUT IS NOT'
         MEXIT
.*--------------------------------------------------------------------*
.*      RESET THE APPROPRIATE BAD GLOBAL BIT                          *
.*--------------------------------------------------------------------*
.RESET   ANOP
&JACT    SETB   0                RESET THE JDTMAC ACTIVE GLOBAL BIT
&VACT    SETB   0                RESET THE JDVERB ACTIVE GLOBAL BIT
&KACT    SETB   0                RESET THE JDKEY ACTIVE GLOBAL BIT
&EJACT   SETB   0                RESET THE ENDJDT ACTIVE GLOBAL BIT
.*--------------------------------------------------------------------*
.*      RESET THE APPROPRIATE GLOBAL BITS-SET SUBLIST END BIT ON      *
.*--------------------------------------------------------------------*
.CONT    ANOP
&PACT    SETB  0                 RESET JDPARM GLOBAL BIT
&SPACT   SETB  0                 RESET JDSUB GLOBAL BIT
&EPACT   SETB  1                 SET END OF SUBLIST DEFINITION BIT
         SPACE
         MEND
