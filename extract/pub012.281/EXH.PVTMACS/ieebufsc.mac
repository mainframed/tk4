          MACRO
&NAME     IEEBUFSC
.*******************************************************************
.*
.*     ROUTINE NAME        IEEBUFSC
.*
.*     FUNCTION            TO SCAN A BUFFER FOR A GIVEN KEYWORD OF
.*                         A GIVEN LENGTH AND TO RETURN A POINTER
.*                         TO THE KEYWORD VALUE AND THE LENGTH OF
.*                         THE VALUE
.*
.*     ENTRY POINT         IEEBUFSC
.*
.*     ATTRIBUTES          REFRESHABLE
.*
.*     OPERATION           1. SET UP PTR TO BUFFER -1 AND PTR
.*                            TO KEYWORD LENGTH
.*                         2. IF BUFFER PTR = SPECIFIED KEYWORD
.*                            GO TO FOUND ROUTINE (3).
.*                            A. IF NOT, INCREMENT BUFFER PTR
.*                               BY ONE.
.*                            B. IF PTR IS AT END OF BUFFER SET
.*                               UP NOT FOUND RETURN CODE (8) IN
.*                               R15 AND EXIT.
.*                            C. IF PTR IS AT A COMMA, GO BACK TO
.*                               OPERATION 2.
.*                            D. IF PTR IS AT A BLANK SET UP NOT
.*                               FOUND RETURN CODE (8) IN R15 AND
.*                               EXIT.
.*                            E. OTHERWISE GO TO OPERATION 2.A.
.*                         3. AT FOUND ROUTINE SET UP R14 TO POINT
.*                            TO KEYWORD VALUE
.*                         4. SCAN KEYWORD VALUE TO THE END OF
.*                            THE BUFFER, A COMMA OR A BLANK
.*                         5. CALCULATE LENGTH OF KEYWORD VALUE.
.*                         6. IF LENGTH IS < OR = ZERO, SET UP
.*                            AN INVALID KEYWORD VALUE LENGTH
.*                            RETURN CODE (4) IN R15 AND EXIT.
.*                         7. OTHERWISE, SET GOOD RETURN CODE
.*                            OF ZERO IN R15 AND EXIT.
.*
.*     EXTERNAL REFS       NONE
.*
.*     SVCS                NONE
.*
.*     INPUT               R1 - PTR TO BEGINNING OF BUFFER
.*                         R0 - PTR TO LAST BYTE OF BUFFER + 1
.*                         R15- PTR TO KEYWORD WITH THE LENGTH
.*                              OF THE KEYWORD IN THE HIGH ORDER
.*                              BYTE.
.*                              EG. KEYWORD FORMAT 'DSNAME='
.*                                  R15 WOULD POINT TO THE 'D'
.*                                  WITH A 6 IN THE HIGH ORDER
.*                                  BYTE.
.*
.*     OUTPUT              R14  PTR TO FIRST BYTE OF KEYWORD
.*                              VALUE IN BUFFER OR WORKAREA
.*                         R1   LENGTH OF KEYWORD VALUE
.*                         R15  RETURN CODE OF ZERO.
.*
.*     ERROR OUTPUT        R15  CONTAINS A FOUR IF KEYWORD
.*                              HAD AN INVALID LENGTH
.*                         R15  CONTAINS AN EIGHT IF THE KEYWORD
.*                              WAS NOT FOUND IN THE BUFFER
.*
.*     EXIT                TO NEXT SEQUENTIAL INSTRUCTION AFTER
.*                         MACRO CALL
.*
.*     NOTES               LISTS OF OPERANDS IN PARENTHESES ARE
.*                         NOT PROCESSED; THIS MACRO IS INTENDED
.*                         TO PROCESS ONLY A SINGLE VALUE  FOR
.*                         A GIVEN KEYWORD
.*******************************************************************
          BCTR  1,0                DEC. ADDRESS TO BEG. OF BUFFER
          SR    14,14              CLEAR LENGTH REGISTER
          SLDL  14,8               KEYWORD LENGTH TO LOW ORDER
          LTR   14,14              IS LENGTH ZERO?
          BZ    NG&SYSNDX          YES, GO SET BAD R.C.
          SRL   15,8               ADDRESS BACK TO LOW ORDER 3 BYTES
AB&SYSNDX EQU   *
          EX    14,CO&SYSNDX       KEYWORD FOUND IN BUFFER?
          BE    FD&SYSNDX          YES, GO TO FOUND ROUTINE
CD&SYSNDX EQU   *
          LA    1,1(1)             INCREMENT BUFFER POINTER
          CR    1,0                IS END OF BUFFER REACHED?
          BE    NF&SYSNDX          YES, KEYWORD NOT PRESENT
          CLI   0(1),MA&SYSNDX     IS IT A COMMA?
          BE    AB&SYSNDX          YES,CHECK FOR KEYWORD AGAIN
          CLI   0(1),LK&SYSNDX     IS IT A BLANK?
          BE    NF&SYSNDX          YES, GO SET BAD R.C.
          B     CD&SYSNDX          NO, RETURN TO SEARCH
FD&SYSNDX EQU   *
          AR    1,14               ADD LENGTH TO KEYWORD PTR.
          LA    14,2(1)            SET PTR TO KEYWORD VALUE
SC&SYSNDX EQU   *
          LA    1,1(1)             INCREMENT PTR TO = SIGN
          CR    1,0                IS END OF BUFFER REACHED?
          BE    LG&SYSNDX          YES,GO CALCULATE VALUE LENGTH
          CLI   0(1),MA&SYSNDX     NO, IS IT A COMMA?
          BE    LG&SYSNDX          YES, CALCULATE VALUE LENGTH
          CLI   0(1),LK&SYSNDX     NO, IS IT A BLANK?
          BE    LG&SYSNDX          YES, CALCULATE VALUE LENGTH
          B     SC&SYSNDX          NO RETURN TO SCAN
LG&SYSNDX EQU   *
          SR    1,14               SUBT. BEG. OF KEYWORD FROM END
          BC    12,NG&SYSNDX       IF < OR = ZERO, NOT FOUND
          SR    15,15              CLEAR RETURN CODE REG.
          B     OT&SYSNDX          LENGTH SET UP IN REG ZERO
NF&SYSNDX EQU   *
          LA    15,8               SET NOT FOUND RETURN CODE
          B     OT&SYSNDX          RETURN TO END
NG&SYSNDX EQU   *
          LA    15,4               SET BAD LENGTH CODE
          B     OT&SYSNDX          RETURN TO END
CO&SYSNDX CLC   1(0,1),0(15)       INPUT KEYWORD = BUFFER WORD?
MA&SYSNDX EQU   C','               COMMA FOR COMPARES
LK&SYSNDX EQU   C' '               BLANK FOR COMPARES
OT&SYSNDX EQU   *
          MEND
