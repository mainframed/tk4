         MACRO
         GENERR &CODE,&LIST             PROTOTYPE FOR GENERR
         GBLC  &ERCD(255)
         GBLB  &GENSW
         LCLC  &CN
         LCLC  &LET,&GEN(8)
         LCLA  &A,&K,&I
         LCLA  &C,&CT,&CA                COUNTERS
.* GENERATES ERROR MESSAGES AND A BRANCH TABLE TO THEM
.* IN CSECT X6C00.  THE CODE MUST BE A SYMBOLIC NUMBER AND THE MSG
.* MUST BE A QUOTED STRING.  ACCEPTS SAME DATA AS JGENIN MACRO.  MSG
.* MUST CONTAIN  BLANK-DOLLAR-BLANK WHEREVER APPENDED DATA IS TO BE
.* INSERTED AND BLANK- POUND -BLANK WHEREVER A COLUMN POINTER IS TO BE
.* INSERTED INTO THE MESSAGE.  USE THESE TWO CHARACTERS ONLY FOR THIS
.* PURPOSE.   ALSO GENERATES EQUATES AND COMMENTS IN THE DSECT JERMSGCD
.* DEPENDING UPON THE SETTING OF THE GLOBAL VARIABLE &GENSW.
&K       SETA  K'&LIST
&I       SETA  1
&CN      SETC  '&CODE'(4,K'&CODE-3)     FIND ERROR CODE NUMBER
&A       SETA  &CN                      CONVERT TO DECIMAL
&CA      SETA  0
.* GENERATES INTERNALLY CODED TEXT.
.* ACCEPTS ALPHA, NUMBERIC, AND ALL SPECIAL CHARACTERS.
         AIF   ('&LIST'(1,1) EQ '''').M1
         AIF   ('&LIST'(&K,1) EQ '''').M1  MISSING QUOTE
         MNOTE 1,'MESSAGE QUOTE MISSING'
         MEXIT                          EXIT
.M1      ANOP                           BRANCH LABEL
&C       SETA  2
&K       SETA  &K-1
         AIF   (&GENSW).CONT           ARE WE GENERATING JERMSGCD DSECT
         SPACE
&CODE    EQU   &CN                      SEE FOLLOWING MNOTE
         MNOTE *,&LIST
         MEXIT
.CONT    ANOP                           BRANCH LABEL
S&CODE   DC    0CL(&K-&C+1)' '
.L2      ANOP
&CT      SETA  1
.L3      AIF   ('&LIST'(&C,1) GE 'A').L5  IS IT ALPHANUMERIC?
         AIF   ('&LIST'(&C,1) EQ '$').L3DOLR      DOLLAR
         AIF   ('&LIST'(&C,1) EQ '#').L3POUND     POUND
         AIF   ('&LIST'(&C,1) EQ ' ').L3B         BLANK
         AIF   ('&LIST'(&C,1) EQ ',').L3C         COMMA
         AIF   ('&LIST'(&C,1) EQ '.').L3P         PERIOD
         AIF   ('&LIST'(&C,1) EQ '=').L3EQ        EQUAL
         AIF   ('&LIST'(&C,1) EQ '+').L3PLUS      PLUS
         AIF   ('&LIST'(&C,1) EQ '-').L3MIN       MINUS
         AIF   ('&LIST'(&C,1) EQ '/').L3SLASH     SLASH
         AIF   ('&LIST'(&C,1) EQ '(').L3LP        LEFT PARN.
         AIF   ('&LIST'(&C,1) EQ ')').L3RP        RIGHT PARN.
         AIF   ('&LIST'(&C,1) EQ '*').L3AST       ASTERISK
         AIF   ('&LIST'(&C,1) EQ '&&'(1,1)).L3AMP      AMPERSAND
         AIF   ('&LIST'(&C,1) EQ ''''(1,1)).L3QUO      QUOTE
.L5      ANOP
&LET     SETC  'J'.'&LIST'(&C,1)
.L6      AIF   (&CT EQ 1).L7
&LET     SETC  ','.'&LET'
.L7      ANOP
&GEN(&CT) SETC '&LET'
&C       SETA  &C+1
         AIF   (&C GT &K).L9
&CT      SETA  &CT+1
         AIF   (&CT LT 9).L3
         DC    AL1(&GEN(1)&GEN(2)&GEN(3)&GEN(4)&GEN(5)&GEN(6)&GEN(7)&GEX
               N(8))
&CT      SETA  1
.L8      ANOP
&GEN(&CT) SETC ''
&CT      SETA  &CT+1
         AIF   (&CT LT 9).L8
         AGO   .L2
.L3DOLR  ANOP
&LET     SETC  'JDOLLAR'
         AGO   .L6
.L3POUND ANOP
&LET     SETC  'JPOUND'
         AGO   .L6
.L3EQ    ANOP
&LET     SETC  'JEQUAL'
         AGO   .L6
.L3PLUS  ANOP
&LET     SETC  'JPLUS'
         AGO   .L6
.L3MIN   ANOP
&LET     SETC  'JMINUS'
         AGO   .L6
.L3SLASH ANOP
&LET     SETC  'JSLASH'
         AGO   .L6
.L3C     ANOP
&LET     SETC  'JCOMMA'
         AGO   .L6
.L3B     ANOP
&LET     SETC  'JBLANK'
         AGO   .L6
.L3P     ANOP
&LET     SETC  'JPERIOD'
         AGO   .L6
.L3LP    ANOP
&LET     SETC  'JLPARN'
         AGO   .L6
.L3RP    ANOP
&LET     SETC  'JRPARN'
         AGO   .L6
.L3AST   ANOP
&LET     SETC  'JASTER'
         AGO   .L6
.L3AMP   AIF   ('&LIST'(&C+1,1) EQ '&&'(1,1)).L3AMP2  2ND AMPERSAND
         MNOTE 1,'ILLEGAL AMPERSAND - AMPERSAND IS NOT DOUBLED'
.L3AMP2  ANOP
&LET     SETC  'JAMPER'
&C       SETA  &C+1
&CA      SETA  &CA+1
         AGO   .L6
.L3QUO   AIF   ('&LIST'(&C+1,1) EQ ''''(1,1)).L3QUO2  2ND QUOTE
         MNOTE 1,'ILLEGAL QUOTE - QUOTE IS NOT DOUBLED'
.L3QUO2  ANOP
&LET     SETC  'JQUOTE'
&C       SETA  &C+1
&CA      SETA  &CA+1
         AGO   .L6
.L9      AIF   (&CA EQ 0).L9D
&LET     SETC  ','.'JBLANK'
&CT      SETA  &CT+1
         AIF   (&CT LT 9).L9B
         DC    AL1(&GEN(1)&GEN(2)&GEN(3)&GEN(4)&GEN(5)&GEN(6)&GEN(7)&GEX
               N(8))
&CT      SETA  1
.L9A     ANOP
&GEN(&CT) SETC ''
&CT      SETA  &CT+1
         AIF   (&CT LT 9).L9A
&CT      SETA  1
&LET     SETC  'JBLANK'
.L9B     ANOP
&GEN(&CT)      SETC                     '&LET'
&CA      SETA  &CA-1
         AGO   .L9
.L9D     DC    AL1(&GEN(1)&GEN(2)&GEN(3)&GEN(4)&GEN(5)&GEN(6)&GEN(7)&GEX
               N(8))
.L10     ANOP
&ERCD(&A) SETC 'S&CODE'                 SORT & SAVE MESSAGE NAME
         MEND
