         MACRO
&NAME    JDSUB &LIST=NO                                            @H1C
.**********************************************************************
.*                                                                    *
.*   MODULE NAME = JDSUB                                              *
.*                                                                    *
.*   DESCRIPTIVE NAME = JCL SUBLIST DEFINITION                        *
.*                                                                    *
.*   COPYRIGHT = 5740-XYN COPYRIGHT IBM CORP. 1982,1983,              *
.*               LICENSED MATERIAL - PROGRAM,                         *
.*               PROPERTY OF IBM, REFER TO COPYRIGHT                  *
.*               INSTRUCTIONS FORM G120-2083                          *
.*                                                                    *
.*   STATUS = OS/VS2 JBB1329                                          *
.*                                                                    *
.*   FUNCTION = JDSUB MACRO FLAGS THE JDPARM MACROS THAT FOLLOW       *
.*              AS DESCRIPTIONS  OF SUBLIST  ELEMENTS UP TO THE       *
.*              NEXT  ENDSUB MACRO.  THE JDPARM  MACRO  DEFINES       *
.*              SUBLISTS  OF A KEYWORD SUBPARAMETER.  THE FIRST       *
.*              JDPARM MACRO  AFTER JDSUB  WILL SET A  BIT THAT       *
.*              SAYS IT IS THE FIRST ELEMENT OF A SUBLIST.  ALL       *
.*              OTHER  JDPARM  MACROS WILL  SET A BIT THAT SAYS       *
.*              THAT  IT IS A MEMBER  OF A SUBLIST, BUT NOT THE       *
.*              FIRST.  IF THE  KEYWORD  LIST= IS  SPECIFIED AS    @H1A
.*              Y OR YES, THEN THE  SUBLIST PARAMETERS  DEFINED    @H1A
.*              BY THE JDPARM MACRO WILL BE LIST ITEMS.            @H1A
.*                                                                    *
.*     NOTES = NONE                                                   *
.*                                                                    *
.*      DEPENDENCIES = NONE                                           *
.*                                                                    *
.*      RESTRICTIONS = 1) THE JDSUB MACRO CAN ONLY FOLLOW JDKEY,      *
.*                     JDPARM, OR THE ENDSUB MACROS.                  *
.*                     2) WHEN THE LIST ITEM BIT IS ON (SET ON     @H1A
.*                     IN THE JDKEY MACRO WITH THE LIST= KEYWORD)  @H1A
.*                     THE INVOCATION OF JDSUB IS INVALID FOR      @H1A
.*                     THAT KEYWORD.                               @H1A
.*                                                                    *
.*   ENTRY POINT = N/A                                                *
.*                                                                    *
.*   MODULE TYPE = MACRO                                              *
.*                                                                    *
.*     PROCESSOR = ASSEMBLER                                          *
.*                                                                    *
.*     INPUT = THE FOLLOWING MAY BE SPECIFIED AS PARAMETERS TO JDPARM *
.*                                                                    *
.*           PARAMETER          DESCRIPTION                           *
.*                                                                    *
.*           - &NAME            ONLY USED AS A LABEL ON THE EXPANSION.*
.*           - &LIST=           JDPARM SUBLIST PARAMETERS ARE LIST @H1A
.*                              ITEMS IF LIST=YES IS SPECIFIED.    @H1A
.*                                                                    *
.*     OUTPUT = THERE IS NO OUTPUT FOR THIS MACRO.                    *
.*                                                                    *
.*   EXIT NORMAL =                                                    *
.*   CONDITION-NO ERRORS DETECTED                                     *
.*   OUTPUT    -NO MNOTE ISSUED. SEVERITY CODE=0.                     *
.*                                                                    *
.*   EXIT ERROR =                                                     *
.*   CONDITION-SEVERE ERROR DETECTED BY THE JDSUB MACRO.              *
.*   OUTPUT    -ERROR MNOTES WITH SEVERITY CODE 12                    *
.*              ISSUED WHEN MACRO IS NOT INVOKED IN THE CORRECT       *
.*              ORDER, IS INVOKED IN AN ITEM LIST SPECIFIED BY     @H1A
.*              THE JDKEY MACRO, OR THE LIST KEYWORD IS            @H1A
.*              INCORRECTLY SPECIFIED.                             @H1A
.*              (SEE RESTRICTIONS AND MESSAGES).                      *
.*                                                                    *
.*   EXIT ERROR =                                                     *
.*   CONDITION-TERMINATING ERROR DETECTED BY THE JDSUB MACRO.         *
.*   OUTPUT    -ERROR MNOTES WITH SEVERITY CODE 16                    *
.*              ISSUED WHEN JDTMAC MACRO IS NOT FIRST MACRO TO BE     *
.*              INVOKED TO START THE JDTS.                            *
.*             (SEE RESTRICTIONS AND MESSAGES).                       *
.*                                                                    *
.*   EXTERNAL REFERENCES = N/A                                        *
.*                                                                    *
.*   TABLES = NONE                                                    *
.*                                                                    *
.*   MESSAGES = (MNOTES)                                              *
.*                                                                    *
.*   ERROR MNOTES =                                                   *
.*                                                                    *
.*    MNOTE 12,'JDSUB MACRO SPECIFIED OUT OF ORDER'                   *
.*    MNOTE 12,'JDSUB MACRO MAY NOT BE SPECIFIED IN AN ITEM LIST'  @H1A
.*    MNOTE 12,'LIST=&LIST INVALID. MUST BE YES, NO, Y, OR N'      @H1A
.*                                                                    *
.*    MNOTE 16,'JDTMAC MACRO MUST BE FIRST MACRO SPECIFIED BUT IS NOT'*
.*                                                                    *
.*   CHANGE ACTIVITY = H0,R1,H1                                    @H1C
.*  $H0= EXTJCL  JBB1328 810315 PDC5: SUPPORT FOR EXTENDED JCL        *
.*  $R1= PPP0045 JBB1328 811202 PDC5: MISSING EXPECTED MNOTE       @R1A
.*  $H1= EXTJCL  JBB1329 820611 PDC5: SUPPORT FOR EXTENDED JCL     @H1A
.*                                                                    *
.**********************************************************************
.* A 000000-999999                                                 @H0A
.* A CHECKING FOR JDPARM AND JDSUB ACTIVE BITS ON SIMULTANEOUSLY   @R1A
.* A CHECKING FOR JDPARM GLOBAL LIST ITEM BIT (SET IN JDKEY)       @H1A
.* A KEYWORD LIST=YES OR NO TO SET A GLOBAL BIT TO PASS TO JDPARM
.*   TO INDICATE THAT THE SUBLIST PARAMETERS ARE LIST ITEMS.       @H1A
.*---------------------------------------------------------------*
.*   GLOBALS THAT MAY BE ACTIVE OR BE MADE ACTIVE IN THIS MACRO, *
.*   RESPECTIVELY: JDTMAC, JDVERB, JDKEY, JDPARM, JDSUB, ENDSUB, *
.*   AND ENDJDT.                                                 *
.*---------------------------------------------------------------*
         GBLB  &JACT,&VACT,&KACT,&PACT,&SPACT,&EPACT,&EJACT
.*---------------------------------------------------------------* @H1A
.*   &L2 - GLOBAL BIT INITIALLY SET IN JDKEY-LIST ITEMS.         * @H1A
.*   &L3 - GLOBAL BIT SET IN THIS MACRO TO PASS TO JDPARM.       * @H1A
.*         INDICATES SUBLIST ELEMENTS ARE A ITEM LIST.           * @H1A
.*---------------------------------------------------------------* @H1A
         GBLB  &L2,&L3             INDICATES LIST ITEMS            @H1A
.*---------------------------------------------------------------* @H1A
.*   CHECK TO SEE IF JDSUB MACRO IS SPECIFIED IN AN ITEM LIST.   * @H1A
.*---------------------------------------------------------------* @H1A
         AIF   (&L2 EQ 0).ORDER    NOT SPECIFIED IN AN ITEM LIST?  @H1A
.*---------------------------------------------------------------* @H1A
.*   JDSUB SPECIFIED IN AN ITEM LIST-> MNOTE IT.                 * @H1A
.*---------------------------------------------------------------* @H1A
       MNOTE 12,'JDSUB MACRO MAY NOT BE SPECIFIED IN AN ITEM LIST' @H1A
         MEXIT
.*---------------------------------------------------------------*
.*   CHECK TO SEE IF JDSUB MACRO IS SPECIFIED IN THE RIGHT ORDER *
.*   ONLY AFTER JDKEY, JDPARM, OR ENDSUB IS VALID                *
.*---------------------------------------------------------------*
.ORDER   ANOP                                                      @H1A
         AIF   (&PACT EQ 1 AND &SPACT EQ 1).NOTEIT WITHIN SUBLIST  @R1A
         AIF   (&KACT EQ 1 OR &PACT EQ 1 OR &EPACT EQ 1).SETUP
.*---------------------------------------------------------------*
.*   IF NONE OF THE FOLLOWING BITS ARE ON THEN THIS MUST BE      *
.*   THE FIRST MACRO INVOKED IN THE JDT DEFINITIONS.             *
.*---------------------------------------------------------------*
         AIF   (&JACT EQ 0 AND &VACT EQ 0 AND &EJACT EQ 0 AND &SPACT   +
               EQ 0).NOJDT     NONE ON?  GO MNOTE IT.
.*---------------------------------------------------------------*
.*   JDSUB MACRO WAS SPECIFIED WITHIN AN ALREADY DEFINED SET OF  *
.*   SUBLISTS--ENDSUB HAS TO BE INVOKED PRIOR TO THE REUSE OF    *
.*   JDSUB MACRO-> MNOTE IT.                                     *
.*---------------------------------------------------------------*
.NOTEIT  ANOP                                                      @R1A
         MNOTE 12,'JDSUB MACRO SPECIFIED OUT OF ORDER'
         AGO   .RESET
.*---------------------------------------------------------------*
.*  JDTMAC MUST BE THE FIRST MACRO SPECIFIED-> MNOTE IT.         *
.*---------------------------------------------------------------*
.NOJDT   ANOP
       MNOTE 16,'JDTMAC MACRO MUST BE FIRST MACRO SPECIFIED BUT IS NOT'
         MEXIT
.*---------------------------------------------------------------*
.*      RESET THE APPROPRIATE GLOBAL BIT                         *
.*---------------------------------------------------------------*
.RESET   ANOP
&JACT    SETB  0                 RESET JDTMAC GLOBAL BIT
&VACT    SETB  0                 RESET VERB GLOBAL BIT
&EJACT   SETB  0                 RESET END JDT GLOBAL BIT
.*---------------------------------------------------------------*
.*      RESET THE APPROPRIATE GLOBAL BIT - SET SUBLIST BIT.      *
.*---------------------------------------------------------------*
.SETUP   ANOP
&KACT    SETB  0                 RESET KEYWORD GLOBAL BIT
&PACT    SETB  0                 RESET PARAMETER GLOBAL BIT
&EPACT   SETB  0                 RESET END SUBLIST GLOBAL BIT
&SPACT   SETB  1                 SET SUBLIST GLOBAL BIT ON
.*---------------------------------------------------------------* @H1A
.*  CHECK TO SEE IF LIST WAS SPECIFIED. (DEFAULT IS NO)          * @H1A
.*---------------------------------------------------------------* @H1A
         AIF   ('&LIST' EQ 'Y' OR '&LIST' EQ 'YES' OR '&LIST' EQ 'N' OR+
               '&LIST' EQ 'NO').L3                                 @H1A
.*---------------------------------------------------------------* @H1A
.*    LIST PARAMETER SPECIFIES AN INVALID PARAMETER-> MNOTE IT.  * @H1A
.*---------------------------------------------------------------* @H1A
         MNOTE 12,'LIST=&LIST INVALID. MUST BE YES, NO, Y, OR N'   @H1A
.L3      ANOP                                                      @H1A
&L3      SETB  ('&LIST' EQ 'Y' OR '&LIST' EQ 'YES')                @H1A
         SPACE
         MEND
