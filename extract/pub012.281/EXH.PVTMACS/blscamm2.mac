*** START OF SPECIFICATIONS *****************************************
*
* $MAC(BLSCAMM2) COMP(SC132) PROD(JIP1329): EXTRACT CHARACTER STRING
*
* COPYRIGHT= 5740-XYN, 5740-XYS COPYRIGHT IBM CORPORATION 1982.
*            LICENCED MATERIAL-PROGRAM PROPERTY OF IBM,
*            REFER TO COPYRIGHT INSTRUCTIONS FORM NUMBER G120-2083.
*
*   CHANGE ACTIVITY:
* $L0= MVSIPCS,EIP1102,78001,DMGPSR: MVS IPCS INITIAL RELEASE CODE
* $D1= QUALITY,HIP2102,81266,DSDPRW: IMPROVE COMMENTS, REMARKS
*
*** END OF SPECIFICATIONS *******************************************
         MACRO
         BLSCAMM2 &LIST,&NUM       EXTRACT CHARACTER STRING        @D1C
         GBLA  &BLSCAGA            PARAMETER SUBLIST COUNT
         GBLC  &BLSCAGC            PARAMETER SUBLIST CHARACTER STRING
         LCLA  &CTR                CURRENT LIST BYTE COUNTER
         LCLA  &ELM                CURRENT ELEMENT NUMBER
         LCLA  &START              CURRENT ELEMENT START BYTE
         LCLA  &LEN                LIST LENGTH
         LCLC  &STR                LIST CHARACTER STRING
&BLSCAGA SETA  0                   RETURN CODE
&BLSCAGC SETC  ''                  RETURNED STRING
&CTR     SETA  1                   CURRENT BYTE COUNTER
&STR     SETC  '&LIST'             LIST CHAR STRING
&LEN     SETA  K'&STR              LIST LENGTH
         AIF   (&LEN EQ 0).END     NULL LIST
.SK1     AIF   ('&STR' EQ '()').END   NULL LIST
.SK2     AIF   ('&STR'(1,1) EQ '(').SK3  MULTI ELEMENT LIST CANDIDATE
&BLSCAGC SETC  '&LIST'             ONE ELEMENT LIST
         AGO   .END
.SK3     ANOP
&ELM     SETA  1                   SET UP ELEMENT COUNT LOOP
&START   SETA  2                   SET ELEMENT START BYTE
         AGO   .LPTST              ENTER LOOP
.LOOP    AIF   ('&STR'(&CTR,1) NE '(').SK4
         MNOTE 8,'LIST CONTAINS AN INNER SUBLIST'
         AGO   .ERR                ERROR EXIT
.SK4     AIF   ('&STR'(&CTR,1) NE ',').SK5  NOT ELEMENT END
         AIF   (&ELM EQ &NUM).MOVE  FOUND REQUESTED ELEMENT
&START   SETA &CTR+1               SET NEW ELEMENT START
&ELM     SETA  &ELM+1              BUMP ELEMENT COUNT
         AGO   .LPTST
.SK5     AIF   ('&STR'(&CTR,1) NE ')').LPTST  NOT END OF PLIST
         AIF   (&CTR EQ &LEN).SK6   LIST END MATCHES LOGICAL END
         MNOTE 8,'INVALID LIST FORMAT'
         AGO   .ERR
.SK6     ANOP
         AIF   (&ELM EQ &NUM).MOVE  REQUESTED SUBLIST ENTRY
         AGO   .END                 REQUESTED ENTRY NOT FOUND
.LPTST   ANOP
&CTR     SETA  &CTR+1               BUMP CURRENT BYTE COUNTER
         AIF   (&CTR LE &LEN).LOOP  NOT END OF LIST
         MNOTE 8,'INVALID LIST FORMAT'
         AGO   .ERR
.MOVE    ANOP
&LEN     SETA  &CTR-&START          MOVE LENGTH
&BLSCAGC SETC  '&STR'(&START,&LEN)  PARM SUBLIST ENTRY
         AGO   .END
.ERR     ANOP
&BLSCAGA SETA  -1                   INDICATE ERROR TO CALLER
.END     MEND
