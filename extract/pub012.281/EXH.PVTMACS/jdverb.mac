         MACRO
&NAME    JDVERB &VERB=,&CNTLVRB=NO                                 @H1A
.**********************************************************************
.*                                                                    *
.*   MODULE NAME = IEFJDVRB                                        @H1C
.*                                                                    *
.*   DESCRIPTIVE NAME = JCL VERB DEFINITION MACRO                     *
.*                                                                    *
.*   COPYRIGHT = 5740-XYN COPYRIGHT IBM CORP. 1982                    *
.*                LICENSED MATERIAL - PROGRAM                         *
.*                PROPERTY OF IBM, REFER TO COPYRIGHT                 *
.*                INSTRUCTIONS FORM G120-2083                         *
.*                                                                    *
.*   FUNCTION = JDVERB GENERATES  THE VERB SECTION IN THE JDT         *
.*              THAT WILL  BE ASSOCIATED  WITH AN EXISTING DD         *
.*              STATEMENT OR THAT WILL DEFINE A NEW VERB.             *
.*                                                                    *
.*     NOTES = NONE                                                   *
.*                                                                    *
.*      DEPENDENCIES = NONE                                           *
.*                                                                    *
.*      RESTRICTIONS = THE  JDVERB MACRO  IS  ONLY  ALLOWED TO        *
.*                     FOLLOW   THE  JDTMAC,  JDPARM,  ENDSUB,        *
.*                     OR  ANOTHER   JDVERB  MACRO.  ONLY  THE     @H1A
.*                     VERB  'DD'   OR  A  NEW  VERB  WILL  BE        *
.*                     SUPPORTED WITH THIS VERSION; JOB, EXEC,        *
.*                     PROC AND  PEND  ARE NOT  SUPPORTED  AND        *
.*                     WILL BE FLAGGED AS AN ERROR.                   *
.*                                                                    *
.*   ENTRY POINT = N/A                                                *
.*                                                                    *
.*   MODULE TYPE = MACRO                                              *
.*                                                                    *
.*   ENTRY POINT = NONE                                               *
.*                                                                    *
.*     PROCESSOR = ASSEMBLER                                          *
.*                                                                    *
.*     INPUT = THE FOLLOWING MAY BE SPECIFIED AS PARAMETERS TO JDTMAC *
.*                                                                    *
.*             PARAMETER         DESCRIPTION                          *
.*                                                                    *
.*             - &NAME           USED ONLY ON THE MACRO EXPANSION     *
.*             - &VERB=          THE VERB NAME (E.G. VERB=DD)         *
.*             - &CNTLVRB=       THIS VERB IS A CONTROL STATEMENT  @H1A
.*                                                                    *
.*     OUTPUT = VERB SECTION OF JDT                                   *
.*                                                                    *
.*   EXIT NORMAL =                                                    *
.*   CONDITION-NO ERRORS DETECTED                                     *
.*   OUTPUT    -NO MNOTE ISSUED. SEVERITY CODE=0.                     *
.*                                                                    *
.*   EXIT ERROR =                                                     *
.*   CONDITION-SEVERE ERROR DETECTED BY THE JDVERB MACRO.             *
.*   OUTPUT    -ERROR MNOTES WITH SEVERITY CODE 12                    *
.*              ISSUED WHEN THE PARAMETER IS INCORRECTLY SPECIFIED    *
.*              OR THE MACRO IS NOT IN THE CORRECT ORDER (SEE         *
.*              RESTRICTIONS). THE MACRO WILL ATTEMPT TO SCAN FOR     *
.*              ADDITIONAL ERRORS. (SEE MESSAGES BELOW)               *
.*                                                                    *
.*   EXIT ERROR =                                                     *
.*   CONDITION-TERMINATING ERROR DETECTED BY THE JDVERB MACRO.        *
.*   OUTPUT    -ERROR MNOTES WITH SEVERITY CODE 16                    *
.*              ISSUED WHEN PARAMETER IS SPECIFIED WITH AN INVALID    *
.*              VERB OR IF NO JDTMAC MACRO HAS BEEN USED TO START     *
.*              THE JDTS (SEE RESTRICTIONS). THE MACRO WILL ATTEMPT   *
.*              TO SCAN FOR ADDITIONAL ERRORS.                        *
.*              (SEE MESSAGES BELOW)                                  *
.*                                                                    *
.*   EXTERNAL REFERENCES = JDSCAN MACRO IS USED FOR VALIDATION        *
.*                         OF THE VERB KEYWORD PARAMETER.             *
.*                         I.E., 1-8 CHARACTERS, FIRST CHARACTER      *
.*                         MUST BE ALPHABETIC OR NATIONAL (@,#,$);    *
.*                         2-7 MUST BE ALPHANUMERIC OR NATIONAL.   @R2A
.*                                                                    *
.*   TABLES = NONE                                                    *
.*                                                                    *
.*   MESSAGES = (MNOTES)                                              *
.*                                                                    *
.*   ERROR MNOTES =                                                   *
.*                                                                    *
.*    MNOTE 12,'REQUIRED VERB PARAMETER IS NOT SPECIFIED'             *
.*    MNOTE 12,'VERB SPECIFICATION EXCEEDS 8 CHARACTERS'              *
.*    MNOTE 12,'VERB=&VERB HAS INVALID CHARACTERS'                 @R2A
.*    MNOTE 12,'JDVERB MACRO SPECIFIED OUT OF ORDER'                  *
.*    MNOTE 12,'AN ENDSUB MACRO EXPECTED TO END A DEFINED SUBLIST,    *
.*              BUT WAS NOT SPECIFIED'                                *
.*    MNOTE 12,'CNTLVRB=&CNTLVRB INVALID. MUST BE YES, NO, Y OR N' @H1A
.*                                                                    *
.*    MNOTE 16,'VERB=&VERB INVALID. JOB, EXEC, PROC, AND PEND NOT     *
.*             SUPPORTED'                                             *
.*    MNOTE 16,'JDTMAC MACRO MUST BE FIRST MACRO SPECIFIED BUT IS NOT'*
.*                                                                    *
.*   CHANGE ACTIVITY = H0,R1,R2,H1,R3                              @R3C
.*    $H0= EXTJCL  JBB1328 810315 PDC5: SUPPORT FOR EXTENDED JCL      *
.*    $R1= PPP0011 JBB1328 810508 PDC5: ASSEMBLER ERROR            @R1A
.*    $R2= PPP0015 JBB1328 810508 PDC5: MISSING MNOTE              @R2A
.*    $H1= EXTJCL  JBB1328 811202 PDC5: SUPPORT FOR EXTENDED JCL   @H1A
.*    $R3= PPP0101 JBB1328 820521 PDC5: POSSIBLE ALIGNMENT ERROR   @R3A
.*                                                                    *
.**********************************************************************
.* A 000000-999999                                                 @H0A
.* A VERB KEYWORD PARAMETER LEFT BLANKS CAUSED ASSEMBLER ERRORS    @R1A
.* A VERB KEYWORD PARAMETER WITH INVALID CHARACTERS DID NOT CAUSE
.*   THE EXPECTED MNOTE.                                           @R2A
.* A ADDED KEYWORD CNTLVRB                                         @H1A
.* A ADDED PROCESSING FOR BYTE OFFSET AND BLOCK ID INITIALIZATION  @H1A
.* A ADDED AN OFFSET FIELD TO NEXT KEY                             @H1A
.* C CHANGED PROCESSING TO ALLOW VERBS TO FOLLOW ONE ANOTHER WITHOUT
.*   NEEDING A KEY AND PARAMETER                                   @H1A
.* D FULLWORD ALIGNMENT BOUNDARY OF DS 0F FROM DEFINITION          @R3A
.*--------------------------------------------------------------------*
.*  GLOBALS USED IN THIS MACRO THAT MAY BE ACTIVE OR BE MADE ACTIVE   *
.*  RESPECTIVELY: JDTMAC, JDVERB, JDKEY, JDPARM, JDSUB, ENDSUB AND    *
.*                ENDJDT.                                             *
.*--------------------------------------------------------------------*
         GBLB  &JACT,&VACT,&KACT,&PACT,&SPACT,&EPACT,&EJACT
         GBLA  &LEN              USED FOR DETERMINING MACRO LENGTH
         GBLA  &BYTEOFF,&BYTECTR USED FOR BYTE OFFSET CALCULATIONS @H1A
         GBLA  &BLKID            USED FOR BLOCK ID CALCULATION     @H1A
         GBLB  &F0               USED FOR CNTLVRB                  @H1A
.*--------------------------------------------------------------------*
.*  GLOBALS USED FOR OFFSETS - VERB COUNTERS ONE AND TWO              *
.*--------------------------------------------------------------------*
         GBLA  &V1,&V2,&K1,&K2,&P1,&P2  VARIABLES FOR OFFSETS
.*---------------------------------------------------------------* @R2A
.*  GLOBALS USED ERROR INDICATORS FROM INTERNAL MACRO JDSCAN.    * @R2A
.*  &$ERRBAD IS THE INVALID CHARACTER ERROR BIT.                 * @R2A
.*  &$ERRBIG IS THE GREATER THAN 8 CHARACTERS ERROR BIT.         * @R2A
.*  &$ERRBLK IS THE REQUIRED VERB PARAMETER BLANKS ERROR BIT.    * @R2A
.*---------------------------------------------------------------* @R2A
         GBLB  &$ERRBAD,&$ERRBIG,&$ERRBLK                          @R2A
.*---------------------------------------------------------------* @R2A
.*  GLOBAL USED FOR PASSING VALUE TO INTERNAL MACRO JDSCAN.      * @R2A
.*---------------------------------------------------------------* @R2A
         GBLC  &ARGU                                               @R2A
.*--------------------------------------------------------------------*
.*   ONE OF THE FOLLOWING GLOBALS SHOULD BE ON OR 'ACTIVE'            *
.*--------------------------------------------------------------------*
         AIF   (&JACT EQ 1).ACTOFF
         AIF   (&VACT EQ 1).VRBEQU                                 @H1A
         AIF   (&PACT EQ 1 OR &EPACT EQ 1).SETEQU
.*--------------------------------------------------------------------*
.*   IF NONE OF THESE BITS ARE ON THEN THIS MUST BE THE FIRST MACRO.  *
.*--------------------------------------------------------------------*
         AIF   (&KACT EQ 0 AND &EJACT EQ 0 AND &SPACT EQ 0).NOJDT  @H1C
.*--------------------------------------------------------------------*
.*  JDVERB DID NOT FOLLOW PROPER MACRO-> MNOTE IT.                    *
.*--------------------------------------------------------------------*
         MNOTE 12,'JDVERB MACRO SPECIFIED OUT OF ORDER'
         AGO   .RESET
.*--------------------------------------------------------------------*
.*  JDTMAC MUST BE THE FIRST MACRO SPECIFIED, BUT IS NOT-> MNOTE IT.  *
.*--------------------------------------------------------------------*
.NOJDT   ANOP
       MNOTE 16,'JDTMAC MACRO MUST BE FIRST MACRO SPECIFIED BUT IS NOT'
         MEXIT
.*--------------------------------------------------------------------*
.*  IF VACT BIT IS ON HERE, MEANS THAT JDVERB IS FOLLOWING ANOTHER    *
.*  JDVERB. SET UP AN EQUATE TO ZERO THE PRECEEDING KEYWORD OFFSET.   *
.*--------------------------------------------------------------------*
.VRBEQU  ANOP                                                      @H1A
KY&K2    EQU   VB&V1                                               @H1A
&K1      SETA  &K1+1             UPDATE KEYWORD GLOBAL COUNTER ONE @H1A
&K2      SETA  &K2+1             UPDATE KEYWORD GLOBAL COUNTER TWO @H1A
         AGO   .SETUP                                              @H1A
.*--------------------------------------------------------------------*
.*  RESET APPROPRIATE ACTIVE GLOBAL BIT.                              *
.*  IF SPACT BIT IS ON HERE, MEANS THAT A SUBLIST WAS STARTED BUT     *
.*  THAT NO PARAMETERS (JDPARM MACROS) FOR IT HAD BEEN DEFINED YET.   *
.*--------------------------------------------------------------------*
.RESET   ANOP
&KACT    SETB  0                 RESET JDKEY  MACRO ACTIVE GLOBAL BIT
&EJACT   SETB  0                 RESET ENDJDT MACRO ACTIVE GLOBAL BIT
&SPACT   SETB  0                 RESET JDSUB  MACRO ACTIVE GLOBAL BIT
         AGO   .SETUP
.SETEQU  ANOP
KY&K2    EQU   KY&K1             ZEROES OFFSET FOR THE LAST KEYWORD AND
PM&P2    EQU   PM&P1             THE LAST PARAMETER OF PREVIOUS VERB
&K1      SETA  &K1+1             INCREMENT THE COUNTER FOR JDKEY
&K2      SETA  &K2+1             INCREMENT THE COUNTER FOR JDKEY
&P1      SETA  &P1+1             INCREMENT THE COUNTER FOR JDPARM
&P2      SETA  &P2+1             INCREMENT THE COUNTER FOR JDPARM
.*--------------------------------------------------------------------*
.*  RESET APPROPRIATE ACTIVE GLOBAL BIT.                              *
.*--------------------------------------------------------------------*
.ACTOFF  ANOP
&JACT    SETB  0                 RESET JDTMAC ACTIVE GLOBAL BIT
&PACT    SETB  0                 RESET JDPARM ACTIVE GLOBAL BIT
&EPACT   SETB  0                 RESET ENDSUB ACTIVE GLOBAL BIT
         AIF   (&SPACT EQ 0).SETUP
.*--------------------------------------------------------------------*
.*  JDVERB WAS SPECIFIED BEFORE A SUBLIST WAS CLOSED-> MNOTE IT.      *
.*--------------------------------------------------------------------*
         MNOTE 12,'AN ENDSUB MACRO EXPECTED TO END A DEFINED SUBLIST,  +
               BUT WAS NOT SPECIFIED'
&SPACT   SETB  0                 RESET JDSUB  MACRO ACTIVE GLOBAL BIT
.SETUP   ANOP
&BYTEOFF SETA  0                 RESET BYTE OFFSET FOR NEW VERB    @H1A
&BYTECTR SETA  0                 RESET BYTE COUNTER FOR NEW VERB   @H1A
&BLKID   SETA  &BLKID+1          SET UP THE VERBS BLOCK ID         @H1A
&VACT    SETB  1                 SET GLOBAL BIT ON FOR JDVERB ACTIVE
&V1      SETA  &V1+1             UPDATE VERB GLOBAL COUNTER ONE
&V2      SETA  &V2+1             UPDATE VERB GLOBAL COUNTER TWO
&LEN     SETA  &LEN+1            SETUP VARIABLE FOR OFFSET OF JDVERB
&ARGU    SETC  '&VERB'           SETUP ARGUMENT FOR PASSING        @R2A
         JDSCAN &ARGU                                              @R2A
         AIF   (&$ERRBAD EQ 1).BADVRB       INVALID CHARACTERS     @R2A
         AIF   (&$ERRBIG EQ 1).BIGVRB       EXCEEDS 8 CHARACTERS   @R2A
         AIF   (&$ERRBLK EQ 1).BLKVRB       MISSING PARAMETER      @R2A
         AGO   .CONTV                       NO ERRORS-CONTINUE     @R2A
.*---------------------------------------------------------------* @R2A
.* VERB PARAMETER HAS INVALID CHARACTERS-> MNOTE IT.             * @R2A
.*---------------------------------------------------------------* @R2A
.BADVRB  ANOP                                                      @R2A
         MNOTE 12,'VERB=&VERB HAS INVALID CHARACTERS'              @R2A
         AGO   .BLANKV                                             @R2A
.*--------------------------------------------------------------------*
.* VERB PARAMETER SPECIFICATION EXCEEDS MAX 8 CHARACTERS-> MNOTE IT.  *
.*--------------------------------------------------------------------*
.BIGVRB  ANOP                                                      @R2A
         MNOTE 12,'VERB SPECIFICATION EXCEEDS 8 CHARACTERS'
         AGO   .BLANKV                                             @R2A
.*--------------------------------------------------------------------*
.* VERB PARAMETER NOT SPECIFIED OR WAS SPECIFIED AS BLANKS-> MNOTE IT.*
.*--------------------------------------------------------------------*
.BLKVRB  ANOP                                                      @R2A
         MNOTE 12,'REQUIRED VERB PARAMETER IS NOT SPECIFIED'
         AGO  .BLANKV                                              @R2A
.*--------------------------------------------------------------------*
.*  VERBS JOB, EXEC, PROC, AND PEND ARE NOT SUPPORTED-> MNOTE IT.     *
.*--------------------------------------------------------------------*
.NOSUPT  ANOP                                                      @R2A
         MNOTE 16,'VERB=&VERB INVALID. JOB, EXEC, PROC, AND PEND NOT SU+
               PPORTED'
.BLANKV  ANOP                                                      @R2A
B&LEN    EQU   *                 BEGIN JDVERB LENGTH DEFINITION
VB&V1    DC    CL8' '            VERBNAME                          @R1A
         AGO   .JUMPV                                              @R1A
.*--------------------------------------------------------------------*
.* VERB PARAMETER HAS BEEN SPECIFIED CORRECTLY SO FAR. CHECK TO SEE   *
.* IF IT HAS BEEN SPECIFIED WITH 'JOB', 'EXEC', 'PROC', OR 'PEND'.    *
.* IF IT HAS, THEN GO MNOTE IT. THEY ARE UNSUPPORTED VERB NAMES.      *
.*--------------------------------------------------------------------*
.CONTV   ANOP
         AIF   ('&VERB' EQ 'JOB' OR '&VERB' EQ 'EXEC' OR '&VERB' EQ    +
               'PROC' OR '&VERB' EQ 'PEND').NOSUPT                 @R2C
B&LEN    EQU   *                 BEGIN JDVERB LENGTH DEFINITION
VB&V1    DC    CL8'&VERB'        VERBNAME
.JUMPV   ANOP                                                      @R1A
         DC    AL2(VB&V2-VB&V1)  OFFSET TO NEXT VERB (ZERO IF NONE)
         DC    AL2(KY&K2-VB&V1)  OFFSET TO FIRST KEYWORD           @H1A
         DC    XL1'01'           VERSION NUMBER OF JDVERB SECTION
.*---------------------------------------------------------------* @H1A
.* CHECK TO SEE IF THE CONTROL VERB PARAMETER IS SPECIFIED.      * @H1A
.* THE DEFAULT IS NO ('NO' MEANS STANDARD JCL STATEMENTS BEING   * @H1A
.* DEFINED. 'YES' MEANS NON-JCL STATEMENTS BEING DEFINED).       * @H1A
.*---------------------------------------------------------------* @H1A
         AIF   ('&CNTLVRB' EQ 'YES' OR '&CNTLVRB' EQ 'Y' OR '&CNTLVRB' +
               EQ 'NO' OR '&CNTLVRB' EQ 'N').SETCNTL               @H1A
.*---------------------------------------------------------------* @H1A
.* CNTLVRB PARAMETER IS SPECIFIED INCORRECTLY-> MNOTE IT.        * @H1A
.*---------------------------------------------------------------* @H1A
      MNOTE 12,'CNTLVRB=&CNTLVRB INVALID. MUST BE YES, NO, Y OR N' @H1A
.SETCNTL ANOP                                                      @H1A
&F0      SETB  ('&CNTLVRB' EQ 'YES' OR '&CNTLVRB' EQ 'Y')          @H1A
         DC    BL1'&F0.0000000'  CONTROL STATEMENT IF ON (NON-JCL) @H1A
         DC    XL2'00'           RESERVED                          @H1C
E&LEN    EQU   *                 END JDVERB LENGTH DEFINITION
         SPACE
         MEND
