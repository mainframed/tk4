         MACRO
&NAME    ENDJDT
.**********************************************************************
.*                                                                    *
.*   MODULE NAME = IEFJDEND                                        @H1C
.*                                                                    *
.*   DESCRIPTIVE NAME =  END JCL DEFINITION TABLE                     *
.*                                                                    *
.*   COPYRIGHT =  5740-XYN COPYRIGHT IBM CORP. 1982                   *
.*                LICENSED MATERIAL - PROGRAM,                        *
.*                PROPERTY OF IBM, REFER TO COPYRIGHT                 *
.*                INSTRUCTIONS FORM G120-2083                         *
.*                                                                    *
.*   FUNCTION =  ENDJDT MACRO SPECIFIES THE END OF THE JCL            *
.*               DEFINITION TABLE  SPECIFIED BY THE JDTMAC            *
.*               MACRO.  IT  SETS UP  EQUATES  FOR JDVERB,            *
.*               JDKEY AND JDPARM MACRO'S OFFSETS.                    *
.*                                                                    *
.*     NOTES = NONE                                                   *
.*                                                                    *
.*      DEPENDENCIES = NONE                                           *
.*                                                                    *
.*      RESTRICTIONS = THE ENDJDT MACRO CAN ONLY FOLLOW JDPARM,       *
.*                     JDVERB, OR AN ENDSUB MACRO.                 @H1C
.*                                                                    *
.*   ENTRY POINT = N/A                                                *
.*                                                                    *
.*   MODULE TYPE = MACRO                                              *
.*                                                                    *
.*     PROCESSOR = ASSEMBLER                                          *
.*                                                                    *
.*     INPUT = THE FOLLOWING MAY BE SPECIFIED AS PARAMETERS TO ENDJDT *
.*                                                                    *
.*           PARAMETER          DESCRIPTION                           *
.*                                                                    *
.*           - &NAME            ONLY USED AS A LABEL ON THE EXPANSION.*
.*                                                                    *
.*     OUTPUT = EQUATES TO DESIGNATE THE LAST VERB, KEYWORD,          *
.*              AND PARAMETER BY MAKING THE "NEXT" OFFSET OF EACH     *
.*              BE EQUAL TO THE "PRESENT" OFFSET. JDVERB, JDKEY,      *
.*              AND JDPARM USE THESE VARIABLES IN AN A-CON.           *
.*                                                                    *
.*              FOR EXAMPLE- JDPARM USES:                             *
.*                PM&P1   DC    AL2(PM&P2-PM&P1) TO CREATE ITS        *
.*                OFFSET. THIS MAKES THE DC IN THE LAST MACROS OF     *
.*                EACH TO GENERATE A ZERO.                            *
.*                                                                    *
.*              THE EQUATES ARE AS FOLLOWS IF PRECEEDED BY JDPARM  @H1C
.*              OR ENDSUB:                                         @H1A
.*                                                                    *
.*              VB&V2   EQU   VB&V1                                   *
.*              KY&K2   EQU   KY&K1                                   *
.*              PM&P2   EQU   PM&P1                                   *
.*                                                                    *
.*              THE EQUATES ARE AS FOLLOWS IF PRECEEDED BY JDVERB: @H1A
.*                                                                    *
.*              VB&V2   EQU   VB&V1                                @H1A
.*              KY&K2   EQU   VB&K1                                @H1A
.*                                                                    *
.*   EXIT NORMAL =                                                    *
.*   CONDITION-NO ERRORS DETECTED                                     *
.*   OUTPUT    -NO MNOTE ISSUED. SEVERITY CODE=0.                     *
.*                                                                    *
.*   EXIT ERROR =                                                     *
.*   CONDITION-SEVERE ERROR DETECTED BY THE ENDJDT MACRO.             *
.*   OUTPUT    -ERROR MNOTES WITH SEVERITY CODE 12                    *
.*              ISSUED WHEN MACRO IS NOT INVOKED IN THE CORRECT       *
.*              ORDER (SEE RESTRICTIONS AND MESSAGES)                 *
.*                                                                    *
.*   EXIT ERROR =                                                     *
.*   CONDITION-TERMINATING ERROR DETECTED BY THE ENDJDT MACRO.        *
.*   OUTPUT    -ERROR MNOTES WITH SEVERITY CODE 16                    *
.*              ISSUED WHEN JDTMAC MACRO IS NOT INVOKED FIRST TO      *
.*              START JDTS. (SEE RESTRICTIONS AND MESSAGES)           *
.*                                                                    *
.*   EXTERNAL REFERENCES = N/A                                        *
.*                                                                    *
.*   TABLES = NONE                                                    *
.*                                                                    *
.*   MESSAGES = (MNOTES)                                              *
.*                                                                    *
.*   ERROR MNOTES =                                                   *
.*                                                                    *
.*    MNOTE 12,'ENDJDT MACRO SPECIFIED OUT OF ORDER'                  *
.*                                                                    *
.*    MNOTE 16,'JDTMAC MACRO MUST BE FIRST MACRO SPECIFIED BUT IS NOT'*
.*                                                                    *
.*   CHANGE ACTIVITY = H0,H1                                       @H1C
.*    $H0= EXTJCL JBB1328 810315 PDC5: SUPPORT FOR EXTENDED JCL       *
.*    $H1= EXTJCL JBB1328 811202 PDC5: SUPPORT FOR EXTENDED JCL    @H1A
.*                                                                    *
.**********************************************************************
.* A 000000-999999                                                 @H0A
.* A EQUATES ALLOW A VERB WITHOUT A KEYWORD/PARAMETER COMBINATION  @H1A
         GBLA  &V1,&V2,&K1,&K2,&P1,&P2  VARIABLES FOR OFFSETS
.*                               V=VERB, K=KEYWORD, P=PARAMETER COUNTER
.*--------------------------------------------------------------------*
.*  GLOBALS USED IN THIS MACRO THAT MAY BE ACTIVE OR BE MADE ACTIVE   *
.*  RESPECTIVELY: JDTMAC, JDVERB, JDKEY, JDPARM, JDSUB, ENDSUB AND    *
.*                ENDJDT.                                             *
.*--------------------------------------------------------------------*
         GBLB  &JACT,&VACT,&KACT,&PACT,&SPACT,&EPACT,&EJACT
.*--------------------------------------------------------------------*
.*       CHECK TO SEE IF MACRO SPECIFIED OUT OF ORDER                 *
.*--------------------------------------------------------------------*
         AIF   (&VACT EQ 1).VRBEQU                                 @H1A
         AIF   (&PACT EQ 1 OR &EPACT EQ 1).ALLEQU                  @H1C
.*--------------------------------------------------------------------*
.*       IF NONE OF THE FOLLOWING BITS ARE ON THEN THIS MUST BE THE   *
.*       FIRST MACRO INVOKED IN THE JDT DEFINITIONS.                  *
.*--------------------------------------------------------------------*
         AIF   (&JACT EQ 0 AND &KACT EQ 0 AND &SPACT EQ 0 AND &EJACT EQ+
                0).NOJDT      NONE ON?  GO MNOTE IT.               @H1C
.*--------------------------------------------------------------------*
.*       MACRO SPECIFIED OUT OF ORDER-> MNOTE IT.                     *
.*--------------------------------------------------------------------*
         MNOTE 12,'ENDJDT MACRO SPECIFIED OUT OF ORDER'
         AGO   .RESET
.*--------------------------------------------------------------------*
.*  JDTMAC MUST BE THE FIRST MACRO SPECIFIED, BUT IS NOT-> MNOTE IT.  *
.*--------------------------------------------------------------------*
.NOJDT   ANOP
       MNOTE 16,'JDTMAC MACRO MUST BE FIRST MACRO SPECIFIED BUT IS NOT'
         MEXIT
.*--------------------------------------------------------------------*
.*       EQUATES FOR LAST VERB TO MAKE THE OFFSETS OF THE NEXT VERB   *
.*       AND KEYWORD EQUAL TO ZEROES.                                 *
.*--------------------------------------------------------------------*
.VRBEQU  ANOP                                                      @H1A
VB&V2    EQU   VB&V1             ZERO FOR THE LAST VERB OFFSET     @H1A
KY&K2    EQU   VB&V1             ZERO FOR THE LAST KEYWORD OFFSET  @H1A
         AGO   .RESET                                              @H1A
.*--------------------------------------------------------------------*
.*       EQUATES FOR LAST VERB, KEYWORD, AND PARAMETER TO MAKE THE    *
.*       OFFSETS OF THOSE EQUAL TO ZEROES.                            *
.*--------------------------------------------------------------------*
.ALLEQU  ANOP                                                      @H1A
VB&V2    EQU   VB&V1            ZERO FOR THE LAST VERB OFFSET      @H1M
KY&K2    EQU   KY&K1            ZERO FOR THE LAST KEYWORD OFFSET   @H1M
PM&P2    EQU   PM&P1            ZERO FOR THE LAST PARAMETER OFFSET @H1M
.*--------------------------------------------------------------------*
.*       RESET THE APPROPRIATE ACTIVE GLOBAL BIT.                     *
.*--------------------------------------------------------------------*
.RESET   ANOP
&JACT    SETB  0                 RESET JDT MACRO GLOBAL BIT
&VACT    SETB  0                 RESET VERB GLOBAL BIT
&KACT    SETB  0                 RESET KEYWORD GLOBAL BIT
&PACT    SETB  0                 RESET PARAMETER GLOBAL BIT
&SPACT   SETB  0                 RESET SUBLIST GLOBAL BIT
&EPACT   SETB  0                 RESET END SUBLIST GLOBAL BIT
&EJACT   SETB  1                 SET THIS MACROS GLOBAL BIT
         MEND
