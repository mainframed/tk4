         MACRO
&NAME    BRING &LOAD=,&CALL=,&DELETE=,&LIST=,&C=
         LCLA  &A,&B,&K,&L
         LCLC  &MOD,&MOD1
&B       SETA  &C+1
&L       SETA  0
&A       SETA  &C/8*128
*/*           TO LOAD, LOAD & CALL, OR DELETE A MODULE       @ZM00952*/
*/*PARAMETERS-                                               @ZM00952*/
*/*      LOAD=(MODNAME)                                      @ZM00952*/
*/*      CALL=(MODNAME)                                      @ZM00952*/
*/*      DELETE=(MODNAME)                                    @ZM00952*/
*/*      LIST=(PARAMETER LIST)                               @ZM00952*/
*/*      C=(N)                                               @ZM00952*/
*/*           (MODNAME) IS THE EIGHT CHARACTER MODULE NAME   @ZM00952*/
*/*                     FROM ANYMOD MACRO OR TWO CHAR SUFFIX @ZM00952*/
*/*                     FROM IFDMOD MACRO. IT CAN BE A SYMBOL@ZM00952*/
*/*                     THAT IS THE ADDR OF THE ITEM, THE    @ZM00952*/
*/*                     ACTUAL NAME IN QUOTES, OR A REGISTER @ZM00952*/
*/*                     IN PARENTHESIS CONTAINING THE ADDR.  @ZM00952*/
*/*           (PARAMETER LIST) IS A PARAMETER LIST TO BE     @ZM00952*/
*/*                     PASSED TO THE CALLED PROGRAM. IT CAN @ZM00952*/
*/*                     BE A SYMBOL OR A REGISTER (IN        @ZM00952*/
*/*                     PARENTHESIS) CONTAINING THE ADDR.    @ZM00952*/
*/*           (N)       IS THE NUMBER OF CHARACTERS SENT     @ZM00952*/
*/*                     BY THE IFDMOD AND ANYMOD MACROS.     @ZM00952*/
*/*                     IT IS TWO FOR IFDMOD AND EIGHT       @ZM00952*/
*/*                     FOR ANYMOD.                          @ZM00952*/
*/***********************************************************@ZM00952*/
         AIF   (T'&LOAD NE 'O').LD1
         AIF   (T'&CALL NE 'O').CL1
         AIF   (T'&DELETE NE 'O').DE1
         MNOTE 8,'KEYWORD LOAD, CALL, OR DELETE MISSING'
         MEXIT
.LD1     ANOP
&A       SETA  &A+32
&MOD     SETC  '&LOAD'
&K       SETA  K'&LOAD
         AIF   (&K LT &B).OK
&L       SETA  &K-&C
&MOD1    SETC  '&LOAD'(&B,&L)
         AGO   .OK
.CL1     ANOP
&MOD     SETC  '&CALL'
&K       SETA  K'&CALL
         AIF   (&K LT &B).OK
&L       SETA  &K-&C
&MOD1    SETC  '&CALL'(&B,&L)
         AGO   .OK
.DE1     ANOP
&A       SETA  &A+16
&MOD     SETC  '&DELETE'
&K       SETA  K'&DELETE
         AIF   (&K LT &B).OK
&L       SETA  &K-&C
&MOD1    SETC  '&DELETE'(&B,&L)
.OK      AIF   (&L EQ 0).OKA
         AIF   ('&MOD'(1,1) EQ '(' OR '&MOD'(1,1) EQ '''').OKA1
         AIF   (&K LT 9).OKA
.ER1A    ANOP
&MOD     SETC  '&MOD'(1,&C)
.ER1B    MNOTE 8,'TOO MANY CHARACTERS IN &MOD&MOD1'
         MEXIT
.OKA1    AIF   (&L LT 3).OKA
&MOD     SETC  '&MOD'(2,&C-1)
&MOD1    SETC  '&MOD1'(1,&L-1)
         AGO   .ER1B
.OKA     ANOP
&NAME    DS    0H
         AIF   (T'&LIST EQ 'O').SKP1
         AIF   ('&LIST'(1,1) NE '(').SKP1
         ST    &LIST.,L&SYSNDX .        STORE LIST ADDRESS
.SKP1    AIF   ('&MOD'(1,1) NE '(').SKP2
         MVC   P&SYSNDX.+2(&C),0&MOD .   NAME INTO PARAMETER LIST
         AGO   .SKP3
.SKP2    AIF   ('&MOD'(1,1) EQ '''').SKP3
         MVC   P&SYSNDX.+2(&C),&MOD .    NAME INTO PARAMETER LIST
.SKP3    ANOP
         LA    R14,R&SYSNDX .
         L     R15,TAB28 .              TRANSIENT MANAGER ENTRY POINT
         CNOP  2,4 .
         BALR  R1,R15 .
P&SYSNDX DC    AL1(&A) .         INDICATORS FOR TRANSIENT MANAGER
         DC    AL1(2) .                 MACRO LEVEL
         AIF   ('&MOD'(1,1) EQ '''').SKP4
         DC    CL&C.' ' .           MODULE
         AGO   .SKP5
.SKP4    AIF   (&L EQ 0).SKP4A
&MOD     SETC  '&MOD'(1,&C)
         DC    CL&C&MOD&MOD1 .             MODULE NAME
         AGO   .SKP5
.SKP4A   ANOP
         DC    CL&C&MOD .    MODULE NAME
.SKP5    ANOP
         AIF   (T'&LIST EQ 'O').SKP3A
L&SYSNDX DC    A(&LIST) .
.SKP3A   ANOP
R&SYSNDX DS    0H .               RETURN ADDRESS
         MEND
