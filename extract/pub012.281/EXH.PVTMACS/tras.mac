         MACRO
&N       TRAS  &TYPE,&ASCB=,&ASID=,&WORK=
.* TRAS SET ESTABLISHES ADDRESSABILITY TO THE MEMORY INDICATED
.*    BY THE ASID OR ASCB SPECIFIED.
.* TRAS RESET RE ESTABLISHES ADDRESSABILITY TO THE MEMORY INDICATED
.*    BY THE ASCB POINTER IN PSAAOLD, IE THE ORIGINAL MEMORY
.*
.* TYPE = SET OR RESET.
.* ASCB = REGISTER CONTAINING ASCB PTR FOR SET. OMIT FOR RESET.
.* ASID = REGISTER CONTAINING ASID NUMBER FOR SET. OMIT FOR RESET.
.* WORK = SINGLE WORK REG FOR RESET.
.*        LIST OF TWO WORK REGS FOR SET AND ASID
.*        OMIT FOR SET AND ASCB.
.*
         AIF   ('&SYSECT' NE 'IECIOSCN').ER6   CHECK FOR AUTHORIZATION
         AIF   ('&TYPE' EQ 'RESET').RESET   RESET REQUEST
         AIF   ('&TYPE' NE 'SET').ER2  TYPE NOT SET OR RESET
         AIF   ('&ASCB' EQ '' AND '&ASID' EQ '').ER3 NEITHER ASCB
.*                                     NOR ASID WAS GIVEN
         AIF   ('&ASCB' NE '' AND '&ASID' NE '').ER4  BOTH GIVEN
         AIF   ('&ASCB' EQ '').ASID    TRANSFER IF ASID WAS GIVEN
         USING ASCB,&ASCB
&N       LCTL  1,1,ASCBSTOR            LOAD STOR FOR NEW MEMORY
         MEXIT
.ASID    AIF   (N'&WORK LT 2).ER1
&N       L     &WORK(1),16             -> CVT
         USING CVTMAP,&WORK(1)
         L     &WORK(1),CVTASVT        -> ASVT
         USING ASVT,&WORK(1)
         LR    &WORK(2),&ASID          ASID NUMBER
         SLA   &WORK(2),2              TIMES 4 IS OFFSET IN ASVT
         L     &WORK(1),ASVTENTY-4(&WORK(2))  -> ASCB
         USING ASCB,&WORK(1)
         LCTL  1,1,ASCBSTOR            LOAD STOR FOR NEW MEMORY
         DROP  &WORK(1)
         MEXIT
.RESET   AIF   (N'&WORK LT 1).ER1      NEED ONE WORK REG
         AIF   ('&ASCB' NE '' OR '&ASID' NE '').ER5 NEITHER MAY BE
.*                                     SPECIFIED FOR THE RESET OPTION
         USING PSA,0
&N       L     &WORK(1),PSAAOLD        -> ASCB IN CONTROL BEFORE SET
         USING ASCB,&WORK(1)
         LCTL  1,1,ASCBSTOR            RESTORE STOR TO ORIGINAL MEMORY
         DROP  &WORK(1)
         MEXIT
.ER1     MNOTE 8,'INSUFFICIENT WORK REGISTERS'
         MEXIT
.ER2     MNOTE 8,'TYPE NOT SET OR RESET'
         MEXIT
.ER3     MNOTE 8,'TYPE=SET && NEITHER ASCB NOR ASID GIVEN'
         MEXIT
.ER4     MNOTE 8,'BOTH ASCB && ASID GIVEN'
         MEXIT
.ER5     MNOTE 8,'TYPE=RESET && ASID OR ASCB GIVEN'
         MEXIT
.ER6     MNOTE 8,'NOT AUTHORIZED TO USE THIS MACRO'
         MEXIT
         MEND
