         MACRO
&NAME    FRRSCOPY  &OPT,&STACK,&OPT2                          @G385PPY
*    MACDATE    79.052
.* -000000-999999                                             @G382PPY
.*
.**********************************************************************
.*                                                                    *
.*    COPYRIGHT =                                                     *
.*        5740-XYN COPYRIGHT IBM CORP. 1980                           *
.*        LICENSED MATERIAL-PROGRAM, PROPERTY OF IBM,                 *
.*        REFER TO COPYRIGHT INSTRUCTIONS FORM                        *
.*        NUMBER G120-2083.                                           *
.*                                                                    *
.*     THE FRRSCOPY MACRO EXPANSION GENERATES CODE WHICH              *
.*     EITHER SAVES AN FRR STACK OR RESTORE AN FRR STACK              *
.*                                                                    *
.*                                                                    *
.* CHANGE-ACTIVITY                                                    *
.*         MVS/SYSTEM PRODUCT RELEASE 2                               *
.*                                                                    *
.**********************************************************************
.******* VERIFY VALID OPTION SPECIFIED. EITHER SAVE OR RESTORE
         AIF   ('&OPT' EQ '').FRR0005  DEFAULT OPT TO BE USED  @G385PPY
         AIF ('&OPT' EQ 'SAVE' OR '&OPT' EQ 'RESTORE').FRR0005 @G385PPY
         MNOTE 8,'INVALID OPTION &OPT SPECIFIED'
         MEXIT
.******* VERIFY VALID OPTION SPECIFIED. EITHER DELETE OR PLUS1 @G385PPY
.FRR0005 ANOP                                                  @G385PPY
         AIF   ('&OPT2' EQ '').FRR0010 DEFAULT OPT2 TO BE USED @G385PPY
         AIF   ('&OPT2' EQ 'DELETE' OR '&OPT2' EQ 'PLUS1').FRR0010     *
                                                               @G385PPY
         MNOTE 8,'INVALID OPTION &OPT2 SPECIFIED'              @G385PPY
         MEXIT                                                 @G385PPY
.******* VERIFY STACK SAVE AREA SPECIFIED
.FRR0010 ANOP
         AIF   ('&STACK' NE '').FRR0014
         MNOTE 8,'SAVE AREA ADDRESS MUST BE SUPPLIED'
         MEXIT
.******* PRODUCE CODE FOR MACRO
.FRR0014 ANOP
         AIF   ('&NAME' EQ '').FRR0015
&NAME    DS    0H
.FRR0015 ANOP
.******* PROCESS STACK PARMETER
         AIF   ('&STACK'(1,1) NE '(').FRR0024 REGISTER SPECIFIED ?
         AIF   ('&STACK(1)' EQ '0').FRR0040   SKIP CODE IF REG 0
         LR    0,&STACK(1) -           GET STACK ADDRESS
         AGO   .FRR0040
.FRR0024 ANOP
         LA    0,&STACK -              GET STACK ADDRESS
.******* PROCESS RESTORE OPTION
.FRR0040 ANOP
         AIF   ('&OPT' EQ '' OR '&OPT' EQ 'SAVE').FRR0042
         LR    14,0                MOVE SOURCE ADDRESS
         L     15,0(,14)           GET LENGTH TO RESTORE
         LA    0,4                 BUMP SOURCE ADDRESS
         ALR   14,0                PASSED LENGTH WORD
         LA    0,12                GET LENGTH OF FRR STACK HEADER      *
                                   WHICH IS CONSTANT AND DOES NOT HAVE *
                                   TO BE RESTORED
         AL    0,PSANSTK-PSA(0,0)  GET ADDRESS NORMAL STACK
         LR    1,15                SECOND LENGTH FOR MVCL
         MVCL  0,14                RESTORE STACK
         MEXIT                                                 @G385PPY
.******* PROCESS SAVE,DELETE OPTIONS                           @G385PPY
.FRR0042 ANOP
         AIF   ('&OPT2' EQ '' OR '&OPT2' EQ 'PLUS1').FRR0050   @G385PPY
         L     14,PSANSTK-PSA(0,0) GET ADDRESS NORMAL STACK    @G385PPY
         L     1,12(0,14)          ADDRESS OF CURRENT ENTRY    @G385PPY
         C     1,0(0,14)           IS STACK EMPTY ?            @G385PPY
         BE    IHB0&SYSNDX         BRANCH EMPTY                @G385PPY
         SL    1,8(0,14)           DELETE ENTRY FROM STACK     @G385PPY
IHB0&SYSNDX DS    0H                                           @G385PPY
         LR    15,0                COPY TARGET ADDRESS         @G385PPY
         ST    1,4(15)             SAVE UPDATED CURRENT ADDR   @G385PPY
         AL    1,8(0,14)           BUMP TO NEXT ENTRY          @G385PPY
         LA    15,12               GET LENGTH OF FRR STACK HEADER      *
                                   WHICH IS CONSTANT AND DOES NOT HAVE *
                                   TO BE SAVED (RESTORED)      @G385PPY
         ALR   14,15               BUMP PASSED CONSTANT AREA   @G385PPY
         SLR   1,14                GET LENGTH TO COPY          @G385PPY
         LR    15,0                COPY TARGET ADDRESS         @G385PPY
         ST    1,0(,15)            SAVE LENGTH OF COPIED STACK IN      *
                                   FIRST WORD OF TARGET AREA   @G385PPY
         LA    15,4                GET LENGTH TO SAVE MINUS CURRENT    *
                                   ENTRY ADDRESS FIELD WHICH HAS       *
                                   ALREADY BEEN SAVED          @G385PPY
         SR    1,15                                            @G385PPY
         ALR   0,15                BUMP PASSED LENGTH WORD AND CURRENT *
                                   ENTRY ADDRESS FIELD         @G385PPY
         ALR   0,15                                            @G385PPY
         ALR   14,15               BUMP STACK ADDRESS PASSED CURRENT   *
                                   ENTRY ADDRESS FIELD AS IT HAS       *
                                   ALREADY BEEN SAVED          @G385PPY
         LR    15,1                SECOND LENGTH FOR MVCL      @G385PPY
         MVCL  0,14                SAVE STACK                  @G385PPY
         MEXIT                                                 @G385PPY
.******* PROCESS SAVE,PLUS1 OPTIONS                            @G385PPY
.FRR0050 ANOP                                                  @G385PPY
         L     14,PSANSTK-PSA(0,0) GET ADDRESS NORMAL STACK
         L     1,12(0,14)          ADDRESS OF CURRENT ENTRY
         AL    1,8(0,14)           BUMP ADDRESS TO NEXT ENTRY
         C     1,4(0,14)           ANY MORE ENTRIES ON STACK ?
         BH    IHB0&SYSNDX         BRANCH NO MORE ENTRIES
         AL    1,8(0,14)           ADJUST FOR RACE CONDITION
IHB0&SYSNDX DS    0H
         LA    15,12               GET LENGTH OF FRR STACK HEADER      *
                                   WHICH IS CONSTANT AND DOES NOT HAVE *
                                   TO BE SAVED
         ALR   14,15               BUMP PASSED CONSTANT AREA
         SLR   1,14                GET LENGTH TO COPY
         LR    15,0                COPY TARGET ADDRESS
         ST    1,0(,15)            SAVE LENGTH OF COPIED STACK IN      *
                                   FIRST WORD OF TARGET AREA
         LA    15,4                BUMP PASSED LENGTH WORD
         ALR   0,15
         LR    15,1                SECOND LENGTH FOR MVCL
         MVCL  0,14                SAVE STACK
         MEXIT                                                 @G385PPY
         MEND
