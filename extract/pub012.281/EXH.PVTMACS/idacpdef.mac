* /*                                                               */
* /* MACCOMP Y-2 SC1DE/SJD61                                       */
* /* MACSTAT Y-2 73103/Y02113                                      */
* /*                                                               */
*/*       CHANGE LEVEL=00                                          */
*/*       DATE OF LAST CHANGE = 20 SEPT 71 ( 71*265 )              */
*/*       IDACPDEF, CHANNEL PROGRAM DEFINITION                     */
*/*                                                                */
*        DCL   1 IDACP   BASED,            /* CHAN PGM DEFINITION  */
*                2 IDACCW1,                /* SEEK HEAD            */
*                  3 IDACC1CC BIT(8),      /* COMMAND CODE         */
*                  3 IDACC1AD PTR(24),     /* COMMAND ADDRESS      */
*                  3 IDACC1ST BIT(16),     /* STATUS               */
*                  3 IDACC1NO FIXED(15),   /* RESIDUAL BYTE COUNT  */
*                2 IDACCW2,                /* SET SECTOR/NOP       */
*                  3 IDACC2CC BIT(8),      /* COMMAND CODE         */
*                  3 IDACC2AD PTR(24),     /* COMMAND ADDRESS      */
*                  3 IDACC2ST BIT(16),     /* STATUS               */
*                  3 IDACC2NO FIXED(15),   /* RESIDUAL BYTE COUNT  */
*                2 IDACCW3,                /* SEARCH ID            */
*                  3 IDACC3CC BIT(8),      /* COMMAND CODE         */
*                  3 IDACC3AD PTR(24),     /* COMMAND ADDRESS      */
*                  3 IDACC3ST BIT(16),     /* STATUS               */
*                  3 IDACC3NO FIXED(15),   /* RESIDUAL BYTE COUNT  */
*                2 IDACCW4,                /* TIC                  */
*                  3 IDACC4CC BIT(8),      /* COMMAND CODE         */
*                  3 IDACC4AD PTR(24),     /* COMMAND ADDRESS      */
*                  3 IDACCIOB PTR(31);     /* ADDRESS OF IOB       */
*        DCL   1 IDACCW5 BASED,            /* DATA TRANSFER DEFINED*/
*                2 IDACC5CC BIT(8),        /* COMMAND CODE         */
*                2 IDACC5AD PTR(24),       /* COMMAND ADDRESS      */
*                2 IDACC5ST BIT(16),       /* STATUS               */
*                2 IDACC5NO FIXED(15);     /* RESIDUAL BYTE COUNT  */
*        DCL   1 IDACCW6 BASED,            /* TIC/NOP SWITCH       */
*                2 IDACC6CC BIT(8),        /* COMMAND CODE         */
*                2 IDACC6AD PTR(24),       /* COMMAND ADDRESS      */
*                2 IDACC6ST BIT(16),       /* STATUS               */
*                2 IDACC6NO FIXED(15);     /* RESIDUAL BYTE COUNT  */
*%       DCL   SEEKHH   CHAR;              /* DEFINE HEAD SEEK     */
*%       DCL   SETSECT  CHAR;              /* SET SECTOR           */
*%       DCL   SIDEQ    CHAR;              /* SEARCH ID EQUAL      */
*%       DCL   TIC      CHAR;              /* TRANSFER IN CHANNEL  */
*%       DCL   NOP      CHAR;              /* NO OPERATION         */
*%       DCL   RDATA    CHAR;              /* READ DATA            */
*%       DCL   RCKD     CHAR;              /* RD COUNT, KEY, DATA  */
*%       DCL   WCKD     CHAR;              /* WRT COUNT, KEY, DATA */
*%       DCL   WD       CHAR;              /* WRITE DATA           */
*%       DCL   MT       CHAR;              /* MULTI-TRACK          */
*%       DCL   PCI      CHAR;              /* PROGRAMMED INTERRUPT */
*%       DCL   CC       CHAR;              /* COMMAND CHAINING     */
*%       DCL   DC       CHAR;              /* DATA CHAINING        */
*%       DCL   SILI     CHAR;              /* SUPPRESS INCORR LEN  */
*%       DCL   SKIP     CHAR;              /* SKIP DATA TRANSFER   */
*%       SEEKHH = '''1B''X';               /* SEEK HEAD            */
*%       SETSECT = '''23''X';              /* SET SECTOR           */
*%       SIDEQ = '''31''X';                /* SEARCH ID EQUAL      */
*%       TIC = '''08''X';                  /* TRANSFER IN CHANNEL  */
*%       NOP = '''03''X';                  /* NO OPERATION         */
*%       RDATA = '''06''X';                /* READ DATA            */
*%       RCKD = '''1E''X';                 /* RD COUNT, KEY, DATA  */
*%       WCKD = '''1D''X';                 /* WRT COUNT, KEY, DATA */
*%       WD = '''05''X';                   /* WRITE DATA           */
*%       MT = '''80''X';                   /* MULTI-TRACK          */
*%       PCI = '''08''X';                  /* PROGRAMMED INTERRUPT */
*%       CC = '''40''X';                   /* COMMAND CHAINING     */
*%       DC = '''80''X';                   /* DATA CHAINING        */
*%       SILI = '''20''X';                 /* SUPPRESS INCORR LEN  */
*%       SKIP = '''10''X';                 /* SKIP DATA TRANSFER   */
*%GOTO    IDACPSL2 ;
*/*                                                                */
*/*                 ASSEMBLER DSECT FOR IDACPDEF                   */
*/*                                                                */
         MACRO
         IDACPDEF  &DSECT=YES
.*         RELEASE 22 DELETIONS/CHANGES
.*         RELEASE 21 DELETIONS/CHANGES
.*         RELEASE 20 DELETIONS/CHANGES
* CONTROL
NOP      EQU   X'03'
SEEK     EQU   X'07'
SEEKCC   EQU   X'0B'
SEEKHH   EQU   X'1B'
SFM      EQU   X'1F'
SPACE    EQU   X'0F'
TIC      EQU   X'08'
*SEARCH  OPS
MT       EQU   X'80'
SHAEQ    EQU   X'39'
SIDEQ    EQU   X'31'
SIDHI    EQU   X'51'
SIDEQHI  EQU   X'71'
SKEQ     EQU   X'29'
SKHI     EQU   X'49'
SKEQHI   EQU   X'69'
SETSECT  EQU   X'23'
* READ   OPS
RHA      EQU   X'1A'
RDCNT    EQU   X'12'
RR0      EQU   X'16'
RDATA    EQU   X'06'
RKD      EQU   X'0E'
RCKD     EQU   X'1E'
RDSECT   EQU   X'22'
* WRITE  OPS
WCKD     EQU   X'1D'
WSCKD    EQU   X'01'
WD       EQU   X'05'
WKD      EQU   X'0D'
WR0      EQU   X'15'
* FLAGS
DC       EQU   X'80'
CC       EQU   X'40'
SILI     EQU   X'20'
SKIP     EQU   X'10'
PCI      EQU   X'08'
* CP OFFSETS
IDACPC4  EQU   4                        OFFSET TO SECOND HALF OF CCW
IDACPC6  EQU   6                        OFFSET TO FIRST BYTE OF COUNT
IDACPC7  EQU   7                        OFFSET TO SECOND BYTE OF COUNT
*
         AIF   ('&DSECT' EQ 'NO').IDACPM1
IDACP    DSECT                          AM/0 CHANNEL PROGRAM DEFINITION
         AGO   .IDACPM2
.IDACPM1 ANOP
IDACP    DS    0D                       AM/0 CHANNEL PROGRAM DEFINITION
.IDACPM2 ANOP
IDACCW1  CCW   SEEKHH,0,CC,6            SEEK TO CORRECT TRACK
IDACCW2  CCW   SETSECT,0,CC+SILI,1      SET SECTOR (NO OP FOR NON RPS)
IDACCW3  CCW   SIDEQ,0,CC,5             SEARCH ID (SILI AND COUNT OF 4
*                                       FOR REPLICATED INDEX READS)
IDACCW4  CCW   TIC,IDACCW3-IDACCW1,0,0  REPEAT UNTIL FOUND
IDACCIOB EQU   IDACCW4+IDACPC4          SECOND WORD USED FOR IOB PTR
*        THE VARIABLE SEGMENT OF THE CHANNEL PROGRAM FOLLOWS.
IDACCW5A CCW   RDATA+MT,0,CC,0          READ DATA/WRITE DATA--REPEATED
*        ONCE FOR EACH PHYSICAL BLOCK IN CONTROL INTERVAL OR ONCE FOR
*        EACH REPLICATED INDEX BLOCK.  IN NON-FIRST CHANNEL PROGRAM
*        SEGMENTS, THE PCI BIT MAY BE SET ALSO.  FOR FORMAT OR UPDATE
*        WRITE, CCW3 AND CCW4 ARE REPEATED FOR EACH CCW5 AND MT IS OFF.
IDACCW5B CCW   WD,0,CC,0                UPDATE WRITE VERSION
IDACCW5C CCW   WCKD,0,DC,8              FMT WRITE VERSION--WRITE COUNT
IDACCW5D CCW   WCKD,0,CC,0              FMT WRITE VERSION--WRITE DATA
IDACCW6  CCW   NOP,0,SILI,1             CC ON AND SET TO TIC IF CHANNEL
*        PROGRAM SEGMENTS ARE LINKED TOGETHER.
         MEND
*                     END OF ASSEMBLER DSECT
*%IDACPSL2: ;
