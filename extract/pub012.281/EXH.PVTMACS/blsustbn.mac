         MACRO
         BLSUSTBN &SCMD,&EP=
.*--------------------------------------------------------------------*
.* BLSUSTBN--DEFINE PROCESSING FOR ONE SUBCOMMAND                     *
.*--------------------------------------------------------------------*
         GBLA  &GA
         GBLC  &GP,&GS
         LCLA  &A1
         LCLB  &BDSD,&BDU,&BINV,&BPDR
         LCLC  &CE,&CT
         AIF   (K'&SCMD GE K'&GS).L01  PRIMARY SORT KEY
         MNOTE 8,'SUBCOMMAND NAMES NOT ORDERED BY LENGTH'
         MEXIT
.L01     AIF   (K'&SCMD EQ K'&GS).L02  NEW PRIMARY SORT KEY
&GS      SETC  ' '                     NO CURRENT SUBCOMMAND NAME
.L02     AIF   ('&SCMD' GE '&GS').L03  SUBCOMMAND NAMES ALPHABETIZED
         MNOTE 8,'SUBCOMMAND NAMES OF SAME LENGTH ARE NOT ALPABETIZED'
         MEXIT
.*------------ PROCESS POSITIONAL KEYWORDS --------------------------*.
.L03     ANOP
&GA      SETA  &GA+1                   DIMENSION OF SUBCOMMAND ARRAY
&GS      SETC  '&SCMD'                 CURRENT SUBCOMMAND NAME
&A1      SETA  2                       SUBSCRIPT FOR SEARCH
&CT      SETC  'L'                     IPCS SUBCOMMAND
.L04     AIF   (&A1 GT N'&SYSLIST).L13 POSITION OPERANDS SCANNED
         AIF   ('&SYSLIST(&A1)' NE 'DSD').L05
&BDSD    SETB  1                       REQUIRES DATA SET DIRECTORY
         AGO   .L12
.L05     AIF   ('&SYSLIST(&A1)' NE 'DUMP').L06
&BDU     SETB  1                       REQUIRES A DUMP DATA SET
         AGO   .L12
.L06     AIF   ('&SYSLIST(&A1)' NE 'INVALID').L07
&BINV    SETB  1                       INVALID IN IPCS
         AGO   .L12
.L07     AIF   ('&SYSLIST(&A1)' NE 'IPCS').L08
&CT      SETC  'L'                     IPCS SUBCOMMAND
         AGO   .L12
.L08     AIF   ('&SYSLIST(&A1)' NE 'PDR').L09
&BPDR    SETB  1                       REQUIRES PROBLEM DIRECTORY
         AGO   .L12
.L09     AIF   ('&SYSLIST(&A1)' NE 'SPECIAL').L10
&CT      SETC  'S'                     SPECIAL COMMAND
         AGO   .L12
.L10     AIF   ('&SYSLIST(&A1)' NE 'TSO').L11
&CT      SETC  'T'                     TSO COMMAND
         AGO   .L12
.L11     MNOTE 8,'''&SYSLIST(&A1)'' KEYWORD NOT SUPPORTED'
.L12     ANOP
&A1      SETA  &A1+1                   SUBSCRIPT FOR SEARCH
         AGO   .L04
.*------------ PROCESS SUBCOMMAND ENTRY POINT NAME ------------------*.
.L13     AIF   (T'&EP EQ 'O').L14      IMPLICIT ENTRY POINT NAME
&CE      SETC  '&EP'                   SUBCOMMAND ENTRY POINT NAME
         AGO   .L15
.L14     ANOP
&CE      SETC  '&SCMD'                 SUBCOMMAND ENTRY POINT NAME
.*------------ GENERATE SUBCOMMAND TABLE ENTRY ----------------------*.
.L15     SPACE
&GP.&GA  DS    0F                      SUBCOMMAND TABLE ENTRY
         DC    CL1'&CT'                TYPE OF SUBCOMMAND
         DC    BL2'&BINV&BPDR&BDSD&BDU.000000000000' ATTRIBUTES
&A1      SETA  K'&SCMD                 LENGTH OF SUBCOMMAND NAME
         DC    FL1'&A1'                LENGTH OF SUBCOMMAND NAME
         DC    CL8'&SCMD'              SUBCOMMAND NAME
         DC    CL8'&CE'                SUBCOMMAND ENTRY POINT NAME
         MEND
