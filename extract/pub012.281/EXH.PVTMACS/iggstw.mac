*  %GOTO STWL1;
         MACRO
         IGGSTW
.* /* START OF SPECIFICATIONS ****
.*
.*01  MODULE-NAME = IGGSTW
.*
.*01  COPYRIGHT = NONE
.*
.*01  STATUS = VS2 RELEASE 3 LEVEL 1/31/75
.*
.*01  DESCRIPTIVE-NAME = STOW WORK AREA
.*
.*01  FUNCTION = MAPS THE WORK AREA USED BY THE STOW MODULES
.*
.*01  MODULE-TYPE = MACRO
.*
.*02    MACRO-SIZE = N/A FOR A MAPPING MACRO
.*
.*02    PARAMETER-RECEIVED = NONE
.*
.*01  TABLES = NONE
.*
.*01  INNER-MACROS = IHAECB, IEZIOB
.*
.*01  SYSTEM-MACROS = NONE
.*
.*01  CHANGE-ACTIVITY = SEE BELOW
.*    VS2 RELEASE 2 DELETIONS
.*    VS2 RELEASE 3 DELETIONS
.*                                                              ZA01548
.*
.**** END OF SPECIFICATIONS ***/
.*/*
.*/ %STWL1: ;
* /* MACCOMP Y-2 SC1D2/SJ0082                                        */
* /* MACSTAT Y-2 75031/ZA01548                                       */
*/********************************************************************/
*/*                                                                  */
*/*      THIS MACRO MAPS THE WORK AREA USED BY THE STOW MODULES      */
*/*                                                                  */
*/*      VS2 RELEASE 3, 1/31/75, LEVEL=1                             */
*/*                                                                  */
*/*      METHOD OF ACCESS                                            */
*/*        BAL  - A USING ON THE DSECT NAME 'STWAREA' ESTABLISHES    */
*/*               ADDRESSABILITY TO ALL SYMBOLS.                     */
*/*        PL/S - REDEFINE A BASE FOR THE STRUCTURE BEFORE           */
*/*               REFERENCING ANY SYMBOLS WITHIN IT.                 */
*/*                                                                  */
*/********************************************************************/
* %; /*
.*/ %GOTO STWL2;  /*
STWAREA  DSECT
****
****     POINTERS AND USER DATA SAVE AREA
****
STWPARM  DS    A                        ADDR OF USER SUPPLIED ENTRY
*                                       NAME (LOWER OF 2 FOR CHANGE)
STWHIGH  DS    A                        ADDR OF HIGHER OF 2 USER SUPP-
*                                       LIED ENTRY NAMES (CHANGE ONLY)
         DS    H                        RESERVED
STWOFFLW DS    H                        OFFSET TO ADD, REPLACE, OR
*                                       DELETE LOCATION IN LOW BLOCK
STWOLDNM DS    CL8                      NAME OF ENTRY BEING DELETED
STWNEWNM DS    CL8                      NAME OF NEW ENTRY
STWTTR   DS    AL3                      MEMBER'S RELATIVE ADDRESS
STWCTTRN DS    XL1                      ALIAS BIT, NUMBER OF TTRNS,
*                                       AND LENGTH OF USER DATA
STWALIAS EQU   X'80'                    THIS MEMBER NAME IS AN ALIAS
STWDATA  DS    CL62                     USER DATA FOR ENTRY
         SPACE 2
****
****     FLAG, CONDITION, AND SWITCH BYTES
****
STWFLAG1 DS    B                        FIRST FLAG BYTE
*                       BIT DEFINITIONS
*        BIT   7                        RESERVED
STWCHNG  EQU   X'80'                    CHANGE FUNCTION, USED IN COMBI-
*                                       NATION WITH STWADD AND STWDEL
STWDEL   EQU   X'40'                    DELETE FUNCTION
STWREPL  EQU   X'20'                    REPLACE FUNCTION
STWADD   EQU   X'10'                    ADD FUNCTION
STWDRYRN EQU   X'08'                    DRY RUN BEING MADE ON DIRECTORY
STWFLOW  EQU   X'04'                    USED TO CONTROL PROGRAM FLOW
STWDCBWR EQU   X'02'                    LAST DCB OPERATION WAS A WRITE
STWMERGE EQU   X'01'                    2 INPUT BLKS=1 OUTPUT   ZA01548
*
STWRTN   DS    CL1                      RETURN CODE SAVE AREA
*
STWFLGLN EQU   *-STWAREA                LENGTH OF SAVE AND FLAG AREAS
         EJECT
****
****     CONTROL BLOCKS FOR STOW'S CHANNEL PROGRAMS
****
         DS    F                        IF ALREADY ON A DBLWORD  Y02072
*                                       GO TO NEXT FULLWORD BDY  Y02072
         DS    0D                       ALIGN TO A DBLWORD BDY   Y02072
         ORG   *-4                      ORG ECB IN FRONT OF IOB  Y02072
         IHAECB  DSECT=NO
         ORG   *-16                     SET STANDARD IOB BEHIND ECB
         IEZIOB  DSECT=NO
         SPACE 2
****
****     STOW'S CHANNEL PROGRAMS
****
         ORG   IOBR+1                   PLACE CHANNEL PROGRAMS
*                                       BEHIND THE IOB SEEK FIELD
STWINCP  DS    0D                       THIS CHANNEL PROGRAM READS THE
*                                       INITIAL TWO DIRECTORY BLOCKS
STWSRCH1 DS    2F                       SEARCH ID EQUAL
STWTIC11 DS    2F                       TRANSFER CONTROL TO SEARCH ID
STWRDCT1 DS    2F                       READ COUNT
STWSRKY1 DS    XL1                      SEARCH ON KEY EQUAL OR HIGH
STWKYAD1 DS    AL3                      KEY ADDRESS
         DS    XL4                      FLAGS AND BYTE COUNT
STWTIC12 DS    2F                       TRANSFER CONTROL TO READ COUNT
STWRDAT1 DS    2F                       READ DATA
STWRCKD1 DS    2F                       READ COUNT KEY DATA
         SPACE
STWWRDCP DS    0D                       THIS CHANNEL PROGRAM WRITES
*                                       AND READS DIRECTORY BLOCKS
STWSRCH2 DS    XL1                      SEARCH ID EQUAL
STWIDAD2 DS    AL3                      ID ADDRESS
         DS    XL4                      FLAGS AND BYTE COUNT
STWTIC2  DS    2F                       TRANSFER CONTROL TO SEARCH ID
STWWRKD1 DS    XL1                      WRITE KEY AND DATA
STWWRAD2 DS    AL3                      WRITE ADDRESS
STWWFLG2 DS    B                        FLAGS
STWCMDCH EQU   X'40'                    COMMAND CHAIN TO NEXT CCW
         DS    XL3                      BYTE COUNT
STWRCKD2 DS    XL1                      READ COUNT KEY DATA
STWRDAD2 DS    AL3                      READ ADDRESS
         DS    XL4                      FLAGS AND BYTE COUNT
STWRCKD3 DS    XL1                      READ COUNT KEY DATA
STWRDAD3 DS    AL3                      READ ADDRESS
STWRDFL3 DS    XL1                      CCW FLAGS               ZA01548
         DS    XL3                      BYTE COUNT              ZA01548
STWRCKD4 DS    XL1                      READ COUNT KEY DATA     ZA01548
STWRDAD4 DS    AL3                      READ ADDRESS            ZA01548
STWRDFL4 DS    XL1                      CCW FLAGS               ZA01548
         DS    XL3                      BYTE COUNT              ZA01548
         SPACE 2
****
****     COMMAND CODE EQUATES FOR CHANNEL PROGRAMS
****
STWIDEQ  EQU   X'31'                    SEARCH ON IDENTIFIER EQUAL
STWWRKD  EQU   X'0D'                    WRITE KEY AND DATA
STWRDKD  EQU   X'8E'                    READ KEY AND DATA
STWRDCKD EQU   X'9E'                    READ COUNT, KEY, AND DATA
STWSKIP  EQU   X'10'                    SUPPRESS DATA TRANSFER  ZA01548
         SPACE 2
****
****     STOW'S INPUT - OUTPUT BUFFERS.  FOR DETAILS SEE THE
****     BUFFER DSECT.
****
STWBUF1  DS    0F                       INITIALLY CONTAINS THE FIRST
         DS    CL276                    OF TWO DIRECTORY BLOCKS READ
STWBUF2  DS    0F                       INITIALLY CONTAINS THE SECOND
         DS    CL276                    OF TWO DIRECTORY BLOCKS READ
STWBUF3  DS    0F                       INITIALLY USED AS THE FIRST
         DS    CL276                    OUTPUT BUFFER
STWBUF4  DS    0F                       TEMPORARILY USED AS 1ST SA66881
         DS    CL276                    INPUT BUFFER FOR EXCP   SA66881
STWEND   DS    0D                       END OF WORK AREA
CORESIZE EQU   STWEND-STWAREA           SIZE OF STOW'S WORK AREA
         EJECT
BUFFER   DSECT
         SPACE
***********************************************************************
****                                                               ****
****     THIS DSECT MAPS STOW'S INPUT - OUTPUT BUFFERS             ****
****                                                               ****
***********************************************************************
         SPACE
BUFCNT   DS    0CL8                     COUNT FIELD CONTAINING
*                                       ABSOLUTE DISK ADDRESS
BUFCCHHR DS    CL5                      CCHHR FIELD
BUFKDD   DS    CL3                      KEY AND DATA LENGTH
BUFKEY   DS    CL8                      KEY FIELD (HIGHEST MEMBER NAME)
BUFDATA  DS    0CL256                   DATA AREA
BUFN     DS    H                        NUMBER OF BYTES USED IN
*                                       THIS DIRECTORY BLOCK
BUFENTRY DS    CL254                    DIRECTORY ENTRIES
BUFADDR  DS    A                        USED TO CHAIN THE BUFFERS
BUFPREFX EQU   L'BUFCNT+L'BUFKEY+L'BUFN  OFFSET TO BUFFER ENTRY AREA
         SPACE 4
RBPRFX   DSECT                          RESUME RB DSECT          Y02072
         ORG   RBEXSAVE
         SPACE
***********************************************************************
****                                                               ****
****     DSECT OF THE EXTENDED SAVE AREA OF THE SVRB               ****
****                                                               ****
***********************************************************************
         SPACE
XSAREG14 DS    A                        SAVE AREA FOR REG 14     Y02072
XSASTWWA DS    A                        ADDR OF STOW WORK AREA   Y02072
* XSAESTAE ESTAE MF=L                   ESTAE LIST AREA          Y02072
XSAESTAE ESTAE MF=L                     ESTAE LIST AREA          Y02072
XSARBXLN EQU   *-RBEXSAVE,(L'RBEXSAVE-(*-RBEXSAVE))  LENGTH OF   Y02072
*                                       EXTENDED SAVE AREA USED  Y02072
*                                       BY STOW                  Y02072
         MEND ,  */
* %STWL2: ;
*
*DCL STWAREA CHAR(1072) BASED;           /* STOW WORK AREA           */
*
*
*/********************************************************************/
*/*      STRUCTURE OF THE EXTENDED SAVE AREA OF THE STOW SVRB.       */
*/********************************************************************/
*
*DCL 1 * DEFINED(RBEXSAVE),
*      2 XSAREG14 PTR(31),              /* SAVE AREA FOR REG 14      */
*      2 XSASTWWA PTR(31),              /* ADDR OF STOW WARK AREA    */
*      2 XSAESTAE CHAR(16),             /* LIST FORM OF ESTAE MACRO  */
*        3 XSAGTRAC CHAR(8),            /* LIST FORM OF GTRACE MACRO */
*        3 XSARTMAD PTR(31) BDY(BYTE);  /* SAVE AREA FOR RTM RTRN ADR*/
