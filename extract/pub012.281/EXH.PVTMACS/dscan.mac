         MACRO
&LABEL   DSCAN &KEY=,&ALT=,&SUCC=,&ROUT=,&BLANKS=,&MAXLEN=,&TYPE=,     X
               &MINLEN=,&IO=YES,&COMMA=YES,&CALLRT=NO
         LCLB  &SW1,&SW2,&SW3,&SW4,&SW5,&SW6,&SW7,&SW8
&SW8     SETB  0
         AIF   ('&ALT' EQ '').ALTOUT
&LABEL   DC    A(&ALT)                 ADDRESS OF ALTERNATE
         AGO   .CONT1
.ALTOUT  ANOP
&LABEL   DC    A(0)                    INDICATE NO ALTERNATE
.CONT1   AIF   ('&SUCC' EQ '').SUCCOUT
         DC    A(&SUCC)                ADDRESS OF SUCCESSOR
         AGO   .CONT2
.SUCCOUT ANOP
         DC    A(0)                    INDICATE NO SUCCESSOR
.CONT2   AIF   ('&ROUT' EQ '').ROUTOUT
         DC    A(&ROUT)                ADDR OF EXIT ROUTINE
         AGO   .CONT5
.ROUTOUT ANOP
         DC    A(0)                    INDICATE NO EXIT ROUTINE
.CONT5   ANOP
         AIF   ('&TYPE' NE 'A').CONT6
&SW1     SETB  1
         AGO   .CONT75
.CONT6   AIF   ('&TYPE' NE 'N').CONT7
&SW2     SETB  1
         AGO   .CONT75
.CONT7   ANOP
         AIF   ('&TYPE' NE 'H').CONT72
&SW6     SETB  1
         AGO   .CONT75
.CONT72  ANOP
         AIF   ('&KEY' NE '').CONT75
&SW1     SETB  1
&SW2     SETB  1
.CONT75  AIF   ('&BLANKS' NE 'ON').CONT8
&SW3     SETB  1
.CONT8   AIF   ('&IO' NE 'YES').CONT85
&SW4     SETB  1
.CONT85  AIF   ('&COMMA' NE 'YES').CONT86
&SW5     SETB  1
.CONT86  AIF   ('&CALLRT' NE 'YES').CONT11                   @Z67SP00
&SW7     SETB  1                                             @Z67SP00
.CONT11  ANOP
         DC    B'&SW1.&SW2.&SW3.&SW4.&SW5.&SW6.&SW7.&SW8' TYPE SW
         AIF   ('&MAXLEN' EQ '').NOLN
         DC    AL1(&MAXLEN)            MAXIMUM ARGUMENT LENGTH
         AGO   .CONT9
.NOLN    ANOP
         DC    AL1(255)                DEFAULT MAX ARG LENGTH
.CONT9   AIF   ('&MINLEN' EQ '').NOMIN
         DC    AL1(&MINLEN)            MINIMUM ARGUMENT LENGTH
         AGO   .CONT10
.NOMIN   ANOP
         DC    AL1(01)                 DEFAULT MIN ARG LENGTH
.CONT10  AIF   ('&KEY' NE '').CONT4
         DC    H'0'                    PARM INDICATOR
         MEXIT
.CONT4   DC    AL1(IHX&SYSNDX-*-1)     LENGTH OF KEY
         DC    C&KEY
IHX&SYSNDX EQU      *
         MEND
