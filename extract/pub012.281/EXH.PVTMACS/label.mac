         MACRO
&NAME    LABEL &UNIT,&DDNAME,&LABEL,&NO,&LNGTH,&CNTRL,&LABELA,         X
               &UNLOAD,&MF=I
         LCLA  &A
         LCLB  &BB,&B(8)
         LCLC  &C
&BB      SETB  ('&UNLOAD' EQ 'UNLOAD')
         AIF   ('&MF(1)' EQ 'E').EROUT
         AIF   ('&MF' NE 'L').CNOP
&NAME    DS    0F                      BUILD LIST ON FULL WORD BDY
         AGO   .UNL
.CNOP    CNOP  0,4
&NAME    BAL   1,*+24                  BRANCH AROUND PARM LIST
.UNL     DC    AL1(192)                CONSTANT OF X'C0'
         DC    BL2'1&BB.0'             REWIND OR UNLOAD OPTION
         AIF   ('&UNIT' EQ '' AND '&MF' EQ 'L').ZERUN
         AIF   ('&UNIT' EQ '').E1
         AIF   ('&UNIT'(1,1) EQ '(').ZERUN
         DC    AL1(&UNIT)              UNIT NUMBER
         AGO   .DD
.ZERUN   DC    AL1(0)                  UNIT NUMBER
.DD      AIF   ('&DDNAME' EQ '' AND '&MF' EQ 'L').ZERDD
         AIF   ('&DDNAME' EQ '').E2
         AIF   ('&DDNAME'(1,1) EQ '(').ZERDD
         DC    A(&DDNAME)              DDNAME ADDRESS
         AGO   .LAB
.ZERDD   DC    A(0)                    DDNAME ADDRESS
.LAB     AIF   ('&LABEL' EQ '' AND '&MF' EQ 'L').ZERLB
         AIF   ('&LABEL' EQ '').E3
         AIF   ('&LABEL'(1,1) EQ '(').ZERLB
         DC    A(&LABEL)               LABEL ADDRESS
         AGO   .LEN
.ZERLB   DC    A(0)                    LABEL ADDRESS
.LEN     AIF   ('&LNGTH' EQ '' AND '&MF' EQ 'L').ZERLN
         AIF   ('&LNGTH' EQ '').E5
         AIF   ('&LNGTH'(1,1) EQ '(').ZERLN
         DC    AL2(&LNGTH)             LABEL LENGTH
         AGO   .NUM
.ZERLN   DC    AL2(0)                  LABEL LENGTH
.NUM     AIF   ('&NO' EQ '' AND '&MF' EQ 'L').ZERNO
         AIF   ('&NO' EQ '').E4
         AIF   ('&NO'(1,1) EQ '(').ZERNO
         DC    AL1(&NO)                NUMBER OF LABELS
         AGO   .CODE
.ZERNO   DC    AL1(0)                  NUMBER OF LABELS
.CODE    AIF   ('&CNTRL' EQ '').E6
&A       SETA  K'&CNTRL
&C       SETC  '&CNTRL'(&A,1)
&A       SETA  &C
         AIF   ('&CNTRL'(1,1) EQ '9').NINE
         AIF   (&A GE 3).E7
&B(1)    SETB  (&A EQ 2)
&B(2)    SETB  (&A EQ 1)
         AIF   ('&CNTRL'(1,1) NE '7').E7
         AIF   ('&CNTRL'(2,1) NE 'C' AND '&CNTRL'(2,1) NE 'E' AND '&CNTX
               RL'(2,1) NE 'T').E7
         AIF   ('&CNTRL'(2,1) EQ 'C').CONV
&B(3)    SETB  1
&B(5)    SETB  ('&CNTRL'(2,1) EQ 'T')
         AIF   (NOT &B(5)).PUT
         AIF   (K'&CNTRL NE 3).PUT
&B(4)    SETB  1
.PUT     AIF   ('&MF(1)' NE 'E').DCCD
&A       SETA  128*&B(1)+64*&B(2)+32*&B(3)+16*&B(4)+8*&B(5)+3
         MVI   15(1),&A                MOVE MODE TO PARM LIST
         AGO   .SVC
.DCCD    DC    BL1'&B(1)&B(2)&B(3)&B(4)&B(5).011'          MODE
         AIF   ('&LABELA' NE '').VAL                             PM0051
         AIF   ('&LABEL' EQ '' AND '&MF' EQ 'L').ZERVAL          PM0051
         AIF   ('&LABEL' EQ '').E3                               PM0051
         AIF   ('&LABEL'(1,1) EQ '(').ZERVAL                     PM0051
         DC    A(&LABEL)               ADDRESS OF EBCDIC LABEL   PM0051
         AGO   .PASSA                                            PM0051
.VAL     AIF   ('&LABELA'(1,1) EQ '(').ZERVAL                    PM0051
         DC    A(&LABELA)              ADDRESS OF EBCDIC LABEL   PM0051
         AGO   .PASSA                                            PM0051
.ZERVAL  DC    A(0)                    ADDRESS OF EBCDIC LABEL   PM0051
.PASSA   ANOP                                                    PM0051
         AIF   ('&MF' EQ 'L').MEXIT
         AIF   ('&UNIT' EQ '').NORUN
         AIF   ('&UNIT'(1,1) NE '(').NORUN
         STC   &UNIT(1),3(0,1)         UNIT NUMBER MOVED TO LIST
.NORUN   AIF   ('&DDNAME' EQ '').NORDD
         AIF   ('&DDNAME'(1,1) NE '(').NORDD
         ST    &DDNAME(1),4(0,1)       STORE DDNAME ADDRESS
.NORDD   AIF   ('&LABEL' EQ '').NORLB
         AIF   ('&LABEL'(1,1) NE '(').NORLB
         ST    &LABEL(1),8(0,1)        STORE VOLUME SET LABEL ADDR
.NORLB   AIF   ('&LNGTH' EQ '').NORLN
         AIF   ('&LNGTH'(1,1) NE '(').NORLN
         STH   &LNGTH(1),12(0,1)       STORE LABEL LENGTH
.NORLN   AIF   ('&NO' EQ '').NORNO                               PM0051
         AIF   ('&NO'(1,1) NE '(').NORNO                         PM0051
         STC   &NO(1),14(0,1)          STORE NUMBER OF LABELS    PM0051
.NORNO   AIF   ('&LABELA' NE '').CKLAB                           PM0051
         AIF   ('&LABEL' EQ '').SVC                              PM0051
         AIF   ('&LABEL'(1,1) NE '(').SVC                        PM0051
         ST    &LABEL(1),16(0,1)       STORE VOL PRINT LABEL     PM0051
         AGO   .SVC                                              PM0051
.CKLAB   AIF   ('&LABELA'(1,1) NE '(').SVC                       PM0051
         ST    &LABELA(1),16(0,1)      STORE VOL PRINT LABEL     PM0051
.SVC     SVC   39                      LABEL SVC
.MEXIT   MEXIT
.NINE    ANOP
         AIF   (K'&CNTRL EQ 1).BNINE
         AIF   ('&CNTRL'(2,1) NE 'D').E7
         AIF   (&A LT 2 OR &A GT 3).E7
&B(1)    SETB  1
&B(2)    SETB  1
&B(5)    SETB  (&A EQ 2)
         AGO   .PUT
.BNINE   ANOP
&B(5)    SETB  1
.CONV    ANOP
&B(4)    SETB  1
         AGO   .PUT
.BOTH    AIF   ('&LNGTH'(1,1) EQ '(' OR '&NO'(1,1) EQ '(').ELN
         B     *+8                     BRANCH AROUND CONSTANTS
         DC    AL2(&LNGTH)             LABEL LENGTH
         DC    AL1(&NO)                NUMBER OF LABELS
         MVC   12(3,1),*-4             MOVE CONSTANTS TO LIST
         AGO   .EUNLR                                            PM0051
.EROUT   ANOP
         AIF   (N'&MF NE 2).E
&NAME    IHBINNRA  &MF(2)
         AIF   ('&UNIT' EQ '').EDD
         AIF   ('&UNIT'(1,1) EQ '(').EUREG
         LA    14,&UNIT.(0,0)          GET UNIT NUMBER
         STC   14,3(0,1)               STORE UNIT NUMBER
         AGO   .EDD
.EUREG   ST    &UNIT(1),3(0,1)         STORE UNIT NUMBER
.EDD     AIF   ('&DDNAME' EQ '').ELAB
         AIF   ('&DDNAME'(1,1) EQ '(').EDDREG
         LA    14,&DDNAME              GET DDNAME ADDRESS
         ST    14,4(0,1)               STORE ADDRESS IN LIST
         AGO   .ELAB
.EDDREG  ST    &DDNAME(1),4(0,1)       STORE DDNAME ADDRESS
.ELAB    AIF   ('&LABEL' EQ '').ELNO
         AIF   ('&LABEL'(1,1) EQ '(').ELBREG
         LA    14,&LABEL               LOAD LABEL ADDRESS
         ST    14,8(0,1)               STORE ADDRESS IN LIST
         AGO   .ELNO
.ELBREG  ST    &LABEL(1),8(0,1)        STORE VOLUME LABEL ADDRESS
.ELNO    AIF   ('&LNGTH' NE '' AND '&NO' NE '').BOTH
.ELN     AIF   ('&LNGTH' EQ '').ENO
         AIF   ('&LNGTH'(1,1) EQ '(').ELNREG
         B     *+6                     BRANCH AROUND CONSTANTS
         DC    AL2(&LNGTH)             LABEL LENGTH
         MVC   12(2,1),*-2             MOVE LENGTH TO LIST
         AGO   .ENO
.ELNREG  STH   &LNGTH(1),12(0,1)       STORE LABEL LENGTH
.ENO     AIF   ('&NO' EQ '').EUNL
         AIF   ('&NO'(1,1) EQ '(').ENOREG
         B     *+6                     BRANCH AROUND CONSTANTS
         DC    AL1(&NO)                NUMBER OF LABELS
         MVC   14(1,1),*-2             MOVE NUMBER OF LABELS TO LIST
         AGO   .EUNL
.ENOREG  STC   &NO(1),14(0,1)          STORE NUMBER OF LABELS
.EUNL    AIF   ('&LABELA' EQ '').EUNLR                           PM0051
         AIF   ('&LABELA'(1,1) EQ '(').EUNLOD                    PM0051
         LA    14,&LABELA              LOAD LABEL ADDRESS        PM0051
         ST    14,16(0,1)              STORE LABEL ADDRESS       PM0051
         AGO   .EUNLR                                            PM0051
.EUNLOD  ST    &LABELA(1),16(0,1)      STORE LABEL ADDRESS       PM0051
.EUNLR   AIF   ('&UNLOAD' EQ '').ECODE                           PM0051
         AIF   (&BB).EOK
         AIF   ('&UNLOAD' EQ 'REWIND').EREW
         IHBERMAC 157,UNLOAD,&UNLOAD
         AGO   .ECODE
.EREW    MVI   2(1),4                  INDICATE REWIND
         AGO   .ECODE
.EOK     OI    2(1),2                  INDICATE UNLOAD
.ECODE   AIF   ('&CNTRL' EQ '').SVC
         AGO   .CODE
.E1      IHBERMAC 2                    FIRST OPERAND MISSING
         MEXIT
.E2      IHBERMAC 3                    FIRST OPERAND MISSING
         MEXIT
.E3      IHBERMAC 4                    FIRST OPERAND MISSING
         MEXIT
.E4      IHBERMAC 5                    FIRST OPERAND MISSING
         MEXIT
.E5      IHBERMAC 14                   FIRST OPERAND MISSING
         MEXIT
.E6      IHBERMAC 17                   MODE OPERAND MISSING
         MEXIT
.E7      IHBERMAC 45,,&CNTRL
         MEXIT
.E       IHBERMAC 35,,&MF
         MEND
