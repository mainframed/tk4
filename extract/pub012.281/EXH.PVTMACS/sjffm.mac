         MACRO
&NAME    SJFFM &R,&LV=,&A=
.**********************************************************************
.*                                                                    *
.*  MODULE NAME = IEFSJFFM                                            *
.*                                                                    *
.*  DESCRIPTIVE NAME = SJF FREE LOCAL STORAGE                         *
.*                                                                    *
.*  COPYRIGHT = 5740-XYN COPYRIGHT IBM CORP. 1982,                    *
.*              LICENSED MATERIAL-PROGRAM, PROPERTY OF IBM,           *
.*              REFER TO COPYRIGHT INSTRUCTIONS FORM G120-2083.       *
.*                                                                    *
.*  STATUS = OS/VS2 JBB1328                                           *
.*                                                                    *
.*  FUNCTION = THIS MACRO IS USED IN THE PLACE OF FREEMAIN BY         *
.*             THE SCHEDULER JCL FACILITY (SJF) SERVICE ROUTINES,     *
.*             TO RELEASE THEIR LOCAL STORAGE FROM THE STORAGE        *
.*             AREA GETMAINED BY THE SJF CONTROL MODULE (IEFSJCNL).   *
.*                                                                    *
.*    OPERATION = THIS MACRO GENERATES CODE TO:                       *
.*                                                                    *
.*            1. CALCULATE THE ADDRESS OF THE NEXT AVAILABLE          *
.*               BYTE OF SJF STORAGE (FIELD SJCNNBYT IN THE           *
.*               SJF CONTROL WORKAREA, IEFSJCNW) BY SUBTRACTING       *
.*               THE SIZE OF STORAGE SPECIFIED IN REGISTER 0.         *
.*                                                                    *
.*            2. VALIDATE THAT THE AMOUNT OF STORAGE TO BE FREED  @H1A*
.*               WILL NOT GO BEYOND THE BEGINNING OF STORAGE.     @H1A*
.*               IF IT WILL, ISSUE AN ABEND WITH A CODE OF 054    @H1A*
.*               AND A REASON CODE OF 2 IN REGISTER 15.           @H1A*
.*                                                                @H1A*
.*            3. INCREASE THE AMOUNT OF SJF STORAGE REMAINING,    @H1C*
.*               (FIELD SJCNSIZE IN IEFSJCNW) BY ADDING THE           *
.*               SIZE OF STORAGE SPECIFIED IN REGISTER 0.             *
.*                                                                    *
.*  NOTES =                                                           *
.*                                                                    *
.*    DEPENDENCIES =                                                  *
.*                                                                    *
.*            1. THE SJF CONTROL MODULE (IEFSJCNL) MUST HAVE          *
.*               GETMAINED A STORAGE AREA FOR ALL OF SJF.             *
.*                                                                    *
.*            2. ALL SJF SERVICE ROUTINES MUST USE THE PL/S           *
.*               STACK OPTION IN CONJUNCTION WITH THE SJFGM AND       *
.*               SJFFM MACROS, OS(STACK('SJFGM','SJFFM')).            *
.*               PL/S WILL GENERATE CODE TO INVOKE SJFFM AS           *
.*               FOLLOWS:                                             *
.*                                                                    *
.*                           SJFFM R,LV=(0),A=(1)                     *
.*                                                                    *
.*            3. ON INPUT TO THE SJF SERVICE ROUTINE, REGISTER        *
.*               1 CONTAINS THE ADDRESS OF A 2 WORD PARAMETER         *
.*               LIST. THE FIRST WORD CONTAINS THE ADDRESS OF         *
.*               THE PARAMETER LIST NEEDED FOR THE PARTICULAR         *
.*               SJF SERVICE ROUTINE. THE SECOND WORD CONTAINS        *
.*               THE ADDRESS OF THE SJF CONTROL WORKAREA              *
.*               (IEFSJCNW). NOTE: THIS MACRO USES THE REGISTER 1     *
.*               THAT WAS SAVED IN THE SAVEAREA POINTED               *
.*               TO BY REGISTER 13.  THEREFORE, REGISTER 13           *
.*               MUST POINT TO THE SAVEAREA OF THE CALLER             *
.*               OF THE SJF SERVICE ROUTINE.                          *
.*                                                                    *
.*            4. THE SJF SERVICE ROUTINE WHICH INVOKES THIS MACRO     *
.*               MUST HAVE INCLUDED THE SJF WORKAREA MAPPING          *
.*               MACRO (IEFSJCNW).                                    *
.*                                                                    *
.*   CHARACTER CODE DEPENDENCIES = NONE                               *
.*                                                                    *
.*   RESTRICTIONS = NONE                                              *
.*                                                                    *
.*   REGISTER CONVENTIONS = THIS MACRO USES REGISTERS 0, 1,       @H1C*
.*                          14 AND 15.                            @H1C*
.*                                                                    *
.*   PATCH LABLE = N/A                                                *
.*                                                                    *
.*  MODULE TYPE = MACRO                                               *
.*                                                                    *
.*    PROCESSOR = ASSEM                                               *
.*                                                                    *
.*    MODULE SIZE = N/A                                               *
.*                                                                    *
.*    ATTRIBUTES = EXECUTABLE MACRO                                   *
.*                                                                    *
.*  ENTRY POINT = N/A                                                 *
.*                                                                    *
.*    ENTRY REGISTERS =                                               *
.*      REGISTER 0 = CONTAINS A SUBPOOL NUMBER IN THE HIGH            *
.*                   ORDER BYTE AND THE LENGTH OF STORAGE             *
.*                   TO BE FREED IN THE LOW ORDER 3 BYTES.            *
.*                                                                    *
.*      REGISTER 1 IN SAVEAREA =                                      *
.*                   CONTAINS THE ADDRESS OF A 2 WORD                 *
.*                   PARAMETER LIST. THE FIRST WORD CONTAINS          *
.*                   THE ADDRESS OF THE PARAMETER LIST NEEDED         *
.*                   FOR THE PARTICULAR SJF SERVICE ROUTINE,          *
.*                   AND THE SECOND WORD CONTAINS THE ADDRESS         *
.*                   OF THE SJF CONTROL WORKAREA (IEFSJCNW).          *
.*                                                                    *
.*      REGISTER 1 = ADDRESS OF THE STORAGE TO RELEASE                *
.*                                                                    *
.*      REGISTER 13 = ADDRESS OF A SAVEAREA                           *
.*                                                                    *
.*    INPUT =                                                         *
.*      SJF CONTROL WORKAREA (IEFSJCNW)                               *
.*        SJCNNBYT = ADDRESS OF NEXT AVAILABLE BYTE OF STORAGE        *
.*        SJCNSIZE = SIZE OF SJF STORAGE REMAINING                    *
.*        SJCNLSTO = ADDRESS OF THE BEGINNING OF ALLOCATABLE      @H1A*
.*                   STORAGE FOR SJF MODULES                      @H1A*
.*      ALSO SEE ENTRY REGISTERS                                      *
.*                                                                    *
.*    OUTPUT =                                                        *
.*      SJF CONTROL WORKAREA (IEFSJCNW)                               *
.*        SJCNNBYT = ADDRESS OF NEXT AVAILABLE BYTE OF SJF            *
.*                   STORAGE                                          *
.*        SJCNSIZE = SIZE OF SJF STORAGE REMAINING                    *
.*                                                                    *
.*  EXTERNAL REFERENCES = NONE                                        *
.*                                                                    *
.*    ROUTINES = NONE                                                 *
.*                                                                    *
.*    DATA AREAS = USES THE SJF CONTROL WORK AREA (IEFSJCNW).         *
.*                                                                    *
.*    CONTROL BLOCKS = N/A                                            *
.*                                                                    *
.*  TABLES = NONE                                                     *
.*                                                                    *
.*  MAPPING MACROS = NONE                                             *
.*                                                                    *
.*  MESSAGES = MNOTE 4, SJFFM PARAMETERS NOT VALID                    *
.*                                                                    *
.*  ABEND CODES = ABEND CODE HEX 054 (DEC 084) AND A REASON CODE  @H1A*
.*                OF 2 IN REGISTER 15 OCCURS WHEN AN ATTEMPT      @H1A*
.*                IS MADE TO FREE MORE STORAGE THAN IS AVAILABLE. @H1A*
.*  SERIALIZATION = NONE                                              *
.*                                                                    *
.*  CHANGE ACTIVITY = H0,H1                                       @H1C*
.*                                                                    *
.*  $H0 = EXTJCL JBB1328 810316 PD2N: SUPPORT OF EXTENDED JCL         *
.*  $H1 = EXTJCL JBB1328 811202 PDXX: SUPPORT OF EXTENDED JCL     @H1A*
.**********************************************************************
.* A 000000-999999                                                @H0A*
.* ADDED CODE TO ENSURE THAT THE STORAGE TO FREE IS WITHIN        @H1A*
.*       THE BEGINNING LIMIT OF SJF STORAGE.  IF IT IS NOT,       @H1A*
.*       AN ABEND IS ISSUED WITH A CODE OF 054 AND A REASON       @H1A*
.*       CODE OF 2 IN REGISTER 15.                                @H1A*
         AIF   ('&R' EQ 'R' AND '&LV' EQ'(0)' AND '&A' EQ '(1)').FMOK
         MNOTE 4, 'SJFFM PARAMETERS NOT VALID'
         AGO   .FMEND
.FMOK    SLA   @00,8(0)            SHIFT LEFT TO ELIMINATE SUBPOOL
         SRA   @00,8(0)            RE-POSITION LENGTH
         L     @15,24(@13)         GET CALLERS REG 1 FROM SAVEAREA
         L     @15,4(@15)          GET ADDRESS OF SJF CONTROL WORKAREA
         USING SJCNW,@15           ESTABLISH ADDRESSABILITY
         L     @01,SJCNNBYT        GET ADDRESS OF NEXT AVAILABLE BYTE
         SR    @01,@00             SUBTRACT THE LENGTH OF THIS REQUEST
         L     @14,SJCNLSTO        GET ADDRESS OF BEGINNING OF     @H1A
*                                  STORAGE                         @H1A
         CR    @01,@14             COMPARE ADDRESS OF NEXT BYTE    @H1A
*                                  OF STORAGE TO THE BEGINNING OF  @H1A
*                                  STORAGE                         @H1A
         BL    FM1&SYSNDX          BRANCH IF NEW NEXT BYTE IS NOT  @H1A
*                                  WITHIN THE BEGINNING LIMIT      @H1A
*                                  OF STORAGE                      @H1A
         ST    @01,SJCNNBYT        STORE NEW NEXT BYTE POINTER     @H1C
         L     @01,SJCNSIZE        GET SIZE OF STORAGE REMAINING
         AR    @01,@00             ADD LENGTH OF THIS REQUEST
         ST    @01,SJCNSIZE        STORE SIZE IN WORKAREA
         DROP  @15
         B     FM2&SYSNDX          BRANCH AROUND ABEND             @H1A
FM1&SYSNDX LA  @15,2               LOAD A REASON CODE OF 2         @H1A
*                                  IN REG 15                       @H1A
         ABEND 084,,,SYSTEM        ABEND WITH A CODE OF DEC 084    @H1A
*                                  (HEX 054)                       @H1A
FM2&SYSNDX DS  0F                  END OF SJFFM                    @H1A
.FMEND   MEND
