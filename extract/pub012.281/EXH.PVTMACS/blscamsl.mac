*** START OF SPECIFICATIONS *****************************************
*
* $MAC(BLSCAMSL) COMP(SC132) PROD(JIP1329): MODEL IDCAMS SYSIN TEXT
*
* COPYRIGHT= 5740-XYN, 5740-XYS COPYRIGHT IBM CORPORATION 1982.
*            LICENCED MATERIAL-PROGRAM PROPERTY OF IBM,
*            REFER TO COPYRIGHT INSTRUCTIONS FORM NUMBER G120-2083.
*
*   CHANGE ACTIVITY:
* $L0= MVSIPCS,EIP1102,78001,DMGPSR: MVS IPCS INITIAL RELEASE CODE
* $D1= QUALITY,HIP2102,81266,DSDPRW: IMPROVE COMMENTS, REMARKS
*
*** END OF SPECIFICATIONS *******************************************
         MACRO
&N       BLSCAMSL &TEXT,&END=YES MODEL IDCAMS SYSIN TEXT           @D1C
         LCLA  &CTR           LOOP COUNTER
         LCLA  &NUM           NUMBER OF TEXT SEGMENTS
         LCLA  &L             TEXT SEGMENT LENGTH
         LCLA  &LL            TEXT SEGMENT MOVE LENGTH
         LCLA  &LLL           TEXT SEGMENT LENGTH + 4
         LCLC  &C             TEXT SEGMENT
         LCLC  &CC            TRUNCATED TEXT SEGMENT
         LCLC  &PAD           ONE BLANK
&CTR     SETA  0              INIT LOOP COUNTER
&NUM     SETA  N'&TEXT        INIT LOOP LIMIT
&PAD     SETC  ' '
         AIF   (N'&N EQ 0).CNT  TEST FOR NO LOCSYMB
&N       DC    0H'0'          ALIGN LIST
         AGO   .CNT
.LOOP    ANOP
&C       SETC  '&TEXT(&CTR)'  TEXT SEGMENT
&L       SETA  K'&C-1
         AIF   (&L LE 72).OK  LENGTH OK
&L       SETA  72             TRUNCATED LENGTH
         MNOTE 8,'SUB PARM NUMBER ''&CTR'' EXCEEDS 71 BYTES. PARAMETER *
               IS TRUNCATED'
.OK      ANOP
&LL      SETA  &L-1           MOVE LENGTH
&LLL     SETA  &L+4           ENTRY LENGTH
&CC      SETC  '&C'(2,&LL)    REMOVE QUOTES AND TRUNCATE IF NECESSARY
         DC    HL2'&LLL',HL2'0',CL&L'&PAD&CC' TEXT SEGMENT
.CNT     ANOP
&CTR     SETA  &CTR+1         BUMP LOOP COUNTER
         AIF   (&CTR LE &NUM).LOOP  TEST LOOP END
         AIF   ('&END' EQ 'NO').MEND
         DC    HL2'0'         END OF TEXT
.MEND    MEND
