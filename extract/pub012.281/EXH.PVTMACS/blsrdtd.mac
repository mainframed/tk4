*** START OF SPECIFICATIONS********************************************
*                                                                     *
* $MAC(BLSRDTD) COMP(SC132) PROD(JIP1329): DEFINE ONE DATA NAME       *
*                                                                     *
* COPYRIGHT= 5740-XYN, 5740-XYS COPYRIGHT IBM CORPORATION 1982.       *
*            LICENCED MATERIAL-PROGRAM PROPERTY OF IBM,               *
*            REFER TO COPYRIGHT INSTRUCTIONS FORM NUMBER G120-2083.   *
*                                                                     *
* FUNCTION= DEFINE PROCESSING ASSOCIATED WITH ONE DATA NAME TO BE     *
* SHOWN IN A DATA NAME PROCESSING TABLE.                              *
*                                                                     *
*   CHANGE ACTIVITY:                                                  *
* $L0= MVSIPCS,EIP1102,78001,DMGPRW: MVS IPCS INITIAL RELEASE CODE    *
* $D1= QUALITY,HIP2102,81015,DSDPRW: IMPROVE COMMENTS, REMARKS        *
*                                                                     *
***** END OF SPECIFICATIONS********************************************
         MACRO
         BLSRDTD &DTD,&FIND=,&NORMAL=,&SCAN=
.*--------------------------------------------------------------------*
.* BLSRDTD--DEFINE PROCESSING FOR ONE DATA NAME                       *
.*--------------------------------------------------------------------*
         GBLA  &DTDIM
         GBLC  &GP,&GD(200),&GN(200),&GS(200),&GV(200)             @D1C
         LCLA  &A1                                                 @D1A
.*------------ PROCESS DATA NAME ----- ----------------------- #MD81015
         AIF   (&DTDIM LT 1).L03       NO NEED TO CHECK ORDER      @D1A
&A1      SETA  K'&GD(&DTDIM)           LENGTH FOR COMPARISON       @D1A
         AIF   (&A1 LT K'&DTD).L01     NEW DATA NAME LONGER        @D1A
.*                                     OLD DATA NAME AS LONG AS NEW
&A1      SETA  K'&DTD                  LENGTH FOR COMPARISON       @D1A
         AIF   ('&GD(&DTDIM)'(1,&A1) LE '&DTD').L03 VALID ORDER    @D1A
         AGO   .L02                    OUT OF ORDER                @D1A
.*                                     NEW DATA NAME LONGER
.L01     AIF   ('&GD(&DTDIM)' LE '&DTD'(1,&A1)).L03 VALID ORDER    @D1C
.*                                     DATA NAME OUT OF ORDER
.L02     MNOTE 12,'DATA NAME OUT OF ORDER' NO. STATE THE PROBLEM   @D1A
         AGO   .L09                    NULLIFY MACRO-EXPANSION     @D1A
.*                                     DATA NAME IN PROPER ORDER
.L03     ANOP  ,                       VALID REQUEST               @D1A
&DTDIM   SETA  &DTDIM+1                DIMENSION OF NAME ARRAY     @D1C
&GD(&DTDIM) SETC '&DTD'                DATA NAME                   @D1C
.*------------ PROCESS NORMALIZED DATA NAME ------------------ #MD81015
         AIF   (T'&NORMAL EQ 'O').L04  IMPLICIT NORMAL DATA TYPE   @D1C
&GN(&DTDIM) SETC '&NORMAL'             NORMAL DATA TYPE
         AGO   .L05                                                @D1C
.L04     ANOP  ,                                                   @D1C
&GN(&DTDIM) SETC '&DTD'                NO GROUP NAME               @D1C
.*------------ PROCESS FIND EXIT NAME  ------------------------------*.
.L05     AIF   (T'&FIND EQ 'O').L06    NO FIND EXIT                @D1C
&GS(&DTDIM) SETC '&FIND'               FIND EXIT NAME
         AGO   .L07                                                @D1C
.L06     ANOP  ,                                                   @D1C
&GS(&DTDIM) SETC ' '                   NO FIND EXIT
.*------------ PROCESS SCAN EXIT NAME  ------------------------------*.
.L07     AIF   (T'&SCAN EQ 'O').L08    NO SCAN EXIT                @D1C
&GV(&DTDIM) SETC '&SCAN'               SCAN EXIT NAME
         AGO   .L09                                                @D1C
.L08     ANOP  ,                                                   @D1C
&GV(&DTDIM) SETC ' '                   NO SCAN EXIT
.L09     ANOP  ,                                                   @D1C
         MEND
