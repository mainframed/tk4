*** START OF SPECIFICATIONS*******************************************
*
* $MAC(BLSRSSTN) COMP(SC132) PROD(JIP1329): SPECIAL SYMBOL ENTRY
*
* COPYRIGHT= 5740-XYN, 5740-XYS COPYRIGHT IBM CORPORATION 1982.
*            LICENCED MATERIAL-PROGRAM PROPERTY OF IBM,
*            REFER TO COPYRIGHT INSTRUCTIONS FORM NUMBER G120-2083.
*
* FUNCTION= DEFINE ONE SPECIAL SYMBOL TABLE ENTRY
*
*   CHANGE ACTIVITY:
* $L0= SYMALIAS,HIP2102,81265,DSDPRW: RECOGNIZE SYMBOLIC ALIASES
* $L1= ASMBLRXF,HIP2102,81293,DSDPRW: ACCEPT ASSEMBLER XF LIMITATIONS
*
***** END OF SPECIFICATIONS*******************************************
         MACRO
         BLSRSSTN &SYMPFX,&CLASS
         GBLA  &GA
         GBLC  &GN(50),&GP,&GS                                     @L1C
         LCLA  &A1
         LCLC  &C1                                                 @L1A
&C1      SETC  '&SYMPFX'               LENGTH OF PREFIX TEXT
         AIF   (K'&C1 EQ K'&GS).L02    EQUAL PREFIX LENGTHS
         AIF   (K'&C1 GT K'&GS).L01    OLD PREFIX SHORT
.*       NEW PREFIX SHORTER THAN OLD
&A1      SETA  K'&C1                   LENGTH OF PREFIX
&GS      SETC  '&GS'(1,&A1)            TRUNCATE OLD PREFIX
.L02     AIF   ('&C1' GE '&GS').L04    PREFIX NAMES ALPHABETIZED
         AGO   .L03                    VERIFY COLLATING SEQUENCE
.*       OLD PREFIX SHORTER THAN NEW
.L01     ANOP  ,                       OLD PREFIX SHORT
&A1      SETA  K'&GS                   LENGTH OF PREFIX
&C1      SETC  '&C1'(1,&A1)            TRUNCATE NEW PREFIX
         AIF   ('&C1' GE '&GS').L04    PREFIX NAMES ALPHABETIZED
.L03     MNOTE 8,'PREFIX NAMES ARE NOT ALPHABETIZED'
         MEXIT
.L04     ANOP
&GA      SETA  &GA+1                   DIMENSION OF ARRAY
&GS      SETC  '&SYMPFX'               CURRENT PREFIX NAME
         SPACE 1
&GP.&GA  DS    0F                      SPECIAL SYMBOL ENTRY
         DC    AL1(&CLASS)             TYPE OF SYMBOLIC SUFFIX
&A1      SETA  K'&SYMPFX               LENGTH OF PREFIX TEXT
         DC    FL1'&A1'                LENGTH OF PREFIX TEXT
         DC    AL2(&SYMPFX-&SYSECT)    OFFSET OF PREFIX TEXT
&GN(&GA) SETC  '&SYMPFX'               PREFIX TEXT                 @L1A
         MEND
