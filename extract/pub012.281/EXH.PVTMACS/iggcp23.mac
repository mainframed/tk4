         MACRO
         IGGCP23
.*         RELEASE 23 DELETIONS/CHANGES
.*         RELEASE 22 DELETIONS/CHANGES
.*         RELEASE 21 DELETIONS/CHANGES
*                                  **** CHANNEL PROGRAM 23 ****
CS1      CCW   SIDEQ,0,CC+SILI,4        VALIDATE SEEK
CS1A     CCW   TIC,CS1-CS1,0,0
CS1B     CCW   SKEQHI,0,CC+SILI,0       TOO FAR ALONG INDEX
CS1C     CCW   TIC,CS2-CS1,0,0
CS1D     CCW   NOP,CS1D-CS1+5,CC+SILI,1 SET SECTOR SLOT FOR RPS
CS1E     CCW   RHA,0,CC+SKIP,5          POSITION TO HOME ADDRESS
CS2      CCW   SKEQHI+MT,0,CC,0         SEARCH INDEX ON KEY
CS3      CCW   TIC,CS2-CS1,0,0
CS4      CCW   RDATA,CS6-CS1+7,CC,10    READ CURRENT INDEX ENTRY
CS5      CCW   TIC,CS8-CS1,0,0
CS6      DC    C'        '              -------M
CS7      DC    C'        '              BBCCHHRF
CS8      CCW   0,CS7-CS1,CC,6           SEEK TRACK INDEX
CS80     CCW   NOP,CS80-CS1+5,CC+SILI,1 SLOT FOR SET SECTOR - RPS
CS9      CCW   SIDEQ,0,CC,5             POSITION TO TRACK INDEX
CS9A     CCW   TIC,CS9-CS1,0,0
CS10     CCW   RDCNT+MT,0,CC,8          COUNT OF CURRENT INDEX ENTRY
CS11     CCW   SKEQHI,0,CC,0            SEARCH ON KEY
CS12     CCW   TIC,CS10-CS1,0,0
CS13     CCW   RDATA,CS17+7-CS1,CC,10    DATA OF CURRENT INDEX ENTRY
CS14     CCW   RDCNT+MT,0,CC,8           COUNT OF NEXT INDEX ENTRY
CS15     CCW   RDATA,0,CC+SILI,10        DATA OF NEXT INDEX ENTRY
CS16     CCW   TIC,CS19-CS1,0,0
CS17     DC    C'        '              -------M
CS18     DC    C'        '              BBCCHHRF
CS19     CCW   0,CS18-CS1,CC,6          SEEK PRIME TRACK
CS19A    CCW   NOP,CS19A+5-CS1,CC+SILI,1 SLOT FOR SET SECTOR - RPS
CS20     CCW   SIDEQ,CS18+2-CS1,CC,5    SEARCH FIRST DATA RECORD
CS21     CCW   TIC,CS20-CS1,0,0
CS26     CCW   TIC,CS22-CS1,0,0        TIC TO CS22
CS25     CCW   RDCNT,0,CC+SILI,5        READ COUNT OF RECORD
CS22     CCW   SKEQ,0,CC+SILI,0         SEARCH FOR DESIRED BLOCK
CS23     CCW   TIC,CS25-CS1,0,0
CS24     CCW   NOP,0,SILI,1             EXIT WHEN RECORD FOUND
*                                       *  OR READ SECTOR
CP23LEN  EQU   CS24+8-CS1
         MEND
