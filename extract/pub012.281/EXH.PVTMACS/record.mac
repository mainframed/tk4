         MACRO
&NAME    RECORD &TYPE=,&RCVRY=,&DATAADR=,&PARMADR=,&LENGTH=,           C
               &BUFFER=YES,&POSTING=NO,&HEADER=NO,&JOBNAME=NO,&TIME=NO,C
               &ERRORID=NO                                    @G17EP5W
* MACCOMP-SC1CM
* MACSTAT-@G17EP5W
* MACMEAN-INVOKE RECORDING ROUTINE FOR WTO,SYS1.LOGREC RECORDS OR
*         EMERGENCY REQUESTS
         LCLA  &CTR1,&CTR2,&CTR3                              @G17EP5W
         LCLB  &B(12)
&NAME    DS    0H
&B(1)    SETB  ('&TYPE' EQ 'TERM')         SYSTEM TERMINATION CALL
&B(2)    SETB  ('&TYPE' EQ 'LOGREC')       WRITE TO SYS1.LOGREC
&B(3)    SETB  ('&TYPE' EQ 'WTO')          WRITE TO OPERATOR
&B(4)    SETB  ('&RCVRY' EQ 'ESTAE')       ESTABLISH RECOVERY VIA ESTAE
&B(5)    SETB  ('&RCVRY' EQ 'SETFRR')      ESTABLISH RECVRY VIA SETFRR
&B(6)    SETB  ('&BUFFER' EQ 'YES')        DATA MUST BE BUFFERED
&B(7)    SETB  ('&BUFFER' EQ 'NO')         DATA MUST NOT BE BUFFERED
&B(8)    SETB  ('&HEADER' EQ 'YES')        BUILD AN AR149 HEADER
&B(9)    SETB  ('&JOBNAME' EQ 'YES')       INSERT JOBNAME AFTER HEADER
&B(10)   SETB  ('&TIME' EQ 'YES')          RETURN TIME PLACED IN RECORD
&B(11)   SETB  ('&POSTING' EQ 'YES')       POST CALLER WHEN BUFFER FREE
&B(12)   SETB  ('&ERRORID' EQ 'YES')       INCLUDE ERRORID IN RECORD
&CTR1    SETA  128*&B(1)+64*&B(2)+32*&B(3)+&B(12)              @G17EP5W
&CTR2    SETA  128*&B(4)+64*&B(5)+32*&B(6)+16*&B(7)+8*&B(8)+4*&B(9)+2*&C
               B(10)+&B(11)
&CTR3    SETA  &B(8)+&B(9)+&B(10)+&B(11)+&B(12)                @G17EP5W
         AIF   (&CTR1 LE 1).ERR1                               @G17EP5W
         AIF   ('&HEADER' NE 'YES' AND '&HEADER' NE 'NO').ERR10
         AIF   ('&TIME' NE 'YES' AND '&TIME' NE 'NO').ERR11
         AIF   ('&JOBNAME' NE 'YES' AND '&JOBNAME' NE 'NO').ERR12
         AIF   ('&POSTING' NE 'YES' AND '&POSTING' NE 'NO').ERR13
         AIF   ('&BUFFER' NE 'YES' AND '&BUFFER' NE 'NO' AND '&BUFFER' N
               NE 'OPT').ERR14
         AIF   ('&ERRORID' NE 'YES' AND '&ERRORID' NE 'NO').ERR15
         AIF   ('&TYPE' EQ 'WTO' AND '&HEADER' EQ 'YES').ERR9
         AIF   ('&TYPE' EQ 'TERM').TERM
         AIF   ('&RCVRY' NE 'ESTAE' AND '&RCVRY' NE 'SETFRR').ERR2
         AIF   ('&DATAADR' NE '' AND '&PARMADR' NE '').ERR4
         AIF   ('&DATAADR' EQ '' AND '&PARMADR' EQ '').ERR3
         AIF   (&CTR3 NE 0 AND '&PARMADR' EQ '').ERR5
         AIF   ('&JOBNAME' EQ 'YES' AND '&HEADER' EQ 'NO').ERR6
         AIF   ('&HEADER' EQ 'YES' AND '&BUFFER' NE 'YES').ERR7
         AIF   ('&LENGTH' EQ '' AND ('&TYPE' NE 'WTO' OR '&BUFFER' NE  N
               'NO')).ERR8
         AIF   ('&ERRORID' EQ 'YES' AND '&TYPE' NE 'LOGREC').ERR16
         AIF   ('&LENGTH' EQ '').LNGR0
         AIF   ('&LENGTH' EQ '(0)').LNGR0
         AIF   ('&LENGTH'(1,1) EQ '(').LNGRN
         CNOP  0,4
         B     *+8                        BRANCH AROUND CONSTANTS
         DC    AL1(&CTR1)                 BIT SETTING FOR TYPE
         DC    AL1(&CTR2)                 BIT SETTING FOR OPTIONS
         DC    AL2(&LENGTH)               LENGTH OF DATA SUPPLIED
         L     0,*-4                      REGISTER 0=OPTIONS AND LNGTH
         AGO   .LOADR1
.LNGRN   LR    0,&LENGTH                  LENGTH IN REG0
.LNGR0   CNOP  0,4                        BRANCH AROUND CONSTANTS
         B     *+6                        BRANCH AROUND CONSTANTS
         DC    AL1(&CTR1)                 BIT SETTING FOR TYPE
         DC    AL1(&CTR2)                 BIT SETTING FOR OPTIONS
         ICM   0,12,*-2                    TYPE AND OPTS IN REG0
.LOADR1  AIF   ('&PARMADR' NE '').PARM
         IHBINNRA &DATAADR                LOAD REG1 WITH ADDR OF DATA
         LCR   1,1                        TWO'S COMPLIMENT FOR DATAADR
         AGO   .LNKAGE
.TERM    LA    0,&CTR1.(0,0)              BIT SETTING FOR TYPE=TERM
         SLL   0,24(0)                    IN HIGH ORDER BYTE OF REG0
         AGO   .LNKAGE
.PARM    IHBINNRA &PARMADR                REG1 WITH ADDR OF PARMLIST
.LNKAGE  L     15,FLCCVT-PSA(0,0)         ADDRESS OF CVT
         L     15,CVTRECRQ-CVTMAP(,15)    ADDRESS OF RECORDING FACILITY
         BALR  14,15                      BRANCH TO RECORDING FACILITY
         AGO   .END
.ERR1    MNOTE 12,'TYPE OPERAND REQD - NOT SPECIFIED'
         MEXIT
.ERR2    MNOTE 12,'RCVRY OPERAND REQD - NOT SPECIFIED'
         MEXIT
.ERR3    MNOTE 12,'DATAADR OR PARMADR OPERAND REQD - NOT SPECIFIED'
         MEXIT
.ERR4    MNOTE 12,'DATAADR AND PARMADR OPERANDS BOTH SPECIFIED'
         MEXIT
.ERR5    MNOTE 12,'PARMADR REQD WITH HEADER, JOBNAME, TIME, POSTING, ORC
                ERRORID'
         MEXIT
.ERR6    MNOTE 12,'HEADER=YES REQUIRED IF JOBNAME=YES'
         MEXIT
.ERR7    MNOTE 12,'BUFFER=YES REQUIRED IF HEADER=YES'
         MEXIT
.ERR8    MNOTE 12,'LENGTH OPERAND REQD - NOT SPECIFIED'
         MEXIT
.ERR9    MNOTE  12,'HEADER=YES INVALID WITH TYPE=WTO'
         MEXIT
.ERR10   IHBERMAC  54,,&HEADER
         MEXIT
.ERR11   IHBERMAC 54,,&TIME
         MEXIT
.ERR12   IHBERMAC 54,,&JOBNAME
         MEXIT
.ERR13   IHBERMAC 54,,&POSTING
         MEXIT
.ERR14   IHBERMAC 54,,&BUFFER
         MEXIT
.ERR15   IHBERMAC 54,,&ERRORID
         MEXIT
.ERR16   MNOTE 12,'TYPE=LOGREC REQUIRED IF ERRORID=YES'
         MEXIT
.END     MEND
