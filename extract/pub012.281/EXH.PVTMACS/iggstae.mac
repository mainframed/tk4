* /* MACCOMP Y-2 SC1D0/SJD48  */
* /* MACSTAT Y-2 73194/021090 */
         MACRO
&LABEL   IGGSTAE &NAMELOC
.* /* START OF SPECIFICATIONS ****
.*
.*01  MODULE-NAME = IGGSTAE
.*
.*01  COPYRIGHT = NONE
.*
.*01  STATUS = VS2 RELEASE 2, LEVEL 1
.*
.*01  CHANGE-ACTIVITY = AS FOLLOWS:
.*     VS2 RELEASE 02 DELETIONS
.*                                                               YM1140
.*
.*01  DESCRIPTIVE-NAME = ESTAE ROUTINE BOOTSTRAP
.*
.*01  FUNCTION = LOCATE AND ENTER TASK RECOVERY ROUTINE
.*
.*01  NOTES = SEE BELOW
.*
.*02    DEPENDENCIES = NONE
.*
.*02    REGISTER-CONVENTIONS = NO REGISTER LABELS ARE USED
.*
.*01  MODULE-TYPE = MACRO
.*
.*02    MACRO-SIZE = 75 STATEMENTS
.*
.*02    PARAMETER-RECEIVED = 1
.*03      TYPE = CHARACTER STRING
.*04        LENGTH+MODE = 8 CHARACTERS
.*
.*04        VALUE = NAME OF RECOVERY ROUTINE
.*
.*02    EXTERNAL-REFERENCES = NONE
.*
.*02    DATA-AREA = NONE
.*
.*01  INNER-MACROS = NONE
.*
.*01  SYSTEM-MACROS = IHASDWA
.*
.**** END OF SPECIFICATIONS ***/
         PUSH  USING
         DROP
&LABEL   LA    15,12                    CODE PASSED IF NO RTCA
         CR    15,0                     WAS AN RTCA PROVIDED
         BER   14                       NO--RETURN TO RTM
         USING SDWA,1                   YES--BASE DSECT
         STM   14,12,12(13)             SAVE REGS FOR RECOVERY RTN
         BALR  12,0                     ESTABLISH BASE FOR CONSTANTS
         USING *,12
         MVC   SDWACSCT,=CL8'&SYSECT'   STORE CSECT NAME IN RTCA
         MVC   SDWAREXN,&NAMELOC        STORE ESTAE NAME IN RTCA
         DROP  1
         L     1,16                     CVT POINTER              YM1140
         USING CVT,1                    CVT REGISTER             YM1140
         L     2,CVTLINK                ADDR OF SYS1.LINKLIB DCB YM1140
         DROP  1                                                 YM1140
         LOAD  EPLOC=&NAMELOC,DCB=(2)   LOAD RECOVERY RTN        YM1140
         LR    5,0                      SAVE ENTRY POINT ADDRESS
         DELETE EPLOC=&NAMELOC          DELETE IT
         DROP  12
         LR    15,5                     ADDRESS OF RECOVERY RTN
         L     14,12(13)                RTM RETURN ADDRESS
         LM    0,12,20(13)              RESTORE REGISTERS
         BR    15                       GO TO RECOVERY ROUTINE
         POP   USING
         MEND
