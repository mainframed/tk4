         MACRO
&CSECT   JDTMAC
.**********************************************************************
.*                                                                    *
.*   MODULE NAME = IEFJDMAC                                        @H1A
.*                                                                    *
.*   DESCRIPTIVE NAME = MACRO FOR JCL DEFINITION TABLE                *
.*                                                                    *
.*   COPYRIGHT = 5740-XYN COPYRIGHT IBM CORP. 1982                    *
.*                LICENSED MATERIAL - PROGRAM                         *
.*                PROPERTY OF IBM, REFER TO COPYRIGHT                 *
.*                INSTRUCTIONS FORM G120-2083                         *
.*                                                                    *
.*   FUNCTION = 1.) ALLOWS THE  USER TO DEFINE  A CSECT  FOR A     @H2C
.*                  JCL DEFINITION TABLE (JDT).                    @H2C
.*              2.) SETS UP GLOBAL DEFINITIONS USED BY ALL THE     @H2C
.*                  OTHER MACROS.                                  @H2C
.*                 A.) EACH  MACRO  ALSO HAS  ITS  OWN  UNIQUE     @H2C
.*                     GLOBAL  BIT THAT  IS SET ON  WHEN IT IS     @H2C
.*                     INVOKED.  ONLY ONE IS EVER ON AT A TIME     @H2C
.*                     (EXCEPT FOR  THE CONDITION OF A SUBLIST     @H2C
.*                     DEFINITION WHEN TWO ARE ON).  WHEN THIS     @H2C
.*                     GLOBAL  BIT  IS  ON, IT  IS  DEFINED AS     @H2C
.*                     'ACTIVE' BY  EACH  MACRO.  ONLY CERTAIN     @H2C
.*                     ACTIVE  BITS  MAY BE  ON  PRIOR  TO THE     @H2C
.*                     INVOCATION OF EACH MACRO.                   @H2C
.*                 B.) THE TABLE THAT FOLLOWS  ILLUSTRATES THE     @H2C
.*                     ORDER THAT THESE MACROS MAY BE INVOKED.     @H2C
.*                                                                    *
.*   TO UNDERSTAND THE CHART, READ COLUMN DOWN THEN LINE ACROSS.      *
.*   FOR EXAMPLE: CAN JDKEY FOLLOW JDVERB? ANSWER=YES.                *
.*   (THE LINE ACROSS ASKS "IF THE ACTIVE GLOBAL BIT IS ON,           *
.*   CAN THIS MACRO FOLLOW IT?")                                      *
.*                                                                    *
.*      FOLLOW->                                                      *
.*   CAN ×                                                            *
.*       V    JDTMAC  JDVERB  JDKEY  JDPARM  JDSUB  ENDSUB  ENDJDT    *
.*   JDTMAC     NO      NO      NO     NO      NO     NO      NO      *
.*   JDVERB     YES     YES     NO     YES     NO     YES     NO   @H2C
.*   JDKEY      NO      YES     NO     YES     NO     YES     NO      *
.*   JDPARM     NO      NO      YES    YES     YES    YES     NO      *
.*   JDSUB      NO      NO      YES    YES(1)  NO     YES     NO      *
.*   ENDSUB     NO      NO      NO     YES(2)  NO     NO      NO      *
.*   ENDJDT     NO      YES     NO     YES     NO     YES     NO   @H2C
.*                                                                    *
.*   NOTES: (1)-ONLY CAN FOLLOW IF NOT DEFINED AS A SUBLIST ALREADY.  *
.*          (2)-ONLY CAN FOLLOW IF IT IS DEFINED AS A SUBLIST.        *
.*                                                                    *
.*     NOTES = NONE                                                   *
.*                                                                    *
.*      DEPENDENCIES = MODID MACRO FORMAT                             *
.*                                                                    *
.*      RESTRICTIONS = ONLY ONE JDTMAC MACRO ALLOWED PER ASSEMBLY,    *
.*                     AND IT MUST BE THE FIRST JDT MACRO SPECIFIED.  *
.*                                                                    *
.*   ENTRY POINT = N/A                                                *
.*                                                                    *
.*   MODULE TYPE = MACRO                                              *
.*                                                                    *
.*     PROCESSOR = ASSEMBLER                                          *
.*                                                                    *
.*     INPUT = THE FOLLOWING MAY BE SPECIFIED AS PARAMETERS TO JDTMAC *
.*                                                                    *
.*             LABEL             DESCRIPTION                          *
.*                                                                    *
.*             - &CSECT          USED AS THE CSECT NAME FOR THE       *
.*                               JCL DEFINITION TABLE                 *
.*                                                                    *
.*     OUTPUT = THE JDT DEFINITION SECTION                            *
.*                                                                    *
.*   EXIT NORMAL =                                                    *
.*   CONDITION-NO ERRORS DETECTED                                     *
.*   OUTPUT    -NO MNOTE ISSUED. SEVERITY CODE=0.                     *
.*                                                                    *
.*   EXIT ERROR =                                                     *
.*   CONDITION-SEVERE ERROR DETECTED BY THE JDTMAC MACRO.             *
.*   OUTPUT    -ERROR MNOTES WITH SEVERITY CODE 12                    *
.*              ISSUED WHEN THE PARAMETER IS USED INCORRECTLY OR      *
.*              MACRO IS NOT INVOKED IN THE CORRECT ORDER (SEE        *
.*              RESTRICTIONS). THE MACRO WILL ATTEMPT TO SCAN FOR     *
.*              ADDITIONAL ERRORS. (SEE MESSAGES BELOW)               *
.*                                                                    *
.*   EXTERNAL REFERENCES = N/A                                        *
.*                                                                    *
.*   TABLES = NONE                                                    *
.*                                                                    *
.*   MESSAGES = (MNOTES)                                              *
.*                                                                    *
.*   ERROR MNOTES =                                                   *
.*                                                                    *
.*   MNOTE 12,'REQUIRED CSECT NAME IS NOT SPECIFIED'                  *
.*   MNOTE 12,'JDTMAC MACRO SPECIFIED MORE THAN ONCE-MACRO IGNORED'   *
.*                                                                    *
.*   MACROS = MODID                                               @H1A*
.*                                                                    *
.*   CHANGE ACTIVITY =  H0,H1,H2                                  @H2C*
.*    $H0= EXTJCL JBB1328 810315 PDC5: SUPPORT FOR EXTENDED JCL       *
.*    $H1= EXTJCL JBB1328 810702 PD2K: SUPPORT FOR EXTENDED JCL   @H1A*
.*    $H2= EXTJCL JBB1328 811202 PDC5: SUPPORT FOR EXTENDED JCL   @H2A*
.*                                                                    *
.**********************************************************************
.* A 000000-999999                                                 @H0A
.* A ADDED MODID TO JDT                                            @H1A
.* A ADDED BLOCK ID VARIABLE                                       @H2A
.* D DELETED DC FOR CSECT NAME (NOW USED BY MODID MACRO)           @H2A
         GBLA  &LEN,&V1,&V2,&K1,&K2,&P1,&P2 VARIABLES FOR OFFSETS
         GBLA  &BLKID            BLOCK ID USED IN JDPARM MACRO     @H2A
.*--------------------------------------------------------------------*
.*  GLOBALS TO DETERMINE WHAT MACRO(S) MAY BE ACTIVE;                 *
.*  (I.E. PREVIOUSLY INVOKED) RESPECTIVELY:                           *
.*       JDTMAC,JDVERB,JDKEY,JDPARM,JDSUB,ENDSUB AND ENDJDT           *
.*--------------------------------------------------------------------*
         GBLB  &JACT,&VACT,&KACT,&PACT,&SPACT,&EPACT,&EJACT
.*--------------------------------------------------------------------*
.*  CHECK TO SEE IF THIS WAS THE ONLY JDTMAC DEFINED. ONLY ONE        *
.*  ALLOWED PER ASSEMBLY. SINCE THE JDTMAC MACRO MUST BE FIRST        *
.*  NO OTHER  GLOBAL BITS  SHOULD BE ON YET.  IF THERE  IS ANY        *
.*  OTHER BITS ON, THEN ANOTHER JDTMAC MACRO MUST HAVE ALREADY        *
.*  BEEN INVOKED.                                                     *
.*                                                                    *
.*  NONE OF THE FOLLOWING GLOBALS SHOULD BE ON OR 'ACTIVE'.           *
.*--------------------------------------------------------------------*
         AIF   (&JACT EQ 1 OR  &VACT EQ 1 OR  &KACT EQ 1 OR  &PACT EQ 1+
                OR &SPACT EQ 1 OR  &EPACT EQ 1 OR &EJACT EQ 1).BADJDT
         AGO   .ORDROK               THIS IS THE FIRST MACRO-CONTINUE
.*--------------------------------------------------------------------*
.*  ANOTHER JDT MUST HAVE ALREADY BEEN DEFINED-> MNOTE IT.            *
.*--------------------------------------------------------------------*
.BADJDT  ANOP
         MNOTE 12,'JDTMAC MACRO SPECIFIED MORE THAN ONCE-MACRO IGNORED'
         MEXIT
.*--------------------------------------------------------------------*
.*  NO GLOBALS WERE ON; OK TO CONTINUE MACRO.                         *
.*  THE FOLLOWING COUNTERS ARE USED TO MAKE UNIQUE LABELS IN EACH     *
.*  INVOCATION OF JDVERB, JDKEY AND JDPARM MACROS. THEY ARE           *
.*  INITIALIZED HERE. THE '&LEN' GLOBAL DEFINITION IS USED TO         *
.*  CALCULATE EACH MACROS LENGTH BY USING A LABEL ON AN EQUATE.       *
.*  THESE LABELS MUST BE UNIQUE TO EACH MACRO ALSO. THE COUNTERS      *
.*  AND THE &LEN GLOBAL DEFINITIONS ARE INCREMENTED IN EACH MACRO.    *
.*--------------------------------------------------------------------*
.ORDROK  ANOP
&BLKID   SETA  0                 START BLOCK ID AT 0 FOR JDT       @H2A
&LEN     SETA  1                 START LENGTH OFFSET AT 1
&V1      SETA  0                 SET VERB COUNTER ONE
&V2      SETA  1                 SET VERB COUNTER TWO
&K1      SETA  0                 SET KEYWORD COUNTER ONE
&K2      SETA  1                 SET KEYWORD COUNTER TWO
&P1      SETA  0                 SET PARAMETER COUNTER ONE
&P2      SETA  1                 SET PARAMETER COUNTER TWO
&JACT    SETB  1                 SET BIT-TO FLAG JDTMAC BEING USED
         AIF   ('&CSECT' NE '').JDTDC   IS CSECT NAME SPECIFIED?
.*--------------------------------------------------------------------*
.*  NO CSECT NAME SPECIFIED-> MNOTE IT.                               *
.*--------------------------------------------------------------------*
         MNOTE 12,'REQUIRED CSECT NAME IS NOT SPECIFIED'
.*--------------------------------------------------------------------*
.*  BEGIN JDTMAC MACRO DATA DEFINITIONS                               *
.*--------------------------------------------------------------------*
.JDTDC   ANOP                    BEGIN DATA DEFINITIONS - JDTMAC  MACRO
&CSECT   CSECT                   JCL DEFINITION TABLE
B&LEN    EQU   *                 BEGINNING OF JDTMAC MACRO SECTION
         DS    0F
         DC    CL4'JDT '         JDT IDENTIFIER-> 'JDT'
         DC    AL2(E&LEN-B&LEN)  LENGTH OF JDTMAC MACRO SECTION
         DC    XL1'01'           VERSION NUMBER
         DC    XL1'00'           RESERVED
         MODID BR=NO             ISSUE MODID FOR THIS CSECT       @H1A
E&LEN    EQU   *                 END OF JDTMAC MACRO SECTION
         SPACE
         MEND
