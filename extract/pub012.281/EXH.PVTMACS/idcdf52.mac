 /***********************************************************@Y30LB39*/
 /* PROCEDURE NAME - SSCMSG                                  @Y30LB39*/
 /*                                                          @Y30LB39*/
 /* FUNCTION - THIS ROUTINE PRINTS MESSAGES                  @Y30LB39*/
 /*   FROM IDCTSSS0 OR IDCTSCM0 AS DICTATED BY THE MSSC      @Y30LB39*/
 /*   REASON CODE OR THE ECB POST CODE, EITHER ONE PASSED AS @Y30LB39*/
 /*   INPUT ARGUMENTS.                                       @Y30LB39*/
 /*                                                          @Y30LB39*/
 /*   THE REASON CODE IS PASSED TO THIS ROUTINE FROM THE     @Y30LB39*/
 /*   CALLER IN SSRCODE, AND THE ECB POST CODE IS PASSED     @Y30LB39*/
 /*   IN SSECBCD.                                            @Y30LB39*/
 /*                                                          @Y30LB39*/
 /*   ONLY ONE MESSAGE IS PRINTED. IF THE REASON CODE        @Y30LB39*/
 /*   CONTAINS A NUMBER GREATER THAN ZERO, IT IS USED AS     @Y30LB39*/
 /*   THE INDICATION TO CALL UPRINT TO READ IN THE FIRST     @Y30LB39*/
 /*   STATIC TEXT ENTRY FROM IDCTSSS0.  THAT ENTRY CONTAINS  @Y30LB39*/
 /*   AN ARRAY OF THE MESSAGE INDICES USED TO PRINT THE      @Y30LB39*/
 /*   MSSC REASON CODE MESSAGE. THE ARRAY IS SCANNED FOR THE @Y30LB39*/
 /*   REASON CODE THAT WAS PASSED AS THE FIRST PARAMETER.    @Y30LB39*/
 /*   IF THE ARRAY CONTAINS AN ENTRY WITH THAT VALUE, THE    @Y30LB39*/
 /*   ARRAY INDEX IS USED TO PRINT THE MESSAGE.  IF THE      @Y30LB39*/
 /*   REASON CODE IS NOT FOUND IN THE ARRAY, THE REASON      @Y30LB39*/
 /*   CODE IS USED AS BLOCK DATA WHEN PRINTING THE "LAST"    @Y30LB39*/
 /*   STATIC TEXT ENTRY IN THE TEXT MODULE.                  @Y30LB39*/
 /*                                                          @Y30LB39*/
 /*   IF THE REASON CODE FIELD IS ZERO, THE ECB POST CODE    @Y30LB39*/
 /*   IS INSERTED AS BLOCK DATA INTO A GENERAL MESSAGE IN    @Y30LB39*/
 /*   STATIC TEXT MODULE IDCTSCM0.                           @Y30LB39*/
 /*                                                          @Y30LB39*/
 /* INPUT - REASON CODE AS FIRST ARGUMENT, OPTIONALLY THE    @Y30LB39*/
 /*         ECB CODE AS THE SECOND ARGUMENT.                 @Y30LB39*/
 /*                                                          @Y30LB39*/
 /* OUTPUT - PRINTED MESSAGE AND RETURN CODE OF ZERO.        @Y30LB39*/
 /*                                                          @Y30LB39*/
 /***********************************************************@Y30LB39*/
                                                           /*@Y30LB39*/
 SSCMSG: PROCEDURE(SSRCODE,            /* SSC RETURN CODE    @Y30LB39*/
                   SSECBCD);           /* ECB POST CODE      @Y30LB39*/
                                                           /*@Y30LB39*/
 @EJECT;                                                   /*@Y30LB39*/
                                                           /*@Y30LB39*/
         /************************************************************/
         /*                                                  @Y30LB39*/
         /*  DECLARATIONS FOR THE SSCMSG PROCEDURE           @Y30LB39*/
         /*                                                  @Y30LB39*/
         /************************************************************/
                                                           /*@Y30LB39*/
                                                           /*@Y30LB39*/
         /************************************************************/
         /*                                                  @Y30LB39*/
         /*  THE NEXT TWO DECLARES ARE FOR THE TWO INPUT     @Y30LB39*/
         /*  ARGUMENTS. 1ST - MSSC REASON CODE OR            @Y30LB39*/
         /*  2ND - MSSC POST CODE                            @Y30LB39*/
         /*                                                  @Y30LB39*/
         /************************************************************/
                                                           /*@Y30LB39*/
         DCL 1 SSRCODE FIXED(31),                          /*@Y30LB39*/
               3 * FIXED(15),                              /*@Y30LB39*/
               3 SSRHALF FIXED(15);    /*1ST ARG REASON CODE @Y30LB39*/
         DCL SSECBCD PTR(8);           /*2ND ARG POST CODE   @Y30LB39*/
                                                           /*@Y30LB39*/
                                                           /*@Y30LB39*/
         /************************************************************/
         /*                                                  @Y30LB39*/
         /*  DECLARE THE DYNAMIC AREA NEEDED TO POINT TO AND @Y30LB39*/
         /*  CONTAIN THE "LOCAL" PRINT ARGUMENT LIST         @Y30LB39*/
         /*                                                  @Y30LB39*/
         /************************************************************/
                                                           /*@Y30LB39*/
         DCL SSDLPTR PTR(31);                              /*@Y30LB39*/
         DCL SSDRGLT CHAR(LENGTH(DARGLIST));               /*@Y30LB39*/
                                                           /*@Y30LB39*/
         /************************************************************/
         /*                                                  @Y30LB39*/
         /*  THE NEXT DECLARE IS FOR THE INDEX WHICH IS      @Y30LB39*/
         /*  INCREMENTED WHILE SEARCHING THE ARRAY OF        @Y30LB39*/
         /*  REASON CODES AND, UPON FINDING THE CORRECT      @Y30LB39*/
         /*  REASON CODE, IS PASSED TO PRINT MODULE AS THE   @Y30LB39*/
         /*  INDEX OF THE MESSAGE NUMBER TO BE PRINTED.      @Y30LB39*/
         /*                                                  @Y30LB39*/
         /************************************************************/
                                                           /*@Y30LB39*/
         DECLARE ENTRYNUM FIXED(8) UNSIGNED;               /*@Y30LB39*/
                                                           /*@Y30LB39*/
         /************************************************************/
         /*                                                  @Y30LB39*/
         /*  THE NEXT DECLARE IS FOR THE COMMON MESSAGE      @Y30LB39*/
         /*  NUMBER TO IDENTIFY THE ECB POST CODE            @Y30LB39*/
         /*  MESSAGE CONTAINED IN STATIC TEXT MODULE         @Y30LB39*/
         /*  IDCTSCM0.                                       @Y30LB39*/
         /*                                                  @Y30LB39*/
         /************************************************************/
                                                           /*@Y30LB39*/
         DECLARE SSMSGNUM FIXED(8) STATIC LOCAL INIT(56);  /*@Y30LB39*/
                                                           /*@Y30LB39*/
         /************************************************************/
         /*                                                  @Y30LB39*/
         /*  THE NEXT DECLARE IS FOR THE ARRAY WHICH IS      @Y30LB39*/
         /*  SEARCHED FOR THE REASON CODE CORRESPONDING      @Y30LB39*/
         /*  TO THE MESSAGE INDEX AND ULTIMATELY, THE        @Y30LB39*/
         /*  MESSAGE TEXT TO BE PRINTED TO THE USER TELLING  @Y30LB39*/
         /*  HIM OF THE EXACT REASON FOR THE MSSC FAILURE.   @Y30LB39*/
         /*                                                  @Y30LB39*/
         /************************************************************/
                                                           /*@Y30LB39*/
         DECLARE 1 SSINXLST BDY(WORD),                     /*@Y30LB39*/
                   3 SSARRAY (60) FIXED(15);               /*@Y30LB39*/
                                                           /*@Y30LB39*/
         /************************************************************/
         /*                                                  @Y30LB39*/
         /*  THE NEXT DECLARATION IS FOR THE MESSAGE ID      @Y30LB39*/
         /*  USED BY THE STATIC TEXT MODULE - IDCTSSS0.      @Y30LB39*/
         /*                                                  @Y30LB39*/
         /************************************************************/
                                                           /*@Y30LB39*/
         DECLARE SSMODSS0 CHAR(3) STATIC INIT('SS0');      /*@Y30LB39*/
                                                           /*@Y30LB39*/
         /************************************************************/
         /*                                                  @Y30LB39*/
         /*  THE NEXT DECLARATION IS FOR THE MESSAGE ID      @Y30LB39*/
         /*  USED BY THE STATIC TEXT MODULE - IDCTSCMO.      @Y30LB39*/
         /*                                                  @Y30LB39*/
         /************************************************************/
                                                           /*@Y30LB39*/
         DECLARE SSMODCM0 CHAR(3) STATIC INIT('CM0');      /*@Y30LB39*/
                                                           /*@Y30LB39*/
 @EJECT;                                                   /*@Y30LB39*/
                                                           /*@Y30LB39*/
         /************************************************************/
         /*  ISSUE UTRACE MACRO TO PROVIDE A TRACK OF FLOW   @Y30LB39*/
         /*  ON CONTROL WITHIN THE SSMSG MODULE.             @Y30LB39*/
         /*  THIS WILL PLACE THE IDENTIFIER SPECIFIED IN     @Y30LB39*/
         /*  A SEPARATE INTRA-MODULE TRACE TABLE.            @Y30LB39*/
         /*                                                  @Y30LB39*/
         /************************************************************/
                                                           /*@Y30LB39*/
         UTRACE = 'SSAA';                                  /*@Y30LB39*/
                                                           /*@Y30LB39*/
         /************************************************************/
         /*                                                  @Y30LB39*/
         /*  ESTABLISH ADDRESSABILITY TO THE DYNAMIC         @Y30LB39*/
         /*  ARGUMENT LIST.                                  @Y30LB39*/
         /*                                                  @Y30LB39*/
         /************************************************************/
                                                           /*@Y30LB39*/
         SSDLPTR = ADDR(SSDRGLT);                          /*@Y30LB39*/
                                                           /*@Y30LB39*/
         /************************************************************/
         /*                                                  @Y30LB39*/
         /*  IF THE REASON CODE IS NOT ZERO, READ IN THE     @Y30LB39*/
         /*  ARRAY FROM THE STATIC TEXT MODULE - IDCTSSS0.   @Y30LB39*/
         /*                                                  @Y30LB39*/
         /************************************************************/
                                                           /*@Y30LB39*/
         IF SSRCODE > 0 THEN
           DO;                                             /*@Y30LB39*/
                                                           /*@Y30LB39*/
             /********************************************************/
             /*                                              @Y30LB39*/
             /*  CLEAR THE DYNAMIC ARGUMENT LIST AND         @Y30LB39*/
             /*  INITIALIZE THE PRINT ARGUMENT LIST TO READ  @Y30LB39*/
             /*  IN THE ARRAY WHICH CONTAINS THE MSSC REASON @Y30LB39*/
             /*  CODES.                                      @Y30LB39*/
             /*                                              @Y30LB39*/
             /********************************************************/
                                                           /*@Y30LB39*/
             SSDRGLT = NULLAREA;                           /*@Y30LB39*/
             SSDLPTR -> DARGRETP = ADDR(SSINXLST);         /*@Y30LB39*/
             SSDLPTR -> DARGSMOD = SSMODSS0;               /*@Y30LB39*/
             SSDLPTR -> DARGSENT = 1;                      /*@Y30LB39*/
             SSDLPTR -> DARGRETL = LENGTH(SSINXLST);       /*@Y30LB39*/
                                                           /*@Y30LB39*/
             /********************************************************/
             /*                                              @Y30LB39*/
             /*  ISSUE UPRINT MACRO TO READ IN THE SEARCH    @Y30LB39*/
             /*  ARRAY.                                      @Y30LB39*/
             /*                                              @Y30LB39*/
             /********************************************************/
                                                           /*@Y30LB39*/
             UPRINT (GDTTBL,PRTFILE,SSDLPTR);              /*@Y30LB39*/
                                                           /*@Y30LB39*/
             /********************************************************/
             /*                                              @Y30LB39*/
             /*  BEGIN DO LOOP TO SEARCH FOR THE MESSAGE     @Y30LB39*/
             /*  INDEX CORRESPONDING TO THE REASON CODE      @Y30LB39*/
             /*  PASSED AS THE FIRST ARGUMENT.               @Y30LB39*/
             /*  THE DO LOOP BEGINS WITH THE SECOND ARRAY    @Y30LB39*/
             /*  ENTRY SINCE THE FIRST IS REQUIRED BY THE    @Y30LB39*/
             /*  SEARCH ARRAY ITSELF.                        @Y30LB39*/
             /*  THE SEARCH CONTINUES UNTIL THE REASON CODE  @Y30LB39*/
             /*  IS FOUND, AT WHICH TIME THE SEARCH INDEX    @Y30LB39*/
             /*  IS PASSED TO THE PRINT MODULE TO PRINT      @Y30LB39*/
             /*  THE MESSAGE ASSOCIATED WITH THAT PARTICULAR @Y30LB39*/
             /*  REASON CODE.                                @Y30LB39*/
             /*  IF THERE IS NO MATCH FOUND IN THE ARRAY,    @Y30LB39*/
             /*  THEN THE INDEX WILL BE THE LAST POSITION IN @Y30LB39*/
             /*  ARRAY.  THIS WILL BE THE INDICATION THAT    @Y30LB39*/
             /*  THE REASON CODE ITSELF SHOULD BE PRINTED.   @Y30LB39*/
             /*                                              @Y30LB39*/
             /********************************************************/
                                                           /*@Y30LB39*/
             ENTRYNUM = 1;                                 /*@Y30LB39*/
             DO UNTIL (SSRHALF=SSARRAY(ENTRYNUM)×SSARRAY(ENTRYNUM)=0);
               ENTRYNUM = ENTRYNUM + 1;                    /*@Y30LB39*/
             END;                                          /*@Y30LB39*/
                                                           /*@Y30LB39*/
             /********************************************************/
             /*                                              @Y30LB39*/
             /*  THE CORRECT INDEX HAS BEEN FOUND            @Y30LB39*/
             /*                                              @Y30LB39*/
             /*  CLEAR THE DYNAMIC ARGUMENT LIST AND FILL    @Y30LB39*/
             /*  OUT THE PRINT ARGUMENT FIELDS WITH EITHER:  @Y30LB39*/
             /*    1) THE INDEX CORRESPONDING TO THE REASON  @Y30LB39*/
             /*       CODE OR                                @Y30LB39*/
             /*    2) THE LAST INDEX AND THE REASON CODE     @Y30LB39*/
             /*       ITSELF AS A STATIC TEXT INSERT.        @Y30LB39*/
             /*                                              @Y30LB39*/
             /********************************************************/
                                                           /*@Y30LB39*/
             SSDRGLT = NULLAREA;                           /*@Y30LB39*/
             SSDLPTR -> DARGSMOD = SSMODSS0;               /*@Y30LB39*/
             SSDLPTR -> DARGSENT = ENTRYNUM;               /*@Y30LB39*/
             IF SSARRAY(ENTRYNUM) = 0 THEN                 /*@Y30LB39*/
               DO;                                         /*@Y30LB39*/
                 SSDLPTR -> DARGDBP = ADDR(SSRCODE);       /*@Y30LB39*/
                 SSDLPTR -> DARGILP = LENGTH(SSRCODE);     /*@Y30LB39*/
               END;                                        /*@Y30LB39*/
           END;                                            /*@Y30LB39*/
 @EJECT;                                                   /*@Y30LB39*/
                                                           /*@Y30LB39*/
         /************************************************************/
         /*                                                  @Y30LB39*/
         /*  THE FIRST ARGUMENT WAS ZERO, INDICATING THAT    @Y30LB39*/
         /*  THIS ROUTINE IS TO PRINT OUT THE ECB POST       @Y30LB39*/
         /*  CODE.  THEREFORE, CLEAR THE DYNAMIC ARGUMENT    @Y30LB39*/
         /*  LIST AND FILL OUT THE PRINT ARGUMENT FIELDS     @Y30LB39*/
         /*  SO THE STATIC TEXT MODULE, IDCTSCM0, WILL       @Y30LB39*/
         /*  PRINT OUT THE MESSAGE ASSOCIATED WITH THE       @Y30LB39*/
         /*  POST CODE.                                      @Y30LB39*/
         /*                                                  @Y30LB39*/
         /************************************************************/
                                                           /*@Y30LB39*/
         ELSE                                              /*@Y30LB39*/
           DO;                                             /*@Y30LB39*/
             SSDRGLT = NULLAREA;                           /*@Y30LB39*/
             SSDLPTR -> DARGSMOD = SSMODCM0;               /*@Y30LB39*/
             SSDLPTR -> DARGSENT = SSMSGNUM;               /*@Y30LB39*/
             SSDLPTR -> DARGDBP = ADDR(SSECBCD);           /*@Y30LB39*/
             SSDLPTR -> DARGILP = LENGTH(SSECBCD);         /*@Y30LB39*/
           END;                                            /*@Y30LB39*/
                                                           /*@Y30LB39*/
         /************************************************************/
         /*                                                  @Y30LB39*/
         /*  ISSUE UPRINT MACRO TO PRINT THE REASON          @Y30LB39*/
         /*  CODE MESSAGE OR THE ECB POST CODE MESSAGE.      @Y30LB39*/
         /*                                                  @Y30LB39*/
         /************************************************************/
                                                           /*@Y30LB39*/
         UPRINT (GDTTBL,PRTFILE,SSDLPTR);                  /*@Y30LB39*/
                                                           /*@Y30LB39*/
         /************************************************************/
         /*                                                  @Y30LB39*/
         /*  RETURN TO CALLER WITH A RETURN CODE OF ZERO     @Y30LB39*/
         /*                                                  @Y30LB39*/
         /************************************************************/
                                                           /*@Y30LB39*/
                                                           /*@Y30LB39*/
         RETURN CODE(0);                                   /*@Y30LB39*/
                                                           /*@Y30LB39*/
         END SSCMSG;                                       /*@Y30LB39*/
