*/********************************************************************/
*/*                                                                  */
*/*         PL/S VERSION OF THE SMFASGN MACRO                        */
*/*                                                                  */
*/* MACRO NAME = SMFASGN                                             */
*/*                                                                  */
*/* FUNCTION =                                                       */
*/* THE SMFASGN MACRO IS A PL/S II MACRO WHICH CAN BE USED TO SET,   */
*/* INCREASE, OR DECREASE SINGLE WORDS USING THE CS INSTRUCTION.     */
*/* THIS IS USEFUL FOR SETTING FIELDS WHICH ARE SUBJECT TO           */
*/* ASYNCHRONOUS UPDATE IN AN MP ENVIRONMENT.                        */
*/*                                                                  */
*/* THE FORMAT IS THE FOLLOWING:                                     */
*/*            ?SMFASGN  FIELD,SAVE,VALUE,FUNCTION;                  */
*/* WHERE                                                            */
*/*      FIELD = THE FIELD TO BE CHANGED.                            */
*/*                                                                  */
*/*      SAVE  = THE FIELD WHERE THE OLD VALUE IS TO BE SAVED.       */
*/*              IF SAVE=FIELD, NO SAVING IS DONE.                   */
*/*                                                                  */
*/*      VALUE = THE VALUE TO BE ASSIGNED TO 'FIELD', OR THE AMOUNT  */
*/*              'FIELD' IS TO BE INCREASED OR DECREASED.            */
*/*                                                                  */
*/*      FUNCTION = 'SET', 'INC', OR 'DEC', INDICATING WHETHER       */
*/*              'FIELD' IS TO BE SET TO 'VALUE', OR INCREASED OR    */
*/*              DECREASED BY 'VALUE'.  THE DEFAULT IS 'SET'.        */
*/*                                                                  */
*/* FOR EXAMPLE, TO CLEAR THE FIELD TCTFLD, AND SAVE THE OLD VALUE   */
*/* IN SMF30SVF, CODE THE FOLLOWING:                                 */
*/*            ?SMFASGN TCTFLD,SMF30SVF,0,SET;                       */
*/*                                                                  */
*/* TO INCREASE SMCANMFL BY 1, WITHOUT SAVING THE OLD VALUE, CODE    */
*/*            ?SMFASGN SMCANMFL,SMCANMFL,1,INC;                     */
*/*                                                                  */
*/* THE ACTION OF THE MACRO IS TO DO THE ASSIGNMENT UNTIL A NON-ZERO */
*/* RETURN CODE IS RECEIVED FROM THE CS INSTRUCTION.                 */
*/*                                                                  */
*/* NOTES = NONE                                                     */
*/*                                                                  */
*/* DEPENDENCIES = NONE                                              */
*/*                                                                  */
*/* CHANGE ACTIVITY =                                                */
*/*                                                                  */
*/********************************************************************/
 %SMFASGN: MACRO;
  DCL (SRC,SINK,VAL,FCN,IDENT) CHAR;
  SRC=MACLIST(1);
  SINK=MACLIST(2);
  VAL=MACLIST(3);
  FCN=MACLIST(4);
  IDENT=MACLABEL××MACNAME××MACLIST;
  ANS(COMMENT(IDENT)) SKIP;
  ANS(MACLABEL××' DO;') SKIP;
  ANS('RFY (R2,R3) RSTD;') SKIP;
  ANS('R2 = '××SRC××';') SKIP;
  ANS('LUP'××MACINDEX××':') SKIP;
  IF SINK ^= SRC THEN
     ANS(SINK××' = R2;') SKIP;
  IF FCN = 'SET' THEN
     ANS('R3 = '××VAL××';') SKIP;
     ELSE
     IF FCN = '' THEN
     ANS('R3 = '××VAL××';') SKIP;
     ELSE
      IF FCN = 'INC' THEN
         ANS('R3 = R2 + '××VAL××';') SKIP;
         ELSE
          IF FCN = 'DEC' THEN
             ANS('R3 = R2 - '××VAL××';') SKIP;
             ELSE
              DO;
                IDENT = 'INVALID FUNCTION SPECIFIED: '××FCN ;
                ANS(COMMENT(IDENT)) SKIP;
                ANS('RFY (R2,R3) UNRSTD;');
                ANS ('END;') SKIP;
                RETURN CODE(8);
              END;
  ANS('CS(R2,R3,'××SRC××');') SKIP;
  ANS('BC(4,LUP'××MACINDEX××');') SKIP;
  ANS('END;') SKIP;
  ANS('RFY (R2,R3) UNRSTD;') SKIP;
 %END;
