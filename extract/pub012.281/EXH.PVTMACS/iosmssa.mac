         MACRO
&NAME    IOSMSSA &WRK1,&WRK2,&UCBR
         LCLC  &A,&B,&N,&U
         AIF   (T'&WRK1 EQ 'O' OR T'&WRK2 EQ 'O' OR T'&UCBR EQ 'O').ERR
&A       SETC  '&WRK1(1)'                    REMOVE PARENTHESES IF ANY
&B       SETC  '&WRK2(1)'
&U       SETC  '&UCBR(1)'
         AIF   (T'&NAME EQ 'O').NONAME
&NAME    DS    0H
.NONAME  ANOP
&N       SETC  '&SYSNDX'
IECL&N   TM    UCBFL4-UCBOB(&U),X'06'  LOST ATTN? (UCBHOLD+UCBMAT)
         BZ    IECQ&N              NO - EXIT RC=0
         BM    IECS&N              MAYBE - SET FLAG FOR NEXT TIME
         L     &A,UCBSQC-UCBOB(,&U)  YES - GET WORD WITH UCBFL4
         LR    &B,&A               COPY IT
         O     &B,IECA&N           SET UCBATTN
         N     &B,IECB&N           RESET UCBHOLD AND UCBMAT
         CS    &A,&B,UCBSQC-UCBOB(&U)  SWAP IF WORD NOT ALTERED
         BNE   IECL&N              ALTERED - START OVER
         LA    &A,4                SWAP OK - SET RC=4 FOR LOST ATTN
         B     IECR&N              EXIT
************** CONSTANTS USED BY MACRO ********************************
         DS    0F
IECA&N   DC    X'00080000'         USED TO SET UCBATTN
IECB&N   DC    X'FFF9FFFF'         USED TO RESET UCBHOLD AND UCBMAT
IECC&N   DC    X'00020000'         USED TO SET UCBMAT
************** CONTINUE CODE EXPANSION ********************************
IECS&N   L     &A,UCBSQC-UCBOB(,&U)  GET WORD WITH UCBFL4
         LR    &B,&A               COPY IT
         O     &B,IECC&N           SET UCBMAT FOR NEXT TIME
         CS    &A,&B,UCBSQC-UCBOB(&U)  SWAP WORD IF NOT ALTERED
         BNE   IECL&N              ALTERED - START OVER
IECQ&N   SLR   &A,&A               SWAP OK OR NOT NEEDED - SET RC=0
IECR&N   DS    0H                  RETURN TO INLINE CODE
         MEXIT
.ERR     AIF   (T'&UCBR NE 'O').ER1
         IHBERMAC 4
.ER1     AIF   (T'&WRK2 NE 'O').ER2
         IHBERMAC 3
.ER2     AIF   (T'&WRK1 NE 'O').ER3
         IHBERMAC 2
.ER3     MEND
