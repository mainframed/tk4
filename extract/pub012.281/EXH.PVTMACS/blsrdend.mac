*** START OF SPECIFICATIONS********************************************
*                                                                     *
* $MAC(BLSRDEND) COMP(SC132) PROD(JIP1329): END Processing Table      *
*                                                                     *
* COPYRIGHT= 5740-XYN, 5740-XYS Copyright IBM Corporation 1982.       *
*            Licenced material-Program property of IBM,               *
*            Refer to copyright instructions form number G120-2083.   *
*                                                                     *
* FUNCTION= Final macro to be used in the definition of a data name   *
* processing table.                                                   *
*                                                                     *
*   CHANGE ACTIVITY:                                                  *
* $L0= MVSIPCS,EIP1102,78001,DMGPRW: MVS IPCS INITIAL RELEASE CODE    *
* $D1= QUALITY,HIP2102,81029,DSDPRW: Improve comments, remarks        *
* $P1= PAA0007,JIP1329,82159,DSDPRW: Match array dimensions           *
*                                                                     *
***** END OF SPECIFICATIONS********************************************
         MACRO
&LABEL   BLSRDEND &PREFIX
.*--------------------------------------------------------------------*
.* BLSRDEND--Terminate a Data Name Processing Table Definition        *
.*--------------------------------------------------------------------*
         GBLA  &DTDIM
         GBLC  &GP,&GD(200),&GN(200),&GS(200),&GV(200)             @P1C
         LCLA  &JD,&JL,&JX
         LCLC  &CD,&CN,&CS,&CV,&CX
&GP.DIM  DC    F'&DTDIM'               DIMENSION OF ARRAY
*------- End of Table Heading -------------------------------- #MD81029
         SPACE
*------- Begin Body of Table --------------------------------- #MD81029
&JD      SETA  1                       CURRENT DATA NAME INDEX
.L01     AIF   (&JD GT &DTDIM).L99     ALL DATA NAME PROCESSED
&CD      SETC  '&GD(&JD)'              CURRENT DATA NAME
&JL      SETA  K'&CD                   LENGTH OF DATA NAME
&CN      SETC  '&GN(&JD)'              GROUP DATA NAME             @D1C
&CS      SETC  '&GS(&JD)'              FIND EXIT ENTRY POINT NAME
&CV      SETC  '&GV(&JD)'              SCAN EXIT ENTRY POINT NAME
&JX      SETA  &JD                     GROUP SEARCH INDEX          @D1C
         AIF   ('&CD' EQ '&CN').L03    SELF-GROUP                  @D1C
&JX      SETA  &DTDIM                  GROUP SEARCH INDEX          @D1C
&CX      SETC  '&GD(&DTDIM)'           SEARCH DATA NAME
.L02     AIF   ('&CN' EQ '&CX').L03    GROUP NAME FOUND            @D1C
&JX      SETA  &JX-1                   GROUP SEARCH INDEX          @D1C
&CX      SETC  '&GD(&JX)'              SEARCH DATA NAME
         AIF   (&JX GE 1).L02          CONTINUE GROUP NAME SEARCH  @D1C
         MNOTE 8,'GROUP DATA NAME ERROR FOR &CD'                   @D1C
&JX      SETA  &JD                     SUBSTITUTE DATA NAME
.L03     ANOP
&GP.&JD  DC    FL1'&JL',CL31'&CD',F'&JX',CL8'&CS',CL8'&CV'
&JD      SETA  &JD+1                   DATA NAME INDEX
         AGO   .L01
.L99     ANOP
         SPACE
*------- End of Data Name Processing Table ------------------- #MD81029
         DC    CL8'ZAPAREA'            EYE-CATCHER IN DUMP TEXT    @D1A
         DC    CL8'&SYSECT'            CSECT NAME                  @D1A
         DC    CL8'&SYSDATE'           DATE OF COMPILATION         @D1A
         DC    CL8'&SYSPARM'           MAINTENANCE LEVEL           @D1A
         MEND
