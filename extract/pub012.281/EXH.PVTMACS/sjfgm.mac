         MACRO
&NAME    SJFGM &R,&LV=
.**********************************************************************
.*                                                                    *
.*  MODULE NAME = IEFSJFGM                                            *
.*                                                                    *
.*  DESCRIPTIVE NAME = SJF GET LOCAL STORAGE                          *
.*                                                                    *
.*  COPYRIGHT = 5740-XYN COPYRIGHT IBM CORP. 1982,                    *
.*              LICENSED MATERIAL-PROGRAM, PROPERTY OF IBM,           *
.*              REFER TO COPYRIGHT INSTRUCTIONS FORM G120-2083.       *
.*                                                                    *
.*  STATUS = OS/VS2 JBB1328                                           *
.*                                                                    *
.*  FUNCTION = THIS MACRO IS USED IN THE PLACE OF GETMAIN BY          *
.*             THE SCHEDULER JCL FACILITY (SJF) SERVICE ROUTINES,     *
.*             TO OBTAIN THEIR LOCAL STORAGE OUT OF THE STORAGE       *
.*             GETMAINED BY THE SJF CONTROL MODULE (IEFSJCNL).        *
.*                                                                    *
.*    OPERATION = THIS MACRO GENERATES CODE TO:                       *
.*                                                                    *
.*            1. CHECK THE AMOUNT OF AVAILABLE STORAGE TO BE      @H1A*
.*               CERTAIN THERE IS ENOUGH STORAGE FOR THIS         @H1A*
.*               REQUEST. IF THERE IS NOT ENOUGH STORAGE ISSUES   @H1A*
.*               AN ABEND WITH A CODE OF 054 AND A REASON CODE    @H1A*
.*               OF 1 IN REGISTER 15.                             @H1A*
.*                                                                @H1A*
.*            2. LOAD THE ADDRESS OF THE NEXT AVAILABLE BYTE      @H1C*
.*               OF SJF STORAGE IN REGISTER 1.                        *
.*                                                                    *
.*            3. UPDATE THE ADDRESS OF THE NEXT AVAILABLE BYTE    @H1C*
.*               OF SJF STORAGE (FIELD SJCNNBYT IN THE SJF CONTROL    *
.*               WORKAREA, IEFSJCNW) BY ADDING THE SIZE OF            *
.*               STORAGE SPECIFIED IN REGISTER 0.                     *
.*                                                                    *
.*            4. DECREASE THE AMOUNT OF SJF STORAGE REMAINING     @H1C*
.*               (FIELD SJCNSIZE IN THE SJF CONTROL WORKAREA,         *
.*               IEFSJCNW), BY SUBTRACTING THE SIZE OF                *
.*               STORAGE SPECIFIED IN REGISTER 0.                     *
.*                                                                    *
.*  NOTES =                                                           *
.*                                                                    *
.*    DEPENDENCIES =                                                  *
.*                                                                    *
.*            1. THE SJF CONTROL MODULE (IEFSJCNL) MUST HAVE          *
.*               GETMAINED A STORAGE AREA FOR ALL OF SJF.             *
.*                                                                    *
.*            2. ALL SJF SERVICE ROUTINES MUST USE THE PL/S           *
.*               STACK OPTION IN CONJUNCTION WITH THE SJFGM AND       *
.*               SJFFM MACROS, OS(STACK('SJFGM','SJFFM')).  PL/S      *
.*               WILL GENERATE CODE TO INVOKE SJFGM AS FOLLOWS:       *
.*                                                                    *
.*                           SJFGM R,LV=(0)                           *
.*                                                                    *
.*               SJFGM WILL BE INVOKED AFTER THE CALLERS              *
.*               REGISTERS HAVE BEEN SAVED.                           *
.*                                                                    *
.*            3. ON INPUT TO THE SJF SERVICE ROUTINE, REGISTER        *
.*               1 CONTAINS THE ADDRESS OF A 2 WORD PARAMETER         *
.*               LIST. THE FIRST WORD CONTAINS THE ADDRESS OF         *
.*               THE PARAMETER LIST NEEDED FOR THE PARTICULAR         *
.*               SJF SERVICE ROUTINE. THE SECOND WORD CONTAINS        *
.*               THE ADDRESS OF THE SJF CONTROL WORKAREA              *
.*               (IEFSJCNW).                                          *
.*                                                                    *
.*            4. THE SJF SERVICE ROUTINE WHICH INVOKES THIS           *
.*               MACRO MUST HAVE INCLUDED THE SJF WORKAREA            *
.*               MAPPING MACRO (IEFSJCNW).                            *
.*                                                                    *
.*   CHARACTER CODE DEPENDENCIES = NONE                               *
.*                                                                    *
.*   RESTRICTIONS = NONE                                              *
.*                                                                    *
.*   REGISTER CONVENTIONS = THIS MACRO USES REGISTERS 14, 15,         *
.*                          0, AND 1.                                 *
.*                                                                    *
.*   PATCH LABEL = N/A                                                *
.*                                                                    *
.*  MODULE TYPE = MACRO                                               *
.*                                                                    *
.*    PROCESSOR = ASSEM                                               *
.*                                                                    *
.*    MODULE SIZE = N/A                                               *
.*                                                                    *
.*    ATTRIBUTES = EXECUTABLE MACRO                                   *
.*                                                                    *
.*  ENTRY POINT = N/A                                                 *
.*                                                                    *
.*    ENTRY REGISTERS =                                               *
.*      REGISTER 0 = CONTAINS A SUBPOOL NUMBER IN THE HIGH            *
.*                   ORDER BYTE AND THE LENGTH OF STORAGE             *
.*                   REQUESTED IN THE LOW ORDER 3 BYTES.              *
.*                                                                    *
.*      REGISTER 1 = (THIS MACRO USES THE REGISTER 1 WHICH IS         *
.*                   IN THE SAVEAREA POINTED TO BY REGISTER 13).      *
.*                   IT CONTAINS THE ADDRESS OF A 2 WORD              *
.*                   PARAMETER LIST. THE FIRST WORD CONTAINS          *
.*                   THE ADDRESS OF THE PARAMETER LIST NEEDED         *
.*                   FOR THE PARTICULAR SJF SERVICE ROUTINE,          *
.*                   AND THE SECOND WORD CONTAINS THE ADDRESS         *
.*                   OF THE SJF CONTROL WORKAREA (IEFSJCNW).          *
.*                                                                    *
.*      REGISTER 13 = ADDRESS OF A SAVEAREA                           *
.*                                                                    *
.*    INPUT =                                                         *
.*      SJF CONTROL WORKAREA (IEFSJCNW)                               *
.*        SJCNNBYT = ADDRESS OF NEXT AVAILABLE BYTE OF STORAGE        *
.*        SJCNSIZE = SIZE OF SJF STORAGE REMAINING                    *
.*      ALSO SEE ENTRY REGISTERS                                      *
.*                                                                    *
.*    OUTPUT =                                                        *
.*      SJF CONTROL WORKAREA (IEFSJCNW)                               *
.*        SJCNNBYT = ADDRESS OF NEXT AVAILABLE BYTE OF SJF STORAGE    *
.*        SJCNSIZE = SIZE OF SJF STORAGE REMAINING                    *
.*      REGISTER 1 CONTAINS THE ADDRESS OF THE STORAGE OBTAINED       *
.*                                                                    *
.*  EXTERNAL REFERENCES = NONE                                        *
.*                                                                    *
.*    ROUTINES = NONE                                                 *
.*                                                                    *
.*    DATA AREAS = USES THE SJF CONTROL WORK AREA (IEFSJCNW).         *
.*                                                                    *
.*    CONTROL BLOCKS = N/A                                            *
.*                                                                    *
.*  TABLES = NONE                                                     *
.*                                                                    *
.*  MAPPING MACROS = NONE                                             *
.*                                                                    *
.*  MESSAGES = MNOTE 4, SJFGM PARAMETERS NOT VALID                    *
.*                                                                    *
.*  ABEND CODES = ABEND CODE OF HEX 054(DEC 084) AND A REASON     @H1A*
.*                CODE OF 1 IN REGISTER 15 OCCURS WHEN THERE IS   @H1A*
.*                NOT ENOUGH STORAGE FOR THIS REQUEST.            @H1A*
.*                                                                    *
.*  SERIALIZATION = NONE                                              *
.*                                                                    *
.*  CHANGE ACTIVITY = H0,H1                                       @H1C*
.*                                                                    *
.*  $H0 = EXTJCL JBB1328 810316 PD2N: SUPPORT OF EXTENDED JCL         *
.*  $H1 = EXTJCL JBB1328 811202 PDXX: SUPPORT OF EXTENDED JCL     @H1A*
.**********************************************************************
.* A 000000-999999                                                @H0A*
.* ADDED CODE TO CHECK IF THERE'S ENOUGH STORAGE FOR THIS         @H1A*
.*       REQUEST.  IF THERE IS NOT THEN AN ABEND IS ISSUED WITH A @H1A*
.*       CODE OF 054 AND A REASON CODE OF 1 IN REGISTER 15.       @H1A*
         AIF   ('&R' EQ 'R' AND '&LV' EQ'(0)').GMOK
         MNOTE 4, 'SJFGM PARAMETERS NOT VALID'
         AGO   .GMEND
.GMOK    SLA   @00,8(0)           SHIFT LEFT TO ELIMINATE SUBPOOL
         SRA   @00,8(0)           RE-POSITION LENGTH
         L     @15,24(@13)        GET CALLERS REG 1 FROM SAVEAREA
         L     @15,4(@15)         GET ADDRESS OF SJF CONTROL WORKAREA
         USING SJCNW,@15          ESTABLISH ADDRESSIBILITY
         L     @14,SJCNSIZE       GET THE SIZE OF AVAILABLE        @H1A
*                                 STORAGE                          @H1A
         CR    @00,@14            COMPARE STORAGE NEEDED TO        @H1A
*                                 AVAILABLE STORAGE                @H1A
         BH    GM1&SYSNDX         BRANCH IF THERE IS NOT ENOUGH    @H1A
*                                 STORAGE                          @H1A
         L     @01,SJCNNBYT       GET ADDRESS OF NEXT AVAILABLE    @H1C
*                                 BYTE                             @H1C
         LR    @14,@01            SAVE IT FOR CALCULATIONS
         AR    @14,@00            ADD LENGTH OF THIS REQUEST
         ST    @14,SJCNNBYT       STORE NEXT AVAILABE BYTE OF STORAGE
         L     @14,SJCNSIZE       GET SIZE OF STORAGE REMAINING
         SR    @14,@00            SUBTRACT LENGTH OF THIS REQUEST
         ST    @14,SJCNSIZE       STORE SIZE REMAINING
         DROP  @15
         B     GM2&SYSNDX         BRANCH AROUND ABEND              @H1A
GM1&SYSNDX LA  @15,1              LOAD A REASON CODE OF 1          @H1A
*                                 INTO REGISTER 15                 @H1A
         ABEND 084,,,SYSTEM       ABEND WITH A CODE OF HEX 054     @H1A
*                                 (DEC 084)                        @H1A
GM2&SYSNDX DS  0F                 END OF SJFGM                     @H1A
.GMEND   MEND
