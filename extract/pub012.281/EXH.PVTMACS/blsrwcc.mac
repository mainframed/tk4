*** START OF SPECIFICATIONS******************************************
*
* $MAC(BLSRWCC) COMP(SC132) PROD(JIP1329): 3270 WRITE-CONTROL-CHAR
*
* COPYRIGHT= 5740-XYN, 5740-XYS COPYRIGHT IBM CORPORATION 1982.
*            LICENCED MATERIAL-PROGRAM PROPERTY OF IBM,
*            REFER TO COPYRIGHT INSTRUCTIONS FORM NUMBER G120-2083.
*
*   CHANGE ACTIVITY:
* $L0= MVSIPCS,EIP1102,78001,DMGPKB: MVS IPCS INITIAL RELEASE CODE
* $D1= QUALITY,HIP2102,81266,DSDPRW: IMPROVE COMMENTS, REMARKS
*
***** END OF SPECIFICATIONS******************************************
         MACRO
&LABEL   BLSRWCC &LIST,&OP=DC      3270 WRITE-CONTROL-CHARACTER    @D1C
         GBLA  &POSIT,&VALID       FOR COMMUNICATION WITH BLSRCKLTMACRO
         GBLC  &TR3270             TO PICK UP EBCDIC REP. OF 3270 CODE
         LCLC  &BYTE               HOLDS OUTPUT VALUE
&VALID   SETA  0                   SINCE IT'S GLOBAL
         BLSRCKLT &LIST,P=ALARM,V=4 SHOULD ALARM BE SOUNDED?
         BLSRCKLT &LIST,P=RESTORE,V=2 SHOULD KEYBOARD BE UNLOCKED?
         BLSRCKLT &LIST,P=RESETMDT,V=1 SHOULD MODIFIED-TAGS BE RESET?
         AIF   (&VALID EQ N'&LIST).OK  WERE ALL VALUES IN LIST OK?
         MNOTE 8,'UNKNOWN OPERAND(S) IN LIST'
.OK      ANOP
&BYTE    SETC  '&TR3270'(2*&POSIT+1,2)  TRANSLATE THE VALUES SUM
&LABEL   &OP   X'&BYTE'            WRITE CONTROL CHARACTER
         SPACE 2
         MEND
