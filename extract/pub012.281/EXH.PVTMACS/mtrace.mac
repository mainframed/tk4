         MACRO
&NAME    MTRACE &TYPE,&ASCB=,&PSW=
.*    COPYRIGHT =
.*       5740-XYN COPYRIGHT IBM CORP 1977,1980;                @G81LPXU
.*       LICENSED MATERIAL-PROGRAM; PROPERTY OF IBM;
.*       REFER TO COPYRIGHT INSTRUCTIONS FORM NUMBER
.*       G120-2083.
.*
.*    STATUS = OS/VS2 JBB1126                                  @G81LPXU
.*
.*  VALIDITY CHECK INPUT PARAMETERS
.*
         AIF  ('&ASCB'(1,1)  NE '(').ERROR1
         AIF  ('&PSW' EQ '').ERROR2
         AIF ('&TYPE' EQ 'SVC').SVC
         AIF ('&TYPE' EQ 'PI').PI
         AIF ('&TYPE' EQ 'SRB').SRB
         AIF ('&TYPE' EQ 'IO').IO
         AIF ('&TYPE' EQ 'TASK').TASK
         AIF ('&TYPE' NE 'RETURN').ERROR3
.*
.*  INPUT PARAMETERS ARE VALID, NOW GENERATE THE OP CODES
.*
&NAME    DC    XL2'E50D'           ASSIST TRACE OF SVC RETURN
         AGO  .COMMON
.TASK    ANOP
&NAME    DC    XL2'E50C'           ASSIST TRACE OF TASK DISPATCH
         AGO  .COMMON
.IO      ANOP
&NAME    NI    PSATRACE-PSA(0),X'FF'-PSATROFF
         DC    XL2'E50B'           ASSIST TRACE OF I/O INTERRUPTS
         AGO  .COMMON
.SRB     ANOP
&NAME    DC    XL2'E50A'           ASSIST TRACE OF SRB DISPATCH
         AGO  .COMMON
.PI      ANOP
&NAME    DC    XL2'E509'           ASSIST TRACE OF PROG CHK INTERRUPTS
         AGO  .COMMON
.SVC     ANOP
&NAME    DC    XL2'E508'           ASSIST TRACE OF SVC INTERRUPTS
.*
.*  COMMON SECTION OF GENERATED CODE FOR ASID AND PSW OPERANDS
.*
.COMMON  ANOP
.*  GENERATE THE ASID OPERAND
         DC    S(ASCBASID-ASCB&ASCB)    OPERAND 1 POINTS TO ASID
.*  GENERATE THE PSW OPERAND
         AIF  ('&PSW'(1,1)  EQ '(').REGPSW
.*  THE PSW SPECIFIED IS AN ABSOLUTE BASE-DISPLACEMENT
         DC    S(&PSW)                  OPERAND 2 POINTS TO THE PSW
         MEXIT
.*  THE PSW SPECIFIED IS A REGISTER QUANITY
.REGPSW  ANOP
         DC    S(0&PSW)                 OPERAND 2 POINTS TO THE PSW
         MEXIT
.*  FOLLOWING ARE THE ERROR MNOTES PRODUCED BY THE MACRO
.ERROR1  MNOTE 8,'ASCB=&ASCB INVALID; REGISTER SPECIFICATION REQUIRED'
         MEXIT
.ERROR2  MNOTE 8,'PSW= KEYWORD NOT SPECIFIED; KEYWORD REQUIRED'
         MEXIT
.ERROR3  MNOTE 8,'TYPE=&TYPE INVALID'
         MEND
