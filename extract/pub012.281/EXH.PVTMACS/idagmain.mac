 %GOTO SKIP;                       /*SKIP MACRO PROLOG               */
 /********************************************************************/
 /*                                                                  */
 /*   MACRO NAME = IDAGMAIN                                          */
 /*                                                                  */
 /*   DESCRIPTIVE NAME = GETMAIN FOR VSAM OPEN/CLOSE/EOV AUTOMATICS. */
 /*                                                                  */
 /*   FUNCTION = PERFORMS GETMAIN'S FOR VSAM OPEN/CLOSE/EOV MODULES. */
 /*              IT GETS THE CORRECT AMOUNT OF STORAGE AREA FOR THE  */
 /*              AUTOMATICS DECLARED BY THE MODULES.                 */
 /*              REGISTER 15 CONTAINS ADDRESS OF GOTTEN CORE IF      */
 /*              GETMAIN WAS SUCCESSFUL, OTHERWISE REISTER 15        */
 /*              CONTAINS ZERO.                                      */
 /*                                                                  */
 /*   CONTROL BLOCK STRUCTURE = NONE                                 */
 /*                                                                  */
 /*   INCLUDED MACROS = NONE                                         */
 /*                                                                  */
 /*   METHOD OF ACCESS = PL/S - NO DECLARES NECESSARY                */
 /*                                                                  */
 /*   CREATED BY: N/A                                                */
 /*                                                                  */
 /*   STATUS = VS1 RELEASE 4, VS2 RELEASE 3.                         */
 /*                                                                  */
 /*   DATE OF LAST CHANGE = 3 DECEMBER 74                    @Z30SSRL*/
 /*                                                                  */
 /********************************************************************/
 %SKIP: ;                          /*END OF MACRO PROLOG             */
 /********************************************************************/
 /*           IDAGMAIN - GETS CORE STORAGE FOR AUTOMATICS.           */
 /*                      FOR VS2 IDAGMAIN WILL MOVE THE      @Z30SSRL*/
 /*                      ID AND DATE OF THE MODULE TO A      @Z30SSRL*/
 /*                      LOCATION DIRECTLY AFTER THE PLS     @Z30SSRL*/
 /*                      GENERATED SAVE AREA.                @Z30SSRL*/
 /*           DATE OF LAST CHANGE = 3 DECEMBER 74            @Z30SSRL*/
 /********************************************************************/
   GEN;
 %        IF ENV='VS2'  %THEN          /* CHECK FOR VS2 SWITCH AND   */
 %          GOTO IDAGMN1;              /* BYPASS VS1 CODE            */
REG1SAVE EQU   20                       SAVE AREA FOR REGISTER 1
GMRESULT EQU   24                       SAVE AREA FOR ADDR OF GETMAIN
GMWORK   EQU   28                       ADDR OF WORK AREA FOR GETMAIN
WORKPOOL EQU   252                      SUBPOOL 252
         L     @15,CVTPTR               LOAD POINTER TO THE CVT
         L     @15,CVTTCBP(,@15)        LOAD POINTER TO TCB POINTERS
         L     @15,TCBPTR(,@15)         LOAD POINTER TO TCB
         L     @15,TCBRBP(,@15)         LOAD POINTER OF RB
         LA    @15,XRBESA(,@15)         LOAD ADDRESS OF RB SAVE AREA
         STM   @05,@09,0(@15)           SAVE REG 5 TO 9 IN RB SAVE AREA
         LR    @05,@15                  SAVE ADDRESS OF RB SAVE AREA
         ST    @01,REG1SAVE(,@15)       SAVE REGISTER 1 IN 6TH WORD OF
*                                       RB SAVE AREA
         LA    @01,GMWORK(,@15)         POINT TO GETMAIN WORK AREA
         LA    @06,GMRESULT(,@15)       POINT TO GETMAIN RESULT AREA IN
*                                       7TH WORD OF RB SAVE AREA
         LA    @09,WORKPOOL             LOAD SUBPOOL NUMBER
         L     @07,@SIZDATD             LOAD NUMBER OF BYTES REQUIRED
         GETMAIN EC,LV=(@07),A=(@06),SP=(@09),MF=(E,(@01)) DO GETMAIN
         SR    @09,@09                  ZERO PAD CHARACTER
         LTR   @15,@15                  TEST RETURN CODE FOR ERROR
         BNZ   GMN00100                 BRANCH IF ERROR ENCOUNTERED
*                                       IF NO ERROR WAS ENCOUNTERED
         L     @06,0(,@06)              GET ADDRESS OF ACQUIRED STORAGE
         MVCL  @06,@08                  ZERO THE ACQUIRED STORAGE
         B     GMN00200                 CONTINUE PROCESSING AT GMN00200
GMN00100 ST    @09,GMRESULT(,@05)       ZERO THE RESULT WORD IN RB
GMN00200 LR    @15,@05                  GET ADDRESS OF RB SAVE AREA
         LM    @05,@09,0(@15)           RESTORE REGISTER 5 THROUGH 9
         L     @01,REG1SAVE(,@15)       RESTORE REGISTER 1
         L     @15,GMRESULT(,@15)       LOAD GETMAIN RESULT
 %         GOTO IDAGMN2;               /* BYPASS VS2 CODE            */
 %IDAGMN1: ;                           /* START VS2 CODE             */
         L     @00,@SIZDATD             LOAD LENGTH OF AUTOMATICS
         GETMAIN RC,LV=(0),SP=0         ISSUE GETMAIN IN SUBPOOL ZERO
         LTR   @15,@15                  TEST RETURN CODE FOR ERROR
         BNZ   GMN00100                 BRANCH IF ERROR ENCOUNTERED
*                                       IF NO ERROR WAS ENCOUNTERED
         LR    @00,@01                  ADDRESS OF GOTTEN AREA IN R0
         LR    @14,@01                  SAVE THE ADDRESS IN REGISTER 14
         L     @01,@SIZDATD             LENGTH OF GOTTEN AREA
         MVCL  @00,@14                  ZERO THE GOTTEN AREA, R15=0
         LR    @15,@14                  ADDRESS OF AREA IN REGISTER 15
         ST    @15,8(,@13)              FORWARD CHAIN THIS SAVE AREA
 %   IF CALLER='192P'  %THEN        /* IF IT IS 192P                 */
 %   GOTO NEXT;                     /* BYPASS SETTING ID             */
         L     @01,@SIZDATD             LENGTH OF GOTTEN AREA
         ST    @01,88(,@15)             STORE LENGTH OF AREA
         LR    @14,@03                  LOAD BASE REGISTER OF MODULE
         LA    @01,23                   LOAD NEGATIVE OFFSET BYTE NO.
         SR    @14,@01                  NEGITIVE OFFSET 23 BYTES
         MVC   72(16,@15),0(@14)        GET MODULE NAME AND DATE
 %NEXT: ;
         B     GMN00200                 BYPASS SETTING ERROR
GMN00100 SR    @15,@15                  REGISTER 15 = 0 INDICATES ERROR
GMN00200 EQU   *                        R15 CONTAINS ADDRESS OR ZERO
 %IDAGMN2: ;
*        REGISTER 15 CONTAINS EITHER THE ADDRESS OF GOTTEN CORE OR
*        ZERO IF THE GETMAIN WAS UNSUCCESSFUL.
 @ENDGEN;
