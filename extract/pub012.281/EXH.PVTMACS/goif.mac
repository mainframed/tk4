         MACRO
&NAME    GOIF  &SW, .                   SWITCH TO BE TESTED            X
               &SW2, .                  COMPARAND FOR CLC OR CLI       X
               &GT=, .                  BRANCH GREATER                 X
               &GE=, .                  BRANCH GREATER OR EQUAL        X
               &EQ=, .                  BRANCH EQUAL                   X
               &LE=, .                  BRANCH LESS OR EQUAL           X
               &LT=, .                  BRANCH LESS                    X
               &NE=, .                  BRANCH NOT EQUAL               X
               &ZERO=, .                STATUS CONDITION               X
               &NOTZERO=, .             STATUS CONDITION               X
               &POS=, .                 STATUS CONDITION               X
               &NOTPOS=, .              STATUS CONDITION               X
               &NEG=, .                 STATUS CONDITION               X
               &NOTNEG=, .              STATUS CONDITION               X
               &MODE=L, .               STATUS CONDITION               X
               &NO=, .                  SWITCH OFF                     X
               &YES=, .                 SWITCH ON                      X
               &EQUAL=, .               ALTERNATE FOR EQ               X
               &NOTEQ=, .               ALTERNATE FOR NE               X
               &ALL=, .                 BRANCH IF ALL ON               X
               &NONE=, .                BRANCH IF NONE ON              X
               &MIX=, .                 BRANCH IF SOME ON (BUT NOT ALL)X
               &NOTALL=, .              BRANCH IF NONE OR SOME         X
               &ANY=, .                 H NOT ALL OFF (ANY OR ALL ON)  X
               &NOTMIX=, .              BRANCH IF ALL ON OR ALL OFF    X
               &ELSE=, .                UNCONDITIONAL FINAL BRANCH     X
               &ON=, .                  BRANCH IF SINGLE SWITCH ON     X
               &OFF=, .                 BRANCH IF SINGLE SWITCH OFF    X
               &HIGH=,&HI=,&NOTLO=,&NOTLOW=,&LOW=,&LO=,&NOTHI=,        X
               &NOTHIGH=
         GBLA  &INDEX
         GBLC  &BYTE,&CRNT
         GBLC  &DEFNAME(255),&SWNAME(255)
         LCLA  &COUNT1,&COUNT2
         LCLB  &FIRST,&COND(3),&USED(6),&TM,&MASK(3)
         LCLB  &NULL
         LCLC  &PLUS(7)
.*
.*             CHANGES TO RELEASE 3
.*             CHANGES TO RELEASE 2
.*             CHANGES TO RELEASE 1
.*
.* GENERATES INSTRUCTIONS TO TEST A GIVEN CONDITION
.* AND BRANCH IF THE BRANCH CONDITION IS SATISFIED
.*
         AIF   (N'&SW GT 8).ERR1 .      TOO MANY SWITCHES
&COUNT1  SETA  0 .                      INITIALIZE FOR VALIDITY CHECK
.LOOP1   ANOP
&COUNT1  SETA  &COUNT1+1 .              NEXT OPERAND
         AIF   (&COUNT1 GT N'&SW).NXT1 . DONE
&COUNT2  SETA  0 .                      INITIALIZE SWNAME LOOKUP
.LOOP2   ANOP
&COUNT2  SETA  &COUNT2+1 .              NEXT IN SWNAME LIST
         AIF   (&COUNT2 GT &INDEX).ERR2 . DIDN'T FIND NAME
         AIF   ('&SW(&COUNT1)' EQ '&SWNAME(&COUNT2)').LOOP3 FOUND NAME
         AGO   .LOOP2
.ERR2    ANOP  , .                      LOG NO NAME FOUND
         AIF   (N'&SW GT 1).ERR2A
&NAME    GOIF1 &SW,&SW2,GT=&GT&HI&HIGH,GE=&GE&NOTLOW&NOTLO,            X
               EQ=&EQ&EQUAL,LE=&LE&NOTHIGH&NOTHI,LT=&LT&LOW&LO,        X
               ZERO=&ZERO,              STATUS CONDITION               X
               NOTZERO=&NOTZERO,        STATUS CONDITION               X
               POS=&POS,                STATUS CONDITION               X
               NOTPOS=&NOTPOS,          STATUS CONDITION               X
               NEG=&NEG,                STATUS CONDITION               X
               NOTNEG=&NOTNEG,          STATUS CONDITION               X
               MODE=&MODE,              STATUS CONDITION               X
               NE=&NE&NOTEQ,ELSE=&ELSE
         MEXIT
.ERR2A   MNOTE 8,'UNDECLARED BIT SWITCH OR MORE THAN ONE CLI SWITCH'
         MEXIT
.ERR1    ANOP
         MNOTE 8,'TOO MANY SWITCHES, ONLY 8 ALLOWED PER CALL'
         MEXIT
.LOOP3   ANOP
         AIF   (&FIRST).SECOND .        FIRST TIME THROUGH
&FIRST   SETB  1 .                      YES
&BYTE    SETC  '&DEFNAME(&COUNT2)' .    SAVE THE NAME
         AGO   .LOOP1 .                 BACK FOR MORE
.SECOND  ANOP
         AIF   ('&DEFNAME(&COUNT2)' EQ '&BYTE').LOOP1
         MNOTE 8,'SWITCHES NOT ALL IN THE SAME BYTE'
         MEXIT
.NXT1    ANOP
&NAME    GOIF3 &SW,                                                    X
               ALL=&ALL&YES&ON,                                        X
               NONE=&NONE&NO&OFF,                                      X
               MIX=&MIX,                                               X
               NOTALL=&NOTALL,                                         X
               ANY=&ANY,                                               X
               NOTMIX=&NOTMIX,                                         X
               ELSE=&ELSE,                                             X
               ON=&ALL&YES&ON,                                         X
               OFF=&NONE&NO&OFF
         MEND
