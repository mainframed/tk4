         MACRO
&NAME    GETSEQ &SEQADDR
* MACCOMP-SC1CM
* MACSTAT-@G17EP5W
* MACMEAN-GET SEQUENCE NUMBER FOR ERROR ID
         CNOP  0,4                     SET UP ALIGNMENT
         L     2,CVTPTR                GET CVT ADDR
         L     2,CVTRTMCT(2)           GET ADDR OF RTCT
         L     0,RTCTRFLG(2)           GET ORIGINAL VALUE
IHBC&SYSNDX LR    1,0                  MOVE TO REGISTER FOR PROCESSING
         LR    2,0                     SAVE ORIGINAL VALUE
         SLDL  0,16                    MOVE HI-2 BYTES TO REG0
         SLL   0,16                    MOVE TO HI-ORDER BYTES IN REG0
         SRL   1,16                    PUT SEQ# IN LOW ORDER BYTES
         LA    1,1(1)                  INCREMENT COUNTER
         SLL   1,16                    SHIFT OUT ADD OVERFLOW FOR HALF
         SRL   1,16                    RETURN COUNTER TO ORIGINAL POS.
         OR    1,0                     PUT NEW VALUE IN REG1
         LR    0,2                     PUT ORIGINAL VALUE IN REG2
         L     2,CVTPTR                GET CVT ADDR
         L     2,CVTRTMCT(2)           GET ADDR OF RTCT
         CS    0,1,RTCTRFLG(2)         TRY TO SET NEW SEQ NUMBER
         BNE   IHBC&SYSNDX             IF NOT SUCCESSFUL--RETRY
         SLL   1,16                    MOVE OUT NON-SEQ# BYTES
         SRL   1,16                    RETURN SEQ TO ORIGINAL POS.
         AIF   ('&SEQADDR' EQ '').NOSTORE
         LR    0,1                     SAVE ANSWER IN REG 0
         IHBINNRA &SEQADDR             PUT ANSWER ADDR IN REG 1
         STH   0,0(1)                SAVE SEQUENCE NUMBER VALUE
.NOSTORE MEXIT
         MEND
