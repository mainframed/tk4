         MACRO
&NME     SSSHSPE &SID=,&SDA=,&TCB=,&FLAG=,&SVC=XXX,&MF=D
         LCLA  &A1
         LCLC  &LSID,&LSDA,&LTCB,&LFLAG,&LBL,&R,&D
&LSID    SETC  '*'
         AIF   ('&SID' EQ '').GOTSID
&LSID    SETC  '&SID'
         AIF   ('&SID'(1,1) NE '''').GOTSID
&LSID    SETC  ' '
         AIF   (K'&SID LE 2).GOTSID
&LSID    SETC  '&SID'(2,K'&SID-2)
.GOTSID  ANOP
&LSDA    SETC  '*-*'
         AIF   ('&SDA' EQ '').GOTSDA
&LSDA    SETC  '&SDA'
.GOTSDA  ANOP
&LTCB    SETC  '0'
         AIF   ('&TCB' EQ '').GOTTCB
&LTCB    SETC  '&TCB'
.GOTTCB  ANOP
&LFLAG   SETC  '0'
&A1      SETA  0
.LP1     AIF   (&A1 EQ N'&FLAG).END1
&A1      SETA  &A1+1
&LFLAG   SETC  '&LFLAG+SSSH&FLAG(&A1)'
         AGO   .LP1
.END1    AIF   ('&LFLAG' EQ '0').GOTFLAG
&LFLAG   SETC  '&LFLAG'(3,K'&LFLAG-2)
.GOTFLAG AIF   ('&MF(1)' EQ 'E').MFE
         AIF   ('&MF(1)' EQ 'L').MFL
         AIF   ('&MF(1)' EQ 'D').MFD
         MNOTE 8,'MF(1)=&MF(1) IS INVALID. MF(1)=D IS ASSUMED.'
.MFD     ANOP
&D       SETC  'DSECT'
         AIF   ('&MF(2)' EQ '').MFDOK3
         AIF   ('&MF(2)'(1,1) EQ 'Y').MFDOK3
&D       SETC  'DS'
         AIF   ('&MF(2)'(1,1) EQ 'N').MFDOK3
         MNOTE 8,'MF(2)=&MF(2) IS INVALID. MF(2)=N IS ASSUMED.'
.MFDOK3  ANOP
*************************************************************
*                                                           *
*        SSSHSPE -- SUB-SCREEN PARAMETER ENTRY. THIS AREA   *
*        CONTAINS DATA THAT IS ASSOCIATED WITH AN ENTIRE    *
*        SUB-SCREEN.                                        *
*                                                           *
*************************************************************
         SPACE 1
         AIF   ('&NME' EQ '').MFDOK1
&NME     &D    0F
SSSHSPE  DS    0F                  MAP NAME
         AGO   .MFDOK2
.MFDOK1  ANOP
SSSHSPE  &D    0F
.MFDOK2  ANOP
SSSHSID  DC    CL8'&LSID'          NAME OF THIS SUB-SCREEN
SSSHSDA  DC    A(&LSDA)            VALUE TO BE PROVIDED TO ALL
*                                  INTERCEPT ROUTINES DEFINED UNDER
*                                  THIS SUB-SCREEN.
SSSHTCB  DC    A(&LTCB)            IF SSSHFTCB, THEN THE ADDRESS OF THE
*                                  (FIRST) TCB TO WHICH THIS SUB-SCREEN
*                                  IS TO BE ASSIGNED.
SSSHFLAG DC    AL1(&LFLAG)         FLAG BYTE.
*                                  NOTE, AT LEAST ONE OF SSSHFCRN,
*                                  SSSHFTCB, OR SSSHFALL MUST BE ON.
*                                  REDUNDANT BIT SETTINGS ARE ALLOWED.
SSSHFDEL EQU   B'10000000'         THIS SUB-SCREEN IS TO BE DELETED
*                                  FROM THE INDICATED TCB'S.
SSSHFSUB EQU   B'01000000'         THIS SUB-SCREEN IS TO BE PROPAGATED
*                                  TO OR DELETED FROM ALL SUBTASKS OF
*                                  THE INDICATED TCB'S.
SSSHFCRN EQU   B'00100000'         THIS SUB-SCREEN IS TO BE ASSIGNED TO
*                                  OR DELETED FROM THE CURRENT TCB.
SSSHFTCB EQU   B'00010000'         THIS SUB-SCREEN IS TO BE ASSIGNED TO
*                                  OR DELETED FROM THE TCB POINTED TO
*                                  BY SSSHTCB.
SSSHFALL EQU   B'00001000'         THIS SUB-SCREEN IS TO BE ASSIGNED TO
*                                  OR DELETED FROM ALL TCB'S IN THIS
*                                  ADDRESS SPACE EXCEPT THE FIRST TCB
*                                  (THE REGION CONTROL TASK'S TCB).
SSSHSEND DS    0F                  END OF SSSHSPE.
SSSHSLEN EQU   SSSHSEND-SSSHSPE    LENGTH OF SSSHSPE.
         AIF   ('&SVC' EQ '').NOSVC
         SPACE 3
SSSHSVC  EQU   &SVC                SSSH'S SVC
.NOSVC   MEXIT
.MFL     ANOP
&NME     DS    0F
         DC    CL8'&LSID'          NAME OF THIS SUB-SCREEN
         DC    A(&LSDA)            VALUE TO BE PROVIDED TO ALL
*                                  INTERCEPT ROUTINES DEFINED UNDER
*                                  THIS SUB-SCREEN.
         DC    A(&LTCB)            IF SSSHFTCB, THEN THE ADDRESS OF THE
*                                  (FIRST) TCB TO WHICH THIS SUB-SCREEN
*                                  IS TO BE ASSIGNED.
         DC    AL1(&LFLAG)         FLAG BYTE.
*                                  NOTE, AT LEAST ONE OF SSSHFCRN,
*                                  SSSHFTCB, OR SSSHFALL MUST BE ON.
*                                  REDUNDANT BIT SETTINGS ARE ALLOWED.
         MEXIT
.MFE     ANOP
&LBL     SETC  '&NME'
&R       SETC  '1'
         AIF   ('&MF(2)' NE '').GETMF2
&LBL     LA    &R,SSSHSPE          --> SPE
&LBL     SETC  ''
         AGO   .GOTMF2
.GETMF2  AIF   ('&MF(2)'(1,1) EQ '(').MF2REG
&LBL     LA    &R,&MF(2)           --> SPE
&LBL     SETC  ''
         AGO   .GOTMF2
.MF2REG  ANOP
&R       SETC  '&MF(2)'(2,K'&MF(2)-2)
.GOTMF2  AIF   ('&SID' EQ '').NOSID
         AIF   (K'&LSID LE 8).LSIDOK
         MNOTE 4,'SID=&SID IS TOO LONG.'
.LSIDOK  ANOP
&LBL     MVC   SSSHSID-SSSHSPE(,&R),=CL8'&LSID' SET NEW SID
&LBL     SETC  ''
.NOSID   AIF   ('&SDA' EQ '').NOSDA
         AIF   ('&SDA' NE '&SDA(1)').SDAREG
&LBL     LA    0,&SDA              --> SDA
&LBL     SETC  ''
         ST    0,SSSHSDA-SSSHSPE(,&R) STORE
         AGO   .NOSDA
.SDAREG  ANOP
&LBL     ST    &SDA(1),SSSHSDA-SSSHSPE(,&R) STORE SDA VALUE
&LBL     SETC  ''
.NOSDA   AIF   ('&TCB' EQ '').NOTCB
         AIF   ('&TCB' NE '&TCB(1)').TCBREG
&LBL     LA    0,&TCB              --> OBJECT TCB
&LBL     SETC  ''
         ST    0,SSSHTCB-SSSHSPE(,&R) STORE
         AGO   .NOTCB
.TCBREG  ANOP
&LBL     ST    &TCB(1),SSSHTCB-SSSHSPE(,&R) STORE TCB PTR
&LBL     SETC  ''
.NOTCB   AIF   ('&FLAG' EQ '').NOFLAG
&LBL     MVC   SSSHFLAG-SSSHSPE(&R),&LFLAG SET FLAGS
&LBL     SETC  ''
.NOFLAG  ANOP
         MEND
