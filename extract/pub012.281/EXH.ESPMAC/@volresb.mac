         MACRO ,                                                GP10187
         @VOLRESB ,                                             GP10187
         COPY  OPTIONGB                                         GP10187
*    THIS CODE ACTS AS A @VOLREAD SUBSTITUTE.                    90260
*    IT IS INVOKED WHEN CALLER IS NOT AUTHORIZED, OR WHEN       GP10187
*        INVOKED WITH THE 'UNAUTHORIZED' OPTION (USED WHEN CHANGES
*        ARE TO BE MADE IN OUTPUT ONLY, NOT IN SOURCE).          90260
*    IT IS INVOKED FOR UNSUPPORTED ACCESS METHODS (E.G., VSAM),  90260
*        AND FOR @VOLREAD UNSUPPORTED DEVICES (E.G., TAPE).      90260
*                                                                90260
VOLFAKE  PGMHEAD SAVE=FAKESAVE,BASE=R12,PARM=R1                  90260
         USING FAKESAVE,R13  DECLARE NEW START OF WORK AREA      90260
         XC    VFRET,VFRET   CLEAR RETURN CODES                  90260
         STM   R0,R1,VFREG0  SAVE ENTRY PARMS                    90260
         ST    R1,VFRET1     ALSO SAVE FOR RETURN                90260
         ICM   R0,15,@INPREAD  INPUT READER AVAILABLE ?          90260
         BNZ   VOLFAKEL      YES                                 90260
         SERVCALL LPALD,=CL8'@INPREAD'  LOAD IT NOW              90260
         ST    R0,@INPREAD                                       90260
VOLFAKEL LM    R0,R1,VFREG0  RELOAD ENTRY REGISTERS              90260
         LA    R7,0(,R1)     CLEAR PARM                          90260
         BIX   ERR=VFEXIT16,PFX=VF,BASE=VOLFAKE,                       *
               LOC=(CLOSE,CLOSE,OPEN,DSCB,DSC3,SERCH,SEEK,FIND,GETDE,  *
               READ,NOTE,POINT,GET,UPDAT)                        90260
VFUPDAT  DS    0H                                                90260
VFFIND   DS    0H                                                90260
VFGET    DS    0H                                                90260
VFEXIT16 MVI   VFRETCC,16    SET MAJOR ERROR                     90260
         B     VFEXIT        AND GET OUT                         90260
         SPACE 1                                                 90260
VFEXIT12 MVI   VFRETCC,12    SET SEVERE ERROR                    90260
         B     VFEXIT                                            90260
         SPACE 1                                                 90260
VFEXIT8  MVI   VFRETCC,8     SET ERROR                           90260
         B     VFEXIT                                            90260
         SPACE 1                                                 90260
VFEXIT4  MVI   VFRETCC,4     SET MINOR PROBLEM                   90260
         SPACE 1                                                 90260
VFEXIT   LM    R15,R1,VFRET  LOAD RETURN VALUES                  90260
         L     R13,4(,R13)   GET OLD SAVE                        90260
         L     R14,12(,R13)  GET RETURN                          90260
         LM    R2,R12,28(R13)  RETORE USER'S REGS                90260
         BR    R14           RETURN                              90260
         SPACE 1                                                 90260
VFCLOSE  INPCLOSE DEV=6      CLOSE THE FILE (DON'T CARE IF ANY)  90260
         ST    R15,VFRET     PROPAGATE RETURN CODE               90260
         MVI   VFFLAG,0      RESET FLAGS                         90260
         B     VFEXIT        RETURN WITH CC SET                  90260
         SPACE 1                                                 90260
VFOPEN   LTR   R7,R7         VOL-SER PASSED ?                    90260
         BZ    VFEXIT16      NO; MAJOR BOO-BOO                   90260
         MVC   VOLSER,0(R7)  SAVE IT                            GP99033
         TM    VFFLAG,VFGOPEN+VFGDSN  CURRENTLY OPEN ?           90260
         BO    VFEXIT12      YES; ERROR                          90260
         CLI   CURTYPE+2,UCB3DACC  DASD INPUT ?                  90260
         BNE   VFOPEN2       NO; DON'T NEED @OBTAINS             90260
         ICM   R0,15,@OBTAIN  LOADED BEFORE ?                    90260
         BNZ   VFOPEN0       YES                                GP99035
         SERVCALL LPALD,=CL8'@OBTAINS'  GET OLD CODE             90260
         ST    R0,@OBTAIN    SAVE ADDRESS                        90260
         LTR   R0,R0         FOUND IT ?                          90260
         BZ    VFOPEN2       NO; WILL DEFAULT TO OS OBTAIN SVC   90260
         SLR   R0,R0                                             90260
         ICM   R0,3,VFREG0   ANY SIZE ?                         GP99035
*?       BZ    VFOPEN0       NO                                 GP99035
         @OBTAIN (R0),OPT=SIZE  SET SIZE                         90260
         ST    R15,VFRET     SAVE RETURN                        GP99033
VFOPEN0  L     R14,PATFMT1   BUILD CAMLISTS                      81340
         LA    R15,CCHHR                                         81340
         LA    R0,VOLSER                                         81340
         LA    R1,DS1DSNAM   OUTPUT AND WORK AREA                81340
         STM   R14,R1,GTDSCB1  SEEK LIST                         81340
         LA    R15,CCHHR3    POINT TO DSCB1 LINKED DSCB         GP99034
         LA    R1,IECSDSL3   POINT TO SEEK ENTRY                GP99034
         STM   R14,R1,GTDSCB3  OTHER SEEK LIST                  GP99034
         L     R14,PATFMT4                                       81340
         LA    R15,DS1DSNAM  SET TO 44X'04'                      81340
*        LA    R0,VOLSER                                         81340
         LA    R1,DS4IDFMT                                       81340
         STM   R14,R1,GTDSCB4  SEARCH FOR FORMAT 4               81340
         MVI   DS1DSNAM,X'04'    MAKE FORMAT 4 DSCB DSNAME       81340
         MVC   DS1DSNAM+1(L'DS1DSNAM-1),DS1DSNAM                 81340
         @OBTAIN GTDSCB4,OPT=LOADED   LOOK FOR FMT4/SET LOAD FG  81357
         LA    R0,DS4IDFMT   RETURN DATA (NO KEY)               GP99033
         STM   R15,R0,VFRET  SET RETURN R15 AND R0              GP99033
         B     *+4(R15)      CHECK RETURN                       GP99033
         B     VFOPEN1       OK - BUT TAKEN FOR DSCB 1 ONLY     GP99033
         B     VFEXIT        NOT MOUNTED                        GP99033
         B     VFOPEN1   SAYS DSCB 4 NOT FOUND; BUT FOUND IT ANYWAY !
         NOP   0             I/O ERROR ?                        GP99033
         NOP   0             INVALID WORK-AREA ? COME ON ?      GP99033
VFOPEN1  MVC   CCHHR(4),DS4VTOCE+2  GET VTOC LOW EXTENT ( CCHH ) 81340
*SORT OPTION RETURNS 1ST FMT1 TWICE - FIX                       GP10187
*DEBUG*  MVI   DS1FMTID,X'A2'  PRESET FOR SORT ON NEXT CALL     GP99033
         SR    R15,R15                                          GP99035
         ST    R15,VFRET     SET GOOD RETURN                    GP99035
         STC   R15,R         SET TO READ RECORD 1 IN VFDSCB     GP99035
VFOPEN2  MVC   VFWORK(VFPATWN-VFPATWK),VFPATWK  @INPREAD REQUEST 90260
         MVC   VFWORK(L'DCBDDNAM),CURDDNM  PROPAGATE DD          90260
         LA    R0,INFMJFCB   GET JFCB                            90260
         ST    R0,VFWORK+32  SET JFCB ADDRESS                    90260
         LA    R0,DEBXLE     GET MEMBER RETURN AREA              90260
         ST    R0,VFWORK+20  SET PDE REQUEST                     90260
         OI    VFFLAG,VFGOPEN  SET VOLUME OPEN                   90260
         B     VFEXIT        RETURN OK                           90260
         SPACE 1                                                 90260
VFSERCH  LTR   R7,R7         ANY ?                               90260
         BZ    VFEXIT16      NO PARM, NO GLORY                   90260
         MVC   DS1DSNAM,0(R7)  SAVE DATASET NAME                 90260
         CLI   CURTYPE+2,UCB3DACC  DASD INPUT ?                  90260
         BNE   VFSERF        NO; FAKE IT                         90260
         SERVCALL DSDJ1,(R7)  GET DSCB FOR DESIRED DS            90260
         MVC   DS1FMTID(LENDSCB1-L'DS1DSNAM),0(R1)  COPY DATA    90260
         MVC   CCHHR3,DS1PTRDS  PRESET SEEK FUNCTION ADDRESS    GP99034
         LA    R0,DS1DSNAM   POINT TO USER'S AREA                90260
         STM   R15,R0,VFRET  RETURN CODE AND ADDRESS             90260
         TM    VFFLAG,VFGDSN      DS OPEN                       GP10196
         BZ    VFEXIT             NO                            GP10196
         INPCLOSE DEV=6           CLOSE IT                      GP10196
         ZI    VFFLAG,VFGDSN      SHOW CLOSED                   GP10196
         B     VFEXIT        RETURN DSCB                        GP99033
         SPACE 1                                                 90260
VFSERF   BAS   R9,VFOPNCK    OPEN THE DATASET                   GP99033
         XC    DS1FMTID(LENDSCB1-L'DS1DSNAM),DS1FMTID  CLEAR     90260
         L     R7,VF@WORK    GET WORK AREA ADDRESS               90260
         USING RDRMAP,R7     DECLARE MAPPING                     90260
         L     R2,RDDCB@     GET DCB ADDRESS                     90260
         MVC   DS1RECFM,DCBRECFM-IHADCB(R2)                      90260
         MVC   DS1LRECL,DCBLRECL-IHADCB(R2)                      90260
         MVC   DS1BLKL,DCBBLKSI-IHADCB(R2)                       90260
         MVI   DS1DSORG,JFCORGPS                                 90260
         MVI   DS1NOEPV,1 ONE EXTENT                             90260
         MVI   DS1LSTAR+L'DS1LSTAR-1,1 ONE BLOCK                GP99033
         MVI   DS1FMTID,C'1' FAKE ID                            GP99033
         LA    R0,DS1DSNAM   SET FAKE RETURN                     90260
         ST    R0,VFRET0     AND GIVE BACK TO USER               90260
         B     VFEXIT                                            90260
         DROP  R7                                                90260
         SPACE 1                                                 90260
VFNOTE   INC   CURTTR,INC=256,WORK=R15                           90260
         ST    R15,VFRET0                                        90260
         B     VFEXIT        QUIT                                90260
         SPACE 1                                                 90260
VFGETDE  BAS   R9,VFOPTST    SEE IF OPEN(ABLE)                  GP99033
VFGETDEG INPREAD DEV=6       GET ANOTHER BLOCK  (./ ADD CARD)    90262
         BXH   R15,R15,VFEODAD  GET OUT IF NO MORE               90260
         MVC   VFRET0,PDNEXT  SET NEXT MEMBER ADDRESS            90260
         OI    VFRET0,X'80'  SET 'LIST DONE' SIGNAL              90262
         B     VFEXIT        RETURN                              90260
         SPACE 1                                                 90260
*        IN PUNK, POINT IS CALLED ONLY TO REPOSITION TO THE      90260
*        START OF A MEMBER OR DATASET AFTER AT LEAST ONE READ.   90260
*          THEREFORE, THE TTR IS IGNORED, AND THE DS RE-OPENED.  90260
*        NOTE THAT VOLREAD POINT ALSO READS THE DESIRED BLOCK    90262
VFPOINT  TM    DS1DSORG,JFCORGPO PARTITIONED ? 90260            GP99033
         BNZ   VFPOINT2                                          90260
         INPCLOSE DEV=6      CLOSE CURRENT DATASET               90260
         NI    VFFLAG,255-VFGDSN  RESET DCB OPEN                 90260
         B     VFREAD        AND OPEN AND READ                   90262
VFPOINT2 INPKEEPM DEV=6      RE-OPEN THE CURRENT MEMBER          90262
*        B     VFREAD        READ THE FIRST BLOCK                90262
         SPACE 1                                                 90260
VFREAD   BAS   R9,VFOPNCK    SEE IF OPEN(ABLE)                  GP99033
         INPREAD DEV=6       GET NEXT BLOCK                      90260
         BXH   R15,R15,VFEODAD  I/O ERROR ?                      90260
         STM   R0,R1,VFRET0  RETURN LENGTH/ADDRESS               90260
         L     R7,VF@WORK    GET WORK AREA ADDRESS               90260
         USING RDRMAP,R7     DECLARE MAPPING                     90260
*PUNK*   TM    RDFLAG,RDFACB VSAM ?                              90260
*PUNK*   BNZ   VFREAD2       YES; RECORD LENGTH IS SET           90260
*PUNK*   L     R2,RDDCB@     GET DCB ADDRESS                     90260
*PUNK*   LH    R0,DS1LRECL GET ORIGINAL SIZE                     90260
*PUNK*   TM    DS1RECFM,DCBRECU F OR V ?                         90260
*PUNK*   BM    VFREAD2       YES                                 90260
*PUNK*   LH    R0,DCBLRECL-IHADCB(R2)  ELSE USE CURRENT          90260
VFREAD2  ST    R0,CURRECLN   FAKE FIXED LOGIC                    90260
         LTR   R1,R1         IS THIS A NEW MEMBER ?              90260
         BNM   VFEXIT        NO; RETURN                          90260
         INPKEEP DEV=6       SET TO REREAD                       90260
         B     VFEXIT4       AND RETURN END-FILE                 90260
         DROP  R7                                                90260
         SPACE 1                                                 90260
VFEODAD  INPCLOSE DEV=6      END-FILE READ; CLOSE INPUT          90260
         NI    VFFLAG,255-VFGDSN  SHOW CLOSED                   GP10196
         B     VFEXIT4       SET EOF RETURN CODE                 90260
         SPACE 1                                                 90260
VFOPTST  LTR   R7,R7         ANY PARM ?                          90260
         BZ    VFEXIT16      NO; GET OUT                         90260
VFOPNCK  TM    VFFLAG,VFGOPEN+VFGDSN   WAS OPEN CALLED/OPENED ?  90260
         BOR   R9            YES; RETURN TO CALLER               90260
         BZ    VFEXIT16      NO; FAIL                            90260
         OI    JFCBTSDM,JFCNWRIT+JFCNDSCB+JFCNDCB  NO MERGES     82053
         MVI   JFCRECFM,X'C0'  FAKE AS RECFM=U FOR INPREAD      GP99035
         MVC   JFCLRECL,=H'100'  AND SET DIRECTORY BLOCK SIZE   GP99035
         AIF   (&SVCJFCB EQ 0).KILLMSK  NO MASK PROCESSING       90260
         TM    DS1DSORG,JFCORGPO PARTITIONED ?                   90260
         BZ    VFOPNOM       NO; DON'T MESS WITH SWA             90260
         LA    R14,VFWORK                                        90260
         LA    R15,ZEROES    NO MEMBER NAME                      90260
         STM   R14,R15,VFDB    MAKE SKELETON                     90260
         MVI   VFDB,X'01'      SET DDNAME ENTRY                  90260
         MVI   VFDB+4,X'84'    SET MEMBER/END-LIST               90260
         LA    R1,VFDB                                           90260
         SVC   &SVCJFCB      REQUEST JFCB MODIFICATION           90260
VFOPNOM  DS    0H                                                90260
.KILLMSK INPOPEN VFWORK,OPT=(JFCB),DEV=6  OPEN THE DATASET       90260
VFOPCOM  CH    R15,=H'4'     DID IT WORK ?                       90260
         BH    VFEXIT8       NO                                  90260
         ST    R0,VF@WORK    SAVE WORK AREA ADDRESS              90260
         MVI   VFFLAG,VFGDSN+VFGOPEN  SHOW DSN OPEN              90260
         BR    R9            ELSE RETURN                         90260
         SPACE 1                                                GP99033
VFDSCB   XR    R2,R2                                            GP99033
         IC    R2,R          GET PREVIOUS RECORD NUMBER         GP99033
         LA    R2,1(,R2)     BUMP BY ONE                        GP99033
         STC   R2,R                                             GP99033
         CLM   R2,1,DS4DEVDT  IS IT HIGHER THAN FITS ON TRACK ?  81340
         BNH   FIND1         NO, CHECK AGAINST LAST DSCB 1      GP99033
         MVI   R,1           YES, RESET RECORD NO. TO 1         GP99033
         LH    R2,HH         GET TRACK NUMBER                   GP99033
         LA    R2,1(,R2)     AND BUMP BY ONE                    GP99033
         STH   R2,HH                                            GP99033
         CH    R2,DS4DEVSZ+2  STILL IN SAME CYLINDER ?           81340
         BL    FIND1         YES, CHECK FOR VALID ADDRESS       GP99033
         STCM  R2,12,HH      NO, RESET TRACK TO 0                81340
         LH    R2,CC         AND BUMP CYLINDER                  GP99033
         LA    R2,1(,R2)     NUMBER BY 1                        GP99033
         STH   R2,CC                                            GP99033
FIND1    CLC   CCHHR,DS4HPCHR   BEYOND LAST FORMAT 1 ?           81340
         BH    VFDSCBX       YES (MAY FAIL IF SORTED?)          GP99033
         @OBTAIN GTDSCB1     LOOK FOR FMT1                      GP99033
         ST    R15,VFRET     SET RETURN R15                     GP99033
         B     *+4(R15)      CHECK RETURN                       GP99033
         B     VFDSCB1       OK - BUT TAKEN FOR DSCB 1 ONLY     GP99033
         B     VFEXIT        NOT MOUNTED                        GP99033
         B     VFDSCB4   SAYS DSCB 4 NOT FOUND; BUT FOUND IT ANYWAY !
         NOP   0             I/O ERROR ?                        GP99033
         NOP   0             INVALID WORK-AREA ? COME ON ?      GP99033
         B     VFDSCBX                                          GP99033
IOERR    B     VFEXIT        SET FOR FORMAT 4 ERROR              81340
         SPACE 1                                                GP99033
VFDSCB1  CLI   DS1FMTID,X'FF'  END OF SORTED VTOC?              GP99033
         BE    VFDSCBX                                          GP99033
         CLI   DS1FMTID,C'1'    IS IT TYPE 1 DSCB ANYWAY ?      GP99033
         BNE   VFDSCB        IF NOT TYPE 1 IT'S A VALID 'NOT FOUND'
         MVC   CCHHR3,DS1PTRDS  PRESET SEEK FUNCTION ADDRESS    GP99034
VFDSCB4  LA    R0,DS1DSNAM   POINT TO START                     GP99033
         SR    R15,R15       FAKE GOOD RETURN                   GP99035
         STM   R15,R0,VFRET  RETURN IN R0                       GP99035
         TM    VFFLAG,VFGDSN      DS OPEN                       GP10196
         BZ    VFEXIT             NO                            GP10196
         INPCLOSE DEV=6           CLOSE IT                      GP10196
         ZI    VFFLAG,VFGDSN      SHOW CLOSED                   GP10196
         B     VFEXIT        RETURN FORMAT 4                    GP99033
VFDSCBX  LA    R15,8                                            GP99033
         SR    R0,R0                                            GP99033
         STM   R15,R0,VFRET  RETURN END OF VOLUME               GP99033
         B     VFEXIT                                           GP99033
         SPACE 1                                                 81340
VFSEEK   MVC   CCHHR3,VFREG1  SET CALLER'S CCHHR ADDRESS        GP99034
VFDSC3   OBTAIN GTDSCB3      READ USER'S ADDRESS                GP99034
         BXH   R15,R15,VFDSCBX  TAKE BAD EXIT                   GP99034
         LA    R0,IECSDSL3   POINT TO RETURN                    GP99034
         ST    R0,VFRET0                                        GP99034
         MVC   CCHHR3,DS3PTRDS  PRESET SEEK FUNCTION ADDRESS    GP99034
         B     VFEXIT                                           GP99034
         SPACE 1                                                 81340
*        LISTS FOR OBTAIN AND SCRATCH                           GP99033
*                                                               GP99033
PATFMT4  CAMLST SEARCH,1,2,3   DSN4,VOLSER,DS4IDFMT              81340
         ORG   PATFMT4+4                                         81340
PATFMT1  CAMLST SEEK,1,2,3  CCHHR,VOLSER,DS1DSNAM                81340
         ORG   PATFMT1+4                                         81340
         SPACE 2                                                 90260
VFPATWK  INPWORK DDNAME,WIDTH=0,JFCB=1,PDE=3, ODAD=VFEODAD,            *
               PDS=(DIR)     READ DIRECTORY ONLY                GP99035
VFPATWN  EQU   *                                                 90260
         LTORG ,
         MEND  ,                                                GP10187
