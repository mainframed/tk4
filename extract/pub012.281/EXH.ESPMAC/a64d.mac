         MACRO ,
&NM      A64D  &CTR,&INC,&WK=R14
         GBLC  &MACPLAB
.*  ADD A 64-BIT VALUE TO A 64-BIT VALUE.
.*  WK MUST BE AN EVEN REGISTER, AND NOT THE '(INC)' PAIR
.*  CTR MAY BE A DOUBLE-WORD STORAGE ADDRESS, OR AN EVEN REGISTER
.*  INC MAY BE A DOUBLE-WORD STORAGE ADDRESS, OR AN EVEN REGISTER
.*                                                   UPD 2013/05/01 GYP
         LCLA  &K
         LCLB  &TOR,&FRO     ON IF REGISTER
         LCLC  &N,&RE,&RO,&FE,&FO
&N       SETC  'ZZA'.'&SYSNDX'
&MACPLAB SETC  '&NM'
         AIF   ('&CTR(1)' EQ '&WK').REGROUT
&K       SETA  K'&CTR
         AIF   (&K LT 3).NOTROUT
         AIF   ('&CTR'(1,1) NE '(').NOTROUT  POSSIBLE REGISTER SPEC?
         AIF   ('&CTR'(2,1) EQ '(').NOTROUT  EXPRESSION?
         AIF   ('&CTR'(&K,1) NE ')').NOTROUT  POSSIBLE REGISTER SPEC?
         AIF   ('&CTR'(&K-1,1) EQ ')').NOTROUT  EXPRESSION?
.REGROUT ANOP  ,
&TOR     SETB  1
&RE      SETC  '(&CTR(1))'
&RO      SETC  '(1+&CTR(1))'
         AGO   .LOOKINC                                         GP13349
.NOTROUT ANOP  ,
&RE      SETC  '&WK'
&RO      SETC  '1+&WK'
.LOOKINC ANOP  ,
&K       SETA  K'&INC
         AIF   (&K LT 3).NOTRINC
         AIF   ('&INC'(1,1) NE '(').NOTRINC  POSSIBLE REGISTER SPEC?
         AIF   ('&INC'(2,1) EQ '(').NOTRINC  EXPRESSION?
         AIF   ('&INC'(&K,1) NE ')').NOTRINC  POSSIBLE REGISTER SPEC?
         AIF   ('&INC'(&K-1,1) EQ ')').NOTRINC  EXPRESSION?
.REGRINC ANOP  ,
&FRO     SETB  1
&FE      SETC  '(&INC(1))'
&FO      SETC  '(1+&INC(1))'
         AGO   .PROCESS
.NOTRINC ANOP  ,
&FE      SETC  '&INC'
&FO      SETC  '4+&INC'
.PROCESS AIF   (&TOR).NOLM   OUTPUT IN REGS - SKIP LOAD
         MACPARM &RE,&RO,&CTR,OP=LM,MODE=THREE
.NOLM    MACPARM &RO,&FO,OP=AL,OPR=ALR
         MACPARM 12,&N.N,OP=BC   NO CARRY
         MACPARM &RE,=F'1',OP=A     PROPAGATE CARRY
&N.N     MACPARM &RE,&FE,OP=A,OPR=AR
         AIF   (&TOR).MEND
         MACPARM &RE,&RO,&CTR,OP=STM,MODE=THREE
.MEND    MEND  ,
