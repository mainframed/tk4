         MACRO
         DSUBSECT
         GBLC  &MODEL                                           GP08250
DSUBSECT CSECT
         USING *,R8 .        ASSUMED BASE REGISTER
UNITSTAR XUCBLOOP R3=YES,LOOP=UNITLOOP   UCBSCAN ROUTINE        GP08250
         B     4(,R9) .      RETURN NEXT UCB
UNITEXIT BR    R9 .          RETURN END OF SCAN
         SPACE 3
         USING EXHBWORK,R11
         USING SPWORK,R7
         USING EXHBWENT,R10
SPACING  LH    R1,EXHFSTRT . GET START DISPLACEMENT
         LR    R2,R1 .       SAVE
         A     R2,BUFAD .    GET ACTUAL START ADDRESS
         ST    R2,SPCURR .   SAVE START POSITION
         XR    R0,R0
         LH    R2,LSIZE .    GET SCREEN LINE SIZE
         AR    R1,R2 .       ADD TO DISPLACEMENT
         DR    R0,R2 .       DIVIDE BY SIZE
         MR    R0,R2 .       MULTIPLY BY SIZE, GIVES NEXT FULL LINE
         A     R1,BUFAD .    MAKE INTO A LINE ADDRESS
         ST    R1,SPLEND .   SET BEGINNING OF NEW LINE
         XR    R0,R0
         LH    R1,EXHFEND .  LAST BYTE OF FUNCTION
         LA    R1,1(,R1) .   ADD ONE
         DR    R0,R2 .       DIVIDE BY SIZE
         MH    R1,LSIZE .    AND BACK
         LTR   R0,R0 .       DID IT END ON A PHYSICAL LINE ?
         BZ    *+6 .         YES, USE END BYTE ONE HIGHER
         BCTR  R0,0 .        USE END ONE LOWER THAN ACTUAL END
         AR    R1,R0 .       NEW END BYTE ADDR.
         A     R1,BUFAD .    AND ADDR. IN STORAGE
         ST    R1,SPLAST .   SAVE END BYTE
         C     R1,SPLEND .   LESS THAN ONE LINE ?
         BNLR  R9 .          NO, RETURN
         ST    R1,SPLEND .   USE END INSTEAD OF NEXT LINE
         BR    R9 .          RETURN
         SPACE 3
*        COMMON CODE TO MOVE STUFF TO DISPLAY LINE
*        SETUP :  R14   LENGTH OF ITEM
*                 R15 'FROM' ADDRESS (MOVE SOURCE) OR ZERO FOR NO MOVE
*        RETURN : R1  START 'TO' MOVE ADDRESS
*        0(R9)  OUTPUT AREA FULL
*        4(R9)  NORMAL RETURN
*    CODE MUST BE INITIALIZED VIA BAS R9,SPACING, WITH R10,R11 NORMAL
*        FOR THE DISPLAY FUNCTIONS; R8 POINTING TO THIS CSECT,
*        AND R7 POINTING TO A WORK AREA OF AT LEAST  16 BYTES.
*     CODE WILL ADD TRAILING BLANKS BETWEEN ITEMS
*
SPFITS   XR    R15,R15 .     ENTRY TO SEE IF ITEM FITS AND LINE ADVANCE
SPMOVE   STM   R2,R3,28(R13) .   SAVE
         CH    R14,LSIZE .   FIT ON ONE LINE ?
         BNH   *+8 .         YES, OK
         LH    R14,LSIZE .   NO, DON'T HONOR MORE THAN LINE SIZE
         L     R1,SPCURR .   GET CURRENT POSITION
         C     R1,SPLAST .   OUTPUT AREA FULL ?
         BNLR  R9 .          YES, SIGNAL
         LA    R2,0(R1,R14) .  ITEM END POSITION + 1 BLANK
         C     R2,SPLEND .   FIT ON THIS LINE ?
         BL    SPMVC .       YES, CONTINUE
         BH    SPNEW .       NO , DO ON NEW LINE
         BCT   R2,SPINC .    YES, BUT NEED LINE INCREMENTATION AFTER MV
SPNEW    L     R1,SPLEND .   NO, SET FOR NEW LINE
         LA    R2,0(R1,R14) .  NEW ITEM END POSITION + 1 BLANK
         C     R2,SPLAST .   ROOM FOR IT ?
         BNH   SPINC .       YES
         L     R2,28(,R13) . NO, RESTORE AND
         BR    R9 .          RETURN - OUTPUT FULL
SPINC    L     R3,SPLEND .   GET CURRENT LINE END
         AH    R3,LSIZE .    NEW LINE END
         C     R3,SPLAST .   PAST END ?
         BNH   *+8 .         NO
         L     R3,SPLAST .   YES, USE END INSTEAD
         ST    R3,SPLEND .   STORE NEW LINE END
SPMVC    LA    R2,1(,R2) .   ALLOW FOR TRAILING BLANK
         ST    R2,SPCURR .   SET NEXT ITEM START POSITION
         LTR   R15,R15 .     MOVE OR TEST REQUEST ?
         BZ    SPRET .       TEST ONLY, RETURN
         LR    R3,R14 .      GET ITEM LENGTH
         SH    R3,H1 .       -1 FOR EXECUTE
         BM    SPRET .       SKIP IF TOO SHORT
         EX    R3,SPEXMVC .  MOVE THE DATA
SPRET    LM    R2,R3,28(R13) .  RESTORE, (R1) HAS 'TO' ADDRESS
         B     4(,R9) .      RETURN
SPEXMVC  MVC   0(0,R1),0(R15) .  MOVE INSTRUCTION, VIA EXECUTE
         SPACE 3
MOUNTDC  DC    C'Mount:',C' '  MOUNT ID FOR MOUNT AND ONLINE PGMS
SPNONE   DC    C'none' .     LITERAL FOR CALLER'S USE
.* SPDUMM REPLACED. ADDRES/COUNT IN EXW@SKIP/EXW#SKIP           GP08250
         SPACE 1                                                 77110
SPCLEAR  BLANKOUT CPU=&MODEL,WK=R15                              78187
         SPACE 1                                                GP02238
SPWORK   DSECT ,             MAP CALLER'S WORK AREA
SPCURR   DC    F'0' .        CURRENT DISPLAY POSITION
SPLEND   DC    F'0' .        END OF CURRENT PHYSICAL LINE
SPLAST   DC    F'0' .        END OF LOGICAL DISPLAY SPACE
SPSAVE   DC    F'0' .        USED BY CALLER
         MEND
