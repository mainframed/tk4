         MACRO
&NM      XLOUT &MSG,&LEN,&CASE=,&NOIN=EXCYES,&RER=EXCRERR       GP15243
.**********************************************************************
.*  THIS MACRO PROVIDES A PAGE DISPLAY, WAIT, AND READ FOR THE USE    *
.*  OF XLINE MACROS WITH (OR WITHOUT) THE COLOR/HIGHLIGHTING OPTIONS. *
.*  THE PROMPT IS DEFINED WITH AN SMI MACRO (MSG/LEN) ABOVE; WHEN THE *
.*  PROMPT IS OMITTED, A 'MORE ?' PROMPT IS USED.                     *
.*  BY DEFAULT INPUT TEXT IS CONVERTED TO UPPER CASE. USE CASE=       *
.*  LOW, LOWER, MIXED, OR ASIS TO PRESERVE CASE.                      *
.*  UP, UPP, AND UPPER CONVERT TO UPPER CASE.                         *
.*                                                                    *
.*  RETURN IS VECTORED:  +0 IF THE USER HIT ENTER ONLY                *
.*                       +4 FOR ANY INPUT (PFK,PAK,text)              *
***********************************************************************
         LCLC  &SMI,&L
&SMI     SETC  '&MSG'
         AIF   ('&SMI' NE '').HAVEMSG
&SMI     SETC  'EXMORE'
.HAVEMSG ANOP  ,
&NM      L     R14,EXWSMEND  GET CURRENT BUFFER END
&L       SETC  'L'''           LENGTH OF PROMPT LESS 1 FOR EX
         MACPARM R15,&LEN,NULL=&L&SMI
         BCTR  R15,0
         MACPARM R2,&SMI     LOAD PROMPT
         EX    R15,IOPAGMVC  MOVE PROMPT TO BUFFER
         LA    R3,1(R15,R14)   NEW BUFFER LENGTH
         S     R3,BUFAD      ADJUST
         L     R1,PAGECCWA   FULL-PAGE CCW
         BAL   R9,EXCRITE+4  WRITE NON-STANDARD SCREEN
          B    EXCWERR         WRITE OR READ ERROR
         XWAIT END=&NOIN                                        GP15243
         XREAD ERR=&RER,CASE=&CASE                              GP15243
         MEND
