         MACRO ,
&NM      FDSCAN &STR,&FDW,&OPTS,&MOVE=                           87312
.*
.*       PROVIDED FOR COMPATIBILITY WITH 3270 FULL-SCREEN FACILITY
.*
         LCLA  &C,&I,&J,&K,&N,&Z
         LCLB  &MORE,&POS,&KEY,&REQ,&ABBR                        87314
         AIF   ('&STR' NE 'END' AND '&STR' NE '*END').PROCESS
&NM      DC    X'FF00'       END OF LIST
         MEXIT ,
.PROCESS AIF   ('&MOVE' EQ '').NOMORE
&MORE    SETB  1
.NOMORE  ANOP  ,
&N       SETA  N'&SYSLIST
&J       SETA  &N-2                                              87360
&I       SETA  3
.OPTLOOP AIF   (&I GT &N).PROCEED
&POS     SETB  (&POS OR '&SYSLIST(&I)' EQ 'POS')
&KEY     SETB  (&KEY OR '&SYSLIST(&I)' EQ 'KEY')
&ABBR    SETB  (&ABBR OR '&SYSLIST(&I)' EQ 'LONG')
&REQ     SETB  (&REQ OR '&SYSLIST(&I)' EQ 'REQ')                 87314
&POS     SETB  (&POS OR '&SYSLIST(&I)' EQ 'POS2')  CHAINED POS   87360
&KEY     SETB  (&KEY OR '&SYSLIST(&I)' EQ 'POS2') CHAINED POS    87360
&I       SETA  &I+1
         AIF   ('&SYSLIST(&I-1)' NE 'POS2').OPTLOOP              87360
&J       SETA  &J+1          FINAGLE                             87360
         AGO   .OPTLOOP
.PROCEED AIF   (&J NE (&POS+&KEY+&ABBR+&REQ)).PARMA              87360
&C       SETA  128*&MORE+64*&POS+32*&KEY+2*&REQ+&ABBR            87314
.* &K       SETA  0             DEFAULT FOR POSITIONAL           87360
.*       AIF   (&POS).COMLEN  NOP'D FOR SCRMARK ERRMSG OPTION    87360
         AIF   ('&STR' EQ '').PARMA
&K       SETA  K'&STR-1
         AIF   ('&STR'(1,1) NE '''').COMLEN
&I       SETA  2
&J       SETA  &K-1
&K       SETA  &J
.LOOP    AIF   ('&STR'(&I,2) EQ '''''').SK2
         AIF   ('&STR'(&I,2) EQ '&&').SK2
&I       SETA  &I+1
         AGO   .INC
.SK2     ANOP  ,
&I       SETA  &I+2
&K       SETA  &K-1
.INC     AIF   (&I LE &J).LOOP
&K       SETA  &K-1
         AIF   (&K GE 0).COMLEN
.PARMA   MNOTE 8,'MISSING OR CONFLICTING PARAMETERS'
         MEXIT ,
.COMLEN  AIF   ('&STR'(1,1) EQ '''').TOAP
&NM      DC    AL1(&K,&C),SL2(&FDW),CL(&K+1)'&STR'
         AGO   .MORE
.TOAP    ANOP  ,
&NM      DC    AL1(&K,&C),SL2(&FDW),CL(&K+1)&STR
.MORE    AIF   ('&MOVE' EQ '').MEND
&K       SETA  K'&MOVE-1
         AIF   ('&MOVE'(1,1) EQ '''').MORQUO
         DC    AL1(&K),CL(&K+1)'&MOVE '
         AGO   .MEND
.MORQUO  ANOP  ,
&I       SETA  2
&J       SETA  &K-1
&K       SETA  &J
.MOOP    AIF   ('&MOVE'(&I,2) EQ '''''').MK2
         AIF   ('&MOVE'(&I,2) EQ '&&').MK2
&I       SETA  &I+1
         AGO   .MNC
.MK2     ANOP  ,
&I       SETA  &I+2
&K       SETA  &K-1
.MNC     AIF   (&I LE &J).MOOP
&K       SETA  &K-1
         AIF   (&K LT 0).MEND
         DC    AL1(&K),CL(&K+1)&MOVE
.MEND    MEND  ,
