         MACRO ,
&NM      XAUCH &P1,&P2,&P3,&P4,&P5,&P6,&TEST=YES,&PFX=NO, ADDED GP15099*
               &SYS=YES,&EXH=                                   GP15114
.**********************************************************************
.*                                                                    *
.*   XAUCH interfaces with EXHASIGN (a.k.a. EXHCCSIG) to perform      *
.*   access validity checking on resources (data set, volume, etc.)   *
.*                                                                    *
.*   The first parameter is the request type (DATASET, DASDVOL,...)   *
.*   The second parameter is the level of access requested (READ,     *
.*   UPDATE, CONTROL, or ALTER)                                       *
.*   The remaining parameters depend on the request type. For data    *
.*   sets, they're the address of the DSN, and if available, the      *
.*   volser.                                                          *
.*                                                                    *
.*   P1 may be a register, in which case it contains both values      *
.*   of the converted P1 and P2 (read ds is R0=0, update ds is x100)  *
.*                                                                    *
.*   PFX=YES causes the code to check whether a data set name high-   *
.*   index is the same as the user id. If it is, all access is        *
.*   permitted.                                                       *
.*                                                                    *
.*   TEST=YES causes the code to check whether the support module     *
.*   EXHASIGN is loaded. If not, the request is accepted.             *
.*                                                                    *
.*   SYS=YES bypasses all checks in systems mode (EXCPRIV,EXCPSYS)    *
.*                                                                    *
.*   EXH= specifies user's access authority for Exhibit and TSO type  *
.*       checking. Supported are (one or more):                       *
.*       KEY - user must have EXCPSYS privilege (systems group)       *
.*       UNL - user must have EXCAUTH operator authority              *
.*       CON - user must have EXCONCAN customer support authority.    *
.*   The EXH requirements are checked only if RACF/RAKF checking is   *
.*       not specified in the options data set.                       *
.*                                                                    *
.*   R14 and R15 are the return address and target addresses.         *
.*   R1 is the address of the parameter list with the VL option.      *
.*                                                                    *
.*   R15 on return is 0 for permitted access                          *
.*   R15 is address of BCON message for denied access, and            *
.*       R0 is a Reason code (e.g., RACHECK return code)              *
.*                                                                    *
.**********************************************************************
         LCLC  &C,&LBL
         LCLB  &B(8)                                            GP15114
         LCLA  &I,&J,&K,&N
&N       SETA  N'&SYSLIST
         AIF   (&N LT 3).MISSING
&LBL     SETC  'ZZ'.'&SYSNDX'
         AIF   ('&P1'(1,1) EQ '(').REGP1                        GP15112
&C SETC '&P1'(1,1)  D-DS M-DS MASK P-DASD T-TAPE S-STORAGE X-XMEM J-Job
.LOOPC   AIF   (&I GE 8).BADP1
&I       SETA  &I+1
         AIF   ('&C' EQ 'DMPTSXJV'(&I,1)).HAVEC  Add VSAM later?
         AGO   .LOOPC
.HAVEC   ANOP  ,
&C SETC '&P2'(1,1)  R-READ U-WRITE/UPDATE C-CONTROL A-ALTER
.LOOPA   AIF   (&J GE 4).BADP2
&J       SETA  &J+1
         AIF   ('&C' EQ 'RUCA'(&J,1)).HAVEA
         AIF   (&J EQ 2 AND '&C' EQ 'W').HAVEA   ACCEPT WRITE
         AGO   .LOOPA
.HAVEA   ANOP  ,
&I       SETA  &I-1
&J       SETA  &J-1
&NM      MACPARM R0,&J*256+&I*4
         AGO   .COMP1                                           GP15112
.REGP1   ANOP  ,                                                GP15112
&NM      MACPARM R0,&P1      PRESET FOR ACCESS/TYPE             GP15112
.COMP1   AIF   ('&EXH' EQ '' OR '&EXH' EQ '()').COMP2           GP15114
.*
.* CONVERT THE EXH= PARAMETER TO A BIT STRING
.*
&N       SETA  N'&EXH                                           GP15114
&I       SETA  0                                                GP15114
&K       SETA  0                                                GP15114
.EXLOOP  ANOP  ,                                                GP15114
&I       SETA  &I+1                                             GP15114
&C       SETC  '&EXH(&I)'                                       GP15114
         AIF   ('&C' EQ '').EXLOOP                              GP15114
&J       SETA  1                                                GP15114
.EXLOOK  AIF   ('&C' EQ 'UNLKEYCANCON'(&J,3)).EXGOOD            GP15114
&J       SETA  &J+3                                             GP15114
         AIF   (&J LT 12).EXLOOK                                GP15114
         MNOTE 8,'XAUCH: INVALID EXH PARM &C'                   GP15114
         AGO   .COMP2                                           GP15114
.EXGOOD  ANOP  ,                                                GP15114
&K       SETA  (&J+2)/3                                         GP15114
&B(&K)   SETB  1                                                GP15114
         AIF   (&I LT &N).EXLOOP                                GP15114
&K       SETA  128*&B(8)+64*&B(7)+32*&B(6)+16*&B(5)+8*&B(4)+4*&B(3)+2*&*
               B(2)+&B(1)                                       GP15114
         MACPARM R0,4,=AL1(&K),OP=ICM,OPR=ICM,MODE=3            GP15114
.COMP2   AIF   ('&P1'(1,1) NE 'D' AND '&PFX' NE 'YES').NOUID    GP15112
         MACPARM R15,(R15),OP=SLR,OPR=SLR,MODE=EVEN  PRESET OK  GP15112
         AIF   ('&SYS' NE 'YES').DOCHECK                        GP15112
         TM    EXCPRIV,EXCPSYS    SYSTEMS MODE?                 GP15112
         BO    &LBL.X               YES; PERMIT ACCESS          GP15112
.DOCHECK ICM   R14,1,EXWHINDL  IS THERE AN INDEX LENGTH ?       GP15112
         BZ    &LBL.U            NO; DO OTHER CHECKING          GP15112
         EX    R14,&LBL.C    DOES INDEX MATCH ?                 GP15112
         BE    &LBL.X          YES; PERMIT                      GP15112
&LBL.U   DS    0H                                               GP15112
&LBL.C   CLC   &P3.(*-*),EXWHINDX    INDEX=UID?                 GP15145
.NOUID   AIF   ('&TEST' NE 'YES' AND '&TEST' NE '').NOTEST
         MACPARM R15,15,EXW@RAC,MODE=3,OP=ICM,OPR=ICM
         BZ    &LBL.X               NO CHECKING; R15=0
         AGO   .SUBCALL
.NOTEST  MACPARM R15,EXW@RAC,OP=L   RACHECK CODE ADDRESS        GP15112
.SUBCALL MACPARM R1,&P3,NULL=0
         MACPARM R1,EXWPARML,OP=ST
         AIF   (&N LE 3).VLBIT
         MACPARM R1,&P4,NULL=0
         MACPARM R1,EXWPARML+4,OP=ST
         AIF   (&N LE 4).VLBIT
         MACPARM R1,&P5,NULL=0
         MACPARM R1,EXWPARML+8,OP=ST
         AIF   (&N LE 5).VLBIT
         MACPARM R1,&P6,NULL=0
         MACPARM R1,EXWPARML+12,OP=ST
.VLBIT   OI    EXWPARML+(&N-3)*4,X'80' SET VL BIT
         SUBCALL (R15),MF=(E,EXWRACWK)
&LBL.X   DS    0H
         MEXIT ,
.BADP1   MNOTE 8,'XAUCH: P1 must be DATASET, MASK, PACK, TAPE, STOR, XM'
               EM, or JOB)'
         MEXIT ,
.BADP2   MNOTE 8,'XAUCH: P2 must be READ, UPDATE, CONTROL, or ALTER'
         MEXIT ,
.MISSING MNOTE 8,'XAUCH: Incorrect parameter list'
         MEND  ,
