         MACRO ,
&NM      SERVICE &CODE,&ADDR,&REG2,&ERR=,&CC0=,&CC4=,&CC8=,&LEN=,      *
               &MAP=                                             87036
         GBLA  &SVC@SVC                                          83100
         GBLB  &SRVCM@P                                          85021
         GBLB  &SRV#NUT      USE TSA BASSM/SVC IF TRUE          GP03142
         GBLC  &MACPLAB,&SRVCM@R                                 81148
         LCLC  &LERR                                             81148
.*--------------------------------------------------------------------*
.*  SERVICE INVOKES THE @SERVICE ROUTINE IN ANY OF THREE WAYS:        *
.*  1) OLD MODE - IF SVC GLOBAL IS NON-ZERO, VIA SVC IGC00SVC         *
.*  2) OLD MODE - IF SVC GLOBAL IS ZERO, VIA BASSM                    *
.*  3) NEW MODE - EXECUTE INSTRUCTION (EITHER BASSM OR SVC); REQUIRES *
.*     NEW SERVINIT MACRO AND SERVDEFS IN SAVE AREA                   *
.*--------------------------------------------------------------------*
&NM      MACPARM R2,&REG2,NULL=SKIP                              85070
         MACPARM R1,&ADDR,NULL=SKIP
.NOR1    AIF   ('&CODE' EQ '').NOR0
         AIF   ('&CODE'(1,1) EQ '(').REG0
         MACPARM R0,VEN&CODE
         AIF   ('&LEN' EQ '').NOR0                              GP99026
         MACPARM R0,8,=AL1(&LEN),OP=ICM,MODE=THREE              GP99026
         AGO   .NOR0
.REG0    MACPARM R0,&CODE
.NOR0    AIF   (NOT &SRV#NUT).NOTEXEC                           GP03142
         AIF   ('&CODE' EQ 'LPALD').NOTSVC   DOESN'T WORK IN SVC
         MACPARM R15,@SERVICE,OP=L  GET MODULE ADDRESS          GP03142
         MACPARM 0,@SERVEXC,OP=EX  EXECUTE BASSM OR SVC         GP03142
         AGO   .COMSVC                                          GP03142
.NOTEXEC AIF   (&SVC@SVC EQ 0).NOTSVC                            83100
&MACPLAB SVC   &SVC@SVC      CALL THE SERVICE ROUTINE
&MACPLAB SETC  ''                                                83100
         AGO   .COMSVC                                           83100
.NOTSVC  MACPARM R15,@SERVICE,OP=L  GET MODULE ADDRESS
&MACPLAB BASSM R14,R15       CALL THE @SERVICE ROUTINE
.COMSVC  AIF   ('&CC0' EQ '' AND '&CC4' EQ '' AND '&CC8' EQ '').NOCC
         AIF   ('&ERR' EQ '' OR '&ERR' EQ 'NO').NODUPE           81148
         MNOTE 4,'CC= AND ERR= ARE MUTUALLY EXCLUSIVE'           81148
.NODUPE  ANOP  ,                                                 81148
&MACPLAB SETC  ''                                                81148
         CH    R15,=H'4'     TEST RETURN                         81148
         MACPARM &CC0,OP=BL,OPR=BLR,MODE=ONE,NULL=SKIP          GP02241
         MACPARM &CC4,OP=BE,OPR=BER,MODE=ONE,NULL=SKIP          GP02241
         MACPARM &CC8,OP=BH,OPR=BHR,MODE=ONE,NULL=SKIP          GP02241
         AGO   .MEND                                             81148
.NOCC    AIF   ('&ERR' EQ 'NO').MEND                             81148
&LERR    SETC  '&ERR'                                            81148
         AIF   ('&LERR' NE '').DOERR                             81148
&LERR    SETC  '&SRVCM@R'                                        81148
         AIF   ('&LERR' EQ '').MEND                              81148
.DOERR   BXH   R15,R15,&LERR  GO TO SET ERROR MESSAGE            81148
.MEND    AIF   (&SRVCM@P OR '&MAP' EQ 'NO').MMEND                87036
&SRVCM@P SETB  1                                                 85021
         COPY  SERVFLAG                                          85021
.MMEND   MEND  ,
