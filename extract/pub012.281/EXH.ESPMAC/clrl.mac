         MACRO ,
&NM      CLRL  &A,&LN,&WORK1=14,&WORK2=0,&FILL=,&OPLEN=LA        84254
.*--------------------------------------------------------------------*
.*  CLEAR AN AREA OF ANY LENGTH USING MVCL (WAS MVC ON 360)           *
.*--------------------------------------------------------------------*
         LCLA  &K
         LCLC  &W1E,&W1O,&W2E,&W2O
         LCLC  &L                                               GP03093
&L       SETC  'L'''                                            GP03093
.*
         AIF   (T'&A EQ 'O').BADA
.*USE L*  AIF   (T'&LN EQ 'O').BADLN
         AIF   (N'&A NE 1).BADA
.*USE L*  AIF   (N'&LN NE 1).BADLN
.*
         AIF   (T'&WORK1 EQ 'O').BADW1
         AIF   (T'&WORK2 EQ 'O').BADW2
         AIF   (N'&WORK1 NE 1).BADW1
         AIF   (N'&WORK2 NE 1).BADW2
.*
&W1E     SETC  '&WORK1(1)'
&W1O     SETC  '&W1E'.'+1'
&W2E     SETC  '&WORK2(1)'
&W2O     SETC  '&W2E'.'+1'
&NM      MACPARM &W1E,&A
         MACPARM &W1O,&LN,NULL=&L&A,OP=&OPLEN                   GP03093
         MACPARM &W2O,0      CLEAR SOURCE LENGTH
         AIF   (T'&FILL EQ 'O').NOFILL
         AIF   ('&FILL' EQ '0').NOFILL
&K       SETA  K'&FILL
         AIF   ('&FILL'(&K,1) EQ '''' OR '&FILL'(&K,1) EQ ')').FILLICM
         ICM   &W2O,8,=AL1(&FILL)
         AGO   .NOFILL
.FILLICM ICM   &W2O,8,=&FILL
.NOFILL  MVCL  &W1E,&W2E     CLEAR THE AREA
         MEXIT ,
.BADLN   MNOTE 4,'INVALID AREA LENGTH &LN'
         MEXIT ,
.BADA    MNOTE 4,'INVALID AREA ADDRESS &A'
         MEXIT ,
.BADW1   MNOTE 4,'INVALID WORK1 &WORK1'
         MEXIT ,
.BADW2   MNOTE 4,'INVALID WORK2 &WORK2'
         MEXIT ,
         MEND  ,
