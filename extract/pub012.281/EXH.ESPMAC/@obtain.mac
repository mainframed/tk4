         MACRO
&NM      @OBTAIN &PARM,&OPT=                                     81152
         GBLB  &WX@OBT
         GBLC  &MACPLAB
.**********************************************************************
.*                                                                   **
.*   REPLACEMENT FOR IBM OBTAIN MACRO/SVC. SEE MY @OBTAINS ROUTINE   **
.*     FOR DETAILS.                                                  **
.*                                                                   **
.**********************************************************************
         LCLC  &NDX                                              82005
&NDX     SETC  'OBT'.'&SYSNDX'                                   82005
&MACPLAB SETC  '&NM'
         AIF   ('&OPT' EQ '').OPTDEF                             81152
         AIF   ('&OPT' EQ 'CLOSE').CLOSE                         81342
         AIF   ('&OPT' EQ 'LOADED' OR '&OPT' EQ 'LOAD').OPTEXT   81342
         AIF   ('&OPT' EQ 'SIZE' OR '&OPT' EQ 'RESERVE').CLOSE   81342
         MNOTE 8,'INVALID OPT=&OPT '                             81152
         AGO   .OPTDEF       BUT KEEP EXPANDING                  81342
.OPTEXT  ANOP  ,                                                 81342
&WX@OBT  SETB  1             MODULE WAS LOADED BY USER           81342
.OPTDEF  MACPARM R1,&PARM                                        81152
         MACPARM R14,&NDX.S+2  SET EXIT ADDRESS     GP08296
         ICM   R15,15,@OBTAIN   LOAD ADDRESS                    GP99033
         BNZR  R15 .         CALL OBTAIN SUBROUTINE
         AIF   (&WX@OBT).SECOND
         B     &NDX.S        ELSE DO SVC                         82005
@OBTAIN  DC    V(@OBTAINS)   ADDRESS OF OBTAIN ROUTINE           81152
         WXTRN @OBTAINS
&WX@OBT  SETB  1
.SECOND  ANOP  ,
&NDX.S   SVC   27            INVOKE OBTAIN SVC                   82005
         AGO   .MEND                                             81152
.CLOSE   MACPARM R15,15,@OBTAIN,MODE=3,OP=ICM  EVER LOADED?     GP08296
         BZ    &NDX.X            NO                              82005
         AIF   ('&OPT' EQ 'CLOSE').CLOSCAL                       81342
         MACPARM R0,&PARM,OP=L   LOAD RESERVE SIZE               81342
         L     R15,28(,R15)  GET @OBSERVE ADDRESS                81342
         ST    R0,0(,R15)    SET STORAGE RESERVE SIZE            81342
         AGO   .CLOSOUT                                          81342
.CLOSCAL L     R15,24(,R15)  LOAD @OBCLOSE ADDRESS               81342
         BALSR R14,R15       CLOSE IT                            81152
.CLOSOUT ANOP  ,                                                 81342
&NDX.X   DS    0H                                                82005
.MEND    MEND  ,
