         MACRO ,                                       ADDED ON GP05140
&NM      XPARM  &VAR,&EL,&STOP=,&ERR=,&OPT=   SCAN FOR OPERAND  GP08116
         LCLC  &L,&SUB
         LCLB  &B0,&B1,&B2,&B3,&B4,&B5,&B6,&B7
         LCLA  &I,&J,&K,&N
&L       SETC  'L'''
&N       SETA  N'&OPT        NUMBER OF OPTIONS
.LOOP    AIF   (&I GE &N).DONEOP
&I       SETA  &I+1
&SUB     SETC  '&OPT(&I)'
         AIF   ('&SUB' EQ '').LOOP
&J       SETA  &B0+&B1+&B2+&B3+&B4+&B5+&B6+&B7
&B0      SETB  (&B0 OR ('&SUB' EQ 'DONE'))            80
&B1      SETB  (&B1 OR ('&SUB' EQ 'NULL'))            40
&B2      SETB  (&B2 OR ('&SUB' EQ 'MASK'))            20
&B3      SETB  (&B3 OR ('&SUB' EQ 'QUOTE'))           10
&B4      SETB  (&B4 OR ('&SUB' EQ 'UNQUO'))           08
&B5      SETB  (&B5 OR ('&SUB' EQ 'INT'))             04
&B6      SETB  (&B6 OR ('&SUB' EQ 'HEX'))             02
&B7      SETB  (&B7 OR ('&SUB' EQ 'FLOAT'))           01
&K       SETA  &B0+&B1+&B2+&B3+&B4+&B5+&B6+&B7
         AIF   (&J NE &K).LOOP
 MNOTE 8,'XPARM: INVALID OPT=&SUB '
         AGO   .LOOP
.DONEOP  ANOP  ,
.*
&SUB     SETC  'EXCPARM0'
&NM      MACPARM R3,&VAR
         MACPARM R4,&EL,NULL=&L&VAR                             GP08116
         AIF   ('&STOP' EQ '').CALL
&SUB     SETC  'EXCPARM'
         AIF   ('&STOP'(1,1) EQ '''').STR
         MACPARM R0,&STOP,OP=L,OPR=LR                           GP14268
         AGO   .CALL
.STR     MACPARM R0,=CL4&STOP,OP=L
.CALL    MACPARM DB+3,B'&B0&B1&B2&B3&B4&B5&B6&B7',OP=MVI
         MACPARM R14,&SUB,OP=BAL
         MACPARM &ERR,OP=B,OPR=??,NULL=SKIP,MODE=ONE
         MEND  ,
