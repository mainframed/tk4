         MACRO ,
&NM      #TPUT &ADDR,&LEN,&TYPE=,&NOTSO=WTO,&MF=
         GBLC  &MACPLAB
.*--------------------------------------------------------------------*
.*   #TPUT IS A SIMPLE TPUT REPLACEMENT. IT INVOKES SUBTPUT TO ISSUE  *
.*     EITHER A PUTLINE (FOR A TSO CALLER) OR A WTO (BATCH).          *
.*                                                                    *
.*   TYPE=  CON, BCON, HCON, VCON, DCS - LEN NOT REQUIRED, BECAUSE    *
.*     ADDR POINTS TO A SELF-DEFINING OBJECT (L/TEXT OR TEXT/X'0')    *
.*                                                                    *
.*   NOTSO= ACTION WHEN NOT UNDER TSO: WTO, WTL, SKIP {PRT}           *
.*--------------------------------------------------------------------*
         LCLA  &I,&NOTS
         LCLB  &SDO
         LCLC  &L
         LCLC  &R0
         LCLC  &C
&MACPLAB SETC  '&NM'
&R0      SETC  '&LEN'
&L       SETC  'L'''
         AIF   (T'&ADDR NE 'O').HAVEAD
         MNOTE 8,'#TPUT: ADDRESS OPERAND REQUIRED'
         MEXIT ,
.HAVEAD  ANOP  ,
&SDO     SETB  ('&TYPE' EQ 'CON')
&SDO     SETB  (('&TYPE' EQ 'DCS') OR &SDO)
&SDO     SETB  (('&TYPE' EQ 'BCON') OR &SDO)
&SDO     SETB  (('&TYPE' EQ 'HCON') OR &SDO)
&SDO     SETB  (('&TYPE' EQ 'VCON') OR &SDO)
         AIF   (NOT &SDO).NEEDLEN
         AIF   (T'&LEN EQ 'O').SKIPLN
       MNOTE 4,'#TPUT:  LENGTH &LEN AND TYPE &TYPE MUTUALLY EXCLUSIVE'
&SDO     SETB  0
.SKIPLN  AIF   ('&ADDR'(1,1) NE '''' AND '&ADDR'(1,1) NE '(').ZEROR0
         MNOTE 4,'#TPUT: TYPE &TYPE INVALID WITH LITERAL TEXT'
&SDO     SETB  0
.ZEROR0  ANOP  ,
&R0      SETC  '0'
.NEEDLEN AIF   (T'&NOTSO EQ 'O').SKIPACT
&C       SETC  '&NOTSO'.'    '
&C       SETC  '&C'(1,4)
.LOOPACT AIF   ('&C' EQ 'WTO WTL PRT SKIP'(&I*4+1,4)).HAVEACT
&I       SETA  &I+1
         AIF   (&I LT 4).LOOPACT
         MNOTE 4,'#TPUT: UNSUPPORTED NOTSO &NOTSO'
         AGO   .SKIPACT
.HAVEACT ANOP  ,
&NOTS    SETA  &I
.SKIPACT AIF   ('&ADDR'(1,1) EQ '=').LIT
         AIF   ('&ADDR'(1,1) NE '''').EXPLIC
&I       SETA  K'&ADDR-2
         MACPARM R0,&I
         MACPARM R1,=CL(&I)&ADDR
         AGO   .SUBCALL
.*
.LIT     AIF   ('&ADDR'(2,2) EQ 'C''').CLIT
         MNOTE 8,'#TPUT: ADDRESS OPERAND NOT SUPPORTED'
.*
.CLIT    ANOP  ,
&I       SETA  K'&ADDR-4
&R0      SETC   '&I'
.*
.EXPLIC  AIF   ('&R0' NE '').HAVELEN
&R0      SETC  '&L'.'&ADDR'
         AIF   ('&ADDR'(1,1) NE '(').HAVELEN
         MNOTE 8,'#TPUT: LENGTH OPERAND REQUIRED'
.*
.HAVELEN MACPARM R0,&R0      LOAD LENGTH REGISTER
         MACPARM R1,&ADDR    LOAD ADDRESS REGISTER
.SUBCALL AIF   (&NOTS EQ 0).TPUT
         MACPARM R0,8,=AL1(&NOTS),OP=ICM,MODE=THREE   SET ACTION
.TPUT    AIF   ('&MF' EQ 'B').MEND                              GP12313
         SUBCALL /SUBTPUT                                       GP12162
.MEND    MEND  ,
