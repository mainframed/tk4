         MACRO
&NM      MSORT &ADDR=,&SIZE=,&OFF=,&COMPL=,&NUM=,&RET=R14,&EXIT=,      *
               &ORDER=A,&LAST=
.*--------------------------------------------------------------------*
.*                                                                    *
.*     ADAPTED FROM SORT APPEARING IN 'FORTRAN PROGRAMMING' BY        *
.*     FREDRIC STUART, WILEY '69. ORIGINATED BY D.L. SHELL, MODIFIED  *
.*     BY MARLENE METZNER (PRATT&WHITNEY).                            *
.*                                                                    *
.*     INPUT PARAMETERS :                                             *
.*     ADDR  ADDRESS OF LIST TO BE SORTED; REL-EXP OR (REG 2-12)      *
.*     SIZE  SIZE OF LIST ENTRY; SELF-DEFINING TERM OR (REG 2-12)     *
.*     OFF   OFFSET OF COMPARE STRING FROM START OF ENTRY;  -''-      *
.*     COMPL LENGTH OF COMPARE STRING;                      -''-      *
.*     NUM   NUMBER OF ENTRIES IN LIST     ×  EXCLUSIVE     -''-      *
.*     LAST  or ADDRESS OF LAST ENTRY.     ×  PARAMETERS    -''-      *
.*                                                               92052*
.*     WHEN THE EXIT= PARAMETER IS SPECIFIED, OFF AND COMPL ARE NOT   *
.*     USED.  THE CODE INVOKES THE EXIT USING SORT'S R0-R9; R4 POINTS *
.*     TO CURRENT ENTRY, R8 TO NEXT ENTRY, USER RETURNS TO R14 WITH   *
.*     CC 2 TO SWAP ENTRIES, CC 0 OR 1 TO RETAIN.                92052*
.*                                                               92052*
.*     THE AREA POINTED TO BY REGISTER 13 IS USED TO SAVE CALLER'S    *
.*     REGISTERS AND AS A WORK AREA FOR RE-ENTRANCY.                  *
.*                                                                    *
.*     RET   RETURN REGISTER                                          *
.*           BR TO RET - ERROR IN PARAMETERS OR VALUES                *
.*           B TO RET+4 - COMPLETED                                   *
.*                                                                    *
.*     ADDED ORDER=A / ORDER=D FOR SORT DIRECTION               GP04020
.*                                                                    *
.*--------------------------------------------------------------------*
         GBLC  &MACPLAB
         LCLA  &I
         LCLC  &ORDBC
&MACPLAB SETC  ''
&I       SETA  &SYSNDX
&ORDBC   SETC  'ADDR'                                           GP04020
         AIF   ('&ADDR' EQ '').MISSOP                           GP04020
&ORDBC   SETC  'SIZE'                                           GP04020
         AIF   ('&SIZE' EQ '').MISSOP                           GP04020
         AIF   ('&EXIT' NE '' AND '&EXIT' NE '0').NOOFF NO NEED GP04234
&ORDBC   SETC  'OFF'                                            GP04020
         AIF   ('&OFF' EQ '').MISSOP                            GP04020
&ORDBC   SETC  'COMPL'                                          GP04020
         AIF   ('&COMPL' EQ '').MISSOP                          GP04020
.NOOFF   ANOP  ,                                                GP04234
&ORDBC   SETC  'NUM'                                            GP04020
         AIF   ('&NUM' EQ '' AND '&LAST' EQ '').MISSOP          GP15129
&ORDBC   SETC  'H'           ASCENDING (BNH)
         AIF   ('&ORDER' EQ 'A').ORDNUNG
&ORDBC   SETC  'L'           DESCENDING (BNL)
         AIF   ('&ORDER' EQ 'D').ORDNUNG
         MNOTE 8,'MSORT: ORDER MUST BE A (ASCENDING) OR D (DESCENDING)'
&ORDBC   SETC  'H'           ASCENDING (BNH)
.ORDNUNG AIF   (T'&NUM NE 'O' AND T'&LAST NE 'O').MANY2         GP15129
.EXPAND  ANOP  ,
&NM      STM   R14,R9,20(R13)   SAVE REGISTERS                   92052
         LA    R0,1                    MAKE CONSTANT
         AIF   ('&EXIT' EQ '' OR '&EXIT' EQ '0').NOEXIT          92052
         MACPARM R15,&EXIT,OP=LA                                 92052
         LTR   R15,R15       VALID EXIT ?                        92052
         BZR   R14           NO; RETURN ERROR                    92052
         ST    R15,12(,R13)  SAVE EXIT ADDRESS                   92052
         AGO   .NOCLC                                            92052
.NOEXIT  MVC   12(6,R13),SRT&I.Z       MOVE PATTERN CLC
         MACPARM R9,&OFF,OP=LA         GET COMPARE OFFSET
         STC   R9,15(,R13)             STASH INTO CLC
         STC   R9,17(,R13)              FOR COMPARE
         MACPARM R9,&COMPL,OP=LA       GET COMPARE LENGTH
         SR    R9,R0                   LENGTH FOR CLC
         BM    SRT&I.Y                 TAKE ERROR EXIT
         STC   R9,13(,R13)             SET INTO CLC LENGTH FIELD
.NOCLC   MACPARM R2,&SIZE,OP=LA        GET SIZE OF ENTRY
         LR    R3,R2                   COPY INTO EXECUTE REGISTER
         SR    R3,R0                   DECREMENT FOR EXECUTE
         BM    SRT&I.Y                 TAKE ERROR EXIT
         MACPARM R1,&ADDR,OP=LA        ADDRESS OF FIRST ELEMENT
         AIF   (T'&LAST EQ 'O').DONUM                           GP15129
         MACPARM R9,&LAST    ADDRESS OF LAST ELEMENT            GP15129
         SR    R9,R1         SIZE OF TABLE                      GP15129
         SLR   R8,R8                                            GP15129
         DR    R8,R2         NUMBER OF ENTRIES                  GP15129
         AGO   .TRENUM                                          GP15129
.DONUM   MACPARM R9,&NUM,OP=L          GET NUMBER OF ENTRIES    GP09245
.TRENUM  CR    R9,R0                   CHECK AT LEAST TWO ENTRIES
         BNH   SRT&I.B                 TAKE GOOD EXIT IF NONE
         MR    R8,R2                   CONVERT TO TABLE LENGTH
         LR    R7,R9                   COPY TO SUBLIST
SRT&I.A  SRL   R7,1                    HALVE SUBLIST SIZE
         XR    R6,R6                     CLEAR FOR DIVIDE
         DR    R6,R2                   CONVERT TO LIST ENTRIES
         MR    R6,R2                   BACK TO INT(M/S)
         LTR   R7,R7                   ANY LEFT TO BE DONE ?
         BP    SRT&I.C                 YES; PROCEED
         AIF   (T'&RET EQ 'O').INEX                             GP15129
SRT&I.B  LM    R14,R9,20(R13)    RESTORE USER'S REGISTERS        92052
         B     4(,&RET)
         AGO   .ROUT                                            GP15129
.INEX    ANOP  ,                                                GP15129
SRT&I.B  B     SRT&I.Y       ERROR EXIT                         GP15129
.ROUT    ANOP  ,                                                GP15129
.*
SRT&I.C  LR    R6,R9                   GET ENTRY SIZE
         SR    R6,R7                   GET SIZE OF OTHER HALF
         XR    R5,R5                   CLEAR COUNTER
SRT&I.D  LA    R4,0(R5,R1)             GET ADDRESS OF NEXT ELEMENT
SRT&I.E  LA    R8,0(R4,R7)             GET ADDRESS OF OTHER ELEMENT
         AIF   ('&EXIT' EQ '' OR '&EXIT' EQ '0').EXCOMP          92052
         L     R15,12(,R13)  LOAD EXIT ADDRESS                   92052
         BASR  R14,R15       INVOKE THE EXIT                     92052
         AGO   .COMPAR       BRANCH AS SET BY USER               92052
.EXCOMP  EX    0,12(,R13)              COMPARE THE TWO
.COMPAR  BN&ORDBC SRT&I.F              IN SEQUENCE - SKIP FLIP
         EX    R3,SRT&I.Z+6            FLIP THE
         EX    R3,SRT&I.Z+12             TWO
         EX    R3,SRT&I.Z+6                LIST ENTRIES
         SR    R4,R7                   GET NEXT ELEMENT
         CR    R4,R1                   BACK TO START OF TABLE ?
         BNL   SRT&I.E                 NO; TRY AGAIN
SRT&I.F  AR    R5,R2                   NEXT COMPARE ELEMENT ADDRESS
         CR    R5,R6                   DONE THIS PART OF LIST ?
         BNL   SRT&I.A                 YES; GET ANOTHER SUBLIST
         B     SRT&I.D                 ELSE PROCEED
.*   PATTERNS
SRT&I.Z  CLC   0(0,R4),0(R8)           PATTERN COMPARE
         XC    0(0,R4),0(R8)           FLIP 1/3
         XC    0(0,R8),0(R4)           FLIP  2
.*
SRT&I.Y  LM    R14,R9,20(R13)  RESTORE REGISTERS                 92052
         AIF   (T'&RET EQ 'O').MEXIT   DO IN-LINE               GP15129
         BR    &RET                    TAKE ERROR EXIT
.MEXIT   MEXIT ,                                                GP04020
.MISSOP  MNOTE 8,'MSORT: MISSING OPERAND &ORDBC'                GP04020
         MEXIT ,                                                GP04020
.MANY2   MNOTE 8,'MSORT: NUM= AND LAST= ARE MUTUALLY EXCLUSIVE' GP15129
         MEXIT ,                                                GP15129
&NM      MACPARM R14,OP=BR,MODE=ONE  EXPAND LABEL & ERROR RETURN
.MEND    MEND  ,
