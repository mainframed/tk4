         MACRO ,
&NM      STORAGE &FUN,&LENGTH=,&ADDR=,&SP=,&BNDRY=,&LOC=,&COND=,       *
               &CALLRKY=,&RELEASE=
.*
.*    BACKWARD COMPATIBILITY FOR MVS 3.8 UNDER HERCULES         GP04234
.*    ALLOW MOST OPERANDS USING GETMAIN/FREEMAIN
.*
         LCLA  &K,&RK
         LCLC  &SB                                              GP08258
         AIF   ('&SP' EQ '0').NOPOOL  TREAT AS SP=              GP08258
&SB      SETC  '&SP'                                            GP08258
.NOPOOL  ANOP  ,                                                GP08258
&K       SETA  K'&SB
&RK      SETA  K'&LENGTH
         AIF   ('&FUN' EQ 'OBTAIN').GET
         AIF   ('&FUN' EQ 'RELEASE').FREE
         MNOTE 8,'STORAGE: FUNCTION &FUN INVALID'
         MEXIT ,
.GET     AIF   ('&COND' EQ 'YES').GETC
         AIF   ('&BNDRY' NE '' OR '&SB' NE '').GETU
&NM      GETMAIN R,A=&ADDR,LV=&LENGTH
         MEXIT ,
.*
.GETC    AIF   ('&SB' EQ '' OR &K LT 3).GETCB
         AIF   ('&SB'(1,1) EQ '(' AND '&SB'(2,1) NE '(' AND            *
               '&SB'(&K,1) EQ ')' AND '&SB'(&K-1,1) NE ')').GETCR
.GETCB   ANOP  ,
&NM      GETMAIN RC,A=&ADDR,LV=&LENGTH,BNDRY=&BNDRY,SP=&SB
         MEXIT ,
.GETCR   ANOP  ,
&NM      MACPARM R0,&LENGTH
         MACPARM R15,&SB     GET SUBPOOL                        GP08089
         GETMAIN RC,A=&ADDR,LV=(0),SP=(15),BNDRY=&BNDRY         GP08089
         MEXIT ,
.*
.GETU    AIF   ('&SB' EQ '' OR &K LT 3).GETUB
         AIF   ('&SB'(1,1) EQ '(' AND '&SB'(2,1) NE '(' AND            *
               '&SB'(&K,1) EQ ')' AND '&SB'(&K-1,1) NE ')').GETUR
.GETUB   ANOP  ,
&NM      GETMAIN RU,A=&ADDR,LV=&LENGTH,BNDRY=&BNDRY,SP=&SB
         MEXIT ,
.GETUR   ANOP  ,
&NM      MACPARM R0,&LENGTH
         MACPARM R15,&SB     GET SUBPOOL                        GP08089
         GETMAIN RU,A=&ADDR,LV=(0),SP=(15),BNDRY=&BNDRY         GP08089
         MEXIT ,
.*
.FREE   AIF   ('&SB' NE '' AND '&LENGTH' EQ '' AND '&ADDR' EQ '').FPOOL
         AIF   ('&SB' NE '').FREESP
&NM      FREEMAIN R,A=&ADDR,LV=&LENGTH
         MEXIT ,
.FREESP  AIF   ('&LENGTH' EQ '' OR &RK LT 3).FREESR
         AIF   ('&LENGTH'(1,1) EQ '(' AND '&LENGTH'(2,1) NE '(' AND    *
               '&LENGTH'(&RK,1) EQ ')' AND                             *
               '&LENGTH'(&RK-1,1) NE ')').FRUR
.FREESR  AIF   ('&SB' EQ '' OR &K LT 3).FREEUB
         AIF   ('&SB'(1,1) EQ '(' AND '&SB'(2,1) NE '(' AND            *
               '&SB'(&K,1) EQ ')' AND '&SB'(&K-1,1) NE ')').FRUR
.FREEUB  ANOP  ,
&NM      FREEMAIN R,A=&ADDR,LV=&LENGTH,SP=&SB
         MEXIT ,
.FRUR  ANOP  ,
&NM      MACPARM R0,&LENGTH
         MACPARM R0,8(R13),OP=ST      SAVE LENGTH
         MACPARM R0,&SB
         MACPARM R0,8(R13),OP=STC    COMBINE WITH SUBPOOL
         MACPARM R0,8(R13),OP=L      AND RELOAD                 GP08251
         FREEMAIN R,A=&ADDR,LV=(0)  LV=&LENGTH,SP=&SB
         MEXIT ,
.FPOOL   ANOP  ,
&NM      FREEMAIN R,SP=&SB   FREE ENTIRE SUBPOOL
.MEND    MEND  ,
