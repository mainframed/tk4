         MACRO
&NM      BLANKOUT &RET=R14,&ADD=R1,&LEN=R3,&CPU=,&WK=R15         78187
         SPACE 1
*        THIS ROUTINE BLANKS OUT AN AREA OF ANY LENGTH
         AIF   ('&CPU' NE '360').MVCL
*
&NM      SH    &LEN,H1       AT LEAST ONE BYTE ?                 78187
         BMR   &RET          NO                                  78187
         MVI   0(&ADD),C' '  SET FIRST BYTE BLANK
         BZR   &RET .        DONE IF ONLY ONE                    78187
&NM.A    BCTR  &LEN,0        LESS ONE FOR EXECUTE                78187
         LA    &WK,255       SET LENGTH-1                        78187
         NR    &WK,&LEN      MASK OUT NUMBER THIS TIME           78187
         EX    &WK,&NM.B     MOVE SPECIFIED NUMBER               78187
         LA    &ADD,1(&WK,&ADD)  BUMP POINTER                    78187
         XR    &LEN,&WK      ACCOUNT FOR NUMBER DONE             78187
         BL    &NM.A .       DO NEXT SEGMENT                     78187
         BR    &RET          RETURN DONE                         78187
&NM.B    MVC   1(0,&ADD),0(&ADD)                                 78187
         MEXIT
.MVCL    SPACE 1                                                 78187
&NM      LTR   &LEN,&LEN .   AT LEAST ONE BYTE ?                 79182
         BNPR  &RET .        NO, RETURN                          79182
         STM   R15,R1,12(R13) .  SAVE REGS                       78187
         AIF   ('&LEN' NE '0' AND '&LEN' NE 'R0').M01
         MACPARM R1,(&LEN)
         MACPARM R0,(&ADD)
         AGO   .MCOM
.M01     MACPARM R0,(&ADD)
         MACPARM R1,(&LEN)
.MCOM    SR    R15,R15
         ICM   R15,8,BLANKS .  BLANK IS PADDING BYTE
         MVCL  R0,R14 .      CLEAR ALL
         LM    R15,R1,12(R13) .  RESTORE
         BR    &RET
         MEND
