         MACRO ,
&NM      XTRAP &LIST,&OPT=(PSW,REGS),&TEST=,&ID=        ADDED ON 86218
.*
.*    THIS MACRO, RUNNING UNDER THE EXORCISE DEBUG PROGRAM, WILL TRACE
.*    INSTRUCTION FLOW, PRINT VARIABLES, AND DISPLAY REGISTERS
.*  NAME CHANGED TO XTRAP TO PREMIT XTRACE USE FOR INSTRUCTION TRACE
.*
.*  MAINTENANCE:  2005-06-14  GYP
.*  ALLOW *var AS INDIRECT LOOKUP REQUEST - 24-BIT ADDRESS IN WORD
.*  ALLOW /var AS INDIRECT INDIRECT LOOKUP
.*    IMPLEMENTED BY TRUNCATING PRINTED NAME TO CL7
.*                2008-04-18  GYP
.*  LENGTH FIELD CHANGED FROM AL2 TO SL2 USING DEBTROLD         GP10021
.*
         LCLC  &LN,&N,&V,&LAB,&TAG,&C                           GP08109
         LCLA  &I,&J,&K,&L,&FG,&NL,&R                           GP08109
         LCLB  &B80,&B40,&B20,&B10,&B08,&B04,&B02,&B01
&TAG     SETC  '&ID'         USE OVERRIDE LABEL
         AIF   ('&TAG' NE '').HVTAG
&TAG     SETC  '&NM'         ELSE USE NAME FIELD
.HVTAG   ANOP  ,
.*       B01 RESERVED FOR FULL MEMORY DUMP
&I       SETA  N'&OPT
&LN      SETC  'L'''
&LAB     SETC  '&NM'
         AIF   ('&TEST' NE '').TEST
         AIF   ('&SYSPARM' NE 'DEBUG').EASY
         AGO   .PROCEED
.TEST    ANOP  ,
&LAB     TM    OPTS,DEBUG .   RUNNING UNDER EXORCISE ?
         BZ    XTR&SYSNDX    NO; BYPASS TRACE
&LAB     SETC  ''
.PROCEED AIF   (&I GT 0).OPTLOOP
         MNOTE 0,'OPTIONS OMITTED - PSW DEFAULTED'
&B40     SETB  1
         AGO   .OPTDONE
.OPTLOOP AIF   (&J GE &I).OPTDONE
&J       SETA  &J+1
&B40     SETB  ('&OPT(&J)' EQ 'PSW' OR &B40)
&B80     SETB  (('&OPT(&J)' EQ 'PSW' AND '&TAG' NE '') OR &B80)
&B20     SETB  ('&OPT(&J)' EQ 'REGS' OR &B20)
&B10     SETB  ('&OPT(&J)' EQ 'MREGS' OR &B10)
&B08     SETB  ('&OPT(&J)' EQ 'MAP' OR &B08)
&B04     SETB  ('&OPT(&J)' EQ 'BUFFER' OR &B04)
         AGO   .OPTLOOP
.OPTDONE AIF   ((&B40+&B20+&B10+&B08+&B04+&B02+&B01) GE &I).OPTOK
         MNOTE 4,'UNRECOGNIZED OPTION SPECIFIED'
.OPTOK   ANOP  ,
&I       SETA  N'&SYSLIST
&J       SETA  0
.COUNTER AIF   (&J GE &I).COUNTED
&J       SETA  &J+1
         AIF   ('&SYSLIST(&J)' EQ '').COUNTER
&K       SETA  (N'&SYSLIST(&J)+1)/2
&NL      SETA  &NL+&K
         AGO   .COUNTER
.COUNTED ANOP  ,
&J    SETA  128*&B80+64*&B40+32*&B20+16*&B10+8*&B08+4*&B04+2*&B02+&B01
&LAB     DC    0H'0',X'830E',AL1(&J,&NL) DEBUG: TRACE
         AIF   (&NL EQ 0 AND NOT &B80).EXPDONE
         B     XTR&SYSNDX
&J       SETA  0
         AIF   (NOT &B80).EXPAND
         DC    CL8'&TAG '
.EXPAND  AIF   (&J GE &I).EXPDONE
&J       SETA  &J+1
         AIF   ('&SYSLIST(&J)' EQ '').EXPAND
&K       SETA  N'&SYSLIST(&J)
&L       SETA  1
.EXPITEM AIF   (&L GT &K).EXPAND
&N       SETC  '&SYSLIST(&J,&L)'
&V       SETC  '&N'                                             GP05165
         AIF   ('&V'(1,1) NE '*' AND '&V'(1,1) NE '/').NIND     GP05165
&V       SETC  '&V'(2,K'&N-1)  ELIMINATE * FROM VARIABLE NAME   GP05165
.* &N    SETC  '&V'          COPY   (DEFER)                     GP05165
.NIND    AIF   ('&SYSLIST(&J,&L+1)' EQ '').DEFLN
&R       SETA  K'&SYSLIST(&J,&L+1)                              GP08109
         AIF   (&R LT 3).NOREGLN                                GP08109
&C       SETC  '&SYSLIST(&J,&L+1)'                              GP08109
         AIF   ('&C'(1,1) NE '(' OR '&C'(2,1) EQ '(').NOREGLN   GP08109
         AIF   ('&C'(&R,1) NE ')' OR '&C'(&K-1,1) EQ ')').NOREGLN
         DC    CL8'&N ',SL2(&V,0&C)                             GP10021
         AGO   .EXPITEX                                         GP08109
.NOREGLN DC    CL8'&N ',SL2(&V,&SYSLIST(&J,&L+1))               GP10021
         AGO   .EXPITEX                                         GP08109
.DEFLN   DC    CL8'&N ',SL2(&V,&LN&V)                           GP10021
.EXPITEX ANOP  ,
&L       SETA  &L+2
         AGO   .EXPITEM
.EASY    AIF   ('&NM' EQ '').EXPDONE
&LAB     DS    0H            NO DEBUG MODE - NO TRACE
.EXPDONE AIF   ('&TEST' EQ '' AND &NL EQ 0 AND NOT &B80).MEXIT
XTR&SYSNDX DS  0H
.MEXIT   MEND  ,
