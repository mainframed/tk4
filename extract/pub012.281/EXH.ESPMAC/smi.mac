         MACRO
&NM      SMI   &STRING,&SMI=
         GBLC  &CRT
         GBLB  &SMINAMS
         LCLC  &HEXCODE
         LCLA  &I,&J,&K,&L,&M
         LCLC  &Q,&LQ
.*    GENERATES DC CONSTANTS WITH SMI AS THE LAST CHARACTER
.*   FOR 3270 GENERATES START PROT FLD, TEXT, '>',
.*         START UNPROT FLD, INSERT CURSOR
&HEXCODE SETC  '4A'          2260 SMI SYMBOL
         AIF   ('&CRT' NE '3270').OK60
&HEXCODE SETC  '6E'          ON OTHER USE >
         AIF   ('&SMI' EQ '').OK60 .  USE DEFAULT '>'
&HEXCODE SETC  '&SMI' .      ELSE USE SUPPLIED HEX CHARACTERS
.OK60    AIF   (&SMINAMS).DONE
&SMINAMS SETB  1
         AIF   ('&CRT' EQ '3270').DOEX7
SMILOFF  EQU   0 .           OFFSET TO START OF TEXT
SMILEND  EQU   1 .           LENGTH OF TRAILING CONTROL BYTES
SMILERR  EQU   X'7F' .       DEVICE CODE FOR ERROR SYMBOL
         AGO   .DONE
.DOEX7   ANOP
SMILOFF  EQU   2 .           OFFSET TO START OF TEXT
SMILEND  EQU   4 .           LENGTH OF TRAILING CONTROL BYTES
SMILERR  EQU   X'6A' .       DEVICE CODE FOR ERROR SYMBOL - NEEDS RPQ
.DONE    ANOP
&Q       SETC  ''''
&LQ      SETC  ' '''
&K       SETA  K'&STRING
&M       SETA  &K
         AIF   ('&STRING' EQ '').PARMBAD
         AIF   ('&STRING'(1,1) NE '''').NOQUOT
         AIF   ('&STRING'(&K,1) NE '''').PARMBAD
&I       SETA  2
&J       SETA  &K-2
&K       SETA  &J
.LOOP    AIF   ('&STRING'(&I,2) EQ '''''').SK2
         AIF   ('&STRING'(&I,2) EQ '&&').SK2
&I       SETA  &I+1
         AGO   .INC
.SK2     ANOP
&I       SETA  &I+2
&K       SETA  &K-1
.INC     AIF   (&I LE &J).LOOP
         AIF   (&K EQ 0).PARMBAD
&M       SETA  &M-1
&Q       SETC  ''
&LQ      SETC  ''
.NOQUOT  AIF   ('&STRING'(&M,1) EQ ' ').OK
&K       SETA  &K+1
.OK      AIF   (&K GT 1).OK2
&K       SETA  2
.OK2     ANOP
&L       SETA  &K-1
         AIF   ('&CRT' EQ '3270').DO70
&NM      DC    0CL(&K)' ',CL(&L)&Q&STRING&LQ,X'&HEXCODE'  SMI OR >
         MEXIT
.DO70    ANOP
&K       SETA  &K+5
&NM      DC 0CL(&K)' ',X'1DE8',CL(&L)&Q&STRING&LQ,X'&HEXCODE',X'1D4013'
         MEXIT
.PARMBAD MNOTE 8,'INVALID PARAMETER'
         MEND
