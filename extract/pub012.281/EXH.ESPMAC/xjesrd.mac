         MACRO
&NM      XJESRD &TTR,&ERR=IOERR,&EOF=IOERR,&CHECK=YES
         GBLC  &HASP
&NM      MACPARM R1,&TTR,OP=L,NULL=DRTTR
         MACPARM R0,0        CLEAR FOR LATER IC
         STCM  R1,8,DRMBB    SET EXTENT BYTE
         ICM   R1,8,ZEROES   AND ZERO IT
         STC   R1,DRMBB+7    SET RECORD NUMBER
         SRL   R1,8          RETAIN TRACK NUMBER ONLY
         LH    R15,DRTKCYL   GET TRACKS PER CYLINDER
         DR    R0,R15        GET TRACKS AND CYLNDERS
         STCM  R0,3,DRMBB+5  SET TRACK
         STCM  R1,3,DRMBB+3  SET CYLINDER ADDRESS
         AIF   ('&CHECK' NE '' AND '&CHECK' NE 'YES').NOCHECK
         AIF   ('&ERR' NE '').OKCHECK
         MNOTE 4,'CHECK BYPASSED - NO ERR BRANCH ADDRESS'
         AGO   .NOCHECK
.OKCHECK L     R15,DRDEB .   GET THE DEB ADDRESS
         NEED  DEB .         REQUEST DEB, DON'T BASE
         CLC   DRMBB(1),DEBNMEXT-DEBBASIC(R15)                   81040
         BNL   &ERR .        FAIL INVALID EXTENT
         SR    R14,R14       CLEAR FOR IC
         IC    R14,DRMBB     RESTORE EXTENT ADDRESS
         SLL   R14,4 .       EXTENT IS 16 BYTES
         LA    R14,DEBBASND-DEBBASIC(R14,R15)  POINT TO EXTENT ENTRY
         CLC   ZEROES(3),1(R14) .  NON-ZERO UCB ?
         BE    &ERR .        NO; VOLUME IS NOT UP
.NOCHECK L     R1,DRDEB      GET DEB FOR UCB READY CHECK
         BALS  R9,DREAD1     PERFORM AN I/O BY MBBCCHHR
         AIF   ('&ERR' EQ '').MEND
         B     &ERR .        TAKE I/O ERROR EXIT
         AIF   ('&EOF' EQ '').MEND
         B     &EOF .        TAKE END-FILE EXIT
.MEND    MEND
