         MACRO ,
         EXHMUPDS ,
         GBLC  &SYSTEM
         GBLA  &REL                                              84310
         MAPIOWK SIZE=64*1024-1-660,USE=BPAM  OVERLAY I/O WORK AREA
         ORG   DREAD         OVERLAY DUE TO INCREASED DCB SIZE
         STM   R9,R13,DWSAVC  USE DIFFERENT SAVE AREA
         ORG   DREAD1-4
         LM    R9,R13,DWSAVC
         ORG   DREADON
         LA    R1,DRECB+4
         EXCP  (1)
         WAIT  ECBLIST=DWECBLST
         CLI   DRECB,X'7F'
         BNE   DRIBAD
         MVC   DRMBB+3(5),DRNCCH
         STM   R9,R13,DWSAVC
         LM    R1,R2,DRDEB
         L     R15,DWPRLTV
         ORG   DRIBAD-8
         LM    R9,R13,DWSAVC
         ORG   DRFREE
         B     DWFREE
         ORG   DRDCB
DWDCB    DCB   DDNAME=DUMMY,DEVD=DA,DSORG=PO,MACRF=(R,W),              *
               EXLST=DWEXLST-XIOWORK,EODAD=DRIBAD-XIOWORK
         SPACE 1
         ORG   DRBUF
*        NOTE - THE SAME BUFFER MAY BE USED TO
*        CONTAIN A PDS DIRECTORY BLOCK OR AN IDR OR
*        CESD RECORD FROM A LOAD MODULE
DIRBUFF  DC    XL256'0'
ATTRYES  DC    XL2'0'
ATTRNO   DC    XL2'0'
ATTRVYES DC    X'0'          VS LKED OPTIONS                     80147
ATTRVNO  DC    X'0'                                              80147
ATTRVAC  DC    X'0'          AC BYTE                             80147
ATTRMODE DC    X'00'         VS FTB2 - RMODE/AMODE               90204
         SPACE 1
         DS    0F                                                80147
MEMBERS  DS    0CL16' '
MEMBER1  DC    CL8' '         FIRST MEMBER NAME
MEMBER2  DC    CL8' '         SECOND MEMBER NAME
MEMBOLD  DC    CL8' '        OLD MEMBER NAME                     80358
         SPACE 1
SAVETTR  DC    XL3'0',X'0'
LKEDDATE DC    XL3'0'
         SPACE 1
FLAGS    DC    XL2'0'         GENERAL STATUS FLAGS
*        FLAG BYTE 1
FMEMBER1 EQU   X'10'          FIRST MEMBER FLAG
FMEMBER2 EQU   X'08'          SECOND MEMBER FLAG
FMEMASK1 EQU   X'04'         MEMBER1 IS A MASK
FLOADMOD EQU   X'02'          DATA SET IN LOAD MODULE FORMAT
FATTR    EQU   X'01'          CHANGE ATTRIBUTES REQUEST
*        FLAGS AT BYTE 1
F1STREAD EQU   X'80'          INITIAL ENTRY TO READ SUBROUTINE
FLINESET EQU   X'40'          OUTPUT LINE IN PROGRESS
FEXIST   EQU   X'20'          MEMBERS EXIST IN DATA SET
FRANGE   EQU   X'10'          MEMBERS EXIST IN DISPLAY RANGE
FIDR     EQU   X'40'          IDR RECORDS EXIST
F1IDR    EQU   X'20'          IDR TITLE PROCESSED
FIDSPLIT EQU   X'10'         IDR TEXT SPLIT BETWEEN BUFFERS
FALLESD  EQU   X'08'          REQUEST ALL ESD ENTRIES
FESDIDR  EQU   X'04'         INCLUDE IDR DATE WITH ESD
FESDOFLO EQU   X'02'         ESD TABLE OVERFLOW
         SPACE 2
         DS    0F
MSGTEXT1 DC    CL132' '
         SPACE 1
         ORG   *-12
DAYTABLE DC    XL12'0'
         SPACE 2
DIRNAME  DC    D'0'          ALIGN FOR IHAPDS EXPANSION
DIRTTR   DC    XL3'0'
DIRFLAG  DC    X'0'
DIRUSER  DC    XL64'0'
DIRBLEND EQU   *
         ORG   DIRNAME
         IHAPDS PDSBLDL=NO,DSECT=NO
         ORG   DIRBLEND      REPOSITION AFTER DIRECTORY ENTRY
PDESAVE  DC    XL(PDSS03LN+PDSS04LN)'0'  SAVE AREA FOR SSI/APF
PDEFLAG  DC    X'00'         ALIAS PROCESSING FLAG
PDEREAL  EQU   X'40'         DE CONTAINS MAIN MEMBER NAME
PDESCTR  EQU   X'01'         COPY OF SCATTER FLAG
PDELEN   DC    X'00'         NEW DE LENGTH
         SPACE 1
DWMEMNUM DC    H'0'          MEMBER # FOR EXHCXUPW RESTART       87211
DWRDSCB  CCW   X'0E',DRBUF,0,140  READ KEY/DATA OF DSCB1
SCANTIC  DC    X'08',AL3(SCANCCWS),A(256)  TIC FOR KEY SCAN
SCANCCWS CCW   X'E9',DIRNAME,X'40',8  SEARCH KEY HI/EQ
         CCW   X'08',SCANCCWS,0,0     TIC *-8
         CCW   X'06',DIRBUFF,0,256    READ DATA
WORKWORK DC    D'0'          RESERVED FOR EXPANSION
         DC    A(0)          RESERVED FOR EXPANSION
         DC    A(0)          RESERVED FOR EXPANSION
         DC    A(0)          RESERVED FOR EXPANSION
         DC    A(0)          RESERVED FOR EXPANSION
         DC    A(0)          RESERVED FOR EXPANSION
         DC    A(0)          RESERVED FOR EXPANSION
         DC    A(0)          RESERVED FOR EXPANSION
         DC    A(0)          RESERVED FOR EXPANSION
DWECBLST DC    A(0),X'80',AL3(DRECB)
ESDPTRS  DC    0A(0)         ADDRESSES OF ESD DATA CHAIN
ESDPTR   DC    A(0,18)       FIRST ESD ENTRY
ESDPTR2  DC    A(0)          LAST ESD ENTRY
ESDWORK  DS    3A(0)         SCATTER LOAD WORK AREA
ESDFLAG  DC    X'0'          FLAG OPTIONS
ESDFNAME EQU   X'20'         SORT ESD ENTRIES BY NAME
ESDFNUC  EQU   X'10'         PROCESS NUCLEUS ENTRY
ESDFNUC1 EQU   X'08'         FIRST TIME NUC. FLAG
ESDBASE  DC    AL3(0)        MAP WITH OFFSET
ESDSCTR  DC    A(0)          ADDRESS OF SCATTER TABLE
ESDTAB   DC    A(0)          ADDRESS OF TRANSLATE TABLE
IDRWORK  DC    3A(0)
IDRPTRS  DC    0A(0,0,0)
IDRPTR   DC    A(0,0)
IDRPTR2  DC    A(0)
DIRPTRS  DC    3F'0'
DWSAVC   DC    5F'0'         CONVERT SAVE AREA
DWPRLTV  DC    A(0)          ABS TO REL TTR CONVERSION ROUTINE
DWEXLST  DC    X'87',AL3(DRBUF)
FOUR     DC    F'4'
DWFREE   STM   R0,R15,DWFSAVE
         XC    DCBFDAD+DWDCB-IHADCB(8),DCBFDAD+DWDCB-IHADCB
         TM    DWFLAG,DWOUTPUT+DWDIRUP  RE-WRITE DSCB ?
         BNO   DWFREEQ       NO; DEQUEUE AND CLOSE
         CLC   DWDIRCQ,DCBDIRCQ+DWDCB-IHADCB  DIR CHANGED ?
         BE    DWFREED       NO
         L     R1,DRDEB      GET DEB ADDRESS FOR DREAD1
         NEED  DCB,DSCB1
         NUSE  DEB,R1
         MVC   DEBBASND+6(8),DWVTOCX  MOVE VTOC EXTENTS
         XC    DRMBB(8),DRMBB  CLEAR ID FIELD
         MVC   DRMBB+3(5),DWDSCB  MOVE DSCB ADDRESS
         MVC   DRNCCH,DWDSCB  ALSO SET FOR REVERSE CONVERSION
         MVC   DRSLI-4(8),DWRDSCB  MAKE DSCB READ CCW
         BAL   R9,DREAD1     REREAD DSCB
         B     DWFREED       SKIP IF ERROR
         B     DWFREED       SKIP IF ERROR
         CLC   DWLSTAR,DRBUF+DS1LSTAR-DS1DSNAM  CHANGED ?
         BNE   DWFREED       YES; SKIP UPDATE
         CLC   DWDSN,DRBUF   DSN CHANGED ? (E.G. SCRATCH)
         BNE   DWFREED       YES; SKIP
         CLC   DCBDIRCQ+DWDCB-IHADCB,DS1NOBDB-DS1DSNAM+DRBUF
         BE    DWFREED
         MVC   DS1NOBDB-DS1DSNAM+DRBUF,DCBDIRCQ+DWDCB-IHADCB
         MVI   DRSLI-4,X'0D'   RE-WRITE KEY AND DATA
         MVC   DRMBB+3(5),DWDSCB  HOPE FOR THE BEST
         BAL   R9,DREAD1     RE-WRITE / R1 HAS DEB ADDRESS
         NOP   0             SHOULD SIGNAL ERROR
         NOP   0             SHOULD NOT HIT EOF
DWFREED  DS    0H ,
         AIF   ('&SYSTEM' EQ 'MVT' OR '&SYSTEM' EQ 'MFT').OSDEBCK
         TM    DWFLAG,DWDEBCHK  NEED TO FREE ?
         BZ    DWFREEQ       NO
         MODESET MODE=SUP
*        DEBCHK DWDCB,AM=EXCP,TYPE=DELETE  FREE STOW'S DEBCHK
         DEBCHK DWDCB,AM=EXCP,TYPE=DELETE  FREE DEBCHECK ENTRY
         MODESET MODE=PROB
.OSDEBCK ANOP  ,
DWFREEQ  DEQ   MF=(E,DWENQDEQ),RET=HAVE
         LM    R0,R15,DWFSAVE
         TM    DRFLAG,DRDEBUG  DEBUG MODE ?
         B     DRFREE+4      CLOSE/DELETE CODE
.*       0C5 FIX - THE ENQ/DEQ EXPANSION DIFFERS BETWEEN MVT AND SVS
.*       THE FOLLOWING ORG PROVIDES THE SAME WORK AREA SIZE FOR  80356
.*       ALL SYSTEM OPTIONS AND OBVIATES RE-ASSEMBLY OF ALL DIR  80356
.*       OVERLAYS WHEN THE USER WISHES TO CHANGE ONLY ONE.       80356
         ORG   DWFREEQ+64    MVT-SHORT DEQ EXPANSION FIX         80356
         SPACE 1
DWFSAVE  DC    16F'0'        ONEXIT SAVE AREA
DWENQDEQ ENQ   (DWSYSDSN,DWDSN,E,44,SYSTEM),RET=USE,MF=L
DWSYSDSN DC    CL8'SYSDSN'
DWDSN    DC    CL44' '       COPY OF DSN
DWVTOCX  DC    XL8'0'        VTOC EXTENTS
DWDSCB   DC    XL5'0'        DSCB1 CCHHR
DWLSTAR  DC    XL5'0'        TTR/TKBAL
DWDIRCQ  DC    X'0'          STARTING DE USE BYTE
DWRECFM  DC    X'0'          FLOADMOD ON IF RECFM=U
DWFLAG   DC    X'00'         SPECIAL PROCESSING FLAG
DWOUTPUT EQU   X'80'         OUTPUT MODE (SCR, REN, ALI, ATT..)
DWENQOLD EQU   X'40'         ENQ EXCLUSIVE ISSUED
DWENQSHR EQU   X'20'         ENQ SHARED ISSUED
DWDIRUP  EQU   X'10'         DSCB1 TO BE RE-WRITTEN
DWDEBCHK EQU   X'08'         DEBCHK DONE
DWOUTRIT EQU   X'01'         DATA UPDATE/WRITE PERMITTED        GP15227
NAMETRT  DC    256X'FF'      SET INVALID CHARACTERS
         ORG   NAMETRT+C' '  BLANK
         DC    X'10'
         ORG   NAMETRT+C'"'  GENERAL MASK
         DC    X'0C'
         ORG   NAMETRT+C'?'  ALTERNATE MASK
         DC    X'08'
         ORG   NAMETRT+C'$'
         DC    X'00'
         ORG   NAMETRT+C'#'
         DC    X'00'
         ORG   NAMETRT+C'@'
         DC    X'00'
         ORG   NAMETRT+X'C0'  ALLOW X'C0' FOR SVC NAMES
         DC    10X'00'       X'C0', A-I
         ORG   NAMETRT+C'J'
         DC    9X'00'
         ORG   NAMETRT+C'S'
         DC    8X'00'
         ORG   NAMETRT+C'0'
         DC    10X'00'
         ORG   NAMETRT+256   END OF TABLE
TRTAB    TRTAB OPT=(SVC),FILL=X'1C'                              90311
         SPACE 1
*        CHECK DIRECTORY ENTRY FOR LKED
*
ALIMONY  MVC   PDELEN,DIRFLAG  GET LENGTH/FLAG BYTE
         NI    PDELEN,X'1F'  KILL FLAG BITS
         TM    FLAGS,FLOADMOD  RECFM=U ?                         80165
         BZR   R9            NO; RETURN ERROR                    80165
         CLI   PDELEN,11     MINIMUM LKED LENGTH ?               80036
         BLR   R9            NO; RETURN ERROR
         CLI   PDELEN,23     LONGER THAN MAX ?                   80358
         BHR   R9            YES; RETURN
         MVC   PDEFLAG,PDELEN  SET FLAG FOR TR
         MVC   DB(1),PDS2FTB1   COPY VS LKED BYTE                80358
         NI    DB,PDSAOSLE   KILL EXTRANEOUS BITS                80358
         LA    R14,ALFGOS-11  TENTATIVELY SET OS LIST            80036
         TM    PDS2FTB1,PDSAOSLE+PDSAPFLG  VS + APF ?            80358
         BNO   ALIMOS        NO; USE OS PROCESSING               80358
         BCTR  R14,0         SLIDE TABLE OVER FOR APF            80358
         OI    DB,PDSAPFLG   SET FOR APF ALSO                    80358
ALIMOS   TR    PDEFLAG,0(R14)  CONVERT LENGTH TO FLAGS
         OC    PDEFLAG,DB    OR IN THE VS AND APF BITS           80358
         CLI   PDEFLAG,X'FF'  INVALID ENTRY ?
         BER   R9            YES; FAIL
         TM    PDEFLAG,PDESCTR  SCATTER LOAD FLAG ON ?
         BZ    4(,R9)        NO; TAKE GOOD RETURN
         TM    PDS2ATR,PDS2SCTR  SCATTER ON ?
         BZR   R9            NO; FAIL
         B     4(,R9)        TAKE GOOD EXIT
         SPACE 1
*        LOAD-MODULE ATTRIBUTES BY LENGTH        NO AC     AC    80358
         DC    AL1(255)                                    11    80358
ALFGOS   DC    AL1(0,255,PDS2SSI,255)            11-14     12-15 80358
         DC    AL1(PDESCTR,PDEREAL)              15-16     16-17 80358
         DC    AL1(PDS2SSI+PDESCTR,PDS2SSI+PDEREAL) 17-18  18-19 80358
         DC    AL1(255,PDESCTR+PDEREAL,255)      19-21     20-22 80358
         DC    AL1(PDS2SSI+PDESCTR+PDEREAL,255)  22-23     23-24 80358
EXTRINP  MSG   'TOO MUCH INPUT SUPPLIED'
RENMEMSG MSG   'RENAME REQUIRES OLD AND NEW MEMBER NAMES'
ESDOFLOW MSG   'REGION TOO SMALL FOR ALL ESD/IDR ENTRIES'
ALIMEMSG MSG   'ALIAS REQUIRES OLD NAME AND ALIAS TO BE ASSIGNED'
NEEDNAME MSG   'MEMBER NAME REQUIRED'
FUNNYRET MSG   'UNEXPECTED RETURN CODE FROM STOW'
MEMTOOLN MSG   'MEMBER NAME EXCEEDS 8 CHARACTERS'
MSGNMBAD MSG   'INVALID MEMBER NAME :'
INVCMD   DC    C'INVALID COMMAND -'
MSGNLOAD MSG   ' DATASET OR MODULE NOT IN LOAD FORMAT'
MSGBADAT MSG   ' INVALID ATTRIBUTE, '
MSGDUPAT MSG   ' CONFLICTING ATTRIBUTES, THIS ATTRIBUTE IGNORED, '
MSGIOERR MSG   ' I/O ERROR IN DIRECTORY'
MSGNODIR MSG   ' DIRECTORY IS FULL'
MSGEMPTY MSG   ' NO MEMBERS EXIST'
MSGRANGE MSG   ' INVALID DISPLAY RANGE'
MSGNODSP MSG   ' NO MEMBERS WITHIN DISPLAY RANGE'
MSGEXIST MSG   ' MEMBER CURRENTLY EXISTS'
MSGNONE  MSG   ' MEMBER NOT FOUND'
MSGGONE  MSG   ' MEMBER SCRATCHED'
MSGRENAM MSG   ' MEMBER RENAMED'
MSGATTRS MSG   'ATTRIBUTES ARE:'
MSGNREAL MSG   ' MODULE IS AN ALIAS, ALIAS NOT ASSIGNED'
MSGREAL  MSG   ' MODULE IS ALIAS FOR '
MSGNOMOD MSG   ' MODULE IS ALIAS BUT REAL MODULE DOES NOT EXIST'
NONEMSG  MSG   ' NONE'
MSGNATTR MSG   ' ATTRIBUTES NOT AVAILABLE'
MSGNOESD MSG   ' MODULE HAS NO EXTERNAL SYMBOLS'
MSGAMBIG MSG   ' AMBIGUOUS SUBCOMMAND'
MSGALIAS MSG   'ALIAS ASSIGNED TO MEMBER'
MSGENTRY MSG   'ENTRY POINT AT '
MSGLEN   MSG   'MODULE LENGTH  '
MSGHIST1 MSG   'HISTORY SUMMARY FOR MODULE '
MSGHISTD MSG   'LAST LINK-EDITED ON'
MSGHISTZ MSG   'IMASPZAP UPDATE HISTORY BY CSECT -'
MSGHISTU MSG   'USER-SUPPLIED UPDATE HISTORY BY CSECT -'
MSGNHIST MSG   ' NO HISTORY AVAILABLE'
DAYMONTH DC    AL1(31,28,31,30,31,30,31,31,30,31,30,31)
DATEMASK DC    X'402120612020612020'
NO       DC    C'NO'
FF       DC    8X'FF'
HEXTRTAB EQU   *-X'F0'
         DC    C'0123456789ABCDEF'
$SEGTAB  DC    CL8'$SEGTAB'
$ENTAB   DC    CL8'$ENTAB'
***********************************************************************
         SPACE
*     PARAMETERS COMMON TO CXPDS AND THE @ZAP FUNCTION OVERLAYS :
         SPACE
***********************************************************************
ZAPWORK  DS    0F            START OF PARAMETER AREA
         SPACE
CURTTR   DC    F'0'          INITIAL OR CURRENT TTR
INITTR   DC    F'0'          INITIAL TTR
TEXTTR   DC    F'0'          TEXT TTR IF LOAD MODULE
OFFSET   DC    H'0'          DISPLAY STARTING OFFSET
OFFNEXT  DC    H'0'          OFFSET AT NEXT INCREMENTATION
OFFSETL  DC    H'0'          OFFSET LAST USED BY LOCATE
OFFSETLN EQU   *-OFFSET      LENGTH TO CLEAR
ZALCCKEY DC    X'70'         COND CODE FOR KEY COMPARE
ZALCCSEQ DC    X'D0'         COND CODE FOR SEQ COMPARE
KL       DC    H'0'          RECORD'S KEY LENGTH
DL       DC    H'0'          RECORD'S DATA LENGTH OR ACTUAL LENGTH READ
CCHH     DS    CL4
RR       DS    C             CCHHR OF CURRENT RECORD
         SPACE 2
MODEFG   DC    X'00'         PROCESSING MODE
MODEZAP  EQU   X'80'           ZAP DATA INITIALIZED
MODEUPD  EQU   X'40'           TEXT UPDATE MODE
MODEHEX  EQU   X'20'           HEX UPDATE MODE
MODELOC  EQU   X'10'           LOCATE COMMAND ACCEPTED
MODELOV  EQU   X'08'           LOCATE W VERIFY
MODEBACK EQU   X'04'           DECREMENT TTRS                    86134
MODELOCK EQU   X'02'           UPD - NO REP FOR UNPRINTABLES     81323
MODELOW  EQU   X'01'           PERMIT LOWER CASE INPUT
         SPACE 2
ZAPFG    DC    X'0'          ZAP PROCESSING CONTROL FLAGS
FZAP     EQU   X'80'           VERIFY VERIFIED
FKEY     EQU   X'40'           DISPLAY IS IN KEY MODE
FBAS     EQU   X'20'           START DISPLAY AT SPECIFIED BASE (-16)
FNO      EQU   X'10'           INHIBIT ZAP
FRED     EQU   X'08'           BUFFER CONTAINS RECORD
FRDA     EQU   X'04'           DISPLAY VER / REP TEXT
FINV     EQU   X'02'           REPLY HAD SYNTAX OR OTHER ERROR
FASK     EQU   FKEY            FLAGS LEFT AFTER 'READ' RESET
         SPACE 2
MSGFG    DC    X'0'          MESSAGE FLAGS
MEOF     EQU   X'80'           END-FILE READ
MERR     EQU   X'40'           I/O ERROR
MINV     EQU   X'20'           INVALID INPUT
MZAP     EQU   X'10'           DISK RE-WRITE COMPLETED OK
MLEN     EQU   X'08'           VERIFY / REPLACE LENGTHS UNEQUAL
MOFF     EQU   X'04'           REP DATA TOO LONG FOR RECORD
MVER     EQU   X'02'           VERIFY FAILED
MREP     EQU   X'01'           REP DATA REPLACED IN STORAGE
MASK     EQU   MEOF+MERR       BITS LEFT ON AFTER REPLY RESET
         SPACE 2
VERAD    DC    H'0'          VERIFY/REPLACE DISPLACEMENT
         SPACE
DCVER    EQU   *             START OF VERIFY LENGTH/TEXT
VERLEN   DC    X'0'          LEN - 1 OF VERIFY DATA
VERTEX   DC    XL16'0'       VERIFY TEXT
         SPACE
DCREP    EQU   *             START OF REPLACEMENT LENGTH/TEXT
REPLEN   DC    X'0'          LEN - 1
REPTEX   DC    XL16'0'       TEXT
         SPACE 2
ZALWORK  DS    0F
ZALEX    DS    2F'0'         POINTERS TO LENGTH/CLC ADDRESS
ZALEX2   DS    2F'0'         POINTERS TO LENGTH/CLC ADDRESS
ZALSECT  DS    0CL8' '       SECTION NAME
ZALSEQ   DS    0CL8' '       SEQUENCE NUMBER
ZALLAY   DS    0CL4' '       HASP OVERLAY NAME
ZALKEY   DS    CL44' '       KEY SCAN
ZALTOPS  EQU   *
         ORG   ZALSECT+L'ZALSECT
ZALSOFF  DS    F'0'          CSECT OFFSET - START                79188
ZALCLEN  DS    F'0'          CSECT LENGTH                        79188
CSDTTR   DS    F             TTR OF CSECT BLOCK
CSDLEN   DS    F             LENGTH OF CSECT
OFFSETC  DC    H'0'          OFFSET OF LD/SD IN CSECT BLOCK
ZALSID   DS    H'0'          ESDID
ZALC     DS    X'0'          CSECT FLAG
CONE     EQU   X'80'           TEXT RECORD FOUND
CTXT     EQU   X'40'           LOOKING FOR TEXT RECORD
CSKP     EQU   X'20'           SKIP NEXT TEXT RECORD
CESD     EQU   X'10'           ESD ENTRY FOUND
ZALSLEN  EQU   *-ZALSOFF     LENGTH TO CLEAR FOR CSECT
         ORG   ZALTOPS
ZALSCOFF DS    H'1'          SCAN INCREMENT
ZALSCAN  DS    CL32' '       SCAN INPUT AREA
ZALF     DS    X'0'          LOCATE PROCESSING FLAG
LCARD    EQU   X'80'           CARD= (NO SCAN ALLOWED)
LKEY     EQU   X'40'           KEY=
LAYO     EQU   X'20'           OLAY=
LSEQ     EQU   X'10'           SEQ=
LSCAN    EQU   X'08'           SCAN MODE
LSECT    EQU   X'04'           FIND SECTION
NOEOF    EQU   X'02'           IGNORE EOF DURING SCAN            81018
LSCAM    EQU   X'01'           SCAN RECURSION FLAG
ZALEN    EQU   *-ZALWORK
         SPACE
         DC    XL3'0'        ...RESERVED
ZAPCLEAR EQU   *-ZAPWORK     END OF COMMON PARAMETERS
         SPACE
***********************************************************************
         SPACE 1
BLOCKBUF DS    0D            START OF PDS/ZAP I/O BUFFER
BEGINESD DS    0H            START OF DYNAMIC ESD ENTRIES
         MEND
