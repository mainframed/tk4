         MACRO ,
&NM      SRST  &R,&S                                      ADDED GP04234
         GBLC  &MACPLAB                                         GP08310
.*
.*    BACKLEVEL IMPLEMENTATION OF SRST FOR HERCULES
.*    DOES NOT SUPPORT ALL POSSIBLE USES, ONLY MY COMMON ONES
.*    E.G., DOES NOT SUPPORT CASE WHERE R2 IS 0
.*          DOES NOT IGNORE HIGH BYTE/BIT
.*
         LCLA  &I
&I       SETA  &SYSNDX
&NM      ST    &R,24(,R13)   SAVE
         ST    &S,16(,R13)   CALLER'S
         MVI   20(R13),3     PRESET FOR
         MVI   21(R13),4     NO MATCH         CLC CC=2  L
&MACPLAB SETC  'ZZS'.'&I'.'L'  LOOP TAG
         AIF   ('&R' EQ '0' OR '&R' EQ 'R0').NOTEST
         MACPARM &R,&S,OP=CR,OPR=CR,MODE=EQU  DONE YET ?
         BE    ZZS&I.X
.NOTEST  MACPARM R0,1,0(&S),OP=CLM,MODE=THREE
         BE    ZZS&I.Q
         LA    &S,1(,&S)
         B     ZZS&I.L
ZZS&I.Q  MVI   21(R13),2     SET MATCH        CLC CC=1  H
         ST    &S,24(,R13)   RETURN MATCH LOCATION
ZZS&I.X  L     &R,24(,R13)   RETURN R1 SAME OR UPDATED
         L     &S,16(,R13)   ORIGINAL R2
         CLC   21(,R13),20(R13)  SET CC = MATCH, HI - NO MATCH  GP08076
         MEND  ,
