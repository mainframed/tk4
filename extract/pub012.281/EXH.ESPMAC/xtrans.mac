         MACRO ,
&NM      XTRANS &ADD,&LEN,&MODE=DATA,&TYPE=LONG
         LCLC  &L
         LCLA  &I
&I       SETA  0             OUT DATA
         AIF   ('&MODE' EQ 'DATA' OR '&MODE' EQ '').MOK
&I       SETA  2             OUT CONTROL
         AIF   ('&MODE' EQ 'CONTROL' OR '&MODE' EQ 'CNTL').MOK
&I       SETA  4             INP UPLOW
         AIF   ('&MODE' EQ 'LOWER' OR '&MODE' EQ 'UPLOW').MOK
&I       SETA  6             INP UPPER
         AIF   ('&MODE' EQ 'UPPER').MOK
         MNOTE 8,'XTRANS: UNSUPPORTED MODE=&MODE '
.MOK     AIF   ('&TYPE' EQ 'INLINE').SHORT
&NM      MACPARM R0,&I       SET TRANSLATION MODE
         MACPARM R2,&ADD     TEXT ADDRESS
&L       SETC  'L'.''''
         MACPARM R3,&LEN,NULL=&L&ADD  LENGTH
         MACPARM R15,SQTRANS,OP=L  TRANSLATOR ROUTINE
         MACPARM R14,R15,OP=BALR
         MEXIT ,
.SHORT   ANOP  ,
&NM      MACPARM R15,SQTRANS,OP=L  GET MODULE ADDRESS
         AH    R15,&I+8(,R15) GET TABLE ADDRESS
         AIF   ('&LEN' NE '').MESSY
         TR    &ADD,0(R15)   TRANSLATE
         MEXIT ,
.*  HANDLE EXPLICIT BASE REGISTER LATER
.MESSY   TR    &ADD(&LEN),0(R15)  TRANSLATE
         MEND  ,
