         MACRO ,
&NM      CMASK &MASKEND='*-',&MASKEQU='?%', 2260 HAD "  ADDED ON 89016 *
               &ML=R15,&MA=R14,&L=R0,&A=R1,&RET=R9
         LCLA  &I,&J,&K
         LCLB  &LEQ
         LCLC  &N,&C                                             89078
&I       SETA  &SYSNDX
&LEQ     SETB  ('&ML' EQ '&L')
&N       SETC  '&NM'
         AIF   (&LEQ).NOCOMP
&N       CR    &ML,&L        IS MASK LONGER THAN DATA ?
&N       SETC  ''
         BHR   &RET          RETURN WITH CC UNEQUAL
.NOCOMP  ANOP  ,             MASK LENGTH DEFINED SAME AS LENGTH
&N       LTR   &ML,&ML       MASK EXHAUSTED ?
         BNPR  &RET          YES - RETURN EQUAL
CMK&I.M  DS    0H
&J       SETA  1
&K       SETA  K'&MASKEND-2  LENGTH W/O QUOTES
.LOOPEND AIF   (&J GT &K).ENDLOOP
&J       SETA  &J+1          SET FOR NEXT ONE
&C       SETC  '&MASKEND'(&J,1)                                  89078
         CLI   0(&MA),C'&C'  FORCED END ?                        89078
         BER   &RET          'END' CHARACTER - RETURN EQUAL
         AGO   .LOOPEND
.ENDLOOP ANOP  ,
&J       SETA  1
&K       SETA  K'&MASKEQU-2  LENGTH W/O QUOTES
.LOOPEQU AIF   (&J GT &K).EQULOOP
&J       SETA  &J+1          SET FOR NEXT ONE
&C       SETC  '&MASKEQU'(&J,1)                                  89078
         CLI   0(&MA),C'&C'  NON-COMPARE ?                       89078
         BE    CMK&I.E       'EQU' CHARACTER - SKIP COMPARE
         AGO   .LOOPEQU
.EQULOOP ANOP  ,
         CLC   0(1,&MA),0(&A)    EQUAL ?
         BNER  &RET          NO - RETURN UNEQUAL
CMK&I.E  LA    &MA,1(,&MA)   NEXT MASK BYTE
         LA    &A,1(,&A)     NEXT DATA BYTE
         BCT   &ML,CMK&I.M   NEW MASK LENGTH
         LTR   &ML,&ML       SET CC EQUAL
         BR    &RET
         MEND  ,
