         MACRO ,                                         NEW ON GP12298
&NM      XXHINT &LEN=R2,&WK=R3,&W2=R4,&MAXL=,&MAX=,&ERR=
.*   THIS MACRO IS USED TO VALIDATE INTEGER FIELDS SPECIFIED ON AN
.*     FDIN MACRO WITH EXIT=
.*   IT IS NOT NEEDED EXCEPT TO VALIDATE SPECIAL CONDITIONS, SUCH AS
.*     POSITIVE VALUE ONLY, OR A MAXIMUM SMALLER THAN POSSIBLE (E.G.,
.*     255 IN A THREE-DIGIT FIELD)
.*
         GBLC  &ZZXXHNM
         LCLC  &LER,&LAB
&LER     SETC  '&ERR'
         AIF   ('&LER' NE '').USERR
&LER     SETC  '&ZZXXHNM'.'R'     NORMAL ERROR LABEL
.USERR   ANOP  ,
&LAB     SETC  'ZZX'.'&SYSNDX'
&NM      MVC   DBWK,=8X'F0'  CLEAR OUTPUT
         AIF   ('&MAXL' EQ '').NOLL
         CH    &LEN,=AL2(&MAXL)   VALID LENGTH ?
         MACPARM &LER,OP=BH,OPR=BHR,MODE=ONE
.NOLL    LR    &WK,&LEN      COPY LENGTH
         BCTR  &WK,0         MAKE EXECUTE LENGTH
         LA    &W2,DBWK+8
         SR    &W2,&LEN      LOCATION TO MOVE TO
         EX    &WK,&LAB.Z    MOVE ZONES
         CLC   DBWK,=8X'F0'  NUMERIC ZONES ?
         MACPARM &LER,OP=BNE,OPR=BNER,MODE=ONE
         EX    &WK,&LAB.M    MOVE TO WORK AREA
         CVB   R0,DBWK       CONVERT
         AIF   ('&MAX' EQ '').NOMAX
         CL    R0,=A(&MAX)   VALID ?
         MACPARM &LER,OP=BH,OPR=BHR,MODE=ONE
.NOMAX   B     *+4+6+6         RETURN TO CALLER
&LAB.Z   MVZ   DBWK(0),FIWTEXT
&LAB.M   PACK  DBWK,FIWTEXT(*-*)
         MEND  ,
