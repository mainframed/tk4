         MACRO ,
&NM      BALS  &R,&A                                    ADDED ON 90308
.*
.*       THIS MODULE GENERATES EITHER A BAL OR A BAS, OR POSSIBLY
.*       A BALR OR BASR, DEPENDING ON THE SYSTEM. BAS AND BASR
.*       REQUIRE AT LEAST SP 2 (MVS/XA)
.*
         GBLB  &MVT
         LCLA  &K
&K       SETA  K'&A
         AIF   (&K LT 3).NORMAL
         AIF   ('&A'(1,1) NE '(' OR '&A'(2,1) EQ '(').NORMAL
         AIF   ('&A'(&K,1) NE ')' OR '&A'(&K-1,1) EQ ')').NORMAL
         AIF   (&MVT).BALR
&NM      DC    0H'0',AL.4(0,13,&R,&A(1))  BASR
         MEXIT ,
.BALR    ANOP  ,
&NM      BALR  &R,&A(1)
         MEXIT ,
.NORMAL  ANOP  ,
&NM      BAL   &R,&A
         AIF   (&MVT).MEND
         ORG   *-4
         DC    X'4D'         BAS
         ORG   *+3
.MEND    MEND  ,
