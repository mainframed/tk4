         MACRO ,
&NM      XCURSOR &A,&B,&BASE=1,&SAVE=YES                    NEW GP12297
         GBLC  &MACPLAB
         GBLB  &ZZXCUR2      GLOBAL USE FLAG FOR SCINIT
         LCLA  &K                                               GP12298
.*                                                                    *
.*   XCURSOR IS USED IN CONJUNCTION WITH EXHASCRN ROUTINES, INVOKED   *
.*   BY SCxxxx MACROS OTHER THAN SCLINE.                              *
.*                                                                    *
.*   XCURSOR n          SET IT TO A BINARY OFFSET                     *
.*   XCURSOR m,n        PLACES THE CURSOR INTO ROW m (1-max) AND      *
.*                      COLUMN n (1-max)                              *
.*          BASE=1      ACCEPTS COORDINATES RELATIVE TO 1 (1-24,1-80) *
.*          BASE=0      ACCEPTS COORDINATES RELATIVE TO 0 (0-42,0-132)*
.*                                                                    *
&MACPLAB SETC  '&NM'
&ZZXCUR2 SETB  1             SHOW USER IS CONTROLLING THE CURSOR
         AIF   ('&A' EQ 'RESET').DEFAULT
         AIF   ('&A' EQ 'RETAIN').PROP
         AIF   ('&A' EQ 'PUSH').SAVEIC
         AIF   ('&A' EQ 'POP').RESTIC
         AIF   ('&SAVE' NE 'YES').NOSAVE
         MACPARM R14,R0,DB,OP=STM,MODE=THREE
.NOSAVE  AIF   (N'&SYSLIST LT 1 OR N'&SYSLIST GT 2).BADPARM
         AIF   (N'&SYSLIST EQ 2).CALC
&K       SETA  K'&A                                             GP12298
         AIF   (&K LT 3).F1                                     GP12298
         AIF   ('&A'(1,1) NE '(').F1                            GP12298
         AIF   ('&A'(&K,1) NE ')').F1                           GP12298
         AIF   ('&A'(2,1) EQ '(' OR '&A'(&K-1,1) EQ ')').F1     GP12298
         MACPARM R0,&A,OP=LH GET BINARY POSITION (0-max)
         MACPARM R0,FDWCUR,OP=STH   AND SET
         AGO   .COMOUT
.F1      MACPARM FDWCUR,&A,OP=MVC                               GP12298
         AGO   .COMOUT                                          GP12298
.CALC    MACPARM R14,&A      GET ROW
         AIF   ('&BASE' EQ '0').NOSUB1
         MACPARM R14,0,OP=BCTR,OPR=BCTR
.NOSUB1  MACPARM R15,&B      GET COLUMN
         AIF   ('&BASE' EQ '0').NOSUB2
         MACPARM R15,0,OP=BCTR,OPR=BCTR
.NOSUB2  MH    R14,LSIZE     GET OFFSET TO CORRECT ROW
         AR    R14,R15       GET TOTAL OFFSET
         STH   R14,FDWCUR    STASH IT
.*
.COMOUT  AIF   ('&SAVE' NE 'YES').NOREST
         MACPARM R14,R0,DB,OP=LM,MODE=THREE
.NOREST  OI    FDWPROFG,FDWPFCUR  USE IT
         MEXIT ,
.BADPARM MNOTE 8,'XCURSOR TAKES ONE OR TWO ARGUMENTS'
         MEXIT ,
.SAVEIC  MACPARM FDWSVCUR,FDWICUD,OP=MVC  SAVE WHERE USER LEFT IT
         MEXIT ,
.RESTIC  MACPARM FDWCUR,FDWSVCUR,OP=MVC  RESTORE FROM SAVE
         OI    FDWPROFG,FDWPFCUR  USE IT
         MEXIT ,
.PROP    MACPARM FDWCUR,FDWICUD,OP=MVC   SET WHERE USER LEFT IT
         OI    FDWPROFG,FDWPFCUR  USE IT
         MEXIT ,
.DEFAULT MACPARM FDWCUR,=X'FFFF',OP=MVC  DEFAULTS TO 1ST INPUT FIELD
         MEND  ,
