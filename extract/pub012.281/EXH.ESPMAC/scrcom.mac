         MACRO
&NM      SCRCOM &B0,&B1,&FN,&A0,&A1,&DEV=
         GBLC  &MACPLAB,&SCRMODE
.**********************************************************************
.*                                                                   **
.*   TSO INTERFACE TO @SCREENS FULL-SCREEN FORMATTING & I/O ROUTINE  **
.*                                                                   **
.**********************************************************************
         LCLA  &I,&J,&K,&VD,&D(8)
         LCLB  &INDEV                                            81259
         LCLC  &DC                                               81259
&MACPLAB SETC  '&NM'                                             81259
         AIF   ('&DEV' EQ '' OR '&DEV' EQ '0').NODV
         AIF   ('&DEV' NE 'ALL').DVSOM
&VD      SETA  255
         AGO   .NODV
.DVSOM   AIF   (K'&DEV LT 2).DVSOL                               81259
         AIF   ('&DEV'(1,1) NE '=').DVSOL                        81259
&INDEV   SETB  1             SET INDIRECT DEVICE NUMBER          81259
         AGO   .NODV                                             81259
.DVSOL   ANOP  ,                                                 81259
&I       SETA  0
&J       SETA  N'&DEV
.DEVLOOP ANOP  ,
&I       SETA  &I+1
         AIF   (&I GT &J).DVEND
         AIF   ('&DEV(&I)' EQ '').DEVLOOP
         AIF   ('&DEV(&I)' EQ '0').DEVLOOP
         AIF   ('&DEV(&I)' LT '1' OR '&DEV(&I)' GT '8').DVERR
&D(&DEV(&I)) SETA  1
         AGO   .DEVLOOP
.DVERR   MNOTE 8,'*** INVALID DEVICE NUMBER &DEV(&I)'
         AGO   .DEVLOOP
.DVEND   ANOP  ,
&VD      SETA  128*&D(8)+64*&D(7)+32*&D(6)+16*&D(5)+8*&D(4)
&VD      SETA  &VD+4*&D(3)+2*&D(2)+&D(1)
.NODV    AIF   ('&B0' NE '0' OR '&B1' NE '0').LONG
         AIF   (&VD GT 15).LONG
&K       SETA  &VD*256+&FN
         MACPARM R0,&K       LOAD DEVICE/FUNCTION INDEX          81259
         AGO   .POST0                                            81259
.LONG    AIF   (K'&B0 LT 2).LONGCON                              82326
         AIF   ('&B0'(1,1) NE '(').LONGCON                       82326
         AIF   ('&B0'(2,1) EQ '(').LONGCON                       82326
         MACPARM R0,&B0      LOAD R0                             82326
         SLL   R0,24         ALIGN LENGTH TO HIGH BYTE           82326
         O     R0,=AL1(0,&B1,&VD,&FN)                            82326
         AGO   .POST0                                            82326
.LONGCON ANOP  ,                                                 82326
&MACPLAB L     R0,=AL1(&B0,&B1,&VD,&FN)                          81259
&MACPLAB SETC  ''            CANCEL LABEL                        81259
.POST0   AIF   (NOT &INDEV).LOAD1                                81259
&VD      SETA  K'&DEV-1                                          81259
&DC      SETC  '&DEV'(2,&VD)                                     81259
&MACPLAB ICM   R0,2,&DC                                          81259
&MACPLAB SETC  ''                                                81259
.LOAD1   AIF   ('&FN' EQ '0' OR '&FN' EQ '1').BAL
         MACPARM R1,&A1      LOAD PARAMETER REGISTER
         AIF   ('&A0' EQ '' OR '&A0' EQ '0').BAL
         ICM   R1,8,=AL1(&A0)
.BAL     AIF   ('&SCRMODE' EQ 'V').VCON
         L     R15,@SCREENS
         AGO   .BALR
.VCON    L     R15,=V(@SCREENS)
.BALR    BASSM R14,R15
         MEND  ,
