         MACRO ,
&NM      XBUFCLR &SKIP=0,&LINES=0,&SAVE=YES
.*--------------------------------------------------------------------*
.*  CLEAR BUFFER USING MVCL (WAS MVC ON 360)                          *
.*  SKIP OMITS CLEARING THE FIRST 'SKIP' CHARACTERS                   *
.*  LINES OMITS CLEARING THAT MANY LEAADING LINES                     *
.*--------------------------------------------------------------------*
         GBLC  &MACPLAB
&MACPLAB SETC  '&NM'
         AIF   ('&SKIP' NE '0' AND '&LINES' NE '0').TOOMANY
         AIF   ('&SAVE' NE 'YES').TEST
         MACPARM R14,R1,20(R13),OP=STM,OPR=STM,MODE=3
.TEST    ANOP  ,
         MACPARM R0,BUFAD,OP=L,OPR=L  START CLEAR
         L     R1,MSGAD      END
         SR    R1,R0         SIZE TO CLEAR
         AH    R1,LSIZE        PLUS MSG AREA
         AIF   ('&SKIP' EQ '0' OR '&SKIP' EQ '').NOSKIP
.TOOMANY MACPLOD R15,&SKIP   LOAD ADDRESS OR LOAD
         AGO   .ADJUST
.NOSKIP  AIF   ('&LINES' EQ '0' OR '&LINES' EQ '').MOVE
         MACPLOD R15,&LINES  LOAD ADDRESS OR LOAD
         MH    R15,LSIZE     CONVERT NUMBER OF LINES TO CHARS
.ADJUST  AR    R0,R15        ADJUST START
         SR    R1,R15        ADJUST LENGTH
         BNP   *+4+4+2         SKIP ON ERROR
.MOVE    L     R15,=X'40000000'   BLANK ALL
         MVCL  R0,R14        CLEAR
         AIF   ('&SAVE' NE 'YES').MEND
         LM    R14,R1,20(R13)
.MEND    MEND  ,
