         MACRO ,
&NM      #FMT  &VALUE,&OLN,&SIGN=,&CENT=N,&GROUP=N,                    *
               &FILL=N,&LJUST=N,&SPACE=N,&TRUNC=N,&TYPE=DEC,           *
               &OPT=,&CALL=CALL,&MF=S                           GP04118
.*--------------------------------------------------------------------*
.*  THIS MACRO INVOKES NUMERIC FORMATTING SERVICES (VIA @FORMATS)     *
.*                                                                    *
.*  FIRST POSITIONAL IS ADDRESS/NAME OF VALUE                         *
.*  SECOND POSITIONAL USED TO SPECIFY (FORCED) OUTPUT LENGTH          *
.*                                                                    *
.*  OTHER SERVICES:    SIGN=Y - NEGATIVE # PREFIXED BY MINUS(DEFAULT) *
.*     CENT=Y  - FORMAT A DECIMAL NUMBER WITH TWO PLACES WWW.PP       *
.*     GROUP=Y - FORMAT IN GROUPS OF THREE 123,456                    *
.*     FILL=Y  - FILL WITH LEADING ZEROES  00012300                   *
.*     LJUST=Y - LEFT JUSTIFY THE NUMBER, THEN ABUT TEXT              *
.*     SPACE=Y - ONE SPACE BETWEEN NUMBER AND TEXT                    *
.*     TRUNC=N - OVERFLOW (***) WHEN SIGNIFICANT DIGITS LOST (CC=4)   *
.*                                                                    *
.*  TYPE=TEXT ADDED. WHEN INPUT=OUTPUT LENGTH, MOVED AND TRANSLATED   *
.*     OTHERWISE RIGHT JUSTIFIED EXCEPT WHEN LJUST=Y OR CENT=Y        *
.*  FOR MF NOT =L, VALUE MAY NOW BE A LITERAL                         *
.*                                                                    *
.*     CLOSE=YES - FREE THEE DYNAMICALLY ACQUIRED WORK AREA           *
.*                                                                    *
.*  EXCEPT FOR LJUST=Y, ALL OUTPUT FIELDS WILL BE THE MAXIMUM LENGTH  *
.*  POSSIBLE (TO MAINTAIN COMMON ALIGNMENT) DEPENDING ON THE INPUT    *
.*  LENGTH.  OUTPUT LENGTH INCLUDES SIGN (UNLESS SIGN=N) AND COMMAS   *
.*  AND PERIODS.  OR SPECIFY EXPLICIT OUTPUT LENGTH (NUM,LEN)         *
.*  LJUST=Y WITH SHORT LENGTH TRUNCATES ON RIGHT, ELSE LEFT.          *
.*                                                                    *
.* INVOCATION WITH #FMT *END OR #FMT OPT=CLOSE RELEASES THE WORK AREA *
.*                                                                    *
.* MF=L EXPANDS A FOUR BYTE DEFINITION; REQUIRES ABSOLUTE OR SELF-    *
.*  DEFINING TERMS FOR THE LENGTHS (USED WITH #OPMSG/SUBWTO)          *
.*--------------------------------------------------------------------*
.*  MAINTENANCE  2012-11-15  CHANGED DEFAULTS FOR SIGN TO DEPEND ON   *
.*                           TYPE (N FOR ABS, HEX, AND TEXT; Y OTHER) *
.*                                                                    *
.*                                                                    *
.*                                                                    *
.*                                                                    *
.*--------------------------------------------------------------------*
         GBLC  &MACPLAB
&MACPLAB SETC  '&NM'
         LCLA  &EPYT,&OPTS
         LCLA  &K,&I,&J,&N                                      GP04118
         LCLB  &O01,&O02,&O04,&O08,&O10,&O20,&O40,&O80
         LCLC  &L,&LOL,&LIN,&LON                                GP04118
&L       SETC  'L'''
         AIF   ('&VALUE' EQ '*END').QUIT
&N       SETA  N'&VALUE                                         GP04118
         AIF   ('&OPT' EQ 'CLOSE' OR '&OPT' EQ 'END').QUIT
&O80     SETB  ('&SIGN' EQ 'Y' OR '&SIGN' EQ 'YES')
&O40     SETB  ('&LJUST' EQ 'Y' OR '&LJUST' EQ 'YES')
&O20     SETB  ('&SPACE' EQ 'Y' OR '&SPACE' EQ 'YES')
&O10     SETB  ('&FILL' EQ 'Y' OR '&FILL' EQ 'YES')
&O08     SETB  ('&TRUNC' EQ 'Y' OR '&TRUNC' EQ 'YES')
.*&O04     SETB  ('&O04' EQ 'Y' OR '&O04' EQ 'YES')
&O02     SETB  ('&GROUP' EQ 'Y' OR '&GROUP' EQ 'YES')
&O01     SETB  ('&CENT' EQ 'Y' OR '&CENT' EQ 'YES')
&EPYT    SETA  1                                                GP04118
         AIF   ('&VALUE'(1,1) EQ '''').LITT                     GP12319
         AIF   ('&TYPE' EQ 'TXT' OR '&TYPE' EQ 'TEXT').LADD     GP04118
&EPYT    SETA  2
         AIF   ('&TYPE' EQ 'DEC' OR '&TYPE' EQ 'PACKED').LADD
&EPYT    SETA  3
         AIF   ('&TYPE' EQ 'ADEC' OR '&TYPE' EQ 'ABSDEC').LADD
&EPYT    SETA  4
         AIF   ('&TYPE' EQ 'INT' OR '&TYPE' EQ 'INTEGER').LADD
&EPYT    SETA  5
         AIF   ('&TYPE' EQ 'AINT' OR '&TYPE' EQ 'ABSINT').LADD
&EPYT    SETA  6
         AIF   ('&TYPE' EQ 'SHEX').LADD
&EPYT    SETA  7
         AIF   ('&TYPE' EQ 'HEX' OR '&TYPE' EQ 'X').LADD        GP12319
&EPYT    SETA  8
         AIF   ('&TYPE' EQ 'SBIN' OR '&TYPE' EQ 'SBIT').LADD
&EPYT    SETA  9
         AIF   ('&TYPE' EQ 'BIN' OR '&TYPE' EQ 'BINARY').LADD
         MNOTE 8,'#FMT: INVALID DATA TYPE &TYPE '               GP12319
&EPYT    SETA  7             DO AS HEX
         AGO   .LADD                                            GP12319
.LITT    AIF   ('&TYPE' EQ '' OR '&TYPE' EQ 'TEXT' OR '&TYPE' EQ 'TXT')*
               .LADD
         MNOTE 4,'#FMT: QUOTED STRING INVALID WITH TYPE &TYPE'  GP12319
.LADD    AIF   (T'&SIGN NE 'O').LADDLE   HAVE SIGN PREFERENCE   GP12319
&O80     SETB  (&O80 OR ('010101010'(1,&EPYT) EQ '1'))          GP12319
.LADDLE  ANOP  ,                                                GP12319
&OPTS    SETA  &O80*128+&O40*64+&O20*32+&O10*16
&OPTS    SETA  &OPTS+&O08*8+&O04*4+&O02*2+&O01
         AIF   ('&MF' EQ 'L').DCONLY                            GP04118
         AIF   ('&VALUE'(1,1) NE '''').NOLIT                    GP04118
&EPYT    SETA  K'&VALUE-2    BORROW VARIABLE                    GP04118
&LIN     SETC  '&EPYT'       SET INPUT LENGTH DEFAULT           GP04118
&EPYT    SETA  1             FORCE TEXT MODE FOR LITERAL        GP04118
         AIF   (&N LT 2).LITLEN                                 GP04118
&LIN     SETC  '&VALUE(2)'                                      GP04118
.LITLEN  MACPARM R0,=AL1(&OLN,&LIN,&EPYT,&OPTS),OP=L            GP04118
         MACPARM R1,=C&VALUE(1)                                 GP04118
         AGO   .CMNUML                                          GP04118
.NOLIT   AIF   ('&OLN' EQ '').NOLOAD                            GP04118
         MACPARM R0,12,=Y(&OLN*256),OP=ICM,MODE=THREE USER'S LENGTH
         MACPARM R0,3,=Y(&EPYT*256+&OPTS),OP=ICM,MODE=THREE TYPE
         AGO   .TESTCC
.NOLOAD  MACPARM R0,&EPYT*256+&OPTS,OP=LA   LOAD FORMATTING SELECTION
.TESTCC  AIF   (&N LT 2).NONUML                                 GP04118
         MACPARM R1,&VALUE(1),OP=LA LOAD ADDRESS OF PACKED COUNTER
         MACPARM R0,4,=AL1(&VALUE(2)),OP=ICM,MODE=THREE
         AGO   .CMNUML
.NONUML  MACPARM R1,&VALUE,OP=LA LOAD ADDRESS OF PACKED COUNTER
         MACPARM R0,4,=AL1(&L&VALUE),OP=ICM,MODE=THREE
         AGO   .CMNUML
.QUIT    MACPARM R0,(R0),OP=SR,OPR=SR,MODE=EVEN SET THE SHUTDOWN SIGNAL
         MACPARM R1,(R1),OP=SR,OPR=SR,MODE=EVEN   R0=R1=0       GP03122
.CMNUML  AIF   ('&CALL' EQ 'CALL').LOAD@                        GP03287
         AIF   ('&CALL' EQ 'CALLA').LOADA                       GP03287
         AIF   ('&CALL' EQ 'CALLV').LOADV                       GP03287
         MNOTE 4,'#CNVRT: UNRECOGNIZED CALL=&CALL '             GP03287
.LOADV   MACPARM R15,=V(@FORMATS),OP=L                          GP03287
         AGO   .LOADCOM                                         GP03287
.LOADA   MACPARM R15,=A(@FORMATS),OP=L                          GP03287
         AGO   .LOADCOM                                         GP03287
.LOAD@   MACPARM R15,@FORMATS,OP=L   LOAD CONVERSION ROUTINE ADDRESS
.LOADCOM ANOP  ,                                                GP03287
         BASR  R14,R15       CALL FORMATTING SERVICES
         MEXIT ,
.DCONLY  AIF   (&N LT 2).DCNUML                                 GP04118
&LIN     SETC  '&VALUE(2)'                                      GP04118
         AGO   .QUOUT                                           GP04118
.DCNUML  ANOP  ,                                                GP04118
&LIN     SETC  '&L'.'&VALUE'                                    GP04118
.QUOUT   AIF   ('&OLN' EQ '').QUODEF                            GP04118
&LON     SETC  '&OLN'                                           GP04118
         AGO   .DCDC                                            GP04118
.QUODEF  ANOP  ,                                                GP04118
&LON     SETC  '0'                                              GP04118
.*                                                              GP04118
.DCDC    MACPARM AL1(&LON,&LIN,&EPYT,&OPTS),MODE=ONE,OP=DC      GP04118
         MEND  ,
