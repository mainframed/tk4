         MACRO ,                                                 88150
&NM      SUBCALL &NAME,&PARM,&VL,&MF=S,&OP=BALR,&MODE=L     ADDED 88150
         GBLC  &MACPLAB                                          88150
         GBLC  &SRVLMOD(20),&SRVLDEL(20)                        GP03150
         GBLB  &MVSESA,&OS390,&Z900                             GP08076
         GBLA  &SRVNMOD                                         GP03150
         LCLA  &I,&J,&N
         LCLC  &LBL,&M,&LOP                                     GP08076
         AIF   ('&MF' EQ 'L').MFL
&LOP     SETC  '&OP'                                            GP08076
.*--------------------------------------------------------------------*
.*  NON-STANDARD HANDLING OF PARAMETER LIST:                          *
.*                                                                    *
.*  &N IS 1 - GENERATE SIMPLE LA UNLESS PARENTHESIZED (BACKWARD COMP) *
.*  &N IS 2 OR MORE - NORMAL PARAMETER LIST                           *
.*--------------------------------------------------------------------*
&MACPLAB SETC  '&NM'                                             88150
&N       SETA  N'&PARM                                          GP03041
         AIF   (&MVSESA OR &OS390 OR &Z900).OKBASS              GP08076
         AIF   ('&OP' EQ 'BSM').FLAKY                           GP12162
         AIF   ('&OP' NE 'BASSM' AND '&LOP' NE 'BASR').OKBASS   GP12162
.FLAKY   ANOP  ,                                                GP12162
&LOP     SETC  'BALR'        DON'T HAVE BASSM ON 360/370        GP08076
.OKBASS  AIF   (&N LT 1).LAPARM                                 GP03041
         AIF   (&N GT 1).NOTONE                                 GP03041
         AIF   ('&PARM'(1,1) EQ '(' AND '&PARM'(1,2) NE '(').NOTONE
         AIF   ('&PARM'(1,2) NE '((' OR                                *
               '&PARM'(K'&PARM-1,2) NE '))').LAPARM             GP03041
.NOTONE  AIF   ('&MF(1)' EQ 'E').NODC                           GP03041
*TEST*   CNOP  0,4           WORD ALIGN FOR PARM LIST           GP03041
         MACPARM *+(&N+1)*4,OP=B,MODE=ONE                       GP03041
&M       SETC  '&SYSNDX'                                        GP03041
ZZ&M.P   DC    &N.AL4(0)     INLINE, NONREFRESHABLE PARM LIST   GP03041
&LBL     SETC  'ZZ'.'&M'.'P'                                    GP03041
         AGO   .HAVEDC                                          GP03041
.NODC    ANOP  ,                                                GP03041
&LBL     SETC  '&MF(2)'                                         GP03041
.HAVEDC  MACPARM R1,&LBL     LOAD PARM LIST                     GP03041
&I       SETA  0             JUST IN CASE                       GP03041
         AIF   ((&N+1) EQ K'&PARM).GETADDR  PARM LIST IS NULL   GP03041
&M       SETC  'R1'          ABNORMAL PARM LIST POINTER         GP03041
&J       SETA  0             PARM OFFSET FOR NEXT ITEM          GP03041
.PRMLOOP AIF   (&I GE &N).GETADDR    DONE                       GP03041
&I       SETA  &I+1                                             GP03041
         AIF   ('&PARM(&I)' EQ '').NOLST                        GP03041
         AIF   ('&PARM(&I)'(1,1) NE '''').NOCHAR                GP03041
         MACPARM R0,=C&PARM(&I)                                 GP03041
         AGO   .HAVER0                                          GP03041
.NOCHAR  MACPARM R0,&PARM(&I)  LOAD USER'S ADDRESS              GP03041
.HAVER0  MACPARM R0,&J.(,&M),OP=ST   PLACE INTO PARAMETER LIST  GP03041
.NOLST   AIF   (&I NE &N).PRMBUMP                               GP03041
         AIF   ('&VL' NE 'VL').GETADDR                          GP03041
         OI    &J.(&M),X'80'  END LIST BIT                      GP03041
         AGO   .GETADDR                                         GP03041
.PRMBUMP ANOP  ,                                                GP03041
&J       SETA  &J+4          NEXT LIST OFFSET                   GP03041
         AGO   .PRMLOOP                                         GP03041
.*                                                              GP03041
.LAPARM  MACPARM R1,&PARM,NULL=SKIP                             GP02241
.*
.*--------------------------------------------------------------------*
.*  HAVE PARM ADDRESS IN R1; NOW GET ROUTINE ADDRESS IN R15 AND GO    *
.*--------------------------------------------------------------------*
.GETADDR AIF   ('&NAME' EQ '(15)' OR '&NAME' EQ '(R15)').BALR    88150
         AIF   ('&NAME'(1,1) NE '(').GETPGM                      88150
&MACPLAB LR    R15,&NAME(1)                                      88150
         AGO   .BAL                                              88150
.*--------------------------------------------------------------------*
.*  SUBCALL INTERACTS WITH THE SERVLOAD MACRO.                        *
.*  WHEN A MODULE LOADED BY SERVLOAD IS REFERENCED HERE, THE L =A()   *
.*  IS REPLACED BY A LOAD FROM THE NAME USED BY SERVLOAD.             *
.*                                                                    *
.*--------------------------------------------------------------------*
.GETPGM  AIF   ('&NAME'(1,1) NE '/' AND '&NAME'(1,1) NE '*').GETLOAD
         MACPARM R15,&NAME                                      GP03264
         AGO   .BAL                                             GP03264
.GETLOAD ANOP  ,                                                 88150
&I       SETA  0             SCAN THROUGH SERVLOAD MODULES      GP03150
.LODLOOP AIF   (&I GE &SRVNMOD).SKIPLOD                         GP03150
&I       SETA  &I+1                                             GP03150
 AIF ('&SRVLMOD(&I)' NE '&NAME' AND '&SRVLDEL(&I)' NE '&NAME').LODLOOP
         MACPARM R15,&SRVLMOD(&I),OP=L                          GP03150
         AGO   .BALR                                            GP03150
.SKIPLOD AIF   ('&MODE' NE 'LA').LOAD                           GP03150
&MACPLAB LA    R15,&NAME     GET SUBROUTINE ADDRESS             GP03150
.LOAD    ANOP  ,                                                 88150
&MACPLAB L     R15,=A(&NAME)  GET SUBROUTINE ADDRESS             88150
.BAL     ANOP  ,                                                 88150
&MACPLAB SETC  ''                                                88150
.BALR    AIF   ('&LOP' EQ 'BALR').OLDBAL                        GP12162
         AIF   (NOT &MVSESA).OLDBAL                             GP12162
&MACPLAB &LOP  R14,R15       INVOKE IT                          GP00020
&MACPLAB SETC  ''                                               GP00020
         MEXIT ,                                                GP03041
.OLDBAL  ANOP  ,                                                GP12162
&MACPLAB BALR  R14,R15       INVOKE IT                          GP12162
&MACPLAB SETC  ''                                               GP12162
         MEXIT ,                                                GP03041
.MFL     AIF   ('&PARM' EQ '' AND '&NAME' NE '').OOPS           GP03041
&NM      DC    A&PARM REMOTE PARM LIST FOR SUBCALL              GP03041
         MEXIT ,                                                GP03041
.OOPS    ANOP  ,                                                GP03041
&NM      DC    A&NAME REMOTE PARM LIST FOR SUBCALL              GP03041
.MEND    MEND  ,                                                 88150
