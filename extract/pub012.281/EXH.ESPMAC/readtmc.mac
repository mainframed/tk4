         MACRO ,
&NM      READTMC &FUN,&PARM,&BUFNO=,&TEST=,&EXT=NO    UPDATED ON 81173
         GBLC  &MACPLAB
         GBLB  &TMC@EXT                                          81173
         LCLA  &I
&MACPLAB SETC  '&NM'         INIT LABEL
         AIF   ('&EXT' EQ 'NO').NTEXT                            81173
&TMC@EXT SETB  1                                                 81173
.NTEXT   AIF   ('&FUN' NE '').HAVEFUN                            81173
         MNOTE 8,'***** FUNCTION REQUIRED : OPEN/CLOSE/VOL/DSN'
         MEXIT ,
.HAVEFUN AIF   ('&FUN' EQ 'OPEN' OR '&BUFNO' EQ '').BUFOK
         MNOTE 0,'***** BUFNO VALID FOR ''OPEN'' ONLY; IGNORED'
.BUFOK   AIF   ('&FUN' EQ 'CLOSE').DOC2
&I       SETA  1
         AIF   ('&FUN' EQ 'OPEN').DOC0
&I       SETA  2
         AIF   ('&FUN' EQ 'VOL').DOC0                            93025
&I       SETA  3
         AIF   ('&FUN' EQ 'DSN' OR '&FUN' EQ 'DSNB').DOC0
&I       SETA  4                                                 93025
         AIF   ('&FUN' EQ 'TMC' OR '&FUN' EQ 'RAW').DOC0         93025
         MNOTE 8,'***** FUNCTION &FUN INVALID; USE OPEN/CLOSE/VOL/DSN'
         MEXIT ,
.DOC0    AIF   ('&PARM' EQ '').ZR1
         MACPARM R1,&PARM    LOAD BUFFER ADDRESS (BXLE FOR OPEN)
         AGO   .DOC1
.ZR1     MACPARM R1,0        CLEAR R1 (LOCATE MODE)
.DOC1    AIF   (&I NE 1).DOC2
         AIF   ('&BUFNO' EQ '' OR '&BUFNO' EQ '0').DOC2
&MACPLAB ICM   R1,8,=AL1(&BUFNO)  LOAD NO. OF BUFFERS
&MACPLAB SETC  ''
.DOC2    MACPARM R0,&I       SET FUNCTION CODE
         AIF   (&TMC@EXT).DOEXT                                  81173
&MACPLAB L     R15,=V(@TMSREAD)
         AGO   .EXTCOM                                           81173
.DOEXT   ANOP  ,                                                 81173
&MACPLAB L     R15,@TMSREAD
.EXTCOM  BALR  R14,R15                                           81173
         AIF   ('&TEST' EQ '' OR '&TEST' EQ 'NO').MEND
         CH    R15,=H'4'     TEST RETURN CODE
.MEND    MEND  ,
