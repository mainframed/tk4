         MACRO ,
&NM      XDBT  &LBL,&TEXT=,&REGS=,&HEX=,&MODE=S,      ADDED ON 2002357 *
               &CTEXT=,&PACK=,                                         *
               &COUNT=,&CALL=DYN,&TEST=NO,                             *
               &LIST=
         GBLB  &MVSSP,&MVSXA,&MVSESA                            GP06354
.**********************************************************************
.*                                                                    *
.*  THIS MACRO INVOKES EXPANDS A DBT (DEBTRACE) REQUEST ROUTED VIA    *
.*  EXORSPIE (DEBUG PROGRAM EXORCIST) TO DEBTRACE.                    *
.*                                                                    *
.*  WHEN TEST=YES IS SPECIFIED, THE 'DEBUG' OPTION FLAG IS CHECKED    *
.*    AND THE REQUEST IS ISSUED ONLY WHEN THE FLAG IS ONE.            *
.*                                                                    *
.*  OTHERWISE THE &SYSPARM (=DEBUG) GLOBAL IS TESTED. IF NOT THERE,   *
.*    THEN EXPANSION IS BYPASSED.                                     *
.*                                                                    *
.**********************************************************************
.*  MAINTENANCE:                                                      *
.*                                                                    *
.*                                                                    *
.**********************************************************************
         GBLC  &MACPLAB,&ZZZDBTW
&ZZZDBTW SETC  'EXHBSQSP'    WORK AREA NAME GLOBAL (NOT USED)
&MACPLAB SETC  '&NM'
         LCLC  &LAB
         AIF   ('&TEST' EQ 'YES').TEST
         AIF   ('&SYSPARM' NE 'DEBUG').SKIPDBT
         AGO   .PROCEED
.*
.TEST    MACPARM OPTS,DEBUG,OP=TM  RUNNING UNDER EXORCISE ?
&LAB     SETC  'XDBT'.'&SYSNDX'
         BZ    &LAB          NO; BYPASS TRACE
.*
.PROCEED AIF   (&MVSSP).USEDBT     ANYTHING AFTER MVS 3.8J?     GP06354
&MACPLAB DBO   &LBL,TEXT=&TEXT,                                        *
               REGS=&REGS,                                             *
               HEX=&HEX,                                               *
               CTEXT=&CTEXT,                                           *
               PACK=&PACK,                                             *
               LIST=&LIST,                                             *
               MODE=S,                                                 *
               CALL=TRC
         AGO   .CLRLAB                                          GP06354
.USEDBT  ANOP  ,                                                GP06354
&MACPLAB DBT   &LBL,TEXT=&TEXT,                                        *
               REGS=&REGS,                                             *
               HEX=&HEX,                                               *
               CTEXT=&CTEXT,                                           *
               PACK=&PACK,                                             *
               LIST=&LIST,                                             *
               MODE=S,                                                 *
               CALL=TRC
.CLRLAB  ANOP  ,                                                GP06354
&MACPLAB SETC  ''
.SKIPDBT MACPARM MODE=LBL    EXPAND LABEL IF NEEDED
.EXPDONE AIF   ('&LAB' EQ '').MEND
&LAB     DS    0H
.MEND    MEND  ,
