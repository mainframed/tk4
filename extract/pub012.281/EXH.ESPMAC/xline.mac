         MACRO ,             LINE MODE BUFFER INTERFACE     NEW GP08164
&NM      XLINE &OP1,&OP2,&FUN=MOVE,&OPT=
         GBLC  &MACPLAB,&MACQSTR
         GBLB  &MACQUOT
         GBLA  &MACPLEN
.**********************************************************************
.**                                                                  **
.**   XLINE PROVIDES LINE AT A TIME, AND TEXT FRAGMENT OUTPUT        **
.**     SERVICES.  CALLS EWXSPLIN IN EXHBWORK, RETURNS ON R14        **
.**     WITH NEW ADDRESS IN R1, AND RESIDUAL SPACE IN R0.            **
.**     R15:2 HAS FUNCTION    R15:3 HAS OPTION BITS                  **
.**     R0 HAS LENGTH OR 0                                           **
.**     R1 HAS TEXT ADDRESS                                          **
.**                                                                  **
.**   FUN=COL AND FUN=HIG ADD COLOR AND HIGH-LIGHTING, BUT ALSO      **
.**     REQUIRE SPECIAL HANDLING FOR BUFFER WRITES (I.E., XPOUT,     **
.**     XWRITE, ETC. WILL NOT WORK). BUFFER MUST BE BLANKED AFTER    **
.**     USE, PRIOR TO BRANCH TO AN EXC ROUTINE. ARGUMENT IN R1.      **
.**                                                                  **
.**********************************************************************
&MACPLAB SETC  '&NM'
         LCLC  &F,&L,&C
         LCLA  &FC,&OC,&I,&J,&K   R15:2 OPTION  R15:3 FUNCTION CODE
         LCLB  &OC1,&OC2,&OC3,&OC4,&OC5,&OC6,&OC7,&OC8  OPTION BITS
.*       GET FUNCTION CODE
&L       SETC  'L'''
&F       SETC  '&FUN'.'   '
&F       SETC  '&FUN'(1,3)   USE SHORT FORM
&J       SETA  N'&OPT        NUMBER OF OPTION ENTRIES           GP08306
&I       SETA  0                                                GP08306
&K       SETA  0                                                GP08306
.OPLOOP  AIF   (&I GE &J).OPDONE                                GP08306
&I       SETA  &I+1                                             GP08306
&C       SETC  '&OPT(&I)'                                       GP08306
         AIF   ('&C' EQ '').OPLOOP          IGNORE NULL         GP08306
&K       SETA  &K+1                         COUNT OPTIONS       GP08306
&OC1     SETB  ('&C' EQ 'JOIN' OR &OC1)     ABUT TERMS
&OC1     SETB  ('&C' EQ 'ABUT' OR &OC1)     ABUT TERMS          GP13279
&OC2     SETB  ('&C' EQ 'NL'   OR &OC2)     LAST/ONLY ITEM ON LINE
&OC3     SETB  ('&C' EQ 'TRIM' OR &OC3)     TRIM TRAILING BLANKS
&OC6     SETB  ('&C' EQ 'ASIS' OR &OC6)     INHIBIT TRANSLATE   GP10245
&OC8     SETB  ('&C' EQ 'ROLL'   OR &OC8)   INIT: SET ROLL MODE GP08306
&OC8     SETB  ('&C' EQ 'SCROLL' OR &OC8)   INIT: SET ROLL MODE GP08306
         AGO   .OPLOOP
.OPDONE  ANOP  ,
&I       SETA  (&OC1+&OC2+&OC3+&OC4+&OC5+&OC6+&OC7+&OC8)
         AIF   (&K EQ &I).OPOK
         MNOTE 4,'XLINE: UNRECOGNIZED OPT VALUE; IGNORED'
.OPOK    AIF   ('&F' EQ 'INI').FUNINIT      INITIALIZE
         AIF   ('&F' EQ 'RES').FUNINIT      RESET
&FC      SETA  1
         AIF   ('&F' EQ 'CLR').FUNINBL      INIT WITH BUFFER CLEAR
         AIF   ('&F' EQ 'CLE').FUNINBL      INIT WITH BUFFER CLEAR
&FC      SETA  2
         AIF   ('&F' EQ 'TIT').FUNHEAD      WRITE TITLE/HEADER
         AIF   ('&F' EQ 'HEA').FUNHEAD
&FC      SETA  3
         AIF   ('&F' EQ 'LIN').FUNCLIN      CONDITIONAL LINE UP
         AIF   ('&F' EQ 'CNL').FUNCLIN      CONDITIONAL LINE UP
         AIF   ('&F' EQ 'CLN').FUNCLIN      CONDITIONAL LINE UP
&FC      SETA  4
         AIF   ('&F' EQ 'ULN').FUNULIN      UNCONDITIONAL LINE
         AIF   ('&F' EQ 'UNL').FUNULIN      UNCONDITIONAL LINE
         AIF   ('&F' EQ 'NL').FUNULIN       NEW LINE
&FC      SETA  5
         AIF   ('&F' EQ 'MOV').FUNMOVE      MOVE DATA, LEN IN R0
&FC      SETA  6
         AIF   ('&F' EQ 'BCO').FUNMOVB      MOVE B-CON DEFINITION
&FC      SETA  7
         AIF   ('&F' EQ 'FIT').FUNLOC       RETURN R1 TO FIT
         AIF   ('&F' EQ 'LOC').FUNLOC
&FC      SETA  8
         AIF   ('&F' EQ 'HEX').FUNMOVE   'MOVE' WITH HEX UNPACK
&FC      SETA  9
         AIF   ('&F' EQ 'ROL').FUNROLL                          GP08306
&FC      SETA  10                                               GP11221
         AIF   ('&F' EQ 'INT').FUNMINT   FORMAT INTEGER IN R1   GP11221
&FC      SETA  11                                               GP13279
         AIF   ('&F' EQ 'COL').FUNCOL    ADD COLOR TO SCREEN    GP13279
&FC      SETA  12                                               GP13279
         AIF   ('&F' EQ 'HIG').FUNHIGH   ADD HIGH-LIGHTING      GP13279
    MNOTE 8,'XLINE: FUNCTION &FUN UNDEFINED'
         MACPARM MODE=LBL    EXPAND LABEL ONLY
         MEXIT ,
.FUNINIT AIF   ('&OPT' EQ 'CLEAR' OR '&OPT' EQ 'BLANK').FUNINBL
         MACPARM R0,&OP1,NULL=0    INDENTATION VALUE
         AGO   .CALL
.FUNINBL ANOP  ,
&FC      SETA  1             INIT WITH SCREEN BLANKING
         MACPARM R0,&OP1,NULL=0    INDENTATION VALUE
         AGO   .CALL
.FUNHEAD MACQOLIT &OP1       LOOK FOR QUOTED STRING
         AIF   (NOT &MACQUOT).FUNHEAP
         MACPARM R1,&MACQSTR    PASS LITERAL
         MACPARM R0,&OP2,NULL=&MACPLEN  USE USER'S OR CALC. LEN
         AGO   .CALL
.FUNHEAP MACPARM R0,&OP2,NULL=&L&OP1   LENGTH OR 0 FOR B-CON
         MACPARM R1,&OP1     ADDRESS OF SCREEN TITLE
         AGO   .CALL
.FUNCLIN ANOP  ,             FALL THROUGH
.FUNULIN AIF   ('&OP1' EQ '').CALL
&OC1     SETB  1             NEW INDENT VALUE SET
         MACPARM R0,&OP1     PASS TO CODE
         AGO   .CALL
.FUNMOVE MACQOLIT &OP1       LOOK FOR QUOTED STRING
         AIF   (NOT &MACQUOT).FUNTEXT
         MACPARM R1,&MACQSTR    PASS LITERAL
         MACPARM R0,&OP2,NULL=&MACPLEN  USE USER'S OR CALC. LEN
         AGO   .CALL
.FUNMINT MACPARM R0,&OP2,NULL=0   ZERO MEANS ACTUAL LENGTH      GP11221
         MACPARM R1,&OP1,OP=L     VALUE OF INTEGER ITEM         GP11221
         AGO   .CALL                                            GP11221
.FUNTEXT MACPARM R0,&OP2,NULL=&L&OP1  LENGTH OR L'ADDR
         MACPARM R1,&OP1     ADDRESS OF TEXT LINE OR ITEM
         AGO   .CALL
.FUNMOVB ANOP  ,
         MACPARM R1,&OP1     ADDRESS OF TEXT LINE OR ITEM
         AGO   .CALL
.FUNLOC  ANOP  ,
         MACPARM R0,&OP1     LENGTH TO RESERVE
         AGO   .CALL
.*
.FUNCOL  AIF   (K'&OP1 LT 3).FUNCOLP                            GP13279
&K       SETA  K'&OP1                                           GP13279
   AIF   ('&OP1'(1,1) NE '(' OR '&OP1'(2,1) EQ '(').FUNCOLP     GP13279
   AIF   ('&OP1'(&K,1) NE ')' OR '&OP1'(&K-1,1) EQ ')').FUNCOLP GP13279
         MACPARM R1,&OP1,OP=LR                                  GP13279
         AGO   .CALL                                            GP13279
.FUNCOLP ANOP  ,                                                GP13279
&F       SETC  '&OP1'.'   '                                     GP13279
&F       SETC  '&F'(1,2)     USE ABBREVIATIONS                  GP13279
&I       SETA  0                                                GP13279
.COLLOOP ANOP  ,                                                GP13279
&I       SETA  &I+1                                             GP13279
.*                NULL BLUE RED PINK GREEN TURQUOISE YELLOW WHITE
         AIF   ('&F' EQ 'NUBLREPIGRTUYEWH'(2*&I-1,2)).COLHAVE   GP13279
.*                NULL BLUE RED MAGENTA GREEN CYAN YELLOW WHITE
         AIF   ('&F' EQ 'NUBLREMAGRCYYEWH'(2*&I-1,2)).COLHAVE   GP13279
         AIF   (&I LT 8).COLLOOP                                GP13279
         MNOTE 8,'XLINE: &OP1 IS NOT A RECOGNIZED COLOR'        GP13279
         MEXIT ,                                                GP13279
.COLHAVE MACPARM R1,&I-1                                        GP13279
         AGO   .CALL                                            GP13279
.*
.FUNHIGH AIF   (K'&OP1 LT 3).FUNHIGP                            GP13279
&K       SETA  K'&OP1                                           GP13279
   AIF   ('&OP1'(1,1) NE '(' OR '&OP1'(2,1) EQ '(').FUNHIGP     GP13279
   AIF   ('&OP1'(&K,1) NE ')' OR '&OP1'(&K-1,1) EQ ')').FUNHIGP GP13279
         MACPARM R1,&OP1,OP=LR                                  GP13279
         AGO   .CALL                                            GP13279
.FUNHIGP ANOP  ,                                                GP13279
&F       SETC  '&OP1'.'   '                                     GP13279
&F       SETC  '&F'(1,2)     USE ABBREVIATIONS                  GP13279
&I       SETA  0                                                GP13279
.HIGLOOP ANOP  ,                                                GP13279
&I       SETA  &I+1                                             GP13279
.*                  NULL BLINK REVERSE UNDERSCORE
         AIF   ('&F' EQ 'NUBLREUN'(2*&I-1,2)).HIGHAVE           GP13279
         AIF   (&I LT 3).HIGLOOP                                GP13279
         MNOTE 8,'XLINE: &OP1 IS NOT A RECOGNIZED HIGHLIGHT'    GP13279
         MEXIT ,                                                GP13279
.HIGHAVE MACPARM R1,&I-1                                        GP13279
         AGO   .CALL                                            GP13279
.*
.FUNROLL ANOP  ,                  ROLL MODE NOT DONE YET
.*
.CALL    ANOP  ,
&OC    SETA  128*&OC8+64*&OC7+32*&OC6+16*&OC5+8*&OC4+4*&OC3+2*&OC2+&OC1
         MACPARM R15,&FC*256+&OC  SET FUNCTION AND OPTION CODES
         MACPARM R14,EXWSPLIN,OP=L   LOAD SERVICE ROUTINE ADDRESS
         BALR  R14,R14       INVOKE IT
         MEND  ,
