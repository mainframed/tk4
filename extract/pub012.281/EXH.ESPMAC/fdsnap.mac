         MACRO ,
&NM      FDSNAP &ADR,&HLN,&OPTS,&BASE=,&LEN=            ADDED ON 83331
.*
.*   FDSNAP IS USED IN AN FDLIST TO DUMP MEMORY.
.*     LIST    (DEFAULT) PRINTS TEXT ONLY (PERIOD FOR UNPRINTABLES)
.*     HEX     FORMATS IN HEXADECIMAL
.*     DUAL    FORMATS HEX ON LEFT, AND TEXT ON RIGHT
.*     VERT    FORMATS THREE LINES: TEXT/ZONES/NUMERICS
.*
.*     ABS     DISPLAYS MEMORY ADDRESS (DEFAULT)
.*     NOABS   OMITS MEMORY ADDRESS
.*
.*     OFFSET  PRINTS OFFSET RELATIVE TO BASE= VALUE
.*     NOOFFSET  OMITS OFFSET VALUE
.*
.*     ANSI
.*     ASCII   CONVERT TEXT TO ASCII (IN OUTPUT LINE)
.*
.*
         GBLA  &FDCNTR
         LCLA  &T,&C,&FG1,&FG2,&I,&MAX
         LCLB  &NOP,&B0,&B1,&B2,&B3,&B4,&B5,&B6,&B7,&B8,&B9,&B10,&B11
         LCLB  &B12,&B13,&B14,&B15
         LCLC  &FDCHN,&OP,&RA,&RB,&LOP,&LVAL                    GP11288
&LOP     SETC  'AL'                                             GP11288
&LVAL    SETC  '&HLN'                                           GP11288
&FDCNTR  SETA  &FDCNTR+1
&FDCHN   SETC  'ZFD'.'&FDCNTR'
&C       SETA  &FDCNTR+1
&MAX     SETA  N'&SYSLIST
&I       SETA  3             FIRST OPTION
         AIF   (T'&LEN EQ 'O').NOLEN
&I       SETA  2             KEYWORD, NOT POSITIONAL, LENGTH
&B0      SETB  1             SET S-FORMAT LENGTH FIELD
&LVAL    SETC  '&LEN'                                           GP11288
.NOLEN   AIF   (T'&BASE EQ 'O').NOBASE
&B13     SETB  1
.NOBASE  AIF   ('&NM' EQ '').NONAME
&NM      EQU   *
.NONAME  AIF   ('&ADR' NE 'END' AND '&ADR' NE '*END').PROCESS
&FDCHN   DC    AL1(0)        END OF FD LIST
         MEXIT ,
.PROCESS AIF   (&I GT &MAX).CHECK
&OP      SETC  '&SYSLIST(&I)'
&I       SETA  &I+1
         AIF   ('&OP' EQ '').PROCESS
&B7      SETB  (&B7 OR ('&OP' EQ 'ASCII') OR ('&OP' EQ 'ANSI'))  83331
&B8      SETB  (&B8 OR ('&OP' EQ 'HEX') OR ('&OP' EQ 'VERT'))
&B9      SETB  (&B9 OR ('&OP' EQ 'DUAL') OR ('&OP' EQ 'VERT'))
&B8      SETB  (&B8 AND '&OP' NE 'LIST' AND '&OP' NE 'DUAL')
&B9      SETB  (&B9 AND '&OP' NE 'LIST' AND '&OP' NE 'HEX')
&B14     SETB  ((&B14 OR '&OP' EQ 'NOABS') AND '&OP' NE 'ABS')
&B15     SETB  ((&B15 OR '&OP' EQ 'OFFSET') AND '&OP' NE 'NOOFFSET')
         AGO   .PROCESS
.CHECK   ANOP  ,
&T       SETA  64*&NOP+29
         AIF   (K'&ADR LT 3).NORA                                85118
         AIF   ('&ADR'(1,1) NE '(').NORA                         85118
         AIF   ('&ADR'(K'&ADR,1) NE ')').NORA                    85118
         AIF   ('&ADR'(2,1) EQ '(').NORA                         85118
&RA      SETC  '0'           CHANGE R TO S FORMAT                85118
.NORA    AIF   (&B0).TESTL                                       85118
         AIF   (K'&HLN LT 3).TESTD                               85118
         AIF   ('&HLN'(1,1) NE '(').TESTD                        85118
         AIF   ('&HLN'(K'&HLN,1) NE ')').TESTD                   85118
         AIF   ('&HLN'(2,1) EQ '(').TESTD                        85118
&B0      SETB  1             SET S-FORMAT LENGTH FIELD          GP11288
&LOP     SETC  'SL'                                             GP11288
&LVAL    SETC  '0'.'&HLN'                                       GP11288
         AGO   .TESTD                                            85118
.TESTL   AIF   (K'&LEN LT 3).TESTD                               85118
         AIF   ('&LEN'(1,1) NE '(').TESTD                        85118
         AIF   ('&LEN'(K'&LEN,1) NE ')').TESTD                   85118
         AIF   ('&LEN'(2,1) EQ '(').TESTD                        85118
&LOP     SETC  'SL'                                             GP11288
&LVAL    SETC  '0'.'&LEN'                                       GP11288
.TESTD   ANOP  ,                                                GP11288
&FG1     SETA  128*&B0+64*&B1+32*&B2+16*&B3+8*&B4+4*&B5+2*&B6+&B7
&FG2     SETA  128*&B8+64*&B9+32*&B10+16*&B11+8*&B12+4*&B13+2*&B14+&B15
&FG1     SETA  &FG1*256+&FG2
         AIF   (&B13).BASED                                      85118
&FDCHN   DC    AL1(ZFD&C-*,&T),AL2(&FG1),SL2(&RA&ADR),&LOP.2(&LVAL)
         MEXIT ,
.BASED   AIF   (K'&BASE LT 3).BASES                              85118
         AIF   ('&BASE'(1,1) NE '(').BASES                       85118
         AIF   ('&BASE'(K'&BASE,1) NE ')').BASES                 85118
         AIF   ('&BASE'(2,1) EQ '(').BASES                       85118
&RB      SETC  '0'           CHANGE R TO S FORMAT                85118
.BASES   ANOP  ,                                                GP11288
&FDCHN   DC    AL1(ZFD&C-*,&T),AL2(&FG1),SL2(&RA&ADR),&LOP.2(&LVAL),SL2*
               (&RB&BASE)                                       GP11288
         MEND  ,
