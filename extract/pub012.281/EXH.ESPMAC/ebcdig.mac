         MACRO ,
&NM      EBCDIG &VAL,&LEN
.**********************************************************************
.*
.*   THIS MACRO CONVERTS AN ABSOLUTE NUMBER, VARIABLE, OR EXPRESSION
.*   INTO AN EBCDIC CONSTANT OF LENGTH &LEN (NO WARNING ON OVERFLOW).
.*   IT'S USEFUL FOR GENERATING CONTROL CARDS, ESPECIALLY FOR SORT.
.*
.*   FOR EXAMPLE:
.*     X EQU 15
.*       EBCDIG X,3   EXPANDS:
.*       DC    AL1(240+0,240+1,240+5)  (MORE OR LESS)  C'015'
.*
.**********************************************************************
         LCLA  &I,&J,&K
&I       SETA  9
&J       SETA  1000000000
&K       SETA  100000000
         AIF   (T'&VAL EQ 'O').BADVAL
         AIF   (T'&LEN NE 'N').BADLEN
         AIF   ('&NM' EQ '').NODC
&NM      DC    0CL(&LEN)' '
.NODC    AIF   (&LEN GT 9).BADLEN
         AIF   (&LEN GT 0).OKLEN
.BADLEN  MNOTE 8,'LENGTH (OPERAND 2) IS UNACCEPTABLE: &LEN '
         MEXIT ,
.BADVAL  MNOTE 8,'VALUE (OPERAND 1) IS MISSING'
         MEXIT ,
.OKLEN   AIF   (&LEN LT &I).NOTLEN
         DC    AL1(240+((&VAL)/&K-((&VAL)/&J)*10))
.NOTLEN  AIF   (&I EQ 1).MEND
&I       SETA  &I-1
&J       SETA  &K
&K       SETA  &K/10
         AGO   .OKLEN
.MEND    MEND  ,
