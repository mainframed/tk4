         MACRO ,
&NM      XCLMAKE &SIZE,&SP=1,&TYPE=,&NAME=,&QUAL=
.*
.*   GET STORAGE FOR AN XCL CONTROL BLOCK
.*   PARM1 - TOTAL SIZE TO GET (ALLOW 16 FOR PREFIX)
.*   SP    - SUBPOOL TO USE (IF NOT DEFAULTED)
.*   TYPE  - ENTRY TYPE (0-STORAGE; 1-DASD BLOCK)
.*   NAME  - ADDRESS OF THREE BYTE NAME, OR QUOTED
.*   QUAL  - OPTIONAL DISAMBIGUATION - LENGTH=4
.*
.*   ERROR CODE IN R15 FROM GETMAIN, ELSE
.*   R1    - BLOCK ADDRESS
.*
         LCLC  &ZZ,&Q
&ZZ      SETC  'ZZ'.'&SYSNDX'
&NM      STM   R0,R7,12(R13)      SAVE SOME REGISTERS
         MACPARM R0,&SIZE
         MACPARM R3,(R0),OP=LR,OPR=LR
         GETMAIN RC,LV=(0),SP=&SP
         LTR   R15,R15
         BNZ   &ZZ.X
         LR    R2,R1
         SLR   R5,R5         NULL PADDING & LENGTH
         MVCL  R2,R4         CLEAR THE GOTTEN STORAGE
         MACPARM R0,4(,R1),OP=ST
         MACPARM 4(R1),&SP,OP=MVI,NULL=SKIP
         MACPARM 8(R1),&TYPE,OP=MVI,NULL=SKIP
         AIF   ('&NAME'(1,1) NE '''').VARN
&Q       SETC  '=C'
.VARN    MACPARM 9(3,R1),&Q&NAME,OP=MVC,NULL=SKIP
         MACPARM 12(4,R1),&QUAL,OP=MVC,NULL=SKIP
&ZZ.X    LM    R2,R7,20(R13)      0 OR ADDRESS IN R15
.MEND    MEND  ,
