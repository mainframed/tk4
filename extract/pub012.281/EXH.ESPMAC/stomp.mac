         MACRO ,                                                GP02318
&NM      STOMP &IV=,&ADDR=,&VAL=,&ADDRV=                        GP10236
         GBLA  &ZZSTMP#                                         GP02318
         GBLC  &ZZSTMP@,&ZZSTMPV                                GP02318
.*
.*   STOMP IS USED FOR DEBUGGING TO SET A FOOTPRINT.
.*   THE FIRST PHYSICAL OCCURRENCE IN AN ASSEMBLY REQUIRES
.*   AND ADDR= OPERAND TO SPECIFY THE BYTE TO RECEIVE THE FOOTPRINT
.*   VALUE. THE IV PARAMETER SETS AN OPTION (RE)STARTING COUNT.
.*
.*   THE OPTIONAL VAL PARAMETER MAY BE USED TO SAVE UP TO 8 BYTES
.*   OF DATA IN ADDRV                                           GP10236
.*
.*
.*
         AIF   ('&IV' EQ '').NONUMB                             GP02318
&ZZSTMP# SETA  &IV                                              GP02318
.NONUMB  AIF   ('&ADDR' EQ '').NONADD                           GP02318
&ZZSTMP@ SETC  '&ADDR'                                          GP02318
.NONADD  AIF   ('&ADDRV' EQ '').NONVAD                          GP10236
&ZZSTMPV SETC  '&ADDRV'                                         GP10236
.NONVAD  ANOP  ,                                                GP02318
&ZZSTMP# SETA  &ZZSTMP#+1                                       GP02318
&NM      MVI   &ZZSTMP@,&ZZSTMP#                                GP02318
 MNOTE *,'***************** STOMP &ZZSTMP# ********************'
         AIF   ('&ZZSTMPV' EQ '' OR '&VAL' EQ '').MEND          GP10236
         AIF   (K'&VAL LT 3).MVC                                GP10236
         AIF   ('&VAL'(1,1) NE '(' OR '&VAL'(2,1) EQ '(').MVC   GP10236
      AIF   ('&VAL'(K'&VAL,1) NE ')' OR '&VAL'(K'&VAL-1,1) EQ ')').MVC
         ST    &VAL(1),&ZZSTMPV                                 GP10236
         MEXIT ,                                                GP10236
.MVC     MVC   &ZZSTMPV,&VAL                                    GP10236
.MEND    MEND  ,                                                GP02318
