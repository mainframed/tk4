         MACRO ,
&NM      SERVWORK &ID,&LEN,&REG=R13,&OPT=GET,&OREG=R9,&BASE=R6,&PREG=
.*--------------------------------------------------------------------*
.*  SERVWORK IS USED BY RESIDENT FUNCTIONS (@SERVICE, @PRINTER, ETC.) *
.*  TO MANAGE WORK AREAS.                                             *
.*    OPT=GET LOCATES A WORK AREA FOR THE FUNCTION, OR OBTAINS IT     *
.*      (R15=0 - OLD ADDRESS OF FSA HEADER IN R1; USER ADDRESS IN R0) *
.*      (R15=4 - R1, R0 SAME; STORAGE OBTAINED ON THIS CALL)          *
.*                                                                    *
.*    OPT=SHARE OR OPT=SHR FUNCTIONS LIKE OPT=GET, EXCEPT THAT A WORK *
.*      ARE OBTAINED PREVIOUSLY BY A HIGHER TASK WILL BE USED.        *
.*                                                                    *
.*    OPT=RELEASE FREES THE WORK AREA (LEN PARAMETER EITHER WORK AREA *
.*      ADDRESS OR ZERO)                                              *
.*                                                                    *
.*    ID - RESERVED FOUR BYTE FUNCTION NAME (E.G., '@PAR'), OR        *
.*      ADDRESS OF FOUR-BYTE NAME                                     *
.*    LEN - FSALEN (FSAPFXL+USER LENGTH)                              *
.*--------------------------------------------------------------------*
         GBLC  &MACPLAB
         GBLC  &ZZZLNM(128)
         GBLB  &ZZZLFG,&MVSESA
         GBLA  &ZZZLNO
         LCLC  &WHO,&ARG
         LCLA  &I,&N
         LCLC  &TAG                                             GP05019
&TAG     SETC  'S@'.'&SYSNDX'                                   GP05019
&WHO     SETC  'SUBSERVW'    GET/LOCATE STORAGE
&MACPLAB SETC  '&NM'
         AIF   (NOT &MVSESA).OLDSTYL                            GP05019
         AIF   ('&OPT' NE 'SHR' AND '&OPT' NE 'SHARE').UNSHARE  GP03312
&WHO     SETC  'SUBSERVS'    GET/LOCATE SHARED STORAGE          GP03312
.UNSHARE AIF   ('&OPT' NE 'RELEASE').GET                        GP03312
&WHO     SETC  'SUBSERVZ'    RELEASE STORAGE
.GET     AIF   (&ZZZLFG).INITDEF
&ZZZLFG  SETB  1             SHOW DEFINED
.*--------------------------------------------------------------------*
.*  DEFINE MODULE<->ORDINAL CORRESPONDENCE FOR MAPTSA WORK AREA       *
.*    LISTED MODULES MAY OBTAIN A WORK AREA USING THE SERVWORK MACRO  *
.*    WITH FAST ACCESS (FIXED OFFSET IN TSA). APPROXIMATELY SAME AS   *
.*    MODULES IN @SERVICE LOADLIST                                    *
.*--------------------------------------------------------------------*
.*ZZLNM(000) SETC '@SERVICE'   MUST BE FIRST ONE BY DESIGN
&ZZZLNM(001) SETC '@SCREENS'   MUST BE FIRST TWO BY DESIGN
&ZZZLNM(002) SETC '@PRINTER'   MUST BE FIRST TWO BY DESIGN
&ZZZLNM(003) SETC '@BANDAID'
&ZZZLNM(004) SETC '@CATREAD'
&ZZZLNM(005) SETC '@CVLREAD'
&ZZZLNM(006) SETC '@DCBEXIT'
&ZZZLNM(007) SETC '@FDRREAD'
&ZZZLNM(008) SETC '@FORMATS'
&ZZZLNM(009) SETC '@INPREAD'
&ZZZLNM(010) SETC '@MESSAGE'
&ZZZLNM(011) SETC '@OBTAINS'
&ZZZLNM(012) SETC '@PARSER '
&ZZZLNM(013) SETC '@PROTECS'
&ZZZLNM(014) SETC '@SRVJES2'
&ZZZLNM(015) SETC '@TMSREAD'
&ZZZLNM(016) SETC '@VOLREAD'
&ZZZLNM(017) SETC '@WRITER '
&ZZZLNM(018) SETC 'DEBTRACE'
&ZZZLNM(019) SETC 'LEXVOLT '
&ZZZLNM(020) SETC 'PGMTRACE'
&ZZZLNM(021) SETC 'SUBCAT  '
&ZZZLNM(022) SETC 'SUBCOMP '
&ZZZLNM(023) SETC 'SUBCOMP '
&ZZZLNM(024) SETC 'SUBTIMER'
&ZZZLNM(025) SETC 'SUBWTO  '
&ZZZLNO  SETA  25                     NUMBER OF ENTRIES
.INITDEF AIF   ('&OPT' EQ 'EXPAND').MAKEDC
&ARG     SETC  '&ID'
         AIF   ('&ID'(1,1) NE '''').LOOKED
&N       SETA  K'&ID
&ARG     SETC  '&ID'(2,&N-2)
.ARGNQ   ANOP  ,
&ARG     SETC  '&ARG'.'        '
&ARG     SETC  '&ARG'(1,4)
.LOOKLUP AIF   (&I GE &ZZZLNO).LOOKED
&I       SETA  &I+1
         AIF   ('&ZZZLNM(&I)'(1,4) NE '&ARG').LOOKLUP
         MACPARM R0,&I
         AGO   .DONLIT
.LOOKED  AIF   ('&ID'(1,1) NE '''').NOTLIT
         MACPARM R0,=CL4&ID
         AGO   .DONLIT
.NOTLIT  MACPARM R0,&ID
.DONLIT  AIF   ('&OPT' NE 'RELEASE').NEEDLEN
         MACPARM R1,&LEN,NULL=0   CLEAR IF NOT USER SPECIFIED
         AGO   .COMLEN
.NEEDLEN MACPARM R1,&LEN     LOAD THE WORK AREA LENGTH
.COMLEN  MACPARM R15,=V(&WHO),OP=L
         MACPARM R14,(R15),OPR=BALR
         AIF  ('&REG' EQ '' OR '&OPT' NE 'GET').MEND
         MACPARM &REG(1),(R0),OP=LR  LOAD SAVE AREA (FSAWORK+FSAPFXL)
         MEXIT ,
.OLDSTYL AIF   ('&OPT' EQ 'EXPAND').MEND
         AIF   ('&OPT' EQ 'SHR' OR '&OPT' EQ 'SHARE').OLDGET    GP05019
         AIF   ('&OPT' EQ 'RELEASE').OLDREL                     GP05019
.OLDGET  PUSH  USING                                            GP05019
.*    REGISTER USE DURING SETUP
.*    R1 - TEMP GETMAIN/OBTAIN RETURN
.*    R2 - CHAIN ELEMENT (WORK AREA) ID
.*    R3 - WORK AREA LENGTH
.*    R4 - CURRENT TCB
.*    R6 - NEW WORK AREA ADDRESS
.*    R7 - TEMP CHAIN CHASING
.*
&NM      LTCB  R4,USE=YES    GET MY TCB                         GP05019
&MACPLAB SETC  ''                                               GP05019
         AIF   ('&ID' NE '').OHVID                              GP05019
&WHO     SETC  '&SYSECT'(1,4)                                   GP05019
         L     R2,=CL4'&WHO'                                    GP05019
         AGO   .OCMID                                           GP05019
.OHVID   AIF   ('&ID'(1,1) NE '''').OICID                       GP05019
         ICM   R2,15,=CL4&ID                                    GP05019
         AGO   .OCMID                                           GP05019
.OICID   MACPARM R2,15,&ID,OP=ICM,MODE=THREE                    GP05019
.OCMID   L     R7,TCBFSA     POINT TO CHAIN HEAD                GP05019
         N     R7,=X'00FFFFFF'  CLEAN FOR AM31                  GP05019
         SR    R6,R6         CLEAR HIGH BYTE
         SR    R15,R15       PRESET RETURN CODE
&TAG.L   ICM   R6,7,1(R7)    LAST ENTRY ON CHAIN ?              GP05019
         BZ    &TAG.G        YES; GET A WORK AREA               GP05019
         USING FSAWORK,R6    DECLARE IT
         CL    R2,FSAID      SAME ID ?
         BE    &TAG.H        YES; USE IT                        GP05019
         LR    R7,R6         SWAP
         B     &TAG.L        TRY AGAIN                          GP05019
&MACPLAB SETC  '&TAG.G'
         AIF   ('&PREG' EQ '').ONEND                            GP05020
         MACPARM &PREG,(&PREG),OP=LTR,OPR=LTR,MODE=EVEN         GP05020
         BZ    &TAG.Z        NO STORAGE IF CLOSE CALL           GP05020
.ONEND   MACPARM R3,&LEN,NULL=FSALEN LN OF WORK AREA            GP05019
         STORAGE OBTAIN,LENGTH=(R3),COND=YES,LOC=BELOW          GP05019
         BXLE  R15,R15,&TAG.W  CLEAR IT                         GP05019
&TAG.Z   LM    R14,R12,12(R13)  ERROR IN PROCESSING OR ENVIRONMENT
         LA    R15,16        SET MAJOR ERROR
         SLR   R0,R0         INDICATE ENVIRONMENT (PGM ERROR)
         BSM   0,R14         RETURN                             GP99026
         SPACE 1
         DROP  R6
         USING FSAWORK,R7
&TAG.W   LR    R6,R1         COPY THIS ONE TO PERMANENT REGISTER
         LR    R14,R1        COPY FOR MVCL                      GP05019
         LR    R15,R3                                           GP05019
         SR    R1,R1                                            GP05019
         MVCL  R14,R0        CLEAR GOTTEN AREA                  GP05019
         MVC   FSALINK-FSAWORK(4,R6),FSALINK  CHAIN OLD AREA    GP05019
         ST    R6,FSALINK    CHAIN THIS AHEAD OF OLD ONE        GP05019
         DROP  R7
         USING FSAWORK,R6
         ST    R2,FSAID      SET ID OF THIS AREA
         ST    R3,FSASPLEN   SAVE GETMAIN LENGTH
         ST    R4,FSATCB     AND TCB
         LA    R15,4         FLAG NEW AREA                      GP05019
&MACPLAB SETC  '&TAG.H'                                         GP05019
         MACPARM &BASE,(R6)  PUT INTO USER'S REGISTER           GP05019
         AIF   ('&REG' EQ '' OR '&OREG' EQ '').ONSAVE           GP05019
         MACPARM &OREG,(&REG) PRESERVE OLD SAVE                 GP05019
         LA    &REG,SAVEAREA  HARD-CODED NAME                   GP05019
         ST    &REG,8(,&OREG) CHAIN                             GP05019
         ST    &OREG,4(,&REG) LINK                              GP05019
.ONSAVE  MACPARM MODE=LBL                                       GP05019
         POP   USING         SKIP ALL BUT BLANKS                GP05019
         MEXIT ,
.OLDREL  ANOP  ,
         PUSH  USING                                            GP05019
&NM      LA    &OREG,0(,R13)   GET CLEAN SAVE AREA              GP05019
         SH    &OREG,=AL2(SAVEAREA-FSAWORK)  BACK TO BEGINNING  GP05019
         USING FSAWORK,&OREG                                    GP05019
         L     R13,4(,R13)   GET CALLER'S SAVE AREA             GP05019
         LM    R2,R3,FSASPLEN  GET LENGTH AND TCB BACK
         L     R3,TCBFSA-TCB(,R3)  POINT TO CHAIN HEAD
         N     R3,=X'00FFFFFF'                                  GP05019
&TAG.L   CLM   &OREG,7,1(R3)    POINTER TO OUR AREA ?           GP05019
         BE    &TAG.F        YES; UNCHAIN                       GP05019
         ICM   R3,7,1(R3)    GET NEXT ENTRY
         BNZ   &TAG.L        CHECK IT                           GP05019
         ABEND 222,DUMP      SHOULD NEVER HAPPEN ?
         SPACE 1
&TAG.F   MVC   0(4,R3),FSALINK  UNCHAIN OURS
         LR    R15,R2        GET SUBPOOL AND LENGTH             GP05019
         SRL   R15,24        FIX SUBPOOL                        GP05019
         N     R0,=X'00FFFFFF'   DELETE SUBPOOL                 GP05019
         STORAGE RELEASE,ADDR=(&OREG),LENGTH=(R2),SP=(15),COND=YES
         LM    R14,R12,12(R13)    RELOAD CALLER'S REGS          GP05019
         SR    R15,R15       SET GOOD RETURN                    GP05019
         BSM   0,R14         RETURN                             GP99026
         POP   USING
         MEXIT ,
.MAKEDC  ANOP  ,
&NM      DC    &ZZZLNO.D'0'  FIXED OFFSET WORK AREA POINTERS
         DC    (64-&ZZZLNO)D'0'   FUTURE EXPANSION
.MEND    MEND  ,
