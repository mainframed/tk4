         MACRO ,
&NM      MVCLIT &TO,&FRO,&ULEN
.**********************************************************************
.*                                                                    *
.*    MOVE DATA (LITERAL) FROM A FIELD TO A FIELD USING THE           *
.*      LENGTH OF THE SECOND OPERAND                                  *
.*                                                                    *
.**********************************************************************
         GBLA  &MACPLEN      RETURN SIGNIFICANT LENGTH OF STRING
         GBLB  &MACPNUL      TRUE IF NULL STRING
         GBLB  &MACQUOT      TRUE IF ORIGINAL WAS QUOTED
         GBLB  &MACPERR      TRUE IF ERROR
         GBLC  &MACQSTR      RETURN QUOTED STRING
         LCLA  &K
         LCLC  &C,&D,&L
       MACQOLIT &FRO         SEE WHAT WE HAVE FOR SOURCE
&D       SETC  '&MACPLEN'
         AIF   (NOT &MACPNUL).QERR
         MNOTE 8,'MVCLIT: SOURCE IS REQUIRED'
         AGO   .FRODO
.QERR    AIF   (NOT &MACPERR).FRODO
         MNOTE 8,'MVCLIT: SOURCE IS MALFORMED'
.FRODO   AIF   (&MACQUOT).QDEST        QUOTED ?
&L       SETC  'L'''
&D       SETC  '&L'.'&FRO'             NO; USE LENGTH ATTRIBUTE
.QDEST   AIF   (T'&TO NE 'O').FRQREG   CHECK FOR REGISTER FORM
         MNOTE 8,'MVCLIT: DESTINATION REQUIRED'
         MEXIT ,
.FRQREG  AIF   (T'&ULEN EQ 'O').CMPLEN
&D       SETC  '&ULEN'
.CMPLEN  ANOP  ,
&K       SETA  K'&TO
         AIF   (&K LT 3).TOADD         DO AS ADDRESS FORM
         AIF   ('&TO'(1,1) NE '(' OR '&TO'(&K,1) NE ')').TOADD
         AIF   ('&TO'(2,1) EQ '(' OR '&TO'(&K-1,1) EQ ')').TOADD
&C       SETC  '&TO'(2,&K-1)           STRIP (
&NM      MVC   0(&D,&C,&MACQSTR
         MEXIT ,
.TOADD   ANOP  ,
&NM      MVC   &TO.(&D),&MACQSTR
.MEND    MEND
