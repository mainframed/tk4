         MACRO ,
&NM      #TGET &ADDR,&LEN,&NOTSO=WTOR                    NEW ON GP12313
         GBLC  &MACPLAB
.*--------------------------------------------------------------------*
.*   #TGET IS A SIMPLE TGET REPLACEMENT. IT INVOKES SUBTGET TO ISSUE  *
.*     EITHER A GETLINE (FOR A TSO CALLER) OR A WTOR (BATCH).         *
.*                                                                    *
.*   USER PASSES BUFFER ADDRESS IN R1, AND MAX LENGTH IN R0           *
.*   NOTSO= ACTION WHEN NOT UNDER TSO: WTOR or SKIP                   *
.*--------------------------------------------------------------------*
         LCLA  &I,&NOTS
         LCLC  &L,&C
&MACPLAB SETC  '&NM'
&L       SETC  'L'''
         AIF   (T'&ADDR NE 'O').HAVEAD
         MNOTE 8,'#TGET: ADDRESS OPERAND REQUIRED'
         MEXIT ,
.HAVEAD  AIF   (T'&NOTSO EQ 'O').SKIPACT
&C       SETC  '&NOTSO'.'    '
&C       SETC  '&C'(1,4)
.LOOPACT AIF   ('&C' EQ 'WTORSKIP'(&I*4+1,4)).HAVEACT
&I       SETA  &I+1
         AIF   (&I LT 2).LOOPACT
         MNOTE 4,'#TGET: UNSUPPORTED NOTSO &NOTSO'
         AGO   .SKIPACT
.HAVEACT ANOP  ,
&NOTS    SETA  &I
.*
.SKIPACT MACPARM R0,&LEN,NULL=&L&LEN
         MACPARM R1,&ADDR    LOAD ADDRESS REGISTER
.SUBCALL AIF   (&NOTS EQ 0).TGET
         MACPARM R0,8,=AL1(&NOTS),OP=ICM,MODE=THREE   SET ACTION
.TGET    ANOP  ,                                                GP12162
         SUBCALL /SUBTGET                                       GP12162
.MEND    MEND  ,
