         MACRO ,
&NM      EXHMLINE &LINE=,&HEAD=,&ERRMSG=,&ENTER=,&INPUT=EXCIN,&MF=L
         GBLC  &ZZLLINE,&ZZLINP,&ZZLHEAD,&ZZLEMSG,&ZZLENTR
         GBLB  &ZZLLINK      DON'T CLEAR @UPLINE
         AIF   ('&LINE' EQ '').NOGLINE
&ZZLLINE SETC  '&LINE'
         AIF   (N'&LINE LT 2).NOGLINE
&ZZLLINE SETC  '&LINE(1)'
&ZZLLINK SETB  ('&LINE(2)' EQ 'KEEP' OR '&LINE(2)' EQ 'RETAIN')
.NOGLINE AIF   ('&HEAD' EQ '').NOGHEAD
&ZZLHEAD SETC  '&HEAD'
.NOGHEAD AIF   ('&ERRMSG' EQ '').NOGEMSG
&ZZLEMSG SETC  '&ERRMSG'
.NOGEMSG AIF   ('&ENTER' EQ '').NOGENTR
&ZZLENTR SETC  '&ENTER'
.NOGENTR AIF   ('&ZZLENTR' NE '').CHECKMF
&ZZLENTR SETC  'SHOWEXOF'
.CHECKMF AIF   ('&MF' EQ 'D').EXPDATA
         AIF   ('&MF' EQ 'INIT').EXPINIT
.**********************************************************************
.*                                                                   **
.*  FORMAT THE CONTENTS OF ONE DISPLAY LINE.                         **
.*  WHEN SCREEN IS FULL, DISPLAY IT                                  **
.*  TEST RESPONSE - ENTER ONLY CONTINUES THE DISPLAY (OR RESTARTS    **
.*    FROM THE BEGINNING WHEN THE END FLAG IS SET).                  **
.*                                                                   **
.**********************************************************************
&NM      SR    R15,R15       SET PROVISIONAL RETURN CODE
         STM   R0,R15,SUBSAVE1  SAVE REGISTERS
         TM    PROFLAGS,FGHEAD   NEED A NEW PAGE?
         BNZ   SHOWUP        NO; JUST PROCESS NORMAL LINE
SHOWANEW TM    PROFLAGS,FGUMSG  IS MESSAGE THE USER'S ?
         BNZ   SHOW          YES; DON'T ERASE IT
SHOWTEST EX    0,PRECLEAR    CLEAR THE ERROR MESSAGE BUFFER
SHOW     BAS   R14,BLANKER   CLEAR THE SCREEN
CLEARIT  NI    FDWOPT,255-FDWOKEEP  PERMIT INPUT BUFFER ERASURE
         B     RESHOW
         SPACE 1
RESHOW2  MVC   REPLY,BLANKS  CLEAR INPUT
.*RESHOW SCINIT FDW          INITIALIZE SCREEN PROCESSING
RESHOW   SCINIT FDW          INITIALIZE SCREEN PROCESSING
         OI    PROFLAGS,FGHEAD   HAVE A NEW PAGE
.*       XREQUIRE KEY,B=RESHOWNP
         XREQUIRE KEY,B=RESHOWNP
         OI    FDWOPT,FDWLOCK  UNLOCK CONDITIONAL INPUT FIELDS
RESHOWNP ICM   R15,15,@UPHEAD  TITLE LINE(S)
         BNZ   RESHOWNQ
         LA    R15,SHLHEAD   USE MINIMAL HEADER
.*SHOWNQ SCLIST FDW,(R15)    DISPLAY THE PAGE
RESHOWNQ SCLIST FDW,(R15)    DISPLAY THE PAGE
SHOWUP   ICM   R15,15,@UPLINE   FORMAT A SPECIAL LINE ?
         BNZ   SHOWALIN      YES
         LA    R15,UPLINE    ELSE USE DEFAULT
.*OWALIN SCLIST FDW,(R15)    FORMAT ONE LINE
SHOWALIN SCLIST FDW,(R15)    FORMAT ONE LINE
         LTR   R0,R0         DID THIS LINE FIT ?
         BNP   PUTPAGE       NO (OR EXACTLY); DISPLAY
         AIF   (&ZZLLINK).KEEP@LN
         XC    @UPLINE,@UPLINE  RESET SO CALLER DOESN'T NEED TO
.KEEP@LN OI    PROFLAGS,FGDATA  DATA ON PAGE
         TM    PROFLAGS,FGSHOW  DISPLAY THIS PAGE ?
         BNZ   PUTPAGE       YES; DISPLAY AND RETURN
SHOWEXOF NI    PROFLAGS,255-FGSHOW-FGUMSG
         LM    R0,R15,SUBSAVE1  RESTORE REGISTERS
         BR    R14           RETURN TO CALLER
         SPACE 1
PUTPAGE  MVC   REPLY,BLANKS  CLEAR THE INPUT
         NI    PROFLAGS,255-FGHEAD   NEED A NEW PAGE
         OI    SUBSAVE1+15*4+3,4  SET PAGE FLAG ON RETURN
         ICM   R15,15,@UPHEAD  TITLE LINE(S)
         BNZ   PUTPHEAD
         LA    R15,SHLHEAD   USE MINIMAL HEADER
.*PUTPHEAD SCPAGE FDW,(R15)    DISPLAY THE PAGE
PUTPHEAD SCPAGE FDW,(R15)    DISPLAY THE PAGE
         BNM   REPLANAL      CHECK INPUT
         BAS   R14,BLANKER
         CLI   FDWIAID,1     WHAT CONDITION ?
         BE    EXCWERR       WRITE ERROR
         BH    EXCRERR       READ ERROR
         TM    PROFLAGS,FGSHOW
         BNZ   SHOWEXOF
         TM    EXCPRIV,EXCPLOOP  LOOP MODE?
         BZ    DOEXIT        NO RESPONSE FROM USER (OR ERROR)
REPLANAL MVC   REPLY(6),BLANKS  CLEAR AID/CUA
         LA    R5,FDWICOD    POINT TO CONVERTED CURSOR
         L     R4,=A(QUICKIES)
.*       XLOOK T=(R4),R=FDWICOD    LOOK FOR NON-DATA FUNCTIONS
         XLOOK T=(R4),R=FDWICOD    LOOK FOR NON-DATA FUNCTIONS
QUICKEND TM    FDWFG,FDWFERR  PREVIOUS TEXT IN ERROR ?
         BNZ   PRECLEAR      YES; REPROCESS THROUGH SCANNER
         ICM   R0,15,FDWSCAL ANY INPUT TEXT ?
         BZ    PRETEXT       NO; LEAVE ERROR MESSAGE INTACT
PRECLEAR XC    &ZZLEMSG,&ZZLEMSG  CLEAR THE ERROR/RESPONSE MESSAGE
PRETEXT  OI    FDWOPT,FDWOKEEP  KEEP INPUT TEXT
.*       SCANAL FDW          ANALYZE THE INPUT
         SCANAL FDW          ANALYZE THE INPUT
         TM    FDWFG,FDWFERR   ANY TEXT IN ERROR ?
         BNZ   SYNSHOW       YES
         TM    FDWFG,FDWFTXT  ANY TEXT TO PROCESS ?
         BZ    &ZZLENTR      NO; JUST RETURN
.*       SCMOVE FDW          MOVE TEXT TO MEMORY
         SCMOVE FDW          MOVE TEXT TO MEMORY
         TM    FDWFG,FDWFERR   ANY ERRORS ?
         BNZ   SYNSHOW       YES; REQUEST CORRECTION
PARSCMD  BAS   R14,BLANKER
         LA    R5,REPLY      POINT TO INPUT
RLOOK    L     R4,=A(COMTABLE)
.*       XLOOK T=(R4)        GO TO COMMAND
         XLOOK T=(R4)        GO TO COMMAND
         CLI   0(R5),C' '    ANY INPUT ?
         BH    &INPUT        PROCESS MAJOR COMMAND              GP04105
         TM    PROFLAGS,FGSHOW
         BNZ   SHOWEXOF
         B     CLEARIT       NO, OR NOT VALID
CMDINVLD MVC   &ZZLEMSG.(15),=C'UNKNOWN COMMAND'
         B     RESHOW
         SPACE 1
SYNSHOW  MVC   &ZZLEMSG.(13),=C'INVALID INPUT'
         MVC   FDWCUR,FDWICUD  SET CURSOR AT ERROR POSITION
         B     RESHOW2       AND REDISPLAY
         SPACE 1
GOHOME   MVC   FDWCUR,FDWCUD SET CURSOR FROM DEFAULT
         B     QUICKEND      AND REDISPLAY
         SPACE 2
         PUSH  PRINT
         PRINT ON,NOGEN &PRTMAC
QUICKIES BTAB  *PA1,DOEXIT,BASE=*
         BTAB  *PF3,DOEXIT                                      GP09212
         BTAB  *PF15,DOEXIT                                     GP09212
         BTAB  *CANCEL,RESHOW2  REDISPLAY SCREEN
         BTAB  *CLEAR,SHOWTEST  REREAD
PFKTAB   BTAB  *PF12,GOHOME  CURSOR TO COMMAND FIELD
         BTAB  *END
         SPACE 1
COMTABLE BTAB  'REREAD',SHOWANEW,BASE=*
         BTAB  'END',DOEXIT  QUIT
         BTAB  *END
         SPACE 2
SHLHEAD  FDOPT SBA=(1,1),IND=0  START A NEW SCREEN
         FD    'Command',PAD,GREEN
         FDIN  REPLY+6,30,UPPER,NULL,DEBR,PINK,LEN=30 MAIN COMMAND
         FD    '&SYSECT',YELLOW,RADJ
SHLHEADV FDCLC &ZZLEMSG,BLANKS,BE=SHLHEADM,BL=SHLHEADM
         FD    &ZZLEMSG,RADJ,DEB,INTENSE,YELLOW,NL ERROR MSG
SHLHEADM FD    *END
         SPACE 1
UPLINE   FDOPT NL
         FD    *END
         POP   PRINT
         MEXIT ,
.EXPDATA ANOP  ,
SUBSAVE1 DC    18A(0)        SAVE AREA FOR ONUNIT EXIT
@UPLINE  DC    A(0)          ADDRESS OF NEXT LINE FD LIST
@UPHEAD  DC    A(0)          ADDRESS OF title FD LIST
         SPACE 1
PROFLAGS DC    X'00'         MISCELLANEOUS FLAGS
FGEND    EQU   X'80'           ON IF program done
FGSHOW   EQU   X'40'           DISPLAY PAGE AND RETURN
FGHEAD   EQU   X'20'           NEED SCINIT
FGDATA   EQU   X'10'           DATA ON PAGE
FGUMSG   EQU   X'08'           ERRMSG FIELD (PRE)SET BY USER
FGTWO    EQU   X'02'             reserved for user
FGONE    EQU   X'01'             reserved for user
         SPACE 1
&ZZLEMSG DC    CL38' '       ERROR MESSAGES
         MEXIT ,
.EXPINIT ANOP  ,
&NM      XC    FDW(FDINWORK-FDW),FDW  CLEAR ALL FIELDS
         LA    R14,&ZZLHEAD  POINT TO FD/FDIN LIST
         LA    R15,FDINWORK  POINT TO FDIN WORK AREA
         LA    R0,FDINLEN    SET SIZE OF INPUT WORK AREA
         STM   R14,R0,FDWFDA   STASH IN PARAMETER LIST
         MVI   PROFLAGS,0    RESET RESULTS
         AIF   (NOT &ZZLLINK OR '&ZZLLINE' EQ '').NOILINE
         MACPARM R0,&ZZLLINE
         ST    R0,@UPLINE    SET USER'S NORMAL LINE FD ADDRESS
.NOILINE AIF   ('&ZZLHEAD' EQ '').NOIHEAD
         MACPARM R0,&ZZLHEAD
         ST    R0,@UPHEAD    SET USER'S NORMAL LINE FD ADDRESS
.NOIHEAD ANOP  ,             MORE LATER
         MEND  ,
