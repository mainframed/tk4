         MACRO
&NM      HQUE  &Q,&Q1,&Q2,&Q3,&SIZE=,&OUT=
         GBLA  &QUECNT,&QUEIN,&QUESIZ,&QUECON
         GBLC  &LOCAL                                             75341
         LCLA  &I,&J,&K,&L,&QC,&KD,&KE
         LCLC  &C,&D,&E
         LCLC  &BIG(10)
         LCLA  &BIGQ(10),&BM
         LCLA  &MAX
&BM      SETA  9 .           NUMBER OF 3-DIGIT Q NAMES
&BIG(1)  SETC  'OUT'
&BIG(2)  SETC  'OLD'
&BIG(3)  SETC  'XEQ'
&BIG(4)  SETC  'INP'
&BIG(5)  SETC  'RDR'
&BIG(6)  SETC  'PLT'         PLOT Q
&BIG(7)  SETC  'FCH'         FETCH Q
         AIF   ('&LOCAL' NE 'DOC').NODOC                          75341
&BIG(7)  SETC  'ATS'                                              75341
.NODOC   ANOP  ,                                                  75341
&BIG(8)  SETC  'O/P'         UNPROCESSED OUTPUT
&BIG(9)  SETC  'REM'         UNSPECIFIED REMOTE
&BIGQ(1) SETA  0
&BIGQ(2) SETA  1
&BIGQ(3) SETA  39
&BIGQ(4) SETA  2
&BIGQ(5) SETA  2
&BIGQ(6) SETA  76            PLOT Q
&BIGQ(7) SETA  77            FETCH Q
&BIGQ(8) SETA  78 .          UNPROCESSED OUTPUT
&BIGQ(9) SETA  79 .          UNSPECIFIED REMOTE
&MAX     SETA  18
         AIF   ('&OUT' EQ '').NOUT
&QUEIN   SETA  1
         AIF   ('&OUT' NE 'IN').NOUT
&QUEIN   SETA  0             SET INPUT QUEUES
.NOUT    ANOP
         AIF   (&QUESIZ NE 1).BIG
&MAX     SETA  9
.BIG     ANOP
&I       SETA  N'&SYSLIST
&J       SETA  1
         AIF   (&QUECNT GT &MAX).MEXIT
         AIF   (&QUECNT GT 0).CHECK
         AIF   ('&NM' NE '').DOEQ
         MNOTE 8,'FIRST ''HQUE'' MUST HAVE A NAME'
&QUECNT  SETA  &MAX+1
         AGO   .MEXIT
.DOEQ    AIF   ('&SIZE' EQ '').DOCL
&MAX     SETA  9
&QUESIZ  SETA  1
.DOCL    ANOP
&QUECON  SETA  0             NO CONCATENATION
&NM      DC    &MAX.CL4' ' .  RESERVE STORAGE FOR ZAPS
         ORG   &NM .          BUMP BACK TO START
.CHECK   AIF   ('&Q' EQ '').MISSING
         AIF   ('&Q' EQ 'END').END
         AIF   ('&Q'(1,1) LT 'A').SPECIAL
&QUECON  SETA  0             NO CONCATENATION
.LOOP    ANOP
&QUECNT  SETA  &QUECNT+1
         AIF   (&QUECNT GT &MAX).OFLOW
&K       SETA  K'&SYSLIST(&J)
         AIF   (&K EQ 1).ONE
         AIF   (&K EQ 3).THREE
         AIF   (&K EQ 2 AND &QUEIN EQ 1).TWO
.MISSING MNOTE 4,'BAD OR MISSING PARAMETER'
         AGO   .MEXIT
.THREE   ANOP
&K       SETA  1
.THL     AIF   ('&SYSLIST(&J)' EQ '&BIG(&K)').F3
&K       SETA  &K+1
         AIF   (&K LE &BM).THL
         AGO   .MISSING
.F3      DC    C'&SYSLIST(&J)',AL1(&BIGQ(&K))
         AIF   (&K NE 1).BUMP
&QUEIN   SETA  1
.BUMP    ANOP
&J       SETA  &J+1
         AIF   (&J GT &I).MEXIT
         AGO   .LOOP
.ONE     AIF   (&QUEIN EQ 1).OUT
&K       SETA  1
.FIT     AIF   ('&SYSLIST(&J)'  EQ                                     *
               'ABCDEFGHIJKLMNOPQRSTUVWXYZ0123456789'(&K,1)).FI1
&K       SETA  &K+1
         AIF   (&K GT 36).MISSING
         AGO   .FIT
.FI1     ANOP
&K       SETA  &K+39
.F1      DC    C' ',C'&SYSLIST(&J)',C' ',AL1(&K)
         AGO   .BUMP
.OUT     ANOP
&K       SETA  1
.FOT     AIF   ('&SYSLIST(&J)' EQ                                      X
               'ABC123456789'(&K,1)).FO1
&K       SETA  &K+1
         AIF   (&K GT 12).MISSING
         AGO   .FOT
.FO1     ANOP
&K       SETA  &K+2
         AGO   .F1
.TWO     AIF   ('&SYSLIST(&J)'(1,1) LT '0' OR                          *
               '&SYSLIST(&J)'(2,1) LT '0').MISSING
&K       SETA  &SYSLIST(&J)+5
         AIF   (&K LT 6 OR &K GT 38).MISSING
         AGO   .F1
.END     AIF   ('&NM' EQ '').END1
&NM      EQU   * .           END OF HASP QUEUE TABLE
.END1    ANOP
         ORG
&QUECNT  SETA  0
&QUEIN   SETA  0
&QUESIZ  SETA  0
&QUECON  SETA  0             NO CONCATENATION
.MEXIT   MEXIT
.OFLOW   MNOTE 4,'ONLY &MAX DISPLAY FIELDS ALLOWED'
         AGO   .END1
.SPECIAL AIF   ('&Q' EQ '#').CONCAT
&QUECON  SETA  1             ALLOW CONCAT
         AIF   ('&Q' EQ '?' AND &I EQ 4).VAR
         AIF   (&I NE 3).MISSING
&D       SETC  '&Q1'
&KD      SETA  K'&Q1
&E       SETC  '&Q2'
&KE      SETA  K'&Q2
         AIF   ('&Q' EQ '*' OR '&Q' EQ '$').S1
         AGO   .MISSING
.S1      ANOP
&QUECNT  SETA  &QUECNT+1
         AIF   (&QUECNT GT &MAX).OFLOW
&QC      SETA  0
.SV      AIF   (&QUEIN EQ 1).SOUT
&K       SETA  1
&L       SETA  1
.SI1     AIF   ('&D'  EQ                                               *
               'ABCDEFGHIJKLMNOPQRSTUVWXYZ0123456789'(&K,1)).SI2
&K       SETA  &K+1
         AIF   (&K GT 36).MISSING
         AGO   .SI1
.SI2     AIF   ('&E'  EQ                                               *
               'ABCDEFGHIJKLMNOPQRSTUVWXYZ0123456789'(&L,1)).SI3
&L       SETA  &L+1
         AIF   (&L GT 36).MISSING
         AGO   .SI2
.SI3     ANOP
&K       SETA  &K+39
&L       SETA  &L+39
.SCOM    AIF   (&K GT &L).MISSING
         DC    C'&Q',AL1(&QC,&K,&L)
         AGO   .MEXIT
.SOUT    ANOP
&K       SETA  1
&L       SETA  1
         AIF   (&KD  EQ 1).SO1
         AIF   (&KD  NE 2).MISSING
         AIF   ('&D'(1,1) LT '0' OR '&D'(2,1) LT '0').MISSING
&K       SETA  &D+5
         AIF   (&K LT 6 OR &K GT 38).MISSING
         AGO   .X2
.SO1     AIF   ('&D' EQ 'ABC123456789'(&K,1)).X
&K       SETA  &K+1
         AIF   (&K GT 12).MISSING
         AGO   .SO1
.X       ANOP
&K       SETA  &K+2
.X2      AIF   (&KE  EQ 1).SO2
         AIF   (&KE  NE 2).MISSING
         AIF   ('&E'(1,1) LT '0' OR '&E'(2,1) LT '0').MISSING
&L       SETA  &E+5
         AIF   (&L LT 6 OR &L GT 38).MISSING
         AGO   .Y2
.SO2     AIF   ('&E' EQ 'ABC123456789'(&L,1)).Y
&L       SETA  &L+1
         AIF   (&L GT 12).MISSING
         AGO   .SO2
.Y       ANOP
&L       SETA  &L+2
.Y2      AGO   .SCOM
.VAR     ANOP
&C       SETC  '&Q1'
&D       SETC  '&Q2'
&KD      SETA  K'&Q2
&E       SETC  '&Q3'
&KE      SETA  K'&Q3
         AIF   (&C LT 1 OR &C GT &MAX).MISSING
&QC      SETA  &MAX-&QUECNT
         AIF   (&QC LE 0).OFLOW
         AIF   (&QC LT &C).NOROOM
&QC      SETA  &C
         AGO   .V1
.NOROOM  MNOTE 4,'REQUEST FOR ''&C'' FIELDS TRUNCATED TO ''&QC'''
.V1      ANOP
&QUECNT  SETA  &QUECNT+&QC
         AGO   .SV
.CONCAT  AIF   (&QUECON EQ 0).MISSING
         AIF   (&I NE 2 AND &I NE 3).MISSING
&QC      SETA  1
&J       SETA  1
.CONCLP  AIF   (&J GE &I).SCOM
&J       SETA  &J+1
&L       SETA  2
.CONCLS  AIF   ('&SYSLIST(&J)' EQ '&BIG(&L)').CONCLT
&L       SETA  &L+1
         AIF   (&L LE &BM).CONCLS
         AGO   .MISSING
.CONCLT  ANOP  ,
&L       SETA  &BIGQ(&L)
         AIF   (&J NE 2).CONCLP
&K       SETA  &L
         AGO   .CONCLP
         MEND
