         MACRO
&NM      BTAB  &STRING,&TO,&XCT,&S,&BASE=
         GBLC  &CRT
         GBLC  &TABMBAS,&TABMOPT
         GBLC  &PFKEY(52),&PFKEX(52)                             88211
         GBLA  &#PFKEY                                           88211
         LCLC  &TEXT                                             88211
         LCLA  &I,&J,&K,&L
&K       SETA  K'&STRING-1
         AIF   ('&STRING' EQ '').PARMA
         AIF   ('&STRING'(1,1) NE '''').COMLEN
&I       SETA  2
&J       SETA  &K-1
&K       SETA  &J
.LOOP    AIF   ('&STRING'(&I,2) EQ '''''').SK2
         AIF   ('&STRING'(&I,2) EQ '&&').SK2
&I       SETA  &I+1
         AGO   .INC
.SK2     ANOP
&I       SETA  &I+2
&K       SETA  &K-1
.INC     AIF   (&I LE &J).LOOP
&K       SETA  &K-1
         AIF   (&K GE 0).COMLEN
&K       SETA  0
.COMLEN  AIF   ('&BASE' EQ '').NBAS
         AIF   ('&BASE' NE '*').UBAS
&TABMBAS SETC  '&SYSECT'
         AGO   .NBAS
.UBAS    ANOP
&TABMBAS SETC  '&BASE'
.NBAS    AIF   ('&TABMBAS' NE '').BASOK
&TABMBAS SETC  'EXHBCOMM' .   DEFAULT BASE CSECT
.BASOK   ANOP
         AIF   ('&STRING' EQ '*END').LAST
         AIF   ('&TO' NE '' AND '&XCT' EQ '' AND '&S' EQ '').TO
         AIF   ('&TO' EQ '' AND '&XCT' NE '' AND '&S' EQ '').XCT
         AIF   ('&TO' EQ '' AND '&XCT' EQ '' AND '&S' NE '').S
.PARMA   MNOTE 8,'MISSING OR CONFLICTING PARAMETERS'
         MEXIT
.BADBASE MNOTE 8,'INCORRECT BASE PARAMETER'
         MEXIT
.TO      AIF   ('&TABMOPT' EQ '' OR '&TABMOPT' EQ 'TO').TOTO
.NONO    MNOTE 8,'INCORRECT MACRO NAME, OR PREVIOUS *END MISSING'
         MEXIT
.TOTO    ANOP
&TABMOPT SETC  'TO'
         AIF   ('&STRING'(1,1) EQ '*').SPEC
         AIF   ('&STRING'(1,1) EQ '''').TOAP
&NM      DC    AL1(&K),AL3(&TO-(&TABMBAS)),C'&STRING'
         MEXIT
.TOAP    ANOP
&NM      DC    AL1(&K),AL3(&TO-(&TABMBAS)),C&STRING
         MEXIT
.XCT     AIF   ('&TABMOPT' NE '' AND '&TABMOPT' NE 'XCT').NONO
&TABMOPT SETC  'XCT'
         AIF   ('&STRING'(1,1) EQ '*').SPEC
         AIF   ('&STRING'(1,1) EQ '''').XAP
&NM      DC    AL1(&K),CL3'&XCT',C'&STRING'
         MEXIT
.XAP     ANOP
&NM      DC    AL1(&K),CL3'&XCT',C&STRING
         MEXIT
.S       AIF   ('&TABMOPT' NE '' AND '&TABMOPT' NE 'S').NONO
&TABMOPT SETC  'S'
         AIF   ('&STRING'(1,1) EQ '''').SAP
         AIF   ('&STRING'(1,1) EQ '*').SPEC
&NM      DC    AL1(&K,0),SL2(&S),C'&STRING'
         MEXIT
.SAP     ANOP
&NM      DC    AL1(&K,0),SL2(&S),C&STRING
         MEXIT
.SPEC    AIF   ('&STRING' EQ '*').PARMA
         PFKEYS ,            INVOKE PFK HEX EQUIVALENTS          88211
&TEXT    SETC  '&STRING'(2,&K)
&I       SETA  0
.SPLP    AIF   (&I GE &#PFKEY).SPHEX                             88211
&I       SETA  &I+1
         AIF   ('&TEXT' NE '&PFKEY(&I)').SPLP                    88211
&TEXT    SETC  '&PFKEX(&I)'                                      88211
&K       SETA  0
         AIF   ('&CRT' EQ '3270' OR '&CRT' EQ '').HEXT          GP04045
         AIF   ('&NM' EQ '').MEND   SKIP IF NOT 3270
&NM      EQU   * .           PLANT A LABEL - NO 3270 CODE EXPANDED
.MEND    MEXIT
.SPHEX   ANOP
&I       SETA  0
.SPHL    AIF   (&I GE &K).SPHEXS
&I       SETA  &I+1
&J       SETA  1
.SPHLD   AIF   ('&TEXT'(&I,1) EQ '0123456789ABCDEF'(&J,1)).SPHL
&J       SETA  &J+1
         AIF   (&J GT 16).PARMA
         AGO   .SPHLD
.SPHEXS  ANOP
&K       SETA  (&K-1)/2
.HEXT    AIF   ('&TABMOPT' EQ 'TO').HEXTO
         AIF   ('&TABMOPT' EQ 'XCT').HEXCT
&NM      DC    AL1(&K,0),SL2(&S),X'&TEXT'
         MEXIT
.HEXCT   ANOP
&NM      DC    AL1(&K),CL3'&XCT',X'&TEXT'
         MEXIT
.HEXTO   ANOP
&NM      DC    AL1(&K),AL3(&TO-(&TABMBAS)),X'&TEXT'
         MEXIT
.LAST    ANOP
&TABMOPT SETC  ''
&NM      DC    X'FF' .       END OF TABLE
         MEND
