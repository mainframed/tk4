         MACRO ,
&NM      DEBTRACE &LBL,&TEXT=,&REGS=,&HEX=,&MODE=S,     ADDED ON 85360 *
               &WK=R9,&DEV=1,&TCB=,         WTO VS @PRT  CHANGED 94011 *
               &ROUT=13,&DES=4,&BUGPARM=NO,                ADDED 95067 *
               &CTEXT=,&PACK=,                             ADDED 96081 *
               &PRTMODE=0,&DCB=0,   USER PRINT DCB/MODE    ADDED 99058 *
               &COUNT=,&CALL=DYN,&OPT=,                  CHANGED 98222 *
               &WA=DBTSAVE,                                ADDED 99114 *
               &LIST=  (WORKS WITH CALL=EXTRN ONLY)        ADDED 95235
.**********************************************************************
.*                                                                   **
.* THIS IS A SAVED VERSION OF DEBTRACE SUPPORTING IN-LINE DUMPING (CODE
.* EXPANDED IN CALLER'S PROGRAM)
.*                                                                   **
.**********************************************************************
     GBLB  &BUGBEAR,&BUGTCB,&BUGSWCH,&BUGSWRT,&BUGFAR,&BUGEXT,&BUGDYN
     GBLB  &BUGTRC           USED WITH ACTIVE PGMTRACE (ESPIE)  GP99113
         GBLC  &V
         LCLA  &LN,&I,&EN,&EM,&EO                               GP95235
         LCLC  &L,&ET,&EL,&EK                                   GP95235
&L       SETC  'L'''                                            GP95235
&V       SETC  'DBT'.'&SYSNDX'
&BUGFAR  SETB  (&BUGFAR OR ('&CALL' EQ 'FAR'))                   95079
&BUGEXT  SETB  (&BUGEXT OR ('&CALL' EQ 'EXTRN'))                 95227
&BUGDYN  SETB  (&BUGDYN OR ('&CALL' EQ 'DYN'))                  GP97261
&BUGDYN  SETB  (&BUGDYN OR ('&CALL' EQ 'DYNAMIC'))              GP97261
&BUGTRC  SETB  (&BUGTRC OR ('&CALL' EQ 'TRC'))                  GP99113
&BUGTRC  SETB  (&BUGTRC OR ('&CALL' EQ 'TRACE'))                GP99113
&BUGTRC  SETB  (&BUGTRC OR ('&CALL' EQ 'PGMTRACE'))             GP99113
         AIF   (&BUGBEAR OR '&BUGPARM' EQ 'NO').DOSOME
         AIF   ('&NM' EQ '').MEND
&NM      DS    0H            DEBUG SWITCH NOT ON
         AGO   .MEND
.DOSOME  ANOP  ,                                                 95067
&BUGSWCH SETB  1                                                 95067
         AIF   ('&MODE' EQ 'D' OR '&MODE' EQ 'M').DATA           95228
         AIF   ('&MODE' EQ 'C').CODE
         AIF   ('&MODE' EQ 'DC').CODE   EXPAND BOTH              95067
         AIF   ('&MODE' EQ 'ON').SWON                            95079
         AIF   ('&MODE' EQ 'OFF').SWOFF                          95079
         AIF   ('&MODE' EQ 'CLOSE').SWEND  CLOSE AND QUIT       GP98222
         AIF   (NOT &BUGTRC).NOTTRC                             GP99113
&NM      DC    X'83CD',S(&WA,&V.X-*)  INVOKE TRACE              GP99113
         AGO   .DONEBAS                                         GP99113
.NOTTRC  ANOP  ,                                                GP99113
&NM      STM   R0,R15,&WA    SAVE ALL REGISTERS
         AIF   ('&COUNT' EQ '').DONECNT                          95079
.*  COUNT(3) - SKIP FIRST N CALLS                                95079
         AIF   ('&COUNT(3)' EQ '').CNTNO3                        95079
         ICM   R14,15,&V.3   LOAD SKIP COUNT                     95079
         BNP   &V.C          LIMIT REACHED - PROCESS             95079
         BCTR  R14,0         DECREMENT                           95079
         STCM  R14,15,&V.3   SAVE FOR NEXT TIME                  95079
         B     &V.X          AND SKIP CALL                       95079
&V.3     DC    AL4(&COUNT(3))  INITIAL SKIP COUNT                95079
&V.C     DS    0H                                                95079
.CNTNO3  AIF   ('&COUNT(2)' EQ '').CNTNO2                        95079
         AIF   ('&COUNT(2)' EQ '1').CNTNO2                       95079
         AIF   ('&COUNT(2)' EQ '0').CNTNO2                       95079
.*  COUNT(2) - PROCESS EVERY NTH CALL ONLY                       95079
         ICM   R14,15,&V.2   LOAD COUNTER                        95079
         BNP   &V.L          BAD - PROCESS CALL                  95079
         BCT   R14,&V.N      NON-ZERO; SAVE AND SKIP             95079
         MVC   &V.2,=AL4(&COUNT(2))  REFRESH                     95079
         B     &V.L          AND GO                              95079
&V.2     DC    AL4(1)        INTERVAL COUNTER (DO FIRST ONE)     95079
&V.N     STCM  R14,15,&V.2   UPDATE COUNTER                      95079
         B     &V.X          AND EXIT                            95079
.CNTNO2  AIF   ('&COUNT(1)' EQ '').DONECNT                       95079
         AIF   ('&COUNT(1)' EQ '0').DONECNT                      95079
         ICM   R14,15,&V.1   LOAD LIMIT COUNT                    95079
         BNP   &V.X          SKIP OUT IF NOT VALID               95079
         BCTR  R14,0         DECREMENT                           95079
         B     &V.M          SAVE, AND CONTINUE                  95079
&V.1     DC    AL4(&COUNT(1))  MAXIMUM CALLS TO MAKE             95079
&V.M     STCM  R14,15,&V.1   SAVE FOR NEXT TIME                  95079
.DONECNT ANOP  ,                                                 95079
&V.L     BAS   R1,&V.B                                           95079
.DONEBAS AIF   ('&LBL' EQ '' AND (&BUGEXT OR &BUGDYN OR &BUGTRC)).NOLBL
         DC    CL8'&LBL '
.NOLBL   AIF   ('&REGS' EQ '' OR '&REGS' EQ 'NO').NOREGS         95079
         AIF   ('&REGS' EQ 'R15' OR '&REGS' EQ 'SHORT'                 *
               OR '&REGS' EQ 'RET').RETREG                      GP97225
         AIF   ('&REGS' EQ 'YES').REGSALL                       GP97225
         AIF   (N'&REGS EQ 2).REGS2                             GP97225
         DC    AL1(0,0),SL2(&REGS(1),&REGS(1))                  GP97225
         AGO   .NOREGS                                          GP97225
.REGS2   DC    AL1(0,0),SL2(&REGS(1),&REGS(2))                  GP97225
         AGO   .NOREGS                                          GP97225
.REGSALL DC    AL1(0,0),SL2(0,15)                               GP97225
         AGO   .NOREGS                                          GP97225
.RETREG  DC    SL2(0,14,1)    R15-R1 ONLY                       GP97225
.NOREGS  AIF   ('&TEXT' EQ '').NOTEXT
         AIF   (N'&TEXT GE 2).TEXT2                             GP97225
         DC    AL1(1,0),SL2(&TEXT(1)),AL2(&L&TEXT(1))           GP97225
         AGO   .NOTEXT                                          GP97225
.TEXT2   DC    AL1(1,0),SL2(&TEXT(1),&TEXT(2))
.NOTEXT  AIF   ('&CTEXT' EQ '').NOCTEXT                         GP97225
         AIF   (N'&CTEXT GE 2).CTEXT2                           GP97225
         DC    AL1(2,0),SL2(&CTEXT(1)),AL2(&L&CTEXT(1))         GP97225
         AGO   .NOCTEXT                                         GP97225
.CTEXT2  DC    AL1(2,0),SL2(&CTEXT(1),&CTEXT(2))                GP97225
.NOCTEXT AIF   ('&HEX' EQ '').NOHEX                             GP97225
         AIF   (N'&HEX GE 2).HEX2                               GP97225
         DC    AL1(3,0),SL2(&HEX(1)),AL2(&L&HEX(1))             GP97225
         AGO   .NOHEX                                           GP97225
.HEX2    DC    AL1(3,0),SL2(&HEX(1),&HEX(2))                    GP97225
.NOHEX   AIF   ('&PACK' EQ '').NOPACK                           GP97225
         AIF   (N'&PACK GE 2).PACK2                             GP97225
         DC    AL1(4,0),SL2(&PACK(1)),AL2(&L&PACK(1))           GP97225
         AGO   .NOPACK                                          GP97225
.PACK2   DC    AL1(4,0),SL2(&PACK(1),&PACK(2))                  GP97225
.NOPACK  AIF   ('&LIST' EQ '' OR N'&LIST LT 1).NOLIST           GP95235
&LN      SETA  N'&LIST                                          GP95235
.DOLIST  AIF   (&I GE &LN).NOLIST   DONE WITH LIST              GP95235
&I       SETA  &I+1          BUMP LOOP INDEX                    GP95235
&EN      SETA  N'&LIST(&I)   NUMBER OF ENTRIES                  GP95235
         AIF   (&EN LT 1).DOLIST  USER IN COMA?                 GP95235
         AIF   (&EN LT 4).TOOLIST WARN                          GP95235
         MNOTE 4,'LIST(&I) HAS TOO MANY (&EN) ENTRIES'          GP95235
.TOOLIST ANOP  ,                                                GP95235
&EK      SETC  '&LIST(&I,1)'
&EM      SETA  K'&EK         LENGTH OF FIRST OPERAND
&EO      SETA  0             PRESET FOR NORMAL ADDRESSING MODE
&ET      SETC  '03'          PRESET FOR HEX DEFAULT             GP95235
         AIF   ('&EK'(1,1) NE '/').NOTIND
&EO      SETA  &EO+1         REQUEST INDIRECT ADDRESSING
&EK      SETC  '&EK'(2,&EM-1)  DELETE LEADING CONTROL BYTE
&EM      SETA  K'&EK         LENGTH OF FIRST OPERAND
.NOTIND  AIF   ('&EK'(&EM,1) NE '%').NOTA24
&EO      SETA  &EO+2         REQUEST FORCED 24-BIT ADDRESSING
&EK      SETC  '&EK'(1,&EM-1)  DELETE TRAILING CONTROL BYTE
&EM      SETA  K'&EK         LENGTH OF FIRST OPERAND
.NOTA24  AIF   ('&EK'(&EM,1) NE '?').NOTA31
&EO      SETA  &EO+4         REQUEST FORCED 31-BIT ADDRESSING
&EK      SETC  '&EK'(1,&EM-1)  DELETE TRAILING CONTROL BYTE
&EM      SETA  K'&EK         LENGTH OF FIRST OPERAND
.NOTA31  AIF   (&EN LT 3 OR '&LIST(&I,3)' EQ 'HEX').HTYPE       GP95235
         AIF   ('&LIST(&I,3)' EQ 'X').HTYPE                     GP97225
         AIF   ('&LIST(&I,3)' EQ 'HEX').HTYPE
         AIF   ('&LIST(&I,3)' EQ 'T').TTYPE                     GP98189
         AIF   ('&LIST(&I,3)' EQ 'TEXT').TTYPE                  GP95235
         AIF   ('&LIST(&I,3)' EQ 'TXT').TTYPE
         AIF   ('&LIST(&I,3)' EQ 'C').CTYPE                     GP97225
         AIF   ('&LIST(&I,3)' EQ 'CT').CTYPE
         AIF   ('&LIST(&I,3)' EQ 'CTEXT').CTYPE                 GP97225
         AIF   ('&LIST(&I,3)' EQ 'PACK').PTYPE                  GP97225
         AIF   ('&LIST(&I,3)' EQ 'PACKED').PTYPE                GP97225
         AIF   ('&LIST(&I,3)' EQ 'P').PTYPE                     GP97225
         AIF   ('&LIST(&I,3)' EQ 'PD').PTYPE
         AIF   ('&LIST(&I,3)' EQ 'D').PTYPE                     GP97225
 MNOTE 4,'TYPE MUST BE TEXT, CTEXT, HEX, OR PACKED, NOT &LIST(&I,3)'
         AGO   .HTYPE                                           GP95235
.TTYPE   ANOP  ,                                                GP95235
&ET      SETC  '01'          SET FOR TEXT                       GP95235
         AGO   .HTYPE                                           GP95235
.CTYPE   ANOP  ,                                                GP97225
&ET      SETC  '02'          SET FOR CONDITIONAL TEXT, ELSE HEX GP97225
         AGO   .HTYPE                                           GP97225
.PTYPE   ANOP  ,                                                GP97225
&ET      SETC  '04'          SET FOR PACKED                     GP97225
.HTYPE   ANOP  ,                                                GP97225
&EL      SETC  '&LIST(&I,2)'                                    GP95235
         AIF   ('&EL' NE '').HLEN                               GP95235
&EL      SETC  '&L'.'&EK'
.HLEN    DC    X'0800',CL8'&LIST(&I,1)',AL1(&ET,&EO),S(&EK,&EL)
         AGO   .DOLIST                                          GP95235
.NOLIST  AIF   (&BUGFAR).FARCL                                   95079
         AIF   (&BUGDYN).FARCL                                  GP97261
         AIF   (&BUGTRC).TRCCL                                  GP99113
         AIF   (&BUGEXT).EXTCL                                   95215
&V.B     BAS   R14,DBTRACE                                       92271
         AGO   .CMCAL                                            95079
.EXTCL   ANOP  ,                                                 95215
&V.B     L     R15,=V(DEBTRACE)
         LA    R0,&WA        PASS ADDRESS OF WORK AREA           95215
         AGO   .FARCM                                            95215
.FARCL   ANOP  ,                                                 95079
&V.B     L     R15,=A(DBTRACE)                                   95079
.FARCM   BASR  R14,R15                                           95079
.CMCAL   ANOP  ,                                                 95079
&V.X     LM    R0,R15,&WA
         AGO   .MEND
.TRCCL   ANOP  ,             INVOKE PGMTRACE VIA ESPIE          GP99113
&V.X     DS    0H            END OF TRACE LIST                  GP99113
         AGO   .MEND                                            GP99113
.SWON    OI    DBTFLAG,DBTFLON  SET TRACING ON                   95079
         AGO   .MEND                                             95079
.SWEND   OI    DBTFLAG,DBTFLEND  CLOSE DCB AND STOP TRACE       GP98222
.SWOFF   NI    DBTFLAG,255-DBTFLON  SET TRACING OFF              95079
         AGO   .MEND                                             95079
.CODE    AIF   (&BUGFAR OR &BUGEXT).TESTDC                      GP97262
.*       PUSH  PRINT
.*       PUSH  USING
.*OOPS*  DROP  ,
.*       PRINT GEN
         AIF   ('&NM' EQ '').NONAME
&NM      DS    0H
.NONAME  AIF   (NOT &BUGDYN).NOLODYN                            GP97262
         DROP  ,                                                GP97262
         USING DBTRACE,R15                                      GP97265
DBTRACE  LA    R0,&WA        PASS ADDRESS OF WORK AREA          GP97262
         STM   R12,R1,DBTLOCSV  SAVE BASE AND RETURN            GP97265
         ICM   R15,15,@DEBTRAC  SEE IF PREVIOUSLY LOADED        GP97265
         BNZR  R15           INVOKE; RETURN VIA R14 TO CALLER   GP97265
         BASR  R12,0         MAKE LOCAL BASE                    GP97262
         DROP  R15                                              GP97265
         USING *,R12                                            GP97265
         LOAD  EPLOC=#DEBTRAC  LOAD EXTERNAL MODULE             GP97261
         ST    R0,@DEBTRAC   SAVE FOR NEXT TIME                 GP97261
         ST    R0,DBTLOCSV+(15-12)*4  UPDATE TARGET ADDRESS     GP97265
         LM    R12,R1,DBTLOCSV  RESTORE                         GP97265
         BR    R15           RETURN TO CALLER VIA R14           GP97262
.*       POP   USING                                            GP97262
.*       POP   PRINT                                            GP97262
         AGO   .TESTDC                                          GP97262
.NOLODYN ANOP  ,                                                 95215
DBTRACE  STM   R12,R15,DBTLOCSV  SAVE BASE AND RETURN
         BASR  R12,(0)       SET LOCAL BASE                      92271
         USING *,R12
         LA    R6,DBTEXITS   SET FOR QUICK EXIT (LABEL ONLY)     95080
         TM    DBTFLAG,DBTFLON  DEBUG ON ?
         BZR   R6            NO; JUST TAKE QUICK RETURN          95080
         AIF   ('&COUNT(1)' EQ '').CNTDONE                       95079
         AIF   ('&COUNT(1)' EQ '0').CNTDONE                      95079
         ICM   R14,15,DBTRACNO   LOAD LIMIT COUNT                95079
         BNPR  R6            IGNORE IF REACHED                   95080
         BCTR  R14,0         DECREMENT                           95079
         B     DBTRACNM      SAVE, AND PROCESS                   95079
DBTRACNO DC    AL4(&COUNT(1))  MAXIMUM CALLS TO MAKE             95079
DBTRACNM STCM  R14,15,DBTRACNO   SAVE FOR NEXT TIME              95079
.CNTDONE ANOP  ,                                                 95079
         LR    &WK,R1        COPY PARM REGISTER
         MVC   DBTWTO(DBTPATL),DBTPAT
         MVC   DBTWTON(DBTWTOC-DBTWTON),DBTWTON-1   BLANK THE LINE
         AIF   ('&TCB' NE 'YES').DBTLUP                          94011
&BUGTCB  SETB  1                                                 94011
         UNPK  DBTWTCB(9),PSATOLD-PSA(5)                         94011
         TR    DBTWTCB,DBTHEXTB-C'0'                             94011
         MVI   DBTWTCB+L'DBTWTCB,C' '                            94011
.DBTLUP  MVC   DBTWTON,0(&WK)  MOVE USER'S LABEL
DBTLOOP  CLI   8(&WK),3      VALID FLAG ?
         BH    DBTLAST       NO
         SLR   R2,R2
         IC    R2,10(,&WK)   GET S(ADDR)
         SRL   R2,4          DELETE LOW BITS OF OFFSET
         SLA   R2,2          *4 => INDEX INTO DBTSAVE
         BZ    *+8           R0 = 0
         L     R2,&WA(R2)      GET USER'S REGISTER
         LA    R0,4095       MAKE MASK
         N     R0,8(,&WK)    MASK OFFSET OF ADDRESS
         ALR   R2,R0         MAKE EFFECTIVE ADDRESS
         SLR   R3,R3
         IC    R3,12(,&WK)   GET S(LEN)
         SRL   R3,4          DELETE LOW BITS OF OFFSET
         SLA   R3,2          *4 => INDEX INTO DBTSAVE
         BZ    *+8           R0 = 0
         L     R3,&WA(R3)      GET USER'S REGISTER
         LA    R0,4095       MAKE MASK
         N     R0,10(,&WK)   MASK OFFSET OF LENGTH
         ALR   R3,R0         MAKE EFFECTIVE ADDRESS
         CLI   8(&WK),0      REALLY SHORT REGISTERS ?
         BH    DBTTTYP       NO; CHECK TYPE
         MVC   DBTPRESV(8),&WA+14*4      PUT 14-15 BEFORE 0-1
         LA    R2,DBTPRESV   POINT TO R14, R15, R0, R1 SEQUENCE
         LA    R3,16         DO THREE REGISTERS
         B     DBTFHEX       AND DO IT SHORTLY
DBTTTYP  CLI   8(&WK),2      TEXT, REGS OR HEX ?
         BL    DBTFTEXT      TEXT
         BH    DBTFHEX       HEX
         LA    R2,&WA        REGS
         LA    R3,16*4
DBTFHEX  BASR  R6,(0)        SET RETURN ADDRESS                  92271
         SLR   R4,R4
         LA    R1,DBTWTOT    POINT TO TEXT
         LA    R5,16         MAX INPUT CHARACTERS ON LINE        92093
DBTFHEXL LTR   R3,R3         ANY MORE TO DO ?
         BNP   DBTLAST
         UNPK  0(3,R1),0(2,R2)  UNPACK ONE BYTE
         TR    0(2,R1),DBTHEXTB-C'0'  MAKE IT PRINTABLE
         MVI   2(R1),C' '    BLANK NEXT
         LA    R2,1(,R2)
         LA    R1,2(,R1)
         BCTR  R3,0          ADJUST RESIDUAL COUNT
         LA    R4,1(,R4)     ADD ONE TO COUNT DONE
         LA    R15,3
         NR    R15,R4        END OF A WORD ?
         BNZ   *+8           NO
         LA    R1,1(,R1)     LEAVE A GAP BETWEEN WORDS
         CR    R4,R5         DONE ONE LINE ?                     92093
         BNL   DBTLAST       YES; PROCESS IT
         B     DBTFHEXL      SEE IF MORE TO DO
DBTFTEXT LA    R4,L'DBTWTOT  SET MAXIMUM TEXT PRINTABLE
         BASR  R6,(0)        SET RETURN POINT                    92271
         LTR   R3,R3         ANY MORE ?
         BNP   DBTLAST       NO; EXIT
         LR    R15,R4        SET TEXT LENGTH
         CR    R3,R15        IS REQUEST LONGER ?
         BNL   *+6           YES
         LR    R15,R3        ELSE USE SHORTER
         BCTR  R15,0         SET EXECUTE LENGTH
         EX    R15,DBTFTMVC  MOVE TO LINE
         AR    R2,R4         UP TEXT ADDRESS
         SR    R3,R4         SET RESIDUAL PRINT LENGTH
         B     DBTLAST       PRINT IT
DBTFTMVC MVC   DBTWTOT(0),0(R2)  MOVE USER'S TEXT
DBTLAST  CLC   DBTWTON(DBTWTOC-DBTWTON),DBTWTON-1  ANYTHING TO PRINT ?
         BE    DBTLASTL      NO
         AIF   (T'&DEV EQ 'O').NOPRT  NO PRT FILE # - WTO        92284
         AIF   ('&DEV' EQ '0').NOPRT  NO PRT FILE # - WTO        95067
         PRTV  DBTWTO,CC=NO,DEV=&DEV  PRINT ON REQUESTED FILE  GP95226
         AGO   .NOWTO                                            92284
.NOPRT   AIF   (&BUGSWRT).NOPRT2           ROUTING/DESCRIPTORS   95067
         AIF   ('&ROUT' EQ '').NOROUT   NO ROUTING/DESCRIPTORS   95067
&BUGSWRT SETB  1                                                 95067
.NOPRT2  MVC   DBTWTOC(4),DBTRTDSC  ADD ROUTING/DESCRIPTOR CODE  95067
.NOROUT  WTO   MF=(E,DBTWTO)   WRITE TO CONSOLE                  95067
.NOWTO   ANOP  ,                                                 92284
DBTLASTL MVC   DBTWTON(DBTWTOC-DBTWTON),DBTWTON-1  BLANK IT      93357
         MVC   DBTWTOM,DBTWTON  CLEAR MESSAGE NUMBER             93357
         MVI   DBTWTOM+5,C'+' INDICATE CONTINUATION              93357
         LTR   R3,R3         ANY MORE TO DO ?
         BPR   R6            RESTART
DBTEXIT  LA    &WK,6(,&WK)   SKIP TO NEXT OPTION ENTRY
         CLI   8(&WK),3      VALID REQUEST ?
         BNH   DBTLOOP       YES; DO IT
DBTEXITS LM    R12,R15,DBTLOCSV  RESTORE BASE AND RETURN
         BR    R14           AND RETURN TO USER
DBTWPAT  WTO   'MSG666 ',ROUTCDE=&ROUT,DESC=&DES,MF=L
DBTPATL  EQU   *-DBTPAT
DBTHEXTB DC    C'0123456789ABCDEF'
         ORG   DBTWPAT
DBTPAT   DC    Y(56+9*&BUGTCB)  TRUE LENGTH AFTER MOVE
DBTRTDSC EQU   DBTHEXTB-4,4,C'X'  DESCRIPTOR/ROUTCDE, IF ANY
         ORG   DBTHEXTB+L'DBTHEXTB  RESUME
.*       POP   PRINT
.*       POP   USING
.TESTDC  AIF   ('&MODE' NE 'DC').MEND                            95067
         AGO   .NODRTE                                           95067
.DATA    AIF   ('&NM' EQ '').NODLBL
&NM      DS    0D
.NODLBL  AIF   ('&ROUT' EQ '').NODRTE                            95067
&BUGSWRT SETB  1                                                 95067
.NODRTE  AIF   (&BUGFAR OR '&MODE' EQ 'M').MEND                  95230
DBTPRESV DC    2F'0'  1/2    FOR SHORT FORMATTING                95230
.NOSVPFX ANOP  ,                                                 95230
&WA      DC    16F'0' 2/2    DEBUG SAVE AREA                    GP97265
DBTFLAG  DC    AL1(&BUGSWCH*DBTFLON+DBTFLWID)  DEBUG FLAG       GP98222
DBTFLTCB EQU   128             INCLUDE TCB ADDRESS IN MESSAGE
DBTFLWTO EQU   64              USE WTO INSTEAD OF PRT            95240
DBTFLWID EQU   32              USE WIDE FORMAT WHEN PRINTING    GP98222
DBTFLPRO EQU   16              PRODUCTION MODE / NEED DD TO PRT GP99113
DBTFLEND EQU   2               THIS IS A TERMINATION CALL       GP98222
DBTFLON  EQU   1               DEBUG BIT
DBTFLAG2 DC    AL1(0)        ..RESERVED..                       GP99062
DBTFLAG3 DC    AL1(0)        ..RESERVED..                       GP99062
DBTFLAG4 DC    AL1(0)        ..RESERVED..                       GP99062
DBTCNT1  DC    A(&COUNT(1)+0)  COUNT OPTION                      95228
DBTCNT2  DC    A(&COUNT(2)+0)  COUNT OPTION                      95228
DBTCNT3  DC    A(&COUNT(3)+0)  COUNT OPTION                      95228
         MAPCMPRT PFX=DBT,DCB=&DCB,PRTMODE=&PRTMODE,DEV=&DEV    GP99113
         AIF   (NOT &BUGDYN).NODYNS                             GP97261
.BUGDYN  WXTRN DEBTRACE      SUPPORT LINKED-IN VERSION          GP97262
@DEBTRAC DC    A(DEBTRACE)   ADDRESS OF LOADED DEBTRACE         GP97261
#DEBTRAC DC    CL8'DEBTRACE'  LOAD MODULE NAME                  GP97261
DBTLOCSV DC    6A(0)         SAVE AREA                          GP97265
         AGO   .MEND                                            GP97262
.NODYNS  AIF   (&BUGFAR OR '&MODE' EQ 'M').MEND                  95228
DBTLOCSV DC    4F'0'         BASE SAVE
DBTWTO   DC    Y(56,0)       VCON                                93357
DBTWTOM  DC    C'MSG666 '    DEBUG HEADER                        93357
DBTWTON  DC    CL8' ',C' '   USER'S LABEL
DBTWTOT  DC    CL36' '       USER'S HEX OR EBCDIC TEXT
         AIF   ('&TCB' NE 'YES' AND NOT &BUGTCB).NOTCB           94011
         DC    C' '          EXTRA FOR UNPACK                    94011
DBTWTCB  DC    CL8' '        CURRENT TCB ADDRESS                 94011
.NOTCB   ANOP  ,                                                 94011
DBTWTOC  DC    C' '          EXTRA FOR UNPACK
         AIF   (NOT &BUGSWRT).MEND  NO ROUTING CODE              95067
         DC    XL3'0'        EXTRA FOR DESCRIPTOR/ROUTING CODES  95067
.MEND    MEND  ,
