         MACRO ,
&NM      XCLFIND &NAME,&QUAL,&TYPE=0
.*
.*   This macro locates an item in the termiation clean-up chain,
.*   or return 0 in R15 when the item is not found.
.*
.*   Each item has a 16-byte prefix: link,subpool/size, type, name
.*
.*   NAME  - ADDRESS OF THREE BYTE NAME, OR QUOTED
.*
         LCLC  &ZZ,&Q
&ZZ      SETC  'ZZ'.'&SYSNDX'
&NM      STM   R0,R7,12(R13)      SAVE SOME REGISTERS
         LA    R15,EXWCLEAN       POINT TO CLEANUP CHAIN
&ZZ.L    ICM   R15,15,0(R15)      GET NEXT ELEMENT
         BZ    &ZZ.X                NOT FOUND
         CLI   8(R15),&TYPE
         BNE   &ZZ.L
         AIF   ('&NAME'(1,1) NE '''').VAR
&Q       SETC  '=C'
.VAR     CLC   9(3,R15),&Q&NAME
         BNE   &ZZ.L
&ZZ.X    LM    R0,R7,12(R13)      0 OR ADDRESS IN R15
.MEND    MEND  ,
