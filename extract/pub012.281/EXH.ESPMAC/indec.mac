         MACRO
&NM      INDEC &TO,&FROM,&FL
         GBLB  &INLINE(50)
         GBLC  &MACPLAB
.**********************************************************************
.*                                                                   **
.*   INLINE DECIMAL CONVERSION FROM INTEGER TO EBCDIC                **
.*                                                                   **
.**********************************************************************
         LCLC  &L
&MACPLAB SETC  '&NM'
&INLINE(11) SETB 1
&L       SETC  'L'''
         AIF   ('&FROM'(1,1) EQ '(').L
         AIF   ('&FL' EQ '').DEF
         AIF   ('&FL' EQ '1').IC
         AIF   ('&FL' EQ '2').LH
         AIF   ('&FL' EQ '3').LA
         AIF   ('&FL' EQ '4').L
         MNOTE 4,'INVALID LENGTH ON INDEC'
         MEXIT
.IC      ANOP
         MACPARM R0,(R0),MODE=EVEN  CLEAR FOR IC
         IC    R0,&FROM .       LOAD VALUE
         AGO   .COM
.DEF     ANOP
.LH      ANOP
         MACPARM R0,&FROM,OP=LH
         AGO   .COM
.LA      ANOP
         MACPARM DB+1(3),&FROM,MODE=EVEN,OP=MVC
         MVC   DB+1(3),&FROM .     MOVE VALUE
         MVI   DB,0 .        CLEAR HIGH BYTE
         L     R0,DB .       LOAD VALUE
         AGO   .COM
.L       ANOP
         MACPARM R0,&FROM,OP=L
.COM     ANOP
         MACPARM R0,DB,OP=CVD . CONVERT TO PACKED
         MVC   DCONWORK,DCONPAT .   MOVE EDIT PATTERN
         ED    DCONWORK,DB .   EDIT
         MVC   &TO,DCONWORK+16-&L&TO .   MOVE OUTPUT
         MEND
