         MACRO ,
       MACQOLIT &STR,&LEN=   DETERMINE LENGTH OF STRING; MAKE QUOTED
.*
.*   INNER MACRO FOR MACRO PROCESSING
.*       MACQOLIT &STR  WHERE &STR IS UNQUOTED, QUOTED, OR CONSTANT
.*                 FORMAT (E.G.,  XYZ, 'text', X'12AB', CL8'HI', =C'A')
.*   RETURNS:
.*       MACPNUL   FOR OMITTED PARAMETER OR EMPTY STRING ('')
.*       MACQUOT=0  IF UNQUOTED; &STR IN MACQSTR, K'&STR IN MACPLEN
.*       MACQUOT=1  =CLnn'text' OR =X'hex' - LITERAL FORMAT
.*
         GBLA  &MACPLEN      RETURN SIGNIFICANT LENGTH OF STRING
         GBLB  &MACPNUL      TRUE IF NULL STRING
         GBLB  &MACQUOT      TRUE IF ORIGINAL WAS QUOTED
         GBLB  &MACPERR      TRUE IF ERROR
         GBLC  &MACQSTR      RETURN QUOTED STRING
         LCLA  &I,&J,&K,&L
         LCLC  &C,&D,&TYPE
.*
&MACQUOT SETB  0             SET UNQUOTED
&MACPERR SETB  0             SET NOT IN ERROR
&MACPLEN SETA  K'&STR        SET PROVISIONAL LENGTH
&MACQSTR SETC  '&STR'        DEFAULT - RETURN AS IS
&TYPE    SETC  'C'           SET STRING TYPE (C OR X) DEFAULT
.*  RETURN IF STRING IS NULL
&MACPNUL SETB  (T'&STR EQ 'O')
&MACPNUL SETB  (&MACPNUL OR ('&STR' EQ ''''''))
         AIF   (&MACPNUL).MEND   DONE IF NULL STRING
.*  RETURN IF STRING IS UNQUOTED
         AIF   (&MACPLEN LT 2).SHORT
&MACQUOT SETB  ('&STR'(1,1) EQ '''' OR '&STR'(&MACPLEN,1) EQ '''')
.SHORT   AIF   (NOT &MACQUOT).MEND
.*  DELETE LITERAL'S EQUAL SIGN IF PRESENT
         AIF   ('&STR'(1,1) NE '=').NOTEQU
&MACQSTR SETC  '&MACQSTR'(2,&MACPLEN)  STRIP EQUAL
&MACPLEN SETA  K'&MACQSTR              UPDATE LENGTH
.*  LOOK FOR LEADING QUOTE, C OR X - FAIL REST
.NOTEQU  ANOP  ,
&C       SETC  '&MACQSTR'(1,1)         ISOLATE FIRST BYTE
&MACPERR SETB  ('&C' NE '''' AND '&C' NE 'C' AND '&C' NE 'X')
         AIF   (&MACPERR).ERROR
.*  LOOK FOR LEADING QUOTE, C OR X - FAIL REST
.STRING  AIF   ('&C' EQ '''').COUNT
&TYPE    SETC  '&C'                    REMEMBER THE TYPE
&MACQSTR SETC  '&MACQSTR'(2,K'&MACQSTR)  STRIP TYPE
&MACPLEN SETA  K'&MACQSTR              UPDATE LENGTH
&C       SETC  '&MACQSTR'(1,1)         ISOLATE FIRST BYTE
&MACPERR SETB  ('&C' NE '''' AND '&C' NE 'L')
         AIF   (&MACPERR).ERROR
.*  LOOK FOR LEADING QUOTE OR L  (I.E., WE WANT 'text' OR CLn'text')
         AIF   ('&C' EQ '''').COUNT    COUNT LENGTH
&L       SETA  0                       NO LENGTH YET
.EXPLOOP ANOP  ,
&MACQSTR SETC  '&MACQSTR'(2,K'&MACQSTR)  STRIP TYPE
&MACPLEN SETA  K'&MACQSTR              UPDATE LENGTH
&C       SETC  '&MACQSTR'(1,1)         ISOLATE FIRST BYTE
         AIF   ('&C' EQ '''').HAVEXPL  DONE WITH EXPLICIT LENGTH
         AIF   ('&C' LT '0' OR '&C' GT '9').ERROR
&L       SETA  &L*10+&C                UPDATE LENGTH
         AGO   .EXPLOOP                TRY ONE MORE
.*   MACQSTR NOW HAS QUOTED STRING, AND L HAS THE LENGTH
.HAVEXPL ANOP  ,
&MACPLEN SETA  &L                      RETURN THE LENGTH
&MACQSTR SETC  '='.'&TYPE'.'L'.'&L'.'&MACQSTR'
         MEXIT ,
.ERROR   ANOP  ,
&MACPERR SETB  1                       RETURN AN ERROR
         MEXIT ,
.*   MACQSTR IS A QUOTED STRING WHOSE LENGTH WE NEED
.*     NOTE THAT APOSTROPHES AND AMPERSANDS ARE DOUBLED (ELSE ERROR)
.COUNT   ANOP  ,
&MACPLEN SETA  K'&MACQSTR              UPDATE LENGTH
&I       SETA  1                       LOOP INDEX (2 TO K'-2)
&L       SETA  0                       SIGNIFICANT LENGTH
.CNTLOOP  ANOP ,
&I       SETA  &I+1
&C       SETC  '&MACQSTR'(&I,1)
         AIF   ('&C' NE '''' AND '&C' NE '&&').CNTONE
         AIF   (&I GE &MACPLEN-1).ERROR
         AIF   ('&MACQSTR'(&I+1,1) NE '&C').ERROR    ERROR?
&I       SETA  &I+1          SKIP DOUBLED CARACTER
.CNTONE  ANOP  ,
&L       SETA  &L+1
         AIF   (&I LE &MACPLEN-2).CNTLOOP
&MACPLEN SETA  &L            SET STRIPPED LENGTH
         AIF   ('&LEN' EQ '').DEFLN                             GP08090
&MACQSTR SETC  '='.'&TYPE'.'L('.'&LEN'.')'.'&MACQSTR'           GP08090
&MACPLEN SETA  &LEN                                             GP08090
         AGO   .MEND                                            GP08090
.DEFLN   ANOP  ,                                                GP08090
&MACQSTR SETC  '='.'&TYPE'.'&MACQSTR'
.MEND    MEND  ,
