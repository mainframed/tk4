         MACRO
         INSECT &DB=DB,&TRANS=YES                                88025
         GBLB  &INLINE(50)
         COPY  OPTIONS
         LCLC  &WK           DOUBLE-WORD WORK SPACE              88025
         LCLB  &INMOVE,&INWORK                                  GP09347
&WK      SETC  '&DB'         USE USER'S                          88025
         AIF   ('&WK' NE '').HAVEWK    OK                        88025
&WK      SETC  'DCONDB'      GENERATE LOCALLY                    88025
.*       (1)   EXPANSION SWITCH
.*       (2)   SPACING INITIALIZATION CALL
.*       (3)   SPACING, LINE ADVANCE, ETC. CALL
.*       (4)   CNVP               PACKED DECIMAL -> PRINT       GP15253
.*       (5)   INLINE HEX EXPANSION
.*       (6)   CVH / CNVD CALL    BINARY INTEGER -> PRINT
.*       (7)   CNVR / CNVX CALL   HEX -> PRINT
.*       (8)   CVI - FORMAT INTEGER WITH COMMAS AND SCALING
.*       (9)   TRT - NEED TRANSLATE TABLE
.*       (10)  CALL TO RANDOM
.*       (11)  INLINE DECIMAL EXPANSION
.*
.HAVEWK  AIF   (NOT &INLINE(1)).OK
         MNOTE 4,'MULTIPLE ''INSECT'' CALLS INVALID'
         MEXIT
.OK      AIF   (&INLINE(2) AND &INLINE(3)).OK1
         AIF   (NOT &INLINE(2) AND NOT &INLINE(3)).OK3
         MNOTE 4,'INCONSISTENT USE OF ''SP XXXX'' CALLS'
.OK1     XSUBSPAC TRANS=&TRANS                                  GP15270
.OK3     ANOP  ,                                                GP09347
&INLINE(5) SETB (&INLINE(5) OR &INLINE(11))                     GP09347
&INWORK  SETB  (&INLINE(5))                                     GP10037
.OK2     AIF   (NOT &INLINE(4)).OK2D                            GP15253
         SPACE 2
*        CONVERT PACKED TO EBCDIC                               GP15253
*        VALUE IN DB    (1) O/P ADDRESS     (15) - O/P LENGTH   GP15253
*                                                               GP15253
PCONVERT STM   R2,R3,28(R13)                                    GP15253
&INWORK  SETB  1                                                GP15253
&INMOVE  SETB  1                                                GP15253
&INLINE(6) SETB 1                                               GP15253
         B     PCONCOM       EDIT                               GP15253
.OK2D    AIF   (NOT &INLINE(6)).OK6
         SPACE 2
*        CONVERT BINARY TO EBCDIC
*        (0) - VALUE    (1) O/P ADDRESS     (15) - O/P LENGTH
*
DCONVERT STM   R2,R3,28(R13)
&INWORK  SETB  1                                                GP09347
&INMOVE  SETB  1                                                GP09347
         CVD   R0,&WK        MAKE VALUE PACKED
PCONCOM  MVC   DCONWORK(16),DCONPAT
         ED    DCONWORK,&WK
         AIF   (NOT &INLINE(7) AND NOT &INLINE(8)).DOCON        GP09347
         B     DCONCOM       GO TO OUTPUT MOVE
.OK6     AIF   (NOT &INLINE(8)).OK8
         SPACE 2
*        CONVERT BINARY TO EBCDIC WITH PERIODS & SCALING        GP09347
*        (0) - VALUE    (1) O/P ADDRESS     (15) - O/P LENGTH   GP09347
*                                                               GP09347
ICONVERT STM   R2,R3,28(R13)                                    GP09347
&INWORK  SETB  1                                                GP09347
&INMOVE  SETB  1                                                GP09347
         CVD   R0,&WK        MAKE VALUE PACKED                  GP09347
         MVC   DCONWORK-1(17),DCOMPAT                           GP09347
         ED    DCONWORK-1(17),&WK                               GP09347
         CLI   DCONWORK+8,C' '    LESS THAN 1M?                 GP09347
         BE    DCONCOM       YES; MOVE IT                       GP09347
         LA    R2,DCONWORK+13                                   GP09347
         MVI   DCONWORK+12,C'K'                                 GP09347
         SR    R2,R15                                           GP09347
         LR    R3,R15                                           GP09347
         BCTR  R3,0                                             GP09347
         EX    R3,DCONMOVE                                      GP09347
         LM    R2,R3,28(R13)                                    GP09347
         BR    R14                                              GP09347
DCOMPAT  DC    X'402020202020202020202020206B202120'            GP09347
.OK8     AIF   (NOT &INLINE(7)).DOCON                           GP09347
         SPACE 2
*        CONVERT HEX TO EBCDIC
*        (0) - INPUT ADDR.   (1) - O/P ADDR.   (15) - O/P LENGTH
*
XCONVERT STM   R2,R3,28(R13)
         LA    R3,1(,R15) .     O/P LEN + 1
         SRA   R3,1 .        /2
         AR    R3,R0 .        I/P + 1/2  O/P LEN
         SH    R3,DCONH8     - 8
&INWORK  SETB  1                                                GP09347
&INMOVE  SETB  1                                                GP09347
         UNPK  DCONWORK(9),0(5,R3) .    UNPACK FIRST WORD
         UNPK  DCONWORK+8(9),4(5,R3) .     SECOND WORD
         TR    DCONWORK(16),HEXTAB .    MAKE VIEWABLE
         SPACE
.DOCON   AIF   (NOT &INMOVE).OK6Z
DCONCOM  LA    R2,DCONWORK+16
         SR    R2,R15
         LR    R3,R15
         BCTR  R3,0
&INMOVE  SETB  1                                                GP09347
         EX    R3,DCONMOVE
         LM    R2,R3,28(R13)
         BR    R14
         SPACE
.OK6Z    AIF   ('&DB' NE '').OKWK                                88025
DCONDB   DC    D'0'          LOCAL WORK AREA                     88025
.OKWK    ANOP  ,                                                 88025
DCONPAT  DC    C' ',13X'20',X'2120' .     EDIT MASK
         AIF   (NOT &INLINE(11) AND NOT &INMOVE).OK7            GP09347
DCONMOVE MVC   0(0,R1),0(R2)
DCONH8   DC    H'8'
.OK7     AIF   (NOT &INWORK).B8                                 GP09347
         DS    C       1/2                                      GP09347
DCONWORK DS    CL16    2/2                                      GP09347
         DS    C
         ORG   DCONWORK
UPD      DS    CL9
         ORG
HEXTAB   EQU   *-C'0'
         DC    C'0123456789ABCDEF'
.B8      AIF   (NOT &INLINE(9)).OK9
TRTAB    TRTAB CODE=&CRT,OPT=ERR
.OK9     AIF   (NOT &INLINE(10)).OK10
         RANDOM
.OK10    ANOP
         MEND
