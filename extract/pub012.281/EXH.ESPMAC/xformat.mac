         MACRO
         XFORMAT &DB=DB                                         GP13008
         GBLB  &INLINE(50)
         COPY  OPTIONS
         LCLC  &WK           DOUBLE-WORD WORK SPACE              88025
         LCLB  &INMOVE,&INWORK                                  GP09347
&WK      SETC  '&DB'         USE USER'S                          88025
         AIF   ('&WK' NE '').HAVEWK    OK                        88025
&WK      SETC  'DCONDB'      GENERATE LOCALLY                    88025
.*       (1)   EXPANSION SWITCH
.*       (4)   CNVP               PACKED DECIMAL -> PRINT       GP15253
.*       (5)   INLINE HEX EXPANSION
.*       (6)   CVH / CNVD CALL    BINARY REGISTER -> PRINT
.*       (7)   CNVR / CNVX CALL   REGISTER/STORAGE HEX -> PRINT
.*       (8)   CVI - FORMAT INTEGER WITH COMMAS AND SCALING
.*       (9)   IN-LINE TRANSLATE TABLE (3278 CHARS)
.*       (10)  CALL TO RANDOM
.*       (11)  INLINE DECIMAL EXPANSION
.*       (12)  INTEGER + 1 DECIMAL                              GP13008
.*       (13)  INTEGER + 2 DECIMALS                             GP13008
.*       (14)  INTEGER + 3 DECIMALS                             GP13008
.*
.HAVEWK  AIF   (NOT &INLINE(1)).OKONCE                          GP13008
         MNOTE 4,'MULTIPLE ''INSECT/XFORMAT'' CALLS INVALID'    GP13008
         MEXIT ,
.OKONCE  ANOP  ,                                                GP13008
&INLINE(1) SETB 1            SHOW EXPANSION REQUESTED           GP13008
         AIF   (&INLINE(2) AND &INLINE(3)).OK1
         AIF   (NOT &INLINE(2) AND NOT &INLINE(3)).OK3
         MNOTE 4,'INCONSISTENT USE OF ''SP XXXX'' CALLS'
.OK1     XSUBSPAC TRANS=&TRANS                                  GP15270
         MNOTE 0,'REPLACE SPxxxx CALLS BY XLINE'                GP13008
.OK3     ANOP  ,                                                GP09347
&INLINE(5) SETB (&INLINE(5) OR &INLINE(11))                     GP09347
&INWORK  SETB  (&INLINE(5))                                     GP10037
.OK2     AIF   (NOT &INLINE(4)).OK2D                            GP15253
         SPACE 2
*        CONVERT PACKED TO EBCDIC                               GP15253
*        VALUE IN DB    (1) O/P ADDRESS     (15) - O/P LENGTH   GP15253
*                                                               GP15253
PCONVERT STM   R2,R3,28(R13)                                    GP15253
&INWORK  SETB  1                                                GP15253
&INMOVE  SETB  1                                                GP15253
&INLINE(8) SETB 1                                               GP15253
         MVC   DCONWORK-1(17),DCOMPAT                           GP15253
         B     PCONCOM       EDIT                               GP15253
.OK2D    AIF   (NOT &INLINE(12)).OK6P1                          GP13008
         SPACE 1
*        CONVERT BINARY TO EBCDIC  NNN.N                        GP13008
*        (0) - VALUE    (1) O/P ADDRESS     (15) - O/P LENGTH   GP13008
*                                                               GP13008
DCONVONE STM   R14,R3,12(R13)                                   GP13008
         MVC   DCONWORK-1(17),DCONPONE                          GP13008
         B     DCONPCOM                                         GP13008
DCONPONE DC    C' ',12X'20',X'21204B20' .   EDIT MASK           GP13008
.OK6P1   AIF   (NOT &INLINE(13)).OK6P2                          GP13008
         SPACE 1
*        CONVERT BINARY TO EBCDIC  NNN.NN                       GP13008
*        (0) - VALUE    (1) O/P ADDRESS     (15) - O/P LENGTH   GP13008
*                                                               GP13008
DCONVTWO STM   R14,R3,12(R13)                                   GP13008
         MVC   DCONWORK-1(17),DCONPTWO                          GP13008
         B     DCONPCOM                                         GP13008
DCONPTWO DC    C' ',11X'20',X'21204B2020' .   EDIT MASK         GP13008
.OK6P2   AIF   (NOT &INLINE(14)).OK6COM                         GP13008
         SPACE 1
*        CONVERT BINARY TO EBCDIC  NNN.NNN                      GP13008
*        (0) - VALUE    (1) O/P ADDRESS     (15) - O/P LENGTH   GP13008
*                                                               GP13008
DCONVTRE STM   R14,R3,12(R13)                                   GP13008
         MVC   DCONWORK-1(17),DCONPTRE                          GP13008
         B     DCONPCOM                                         GP13008
DCONPTRE DC    C' ',10X'20',X'21204B202020' . EDIT MASK         GP13008
.OK6COM  AIF (NOT &INLINE(12) AND NOT &INLINE(13) AND NOT &INLINE(14)).*
               OK6P                                             GP13008
DCONPCOM CVD   R0,&WK        MAKE VALUE PACKED                  GP13008
         ED    DCONWORK-1(17),&WK                               GP13008
&INWORK  SETB  1                                                GP13008
&INMOVE  SETB  1                                                GP13008
         AIF   (NOT &INLINE(6)).OK6P                            GP13008
         B     DCONCOM       GO TO OUTPUT MOVE
.OK6P    AIF   (NOT &INLINE(6)).OK6
         SPACE 2
*        CONVERT BINARY TO EBCDIC
*        (0) - VALUE    (1) O/P ADDRESS     (15) - O/P LENGTH
*
DCONVERT STM   R14,R3,12(R13)
         MVC   DCONWORK(16),DCONPAT                             GP15253
DCONVCOM CVD   R0,&WK        MAKE VALUE PACKED
         ED    DCONWORK,&WK
&INWORK  SETB  1                                                GP09347
&INMOVE  SETB  1                                                GP09347
         AIF   (NOT &INLINE(7) AND NOT &INLINE(8)).DOCON        GP09347
         B     DCONCOM       GO TO OUTPUT MOVE
.OK6     AIF   (NOT &INLINE(8)).OK8
         SPACE 2
*        CONVERT BINARY TO EBCDIC                               GP09347
*        (0) - VALUE    (1) O/P ADDRESS     (15) - O/P LENGTH   GP09347
*                                                               GP09347
ICONVERT STM   R14,R3,12(R13)                                   GP09347
&INWORK  SETB  1                                                GP09347
&INMOVE  SETB  1                                                GP09347
         CVD   R0,&WK        MAKE VALUE PACKED                  GP09347
         MVC   DCONWORK-1(17),DCOMPAT                           GP09347
PCONCOM  ED    DCONWORK-1(17),&WK                               GP09347
         CLI   DCONWORK+8,C' '    LESS THAN 1M?                 GP09347
         BE    DCONCOM       YES; MOVE IT                       GP09347
         LA    R2,DCONWORK+13                                   GP09347
         MVI   DCONWORK+12,C'K'                                 GP09347
         SR    R2,R15                                           GP09347
         LR    R3,R15                                           GP09347
         BCTR  R3,0                                             GP09347
         EX    R3,DCONMOVE                                      GP09347
         LM    R14,R3,12(R13)                                   GP09347
         BR    R14                                              GP09347
DCOMPAT  DC    X'402020202020202020202020206B202120'            GP09347
.OK8     AIF   (NOT &INLINE(7)).DOCON                           GP09347
         SPACE 2
*        CONVERT HEX TO EBCDIC
*        (0) - INPUT ADDR.   (1) - O/P ADDR.   (15) - O/P LENGTH
*
XCONVERT STM   R14,R3,12(R13)
         LA    R3,1(,R15) .     O/P LEN + 1
         SRA   R3,1 .        /2
         AR    R3,R0 .        I/P + 1/2  O/P LEN
         SH    R3,DCONH8     - 8
&INWORK  SETB  1                                                GP09347
&INMOVE  SETB  1                                                GP09347
         UNPK  DCONWORK(9),0(5,R3) .    UNPACK FIRST WORD
         UNPK  DCONWORK+8(9),4(5,R3) .     SECOND WORD
         TR    DCONWORK(16),HEXTAB .    MAKE VIEWABLE
         SPACE
.DOCON   AIF   (NOT &INMOVE).OK6Z
DCONCOM  LA    R2,DCONWORK+16
         SR    R2,R15
         LR    R3,R15
         BCTR  R3,0
&INMOVE  SETB  1                                                GP09347
         EX    R3,DCONMOVE
         LM    R14,R3,12(R13)
         BR    R14
         SPACE
.OK6Z    AIF   ('&DB' NE '').OKWK                                88025
DCONDB   DC    D'0'          LOCAL WORK AREA                     88025
.OKWK    ANOP  ,                                                 88025
DCONPAT  DC    C' ',13X'20',X'2120' .     EDIT MASK
         AIF   (NOT &INLINE(11) AND NOT &INMOVE).OK7            GP09347
DCONMOVE MVC   0(0,R1),0(R2)
DCONH8   DC    H'8'
.OK7     AIF   (NOT &INWORK).B8                                 GP09347
         DS    C       1/2                                      GP09347
DCONWORK DS    CL16    2/2                                      GP09347
         DS    C
         ORG   DCONWORK
UPD      DS    CL9
         ORG
HEXTAB   EQU   *-C'0'
         DC    C'0123456789ABCDEF'
.B8      AIF   (NOT &INLINE(9)).OK9
TRTAB    TRTAB CODE=&CRT,OPT=ERR
.OK9     AIF   (NOT &INLINE(10)).OK10
.*  RANDOM IS DEPRECATED - USE XRAND MACRO AND SERVICE
         RANDOM ,
.OK10    ANOP
         MEND
