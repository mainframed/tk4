         MACRO ,
&NM      MACPLOP &FROM,&TO,&NULL=,&OP=ST
.**********************************************************************
.*                                                                    *
.*  MACPLOP IS AN INNER MACRO (PART OF THE MACPARM GROUP) FOR         *
.*  PROCESSING USER SUPPLIED VALUES FOR AN MF=E/S PARAMETER LIST.     *
.*                                                                    *
.*  1) &FROM IS (R) - GENERATE SIMPLE STORE                           *
.*  2) &FROM IS QUOTED STRING - GENERATE LA/ST                        *
.*  3) OTHER - LA/ST                                                  *
.*                                                                    *
.**********************************************************************
         LCLC  &V
         LCLA  &K
&V       SETC  '&FROM'
         AIF   ('&V' NE '').TEST
&V       SETC  '&NULL'
         AIF   ('&V' NE 'YES').FSKIP
&V       SETC  '0'
.FSKIP   AIF   ('&V' EQ 'SKIP').MEND   SKIP IF OMITTED OK
         AIF   ('&V' NE '').TEST
 MNOTE 8,'MACPLOP: SOURCE ADDRESS REQUIRED'
         MEXIT ,
.TEST    AIF   ('&TO' NE '').BOTH
 MNOTE 8,'MACPLOP: DESTINATION ADDRESS REQUIRED'
         MEXIT ,
.BOTH    AIF   ('&V'(1,1) EQ '''').QUOTE
&K       SETA  K'&V
         AIF   (&K LT 3).NORMAL
         AIF   ('&V'(1,1) NE '(' OR '&V'(2,1) EQ '(').NORMAL
         AIF   ('&V'(&K,1) NE ')' OR '&V'(&K-1,1) EQ ')').NORMAL
&NM      MACPARM &V,&TO,OP=ST,OPR=ST,MODE=EVEN
         MEXIT ,
.NORMAL  ANOP  ,
&NM      MACPARM R0,&V,OP=LA
         AGO   .ST
.QUOTE   ANOP  ,
&NM      MACPARM R0,=C&V
.ST      MACPARM R0,&TO,OP=&OP
.MEND    MEND  ,
