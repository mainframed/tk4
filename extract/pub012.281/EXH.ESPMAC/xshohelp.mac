         MACRO ,
&NM      XSHOHELP &TEXT,&LEN,&EXIT=EXCIN,&PROMPT=EXMORE,&WK=R5
.*       ASSUMES TEXT IS N*CL80; MOVED WITH XLINE
.*  R10 CHANGED TO &WK (FOR EXHCCSUD)                           GP05337
         LCLA  &I                                                87194
&I       SETA  &SYSNDX                                           87194
&NM      MACPARM R4,&TEXT    LOAD TEXT ADDRESS
         MACPARM &WK(1),&LEN,OP=LH                              GP13257
         ZI    EXCPRIV,EXCPLOOP   RESET LOOP MODE               GP16015
ZZZ&I.PG LA    R2,80         LOGICAL INPUT LINE SIZE             87194
ZZZ&I.LN LR    R0,&WK        COPY RESIDUAL LENGTH                87194
         MIN   R0,(R2)       LOAD SHORTER OF LINE OR LENGTH      87194
         XLINE (R4),(R0),OPT=NL   WRITE THE LINE                GP13257
         BNP   ZZZ&I.XP      DISPLAY SCREEN WHEN FULL           GP13257
         AR    R4,R2         NEXT INPUT                          87194
         SR    &WK,R2        RESIDUAL INPUT LENGTH               87194
         BP    ZZZ&I.LN        DO ANOTHER LINE                   87194
         B     EXCYES          DISPLAY                          GP16015
ZZZ&I.XP STM   R0,R5,ZZZ&I.SV     SAVE LOOP DATA                GP15243
         XPUTGET &PROMPT,ENTER=EXCIN                            GP16015
          B    ZZZ&I.XE      ENTER ONLY - CONTINUE
         MACPARM &EXIT,MODE=1,OP=B,OPR=BR   ELSE TAKE EXIT
ZZZ&I.SV DC    6F'0'         SAVE LOOP DATA                     GP15243
ZZZ&I.XE LM    R0,R5,ZZZ&I.SV     SAVE LOOP DATA                GP15243
         LTR   &WK,&WK       ANY MORE INPUT TEXT ?
         BNP   ZZZ&I.EX      EXIT IF DONE                        87194
         L     R15,BUFAD                                        GP15243
         AH    R15,LSIZE     START BLANKER AFTER TITLE LINE     GP15243
         BAL   R14,BLANKER1  AND CLEAR BUFFER                   GP15243
         XLINE FUN=INI       JUST INITIALIZE                    GP15243
ZZZ&I.BL CLC   0(80,R4),BLANKS  NEXT LINE BLANK ?                87194
         BNE   ZZZ&I.PG      NO; DO NEXT PAGE                    87194
         AR    R4,R2                                             87194
         SR    &WK,R2        SKIP OVER BLANK LINE                87194
         BP    ZZZ&I.BL                                          87194
ZZZ&I.EX DS    0H            EXIT FROM LOOP (ENTER, LOOP...)    GP13257
         MEND  ,
