         MACRO ,             EXPAND PARSER
&NM      DEBTRACY &VA,&VL,&VT,&VKO,&VKL,&DT,&LABEL=N,&VL80=0
.*  DEBTRACY IS USED AS AN INNER MACRO TO EXPAND ONE OPERAND, OR TO
.*    CHANGE THE DEFAULT VARIABLE TYPE, FOR THE DBT MACRO
         GBLA  &ZZZDBA#,&ZZZDBAN
         GBLB  &ZZZDBTF      (FIRST TIME FLAG)
         GBLB  &ZZZ80FG      SET WHEN END OF LIST GENERATED
         GBLC  &ZZZDBTV,&ZZZDBTT,&ZZZDBAT(40),&ZZZDBAI(40),&ZZZDBAL(40)
         LCLA  &LN,&I,&EN,&EM,&EO
         LCLC  &L,&ET,&EL,&EK,&DFT,&EKO,&EKL
&EK      SETC  '&VA'
&EM      SETA  K'&EK         LENGTH OF FIRST OPERAND
&DFT     SETC  '&DT'         SET TYPE OVERRIDE
&EKO     SETC  '0'           DEFAULT CHAIN POINTER OFFSET
&EKL     SETC  '4'           DEFAULT CHAIN POINTER LENGTH
.*  SEE WHETHER THE CURRENT VARIABLE NAME IS ONE OR TWO CHARACTERS,
.*  HAS NO LENGTH OR TYPE SPECIFICATIONS, AND APPEARS IN TYPE TABLE.
.*  IF SO, JUST RESET GLOBAL TYPE TO SPECIFIED ONE.
         AIF   (&EM GT 2).NOLOOPT
         AIF   (&EM LE 0).MEND
         AIF   ('&VL' NE '' OR  '&VT' NE '').NOLOOPT
.LOOPTYP AIF   (&I GE &ZZZDBA#).NOLOOPT  NOT A REDEFINITION
&I       SETA  &I+1
         AIF   ('&VA' NE '&ZZZDBAT(&I)').LOOPTYP                GP02365
&ZZZDBTT SETC  '&ZZZDBAI(&I)'  SET NEW DEFAULT
         MEXIT ,
.*  FIRST CHECK THE DEFAULT TYPE - IF NULL, ASSIGN THE GLOBAL ONE.
.*  IF GLOBAL IS NULL, SET GLOBAL AND LOCAL TO HEX (03)
.NOLOOPT AIF   ('&DFT' NE '').NONUTYP
&DFT     SETC  '&ZZZDBTT'    COPY GLOBAL TYPE
         AIF   ('&DFT' NE '').NONUTYP
&DFT     SETC  '10'          DEFAULT IS HEX
&ZZZDBTT SETC  '10'          DEFAULT IS HEX
.NONUTYP AIF   ('&VKO' EQ '').NONVKO
&EKO     SETC  '&VKO'
.NONVKO  AIF   ('&VKL' EQ '').NONVKL
&EKL     SETC  '&VKL'
.NONVKL  ANOP  ,
&L       SETC  'L'''
&EL      SETC  '&VL'
&EO      SETA  0             PRESET FOR NORMAL ADDRESSING MODE
&ET      SETC  '&DFT'        PRESET FOR DEFAULT
         AIF   ('&EK'(1,1) NE '/' AND '&EK'(1,1) NE '*').NOTIND GP03128
&EO      SETA  &EO+1         REQUEST INDIRECT ADDRESSING
&EK      SETC  '&EK'(2,&EM-1)  DELETE LEADING CONTROL BYTE
&EM      SETA  K'&EK         LENGTH OF FIRST OPERAND
.NOTIND  AIF   (&EM LE 0).MEND
         AIF   ('&EK'(&EM,1) NE '%').NOTA24
&EO      SETA  &EO+2         REQUEST FORCED 24-BIT ADDRESSING
&EK      SETC  '&EK'(1,&EM-1)  DELETE TRAILING CONTROL BYTE
&EM      SETA  K'&EK         LENGTH OF FIRST OPERAND
.NOTA24  AIF   (&EM LE 0).MEND
         AIF   ('&EK'(&EM,1) NE '?').NOTA31
&EO      SETA  &EO+4         REQUEST FORCED 31-BIT ADDRESSING
&EK      SETC  '&EK'(1,&EM-1)  DELETE TRAILING CONTROL BYTE
&EM      SETA  K'&EK         LENGTH OF FIRST OPERAND
.NOTA31  AIF   (&EM LE 0).MEND
&I       SETA  0
.TESTTYP AIF   (&I GE &ZZZDBAN).NOTYPED  NOT A VALID ENTRY
&I       SETA  &I+1
         AIF   ('&VT' NE '&ZZZDBAT(&I)').TESTTYP
&ET      SETC  '&ZZZDBAI(&I)'   SET NEW TYPE
         AGO   .EXPAND
.NOTYPED AIF   ('&VT' NE '').NOTYPO
&ET      SETC  '&DFT'           ESLE SET DEFAULT
         AGO   .EXPAND
.NOTYPO  MNOTE 4,'DEBTRACY: VARIABLE/REQUEST TYPE INVALID: &VT'
.*  NOW WE HAVE THE VARIABLE NAME AND THE TYPE. CHECK LENGTH
.EXPAND  AIF   ('&EL' NE '').HLEN
&EL      SETC  '&L'.'&EK'
.HLEN    AIF   ('&LABEL' EQ 'N').NOLABEL
         DC    X'0100',CL8'&VA '  ITEM LABEL
.NOLABEL ANOP  ,
&ZZZ80FG SETB  (&ZZZ80FG OR &VL80)
         AIF   ('&ZZZDBAL(&I)' EQ '6' OR '&ZZZDBAL(&I)' EQ '').LEN6
         AIF   ('&ZZZDBAL(&I)' EQ '2').LEN2
         AIF   ('&ZZZDBAL(&I)' EQ '4').LEN4
         AIF   ('&ZZZDBAL(&I)' EQ '8').LEN8
         MNOTE 'DEBTRACY: INVALID ENTRY PASSED: &VA,&VL,&VT,&VKO,&VKL'
.LEN8    AIF   ('&ZZZDBAI(&I)' NE '16').LEN811   NOT FLAG
.LEN820  DC    AL1(X'80'*&VL80+&ET,&EO),SL2(&EK,&EL,&EKO)
         MEXIT ,
.LEN811  DC    AL1(X'80'*&VL80+&ET,&EO),SL2(&EK,&EL),AL1(&EKO,&EKL)
         MEXIT ,
.LEN6    DC    AL1(X'80'*&VL80+&ET,&EO),SL2(&EK,&EL)            GP02365
         MEXIT ,
.LEN4    DC    AL1(X'80'*&VL80+&ET,&EO),SL2(&EK)
         MEXIT ,
.LEN2    DC    AL1(X'80'*&VL80+&ET,&EO)
.MEND    MEND  ,
