         MACRO
&NM     XTHINK &SET,&SAVE=,&XCTL=NO         RENAMED FROM HOLD ON 04234
.*   THIS MACRO SETS/RESETS HOLD MODE FOR EXHIBIT GAMES AND STUFF
.*   GENERATED CODE IS NON-REENTRANT (UNLESS RENT SAVE=)
.*     THREE SET PARAMETERS ARE SUPPORTED :
.*       SET   SETS LONG HOLD MODE
.*       RESET RESETS TO SHORT WAIT MODE
.*       ONSET SET HOLD MODE (TO BE RESET VIA ONUNIT CALL)
.*     WHEN SAVE=HALFWORD IS USED, CODE MAY BE REENTRANT
.*       XCTL= MAY BE USED FOR ONUNIT OPTION
.*
         LCLC  &SV
         LCLB  &SW
&SV      SETC  '&SAVE'       SET SAVE PARM
         AIF   (T'&SAVE NE 'O').USE    USER SUPPLIED SAVE PARM
&SV      SETC  'SAVEWAIT'    ELSE USE DEFAULT
&SW      SETB  1             SET SAVE AREA NAMED SWITCHED
.USE     AIF   ('&SET' EQ 'SET').GOSET
         AIF   ('&SET' EQ 'ONSET').GOON
         AIF   ('&SET' EQ 'RESET').GOOFF
         MNOTE 4,'XTHINK - SET ''&SET'' INVALID'
         MEXIT ,
.GOOFF   ANOP  ,
&NM      MVC   EXCTIMER,&SV    RESTORE ORIGINAL WAIT            GP04234
.MEX     MEXIT ,
.GOSET   ANOP  ,
&NM      MVC   &SV,EXCTIMER    SAVE CURRENT WAIT TIME           GP04234
         MVC   EXCTIMER,EXCTIMHI    SET HOLD MODE               GP04234
         AIF   (NOT &SW).MEX SKIP ALLOC IF NAMED AREA
         B     *+8           SKIP ALLOC
SAVEWAIT DC    AL4(0)        WAIT TIME SAVE AREA                GP04234
         MEXIT ,
.GOON    ANOP  ,
&NM      ONEXIT HOLDUNIT,XCTL=&XCTL   SET ONUNIT EXIT
         MVC   &SV,EXCTIMER    SAVE CURRENT WAIT TIME           GP04234
         MVC   EXCTIMER,EXCTIMHI    SET HOLD MODE               GP04234
         B     HOL&SYSNDX.A
         PUSH  USING                                            GP03007
         DROP  ,                                                GP03007
         NUSE  COMM,R8       BUT RESTORE EXCTIMER'S ADDRESSABILITY
         USING HOLDUNIT,R15  WAIT TIME RESET - ONUNIT
HOLDUNIT MVC   EXCTIMER,&SV    RESET ORIGINAL WAIT TIME         GP04234
         BR    R14           RETURN TO ONEXIT CALLER
         POP   USING                                            GP03007
         AIF   (NOT &SW).NOX
SAVEWAIT DC    AL4(0)        WAIT TIME SAVER                    GP04234
.NOX     ANOP  ,
HOL&SYSNDX.A DS 0H           BRANCH AROUND ONUNIT
         MEND  ,
