         MACRO ,                                        ADDED ON 79226
&NM      STORQ &IN=,&LEN=,&OUT=,&ERR=EXCSOP,                           *
               &WK0=R0,&WK1=R1,&WK2=R2,&WK3=R3,&WK4=R4,&WK5=R5
.**********************************************************************
.*                                                                   **
.*   USED BY EXHIBIT COMMANDS TO PARSE A QUOTED STRING               **
.*                                                                   **
.**********************************************************************
         LCLC  &T
&T       SETC  'STQ'.'&SYSNDX'
&NM      LA    &WK0,1        CONSTANT ONE
         MACPARM &WK1,&OUT
         MACPARM &WK2,&LEN
         MACPARM &WK5,&IN
         LR    &WK3,&WK2     SAVE FULL LENGTH
         LNR   &WK4,&WK5     SET MINUS FOR UNQUOTED
         CLI   0(&WK5),C''''  QUOTED ?
         BE    &T.A-2        YES
         CLI   0(&WK5),C'"'   REAL QUOTE ?
         BNE   &T.A+2        NO
         LR    &WK4,&WK5     SAVE LOCATION OF QUOTE
&T.A     AR    &WK5,&WK0     GET NEXT BYTE
         LTR   &WK4,&WK4     DOING QUOTED STRING ?
         BP    &T.C          YES
         CLI   0(&WK5),C' '  END OF INPUT ?
         BE    &T.D          YES
         CLI   0(&WK5),C','  ALTERNATE END
         BE    &T.D          YES
&T.B     LTR   &WK2,&WK2     ROOM FOR ANOTHER ?
         BNP   &ERR          NO; FAIL
         MVC   0(1,&WK1),0(&WK5)  MOVE ONE
         AR    &WK1,&WK0     UP ONE
         BCTR  &WK2,0        ADJUST LENGTH
         B     &T.A          SKIP AND TEST AGAIN
&T.C     CLC   0(1,&WK5),0(&WK4)  QUOTE ?
         BNE   &T.B          NO; COPY
         AR    &WK5,&WK0     SKIP FIRST DOUBLED QUOTE            80328
         CLC   0(1,&WK5),0(&WK4)  DOUBLED ?                      89166
         BE    &T.B          AND MOVE SECOND
         SR    &WK5,&WK0      FINAGLE FOR NEXT ADD
&T.D     AR    &WK5,&WK0     SKIP SEPARATION CHARACTER
         SR    &WK3,&WK2     SET STRING LENGTH
         MEND  ,
