         MACRO ,
&NM      EBCHEX &VAL,&LEN                                       GP97272
.**********************************************************************
.*                                                                   **
.*       THIS MACRO CONVERTS A NUMERIC VARIABLE (E.G. L') INTO AN
.*       EBCDIC CONSTANT OF LENGTH &LEN IN HEXADECIMAL FORMAT
.*                                                                   **
.**********************************************************************
         LCLA  &I,&J,&K
&I       SETA  8
&K       SETA  268435456
         AIF   (T'&LEN NE 'N').BADLEN
         AIF   ('&NM' EQ '').NODC
&NM      DC    0CL(&LEN)' '
.NODC    AIF   (&LEN GT 8).BADLEN
         AIF   (&LEN GT 0).OKLEN
.BADLEN  MNOTE 8,'LENGTH (OPERAND 2) IS UNACCEPTABLE: &LEN '
         MEXIT ,
.OKLEN   AIF   (&LEN LT &I).NOTLEN
         AIF   (&I LT 8).LONGFM
         DC    AL1(240+((&VAL)/&K)-((C'0'-C'A'+10)*(((&VAL)/&K)/10)))
         AGO   .NOTLEN
.LONGFM  DC    AL1(240+(((&VAL)/&K)-((&VAL)/&J)*16)-((C'0'-C'A'+10)*(((*
               (&VAL)/&K)-((&VAL)/&J)*16)/10)))
.NOTLEN  AIF   (&I EQ 1).MEND
&I       SETA  &I-1
&J       SETA  &K
&K       SETA  &K/16
         AGO   .OKLEN
.MEND    MEND  ,
