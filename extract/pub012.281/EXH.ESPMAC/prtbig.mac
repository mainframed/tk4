         MACRO ,
&NM      PRTBIG &ITEM,&LIST=,&DEV=,&OPT=                ADDED ON 81159
         LCLA  &OP,&I,&J
         LCLB  &B1,&B2,&B4,&B8,&B16,&B32,&B64,&B128              91056
         AIF   ('&ITEM' NE '' AND '&LIST' NE '').BOTH
         AIF   ('&LIST' NE '').LIST
&J       SETA  N'&OPT
         AIF   (&J EQ 0).ITMNOPT
.ITMLOOP AIF   (&I GE &J).ITMTEST
&I       SETA  &I+1
&B128    SETB  (&B128 OR '&OPT(&I)' EQ 'CH12' OR '&OPT(&I)' EQ 'BIG')
&B64     SETB  (&B64 OR '&OPT(&I)' EQ 'MORE')                    91056
&B32     SETB  (&B32 OR '&OPT(&I)' EQ 'SMALLER')                 91056
&B16     SETB  (&B16 OR '&OPT(&I)' EQ 'CH8' OR '&OPT(&I)' EQ 'SMALL')
&B8      SETB  (&B8 OR '&OPT(&I)' EQ 'CENT' OR '&OPT(&I)' EQ 'CENTER')
&B4      SETB  (&B4 OR '&OPT(&I)' EQ 'LEFT')                     91056
&B2      SETB  (&B2 OR '&OPT(&I)' EQ 'RIGHT')                    91056
&B1      SETB  (&B1 OR '&OPT(&I)' EQ 'NEG' OR '&OPT(&I)' EQ 'NEGATIVE')
         AGO   .ITMLOOP
.ITMTEST AIF   ((&B128+&B16+&B32) LT 2).ITMSONE                  91056
       MNOTE 4,'MUTUALLY EXCLUSIVE SIZE OPTIONS SPECIFIED; SMALL USED'
.ITMSONE ANOP  ,                                                 86064
&OP      SETA  &B128+&B64+&B32+&B16+&B8+&B4+&B2+&B1              91056
         AIF   (&OP NE &J).BADOPT
&OP      SETA  &B128*128+&B64*64+&B32*32+&B16*16+&B8*8+&B4*4+&B2*2+&B1
.ITMNOPT ANOP  ,
&NM      PRTCOM PRTBIG,FUN=8,B0=0,B1=&OP,A1=&ITEM,DEV=&DEV
         MEXIT ,
.BOTH    MNOTE 4,'ITEM OPERAND AND LIST= MUTUALLY EXCLUSIVE'
         MEXIT ,
.BADOPT  MNOTE 4,'ERRONEOUS OPT= VALUE; USE ''CENT'' AND ''NEG'' ONLY'
         MEXIT ,
.LIST    ANOP  ,
&NM      PRTCOM PRTBIG,FUN=8,A1=&LIST,DEV=&DEV,OPT=&OPT,A80=ON
         MEND  ,
