         MACRO ,
&NM      #TRC  &MODE,&FAST,&ARG,&LOAD=YES,&ADDR=@TRACE,     NEW GP98300*
               &RENT=YES,&PFX=PGT,&DSECT=NO,&ADCON=NO,          GP08157*
               &REGSAVE=                                        GP11243
         GBLB  &ZZ$TRFG,&ZZ$TDFG                                GP00192
         GBLB  &ZZ$TRNT                                         GP99364
         GBLC  &ZZ$TRAD
         GBLC  &MACPLAB                                         GP99364
.**********************************************************************
.*                                                                   **
.*   #TRC CONTROLS THE INVOCATION OF THE PROGRAM TRACE ROUTINE;      **
.*                                                                   **
.*   REQUIRES TRACEIN AND TRACEOUT DD CARDS. SEE PGMTRACE SOURCE.    **
.*                                                                   **
.**********************************************************************
         LCLC  &SAVNAM            WHERE TO SAVE REGS            GP14249
         LCLC  &EPNAME,&PTR,&P
         LCLB  &DYN
         LCLA  &OFF,&I,&RQC
&I       SETA  &SYSNDX
&DYN     SETB  ('&LOAD' EQ 'YES' OR '&MODE' EQ 'DYN')
&PTR     SETC  '&ADDR'
&MACPLAB SETC  '&NM'                                            GP99364
&ZZ$TRNT SETB  (&ZZ$TRNT OR ('&RENT' EQ 'YES'))                 GP99364
&EPNAME  SETC  'TRACEON'
&OFF     SETA  8
&RQC     SETA  1             TRACE ON FLAG
         AIF   ('&MODE' EQ 'DATA').DODATA                       GP00192
         AIF   ('&REGSAVE' NE 'YES').NOSAVE                     GP11243
&SAVNAM  SETC  'ZZ&I.V'      INLINE SAVE AREA                   GP14249
         AIF   (NOT &ZZ$TRNT).OKSAVE                            GP14249
&SAVNAM  SETC  'DB'          MUST BE FOUR WORDS                 GP14249
.OKSAVE  ANOP  ,                                                GP14249
&MACPLAB STM   R14,R1,&SAVNAM     SAVE USER'S REGISTERS         GP14249
&MACPLAB SETC  ''                                               GP11243
.NOSAVE  AIF   (NOT &DYN).HAVBAS
         AIF   ('&ADDR' EQ '').DEFBAS
&ZZ$TRAD SETC  '&ADDR'
.DEFBAS  ANOP  ,
&PTR     SETC  '&ZZ$TRAD'
         AIF   ('&PTR' NE '').HAVBAS
&PTR     SETC  '=A(PGMTRACE)'
&ZZ$TRAD SETC  '=A(PGMTRACE)'
.HAVBAS  AIF   (&ZZ$TRFG).LATER
&ZZ$TRFG SETB  1
         WXTRN TRACE,TRACEON,TRACEOFF,TRACKILL
.LATER   AIF   ('&MODE' EQ 'ON' OR '&MODE' EQ 'TRACEON').SPEED
&RQC     SETA  0             TRACE OFF FLAG
&OFF     SETA  12
&EPNAME  SETC  'TRACEOFF'
         AIF   ('&MODE' EQ 'OFF' OR '&MODE' EQ 'TRACEOFF').SPEED
&OFF     SETA  4
&EPNAME  SETC  'TRACKILL'
         AIF   ('&MODE' EQ 'END' OR '&MODE' EQ 'TRACKILL').EXPAND
         AIF   ('&MODE' EQ 'QUIT' OR '&MODE' EQ 'EXIT').EXPAND
         AIF   ('&MODE' EQ 'KILL' OR '&MODE' EQ 'DONE').EXPAND
&OFF     SETA  24                                               GP14249
&EPNAME  SETC  'TRACSUSP'                                       GP14249
         AIF   ('&MODE' EQ 'SUS' OR '&MODE' EQ 'TRACSUSP').EXPAND
         AIF   ('&MODE' EQ 'SUSP' OR '&MODE' EQ 'SUSPEND').EXPAND
&OFF     SETA  0
&EPNAME  SETC  'TRACE'
         AIF   ('&MODE' EQ 'INIT' OR '&MODE' EQ 'TRACE').SPECIAL
         MNOTE 8,'#TRC - UNRECOGNIZED OPERAND : &MODE'
         MEXIT ,
.SPECIAL AIF   ('&ADCON' NE 'YES').NOADCON                      GP08157
         AIF   (&ZZ$TRNT).NOADZER                               GP99364
&MACPLAB B     4+&PTR                                           GP08255
&PTR     DC    A(0)                                             GP08157
&MACPLAB SETC  ''                                               GP99364
         AGO   .NOADCON
.NOADZER ANOP  ,                                                GP08157
&MACPLAB B     4+&ZZ$TRAD                                       GP08157
&ZZ$TRAD DC    V(PGMTRACE)                                      GP08157
&MACPLAB SETC  ''                                               GP99364
&DYN     SETB  0             CANCEL DYNAMIC OPTION              GP99364
.NOADCON AIF   (&ZZ$TRNT).NOMOD                                 GP99364
&MACPLAB NOP   ZZ&I.B
&MACPLAB SETC  ''                                               GP99364
         OI    *-4+1,X'F0'
.NOMOD   AIF   (&DYN).DYNLOAD                                   GP99364
         MACPARM R1,&FAST,NULL=0  SET API OPTION ADDRESS OR 0
         ICM   R15,15,&PTR
         BZ    ZZ&I.Z
         BASR  R14,R15
&EPNAME  SETC  'TRACEON'
&OFF     SETA  8
ZZ&I.B   DS    0H
         AGO   .EXPAND
.DYNLOAD ANOP  ,                                                GP10164
&MACPLAB ICM   R15,15,&PTR  LOADED BEFORE?
&MACPLAB SETC  ''                                               GP10164
         BNZ   ZZ&I.A        YES?
         LOAD  EP=PGMTRACE,ERRET=ZZ&I.Z
         LA    R15,&PTR
         ST    R0,0(,R15)    NON-KOSHER
ZZ&I.A   DS    0H
         AIF   ('&FAST' EQ 'ON').PRM3ON
         AIF   ('&FAST' NE 'OFF').PRM2
&OFF     SETA  12            INITIALIZE WITHOUT TRACE PRINTING
&EPNAME  SETC  'TRACEOFF'
         AGO   .PRM3
.PRM3ON  ANOP  ,             ALREADY SET FOR NORMAL TRACE?
.PRM3    MACPARM R1,&ARG,NULL=0   SET API OPTION ADDRESS OR 0
         AGO   .PRM2N3
.PRM2    MACPARM R1,&FAST,NULL=0  SET API OPTION ADDRESS OR 0
.PRM2N3  L     R15,&PTR
         L     R15,&OFF+64(,R15)
         BASR  R14,R15
         B     ZZ&I.Z
         AIF   ('&REGSAVE' NE 'YES' OR &ZZ$TRNT).NOS14R1        GP14249
ZZ&I.V   DC    4A(0)                                            GP13222
.NOS14R1 AIF   (&OFF NE 0).PRMOFF
&EPNAME  SETC  'TRACEON'
&OFF     SETA  8
.PRMOFF  ANOP  ,
ZZ&I.B   DS    0H
.EXPAND  AIF   (NOT &DYN).EXPANDS
         MACPARM  R15,15,&PTR,OP=ICM,MODE=THREE                 GP02242
         BZ    ZZ&I.Z
         L     R15,&OFF+64(,R15)
         BASR  R14,R15
         AGO   .REGREST
.EXPANDS ANOP  ,
         MACPARM  R15,15,=A(&EPNAME),OP=ICM,MODE=THREE          GP02242
         BZ    ZZ&I.Z
         BASR  R14,R15
.REGREST AIF   ('&REGSAVE' NE 'YES').GOAWAY                     GP11243
ZZ&I.Z   LM    R14,R1,&SAVNAM     RESTORE REGISTERS             GP14249
         MEXIT ,
.GOAWAY  ANOP  ,
ZZ&I.Z   DS    0H
         MEXIT ,
.SPEED   AIF   ('&FAST' NE 'FAST').EXPAND
         AIF   ('&ARG' EQ '').NOARG
&MACPLAB DC    0H'0',X'83',AL1(X'C0'+&RQC),SL2(&ARG)
         MEXIT ,
.NOARG   ANOP  ,
&MACPLAB DC    0H'0',X'83',AL1(X'C0'+&RQC),AL2(0)
         MEXIT ,                                                GP00192
.DODATA  ANOP  ,                                                GP00192
&P       SETC  '&PFX'                                           GP00192
         AIF   ('&DSECT' EQ 'NO').NODADSC                       GP00192
         AIF   ('&NM' EQ '').NFDADS                             GP00203
&MACPLAB DSECT ,             PGMTRADA PARAMETER LIST            GP00203
         AGO   .NODADS                                          GP00203
.NFDADS  ANOP  ,                                                GP00203
&P.SECT  DSECT ,             CALLER'S PARM DSECT                GP00192
         AGO   .NODADS                                          GP00203
.NODADSC AIF   ('&NM' EQ '').NODADS                             GP00203
&MACPLAB DS    0D            PGMTRADA PARAMETER LIST            GP00203
.NODADS  ANOP  ,                                                GP00192
&P.FUN   DS    X             FUNCTION (L - LOOK-UP; M-MEMBER BUILD)
         AIF   (&ZZ$TDFG).HAVEMAP                               GP00192
CFLOOK   EQU   C'L'            LOCATE MEMBER/OFFSET - PRINT DATA
CFPOINT  EQU   C'M'            LOCATE MEMBER; BUILD SOURCE CHAIN
CFCLOSE  EQU   C'C'            CLOSE AND FREE EVERYTHING        GP00192
.HAVEMAP ANOP  ,                                                GP00192
&P.FLG1  DC    AL1(0)        PRINT OPTIONS                      GP00192
         AIF   (&ZZ$TDFG).HAVEFG1                               GP00192
CFDIR    EQU   X'80'           PRINT DIRECTORY ENTRY DATA       GP00192
CFESD    EQU   X'40'           PRINT CESD LISTING               GP00192
CFRLD    EQU   X'20'           PRINT RLD LISTING                GP00192
CFSYM    EQU   X'10'           PRINT SYM LISTING                GP00192
CFDAT    EQU   X'08'           PRINT SYSADATA INFO (LATER)      GP00192
CFLBL    EQU   X'02'           PRINT LABELS                     GP00192
CFTRC    EQU   X'01'           PRINT THE TRACE TABLE ON ABNORMAL END
.HAVEFG1 ANOP  ,                                                GP00192
&P.FLG2  DC    AL1(0)        PRINT OPTIONS                      GP00192
         AIF   (&ZZ$TDFG).HAVEFG2                               GP00192
CFHEX    EQU   X'80'           PRINT THE CSECT HEX DUMP         GP00192
CFLST    EQU   X'40'           PRINT THE ASSEMBLY LISTING       GP00192
CFXRF    EQU   X'20'           PRINT A LABEL CROSS-REFERENCE    GP00192
CFPUN    EQU   X'10'           PUNCH OUTPUT (?)                 GP00192
CFBUG    EQU   X'01'           PRINT ADDITIONAL DEBUG INFO      GP00192
.HAVEFG2 ANOP  ,                                                GP00192
&P.FLG3  DC    X'00'         PROCESSING FLAG                    GP00192
         AIF   (&ZZ$TDFG).HAVEFG3                               GP00192
CFMAC    EQU   X'80'           INCLUDE MACRO EXPANDED CODE (SYSADATA)
CFIMAC   EQU   X'40'           INCLUDE INLINE MACRO CODE   (SYSADATA)
CFCMT    EQU   X'20'           INCLUDE COMMENTS            (SYSADATA)
CFASM    EQU   X'10'           INCLUDE ASSEMBLER PSEUDO-OPS(SYSADATA)
&ZZ$TDFG SETB  1                                                GP00192
.HAVEFG3 ANOP  ,                                                GP00192
&P.MEM   DC    CL8' '        MEMBER NAME                        GP00192
&P.ESD   DC    CL8' '        CSECT NAME (NOT USED AT PRESENT)   GP00192
&P.OFF   DC    AL4(0)        OFFSET FROM LOAD POINT             GP00192
&P.WORK  DC    A(0)          WORK AREA (BUILT/FREED HERE)       GP00192
&P.@PRT  DC    A(0)          CALLER'S PRINT ROUTINE (R1 - BUFFER)
.MEND    MEND  ,
