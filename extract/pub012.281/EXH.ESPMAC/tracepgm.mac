         MACRO ,
&NM      TRACEPGM &MODE,&FAST,&ARG,&LOAD=YES,&ADDR=    ADDED ON GP98300
.* THIS IS USED TO INVOKE TRACEPGM DYNAMICALLY WHILE TESTING PGMTRACE
.*
.*  FOR OTHER DEBUGGING, USE PGMTRACE - IT'S MORE LIKELY TO BE CURRENT
.*
         GBLB  &ZZ$TRFG
         GBLC  &ZZ$TRAD
         LCLC  &EPNAME,&PTR
         LCLB  &DYN
         LCLA  &OFF,&I,&RQC
&I       SETA  &SYSNDX
&DYN     SETB  ('&LOAD' EQ 'YES' OR '&MODE' EQ 'DYN')
&PTR     SETC  '&ADDR'
&EPNAME  SETC  'TRACEON'
&OFF     SETA  8
&RQC     SETA  1             TRACE ON FLAG
         AIF   (NOT &DYN).HAVBAS
         AIF   ('&ADDR' EQ '').DEFBAS
&ZZ$TRAD SETC  '&ADDR'
.DEFBAS  ANOP  ,
&PTR     SETC  '&ZZ$TRAD'
         AIF   ('&PTR' NE '').HAVBAS
&PTR     SETC  '=A(TRACEPGM)'
&ZZ$TRAD SETC  '=A(TRACEPGM)'
.HAVBAS  AIF   (&ZZ$TRFG).LATER
&ZZ$TRFG SETB  1
         WXTRN TRACE,TRACEON,TRACEOFF,TRACKILL
.LATER   AIF   ('&MODE' EQ 'ON' OR '&MODE' EQ 'TRACEON').SPEED
&RQC     SETA  0             TRACE OFF FLAG
&OFF     SETA  12
&EPNAME  SETC  'TRACEOFF'
         AIF   ('&MODE' EQ 'OFF' OR '&MODE' EQ 'TRACEOFF').SPEED
&OFF     SETA  4
&EPNAME  SETC  'TRACKILL'
         AIF   ('&MODE' EQ 'END' OR '&MODE' EQ 'TRACKILL').EXPAND
         AIF   ('&MODE' EQ 'QUIT' OR '&MODE' EQ 'EXIT').EXPAND
         AIF   ('&MODE' EQ 'KILL' OR '&MODE' EQ 'DONE').EXPAND
&OFF     SETA  0
&EPNAME  SETC  'TRACE'
         AIF   ('&MODE' EQ 'INIT' OR '&MODE' EQ 'TRACE').SPECIAL
         MNOTE 8,'TRACEPGM - UNRECOGNIZED OPERAND : &MODE'
         MEXIT ,
.SPECIAL ANOP  ,
&NM      NOP   ZZZZ&I.B
         OI    *-4+1,X'F0'
         AIF   (&DYN).DYNLOAD
         MACPARM R1,&FAST,NULL=0  SET API OPTION ADDRESS OR 0
         ICM   R15,15,&PTR
         BZ    ZZZZ&I.Z
         BASR  R14,R15
&EPNAME  SETC  'TRACEON'
&OFF     SETA  8
ZZZZ&I.B DS    0H
         AGO   .EXPAND
.DYNLOAD ICM   R15,15,&PTR  LOADED BEFORE?
         BNZ   ZZZZ&I.A     YES?
         LOAD  EP=TRACEPGM,ERRET=ZZZZ&I.Z
         LA    R15,&PTR
         ST    R0,0(,R15)    NON-KOSHER
ZZZZ&I.A DS    0H
         AIF   ('&FAST' EQ 'ON').PRM3ON
         AIF   ('&FAST' NE 'OFF').PRM2
&OFF     SETA  12            INITIALIZE WITHOUT TRACE PRINTING
&EPNAME  SETC  'TRACEOFF'
         AGO   .PRM3
.PRM3ON  ANOP  ,             ALREADY SET FOR NORMAL TRACE?
.PRM3    MACPARM R1,&ARG,NULL=0   SET API OPTION ADDRESS OR 0
         AGO   .PRM2N3
.PRM2    MACPARM R1,&FAST,NULL=0  SET API OPTION ADDRESS OR 0
.PRM2N3  L     R15,&PTR
         L     R15,&OFF+64(,R15)
         BASR  R14,R15
         B     ZZZZ&I.Z
         AIF   (&OFF NE 0).PRMOFF
&EPNAME  SETC  'TRACEON'
&OFF     SETA  8
.PRMOFF  ANOP  ,
ZZZZ&I.B DS    0H
.EXPAND  AIF   (NOT &DYN).EXPANDS
         ICM   R15,15,&PTR
         BZ    ZZZZ&I.Z
         L     R15,&OFF+64(,R15)
         BASR  R14,R15
ZZZZ&I.Z DS    0H
         MEXIT ,
.EXPANDS ANOP  ,
         ICM   R15,15,=A(&EPNAME)
         BZ    ZZZZ&I.Z
         BASR  R14,R15
ZZZZ&I.Z DS    0H
         MEXIT ,
.SPEED   AIF   ('&FAST' NE 'FAST').EXPAND
         AIF   ('&ARG' EQ '').NOARG
&NM      DC    0H'0',X'83',AL1(X'C0'+&RQC),SL2(&ARG)
         MEXIT ,
.NOARG   ANOP  ,
&NM      DC    0H'0',X'83',AL1(X'C0'+&RQC),AL2(0)
.MEND    MEND  ,
