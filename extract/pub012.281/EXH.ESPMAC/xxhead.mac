         MACRO ,                                          ADDED GP08211
&NM      XXHEAD &FIW=R9,&SKIP=HELP,&LEN=R2,&SAVE=,&WORK=R11,&FDW=R10,  *
               &FGTEST=,&NULL=SKIP,&BASE=R12                    GP15305
         GBLC  &ZZXXHSV,&ZZXXHNM,&ZZXXHFI
&ZZXXHSV SETC  '&SAVE'
&ZZXXHNM SETC  '&NM'
&ZZXXHFI SETC  '&FIW'
         AIF   ('&SAVE' NE 'YES').USAVE                         GP15305
&ZZXXHSV SETC  '&NM'.'S'     GENERATED SAVE AREA                GP15305
.USAVE   PUSH  USING
         DROP  ,
         USING &NM,R15       DECLARE ENTRY
         AIF   ('&BASE' NE '15' AND '&BASE' NE 'R15').UBASE     GP15305
&NM      LTR   R15,R15       SCANAL OR SCMOVE CALL ?
         BNMR  R14           SKIP SCMOVE; DO SCANAL, SCLOOP
         MACPARM R0,R15,&ZZXXHSV,MODE=THREE,NULL=SKIP,OP=STM
         AGO   .CBASE
.UBASE   ANOP  ,                                                GP15305
&NM      MACPARM R0,R15,&ZZXXHSV,MODE=THREE,NULL=SKIP,OP=STM    GP15305
         DROP  R15                                              GP15305
         MACPARM &BASE,R15,OP=LTR,OPR=LTR,MODE=EVEN             GP15305
         BNMR  R14           SKIP SCMOVE; DO SCANAL, SCLOOP     GP15305
         USING &NM,&BASE     DECLARE ENTRY                      GP15305
.CBASE   AIF   ('&WORK' EQ '').NOWORK                           GP12297
         USING EXHBWORK,&WORK(1)                                GP12297
.NOWORK  AIF   ('&FDW' EQ '').NOFDW                             GP12297
         USING FDWFDA,&FDW(1)                                   GP12297
.NOFDW   AIF   ('&FIW' EQ '').MEND
         L     &FIW,8(,R1)     LOAD FIW ADDRESS
         USING FIWSECT,&FIW  DECLARE IT
         AIF   ('&FGTEST' EQ 'NO').SKPHELP
         AIF   ('&SKIP' NE 'HELP').SKPBOTH
         TM    FIWFG,FIWFHLP  NON-TEXT ?
         AGO   .SKPRET
.SKPBOTH TM    FIWFG,FIWFERR+FIWFHLP  NON-TEXT ?
.SKPRET  BNZR  R14           NO
.SKPHELP AIF   ('&LEN' EQ '').MEND
         ZICM  &LEN,FIWLEN   GET CURRENT LENGTH                 GP15305
         AIF   ('&NULL' NE 'SKIP').MEND                         GP12298
         BZR   R14           RETURN ON EMPTY                    GP12298
.MEND    MEND  ,
