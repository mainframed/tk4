         MACRO ,
&NM      RESCALL &OPT,&SHORT=                           ADDED ON 79156
         GBLB  &MVSXA                                            93017
.**********************************************************************
.*                                                                   **
.*   EXHIBIT INVOCATION OF (E)CSA RESIDENT CODE                      **
.*                                                                   **
.**********************************************************************
         LCLA  &OFF
&OFF     SETA   0
         AIF   ('&OPT' EQ 'LOOP').OK
&OFF     SETA  4
         AIF   ('&OPT' EQ 'SCREEN').OK
&OFF     SETA  8                                                 79159
         AIF   ('&OPT' EQ 'INIT').OK                             79159
         MNOTE 8,'INVALID FUNCTION - &OPT'
         MEXIT
.OK      AIF   (&MVSXA).XACALL                                   93017
&NM      LAT   R10,EXRESAD .  CHECK RESIDENT MODULE
         BZ    *+12 .         LONG PATH IF UNAVAILABLE
         BALS  R9,&OFF.(,R10)  INVOKE RESIDENT FUNCTION
         AIF   ('&SHORT' EQ '').MEND
         AIF   ('&SHORT'(1,1) EQ '(').REG
         B     &SHORT .       TAKE FAST PATH                     79156
         MEXIT
.REG     BR    &SHORT(1) .    TAKE FAST PATH
         NOPR  0 .             PAD FOR SLOW (+4) RETURN
         MEXIT ,                                                 93017
.XACALL  ANOP  ,                                                 93017
&NM      ICM   R10,15,EXRESAD  CHECK RESIDENT MODULE ESTABLISHED ?
         AIF   ('&OPT' EQ 'INIT').NOBZ                           93017
         BZ    *+18           LONG PATH IF UNAVAILABLE           93017
.NOBZ    LA    R15,&OFF.(,R10)  MAKE FUNCTION ADDRESS (R10 IS BASE)
         O     R15,=X'80000000'  ENTER IN 31-BIT MODE            93017
         BASSM R9,R15         INVOKE RESIDENT FUNCTION           93017
         AIF   ('&SHORT' EQ '').MEND                             93017
         AIF   ('&SHORT'(1,1) EQ '(').REGA                       93017
         B     &SHORT         TAKE FAST PATH                     93017
         MEXIT                                                   93017
.REGA    BR    &SHORT(1)      TAKE FAST PATH                     93017
         NOPR  0               PAD FOR SLOW (+4) RETURN          93017
.MEND    MEND
