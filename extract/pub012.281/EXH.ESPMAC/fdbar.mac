         MACRO
&NM      FDBAR &O1,&O2,&O3,&O4,&O5,&O6,&O7,&O8,&O9,&O10,&O11,&O12,&O13,*
               &O14,&O15,&O16,&LEN=50,                                 *
               &CHAR=C'*',&MAX=0
.*--------------------------------------------------------------------*
.*   FDBAR PRODUCES A BAR (E.G., PERCENTAGE BY MAKING THE LEN= AREA   *
.*     NON-BLANK FOR THE CALCULATED PERCENTAGE OF &O1 * 100 / MAX=    *
.*     THE EXPANSION IS THE SAME AS FOR A REGULAR FD, FOLLOWED BY THE *
.*     PLOT CHARACTER AND THE S(MAX-ADDR)  MAX=0 DEFAULTS TO 100      *
.*--------------------------------------------------------------------*
         LCLA  &N,&K
         LCLB  &I1,&I2,&I3
         LCLC  &C
&N       SETA  N'&SYSLIST
&K       SETA  1             FIRST IS DIVIDEND, SKIP TEST
.LOOP    AIF   (&K GE &N).TEST
&K       SETA  &K+1
&C       SETC  '&SYSLIST(&K)'
&I1      SETB  (('&C' EQ 'I') OR &I1)
&I2      SETB  (('&C' EQ 'D') OR &I2)
&I3      SETB  (('&C' EQ 'F') OR &I3)
         AGO   .LOOP
.TEST    AIF   ((&I1+&I2+&I3) NE 1).BADTYP
         AIF   ('&CHAR' NE '').CHAROK
.BADTYP  MNOTE 'FDBAR: CHAR= MISSING'
         MEXIT ,
.CHAROK  AIF   ('&MAX' NE '').GOOD
         MNOTE 'FDBAR: MAX= (DIVISOR) MISSING'
         MEXIT ,
.GOOD    ANOP  ,
&N       SETA  48+&I2+2*&I3
         FD    &O1,&O2,&O3,&O4,&O5,&O6,&O7,&O8,&O9,&O10,&O11,&O12,&O13,*
               &O14,&O15,&O16,LEN=&LEN,TYPE=&N
         DC    SL2(&MAX),AL1(&CHAR,0)
         MEND  ,
