         MACRO ,
&NM      MACPLOD &REG,&ADD
.**********************************************************************
.*                                                                    *
.*  MACPLOP IS AN INNER MACRO (PART OF THE MACPARM GROUP) FOR         *
.*  LOADING AN ADDRESS DEPENDING ON THE SUPPLIED STRING.              *
.*                                                                    *
.*  1) &ADD  IS ((.) OR (.)) EXPRESSION - GENERATE LA                 *
.*  2) &ADD  IS (R) - GENERATE SIMPLE LOAD REGISTER                   *
.*  3) &ADD  IS NUMERIC < 4096 - GENERATE LA                          *
.*  4) &ADD  BEGINS WITH / OR * - GENERATE L                          *
.*  5) &ADD  IS SOMETHING ELSE - GNERATE L =A(&ADD)                   *
.*                                                                    *
.**********************************************************************
         GBLC  &MACPLAB
         LCLA  &K
&MACPLAB SETC  '&NM'
&K       SETA  K'&ADD
         AIF   (&K LT 3).TESTR
         AIF   ('&ADD'(1,1) NE '(' OR '&ADD'(2,1) EQ '(').LA
         AIF   ('&ADD'(&K,1) NE ')' OR '&ADD'(&K-1,1) EQ ')').LA
.TESTR   AIF   ('&ADD'(1,1) EQ '(' AND '&ADD'(&K,1) EQ ')').LR
         AIF   ('&ADD'(1,1) EQ '''').LS
         AIF   ('&ADD'(1,1) EQ '/').LI
         AIF   ('&ADD'(1,1) EQ '*').LI
         AIF   (T'&ADD NE 'N').LI
         AIF   (&ADD LT 4096).LA
.LI      MACPARM &REG,=A(&ADD),OP=L,OPR=L   SIMPLE L
         MEXIT ,
.LS      MACPARM &REG,=C&ADD,OP=LA,OPR=LA   SIMPLE LA FOR STRING
         MEXIT ,
.LR      MACPARM &REG,&ADD,OP=LR,OPR=LR     SIMPLE LR
         MEXIT ,
.LA      MACPARM &REG,&ADD(1),OP=LA,OPR=LA  SIMPLE LA
.MEND    MEND  ,
