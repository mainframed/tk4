         MACRO ,
&NM      DBT   &LBL,&TEXT=,&HEX=,&MODE=,&MF=,       REPLACED ON 2003018*
               &REGS=YES,&REGTYPE=,                                    *
               &ID=NO,&SYSINFO=,                                       *
               &CTEXT=,&PACK=,                                         *
               &BUGPARM=NO,         USER DEBUG SWITCH                  *
               &PRTMODE=0,&DCB=0,&DEV=1,  USER PRINT DCB/MODE          *
               &COUNT=,&CALL=DYN,&PFX=,                                *
               &LIST=,&LNAME=
.**********************************************************************
.*                                                                    *
.*  THIS MACRO INVOKES EXTERNAL LOAD MODULE DEBTRACE TO PRODUCE       *
.*  TRACING, REGISTER CONTENTS, AND VARIABLES. (DEBTRACE SHOULD BE IN *
.*  A LINKLIB; AUTHORIZATION IS NOT REQUIRED). OUTPUT WILL BE BY WTO  *
.*  UNLESS A DEBTRACE DD CARD IS SUPPLIED (DFLT FBA 133)              *
.*                                                                    *
.*  REQUIRED:  IN A CSECT OR DSECT:   DBT MODE=D       DEFINES DATA   *
.*    (NOTE: REQUIRED FORM MUST NOT APPEAR PRIOR TO FIRST OPTIONAL)   *
.*                                                                    *
.*  OPTIONAL:  LABEL DBT ...                                          *
.*                TAG OR ,  -  IDENTIFIER ON OUTPUT LISTING / CONSOLE *
.*                ID=YES    -  SHOW CALLER'S PSW, TCB, ASCB
.*                                                                    *
.*                REGS= (DEFAULT) × REGS=NO - NO REGISTERS            *
.*                REGS=YES  -  REGISTERS R0 THROUGH R15               *
.*                REGS=(R1,R2) - REGISTERS R1 THROUGH R2              *
.*                REGS=SHORT   - R14 THROUGH R1                       *
.*                REGTYPE=     - ALL, OR ONE OR MORE OF GPR, ACR, FPR *
.*                                                                    *
.*                TEXT=NAME -  TEXT STRING TO BE SHOWN                *
.*                TEXT=(NAME,LEN) - TEXT W/EXPLICIT LENGTH            *
.*                                                                    *
.*                CTEXT=NAME - CONDITIONAL TEXT STRING TO BE SHOWN    *
.*                CTEXT=(NAME,LEN) - TEXT W/EXPLICIT LENGTH           *
.*                              OUTPUT IN HEX IF NOT PRINTABLE        *
.*                                                                    *
.*                HEX=NAME   -  DATA TO BE SHOWN IN HEXADECIMAL       *
.*                HEX=(NAME,LEN) - TEXT W/EXPLICIT LENGTH             *
.*                                                                    *
.*                PACK=NAME  -  DATA TO BE CONVERTED FROM PACKED      *
.*                PACK=(NAME,LEN) - TEXT W/EXPLICIT LENGTH (LEN IGNRD)*
.*                                                                    *
.*           LIST=((OP1,LN1,FM1),(OP2,LN2,FM2), ... )                 *
.*                                                                    *
.*                OP - ADDRESS EXPRESSION VALID IN S CONSTANT         *
.*                LN - LENGTH EXPRESSION; DEFAULT IS L'OP             *
.*                FM - TEXT × CTEXT × HEX × PACK - DEFAULT IS HEX     *
.*                     OR ABBREVIATED   T × CT × H × P                *
.*                                                                    *
.*  THE REQUIRED FORMS MAY BE OMITTED WHEN PGMTRACE WILL ALSO BE USED *
.*  AND ACTIVATED. IN THAT CASE THE FIRST OPTIONAL FORM MUST INCLUDE  *
.*  CALL=TRC TO GENERATE SHORTER PARAMETER LISTS.                     *
.*                                                                    *
.*  EQUIVALENT FUNCTIONALITY IS AVAILABLE IN EXORCIST USING XTRAP     *
.*                                                                    *
.**********************************************************************
.*  MAINTENANCE:                                                      *
.*                                                                    *
.*  2000/01/03  GYP  REMOVED IN-LINE DEBUG CODE;                      *
.*                   FIXED MODE=C AND MODE=D FOR USE WITH REENTRANT   *
.*                     PROGRAMS.                                      *
.*  2000/06/11  GYP  CHANGED MACRO DEBTRACE TO DBT, AND ADDED NEW     *
.*                     MACRO DEBTRACY FOR EASIER PARSING.             *
.*                   LIST= FORM MAY NOW CONTAIN A TEXT FORMATTING     *
.*                     ITEM, INSTEAD OF DATA, TO SET DEFAULT TYPE.    *
.*                                                                    *
.*  2000/07/15  GYP  CHANGED DBT FOR AR MODE DEBUGGING                *
.*                                                                    *
.*  2002/09/09  GYP  CHANGED WA PARAMETER HANDLING. WORK AREA NOW A   *
.*                     GLOBAL, SETTABLE FOR MULTIPLE CSECT ASSEMBLIES *
.*                     WITH A DISTINCT DBT MODE=D IN EACH.            *
.*                                                                    *
.*  2002/12/31  GYP  FIXED LIST= DEFAULT TYPE (MISSPELLED VARIABLE)   *
.*                     ADDED CODE TO SET VL (X'80') BIT IN LAST ITEM. *
.*                                                                    *
.*  2003/01/01  GYP  MIGRATED CHANGES FROM OLD DBT (EOL BIT, ETC.)    *
.*                                                                    *
.*  2003/01/03  GYP  COMPLETELY REORGANIZED; ADDED SUBDEBTR STUB RTNE *
.*                   MODE=C AND MODE=DC GONE, ONLY MODE=D REQUIRED    *
.*  2003/01/13  GYP  ADDED NEW FEATURES; REORGANIZED; ADDED ESPIE     *
.*                                                                    *
.**********************************************************************
         GBLB  &BUGBEAR,&BUGSWCH,&BUGSWRT
         GBLB  &BUGTRC       USED WITH ACTIVE PGMTRACE (ESPIE)  GP99113
         GBLA  &ZZZDBA#,&ZZZDBAN
         GBLB  &ZZZDBTF      (FIRST TIME FLAG)
         GBLB  &ZZZ80FG      END OF LIST FLAG SET
         GBLC  &ZZZDBTV,&ZZZDBTT,&ZZZDBAT(40),&ZZZDBAI(40),&ZZZDBAL(40)
         GBLC  &ZZZDBTW,&ZZZDBTP                                GP02248
         LCLA  &LN,&I,&EN,&EM,&EO                               GP95235
         LCLC  &L,&ET,&EL,&EK,&Q,&RGT,&LISTNM,&RQ1,&RQ2,&RQ3
         LCLB  &HBREG,&HBHEX,&HBTXT,&HBCTX,&HBPAK,&HBLST        GP02365
         LCLB  &RTGPR,&RTACR,&RTCTL,&RTFPR
&L       SETC  'L'''                                            GP95235
&ZZZDBTT SETC  '10'          RESET LIST FORMAT DEFAULT TO HEX
&ZZZDBTV SETC  'DBT'.'&SYSNDX'
&BUGTRC  SETB  (&BUGTRC OR ('&CALL' EQ 'TRC'))                  GP99113
&BUGTRC  SETB  (&BUGTRC OR ('&CALL' EQ 'TRACE'))                GP99113
&BUGTRC  SETB  (&BUGTRC OR ('&CALL' EQ 'PGMTRACE'))             GP99113
&ZZZ80FG SETB  0             NO LIST END GENERATED
.*--------------------------------------------------------------------*
.*  ALLOW USER TO SPECIFY REQUEST WITH MODE= OR WITH MF=              *
.*--------------------------------------------------------------------*
&RQ1     SETC  'S'           DEFAULT - GENERATE EXEC + LIST
         AIF   ('&MODE' EQ '' AND '&MF' EQ '').DONMODE
&RQ1     SETC  '&MODE(1)'    ELSE TRY OLD MODE
&RQ2     SETC  '&MODE(2)'    ELSE TRY OLD MODE
         AIF   ('&MF' EQ '').DONMODE
         AIF   ('&MF' EQ '&MODE').DONMODE  BUT WHY ?
&RQ1     SETC  '&MF(1)'      SET NEW VARIABLE
&RQ2     SETC  '&MF(2)'      SET NEW VARIABLE
         AIF   ('&MODE' EQ '').DONMODE
         MNOTE 4,'DBT: BOTH MODE AND MF SPECIFIED; MODE IGNORED'
.*
.DONMODE AIF   (&ZZZDBTF).DONEDEF
.*--------------------------------------------------------------------*
.*  ASSIGN GLOBAL VALUES FOR USE HERE AND IN DEBTRACY MACRO           *
.*--------------------------------------------------------------------*
&ZZZDBAT(01) SETC 'X'        HEX - DEFAULT TYPE
&ZZZDBAI(01) SETC '10'             INDEX
&ZZZDBAL(01) SETC '6'              ENTRY LENGTH
&ZZZDBAT(02) SETC 'T'        TEXT MODE
&ZZZDBAI(02) SETC '08'
&ZZZDBAL(02) SETC '6'              ENTRY LENGTH
&ZZZDBAT(03) SETC 'C'        TEXT MODE - PREFERRED FORM
&ZZZDBAI(03) SETC '08'
&ZZZDBAL(03) SETC '6'              ENTRY LENGTH
&ZZZDBAT(04) SETC 'P'        PACKED DECIMAL
&ZZZDBAI(04) SETC '11'
&ZZZDBAL(04) SETC '6'              ENTRY LENGTH
&ZZZDBAT(05) SETC 'D'        PACKED DECIMAL - ALTERNATE FORM
&ZZZDBAI(05) SETC '11'
&ZZZDBAL(05) SETC '6'              ENTRY LENGTH
&ZZZDBAT(06) SETC 'CT'       CONDITIONL - TEXT IF PRINTABLE, ELSE HEX
&ZZZDBAI(06) SETC '09'
&ZZZDBAL(06) SETC '6'              ENTRY LENGTH
&ZZZDBAT(07) SETC 'CH'       CONDITIONL - TEXT IF PRINTABLE, ELSE HEX
&ZZZDBAI(07) SETC '09'
&ZZZDBAL(07) SETC '6'              ENTRY LENGTH
&ZZZDBAT(08) SETC 'PD'       PACKED DECIMAL - ALTERNATE FORM
&ZZZDBAI(08) SETC '11'
&ZZZDBAL(08) SETC '6'              ENTRY LENGTH
&ZZZDBAT(09) SETC 'I'        INTEGER - NOT DONE YET
&ZZZDBAI(09) SETC '12'
&ZZZDBAL(09) SETC '6'              ENTRY LENGTH
&ZZZDBAT(10) SETC 'AI'       ABSOLUTE INTEGER - NOT DONE YET
&ZZZDBAI(10) SETC '13'
&ZZZDBAL(10) SETC '6'              ENTRY LENGTH
&ZZZDBAT(11) SETC 'FP'       FLOATING POINT (HEX)
&ZZZDBAI(11) SETC '14'
&ZZZDBAL(11) SETC '6'              ENTRY LENGTH
&ZZZDBAT(12) SETC 'AD'       ADDRESS INFORMATION
&ZZZDBAI(12) SETC '04'
&ZZZDBAL(12) SETC '4'              ENTRY LENGTH
&ZZZDBAT(13) SETC 'EX'
&ZZZDBAI(13) SETC '06'
&ZZZDBAL(13) SETC '4'              ENTRY LENGTH
&ZZZDBAT(14) SETC 'FG'
&ZZZDBAI(14) SETC '16'
&ZZZDBAL(14) SETC '8'              ENTRY LENGTH
&ZZZDBA# SETA 14             SET FOR SEVEN SHORT ENTRIES
&ZZZDBAT(15) SETC 'HEX'
&ZZZDBAI(15) SETC '10'
&ZZZDBAL(15) SETC '6'              ENTRY LENGTH
&ZZZDBAT(16) SETC 'HEXADECIMAL'
&ZZZDBAI(16) SETC '10'
&ZZZDBAL(16) SETC '6'              ENTRY LENGTH
&ZZZDBAT(17) SETC 'TXT'
&ZZZDBAI(17) SETC '08'
&ZZZDBAL(17) SETC '6'              ENTRY LENGTH
&ZZZDBAT(18) SETC 'TEXT'
&ZZZDBAI(18) SETC '08'
&ZZZDBAL(18) SETC '6'              ENTRY LENGTH
&ZZZDBAT(19) SETC 'CTEXT'
&ZZZDBAI(19) SETC '09'
&ZZZDBAL(19) SETC '6'              ENTRY LENGTH
&ZZZDBAT(20) SETC 'PACK'
&ZZZDBAI(20) SETC '11'
&ZZZDBAL(20) SETC '6'              ENTRY LENGTH
&ZZZDBAT(21) SETC 'PACKED'
&ZZZDBAI(21) SETC '11'
&ZZZDBAL(21) SETC '6'              ENTRY LENGTH
&ZZZDBAT(22) SETC 'INT'      INTEGER - NOT DONE YET
&ZZZDBAI(22) SETC '12'
&ZZZDBAL(22) SETC '6'              ENTRY LENGTH
&ZZZDBAT(23) SETC 'ADDR'     ADDRESS INFORMATION
&ZZZDBAI(23) SETC '04'
&ZZZDBAL(23) SETC '4'              ENTRY LENGTH
&ZZZDBAT(24) SETC 'SYS'      SYSTEM INFORMATION
&ZZZDBAI(24) SETC '05'
&ZZZDBAL(24) SETC '4'              ENTRY LENGTH
&ZZZDBAT(25) SETC 'EXEC'
&ZZZDBAI(25) SETC '06'
&ZZZDBAL(25) SETC '4'              ENTRY LENGTH
&ZZZDBAT(26) SETC 'CHAIN'
&ZZZDBAI(26) SETC '07'
&ZZZDBAL(26) SETC '8'              ENTRY LENGTH
&ZZZDBAT(27) SETC 'FLOAT'
&ZZZDBAI(27) SETC '14'
&ZZZDBAL(27) SETC '6'              ENTRY LENGTH
&ZZZDBAT(28) SETC 'IEEE'
&ZZZDBAI(28) SETC '15'
&ZZZDBAL(28) SETC '6'              ENTRY LENGTH
&ZZZDBAT(29) SETC 'FLAG'
&ZZZDBAI(29) SETC '16'
&ZZZDBAL(29) SETC '8'              ENTRY LENGTH
&ZZZDBAN SETA 29             SET FOR LONG ENTRIES
&ZZZDBTF SETB  1             INITIALIZATION DONE
.DONEDEF ANOP  ,
.*--------------------------------------------------------------------*
.*                                                                    *
.*  BRANCH ACCORDING TO MODE REQUEST:                                 *
.*                                                                    *
.*    S         - DEFAULT; EXPAND FORMATTING REQUEST. IF NOT TRACE    *
.*                  MODE, EXPAND CODE TO CALL SUBDEBTR                *
.*    E,LIST    - EXECUTE TRACE WITH REMOTE PARAMETER LIST            *
.*    L         - DEFINE TRACE PARAMETER LIST                         *
.*                                                                    *
.*    ON        - SET NOTRACE FLAG OFF                                *
.*    OFF       - SET NOTRACE FLAG OFF (I.E., FORMAT)                 *
.*    CLOSE     - CLOSE OUTPUT DCB, IF ANY, AND FREE WORK AREA        *
.*                                                                    *
.*    D/M/MAP   - GENERATE USER'S DBT WORK AREA                       *
.*    DW/MW     - GENERATE DEBTRACE WORK AREA (FOR SUBDEBTR)          *
.*                                                                    *
.*--------------------------------------------------------------------*
.*  SKIP EXPANSION UNLESS IN DEBUG MODE                               *
.*--------------------------------------------------------------------*
         AIF   (&BUGBEAR OR '&BUGPARM' EQ 'NO').DOSOME
         AIF   ('&NM' EQ '').MEND
&NM      DS    0H            DEBUG SWITCH NOT ON
         AGO   .MEND
.DOSOME  AIF   ('&RQ1' EQ 'DW' OR '&RQ1' EQ 'MW').WORK DEBTRACE
         AIF   ('&PFX' EQ '').CHEKPF                            GP02248
&ZZZDBTP SETC  '&PFX'        PREFIX GLOBAL                      GP02248
.CHEKPF  AIF   ('&ZZZDBTP' NE '').DONEPF                        GP02248
&ZZZDBTP SETC  'DBT'         DEFAULT PREFIX                     GP02248
.DONEPF  ANOP  ,                                                 95067
&Q       SETC  '&ZZZDBTP'    SHORT VARIABLE NAME                GP02248
&BUGSWCH SETB  1                                                 95067
         AIF   ('&RQ1' EQ 'S').HEAD NORMAL EXPANSION ?
         AIF   ('&RQ1' EQ 'D' OR '&RQ1' EQ 'M').DATA             95228
         AIF   ('&RQ1' EQ 'MAP').DATA
         AIF   ('&RQ1' EQ 'DC').DATA EXPAND DATA ONLY
         AIF   ('&RQ1' EQ 'L').DONEBAS EXPAND PARM LIST ONLY
         AIF   ('&RQ1' EQ 'E').NOTTRC EXECUTE SPECIFIED LIST
         AIF   ('&RQ1' EQ 'ON').SWON                             95079
         AIF   ('&RQ1' EQ 'OFF').SWOFF                           95079
         AIF   ('&RQ1' EQ 'CLOSE').SWEND CLOSE AND QUIT         GP98222
         MNOTE 8,'DBT: UNRECOGNIZED MODE &RQ1 '
.HEAD    ANOP  ,             STANDARD EXPANSION
.*--------------------------------------------------------------------*
.*                                                                    *
.*  WHEN THE BUGTRC GLOBAL IS SET, THE REQUEST IS INTERCEPTED BY AN   *
.*  ESPIE EXIT ROUTINE, SUCH AS ONES CONTAINED IN PGMTRACE, EXORSPIE, *
.*  AND @BANDAID. THE NORMAL SUBROUTINE CALL IS NOT NECESSARY, NOR IS *
.*  THE STANDARD WORK AREA                                            *
.*                                                                    *
.*--------------------------------------------------------------------*
         AIF   (NOT &BUGTRC).NOTTRC                             GP99113
         AIF   ('&RQ1' EQ 'E').DIAGREM    REMOVE LIST
&NM      DC    X'83CD',SL2(*+2) INVOKE TRACE VIE ESPIE; LIST FOLLOWS
         AGO   .DONEBAS                                         GP99113
.DIAGREM ANOP  ,                                                GP99113
&NM      DC    X'83CD',SL2(&RQ2) INVOKE TRACE VIE ESPIE; LIST NAMED
         AIF   ('&RQ2' NE '').DONEBAS
         MNOTE 8,'DBT: MODE E REQUIRES LIST ADDRESS (E,LIST)'
         AGO   .DONEBAS                                         GP99113
.NOTTRC  ANOP  ,                                                GP99113
.*--------------------------------------------------------------------*
.*                                                                    *
.*  SAVE THE MINIMAL REGISTERS, AND GO TO SUBDEBTR TO DO THE OTHER    *
.*  REGISTERS, SAVE THE AR MODE, AM, LOAD DEBTRACE, GET WORKING       *
.*  STORAGE, AND EXECUTE THE QUEST.                                   *
.*                                                                    *
.*--------------------------------------------------------------------*
&NM      BAKR  R0,R0         SAVE EVERYTHING; RETURN HERE       GP03006
         XI    &Q.FLAG2,&Q.FBAKR  SECOND TIME AROUND ?          GP03006
         BZ    &ZZZDBTV.X    YES; GET OUT                       GP03006
         SAC   0             GET PRIMARY                        GP03006
         AIF   ('&COUNT' EQ '').NONEWCT
         LA    R14,&COUNT(1)+0
         ST    R14,&Q.CNT1   SET NEW LIMIT COUNT
         LA    R14,&COUNT(2)+0
         ST    R14,&Q.CNT2
         ST    R14,&Q.CNT2I  SET NEW CYCLE COUNT
         LA    R14,&COUNT(3)+0
         ST    R14,&Q.CNT3   SET NEW SKIP COUNT
.NONEWCT LA    R0,&Q.WORK    POINT TO DBT WORK AREA             GP03006
         L     R15,=V(SUBDEBTR)  INTERFACE ROUTINE ADDR.        GP03006
         AIF   ('&RQ1' NE 'E').GOCALL NORMAL CALL/LIST EXPANSION
         AIF   ('&RQ2' NE '').LOADPRM
         MNOTE 8,'DBT: MODE E REQUIRES LIST ADDRESS (E,LIST)'
         MEXIT ,             OUCH
.LOADPRM LA    R14,&RQ2      LOAD PARM LIST ADDRESS
         BR    R15           INVOKE THE STUB
         AGO   .DONEBAS      JOIN COMMON
.GOCALL  BASR  R14,R15       INVOKE IN CURRENT MODE, ETC.
.*   NOTE THAT DEBTRACE RETURNS AFTER BAKR
.*--------------------------------------------------------------------*
.*                                                                    *
.*  EXPAND THE REQUEST AS FOLLOWS:                                    *
.*       HALFWORD GIVING THE LENGTH OF THE LIST                       *
.*       (OPTIONAL) CL8 NAME FOR THIS CALL                            *
.*       ZERO OR MORE FORMAT REQUESTS - TYPE/MODIFIER/S(ADDRESS,LEN)  *
.*       X'80' ON IN LAST TYPE ENTRY                                  *
.*                                                                    *
.*--------------------------------------------------------------------*
.DONEBAS AIF   ('&RQ1' EQ 'E').NEEDLBL
&LISTNM  SETC  '&LNAME'
         AIF   ('&RQ1' NE 'L').LABLBL
         AIF   ('&NM' EQ '' AND '&LNAME' EQ '').BADLLB  NO LABELS
         AIF   ('&NM' NE '' AND '&LNAME' NE '').NMLAB  TWO LABELS
&LISTNM  SETC  '&NM'.'&LNAME'  USE WHICHEVER IS NON-NULL
         AGO   .LABLBL
.NMLAB   ANOP  ,
&NM      DS    0H            LIST NAME
         AGO   .LABLBL
.BADLLB  MNOTE 8,'DBT: MODE L REQUIRES A LIST NAME'
.LABLBL  AIF   ('&LBL' EQ '').NOLBL                             GP03006
&LISTNM  DC    X'0100',CL8'&LBL '  ADD A NAME TO THE FIRST LINE
         AGO   .NOLBL
.NMLBL   AIF   ('&LISTNM' EQ '').NOLBL
&LISTNM  DS    0H
.NOLBL   AIF   ('&ID' EQ 'NO').NOIDS
         DC    X'0200'       IDENTIFY CALLER
.NOIDS   AIF   ('&LIST' NE '').HBLIST                           GP02365
         AIF   ('&PACK' NE '').HBPACK                           GP02365
         AIF   ('&HEX' NE '').HBHEX                             GP02365
         AIF   ('&CTEXT' NE '').HBCTEXT                         GP02365
         AIF   ('&TEXT' NE '').HBTEXT                           GP02365
         AIF   ('&REGS' NE '' OR '&REGTYPE' NE '').HBREGS
.HBREGS  ANOP  ,                                                GP02365
&HBREG   SETB  1                                                GP02365
         AGO   .DONEVL                                          GP02365
.HBTEXT  ANOP  ,                                                GP02365
&HBTXT   SETB  1                                                GP02365
         AGO   .DONEVL                                          GP02365
.HBCTEXT ANOP  ,                                                GP02365
&HBCTX   SETB  1                                                GP02365
         AGO   .DONEVL                                          GP02365
.HBHEX   ANOP  ,                                                GP02365
&HBHEX   SETB  1                                                GP02365
         AGO   .DONEVL                                          GP02365
.HBPACK  ANOP  ,                                                GP02365
&HBPAK   SETB  1                                                GP02365
         AGO   .DONEVL                                          GP02365
.HBLIST  ANOP  ,                                                GP02365
&HBLST   SETB  1                                                GP02365
.DONEVL  AIF   ('&REGS' EQ '' OR '&REGS' EQ 'NO').NOREGS         95079
&RGT     SETC  '0'
&I       SETA  0
&LN      SETA  N'&REGTYPE
.LUPRTYP AIF   (&I GE &LN).DONRTYP
&I       SETA  &I+1
&RGT     SETC  '&REGTYPE(&I)'
&RTGPR   SETB  (&RTGPR OR '&RGT' EQ 'GP' OR '&RGT' EQ 'GPR'            *
               OR '&RGT' EQ 'ALL')
&RTACR   SETB  (&RTACR OR '&RGT' EQ 'AC' OR '&RGT' EQ 'ACR'            *
               OR '&RGT' EQ 'ALL')
&RTCTL   SETB  (&RTCTL OR '&RGT' EQ 'CT' OR '&RGT' EQ 'CTL'            *
               OR '&RGT' EQ 'ALL')
&RTFPR   SETB  (&RTFPR OR '&RGT' EQ 'FP' OR '&RGT' EQ 'FPR'            *
               OR '&RGT' EQ 'HFP' OR '&RGT' EQ 'BFP'                   *
               OR '&RGT' EQ 'ALL')
.DONRTYP ANOP  ,
&I       SETA  1*&RTGPR+2*&RTACR+4*&RTCTL+8*&RTFPR
&RGT     SETC  '&I'
&ZZZ80FG SETB  (&ZZZ80FG OR &HBREG)
         AIF   ('&REGS' EQ '' AND '&REGTYPE' NE '').REGSALL
         AIF   ('&REGS' EQ 'R15' OR '&REGS' EQ 'SHORT'                 *
               OR '&REGS' EQ 'RET').RETREG                      GP97225
         AIF   ('&REGS' EQ 'YES' OR '&REGS' EQ 'ALL').REGSALL   GP02246
         AIF   ('&REGS' EQ 'YES').REGSALL                       GP97225
         AIF   (N'&REGS EQ 2).REGS2                             GP97225
         DC    AL1(&HBREG*X'80'+3,&RGT,&REGS(1),&REGS(1))
         AGO   .NOREGS                                          GP97225
.REGS2   DC    AL1(&HBREG*X'80'+3,&RGT,&REGS(1),&REGS(2))
         AGO   .NOREGS                                          GP97225
.REGSALL DC    AL1(&HBREG*X'80'+3,&RGT,0,15)
         AGO   .NOREGS                                          GP97225
.RETREG  DC    AL1(&HBREG*X'80'+3,&RGT,14,1) R14-R1 ONLY
.NOREGS  AIF   ('&TEXT' EQ '').NOTEXT
         DEBTRACY &TEXT(1),&TEXT(2),T,VL80=&HBTXT               GP02365
.NOTEXT  AIF   ('&CTEXT' EQ '').NOCTEXT                         GP97225
         DEBTRACY &CTEXT(1),&CTEXT(2),CT,VL80=&HBCTX            GP02365
.NOCTEXT AIF   ('&HEX' EQ '').NOHEX                             GP97225
         DEBTRACY &HEX(1),&HEX(2),X,VL80=&HBHEX                 GP02365
.NOHEX   AIF   ('&PACK' EQ '').NOPACK                           GP97225
         DEBTRACY &PACK(1),&PACK(2),P,VL80=&HBPAK               GP02365
.NOPACK  AIF   ('&LIST' EQ '' OR N'&LIST LT 1).NOLIST           GP95235
&I       SETA  0             JUST IN CASE                       GP03009
&LN      SETA  N'&LIST                                          GP95235
.DOLIST  AIF   (&I GE &LN).NOLIST   DONE WITH LIST              GP95235
&I       SETA  &I+1          BUMP LOOP INDEX                    GP95235
&EN      SETA  N'&LIST(&I)   NUMBER OF ENTRIES                  GP95235
         AIF   (&EN LT 1).DOLIST  USER IN COMA?                 GP95235
         AIF   (&EN LT 6).TOOLIST WARN                          GP95235
         MNOTE 4,'DEBTRACY: LIST(&I) HAS TOO MANY (&EN) ENTRIES'
.TOOLIST ANOP  ,                                                GP02365
&HBLST   SETB  (&LN EQ &I)   LAST LIST ITEM ?                   GP02365
         DEBTRACY &LIST(&I,1),&LIST(&I,2),&LIST(&I,3),&LIST(&I,4),&LIST*
               (&I,5),&ZZZDBTT,LABEL=Y,VL80=&HBLST
         AGO   .DOLIST       DO NEXT LIST ITEM
.NOLIST  AIF   (&ZZZ80FG).NEEDLBL
         DC    X'8000'       ADD NOP WITH END OF LIST FLAG
.NEEDLBL ANOP  ,             TARGET FOR MODE=E
&ZZZDBTV.X DS  0H            END OF TRACE LIST                  GP99113
         AGO   .MEND                                            GP99113
.SWON    OI    &Q.FLAG,&Q.FLON  SET TRACING ON                  GP02248
         AGO   .MEND                                             95079
.SWEND   OI    &Q.FLAG,&Q.FLEND  CLOSE DCB AND STOP TRACE       GP02248
.SWOFF   NI    &Q.FLAG,255-&Q.FLON  SET TRACING OFF             GP02248
         AGO   .MEND                                             95079
.*--------------------------------------------------------------------*
.*                                                                    *
.*  EXPAND THE DBT (USER'S) WORK AREA. &Q IS GLOBAL PREFIX            *
.*                                                                    *
.*--------------------------------------------------------------------*
.DATA    AIF   ('&NM' EQ '').NODLBL
&NM      DS    0D
.NODLBL  ANOP  ,                                                 95067
&BUGSWRT SETB  1                                                 95067
&Q.WORK  DS    0F            START OF DEBTRACE WORK AREA        GP03006
&Q.@CODE DC    A(0)          LOADED ADDRESS OF DEBTRACE
&Q.@WORK DC    A(0)          STORAGE ADDRESS OF DEBTRACE WORK
&Q.FLAG  DC    AL1(&BUGSWCH*&Q.FLON+&Q.FLWID)  DEBUG FLAG
&Q.FLTCB EQU   128             INCLUDE TCB ADDRESS IN MESSAGE
&Q.FLPUT EQU   64              USE TPUT UNLESS DD AVAIL         GP04052
&Q.FLWTO EQU   32              USE WTO INSTEAD OF PRT
&Q.FLWID EQU   16              USE WIDE FORMAT WHEN PRINTING
&Q.FLPRO EQU   08              PRODUCTION MODE / NEED DD TO PRT
&Q.FLSPI EQU   04              DON'T ISSUE ESPIE
&Q.FLEND EQU   02              THIS IS A TERMINATION CALL
&Q.FLON  EQU   01              DEBUG BIT
&Q.FLAG2 DC    AL1(0)        (VALID) BAKR RECURSION FLAG        GP03006
&Q.FBAKR EQU   2               ON IF RETURN FROM PR/BAKR        GP03006
&Q.FLAG3 DC    AL1(0)        ..RESERVED..
&Q.STOMP DC    AL1(0)        USED BY DEBSTOMP MACRO TO TRACK PROGRESS
         MAPCMPRT PFX=&Q,DCB=&DCB,PRTMODE=&PRTMODE,DEV=&DEV     GP03003
&Q.CNT1  DC    A(&COUNT(1)+0)  LIMIT COUNT
&Q.CNT2I DC    A(&COUNT(2)+0)  INTERVAL COUNT
&Q.CNT2  DC    A(&COUNT(2)+0)  INTERVAL REFRESH FOR NEXT CYCLE
&Q.CNT3  DC    A(&COUNT(3)+0)  INITIAL SKIP COUNT
         MEXIT ,
.WORK    ANOP  ,
.*--------------------------------------------------------------------*
.*                                                                    *
.*  THIS WORK AREA CONTAINS EVERYTHING NEEDED TO MAKE DEBTRACE        *
.*  RE-ENTRANT AND REFRESHABLE, AND IS MAPPED SEPARATELY TO EXCHANGE  *
.*  INFORMATION WITH SUBDEBTR, THE INTERFACE STUB FOR DBT MACROS.     *
.*                                                                    *
.*--------------------------------------------------------------------*
&NM      DS    0D            ALIGNMENT FOR DB, ETC
&Q       SETC  '&PFX'        SET PREFIX
         AIF   ('&PFX' NE '').WORKOUT
&Q       SETC  'DBW'         LOCAL PFX FOR DEBTRACE WORK AREA
.WORKOUT ANOP  ,
&Q.BLKID DC    CL4'DBTW'     IDENTIFY IN DUMP
&Q.TKSPI DC    A(0)          HIGHER ESPIE TOKEN                 GP02353
&Q.DB    DC    D'0'          WORK SPACE
&Q.DB2   DC    D'0'          WORK SPACE
&Q.DB3   DC    D'0'          WORK SPACE
&Q.SUBSV DC    18A(0)        DEBTRACE SAVE AREA
&Q.PK16  DC    PL16'0'       WORK SPACE / TEMP SAVE
&Q.PK16A DC    PL16'0'       MAX PACK-TOR
&Q.PK16B DC    PL16'0'       MAX PACK-TOR
&Q.PRTSV DC    8A(0)         WORK SPACE FOR PRINTING
&Q.SVUSR DC    32A(0)        SAVED 16 64-BIT GENERAL REGISTERS
&Q.SVACR DC    16A(0)        SAVED 16 32-BIT ACCESS CONTROL REGS
&Q.SVCCR DC    32A(0)        SAVED 16 64-BIT CONTROL REGS
&Q.SVFPR DC    16D'0'        FLOATING POINT REGISTERS (EITHER)
&Q.OPSW  DC    XL8'0',XL8'0'   CALLER'S PSW
         SPACE 1                                                GP96109
&Q.MXOLN DC    H'0' (0 IS FG) MAXIMUM OUTPUT LENGTH, INCL. RDW  GP99113
&Q.PRMLN DC    H'0'          LENGTH OF CURRENT PARM LIST ENTRY
&Q.RCFMX DC    X'0'          INDEX FOR RECFM F/U VS V/D         GP99113
&Q.CCHAR DC    C' '          ASA SINGLE SPACE/MCC SKIP+PRINT    GP99113
&Q.FLAGS DC    X'00'         LOCAL PROCESSING FLAGS             GP99113
&Q.FGWID EQU   X'80'           PROCESSING WIDE LINES            GP99113
&Q.FGBOT EQU   X'40'           SHOW HEX + TEXT                  GP00162
&Q.FGNEG EQU   X'04'           PROCESSING A NEGATIVE NUMBER
&Q.FGSPI EQU   X'02'           ESPIE ISSUED
&Q.FGCAN EQU   X'01'           GLOBAL CANCEL OF FUNCTION        GP00162
&Q.FGGBL EQU   (&Q.FGCAN)      FLAGS RETAINED GLOBALLY          GP99113
&Q.ACRMD DC    X'00'         ADDRESSING BITS (SAC FORMAT)
         SPACE 1
&Q.ADMSK DC    X'7FFFFFFF',X'FFFFFFFF'   ADDRESS MODE MASK
&Q.OPDCB OPEN  (&Q.PRDCB,OUTPUT),MF=L MODE=31
&Q.PRDCB DCB   DDNAME=DEBTRACE,DSORG=PS,MACRF=PM,RECFM=FBA,           **
               LRECL=&Q.MGRD2-&Q.MGHED+1                        GP03011
&Q.MGRDW DC    Y(&Q.MGRDC-*),X'8000'  WTO HEADER                GP96081
&Q.MGRCC DC    C' '          CARRIAGE CONTROL                   GP99113
&Q.MGHED DC    C'MSG666 '    DEBUG HEADER                        93357
&Q.MGLBL DC    CL8' ',C' '   USER'S LABEL                       GP97265
&Q.MGFLG DC    CL2' ',C' '   ANNOTATION/ERROR MSGS              GP97265
&Q.MGTXT DC    CL36' '       USER'S HEX OR EBCDIC TEXT          GP97265
         DC    C' '          EXTRA FOR UNPACK                    94011
&Q.MGTCB DC    CL8' ',C' '   CURRENT TCB ADDRESS+PADDING FOR UNPK
&Q.MGRDC DC    X'01000020'   DESCRIPTOR 8; ROUTCDE 11
         ORG   &Q.MGTCB        REDEFINE FOR WIDE OPTION         GP99113
         DC    C' '          GROUP SEPARATOR                    GP99113
&Q.MGTX2 DC    CL36' '       ANOTHER 4 HEX WORDS                GP99113
         DC    C' '          EXTRA FOR UNPACK                    94011
&Q.MGTC2 DC    CL8' ',C' '   CURRENT TCB ADDRESS+PADDING FOR UNPK
         ORG   &Q.MGTC2      RE-USE FOR WIDE DUAL OPTION        GP00162
         DC    C' '          LEFT FRAME *                       GP00162
&Q.MGHTX DC    CL32' '       TEXT FOR HEX DUAL OPTION           GP00162
         DC    C' '          RIGHT FRAME *                      GP00162
         ORG   &Q.MGRCC+133                                     GP99113
&Q.MGRD2 DC    X'01000020'   DESCRIPTOR 8; ROUTCDE 11           GP99113
&Q.SVRDW DC    AL2(5),X'8000',C' '  BACKUP WTO HEADER
&Q.ESPIE ESPIE SET,&Q.ESPIN,(1,2,3,4,5,6,7,8,9,10,11,15),PARAM=1,      *
               MF=L          PASS THE USER ADDRESS AS A PARAMETER
&Q.ESPSW DC    2XL8'0'       INTERRUPT PSW
&Q.ESPIN DC    4A(0)         INTERRUPT DATA
&Q.ESPGR DC    32A(0)        GEN REGS - 32 OR 64 BIT
&Q.ESPAR DC    16A(0)        ACC REGS - 32-BIT
&Q.ESPPF DC    A(0)          SAVED PROGRESS DATA
&Q.CHAIN DC    A(0)          POINTER FOR ANOTHER REQUEST
&Q.CHNPT DC    A(0)          REQWORK AT START OF CHAIN
&Q.CHNAD DC    A(0)          ADDRESS OF FIRST ELEMENT
&Q.CHNLN DC    F'0'          LENGTH OF EVERY ELEMENT
&Q.CHNCT DC    H'0'          ELEMENT NUMBER ON CHAIN (REL TO 1)
&Q.EXECS DC    4A(0)         PUSHED EXEC STACK
&Q.EXECZ DC    A(0)          PUSHED EXEC STACK - LAST ENTRY
&Q.SPIBY DC    A(0)          RECOVERY ADDRESS (AM31 FORCED)
&Q.SPIPF DC    A(0)          PROGRESS DATA
&Q.SPIPM DC    A(0)          PROGRESS DATA - PARM LIST ADDRESS
&Q.SPIAD DC    A(0)          PROGRESS DATA - DATA ADDRESS
&Q.SPILN DC    A(0)          PROGRESS DATA - DATA LENGTH
&Q.SPISZ EQU   *-&Q.ESPSW    CLEAR ON ENTRY
         SPACE 1
&Q.PATCH DC    30S(*)        MINI-PATCH                         GP95235
&Q.SIZE  EQU   *-&Q.BLKID    STORAGE SIZE FOR WORK BLOCK
.MEND    MEND  ,
