         MACRO ,
&NM      XRENT &R,&PAT,&LEN,&MODE=MAIN,&CLEAR=YES
.**********************************************************************
.*                                                                    *
.*   XRENT IS AN EXHIBIT MACRO OBVIATING A GETMAIN FOR AREAS < 4K     *
.*                                                                    *
.*   R IS REGISTER 2-7, 9, OR POSSIBLY 10, UNUSED BY EXHIBIT FUNCT.   *
.*   (R,LABEL)  ALSO GENERATES A USING LABEL,R                        *
.*                                                                    *
.*   PAT,LEN SPECIFY THE ADDRESS AND LENGTH OF A PATTERN AREA TO BE   *
.*     MOVED TO THE WORK AREA. UNUSED SPACE IS ZEROES. NOTE THAT      *
.*     ADDRESS CONSTANTS ARE NOT RELOCATED.                           *
.*   WHEN LEN IS OMITTED, &PAT.L IS USED                              *
.*   OMITTING PAT CAUSES THE AREA TO BE CLEARED                       *
.*                                                                    *
.*   CLEAR=NO BYPASSES CLEARING EVEN IF PAT IS SPECIFIED              *
.*                                                                    *
.*   MODE=MAIN REQUESTS THE AREA RESERVED FOR MAIN PROGRAMS           *
.*   MODE=SUB  REQUESTS THE AREA RESERVED FOR SUBROUTINES             *
.*                                                                    *
.**********************************************************************
         LCLA  &M
&M       SETA  0             MAIN WORK AREA
         AIF   (T'&O NE 'O').ROK
         MNOTE 8,'XRENT: REGISTER REQUIRED'
         MEXIT ,
.ROK     AIF   ('&MODE' EQ 'MAIN' OR '&MODE' EQ '').MOK
&M       SETA  8             SUBSIDIARY WORK AREA
         AIF   ('&MODE' EQ 'SUB').MOK
         MNOTE 8,'XRENT: INVALID MODE=&MODE '
.MOK     AIF   ('&CLEAR' EQ 'YES' OR '&CLEAR' EQ '').COK
         AIF   ('&CLEAR' EQ 'NO').COK
         MNOTE 4,'XRENT: INVALID CLEAR=&CLEAR'
.COK     ANOP  ,
&NM      MACPARM &R(1),EXW@RENT+&M   GET WORK AREA ADDRESS
         AIF   (N'&R LT 2).NOUSE
         USING &R(2),&R(1)
.NOUSE   AIF   ('&CLEAR' EQ 'NO').MEND
         AIF   (T'&PAT EQ 'O').DEFCLR
         MACPARM R14,&PAT
         MACPARM R15,&LEN,NULL=&PAT.L
         AGO   .COMCLR
.DEFCLR  MACPARM R15,R15,OP=SLR,OPR=SLR,MODE=EVEN
.COMCLR  MACPARM R0,R1,&M+EXW@RENT
         MACPARM R0,R14,OP=MVCL
.MEND    MEND   ,
