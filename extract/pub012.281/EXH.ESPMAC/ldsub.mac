         MACRO
&L       LDSUB &DUMMY,&NAME=DMP,&PAD=,&RESERVE=ALL,&SIZE=,             *
               &REG=R2,&LOADADD=R7,&MODE=32K,&MIN=12*1024,&MAX=62*1024
         GBLC  &SYSTEM
         GBLB  &OS390,&MVS   EXHFSIZ NOT HONORED BY SYSTEM      GP04234
         LCLC  &L1,&L2,&L3
         LCLC  &MASK
         AIF   ('&PAD' EQ '').NOPAD1
         AIF   ('&RESERVE' EQ '' OR '&RESERVE' EQ 'ALL').NOPAD1  80013
         MNOTE 12,'&&PAD AND &&RESERVE ARE MUTUALLY EXCLUSIVE'
         MEXIT
.NOPAD1  ANOP  ,
         AIF   ('&SIZE' EQ '').NOSIZ1
         AIF   ('&PAD' EQ '').NOPAD2
         MNOTE 12,'&&PAD AND &&SIZE ARE MUTUALLY EXCLUSIVE'
         MEXIT
.NOPAD2  AIF   ('&RESERVE' EQ '' OR '&RESERVE' EQ 'ALL').NOSIZ1
         MNOTE 12,'&&SIZE AND &&RESERVE ARE MUTUALLY EXCLUSIVE'
         MEXIT
.NOSIZ1  ANOP  ,
&L1      SETC  'TRA&SYSNDX'
&L2      SETC  'TRB&SYSNDX'
&L3      SETC  'TRC&SYSNDX'
&MASK    SETC  '00FFF000'
         AIF   ('&SYSTEM' EQ 'SVS' OR '&SYSTEM' EQ 'MVS').PAGE4K
&MASK    SETC  '00FFF800'
.*AGE4K  AIF   ('&MODE' EQ '64K' OR '&MODE' EQ '65K').BIGLOAD    85317
.PAGE4K  AIF   ('&MODE' EQ '64K' OR '&MODE' EQ '65K').LITLOAD   GP04234
         AIF   ('&MODE' EQ '' OR '&MODE' EQ '32K').LITLOAD       85317
         MNOTE 4,'INVALID MODE OPERAND (NOT 32K OR 64K)'         85317
.LITLOAD AIF   (&MVS).NEWCODE  DROP REGION MANAGEMENT           GP04234
&L       MVI   EXCINVOK+22,0           RESET FOR BLDL TEST
         MVC   EXCINVOK+5(3),=C'&NAME' COMPLETE NAME OF SUBROUTINE
         LA    R0,EXCSBLDL   POINT TO BLDL HEADER                86349
         L     R15,SQBLDLA   GET ADDRESS OF BLDL CODE            86349
         BALR  R14,R15       INVOKE BLDL                         86349
         MVI   DB,0                                             GP04234
         MVC   DB+1(3),EXHFSIZ-EXHBWENT+EXCINVOK    SAVE ACTUAL SIZE
         AIF   ('&SIZE' NE '').SIZ2
         AIF   ('&PAD' EQ '').NOPAD3
         L     R0,DB         ADD PAD TO ACTUAL SIZE             GP04234
         MACPARM R0,&PAD,OP=AH,OPR=AR
         AGO   .NOSIZ2
.SIZ2    MACPARM R0,&SIZE,OP=LH,OPR=LR GET DESIRED LOAD SIZE
         C     R0,DB                   IS IT LESS THAN MODULE SIZE ?
         BL    &L2                      YES - REJECT REQUEST
.NOSIZ2  ST    R0,DB                   SET MODULE SIZE TO NEW VALUE
         L     &REG,EXCSTGMX           GET FREE STORAGE SIZE    GP04234
         N     &REG,=X'&MASK'          TRUNCATE TO PAGE BOUNDARY
         CR    &REG,R0                 WILL MODULE FIT ?
         AGO   .PAD3
.NOPAD3  L     &REG,EXCSTGMX           GET FREE STORAGE SIZE    GP04234
         AIF   ('&RESERVE' EQ '' OR '&RESERVE' EQ 'ALL').NORSV1
         LR    R0,&REG                 SAVE FOR RESERVE CALCULATION
.NORSV1  ANOP  ,
         N     &REG,=X'&MASK'          TRUNCATE TO PAGE BOUNDARY
         AIF   ('&RESERVE' EQ '' OR '&RESERVE' EQ 'ALL').NORSV2
         SR    R0,&REG                 R0=FREE STORAGE IN LAST PAGE
         MACPARM R0,&RESERVE,OP=CH,OPR=CR IS IT ENOUGH FOR RESERVE?
         BNL   &L1                       YES - LEAVE WELL ENOUGH ALONE
         MACPARM &REG,&RESERVE,OP=SH,OPR=SR NO - CUT BACK LOAD SIZE
.NORSV2  ANOP  ,
&L1      ST    &REG,DB+4               SAVE CALCULATED LOAD SIZE
         S     &REG,DB                 AMOUNT IN EXCESS OF MODULE SIZE
.PAD3    BNM   &L3                     GOOD, MODULE FITS
&L2      MVC   INVOKE(8),EXCINVOK      COPY NAME
         B     EXCXCB               AND SHOW MODULE TO BE UNAVAILABLE
&L3      MVC   EXHFSIZ-EXHBWENT+EXCINVOK(3),DB+5    FORCE LARGEST
         BAL   R9,EXCLOD2    TEST IF REALLY AVAILABLE
         LTR   &LOADADD,R0             GOT IT ?
         BZ    &L2                     NO, SIGNAL ERROR
         MEXIT ,                                                 85317
.BIGLOAD ANOP  ,                                                 85317
&L       MVI   EXCINVOK+22,0           RESET FOR BLDL TEST       85317
         MVC   EXCINVOK+5(3),=C'&NAME' COMPLETE NAME OF SUBROUTINE
         LA    R0,EXCSBLDL   POINT TO BLDL HEADER                86349
         L     R15,SQBLDLA   GET ADDRESS OF BLDL CODE            86349
         BALR  R14,R15       INVOKE BLDL                         86349
         TM    EXCINVOK+22,EXHF1ATT  REUS + EXEC ?               85317
         BNO   &L2           NO                                  85317
         L     R0,EXCSTGMX   GET AVAILABLE STORAGE              GP04234
         ST    R0,DB+4       SET AS AVAILABLE MAXIMUM            85317
         GETMAIN VC,LA=&L1.L,A=&L1.A   GET SOME STORAGE          85317
         BXH   R15,R15,&L1.F   NONE GOTTEN                       85317
         LM    R15,R0,&L1.A  GET ADDRESS/LENGTH                  85317
         LR    R1,R15        SWAP                                85317
         ST    R0,DB+4       SAVE SIZE AVAILABLE                 85317
         FREEMAIN R,LV=(0),A=(1)  FREE IT UP AGAIN               85317
         B     &L1.F         BRANCH AROUND LISTS                 85317
&L1.L    DC    A(&MIN,&MAX)  REQUESTED STORAGE                   85317
&L1.A    DC    A(0,0)        OBTAINED ADDRESS/LENGTH             85317
&L1.F    MVC   DB+1(3),EXHFSIZ-EXHBWENT+EXCINVOK   ACTUAL SIZE  GP04234
         AIF   ('&SIZE' NE '').BSIZ2                             85317
         AIF   ('&PAD' EQ '').BNOPAD3                            85317
         MACPARM R0,&PAD,OP=LH,OPR=LR  GET PAD SIZE              85317
         N     R0,=X'0000FFFF'  KILL SIGN EXTENSION              85317
         A     R0,DB         ADD THE MODULE SIZE                 85317
         AGO   .BNOSIZ2                                          85317
.BSIZ2   MACPARM R0,&SIZE,OP=LH,OPR=LR GET DESIRED LOAD SIZE     85317
         N     R0,=X'0000FFFF'  KILL SIGN EXTENSION              85317
         CL    R0,DB         IS IT LESS THAN MODULE SIZE ?       85317
         BL    &L2           YES - REJECT REQUEST                85317
.BNOSIZ2 ST    R0,DB                   SET MODULE SIZE TO NEW VALUE
         L     &REG,DB+4     GET FREE STORAGE SIZE               85317
         N     &REG,=X'&MASK'          TRUNCATE TO PAGE BOUNDARY 85317
         CR    &REG,R0       WILL MODULE FIT ?                   85317
         AGO   .BPAD3                                            85317
.BNOPAD3 L     &REG,DB+4     GET FREE STORAGE SIZE               85317
         AIF   ('&RESERVE' EQ '' OR '&RESERVE' EQ 'ALL').BNORSV1 85317
         LR    R0,&REG                 SAVE FOR RESERVE CALCULATION
.BNORSV1 ANOP  ,                                                 85317
         N     &REG,=X'&MASK'          TRUNCATE TO PAGE BOUNDARY 85317
         AIF   ('&RESERVE' EQ '' OR '&RESERVE' EQ 'ALL').BNORSV2 85317
         SR    R0,&REG                 R0=FREE STORAGE IN LAST PAGE
         MACPARM R15,&RESERVE,OP=LH,OPR=LR GET RESERVE SIZE      85317
         N     R15,=X'0000FFFF'        KILL SIGN EXTENSION       85317
         CR    R0,R15        ENOUGH FOR RESERVE ?                85317
         BNL   &L1                       YES - LEAVE WELL ENOUGH ALONE
         SR    &REG,R15      REMOVE RESERVE SIZE                 85317
.BNORSV2 ANOP  ,                                                 85317
&L1      ST    &REG,DB+4               SAVE CALCULATED LOAD SIZE 85317
         L     R15,DB        GET MODULE SIZE BACK                85317
         N     R15,=X'00FFFFFF'   KILL SIGN EXTENSION            85317
         SR    &REG,R15      AMOUNT IN EXCESS OF MODULE SIZE     85317
.BPAD3   BNM   &L3                     GOOD, MODULE FITS         85317
&L2      MVC   INVOKE(8),EXCINVOK      COPY NAME                 85317
         B     EXCXCB               AND SHOW MODULE TO BE UNAVAILABLE
&L3      MVC   EXHFSIZ-EXHBWENT+EXCINVOK(3),DB+5  FORCE LARGEST GP04234
         AIF   ('&RESERVE' NE '' AND '&RESERVE' NE 'ALL').BNORSVL
         SLR   R15,R15                                           85317
.BNORSVL AIF   ('&RESERVE' EQ '' OR '&RESERVE' EQ 'ALL').BNORSV3 85317
         MACPARM R15,&RESERVE,OP=LH,OPR=LR GET RESERVE SIZE      85317
         N     R15,=X'0000FFFF'        KILL SIGN EXTENSION       85317
.BNORSV3 L     R0,EXCSTGMX   GET SUPPOSED AVAILABLE STORAGE     GP04234
         SR    R0,R15        SUBTRACT THE RESERVE SIZE           85317
         ST    R0,EXCSZLOD   SET AS FAKE MODULE SIZE            GP04234
         ST    R15,EXCSTGMX  SET RESERVE AS REMAINDER           GP04234
         BAL   R9,EXCLOD5    LOAD THE MODULE                     85317
         LTR   &LOADADD,R0             GOT IT ?                  85317
         BZ    &L2                     NO, SIGNAL ERROR          85317
         MEXIT ,                                                GP02231
.NEWCODE ANOP  ,                                                GP02231
.*  IN THE LATEST INCARNATION, REGION MANAGEMENT HAS BEEN DROPPED.
.*  LOAD MODULE SIZE IS FIXED (FOR DAP DETERMINED BY NAME), RESERVE,
.*  SIZE, AND PAD ARE NO LONGER MEANINGFUL.                     GP02231
&L       XLOAD '&NAME',&LOADADD  LOAD THE MODULE                GP02231
.*            EXITS TO EXCXCB WITH MESSAGE IF UNAVAILABLE       GP02231
         MVI   DB,0                                             GP02231
         MVC   DB+1(3),EXHFSIZ-EXHBWENT+EXCINVOK  SAVE ACTUAL SIZE
         MVC   DB+4(4),=A(&MAX)                                 GP02231
         SR    &REG,&REG                                        GP02231
.MEND    MEND  ,                                                GP02231
