         MACRO
&NM      XTRANHED &DOLL=C'Ö',&MOD=                              GP15263
         COPY  ASMOPTS
*   This module provides translation code and translate tables
*     for specific devices.  The code assumes that the translate
*     instruction may be used; e.g. APL and similar keyboards
*     with two-character shift sequences are not supported.
*
*   Input R2 - buffer to be translated  R3 - buffer length
*         R0 - function switch 0/out,data  2/out,control
*                              4/in,var. case  6/in,upper case
*         R15 - base address of this module
*         R14 - return address
*        R0-R3 are destroyed
*
*   MOD=       generates the expansion for EXHTxxxx modules
*   MOD=WORK   generates the expansion for MAPWORK
*
*   Adapted and converted from TRANHEAD macro
*   Note that expansions in EXHT and xxxxWORK modules must be
*     the same length (text copied to WORK by EXHALONE/EXHAINIT)
*
         LCLA  &I
         LCLC  &N1,&LBL
&N1      SETC  '&NM'
&LBL     SETC  '&NM'
         AIF   ('&N1' EQ '').BOOBOO
&I       SETA  K'&NM-4
         AIF   (&I LT 1).BOOBOO
         AIF   ('&N1'(1,4) NE 'EXHT').BOOBOO
&N1      SETC  '&N1'(5,&I)
         AIF   ('&MOD' EQ 'WORK').WORKHD
&LBL     START 0
&LBL     SETC  ''
         AGO   .COM
.WORKHD  PUSH  USING
         DROP  ,
.COM     USING &NM,R15
         NUSE  SQSP,R13
         NUSE  WORK,R11
&LBL     B     TRANSLAT      GO TO TRANSLATION CODE
         SPACE 1
         DC    CL4'&N1 '     MODULE ID
TRANPOIN DC    Y(OUTDATA-&NM)  OUTPUT TABLE; DATA ONLY
         DC    Y(OUTCON-&NM)   OUTPUT TABLE; CONTROL AND DATA
TRANULOW DC    Y(INUPLOW-&NM)  INPUT TABLE; UPPER+LOWER CASE
         DC    Y(INUPPER-&NM)  INPUT TABLE; UPPER CASE ONLY
TRANSDOL DC    AL1(&DOLL)    ALTERNATE FOR $ IN COMMANDS         87176
         DC    XL3'0'          SPARE                             87176
         DC    3A(0)         FOR EXPANSION
         SPACE 1
TRANSLAT CH    R0,TRANULOF   UPLOW INPUT ?
         BNE   TRANSLAS        NO
         TM    SQTRANS,X'80' UPLOW FLAG SET ?
         BNZ   TRANSLAS        YES; LEAVE LOWER CASE
         LA    R0,TRANULOW+2-TRANPOIN  SET UPPER CASE ONLY
TRANSLAS LR    R1,R0         COPY TABLE OFFSET
         LH    R1,TRANPOIN(R1)  GET TABLE OFFSET IN MODULE
         AR    R1,R15        GET TABLE ADDRESS
         LTR   R3,R3         VALID LENGTH ?
         BNPR  R14             NO; RETURN
         LR    R0,R14        SAVE RETURN ADDRESS
         LA    R14,255       MAKE LENGTH VALUE FOR EXECUTE
TRANLOOP CH    R3,TRAN256    MORE TO DO ?
         BNL   *+8             YES
         LR    R14,R3
         BCTR  R14,0         SET FOR EXECUTE
         EX    R14,TRANTR    TRANSLATE
         LA    R2,1(R2,R14)  BUMP BUFFER
         BCTR  R3,0            DECREMENT LENGTH
         SR    R3,R14        MORE TO DO ?
         BP    TRANLOOP        YES
         LR    R14,R0        REGAIN EXIT ADDRESS
         BR    R14           RETURN TO CALLER
TRANTR   TR    0(0,R2),0(R1)  TRANSLATE BUFFER
TRAN256  DC    H'256'        AVOID LITERALS
TRANULOF DC    AL2(TRANULOW-TRANPOIN)  UPLOW INPUT ?
         AIF   ('&MOD' EQ 'WORK').POP
         SPACE 1
         PUSH  PRINT
         PRINT &PRTSYS
&NM      CSECT ,
         MSECT ,
&NM      CSECT ,
         POP   PRINT
.MEXIT   MEXIT ,
.POP     POP   USING
         MEXIT ,
.BOOBOO  MNOTE 12,'NAME MISSING OR INVALID'
         MEND
