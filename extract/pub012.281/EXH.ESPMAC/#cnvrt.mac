         MACRO ,
&NM      #CNVRT &FUN,&ADR,&LEN,&OPF,&OPT=,&BZ=,&BNZ=,&BM=,&BP=  GP03122
         GBLC  &ZZCFXNM(32)
         GBLC  &ZZCFXF@                                         GP04055
         GBLC  &ZZCFXFC                                         GP04055
         GBLC  &MACPLAB
         GBLB  &ZZCFXFG
         GBLA  &ZZCFXFM
.*--------------------------------------------------------------------*
.*                                                                    *
.*  MACRO #CNVRT IS USED BY FUNCTIONS @PRINTER, @SCREENS, AND EXHIBIT *
.*  MODULE EXHASCRN TO INVOKE COMMON DATA CONVERSION ROUTINES IN      *
.*  MODULE @FORMATS                                                   *
.*                                                                    *
.*--------------------------------------------------------------------*
         LCLA  &I
&MACPLAB SETC  '&NM'
         AIF   ('&OPT' EQ 'TABLE' OR '&OPT' EQ 'EXPAND').LOCOPT GP04055
         AIF   ('&OPT' EQ '').KEEPOPT                           GP04055
&ZZCFXFC SETC  '&OPT'                                           GP04055
.KEEPOPT AIF   ('&ZZCFXFC' NE '').LOCOPT                        GP04055
&ZZCFXFC SETC  'CALL'        DEFAULT INVOKATION MODE            GP04055
.LOCOPT  AIF   ('&ZZCFXF@' NE '').HAVEPT                        GP04055
&ZZCFXF@ SETC  '@FORMATS'    DEFAULT ADDRESS OF @FORMATS        GP04055
     AIF   ('&SYSECT' NE 'EXHASCRN' AND '&SYSECT' NE '@SCREENS').HAVEPT
&ZZCFXF@ SETC  'ADFORMAT'    ADDRESS OF @FORMATS                GP04055
.HAVEPT  AIF   (&ZZCFXFG).DEFINED                               GP04055
&ZZCFXFG SETB  1
&ZZCFXNM(01) SETC 'DBK'      DEBLANK (STRIP "B")
&ZZCFXNM(02) SETC 'DBKL'     DEBLANK (STRIP "H")
&ZZCFXNM(03) SETC 'DBKR'     DEBLANK (STRIP "T")
&ZZCFXNM(04) SETC 'DBKZ'     REMOVE LEADING HEX AND CHAR ZEROES
&ZZCFXNM(05) SETC 'CENTER'   DEBLANK, THEN CENTER
&ZZCFXNM(06) SETC 'DATE'     8-CHAR DATE MM/DD/YY
&ZZCFXNM(07) SETC 'TIME'     8-CHAR TIME; 24-HOUR
&ZZCFXNM(08) SETC 'CONVERT'  NUMERIC (FD BASED) CONVERSION (INT->EXT)
&ZZCFXNM(09) SETC 'TREVNOC'  CONVERSION FROM EXTERNAL TO INTERNAL
&ZZCFXFM SETA  9
.DEFINED AIF   ('&OPT' EQ 'TABLE').EXPAND                       GP04055
         AIF   ('&OPT' EQ 'EXPAND').EXPAND                      GP04055
.LOOKUP  AIF   (&I GE &ZZCFXFM).NOMATCH
&I       SETA  &I+1
         AIF   ('&ZZCFXNM(&I)' EQ '&FUN').MATCH
         AGO   .LOOKUP
.NOMATCH MNOTE 8,'#CNVRT: FUNCTION &FUN NOT DEFINED'
&I       SETA  8             SET FOR CONVERT (?)
.MATCH   MACPARM R0,&LEN,NULL=SKIP   LOAD OPTIONS/LENGTH
         MACPARM R15,&OPF,NULL=SKIP  LOAD OPTIONS/OUTLEN
         MACPARM R1,&ADR,NULL=SKIP   LOAD ITEM ADDRESS
         AIF   ('&ZZCFXFC' EQ 'CALL').LOAD@                     GP03287
         AIF   ('&ZZCFXFC' EQ 'CALLA').LOADA                    GP03287
         AIF   ('&ZZCFXFC' EQ 'CALLV').LOADV                    GP03287
         MNOTE 4,'#CNVRT: UNRECOGNIZED OPT=&ZZCFXFC '           GP03287
.LOADV   MACPARM R14,=V(@FORMATS),OP=L                          GP03287
         AGO   .LOADCOM                                         GP03287
.LOADA   MACPARM R14,=A(@FORMATS),OP=L                          GP03287
         AGO   .LOADCOM                                         GP03287
.LOAD@   MACPARM R14,&ZZCFXF@,OP=L   LOAD CONVERSION ROUTINE ADDRESS
.LOADCOM ANOP  ,                                                GP03287
&I       SETA  &I-1
         AH    R14,32+&I*2(,R14)  GET FUNCTION ENTRY ADDRESS
         BASR  R14,R14       INVOKE FUNCTION
 AIF ('&BZ' EQ '' AND '&BNZ' EQ '' AND '&BM' EQ '' AND '&BP' EQ '').MND
.*OOPS   LTR   R0,R0         SET CONDITION CODE                 GP05032
         MACPARM &BZ,MODE=ONE,NULL=SKIP,OP=BZ,OPR=BZR           GP05032
         MACPARM &BM,MODE=ONE,NULL=SKIP,OP=BM,OPR=BMR           GP05032
         MACPARM &BP,MODE=ONE,NULL=SKIP,OP=BP,OPR=BPR           GP05032
         MACPARM &BNZ,MODE=ONE,NULL=SKIP,OP=BNZ,OPR=BNZR        GP05032
.MND     MEXIT ,
.EXPAND  AIF   (&I GE &ZZCFXFM).MEND
&I       SETA  &I+1
&MACPLAB DC    AL2(&ZZCFXNM(&I)-&SYSECT)
&MACPLAB SETC  ' '
         AGO   .EXPAND
.MEND    MEND  ,
