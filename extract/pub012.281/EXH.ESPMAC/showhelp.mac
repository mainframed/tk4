         MACRO ,
&NM      SHOWHELP &TEXT,&LEN,&EXIT=EXCIN,&PROMPT=EXMORE,&WK=R10
.*       ASSUMES TEXT IS N*CL80; MOVED LINE BY LINE
.*  R10 CHANGED TO &WK (FOR EXHCCSUD)                           GP05337
         LCLA  &I                                                87194
&I       SETA  &SYSNDX                                           87194
&NM      MACPARM R4,&TEXT    LOAD TEXT ADDRESS
         AIF   (K'&LEN LT 3).LNABS                              GP06268
         AIF   ('&LEN'(1,1) NE '(' OR '&LEN'(K'&LEN,1) NE ')').LNABS
         AIF   ('&LEN'(2,1) EQ '(' OR '&LEN'(K'&LEN-1,1) EQ ')').LNABS
         MACPARM &WK,&LEN    GENERATE LR FOR LENGTH             GP06268
         AGO   .LNCOM                                           GP06268
.LNABS   MACPARM &WK,=AL2(&LEN),OP=LH    LOAD TEXT LENGTH       GP03358
.LNCOM   MACPARM R2,BUFAD,OP=L GET THE TOP SCREEN LINE
         LH    R3,BUFSIZ     GET SIZE OF SCREEN BUFFER
ZZZ&I.PG LR    R15,R2        COPY BUFFER START                   87194
         BALS  R14,BLANKER1  CLEAR PART OR ALL OF BUFFER         87194
         LA    R0,80         LOGICAL INPUT LINE SIZE             87194
         LH    R15,LSIZE     PHYSICAL OUTPUT SIZE                87194
ZZZ&I.LN LR    R14,R4        COPY RESIDUAL LENGTH                87194
         MIN   R14,(R0)      LOAD SHORTER OF LINE OR LENGTH      87194
         BCTR  R14,0         LESS ONE FOR EXECUTE                87194
         EX    R14,*+4       MOVE ONE LINE                       87194
         MVC   0(0,R2),0(R4) MOVE LINE TO SCREEN                 87194
         AR    R2,R15        NEXT DISPLAY POSITION               87194
         AR    R4,R0         NEXT INPUT                          87194
         SR    &WK,R0        RESIDUAL INPUT LENGTH               87194
         SR    R3,R15         REMAINING SCREEN SPACE             87194
         BNP   ZZZ&I.XP      WRITE PAGE IF FULL                  87194
         LTR   &WK,&WK       ANY MORE INPUT ?                    87194
         BP    ZZZ&I.LN      YES; DO ANOTHER LINE                87194
ZZZ&I.XP XPOUT &PROMPT       WRITE THE PAGE                      87194
         B     ZZZ&I.XE      ENTER ONLY - CONTINUE
         MACPARM &EXIT,MODE=1,OP=B,OPR=BR   ELSE TAKE EXIT
ZZZ&I.XE LTR   &WK,&WK       ANY MORE INPUT TEXT ?
         BNP   ZZZ&I.EX      EXIT IF DONE                        87194
         L     R2,BUFAD      GET TOP OF SCREEN
         LH    R3,BUFSIZ     GET LENGTH OF SCREEN
         LA    R0,80         INPUT LINE SIZE                     87194
         LH    R15,LSIZE     OUTPUT SIZE                         87194
         AR    R2,R15        LEAVE TOP LINE AS TITLE             87194
         SR    R3,R15                                            87194
ZZZ&I.BL CLC   0(80,R4),BLANKS  NEXT LINE BLANK ?                87194
         BNE   ZZZ&I.PG      NO; DO NEXT PAGE                    87194
         AR    R4,R0                                             87194
         SR    &WK,R0        SKIP OVER BLANK LINE                87194
         BP    ZZZ&I.BL                                          87194
ZZZ&I.EX DS    0H            EXIT FROM LOOP
         MEND  ,
