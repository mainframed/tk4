         MACRO ,                                                 77077
&N       XXCTL &OP,&SUF=,&GROUP=X                                79216
         LCLC  &XOP,&NM,&L                                       77077
&NM      SETC  '&N'                                              77077
         LCLA  &K
         AIF   ('&GROUP' EQ 'X' OR '&GROUP' EQ 'Y').GROUPOK      79216
         MNOTE 8,'*** INVALID GROUP VALUE'                       79216
.GROUPOK AIF   ('&SUF' NE '').SUFFIX                             79216
         AIF   ('&OP' EQ '').MVI
         AIF   ('&OP' EQ 'ASIS').BAL   NAME OK ?                 77077
         AIF   ('&OP'(1,1) EQ '''').LIT
&NM      MVC   INVOKE+5(3),&OP .  COMPLETE NAME
&NM      SETC  ''                                                77077
.MVI     ANOP  ,
&NM      MVI   INVOKE+4,C'&GROUP' .    NAME STASHED ?            79216
         AGO   .BLA                                              77077
.SUFFIX  AIF   ('&SUF'(1,1) EQ '(').SUFREG                       77077
&K       SETA  K'&SUF                                            77077
         AIF   ('&SUF'(1,1) EQ '''').SLIT  SUFFIX A LITERAL      77077
&L       SETC  'L'''                                             77077
&NM      MVC   INVOKE+8-&L&SUF.(&L&SUF),&SUF  MOVE SUFFIX        77077
         AGO   .BLA                                              77077
.SLIT    ANOP  ,                                                 77077
         AIF   (&K LE 3).SUFMVI                                  77077
&NM      MVC   INVOKE+10-&K.(&K-2),=C&SUF  ADD MODULE SUFFIX     77077
         AGO   .BLA                                              77077
.SUFMVI  ANOP  ,                                                 77077
&L       SETC  '&SUF'(2,1)                                       77077
&NM      MVI   INVOKE+7,C'&L'  ADD MODULE SUFFIX                 77077
         AGO   .BLA                                              77077
.SUFREG  ANOP  ,                                                 77077
&NM      STC   &SUF(1),INVOKE+7  SET MODULE SUFFIX               77077
         AGO   .BLA                                              77077
.LIT     ANOP  ,
&K       SETA  K'&OP-2
         AIF   (&K EQ 3).LIT3                                   GP13048
         AIF   (&K EQ 4).LIT4                                   GP13048
         MNOTE 4,'XXCTL: LITERAL MUST BE 3 OR 4 CHARACTERS'     GP13048
.LIT3    ANOP  ,                                                GP13048
&XOP     SETC  '&GROUP'.'&OP'(2,&K)                              79216
&NM      MVC   INVOKE+4(4),=CL4'&XOP' .   MAKE NAME
         AGO   .BLA                                             GP13048
.LIT4    ANOP  ,                                                GP13048
&NM      MVC   INVOKE+4(4),=CL4&OP    .   MAKE NAME             GP13048
.BLA     ANOP  ,                                                 77077
&NM      SETC  ''                                                77077
.BAL     ANOP  ,                                                 77077
&NM      B     EXCXCTL .     DO BLDL AND XCTL                    77077
         MEND  ,
