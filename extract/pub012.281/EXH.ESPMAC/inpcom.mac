         MACRO
&NM      INPCOM &B0,&B1,&FN,&P0,&A1,&DEV=             UPDATED ON 99007
.**********************************************************************
.*                                                                   **
.*   COMMON INNER MACRO FOR @INPREAD INVOCATION                      **
.*                                                                   **
.*  ESA AND OS/390 CHANGE - FLAG BYTE NOW IN R0:1 FROM R1:0     GP98365
.**********************************************************************
         GBLC  &MACPLAB,&INPMODE
         LCLA  &I,&J,&K,&VD,&D(8)
         LCLB  &INDEV                                            82116
         LCLC  &DC,&A0                                           82116
&MACPLAB SETC  '&NM'                                             82116
&A0      SETC  '&P0'                                            GP99007
         AIF   ('&A0' NE '').LENOK                              GP99007
&A0      SETC  '0'                                              GP99007
.LENOK   AIF   ('&DEV' EQ '' OR '&DEV' EQ '0').NODV             GP99007
         AIF   ('&DEV' NE 'ALL').DVSOM
&VD      SETA  255
         AGO   .NODV
.DVSOM   AIF   (K'&DEV LT 2).DVSOL                               82116
         AIF   ('&DEV'(1,1) NE '=').DVSOL                        82116
&INDEV   SETB  1             SET INDIRECT DEVICE NUMBER          82116
         AGO   .NODV                                             82116
.DVSOL   ANOP  ,                                                 82116
&I       SETA  0
&J       SETA  N'&DEV
.DEVLOOP ANOP  ,
&I       SETA  &I+1
         AIF   (&I GT &J).DVEND
         AIF   ('&DEV(&I)' EQ '').DEVLOOP
         AIF   ('&DEV(&I)' EQ '0').DEVLOOP
         AIF   ('&DEV(&I)' LT '1' OR '&DEV(&I)' GT '8').DVERR
&D(&DEV(&I)) SETA  1
         AGO   .DEVLOOP
.DVERR   MNOTE 8,'*** INVALID DEVICE NUMBER &DEV(&I)'
         AGO   .DEVLOOP
.DVEND   ANOP  ,
&VD      SETA  128*&D(8)+64*&D(7)+32*&D(6)+16*&D(5)+8*&D(4)
&VD      SETA  &VD+4*&D(3)+2*&D(2)+&D(1)
.NODV    AIF   ('&B0' NE '0' OR '&A0' NE '0').LONG              GP98365
         AIF   (&VD GT 15).LONG
&K       SETA  &VD*256+&FN
         MACPARM R0,&K       LOAD DEVICE/FUNCTION INDEX          82116
         AIF   ('&A0' EQ '0').POST0                             GP99007
         ICM   R0,4,=AL1(&A0)                                   GP98365
         AGO   .POST0                                            82116
.LONG    ANOP  ,
&MACPLAB L     R0,=AL1(&B0,&A0,&VD,&FN)                         GP98365
&MACPLAB SETC  ''            CANCEL LABEL                        82116
.POST0   AIF   (NOT &INDEV).LOAD1                                82116
&VD      SETA  K'&DEV-1                                          82116
&DC      SETC  '&DEV'(2,&VD)                                     82116
&MACPLAB ICM   R0,2,&DC                                          82116
&MACPLAB SETC  ''                                                82116
.LOAD1   AIF   ('&FN' EQ '0' OR '&FN' EQ '1').BAL
         MACPARM R1,&A1      LOAD PARAMETER REGISTER
.BAL     AIF   ('&INPMODE' EQ 'V').VCON
         L     R15,@INPREAD
         AGO   .BALR
.VCON    L     R15,=V(@INPREAD)
.BALR    BALSR R14,R15
.*ALR    BASSM R14,R15
         MEND  ,
