         MACRO ,
&NM      PARKEYFG &TEXT,&FLAG,&OFF,&ON,&LEN=,&MIN=3
.*--------------------------------------------------------------------*
.*  THIS MACRO BUILDS ON/OFF FLAG SETTINGS FOR KEYWORD PROCESSING.    *
.*  USE IN CONJUNCTION WITH @PARSER/PARSE, AND PARKEYBX SERVICES,     *
.*  AND THE SUBVERB SUBROUTINE (ALSO SEE PARFGSET MACRO)              *
.*                                                                    *
.*  TEXT : quoted or unquoted string; only first eight bytes used.    *
.*  FLAG : name of flag byte(s) suitable for sl2 constant             *
.*  OFF  : expression listing bits to be turned off; default=0        *
.*  ON   : expression listing bits to be turned on;  default=0        *
.*  LEN  : length of flag byte(s); default=1                          *
.*  MIN  : minimum length of text considered for match                *
.*                                                                    *
.*--------------------------------------------------------------------*
         GBLB  &ZZZOPTF
         GBLC  &ZZZOPTL
         GBLA  &MACPLEN                                         GP15201
         GBLB  &MACPNUL,&MACPERR,&MACQUOT                       GP15201
         GBLC  &MACQSTR                                         GP15201
         LCLC  &LN                                              GP15201
         LCLA  &I
&LN      SETC  '&MIN'                                           GP15201
         AIF   ('&LEN' EQ '').NOLOVR
&ZZZOPTL SETC  '&LEN'
.NOLOVR  AIF   ('&ZZZOPTL' NE '').HAVELEN
&ZZZOPTL SETC  '1'
.HAVELEN AIF   (&ZZZOPTF).LATER
OFOFTXT  EQU   0,8,C'C'      VERB
OFOFMIN  EQU   8,1,C'F'      MINIMUM LENGTH (-1)
OFOFLEN  EQU   9,1,C'F'      OFF/ON DATA LENGTH (-1)
OFOFSAD  EQU  10,2,C'S'      FLAG ADDRESS S-CON
OFOFOFF  EQU  12,1,C'X'      FLAGS TO BE RESET
OFOFONN  EQU  13,1,C'X'      FLAGS TO BE SET
&ZZZOPTF SETB  1             DEFINITIONS DONE
.LATER   MACQOTER &TEXT,OPT=STRIP                               GP15201
         AIF   (NOT &MACPERR).STROK                             GP15201
         MNOTE 8,'PARKEYFG: ERROR PROCESSING VERB &TEXT'        GP15201
&MACQSTR SETC  '?'                                              GP15201
&MACPLEN SETA  3             PREVENT A MATCH                    GP15201
.STROK   AIF   (&MACPLEN GE 3).LENOK                            GP15201
&LN      SETC  '&MACPLEN'                                       GP15201
.LENOK   ANOP  ,                                                GP15201
&NM      DC    CL8'&MACQSTR',AL1(&LN-1,&ZZZOPTL-1),SL2(&FLAG),AL(&ZZZOP*
               TL)(255-(&OFF+0),&ON+0)                          GP15201
         MEND  ,
