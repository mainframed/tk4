         MACRO ,
&NM      LAT   &R,&ADDR,&BZ,&BNZ,&LA=FW                         GP98339
         GBLB  &MVSXA                                            91216
         GBLC  &MACPLAB
.**********************************************************************
.*                                                                   **
.*   LOAD AN ADDRESS INTO A REGISTER AND TEST FOR ZERO; OPTIONALLY   **
.*     BRANCH ON ZERO BZ=label  OR NONZERO BNZ=label                 **
.*                                                                   **
.**********************************************************************
&MACPLAB SETC  '&NM'
         AIF   ('&LA' EQ '').ICM370
         AIF   ('&LA' EQ '34' OR '&LA' EQ '34Z').ICM34           91216
         AIF   ('&LA' EQ 'RX').ICMRX
         AIF   ('&LA' EQ '0').ICMSR
         AIF   ('&LA' EQ 'FW').ICMFW
         MNOTE 8,'&&LA=&LA INVALID - MUST BE RX, 0, OR FW'
         MNOTE 8,'&&LA=RX ASSUMED'
.ICMRX   MACPARM &R,&ADDR,OP=LA  OFFSET(X,BASE)
         MACPARM &R(1),7,1(&R(1)),OP=ICM,MODE=THREE
         AGO   .BRT
.ICMSR   MACPARM &R,0
         AGO   .ICM370                                           92273
.ICM34   AIF   (&MVSXA).ICMFW                                    91216
.ICM370  MACPARM &R(1),7,1+&ADDR,OP=ICM,MODE=THREE  LOAD ADDRESS
         AGO   .BRT
.ICMFW   MACPARM &R(1),15,&ADDR,OP=ICM,MODE=THREE  LOAD FULLWORD
         AIF   ('&LA' NE '34Z').BRT                              91216
         MACPARM &R(1),0(,&R(1))                                 91216
.*
.BRT     MACPARM &BZ,OP=BZ,OPR=BZR,MODE=ONE,NULL=SKIP           GP06266
         MACPARM &BNZ,OP=BNZ,OPR=BNZR,MODE=ONE,NULL=SKIP        GP06266
.MEX     MEND  ,
