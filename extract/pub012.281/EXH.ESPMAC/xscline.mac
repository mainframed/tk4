         MACRO ,
&NM      XSCLINE &FUN=PAGETAB                                   GP13162
.*   THE XSCLINE MACRO PROVIDES A FULL PAGE OUT, AND READ INPUT GP13162
.*   SERVICE FOR SCREENS DEFINED USING THE FDLINE SERVICE.      GP13162
.*   ON ENTRY R0 DEFINES A FUNCTION CODE, THAT *16 IS AN INDEX  GP13162
.*   INTO A TABLE OF ADDRESSES DEFINED AS:                      GP13162
.*    A(screen list,help list,prompt,quicktab)                  GP13162
.*   screen list IS THE ADDRESS VECTOR OF SCLINES THAT COMPOSE  GP13162
.*     THE SCREEN.                                              GP13162
.*   help list IS AN ADDRESS VECTOR WITH HELP INFORMATION OR 0  GP13162
.*   prompt IS THE ADDRESS OF BCON TEXT FOR THE PROMPT MESSAGE  GP13162
.*   quicktab IS A LIST OF BTAB ENTRIES FOR PF/PA KEYS          GP13162
.*                                                              GP13162
         LCLC  &NAME         SUBROUTINE NAME                    GP13162
&NAME    SETC  '&NM'                                            GP13162
         AIF   ('&NAME' NE '').DOPROM                           GP13162
&NAME    SETC  'PAGENEW'                                        GP13162
.DOPROM  ANOP  ,                                                GP13162
&NAME    STM   R5,R7,DB2     SAVE OVER SUBROUTINES              GP13162
         LR    R6,R0         PRESERVE ENTRY CODE                GP13162
         SLL   R6,4          * 16                               GP13162
         LA    R6,&FUN.(R6)  POINT TO FUNCTION ADDRESSES    GP13162
         LA    R5,REPLY      RESET                              GP13162
         ST    R5,REPPTR       INPUT POINTER                    GP13162
         ICM   R2,15,8(R6)   GET PROMPT ADDRESS                 GP13162
         BZ    PAGENPRO        NO PROMPT                        GP13162
         MVC   ZPROM,BLANKS  CLEAR PROMPT TEXT                  GP13162
         SR    R1,R1                                            GP13162
         IC    R1,0(,R2)     GET PROMPT LENGTH                  GP13162
         BCTR  R1,0            EX LENGTH                        GP13162
         EX    R1,EXMVCPRO   MOVE PROMPT TO SCREEN              GP13162
PAGENPRO LM    R2,R3,0(R6)   LOAD MAIN AND HELP SCREEN ADDR     GP13162
         SCINIT FDW,(R2)     PREPARE FOR SCREEN                 GP13162
         TM    PROFLAGS,PFCURSE   SET CURSOR ?                  GP13162
         BZ    PAGEQUE                                          GP13162
         MVC   FDWCUR,SAVECUR     SET POSITION                  GP13162
         OI    FDWPROFG,FDWPFCUR  USE CURSOR                    GP13162
PAGEQUE  MVC   ZCMD,BLANKS   CLEAR INPUT                        GP13162
         SCLINE FDW,(R2),(R3)  DISPLAY BUFFER AND HELP SCREEN   GP13162
         BNM   REPLANAL      CHECK INPUT                        GP13162
         CLI   FDWIAID,1     WHAT CONDITION ?                   GP13162
         BE    EXCWERR        WRITE ERROR                       GP13162
         BH    EXCRERR        READ ERROR                        GP13162
         TM    EXCPRIV,EXCPLOOP  LOOP MODE?                     GP13162
         BZ    GETOUT        NO RESPONSE FROM USER (OR ERROR)   GP13162
REPLANAL MVC   REPLY,BLANKS                                     GP13162
         ICM   R4,15,12(R6)  LOAD QUICKTAB                      GP13162
         BZ    PAGENQIK        SKIP IF NONE                     GP13162
         LA    R5,FDWICOD    POINT TO CONVERTED AID             GP13162
         L     R7,DB2+8      RESTORE GETMAIN BASE               GP13162
         XLOOK T=(R4),R=FDWICOD    LOOK FOR NON-DATA FUNCTIONS  GP13162
         MVC   REPLY+6(L'ZCMD),ZCMD  COPY MAJOR COMMAND         GP13162
PAGENQIK LM    R5,R7,DB2     RESTORE                            GP13162
         BR    R6                                               GP13162
EXMVCPRO MVC   ZPROM(*-*),1(R2)   OVE PROMPT TEXT               GP13162
         MEND  ,
