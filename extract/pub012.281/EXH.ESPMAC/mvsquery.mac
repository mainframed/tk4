         MACRO ,             LOCAL CSVQUERY REPLACEMENT (MVS 3.8)
&NM      MVSQUERY &INEPNM=,&INADDR=,&SEARCHM=,&SEARCH=JPALPA,&OLEN=,&OX*
               TLST=,&OEPA=,&OEPNM=,&OMJNM=,&OLOADPT=,&OSP=,&OATTR1=,&O*
               ATTR2=,&OATTR3=,&OVALID=,&OPDATA=,&OPID=,&ODIAG=,&RETCOD*
               E=,&MF=S,&PFX=CVQ,&DSECT=DSECT,&OEPTKN=,&TCB=0
         GBLC  &MACPLAB
         LCLC  &P
         LCLC  &A01,&A02,&A03,&A04,&A05,&A06,&A07,&A08,&A09
         LCLC  &A10,&A11,&A12,&A13,&A14,&A15,&A16,&A17,&A18
         LCLA  &OPTFG,&I,&F
         LCLB  &OPJPA,&OPLPA,&OPMIN,&OPNAM
&P       SETC  '&PFX'
&MACPLAB SETC  '&NM'
&I       SETA  &SYSNDX
         AIF   ('&MF' EQ 'D').MAPDSCT
.*
&OPJPA   SETB  ('&SEARCH' EQ 'JPA' OR '&SEARCH' EQ 'JPALPA')
&OPLPA   SETB  ('&SEARCH' EQ 'LPA' OR '&SEARCH' EQ 'JPALPA')
&OPNAM   SETB  ('&INEPNM' NE '')
&OPMIN   SETB  ('&SEARCHM' EQ 'YES')
         AIF   (&OPNAM AND '&INADDR' EQ '').INOK
         AIF   (NOT &OPNAM AND '&INADDR' NE '').INOK
         AIF   ('&MF(1)' EQ 'L' OR '&MF(1)' EQ 'E').INOK  DEFER
 MNOTE 8,'EITHER INEPNM OR INADDR MUST BE SPECIFIED, BUT NOT BOTH'
         MEXIT ,
.*
.INOK    AIF   ('&MF' EQ '').BADMF
&F       SETA  128*&OPJPA+64*&OPLPA+32*&OPNAM+16*&OPMIN
         AIF   (N'&MF GT 1 AND '&MF(1)' EQ 'L').LFORM
         AIF   ('&MF' EQ 'S').STDFORM
         AIF   (N'&MF LT 2).BADMF
         AIF   ('&MF(1)' EQ 'E').EFORM
         AIF   ('&MF(1)' EQ 'M').EFORM
         AIF   ('&MF(1)' EQ 'L').LFORM
.BADMF   MNOTE 8,'VALUE OF MF= IS UNSUPPORTED'
         MEXIT ,
.STDFORM CNOP  0,4           FORCE ALIGNMENT
         MACPARM R1,ZZQ&I.S,OP=BAL  LOAD THE IN-LINE LIST ADDRESS
&MACPLAB SETC  'ZZQ'.'&I'.'S'  MAKE TARGET LABEL
         DC    XL(18*4)'0'     DEFINE PARAMETER LIST SPACE
         AGO   .LOADLST
.EFORM   MACPARM R1,&MF(2)   LOAD PARM LIST ADDRESS
         AIF   (NOT &OPNAM AND '&INADDR' EQ '').LOADLST  MF=L HAS?
         MACPARM 0(R1),X'DF',OP=NI  RESET OLD INEPNM FLAG
         MACPARM 0(R1),&F,OP=OI,OPR=OI  SET  OPTION FLAGS
         AGO   .LOADFLG
.LOADLST MACPARM 0(R1),&F,OP=MVI,OPR=MVI  LOAD OPTION FLAGS
.*       MVI   3(R1),AL1(&PLVER+0)  PLIST VERSION - IGNORE
.LOADFLG MACPARM R0,&INEPNM,NULL=SKIP
         MACPARM R0,&INADDR,NULL=SKIP
         ST    R0,04(,R1)     INPUT POINTER
         AIF   ('&RETCODE' EQ '').SKIP08
         MACPARM R0,&RETCODE
         ST    R0,08(,R1)     RETURN CODE
.SKIP08  AIF   ('&OVALID' EQ '').SKIP12
         MACPARM R0,&OVALID
         ST    R0,12(,R1)     VALIDITY FLAGS
.SKIP12  AIF   ('&OLEN' EQ '').SKIP16
         MACPARM R0,&OLEN
         ST    R0,16(,R1)     MODULE LENGTH
.SKIP16  AIF   ('&OEPA' EQ '').SKIP20
         MACPARM R0,&OEPA
         ST    R0,20(,R1)     ENTRY ADDRESS W/AM FLAG IN HIGH BIT
.SKIP20  AIF   ('&OEPNM' EQ '').SKIP24
         MACPARM R0,&OEPNM
         ST    R0,24(,R1)     MODULE NAME/ALIAS
.SKIP24  AIF   ('&OMJNM' EQ '').SKIP28
         MACPARM R0,&OMJNM
         ST    R0,28(,R1)     MAJOR NAME
.SKIP28  AIF   ('&OSP' EQ '').SKIP32
         MACPARM R0,&OSP
         ST    R0,32(,R1)     SUBPOOL
.SKIP32  AIF   ('&OATTR1' EQ '').SKIP36
         MACPARM R0,&OATTR1
         ST    R0,36(,R1)     ATTRIBUTE 1
.SKIP36  AIF   ('&OATTR2' EQ '').SKIP40
         MACPARM R0,&OATTR2
         ST    R0,40(,R1)     ATTRIBUTE 2
.SKIP40  AIF   ('&OATTR3' EQ '').SKIP44
         MACPARM R0,&OATTR3
         ST    R0,44(,R1)     ATTIBUTE 3
.SKIP44  AIF   ('&OLOADPT' EQ '').SKIP48
         MACPARM R0,&OLOADPT
         ST    R0,48(,R1)     LOAD POINT (FIRST EXTENT)
.SKIP48  AIF   ('&OPDATA' EQ '').SKIP52
         MACPARM R0,&OPDATA
         ST    R0,52(,R1)     P DATA
.SKIP52  AIF   ('&OPID' EQ '').SKIP56
         MACPARM R0,&OPID
         ST    R0,56(,R1)     P ID
.SKIP56  AIF   ('&OEPTKN' EQ '').SKIP60
         MACPARM R0,&OEPTKN
         ST    R0,60(,R1)     TOKEN - NOT SUPPORTED
.SKIP60  AIF   ('&OXTLST' EQ '').SKIP64
         MACPARM R0,&OXTLST
         ST    R0,64(,R1)     EXTENT LIST
.SKIP64  AIF   ('&ODIAG' EQ '').SKIP68
         MACPARM R0,&ODIAG
         ST    R0,68(,R1)     DIAG - NOT IMPLEMENTED
.SKIP68  AIF   ('&MF(1)' EQ 'M').MEND  MODIFY ONLY
         MACPARM R0,&TCB,NULL=0                                 GP08117
         MACPARM R15,=V(SUBQUERY),OP=L  ADDRESS OF SUBROUTINE
         MACPARM R14,(R15),OP=BALR,OPR=BALR
         MEXIT ,
.LFORM   ANOP  ,
&A01     SETC  '&INEPNM&INADDR+0'
&A02     SETC  '&RETCODE+0'
&A03     SETC  '&OVALID+0'
&A04     SETC  '&OLEN+0'
&A05     SETC  '&OEPA+0'
&A06     SETC  '&OEPNM+0'
&A07     SETC  '&OMJNM+0'
&A08     SETC  '&OSP+0'
&A09     SETC  '&OATTR1+0'
&A10     SETC  '&OATTR2+0'
&A11     SETC  '&OATTR3+0'
&A12     SETC  '&OLOADPT+0'
&A13     SETC  '&OPDATA+0'
&A14     SETC  '&OPID+0'
&A15     SETC  '&OEPTKN+0'
&A16     SETC  '&OXTLST+0'
&A17     SETC  '&ODIAG+0'
&A18     SETC  '0'
&MF(2)   MACMAPHD DSECT=NO,PFX=&PFX
         AIF   ('&NM' EQ '').MAPCOM
&NM      DS    0A
         AGO   .MAPCOM
.MAPDSCT ANOP  ,
&A01     SETC  '0'
&A02     SETC  '0'
&A03     SETC  '0'
&A04     SETC  '0'
&A05     SETC  '0'
&A06     SETC  '0'
&A07     SETC  '0'
&A08     SETC  '0'
&A09     SETC  '0'
&A10     SETC  '0'
&A11     SETC  '0'
&A12     SETC  '0'
&A13     SETC  '0'
&A14     SETC  '0'
&A15     SETC  '0'
&A16     SETC  '0'
&A17     SETC  '0'
&A18     SETC  '0'
&NM      MACMAPHD DSECT=&DSECT,PFX=&PFX,DFLT=DSECT
.MAPCOM  ANOP  ,
&P.FLAGS DC    0A(0),AL1(&F) REQUEST FLAGS
&P.OPJPA EQU   X'80'           SEARCH JOB'S JPA AND LOAD LIST
&P.OPLPA EQU   X'40'           SEARCH SYSTEM'S MLPA AND PLPA
&P.OPNAM EQU   X'20'           SEARCH FOR NAME, NOT ADDRESS
&P.OPMIN EQU   X'10'           SEARCH INCLUDES MINOR CDES
         DC    X'00'
         DC    X'00'
&P.#PLST DC    B'0'          LIST VERSION
&P.@INRQ DC    A(&A01)  4    ADDRESS REQUESTED, OR LOC OF NAME
&P.@OCOD DC    A(&A02)  8    ADDRESS OF OUTPUT RETURN CODE
&P.@OVAL DC    A(&A03) 12    ADDRESS OF OUTPUT VALIDITY FLAGS
&P.@OLEN DC    A(&A04) 16    ADDRESS OF OUTPUT LENGTH WORD
&P.@OEPA DC    A(&A05) 20    ADDRESS OF OUTPUT ENTRY ADDRESS
&P.@ONAM DC    A(&A06) 24    ADDRESS OF OUTPUT ENTRY NAME
&P.@OMAJ DC    A(&A07) 28    ADDRESS OF OUTPUT MAJOR NAME
&P.@OSP  DC    A(&A08) 32    ADDRESS OF OUTPUT SUBPOOL ADDRESS
&P.@OAT1 DC    A(&A09) 36    ADDRESS OF OUTPUT ATTRIBUTE ADDRESS
&P.@OAT2 DC    A(&A10) 40    ADDRESS OF OUTPUT ATTRIBUTE ADDRESS
&P.@OAT3 DC    A(&A11) 44    ADDRESS OF OUTPUT ATTRIBUTE ADDRESS
&P.@OLOD DC    A(&A12) 48    ADDRESS OF OUTPUT LOAD ADDRESS
&P.@OPDA DC    A(&A13) 52    ADDRESS OF OUTPUT ?     - NOT IMPLEMENTED
&P.@OPID DC    A(&A14) 56    ADDRESS OF OUTPUT ?     - NOT IMPLEMENTED
&P.@OTKN DC    A(&A15) 60    ADDRESS OF OUTPUT TOKEN - NOT IMPLEMENTED
&P.@OXTL DC    A(&A16) 64    ADDRESS OF OUTPUT EXTENT LIST AREA
&P.@ODIA DC    A(&A17) 68    ADDRESS OF OUTPUT DIAG  - NOT IMPLEMENTED
&P.LEN   EQU   *-&P.FLAGS      LIST LENGTH
.MEND    MEND  ,
