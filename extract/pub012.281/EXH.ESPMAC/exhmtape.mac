         MACRO ,
         EXHMTAPE &SECT=D
         GBLB  &MVS,&MVSSP,&MVSXA,&MVSESA,&OS390,&Z900          GP04234
         GBLC  &SYSTEM
TAPEWORK DS    0D
         USING *,R7
         SPACE 1
TAPEEXCP WAIT  ECB=TAPEECB   AWAIT COMPLETION OF PURGE
         TM    TAPEFLAG,FGPURGE  RECORD PURGED ?                 81285
         BZ    *+8           YES; COUNTS ALREADY FIXED           81285
         BALS  R14,TAPEXFIX  FIX FILE/RECORD COUNTS              81285
         MVI   TAPEECB,0     CLEAR ECB
         NEED  IOB
         NI    TAPEIOB+IOBFLAG1-IOBSTDRD,255-IOBRSTRT  RESET
         NI    TAPEFLAG,255-FGPURGE-FGSLI  RESET FLAGS
         EXCP  TAPEIOB       REQUEST I/O
         WAIT  ECBLIST=TAPEECBS
         TM    TAPEECB,X'40'  DID I/O COMPLETE AT ALL ?
         BZ    *+12          NO                                  81285
         BALS  R14,TAPEXFIX  YES; FIX FILE/RECORD COUNTS         81285
         B     4(,R9)        RETURN NORMALLY                     81285
         OI    TAPEFLAG,FGPURGE  SHOW I/O PURGED
         IOHALT TAPEUCB      PURGE THE CURRENT I/O
         BR    R9            RETURN WITH ATTN
         SPACE 1                                                 81285
TAPEXFIX TM    TAPECSW+4,X'2E'  DISASTER ?                       81285
         BNZR  R14           YES; NO CHANGE                      81285
         MVC   TAPEECB+3(1),COUNTFG  SAVE FLAG                   81285
         TM    TAPECSW+3,1   UNIT EXCEPTION ?                    81285
         BNZ   TAPEEXCL      YES; ALTERNATE COUNT                81285
         CLI   TAPEECB,X'7F'   NORMAL COMPLETION ?               81285
         BE    TAPEEXCN      YES; DO NORMAL COUNT                81285
         CLI   TAPESENS,0    SENSE 1 =0                          81285
         BNE   EXCPTANS      NO                                  81285
         CLI   TAPESENS+1,X'60'  MEMOREX LONG GAP ?              81285
         BER   R14           YES; NO CHANGE                      81285
         CLI   TAPESENS+1,0  ALL ZERO SENSE ?                    81285
         BNE   EXCPTANS      NO                                  81285
         TM    TAPECSW+4,255-X'40'  INCORRECT LENGTH ONLY ?      81285
         BZ    TAPEEXCN      YES; DO NORMALLY                    81285
EXCPTANS TM    TAPESENS,X'C2'  CMD REJECT OR NOT READY ?         81285
         BNZR  R14           YES; NO CHANGE                      81285
         TM    TAPESENS+1,X'03'  OTHER ?                         81285
         BNZR  R14           YES; NO CHANGE                      81285
         TM    TAPESENS+1,X'08'  LOAD POINT ?                    81285
         BNZ   TAPEEXCC      YES; RESET                          81285
TAPEEXCL UNPK  TAPEECB+3(1),COUNTFG  USE LOW BITS OF FLAG        81285
TAPEEXCN TM    TAPEECB+3,X'F0'   ANY CHANGES AT ALL ?            81285
         BZR   R14           NO                                  81285
         TM    TAPEECB+3,CLRFILE  CLEAR FILE ?                   81285
         BO    TAPEEXCC      YES                                 81285
         BZ    TAPEEXTR      TEST RECORD NUMBER                  81285
         TM    TAPEECB+3,UPFILE  INCREASE FILE ?                 81285
         BNZ   TAPEEXUF      YES                                 81285
         L     R0,NUMFILE    GET CURRENT FILE NUMBER             81285
         SH    R0,H1         DECREASE BY ONE                     81285
         BNP   TAPEEXCC      RESET TO START OF TAPE              81285
         ST    R0,NUMFILE    STASH BACK                          81285
         MVC   NUMBLOCK,TAPXMAX  SET MAXIMUM BLOCK               81285
         BR    R14                                               81285
TAPEEXUF LA    R0,1                                              81285
         A     R0,NUMFILE                                        81285
         ST    R0,NUMFILE                                        81285
         MVC   NUMBLOCK,ZEROES                                   81285
         BR    R14                                               81285
TAPEEXCC XC    NUMFILE(8),NUMFILE  CLEAR FILE AND RECORD COUNT   81285
         MVI   NUMFILE+3,1   SET FILE 1                          81285
         BR    R14                                               81285
TAPEEXTR TM    TAPEECB+3,CLRRCD  CHANGE RECORD COUNT ?           81285
         BZR   R14           NO                                  81285
         BO    TAPEEXRC      RESET TO ZERO                       81285
         TM    TAPEECB+3,UPRCD  INCREASE RECORD ?                81285
         BNZ   TAPEEXUR      YES                                 81285
         L     R0,NUMBLOCK                                       81285
         SH    R0,H1         DECREASE BY ONE                     81285
         BNM   TAPEEXCR      STASH IT BACK                       81285
TAPEEXRC XR    R0,R0                                             81285
         B     TAPEEXCR      SET TO ZERO                         81285
TAPEEXUR LA    R0,1                                              81285
         A     R0,NUMBLOCK                                       81285
TAPEEXCR ST    R0,NUMBLOCK                                       81285
         BR    R14           RETURN                              81285
         SPACE 1
TAPEONEX STM   R0,R15,TAPEREC  SAVE
         TM    OPTS,DEBUG    DEBUG MODE ?
         BNZ   TAPEONXC      YES; DO CLOSE
         TM    TAPEFLAG,FGUCB  UCB ALLOCATED ?
         BZ    TAPEONXX      NO
         L     R3,TAPEUCB    GET UCB ADDRESS
         NUSE  UCB,R3
         TM    UCBFL1,UCBNOTRD  UNLOADED ?
         BNZ   TAPEONXR      YES
         LA    R1,CCWRUN     DO REWIND/UNLOAD
         MVC   MSGTEXT1,MSGDCUNL   UNLOAD                       GP03336
         MVC   MSGTEXT2,BLANKS                                  GP03336
         MVC   MSGTEXT2+1(6),UCBVOLI                            GP03336
         TM    UCBSTAT,UCBRESV  'MOUNT'ED TAPE ?                 81285
         BZ    TAPEONUN                                         GP03336
         LA    R1,CCWREW     YES; REWIND ONLY
         MVC   MSGTEXT1,MSGDCREW   UNLOAD                       GP03336
TAPEONUN ST    R1,TAPECCW@   SET ADDRESS INTO IOB               GP03336
         AIF   (NOT &MVSXA AND NOT &MVSESA AND NOT &OS390).SKPDSP
         MSGDISP GEN,UCB=(R3),TXT=MSGTEXT1,TXT2=MSGTEXT2,TEST=YES,     *
               MF=(E,MSGLIST)                                   GP03336
.SKPDSP  MVI   TAPEECBS,X'80'  SHORTEN LIST
         BALS  R9,TAPEEXCP   RUN
         NOP   0             NO INTERRUPT PROCESSING
         AIF   (NOT &MVSXA AND NOT &MVSESA AND NOT &OS390).SKPDSP2
         MSGDISP RESET,UCB=(R3),TEST=YES,MF=(E,MSGLIST)         GP03336
.SKPDSP2 ANOP  ,
TAPEONXR TM    UCBSTAT,UCBRESV  'MOUNT'ED TAPE ?                 81285
         BNZ   TAPEONXA      YES; JUST UN-ALLOCATE
         NI    UCBSTAT,255-UCBRESV-UCBDADI  RESET MOUNT FLAGS    81285
         NI    UCBSTAB,255-UCBBSTR  RESET ANSI LABEL FLAG        81285
         OI    UCBFL1,UCBNOTRD  SET UCB NOT READY
         XC    UCBVOLI(6),UCBVOLI  CLEAR VOL-SER
TAPEONXA XC    UCBFSCT(4),UCBFSCT  CLEAR FILE NUMBERS            81285
TAPEONXY NI    UCBSTAT,255-UCBALOC  RESET ALLOCATION BIT
TAPEONXX XTHINK  RESET,SAVE=TAPEHOLD  RESET PRIOR WAIT MODE     GP04234
         LM    R0,R15,TAPEREC  RESTORE REGISTERS
TAPENDAG BR    R14           RETURN TO CALLER
TAPEONXC CLOSE (TAPEDCB,REWIND)
         B     TAPEONXX      AND EXIT
         SPACE 2
TAPEHOLD DC    XL4'0'        DELAY MODE SAVE WORD
TAPEFLAG DC    X'00'         PROCESSING FLAG
FGUCB    EQU   X'80'         UCB ALLOCATED
FGMOUNT  EQU   X'40'         TAPE IS HARD-MOUNTED
FGUNLD   EQU   X'20'         PREVIOUS COMMAND UNLOADED TAPE
FGDUMP   EQU   X'10'         DOING DUMP (RATHER THAN LIST)
FGANSI   EQU   X'04'         CONVERT FROM ANSI TO EBCDIC  (04 REQUIRED)
FGSLI    EQU   X'02'         SHORT LENGTH RECORD READ
FGPURGE  EQU   X'01'         I/O REQUEST PURGED
COUNTFG  DC    X'00'         RECORD/FILE COUNT CHANGE FLAG       81285
UPFILE   EQU   X'80'         INCREASE FILE NUMBER; RECORD=0      81285
DOWNFILE EQU   X'40'         DECREASE FILE NUMBER; RECORD=999999 81285
CLRFILE  EQU   UPFILE+DOWNFILE  RESET TO START OF TAPE           81285
UPRCD    EQU   X'20'         INCREASE RECORD BY ONE              81285
DOWNRCD  EQU   X'10'         DECREASE RECORD BY ONE              81285
CLRRCD   EQU   UPRCD+DOWNRCD   RESET RECORD TO ZERO              81285
TAPEHEAD DC    0CL39' '      PAGE HEADER                         81285
TAPHVOL  DC    CL6' ',C' '   VOLSER FOR WTL
TAPHUCB  DC    CL3' ',C' '   UCB NAME
TAPHDEN  DC    CL5' ',C' '   DENSITY                             81285
         DC    C'F='         FILE ID                             81285
TAPHFILE DC    CL5' ',C' '   FILE #
         DC    C'R='         RECORD ID                           81285
TAPHREC  DC    CL7' ',C' '   RECORD #
TAPHANSI DC    C' ',C' '     E(BCDIC) OR A(SCII)                 81285
TAPHCMD  DC    CL3' '        COMMAND/FUNCTION                    81285
TAPEHDLN EQU   *-TAPEHEAD
NUMFILE  DC    F'1'          CURRENT FILE NUMBER                 81285
NUMBLOCK DC    F'0'          CURRENT RECORD NUMBER               81285
TAPXMAX  DC    F'999999'     MAX BLOCK AFTER BSF                 81285
TAPEDCB  DC    4F'0'
TAPETRCH DC    X'00'                                             81285
TAPEDEVT DC    X'00'
TAPEDENS DC    X'00'
         DC    X'00'
         DC    5F'0'
         DC    H'0'
         DC    BL2'1101000000001000'
         DC    A(TAPEDEB)
         DC    X'10000000'
         DC    5F'0'
         SPACE 1
TAPIOVEC DC    A(TAPENDAG)
         DC    A(TAPENDAG)
         DC    A(TAPENDAG)
         DC    A(TAPENDAG)
         DC    A(TAPENDAG)
         DC    3F'0'
         DC    X'06000000'
TAPEDEB  DC    F'0'
         DC    X'17000000'
         DC    X'C0000000'
         DC    X'30000000'
         DC    2F'0'
         DC    X'0F'
         DC    AL3(TAPEDCB)
         DC    X'02'
         DC    AL3(TAPIOVEC)
TAPEUCB  DC    0F'0'         UCB ADDRESS
TAPDEBMD DC    X'00'
TAPDBUCB DC    X'000000'
         DC    F'0'
         SPACE 1
TAPEECB  DC    F'0'
TAPEECBS DC    A(TAPEECB,0),X'80',AL3(0)
         SPACE 1
TAPEIOB  DC    0A(0),X'4200'
TAPESENS DC    H'0'
         DC    X'7F'
         DC    AL3(TAPEECB)
TAPESTAT DC    X'00'
TAPECSW  DC    XL7'0'
TAPECCW@ DC    A(TAPECCW1)   DEFAULT CCW
         DC    A(TAPEDCB)
         DC    F'0'
         DC    H'1'
         DC    H'0'
         SPACE 1
TAPECCW1 DC    D'0'          FIRST CCW
TAPECCW2 DC    D'0'          SECOND CCW
CCWRUN   CCW   X'0F',CCWRUN,X'60',1  REWIND/UNLOAD
CCWNOP   CCW   X'03',1,X'20',1 NOP
CCWREW   CCW   X'07',CCWREW,X'60',1  REWIND
         CCW   X'03',1,X'20',1 NOP
CCWERG   CCW   X'17',0,X'60',1  ERASE GAP
CCWDSE   CCW   X'97',0,X'20',1  SECURITY ERASE
CCWREAD  CCW   X'02',TAPEREC,X'00',TAPEWEND-TAPEREC+1  READ     GP03336
         SPACE 1                                                GP03336
         AIF   (NOT &MVSXA AND NOT &MVSESA AND NOT &OS390).NOMDISP
MSGLIST  MSGDISP GEN,FLASH=ALT,                                        *
               VOL=REMOVE,WAIT=NO,MF=L                          GP03336
.NOMDISP ANOP  ,                                                GP04234
MSGTEXT1 DC    CL8' '        PRIMARY MESSAGE ON DISPLAY         GP03336
MSGTEXT2 DC    CL8' '        SECONDARY MESSAGE                  GP03336
MSGDCREW DC    CL8'*REWIND*'                                    GP03336
MSGDCUNL DC    CL8'*UNLOAD*'                                    GP03336
         SPACE 1                                                 90315
MSGBADIN DC    C'*** INVALID COMMAND ***'                        90315
MSGATTN  DC    C'*** REQUEST TERMINATED - INTERRUPT ***'         90315
MSGBDCOU DC    C'*** INVALID REPEAT COUNT FIELD ***'             90315
MSGERROR DC    C'*ERROR*  ECB '                                  90315
MSGERECB DC    C'XX',C' STATUS '                                 90315
MSGERSTA DC    C'XXXX',C' SENSE '                                90315
MSGERSEN DC    C'XXXX',C' CCW '                                  90315
MSGERCCW DC    C'XXXXXXXX-XXXXXXXX',C' CSW '                     90315
MSGERCSW DC    C'XXXXXX-XXXXXXXX'                                90315
MSGERLEN EQU   *-MSGERROR                                        90315
MSGEOF   DC    C'--- END OF FILE ---'                            90315
MSGBADVS DC    C'*** UNACCEPTABLE VOLUME SERIAL ***'             90315
MSGNODEN DC    C'*** COMMAND IGNORED - DENSITY NOT SET'          90315
MOUNTMSG DC    C'>>> READY TAPE <<<'                             90315
         SPACE 1                                                GP03336
         LTORG ,             REQUIRED BY MSGDISP                GP03336
         SPACE 1                                                 81285
ANSITAB  TRTAB TYPE=ITOE     ANSI 3 (ISCII) TRANSLATION          90315
TRTAB    TRTAB OPT=3278                                         GP08167
         SPACE 1
*   WORK SPACE ADDED FOR EXHCXTIP                               GP08155
TIPWORK  DC    8XL256'0'                                        GP08155
         SPACE 1
TAPEREC  DS    0D ,          START OF I/O RECORD
         ORG   TAPEREC+(64*1024-2)                              GP02231
TAPEWEND DS    X                                                GP02231
         ORG   TAPEREC ,     FOR PATCH
         MEND  ,
