         MACRO ,
&NM      YCON  &STR,&END=,&BNDRY=H                      ADDED ON 89272
         GBLB  &YCON@OP
         GBLC  &YCON@NM
         LCLA  &I,&J,&K,&L
         LCLC  &L2,&LQ                                          GP00027
&K       SETA  K'&STR
         AIF   (T'&END NE 'O').TSTOPEN
         AIF   (T'&STR EQ 'O').CLOSE
         AIF   ('&STR'(1,1) EQ '*').CLOSE
.TSTOPEN AIF   (&K EQ 0).COMLEN
         AIF   ('&STR'(1,1) NE '''').COMLEN
&I       SETA  2
&J       SETA  &K-2
&K       SETA  &J
.LOOP    AIF   ('&STR'(&I,2) EQ '''''').SK2
         AIF   ('&STR'(&I,2) EQ '&&').SK2
&I       SETA  &I+1
         AGO   .INC
.SK2     ANOP  ,
&I       SETA  &I+2
&K       SETA  &K-1
.INC     AIF   (&I LE &J).LOOP
.COMLEN  AIF   (NOT &YCON@OP).NOPEN
         MNOTE 4,'PRIOR YCON NOT TERMINATED'
&YCON@OP SETB  0
.NOPEN   AIF   ('&BNDRY' EQ 'H' OR '&BNDRY' EQ 'Y').NOBOUND
         AIF   ('&BNDRY' NE 'X' AND '&BNDRY' NE 'C').DOBOUND
&L2      SETC  'L2'
         AGO   .NOBOUND
.DOBOUND DS    0&BNDRY
.NOBOUND AIF   (T'&END NE 'O').OPEN
&I       SETA  K'&STR                                           GP00027
         AIF   (&I LT 5).NOXLN                                  GP00027
         AIF   ('&STR'(1,2) EQ 'CL' OR '&STR'(1,2) EQ 'XL').XCHR
.NOXLN   ANOP  ,                                                GP00027
         AIF   (&K EQ 0).REQSTR
         AIF   ('&STR'(1,1) EQ '''').QSTR
&NM      DC    Y&L2.(&K+4),C'&STR'                               89272
         AGO   .MEND
.XCHR    ANOP  ,                                                GP00027
&I       SETA  &SYSNDX                                          GP00027
&LQ      SETC  'L'''                                            GP00027
&NM      DC    Y&L2.(&LQ.ZZDH&I)
ZZDH&I   DC    &STR                                             GP00027
         AGO   .MEND                                            GP00027
.QSTR    ANOP  ,
&NM      DC    Y&L2.(&K+4),C&STR                                 89272
         AGO   .MEND
.OPEN    AIF   (&K NE 0).OPSTR
&NM      DC    Y&L2.(&END-*)                                     89272
         AGO   .SETOPEN
.OPSTR   AIF   ('&STR'(1,1) EQ '''').OQSTR
&I       SETA  K'&STR                                           GP00027
         AIF   (&I LT 5).NOXLEN                                 GP00027
         AIF   ('&STR'(1,2) EQ 'CL' OR '&STR'(1,2) EQ 'XL').OXCHR
.NOXLEN  ANOP  ,                                                GP00027
&NM      DC    Y&L2.(&END-*),C'&STR'                             89272
         AGO   .SETOPEN
.OXCHR   ANOP  ,                                                GP00027
&NM      DC    Y&L2.(&END-*-2),&STR
         AGO   .SETOPEN                                         GP00027
.OQSTR   ANOP  ,
&NM      DC    Y&L2.(&END-*),C&STR                               89272
.SETOPEN ANOP  ,
&YCON@NM SETC  '&END'
&YCON@OP SETB  1
         MEXIT ,
.REQSTR  MNOTE 4,'TEXT STRING REQUIRED'
         MEXIT ,
.CLOSE   AIF   (&YCON@OP).WASOPEN
         MNOTE 4,'YCON END OUT OF SEQUENCE'
.WASOPEN AIF   ('&NM' EQ '' OR '&NM' EQ '&YCON@NM').BLAB
&NM      EQU   *
.BLAB    ANOP  ,
&YCON@NM EQU   *
&YCON@NM SETC  ''
&YCON@OP SETB  0
.MEND    MEND  ,
