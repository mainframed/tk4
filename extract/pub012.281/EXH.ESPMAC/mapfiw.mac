         MACRO ,
&NM      MAPFIW &DSECT=,&PFX=FIW,&LEN=0                 ADDED ON 84237
         LCLC  &P,&Q
&P       SETC  '&PFX'
&Q       SETC  '&P'.'SECT'
         AIF   ('&NM' EQ '').DFDS
&Q       SETC  '&NM'
.DFDS    AIF   ('&DSECT' EQ 'NO').NODS
&Q       DSECT ,
         AGO   .CMDS
.NODS    AIF   ('&NM' EQ '').CMDS
&Q       DS    0X
.CMDS    ANOP  ,
&P.FG    DC    X'00'         ENTRY FLAG
&P.FTXT  EQU   X'80'           ENTRY CONTAINS TEXT
&P.FERR  EQU   X'40'           TEXT IS IN ERROR
&P.FPEN  EQU   X'20'           FIELD SELECTED BY LIGHT-PEN
&P.FHLP  EQU   X'10'           TEXT IS HELP OR ?                 88221
&P.FPRO  EQU   X'08'           PROCESSED BY EXIT; IGNORE        GP04111
&P.FINT  EQU   X'02'           DISPLAY FIELD INTENSIFIED
&P.FINV  EQU   X'01'           DISPLAY OPPOSITE INTENSITY
&P.LEN   DC    AL1(0)        CURRENT TEXT LENGTH
&P.ATB   DC    AL2(0)        ADDRESS+1 OF ATTRIBUTE BYTE
         AIF   ('&LEN' EQ '0' OR '&LEN' EQ '').NULLEN            87166
&P.TEXT  DC    CL(&LEN)' '   TEXT FIELD (FROM CRT)               87166
         AGO   .COMLEN                                           87166
.NULLEN  ANOP  ,                                                 87166
&P.TEXT  DC    0C' '         TEXT FIELD (FROM CRT)
.COMLEN  ANOP  ,                                                 87166
&P.SIZE  EQU   *-&P.FG       SIZE OF ONE ENTRY (+FDOLEN)         87166
         MEND  ,
