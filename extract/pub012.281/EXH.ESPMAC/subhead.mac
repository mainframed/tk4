         MACRO ,                                                GP97332
&NM      SUBHEAD &F,&L,&BASE=      START A SUBROUTINE           GP03034
.*  SUBHEAD BEGINS A SUBROUTINE, USING A SAVE AREA STACK
.*    THE MACRO MUST PHYSICALLY PRECEDE THE MATCHING SUBRET
.*    MACRO FOR CORRECT REGISTER RESTORE.
.*  BY SPECIFYING &F AND &L APPROPRIATELY, (E.G., R2,R0),
.*    ALTERED REGISTER VALUES (IN THIS CASE, R1) CAN BE RETURNED
.*    TO THE CALLER
.*    FOR FUTURE COMPATIBILITY (BAKR/PR) USE R0,R15             GP03034
.*                                                              GP03034
.* WHEN BASE= IS SPECIFIED, APPROPRIATE USINGS ARE ISSUED, OTHERWISE
.*    THE CALLER'S USINGS REMAIN IN EFFECT.                     GP03034
.*                                                              GP03034
.*                                                              GP03034
.*                                                              GP03034
         GBLC  &ZZSUBR1,&ZZSUBR2                                GP00020
         LCLC  &EX,&CMU                                         GP03029
         LCLA  &I,&K                                            GP03034
&ZZSUBR1 SETC  '&F'                                             GP00020
&ZZSUBR2 SETC  '&L'                                             GP00020
         AIF   ('&F' NE '').HAVE1                               GP00020
&ZZSUBR1 SETC  'R0'                                             GP00020
.HAVE1   AIF   ('&L' NE '').HAVE2                               GP00020
&ZZSUBR2 SETC  '((16+&ZZSUBR1-1)-(16+&ZZSUBR1-1)/16*16)'        GP03029
.HAVE2   ANOP  ,                                                GP00020
&NM      STM   &ZZSUBR1,&ZZSUBR2,8(R13) SAVE CALLER'S REGISTERS GP00020
         ST    R13,LOCSAVE1-LOCSAVE+4(,R13)  MAKE BACK CHAIN    GP03034
         LA    R13,LOCSAVE1-LOCSAVE(,R13)  PUSH THE STACK       GP97332
.*--------------------------------------------------------------------*
.*  LOAD AND DECLARE BASE REGISTERS                                   *
.*--------------------------------------------------------------------*
&K       SETA  N'&BASE      MAX NUMBER OF BASES SPECIFIED       GP03034
         AIF   (&K LT 1).MEND  *** NONE *** USE CALLER'S        GP03034
&CMU     SETC  '&BASE(1)'    SET THE FIRST ONE                  GP03034
         MACPARM &BASE(1),(R15),OP=LA                           GP03034
         AIF   (&K LT 2).DOUSE                                  GP03034
&I       SETA  1                                                GP03034
         LA    &BASE(&K),2048                                   GP03034
.BASLOOP AIF   (&I GE &K).DOUSE                                 GP03034
&I       SETA  &I+1                                             GP03034
         AIF   ('&BASE(&I)' EQ '').BASLOOP                      GP03034
         LA    &BASE(&I),2048(&BASE(&K),&BASE(&I-1))            GP03034
&CMU     SETC  '&CMU'.','.'&BASE(&I)'                           GP03034
         AGO   .BASLOOP                                         GP03034
.DOUSE   USING &NM,&CMU                                         GP03034
.MEND    MEND  ,                                                GP03034
