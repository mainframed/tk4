         MACRO ,                                                GP06277
&NM      XPDCHECK &VAR,&ULN,&CHECK=YES,&FAIL=,&PASS=            GP06277
.**********************************************************************
.*   XPDCHECK verifies that a field is a valid packed decimal number. *
.*   &VAR specifies the variable location in a format that a length   *
.*        may be suffixed to ªxyz, abc-def+3, ...; not 0(R5) or (3)³  *
.*   &LEN specifies the explicit test length when L' is unwanted      *
.*   &CHECK=YES compares the valid length to the specified length     *
.*   &FAIL and &PASS specify branch addresses for bad/good numbers    *
.*       and are optional.                                            *
.*                                                                    *
.*   With CHECK=YES, R1 returns the TRT stop address, R0 the end      *
.*   So LA r,&VAR / SR r,R1  yields the valid length                  *
.**********************************************************************
         LCLB  &DOLEN                                           GP06277
         LCLC  &LEN,&LLN,&L                                     GP06277
&DOLEN   SETB  (('&CHECK' EQ 'YES' OR '&CHECK' EQ 'Y'))         GP06277
&L       SETC  'L'''                                            GP06277
&LLN     SETC  '&L'.'&VAR'                                      GP06277
         AIF   ('&LEN' EQ '').USLEN                             GP06277
&LEN     SETC  '('.'&ULN'.')'                                   GP06277
&LLN     SETC  '0'                                              GP06277
.*
.USLEN   ANOP  ,                                                GP06277
&NM      L     R14,EXWPDTRT  GET PACKED DECIMAL CHECK TABLE     GP06277
         TRT   &VAR,0(R14)   VALID PACKED DECIMAL?              GP06277
.*  N.B.: THE TRT TABLE HAS 0 FOR 2-DIGIT COMINATIONS, 4 FOR DIGIT/SIGN
.*         AND 8 FOR EVERYTHING ELSE. A GOOD NUMBER WILL END WITH 4
         MACPARM R2,1,EXWPDTRT+X'0F',MODE=3,OP=CLM,OMIT=&FAIL&PASS
         MACPARM &FAIL,MODE=ONE,NULL=SKIP,OP=BNE,OPR=BNER       GP06277
         AIF   (&DOLEN).LENCHK                                  GP06277
         MACPARM &PASS,MODE=ONE,NULL=SKIP,OP=BE,OPR=BER         GP06277
         MEXIT ,                                                GP06277
.LENCHK  LA    R0,&LLN-1+&VAR                                   GP06277
         CR    R0,R1         VALID LENGTH ?                     GP06277
         MACPARM &FAIL,MODE=ONE,NULL=SKIP,OP=BNE,OPR=BNER       GP06277
         MACPARM &PASS,MODE=ONE,NULL=SKIP,OP=BE,OPR=BER         GP06277
.MEND    MEND  ,                                                GP06277
