         MACRO -- $FCB3 -- GENERATE 3800 FORMS CONTROL BLOCK IMAGE
&LABEL   $FCB3 &LDN,&LPP,&DEFAULT=YES,&INDEX=LEFT,&INDENT=0,           *
               &SKIP=NO,&LPI=                                    93036
.*
.*       AUTHOR UNKNOWN - OBTAINED FROM OLD SHARE OR CBT TAPE
.*         $FCB RENAMED TO $FCB2; THIS VERSION ADAPTED FOR IBM 3800
.*         AND STC 6100. NOTE THAT THE STC 6100 DOES NOT HAVE THE HALF-
.*         INCH MULTIPLE PAPER LENGTH RESTRICTION THE 3800 HAS, AND
.*         THAT IT ACCEPTS FORMS LONGER THAN 11 INCHES, HENCE THERE
.*         ARE NO CHECKS FOR EITHER CONDITION.                   93006
.*       THE 6100 SUPPORTS (HARDWARE FEATURE) PERFORATION OVERPRINTING.
.*         THIS IS SET WITH SKIP=NO. CHANGE TO SKIP=YES FOR 3800S.
.*         SKIP=NO PRODUCES A "SIGNATURE FRAME" OF 0B0000 AND 000000
.*         AROUND THE ACTUAL FCB TEXT (LINE COUNT INCREASED TO MATCH).
.*         ADDITIONALLY, IN THIS MODE A LINE DENSITY OF 24/INCH IS
.*         SUPPORTED, AND FORMS LENGTH MAY BE MULTIPLE OF 1/6 INCH.
.*                                                               93036
.*         NOTE THAT CHANNEL ONE MAY APPEAR ONLY ONCE, ON LINE 1.
.*       THE DEFAULT, INDEX AND INDENT OPTIONS WERE KEPT TO ALLOW $FCB2
.*         DECKS TO BE USED AS IS (WITH FCB2 => FCB3)            93006
.*
.**********************************************************************
.*                                                                    *
.*   NOTE: $FCB3 IS COPIED TO MACLIB AFTER UPDATES FOR USER IMAGELIB  *
.*                                                                    *
.**********************************************************************
         LCLA  &A(336),&LP,&N,&WK,&CC,&LAST,&K,&K1,&LI,&NP,&I,&J,&MAX
         LCLA  &O1,&O2                                          GP03245
         LCLB  &Q,&C,&PASS,&CCH(12),&DFI,&POF                    93036
         LCLC  &V(336),&CH,&BLANK,&B,&B1,&IX,&NAM,&CD,&DV        93036
&POF     SETB  ('&SKIP' EQ 'NO')   PERFORATION SKIP              93036
&LAST    SETA  N'&SYSLIST          GET NR OF OPERANDS
&DV      SETC  '3800'                                            93036
&MAX     SETA  336                                               93036
         AIF   (NOT &POF).DV3800                                 93036
&MAX     SETA  330                                               93036
&DV      SETC  '6100'                                            93036
.DV3800  AIF   (&LAST LT 3).MACBAD ERROR IF LT 3
.*
.*             STEP 1 - VERIFY LINES-PER-INCH OPERAND
.*
.OP1     AIF   (T'&LDN NE 'N').OP1BAD  OP1 MUST BE NUMERIC       93006
         AIF   (&LDN EQ 6 OR &LDN EQ 8).OP1GUD                   93036
         AIF   (&LDN NE 10 AND &LDN NE 12 AND &LDN NE 24).OP1BAD 93036
.OP1GUD  ANOP  ,                                                 93036
&LI      SETA  &LDN          SET LINES PER INCH                  93006
&NP      SETA  &LI/2         NON-PRINTING MARGIN                 93006
&CD      SETC  '.....0.1.2.3...........5'(&LI,1)  SET LPI FLAG VALUE
.*
.*             STEP 2 - VERIFY LINES-PER-PAGE OPERAND
.*
.OP2     AIF   (T'&SYSLIST(2) NE 'N').OP2BAD  OP2 MUST BE NUMERIC
         AIF   (&SYSLIST(2) LE &LI OR &SYSLIST(2) GT &MAX).OP2BAD
&LP      SETA  &SYSLIST(2)         SET NR OF LINES PER PAGE
&N       SETA  1             SET LOOP INDEX                      93036
         AIF   (&POF).OP2I   PRINT OVER FOLD - KEEP LENGTH       93036
&LP      SETA  &LP-&LI       ALLOW FOR TOP AND BOTTOM MARGIN     93006
.OP2I    ANOP  ,                                                 93036
&V(&N)   SETC  '&CD'         PRESET ALL LINES FOR BASIC DENSITY  93036
&N       SETA  &N+1                                              93036
         AIF   (&N LE &LP).OP2I                                  93036
.*                                                               93036
.*   LOOK FOR LINE DENSITY OVERRIDES:                            93036
.*     LPI=((LPI),(LPI,N),(,N)...)                               93036
.*   WHERE (LPI) DOES ONE LINE AT 'LPI' DENSITY                  93036
.*       (LPI,N) DOES 'N' LINES AT DENSITY 'LPI'                 93036
.*     AND  (,N) DOES 'N' LINES AT DEFAULT DENSITY               93036
.*                                                               93036
         AIF   (T'&LPI EQ 'O').OP4   NO OVERRIDES                93036
&N       SETA  N'&LPI        NUMBER OF SUBOPERANDS               93036
&K       SETA  0                                                 93036
&I       SETA  1             ARRAY INDEX                         93036
.OP3LUP  AIF   (&K GE &N).OP4  ALL DONE                          93036
&K       SETA  &K+1                                              93036
         AIF   (N'&LPI(&K) LT 1).OP3LUP                          93036
&O1      SETA  &LDN          SET DEFAULT LINES PER INCH          93036
&O2      SETA  1             SET DEFAULT LINE COUNT              93036
         AIF   (N'&LPI(&K) LT 2).OP3S                            93036
         AIF   (N'&LPI(&K) GT 2).OP3BAD                          93036
         AIF   ('&LPI(&K,1)' EQ '').OP31D                        93036
&B       SETC  '&LPI(&K,1)'                                      93036
         AIF   (T'&B NE 'N').OP3BAD                              93036
&O1      SETA  &B                                                93036
.OP31D   AIF   (T'&LPI(&K,2) NE 'N').OP3BAD                      93036
&O2      SETA  &LPI(&K,2)                                        93036
         AGO   .OP3MAKE                                          93036
.OP3S    AIF   (T'&LPI(&K,1) NE 'N').OP3BAD                      93036
&O1      SETA  &LPI(&K,1)                                        93036
.OP3MAKE AIF   (&O1 EQ 6 OR &O1 EQ 8).OP3GUD                     93036
         AIF   (&O1 NE 10 AND &O1 NE 12 AND &O1 NE 24).OP3BAD    93036
.OP3GUD  ANOP  ,                                                 93036
&B       SETC  '.....0.1.2.3...........5'(&O1,1)  SET LPI FLAG VALUE
&J       SETA  &I+&O2                                            93036
         AIF   (&J LE &LP+1).OP3FILL                             93036
 MNOTE 8,'LPI OPERAND &K &LPI(&K) EXCEEDS NUMBER OF LINES SPECIFIED'
         AGO   .OP4                                              93036
.OP3FILL ANOP  ,                                                 93036
&V(&I)   SETC  '&B'                                              93036
&I       SETA  &I+1                                              93036
         AIF   (&I LT &J).OP3FILL                                93036
         AGO   .OP3LUP                                           93036
.OP3BAD  MNOTE 8,'INVALID LPI OPERAND &K : &LPI(&K) '            93036
         AGO   .OP3LUP                                           93036
.*
.*             STEP 4 - PROCESS OPERANDS
.*
.OP4     ANOP  ,
&N       SETA  2                   SET OPERAND CTR TO 2
.OPN     ANOP                      START LOOP
&N       SETA  &N+1                ADD 1 TO OPERAND CTR
         AIF   (&N GT &LAST).ENDSCAN  B IF NO MORE OPERANDS
         AIF   (T'&SYSLIST(&N) NE 'N').OPH  B IF NOT NUMERIC OPD
         AIF   (&N EQ 3).OP3BAD    ERROR IF OP3 NOT HYPHENATED
         AIF   (NOT &C).OPN        IGNORE OP IF INVALID &CC
&WK      SETA  &SYSLIST(&N)        SET WORK TO OPERAND VALUE
.OPN1    AIF   (&WK LT 1 OR &WK GT &LP).OPLBAD  B IF BAD LINE NR
         AIF   (&A(&WK) NE 0 AND &A(&WK) NE &CC).DUPBAD  B IF DUP
&A(&WK)  SETA  &CC                 SET &WK-TH LINE TO &CC
         AGO   .OPN                LOOP
.*
.*             STEP 3A - DECODE HYPHENATED OPERAND
.*
.OPH     ANOP                      NON-NUMERIC OPERAND
&K       SETA  K'&SYSLIST(&N)      SET OPERAND CHARACTER COUNT
&K1      SETA  0                   SET CHAR CT TO ZERO
&WK      SETA  0                   SET WORK TO ZERO
&PASS    SETB  0                   SHOW FIRST PASS
.*
.*             1ST PASS - FIND CARRIAGE CHANNEL NUMBER
.*             2ND PASS - FIND FIRST LINE NUMBER
.*
.OPH1    ANOP                      START LOOP
&K1      SETA  &K1+1               ADD 1 TO CHAR CTR
         AIF   (&K1 GT &K).OPH3    B IF END OF STRING
&CH      SETC  '&SYSLIST(&N)'(&K1,1)  GET A CHARACTER
         AIF   ('&CH' LT '0' OR '&CH' GT '9').OPH2  B IF NON-NUM
&WK      SETA  &WK*10+&CH          COMPILE DECIMAL NUMBER
         AGO   .OPH1               LOOP
.*
.*             NON-NUMERIC CHARACTER FOUND.
.*             IF FIRST PASS, EXPECT HYPHEN.
.*             IF SECOND PASS, ERROR.
.*
.OPH2    AIF   (&PASS OR '&CH' NE '-').HYPBAD  B IF ERROR
&CC      SETA  &WK                 SET CARRIAGE CHANNEL
&WK      SETA  0                   ZERO WORK
&PASS    SETB  1                   SHOW SECOND PASS
         AGO   .OPH1               RETURN TO LOOP
.*
.*             END OF CHARACTER STRING.
.*             IF FIRST PASS, LOGIC ERROR.
.*             IF SECOND PASS, VERIFY &CC.
.*
.OPH3    AIF   (NOT &PASS).LOGBAD  ERROR IF FIRST PASS
         AIF   (&CC LT 1 OR &CC GT 12).OPCBAD  B IF CC BAD
&CCH(&CC) SETB 1                   SHOW CARRIAGE CHANNEL USED
&C       SETB  1                   SHOW VALID &CC
         AGO   .OPN1               GO CHECK VALID LINE NR
.*
.*             OPERAND SCAN IS ENDED.  TEST ITS SUCCESS.
.*
.ENDSCAN AIF   (&Q).QUITBAD        EXIT IF QUIT SWITCH
         AIF   (K'&LABEL LE 4).LABBAD  B IF LABEL NOT SPECIFIED
         AIF   ('&LABEL'(1,4) NE 'FCB3').LABBAD
&N       SETA  5
&K       SETA  K'&LABEL
&NAM     SETC  '&LABEL'(5,&K-4)  MAKE FCB NAME                   93006
.LABLOOP AIF   (&N GT &K).END6
&CH      SETC  '&LABEL'(&N,1)  GET A CHARACTER OF LABEL
&N       SETA  &N+1          SET FOR NEXT TIME
&C       SETB  0
&C       SETB  (&C OR ('&CH' GE '0' AND '&CH' LE '9'))
&C       SETB  (&C OR ('&CH' GE 'A' AND '&CH' LE 'I'))
&C       SETB  (&C OR ('&CH' GE 'J' AND '&CH' LE 'R'))
&C       SETB  (&C OR ('&CH' GE 'S' AND '&CH' LE 'Z'))
         AIF   (NOT &C).LABBAD     B IF NOT NUM OR ALPHA
         AGO   .LABLOOP
.END6    ANOP
&DFI     SETB  1             PROVISIONALLY SET DEFAULT IMAGE
         AIF   ('&DEFAULT' EQ '' OR '&DEFAULT' EQ 'YES').END7
&DFI     SETB  0             SET NO DEFAULT IMAGE AS DEFAULT
         AIF   ('&DEFAULT' NE 'NO').DEFBAD
.END7    ANOP  ,
.*
.*             ENSURE ALL CHANNELS ARE PUNCHED TO PREVENT RUNAWAY
.*
&N       SETA  0                   ZERO CHANNEL COUNTER
&C       SETB  0                   ZERO FIRST-TIME MSG SWITCH
&K       SETA  0                   ZERO TOP-DOWN LINE COUNTER
&K1      SETA  &LP+1               SET BOTTOM-UP LINE CTR TO MAX+1
.END11   ANOP                      START LOOP
&N       SETA  &N+1                ADD ONE TO CHANNEL COUNTER
         AIF   (&N GT 12).END19    B IF DONE WITH ALL CHANNELS
         AIF   (&CCH(&N)).END11    B IF CHANNEL IS ASSIGNED
         AIF   (&C).END12          B IF NOT FIRST TIME
         SPACE 1
         MNOTE *,'THE FOLLOWING ASSIGNMENTS OF UNSPECIFIED '
         MNOTE *,'CARRIAGE CHANNELS PREVENT RUN-AWAY FORMS ---'
&C       SETB  1                   SHOW FIRST-TIME MSG WRITTEN
.END12   AIF   (&N EQ 9 OR &N EQ 12).END14  B IF CH 9 OR 12
.END13   ANOP                      UNASSIGNED CHANNEL FOUND
&K       SETA  &K+1                ADD ONE TO LINE COUNTER
         AIF   (&K GT &LP).END17   B IF ALL LINES HAVE CHANNELS
         AIF   (&A(&K) NE 0).END13 B IF THIS LINE HAS A CHANNEL
&WK      SETA  &K                  SET LINE NUMBER AND
         AGO   .END15              GO ASSIGN CHANNEL TO IT
.END14   ANOP                      UNASSIGNED CHANNEL 9 OR 12
&K1      SETA  &K1-1               LOWER LINE COUNTER BY ONE
         AIF   (&K1 LT 1).END17    B IF ALL LINES HAVE CHANNELS
         AIF   (&A(&K1) NE 0).END14  B IF THIS LINE HAS A CHANNEL
&WK      SETA  &K1                 SET LINE NUMBER
.END15   ANOP                      ASSIGN CHANNEL TO LINE
&BLANK   SETC  ''                  ASSUME CHANNEL 10 OR GREATER
         AIF   (&N GT 9).END16     B IF SO
&BLANK   SETC  ' '                 ADD A BLANK FOR CHAN 9 OR LESS
.END16   MNOTE *,'   CARRIAGE CHANNEL &BLANK.&N IN LINE &WK'
&A(&WK)  SETA  &N                  ASSIGN CHANNEL TO LINE
&CCH(&N) SETB  1                   SHOW CHANNEL ASSIGNED
         AGO   .END11              B TO START OF LOOP
.END17   AIF   (&CCH(1)).END18     GO IF CHANNEL 1 IS ASSIGNED
         MNOTE 4,'ERROR - CARRIAGE CHANNEL 1 IS NOT SPECIFIED AND'
         MNOTE *,'CANNOT BE INSERTED.  CHANNEL 1 PUNCH IS'
         MNOTE *,'REQUIRED FOR 3800 CARRIAGE-RESTORE FUNCTION.'  93006
         AGO   .QUITBAD
.END18   MNOTE 0,'WARNING - UNSPECIFIED CARRIAGE CHANNELS'
         MNOTE 0,'STARTING WITH CHANNEL &N CAN CAUSE'
         MNOTE 0,'DATA CHECKS.'                                  93036
.END19   ANOP  ,
&IX      SETC  ''            NO INDEX
         AIF   (T'&INDENT EQ 'O' OR '&INDENT' EQ '0').SKIPIX
         AIF   (T'&INDENT EQ 'N').TESTIND
.BADIND  MNOTE 4,'INDENT MUST BE NUMERIC (0 TO 31)'
         AGO   .QUITBAD
.TESTIND AIF   (&INDENT GT 31).BADIND
&IX      SETC  '10'          SET DEFAULT (LEFT) INDENT
         AIF   (T'&INDEX EQ 'O' OR '&INDEX' EQ 'LEFT').SKIPIX
&IX      SETC  '01'          SET RIGHT
         AIF   ('&INDEX' EQ 'RIGHT').SKIPIX
         MNOTE 4,'INVALID INDEX - SHOULD BE LEFT OR RIGHT'
         AGO   .QUITBAD
.SKIPIX  ANOP  ,
.*
.*             CREATE FORMS CONTROL BUFFER OVERLAY
.*
******************************************************************
*                                                                *
         MNOTE *,' &DV FORMS CONTROL BUFFER LOAD FOR &LABEL'     93006
*                                                                *
******************************************************************
*                                                                *
         SPACE 1
&LABEL   CSECT ,
         SPACE 1
         DC    CL4'&NAM '    FCB3 NAME
         AIF   (NOT &POF).SKPFLD  SKIP OVER FOLD                 93036
         DC    A(&LPP+6)     LINES PER PAGE + SIGNATURE          93036
         DC    X'0B0000'     NON-SKIP REQUEST                    93036
         AGO   .PROVFD       SKIP SKIP CODE                      93036
.SKPFLD  DC    A(&LPP)       LINES PER PAGE                      93006
         DC    &NP.AL.4(&CD,0)  NON-PRINTING TOP MARGIN          93006
.PROVFD  SPACE 1
*                             GENERATE FORMS CONTROL BUFFER
&N       SETA  0                   SET CUR BYTE PTR TO ZERO
.DOLP    ANOP  ,                   BUILD THE FCB
&N       SETA  &N+1                POINT TO THE NEXT BYTE
&K       SETA  &N            REMEMBER THE STARTING POINT         93036
         AIF   (&A(&N) NE 0 OR &N GE &LP).DO1                    93036
.DOMR    AIF   (&N GE &LP).DOXP                                  93036
         AIF   (&A(&N) NE &A(&N+1) OR &V(&N) NE &V(&N+1)).DOXP   93036
&N       SETA  &N+1                                              93036
         AGO   .DOMR                                             93036
.DOXP    ANOP  ,                                                 93036
&I       SETA  &N-&K+1       SET NUMBER OF EQUAL LINES           93036
         AIF   (&I LT 2).DO1       SKIP DOING THEM IF NONE
         DC    &I.AL.4(&V(&N),&A(&N))                            93036
         AGO   .DO4          SET FOR NEXT                        93036
.DO1     ANOP  ,
.*
.*             SELECT PROPERLY-COMMENTED DC STATEMENT
.*
&WK      SETA  &A(&N)              SET WORK TO BYTE'S VALUE
         AIF   (&WK EQ 0).DO3      B IF ZERO-DIGIT ENTRY         93036
&B       SETC  ''                  SET TWO LCLC VARIABLES
         AIF   (&WK GT 9).DO2      B IF ADCON IS TWO DIGITS
&B       SETC  ' '                 ELSE ADD BLANK TO COMMENT
.* ONE- OR TWO-DIGIT ENTRY
.DO2     DC AL.4(&V(&N),&A(&N)),0C'      &B.CHAN &B.&WK IS AT LINE &N'
         AGO   .DO4
.* NO-DIGIT ENTRY
.DO3     DC    AL.4(&V(&N),&A(&N))                               93036
.DO4     AIF   (&N EQ &LP).END     B IF LAST BYTE PROCESSED
         AGO   .DOLP               GO LOOP
.*
.*
.*
.*
.*             DIAGNOSTIC ERROR MESSAGES
.*
.OP1BAD  MNOTE 4,'INVALID OPERAND 1 - &SYSLIST(1) -'
         MNOTE *,'FIRST OPERAND MUST BE EITHER 6 OR 8.'
&Q       SETB  1
         AGO   .OP2
.*
.OP2BAD  MNOTE 4,'INVALID OPERAND 2 - &SYSLIST(2) -'
         MNOTE *,'SECOND OPERAND MUST BE A NUMBER GREATER THAN 1'
         MNOTE *,'AND NOT GREATER THAN &MAX.'                    93036
&Q       SETB  1
         AGO   .OP4
.*
.OP4BAD  MNOTE 4,'INVALID OPERAND 3 - &SYSLIST(3) -'
         MNOTE *,'THIRD OPERAND MUST HAVE A CARRIAGE CHANNEL'
         MNOTE *,'NUMBER, A HYPHEN, AND A LINE NUMBER.'
&Q       SETB  1
         AGO   .OPN
.*
.OPLBAD  AIF   (T'&SYSLIST(&N) NE 'N').OPLBADA
         MNOTE 4,'INVALID OPERAND &N - &SYSLIST(&N) -'
         AGO   .OPLBADB
.OPLBADA MNOTE 4,'INVALID SUBOPERAND &WK OF OPERAND &N - &SYSLIST(&N) -X
               '
.OPLBADB MNOTE *,'LINE NUMBER MUST BE GREATER THAN 1 AND '
         MNOTE *,'NOT GREATER THAN &LP..'
&Q       SETB  1
         AGO   .OPN
.*
.DUPBAD  MNOTE 4,'DUPLICATE SPECIFICATION - OPERAND &N ASSIGNS'
         MNOTE *,'CARRIAGE CHANNEL &CC TO LINE &WK, WHICH WAS'
         MNOTE *,'PREVIOUSLY ASSIGNED CHANNEL &A(&WK)..'
&Q       SETB  1
         AGO   .OPN
.*
.HYPBAD  MNOTE 4,'INVALID OPERAND &N - &SYSLIST(&N) -'
         AIF   (&PASS).HYPBADA
         MNOTE *,'UNRECOGNIZABLE CHARACTER ''&CH'''
         AGO   .HYPBADB
.HYPBADA MNOTE *,'UNRECOGNIZABLE CHARACTER ''&CH'' IN LINE NUMBER.'
.HYPBADB ANOP
&Q       SETB  1
         AGO   .OPN
.*
.OPCBAD  MNOTE 4,'INVALID OPERAND &N - &SYSLIST(&N) -'
         MNOTE *,'CARRIAGE CHANNEL MUST BE GREATER THAN 0'
         MNOTE *,'AND NOT GREATER THAN 12.'
&C       SETB  0                   SHOW INVALID &CC
&Q       SETB  1
         AGO   .OPN
.*
.LOGBAD  MNOTE 4,'INTERNAL LOGIC ERROR AT OPERAND &N'
         AGO   .QUITBAD
.*
.MACBAD  MNOTE 4,'TOO FEW OPERANDS SPECIFIED.  AT LEAST LINES PER'
         MNOTE *,'INCH, LINES PER PAGE, AND ONE CARRIAGE CONTROL'
         MNOTE *,'PUNCH MUST BE SPECIFIED.'
         AIF   (N'&SYSLIST EQ 2).OP1
         AGO   .QUITBAD
.*
.QUITBAD MNOTE *,'FCB WILL NOT BE GENERATED'
         MEXIT
.*
.LABBAD  MNOTE 8,'ERROR - LABEL MISSING OR INCORRECT (NOT FCB3XXXX)'
         AGO   .QUITBAD
.*
.DEFBAD  MNOTE 8,'ERROR - DEFAULT OPTION MUST BE ''YES'' OR ''NO'''
         AGO   .QUITBAD
.*
.END     AIF   (NOT &POF).ENDSKP                                 93036
         DC    X'000000'     FAKE BOTTOM MARGIN                  93036
         MEXIT ,                                                 93036
.ENDSKP  DC    &NP.AL.4(&CD,0)   NON-PRINTING BOTTOM MARGIN      93006
         SPACE 2
         MEND
