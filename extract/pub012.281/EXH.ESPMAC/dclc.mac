         MACRO
&NAME    DCLC  &AA,&DUMMY1,&DUMMY2,&CAPWORD=,&CAPLETR=,&PRINT=
         GBLB  &DCLCLST      IF ON, PRINT COMMENTS SHOWING FLAGS
         LCLA  &LENGTH,&AIN,&AOUT,&K,&USERLNG,&QUOTE1
         LCLB  &CW           IF ON , PROCESSING A CAPITAL WORD
         LCLC  &CAPL,&CAPW,&CIN,&COUT,&DUPLC,&LNC,&T
         LCLC  &P(15),&Q(120)
         ACTR  30*K'&AA+3    30 TIMES NO. OF CHARACTERS IN OPERAND
.*360D-CM-000  DCLC          DEFINE-CONSTANT-LOWER-CASE    V 01/08/69 *
.* BY  CHUCK MEYER, IBM, N.Y.PUBLISHING, 555 MADISON, NYC, 10022, USA *
&DCLCLST SETB  ((&DCLCLST AND '&PRINT' NE 'OFF')  OR  '&PRINT' EQ 'ON')
.*
         AIF   (K'&AA LT 3).ERROR1
         AIF   ('&AA'(K'&AA,1)  EQ  '''').OKPARAM
.ERROR1  AIF   ('&PRINT'  EQ  'ON'  OR  '&PRINT'  EQ  'OFF').MEND
         MNOTE 4,'INVALID OR MISSING OPERAND.  DATA IGNORED   '
         AIF   ('&NAME'  EQ  '').MEND
&NAME    EQU   *             BUT AT LEAST YOUR -NAME- WILL BE DEFINED.
         AGO   .MEND
.*
.OKPARAM ANOP
.*   DETERMINE CAP-WORD,  BYTE WHICH WILL -FLAG- START OF CAPITAL WORD.
&CAPW    SETC  '/'     SLASH     *STD-VALU*
         AIF   (K'&CAPWORD NE  1  AND  K'&CAPWORD  NE  3).OKCAPW
&CAPW    SETC  '&CAPWORD'(1,1)    PICK UP FIRST POSITION
         AIF   (K'&CAPWORD  NE  3).OKCAPW
&CAPW    SETC  '&CAPWORD'(2,1)   PICK UP SECOND POSITION
.OKCAPW  ANOP
.*
.*   DETERMINE CAP-LETTER,  BYTE TO -FLAG- NEXT LETTER AS A CAPITAL
&CAPL    SETC  '<'   LESS-THAN SIGN    *STD-VALU*
         AIF   (K'&CAPLETR  NE  1  AND  K'&CAPLETR  NE  3).OKCAPL
&CAPL    SETC  '&CAPLETR'(1,1)   PICK UP FIRST POSITION
         AIF   (K'&CAPLETR  NE  3).OKCAPL
&CAPL    SETC  '&CAPLETR'(2,1)    PICK UP SECOND POSITION
.OKCAPL  ANOP
.*
         AIF   (NOT  &DCLCLST).NONOTES    PRINT NOTES, IF REQUESTED
         MNOTE *,'CAP-WORD   CHARACTER IS ''&CAPW'' .'
         MNOTE *,'CAP-LETTER CHARACTER IS ''&CAPL'' .'
.NONOTES ANOP
.*
.QTLOOP  ANOP  ,   SCAN LEFT-TO-RIGHT FOR FIRST QUOTE
&QUOTE1  SETA  &QUOTE1+1     BUMP TO NEXT POSITION
         AIF   ('&AA'(&QUOTE1,1)  NE  '''').QTLOOP
.*  &QUOTE1  NOW  POINTS  TO  LEADING  QUOTE  IN  CONSTANT
.*   CHECK FOR USER-SUPPLIED DUPLICATION-FACTOR
         AIF   ('&AA'(1,1) LT '0').NODUPL
.DUPLOOP AIF   ('&AA'(&AIN+1,1)  LT  '0').DUPEND
&AIN     SETA  &AIN+1
         AIF   (&AIN  LT  8).DUPLOOP
.DUPEND  ANOP
&DUPLC   SETC  '&AA'(1,&AIN)   PICK UP USERS DUPLICATION-FACTOR
.NODUPL  ANOP  ,   &AIN POINTS TO LAST DIGIT IN DUPL.FACTOR OR ZERO
.*
.*   CHECK FOR USER-SUPPLIED LENGTH ASSIGNMENT
.LNGLOOP ANOP  ,       LOOP TO FIND BEGINNING OF LENGTH FIELD
&AIN     SETA  &AIN+1
         AIF   ('&AA'(&AIN,1)  EQ  '''').NOLNGTH
         AIF   ('&AA'(&AIN,1)  LT  '0'  AND  &AIN  LT  &QUOTE1).LNGLOOP
.*  &AIN NOW POINTS TO FIRST DIGIT IN LENGTH ATTRIBUTE.
&LNC     SETC  '&AA'(&AIN,&QUOTE1-&AIN)       USERS-LENGTH IN CHARS
&USERLNG SETA  &LNC          USERS-LENGTH IN NUMERICS
.NOLNGTH ANOP
.*
.*   NOW WE CAN START SCANNING THE ACTUAL DATA
.*
&AIN     SETA  &QUOTE1       SET INPUT POINTER TO FIRST QUOTE
&LENGTH  SETA  K'&AA-&AIN-1    TENTATIVE LENGTH ATTRIBUTE
.*
.LOOP1   ANOP  ,   LOOP THRU HERE AS WE SCAN EACH INPUT CHARACTER
&AIN     SETA  &AIN+1        BUMP INPUT-POINTER
&AOUT    SETA  &AOUT+1       BUMP OUTPUT-POINTER
         AIF   (&AIN  GE  K'&AA).ENDSCAN   IF LAST CHARACTER, ALL DONE.
&CIN     SETC  '&AA'(&AIN,1)  SAVE THIS INPUT CHARACTER
&CW      SETB  ((&CW  AND  '&CIN'  NE  ' ')  OR  '&CIN'  EQ  '&CAPW')
.*   TURN ON &CW IF THIS IS CAP-WORD CHAR, TURN OFF &CW IF THIS IS BLNK
         AIF   ('&CIN'  EQ  '&CAPW'  OR  '&CIN'  EQ  'CAPL').CAPITAL
         AIF   ('&CIN'  EQ  ''''   OR '&CIN'  EQ  '&&'(1,1)).QUOTE
         AIF   ('&CIN'  LT  'A'  OR  '&CIN'  GT  'Z'  OR  &CW).SETSAME
.*
.*  &AIN NOW POINTS TO A CAPITAL LETTER ON INPUT, WHICH MUST BE
.*  CONVERTED TO ITS LOWER-CASE EQUIVALENT.
.*   THE 26 LETTERS OF THE ALPHABET ARE LISTED HERE,
.*   ORDERED BY THEIR FREQUENCY OF OCCURANCE IN THE ENGLISH LANGUAGE ..
.*              'ETAOSINRHLDCUMYBPWFGVKJXQZ'
.*             ERGO,  WE WILL BUILD OUR TABLE IN THAT ORDER.
.*
&K       SETA  1
.LOOP2   AIF   ('ETAOSINRHLDCUMYBPWFGVKJXQZ'(&K,1)  EQ  '&CIN').LCFOUND
&K       SETA  &K+1
         AIF   (&K  LE  26).LOOP2
         AGO   .SETSAME      WAS NOT ALPHABETIC
.LCFOUND ANOP  ,    &K POINTS TO POSITION IN TABLE FOR THIS LETTER
&COUT    SETC  'etaosinrhldcumybpwfgvkjxqz'(&K,1)  LOWER CASE PUNCHES
&Q(&AOUT) SETC '&COUT'       SET OUTPUT LETTER TO THIS L-C LETTER
         AGO   .LOOP1        GET NEXT LETTER
.*
.*
.QUOTE   ANOP  ,   TO HANDLE PAIRED QUOTES AND PAIRED AMPERSANDS
&Q(&AOUT) SETC '&CIN'        SET UP OUTPUT CHARACTER
&AOUT    SETA  &AOUT+1       BUMP OUTPUT-CHARACTER-POINTER
.*
.CAPITAL ANOP  ,   TO HANDLE CAP-LETR AND CAP-WORD CHARACTERS
&LENGTH  SETA  &LENGTH-1     DOESN'T COUNT IN LENGTH
         AIF   (&AIN+1  GE  K'&AA).ENDSCAN    LAST CHARACTER  IN  LIT ?
&AIN     SETA  &AIN+1        BUMP INPUT-CHARACTER-POINTER
&CIN     SETC  '&AA'(&AIN,1)   PICK UP NEXT INPUT CHARACTER
.*
.SETSAME ANOP  ,   TO SET UP OUTPUT CHARACTER WITHOUT CHANGING IT
&Q(&AOUT) SETC '&CIN'
         AGO   .LOOP1        GET NEXT LETTER
.*
.ENDSCAN ANOP  ,  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *
.*  WE HAVE COMPLETED THE SCAN OF INPUT,  NOW PREPARE THE -DC-.      *
.*                                                                   *
         AIF   (&USERLNG  EQ  &LENGTH  OR  &USERLNG  EQ  0).OKLNGTH
&T       SETC  '*'           SET UP MNOTE TO BE JUST A COMMENT
         AIF   (&USERLNG  GT  &LENGTH).LNMNOTE
&T       SETC  '1'           MAKE IT AN ERROR,  TRUNCATION
.LNMNOTE AIF   (NOT &DCLCLST).LNMNPRT  SKIP MNOTE IF PRINT OFF   87287
         MNOTE &T,'YOUR ''CL&USERLNG'' SPEC. OVERRIDES ASSUMED LENGTH O*
               F &LENGTH..'
.LNMNPRT ANOP  ,                                                 87287
&LENGTH  SETA  &USERLNG      PICK UP USER'S LENGTH
.OKLNGTH ANOP
&AIN     SETA  1
&AOUT    SETA  1
.*  THE NEXT 5 LINES CONVERT THE 120 SUBSCRIPTED SET SYMBOLS
.*  (EACH OF WHICH CONTAINS ONE BYTE OF DATA OR NULL)  INTO 15
.*  SUBSCRIPTED SET SYMBOLS (EACH CONTAING 8 BYTES).
.LOOP3   ANOP
&P(&AOUT) SETC '&Q(&AIN)&Q(&AIN+1)&Q(&AIN+2)&Q(&AIN+3)&Q(&AIN+4)&Q(&AIN+
               +5)&Q(&AIN+6)&Q(&AIN+7)'    EIGHT AT A TIME
&AIN     SETA  &AIN+8
&AOUT    SETA  &AOUT+1
         AIF   (&AOUT  LE  15).LOOP3    KEPP LOOKING
.*
&NAME    DC   &DUPLC.CL&LENGTH'&P(1)&P(2)&P(3)&P(4)&P(5)&P(6)&P(7)&P(8)&
               &P(9)&P(10)&P(11)&P(12)&P(13)&P(14)&P(15)'
.MEND    MEND
