         MACRO ,
         LCSTR &S            MACRO TO MAKE STRING LOWER CASE     87193
         GBLC  &LCSTR
         LCLA  &I,&J,&K
         LCLC  &C
&K       SETA  K'&S
&LCSTR   SETC  '&S'
.LOOP    AIF   (&I GE &K).MEND
&I       SETA  &I+1
&C       SETC  '&LCSTR'(&I,1)
         AIF   ('&C' LT 'A' OR '&C' GT 'Z').LOOP
&J       SETA  1
.CHAR    AIF   ('&C' EQ 'ABCDEFGHIJKLMNOPQRSTUVWXYZ'(&J,1)).REP
&J       SETA  &J+1
         AIF   (&J LE 26).CHAR
.REP     ANOP  ,
&C       SETC  'abcdefghijklmnopqrstuvwxyz'(&J,1)
         AIF   (&I EQ &K).LAST
         AIF   (&I EQ 1).FIRST
&LCSTR   SETC  '&LCSTR'(1,&I-1).'&C'.'&LCSTR'(&I+1,&K-&I)
         AGO   .LOOP
.FIRST   ANOP  ,
&LCSTR   SETC  '&C'.'&LCSTR'(2,&K-1)
         AGO   .LOOP
.LAST    ANOP  ,                                                 87193
&LCSTR   SETC  '&LCSTR'(1,&K-1).'&C'
.MEND    MEND  ,
