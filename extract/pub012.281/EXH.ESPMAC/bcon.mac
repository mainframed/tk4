         MACRO ,
&NM      BCON  &STR,&END=                              ADDED ON GP02242
         GBLB  &VCON@OP
         GBLC  &VCON@NM
         LCLA  &I,&J,&K,&L
.**********************************************************************
.**                                                                  **
.**  BCON BUILDS A TEXT MESSAGE BEGINNING WITH A ONE-BYTE LENGTH,    **
.**    FOLLOWED BY TEXT.                                             **
.**                                                                  **
.**  USE   BCON  'TEXT'                                              **
.**                                                                  **
.**  OR    BCON  'TEXT1',END=LABEL                                   **
.**        DC     ...ZERO OR MORE STORAGE ITEMS                      **
.**  LABEL BCON   *END    TO GENERATE A SINGLE MESSAGE               **
.**                                                                  **
.**********************************************************************
&K       SETA  K'&STR
         AIF   (T'&END NE 'O').TSTOPEN
         AIF   (T'&STR EQ 'O').CLOSE
         AIF   ('&STR'(1,1) EQ '*').CLOSE
.TSTOPEN AIF   (&K EQ 0).COMLEN
         AIF   ('&STR'(1,1) NE '''').COMLEN
&I       SETA  2
&J       SETA  &K-2
&K       SETA  &J
.LOOP    AIF   ('&STR'(&I,2) EQ '''''').SK2
         AIF   ('&STR'(&I,2) EQ '&&').SK2
&I       SETA  &I+1
         AGO   .INC
.SK2     ANOP  ,
&I       SETA  &I+2
&K       SETA  &K-1
.INC     AIF   (&I LE &J).LOOP
.COMLEN  AIF   (NOT &VCON@OP).NOPEN
         MNOTE 4,'PRIOR BCON/VCON NOT TERMINATED'
&VCON@OP SETB  0
.NOPEN   AIF   (T'&END NE 'O').OPEN
         AIF   (&K EQ 0).REQSTR
         AIF   ('&STR'(1,1) EQ '''').QSTR
&NM      DC    AL1(&K),C'&STR'
         AGO   .MEND
.QSTR    ANOP  ,
&NM      DC    AL1(&K),C&STR
         AGO   .MEND
.OPEN    AIF   (&K NE 0).OPSTR
&NM      DC    AL1(&END-*-1)
         AGO   .SETOPEN
.OPSTR   AIF   ('&STR'(1,1) EQ '''').OQSTR
&NM      DC    AL1(&END-*-1),C'&STR'
         AGO   .SETOPEN
.OQSTR   ANOP  ,
&NM      DC    AL1(&END-*-1),C&STR
.SETOPEN ANOP  ,
&VCON@NM SETC  '&END'
&VCON@OP SETB  1
         MEXIT ,
.REQSTR  MNOTE 4,'TEXT STRING REQUIRED'
         MEXIT ,
.CLOSE   AIF   (&VCON@OP).WASOPEN
         MNOTE 4,'BCON/VCON END OUT OF SEQUENCE'
.WASOPEN AIF   ('&NM' EQ '' OR '&NM' EQ '&VCON@NM').BLAB
&NM      EQU   *
.BLAB    ANOP  ,
&VCON@NM EQU   *
&VCON@NM SETC  ''
&VCON@OP SETB  0
.MEND    MEND  ,
