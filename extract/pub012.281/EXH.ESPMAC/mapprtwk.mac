         MACRO ,                                        ADDED ON 81201
&NM      MAPPRTWK &PREFIX=PW,&WIDTH=    PRINTER WORK AREA MAPPING
         LCLC  &DS
         LCLC  &P            SHORT PREFIX
&P       SETC  '&PREFIX'
&DS      SETC  '&P'.'WORK'
         AIF   ('&NM' EQ '').NONAME
&DS      SETC  '&NM'
.NONAME  ANOP  ,
&DS      DSECT ,             MAPPING OF PRINT DCB AND WORK AREA
&P.LINK   DS   F             LINK TO NEXT GETMAINED AREA
&P.ID     DS   C'PRT-'       ID OF PRINT FILE 'N'
&P.SPLEN  DS   F             SUBPOOL/LENGTH OF THIS AREA
&P.TCB    DS   A             ADDRESS OF OWNING TCB
&P.DCB@   DS   X'BF',AL3(&P.DCB)  DCB POINTER
&P.PU@    DS   A             ADDRESS OF USER'S OPEN WORK AREA
&P.TRAN   DC   F'0'          ADDRESS OF TRANSLATE TABLE OR 0
&P.DDNAM  DS   CL8           DDNAME
&P.UCS    DS   CL4           UCS NAME
&P.FCB    DS   CL4           FCB NAME
&P.LSTWRK DS   0XL14         WORK AREA FOR EACH FD ENTRY
&P.LSTLEN DS   A             FIELD LENGTH
&P.LSTLTX DS   A             ITEM LENGTH
&P.LSTADD DS   A             ITEM ADDRESS
&P.LSTDAT DS   X             DATA TYPE
&P.CUROPT DS   X             CURRENT PROCESSING OPTIONS
&P.PREOPT DS   X             PRIOR OPTIONS
&P.LSTEDT DS   X             EDIT OPTIONS
&P.BUFNXT DS   A             ADDRESS OF NEXT BYTE
&P.SCRCUR DS   H             BYTES USED IN CURRENT LINE
&P.FLAG   DS   X             PROCESSING FLAG
&P.FPUN   EQU  X'80' =PUPUNCH   THIS IS A PUNCH FILE
&P.FAKE   EQU  X'40'         IN-STORAGE ACCESS METHOD            83275
&P.FCON   EQU  X'20'         OUTPUT ROUTED TO CONSOLE
&P.FHEAD  EQU  X'10'         TITLE PRINTED ON THIS PAGE
&P.FPRINT EQU  X'08'         BUFFER CONTAINS TEXT
&P.FPXLST EQU  X'04' =PUPGXLST   EXIT LIST PRESENT               84169
&P.FUPAGE EQU  X'02' =PUPUPAGE   USER MAINTAINS PAGE #S          84169
&P.FGPAGE EQU  X'01' =PUPGPAGE   USER PAGE # FEED-BACK OPTION
&P.FLG2  DS    X             PROCESSING FLAGS                    90309
&P.F3800 EQU   X'80'           IBM 3800 LASER MODE (?)           90309
&P.F4050 EQU   X'40'           XEROX 4050/9700/2700 ...(?)       90309
&P.FNUST EQU   X'10'           EJECT TO NEW SHEET                90309
&P.FOVER EQU   X'02'           REPLACE BOLD(OVERPRINT) BY FONT+1 90309
&P.FOPTJ EQU   X'01'           INSERT FONT CONTROL CHARACTERS    90309
&P.FONT# DS    X             CURRENT FONT NUMBER                 90309
&P.FONTB DS    XL2           FONT # PUSHDOWN STACK               90309
&P.CHARS DS    4CL4          SAVED CHARACTERS/XEROX SETUP/PAPER  90309
&P.FONT$ DS    8Y            CHARACTERS/LINE PER FONT (0-7)      90309
&P.EXTEN DS    2A            SUBPOOL/LENGTH/ADDRESS OF EXTENSION 90309
&P.SOUTFM DC   CL8' '        SYSOUT FORM NAME (BLANKS IF N/A)    91001
&P.SOUTWT DC   CL8' '        SYSOUT SPECIAL WRITER NAME          91001
&P.FILL   DC   AL1(0)        UNPRINTABLE CHARACTER REPLACEMENT
&P.SOUTCL DC   C' '          SYSOUT CLASS (OR MSGCLASS IF *)     91001
          DC   AL1(0,0)         RESERVED
&P.DCB    DCB  DDNAME=ANY,DSORG=PS,MACRF=PM,EROPT=ACC,                 *
               RECFM=VBSA,LRECL=137,EXLST=&P.EXLIST
&P.EXLIST DC   0A(0),X'87',AL3(0),A(0,0) +JFCB+@DCBEXIT
&P.TOPPG  DC   F'0'          EDIT LEN/ADDRESS FOR PAGE NUMBER
&P.TOPPS  DC   F'0'             DITTO FOR SUB-PAGE
&P.TOPDT  DC   F'0'             DITTO FOR DATE
&P.TOPTM  DC   F'0'             AND TIME
&P.BOTPG  DC   F'0'          EDIT LEN/ADDRESS FOR PAGE NUMBER
&P.BOTPS  DC   F'0'             DITTO FOR SUB-PAGE
&P.BOTDT  DC   F'0'             DITTO FOR DATE
&P.BOTTM  DC   F'0'             AND TIME
&P.CURLN  DC   H'-1'
&P.MAXLN  DC   H'60'         MAXIMUM LINES PER PAGE (INCL. TITLES)
&P.WIDTH  DC   H'0'          DATA WIDTH FOR TRUNCATION
&P.TIT#   DC   H'0'          NUMBER OF TITLE LINES
&P.FOOT#  DC   H'0'          NUMBER OF FOOTER LINES
&P.PAGE   DC   H'0'          CURRENT PAGE NUMBER
&P.SPAGE  DC   H'0'          PAGE SINCE LAST TITLE CHANGE
&P.TRTAB  DC   256AL1(0)     TRANSLATE TABLE
&P.CCSAV  DC   C' '          SAVE PWRECCC OVER PUT
&P.BUFSP  DC   AL1(0)        SUB-POOL OF GETPOOL REQUEST         83275
&P.VCON   DC   F'0'          V-FORMAT RECORD HEADER
&P.RECCC  DC   C' '          CARRIAGE CONTROL
         AIF   ('&WIDTH' NE '').HAVEW
&P.REC    EQU  *             DATA RECORD
         AGO   .MEND         TRUNCATE THE MAPPING
.HAVEW   ANOP  ,
&P.REC    DC   CL(&WIDTH+1)' '   TEXT RECORD                     90309
         DS    0F
&P.MCREC  DC   CL((&WIDTH+6+3)/4*4)' '   MACHINE CONTROL SAVE RECORD
&P.MCC    EQU  &P.MCREC+4,1,C'C'        CARRIAGE CONTROL
         SPACE 1
         DS    0F
&P.SIZE  EQU   *-&DS         LENGTH OF FIXED AREA
&P.ULOP   DC   CL((&WIDTH+6+3)/4*4)' '   UNDERLINE/OVERPRINT RCD
         DS    0F
&P.TIT1   DC   CL((&WIDTH+6+3)/4*4)' '   1-14 TITLE/FOOTER RECORDS
.MEND    MEND  ,
