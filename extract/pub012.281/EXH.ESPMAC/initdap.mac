         MACRO
&L     INITDAP &DUMMY,&NAME=DAP,&PAD=,&RESERVE=,&SIZE=,                *
               &REG=R2,&LOADADD=R7,&MODE=32K,&MIN=12*1024,&MAX=62*1024
&L       LDSUB NAME=&NAME,PAD=&PAD,RESERVE=&RESERVE,SIZE=&SIZE,        *
               REG=&REG,LOADADD=&LOADADD,MODE=&MODE,MIN=&MIN,MAX=&MAX
         USING IOWORK,&LOADADD         DECLARE IT
*              MACRO MAPIOWK NEEDED FOR EXHCSDAP MAPPING
         MVI   DRFLAG,0                RESET FLAG BYTE
         TM    OPTS,DEBUG              RUNNING IN DEBUG MODE ?
         BZ    DRDBG2                  NO, LEAVE AS IS
         MVI   DRFLAG,DRDEBUG          PROPAGATE DEBUG BIT
DRDBG2   XC    DRTTR(8),DRTTR          CLEAR TTR FIELDS
         MVC   DRDSN(L'DRDSN+L'DRVOL+L'DRMEM),BLANKS    CLEAR NAMES
         SLR   &REG,&REG                                         92250
         ICM   &REG,7,EXCINVOK+EXHFSIZ-EXHBWENT                  92250
         SH    &REG,=AL2(DRBUF-XIOWORK)                          92250
         AIF   ('&MODE' NE '32K').NOT32K                         92250
.MASK32K MIN   &REG,=F'32767'                                    92250
         AGO   .COMMODE                                          92250
.NOT32K  AIF   ('&MODE' NE '64K' AND '&MODE' NE '65K').COMMODE   92250
         MIN   &REG,=F'65535'                                    92250
.COMMODE STH   &REG,DRMAXMSZ           SET ASSEMBLED MAXIMUM SIZE
         STH   &REG,DRMAXL             SET ACTUAL SIZE AVAILABLE
         STH   &REG,DRLEN              MAX RECORD SIZE
         MVC   DRNAME,INVOKE+5         SAVE VERB USED TO INVOKE THIS
         MVC   DRECBC,CANECBB          COPY CANCEL ECB ADDRESS
         XC    DRNCCH,DRNCCH           CLEAR CCHH FIELDS
         MEND
