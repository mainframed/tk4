         MACRO ,
&NM      PARSE &TEXT,&LEN,&OPT=COMMA,&PARM=PARPARM
         GBLC  &MACPLAB
         LCLC  &L
         LCLC  &ON(10),&OV(10),&O1,&O2
         LCLC  &RN(10),&RV(10),&RQ
         LCLA  &OM,&RM,&I,&J,&N
&L       SETC  'L'''
&MACPLAB SETC  '&NM'
&ON(01)  SETC  'WORD'        REXX WORD LIST (BLANK SEPARATORS)
&OV(01)  SETC  'PAR$TPBK'      BLANK SEPARATION
&ON(02)  SETC  'COMMA'       COMMA/BLANK SEPARATED LIST
&OV(02)  SETC  'PAR$TPBC'      BLANK, COMMA
&ON(03)  SETC  'KEYWORD'     COMMA/BLANK/KEYWORD=
&OV(03)  SETC  'PAR$TPKW'      BLANK, COMMA, (OPT.PARENTHESES), EQUAL
&ON(04)  SETC  'LIST'        COMMA/BLANK/PARENTHESIZED LISTS
&OV(04)  SETC  'PAR$TPLS'      BLANK, COMMA, PARENTHESES
&ON(05)  SETC  'ITEM'        COMMA/BLANK/PARENTHESIZED LISTS
&OV(05)  SETC  'PAR$TPLI'      BLANK, COMMA, PARENTHESES (SEPARATED)
&OM      SETA  5             CURRENT MAXIMUM
&ON(06)  SETC  '        '    RESERVED
&OV(06)  SETC  'PAR$TPBK'      RESERVED
.*
&RN(01)  SETC  'PARKEY'
&RV(01)  SETC  'PAR$PARK'
&RN(02)  SETC  'SUBLIST'
&RV(02)  SETC  'PAR$COSP'
&RN(03)  SETC  'COLKEY'
&RV(03)  SETC  'PAR$COKW'
&RN(04)  SETC  'UNQUOTE'
&RV(04)  SETC  'PAR$COUQ'
&RM      SETA  4             CURRENT MAXIMUM
&RN(05)  SETC  '        '
&RV(05)  SETC  '        '
&N       SETA  N'&OPT
         AIF   ('&TEXT' EQ '*END').DONE
         AIF   ('&OPT' NE 'CLOSE').TEXT
.DONE    MACPARM R1,(R1),OPR=SR,MODE=EQU
         AGO   .CALL
.TEXT    MACPARM R15,&TEXT
         MACPARM R0,&LEN,NULL=&L&TEXT
         MACPARM R1,&PARM
         MACPARM R15,R0,PAR@TEXT-PARPARM(R1),MODE=THREE,OP=STM
         AIF   (&N LT 1).CALL
&O1      SETC  '&OPT(1)'
         AIF   ('&O1' EQ '').SEESUB
&I       SETA  1
.TRYOPT  AIF   ('&O1' EQ '&ON(&I)').HAVEOPT
&I       SETA  &I+1
         AIF   (&I LE &OM).TRYOPT
.BADOPT  MNOTE 4,'PARSE: OPTION &O1 NOT RECOGNIZED; IGNORED'
         AGO   .CALL
.HAVEOPT MACPARM PAR$TYPE-PARPARM(R1),&OV(&I),OP=MVI  SET CALL TYPE
.SEESUB  AIF   (&N LT 2).CALL
&J       SETA  1
.OPLOOP  AIF   (&J GE &N).EXPSUB
&J       SETA  &J+1
&O1      SETC  '&OPT(&J)'
         AIF   ('&O1' EQ '').OPLOOP
&I       SETA  1
.TRYSUB  AIF   ('&O1' EQ '&RN(&I)').HAVESUB
&I       SETA  &I+1
         AIF   (&I LE &RM).TRYSUB
         MNOTE 8,'PARSE: OPT &O1 NOT A VALID OPTION'
         AGO   .OPLOOP
.HAVESUB ANOP  ,
&RQ      SETC  '&RQ'.'+'.'&RV(&I)'
         AGO   .OPLOOP
.EXPSUB  AIF   ('&RQ' EQ '').CALL  SKIP ALL NULLS
         MACPARM PAR$RQFG-PARPARM(R1),&RQ,OP=MVI  SET SUB OPTION
.CALL    MACPARM R15,@PARSER,OP=L
         MACPARM R14,(R15),OP=BALR,OPR=BALR
         MEND  ,
