         MACRO ,
         MDEFDICT &WORD,&TYPE=C
         COPY  MDEFGBL
         LCLA  &I,&J,&K
         LCLC  &LQ,&CHIX
.*  FIND WORD IN DICTIONARY
.LOOP    AIF   (&I GE &ZZDCTIX).ADDWORD
&I       SETA  &I+1
         AIF   ('&WORD' NE '&ZZDCT(&I)').LOOP
.*  FOUND. RETURN INDEX
&ZZDCTRT SETA &I
         MEXIT ,
.*  NOT FOUND. ERROR IF DICTIONARY FULL; OTHERWISE BUILD ENTRY.
.*  RETURN INDEX IN ZZDCTRT
.ADDWORD AIF   (&ZZDCTIX GE &ZZDICMX).MUCH2
&ZZDCTIX SETA &ZZDCTIX+1
&ZZDCT(&ZZDCTIX) SETC '&WORD'
&ZZDCTRT SETA &ZZDCTIX
MSGDSCT  CSECT ,
&LQ      SETC  'L'''
&CHIX    SETC  '00000'.'&ZZDCTIX'
&J       SETA  K'&CHIX
&CHIX    SETC  '&CHIX'(&J-4,5)
DWD&CHIX DC    AL1(&LQ.DWT&CHIX,&LQ.DWT&CHIX,C'&TYPE')
&K       SETA  K'&WORD
         AIF   (&K LT 4).NOSPEC
         AIF   ('&WORD'(1,1) EQ '''' OR '&WORD'(2,1) NE '''').NOSPEC
DWT&CHIX DC    &WORD         EXPAND SELF-DEFINING TERM
         AGO   .USECT
.NOSPEC  ANOP  ,
DWT&CHIX DC    &TYPE.&WORD
.USECT   ANOP  ,
&ZZSECT  CSECT ,
         MEXIT ,
.MUCH2 MNOTE 12,'DESIGN LIMIT OF &ZZDICMX DICTIONARY ENTRIES EXCEEDED.'
         MEND  ,
