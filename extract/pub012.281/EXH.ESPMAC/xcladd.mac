         MACRO ,
&NM      XCLADD &ADDR,&OPT=FIRST
.*
.*   XCLADD ADDS A CLEANUP ELEMENT TO THE EXWCLEAN CHAIN.
.*   BY DEFAULT IT'S ADDED AT THE FRONT, BUT SETTING OPT TO
.*   SOMETHING OTHER THAN FIRST (DEFAULT) CAUSES THE ELEMENT
.*   TO BE ADDED AT THE END
.*
.*   THE POSITIONAL OPERAND IS THE ADDRESS OF THE ELEMENT TO
.*   BE ADDED.
.*
         LCLC  &ZZ
&ZZ      SETC  'ZZ'.'&SYSNDX'
&NM      STM   R0,R7,12(R13)      SAVE SOME REGISTERS
         MACPARM R15,&ADDR        LOAD ELEMENT ADDRESS
         LA    R15,0(,R15)        CLEAN HIGH
         LTR   R15,R15            ANY SUPPLIED?
         BZ    &ZZ.X                NO; JUST RETURN R15=0
         AIF   ('&OPT' NE 'FIRST').TACKEND
         MVC   0(4,R15),EXWCLEAN  MAKE FORWARD LINK
         ST    R15,EXWCLEAN       MAKE NEW HEAD
.*NEXT   B     &ZZ.X              AND RETURN
.TACKEND LA    R1,EXWCLEAN        POINT TO CHAIN HEAD
&ZZ.L    LR    R2,R1              REMEMBER PREVIOUS ADDRESS
         ICM   R1,15,0(R1)        GET NEXT
         BNZ   &ZZ.L                NOT LAST
         ST    R15,0(,R2)         MAKE NEW END
         XC    0(4,R15),0(R15)    ENSURE PROPER END
&ZZ.X    LM    R0,R7,12(R13)      0 OR ADDRESS IN R15
.MEND    MEND  ,
