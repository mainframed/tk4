         MACRO
&NM      PRTS  &FAD,&DEV=,&TITLE=0,&FOOTER=0,&CC=,&OPT=,&END=0,&MODE=
.*  NOTE THAT OPT= USES THE HIGH BYTE OF R1 - PERMANENT RESTRICTION
         LCLA  &CT,&HF,&J,&K,&L
         AIF   (&TITLE EQ 0 AND &FOOTER EQ 0).NOT
         AIF   (&TITLE LT 16 AND &FOOTER LT 16).OKT
         MNOTE 4,'*** NON-NUMERIC TITLE/FOOTER NOT SUPPORTED'
.OKT     ANOP  ,
&HF      SETA  &TITLE*16+&FOOTER
.NOT     AIF   ('&FAD'(1,1) NE '''').NOTLIT
&K       SETA  K'&FAD-1
&L       SETA  2
&J       SETA  &K-1
&K       SETA  &J
.LOOP    AIF   ('&FAD'(&L,2) EQ '''''').SK2
         AIF   ('&FAD'(&L,2) EQ '&&').SK2
&L       SETA  &L+1
         AGO   .INC
.SK2     ANOP
&L       SETA  &L+2
&K       SETA  &K-1
.INC     AIF   (&L LE &J).LOOP
.*
         AIF   (&K GE 0).COMLEN
&K       SETA  0
.COMLEN  ANOP  ,
&NM      LA    R1,=C&FAD
      PRTCOM PRTS,FUN=11,DEV=&DEV,B1=&HF,A0=&END,A1=(R1),OPT=(&OPT,&CC)
         MEXIT ,
.NOTLIT  ANOP  ,
&NM   PRTCOM PRTS,FUN=11,DEV=&DEV,B1=&HF,A0=&END,A1=&FAD,OPT=(&OPT,&CC)
         MEND
