         MACRO ,
&NM      PARFGSET &BXLE,&TEXT=PRS$TEXT,&ERR=?,&DONE=            GP03229
.*--------------------------------------------------------------------*
.*  PARFGSET IS USED AFTER A PARSE REQUEST FINDS A SELF-DEFINING      *
.*    KEYWORD REPRESENTING A FLAG TO BE TURNED ON OR OFF, AS DEFINED  *
.*    BY A TABLE BUILT WITH PARKEYBX/PARKEYFG MACROS.                 *
.*                                                                    *
.*  USES REGISTERS 14-2                                               *
.*                                                                    *
.*  ERR  is the mandatory target for a non-found return from SUBVERB. *
.*  DONE is an optional label after the flags are set; when omitted,  *
.*       control is returned after the macro.                         *
.*                                                                    *
.*  Note that PARKEYFG definitions specify the bits to be changed;    *
.*   the expansion for OFF flags generates the ones complement of the *
.*   flags suitable for direct NC use.                                *
.*                                                                    *
.*--------------------------------------------------------------------*
         GBLB  &ZZPARFG
         LCLC  &P                                               GP03229
&P       SETC  'ZZP'.'&SYSNDX'                                  GP03229
*---------------------------------------------------------------------*
*  LOOK UP TEXT &TEXT USING BXLE TABLE &BXLE
*---------------------------------------------------------------------*
&NM      MACPARM R1,&BXLE    GET BXLE POINTER FOR PARKEYFG TABLE
         MACPARM R0,&TEXT    VERB TO BE LOCATED
         SUBCALL SUBVERB     (NEEDS EXTRN OR SERVLOAD FOR SUBVERB)
         LTR   R2,R15        DID IT WORK ?
         MACPARM &ERR,OP=BZ,OPR=BZR,MODE=ONE  TAKE ERROR BRANCH
         SPACE 1
*---------------------------------------------------------------------*
*  APPLY FLAG OFF, THEN FLAG ON REQUEST                               *
*---------------------------------------------------------------------*
         SR    R15,R15       GET THE LENGTH
         IC    R15,OFOFLEN(,R2)  LOAD FLAG LENGTH - 1
         EX    R15,EXBTOFF   SET FLAG(S) OFF
         LA    R14,OFOFONN(R15,R2)  POINT TO ON FLAG
         EX    R15,EXBTON    SET FLAGS ON
         MACPARM &DONE,OP=B,OPR=BR,MODE=ONE,NULL=SKIP  KEYWORD PROC
         AIF   (&ZZPARFG).MEND
         AIF   ('&DONE' NE '').NORND                            GP03229
         B     &P.X
.NORND   ANOP  ,                                                GP03229
EXBTOFF  NC    0(*-*,R1),OFOFOFF(R2)  SET FLAGS OFF
EXBTON   OC    0(*-*,R1),0(R14)       SET FLAGS ON
&ZZPARFG SETB  1
         AIF   ('&DONE' NE '').MEND                             GP03229
&P.X     DS    0H
.MEND    MEND  ,
