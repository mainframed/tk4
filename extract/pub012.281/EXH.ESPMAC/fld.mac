         MACRO
&L       FLD   &TYP,&LEN,&LAB,&BLEN,&SEP=1
.*             TYP IS V, H, S, OR T
.*             LEN IS THE DECIMAL LENGTH FOR H AND V,.*
.*                    THE COLUMN NUMBER FOR T,
.*                    AND THE NUMBER OF COLUMNS FOR S.
.*             LAB IS THE LABEL OR LABEL LENGTH FOR H AND V
.*             BLEN IS THE NUMBER OF BLANKS TO ALLOW AFTER THE FIELD
.*             SEP, FOR H ONLY, IS THE NUMBER OF BLANKS BETWEEN
.*                              THE LABEL AND THE FIELD
         GBLA  &PCCLINE,&PCCPOS,&PCCLTYP
         GBLB  &DBMAC
         LCLA  &L2,&L3,&LN
         LCLC  &KLUDGE
         AIF   (NOT &DBMAC).NOSNAP
         MNOTE *,'PCCPOS=&PCCPOS PCCLINE=&PCCLINE'
.NOSNAP  ANOP
         AIF   ('&TYP' EQ 'H').H        IS IT HORIZONTAL FIELD?
         AIF   ('&TYP' EQ 'V').V        IS IT VERTICAL FIELD?
         AIF   ('&TYP' EQ 'T').T        IS IT TAB?
         AIF   ('&TYP' EQ 'S').S        IS IT SPACE?
         MNOTE 8,'1ST OPERAND NOT H, V, S, OR T'
         MEXIT
.*
.*
.*
.S       AIF   ('&LEN'(1,1) EQ '-').SBACK
         AIF   (&PCCPOS+&LEN GT 80).SPACEU
&PCCPOS  SETA  &PCCPOS+&LEN
         MEXIT
.SBACK   ANOP
&KLUDGE  SETC  '&LEN'(2,K'&LEN-1)
         AIF   (NOT &DBMAC).NOSNAP1
         MNOTE *,'KLUDGE=&KLUDGE'
.NOSNAP1 ANOP
         AIF   (&PCCPOS-&KLUDGE LE 0).BACKU
&PCCPOS  SETA  &PCCPOS-&KLUDGE
         MEXIT
.BACKU   MNOTE 8,'BACKSPACE PAST START OF LINE - POS SET TO 1'
&PCCPOS  SETA  1
         MEXIT
.*
.*
.*
.T       AIF   (&LEN GT 80).TABU
&PCCPOS  SETA  &LEN
         MEXIT
.TABU    MNOTE 4,'TAB PAST POSITION 80 TREATED AS LNE NEXT'
.SPACEU  LNE
         MEXIT
.*
.*
.*
.H       ANOP
&PCCLTYP SETA  0
         AGO   .TSTLAB
.*
.*
.*
.V       ANOP
&PCCLTYP SETA  1
.TSTLAB  AIF   (T'&LAB EQ 'N').LABN
         AIF   (T'&LAB EQ 'O').TSTBLEN
&L2      SETA  K'&LAB
         AIF   ('&LAB'(1,1) NE '''').TSTBLEN
&L2      SETA  &L2-2
         AGO   .TSTBLEN
.LABN    ANOP
&L2      SETA  &LAB
.TSTBLEN AIF   (T'&BLEN EQ 'O').NOBLEN
&L3      SETA  &BLEN
.NOBLEN  AIF   ('&TYP' EQ 'V' AND &PCCPOS+&L2 LE 81                    *
               AND &PCCPOS+&LEN LE 81                                  *
               OR &PCCPOS+&L2+&LEN LE 81).NOSKIP
         LNE
.NOSKIP  AIF   ('&TYP' EQ 'H').HORG
&LN      SETA  &L2
         AIF   (&LN GE &LEN).VORG
&LN      SETA  &LEN
.VORG    AIF   (&L2 EQ 0).NOVL
         ORG   BUF+(&PCCLINE-1)*80+&PCCPOS+&LN-&L2-1
L&L      DS    CL&L2
.NOVL    ORG   BUF+&PCCLINE*80+&PCCPOS+&LN-&LEN-1
B&L      DS    CL&LEN
&PCCPOS  SETA  &PCCPOS+&LN+&L3
         MEXIT
.HORG    ORG   BUF+(&PCCLINE-1)*80+&PCCPOS-1
         AIF   (&L2 EQ 0).NOHL
L&L      DS    CL&L2
         AIF   ('&SEP' EQ '' OR '&SEP' EQ '0').NOHL
&L2      SETA  &L2+&SEP
         DS    CL&SEP
.NOHL    AIF   (&LEN EQ 0).NOHB
B&L      DS    CL&LEN
.NOHB    ANOP
&PCCPOS  SETA  &PCCPOS+&L2+&LEN+&L3
         MEND
