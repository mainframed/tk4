         MACRO
         MAPIOWK &SIZE=,&USE=BPAM                               GP12360
.*  REPLACE USE=BPAM WITH USE= FOR NEXT COMPLETE ASSEMBLY       GP12360
         GBLC  &SYSTEM                                           78268
         LCLC  &PAD                                             GP02229
&PAD     SETC  '328'                                            GP02229
         AIF   ('&SIZE(1)' EQ '').DFLTPAD                       GP02229
&PAD     SETC  '&SIZE(1)'                                       GP02229
.DFLTPAD ANOP  ,                                                GP02229
XIOWORK  DS    0D
         USING *,R7
         SPACE 1
DREAD    STM   R9,R13,DRSAVC   SAVE REGISTERS OVER CONVERT CALL
         L     R1,DRTTR      GET REQUESTED TTR
         SL    R1,DRTTOFF     CONVERT TO RELATIVE
         ST    R1,DRTTRW     STASH FOR CONVERT
         LM    R15,R2,DRCNVT   FOR TTR CONVERT
         BASR  R14,R15       INVOKE TTR TO MBBCCHHR CONVERSION
         LM    R9,R13,DRSAVC   RESTORE REGS
DREAD1   XC    DRECB,DRECB   ZERO I/O ECB
         L     R1,32(,R1)    GET UCB ADDRESS
         TM    6(R1),X'40'   DEVICE IN NOT READY STATUS ?
         BOR   R9            YES- SIGNAL ERROR
         CLI   DRVOL,C' '    WAS REQUEST WITH VOL-SER ?
         BNH   DREADON       NO, CONTINUE
         CLC   DRVOL,28(R1)  IS VOLUME STILL MOUNTED ?
         BNER  R9            NO - SIGNAL ERROR
DREADON  LA    R1,DRECB+4    GET IOB ADDRESS
         EXCP  (1)           READ
         WAIT  ECBLIST=DRECBLST   WAIT FOR I/O COMPLETION OR CANCEL
         CLI   DRECB,X'7F'   NORMAL COMPLETION ?
         BNE   DRIBAD        NO, CHECK FURTHER
         MVC   DRMBB+3(5),DRNCCH   GET CCHHR OF NEXT RECORD
         STM   R9,R13,DRSAVC   SAVE
         LM    R1,R2,DRDEB   GET DEB AND MBBCCHHR ADDRESS
         L     R15,DRPRLTV   GET MBBCCHHR TO TTR CONVERSION ROUTINE
         BASR  R14,R15       CONVERT BACK TO TTR FORMAT
         ST    R0,DRTTRW     STASH ACTUAL TTR
         AL    R0,DRTTOFF    CONVERT TO RELATIVE TTR OFFSET
         ST    R0,DRTTR      SET TTR FOR NEXT TIME
         LM    R9,R13,DRSAVC   GET REGS BACK
         B     8(,R9)        TAKE NORMAL RETURN
DRIBAD   TM    DRECB+16,1    WAS THIS AN EOF ?
         BNZ   4(,R9)        YES; EXIT WITH EOF INDICATION
         TM    DRSLI,X'40'   SINGLE CCW ?
         BZR   R9            YES, TAKE I/O ERROR EXIT
         CLI   DRECB,X'42'   END OF EXTENT ERROR ?
         BNER  R9            NO, REPORT ERROR
         NI    DRSLI,255-X'40'   TURN OFF CHAINING
         B     DREAD         AND RETRY THE I/O
         SPACE 1
DRLINE   AH    R10,LSIZE     POSITION TO NEXT SCREEN LINE        88249
         C     R10,MSGAD     ROOM FOR ANOTHER LINE ?
         BL    4(,R4)        YES, RETURN FOR MORE STUFF
         SPACE 1
DRPAGE   TM    DRFLAG,DREND  LAST WRITE REQUEST ?
         BO    DRXIT         YES, EXIT AFTER RITE
         XPAGE EXMORE,L'EXMORE,ERR=DRWERR
         XWAIT END=DRWAIT
         XREAD ERR=DRRERR
         B     DRMORE-4      ENTER ONLY - SET FLAG
         CLC   DRDCPRIN,REPLY+L'REPLY-80   IS IT A 'PRINT' REQUEST ?
         BE    DRMORE+4      YES - DON'T BLANK SCREEN BUFFER
         B     DRMORE        ELSE CLEAR SCREEN
         MVI   REPLY,X'FF'     SIGNAL 'ENTER' ONLY
DRMORE   BAL   R14,BLANKER   CLEAR BUFFER
         L     R10,BUFAD     SET TO FIRST LINE
         BR    R4            AND RETURN WITH NEW PAGE INDICATION
         SPACE 1
DRXIN    LA    R9,EXCIN      GO TO ANALYZE INPUT                GP10213
         B     DRFREE        AFTER FREEING THINGS UP            GP10213
         SPACE 1
DRWERR   LA    R9,EXCWERR    WRITE ERROR                        GP10213
         B     DRFREE        AFTER FREEING THINGS UP            GP10213
         SPACE 1
DRRERR   LA    R9,EXCRERR    READ  ERROR                        GP10213
         B     DRFREE        AFTER FREEING THINGS UP            GP10213
         SPACE 1
DRWAIT   LA    R9,EXCEXIT    WAIT EXPIRED                       GP10213
         B     DRFREE        AFTER FREEING THINGS UP            GP10213
         SPACE 1
DRXIT    LA    R9,EXCYES     COMMAND PROMPT                     GP10213
*NEXT*   B     DRFREE        AFTER FREEING THINGS UP            GP10213
         SPACE 1
DRFREE   TM    DRFLAG,DRDEBUG   RUNNING DEBUG MODE ?
         BO    DRFDEB        YES, JUST CLOSE DCB
         ICM   R1,15,DRDEBA  LOAD AND TEST DEB ADDRESS          GP03091
         BZR   R9            ALREADY FREED OR NOT OPENED        GP03091
         L     R1,DRDEB      GET DEBBASIC                       GP04234
         L     R15,0(,R1)    LOAD DEBTCB (HIGH BYTE 0)          GP03091
         LA    R15,4(,R15)   POINT TO TCBDEB-4                  GP03091
DRDEBLP  CLM   R1,7,1+4(R15)  POINTS TO MY DEB ?                GP03091
         BE    DRUNCHN       YES; UNCHAIN AND FREE              GP03091
         ICM   R15,7,1+4(R15)  END OF CHAIN ?                   GP03091
         BNZ   DRDEBLP       NO; CHECK NEXT ENTRY               GP03091
         B     DRUNNON       JUST FREE STORAGE                  GP03091
DRUNCHN  MVC   5(3,R15),5(R1)  UNCHAIN MY ENTRY                 GP03091
DRUNNON  LM    R0,R1,DRDEBL  LOAD SUBPOOL, LENGTH, AND DEBPFX   GP04234
         SVC   10            FREE DEB STORAGE                   GP03091
         XC    DRDEBA,DRDEBA  SIGNAL FREED                      GP03091
         BR    R9            RETURN
DRFDEB   CLOSE (DRDCB)       IN DEBUG MODE CLOSE DCB ONLY
         BR    R9
         EJECT ,
         XDAP  DRECB,RI,DRDCB,DRBUF,MF=L
         ORG   *-4
DRSLI    DC    X'40'         COMM. CHAIN, SLI MAY GET SET LATER
         DC    X'00'
DRLEN    DC    AL2(DRBUFN-DRBUF)  REQUESTED I/O LENGTH
         CCW   X'92',DRNCCH,0,8   READ COUNT OF NEXT RECORD
DRNCCH   DC    XL8'0'        CCHHR OF NEXT RECORD
         ORG
DRCSW    EQU   DRECB+13,7    CHANNEL STATUS WORD                GP14240
DR@CCW   EQU   DRECB+20,4    ADDRESS OF FIRST CCW               GP14240
DRMBB    EQU   DRECB+36,8    MBBCCHHR FIELD IN IOB
DRCCW1   EQU   DRECB+44,8    FIRST CCW                          GP14240
DRCCW2   EQU   DRECB+52,8    SECOND CCW                         GP14240
DRCCW3   EQU   DRECB+60,8    THIRD CCW                          GP14240
DRCCW4   EQU   DRECB+68,8    FOURTH CCW                         GP14240
DRCCW5   EQU   DRECB+76,8    FIFTH CCW                          GP14240
DRDCB    DCB   DDNAME=DUMMY,DEVD=DA,DSORG=PS,MACRF=(E),EXLST=DREXLST,  *
               EODAD=DRIBAD,RECFM=U
         ORG   DRDCB+12
         DC    A(DRDVTBL)    DEVICE TABLE ADDRESS
         AIF   ('&USE' NE 'BPAM').MOST                          GP12360
         ORG   DRDCB+88      ROOM FOR BPAM DCB                  GP10203
.MOST    ORG   ,
DRECBLST DC    A(DRECB)      I/O ECB  ADDR.
DRECBC   DC    A(0)          CANCEL ECB ADDR WITH HIGH X'80'
DREXLST  DC    0A(0),X'87',AL3(DRBUF)    BUFFER DOUBLES AS JFCB
DRSAVC   DC    5A(0)         SAVE AREA FOR TTR CONVERT
DRPRLTV  DC    A(0)          ADDRESS OF MBBCCHHR TO TTR CONV. ROUTINE
DRTTR    DC    F'0'          RELATIVE TTR FOR I/O
DRTTOFF  DC    F'0'          CONVERSION FROM HASP ABS. TO RELATIVE TTR
DRCNVT   DC    A(0) CONVERT PARM:  ADDR OF CONVERT ROUTINE (15)
DRTTRW   DC    F'0'          ACTUAL TTR FOR I/O
DRDEB    DC    A(0)          DEB ADDR. (1)
         DC    A(DRMBB)      MBBCCHHR ADDR (2)
         AIF   ('&SYSTEM' NE 'MVS').SP233                        82158
DRDEBL   DC    0A(0),X'E6',AL3(0)    DEB SUBPOOL AND LENGTH      82158
         AGO   .COMPOOL                                          82158
.SP233   AIF   ('&SYSTEM' NE 'VS1').SP243                        82158
DRDEBL   DC    0A(0),X'E9',AL3(0)    DEB SUBPOOL AND LENGTH      78268
         AGO   .COMPOOL                                          78268
.SP243   ANOP  ,                                                 78268
DRDEBL   DC    0A(0),X'F3',AL3(0)    DEB SUBPOOL AND LENGTH
.COMPOOL ANOP  ,                                                 78268
DRDEBA   DC    A(0)          DEB GETMAIN ADDRESS
DRDSN    DC    CL44' '       DSNAME
DRVOL    DC    CL6' '        VOL-SER
DRMEM    DC    CL8' '        MEMBER NAME FOR SELECTIVE RETRIEVAL
DRMAXL   DC    AL2(DRBUFN-DRBUF)  MAXIMUM LENGTH AVAILABLE
DRMAXMSZ DC    AL2(DRBUFN-DRBUF) .   ASSEMBLED MAXIMUM SIZE AVAILABLE
DRMAXR   DC    AL1(255,0)    MAX RECORD NUMBER ON A TRACK
DRFLAG   DC    X'0'          PROCESSING FLAGS
DRFIRST  EQU   X'01'         FIRST TIME FLAG USED FOR SYNTAX CHECKING
DRSECP   EQU   X'02'         SECOND POS. PARM RECEIVED
DRFASP   EQU   X'04'         TTR SUPPLIED IN ABSOLUTE DISPL. (HASP)
DRFABS   EQU   X'08'         CCHH OPTION USED, VALUE IN DRNCCH
DRFPDS   EQU   X'10'         DATASET IS A PDS
DRFSLOG  EQU   X'20'         LOG DS / ONE DEB - TWO DATASETS
DRDEBUG  EQU   X'40'         RUNNING IN DEBUG MODE IF ON
DREND    EQU   X'80'         END FILE ON INPUT, OTHER TERMINATION COND.
DRNAME   DC    CL3' '        NAME OF I/O FORMATTING ROUTINE
P1       DC    P'1'          FOR PACKED ADD
DRDCPRIN DC    C'PRI'        COMMAND VERB FOR PRINT FUNCTION SUPPORT
         DS    0H
DRDVTBL  DC    XL12'0'       DEVICE TABLE
DRTKCYL  EQU   DRDVTBL+2     NO. OF TRACKS / CYLINDER
DRMODFG  DC    X'00'         COMMAND MODIFIER FLAGS              81322
DRMODTXT EQU   X'01'           POSITION TO LOAD-MODULE TEXT      81322
*                            OTHER BITS AS IN MODEFG (EXHM@ZAP)  81322
DRMRECFM DC    X'00'         DSCB1 RECFM                         83178
DRMLRECL DC    XL2'00'       DSCB1 LRECL (MAY BE 0)              88258
         SPACE 2
DRBUF    DS    0D            I/O BUFFER, ALSO LOCATE/OBTAIN WORK AREA
         ORG   DRBUF+&PAD-1  FORCE THE MINIMUM                  GP02229
         DC    X'00'         NECESSARY MODULE SIZE              GP02257
DRBUFN   EQU   *
         ORG
         MEND
