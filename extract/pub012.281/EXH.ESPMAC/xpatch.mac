         MACRO
&NM      XPATCH &SIZE=       RENAMED FROM PATCH  2004-12-2      GP04234
         GBLC  &MODULE,&SYSTEM                                   78255
         LCLC  &PATCH,&PATL1,&PATL2,&LIZE                        78258
         LCLA  &S20,&S21
.*
.*        THIS MACRO CREATES 'ZAP' SPACE AT THE END OF A MODULE
.*       THE SIZE IS SPECIFIED AS A KEYWORD PARAMETER, DEFAULT MODULE
.*       SIZE IS 2K.   THE INVOLVED SET OF EQUATES IS USED TO GENERATE
.*       NO MORE THAN THE ZAP SPACE CORRESPONDING TO THE ABSOLUTE
.*       DIFFERENCE BETWEEN THE ACTUAL AND THE INTENDED MODULE LENGTH;
.*       THUS AVOIDING A LONG LOOP IN THE ASSEMBLER DUE TO A NEGATIVE
.*       LENGTH VALUE - THIS CODE WAS COPIED FROM THE XCTLTABL MACRO
.*
&PATCH   SETC  'PATCH'.'&MODULE'(6,3)
         AIF   ('&NM' EQ '').OK
&PATCH   SETC  '&NM'(1,5).'&PATCH'(6,3)
.OK      ANOP
&LIZE    SETC  '&SIZE'       COPY                                78255
         AIF   ('&LIZE' NE '').HAVEIT                            78255
&LIZE    SETC  '2048'                                            78255
         AIF   ('&SYSTEM' EQ 'MFT' OR '&SYSTEM' EQ 'MVT'  OR           *
               '&SYSTEM' EQ 'VS1').HAVEIT                        78255
&LIZE    SETC  '4096'                                            78255
.HAVEIT  ANOP  ,                                                 78255
&S20     SETA  2*(&LIZE)
&S21     SETA  &S20+1
&PATL1   SETC  '&PATCH'(1,3).'L1'.'&PATCH'(6,3)
&PATL2   SETC  '&PATCH'(1,3).'L2'.'&PATCH'(6,3)
&MODULE  CSECT ,  MAKE SURE WE'RE IN THE CORRECT CONTROL SECTION
         LTORG
&PATCH   DS    0F .          END OF MODULE CODE
&PATL1   EQU   *-&MODULE .     LENGTH OF USED LENGTH
&PATL2   EQU   ((&S21-2*&PATL1)/(&S20-2*&PATL1)*((&LIZE)-&PATL1))/4
         DC    (&PATL2)C'ZAPS' .   ZAP SPACE
         MEND
