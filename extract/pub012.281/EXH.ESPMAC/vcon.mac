         MACRO ,
&NM      VCON  &STR,&END=,&BNDRY=H                      ADDED ON 81155
         GBLB  &VCON@OP
         GBLC  &VCON@NM
         LCLA  &I,&J,&K,&L
         LCLC  &L2
.**********************************************************************
.**                                                                  **
.**  VCON BUILDS A TEXT MESSAGE BEGINNING WITH A TWO-BYTE LENGTH,    **
.**  TWO BYTES OF ZERO, AND TEXT OF THAT LENGTH (WTO / RECFM=V FMT)  **
.**                                                                  **
.**  USE   VCON  'TEXT'                                              **
.**                                                                  **
.**  OR    VCON  'TEXT1',END=LABEL                                   **
.**        DC     ...ZERO OR MORE STORAGE ITEMS                      **
.**  LABEL VCON   *END    TO GENERATE A SINGLE MESSAGE               **
.**                                                                  **
.**********************************************************************
&K       SETA  K'&STR
         AIF   (T'&END NE 'O').TSTOPEN
         AIF   (T'&STR EQ 'O').CLOSE
         AIF   ('&STR'(1,1) EQ '*').CLOSE
.TSTOPEN AIF   (&K EQ 0).COMLEN
         AIF   ('&STR'(1,1) NE '''').COMLEN
&I       SETA  2
&J       SETA  &K-2
&K       SETA  &J
.LOOP    AIF   ('&STR'(&I,2) EQ '''''').SK2
         AIF   ('&STR'(&I,2) EQ '&&').SK2
&I       SETA  &I+1
         AGO   .INC
.SK2     ANOP  ,
&I       SETA  &I+2
&K       SETA  &K-1
.INC     AIF   (&I LE &J).LOOP
.COMLEN  AIF   (NOT &VCON@OP).NOPEN
         MNOTE 4,'PRIOR VCON NOT TERMINATED'
&VCON@OP SETB  0
.NOPEN   AIF   ('&BNDRY' EQ 'H' OR '&BNDRY' EQ 'Y').NOBOUND
         AIF   ('&BNDRY' NE 'X' AND '&BNDRY' NE 'C').DOBOUND
&L2      SETC  'L2'
         AGO   .NOBOUND
.DOBOUND DS    0&BNDRY
.NOBOUND AIF   (T'&END NE 'O').OPEN
         AIF   (&K EQ 0).REQSTR
         AIF   ('&STR'(1,1) EQ '''').QSTR
&NM      DC    Y&L2.(&K+4,0),C'&STR'
         AGO   .MEND
.QSTR    ANOP  ,
&NM      DC    Y&L2.(&K+4,0),C&STR
         AGO   .MEND
.OPEN    AIF   (&K NE 0).OPSTR
&NM      DC    Y&L2.(&END-*,0)
         AGO   .SETOPEN
.OPSTR   AIF   ('&STR'(1,1) EQ '''').OQSTR
&NM      DC    Y&L2.(&END-*,0),C'&STR'
         AGO   .SETOPEN
.OQSTR   ANOP  ,
&NM      DC    Y&L2.(&END-*,0),C&STR
.SETOPEN ANOP  ,
&VCON@NM SETC  '&END'
&VCON@OP SETB  1
         MEXIT ,
.REQSTR  MNOTE 4,'TEXT STRING REQUIRED'
         MEXIT ,
.CLOSE   AIF   (&VCON@OP).WASOPEN
         MNOTE 4,'VCON END OUT OF SEQUENCE'
.WASOPEN AIF   ('&NM' EQ '' OR '&NM' EQ '&VCON@NM').BLAB
&NM      EQU   *
.BLAB    ANOP  ,
&VCON@NM EQU   *
&VCON@NM SETC  ''
&VCON@OP SETB  0
.MEND    MEND  ,
