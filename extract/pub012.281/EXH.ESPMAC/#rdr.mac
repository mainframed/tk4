         MACRO ,
&NM      #RDR  &AD,&TYPE=GET,&DEV=0,&OPT=
         GBLB  &ZZ#RDRF
         GBLC  &MACPLAB,&ZZ#RDRN(10),&ZZ#RDRI(10)
.**********************************************************************
.*                                                                    *
.*   #RDR GENERATES CALLS TO SUBROUTINE SUBRDR FOR READING UP TO      *
.*   EIGHT OPEN INPUT STREAMS (DEV=1 OR DEV=(3,7)...                  *
.*                                                                    *
.*   INPUT DDs ARE ASSOCIATED WITH #RDRWRK MACROS                     *
.*                                                                    *
.**********************************************************************
         LCLA  &I,&J,&K,&N
         LCLB  &B0,&B1,&B2,&B3,&B4,&B5,&B6,&B7
         LCLB  &F0,&F1,&F2,&F3,&F4,&F5,&F6,&F7
         LCLC  &PT,&PD,&M,&DV,&FN,&C
&MACPLAB SETC  '&NM'
&M       SETC  '&SYSNDX'
&DV      SETC  '&DEV'
&FN      SETC  '0'
         AIF   (T'&DEV NE 'O').DEVNUMT
         AIF   ('&TYPE' NE 'CLOSE').DEVNUMT
&DV      SETC  '255'         CLOSE ALL
         AGO   .DEVASIS
.DEVNUMT AIF   (T'&DEV EQ 'N').DEVASIS
         AIF   (N'&DEV EQ 1).DEVASIS
&N       SETA  0
.DEVLOOP AIF   (&J GE N'&DEV).DEVDONE
&J       SETA  &J+1
&C       SETC  '&DEV(&J)'
         AIF   ('&C' EQ '').DEVLOOP
         AIF   ('&C' GE '1' OR '&C' LE '8').DEVOK
         MNOTE 8,'#RDR: DEV=&C UNACCEPTABLE'
         AGO   .DEVLOOP
.DEVOK   ANOP  ,
&N       SETA  &N+1
&B0      SETB  (&B0 OR ('&C' EQ '8'))
&B1      SETB  (&B1 OR ('&C' EQ '7'))
&B2      SETB  (&B2 OR ('&C' EQ '6'))
&B3      SETB  (&B3 OR ('&C' EQ '5'))
&B4      SETB  (&B4 OR ('&C' EQ '4'))
&B5      SETB  (&B5 OR ('&C' EQ '3'))
&B6      SETB  (&B6 OR ('&C' EQ '2'))
&B7      SETB  (&B7 OR ('&C' EQ '1'))
         AGO   .DEVLOOP
.*
.DEVDONE AIF   (&N EQ (&B0+&B1+&B2+&B3+&B4+&B5+&B6+&B7)).DOVDON
         MNOTE 4,'#RDR: ERROR IN DEV= PARAMETER'
.DOVDON  ANOP  ,
&N       SETA  (128*&B0+64*&B1+32*&B2+16*&B3+8*&B4+4*&B5+2*&B6+&B7)
&DV      SETC  '&N'
.DEVASIS AIF   (&ZZ#RDRF).NOT1ST
&ZZ#RDRN(01) SETC 'GET'
&ZZ#RDRI(01) SETC '0'
&ZZ#RDRN(02) SETC 'READ'
&ZZ#RDRI(02) SETC '0'
&ZZ#RDRN(03) SETC 'KEEP'
&ZZ#RDRI(03) SETC '1'
&ZZ#RDRN(04) SETC 'OPEN'
&ZZ#RDRI(04) SETC '2'
&ZZ#RDRN(05) SETC 'CLOSE'
&ZZ#RDRI(05) SETC '3'
&ZZ#RDRN(06) SETC 'REREAD'
&ZZ#RDRI(06) SETC '1'
&ZZ#RDRF SETB  1             FIRST TIME STUFF DONE
.NOT1ST  AIF   (T'&OPT EQ 'O').DONEFUN
&J       SETA  0
&N       SETA  0
.LOOPFUN AIF   (&J GE N'&OPT).SETFUN
&J       SETA  &J+1
&C       SETC  '&OPT(&J)'
         AIF   ('&C' EQ '').LOOPFUN
&N       SETA  &N+1
&F1      SETB  (&F0 OR ('&C' EQ 'DUMP'))
&F0      SETB  (&F0 OR ('&C' EQ 'NOCC'))
&F1      SETB  (&F0 OR ('&C' EQ 'ABEND'))
&F7      SETB  (&F7 OR ('&C' EQ 'NOWTO'))
         AGO   .LOOPFUN
.SETFUN  AIF   (&N EQ (&F0+&F1+&F2+&F3+&F4+&F5+&F6+&F7)).SATFUN
         MNOTE 4,'#RDR: ERROR IN OPT= PARAMETER'
.SATFUN  ANOP  ,
&N       SETA  (128*&F0+64*&F1+32*&F2+16*&F3+8*&F4+4*&F5+2*&F6+&F7)
&FN      SETC  '&N'
.DONEFUN AIF   (T'&AD NE 'O').HAVEAD
         AIF   ('&TYPE' NE 'OPEN').HAVEAD
         MNOTE 8,'#RDR: FIRST OPERAND REQUIRED (ADDR/TEXT/RDRWRK)'
         MEXIT ,
.HAVEAD  AIF   (&I GE 6).BADTYPE
&I       SETA  &I+1
         AIF   ('&TYPE' NE '&ZZ#RDRN(&I)').HAVEAD
&PT      SETC  '&ZZ#RDRI(&I)'
         AGO   .HAVTYPE
.BADTYPE MNOTE 8,'#RDR: TYPE=&TYPE INVALID'
         MEXIT ,
.*
.HAVTYPE AIF   ('&FN' NE '0').HAVTYPF
         MACPARM R0,256*&PT+&DV
         AGO   .HAVTYPA
.HAVTYPF MACPARM R0,=AL1(&FN,0,&PT,&DV),OP=L
.HAVTYPA MACPARM R1,&AD,NULL=SKIP     GET STORAGE ADDRESS
         MACPARM R15,=V(SUBRDR),OP=L    GET SUBROUTINE ADDRESS
         MACPARM R14,R15,OP=BALR,OPR=BALR  CALL SUBROUTINE
.MEND    MEND ,
