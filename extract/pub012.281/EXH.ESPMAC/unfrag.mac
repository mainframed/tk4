         MACRO
&NM      UNFRAG &OPT=
         GBLB  &UNFRFLG                                          79329
         GBLB  &MVS
.**********************************************************************
.*                                                                   **
.*   EXHIBIT COMMAND SPECIFIED TO UNLOAD UNUSED MODULES (PRE-MVS)    **
.*                                                                   **
.**********************************************************************
         LCLA  &I
         LCLC  &SUF,&NOP(1)
&SUF     SETC  'F'           DEFAULT SUFFIX BYTE
         AIF   ('&OPT' EQ '').HAV
&NOP(1)  SETC  'WAIT'        WAIT MESSAGE OPTION
.LOOP    AIF   (&I GT 1).BOO
&I       SETA  &I+1
         AIF   ('&OPT' EQ '&NOP(&I)').SET
         AGO   .LOOP
.SET     ANOP
&SUF     SETC  '&OPT'(1,1)   USE FIRST LETTER OF OPTION
         AGO   .HAV
.BOO     MNOTE 8,'INVALID UNFRAG OPT=''&OPT'' IGNORED'
.HAV     AIF   (&UNFRFLG).FRAGGED                                79329
         AIF   (&MVS).FRAGGED
&UNFRFLG SETB  1                                                 79329
&NM      TM    EXCPRIV,EXCUNFG .   RETURN FROM PREVIOUS REQUEST ?
         BO    UNFRAG .      YES, CONTINUE WITH THE PROGRAM
         NI    EXCPRIV,255-EXCUNFG .   TURN FLAGS OFF
         OI    EXCPRIV,EXCUNFG1 .   AND SET FLAG FOR UNFRAGMENTATION
         MVC   INVOKE(8),UNFRAMOD .   SET MODULE NAME
         MVI   INVOKE+22,0 .   CLEAR TEST BYTE (LKED ATTR)
         LA    R0,BLDL       POINT TO BLDL HEADER                86349
         L     R15,SQBLDLA                                       86349
         BALSR R14,R15       DO BLDL                             86349
         MVC   EXHFSIZ-EXHBWENT+1+INVOKE(2),MODSIZ  FORCE LARGEST SIZE
         B     EXCXCTST .    AND TEST /  XCTL TO IT
UNFRAMOD DC    CL8'EXHC#UN&SUF'  STORAGE UNFRAGMENTATION MODULE
UNFRAG   NI    EXCPRIV,255-EXCUNFG .  TURN ALL FLAGS OFF
         MEXIT ,
.FRAGGED AIF   ('&NM' EQ '').MEND
&NM      DS    0H
.MEND    MEND  ,                                                 79329
