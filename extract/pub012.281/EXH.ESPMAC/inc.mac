         MACRO ,
&NM      INC   &R,&INC=,&WORK=R0,&IN@=     RENAMED FROM COUNT ON 89247
         GBLC  &MACPLAB
         LCLA  &K                                               GP09016
         LCLB  &REG                                             GP09016
         LCLC  &WROK,&CNI                                       GP09016
&WROK    SETC  '&WORK(1)'                                       GP09016
&MACPLAB SETC  '&NM'
&K       SETA  K'&R                                             GP09016
         AIF   (&K LT 3).NREG                                   GP09016
         AIF   ('&R'(1,1) NE '(' OR '&R'(2,1) EQ '(').NREG      GP09016
         AIF   ('&R'(&K,1) NE ')' OR '&R'(&K-1,1) EQ ')').NREG  GP09016
&WROK    SETC  '&R(1)'                                          GP09016
&REG     SETB  1                                                GP09016
.NREG    AIF   ('&IN@' NE '').LOAD                              GP02250
         AIF   ('&INC' EQ '-1').BCTR
&K       SETA  K'&INC                                           GP09016
         AIF   (&K LT 3).LA                                     GP09016
         AIF   ('&INC'(1,1) NE '(' OR '&INC'(2,1) EQ '(').LA
         AIF   ('&INC'(&K,1) NE ')' OR '&INC'(&K-1,1) EQ ')').LA
         MACPARM &WROK,&R,OP=L,OPR=LR
         MACPARM &WROK,&INC,OPR=AR,OPMR=SR                       82003
         AGO   .COMST
.BCTR    MACPARM &WROK,&R,OP=L,OPR=LR
         MACPARM &WROK,(0-0),OPR=BCTR
         AGO   .COMST
.LOAD    AIF   ('&INC' EQ '' OR '&INC' EQ '1').LOADER           GP02250
         MNOTE 'INC: INC KEYWORD &INC CONFLICTS WITH IN@; IGNORED'
.LOADER  AIF   ('&WROK' EQ '&IN@(1)').LOADRV                    GP02250
         MACPARM &WROK,&R,OP=L,OPR=LR                           GP02250
         MACPARM &WROK,&IN@,OP=A,OPR=AR,OPM=S,OPMR=SR           GP02250
         AGO   .COMST                                           GP02250
.LOADRV  MACPARM &WROK,(&IN@(1)),OP=L,OPR=LR                    GP02250
         MACPARM &WROK,&R,OP=A,OPR=AR                           GP02250
         AGO   .COMST                                           GP02250
.LA      AIF   (NOT &REG).LAST                                  GP09016
         AIF   ('&INC' NE '').AINC                              GP09016
         MACPARM &WROK,=F'1',OP=A                               GP09016
         MEXIT ,                                                GP09016
.AINC    AIF   ('&INC'(1,1) NE '-').BINC                        GP09016
&K       SETA  K'&INC                                           GP09016
         AIF   (&K LT 4).BINC                                   GP09016
         AIF   ('&INC'(2,1) NE '(' OR '&INC'(3,1) EQ '(').BINC  GP09016
         AIF   ('&INC'(&K,1) NE ')' OR '&INC'(&K-1,1) EQ ')').BINC
&CNI     SETC  '&INC'(2,&K-1)
         MACPARM &WROK,&CNI,OP=SR,OPR=SR                        GP09016
         MEXIT ,                                                GP09016
.BINC    MACPARM &WROK,=A(&INC),OP=A                            GP09016
         MEXIT ,                                                GP09016
.LAST    MACPARM &WROK,&INC,NULL=1
         MACPARM &WROK,&R,OP=A,OPR=AR,OPM=S,OPMR=SR              82003
.COMST   MACPARM &WROK,&R,OP=ST,OPR=LR,MODE=REV
         MEND  ,
