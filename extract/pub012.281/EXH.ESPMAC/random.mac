         MACRO ,                                        REPLACED 78029
&ALIAS   RANDOM &R=R14,&SECT=,&SCALE=10                 REWRITE 2003358
         GBLB  &MVS,&MVSSP,&MVSXA,&MVSESA,&OS390,&Z900          GP04234
         GBLC  &MACPLAB                                         GP03358
&MACPLAB SETC  '&ALIAS'                                         GP03358
.*--------------------------------------------------------------------*
.*   RANDOM - INLINE MACRO TO EXPAND RANDOM NUMBER GENERATION SUBRTNE *
.*     NEEDS LTORG FOR THE CURRENT CSECT PRIOR TO END                 *
.*                                                                    *
.*     INVOKE AS BAS R14,RANDOM                                       *
.*     XC RANDAU(4),RANDAU  TO RE-INITIALIZE                          *
.*     RETURN IN GPR 0 INTEGER 0 TO 9 UNLESS SCALE IS REQUESTED       *
.*                                                                    *
.*     SECT= SPECIFIES DSECT NAME WHEN REFRESHABLE CODE IS NEEDED     *
.*     (USE AFTER DEFINITION FOR THAT DSECT)                          *
.*                                                                    *
.*     WHEN USED WITH SCALE=(REG), THE REGISTER MAY BE ANY FLOATING   *
.*     POINT REGISTER, BUT IF REGISTER 0, MUST BE WRITTEN AS (0) OR   *
.*     (R0). THIS USAGE REQUIRES  LE REG,=E'MAXVAL'/BAS R14,RANDOM    *
.*     FOR ALL CALLS.                                                 *
.*--------------------------------------------------------------------*
         LCLC  &MYSECT,&FPR                                     GP03358
&MYSECT  SETC  '&SYSECT'                                        GP03358
&FPR     SETC  'R0'          USE FLOATING POINT REGISTER 0      GP03358
         AIF   ('&SCALE' EQ '').NOFREG                          GP03358
         AIF   ('&SCALE'(1,1) NE '(').NOFREG                    GP03358
         AIF   ('&SCALE' EQ '(0)').DOFREG                       GP03358
         AIF   ('&SCALE' EQ '(R0)').DOFREG                      GP03358
         AIF   ('&SCALE' EQ '(FR0)').DOFREG                     GP03358
         AIF   ('&SCALE' EQ '(FPR0)').DOFREG                    GP03358
         AGO   .NOFREG                                          GP03358
.DOFREG  ANOP  ,                                                GP03358
&FPR     SETC  'R6'          USE FLOATING POINT REGISTER 6      GP03358
.NOFREG  AIF   (NOT &MVSESA).OLDSV                              GP03358
RANDOM   MACPARM (&R),(0),OP=BAKR,OPR=BAKR                      GP03358
         MACPARM (R15),(R1),RANDSAVE,MODE=THREE,OP=STM,OPR=STM  GP03358
         AGO   .COMSV                                           GP04234
.OLDSV   ANOP  ,                                                GP04234
RANDOM   MACPARM (R14),(R1),RANDSAVD,MODE=THREE,OP=STM,OPR=STM  GP04234
.COMSV   ICM   R1,15,RANDAU  CALLED BEFORE ?                    GP03358
         BNZ   RANDOLD                                          GP03358
         TIME  BIN
         AR    R1,R0
         N     R1,=X'3FFFFFFF'                                  GP03358
         LA    R0,1
         OR    R1,R0
RANDOLD  M     R0,=F'13187'  WAS X'0001000D'                    GP03358
         SRL   R1,8          KILL SIGN AND SOME                 GP03358
         ST    R1,RANDAU     TEMP                               GP03358
         ST    R1,RANDSAV0   TEMP                               GP03358
         MVI   RANDSAV0,X'40' SET EXPONENT                      GP03358
         LE    &FPR,RANDSAV0                                    GP03358
         AIF   ('&SCALE' EQ '').NOSCALE                         GP03358
         AIF   (T'&SCALE NE 'N').VAR                            GP03358
         MACPARM &FPR,=E'&SCALE',OP=ME,OPR=MER                  GP03358
         AGO   .NOSCALE                                         GP03358
.VAR     MACPARM &FPR,&SCALE,OP=ME,OPR=MER                      GP03358
.NOSCALE ANOP  ,
         AU    &FPR,=X'46000000' POSITION AS INTEGER            GP03358
         STE   &FPR,RANDSAV0
         MVI   RANDSAV0,0    KILL EXPONENT
         AIF   (NOT &MVSESA).OLDRS                              GP04234
         LM    R15,R1,RANDSAVE                                  GP03358
         PR    ,             ANSWER IN (0)                      GP03358
         AGO   .COMRS                                           GP04234
.OLDRS   LM    R14,R1,RANDSAVD                                  GP04234
         BR    &R            RETURN ANSWER IN R0                GP04234
.COMRS   SPACE 1                                                GP03358
         AIF   ('&SECT' EQ '').NORENT                           GP03358
&SECT    DSECT ,             REFRESHABLE (ZEROED INITIALLY)     GP03358
.NORENT  ANOP  ,                                                GP03358
RANDAU   DC    D'0'                                             GP03358
RANDSAVD DC    F'0'   0/2    SAVE 14                            GP04234
RANDSAVE DC    F'0'   1/2    SAVE 15                             *TSM*
RANDSAV0 DC    2F'0'  2/2    SAVE 0,1                            *TSM*
         AIF   ('&SECT' EQ '').NOREFR                           GP03358
&MYSECT  CSECT ,             RESTORE USER'S SECTION             GP03358
.NOREFR  ANOP  ,                                                GP03358
         MEND  ,
