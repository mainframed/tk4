         MACRO ,                                        ADDED ON 88211
&NM      MAPSCR &PREFIX=SU,&DSECT=YES,&EXPARM=NO SCRWORK MAPPING
         LCLC  &DS,&P
&P       SETC  '&PREFIX'
&DS      SETC  '&P'.'PARM'
         AIF   ('&NM' EQ '').HAVEDS
&DS      SETC  '&NM'
.HAVEDS  AIF   ('&DSECT' NE 'YES').NODSECT
&DS      DSECT ,             MAPPING OF SCRWORK EXPANSION
         AGO   .COMDS
.NODSECT ANOP  ,
&DS      DS    0H            MAPPING OF SCRWORK EXPANSION
.COMDS   ANOP  ,
&P.DDNAM  DC   CL8' '        DDNAME (BTAM OR VTAM)
&P.DDALT  DC   CL8' '        ALTERNATE DDNAME (BTAM) OR LU ID (VTAM)
&P.HIL    DC    X'07'        MASK FOR EXTENDED HIGH-LIGHTING
&P.LPP    DC   X'0'          PREFERRED LINES PER PAGE
&P.FILL   DC   X'0'          TRANSLATE TABLE UNPRINTABLE CHARACTER
&P.WIDTH  DC   X'0'          MAXIMUM WIDTH TO USE (0, 80 OR 132)
&P.TIT#   DC   X'00'         NUMBER OF TITLES
&P.FOOT#  DC   X'00'         NUMBER OF FOOTERS
&P.COL    DC   X'07'         MASK FOR EXTENDED COLOR
&P.PRFG   DC   X'00'         OPTION FLAGS
&P.FVTAM  EQU  X'80'           USE VTAM RATHER THAN BTAM
&P.PGXLST EQU  X'04'           EXIT LIST SUPPLIED                84169
&P.PUPAGE EQU  X'02'           PAGE NUMBERS MAINTAINED IN USER AREA
&P.PGPAGE EQU  X'01'           PAGE NUMBER FEED-BACK
*        PAGE NUMBERS PRESENT ONLY IF USER REQUESTED FEED-BACK
*        OR USER UPDATING.  EXIT LIST (IF PRESENT) FOLLOWS PAGE #S.
&P.PAGE   DC   H'0'          CURRENT PAGE NUMBER
&P.SPAGE  DC   H'0'          PAGE NUMBER SINCE LAST TITLE REQUEST
&P.EXLST DC    F'0'          OPTIONAL EXIT LIST                  84169
*        EXIT LIST FORMAT : 6 BYTES PER ENTRY                    84169
*          FIRST BYTE: TYPE FLAGS AND END OF LIST X'80'          84169
*          SECOND BYTE: TITLE, FOOTER OR LINE NUMBER             84169
*          FOUR BYTES: 0 OR EXIT ADDRESS                         84169
&P.XOTYPE EQU   0,1,C'B'     EXIT LIST - TYPE FIELD              84171
&P.XFEND EQU   X'80'           END OF LIST                       84169
&P.XFOOT EQU   X'04'           FOOTER EXIT                       84169
&P.XFTIT EQU   X'02'           TITLE EXIT                        84169
&P.XFLIN EQU   X'01'           LINE EXIT                         84169
         AIF   ('&EXPARM' EQ 'NO').MEND                          84171
&P.XONUM EQU   1,1,C'F'      OFFSET TO LINE NUMBER               84171
&P.XOADDR EQU  2,4,C'A'      OFFSET TO EXIT ADDRESS              84171
&P.XOLEN EQU   6               LENGTH OF ONE EXIT ENTRY          84171
&P.XPLIST DSECT ,                                                84171
&P.XPTYPE DS   X             EXIT TYPE                           84171
&P.XPNUM  DS   X             LINE NUMBER OR MAXIMUM # OF HDR/FTR 84171
&P.XPSIZE DS   H             MAXIMUM LINE WIDTH, INCL. RDW+CC    84171
&P.XPPWAD DS   A             ADDRESS OF PRINTER WORK AREA        84171
&P.XPSAVE DS   A             ORIGINAL SAVE AREA                  84171
&P.XPLINE DS   A             ADDRESS OF PRINT LINE(S)            84171
         DS    A               RESERVED                          84171
.MEND    MEND  ,
