         MACRO ,
&NM      MACSRVLD &NAME
.*--------------------------------------------------------------------*
.*  MACSRVLD IS AN INNER MACRO TO DETERMINE WHETHER A MODULE &NAME    *
.*  HAS BEEN REQUESTED BY THE SERVLOAD MACRO/SERVICE.  IF IT HAS,     *
.*  GLOBAL &MACPLOD IS SET TO THE NAME OF THE ADDRESS CONTAINING THE  *
.*  LOADED ADDRESS (USUALLY THE MODULE NAME).                         *
.*    OTHERWISE THE VALUE RETURNED IS =V(&NAME)                       *
.*  2012-06-02  GYP  NEW; ADDED TO SUPPORT THE CPOOL MACRO            *
.*--------------------------------------------------------------------*
         GBLC  &MACPLOD
         GBLC  &SRVLMOD(20),&SRVLDEL(20)
         GBLB  &SRVBMOD(20),&BUGBEAR
         GBLA  &SRVNMOD
         LCLC  &CL,&CM
         LCLA  &I,&J,&N
&CL      SETC  '&NAME'
&CM      SETC  '&NAME'
         AIF   ('&NAME' NE '').MEMLOOP
         MNOTE 8,'MACSRVLD: ROUTINE NAME OMITTED'
         MEXIT ,
.*
.MEMLOOP AIF   (&I GE &SRVNMOD).OOPS
&I       SETA  &I+1
         AIF   ('&NAME' NE '&SRVLDEL(&I)').MEMLOOP
&MACPLOD SETC  '&SRVLMOD(&I)'
         MEXIT  ,
.OOPS    ANOP  ,
&MACPLOD SETC  '=V('.'&NAME'.')'
         MEND  ,
