         MACRO
&NM      XRITE &MSG,&LEN,&ERR=EXCWERR,&BAL=EXCRITE
         LCLC  &NAM
         LCLB  &MVC
&NAM     SETC  '&NM'
         AIF   ('&MSG' EQ '').CLEN
&MVC     SETB  1
         AIF   ('&MSG'(1,1) EQ '(').MREG
&NAM     LA    R2,&MSG       ADDRESS OF MESSAGE
&NAM     SETC  ''
         AGO   .CLEN
.MREG    AIF   ('&MSG' EQ '(2)' OR '&MSG' EQ '(R2)').CLEN
&NAM     LR    R2,&MSG(1)    LOAD MESSAGE ADDRESS
&NAM     SETC  ''
.CLEN    AIF   ('&LEN' EQ '').DEF
         AIF   ('&LEN'(1,1) EQ '(').LREG
&NAM     LA    R3,&LEN       MESSAGE LENGTH
&NAM     SETC  ''
         AGO   .TMOV
.LREG    AIF   ('&LEN' EQ '(3)' OR '&LEN' EQ '(R3)').TMOV
&NAM     LR    R3,&LEN(1)    MESSAGE LENGTH
&NAM     SETC  ''
         AGO   .TMOV
.DEF     ANOP
&NAM     LH    R3,BUFSIZ     GET DEFAULT MESSAGE LENGTH
&NAM     SETC  ''
.TMOV    AIF   (NOT &MVC).BAL
&NAM     LR    R15,R2        COPY START ADDRESS
&NAM     SETC  ''
         LR    R14,R3        COPY LENGTH
         L     R1,BUFAD      LOAD OUTPUT ADDRESS
         MVC   0(80,R1),0(R15)    MOVE 80 BYTES AT A TIME
         LA    R15,80(,R15)  NEXT INPUT
         LA    R1,80(,R1)    NEXT OUTPUT
         SH    R14,H80       REMAINING LENGTH
         BP    *-18          MOVE REST
.BAL     ANOP
&NAM     BALS  R9,&BAL       CALL OUTPUT I/O ROUTINE
         AIF   ('&ERR' EQ '').MEX
         B     &ERR          BRANCH ON WRITE ERROR
.MEX     MEND  ,
