         MACRO
&NM      XUSE   &SECT,&REG,&LEN,&TEST=
         COPY OPTIONS
.**********************************************************************
.*    THIS MACRO, RUNNING UNDER THE EXORCISE DEBUG PROGRAM, WILL SNAP
.*    REGISTER CONTENTS FOR THE DSECT/REGISTER SPECIFIED.
.*
.*    OOPS - MAKE MACRO MATCH EXORSPIE CODE:  WHEN EITHER THE REGISTER
.*      OR THE LENGTH IS ZERO, OMIT THE NAME                    GP05334
.**********************************************************************
.*    SECT IS A 1-8 BYTE NAME OF A DSECT USED IN THE INVOKING PROGRAM
.*    REG IS THE REGISTER POINTING TO STORAGE MAPPED BY DSECT
.*      A REG VALUE OF ZERO CLEARS ALL PRIOR XUSE DEFINITIONS
.*    LEN IS AN INTEGER IN THE RANGE 1-5, OR A SELF-DEFINING VALUE
.*      IN THAT RANGE.  A VALUE OF ZERO CANCELS THAT REGISTER ENTRY.
.**********************************************************************
         LCLC  &LBL                                             GP05334
&RING    SETC  '&NM'
         AIF   ('&SYSPARM' NE 'DEBUG').EASY                      *TSM*
         AIF   (T'&LEN NE 'N').NONNUM                            94222
         AIF   (&LEN GT 0 AND &LEN LT 4096).NONNUM               94222
         MNOTE 8,'LENGTH INVALID. USE 1-4095'                    94222
         MEXIT ,                                                 94222
.NONNUM  AIF   ('&SECT' EQ '' OR '&REG' EQ '' OR '&LEN' EQ '').BOMB
         AIF   ('&TEST' EQ '').SNAP                             GP05334
&LBL     SETC  'ZZZ'.'&SYSNDX'.'B'
&RING    TM    OPTS,DEBUG .   RUNNING UNDER EXORCISE ?
         BZ    &LBL          NO; BYPASS SNAP
&RING    SETC  ''
.SNAP    AIF   ('&SECT' EQ '' OR '&REG' EQ '' OR '&LEN' EQ '').BOMB
         AIF   ('&REG' EQ '0' OR '&LEN' EQ '0').SHORT           GP05334
&RING    DC    0H'0',X'8308',AL.4(&REG),AL.12(&LEN),CL8'&SECT '
         AGO   .LABEND
.SHORT   ANOP  ,             DON'T NEED SECT NAME               GP05334
&RING    DC    0H'0',X'8308',AL.4(&REG),AL.12(&LEN)             GP05334
.LABEND  AIF   ('&LBL' EQ '').MEND                              GP05343
&LBL     DS    0H            LABEL FOR DEBUG TEST               GP05334
         MEXIT ,                                                GP05334
.BOMB    MNOTE  2,'XUSE NEEDS DSECT/REGISTER/LENGTH'
         MEXIT ,
.EASY    AIF   ('&NM' EQ '').MEND
&NM      DS    0H .          LABEL ONLY - NO SNAP EXPANSION
.MEND    MEND
