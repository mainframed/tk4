         MACRO ,                                        ADDED ON 79157
&NM      RESCODE ,            DEFINE RESIDENT LPA GROUP CODE     79157
         COPY  OPTIONS
         EJECT ,
         LCLA  &LEN,&SLEN(36)                                    84288
         LCLB  &LOCO,&OSFG                                       80042
         LCLC  &SI
&SLEN(1) SETA  480
&SLEN(2) SETA  960
&SLEN(3) SETA  1920
&SLEN(4) SETA  2560
&SLEN(5) SETA  2720
&SLEN(6) SETA  3440
&SLEN(13) SETA 1600          20*80 3278/9 43XX CONSOLE           84288
&SLEN(22) SETA 3564          27*132 3278/3180                    88250
&LOCO    SETB  ('&LOCAL' EQ 'AMS' OR '&LOCAL' EQ 'TECH')         80042
&LOCO    SETB  (&LOCO OR '&LOCAL' EQ 'GMSF' OR '&LOCAL' EQ 'CCSI')
&OSFG    SETB  ('&SYSTEM' EQ 'MFT' OR '&SYSTEM' EQ 'MVT')        80042
         AIF   (K'&NM NE 8).BADNM
         AIF   ('&NM'(1,4) NE 'EXHR').BADNM
         AIF   ('&NM'(8,1) NE 'C').NOT43XX                       84288
&LEN     SETA  &SLEN(13)                                         84288
&SI      SETC  'C'                                               84288
         AGO   .COMNAM                                           84288
.NOT43XX AIF   ('&NM'(8,1) GT '0' AND '&NM'(8,1) LT '7').GOODNM
&SI      SETC  'M'                                               88250
&LEN     SETA  &SLEN(22)                                         88250
         AIF   ('&NM'(8,1) EQ 'M').COMNAM                        88250
.BADNM MNOTE 8,'RESCODE NAME MUST BE EXHR - GROUP(3) - SIZE(1-6)'
         MEXIT ,
.GOODNM  SPACE 1
&SI      SETC  '&NM'(8,1)      GET LAST BYTE
&LEN     SETA  &SLEN(&SI)
.COMNAM  SPACE 1                                                 84288
&NM      START 0
         USING *,R10         DECLARE BASE
         NUSE  SQSP,R13      DECLARE SQS WORK AREA
         NUSE  WORK,R11      DECLARE DISPLAY WORK AREA
         SPACE 1
*        THIS MODULE MAY BE ADDED TO THE LINKPACK AREA
*              IN MVS AND SVS BY LINKEDIT TO SYS1.LPALIB         82347
*              IN MVS/SP AND XA, BY INCLUSION IN IEALPAXX OR IEAFIXNN
*              IN MVT BY LINKEDIT TO SYS1.SVCLIB + ADDITION TO
*                A RAM LIST IN SYS1.PARMLIB
*
*        MODULE NAMING IS A FOLLOWS :
*              FIRST 4 BYTES - EXHR
*              NEXT 3 - MUST MATCH THE GROUP= PARAMETER IN THE
*              DOPT DEFINITION (EXHP.... MODULE) FOR THIS CRT.
*              LAST BYTE - DETERMINED BY SCREEN SIZE
*              1 -  480  2 -  960  3 - 1920      C - 1600        84288
*              4 - 2560  5 - 2720  6 - 3440
*        ONLY THE EXHR MODULE ACTUALLY USED SHOULD BE COPIED
*        FROM SYS1.EXHIBIT TO THE APPROPRIATE LIBRARY.
         SPACE 1
         B     BEGIN         RESCALL LOOP (+0)
         B     SCREENUP      RESCALL SCREEN (+4)                 79159
         B     INIT          RESCALL INIT (+8)                   80042
         DC    A(BDCSTMSG-RESFIXBG+ONESPACE) BDCST MSG ADDR. (+12)
EXTFLAGS DC    XL4'0'        EXTERNAL FLAGS  (+16)               83010
         B     INDSN         DSN PREFIX CONVERSION  (+20)        84288
         DC    A(BIGWORK)    MORE WORK SPACE  (+24)              84288
         DC    A(ENQPAT)     ENQ LIST OVLY    (+28)             GP04234
         DC    A(0,0,0)      SPARES                              87281
         SPACE 1                                                 79159
*        INITIALIZATION CALL                                     79159
         SPACE 1                                                 79159
INIT     XC    EXTFLAGS,EXTFLAGS  RESET ALL FLAGS                83010
         LA    R2,ONESPACE                                       87281
         USING RESFIXBG,R2   MAP IT                              87281
         CLC   EXWJ2NAM(4),RESSSNM  SAME SUBSYSTEM ?             87281
         BE    INITSSNM      YES                                 87281
         CLI   RESSSNM,C' '  FIRST CALL ?                        87281
         BNH   INITSSNM      YES                                 87281
         LA    R2,TWOSPACE-ONESPACE(,R2)  USE SECOND PAGE        87281
INITSSNM MVC   RESSSNM,EXWJ2NAM  SET CURRENT SUBSYSTEM NAME      87281
         AIF   (NOT &MVS OR &MVSXA).NOMVSFX                      92265
         CLI   ENQPAT+2,X'01'  JOB LEVEL ?                      GP04234
         BE    INITWAIT      YES; BYPASS PAGE FIX               GP04234
         LA    R3,RESFIXND+1-RESFIXBG(,R2)  END AREA TO BE FIXED 84289
         XC    DB(4),DB      CLEAR ECB                           82347
         LA    R0,DB         MAKE ECB ADDRESS                    82347
         PGFIX R,A=(2),EA=(3),ECB=(0),LONG=Y                     82347
         CH    R15,=H'8'     DELAYED ?                           82347
         BNE   INITWAIT                                          82347
         WAIT  ECB=DB        WAIT FOR COMPLETION                 82347
INITWAIT DS    0H                                                82347
.NOMVSFX XC    RESXC(RESXCLEN),RESXC   CLEAR PREVIOUS VALUES     82347
         MVI   RESBL,C' '     INITIALIZE 'BLANK' FIELDS          79160
         MVC   RESBL+1(RESBLLEN-1),RESBL  BLANK SPECIAL FIELDS   79160
         MVI   RESTIME,X'FF'  MAKE LAST TIME VERY NEGATIVE       79160
         BR    R9            RETURN
         DROP  R2                                                87281
         SPACE 1
*        THIS CODE IS ENTERED AT THE BEGINNING OF EXHDZZZZ
*        IF THE CURRENT USER HAS ENQ CONTROL, THE SCREEN AND
*        RELATED VALUES (CYCLE, ETC) ARE SAVED FOR RE-USE.
*        IF THE CALLER LACKS CONTROL, THE PREVIOUS SCREEN IS
*        COPIED TO THE CALLER'S BUFFER; COUNTERS ARE UPDATED.
*              WITH THE LOOP CALL, THIS PERMITS MULTIPLE
*              EXHIBIT TASKS RUNNING AS A GROUP (SIMILAR OPTIONS)
*              TO AVOID THE OVERHEAD OF GOING THROUGH THE DISPLAY
*              CODE; ONLY ONE DOES THE COMPUTATION, THE OTHERS
*              COPY THE SCREEN DATA.
         SPACE 1
SCREENUP L     R4,MSGAD      MAKE COMMON WORK AREA
         USING TEMPWORK,R4   DECLARE IT
         ST    R5,SAVER5     SAVE REGISTER 5                     87281
         LA    R5,ONESPACE                                       87281
         USING RESFIXBG,R5   MAP IT                              87281
         CLC   EXWJ2NAM(4),RESSSNM  SAME SUBSYSTEM ?             87281
         BE    SCUPSSNM      YES                                 87281
         CLI   RESSSNM,C' '  FIRST CALL ?                        87281
         BNH   SCUPSSNM      YES                                 87281
         LA    R5,TWOSPACE-ONESPACE(,R5)  USE SECOND PAGE        87281
SCUPSSNM MVC   RESSSNM,EXWJ2NAM  SET CURRENT SUBSYSTEM NAME      87281
         MVC   ENQLIST(ENQLEN),ENQPAT  MAKE DEQ LIST
         L     R14,BUFAD     GET SCREEN OF THIS TASK             79159
         LH    R15,RESIZE    GET SCREEN SIZE
         LR    R3,R15        COPY
         LA    R2,RESCREEN   GET RESIDENT SCREEN
         TM    EXRESAD,1     DOES THIS USER HAVE CONTROL ?
         BZ    COMMUPD       NO; JUST UPDATE COUNTERS & SCREEN   79165
         NI    EXRESAD,254   RESET CONTROL FLAG
         XR    R2,R14        FLIP FOR COMMON SUBROUTINE          80042
         XR    R14,R2        (SAVES STORAGE IN OS)               80042
         XR    R2,R14                                            80042
         BALS  R1,COMMOVE    GO TO COMMON MOVER                  80042
         LA    R2,INVOKEZ    GET DZZZZ
         USING EXHBWENT,R2
         MVC   RESDZZ,EXHFSTRT-14   SAVE CPU VALUES
         MVC   RESHASP(SQTRANS-HASPHCT),HASPHCT  SAVE HASP       83227
         AIF   (&OSFG).NOVS1                                     80042
         SH    R2,=AL2(INVOKED-INVOKE)  GET DPA ADDRESS
         MVC   RESDPA,EXHFSTRT-10  SAVE PAGING VALUES
.NOVS1   AIF   (NOT &LOCO).NOLO1                                 80042
         MVC   RESXSAVE,USERXSAV  SAVE USER DATA
.NOLO1   TIME  BIN           GET SCREEN UPDATE TIME
         ST    R0,RESTIME    AND SAVE IT
         DEQ   MF=(E,ENQLIST)     RELEASE RESOURCE
         L     R5,SAVER5     RESTORE                             87281
         B     4(,R9)        TAKE SLOW EXIT
         SPACE 1
*        COPYING TASK - UPDATE SCREEN AND COUNTERS
*
COMMUPD  LA    R1,COMMCOMM   SET RETURN FROM IN-LINE SUBROUTINE  80042
COMMOVE  LMVC  R14,R2        COPY OLD (CURRENT?) SCREEN          80042
         AIF   ('&SI' LT '3' AND '&SI' GE '0').NOBDCST           88251
         CLC   BDCSTMSG+2(L'BDCSTMSG-2-2-3),BLANKS  ANY TEXT ?   88251
         BER   R1            NO; SKIP                            88251
         L     R14,BUFAD     GET SCREEN ADDRESS                  80042
         AH    R14,LSIZE     SKIP A LINE                         88250
COMMULUP AH    R14,LSIZE     GET NEXT LINE                       88250
         C     R14,MSGAD     END OF SCREEN ?                     80042
         BNLR  R1            YES; NO ROOM FOR MESSAGE            80042
         CLC   2(L'BDCSTMSG-7,R14),BDCSTMSG+2  ALREADY ON ?      90268
         BER   R1            YES; JUST RETURN                    90268
         CLC   0(L'BDCSTMSG,R14),BLANKS  CLEAR LINE ?            80042
         BNE   COMMULUP      NO                                  80042
         TM    OPTS,DEBUG+OPTNCRT  NON-3270 ?                    80042
         BNZ   COMMUPAR      MOVE MSG WITHOUT ORDERS             80042
         L     R3,SQIQPARM   GET UCB ADDRESS                     80042
         CLI   19(R3),X'08'  3066 ?                              80042
         BE    COMMUPAR      YES; NO ORDERS                      80042
         MVC   0(L'BDCSTMSG,R14),BDCSTMSG  MOVE MSG              80042
         LA    R3,L'BDCSTMSG(,R14)  GET END MSG ADDRESS          88250
         S     R3,BUFAD      GET SCREEN OFFSET TO NEXT LINE      80042
         STCM  R3,3,L'BDCSTMSG-2(R14)  STASH AS IS               88250
         TM    L'BDCSTMSG-2(R14),X'F0'  NEED 14 OR 16 BITS ?     88250
         BNZ   COMMUPAR      YES; LEAVE AS IS                    88250
         SRL   R3,6          HIGH BUFFER ADDRESS                 80042
         STC   R3,L'BDCSTMSG-2(,R14)  SET IT                     80042
         NI    L'BDCSTMSG-1(R14),X'3F'  MASK                     80042
         TR    L'BDCSTMSG-2(2,R14),CONADDR  MAKE EBCDIC          80042
         BR    R1            RETURN                              80042
COMMUPAR MVC   2(L'BDCSTMSG-2-2-3,R14),BDCSTMSG+2  TEXT ONLY     80042
.NOBDCST BR    R1            RETURN                              80042
COMMCOMM LA    R2,INVOKEZ    GET DZZZZ
         USING EXHBWENT,R2
         MVC   EXHFSTRT-14(14),RESDZZ   SET CPU VALUES
         MVC   HASPHCT(SQTRANS-HASPHCT),RESHASP  HELP HASP       83227
         AIF   (&OSFG).NOVS2                                     80042
         SH    R2,=AL2(INVOKED-INVOKE)  GET DPA ADDRESS
         MVC   EXHFSTRT-10(10),RESDPA  SET PAGING VALUES
.NOVS2   AIF   (NOT &LOCO).NOLO2                                 80042
         MVC   USERXSAV,RESXSAVE  SET USER DATA
.NOLO2   BR    R9            RETURN WITH FAST PATH
         DROP  R5                                                87281
         SPACE 2
*        THIS CODE IS ENTERED PRIOR TO BEGINNING THE DISPLAY
*        LOOP.  THE CODE TESTS WHETHER AN OLD, BUT RECENT SCREEN
*        IS AVAILABLE.  IF SO, THE DISPLAY CYCLE IS SKIPPED.
*        ELSE THE ENQ IS KEPT UNTIL DZZZZ RELEASES IT ON ANOTHER
*        RESCALL (AN ABEND WOULD ALSO RELEASE IT).
*
BEGIN    L     R4,MSGAD      MAKE WORK SPACE
         USING TEMPWORK,R4     DECLARE IT
         ST    R5,SAVER5     SAVE REGISTER 5                     87281
         LA    R5,ONESPACE                                       87281
         USING RESFIXBG,R5   MAP IT                              87281
         CLC   EXWJ2NAM(4),RESSSNM  SAME SUBSYSTEM ?             87281
         BE    SCBGSSNM      YES                                 87281
         CLI   RESSSNM,C' '  FIRST CALL ?                        87281
         BNH   SCBGSSNM      YES                                 87281
         LA    R5,TWOSPACE-ONESPACE(,R5)  USE SECOND PAGE        87281
SCBGSSNM MVC   RESSSNM,EXWJ2NAM  SET CURRENT SUBSYSTEM NAME      87281
         MVC   ENQLIST(ENQLEN),ENQPAT  MAKE ENQ LIST
         ENQ   MF=(E,ENQLIST)     ENQUEUE
         TIME  BIN           GET CURRENT TIME
         C     R0,RESTIME    IS IT LOWER THAN PREVIOUS ?
         BNL   *+8           NO
         A     R0,=A(24*60*60*100)  ADD A DAY
         L     R1,RESTIME    GET LAST UPDATE TIME
         A     R1,DELAY      ADD LOOP TIME
         CR    R0,R1         TIME TO DO FULL UPDATE ?
         BH    BEGINUP       YES
         DEQ   MF=(E,ENQLIST)     RELEASE RESOURCE
         B     COMMCOMM      UPDATE COUNTERS ONLY
         SPACE 1
BEGINUP  OI    EXRESAD,1     SHOW TASK IN CONTROL
         L     R5,SAVER5     RESTORE R5                          87281
         B     4(,R9)        TAKE SLOW EXIT
         DROP  R5                                                87281
         SPACE 2                                                 84288
*        COMMON DATASET NAME CONVERSION FOR SHORT FORM PREFIXES. 84288
*        OUTPUT ADDRESS IN R14 (RETURNED POINTING TO NEXT BYTE)  84288
*        OUTPUT LENGTH (MAINTAINED)                              84288
*        R0 - WORK REGISTER                                      84288
*        INPUT ADDRESS IN R1 (MAINTAINED)                        84288
*        RETURN TO R9 IF PREFIX SUBSTITUTED                      84288
*        RETURN TO 4(R9) IF NO MATCH FOUND                       84288
*                                                                84288
INDSN    LR    R0,R15        SAVE LENGTH ACROSS CALCULATION      84288
         AIF   ('&LOCAL' NE 'CCSI').NOWYL0                       84288
         LA    R15,INPDWYL1  PROV. SET TECH. SUPPORT TABLE       84288
         CLI   0(R1),C'/'    TECH. SUPPORT NAME ?                84288
         BE    INPDPLUS       YES - CONVERT                      84288
         LA    R15,INPDWYL2  PROV. SET PROGRAMMER'S TABLE        84288
         CLI   0(R1),C'-'    PROGRAMMING STAFF ?                 84288
         BE    INPDPLUS       YES - CONVERT                      84288
         LA    R15,INPDWYL3  CUSTOMER SUPPORT / OPERATIONS       86101
         CLI   0(R1),C'^'    NOTS ?                              86101
         BE    INPDPLUS      THEY ALL ARE                        86101
         LR    R15,R0        RESTORE FOR UNMODIFIED BRANCH       84288
         CLI   0(R1),C':'    PUBLIC LIBRARY REQUEST ?            84288
         BNE   *+4+6+4       NO; HOP AROUND                      84288
         MVC   0(L'INPDPFX+9,R14),INPDPFX  MOVE THE PREFIX       84288
         B     INPDLEN1      FINAGLE LENGTHS                     84288
.NOWYL0  CLI   0(R1),C'&&'   SPECIAL INPUT NAME ?                84288
         BNE   4(,R9)         NO  - GO TO NORMAL MOVER           84288
         AIF   ('&LOCAL' NE 'CCSI').NOWYL2                       84288
         CLI   1(R1),C'0'    SECOND DIGIT NUMERIC ?              84288
         BL    INPDAMPS .     NO; DO NORMAL INPUT                84288
         LA    R15,INPDWYL0  GET START OF SYSTEMS TABLE          84288
INPDPLUS MVC   0(L'INPDPFX,R14),INPDPFX .  MOVE PREFIX           84288
INPDLOOP CLC   1(1,R1),0(R15)  MATCHING ENTRY ?                  84288
         BE    INPDMOVE       YES - PROCESS                      84288
         CLI   0(R15),C'"'   END OF TABLE ?                      84288
         BE    INPDMOVE       YES                                84288
         LA    R15,L'INPDWYL0(,R15)  TRY NEXT ONE                84288
         B     INPDLOOP                                          84288
INPDMOVE MVI   21(R14),C'.'  TRAILING SEPARATOR                  84288
         MVC   13(8,R14),1(R15)  USER ID                         84288
         CLI   0(R1),C'^'    NOTS ?                              86101
         BE    INPDSUPP      NUTS                                86101
         MVI   7(R14),C'5'   SET PROGRAMMING NUMBER              84288
         CLI   0(R1),C'-'    TECH. SUPPORT ?                     84288
         BH    INPDCUSS      YES; SUPPORT STAFF                  84288
         BE    INPDTECH      NO; PROGRAMMING STAFF SUPPORT       84288
         MVI   7(R14),C'4'   SET SYSTEMS                         84288
         MVI   10(R14),C'1'  SET SPECIAL SYSTEM CODE             84288
         MVC   11(1,R14),1(R1)  SUBACCT                          84288
         B     INPDLEN2      CAL. NEW LENGTH                     84288
INPDSUPP MVI   7(R14),C'6'   SET SUPPORT AND OPERATIONS          86101
         B     INPDTECH      JOIN COMMON                         86101
INPDCUSS MVI   7(R14),C'8'   SET CUST. SUPP                      84288
INPDTECH MVC   10(1,R14),1(R1)  SUBACCT                          84288
INPDLEN2 LA    R1,1(,R1)     SKIP ONE INPUT BYTE                 84288
INPDLEN1 LA    R1,1(,R1)     SKIP ANOTHER                        84288
         LR    R15,R0                                            84288
         LA    R0,L'INPDPFX+9  SET OUTPUT LENGTH TO SKIP         84288
         B     INPDLCOM      FIX LENGTHS AND RETURN              84288
INPDPFX  DC    C'WYL.Z9000000.'     COMMON DTS WYLBUR PREFIX     84288
         DC    C'DTPUBLIC.'  'PUBLIC' LIBRARY PREFIX             84288
INPDWYL0 DC    C'0""""""""'  OPEN                                84288
         DC    C'1MICHAELR'  MIKE RATS                           84288
         DC    C'2GERHARDP'  WHO, ME ?                           84288
         DC    C'3GERHARDP'  ERIC ?                              84288
         DC    C'4DICKDICK'  WOULD YOU BELIEVE DICK ?            84288
         DC    C'5""""""""'   OPEN                               84288
         DC    C'6CBENNETT'                                      85130
         DC    C'7""""""""'   OPEN                               84288
         DC    C'8J3TRODES'  DEFUNCT                             84288
         DC    C'9METZMETZ'  TSM                                 84288
INPDWYL1 DC    C'2WOODSMAN'  RAY                                 84288
         DC    C'3DMKELLEY'  DAVID                               84288
         DC    C'5""""""""'  SPARE                               86101
INPDWYL3 DC    C'1TLIBRARY'  BRON                                86101
         DC    C'2CUSTSVCS'  BRON ?                              86101
         DC    C'5""""""""'    SPARE                             86101
         DC    C'4""""""""'                                      84288
INPDWYL2 DC    C'1VIALETTA'  V.GRAHAM                            85218
         DC    C'2""""""""'                                      84288
         DC    C'6DAMBERGS'  ROB                                 86143
         DC    C'"""""""""'   END OF LIST                        84288
INPDAMPS DS    0H ,          BRANCH AROUND                       84288
.NOWYL2  ANOP  ,                                                 84288
         LA    R1,1(,R1) . SKIP OVER AMPERSAND                   84288
         MVC   0(5,R14),=C'SYS1.' MOVE INDEX LEVEL               84288
         LA    R0,5          SET OUTPUT LENGTH ADJUSTMENT        84288
         CLI   0(R1),C'0'    IS NEXT BYTE NUMERIC ?              84288
         BL    INPDLCOM      NO; NOT NUMERIC                     84288
         MVC   3(1,R14),0(R1)  MAKE SYSN.                        84288
         LA    R1,1(,R1)     ADJUST INPUT ONE MORE               84288
INPDLCOM AR    R14,R0        SET NEW OUTPUT ADDRESS              84288
         SR    R15,R0        SET REMAINING LENGTH                84288
         BR    R9            GO TO COMMON MOVER                  84288
         SPACE 1
         LTORG ,
         EJECT ,
         PRINT NOGEN
&NM      CSECT ,             KEEP MSECT HONEST
         MSECT ,
         PRINT GEN                                               79159
&NM      CSECT ,             RESTORE                             79159
         SPACE 1                                                 79159
MAJOR    DC    CL8'SYSEXHIB'  MAJOR ENQ NAME
MINOR    DC    CL8'&NM '      MINOR NAME
ENQPAT   ENQ   (MAJOR,MINOR,E,8,SYSTEM),RET=HAVE,MF=L  ENQ PATTERN
ENQLEN   EQU   *-ENQPAT      LENGTH OF PATTERN
         SPACE 1                                                 84288
BIGWORK  DC    8F'0'         ROOM FOR CPU STATISTICS             84288
         DC    16F'0'                 PAGING STATISTICS          84288
         DC    8F'0'                  QUEUE COUNTERS (STC/TS/BAT)
         DC    32F'0'        RESERVED                            84288
         AIF   ('&SI' LT '3' AND '&SI' GE '0').NOBDMSG           84288
CONADDR  DC    X'40C1C2C3C4C5C6C7C8C94A4B4C4D4E4F'   0 - 15      80042
         DC    X'50D1D2D3D4D5D6D7D8D95A5B5C5D5E5F'  16 - 31      80042
         DC    X'6061E2E3E4E5E6E7E8E96A6B6C6D6E6F'  32 - 47      80042
         DC    X'F0F1F2F3F4F5F6F7F8F97A7B7C7D7E7F'  48 - 63      80042
.NOBDMSG SPACE 1                                                 80042
RESFIXBG DSECT ,             START OF AREA FIXED/UNSWAPPED IN MVS
RESSSNM  DC    CL4' '        SUBSYSTEM NAME                      87281
RESXC    DS    0A            START OF XC CLEAR AREA              79160
RESTIME  DC    F'0'          TIME OF LAST SCREEN UPDATE
RESHASP  DC    XL(SQTRANS-HASPHCT)'0'  HASP/ASP POINTERS         83227
         AIF   (NOT &LOCO).NOLO3                                 80084
RESXSAVE DC    XL4'0'        AMS/CCSI/TECH/GMSF USEREXIT SAVE
.NOLO3   ANOP  ,                                                 80042
RESXCLEN EQU   *-RESXC       LENGTH TO CLEAR VIA XC              79160
RESBL    EQU   *             START OF AREA CLEARED TO BLANKS     79160
         AIF   (&OSFG).NOVS3                                     80042
RESDPA   DC    XL10'0'       PAGING STAT SAVE
.NOVS3   ANOP  ,                                                 80042
RESDZZ   DC    XL14'0'       CPU STAT SAVE
RESBLLEN EQU   *-RESBL       LENGTH TO CLEAR TO BLANKS           79160
RESIZE   DC    Y(&LEN)       SCREEN SIZE
         SPACE 1                                                 84288
*        THE FOLLOWING BROADCAST MESSAGE IS SET IN EXHCCBRO      80042
BDCSTMSG DC    0CL80' ',CL77' ',X'114040'  BROADCAST MESSAGE     80042
RESCREEN DC    (&LEN)C' '    SAVED DISPLAY SCREEN
RESFIXND EQU   *             END OF AREA TO BE PAGE-FIXED        82347
&NM      CSECT ,             RESTORE OUR CSECT                   87281
ONESPACE DC    (RESFIXND-RESFIXBG)X'0'  FIRST SUBSYSTEM          87281
         ORG   ONESPACE+(BDCSTMSG-RESFIXBG)                      87333
         DC    CL77' '                                           87333
         DC    X'114040'                                         87281
         ORG   ONESPACE+(RESIZE-RESFIXBG)                        87281
         DC    Y(&LEN)                                           87281
         ORG   ,                                                 87281
         SPACE 1                                                 87281
TWOSPACE DC    (RESFIXND-RESFIXBG)X'0'  SECOND SUBSYSTEM         87281
         ORG   TWOSPACE+(BDCSTMSG-RESFIXBG)                      87333
         DC    CL77' '                                           87333
         DC    X'114040'                                         87281
         ORG   TWOSPACE+(RESIZE-RESFIXBG)                        87281
         DC    Y(&LEN)                                           87281
         ORG   ,                                                 87281
         SPACE 2
TEMPWORK DSECT ,             MSGAD WORK AREA
SAVER5   DS    A             SAVE R5                             87281
ENQLIST  DC    XL(ENQLEN)'0'  ROOM FOR ENQ LIST
         SPACE 1
         MEND  ,
