         MACRO ,
&NM      TABIT &OP,&ROOT
         GBLC  &ZZTBBT,&MACPLAB
.**********************************************************************
.*   TABIT INVOKES SUBTABIT TO SET, RESET, FLIP, TEST, AND CLEAR      *
.*   A TABLE OF BIT VALUES. THE PARAMETER BLOCK NAME NEEDS TO BE      *
.*   SPECIFIED ON THE FIRST MACRO OCCURRENCE ONLY.                    *
.*                                                                    *
.*   FUNCTIONS F0 AND F1 FIND THE FIRST ZERO/ONE IN THE TABLE, AND    *
.*   SET THE PARAMETER BLOCK FOR A SUBSEQUENT ON/OFF/FLIP OPERATION.  *
.*   WHEN THE REQUEST CANNOT BE SATISFIED, R15 RETURNS 8              *
.*                                                                    *
.*   ALSO SEE MACRO MAPSBIT FOR THE PARAMETER BLOCK MAPPING, AND      *
.*   SOURCE FOR SUBTABIT FOR MORE DATA.                               *
.*                                                                    *
.*   R15 IS 0 FOR NORMAL FUNCTION, 12 FOR INVALID REQUEST, AND 16 FOR *
.*   AND INVALID PARAMETER BLOCK ADDRESS.                             *
.*                                                                    *
.**********************************************************************
         LCLC  &C
         LCLA  &I,&J
&MACPLAB SETC  '&NM'
         AIF   ('&ROOT' EQ '').NONEW
&ZZTBBT  SETC  '&ROOT'
.NONEW   MACPARM R1,&ZZTBBT
&C       SETC  '&OP '.'    '
&C       SETC  '&C'(1,3)
.LOOP    AIF   ('&C' EQ 'TESON OFFFLICLEF0 F1 '(3*&I+1,3)).MATCH
&I       SETA  &I+1
         AIF   (&I LT 7).LOOP
         MNOTE 8,'TABIT: INVALID FUNCTION &OP '
         MEXIT ,
.MATCH   MACPARM R0,&I
         MACPARM R15,@UBTABIT,OP=L
         MACPARM R14,R15,OP=BALR,OPR=BALR
         MEND  ,
