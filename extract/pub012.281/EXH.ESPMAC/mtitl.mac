         MACRO
&L       MTITL &TIT,&LEN=40
.*             THIS MACRO IS CALLED AT THE BEGINNING OF AN ASSEMBLY,
.*             IN ORDER TO SET UP THE LEFT HALF OF THE TITLE.
.*             IT IS INTENDED TO BE USED IN CONJUNCTION WITH STITL.
.*             MAIN HALF OF TITLE IS PADDED TO &LEN CHARACTERS.
.*
.*             THIS MACRO MAY NOT BE USED WITH ASSEMBLER F.
.*             USE F EXTENDED(FROM OS/VS), G WITH LSETC=255, OR H.
.*       CHANGES DATED 81154 MADE TO SUPPORT ASSEMBLER G AS ADVERTISED
         GBLC  &MTITLE
         LCLA  &I,&LN
         LCLC  &BS                                               81154
&BS      SETC  '                                        '  L'40  81154
&I       SETA  1
         AIF   ('&TIT'(1,1) NE '''' OR '&TIT'(K'&TIT,1) NE '''').DEL
.LOOP    AIF   (&I GE K'&TIT-1).ENDLOOP
         AIF   (&LN GE &LEN).TOOBIG
&I       SETA  &I+1
&LN      SETA  &LN+1
         AIF   ('&TIT'(&I,1) EQ '''' OR '&TIT'(&I,1) EQ '&&').SPECIAL
&MTITLE  SETC  '&MTITLE'.'&TIT'(&I,1)
         AGO   .LOOP
.*
.DEL     MNOTE 4,'TITLE NOT DELIMITED BY "''" - IGNORED'
&MTITLE  SETC  'LEARN TO PUT QUOUTES AROUND THINGS'
.*
.SPECIAL AIF   (&I EQ K'&TIT).NOTPAIR
         AIF   ('&TIT'(&I,1) NE '&TIT'(&I+1,1)).NOTPAIR
&MTITLE  SETC  '&MTITLE'.'&TIT'(&I,2)
&I       SETA  &I+1
         AGO   .LOOP
.*
.NOTPAIR MNOTE 4,' CHARACTER "''" OR "&&" NOT PAIRED'
&MTITLE  SETC  '&MTITLE'.'&TIT'(&I,1).'&TIT'(&I,1)               81154
         AGO   .LOOP
.*
.TOOBIG  MNOTE 4,'TITLE LONGER THAN &LEN - EXCESS TRUNCATED'
         MEXIT
.*
.ENDLOOP AIF   (&LN EQ &LEN).END
&I       SETA  &LEN-&LN                                          81154
.PADLOOP AIF   (&I LE 40).TRUNCB                                 81154
&MTITLE  SETC  '&MTITLE'.'&BS'                                   81154
&I       SETA  &I-40                                             81154
         AGO   .PADLOOP                                          81154
.TRUNCB  AIF   (&I LT 1).END                                     81154
&MTITLE  SETC  '&MTITLE'.'&BS'(1,&I)                             81154
.END     MEND
