         MACRO ,
&NM      XTAG  &VAR,&OFF=,&LEN=4,&BASE=,&SEP=,&ALIAS=,&MAX=
         GBLC  &ZZXTBAS
         GBLB  &ZZXTMAX
.*  THIS MACRO IS USED TO BUILD TABLES FOR THE CVT AND FMT DISPLAYS
.*  PRIMARY USE IS EXHIBIT.
.*
.*  &VAR IS THE FULL NAME OF A VARIABLE DEFINED IN THAT CONTROL BLOCK
.*  &BASE (USED WHEN A CHANGE IS REQUIRED) DEFINES THE BASE FROM WHICH
.*        THE OFFSET IS MADE
.*  &OFF IS A SELF-DEFINING TERM GIVING THE OFFSET FROM THE BASE; WHEN
.*        OMITTED, THE VARIABLE IS EXPECTED TO BE COVERED BY A USING
.*  &LEN IS THE ITEM LENGTH; VALID ARE 1, 2, 3, 4, AND 8.
.*
.*  &MAX (DEFAULT) NO FOR TRUNCATED NAMES
.*        ELSE YES FOR CL8 NAMES
.*        NULL TO USE PREVIOUS VALUE
.*
.*  ALIAS MAY BE YES TO SUPPRESS ITEM DISPLAY
.*
.*  SEP=NAME IS THE ONLY ITEM, AND CAUSES A CHANGE OF SECT MESSAGE
.*                       SEE MAKEXTAG PROGRAM FOR AUTOMATIC CREATION
         LCLC  &L,&N
         LCLB  &AL
&AL      SETB  ('&ALIAS' EQ 'YES')
&L       SETC  'L'''
         AIF   (T'&MAX EQ 'O').NOMAX
&ZZXTMAX SETB  ('&MAX' EQ 'YES')
.NOMAX   AIF   (T'&SEP NE 'O').CL8
         AIF   (T'&BASE EQ 'O').NONEW
&ZZXTBAS SETC  '&BASE'
.NONEW   ANOP  ,
&N       SETC  '&VAR'.'       '
         AIF   (&ZZXTMAX).FULL
&N       SETC  '&VAR'(4,5)
.FULL    AIF   (T'&OFF EQ 'O').OLDSTER
&NM  DC CL(5+3*&ZZXTMAX)'&N ',AL.1(&AL,0),AL.6(&LEN),SL2(&ZZXTBAS+&OFF)
         MEXIT ,
.OLDSTER ANOP  ,
&NM      DC    CL(5+3*&ZZXTMAX)'&N',AL.1(&AL,0),AL.6(&L&VAR),SL2(&VAR)
         MEXIT ,
.CL8     ANOP  ,
&NM      DC    CL(8+3*&ZZXTMAX)'&SEP '    BECOMES SEPARATOR LINE
         MEND  ,
