         MACRO
&L       ENDM  &DUMMY,&DISP=FREEMAIN,&PFX=SAVE,&NEXT=,                 *
               &RC=,&RC0=,&RC1=,&COPYRET=,&RETADDR=(R14)         83087
.*
.*             COPYRIGHT 1978 BY SHMUEL (SEYMOUR J.) METZ
.*                        ALL RIGHTS RESERVED.
.*
.*             THIS MACRO IS NOT TO BE DISTRIBUTED WITHOUT PERMISSION,
.*             AS DESCRIBED IN MEMBER $$RIGHTS.
.*
         LCLC  &LB                 LABEL                         81208
         LCLC  &SB                 SAVE AREA BASE NAME           81208
         LCLC  &SV                 SAVE AREA PREFIX              81208
&LB      SETC  '&L'
&SV      SETC  '&PFX'
&SB      SETC  '&SV'.'SPLN'                                      81208
         AIF   ('&DISP' EQ 'FREEMAIN' OR '&DISP' EQ 'RETAIN'           *
               OR '&DISP' EQ 'POP' OR '&DISP' EQ 'STATIC'              *
               OR '&DISP' EQ 'TEST').DISPOK                      81347
         MNOTE 4,'UNSUPPORTED VALUE DISP=&DISP'                  81347
.DISPOK  AIF   ('&DISP' EQ 'RETAIN').RETAIN                      81347
&L       L     R0,&SV.SPLN-&SB.(,R13)                            81208
         LR    R1,R13
         L     R13,&SV.13-&SB.(,R13)                             81208
&LB      SETC  ''
.RETAIN  AIF   (T'&COPYRET EQ 'O').DONCOPY                       81154
         AIF   (N'&COPYRET LT 2).ONECOPY                         81154
         AIF   (N'&COPYRET EQ 2).TWOCOPY                         81154
         MNOTE 4,' COPYRET PARAMETER INVALID; USE (ADDR-EXPR,LENGTH)'
.TWOCOPY ANOP  ,                                                 81154
&LB      MVC   &SV.15-&SB.(&COPYRET(2),R13),&COPYRET(1)          81208
         AGO   .TSTCOPY                                          81154
.ONECOPY ANOP  ,                                                 81154
&LB      MVC   &SV.15-&SB.(4,R13),&COPYRET(1)                    81208
.TSTCOPY ANOP  ,                                                 81154
&LB      SETC  ''                                                81154
.DONCOPY AIF   (T'&NEXT NE 'O').XCTL                             81154
         AIF   (T'&RC EQ 'O').T1RC0
         AIF   ('&RC'(1,1) EQ '(').SVRC
         AIF   (T'&RC EQ 'N').T1RC0
         MNOTE 12,'INVALID RETURN CODE &RC'
         MEXIT
.SVRC    ANOP  ,
&LB      ST    &RC(1),&SV.15-&SB.(,R13)                          81208
&LB      SETC  ''
.T1RC0   AIF   (T'&RC0 EQ 'O').T1RC1
         AIF   ('&RC0' EQ '(0)' OR '&RC0' EQ '(R0)').BUMRC0
         AIF   ('&RC0'(1,1) EQ '(').SVRC0
         AIF   (T'&RC0 EQ 'N').EXIT
.BUMRC0  MNOTE 12,'INVALID SECONDARY RETURN CODE &RC0'
         MEXIT
.SVRC0   ANOP  ,
&LB      ST    &RC0(1),&SV.0-&SB.(,R13)                          81208
&LB      SETC  ''
.T1RC1   AIF   (T'&RC1 EQ 'O').EXIT
         AIF   ('&RC1' EQ '(1)' OR '&RC1' EQ '(R1)').BUMRC1
         AIF   ('&RC1'(1,1) EQ '(').SVRC1
         AIF   (T'&RC1 EQ 'N').EXIT
.BUMRC1  MNOTE 12,'INVALID SECONDARY RETURN CODE &RC1'
         MEXIT
.SVRC1   ANOP  ,
&LB      ST    &RC1(1),&SV.1-&SB.(,R13)                          81208
&LB      SETC  ''
         AGO   .EXIT
.XCTL    AIF   ('&DISP' EQ 'FREEMAIN').XCTL1                     81347
         AIF   ('&DISP' NE 'TEST').XCTL2                         81347
&LB      ICM   R0,7,1+&SV.SPLN-&SB.(R1) ZERO LENGTH ?            81347
&LB      SETC  ''                                                81347
         BZ    *+6           SKIP FREE IF SO                     81347
.XCTL1   ANOP  ,                                                 81347
.*       FREEMAIN R,LV=(0),A=(1)                                 81347
&LB      SVC   10                                                81347
&LB      SETC  ''
.XCTL2   ANOP  ,
&LB      LA    R15,X@&SYSNDX
         LM    R0,R12,&SV.0-&SB.(R13)                            81208
         MVI   &SV.14-&SB.(R13),X'FF'   FLAG AS LAST SAVE AREA   81208
         XCTL  SF=(E,(15))
X@&SYSNDX XCTL EP=&NEXT,SF=L
         MEXIT
.EXIT    AIF   ('&DISP' EQ 'FREEMAIN').EXIT1                     81347
         AIF   ('&DISP' NE 'TEST').NOFM                          81347
&LB      ICM   R0,7,1+&SV.SPLN-&SB.(R1) ZERO LENGTH ?            81347
&LB      SETC  ''                                                81347
         BZ    *+6           SKIP FREE IF SO                     81347
.EXIT1   ANOP  ,                                                 81347
.*       FREEMAIN R,LV=(0),A=(1)                                 81347
&LB      SVC   10                                                81347
&LB      SETC  ''
.NOFM    ANOP  ,
&LB      LM    R14,R12,&SV.14-&SB.(R13)                          81208
         MVI   &SV.14-&SB.(R13),X'FF'   FLAG AS LAST SAVE AREA   81208
         AIF   (T'&RC NE 'N').T2RC0
         AIF   ('&RC'(1,1) EQ '(').T2RC0
         AIF   ('&RC' EQ '0').EFES
         LA    R15,&RC
         AGO   .T2RC0
.EFES    XR    R15,R15
.T2RC0   AIF   (T'&RC0 NE 'N').T2RC1
         AIF   ('&RC0' EQ '0').EFES0
         LA    R0,&RC0
         AGO   .T2RC1
.EFES0   XR    R0,R0
.T2RC1   AIF   (T'&RC1 NE 'N').BR
         AIF   ('&RC1' EQ '0').EFES1
         LA    R1,&RC1
         AGO   .BR                                               83087
.EFES1   XR    R1,R1
.BR      AIF   (T'&RETADDR EQ 'O').END                           83087
         AIF   (K'&RETADDR LT 3).BRL                             83087
         AIF   ('&RETADDR'(1,1) EQ '(' AND '&RETADDR'(2,1) NE '(').BRR
.BRL     B     &RETADDR                                          83087
         AGO   .END                                              83087
.BRR     BR    &RETADDR(1)                                       83087
.END     MEND
