         MACRO ,             LOAD PARM TEXT ADDRESS/LENGTH
&NM      PARMLOAD &R,&PTR=                                ADDED GP09247
.**********************************************************************
.*                                                                    *
.*  GET PARM TEXT ADDRESS FOR MVS OR TSO CP INVOCATION                *
.*    FIRST OPERAND SPECIFIES THE EVEN REGISTER OF AN EVEN/ODD PAIR   *
.*      TO RECEIVE THE TEXT ADDRESS AND LENGTH.                       *
.*    PTR= SPECIFIES THE REGISTER CONTAINING THE ADDRESS OF THE PARM  *
.*      POINTER (AS RECEIVED BY MODULE ON ENTRY)                      *
.*                                                                    *
.**********************************************************************
         GBLC  &SAVZPRM      PGMHEAD SAVED PARM
         LCLC  &OR1,&S       ORIGINAL POINTER
         LCLC  &NO
&NO      SETC  '&SYSNDX'
&OR1     SETC  '&PTR'
&S       SETC  '1+'.'&R'
         AIF   ('&OR1' NE '').HAVEOR
&OR1     SETC  '&SAVZPRM'    COPY FROM PGMHEAD MACRO
.HAVEOR  ANOP  ,
&NM      MACPARM &R,0(,&OR1),OP=L LOAD POINTER ADDRESS
         LH    &S,0(,&R)     GET LENGTH
         CLI   2(&R),0       TSO CP CALL?
         BH    ZZ&NO.X       NO, USE OS FORM
         SH    &S,=H'4'      ALLOW FOR LENGTH LENGTH
         SH    &S,2(,&R)     ALLOW FOR PARSED COMMAND NAME
         AH    &R,2(,&R)     ALLOW FOR PARSED COMMAND NAME
         LA    &R,2(,&R)     SKIP SECOND LENGTH
ZZ&NO.X  LA    &R,2(,&R)     SKIP FIRST LENGTH
         MEND  ,
