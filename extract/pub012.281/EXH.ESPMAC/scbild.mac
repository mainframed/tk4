         MACRO ,                                       ADDED ON GP12304
&NM      SCBILD &OP,&LIST,&FILL,  BUILD A SCREEN ADDRESS LIST AND      *
               &HEADAD=ASIS                                     GP15221
&NM      MACPARM R1,&OP,NULL=SKIP  LOAD FDW LIST ADDRESS
.*   This macro builds an address list and matching data array in
.*   previously allocated storage (EXW@RENT), for use by SCLINE.
.*   List data are copied as supplied; fill is repeated for remainder.
.*
.*   FILL data are copied to the work space.
.*
.*   HEADER (list item) data action depends on the FDWBDUSE flag:
.*     ASIS - don't touch the option flag
.*     COPY - copy list data to work area; default if flag is off.
.*     USE  - use caller's address(es) directly, allowing easier
.*            processing of command input (usually ZCMD) and titles.
.*
         AIF   ('&HEADAD' EQ 'ASIS' OR '&HEADAD' EQ '').NOFLAG  GP15221
         AIF   ('&HEADAD' EQ 'COPY').COFLAG                     GP15221
         AIF   ('&HEADAD' NE 'USE' AND '&HEADAD' NE 'USER').OOP GP15221
         MACPARM FDWSOCFG-FDW(R1),FDWBDUSE,OP=OI                GP15221
         AGO   .NOFLAG                                          GP15221
.OOP MNOTE 4,'SCBILD: HEADAD VALUE NOT RECOGNIZED; USING COPY'  GP15221
.COFLAG  MACPARM FDWSOCFG-FDW(R1),255-FDWBDUSE,OP=NI            GP15221
.NOFLAG  MACPARM R15,&LIST,NULL=0  LOAD LIST ADDRESS
         MACPARM R15,0(,R1),OP=ST    SET LIST ADDRESS INTO FDWFDA
         MACPARM R15,&FILL,NULL=0  LOAD FILLER ADDRESS
         MACPARM R15,12(,R1),OP=ST   SET IT ADDRESS INTO FDWSCAN
         MACPARM R15,EXWASCRN,OP=L LOAD SCREEN SERVICE ROUTINE
         L     R15,80(,R15)  LOAD ADDRESS OF ROUTINE
         BASR  R14,R15       CALL IT
         MEND  ,
