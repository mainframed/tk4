         MACRO
&NM      VTAB  &OP,&TO,&IN,&INTO
         LCLA  &I
         LCLC  &L,&T(16),&S(16)
&L       SETC  'L'''
&T(1)    SETC  'ALL'
&T(2)    SETC  'MVC'
&T(3)    SETC  'REG'
&T(4)    SETC  'HEX'
&T(5)    SETC  'I1'
&T(6)    SETC  'I2'
&T(7)    SETC  'I3'
&T(8)    SETC  'I4'
&T(9)    SETC  'BIN'
&T(10)   SETC  'DEC'
&T(11)   SETC  'N A'
&T(12)   SETC  'N A'
&T(13)   SETC  'N A'
&T(14)   SETC  'CLC'
&T(15)   SETC  'N A'
&T(16)   SETC  'LOOP'
&S(1)    SETC  'NOP'
&S(2)    SETC  'AGO'
&S(3)    SETC  'LD'
&S(4)    SETC  'LO'
&S(5)    SETC  'LA'
&S(6)    SETC  'LTR'
&S(7)    SETC  'AH'
&S(8)    SETC  'ST'
&S(9)    SETC  'TM'
&S(10)   SETC  'MAX'
&S(11)   SETC  'BCT'
&S(12)   SETC  'N A'         UNUSED
&S(13)   SETC  'N A'         UNUSED
&S(14)   SETC  'N A'         UNUSED
&S(15)   SETC  'N A'         UNUSED
&S(16)   SETC  'N A'         UNUSED
&I       SETA  1
.LOOP    ANOP
         AIF   ('&OP' EQ '&T(&I)').FND
&I       SETA  &I+1
         AIF   (&I LT 17).LOOP
         AIF   ('&OP' EQ '*END').END
         AIF   ('&OP' NE 'END').BLKQ
.END     ANOP
&NM      DC    X'FFFFFFFFFF' .   END OF TABLE
         AGO   .MEX
.BLKQ    AIF   ('&OP' NE 'BLK').LOTS
&I       SETA  1
.FND     AIF   (&I EQ 16).DOL
&I       SETA  (&I-1)*16
         AIF   ('&OP' EQ 'BLK').BLK
         AIF   (&I EQ 0).ALL
         AIF   ('&TO'(1,1) EQ '(' AND '&TO(2)' NE '').SUB
&NM      DC    SL2(&TO),AL1(&I+((&L&TO-1)-((&L&TO-1)/16)*16)),SL2(&IN)
.MEX     MEXIT
.SUB     ANOP
&NM DC SL2(&TO(1)),AL1(&I+((&TO(2)-1)-((&TO(2)-1)/16)*16)),SL2(&IN)
         AGO   .MEX
.BLK     AIF   ('&TO'(1,1) EQ '(' AND '&TO(2)' NE '').BSUB
&NM      DC    SL2(&TO),AL1(&I+((&L&TO-1)-((&L&TO-1)/16)*16)),AL1((&L&TX
               O-1)/16,C' ') <    MOVE BLANKS
         AGO   .MEX
.BSUB    ANOP
&NM      DC    SL2(&TO(1)),AL1(&I+((&TO(2)-1)-((&TO(2)-1)/16)*16)),AL1(X
               (&TO(2)-1)/16,C' ') <     MOVE BLANKS
         AGO   .MEX
.ALL     AIF   ('&TO'(1,1) EQ '(' AND '&TO(2)' NE '').BALL
&NM      DC    SL2(&TO),AL1(&I+((&L&TO-1)-((&L&TO-1)/16)*16)),AL1((&L&TX
               O-1)/16,&IN) <   MOVE ALL
         AGO   .MEX
.BALL    ANOP
&NM      DC    SL2(&TO(1)),AL1(&I+((&TO(2)-1)-((&TO(2)-1)/16)*16)),AL1(X
               (&TO(2)-1)/16,&IN) <    MOVE ALL
         AGO   .MEX
.DOL     ANOP
&NM      DC    AL2(&IN),AL1(X'F0'+((&TO-1)-(&TO-1)/16*16)),AL2(&INTO)
         AGO   .MEX
.LOTS    ANOP
&I       SETA  1
.LLOP    AIF   ('&OP' EQ '&S(&I)').LFND
&I       SETA  &I+1
         AIF   (&I LT 17).LLOP
.BOOBOO  MNOTE 8,'INVALID OPERATION CODE ''&OP'''
         MEXIT
.LFND    ANOP
&I       SETA  14*16+&I-1
         AIF   ('&OP' EQ 'AGO').SS
         AIF   ('&OP' EQ 'LD' OR '&OP' EQ 'LA'                         *
               OR '&OP' EQ 'BCT'                                       *
               OR '&OP' EQ 'ST').RS
         AIF   ('&OP' EQ 'LTR').RM
         AIF   ('&OP' EQ 'AH').RH
         AIF   ('&OP' EQ 'TM').SM
         AIF   ('&OP' EQ 'MAX' AND '&TO' NE '').SZ
         AIF   ('&OP' EQ 'MAX').ZM
         AIF   ('&OP' EQ 'LO').RZ
.*       ASSUME NOP OR NOT CODED YET
&NM      DC    AL1(0,0,&I,0,0) .     NOP
         AGO   .MEX
.RS      ANOP
&NM      DC    AL1(0,(&TO-(&TO/16)*16),&I),SL2(&IN)
         AGO   .MEX
.SS      ANOP
&NM      DC    SL2(&TO),AL1(&I),SL2(&IN)
         AGO   .MEX
.RM      ANOP
&NM      DC    AL1(0,(&TO-(&TO/16)*16),&I,0,&IN)
         AGO   .MEX
.RH      ANOP
&NM      DC    AL1(0,(&TO-(&TO/16)*16),&I),AL2(&IN)
         AGO   .MEX
.SM      ANOP
&NM      DC    SL2(&TO),AL1(&I,0,&IN)
         AGO   .MEX
.ZM      DC    AL1(0,0,&I,0,&IN)
         AGO   .MEX
.SZ      ANOP
&NM      DC    SL2(&TO),AL1(&I,0,0)
         AGO   .MEX
.RZ      ANOP
&NM      DC    AL1(0,(&TO-(&TO/16)*16),&I,0,0)
         AGO   .MEX
         MEND
