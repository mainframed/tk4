         MACRO
&NM     XUCBLOOP &ERR=UNITEXIT,&LOOP=UNITBUMP,&T3=,&T4=,&VOL=,&NAME=,&O*
               NLINE=,&DEV=,&R3=                                GP08250
.*  XUCBLOOP INVOKES UCBSCAN IN THE EXWORK MACRO.               GP08250
.*    BY DEFAULT THE UCB IS RETURNED AND EXPECTED IN R1         GP08250
.*    R3=YES MAKES USE OF R3 INSTEAD (OLD CODE COMPATIBILITY)   GP08250
.*
         PUSH  USING
         AIF   ('&R3' EQ 'YES').OLDR3
&NM      MACPARM MODE=LBL    EXPAND LABEL IF NEEDED             GP08254
&LOOP    BAL   R14,EXWUCBLK  GET A UCB
         BXH   R15,R15,&ERR  END OF CHASE
         NUSE  UCB,R1
         AGO   .COMR1R3
.OLDR3   ANOP  ,
&NM      SLR   R3,R3         START FRESH
&LOOP    LR    R1,R3         LOAD PARAMETER REGISTER            GP08250
         BAL   R14,EXWUCBLK  GET A UCB
         BXH   R15,R15,&ERR  END OF CHASE
         LR    R3,R1         LOAD USER'S REGISTER               GP08250
         NUSE  UCB,R3
.COMR1R3 AIF   ('&DEV' EQ '').NTDV                              GP13265
         CLC   UCBCHAN,&DEV  SAME DEVICE NUMBER ?
         BNE   &LOOP         NO; TRY AGAIN
.NTDV    AIF   ('&NAME' EQ '').NTNM
         CLC   UCBNAME,&NAME  SAVE NAME ?
         BNE   &LOOP         NO; TRY AGAIN
.NTNM    AIF   ('&ONLINE' NE 'YES').NTON
         TM    UCBSTAT,UCBONLI  ON-LINE ?
         BZ    &LOOP         NO; TRY AGAIN
.NTON    AIF   ('&T3' EQ '').NTT3
         TM    UCBTBYT3,&T3  CORRECT TYPE?
         BZ    &LOOP         NO; TRY NEXT ONE
.NTT3    AIF   ('&T4' EQ '').NTT4
         CLI   UCBTBYT4,&T4  CORRECT SUB TYPE?
         BNE   &LOOP         NO; TRY NEXT ONE
.NTT4    AIF   ('&VOL' EQ '').NTVS
         AIF   ('&T3' NE '').NOGOOF
         TM    UCBTBYT3,UCB3TAPE+UCB3DASD  DEVICE WITH SERIAL ?
         BM    &LOOP         NO; TRY NEXT ONE
.NOGOOF  CLC   UCBVOLI,&VOL  SAME VOLUME ?
         BNE   &LOOP
.NTVS    ANOP  ,
         POP   USING
         AIF   ('&R3' NE 'YES').MEND
         USE   UCB,R3
.MEND    MEND
