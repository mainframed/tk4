         MACRO ,
&NM      MVC2  &TO,&FR                                      NEW GP08096
.*
.*   THIS LITTLE GOODY IS BASED ON AN IDEA OFFERED IN THE ASSEMBLER
.*   LIST, NAMELY THAT THERE SHOULD BE A MOVE INSTRUCTION WHOSE DEFAULT
.*   LENGTH IS THAT OF THE SECOND OPERAND.
.*
         GBLA  &MACPLEN      RETURN SIGNIFICANT LENGTH OF STRING
         GBLB  &MACPNUL      TRUE IF NULL STRING
         GBLB  &MACQUOT      TRUE IF ORIGINAL WAS QUOTED
         GBLB  &MACPERR      TRUE IF ERROR
         GBLC  &MACQSTR      RETURN QUOTED STRING
         LCLA  &K,&I,&J
         LCLC  &L,&LF,&RT
&L       SETC  'L'''
         AIF   ('&TO' EQ '').NOTO
         AIF   ('&FR' EQ '').NOFR
&K       SETA  K'&TO
         MACQOLIT &FR        CHECK FOR QUOTED STRING            GP08130
         AIF   (&MACQUOT).QUOTED                                GP08130
         AIF   ('&TO'(&K,1) NE ')').SIMPLE
.*  PARSE xxx ( yyy ) - CHANGE TO xxx ( L'fr , yyy )
&I       SETA  &K
&J       SETA  1
.LOOP    AIF   (&I LE 1).ERRTO
&I       SETA  &I-1
         AIF   ('&TO'(&I,1) EQ '(').HAVLEFT
         AIF   ('&TO'(&I,1) EQ ')').HAVRITE
         AGO   .LOOP
.HAVRITE ANOP  ,
&J       SETA  &J+1
         AGO   .LOOP
.HAVLEFT ANOP  ,
&J       SETA  &J-1
         AIF   (&J GT 0).LOOP
         AIF   (&I LE 1).SIMPLE   ((XYZ-RST))
&LF      SETC  '&TO'(1,&I)
&RT      SETC  '&TO'(&I+1,&K-&I)
&NM      MVC   &LF&L&FR,&RT,&FR
         MEXIT ,
.QUOTED  AIF   ('&TO'(&K,1) NE ')').QSIMP                       GP08130
.*  PARSE xxx ( yyy ) - CHANGE TO xxx ( L'fr , yyy )            GP08130
&I       SETA  &K                                               GP08130
&J       SETA  1                                                GP08130
.QLOOP   AIF   (&I LE 1).ERRTO                                  GP08130
&I       SETA  &I-1                                             GP08130
         AIF   ('&TO'(&I,1) EQ '(').QLEFT                       GP08130
         AIF   ('&TO'(&I,1) EQ ')').QRITE                       GP08130
         AGO   .QLOOP                                           GP08130
.QRITE   ANOP  ,                                                GP08130
&J       SETA  &J+1                                             GP08130
         AGO   .QLOOP                                           GP08130
.QLEFT   ANOP  ,                                                GP08130
&J       SETA  &J-1                                             GP08130
         AIF   (&J GT 0).QLOOP                                  GP08130
         AIF   (&I LE 1).QSIMP   ((XYZ-RST))                    GP08130
&LF      SETC  '&TO'(1,&I)                                      GP08130
&RT      SETC  '&TO'(&I+1,&K-&I)                                GP08130
&NM      MVC   &LF&MACPLEN,&RT,&MACQSTR                         GP08130
         MEXIT ,                                                GP08130
.QSIMP   ANOP  ,                                                GP08130
&NM      MVC   &TO.(&MACPLEN),&MACQSTR                          GP08130
         MEXIT ,                                                GP08130
.ERRTO   MNOTE 8,'MVC2: TO OPERAND IS MALFORMED'
         MEXIT ,
.SIMPLE  ANOP  ,
&NM      MVC   &TO.(&L&FR),&FR
         MEXIT ,
.NOTO    MNOTE 8,'MVC2: TO OPERAND MISSING'
         MEXIT ,
.NOFR    MNOTE 8,'MVC2: FROM OPERAND MISSING'
         MEND  ,
