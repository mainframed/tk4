         MACRO
&NM    MACMAPHD &OP1,&DSECT=,&PFX=,&SFX=,&DFLT=,&NULL=SKIP      GP03144
         LCLC  &FOOF
.*--------------------------------------------------------------------*
.*  MACMAPHD IS USED AS AN INNER MACRO TO DEFINE DS/DSECT/CSECT       *
.*  FOR CONTROL BLOCK MAPPINGS                                        *
.*                                                                    *
.*  &NM IS THE (OPTIONAL) BLOCK NAME                                  *
.*  &OP1 IS AN ALTERNATE NAME WHEN &NM IS VOID                        *
.*  &DFLT IS A DEFAULT NAME WHEN BOTH &NM AND &OP1 ARE OMITTED        *
.*    WHEN BOTH ARE VOID, AND DSECT= IS VOID, AND NULL=SKIP, THEN     *
.*    NOTHING IS EXPANDED                                             *
.*                                                                    *
.*  &PFX AND &SFX DEFINE AN OPTIONAL PREFIX AND SUFFIX THAT WILL BE   *
.*    APPLIED TO ALL NAMES EXCEPT &NM                                 *
.*                                                                    *
.*  &DSECT - WHEN VOID OR =YES, GENERATES DSECT                       *
.*    WHEN =NO, EXPANDS DS 0D (UNLESS ALL LABELS VOID AND NULL-SKIP)  *
.*    OTHERWISE USED AS IS (FOR RSECT AND CSECT)                      *
.*--------------------------------------------------------------------*
         LCLC  &LBL          EXPANDED NAME FIELD
         LCLB  &DS,&LBF
         LCLC  &OPCD         DSECT, DS, OR ?
         LCLC  &OPER         MATCHING OPERAND (, OR 0D)
         AIF   ('&NM' EQ '').NONAME
&LBF     SETB  1             USER SUPPLIED LABEL
&LBL     SETC  '&NM'
         AGO   .TYPETST
.NONAME  AIF   ('&OP1' EQ '').NOALTNM
&LBF     SETB  1             USER SUPPLIED LABEL
&LBL     SETC  '&PFX'.'&OP1'.'&SFX'
         AGO   .TYPETST
.NOALTNM AIF   ('&DFLT' NE '').GOODFLT
&FOOF    SETC  '&SYSNDX'
&LBL     SETC  '&PFX'.'MAP'.'&FOOF'.'&SFX'
         AIF   ('&DSECT' NE 'NO' AND '&DSECT' NE 'DS').NODFLT
         AIF   (NOT &LBF AND '&NULL' EQ 'SKIP').MEND
.NODFLT  MNOTE 4,'MACMAPHD: LABEL, OPERAND, AND DFLT ALL VOID'
         AGO   .TYPETST
.GOODFLT ANOP  ,
&LBL     SETC  '&PFX'.'&DFLT'.'&SFX'
.TYPETST AIF   ('&DSECT' NE 'NO' AND '&DSECT' NE 'DS').NOTDS
         AIF   (NOT &LBF AND '&NULL' EQ 'SKIP').MEND
&OPCD    SETC  'DS'
&OPER    SETC  '0D'
         AGO   .EXPAND
.NOTDS   AIF   ('&DSECT' NE ''   AND '&DSECT' NE 'YES').NOTDSY
&OPCD    SETC  'DSECT'
&OPER    SETC  ','
         AGO   .EXPAND
.NOTDSY  ANOP  ,
&OPCD    SETC  '&DSECT'
&OPER    SETC  ','
.EXPAND  ANOP  ,
&LBL     &OPCD &OPER
.MEND    MEND  ,
