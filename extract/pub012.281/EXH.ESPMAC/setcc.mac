         MACRO ,
&N       SETCC &CODE,&REAS,&RESULT=                    NEW 2003.091 GYP
.*
.*   SETCC replaces a condition code, and optional reason code,
.*   provided it is higher than the current one.
.*
.*   For unconditional setting see MVICC and OICC
.*
.*   &CODE MAY NOT SPECIFY A REGISTER
.*
         GBLC  &ZZCCNAM
         LCLC  &L,&T
&L       SETC  'L'''
&T       SETC  'ZZ'.'&SYSNDX'.'X'
         AIF   ('&RESULT' EQ '').NONEW
&ZZCCNAM SETC  '&RESULT'
.NONEW   AIF   ('&ZZCCNAM' NE '').NODEF
         MNOTE *,'SETCC: RESULT= NOT SPECIFIED - DEFAULTED TO RETCODE'
&ZZCCNAM SETC  'RETCODE'
.NODEF   ANOP  ,
&N MACPARM &ZZCCNAM+&L&ZZCCNAM-1,&CODE,OP=CLI,OPR=CLM,NULL=SKIP,       *
               MODE=REV
         MACPARM &T,OP=BNL,MODE=ONE   NO CHANGE UNLESS HIGHER CODE
   MACPARM &ZZCCNAM+&L&ZZCCNAM-1,&CODE,OP=MVI,OPR=STC,NULL=SKIP,       *
               MODE=REV
   MACPARM &ZZCCNAM+&L&ZZCCNAM+3,&REAS,OP=MVI,OPR=STC,NULL=SKIP,       *
               MODE=REV
&T       MACPARM MODE=LABEL
         MEND  ,
