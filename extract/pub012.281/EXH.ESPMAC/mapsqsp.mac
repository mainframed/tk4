         MACRO
         MAPSQSP &DSECT=,&P=SQ
         GBLA  &REL,&QENTSIZ,&JITSIZE,&PITSIZE                   84302
         GBLC  &CRT,&SYSTEM                                      82157
         LCLC  &L1,&L2,&L3,&L4,&L5,&L6,&L7,&L8,&L9
         LCLC  &Q
         LCLC  &E1,&E2,&E3,&E4,&E5
&Q       SETC  '&P'
         AIF   ('&DSECT' EQ 'NO').NOS
         AIF   ('&DSECT' EQ 'C').NOQ
EXHBSQSP DSECT
&Q       SETC  ''
         AGO   .NOS
.NOQ     ANOP
&Q       SETC  ''
EXHBSQSP DS    0D
.NOS     ANOP
&P.SAVE  DC    9D'0'         SAVE AREA FOR ALL ROUTINES
         SPACE 1
&P.TE    DC    A(0)          START OF PSEUDO GRAPHICS 'TE'
&P.TEREB DC    A(0)          REB ADDRESS, = 0
&P.TETCB DC    A(0)          ADDR. OF CURRENT EXHIBIT TCB
&P.TECAN DC    A(0)          = 0
&P.TEUSE DC    A(0)          TE USE COUNT
&P.TEFLGS DC   0A(0),X'80',AL3(0)     FLAG FOR BASIC ATTN HANDLING
&P.TEIRB DC    A(&P.IRBSAV)  IRB ADDRESS                        GP09115
&P.TEIR  DC    A(300)        NOT T.E. - USED TO SET 'LOOP' TIMER INTVL
         SPACE 2
         USING &P.SAVE,R13
         USING EXHBWORK,R11
         USING EXHBWENT,R10
&P.SSMON DS    0H            SSM OBSOLETE                       GP15266
&P.SSMOF BR    R14           SSM OBSOLETE                       GP15266
         SPACE 2
         PUSH  USING                                            GP01013
         DROP  ,                                                GP01013
         USING &P.SPIEX,R15  PROGRAM INTERRUPT EXIT
&P.SPIEX OC    &P.SPIAD,&P.SPIAD    EXIT ADDRESS SET ?
         BNZ   &P.SPIXE      YES - HONOR IT
         LR    R2,R1         SAVE PIE ADDRESS FOR DUMP
         L     R1,&P.SPABC   LOAD C00 ABEND CODE
         SVC   13            ABEND
&P.SPIXE MVC   9(3,R1),&P.SPIAD+1      RESTART ADDRESS
         XC    &P.SPICA+4(8),&P.SPICA+4  CLEAR MASK & EXIT ADDRESS
         BR    R14           EXIT AND RESTART
         POP   USING                                            GP01013
         SPACE 1             NOTE X'80' IN SPICA TO SUPPRESS STAE MSGS
&P.SPICA DC    A(0),X'0E000000'  AL3(SQSPIEX) - PICA             82165
&P.SPIAD DC    A(0)          RESTART ADDRESS - MUST FOLLOW PICA
&P.SPABC DC    X'80C00000'   SPIE EXIT ABEND CODE
.*.SSMONM DC   F'2'          ENABLE INTERRUPTS  (MODESET)       GP15266
.*.SSMOFM DC   F'3'          DISABLE INTERRUPTS (MODESET)       GP15266
         SPACE 1                                                  *TSM*
&P.IRBPFX DC   8D'0'         MVS RB PREFIX                       82157
&P.IRBSAV DC   A(0)          IRB FOR 'GAM' ATTN                  82157
&P.IRAPSW DC   A(0)          APSW                                82157
&P.IRWTSZ DC   AL1(0,(&P.OUTLM+11-&P.IRBPFX)/8)  WAIT / SIZE FIELD
&P.IRSTAB DC   X'400C'       IRB CONTROL FLAGS, IQE, NO FREE, CHAIN NXA
&P.IREP  DC    A(0)          IRB ENTRY POINT (ATTNPOST IN EXHBWORK)
&P.IROPSW DC   XL8'070C000000000000'   OLD PSW                   82157
&P.IRBIQE DC   A(0)          IQE LINK FIELD                      82157
&P.IRLINK DC   A(0)          IRB LINK FIELD                      82157
&P.IRGRS DC    16A(0)        SAVE REGISTERS                      82157
&P.IRNXA DC    A(&P.IQE)     NEXT AVAILABLE IQE ADDRESS         GP09115
         SPACE 1
&P.IQE   DC    A(0)          IQE LINK FIELD                      82157
&P.IQPARM DC   A(&P.IRBPFX)  PARM FIELD FROM ATTN HANDLER (CRUD) 82157
&P.IQIRB DC    A(&P.IRBSAV)  IRB ADDR. FOR THIS IQE             GP09115
&P.IQTCB DC    A(0)          TCB ADDRESS FOR THIS IRB            82157
&P.DCBQ  DC    A(0)          DCB ADDRESS                         82157
&P.OUTLM DC    A(0)          OUTLIM ADDRESS                      82157
         DC    6A(0)           SPARES                            82157
*CRT     DCB   MACRF=(E),DDNAME=IEFRDER   CRT DCB FOR EXCP       82199
&Q.CRT   DC    10A(0),C'IEFR'  COMPENSATE FOR MVS DCB PTF        82199
&Q.CRTDEB DC   CL4'DER',AL1(2,0,128,0),A(0)                      82199
.IQECOM  DC    XL4'0'        SPARE                               82199
&P.STMCK1 DC   A(0)          MVT TIMECHEK ECB                    79345
&P.STMCK2 DC   A(0)          MVT TIMECHEK FLAG                   79345
&P.SAVWT DC    A(3000)       GLOBAL DELAY TIME (30 SECONDS)     GP04234
&P.SAVPR DC    X'0'          GLOBAL PRIVILEGE FLAGS
&P.SAVPO DC    X'0'          ADDITIONAL OPTIONS                  78216
&P.SAFIO EQU   X'80'         DELAY IF INTERVENTION REQ.          78216
&P.SAFIG EQU   X'40'         DELAY IF ANY ERROR                  78216
&P.SAFDL EQU   X'C0'           DISCONNECTABLE 'LOCAL'           GP10213
&P.SAFSH EQU   X'20'         MULTIPLE INPUT CRTS PER TASK        78277
&P.SAPRM EQU   X'10'         WRITE PROMPT ON DISPLAY CYCLE       87349
&P.SNGAM EQU   X'08'         GAMES NOT PERMITTED ON THIS CRT     79194
&P.SADSC EQU   X'04'         END ON ERROR (TN3270 DISCONNECT)   GP11230
&P.SAMUL EQU   X'02'           MULTI-TASKING ENVIRONMENT         86344
&P.SAFVM EQU   X'01'         RUNNING UNDER VM                    78216
         AIF   ('&SYSTEM' NE 'MVS').OMVS                         82199
&P.DAOPT DC    X'00'         DDA OPTIONS                         82199
&P.DASDR EQU   X'80'           SUPPRESS DRAINED INITIATORS       82199
&P.DASID EQU   X'40'           SUPPRESS IDLE INITIATORS          82199
&P.DASTC EQU   X'20'           SUPPRESS START TASKS              82199
&P.DATSU EQU   X'10'           SUPPRESS TS USERS                 82199
&P.DAPRM EQU   X'08'           SUPPRESS PERMANENT START TASKS    85055
&P.DAJOB EQU   X'04'           CPU TIME FOR JOB, NOT STEP       GP08093
&P.DAENQ EQU   X'02'           SUPPRESS ENQ, ETC. HANG CHECKS   GP08093
&P.ZZIDL EQU   X'01'           SUPPRESS EXHDZZZZ ID LINE        GP08093
&P.SEDFG DC    X'00'         EDIT OPTIONS FOR SQEXCP             87004
&P.SENED EQU   X'80'           BYPASS EDITING                    87004
&P.SENCM EQU   X'40'           BYPASS BUFFER COMPRESSION         87004
&P.SENCT EQU   X'20'           BYPASS COMPR. ON NEXT WRITE ONLY  87004
&P.SENCO EQU   X'10'           BYPASS COLOR MAPPING ON NEXT WRT  87172
&P.SEMGE EQU   X'08'           BYPASS GE CHARS IN TRANSLATE
&P.SENSF EQU   X'04'           BYPASS SF×PROT INSERTION          90239
&P.SELOW EQU   X'01'           KEEP INPUT IN MIXED CASE         GP10213
&P.SEMFG DC    X'00'         MISCELLANEOUS FLAGS                 91102
&P.SEMSF EQU   X'20'           3270DS STRUCT. FIELD SUPPORT      91102
&P.SEM3P EQU   X'04'           TRIPLE-PLANE SYMBOLS AVAIL.       91102
&P.SEM16 EQU   X'02'           16-BIT ADDRESSING SUPPORTED       92314
&P.SEM14 EQU   X'01'           14-BIT ADDRESSING SUPPORTED       91102
&P.SECFG DC    X'00'         SECURITY FLAGS                      87349
&P.SECNO EQU   X'80'           NO CHECKING OF ANY KIND          GP15114
&P.SECRA EQU   X'40'           SIGNON WITH RACF/RAKF            GP13272
&P.SECTS EQU   X'20'           SIGNON WITH TSO (SYS1.UADS)      GP13272
&P.SECMD EQU   X'10'           SIGNON REQ. FOR ANY CONV.         87349
&P.SECRQ EQU   X'08'           SIGNON RESTRICTED TO SYSTEMS      87349
&P.SECON EQU   X'01'           USER SIGNED ON                    87349
&P.DQOPT DC    X'80'         DDQ OPTIONS                        GP09012
&P.DQJ2# EQU   X'80'           SUPPRESS JES2 PCE+DCT COUNTS     GP09012
&P.ENVFG DC    X'00'         ENVIRONMENT FLAG                   GP10041
&P.EF380 EQU   X'80'           HERCULES W/380 MODS              GP10041
&P.EFHER EQU   X'40'           HERCULES                         GP10041
.*.EF    EQU   X'02'           --RESERVED--                     GP10041
.*.EF    EQU   X'01'           --RESERVED--                     GP10041
.*                                                              GP10041
         DC    A(0)          --RESERVED--                       GP09012
.OMVS    SPACE 1                                                 82199
&Q.WCCW  CCW   5,0,X'20',961     LINE ADDRESSING WRITE CCW
&Q.WCCW2 CCW   1,0,X'20',960    NO LINE ADDRESSING, USED WITH ERASE
         AIF   ('&P' EQ 'SQ').CNORM
&L1 SETC '&P'.'RCCW'
&L2 SETC '&P'.'RCC1'
&L3 SETC '&P'.'RCC2'
&L4 SETC '&P'.'PGLN'
&L5 SETC '&P'.'PCCA'
&L6 SETC '&P'.'RCCA'
&L7 SETC '&P'.'REPL'
&L8 SETC '&P'.'REPA'
&L9 SETC '&P'.'BLKS'
         AGO   .CSP
.CNORM   ANOP
&L1      SETC 'READCCW'
&L2      SETC 'RCCW1'
&L3      SETC 'RCCW2'
&L4      SETC 'PAGELEN'
&L5      SETC 'PAGECCWA'
&L6      SETC 'RITECCWA'
&L7      SETC 'REPLY'
&L8      SETC 'REPLAST'
&L9      SETC 'BLANKS'
.CSP     AIF   ('&CRT' EQ '3270').CSP7
&L1      CCW   2,0,X'20',80      READ CCW
&L2      CCW   7,0,X'60',2     WRITE CCW 1
&L3      CCW   1,0,X'20',0     WRITE CCW 2
&L4      EQU   *-2           LENGTH OF WRITE
&L1      SETC  '80'
         AGO   .CSP67
.CSP7    ANOP
&L1      CCW   X'0B',0,X'60',1     OVERLAP DEVICE TO CONTROL UNIT
         CCW   6,0,X'20',86     READ MODIFIED
&L2      EQU   *,8           FIRST WRITE CCW
&L3      CCW   5,0,X'20',0   ERASE/WRITE CCW
&L4      EQU   *-2,2         LENGTH OF WRITE
&L1      SETC  '86'
         SPACE 1
&P.PFK   DC    X'11',CL5'$F   ',X'12',CL5'$I   ',X'13',CL5'$JOB '
         DC    X'14',CL5'$N   ',X'15',CL5'$U   ',X'16',CL5'$MSG '
         DC    X'17',CL5'CON -',X'18',CL5'STOR ',X'19',CL5'ENQ  '
         DC    X'1A',CL5'PIO  ',X'1B',CL5'U M  ',X'1C',CL5'U D,T'
         AIF   ('&SYSTEM' NE 'MVS').AMVS78                       82199
         DC    X'21',CL5'$J   ',X'22',CL5'ASCB ',X'23',CL5'$DSN '
         DC    X'24',CL5'$LA  ',X'25',CL5'NET  ',X'26',CL5'$LOG '
         DC    X'27',CL5'DCO  ',X'28',CL5'PFK  ',X'29',CL5'RSV  '
         DC    X'2A',CL5'PAT  ',X'2B',CL5'SMC  ',X'2C',CL5'U CRT'
.AMVS78  ANOP  ,                                                 82199
&P.PAK   DC    X'10',CL5'ASI *',X'0C',CL5'^PA1 ',X'0B',CL5'^PA3 '
         DC    X'0E',CL5'^CAN'
         AIF   ('&SYSTEM' NE 'MVS').BMVS78                       82199
         DC    X'1E',CL5'PEN',X'06',CL5'OID',X'07',CL5'MSR'      82199
.BMVS78  DC    X'0D',CL5'^CLR '                                  82199
&P.PAKE  DC    X'1D',CL5' '     DEFAULT - ENTER
         SPACE 1
&P.UCMID DC    X'00'         CURRENT COMMAND'S UCM ID           GP03071
&P.UCMST DC    X'00'         START COMMAND'S UCM ID             GP03071
         SPACE 1
.CSP67   ANOP
&L5      DC    A(0)          ADDR OF FIRST PAGE WRITE CCW
&L6      DC    A(0)          ADDR. OF PARTIAL WRITE CCW
&L7      DC    CL&L1' '      REPLY INPUT BUFFER
&L8      EQU   *-1           LAST INPUT BYTE
&L9      DC    CL&L1' '      BLANKS
         DS    0F
         SPACE 1
&P.IOB   DC    X'42',AL3(0)    START OF IOB
&P.IOBECB DC   A(&Q.WECB)      ECB ADDRESS
&P.IOBCSW DC   XL8'0'        CHANNEL STATUS WORD
&P.IOBCCW DC   A(&Q.WCCW)      CCW ADDRESS
&P.IOBCRT DC   A(&Q.CRT,0,0)     DCB ADDRESS
&P.IOBCR# DC   A(0,0)        DEVICE INDEX, OTHER JUNK
         SPACE 1
&Q.WECB  DC    A(0)          I/O ECB
         AIF   ('&P' EQ 'SQ').EXH
         DC    11A(0)        HASP / DYNWORK
         AGO   .NEX
.EXH     ANOP
HASPHCT  DC    A(0)          ADDR. OF HASP COMMUNICATIONS TABLE OR 0
HASPFULL DC    A(0)          DISPL. TO HASP QUEUE SIZE COUNTERS
HASPPIT  DC    A(0)          HASP FIRST PIT ADDRESS
HASPJQEL DC    H'0'          HASP QUESIZE                         75305
HASPJITL DC    H'0'          HASP JITSIZE                         75305
HASPPITL DC    H'0'          HASP PIT SIZE                        75305
HASPJOB# DC    H'0'          HASP JOB COUNT (&MAXJOBS)            75293
HASPTABE EQU   *             END OF HASP POINTERS
ASPVT    DC    A(0)          ASP VECTOR TABLE POINTER             75293
&P.TRANS DC    A(0)          TRANSLATE MODULE ADDRESS            79299
DYNWORK  DC    4A(0)         MODULE WORK AREA
.NEX     ANOP
         SPACE 2
         AIF   ('&P' EQ 'SQ').ENORM
&E1      SETC  '&P'.'ENTR'
&E2      SETC  '&P'.'WHEE'
&E3      SETC  '&P'.'XASI'
&E4      SETC  '&P'.'XCTL'
         AGO   .ESP
.ENORM   ANOP
&E1      SETC  'ENTRY'
&E2      SETC  'WHEEL'
&E3      SETC  'XCTLASIS'
&E4      SETC  'XCTL'
.ESP     ANOP
         SPACE 1
*        SUBROUTINE TO CALCULATE DISPLAY SEGMENT VALUES
*
&E1      L     R1,BUFAD      GET DISPLAY BUFFER START ADDRESS
         LR    R2,R1         DUPLICATE
         AH    R1,EXHFSTRT    ADD DISPLACEMENT TO FIRST AVAIL. BYTE
         AH    R2,EXHFEND    AND TO LAST BYTE
         LA    R3,1(,R2)     ONE BEYOND
         SR    R3,R1         LESS START ADDR. = LENGTH AVAILABLE
         CH    R3,H12        MORE THAN 12 BYTES ?
         BHR   R14           YES, RETURN
         SPACE 2
*        XCTL TO NEXT DISPLAY MODULE
*
&E2      LA    R10,EXHFLEN(,R10)     GET BLDL LIST FOR NEXT MODULE
&E3      CLC   LEVEL,EXHFTIM     MODULE TO BE INVOKED THIS CYCLE ?
         BL    &E2        NO, CHECK NEXT ONE
         SPACE 1
*        XCTL  DE=(R10)      XCTL TO NEXT MODULE
&E4      XCTL  DE=(R10)      XCTL TO NEXT MODULE
         AIF   ('&SYSTEM' NE 'MVS' OR &REL LT 102).NOLXCTL       84302
         ORG   &E4+28        ALLOW FOR VARIOUS XCTL MACROS       84302
.NOLXCTL SPACE 1
&P.EXCPS STM   R0,R15,&P.SAVER  SAVE ALL REGISTERS               83324
         LA    R1,&P.IOB     GET STANDARD IOB                    83345
         L     R15,&P.IOBIO  GET I/O ROUTINE ADDRESS             83324
         BASR  R14,R15       INVOKE IT                           83324
&P.EXCPX LM    R0,R15,&P.SAVER  RESTORE REGISTERS                83324
         BR    R14           RETURN TO CALLER                    83324
&P.DFLIO EXCP  (1)           DEFAULT I/O REQUEST                 83324
         BR    R14           RETURN TO CALLER                    83324
&P.XATTN STM   R0,R15,&P.SAVER  SAVE ALL REGISTERS               83324
         L     R15,&P.ATTNR  GET EXTERNAL ATTENTION ROUTINE      83324
         BASR  R14,R15       INVOKE IT                           83324
         LM    R0,R15,&P.SAVER  RESTORE REGISTERS                83324
         BR    R14                                               83324
         SPACE 1                                                GP09066
&P.BUGFG DC    X'00'         SPECIAL DEBUG FLAG                 GP09066
&P.BUGA  EQU   X'80'           EXHA MODULES                     GP09066
&P.BUGC  EQU   X'40'           EXHC MODULES                     GP09066
&P.BUGD  EQU   X'20'           EXHD MODULES                     GP09066
.*.BUG   EQU   X'10'           EXH  MODULES                     GP09066
.*.BUG   EQU   X'08'           EXH  MODULES                     GP09066
.*.BUG   EQU   X'04'           EXH  MODULES                     GP09066
.*.BUG   EQU   X'02'           EXH  MODULES                     GP09066
&P.BUGT  EQU   X'01'           START PGMTRACE                   GP15260
         DC    X'00'         RESERVED                           GP09066
&P.SAVER DC    16A(0)        EXTENDED FUNCTION SAVE AREA         83324
&P.IOBIO DC    A(&P.DFLIO)   I/O ROUTINE ADDRESS                 83324
&P.ATTNR DC    A(0)          ATTENTION ROUTINE ADDRESS           83324
&P.LODCB DC    A(0)          LOAD LIBRARY DCB                    83324
&P.CECB  DC    A(0)          FAKE CANCEL ECB                     83324
&P.ECBX  DC    A(0)          SUB-TASK ECB                        83324
&P.TCBX  DC    A(0)          SUB-TASK TCB                        83324
&P.TJID  DC    Y(0)          TJID IF UNDER TSO                   83324
&P.TPK   DC    X'00'         ORIGINAL PROTECT KEY                83324
&P.SIMFG DC    X'00'         EXTENDED FLAGS                      83324
&P.SICRT EQU   X'80'           OUTPUT IS TO A CRT                83324
&P.SIPRT EQU   X'40'           PRINTED OUTPUT REQUESTED          83324
&P.SITSO EQU   X'20'           TSO FLAG (SAME AS OPTSO)          83324
&P.SIVTM EQU   X'10'           RUNNING UNDER VTAM                83324
&P.SIFSC EQU   X'08'           FULL-SCREEN I/O SUPPORT PRESENT   83324
&P.SIF78 EQU   X'04'           EXTENDED HIGH-LIGHTING AVAILABLE  83324
&P.SIF79 EQU   X'02'           SEVEN-COLOR SUPPORT AVAILABLE     83324
&P.SINPR EQU   X'01'           PRINT FUNCTION PERMANENTLY DISABLED
&P.WRKA  DC    A(0)          EXTENDED WORK AREA ADDRESS          83324
&P.WRKL  DC    A(0)          EXTENDED WORK AREA SP/LEN           83324
&P.STAXP DC    5A(0)         STAX AND WORK AREA                  83324
         DC    A(0)            RESERVED                          83324
&P.TRGE  DC    A(0)          TRANSLATE WITH GE CHARACTERS       GP99106
&P.INPAD DC    A(0)          SPECIAL COMMAND SCANNER             84328
&P.CSCBA DC    A(0)          CSCB ADDRESS (FAKE FOR M/T)         86349
&P.BLDLA DC    A(&P.BLDL)    BLDL ROUTINE                        86349
&P.BLDLS L     R15,&P.BLDLA                                      86349
         BR    R15           INVOKE THE BLDL CODE                86349
&P.ATIX  DC    X'00'         UCB ATTN INDEX                      93355
&P.RACF  DC    X'00'         RACF CHECK RESULTS                 GP15143
.*.RF    EQU                                                    GP15143
.*.RF    EQU                                                    GP15143
.*.RF    EQU                                                    GP15143
.*.RF    EQU                                                    GP15143
&P.RFCON EQU   X'08'           CONTROL                          GP15143
&P.RFALT EQU   X'04'           ALTER AUTHORITY                  GP15143
&P.RFUP  EQU   X'02'           WRITE/UPDATE AUTHORITY           GP15143
&P.RFRD  EQU   X'01'           READ AUTHORITY                   GP15143
&P.OJLB  DC    A(0)          ORIGINAL STEPLIB ADDRESS            87280
&P.LDDCB DC    A(0)          CHAIN OF LOADXXXX DCBS              87280
&P.@WORK DC    A(0)          ADDRESS OF EXHBWORK AREA            93185
&P.@BUFF DC    A(0)          ADDRESS OF DISPLAY BUFFER           93185
&P.@BUFL DC    A(0)              SIZE OF BUFFER                  93185
         DC    A(0)            RESERVED                          83324
         DC    A(0)            RESERVED                          83324
         DC    A(0)            RESERVED                          83324
         DC    A(0)            RESERVED                          83324
         DC    A(0)            RESERVED                          83324
         DC    A(0)            RESERVED                          83324
&P.BLDL  STM   R0,R15,&P.SAVER  SAVE REGISTERS                   86349
         LR    R2,R0         PRESERVE USER'S LIST POINTER        86349
         L     R1,&P.LODCB   GET STEPLIB/LOADLIB DCB             86349
*        BLDL  (1),(0)                                           86349
         BLDL  (1),(0)                                           86349
         TM    &P.SAVPO,&P.SAMUL  MULTI-TASKING ?                86349
         BZ    *+8           NO                                  86349
         NI    4+22(R2),255-X'C0'  NOT RENT, NOT REUS            86349
         OI    4+32(R2),X'20'  PDS2FTB1,PDS2PAGA  PAGE ALIGN    GP04234
         LM    R0,R14,&P.SAVER  RESTORE ALL BUT COND. CODE       86349
         BR    R14                                               86349
         AIF   ('&DSECT' EQ 'NO').MEXIT
&P.SPLEN EQU   *-EXHBSQSP     LENGTH FOR GETMAIN
.MEXIT   MEND
