         MACRO
&NM      FDLITEM &ARGS
.*
.*   Build one or more display item definitions for the SCLINE service
.*
.*   Use after  FDLINE ...END=name                    1/n
.*   FDLITEM label:,(field attribs),lbl:,'text'...    2/n
.*   DC or whatever                                   m/n
.*   FDLINE *END             generate closing name    n/n
.*
.*   ATTRIBUTES:
.*
.*   IN, OUT, INT(ense out),
.*   RED, BLUE, GREEN, CYAN/TURQ(oise), PINK/MAGENTA, WHITE, YELLOW
.*   BLACK (non-display) - forces INput mode                    GP09353
.*
.*   This macro generates neither the line nor flag fields; use FDLINE
.*   for those
.*
.*   When FDLITEM has a label, the label will be expanded after the
.*   data, to produce a single item with the correct length.
.*
         GBLC  &ZZZLEND      PRIOR/NEXT END LABEL
         GBLC  &ZZZFLAB      NEXT FIELD LABEL
         GBLC  &ZZZFLAN      FDLINE  LABEL                      GP09358
         LCLC  &LABEL,&C,&FLAB,&D,&TLB                          GP15257
         LCLB  &F0,&F1,&F2,&F3,&F4,&F5,&F6,&F7
         LCLB  &INP,&INT,&BLACK                                 GP09353
         LCLA  &I,&J,&K,&L,&M,&N
         AIF   ('&ARGS' EQ '*END').CLOSER
&N       SETA  N'&SYSLIST
         AIF   ('&NM' EQ '').DEFLBL                             GP15257
&LABEL   SETC  'ZZ'.'&SYSNDX'                                   GP15257
&TLB     SETC  '&LABEL'                                         GP15257
.DEFLBL  AIF   (&N GT 0).LOOP     HAVE MULTIPLE ARGUMENTS
         MNOTE 0,'FDLITEM: NO ARGUMENTS SUPPLIED'
         AGO   .DONE
.LOOP    AIF   (&I GE &N).DONE
&I       SETA  &I+1
&C       SETC  '&SYSLIST(&I)'
         AIF   ('&C' EQ '').LOOP
         AIF   ('&C'(1,1) EQ '(').FIELD
         AIF   ('&C'(1,1) EQ '''').STRING
         AIF   ('&C'(K'&C,1) EQ ':').LABEL
         AIF   ('&C'(K'&C,1) EQ '-').OFFSET
         AIF   ('&C'(1,1) EQ 'C').COUNT
         AIF   ('&C'(1,1) EQ 'X').HEXER
         AIF   ('&C'(1,1) GE '0' AND '&C'(1,1) LE '9').COUNT
.ARGBAD  MNOTE 8,'FDLITEM: ARGUMENT &I INVALID - &C NOT A VALID ENTRY'
         AGO   .LOOP
.*
.STRING  ANOP  ,
&LABEL   DC    C&C
&LABEL   SETC  ''
         AGO   .LOOP
.*
.COUNT   AIF   ('&C'(K'&C,1) NE '''').COUNTS
&LABEL   DC    &C
&LABEL   SETC  ''
         AGO   .LOOP
.COUNTS  ANOP  ,
&LABEL   DC    &C' '
&LABEL   SETC  ''
         AGO   .LOOP
.HEXER   AIF   ('&C'(K'&C,1) NE '''').HEXED
&LABEL   DC    &C
&LABEL   SETC  ''
         AGO   .LOOP
.HEXED   ANOP  ,
&LABEL   DC    &C'0'
&LABEL   SETC  ''
         AGO   .LOOP
.*
.FIELD   ANOP  ,
&J       SETA  0
&L       SETA  N'&SYSLIST(&I)  NUMBER OF SUBARGUMENTS
&F0      SETB  0             MUST BE OFF
&F1      SETB  0             MUST BE OFF (RANGE 01-3F)
&F2      SETB  0             HIGHLIGHTING; NOT COLOR CODE       GP15257
&F3      SETB  0        1 0  OUTPUT  ;  1 1  OUT INTENSE
&F4      SETB  0        0 1  INPUT   ;  0 0  NON-DISPLAY
&F5      SETB  0             COLOR: GREEN
&F6      SETB  0             COLOR: RED
&F7      SETB  0             COLOR: BLUE
&INP     SETB  0             INPUT                              GP09353
&INT     SETB  0             INTENSE                            GP09353
&BLACK   SETB  0             NON-DISPLAY                        GP09353
.FDLOOP  AIF   (&J GE &L).EXPFLD
&J       SETA  &J+1
&C       SETC  '&SYSLIST(&I,&J)'
         AIF   ('&C' EQ '').FDLOOP
         AIF   ('&C' EQ 'BL').BLUE
         AIF   ('&C' EQ 'RE').RED
         AIF   ('&C' EQ 'PI').PINK
         AIF   ('&C' EQ 'MA').PINK
         AIF   ('&C' EQ 'GR').GREEN
         AIF   ('&C' EQ 'CY').TURQ
         AIF   ('&C' EQ 'TU').TURQ
         AIF   ('&C' EQ 'YE').YELLOW
         AIF   ('&C' EQ 'WH').WHITE
         AIF   ('&C' EQ 'BK').BLACK                             GP09353
         AIF   ('&C' EQ 'ND').BLACK                             GP09353
         AIF   ('&C' EQ 'IN').INPUT
         AIF   ('&C' EQ 'BLUE').BLUE
         AIF   ('&C' EQ 'RED').RED
         AIF   ('&C' EQ 'PINK').PINK
         AIF   ('&C' EQ 'MAGENTA').PINK
         AIF   ('&C' EQ 'GREEN').GREEN
         AIF   ('&C' EQ 'CYAN').TURQ
         AIF   ('&C' EQ 'TURQ').TURQ
         AIF   ('&C' EQ 'TURQOISE').TURQ
         AIF   ('&C' EQ 'YELLOW').YELLOW
         AIF   ('&C' EQ 'WHITE').WHITE
         AIF   ('&C' EQ 'BLACK').BLACK                          GP09353
         AIF   ('&C' EQ 'INPUT').INPUT
         AIF   ('&C' EQ 'OUT').OUTPUT
         AIF   ('&C' EQ 'INT').INTENSE
         AIF   ('&C' EQ 'INTENSE').INTENSE
         AIF   ('&C' EQ 'REV').REVERSE
         AIF   ('&C' EQ 'BLI').BLINK
         AIF   ('&C' EQ 'UND').UNDER
         AIF   ('&C' EQ 'DFL').NULL
         AIF   ('&C' EQ 'REVERSE').REVERSE
         AIF   ('&C' EQ 'BLINK').BLINK
         AIF   ('&C' EQ 'UNDER').UNDER
         AIF   ('&C' EQ 'DFLT').NULL
       MNOTE 8,'FDLITEM: ARGUMENT &I,&J INVALID - &C NOT A VALID ENTRY'
         AGO   .FDLOOP
.BLUE    ANOP  ,
&F5      SETB  0
&F6      SETB  0
&F7      SETB  1
         AGO   .FDLOOP
.*
.RED     ANOP  ,
&F5      SETB  0
&F6      SETB  1
&F7      SETB  0
         AGO   .FDLOOP
.*
.PINK    ANOP  ,
&F5      SETB  0
&F6      SETB  1
&F7      SETB  1
         AGO   .FDLOOP
.*
.GREEN   ANOP  ,
&F5      SETB  1
&F6      SETB  0
&F7      SETB  0
         AGO   .FDLOOP
.*
.TURQ    ANOP  ,
&F5      SETB  1
&F6      SETB  0
&F7      SETB  1
         AGO   .FDLOOP
.*
.YELLOW  ANOP  ,
&F5      SETB  1
&F6      SETB  1
&F7      SETB  0
         AGO   .FDLOOP
.*
.WHITE   ANOP  ,
&F5      SETB  1
&F6      SETB  1
&F7      SETB  1
         AGO   .FDLOOP
.*
.BLACK   ANOP  ,                                                GP09353
&F5      SETB  0                                                GP09353
&F6      SETB  0                                                GP09353
&F7      SETB  1             FORCE NON-ZERO                     GP09353
&BLACK   SETB  1                                                GP09353
         AGO   .FDLOOP
.*
.INPUT   ANOP  ,
&F3      SETB  0
&F4      SETB  1
&INP     SETB  1                                                GP08356
         AIF   (&INT).CONFII                                    GP08356
         AGO   .FDLOOP
.*
.OUTPUT  ANOP  ,
&F3      SETB  1
&F4      SETB  0
         AGO   .FDLOOP
.*
.INTENSE ANOP  ,
&F4      SETB  1
&INT     SETB  1                                                GP08356
         AIF   (&INP).CONFII                                    GP08356
&F3      SETB  1
         AGO   .FDLOOP
.*
.REVERSE ANOP  ,                                                GP15257
&F2      SETB  1             HIGH-LIGHT                         GP15257
&F6      SETB  1             REVERSE                            GP15257
         AGO   .FDLOOP                                          GP15257
.*
.UNDER   ANOP  ,                                                GP15257
&F2      SETB  1             HIGH-LIGHT                         GP15257
&F5      SETB  1             UNDERLINE                          GP15257
         AGO   .FDLOOP                                          GP15257
.*
.BLINK   ANOP  ,                                                GP15257
&F2      SETB  1             HIGH-LIGHT                         GP15257
&F7      SETB  1             BLINK                              GP15257
         AGO   .FDLOOP                                          GP15257
.*
.NULL    ANOP  ,                                                GP15257
&F2      SETB  1             HIGH-LIGHT                         GP15257
&F5      SETB  0             RESET                              GP15257
&F6      SETB  0             RESET                              GP15257
&F7      SETB  0             RESET                              GP15257
         AGO   .FDLOOP                                          GP15257
.*
.CONFII  MNOTE *,'FDLITEM: INTENSE INPUT NOT SUPPORTED'         GP08356
         AGO   .FDLOOP                                          GP08356
.*
.EXPFLD  AIF   (NOT &BLACK).EXPFLD1                             GP09353
&F4      SETB  0             FORCED INPUT MODE                  GP09353
         AIF   (NOT &F3).EXPFLD2                                GP09353
         MNOTE 4,'FDLITEM: CONFLICTING NON-DISPLAY & OUTPUT ATTR &I'
.EXPFLD1 AIF   (&F3 OR &F4).EXPFLD2
&F3      SETB  1             OUTPUT IS DEFAULT
.EXPFLD2 ANOP  ,
&LABEL   DC    B'&F0&F1&F2&F3&F4&F5&F6&F7'
&LABEL   SETC  ''
&INP     SETB  0                                                GP08356
&INT     SETB  0                                                GP08356
         AGO   .LOOP
.*
.LABEL   AIF   ('&LABEL' EQ '').LABNEW
         MNOTE 0,'FDLITEM: CONSECUTIVE LABELS &LABEL AND &C '
&LABEL   DS    0X
.LABNEW  ANOP  ,
&LABEL   SETC  '&C'(1,K'&C-1)
         AGO   .LOOP
.*
.OFFSET  ANOP  ,                                                GP09358
&D       SETC  '&C'(1,K'&C-1)                                   GP09358
&D       EQU   *-&ZZZFLAN    FIELD OFFSET (NO LEN)              GP09358
         AGO   .LOOP                                            GP09358
.*
.DONE    AIF   ('&LABEL' EQ '').CLOSER                          GP15257
&LABEL   DS    0X
&LABEL   SETC  ''                                               GP15257
.*
.CLOSER  AIF   ('&NM' EQ '').MEND                               GP15257
&NM      EQU   &TLB,*-&TLB,C'C'   DEFINE AS SINGLE ITEM         GP15257
.*
.MEND    MEND  ,
