         MACRO ,
&NM      XOPT  &FUN,&ADD                                 NEW ON GP08082
.*
.*  EXHIBIT REQUEST TO MANIPULATE AN OPTIONS DATA BLOCK
.*
         GBLC  &MACPLAB
         LCLC  &C
         LCLA  &I,&J,&IX(10),&MX
&MACPLAB SETC  '&NM'
&IX(01)  SETA  0             FIND
&IX(02)  SETA  0             GET
&IX(03)  SETA  1             DELETE
&IX(04)  SETA  2             ADD (NO REPLACE)
&IX(05)  SETA  3             REPLACE OR ADD
&IX(06)  SETA  4             CHANGE (REPLACE ONLY IF NOT LONGER)
&IX(07)  SETA  5             RELEASE ALL ENTRIES
&MX      SETA  7*3           MAX STRING LENGTH
&C       SETC  '&FUN '.'   '
&C       SETC  '&C'(1,3)
&J       SETA  1
.*                       1  2  3  4  5  6  7
.LOOP    AIF   ('&C' EQ 'FINGETDELADDREPCHAREL'(&J,3)).MATCH
&J       SETA  &J+3
         AIF   (&J LT &MX).LOOP
 MNOTE 8,'XOPT: UNKNOWN FUNCTION &FUN'
         MEXIT ,
.MATCH   MACPARM R1,&ADD,NULL=SKIP,QUOTE=(LA,8)
&J       SETA  (&J+2)/3
&I       SETA  &IX(&J)
         MACPARM R0,&I        LOAD FUNCTION
         MACPARM R15,EXW@OPT,OP=L
         BALR  R14,R15       REQUEST OPTION OPERATION
         MEND  ,
