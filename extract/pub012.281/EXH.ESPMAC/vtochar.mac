         MACRO
         VTOCHAR
         LCLC  &SECT
&SECT    SETC  '&SYSECT'
CHARBIT  LA    R14,CHAROUT   START OF OUTPUT,AT END WILL BE LENGTH
         LR    R15,R14       START ADDRESS - SETUP FOR SPMOVE
         MVC   CHAROUT,BLANKS   CLEAR OUTPUT FIELD
         USING CHARMS,R1     MAP INPUT TABLE
         SR    R2,R2         CLEAR IC REGISTER
CHARLP   CLI   CHARL,255     END OF TABLE ?
         BNE   CHARTS        NO, CONTINUE
         SR    R14,R15       GET LENGTH OF OUTPUT STASHED
         BPR   R9            AT LEAST ONE BYTE ?
         LA    R14,1         FORCE ONE
         BR    R9            RETURN
         SPACE
CHARTS   IC    R2,CHARL      GET LENGTH - 1 OF TEXT
         MVC   CHARN,CHAR    COPY TEST BYTE TO WORK BYTE
         NC    CHARN,CHART   MASK OUT ALL BUT TESTED BITS
        CLC   CHARN,CHART   IS THIS THE PATTERN ?
         BNE   CHARINC       NO, TRY NEXT ONE
         EX    R2,CHARMV     MOVE TEXT TO OUTPUT
         MVI   CHARX,255     PREPARE TO COMPLIMENT
         XC    CHARX,CHART   COMPLEMENT TEST PATTERN
         NC    CHAR,CHARX    MASK OUT BITS USED BY TEST
         LA    R14,1(R2,R14)    POINT TO NEXT O/P POSITION
         SPACE
CHARINC  LA    R1,3(R1,R2)   POINT TO NEXT INPUT TABLE POSITION
         B     CHARLP        TEST FOR END OR NEXT
         DROP  R1
         SPACE 2
CHARMS   DSECT
CHART    DS    X             TEST BIT PATTERN
CHARL    DS    X             LENGTH - 1 OF TEXT
CHARC    DS    C             CHARACTERS
         SPACE
&SECT    CSECT
CHARMV   MVC   0(0,R14),2(R1)   MOVE TO OUTPUT
CHAR     DC    X'0'          BYTE TO BE ANALYZED
CHARN    DC    X'0'          TEST AREA
CHARX    DC    X'0'          COMPLEMENT OF TEST BYTE
CHAROUT  DC    CL39' '       OUTPUT AREA
         MEND
