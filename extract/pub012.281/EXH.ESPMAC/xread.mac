         MACRO
&NM      XREAD &REP,&LEN,&ERR=EXCRERR,&BAL=EXCREAD,&CASE=
         LCLC  &L,&N
&L       SETC  'L'''
&N       SETC  '&NM'
         AIF   ('&REP' EQ '').RDEF
         AIF   ('&REP'(1,1) EQ '(').RREG
&N       LA    R2,&REP .     MESSAGE ADDRESS
&N       SETC  ''
         AIF   ('&LEN' NE '').CLEN
         LA    R3,&L&REP .   MESSAGE LENGTH
         AGO   .BAL
.RDEF    ANOP
&N       LA    R2,REPLY .    INPUT BUFFER
&N       SETC  ''
         AIF   ('&LEN' NE '').CLEN
         LA    R3,&L.REPLY .   MAXIMUM INPUT LENGTH
         AGO   .BAL
.RREG    AIF   ('&REP' EQ '(R2)' OR '&REP' EQ '(2)').CLEN
&N       LR    R2,&REP(1) .   LOAD MESSAGE ADDRESS
&N       SETC  ''
.CLEN    AIF   ('&LEN'(1,1) EQ '(').LREG
&N       LA    R3,&LEN .     MESSAGE LENGTH
&N       SETC  ''
         AGO   .BAL
.LREG    AIF   ('&LEN' EQ '(R3)' OR '&LEN' EQ '(3)').BAL
&N       LR    R3,&LEN(1) .   MESSAGE LENGTH
&N       SETC  ''
.BAL     AIF   ('&CASE' EQ 'ASIS' OR '&CASE' EQ 'LOW').BLO      GP10226
         AIF   ('&CASE' EQ 'LOWER' OR '&CASE' EQ 'MIXED').BLO   GP10226
         AIF   ('&CASE' EQ 'UPPER' OR '&CASE' EQ '').BUP        GP10226
         AIF   ('&CASE' EQ 'UP' OR '&CASE' EQ 'UPP').BUP        GP10226
         MNOTE 4,'CASE=&CASE NOT RECOGNIZED; IGNORED'           GP10226
         AGO   .BUP                                             GP10226
.BLO     ANOP  ,                                                GP10226
&N       OI    SQSEDFG,SQSELOW    RETAIN INPUT CASE             GP10226
&N       SETC  ''                                               GP10226
.BUP     ANOP  ,                                                GP10226
&N       BALS  R9,&BAL .     CALL I/O ROUTINE
         AIF   ('&ERR' EQ '').MEX
         B     &ERR .        BRANCH ON I/O ERROR
.MEX     MEND  ,
