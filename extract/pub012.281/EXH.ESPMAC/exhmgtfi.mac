         MACRO ,
&N      EXHMGTFI &MASK=,&LOAD=  GTF INTERFACE INITIALIZATION NEW 78205
         GBLC  &SYSTEM                                           79071
         LCLA  &I,&J
         LCLB  &M(16),&ALL
         LCLC  &SECT,&C,&MOD
&MOD     SETC  '&LOAD'
         AIF   ('&LOAD' EQ '').DEFLOAD
         AIF   (K'&LOAD EQ 3).OKLOAD
         MNOTE 4,'INVALID LOAD=&LOAD'
.DEFLOAD ANOP  ,
&MOD     SETC  '&SYSECT'(6,3)
.OKLOAD  AIF   (K'&MASK EQ 0).NOMASK
         AIF   (K'&MASK EQ 18).TSTMASK
.BADMASK MNOTE 4,'INVALID MASK=&MASK'
         MEXIT ,
.TSTMASK AIF   ('&MASK'(1,1) NE '''' OR '&MASK'(18,1) NE '''').BADMASK
&I       SETA  0
.LOOPBIT AIF   (&I GT 15).DONMASK
&I       SETA  &I+1
&C       SETC  '&MASK'(&I+1,1)
         AIF   ('&C' EQ '0').LOOPBIT
         AIF   ('&C' NE '1').BADMASK
&M(&I)   SETB  1
         AGO   .LOOPBIT
.NOMASK  ANOP  ,
&ALL     SETB  1
&M(1)    SETB  1
&M(2)    SETB  1                                                 79075
&M(3)    SETB  1
&M(4)    SETB  1
&M(5)    SETB  1
&M(6)    SETB  1
&M(7)    SETB  1
&M(8)    SETB  1
&M(9)    SETB  1
&M(10)   SETB  1
&M(11)   SETB  1
&M(12)   SETB  1
&M(13)   SETB  1
&M(15)   SETB  1
&M(16)   SETB  1
.DONMASK AIF   ('&SYSTEM' NE 'MVT').SVSMASK                      79075
&M(2)    SETB  1             CONTROL/ABEND - REQUIRED FOR
&M(14)   SETB  1             PROGR CHECK      CLEANUP
.SVSMASK AIF   ('&SYSTEM' NE 'SVS').COMMASK                      79075
&M(6)    SETB  1             PROGRAM INTERRUPT                   79075
&M(10)   SETB  1             CONTROL/ABEND                       79075
.COMMASK ANOP  ,                                                 79075
&N       UNFRAG ,
         AIF ('&SYSTEM' NE 'MVS' AND '&SYSTEM' NE 'VS1').VS1     79071
         B     EXCXCB        NOT SUPPORTED (YET?)                79071
.VS1     XREQUIRE TSO,PCP,MFT,MP,DEBUG,CON                       79071
         XLOAD '&MOD',R7
         AIF   ('&SYSTEM' EQ 'SVS' OR '&SYSTEM' EQ 'MVS').POOL4  80251
         TM    COREVAIL,X'78'  2K POOL AVAILABLE ?               80251
         AGO   .POOL42                                           80251
.POOL4   TM    COREVAIL,X'70'  4K POOL AVAILABLE ?               80251
.POOL42  BZ    EXCXCB        NO; NEED ROOM FOR ENQUEUE/DEQUEUE   80251
         USING HCT,R7        DECLARE WORK AREA
         MVC   HCTTCB+1(3),SQIQTCB+1  COPY TCB ADDRESS
         ENQ   MF=(E,GTFENQLS),RET=USE
         BXH   R15,R15,EXCXCB  RETURN IF NOT AVAILABLE
         BALS  R14,SQSSMOF
         L     R3,CVTPTR     GET CVT
         NUSE  CVT,R3        DECLARE IT
         TM    CVTGTFST,X'FF'  ANY BITS ON ?
         BNZ   GTFEXDEQ      YES - EXIT
         LAT   R2,CVTGTF,GTFEXDEQ  EXIT IF NO CMT POINTER
         ST    R3,HCX@CVT    ENSURE GOOD ADDRESS
         LA    R7,0(,R7)     MAKE SURE OF HIGH BYTE
         ONEXIT EXITONUN,XCTL=YES     DECLARE ERROR/END EXIT
         AIF   ('&SYSTEM' EQ 'SVS').VS2                          79071
         ST    R7,0(,R2)     SET HCT POINTER
&I       SETA  &M(1)+&M(2)+&M(3)+&M(4)+&M(5)+&M(6)+&M(7)+&M(8)+&M(9)
&I       SETA  &I+&M(10)+&M(11)+&M(12)+&M(13)+&M(14)+&M(15)+&M(16)
         MVI   4(R2),7+(&I/9)*X'A8'  MAKE 07 OR AF
         MVI   5(R2),0
         MVC   6(30,R2),4(R2)  IN ALL CLASS MASK ENTRIES
         AIF   (&ALL).MASKDON
&J       SETA  0
&C       SETC  'AF'
&ALL     SETB  1             TURN ON SELECTED ONES
         AIF   (&I LT 9).MASKLOO
&C       SETC  '07'
&ALL     SETB  0             TURN OFF SELECTED ONES
.MASKLOO AIF   (&J GT 15).MASKDON
&J       SETA  &J+1
         AIF   (&M(&J) NE &ALL).MASKLOO
         MVI   2*&J+2(R2),X'&C'  SET OPTION
         AGO   .MASKLOO
.MASKDON OI    CVTGTFST,CVTGTFAC  SHOW ACTIVE
         TM    CVTOPTB,CVTTOD  TIME OF DAY CLOCK ?
         BZ    MDL360        NO
         OI    HCTFLG1,HCTTODCK  SET FOR REAL CLOCK
         OI    HCTGTFST,HCTMCTYP  SET HOOK TYPE REAL
         LCTL  R8,R8,=BL4'&M(1)&M(2)&M(3)&M(4)&M(5)&M(6)&M(7)&M(8)&M(9)*
               &M(10)&M(11)&M(12)&M(13)&M(14)&M(15)&M(16)' BIG TURN-ON
MDL360   DEQ   MF=(E,GTFENQLS),RET=HAVE  DEQ
         AGO   .VS3                                              79071
.VS2     PGFIX R,A=(R7),ECB=GTFECB,LONG=Y  FIX WORK SPACE        79071
         LTR   R15,R15                                           79071
         BZ    GTFFIXED      FIX OK                              79071
         LA    R14,8                                             79071
         CR    R14,R15       NORMAL ?                            79071
         BNE   GTFEXDEQ      NO; QUIT NOW                        79071
         WAIT  ECB=GTFECB    WAIT TO BE FIXED                    79071
GTFFIXED MODESET MODE=SUP,ENABLE=NO   FREEZE EVERYTHING          79071
         ST    R7,0(,R2)     SET HCT POINTER                     79071
         MVI   5(R2),X'FF'   CHANGE NOP TO BRANCH HOOKS          79071
         OI    CVTGTFST,CVTGTFAC  SET GTF ACTIVE                 79071
         OI    CVTCTLFG,CVTGTRCE  SET TRACE DISABLED             79071
         NI    CVTTRACE+1,X'0F'   DISABLE TRACE                  79071
         LCTL  R8,R8,=BL4'&M(1)&M(2)&M(3)&M(4)&M(5)&M(6)&M(7)&M(8)&M(9)*
               &M(10)&M(11)&M(12)&M(13)&M(14)&M(15)&M(16)' BIG TURN-ON
         OI    HCTFLG1,HCTTODCK  SET REAL CLOCK                  79071
         OI    HCTGTFST,HCTMCTYP  SET REAL HOOK                  79071
         MODESET MODE=PROB,ENABLE=YES  RESTORE INTERRUPTS        79071
         DEQ   MF=(E,GTFENQLS),RET=HAVE  DEQ                     79077
.VS3     ANOP  ,                                                 79071
         MVC   EXITTIME,SQTEIR+2  SAVE LOOP TIME
         OI    EXCPRIV,EXCPLOOP  SET LOOP MODE
         MVC   SQTEIR+2(2),H100  1 SECOND DEFAULT
         B     GTFUSER       GO TO USER CODE
GTFOUT   TM    HCTFLG1,HCTTERMD  TERMINATED BY ERROR ?
         BNZ   GTFEXIT       YES - QUIT
         XPOUT ,             DISPLAY SCREEN
         B     GTFUSER       GO TO USER CODE
GTFEXIT  BALS  R14,SQSSMOF   DON'T TRUST ANYONE
         BALS  R14,ONUNIT    KILL ONUNIT
         B     EXCYES        AND TERMINATE
         SPACE 1
GTFEXDEQ DEQ   MF=(E,GTFENQLS),RET=HAVE  DEQ
         B     EXCXCB        SHOW NOT AVAILABLE
         SPACE 1
GTFENQLS ENQ   (GTFENQMJ,GTFENQMN,E,7,SYSTEM),MF=L  REMOTE LIST
         AIF ('&SYSTEM' EQ 'SVS').VS4                            79071
GTFENQMJ DC    CL8'IHLQNAME'
GTFENQMN DC    C'IHLINIT'
         AGO   .VS5                                              79071
.VS4     ANOP  ,                                                 79071
GTFECB   DC    A(0)          PGFIX ECB                           79071
GTFENQMJ DC    CL8'AHLQNAME'                                     79071
GTFENQMN DC    C'IHLINIT'                                        79071
.VS5     SPACE 1                                                 79071
&SECT    SETC  '&SYSECT'
HCT      EXHMGTFX ,
&SECT    CSECT ,
GTFUSER  DS    0H ,          START OF USER CODE
         MEND  ,
