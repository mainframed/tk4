         MACRO ,
&NM      READFDR &TYPE,&OPERAND,&USE=                   ADDED ON 82248
         GBLC  &MACPLAB
.*
.*   This macro is used for reading FDR dump tapes, in conjunction
.*   with the @FDRREAD service routine. The current version requires
.*   authorization to diddle the DEB.
.*
.*   Used by COPYFDR program, and the @OBTAINS and @VOLREAD routines.
.*
         LCLA  &I,&J
&MACPLAB SETC  '&NM'
&I       SETA  0
         AIF   ('&TYPE' EQ 'CLOSE').CLOSE
&I       SETA  1
         AIF   ('&TYPE' EQ 'TCLOSE').CLOSE
&I       SETA  2
         AIF   ('&TYPE' EQ 'OPEN').CHKUSE
&I       SETA  3
         AIF   ('&TYPE' EQ 'READ').TEST1
&I       SETA  4
         AIF   ('&TYPE' EQ 'NEXT').TEST1
&I       SETA  5                                                GP13222
         AIF   ('&TYPE' EQ 'TRACK').TEST1                       GP13222
         AIF   ('&TYPE' EQ 'RDM').TEST1                         GP13222
&I       SETA  6                                                GP13222
         AIF   ('&TYPE' EQ 'MORE').TEST1                        GP13222
         AIF   ('&TYPE' EQ 'LOAD' OR '&TYPE' EQ 'INIT').INIT
         MNOTE 8,'TYPE OPERAND REQUIRED'
         MEXIT ,
.INIT    ANOP  ,
&I       SETA  &SYSNDX
&NM      ICM   R15,7,@FDRREAD+1   PRIOR LOAD ?
         BNZ   VLR@&I
         SERVCALL LPALD,=CL8'@FDRREAD'
         ST    R0,@FDRREAD
VLR@&I   DS    0H
         MEXIT ,
.TEST1   AIF   (T'&OPERAND EQ 'O').CLOSE
         AGO   .LR1
.CHKUSE  AIF   (T'&USE EQ 'O').LOAD1
&J       SETA  128
         AIF   ('&USE' EQ 'VTOC').LOAD1
&J       SETA  0
         MNOTE 4,'INVALID USE PARAMETER &USE'
.LOAD1   AIF   (T'&OPERAND NE 'O').LR1
         MNOTE 8,'FUNCTION &TYPE REQUIRES AN OPERAND'
         MEXIT ,
.LR1     MACPARM R1,&OPERAND,NULL=0
         AIF   (&J EQ 0).CLOSE
         ICM   R1,8,=AL1(&J)
.CLOSE   MACPARM R0,&I
.BAL     ANOP  ,
&MACPLAB L     R15,@FDRREAD
         BASR  R14,R15
&MACPLAB SETC  ''
         MEND  ,
