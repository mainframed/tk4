         MACRO
&NM      ONSPIE &AD,&MSK,&BCR=YES,&WORK=R15,&AM=                  *TSM*
         GBLC  &SYSTEM,&MACPLAB
         GBLB  &MVSXA
         LCLC  &C,&EXPT
         LCLB  &BUGGY
         LCLA  &K                                               GP12316
&MACPLAB SETC  '&NM'
&EXPT    SETC  'SQSPIAD'     EXHIBIT RESTART POINT
&C       SETC  '&SYSECT'     GET CODE SECTION NAME
         AIF   ('&C'(1,3) NE 'EXO').CODE
&BUGGY   SETB  1             ASSEMBLING AN EXORCIST MODULE
&EXPT    SETC  'SPIETRAP'    EXORCIST RESTART POINT
.CODE    ANOP  ,
.**********************************************************************
.*
.*   THIS MACRO SUPPORTS SETTING/RESETTING SPIE RECOVERY FLAGS
.*     AND MATCHING EXIT ADDRESS.
.*       FIRST POSITIONAL IS THE ADDRESS OF THE EXIT OR 0-RESET
.*       SECOND IS SELF-DEFINING MASK FOR PICA - DEFAULT
.*         GIVES 0C4/0C5/0C6 RECOVERY
.*         THE MASK MAY BE 2 BYTES, EITHER AS A SELF-DEFINING VALUE, OR
.*         AN X'hex' OR B'nnn' LITERAL. THE LENGTH IS USED TO DETERMINE
.*         WHETHER AN MVI TO THE LEFT BYTE, OR AN MVC TO BOTH BYTES
.*         IS APPROPRIATE. A REGISTER MASK USES STH.
.*         UNCLASSIFIED VALUES ARE TREATED AS TWO-BYTE MASKS.
.*
.*       KEYWORD BCR=  DRAINS THE INSTRUCTION LOOKAHEAD PIPELINE
.*       KEYWORD WORK SPECIFIES A WORK REGISTER (DEFAULT R15)
.*       KEYWORD AM SPECIFIES AMODE FOR RECOVERY ADDRESS: AM=24, =31
.*         OR AM=ASIS FOR MODE IN (&AD) REGISTER FORMAT, OR
.*         AM= TO SET CURRENT AMODE (DEFAULT)
.*
.**********************************************************************
         AIF   ('&BCR' NE 'YES').NOBCR
         MACPARM 15,(0),OPR=BCR DRAIN PIPELINE
.NOBCR   AIF   ('&AD' EQ '' OR '&AD' EQ '0').RESET
         AIF   (NOT &BUGGY).PRAM
         MACPARM R0,R15,TRAPREGS,OP=STM,MODE=THREE
.PRAM    MACPARM &WORK,&AD     GET EXIT ADDRESS                   *TSM*
         AIF   (NOT &MVSXA).NOBSM
         AIF   ('&AM' EQ 'ASIS').NOBSM
         AIF   ('&AM' EQ '24' OR '&AM' EQ 'AM24').MODE24
         AIF   ('&AM' EQ 'AMODE24').MODE24
         AIF   ('&AM' EQ '31' OR '&AM' EQ 'AM31').MODE31
         AIF   ('&AM' EQ 'AMODE31').MODE31
         MACPARM &WORK,(0),OPR=BSM SAVE CURRENT AMODE
         AGO   .NOBSM
.MODE24  MACPARM &WORK,=X'7FFFFFFF',OP=N
         AGO   .NOBSM
.MODE31  MACPARM &WORK,=X'80000000',OP=O
.NOBSM   MACPARM &WORK,&EXPT,OP=ST SET EXIT
         AIF   (&BUGGY).MEND
         AIF   ('&MSK' EQ '').DEF456
&K       SETA  K'&MSK                                           GP12316
         AIF   (&K LT 3).NOTREG                                 GP12316
         AIF   ('&MSK'(1,1) NE '(' OR '&MSK'(2,1) EQ '(').NOTREG
         AIF   ('&MSK'(&K,1) NE ')' OR '&MSK'(&K-1,1) EQ ')').NOTREG
.REGST   STH   &MSK(1),SQSPICA+4                                GP12316
         MEXIT ,                                                GP12316
.NOTREG  AIF   (&K LT 4).TWOBYTE                                GP12316
         AIF   ('&MSK'(1,2) EQ 'X''').MAX5                      GP12316
         AIF   ('&MSK'(1,2) NE 'B''').MAX11                     GP12316
         AIF   (T'&MSK EQ 'N').TESTNUM                          GP12316
.MAX11   AIF   (&K GT 11).TWOBYTE                               GP12316
         AGO   .ONEBYTE                                         GP12316
.MAX5    AIF   (&K GT 5).TWOBYTE                                GP12316
         AGO   .ONEBYTE                                         GP12316
.TESTNUM AIF   (&MSK LT 256).ONEBYTE                            GP12316
.TWOBYTE MVC   SQSPICA+4(2),=AL2(&MSK)                          GP12316
         MEXIT ,                                                GP12316
.ONEBYTE MVI   SQSPICA+4,&MSK  SET FLAGS
         MEXIT
.DEF456  MVI   SQSPICA+4,X'0E' . DEFAULT PROT/ADDR/SPEC
         MEXIT
.RESET   AIF   (&BUGGY).RESETX
&MACPLAB XC    SQSPICA+4(8),SQSPICA+4    RESET FLAGS/EXIT
         MEXIT ,
.RESETX  ANOP  ,
&MACPLAB XC    &EXPT,&EXPT   RESET RECOVERY
.MEND    MEND
