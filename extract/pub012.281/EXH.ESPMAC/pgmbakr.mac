         MACRO ,
&NM      PGMBAKR &OP1,&OP2,&BASE=R12,&AM=ANY,&ID=0,&BREG=
.**********************************************************************
.*                                                                    *
.*   THIS MACRO INVOKES BAKR UNDER MVS ESA AND LATER, ELSE PGMHEAD    *
.*   UNDER BAKR, NO SAVE AREA IS OBTAINED                             *
.*                                                                    *
.**********************************************************************
         GBLB  &MVSESA,&ZZZBAKR,&MAPONCE
         LCLA  &K,&I         BASE COUNT                         GP04234
         LCLC  &CASE,&CMU                                       GP04234
         AIF   (&MVSESA).BAKR
&NM      PGMHEAD &OP1,&OP2,NOENTRY,BASE=&BASE,AM=&AM,SETAM=&AM,CSECT=,I*
               D=&ID,BREG=&BREG                                 GP05018
&ZZZBAKR SETB  0
         AGO   .COMMON
.BAKR    ANOP  ,
&NM      BAKR  ,
&ZZZBAKR SETB  1             NEED PR IN PGMBAND MACRO
&K       SETA  N'&BASE      MAX NUMBER OF BASES SPECIFIED       GP04234
&CMU     SETC  '&BASE(1)'    SET THE FIRST ONE                  GP04234
&CASE    SETC  '&BASE(1)'    SET THE FIRST ONE                  GP04234
&K       SETA  N'&BASE                                          GP04234
         AIF   (&K LT 2).DONEBAS                                GP04234
&I       SETA  1                                                GP04234
         LA    &BASE(&K),2048                                   GP04234
.BASLOOP AIF   (&I GE &K).DONEBAS                               GP04234
&I       SETA  &I+1                                             GP04234
         AIF   ('&BASE(&I)' EQ '').BASLOOP                      GP04234
         LA    &BASE(&I),2048(&BASE(&K),&BASE(&I-1))            GP04234
&CMU     SETC  '&CMU'.','.'&BASE(&I)'                           GP04234
         AGO   .BASLOOP                                         GP04234
.DONEBAS USING &NM,&CMU                                         GP04234
         SETAM &AM           SET MODE AS REQUESTED (OR NOT)
.COMMON  AIF   (&MAPONCE).MEND
&MAPONCE SETB  1
         YREGS ,
         MASKEQU ,
.MEND    MEND  ,
