         MACRO ,
         MACSTLEN &STR
.*-------------------------------------------------------------------*.
.*  NAME:                M A C S T L E N                             *.
.* PURPOSE:                                                          *.
.*                                                                   *.
.*  INTERNAL MACRO TO DETERMINE THE LOGICAL LENGTH OF A STRING       *.
.*                                                                   *.
.*                                                                   *.
.*  INPUT :                                                          *.
.*    THE STRING PARAMETER                                           *.
.*                                                                   *.
.*  OUTPUT:                                                          *.
.*   MACPERR  GLOBAL FLAG TO INDICATE AN ERROR OCCURRED              *.
.*   MACQUOT  GLOBAL FLAG TO INDICATE THAT INPUT WAS A QUOTED STRING *.
.*   MACPNUL  GLOBAL FLAG TO INDICATE THAT INPUT WAS NULL            *.
.*   MACPLEN  LOGICAL STRING LENGTH                                  *.
.*-------------------------------------------------------------------*.
         GBLA  &MACPLEN
         GBLB  &MACPNUL,&MACPERR,&MACQUOT
         GBLC  &MACQSTR
         LCLA  &I,&J,&K
         LCLC  &S,&C
&MACPNUL SETB  (T'&STR EQ 'O')
&MACPERR SETB  0
&MACQUOT SETB  0
&MACPLEN SETA  0
&S       SETC  '&STR'
&K       SETA  K'&S
         AIF   (&K LT 4).NOPFX
         AIF   ('&S'(1,2) NE '=C' AND '&S'(1,2) NE '=X').NOPFX
&S       SETC  '&S'(3,&K-2)
&K       SETA  K'&S
.NOPFX   AIF   ('&S'(1,1) NE '''' AND '&S'(&K,1) NE '''').NOQUO
&S       SETC  '&S'(3,&K-2)
&K       SETA  K'&S
&MACQUOT SETB  1
.NOQUO   AIF   (&I GE &K).MEND
&I       SETA  &I+1
&C       SETC  '&S'(&I,1)
         AIF   ('&C' NE '''' AND '&C' NE '&&').SINGLE
         AIF   (&I GE &K).SINGLE
         AIF   ('&C' NE '&S'(&I+1,1)).SINGLE
&I       SETA  &I+1          SKIP FIRST DOUBLE
.SINGLE  ANOP  ,
&MACPLEN SETA  &MACPLEN+1
         AGO   .NOQUO
.MEND    MEND  ,
