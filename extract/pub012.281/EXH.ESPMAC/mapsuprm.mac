         MACRO ,
&NM      MAPSUPRM &PFX=ZP,&DSECT=                           NEW GP14220
.*  THIS MACRO DESCRIBES/DEFINES THE OPEN I/O MODE AND ASSOCIATED WORK
.*  AREA USED BY THE MVSSUPA SERVICE ROUTINE.
.*  MODE IS INPUT AND RETURNED
.*  ID IS INPUT AND UPDATED
.*  REST ARE OUTPUT ONLY
.*
         LCLC  &P,&N
&P       SETC  '&PFX'
&N       SETC  '&NM'
         AIF   ('&N' NE '').HAVSECT
&N       SETC  'MAP'.'&P'
.HAVSECT AIF   ('&DSECT' EQ 'NO').NOSEC
&N       DSECT ,
         AGO   .COMSEC
.NOSEC   AIF   ('&NM' EQ '').COMSEC
&NM      DS    0F
.COMSEC  ANOP  ,
&P.MODE  DC    F'0' I/O MODE (0-IN,1-OUT,2-UPD,3-APP,4-INOUT,5-OUTIN)
.*                  +8-USE EXCP FOR TAPE
.*                  +10-USE BLOCK MODE (BSAM RATHER THAN QSAM MODE)
.*                  +80-TERMINAL GETLINE  +81-TERMINAL PUTLINE
.*                  RETURNS 40-VSAM; 20-BPAM UNLIKE CONCAT
.*
&P.DVTYP EQU   &P.MODE,1     Device type of first/only DD       GP15112
.*
&P.RECFM EQU   &P.MODE+1,1   Equivalent RECFM bits
&P.RFU   EQU   X'C0'           UNDEFINED
&P.RFF   EQU   X'80'           FIXED
&P.RFV   EQU   X'40'           VARIABLE (WITH BDW/RDW)
&P.RFD   EQU   X'20'           ASCII VARIABLE (WITH BIT 0-1 OFF)
&P.RFT   EQU   X'20'           TRACK OVERFLOW (WITH 0-1 NOT OFF)
&P.RFB   EQU   X'10'           BLOCKED
&P.RFS   EQU   X'08'           STANDARD(F), SPANNED(V)
&P.RFA   EQU   X'04'           ANSI CONTROL CHARACTERS
&P.RFM   EQU   X'02'           MACHINE CONTROL CHARACTERS (1403 CCW)
.*
&P.SFLGS EQU   &P.MODE+2,1   Additional MODE related flags      GP15112
&P.FUPDT EQU   X'80'           Update mode (BSAM/VSAM)          GP15112
&P.FUPIN EQU   X'40'           Last was input (GET/READ)        GP15112
&P.FUPOU EQU   X'20'           Last was output (PUT/WRITE)      GP15112
&P.FVSAM EQU   X'08'           Use VSAM
&P.FVSRR EQU   X'04'           RRDS
&P.FVSES EQU   X'02'           ESDS
&P.FVSKS EQU   X'01'           KSDS
.*
&P.MFLGS EQU   &P.MODE+3,1   Remember open MODE
&P.FTERM EQU   X'80'           Using GETLINE/PUTLINE
&P.FBPAM EQU   X'20'           Unlike BPAM concat - special handl
&P.FBLOCK EQU  X'10'           Using BSAM READ/WRITE mode
&P.FEXCP EQU   X'08'           Use EXCP for TAPE
.*.FUPD  EQU   X'06'           Update in place (xSAM, VSAM)
.*.      EQU                   (RESERVED)
&P.FOUT  EQU   X'01'           Output mode
&P.LEN   DC    AL2(&P.SIZE)  CONTROL BLOCK LENGTH
&P.ID    DC    H'42'         BLOCK IDENTIFIER (0 ON RETURN)
.*
&P.KEYL  DC    F'0'          KEY LENGTH
&P.KEYP  DC    F'0'          KEY POSITION
&P.LRECL DC    F'0'          RECORD LENGTH
&P.BLKSZ DC    F'0'          BLOCK SIZE
&P.MAXRC DC    F'0'          MAXIMUM RECORD NUMBER
.*
&P.ORG   DC    XL2'0'        DSORG
.*
&P.PIX   DC    H'0'          PROCESSING INDEX
&P.IXSAM EQU   0               BSAM/BPAM - default
&P.IXQSM EQU   4               QSAM - same as BSAM, with deblocking
&P.IXVSM EQU   8               VSAM data set
&P.IXVTC EQU   12              VTOC reader
&P.IXTRM EQU   16              TSO terminal
.*.IX    EQU   20              (RESERVED)
.*.IX    EQU   24              (RESERVED)
.*
&P.FMOD  DC    X'00'         CALLER'S MODE: 0-F  1-V  2-U
&P.FMFIX EQU   0               Fixed RECFM (blocked)
&P.FMVAR EQU   1               Variable (blocked)
&P.FMUND EQU   2               Undefined
.*
&P.DDFLG DC    X'0'          DD SCAN FLAG
&P.FDD   EQU   X'80'           FOUND A DD - LATER CONCAT FLAG
&P.FSEQ  EQU   X'40'           USE IS SEQUENTIAL
&P.FPDQ  EQU   X'20'           DS IS PDS WITH MEMBER NAME
&P.FPDS  EQU   X'10'           DS IS PDS (or PDS/E with S390)
&P.FVSM  EQU   X'08'           DS IS VSAM (limited support)
&P.FVTOC EQU   X'04'           DS IS VTOC (limited support)
&P.FBLK  EQU   X'02'           DD HAS FORCED BLKSIZE
&P.SIZE  EQU   *-&P.MODE     SIZE TO CLEAR
         MEND  ,
