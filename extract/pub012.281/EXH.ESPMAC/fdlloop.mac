         MACRO ,
&NM      FDLLOOP &LR=R4,&DR=R3,&MAP=DSPDSECT,&HD=2              GP15241
         GBLC  &ZZFDL,&ZZFDLLR,&ZZFDLDR
.*
.*   FDLLOOP PROVIDES AN EASY WAY OF LOOPING THROUGH ADDRESS
.*   LISTS CREATED BY SCBILD, AS USED BY SCLINE
.*
.*   THE LOOP IS TERMINATED WITH THE FDLBUMP MACRO, THAT NEEDS
.*   NO OPERANDS.
.*
.*   LR=  REGISTER USED FOR LIST TRAVERSING
.*   DR=  REGISTER USED TO POINT TO THE INDIVIDUAL LINE
.*
.*   HD=  NUMBER OF LINES FOR FIXED HEADING TO BYPASS
.*          NOTE THAT THIS IS NOT VALIDITY CHECKED.
.*
.*   MAP= NAME OF DSECT/CSECT MAPPING THE DATA LINE
.*          SET TO NULL STRING TO BYPASS USING
.*
&ZZFDLLR SETC  '&LR'
&ZZFDLDR SETC  '&DR'
&ZZFDL   SETC  'ZF'.'&SYSNDX'
&NM      L     &LR,EXW@RENT  GET NUMBER OF SCREEN LINES
         LA    &LR,&HD*4(,&LR)   FIRST DATA LINE ADDRESS
&ZZFDL.LP L    &DR,0(,&LR)   FIRST DATA LINE
         AIF   (T'&MAP EQ 'O').SKIPUSE
         USING &MAP,&DR      DECLARE IT
.SKIPUSE LA    &DR,0(,&DR)   CLEAN HIGH
         LTR   &DR,&DR       ANY ADDRESS?
         BZ    &ZZFDL.UP       NO; SKIP IT
         MEND  ,
