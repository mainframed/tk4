         MACRO ,
         MACBDDDL &OP,&LEN                               NEW ON 2010290
         GBLC  &MACSTR       RETURN OPERAND STRING
         GBLA  &MACTYP       RETURN OPERAND TYPE (1-REG,2-TXT,3-MIX)
.*
.*   THIS MACRO ACCEPTS AN OPERAND EXPRESSION AND A LENGTH,
.*   AND RETURNS A SINGLE OPERAND COMBINING THE TWO.  E.G.,
.*     TEXT,5                -> TEXT(5)
.*     (R6),4                -> 0(4,R6)
.*     XYZ(R9),3             -> XYZ(3,R9)
.*     ((R6)),8              -> ((R6))(8)
.*
         LCLC  &OP1,&OP2
         LCLA  &I,&J,&K,&L
&MACSTR  SETC  ''            ERROR INDICATION
&MACTYP  SETA  0             ERROR INDICATION
&K       SETA  K'&OP         LENGTH OF INPUT OPERAND
         AIF   (&K LT 1).MEND     ERROR
         AIF   (&K LT 3).BDDD     PLAIN TEXT
         AIF   ('&OP'(&K,1) NE ')').BDDD    SIMPLE TEXT
         AIF   ('&OP'(&K-1,1) EQ ')').BDDD
         AIF   ('&OP'(1,1) NE '(' OR '&OP'(2,1) EQ '(').MIX
.REG0    ANOP  ,
&MACTYP  SETA  1             REGISTER
&MACSTR  SETC  '0('.'&LEN'.','.'&OP'.')' GENERATE OPERAND FIELD
         MEXIT ,
.MIX     ANOP  ,
.*  PARSE xxx ( yyy ) - CHANGE TO xxx ( len , yyy )
&I       SETA  &K
&J       SETA  1
.QLOOP   AIF   (&I LE 1).BDDD
&I       SETA  &I-1
         AIF   ('&OP'(&I,1) EQ '(').QLEFT
         AIF   ('&OP'(&I,1) EQ ')').QRITE
         AGO   .QLOOP
.QRITE   ANOP  ,
&J       SETA  &J+1
         AGO   .QLOOP
.QLEFT   ANOP  ,
&J       SETA  &J-1
         AIF   (&J GT 0).QLOOP
         AIF   (&I LE 1).BDDD
&OP1     SETC  '&OP'(1,&I)
&OP2     SETC  '&OP'(&I+1,&K-&I)
&MACSTR  SETC  '&OP1'.'&LEN'.','.'&OP2'
&MACTYP  SETA  3             MIXED
         MEXIT ,
.BDDD    ANOP  ,
&MACSTR  SETC  '&OP'.'('.'&LEN'.')'    GENERATE OPERAND FIELD
&MACTYP  SETA  2             TEXT
.MEND    MEND  ,
