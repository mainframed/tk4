         MACRO
&NM      DSTART &NAM,&SECT=,&CHECK=,&BASE=R12,&WENT=            GP09030
         GBLC  &MODULE
         LCLA  &K,&I         BASE COUNT                         GP09030
         LCLC  &CASE,&CMU                                       GP09030
         AIF   ('&NM' EQ '').BOO
         AIF   ('&CHECK' EQ 'NO').OK
         AIF   ('&NM'(1,4) EQ 'EXHD').OK
.BOO     MNOTE 4,'INVALID START NAME'
         AGO   .NOT
.OK      AIF   ('&SECT' NE '').NOT
&NM      START 0
.NOT     AIF   ('&SECT' EQ '').NOD                               75305
&NM      DS    0D
.NOD     ANOP
&NAM     STM   R14,R12,12(R13)  .   SAVE FOR DEBUGGING
&K       SETA  N'&BASE      MAX NUMBER OF BASES SPECIFIED       GP09030
&CMU     SETC  '&BASE(1)'    SET THE FIRST ONE                  GP09030
&CASE    SETC  '&BASE(1)'    SET THE FIRST ONE                  GP09030
&K       SETA  N'&BASE                                          GP09030
         LA    &BASE(1),0(,R15) LOAD AND CLEAN BASE             GP09030
         AIF   (&K LT 2).DONEBAS                                GP09030
&I       SETA  1                                                GP09030
         LA    &BASE(&K),2048                                   GP09030
.BASLOOP AIF   (&I GE &K).DONEBAS                               GP09030
&I       SETA  &I+1                                             GP09030
         AIF   ('&BASE(&I)' EQ '').BASLOOP                      GP09030
         LA    &BASE(&I),2048(&BASE(&K),&BASE(&I-1))            GP09030
&CMU     SETC  '&CMU'.','.'&BASE(&I)'                           GP09030
         AGO   .BASLOOP                                         GP09030
.DONEBAS USING &NM,&CMU                                         GP09030
         USING EXHBSQSP,R13 . SUBPOOL 253 MAPPING
         USING EXHBWORK,R11 .  SUBPOOL 252 MAPPING
         AIF   ('&WENT' EQ 'NO').NOGO                           GP09048
         USING EXHBWENT,R10 .   DISPLAY BLDL MAPPING
.NOGO    NEED  SQSP,WORK .   FORCE REQUIRED DSECT MAPPINGS
&MODULE  SETC  '&NM' .       SAVE CSECT NAME FOR LATER USE
         PUSH  PRINT                                             81082
         PRINT GEN,ON                                            81082
         SPACE 1                                                 81075
*                           RESTRICTED RIGHTS LEGEND
*
*          Use, duplication or disclosure is subject to restrictions
*                   stated in Contract No. GS-00C-01222 with
*                      American Management Systems, Inc.
*
*                   This program is a proprietary product of
*                       Expert System Programming, Inc.
         SPACE 1                                                 81075
         POP   PRINT                                             81082
         MEND
