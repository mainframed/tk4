         MACRO
&L       WTLST &DUM,&TYPE=DESC
         GBLB  &WTFAIL
         LCLA  &I,&J,&UNIQUE
         LCLB  &BIT(16)
         LCLC  &DIGIT
         AIF   (N'&SYSLIST(1) EQ 0).NOLIST
         AIF   (N'&SYSLIST(1) LE 16).LOOP1
         MNOTE 12,'&TYPE HAS MORE THAN 16 OPERANDS'
.FAIL    ANOP
&WTFAIL  SETB  1
         MEXIT
.NOLIST  MNOTE 12,'&TYPE IS A REQUIRED OPERAND'
         AGO   .FAIL
.NOTNUM  MNOTE 12,'&TYPE.(&I.) MUST BE NUMERIC'
         AGO   .FAIL
.RANGE   MNOTE 12,'&TYPE.(&I.) MUST BE IN THE RANGE 1-16'
         AGO   .FAIL
.NOTU    MNOTE 12,'DESCRIPTORS CODES 1-7 MUST BE UNIQUE'
         AGO   .FAIL
.LOOP1   ANOP
&I       SETA  &I+1
         AIF   (T'&SYSLIST(1,&I) NE 'N').NOTNUM
         AIF   (&SYSLIST(1,&I) LE 0 OR &SYSLIST(1,&I) GT 16).RANGE
&BIT(&SYSLIST(1,&I)) SETB 1
         AIF   (&SYSLIST(1,&I) GT 7 OR '&TYPE' NE 'DESC').END1
&UNIQUE  SETA  &UNIQUE+1
.END1    AIF   (&I LT N'&SYSLIST(1)).LOOP1
         AIF   (&UNIQUE GT 1).NOTU
         AIF   ('&TYPE' NE 'DESC' OR NOT &BIT(8) OR &BIT(9)).OUTOK
         MNOTE 8,'DESC=8(OUT OF LINE) REQUIRES DESC=9(ID ADDED)'
         MNOTE *,'DESC=9 ADDED TO LIST - ASSEMBLY CONTINUES'
&BIT(9)  SETB  1
.OUTOK   ANOP
&I       SETA  1
.LOOP2   ANOP
&J       SETA  1+8*&BIT(&I)+4*&BIT(&I+1)+2*&BIT(&I+2)+&BIT(&I+3)
&DIGIT   SETC  '&DIGIT'.'0123456789ABCDEF'(&J,1)
&I       SETA  &I+4
         AIF   (&I LE 16).LOOP2
&L       DC    X'&DIGIT'
         MEND
