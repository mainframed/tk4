         MACRO ,
&NM      GETREG &R1,&R2,&MODE=OS,&WORK=R15                      GP04234
.**********************************************************************
.*                                                                    *
.*   LOAD REGISTER(S) FROM SAVE AREA OR STACK                         *
.*   MODE = OS - R13 IS CURRENT SAVE AREA; GET FROM OLDER S.A.        *
.*   MODE = USER - R13 POINTS TO USER'S AREA                          *
.*   MODE = BAKR (OR ZZZBAKR GLOBAL ON) - USE EREG                    *
.*                                                                    *
.*   ONLY R14 - R12 ARE VALID, AND OPTIONAL SECOND REGISTER MUST      *
.*     NOT BE SAVE AREA AFTER FIRST (R14-R5 OK; R12-R15 NOT)          *
.*                                                                    *
.**********************************************************************
         GBLB  &MVSESA,&ZZZBAKR
         GBLC  &MACPLAB
         LCLC  &O1,&O2,&W,&F
&MACPLAB SETC  '&NM'
&W       SETC  '&WORK'
&O1      SETC  '&R1(1)'
&O2      SETC  '&R2(1)'
         AIF   ('&O2' NE '').O
&O2      SETC  '&O1'
.O       AIF   ('&MODE' EQ 'BAKR' OR &ZZZBAKR).BAKR
&F       SETC  '4*((&O1+2)-(((&O1+2)/16)*16))+12'
   AIF   ('&MODE' EQ 'OS' OR '&MODE' EQ 'OSSAVE' OR '&MODE' EQ '').STD
&W       SETC  'R13'         ALREADY POINTS TO USER'S SAVE AREA
         AIF   ('&MODE' EQ 'USER').USER
         MNOTE 4,'GETREG: INVALID MODE=&MODE '
.STD     AIF   ('&O2' EQ '0' OR '&O2' EQ 'R0').STD1
&W       SETC  '&O2'
         AGO   .STDL
.STD1    AIF   ('&O1' EQ '0' OR '&O1' EQ 'R0').STDL
&W       SETC  '&O1'
.STDL    MACPARM &W,4(,R13),OP=L  USER'S SAVE AREA
.USER    AIF   ('&O1' NE '&O2').STDLM
         MACPARM &O1,&F.(,&W),OP=L
         MEXIT ,
.STDLM   MACPARM &O1,&O2,&F.(&W),OP=LM,MODE=THREE
         MEXIT ,
.BAKR    EREG  &O1,&O2
         MEND  ,
