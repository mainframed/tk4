         MACRO ,
&NM      XSLOAD &NAME1,&NAME2,&LFETCH=NO
.*--------------------------------------------------------------------*
.*  XSLOAD IS USED TO LOAD AND STORE EXHIBIT SUBROUTINE MODULES       *
.*  USING STANDARD CONVENTIONS (E.G., SUBXSCRN)                       *
.*  MODULE NAME IS THE SAME AS THE ADDRESS {I.E., SUBXSCRN DC A(0)}   *
.*  UNLESS A SECOND PARAMETER IS SPECIFIED {E.G., (SUBXSCRN,XLINE)  } *
.*    A THIRD SUBPARAMETER OF N MAY BE SPECIFIED TO INHIBIT EXPANSION *
.*  OF A DS BY SERVDEFS                                               *
.*--------------------------------------------------------------------*
         GBLC  &MACPLAB
         GBLC  &SRVLMOD(20),&SRVLDEL(20)
         GBLB  &SRVBMOD(20),&BUGBEAR                            GP03246
         GBLA  &SRVNMOD
         LCLC  &CL,&CM
         LCLB  &USELOAD                                         GP03246
         LCLA  &I,&J,&N
&N       SETA  N'&SYSLIST
&MACPLAB SETC  '&NM'
         AIF   ('&LFETCH' EQ '').DEFLOAD                        GP03246
         AIF   ('&LFETCH'(1,1) EQ 'Y').SETLOAD                  GP03246
         AIF   ('&LFETCH'(1,1) EQ 'N').SVCLOAD                  GP03246
.DEFLOAD AIF   (NOT &BUGBEAR).SVCLOAD                           GP03246
.SETLOAD ANOP  ,                                                GP03246
&USELOAD SETB  1             USE LOAD RATHER THAN SERVCALL LPALD
.SVCLOAD AIF   (&N LT 1).OOPS                                   GP03246
.MEMLOOP AIF   (&I GE &N).TEST
&I       SETA  &I+1
&SRVBMOD(&SRVNMOD+1) SETB 0    JUST IN CASE
.*--------------------------------------------------------------------*
.*  SUBOPERAND OF FORM (MOD-NAME,DC-NAME)                             *
.*--------------------------------------------------------------------*
         AIF   (N'&SYSLIST(&I) EQ 1).TRYONE
         AIF   ('&SYSLIST(&I,1)' EQ '').MEMLOOP
&CL      SETC  '&SYSLIST(&I,1)'
&CM      SETC  '&SYSLIST(&I,1)'
         AIF   ('&SYSLIST(&I,2)' EQ '').SEE3SUB
&CM      SETC  '&SYSLIST(&I,2)'
         AGO   .SEE3
.SEE3SUB AIF   (K'&CL LT 4).SEE3
         AIF   ('&CL'(1,3) NE 'SUB').SEE3
&CM      SETC  '@'.'&CM.'      '
&CM      SETC  '&CM'(1,8)
.SEE3    AIF   (N'&SYSLIST(&I) LT 3).DONTWO
         AIF   ('&SYSLIST(&I,3)' NE 'N' AND '&SYSLIST(&I,3)' NE 'NO'   *
               AND '&SYSLIST(&I,3)' NE '''N''').DONTWO
&SRVBMOD(&SRVNMOD+1) SETB 1    INHIBIT DS/DC EXPANSION
         AGO   .DONTWO
.*--------------------------------------------------------------------*
.*  SUBOPERAND OF FORM MOD-NAME - SAVE IN SAME NAME UNLESS SUB----    *
.*--------------------------------------------------------------------*
.TRYONE  ANOP  ,
&CL      SETC  '&SYSLIST(&I)'
&CM      SETC  '&SYSLIST(&I)'
         AIF   (K'&CL LT 4).DONTWO
         AIF   ('&CL'(1,3) NE 'SUB').DONTWO
&CM      SETC  '@'.'&CM.'      '
&CM      SETC  '&CM'(1,8)
.DONTWO  AIF   ('&CL' EQ '').MEMLOOP
&J       SETA  &J+1
&MACPLAB LOAD  0,EPLOC==CL8'&CL '                               GP03250
&MACPLAB SETC  ''                                               GP03250
.DOCOM   ST    R0,&CM                                           GP03246
.*--------------------------------------------------------------------*
.*  REMEMBER DS NAME FOR SAVE AREA; IF DS DIFFERENT, REMEMBER DELETE  *
.*--------------------------------------------------------------------*
&SRVNMOD SETA  &SRVNMOD+1
&SRVLMOD(&SRVNMOD) SETC  '&CM'
         AIF   ('&CL' EQ '&CM').MEMLOOP
&SRVLDEL(&SRVNMOD) SETC  '&CL'
         AGO   .MEMLOOP
.TEST    AIF   (&J GT 0).GOODBYE
.OOPS    MNOTE 0,'XSLOAD - NO USABLE MODULE NAMES SPECIFIED'
         MACPARM MODE=LBL
.GOODBYE MEND  ,
