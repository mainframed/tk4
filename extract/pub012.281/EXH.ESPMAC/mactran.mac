         MACRO ,                                       ADDED ON GP14231
        MACTRAN &STR,&IN,&OUT     TRANSLATE CHARACTERS IN &STRING
.*
.*   INNER MACRO FOR MACRO PROCESSING
.*       MACTRAN &STR  WHERE &STR IS UNQUOTED, QUOTED, OR CONSTANT
.*                 FORMAT (E.G.,  XYZ, 'text', X'12AB', CL8'HI', =C'A')
.*   RETURNS:
.*       &MACPNUL   FOR OMITTED PARAMETER OR EMPTY STRING ('')
.*       &STR IN &MACQSTR, K'&STR IN &MACPLEN
.*
         GBLA  &MACPLEN      RETURN SIGNIFICANT LENGTH OF STRING
         GBLB  &MACPNUL      TRUE IF NULL STRING
         GBLB  &MACQUOT      TRUE IF ORIGINAL WAS QUOTED
         GBLB  &MACPERR      TRUE IF ERROR
         GBLC  &MACQSTR      RETURN QUOTED STRING
         LCLA  &I,&J,&K,&LENIN,&LENOU
         LCLC  &C,&F,&T,&LOCIN,&LOCOU
         MACQOTER &IN,OPT=STRIP   UNQUOTE 'FROM' CHARACTERS
         AIF   (&MACPNUL).NEEDTO
&LOCIN   SETC  '&MACQSTR'
&LENIN   SETA  K'&LOCIN          &MACPLEN
         MACQOTER &OUT,OPT=STRIP  UNQUOTE 'TO' CHARACTERS
&LOCOU   SETC '&MACQSTR'
&LENOU   SETA  &LENIN            &MACPLEN
&LOCOU   SETC  '&LOCOU'.'                                              *
                                                                      '
&LOCOU   SETC  '&LOCOU'(1,&LENIN)  ONE INPUT CHAR PER OUTPUT CHAR
&K       SETA  K'&STR
&MACPNUL SETB  1
&MACPLEN SETA  &K
&MACQSTR SETC  '&STR'
         AIF   (&K LT 1).MEND
&MACPNUL SETB  0
&I       SETA  1
.LOOP    ANOP  ,
&J       SETA  1
&C       SETC  '&MACQSTR'(&I,1)
.INNER   ANOP  ,
&F       SETC  '&LOCIN'(&J,1)
&T       SETC  '&LOCOU'(&J,1)
         AIF   ('&C' NE '&F').BUMPJ
         AIF   (&J EQ 1).FRONT
         AIF   (&J EQ &K).BACK
&MACQSTR SETC  '&MACQSTR'(1,&I-1).'&T'.'&MACQSTR'(&I+1,&K-&I)
         AGO   .BUMPI
.FRONT   ANOP  ,
&MACQSTR SETC  '&T'.'&MACQSTR'(2,&K-1)
         AGO   .BUMPI
.BACK    ANOP  ,
&MACQSTR SETC  '&MACQSTR'(1,&K-1).'&T'
         AGO   .BUMPI
.BUMPJ   AIF   (&J GE &LENIN).BUMPI
&J       SETA  &J+1
         AGO   .INNER
.BUMPI   AIF   (&I GE &K).MEND
&I       SETA  &I+1
         AGO   .LOOP
.NEEDTO  MNOTE 8,'MACTRAN: INPUT CHARACTER STRING MISSING'
.MEND    MEND  ,
