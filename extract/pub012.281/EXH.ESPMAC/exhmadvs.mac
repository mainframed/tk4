         MACRO ,
         EXHMADVS &IN=NO,&YES=NO,&RAN=NO,&VOCAB=NO,&SUB=,&FUN=
         GBLC  &CRT
         AIF   ('&RAN' EQ 'NO').NORAN
*
*RAN: PROC (N) RETURNS (FIXED BIN(31));
*
*/* RETURNS RANDOM NUMBER BETWEEN 0 AND N-1 */
*
*        DCL N FIXED BIN(31);
*        DCL RANRTN FIXED BIN(31);
*        DCL Y FLOAT(6);
*
*        RANRTN=N;
*        DO WHILE (RANRTN = N);
*            CALL RANDU(IX,IY,Y);
*            IX=IY;
*            RANRTN=Y*N;
*            END;
*        RETURN(RANRTN);
*        END;
RAN      STM   R1,R8,24(R13)
         ST    R9,SVGETIN
         LH    R0,0(,R1)
         ST    R0,DB+4
         MVI   DB+4,X'46'    FLOAT
         BALS  R9,PCT
         LD    R0,RANDAU
         AD    R0,RANDAU
         ME    R0,=E'0.23283065E-9'  SCALE TO .0 - .999
         ME    R0,DB+4       SCALE TO SUPPLIED VALUE
         L     R9,SVGETIN
         AU    R0,=X'46000000'  NORMALIZE
         STE   R0,DB
         MVI   DB,0          KILL EXPONENT
         L     R0,DB
         B     FNEXIT
*
*PCT: PROC (N) RETURNS (BIT(1));
*
*        DCL N FIXED BIN(31);
*
*        IC3=100;
*        IC3=RAN(IC3);
*        RETURN(IC3 < N);
*        END;
PCT      STM   R1,R8,24(R13)
         L     R1,RANDAU+4
         CLI   RANDAU,0      INITIALIZED YET ?
         BNE   PCTOLD        YES
         TIME  BIN
         AR    R1,R0
         N     R1,=X'3FFFFFFF'
         LA    R0,1          MAKE SURE IT'S ODD
         OR    R1,R0
         MVI   RANDAU,X'4E'  MAKE EXPONENT
PCTOLD   M     R0,=X'0001000D'
         SLL   R1,1
         SRL   R1,1          KILL SIGN
         ST    R1,RANDAU+4
         LD    R0,RANDAU     GET VALUE/2
         AD    R0,RANDAU     DOUBLE
         ME    R0,=E'0.23283065E-7'  SCALE 0-99
PCTRAN   AU    R0,=X'46000000'  NORMALIZE
         STE   R0,DB
         MVI   DB,0          KILL EXPONENT
         L     R1,24(R13)    GET PARM POINTER BACK
         LH    R0,0(,R1)     GET VALUE
         C     R0,DB
         BH    FNTRUE
         B     FNFALSE
.NORAN   SPACE 2
*/* I/O ROUTINES (SPEAK, PSPEAK, RSPEAK, GETIN, YES, A5TOA1) */
*
*
*SPEAK: PROC (N);
*
*        DCL N FIXED BIN(31);
*        DCL (I,K,L) FIXED BIN (31);
*
*/*
*   PRINT THE MESSAGE WHICH STARTS AT LINES(N).  PRECEDE IT
*   WITH A BLANK LINE UNLESS BLKLIN IS FALSE.
**/
*
*        IF N = 0 THEN RETURN;
*        IF LINES(N+1) = '>$<' THEN RETURN;
*        IF (BLKLIN) THEN CALL LINESKP;
*        K=N;
*L1:     L=ABS(LINES(K))-1;
*        K=K+1;
*        PUT STRING (OUTSTR) EDIT ((LINES(I) DO I=K TO L)) (14 A(5));
*        CALL LINEOUT;
*        K=L+1;
*        IF LINES(K) >= 0 THEN GOTO L1;
*        RETURN;
*        END;
SPEAK    STM   R1,R10,SPEAKSV  CLOBBERS R14-R0
SPEAKS   MVC   CURRCARD(4),0(R1)   MOVE CARD # / # CARDS
         CLC   CURRCARD,ZEROES
         BE    SPEAKEXT
         LH    R0,NEXTNUM
         LTR   R0,R0
         BP    *+12
         LA    R0,1
         STH   R0,NEXTNUM    AT LEAST ONE LINE
SPEAKONE BALS  R9,GETCARD    GET A CARD
         CLC   =C'>$<',8(R1)  DUMMY ?
         BE    SPEAKEXT      YES; QUIT
         BALS  R14,LINEOUT   SKIP A LINE
         TM    BLKLIN,1      FORCED SKIP ?
         BZ    *+8           NO
SPEAKMOR BALS  R14,LINEOUT   SKIP ANOTHER
         MVI   BLKLIN,0
         MVC   0(72,R10),8(R1)  MOVE THE TEXT
         LH    R0,NEXTNUM
         BCT   R0,SPEAKTWO
SPEAKEXT LM    R1,R10,SPEAKSV   RELOAD
         BR    R9
SPEAKTWO STH   R0,NEXTNUM
         BALS  R9,GETNCARD
         CLC   =C'>$<',8(R1)
         BNE   SPEAKMOR
         B     SPEAKEXT
*
*
*
*PSPEAK:PROC (MSG,SKIP);
*
*        DCL (MSG,SKIP) FIXED BIN(31);
*        DCL (I,M) FIXED BIN (31);
*
*/*
*   FIND THE SKIP+1ST MESSAGE FROM MSG AND PRINT IT.  MSG
*   SHOULD BE THE INDEX OF THE INVENTORY MESSAGE FOR OBJECT.
*   (INVEN+N+1 MESSAGE IS PROP=N MESSAGE).
**/
*
*        M=PTEXT(MSG);
*        IF SKIP < 0 THEN GOTO L9;
*        DO I=0 TO SKIP;
*L1:         M=ABS(LINES(M));
*            IF LINES(M) >= 0 THEN GOTO L1;
*            END;
*L9:     CALL SPEAK(M);
*        RETURN;
*        END;
PSPEAK   STM   R1,R10,SPEAKSV
         LH    R1,0(,R1)     GET MESSAGE NUMBER
         LTR   R1,R1
         BNPR  R9            INVALID; SKIP
         SLL   R1,2
         LA    R1,PTEXT-4(R1)  POINT TO FIRST MESSAGE OF GROUP
         LH    R2,0(,R2)     GET SKIP VALUE
         LTR   R2,R2         ANY ?
         BM    SPEAKS        NO; DO THE HEADER MESSAGE
         STH   R2,NEXTNUM
         OI    NEXTNUM+1,C'0'  MAKE SUB-SECTION NUMBER
         MVC   CURRCARD,0(R1)  MOVE CARD NUMBER, BUT READ NEXT
PSPEAKER BALS  R9,GETNCARD   GET NEXT CARD AFTER CURRENT
         CLC   =C'00',1(R1)  IS IT A SUB-SECTION CARD ?
         BNE   SPEAKEXT      NO; QUIT
         CLC   0(1,R1),NEXTNUM+1  CORRECT SKIP NUMBER ?
         BH    SPEAKEXT      NO
         BL    PSPEAKER      LOW; GET NEXT CARD
         CLC   =C'>$<',8(R1)  DUMMY CARD ?
         BE    SPEAKEXT      YES; QUIT
         BALS  R14,LINEOUT   GET ROOM FOR LINE
         TM    BLKLIN,1      IS SKIP ON ?
         BZ    *+8           NO
         BALS  R14,LINEOUT   SKIP ANOTHER
         MVI   BLKLIN,0      TURN SKIP OFF
         MVC   0(72,R10),8(R1)  MOVE THE LINE
         B     PSPEAKER      TRY AGAIN
*
*
*
*RSPEAK: PROC (I);
*
*        DCL I FIXED BIN(31);
*
*/* PRINT THE I-TH "RANDOM" MESSAGE (SECTION 6 OF DATABASE). */
*
*        IF I ^= 0 THEN CALL SPEAK(RTEXT(I));
*        RETURN;
*        END;
RSPEAK#  STM   R1,R10,SPEAKSV
         B     RSPEAKS#
RSPEAK   STM   R1,R10,SPEAKSV
         LH    R1,0(,R1)
RSPEAKS# SH    R1,=H'1'
         BMR   R9
         SLL   R1,2
         LA    R1,RTEXT(R1)
         B     SPEAKS
         SPACE 2
GETNCARD LH    R1,CURRCARD   GET CURRENT CARD NUMBER
         LA    R1,1(,R1)     UP
         STH   R1,CURRCARD   STASH BACK
GETCARD  LH    R1,CURRCARD   GET CARD TO BE READ
         XR    R0,R0
         A     R1,RECPBLK    ADD RECORDS PER BLOCK
         BCTR  R1,0           LESS 1
         D     R0,RECPBLK    GET BLOCK AND RESIDUAL
         MH    R0,RECLEN+2   OFFSET TO CARD
         STH   R0,NEXTOFF    SAVE IT
         XR    R0,R0
         L     R14,BLKPTRK   GET BLOCKS PER TRACK
GETCARD2 BCTR  R1,0          SUBTRACT 1
         DR    R0,R14        GET R IN R0 / TT IN R1
         SLL   R1,16         SHIFT TT
         AH    R0,=H'1'      ADD FINAGLE FACTOR BACK
         SLL   R0,8          SHIFT R
         OR    R1,R0         MAKE TTR0
GETCARD3 C     R1,NEXTTTR    BLOCK IN STORAGE ?
         BE    GETCARDO      YES; JUST GET OFFSET TO CARD
         ST    R9,DYNWORK+12  SAVE RETURN REGISTER
         ST    R1,NEXTTTR    SAVE TTR
         ST    R1,DRTTR      SET FOR READ
         BALS  R9,DREAD
         B     GETCARDF      I/O ERROR
         B     GETCARDF      END-FILE ?
         L     R9,DYNWORK+12   RELOAD RETURN REGISTER
GETCARDO LH    R1,NEXTOFF    GET OFFSET TO THIS CARD
         LA    R1,DATABLOK(R1)  GET CARD ADDRESS
         CLI   7(R1),C' '    UNSCRAMBLED ?
         BER   R9            YES; LEAVE IT
         XC    0(80,R1),0(R7)  UNSCRAMBLE
         BR    R9            RETURN TO CALLER
GETCARDF BALS  R14,LINEOUT
         MVC   0(31,R10),=C'*** Unrecoverable I/O Error ***'
         OI    SUSPEND,SUSTEMP  INHIBIT WRITE
         B     DRXIT         QUIT
         SPACE 2
         AIF   ('&IN' EQ 'NO').NOIN
*
*GETIN: PROC (WORD1,WORD1X,WORD2,WORD2X);
*
*/*
*   GET A COMMAND FROM THE ADVENTURER.  SNARF OUT THE FIRST
*   WORD, PAD IT WITH BLANKS, AND RETURN IT IN WORD1.  CHARS
*   6 THRU 10 ARE RETURNED IN WORD1X, IN CASE WE NEED TO
*   PRINT OUT THE WHOLE WORD IN AN ERROR MESSAGE.  ANY NUMBER
*   OF BLANKS MAY FOLLOW THE WORD.  IF A SECOND WORD APPEARS,
*   IT IS RETURNED IN WORD2 (CHARS 6 THRU 10 IN WORD2X), ELSE
*   WORD2 IS SET TO ZERO.
**/
*
*        DCL (WORD1,WORD1X,WORD2,WORD2X) CHAR(5);
*        DCL (ICX) FIXED BIN(31);
*
*        WORD1,WORD1X,WORD2,WORD2X='     ';
*        IF LOGON THEN PUT FILE(SYSPRINT) SKIP;
*        WORDSTRT=0;
*        DO WHILE (WORDSTRT = 0);
*            IF BLKLIN THEN CALL LINESKP;
*            IC1=0;
*            CALL TREAD(INSTR,IC1,INSTR,INLEN,CCODE);
*            IF CCODE = 1 THEN DO;
*                OUTSTR=SUBSTR(INSTR,1,INLEN);
*                WORDSTRT=VERIFY(OUTSTR,' ');
*                END;
*            ELSE DO;
*                IF BLKLIN THEN CALL LINESKP;
*                PUT STRING (OUTSTR) EDIT
*                   ('Terminal error..reenter.') (A);
*                CALL LINEOUT;
*                END;
*            END;
*        IF LOGON THEN PUT FILE(SYSPRINT) SKIP EDIT (OUTSTR) (A);
*        OUTSTR=SUBSTR(OUTSTR,WORDSTRT);
*        WORDEND=INDEX(OUTSTR,' ')-1;
*        IF WORDEND=-1 THEN WORDEND=LENGTH(OUTSTR);
*        WORDSIZE=MIN(WORDEND,5);
*        WORD1=SUBSTR(OUTSTR,1,WORDSIZE);
*        IF WORDEND > 5 THEN DO;
*            WORDSIZE=MIN(WORDEND-5,5);
*            WORD1X=SUBSTR(OUTSTR,6,WORDSIZE);
*            END;
*        IF WORDEND=LENGTH(OUTSTR) THEN RETURN;
*        OUTSTR=SUBSTR(OUTSTR,WORDEND+1);
*        WORDSTRT=VERIFY(OUTSTR,' ');
*        IF WORDSTRT = 0 THEN RETURN;
*        OUTSTR=SUBSTR(OUTSTR,WORDSTRT);
*        WORDEND=INDEX(OUTSTR,' ')-1;
*        IF WORDEND = -1 THEN WORDEND=LENGTH(OUTSTR);
*        WORDSIZE=MIN(WORDEND,5);
*        WORD2=SUBSTR(OUTSTR,1,WORDSIZE);
*        IF WORDEND > 5 THEN DO;
*            WORDSIZE=MIN(WORDEND-5,5);
*            WORD2X=SUBSTR(OUTSTR,6,WORDSIZE);
*            END;
*        RETURN;
*        END;
GETIN    ST    R9,SVGETIN    SAVE RETURN REGISTER
         STM   R1,R2,PARMS1   SAVE RETURN ADDRESSES
GETINWRT XPOUT PROMPT,L'PROMPT
         EX    0,BLANKREP    ENTER ONLY - BLANK REPLY
         AIF   ('&CRT' NE '3270').NOKEYS
         CLI   REPLY,C' '    PA OR PF KEY USED ?
         BNL   GETINKEY      NO
         CLI   REPLY,X'0D'   CLEAR ?
         BNE   *+12          NO
         BALS  R14,BLANKER   CLEAR THE SCREEN
         B     GETINWRT
         MVC   DB(1),REPLY   SAVE THE KEY VALUE
         EX    0,BLANKREP     BLANK THE REPLY
         LA    R14,KEYTAB    GET KEY TABLE
         LA    R15,(KEYTABN-KEYTAB)/(KEYTAB2-KEYTAB)  GET NUMBER
GETINKLP CLC   DB(1),0(R14)  MATCH ?
         BE    GETINMVK      YES; MOVE REPLACEMENT VALUE
         LA    R14,KEYTAB2-KEYTAB(,R14)
         BCT   R15,GETINKLP  TRY AGAIN
         B     GETINWRT      NOT FOUND
KEYTAB   DC    X'10',CL5'SCORE'  TEST-REQ
KEYTAB2  DC    X'11',CL5'NW   '  PFK 1
         DC    X'12',CL5'N    '  PFK 2
         DC    X'13',CL5'NE   '  PFK 3
         DC    X'14',CL5'W    '  PFK 4
         DC    X'15',CL5'UP   '  PFK 5
         DC    X'16',CL5'E    '  PFK 6
         DC    X'17',CL5'SW   '  PFK 7
         DC    X'18',CL5'S    '  PFK 8
         DC    X'19',CL5'SE   '  PFK 9
         DC    X'1A',CL5'INVEN'  PFK 10
         DC    X'1B',CL5'DOWN '  PFK 11
         DC    X'1C',CL5'LOOK '  PFK 12
         DC    X'0C',CL5'SUSPE'  PA 1
         DC    X'0E',CL5'SUSPE'  CANCEL
KEYTABN  EQU   *
GETINMVK MVC   REPLY+6(5),1(R14)  MOVE PA/PFK VALUE TO REPLY
.NOKEYS  SPACE 1
GETINKEY BALS  R14,LINEOUT
         MVC   3(77,R10),REPLY  ECHO THE REPLY
         LM    R2,R3,PARMS1   GET THE POINTERS BACK
         MVC   0(10,R2),BLANKS   BLANK THE RESPONSE
         MVC   0(10,R3),BLANKS
         LA    R14,1         BXLE INCREMENT
         LA    R15,REPLAST   LAST BYTE OF REPLY
         LA    R1,REPLY
GETIN1BK CLI   0(R1),C' '    LEADING BLANK ?
         BNE   GETIN1WD      NO
         BXLE  R1,R14,GETIN1BK
GETINEXT L     R9,SVGETIN
         BR    R9            RETURN
GETIN1WD LA    R10,10        SET MAX TO MOVE
GETIN1WL CLI   0(R1),C' '    BLANK ?
         BE    GETIN2BK      YES; SKIP IT
         CLI   0(R1),C'A'    PUNCTUATION ?
         BL    GETIN1WX      YES; IGNORE
         LTR   R10,R10       ROOM FOR MORE ?
         BNP   GETIN1WX      NO
         MVC   0(1,R2),0(R1)  COPY A BYTE
         LA    R2,1(,R2)
         BCTR  R10,0
GETIN1WX BXLE  R1,R14,GETIN1WL  TRY AGAIN
         B     GETINEXT
GETIN2BK CLI   0(R1),C' '    SEPARATING BLANK ?
         BNE   GETIN2WD      NO; DO SECOND WORD
         BXLE  R1,R14,GETIN2BK
         B     GETINEXT
GETIN2WD LA    R10,10
GETIN2WL CLI   0(R1),C' '    END ?
         BE    GETIN3BK
         CLI   0(R1),C'A'    PUNCTUATION ?
         BL    GETIN2WX
         LTR   R10,R10
         BNP   GETIN2WX
         MVC   0(1,R3),0(R1)
         LA    R3,1(,R3)
         BCTR  R10,0
GETIN2WX BXLE  R1,R14,GETIN2WL
         B     GETINEXT
GETIN3BK L     R3,PARMS1+4    RELOAD SECOND WORD
         CLC   =C'A ',0(R3)  IS IT 'A ' ?
         BE    GETIN3BM      YES; MAYBE SKIP IT
         CLC   =C'THE ',0(R3)  IS IT 'THE' ?
         BNE   GETINEXT      NO
GETIN3BM CLC   BLANKS,0(R1)  ANY MORE TEXT ?
         BE    GETINEXT      NO; RETURN IT AS IS
         MVC   0(10,R3),BLANKS  CLEAR THE FIELD AGAIN
         B     GETIN2BK      YES; SKIP THE THE
         AIF   ('&YES' EQ 'NO').YESIN
.NOIN    AIF   ('&YES' EQ 'NO').NOYES
*
*
*
*YES: PROC (X,Y,Z) RETURNS (BIT(1));
*
*        DCL (X,Y,Z) FIXED BIN(31);
*
*/*
*   PRINT MESSAGE X, WAIT FOR YES/NO ANSWER.  IF YES, PRINT Y
*   AND LEAVE YEA TRUE; IF NO, PRINT Z AND LEAVE YEA FALSE.
**/
*
*
*L1:     IF X ^= 0 THEN CALL RSPEAK(X);
*        CALL GETIN(REPLI,JUNK1,JUNK2,JUNK3);
*        IF REPLI = 'YES' × REPLI = 'Y' THEN GOTO L10;
*        IF REPLI = 'NO' × REPLI = 'N' THEN GOTO L20;
*        CALL LINESKP;
*        PUT STRING (OUTSTR) EDIT ('Please answer the question!') (A);
*        CALL LINEOUT;
*        GOTO L1;
*L10:    IF Y ^= 0 THEN CALL RSPEAK(Y);
*        RETURN('1'B);
*L20:    IF Z ^=0 THEN CALL RSPEAK(Z);
*        RETURN('0'B);
*        END;
GETYES   ST    R9,SVGETIN
         STM   R1,R3,PARMS1  SAVE ENTRY VALUES
         LH    R0,0(,R1)     ANY TEXT ?
         LTR   R0,R0
         BNP   YESL1         NO
         BALS  R9,RSPEAK     PUT IT OUT
YESL1    LA    R2,PROMPT
         LA    R3,L'PROMPT   SET NORMAL PROMPT
YESREP   XPOUT (R2),(R3)     WRITE AND GET REPLY
         B     YESBAD        ENTER ONLY - REPROMPT
         LA    R1,REPLY
         LA    R2,L'REPLY
YESLOC   CLI   0(R1),C' '    BLANK ?
         BNE   YESTEST
         LA    R1,1(,R1)
         BCT   R2,YESLOC
YESBAD   LA    R2,YESPROMP
         LA    R3,L'YESPROMP
         B     YESREP        FORCE A RESPONSE
YESTEST  CLI   0(R1),C'Y'    POSSIBLE YES ?
         BNE   YESTNO
         CLI   1(R1),C' '    JUST 'Y' ?
         BE    YESL10        YES
         CLI   1(R1),C'E'    ACCEPT YES, YEP, YEAH, ETC.
         BE    YESL10
         B     YESBAD
YESTNO   CLI   0(R1),C'N'    NO ?
         BNE   YESBAD
YESL20   L     R1,PARMS3     GET PARMS BACK
         BALS  R9,RSPEAK     MESSAGE, IF ANY
         L     R9,SVGETIN
         STM   R1,R8,24(R13)  SAVE ALMOST ALL PARMS
         B     FNFALSE
YESL10   L     R1,PARMS2
         BALS  R9,RSPEAK
         L     R9,SVGETIN
         STM   R1,R8,24(R13)  SAVE ALMOST ALL PARMS
         B     FNTRUE
YESPROMP SMI   'Please answer the question  ',SMI=5A
.YESIN   ANOP  ,
PROMPT   SMI   'Command me  ',SMI=7A
BLANKREP MVC   REPLY,BLANKS  CLEAR THE RESPONSE
.NOYES   SPACE 2
         AIF   ('&VOCAB' EQ 'NO').NOVOCAB
*/*
*   DATA STRUCTURE ROUTINES (VOCAB, DSTROY, JUGGLE, MOVE,
*   PUT, CARRY, DROP)
**/
*
*VOCAB: PROC (ID,INIT) RETURNS(FIXED BIN(31));
*
*        DCL ID CHAR(5);
*        DCL INIT FIXED BIN(31);
*        DCL I    FIXED BIN(31);
*
*/*
*   LOOK UP ID IN THE VOCABULARY (ATAB) AND RETURN ITS
*   "DEFINITION" (KTAB), OR -1 IF NOT FOUND.  IF INIT IS
*   POSITIVE, THIS IS AN INITIALISATION CALL SETTING UP A
*   KEYWORD VARIABLE, AND NOT FINDING IT CONSTITUTES A BUG.
*   IT ALSO MEANS THAT ONLY KTAB VALUES WHICH TAKEN OVER 1000
*   EQUAL INIT MAY BE CONSIDERED.  (THUS "STEPS", WHICH IS A
*   MOTION VERB AS WELL AS AN OBJECT, MAY BE LOCATED AS AN
*   OBJECT.)  AND IT ALSO MEANS THE KTAB VALUE IS TAKEN MOD
*   1000.
**/
*
*       DCL VOCRTN FIXED BIN(31);
*
*        DO I=1 TO TABSIZ;
*            IF KTAB(I) = -1 THEN GOTO L2;
*            IF INIT >= 0 & KTAB(I)/1000 ^= INIT THEN GOTO L1;
*            IF ATAB(I) = ID THEN GOTO L3;
*L1:         END;
*  ICONST=21;
*        CALL BUG(ICONST);
*
*L2:     IF INIT < 0 THEN RETURN(-1);
*  ICONST=5;
*        CALL BUG(ICONST);
*
*L3:     VOCRTN=KTAB(I);
*        IF INIT >= 0 THEN VOCRTN=MOD(VOCRTN,1000);
*        RETURN(VOCRTN);
*        END;
VOCAB    STM   R1,R8,24(R13)
         LH    R2,TABSIZ
         LA    R3,KTAB
         LA    R4,ATAB
         LH    R0,=H'-1'     PROV. SET FALSE
VOCABLE  TM    0(R3),X'80'   END OF LIST ?
         BNZ   FNEXIT        RETURN FALSE
         CLC   0(5,R1),0(R4) MATCHING WORD ?
         BNE   VOCABL2
         CLC   0(2,R3),IC1
         BL    VOCABL2       NOT IN RANGE
         CLC   0(2,R3),IC2
         BNH   VOCABL3
VOCABL2  LA    R3,2(,R3)
         LA    R4,5(,R4)
         BCT   R2,VOCABLE
         LA    R3,=H'-1'     FAKE END
VOCABL3  LH    R0,0(,R3)
         CLC   IC1(4),=H'0,3999'  GENERAL SEARCH ?
         BE    FNEXIT        YES; RETURN TRUE VALUE
         SRDA  R0,32
         D     R0,=F'1000'   RETURN MOD
         B     FNEXIT        RETURN WITH CONDITION CODE
.NOVOCAB SPACE 2
         AIF   ('&SUB' EQ 'NO').NOSUB
*
*
*
*DSTROY: PROC (OBJECT);
*
*        DCL OBJECT FIXED BIN(31);
*
*/*
*   PERMANENTLY ELIMINATE "OBJECT" BY MOVING TO A
*   NON-EXISTANT LOCATION.
**/
*
*
*        ICONST=0;  CALL MOVE(OBJECT,ICONST);
*        RETURN;
*        END;
DSTROY   STM   R1,R8,24(R13)
         LA    R2,ICONST
         XC    ICONST,ICONST
         B     MOVER         CALL MOVE
*
*
*
*JUGGLE: PROC (OBJECT);
*
*        DCL OBJECT FIXED BIN(31);
*        DCL (I,J)  FIXED BIN(31);
*
*/*
*   JUGGLE AN OBJECT BY PICKING IT UP AND PUTTING IT DOWN
*   AGAIN, THE PURPOSE BEING TO GET THE OBJECT TO THE FRONT
*   OF THE CHAIN OF THINGS AT ITS LOC.
**/
*
*        I=PLACE(OBJECT);
*        J=FIXED(OBJECT);
*        CALL MOVE(OBJECT,I);
*        CALL MOVE(OBJECT+100,J);
*        RETURN;
*        END;
JUGGLE   STM   R1,R8,24(R13)
         ST    R9,SVGETIN
         LH    R14,0(,R1)
         LA    R0,100(,R14)  OBJECT+100
         STH   R0,DB         TEMP SAVE
         SLL   R14,1
         LH    R2,PLACE-2(R14)
         STH   R2,DB+4
         LA    R2,DB+4       SAVE VALUE OVER CARRY/DROP CALL
         LH    R3,FIXED-2(R14)
         STH   R3,DB+6
         BALS  R9,MOVER
         LA    R1,DB
         LA    R2,DB+6
         BALS  R9,MOVER
         L     R9,SVGETIN
         B     FNEXIT
*
*
*
*MOVE: PROC (OBJECT,WHERE);
*
*        DCL (OBJECT,WHERE) FIXED BIN(31);
*
*/*
*   PLACE ANY OBJECT ANYWHERE BY PICKING IT UP AND DROPPING
*   IT.  MAY ALREADY BE TOTING, IN WHICH CASE THE CARRY IS A
*   NO-OP.  MUSTN'T PICK UP OBJECTS WHICH ARE NOT AT ANY LOC,
*   SINCE CARRY WANTS TO REMOVE OBJECTS FROM ATLOC CHAINS.
**/
*
*        IF OBJECT > 100 THEN GOTO L1;
*        FROM=PLACE(OBJECT);
*        GOTO L2;
*L1:     FROM=FIXED(OBJECT-100);
*L2:     IF FROM > 0 & FROM <= 150 THEN CALL CARRY(OBJECT,FROM);
*        CALL DROP(OBJECT,WHERE);
*        RETURN;
*        END;
MOVE     STM   R1,R8,24(R13)
MOVER    ST    R9,SVSPEAK
         STM   R1,R2,SPEAKSV   SAVE FOR SPECIAL ENTRY
         LH    R14,0(,R1)
         CH    R14,=H'100'
         SLL   R14,1
         BH    MOVEL1
         LH    R0,PLACE-2(R14)
         B     MOVEL2
MOVEL1   LH    R0,FIXED-202(R14)
MOVEL2   STH   R0,FROM
         LTR   R0,R0
         BNP   MOVEL3
         CH    R0,=H'150'
         BH    MOVEL3
         LA    R2,FROM
         BALS  R9,CARRIER
MOVEL3   L     R9,SVSPEAK
         LM    R1,R2,SPEAKSV   RELOAD FOR SPECIAL ENTRY
         B     DROPPER
*
*
*
*PUT: PROC (OBJECT,WHERE,PVAL) RETURNS(FIXED BIN(31));
*
*        DCL (OBJECT,WHERE,PVAL) FIXED BIN(31);
*
*/*
*   PUT IS THE SAME AS MOVE, EXCEPT IT RETURNS A VALUE USED
*   TO SET UP THE NEGATED PROP VALUES FOR THE REPOSITORY
*   OBJECTS.
**/
*
*        CALL MOVE(OBJECT,WHERE);
*        RETURN((-1)-PVAL);
*        END;
PUT      STM   R1,R8,24(R13)
         ST    R9,SVGETIN
         BALS  R9,MOVER
         L     R9,SVGETIN
         LH    R0,=H'-1'
         SH    R0,0(,R3)     -1-PVAL
         B     FNEXIT
*
*
*
*CARRY: PROC (OBJECT,WHERE);
*
*        DCL (OBJECT,WHERE) FIXED BIN(31);
*
*/*
*   START TOTING AN OBJECT, REMOVING IT FROM THE LIST OF
*   THINGS AT ITS FORMER LOCATION.  INCR HOLDNG UNLESS IT WAS
*   ALREADY BEING TOTED.  IF OBJECT>100 (MOVING "FIXED"
*   SECOND LOC), DON'T CHANGE PLACE OR HOLDNG.
**/
*
*
*
*        IF OBJECT > 100 THEN GOTO L5;
*        IF PLACE(OBJECT) = -1 THEN RETURN;
*        PLACE(OBJECT)=-1;
*        HOLDNG=HOLDNG+1;
*L5:     IF ATLOC(WHERE) ^= OBJECT THEN GOTO L6;
*        ATLOC(WHERE)=LINK(OBJECT);
*        RETURN;
*L6:     TEMP=ATLOC(WHERE);
*L7:     IF LINK(TEMP) = OBJECT THEN GOTO L8;
*        TEMP=LINK(TEMP);
*        GOTO L7;
*L8:     LINK(TEMP)=LINK(OBJECT);
*        RETURN;
*        END;
CARRY    STM   R1,R8,24(R13)
CARRIER  LH    R3,0(,R1)
         LR    R0,R3
         SLL   R3,1
         CH    R0,=H'100'
         BH    CARRYL5
         LA    R14,PLACE-2(R3)
         CLC   =H'-1',0(R14)
         BE    FNEXIT
         MVC   0(2,R14),=H'-1'
         LH    R0,HOLDNG
         AH    R0,=H'1'
         STH   R0,HOLDNG
CARRYL5  LH    R4,0(,R2)
         SLL   R4,1
         LA    R14,ATLOC-2(R4)
         CLC   0(2,R14),0(R1)
         BNE   CARRYL6
         LA    R15,LINK-2(R3)
         MVC   0(2,R14),0(R15)
         B     FNEXIT
CARRYL6  MVC   TEMP,0(R14)
CARRYL7  LH    R14,TEMP
         SLA   R14,1
         BZ    BUG99         PROGRAM ERROR IF NOT HERE
         LA    R14,LINK-2(R14)
         CLC   0(2,R14),0(R1)
         BE    CARRYL8
         MVC   TEMP,0(R14)
         B     CARRYL7
CARRYL8  LH    R0,LINK-2(R3)
         STH   R0,0(,R14)
         B     FNEXIT
*
*
*
*DROP: PROC (OBJECT,WHERE);
*
*        DCL (OBJECT,WHERE) FIXED BIN(31);
*
*/*
*   PLACE AN OBJECT AT A GIVEN LOC, PREFIXING IT ONTO THE
*   ATLOC LIST.  DECR HOLDNG IF THE OBJECT WAS BEING TOTED.
**/
*
*        IF OBJECT > 100 THEN GOTO L1;
*        IF PLACE(OBJECT) = -1 THEN HOLDNG=HOLDNG-1;
*        PLACE(OBJECT)=WHERE;
*        GOTO L2;
*L1:     FIXED(OBJECT-100)=WHERE;
*L2:     IF WHERE <= 0 THEN RETURN;
*        LINK(OBJECT)=ATLOC(WHERE);
*        ATLOC(WHERE)=OBJECT;
*        RETURN;
*        END;
DROP     STM   R1,R8,24(R13)
DROPPER  LH    R14,0(,R1)
         CH    R14,=H'100'
         SLL   R14,1
         BH    DROPL1
         LA    R15,PLACE-2(R14)
         CLC   =H'-1',0(R15)
         BNE   DROPL0
         LH    R0,HOLDNG
         BCTR  R0,0
         STH   R0,HOLDNG
         B     DROPL0
DROPL1   LA    R15,FIXED-202(R14)
DROPL0   MVC   0(2,R15),0(R2)
DROPL2   LH    R15,0(,R2)
         LTR   R15,R15
         BNP   FNEXIT
         LA    R14,LINK-2(R14)
         LH    R15,0(,R2)
         SLL   R15,1
         LA    R15,ATLOC-2(R15)
         MVC   0(2,R14),0(R15)
         MVC   0(2,R15),0(R1)
         B     FNEXIT
         AGO   .SUBFUN
.NOSUB   AIF   ('&FUN' EQ 'NO').NOFUN
.SUBFUN  SPACE 1
*/*  SUBROUTINES/FUNCTIONS
*
*
*   TOTING(OBJ)  = TRUE IF THE OBJ IS BEING CARRIED
*   HERE(OBJ)    = TRUE IF THE OBJ IS AT "LOC" (OR IS BEING CARRIED)
*   AT(OBJ)      = TRUE IF ON EITHER SIDE OF TWO-PLACED OBJECT
*   LIQ(DUMMY)   = OBJECT NUMBER OF LIQUID IN BOTTLE
*   LIQLOC(LOC)  = OBJECT NUMBER OF LIQUID (IF ANY) AT LOC
*   BITSET(L,N)  = TRUE IF COND(L) HAS BIT N SET (BIT 0 IS UNITS BIT)
*   FORCED(LOC)  = TRUE IF LOC MOVES WITHOUT ASKING FOR INPUT (COND=2)
*   DARK(DUMMY)  = TRUE IF LOCATION "LOC" IS DARK
*   PCT(N)       = TRUE N% OF THE TIME (N INTEGER FROM 0 TO 100)
*
**/
*
*TOTING: PROC (OBJ) RETURNS (BIT(1));
*
*        DCL OBJ FIXED BIN(31);
*
*        RETURN(PLACE(OBJ) = -1);
*        END;
TOTING   STM   R1,R8,24(R13)
         LH    R15,0(,R1)
         SLL   R15,1
         LA    R15,PLACE-2(R15)  SET FOR 'HERE' RETURN
         CLC   =H'-1',0(R15)
         BE    FNTRUE
FNFALSE  XR    R0,R0
         B     FNEXIT
FNTRUE   LA    R0,1
FNEXIT   LTR   R0,R0
         LM    R1,R8,24(R13)
         BR    R9
*
*HERE: PROC (OBJ) RETURNS (BIT(1));
*
*        DCL OBJ FIXED BIN(31);
*
*        RETURN(PLACE(OBJ) = LOC × TOTING(OBJ));
*        END;
HERE     STM   R1,R8,24(R13)
HERENSV  LR    R14,R9        SAVE RETURN REGISTER
         BALS  R9,TOTING
         LR    R9,R14
         BPR   R9            TRUE IF TOTING
         CLC   LOC,0(R15)    OBJECT HERE ?
         BE    FNTRUE
         B     FNFALSE
*
*AT: PROC (OBJ) RETURNS (BIT(1));
*
*        DCL OBJ FIXED BIN (31);
*
*        RETURN(PLACE(OBJ) = LOC × FIXED (OBJ) = LOC);
*        END;
AT       STM   R1,R8,24(R13)
         LH    R15,0(,R1)
         SLL   R15,1
         LH    R0,LOC
         CH    R0,PLACE-2(R15)
         BE    FNTRUE
         CH    R0,FIXED-2(R15)
         BE    FNTRUE
         B     FNFALSE
*
*LIQ2: PROC (PBOTL) RETURNS (FIXED BIN(31));
*
*        DCL PBOTL FIXED BIN(31);
*        DCL LIQ2TEMP FIXED BIN(31);
*
*        LIQ2TEMP=PBOTL/2;
*        RETURN((1-PBOTL)*WATER+(LIQ2TEMP)*(WATER+OIL));
*        END;
*
*LIQ: PROC (DUMMY) RETURNS (FIXED BIN(31));
*
*        DCL DUMMY FIXED BIN(31);
*
*        IC1=PROP(BOTTLE);
*        IC2=-1-PROP(BOTTLE);
*        IC3=MAX(IC1,IC2);
*        ICONST=LIQ2(IC3);
*        RETURN(ICONST);
*        END;
LIQ      STM   R1,R8,24(R13)
         LH    R15,BOTTLE
         SLL   R15,1
         LH    R14,PROP-2(R15)
         B     LIQ2LIQ
*
*LIQLOC: PROC (LOC) RETURNS (FIXED BIN(31));
*
*        DCL LOC FIXED BIN(31);
*        DCL (LIQTEMP1,LIQTEMP2) FIXED BIN(31);
*
*        LIQTEMP1=COND(LOC)/2;
*        LIQTEMP1=LIQTEMP1*2;
*        LIQTEMP2=COND(LOC)/4;
*        RETURN(LIQ2((MOD(LIQTEMP1,8)-5)*MOD(LIQTEMP2,2)+1));
*        END;
LIQLOC   STM   R1,R8,24(R13)
         LH    R15,0(,R1)
         SLL   R15,1
         LA    R15,COND-2(R15)
         TM    1(R15),X'04'  ANY LIQUID ?
         BZ    FNFALSE       TAKE QUICK EXIT
         LH    R14,0(,R15)   LOAD COND
         LA    R15,2         MASK FOR OIL
         NR    R14,R15       ISOLATE
         B     LIQ2LIQ       AND SET IT
LIQ2LIQ  XR    R0,R0         ENTRY 0 - WATER, 1 - NOTHING, 2 -OIL
         CH    R14,=H'1'
         BE    FNEXIT        NOTHING
         LH    R0,WATER
         BL    FNEXIT
         LH    R0,OIL
         B     FNEXIT
*
*BITSET: PROC (L,N) RETURNS (BIT(1));
*
*        DCL (L,N) FIXED BIN(31);
*        DCL BITTEMP FIXED BIN(31) STATIC;
*
*        BITTEMP=COND(L)/2**N;
*        RETURN(MOD(BITTEMP,2) ^= 0);
*        END;
BITSET   STM   R1,R8,24(R13)
         LH    R14,0(,R1)
         LH    R15,0(,R2)
         LA    R1,1
         SLL   R1,0(R15)
         SLL   R14,1
         LH    R0,COND-2(R14)
         STM   R0,R1,DB
         NC    DB+2(2),DB+6
         CLC   DB+2(2),DB+6
         BE    FNTRUE
         B     FNFALSE
*
*FORCED: PROC (LOC) RETURNS (BIT(1));
*
*        DCL LOC FIXED BIN(31);
*
*        RETURN(COND(LOC) = 2);
*        END;
FORCED   STM   R1,R8,24(R13)
         LH    R15,0(,R1)
         SLL   R15,1
         LH    R15,COND-2(R15)
         CH    R15,=H'2'
         BE    FNTRUE
         B     FNFALSE
*
*DARK: PROC (DUMMY) RETURNS (BIT(1));
*
*        DCL DUMMY FIXED BIN(31);
*
*        RETURN(MOD(COND(LOC),2) = 0 &
*             (PROP(LAMP) = 0 × ^ HERE(LAMP)));
*        END;
DARK     STM   R1,R9,24(R13)
         LA    R1,LOC
         BALS  R9,QCOND
         L     R9,20+9*4(R13)
         TM    1(R14),1
         BNZ   FNFALSE
         LA    R1,LAMP
         BALS  R9,QPROP
         L     R9,20+9*4(R13)
         BZ    FNTRUE
         BALS  R9,HERENSV
         L     R9,20+9*4(R13)
         BZ    FNTRUE        TRUE IF NO LAMP
         B     FNFALSE
         SPACE 1
*****    QUICKY SUBROUTINES ADDED TO SHORTEN THE CODE
         SPACE 1
QPROP    LA    R14,PROP-2
         B     QCOMMON
QFIXED   LA    R14,FIXED-2
         B     QCOMMON
QPLAC    LA    R14,PLAC-2
         B     QCOMMON
QKEY     LA    R14,KEY-2
         B     QCOMMON
QFIXD    LA    R14,FIXD-2
         B     QCOMMON
QATLOC   LA    R14,ATLOC-2
         B     QCOMMON
QLINK    LA    R14,LINK-2
         B     QCOMMON
QPLACE   LA    R14,PLACE-2
         B     QCOMMON
QCOND    LA    R14,COND-2
QCOMMON  STM   R1,R8,24(R13)
         LH    R1,0(,R1)
         SLL   R1,1
         LA    R14,0(R1,R14)  RETURN LOCATION
         LH    R0,0(,R14)    AND VALUE
         B     FNEXIT
.NOFUN   SPACE 1
COMXCTL  MVC   INVOKE+7(1),0(R1)  COPY OVERLAY SUFFIX
         MVC   BROFF2,1(R1)  AND BRANCH CODE
         B     EXCXCTL       XCTL
*
*
*
*A5TOA1: PROC (A,B,CHARS,LENG);
*
*/*
*  A AND B CONTAN A 1-10 CHARACTER WORD IN A5 FORMAT.
*  THEY ARE CONCATENATED AND MOVED INTO A CHAR(1)
*  ARRAY UNTIL A BLANK IS ENCOUNTERED.  THE TOTAL
*  LENGTH IS RETURNED IN LENG.
*
**/
*
*        DCL (A,B) CHAR(5);
*        DCL LENG FIXED BIN(31);
*        DCL CHARS(10) CHAR(1);
*
*        DCL WORDS(2) CHAR(5);
*        DCL XLATETO CHAR(26) INIT ('abcdefghijklmnopqrstuvwxyz');
*        DCL XLATEFR CHAR(26) INIT ('ABCDEFGHIJKLMNOPQRSTUVWXYZ');
*
*        WORDS(1)=TRANSLATE(A,XLATETO,XLATEFR);
*        WORDS(2)=TRANSLATE(B,XLATETO,XLATEFR);
*        LENG=0;
*        DO WORD=1 TO 2;
*            DO CH=1 TO 5;
*                CHARS(LENG+1)=SUBSTR(WORDS(WORD),CH,1);
*                IF CHARS(LENG+1) = ' ' THEN RETURN;
*                LENG=LENG+1;
*                END;
*            END;
*        RETURN;
*        END;
*
*/*  UTILITY ROUTINES (RAN, CIAO, BUG) */
*
*CIAO: PROC;
*
*/*
*   EXITS...NO MESSAGE OUTPUT SINCE CAN'T SAVE CORE IMAGE
**/
*
*        STOP;
*        END;
*
*
*
*BUG: PROC (NUM);
*
*        DCL NUM FIXED BIN(31);
*
*/*
*   THE FOLLOWING CONDITIONS ARE CURRENTLY CONSIDERED FATAL
*   BUGS.  NUMBERS < 20 ARE DETECTED WHILE READING THE
*   DATABASE; THE OTHERS OCCUR AT "RUN TIME".
*        0       MESSAGE LINE > 70 CHARACTERS
*        1       NULL LINE IN MESSAGE
*        2       TOO MANY WORDS OF MESSAGES
*        3       TOO MANY TRAVEL OPTIONS
*        4       TOO MANY VOCABULARY WORDS
*        5       REQUIRED VOCABULARY WORD NOT FOUND
*        6       TOO MANY RTEXT OR MTEXT MESSAGES
*        7       TOO MANY HINTS
*        8       LOCATION HAS COND BIT BEING SET TWICE
*        9       INVALID SECTION NUMBER IN DATABASE
*        20      SPECIAL TRAVEL (500>L>300) EXCEEDS GOTO LIST
*        21      RAN OFF END OF VOCABULARY TABLE
*        22      VOCABULARY TYPE (N/1000) NOT BETWEEN 0 AND 3
*        23      INTRANSITIVE ACTION VERB EXCEEDS GOTO LIST
*        24      TRANSITIVE ACTION VERB EXCEEDS GOTO LIST
*        25      CONDITIONAL TRAVEL ENTRY WITH NO ALTERNATIVE
*        26      LOCATION HAS NO TRAVEL ENTRIES
*        27      HINT NUMBER EXCEEDS GOTO LIST
*        28      INVALID MONTH RETURNED BY DATE FUNCTION
**/
*
*
*        PUT STRING (OUTSTR) EDIT ('Fatal error # ',NUM) (A,F(2));
*        CALL LINEOUT;
*        STOP;
*        END;
BUG99    MVI   ICONST,99     PROGRAM CHECK
BUG      BALS  R14,LINEOUT
         MVC   0(14,R10),=C'Program error '
         XR    R0,R0
         IC    R0,ICONST
         CVD   R0,DB
         UNPK  14(3,R10),DB+6(2)
         OI    16(R10),C'0'
         OI    SUSPEND,SUSTEMP  NO SUSPEND
         B     DRXIT
         SPACE 1
*
*LINESKP: PROC;
*
*/* OUTPUTS A BLANK LINE */
*
*        OUTSTR=' ';
*        CALL LINEOUT;
*        RETURN;
*        END;
*
*LINEOUT: PROC;
*
*/* OUTPUT A LINE TO MILTEN */
*
*        DCL (ICX) FIXED BIN(31);
*
*        INSTR=OUTSTR;
*        IF LOGON & OUTSTR ^= ' ' THEN
*             PUT FILE(SYSPRINT) SKIP EDIT (OUTSTR) (A);
*        CCODE=0;
*        DO WHILE (CCODE = 0);
*            CALL TWRITE(INSTR,LENGTH(OUTSTR),CCODE);
*            END;
*        RETURN;
*        END;
LINEOUT  L     R10,BUFAD     GET TOP OF SCREEN
         LH    R0,LSIZE      GET LINESIZE
         STC   R0,LINEOUT1+5  MAKE OFFSET TO NEXT LINE           89211
         AR    R10,R0        LEAVE TOP LINE BLANK
         L     R15,MSGAD     GET END OF SCREEN
         SR    R15,R0        GET LAST LINE ADDRESS
         STC   R0,LINEOUT1+1  MAKE LENGTH OF MOVE (+1)          GP13068
LINEOUT1 MVC   0(*-*,R10),80(R10)  MOVE A LINE UP               GP13058
         AR    R10,R0
         CR    R10,R15       ALL LINES MOVED ?
         BL    LINEOUT1      NO
         MVC   0(80,R10),BLANKS  CLEAR LAST LINE
         BR    R14           RETURN TO CALLER
         SPACE 1
*
*
* TREAD: PROC(INST1,LINST1,INST2,LINST2,CODE);
*        DCL (INST1,INST2) CHAR(133);
*        DCL (INST2V) CHAR(80) VARYING;
*        DCL (LINST1,LINST2,CODE,ICX) FIXED BIN(31);
*        IF LINST1 = 0 THEN GO TO NOWRITEG;
*     /* PUT FILE(SYSPRINT) SKIP EDIT (INST1) (A); */
*        CALL TPUT(INST1,ICX);
*  /* NOWRITEG: GET SKIP FILE(SYSIN) EDIT (INST2V) (A(133)); */
*     NOWRITEG: CALL TGET(INST2V,ICX);
*               INST2 = INST2V;
*        LINST2 = LENGTH(INST2V);
*        CODE = 1;
*        RETURN;   END;
* TWRITE: PROC(INST,LINST,CODE);
*         DCL (INST) CHAR(133) VAR;
*         DCL (LINST,CODE,ICX) FIXED BIN(31);
*      /* PUT FILE(SYSPRINT) SKIP EDIT(INST) (A); */
*         CALL TPUT(INST,ICX);
*         CODE = 1;
*         RETURN;   END;
*
*  RANDU: PROC(IX,IY,Y);
*         ON FIXEDOVERFLOW;
*         DCL (IX,IY) FIXED BIN(31,0);
*         DCL Y FLOAT(6);
*         IY=IX*65539;
*         IF IY < 0 THEN IY=IY+2147483647+1;
*         Y=IY;
*         Y=Y*.4656613E-9;
*         RETURN;
*          END RANDU;
*
         MEND  ,
