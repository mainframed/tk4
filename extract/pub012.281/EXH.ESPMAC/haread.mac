         MACRO
&NM      HAREAD &TTR,&ERR=IOERR,&EOF=IOERR,&CHECK=YES
         GBLC  &HASP
.**********************************************************************
.*                                                                   **
.*   EXHIBIT MACRO TO READ SPOOL BLOCKS (C.F. EXHCX$JL)              **
.*                                                                   **
.**********************************************************************
&NM      MACPARM R1,&TTR,OP=L,NULL=DRTTR
         MACPARM R0,0        CLEAR FOR LATER IC
         MACPARM R0,8,OP=SLDL GET 'M' BYTE
         ST    R1,DRTTR .    STASH TTR LEFT-ADJUSTED
         STC   R0,DRTTR+3 .   SET EXTENT BYTE FOR MBB CONVERSION 81040
         AIF   ('&CHECK' NE '' AND '&CHECK' NE 'YES').NOCHECK
         AIF   ('&ERR' NE '').OKCHECK
         MNOTE 4,'CHECK BYPASSED - NO ERR BRANCH ADDRESS'
         AGO   .NOCHECK
.OKCHECK L     R15,DRDEB .   GET THE DEB ADDRESS
         NEED  DEB .         REQUEST DEB, DON'T BASE
         CLC   DRTTR+3(1),DEBNMEXT-DEBBASIC(R15)                 81040
         BNL   &ERR .        FAIL INVALID EXTENT
         LR    R14,R0 .      GET EXTENT NUMBER BACK             77110
         SLL   R14,4 .       EXTENT IS 16 BYTES
         LA    R14,DEBBASND-DEBBASIC(R14,R15)  POINT TO EXTENT ENTRY
         CLC   ZEROES(3),1(R14) .  NON-ZERO UCB ?
         BE    &ERR .        NO; VOLUME IS NOT UP
.NOCHECK BALS  R9,DREAD .    PERFORM AN I/O
         AIF   ('&ERR' EQ '').MEND
         B     &ERR .        TAKE I/O ERROR EXIT
         AIF   ('&EOF' EQ '').MEND
         B     &EOF .        TAKE END-FILE EXIT
.MEND    MEND
