         MACRO ,                                       NEW 2003/01/01
&NM      BANDAID &ARG,&A=
         GBLC  &LOCAL                                           GP09301
         GBLB  &ZZSTAE,&ZZSPIE
         LCLA  &OV,&I,&J,&K,&L,&N
         LCLB  &SYSDEB,&SPIE
         LCLC  &C,&OPT,&OPT1,&OPT2,&LBL
&N       SETA  N'&SYSLIST    NUMBER OF ARGUMENTS
&LBL     SETC  '&NM'
         AIF   (&N GT 0).OKARG
.NOARG   MNOTE 8,'BANDAID REQUIRES AN ARGUMENT'
         MEXIT ,
.OKARG   AIF   (&L GE &N).EXPAND
&L       SETA  &L+1
&OPT     SETC  '&SYSLIST(&L)'
         AIF   ('&OPT' EQ '').OKARG
&C       SETC  '&OPT'(1,3).'   '
&C       SETC  '&C'(1,3)
&I       SETA  -2
&J       SETA  1+8*3
.ARGLOOP AIF   (&I GE &J).FAIL
&I       SETA  &I+3
         AIF   ('&C' NE 'INITERCLOSPITRARESPUSPOP'(&I,3)).ARGLOOP
         AIF   (&I GE 7).PART2
         AIF   (&SYSDEB).DUPE
&SYSDEB  SETB  1
&OPT1    SETC  '&OPT'
         AGO   .OKARG
.*
.PART2   AIF   (&SPIE).DUPE
&OV      SETA  ((&I-6)/3)
&OPT2    SETC  '&OPT'
&SPIE    SETB  1
         AGO   .OKARG
.*
.EXPAND  AIF   (NOT &SYSDEB AND NOT &SPIE).NOARG
         AIF   (NOT &SYSDEB).DONDEB
.*   When OPT is INIT:        FOR TERM INVOKES WITHDRAWAL
.*   USE THIS MACRO TO INITIALIZE THE @BANDAID (SYSDEBUG) ESTAE/ESPIE
.*   SERVICE ROUTINE.  REQUIRES //SYSDEBUG OUTPUT DD
&LBL     LOAD  EP=@BANDAID   LOAD @BANDAID, ISSUE ESTAE, ESPIE, ETC.
&LBL     SETC  ' '
         LR    R15,R0
         L     R0,=CL4'&OPT1'  PASS OPTION (FOR LATER ESPIE SUPPORT)
         BASSM R14,R15       INVOKE WITH AMODE31
&ZZSTAE  SETB  1
.*
.DONDEB  AIF  (NOT &SPIE).MEND
.*   When OPT is SPIE:  initialize SPIE services
.*   When OPT is TRAP:  specify address of recovery routine
.*   When OPT is RESET: cancel recovery
.*   When OPT is PUSH:  specify new recovery routine; save old one
.*   When OPT is POP:   cancel current and reinstate old recovery
.*   When OPT is CLOSE: close SYSDEBUG DCB, remove SPIE
.*   REQUIRES //SYSDEBUG OUTPUT DD
&LBL     LOAD  EP=@SPIEDER   LOAD @SPIEDER, ISSUE ESPIE, ETC.
&LBL     SETC  ' '
         LR    R15,R0
         AIF   ('&LOCAL' EQ '').ALIEN  NOT ESP ENVIRONMENT      GP09301
         ST    R15,@SPIEDER
.ALIEN   LA    R0,&OV        PASS OPTION TO INIT
         MACPARM R1,&A,NULL=SKIP
         BASSM R14,R15       INVOKE WITH AMODE
&ZZSPIE  SETB  1
         MEXIT ,
.DUPE    MNOTE 4,'BANDAID: OPTION &OPT CONFLICTS'
         MEXIT ,
.FAIL    MNOTE 4,'BANDAID: OPTION &OPT NOT RECOGNIZED'
.MEND    MEND  ,
