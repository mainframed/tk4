         MACRO ,
&NM      CPOOL &FUN,&OPT,&LINKAGE=,&REGS=,&LOC=,&KEY=,&TCB=,&HDR=,     *
               &PCELLCT=,&SCELLCT=,&CSIZE=,&SP=,&CPID=,&CELL=,         *
               &MF=S,&VERIFY=,&OWNER=
         GBLC  &MACPLAB
&MACPLAB SETC  '&NM'
.**********************************************************************
.*                                                                    *
.*   THIS VERSION OF THE CPOOL MACRO INTERFACES TO SUBROUTINE         *
.*   SUBCPOOL. IT PROVIDES BACKWARD COMPATIBILITY FOR HERCULES        *
.*   RUNNING MVS 3.8J                                                 *
.*     NOT SUPPORTED ARE KEY, TCB, LIST/WORKAREA, PRIV. SP,           *
.*     VERIFY, AND OWNER.                                             *
.*                                                                    *
.**********************************************************************
.*                                                                    *
.*       GET    R0 = CPID  @ -> CELL  CALL SUBCPOOL                   *
.*       FREE   R0 = CPID, R1 = CELL  CALL SUBPOOL+24*                *
.*       BUILD  R1 = PARM LIST        CALL SUBPOOL+28*                *
.*       DELETE R0 = CPID             CALL SUBPOOL+32*                *
.*                                                                    *
.**********************************************************************
.*                                                                    *
.*   PARM LIST IS SAME AS IBM'S FOR COMPATIBILITY                     *
.*      0 - 3     NUMBER OF CELLS REQUESTED
.*      4 - 7     SECONDARY AMOUNT
.*      8 -11     SIZE OF EACH CELL
.*     12         SUBPOOL (ONLY 0-127 WORK)
.*     13         KEY(IGNORED)
.*     14         FLAGS
.*         80       LOC HAS REAL (IGNORED)
.*         60       LOC ANY; 20 BELOW (FORCED); 00 RES
.*         10       USER SPECIFIED TCB (IGNORED)
.*         08       USER SPECIFIED KEY (IGNORED)
.*         04       USER SPECIFIED HDR
.*         03       OWNER IS SYSTEM; 01 PRIMARY; 00 HOME (IGNORED)
.*     15         UNUSED
.*     16-19      TCB (IGNORED)
.*     20-43      HEADER
.*                                                                    *
.**********************************************************************
         GBLC  &MACPLOD                                         GP12154
         LCLA  &BOFF         FUNCTION'S BRANCH OFFSET
         LCLC  &ISN          SECONDARY CELL COUNT
         LCLA  &FGLOC,&FGTCB,&FGKEY,&FGHDR,&FGOWN
         LCLC  &VPN,&VSN,&VSZ,&VSP,&VKY,&VTC,&VHD
         AIF   ('&FUN' EQ 'GET').COMMID     NO OFFSET
&BOFF    SETA  24
         AIF   ('&FUN' EQ 'FREE').FREECEL   BRANCH WITH POOL ID
&BOFF    SETA  28
         AIF   ('&FUN' EQ 'BUILD').BUILDER  BRANCH WITH POOL ID
&BOFF    SETA  32
         AIF   ('&FUN' EQ 'DELETE').COMMID  BRANCH WITH POOL ID
         MACPARM MODE=LBL    EXPAND LABEL, IF ANY
 MNOTE 8,'CPOOL: UNSUPPORTED FUNCTION &FUN '
         MEXIT ,
.FREECEL MACPARM R1,&CELL,NULL=CELL-ADD
.COMMID  MACPARM R0,&CPID,OP=L,NULL=POOL-ID  LOAD THE CELL POOL ID
.CALL    MACSRVLD SUBCPOOL   GET AD-CON NAME FOR SUBROUTINE
         MACPARM R15,&MACPLOD,OP=L  GET SUBROUTINE ADDRESS
         AIF   (&BOFF NE 0).U  ONLY GET HAS COND ENTRY
         AIF   ('&OPT' EQ '' OR '&OPT' EQ 'U' OR '&OPT' EQ 'UNCOND').U
         AIF   ('&OPT' EQ 'C' OR '&OPT' EQ 'COND').C
 MNOTE 8,'CPOOL: REQUEST NOT COND NOR UNCOND. &OPT INVALID'
         AGO   .U
.C       O     R15,=X'80000000'  CONDITIONAL REQUEST
.U       AIF   (&BOFF EQ 0).BR  NO OFFSET
         MACPARM R14,&BOFF.(,R15),OP=BAL  GET ENTRY ADDRESS
         AIF   (&BOFF NE 28).MEXIT  NOT BUILD
         MACPARM R0,&CPID,OP=ST,MODE=REV
.MEXIT   MEXIT ,
.BR      MACPARM R14,(R15),OP=BALR,OPR=BALR
         MACPARM R1,&CELL,OP=ST,MODE=REV,NULL=SKIP  GET RETURNS CELL @
         MEXIT ,
.BUILDER AIF   ('&MF' EQ '').TYPES
         AIF   ('&MF(1)' EQ 'L').TYPEL
         AIF   ('&MF(1)' EQ 'S').TYPES
         AIF   ('&MF(1)' EQ 'E').TYPEE
         MACPARM MODE=LBL
 MNOTE 8,'CPOOL: INVALID TYPE &MF'
         MEXIT ,
.TYPEE   MACPARM R1,&MF(2),NULL=LIST-ADD
         AGO   .POPLIST
.TYPES   MACPARM 2,4,OP=CNOP,OPR=CNOP
         MACPARM R1,*+4+44,OP=BAL  BRANCH AROUND IN-LINE PARM LIST
         DC    XL44'0'       IN-LINE PARM LIST
.POPLIST MACPLOP &PCELLCT,0(,R1),NULL=PCELL-CT
&ISN     SETC  '&PCELLCT'
         AIF   ('&SCELLCT' EQ '').HV2
&ISN     SETC  '&SCELLCT'
.HV2     MACPLOP &ISN,4(,R1),NULL=SCELL-#
         MACPLOP &CSIZE,8(,R1),NULL=CELL-SZ
         AIF   ('&SP' EQ '').NOSP
         MACPLOP &SP,14(,R1),OP=STC
.NOSP    AIF   ('&KEY' EQ '').NOKEY
         MACPLOP &KEY,15(,R1),OP=STC
&FGKEY   SETA  8             KEY SPECIFIED
.NOKEY   AIF   ('&TCB' EQ '').NOTCB
         MACPLOP &TCB,16(,R1)
&FGTCB   SETA  16            TCB SPECIFIED
.NOTCB   AIF   ('&HDR' EQ '').NOHDR
&FGHDR   SETA  4             HDR SPECIFIED
         AIF   ('&HDR'(1,1) NE '''').NOQ
         MACPARM 20(24,R1),=C&HDR,OP=MVC
         AGO   .NOHDR
.NOQ     MACPARM R15,&HDR
         MACPLOP 20(24,R1),0(R15),OP=MVC
.NOHDR   MVI   14(R1),&FGLOC+&FGTCB+&FGKEY+&FGHDR+&FGOWN
         AGO   .CALL         CALL THE BUILD FUNCTION
.TYPEL   ANOP  ,
&VPN     SETC  '0'
&VSN     SETC  '0'
&VSZ     SETC  '0'
&VSP     SETC  '0'
&VKY     SETC  '0'
&VTC     SETC  '0'
&VHD     SETC  '0'
         AIF   (T'&PCELLCT EQ 'O').DPN
&VPN     SETC  '&PCELLCT'
&VSN     SETC  '&VPN'
.DPN     AIF   (T'&SCELLCT EQ 'O').DSN
&VSN     SETC  '&SCELLCT'
.DSN     AIF   (T'&CSIZE EQ 'O').DSZ
&VSZ     SETC  '&CSIZE'
.DSZ     AIF   (T'&SP EQ 'O').DSP
&VSP     SETC  '&CSIZE'
.DSP     AIF   (T'&KEY EQ 'O').DKY
&VKY     SETC  '&KEY'
&FGKEY   SETA  8
.DKY     AIF   (T'&TCB EQ 'O').DTC
&VTC     SETC  '&TCB'
&FGTCB   SETA  16
.DTC     AIF   (T'&HDR EQ 'O').DHD
&FGHDR   SETA  4
&VHD     SETC  '&HDR'
.DHD     MACPARM A(&VPN,&VSN,&VSZ),MODE=ONE,OP=DC
         DC    AL1(&VSP,&VKY,&FGLOC+&FGTCB+&FGKEY+&FGHDR+&FGOWN,0)
         AIF   ('&HDR' NE '').HVHD
         DC    A(&VTC),XL24'0'
         MEXIT ,
.HVHD    AIF   ('&HDR'(1,1) NE '''').PNHD
         DC    A(&VTC),CL24&VHD
         MEXIT ,
.PNHD    DC    A(&VTC),CL24'&VHD '
         MEND  ,
