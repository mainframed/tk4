         MACRO
&NM      UCBDEVN &UCBPTR=,&DEVN=,&NONBASE=,&HEXTAB=,&MODE=
.*    MVS/XA AND LATER SYSTEMS:   4 PRINTABLE HEX DIGITS FROM UCBCHAN
.*    NO MODE:  3 HEX UCBNAME + BLANK
.*    MODE=1    ZERO + 3 HEX DIGITS FROM UCBCHAN
.*    MODE=2    3 HEX UCBNAME + BLANK
.*    MODE=3    BLANK + 3 HEX UCBNAME
.*    MODE=4    ZERO + 3 HEX DIGITS FROM UCBNAME
.*
         GBLC  &MACPLAB
         GBLB  &MVSXA        ON IF X/A OR LATER                 GP10227
.*   QUICK AND DIRTY TO PROVIDE MODERN SYSTEM COMPATIBILITY
         LCLC  &R
&R       SETC  '1'
&MACPLAB SETC  '&NM'
         AIF   (K'&UCBPTR LT 3).NOREG
         AIF   ('&UCBPTR'(1,1) NE '(' OR '&UCBPTR'(2,1) EQ '(').NOREG
         AIF   ('&UCBPTR'(K'&UCBPTR,1) NE ')').NOREG
         AIF   ('&UCBPTR'(K'&UCBPTR-1,1) EQ ')').NOREG
&R       SETC  '&UCBPTR(1)'
         AGO   .DOREG
.NOREG   ANOP  ,
         MACPARM &R,&UCBPTR,OP=L
.DOREG   AIF   ('&MODE' EQ '1' OR &MVSXA).NEWFORM               GP10227
         MACPARM R15,&DEVN                                      GP10227
         AIF   ('&MODE' EQ '2').MVS4                            GP13220
         AIF   ('&MODE' EQ '3').MVS3                            GP14142
         MVC   0(3,R15),13(&R)    UCB NAME                      GP10227
         MVI   3(R15),C' '   DEVN COMPATIBILITY                 GP10227
         MEXIT ,                                                GP10227
.MVS3    MVC   1(3,R15),13(&R)    UCB NAME                      GP14142
         MVI   0(R15),C' '   DEVN COMPATIBILITY                 GP14142
         MEXIT ,                                                GP14142
.MVS4    MVC   1(3,R15),13(&R)    UCB NAME                      GP13220
         MVI   0(R15),C'0'   DEVN COMPATIBILITY                 GP13220
         MEXIT ,                                                GP13220
.NEWFORM MACPARM 20(5,13),4(3,&R),OP=UNPK  UCBCHAN/DEVN
         AIF   ('&HEXTAB' EQ '').LONG
         TR    20(4,13),&HEXTAB
         AGO   .COMM
.LONG    NC    20(4,13),=X'0F0F0F0F'
         TR    20(4,13),=C'0123456789ABCDEF'
.COMM    MACPARM 15,&DEVN
         MVC   0(4,15),20(13)   RETURN RESULT
         MEND
