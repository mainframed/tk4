         MACRO ,
&NM      XCURCON &XOFF=0,&YOFF=0,&ERR=,&XINT=2,&YINT=1,&XMAX=,&YMAX=,  *
               &XFUZZ=0,&YFUZZ=0,                                      *
               &FLAGS=FDWPROFG,&FBIT=FDWPFCUR,&VAL=FDWCUR   NEW GP13105
.*
.*   XCURCON ALLOWS THE USER TO PLACE THE CURSOR AT A SCREEN LOCATION
.*   AND HIT ENTER. THE MACRO EITHER BRANCHES TO THE 'ERR' LABEL,
.*   OR ENDS WITH AN X OFFSET IN R3 AND A Y OFFSET IN R1 (REL. TO 0).
.*     (FLAG AND CURSOR DEFAULTS SET FOR SCLINE SERVICE)
.*
.*   &VAL   - THE CURSOR ADDRESS SUITABLE FOR THE NEXT WRITE
.*   &FLAGS - CONTROL BYTE SET ON FOR A MINIMAL VALID ADDRESS
.*   &FBIT  - CONTROL BIT INDICATING A CURSOR ADDRESS PROCESSED
.*   &ERR   - LABEL FOR ERROR (CURSOR NOT IN TARGET RECTANGLE)
.*   &XOFF  - MINIMUM X OFFSET IN SCREEN LINE (DEFAULT IS 0)
.*   &XINT  - X SPACING (DEFAULT IS 2; INTEGER OR HALFWORD)
.*   &XMAX  - NUMBER OF HORIZONTAL POSITIONS
.*   &XFUZZ - DEVIATION FROM MINIMUM (FUZZY POSITION)           GP13162
.*   &YOFF  - MINIMUM OFFSET ON SCREEN (DEFAULT IS 0)
.*   &YINT  - Y SPACING (DEFAULT IS 1; INTEGER OR HALFWORD)     GP13162
.*   &YMAX  - NUMBER OF VERTICAL POSITIONS
.*   &YFUZZ - DEVIATION FROM MINIMUM (FUZZY POSITION)           GP13162
.*
.*  XFUZZ/YFUZZ ALLOW CURSOR IN AN AREA TO REGISTER AS A POINT. GP13162
.*
&NM      MVC   DB(3),EXCRAWIN  COPY TO PARAMETER AREA
         BAS   R14,EXWFRSBA  GET SCREEN ADDRESS OF CURSOR
         STH   R0,&VAL       SAVE BINARY CURSOR ADDRESS
         MACPARM R15,&ERR    QUICK RETURN ON ERROR
         SRDA  R0,32         MOVE TO R1 AND ZERO R0
         LH    R2,LSIZE      GET SCREEN WIDTH
         DR    R0,R2         GET COLUMN / ROW OF CURSOR
         LR    R3,R0         COPY Y OFFSET                      GP13162
         AIF   (T'&XOFF EQ 'N').NUMX
         MACPARM R3,&XOFF,OP=SH,OPR=SR
         AGO   .TESTY
.NUMX    SH    R3,=AL2(&XOFF)  LESS STARTING ROW
.TESTY   BMR   R15           NOT IN DISPLAY AREA
         AIF   (T'&YOFF EQ 'N').NUMY
         MACPARM R1,&YOFF,OP=SH,OPR=SR
         AGO   .TESTZ
.NUMY    SH    R1,=AL2(&YOFF)  LESS STARTING ROW
.TESTZ   BMR   R15           NOT IN DISPLAY AREA
         SR    R0,R0         PREPARE FOR DIVIDE                 GP13162
         AIF   ('&YINT' EQ '1').VERTZ                           GP13162
         AIF   (T'&YINT NE 'N').VERTN                           GP13162
         LA    R14,&YINT                                        GP13162
         AGO   .VERTR                                           GP13162
.VERTN   MACPARM R14,&YINT,OP=LH                                GP13162
.VERTR   DR    R0,R14                                           GP13162
         AIF   ('&YFUZZ' EQ '0').VERTP                          GP13162
         CH    R0,=AL2(&YFUZZ)                                  GP13162
         BNLR  R15                                              GP13162
         AGO   .VERTZ                                           GP13162
.VERTP   LTR   R0,R0         ANY REMAINDER                      GP13162
         BNZR  R15           YES; NOT ON DISPLAY CHARACTER      GP13162
.VERTZ   OI    &FLAGS,&FBIT  SHOW CURSOR GENERATED
         AIF   ('&XINT' EQ '2').TMODD
         AIF   ('&XINT' EQ '1').TMAXS
         SR    R2,R2
         D     R2,=A(&XINT)  TEST
         AIF   ('&XFUZZ' EQ '0').HORFZ                          GP13162
         CH    R2,=AL2(&XFUZZ)                                  GP13162
         BNLR  R15                                              GP13162
         AGO   .TMAXS                                           GP13162
.HORFZ   LTR   R2,R2         REMAINDER ?
         BNZR  R15             YES; INVALID OFFSET
         AGO   .TMAXS        CHECK FOR MAXIMA
.TMODD   EX    R3,EXTMODD    IS THE COLUMN OFFSET ODD ?
         BNZR  R15           YES; NOT ON DISPLAY CHARACTER
         SRA   R3,1          CONVERT INDEX TO OFFSET
.TMAXS   CH    R3,=AL2(&XMAX)  VALID ?
         BNLR  R15           NO; IGNORE
         CH    R1,=AL2(&YMAX)  VALID ?
         BNLR  R15           NO; IGNORE
         AIF   ('&XINT' NE '2').MEND                            GP13162
         B     *+8           PROCESS
EXTMODD  TM    =X'01',*-*    IS REGISTER ODD ?
.MEND    MEND  ,
