         MACRO ,
&NM      RACFAC &ACC,&RES,&FAC='FACILITY',&OK=,&FAIL=           GP15123
.*  This code has been adapted from Juergen Winkelmann's security
.*    modifications for sensitive modules in the tk4- distribution
.*
.*  Check for authority to a specified facility
.*
.*  First positional is requested access (READ, UPDATE, CONTROL, ALTER)
.*  Second positional is the resource ("facility") to be checked.
.*
.*  OK= specifies the optional label to branch to on a valid access
.*
.*  FAIL= specifies the optional label for a failed access. For this
.*    case R15 contains the return code from RACHECK, except when
.*    no security is active (code 32 is not issued - bypassed tests).
.*
         LCLC  &ZZ
         PUSH  USING
&ZZ      SETC  'ZZ'.'&SYSNDX'
&NM      SLR   R15,R15       Preset for acceptance
         L     R1,CVTPTR          get CVT address               JW12138
         ICM   R1,B'1111',CVTSAF(R1) SAFV defined ?             JW12138
         BZ    &ZZ.X           no RAC, go execute
         USING SAFV,R1            addressability of SAFV        JW12138
         CLC   =C'SAFV',SAFVIDEN  SAFV initialized ?
         BNE   &ZZ.X           no RAC, go execute               JW12138
         MACPARM R1,=CL39'&RES '
         RACHECK ENTITY=((R1)),CLASS='FACILITY',ATTR=&ACC
         LTR   R15,R15            RAC authorization granted?    JW12138
&ZZ.X    MACPARM &OK,NULL=SKIP,OP=BE,OPR=BER,MODE=1
         MACPARM &FAIL,NULL=SKIP,OP=BNE,OPR=BNER,MODE=1
         MACPARM MODE=LABEL  IN CASE NEITHER BRANCH TARGET SUPPLIED
         POP   USING
         MEND  ,
