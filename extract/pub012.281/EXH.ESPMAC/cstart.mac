         MACRO
&NM      CSTART &NAM,&COMM=,&DSECT=,&CHECK=,&ID=,&BASE=R12,     GP04234*
               &WORK=                                           GP09042
         GBLC  &MODULE
         GBLB  &BUGBEAR      DEBUG FLAG                         GP03264
&BUGBEAR SETB  (&BUGBEAR OR '&SYSPARM' EQ 'DEBUG')              GP03264
         LCLA  &K,&I         BASE COUNT                         GP04234
         LCLC  &CASE,&CMU                                       GP04234
         LCLC  &N1,&N2,&X                                        79216
&N1      SETC  '&NM'
&N2      SETC  '&NAM'
         AIF   ('&N1' EQ '' AND '&N2' EQ '').BOOBOO
         AIF   ('&N1' NE '').USE2
&N1      SETC  '&N2'
&N2      SETC  ''
.USE2    AIF   ('&CHECK' EQ 'NO').USEIT
         AIF   ('&N1'(1,4) NE 'EXHC').NOUSE
&X       SETC  '&N1'(5,1)                                        79216
.*
.*  EXH$ - JES2 MODULE  EXHC - COMMAND      EXH@ - JES3 (NONE)
.*  EXH8 - RESERVED     EXHX - XCTL CMD     EXHY - NEW XCTL
.*  EXHG - GAME PGM     EXHD - DISPLAY      EXHP - LIGHT PEN CMD
.*
 AIF ('&X' EQ '$' OR '&X' EQ 'C' OR '&X' EQ 'D' OR '&X' EQ 'G' OR '&X' *
               EQ 'P' OR '&X' EQ 'X' OR '&X' EQ 'Y' OR '&X' EQ '8' OR  *
               '&X' EQ '@').USEIT                                80165
.NOUSE   MNOTE 8,'START NAME ''&N1'' NOT VALID FOR EXHIBIT'
.USEIT   AIF   ('&DSECT' EQ '').START
         AIF   ('&DSECT' EQ 'D').DS
         AIF   ('&DSECT' NE 'NO').STM
&N2      SETC  '&N1'
         CNOP  0,8
         AGO   .STM
.DS      ANOP
&N1      DSECT
         AGO   .STM
.START   ANOP
&N1      START 0
         AIF   ('&ID' NE '' AND '&ID' NE 'YES').STM              93178
&X      SETC  '&SYSNDX'                                         93178
&N2      B     ZZT&X-*(,R15)                                    93178
         DC    AL1(ZZT&SYSNDX-*-1),C'&N1 &SYSDATE &SYSTIME.'     93178
ZZT&X   DS    0H                                                93178
.STM     ANOP
&MODULE  SETC  '&N1' .       SAVE CSECT FOR LATER CODE
&N2      STM   R14,R12,12(R13)  SAVE FOR DEBUGGING
&K       SETA  N'&BASE      MAX NUMBER OF BASES SPECIFIED       GP04234
&CMU     SETC  '&BASE(1)'    SET THE FIRST ONE                  GP04234
&CASE    SETC  '&BASE(1)'    SET THE FIRST ONE                  GP04234
&K       SETA  N'&BASE                                          GP04234
         LA    &BASE(1),0(,R15) LOAD AND CLEAN BASE             GP04234
         AIF   (&K LT 2).DONEBAS                                GP04234
&I       SETA  1                                                GP04234
         LA    &BASE(&K),2048                                   GP04234
.BASLOOP AIF   (&I GE &K).DONEBAS                               GP04234
&I       SETA  &I+1                                             GP04234
         AIF   ('&BASE(&I)' EQ '').BASLOOP                      GP04234
         LA    &BASE(&I),2048(&BASE(&K),&BASE(&I-1))            GP04234
&CMU     SETC  '&CMU'.','.'&BASE(&I)'                           GP04234
         AGO   .BASLOOP                                         GP04234
.DONEBAS USING &MODULE,&CMU                                     GP04234
         NEED  WORK,SQSP
         USING EXHBSQSP,R13
         AIF   ('&WORK' EQ 'NO').MEXIT                          GP09042
         L     R11,SQ@WORK   GET THE EXHBWORK ADDRESS           GP04234
         USING EXHBWORK,R11
         AIF   ('&COMM' EQ 'NO').MEXIT
         L     R8,EXWSTGAD    GET ADDRESS OF COMM WORK AREA     GP04234
         NEED  COMM
         USING EXHBCOMM,R8
.MEXIT   PUSH  PRINT                                             81082
         PRINT GEN,ON                                            81082
         SPACE 1                                                 81075
*                           RESTRICTED RIGHTS LEGEND
*
*          Use, duplication or disclosure is subject to restrictions
*                   stated in Contract No. GS-00C-01222 with
*                      American Management Systems, Inc.
*
*                   This program is a proprietary product of
*                       Expert System Programming, Inc.
         SPACE 1                                                 81075
         POP   PRINT                                             81082
         MEXIT ,                                                 81075
.BOOBOO  MNOTE 12,'START NAME MISSING'
         MEND
