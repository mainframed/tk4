        MACRO
&LABEL  TRINV  ,
.* Construct reverse bits translate table
.* Contributed by Kenneth Wilkerson on IBM-MAIN as REVTABLE
         LCLA  &I,&J,&K,&L,&M,&N,&O
         LCLC  &X
&LABEL   DS    0D            LIKE EM DOUBLE WORD ALIGNED
&I       SETA  0             STARTING VALUE
.TABLOOP ANOP  ,             LOOP UNTIL TABLE IS DONE
&K       SETA  1             NEED SIXTEEN ENTRIES PER LINE
&X       SETC  'AL1('
         AGO   .X16LP
.X16NXT  ANOP  ,
&X       SETC  '&X'.'&J'.','
.X16LP   ANOP  ,             16 ENTRY LOOP
&J       SETA  0             STARTING RESULT
&L       SETA  1             STARTING ADDEND
&M       SETA  1             8 BITS PER BYTE
&N       SETA  128           STARTING COMPARAND X'80'
&O       SETA  &I            COPY CURRENT BYTE TO REVERSE
.BYTELP  ANOP  ,
         AIF   (&O LT &N).BYTEFT     LESS THAN CURRENT - 0
&O       SETA  &O-&N
&J       SETA  &J+&L
.BYTEFT  ANOP  ,
&L       SETA  &L*2          NEXT ADDEND
&N       SETA  &N/2          NEXT COMPARAND
&M       SETA  &M+1          NEED EIGHT BITS
         AIF   (&M LE 8).BYTELP
&I       SETA  &I+1
&K       SETA  &K+1
         AIF   (&K LE 16).X16NXT
&X       SETC  '&X'.'&J'.')'
      DC &X
         AIF   (&I LT 256).TABLOOP
         MEND  ,
