         MACRO
&NM    DASDTYPE &UCB=R3,&OUT=R1,&R=R15,&S=R14,&T=R0
         GBLC  &SYSTEM                                           82158
         LCLA  &I
.*  THIS MACRO EXPANDS IN-LINE CODE TO SIMULATE THE DEVTYPE SVC
.*  FOR DISK DEVICES. INPUT IS A UCB ADDRESS IN REGISTER UCB;
.*  OUTPUT IS 20 BYTES IN AREA POINTED TO BY REGISTER OUT;
.*  ON A WORD BOUNDARY, CORRESPONDING TO WORDS 2-6 OF DEVTYPE.
.*   CODE WAS LIFTED FROM DEVTYPE SVC OS 21.8
.*
&I       SETA  &SYSNDX
         PUSH  USING                                             93032
&NM      XC    0(20,&OUT),0(&OUT)     CLEAR OUTPUT AREA
         NEED  CVT           REQUEST EXPANSIONS VIA MSECT
         NUSE  UCB,&UCB                                          93032
         IC    &R,UCBTBYT4   GET THE DISK SUB-TYPE               93032
         LA    &S,X'0F'      MASK FOR SUB-TYPE INDEX
         NR    &R,&S         GET INDEX
         L     &S,CVTPTR     GET CVT
         L     &S,CVTZDTAB-CVTMAP(,&S)  GET CVT DEVICE POINTER
         IC    &R,0(&R,&S)    GET INDEX TO DEVICE ENTRY
         AR    &S,&R         GET ENTRY FOR THIS TYPE
         MVC   4(12,&OUT),0(&S)    MOVE DATA
         TM    UCBTBYT2,UCB2OPT3    RPS DEVICE ?                 93032
         BZ    *+10          NO
         MVC   16(4,&OUT),14(&S)     MOVE RPS WORD ALSO
         LH    &R,4(,&S)     GET BLOCK SIZE
         AIF   ('&SYSTEM' EQ 'MVS').ISMVS                        82157
         TM    UCBFL5,UCBEXTSN     EXTENDED SENSE DEVICE ?       93032
         BZ    ZZZZ&I        NO; SKIP RECALCULATION
.ISMVS   XR    &T,&T                                             82157
         IC    &T,6(,&S)     GET OVERHEAD
         TM    9(&S),8       TWO BYTE OVERHEAD ?
         BZ    *+8           NO
         LH    &T,6(,&S)     GET FULL OVERHEAD
         SR    &R,&T         ADJUST
         XR    &T,&T
         IC    &T,8(,&S)     GET ADJUSTMENT TO ADJUSTMENT
         AR    &R,&T         FINAL ADJUSTED SIZE
ZZZZ&I   ST    &R,0(,&OUT)   SET RECALCULATED BLOCKSIZE
         POP   USING                                             93032
         MEND
