         MACRO
&NAME    WTU   &MESG,&MF=I,&ROUTCDE=,&DESC=,&MSGTYP=,&MCSFLAG=,&UCMID=W*
               RKUCMID,&TSO=NO
         GBLB  &IHBWTL,&IHBWTOR
         GBLC  &MACPLAB
.**********************************************************************
.*                                                                   **
.*   ISSUE MESSAGE AS TPUT UNDER TSO, AND WTO OTHERWISE              **
.*     IF STC, USE START CONSOLE UCMID FOR RESPONSES                 **
.*                                                                   **
.**********************************************************************
         LCLA  &LT(256),&H,&I,&N,&J,&LEN,&LNUM,&LLCNT            S21002
         LCLB  &NODFLT
         LCLB  &B(16),&AD,&E,&E1,&E2,&E3,&E4,&E5,&MCS,&MLW       S21002
         LCLB  &SECONDL,&SWM,&TWO,&PAIR                          S21002
         LCLC  &CFLG(16),&CD(16),&GNAME                          S21002
.*
.*       THIS IS A COPY OF IBM'S WTO MACRO - ONLY MODIFICATION
.*       WAS TO FORCE MCSFLAG=REG0 AND ADDED CODE TO LOAD
.*       UCMID FROM LABEL WRKUCMID VIA IC
.*
&GNAME   SETC  'IHB'.'&SYSNDX'
&MACPLAB SETC  '&NAME'
         AIF    ('&MF' EQ 'I' OR '&MF' EQ 'L').NUMCHK            S21002
         AIF   (N'&MF NE 2).E1
         AIF   ('&MF(1)' NE 'E').E1
&NAME    IHBINNRA &MF(2)
&MACPLAB SETC  ''                                                90014
         AIF   (&IHBWTL).END
.*       SVC   35                                ISSUE SVC
         AGO   .UCMSVC
.NUMCHK  ANOP                                                    S21002
         AIF   (N'&SYSLIST LE 1).LABLL                            66488
         AIF   ('&SYSLIST(1)'(1,1) EQ '(').LABLL                  66488
         MNOTE 8,'MLWTO MSG LINES NOT ENCLOSED IN PARENTHESES'    66488
         AGO   .END                                               66488
.LABLL   AIF   (N'&SYSLIST EQ 0 OR N'&SYSLIST GT 10).NOTXT        66488
         AIF   ('&MF' EQ 'I').IROUT                              S21002
.LROUT   AIF   (&IHBWTOR).MESCHK
&NAME    DS    0H
&MACPLAB SETC  ''                                                90014
.MESCHK  ANOP
&I       SETA  1
&MCS     SETB  (T'&ROUTCDE NE'O' OR T'&MSGTYP NE 'O' )
&B(3)    SETB  0                                                   MCS
         AIF   ('&MESG' EQ '').NOTXT                             S21002
&MLW     SETB  (N'&SYSLIST NE 1 OR N'&SYSLIST(1) NE 1)           S21002
         AIF   (&IHBWTOR AND &MLW).MLWTOR                        S21002
         AIF   (T'&DESC EQ 'O').SETNCLR
&MCS     SETB  1
.*
.*  DESCRIPTOR CODES
.*
.DCHK    ANOP
&N       SETA  &DESC(&I)
&I       SETA  &I+1
         AIF   (&N GE 1 AND &N LE 16).ASSIGND
         MNOTE 8,'&DESC(&I-1) IS INVALID DESCRIPTOR- IGNORED'     67737
         AGO   .NXTD
.ASSIGND ANOP
&B(&N)   SETB  1
.NXTD    AIF   (&I LE N'&DESC).DCHK
&I       SETA  1
         AGO   .SETNCLR
.*
.*  ROUTE CODES
.*
.RTCHK   AIF   (T'&ROUTCDE NE 'O').RCHK0
.DEFLAG  ANOP
&I       SETA  3
         AIF   (&J EQ N'&MCSFLAG OR T'&MCSFLAG EQ 'O').DODEF
&J       SETA  &J+1
         AIF   ('&MCSFLAG(&J)' EQ 'REG0' OR '&MCSFLAG(&J)' EQ 'QREG0').*
               SETNCLR
         AGO   .DEFLAG
.DODEF   ANOP
         AIF   (T'&MSGTYP NE 'O').SETNCLR                         68508
         AIF   (T'&UCMID NE 'O').SETNCLR
&B(2)    SETB  (&MCS)
&I       SETA  3
         AGO   .SETNCLR
.*
.RCHK0   ANOP
.RCHK    ANOP
&N       SETA  &ROUTCDE(&I)
&I       SETA  &I+1
         AIF   (&N GE 1 AND &N LE 16).ASSIGNR
         MNOTE 8,'ROUTCDE(&I-1) IS INVALID ROUTE- IGNORED'        66111
         AGO   .NXTR
.ASSIGNR ANOP
&NODFLT  SETB  (&NODFLT OR &N NE 16)
&B(&N)   SETB  1
.NXTR    AIF   (&I LE N'&ROUTCDE).RCHK
&I       SETA  3
&B(2)    SETB  (&B(2) OR NOT &NODFLT)
&NODFLT  SETB  0
&B(16)   SETB  0
.NOT11   ANOP
.*
.*  SET OUTPUT AREAS AND CLEAR BINARY FLAGS
.*
.SETNCLR ANOP
&CD(&I)  SETC  '&B(1)&B(2)&B(3)&B(4)&B(5)&B(6)&B(7)&B(8)'
&CD(&I+1) SETC '&B(9)&B(10)&B(11)&B(12)&B(13)&B(14)&B(15)&B(16)'
&N       SETA  1
.CLR     ANOP
&B(&N)   SETB  0
&N       SETA  &N+1
         AIF   (&N LT 17).CLR
         AIF   (&I EQ 1).RTCHK
         AIF   (&I EQ 5).FLGCHK0
         AIF   (&I EQ 7).GENDCS                                  S21002
.*
.*  MESSAGE TYPES
.*
.TYPCHK  AIF   (T'&MSGTYP EQ 'O').FLGCHK0
         AIF   ('&MSGTYP(1)' EQ 'N').FLGCHK0
&SWM     SETB  1                                                 S21002
&I       SETA  5
&N       SETA  1
         AIF   ('&MSGTYP(1)' EQ 'Y').SETNCLR
&I       SETA  1
.*
&CFLG(1) SETC  'JOBNAMES'
&CFLG(2) SETC  'STATUS'
&CFLG(3) SETC  'ACTIVE'                                          S21002
&CFLG(4) SETC  ''                                                S21002
&CFLG(5) SETC  'SHOW'                                              CRJE
&CFLG(6) SETC  'SESS'                                            20034
.*
.TCHK    AIF   ('&MSGTYP(&I)' EQ '&CFLG(&N)').SETTYP
&N       SETA  &N+1
         AIF   (&N LE 8).TCHK
         MNOTE 8,'MESSAGE TYPE FIELD INVALID- N IS ASSUMED'
&N       SETA  1                                                 S21002
&I       SETA  5                                                 S21002
&SWM     SETB  0                                                 S21002
         AGO   .CLR                                              S21002
         AGO   .FLGCHK0
.SETTYP  AIF   ('&CFLG(&N)' EQ '').ADDIT
&B(&N)   SETB 1
.ADDIT   ANOP
&I       SETA  &I+1
&N       SETA  1
         AIF   (&I LE N'&MSGTYP).TCHK
&I       SETA  5
         AGO   .SETNCLR
.*
.*  MCSFLAGS
.*
.FLGCHK0 ANOP
&MCS    SETB  (T'&ROUTCDE NE 'O' OR T'&MSGTYP NE 'O' OR T'&DESC NE 'O')
.*                                                               S21002
&B(1)    SETB  (&MCS)
         AIF   (T'&UCMID EQ 'O').NOUCMFG
&B(2)    SETB  1             SET UCMID FLAG
.NOUCMFG ANOP
&B(4)    SETB  (&SWM)
&B(10)   SETB  (&MLW)              THIS IS MLWTO LIST            S21002
&I       SETA  7
         AIF   (T'&MCSFLAG EQ 'O').SETNCLR
&I       SETA  1
&N       SETA  1
.*
&CFLG(1) SETC  'REG0'
&CFLG(2) SETC  'RESP'
&CFLG(3) SETC  ''
&CFLG(4) SETC  'REPLY'
&CFLG(5) SETC  'BRDCST'
&CFLG(6) SETC  'HRDCPY'
&CFLG(7) SETC  'QREG0'
&CFLG(8) SETC  'NOTIME'
&CFLG(13) SETC 'NOCPY'
.*
.FLGCHK  AIF   ('&MCSFLAG(&I)' EQ '&CFLG(&N)').SETFLG
&N       SETA  &N+1
         AIF   (&N LE 15).FLGCHK
         MNOTE 8,'&MCSFLAG(&I) IS INVALID- IGNORED'
         AGO   .ADDI
.SETFLG  AIF   ('&CFLG(&N)' EQ '').ADDI
         AIF   ('&CFLG(&N)' NE 'HRDCPY').HDCYOK               BE A52575
         AIF   (NOT &MLW).HDCYOK                              BE A52575
         IHBERMAC 248                                         BE A52575
&GNAME.A DS    0H
         MEXIT
.HDCYOK  ANOP                                                 BE A52575
&B(&N+1) SETB  1
.ADDI    ANOP
&I       SETA  &I+1
&N       SETA  1
         AIF   (&I LE N'&MCSFLAG).FLGCHK
         AIF   (&B(7) AND &B(14)).MUTEXC  THESE PARAM MUTUALLY EXCLUSIV
.*                                      HARDCOPY AND NO HARD COPY
         AGO   .SETTER
.MUTEXC  ANOP
&B(14)   SETB  0
         MNOTE 8,'HRDCPY AND NOCPY MUTUALLY EXCLUSIVE, HRDCPY ASSUMED'
.SETTER  ANOP
&I       SETA  7
         AGO   .SETNCLR
.IROUT   AIF   (&IHBWTOR).MESCHK
         CNOP  0,4
&NAME    BAL   1,&GNAME.A                        BRANCH AROUND MESSAGE
&MACPLAB SETC  ''                                                90014
         AGO   .MESCHK
.*                                                               S21002
.*  * *  SET LINETYPE  * * * * *                                 S21002
.*                                                               S21002
.GENDCS  AIF   (NOT &MLW).NOTMLW1  GENERATE REGULAR WTO          S21002
&H       SETA  1                                                 S21002
         AIF   ('&SYSLIST(1,1)' EQ '').MLW04                     S21002
         AIF   (N'&SYSLIST(1) GT 2).MLW05                        S21002
         AIF   ('&SYSLIST(1,2)' NE 'C').MLW02                    S21002
&LT(1)   SETA  80                                                S21002
.MLW01   AIF   (N'&SYSLIST LE &H).MLW11                          S21002
&H       SETA  &H+1                                              S21002
         AIF   (N'&SYSLIST(&H) GT 2).MLW05                       S21002
.MLW02   AIF   ('&SYSLIST(&H,2)' NE 'L' OR '&SYSLIST(&H,1)' EQ '').MLW0X
               4                                                 S21002
&LT(&H)  SETA  40                                                S21002
         AIF   (&SECONDL).MLW03                                  S21002
&SECONDL SETB  1                                                 S21002
         AGO   .MLW01                                            S21002
.MLW03   AIF   (N'&SYSLIST LE &H).MLW11                          S21002
&H       SETA  &H+1                                              S21002
         AIF   (N'&SYSLIST(&H) GT 2).MLW05                       S21002
.MLW04   AIF   ('&SYSLIST(&H,2)' EQ 'E').MLW06                   S21002
         AIF   ('&SYSLIST(&H,1)' EQ '').MLW05                    S21002
         AIF   ('&SYSLIST(&H,2)' EQ 'DE').MLW08                  S21002
         AIF   ('&SYSLIST(&H,2)' EQ 'L' OR '&SYSLIST(&H,2)' EQ 'C').MLWX
               09                                                S21002
         AIF   ('&SYSLIST(&H,2)' NE 'D' AND '&SYSLIST(&H,2)' NE '').MLW*
               10                                                S21002
&LT(&H)  SETA  20                                                S21002
         AGO   .MLW03                                            S21002
.MLW05   ANOP                                                    S21002
&E5      SETB  1                                                 S21002
&LT(&H)  SETA  10                                                S21002
         AGO   .MLW11                                            S21002
.MLW06   ANOP                                                    S21002
&LT(&H)  SETA  10                                                S21002
.MLW07   ANOP                                                    S21002
&E4      SETB  (&H NE N'&SYSLIST)                                S21002
         AGO   .MLW11                                            S21002
.MLW08   ANOP                                                    S21002
&LT(&H)  SETA  30                                                S21002
         AGO   .MLW07                                            S21002
.MLW09   ANOP                                                    S21002
&E3      SETB  1                                                 S21002
&LT(&H)  SETA  30                                                S21002
         AGO   .MLW11                                            S21002
.MLW10   ANOP                                                    S21002
&E5      SETB  1                                                 S21002
&LT(&H)  SETA  30                                                S21002
.MLW11   ANOP                                                    S21002
&LLCNT   SETA  &H                                                S21002
&H       SETA  1                                                 S21002
.NOTMLW1 ANOP                                                    S21002
&I       SETA  1                                              MA S21002
&LEN     SETA  K'&SYSLIST(1,1)-2                              MA S21002
&PAIR    SETB  0                                              MA S21002
.QLOOP1  ANOP                                                 MA S21002
&I       SETA  &I+1+&PAIR                                     MA S21002
         AIF   (&I GE K'&SYSLIST(1,1)).QDONE1                 MA S21002
&PAIR    SETB  ('&SYSLIST(1,1)'(&I,2) EQ '''''' OR '&SYSLIST(1,1)'(&I,2*
               ) EQ '&&')
&LEN     SETA  &LEN-&PAIR                                     MA S21002
         AGO   .QLOOP1                                        MA S21002
.QDONE1  ANOP                                                 MA S21002
&AD      SETB  (&LT(1) NE 10)      0 IF E-TYPE LINE, 1 IF NOT MA S21002
&LEN     SETA  4+&LEN*&AD                                     MA S21002
         DC    AL2(&LEN)           TEXT LENGTH
         DC    B'&CD(7)&CD(8)'     MCS FLAGS
         AIF   (&LEN EQ 4).SKIPDC                                S21002
         DC    C&SYSLIST(1,1)
.SKIPDC  AIF   (NOT &MCS).OLDEXIT                                S21002
         DC    B'&CD(1)&CD(2)'     DESCRIPTOR CODES
         DC    B'&CD(3)&CD(4)'     ROUTING CODES
         AIF   ('&MSGTYP(1)' NE 'Y' AND NOT &SWM).OLDEXIT        S21002
         DC    B'&CD(5)&CD(6)'     MESSAGE TYPE
.OLDEXIT AIF   (NOT &MLW).NOTMLW2                                S21002
         DC    XL2'&LT(1)00'       LINE TYPE
         DC    AL2(&LLCNT)         TOTAL NUMBER OF LINES
.MLW12   AIF   (&H GE &LLCNT).MLW14                              S21002
&H       SETA  &H+1                                              S21002
&I       SETA  1                                              MA S21002
&LEN     SETA  K'&SYSLIST(&H,1)-2                             MA S21002
&PAIR    SETB  0                                              MA S21002
.QLOOPH  ANOP                                                 MA S21002
&I       SETA  &I+1+&PAIR                                     MA S21002
         AIF   (&I GE K'&SYSLIST(&H,1)).QDONEH                MA S21002
&PAIR    SETB  ('&SYSLIST(&H,1)'(&I,2) EQ '''''' OR '&SYSLIST(&H,1)'(&I*
               ,2) EQ '&&')
&LEN     SETA  &LEN-&PAIR                                     MA S21002
         AGO   .QLOOPH                                        MA S21002
.QDONEH  ANOP                                                 MA S21002
&AD      SETB  (&LT(&H) NE 10)     0 IF E-TYPE LINE, 1 IF NOT MA S21002
&LEN     SETA  4+&LEN*&AD                                     MA S21002
         DC    AL2(&LEN)           LENGTH
         DC    XL2'&LT(&H)00'      LINE TYPE
         AIF   (&LEN EQ 4).MLW12                                 S21002
         DC    C&SYSLIST(&H,1)
         AGO   .MLW12                                            S21002
.MLW14   AIF   (NOT &E4).MLW15                                   S21002
         IHBERMAC 242                                         MB  M1428
.MLW15   AIF   (NOT &E5).MLW17                                   S21002
         IHBERMAC 243                                         MB  M1428
.MLW17   AIF   (NOT &E3).NOTMLW2                                 S21002
         IHBERMAC 244                                         MB  M1428
.NOTMLW2 AIF   (&IHBWTOR OR '&MF' NE 'I').END                    S21002
.MLWHC   ANOP                                                 BE A52575
&GNAME.A DS    0H
         AIF   (&E3 OR &IHBWTL).END                              S21002
.UCMSVC  AIF   (T'&UCMID EQ 'O').DOSVC
         AIF   ('&UCMID'(1,1) EQ '(').NOSR
&MACPLAB SLR   R0,R0         CLEAR IC REGISTER                   90014
&MACPLAB SETC  ''                                                90014
.NOSR    MACPARM R0,&UCMID,OP=IC
         AIF   ('&TSO' EQ 'NO').DOSVC
&MACPLAB LA    R15,255       MAX UCMID
         CR    R0,R15        MAX ?
         BL    *+22          SKIP TPUT CODE
         LH    R0,0(,R1)     LOAD LENGTH+4
         SH    R0,*+6        ACTUAL LENGTH
         LA    R1,4(R1,0)    TEXT START
         SVC   93            TPUT
         B     *+6           BRANCH AROUND WTO
.DOSVC   ANOP  ,
&MACPLAB SVC   35
         MEXIT                                                   S21002
.E1      IHBERMAC 35,,&MF
         MEXIT
.NOTXT   ANOP                                                    S21002
         IHBERMAC 245                                         MB  M1428
         MEXIT                                                   S21002
.MLWTOR  IHBERMAC 246                                         MB  M1428
.END     MEND
