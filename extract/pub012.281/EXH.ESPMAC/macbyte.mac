         MACRO ,
         MACBYTE &REQ,&PARM=,&PAX=
         GBLA  &MACPVAL,&MACPCNT
         GBLB  &MACPNUL,&MACPERR
.*                                                                    *
.*   THIS MACRO IS USED WITHIN OTHER MACROS TO CONVERT A POSITIONAL   *
.*   OPTION WORD INTO AN INTEGER.                                     *
.*                                                                    *
.*   PARM(n) and PAX(n) CONTAIN THE KEYWORD AND VALUE, RESPECTIVELY.  *
.*                                                                    *
.*   SAMPLE: MACBYTE THREE,PARM=(ONE,TWO,THREE...SIX),                *
.*                                        PAX=(0,4,8...20)            *
.*       &MACPCNT IS SET TO 3 (THIRD LIST ITEM)                       *
.*       &MACPVAL IS SET TO 8 (THIRD PAX VALUE)                       *
.*                                                                    *
         LCLA  &I,&J,&K,&M,&N,&O
&MACPCNT SETA  0
&MACPVAL SETA  0
&MACPNUL SETB  1
&MACPERR SETB  1
&N       SETA  N'&SYSLIST
         AIF   (&N EQ 0).MEND     RETURN ERROR
         AIF   ('&REQ' EQ '').MEND     ERROR IF NULL            GP15294
&MACPNUL SETB  0
         AIF   (N'&PARM NE N'&PAX).BALERR ERROR
&MACPERR SETB  0
         AIF   (T'&REQ EQ 'O').MEND  VAL=CNT=0, NO ERROR
.*
&J       SETA  1
.WORDLUP AIF   (&J GT N'&PARM).NOTFUND
         AIF   ('&REQ' EQ '&PARM(&J)').FOUND
&J       SETA  &J+1
         AGO   .WORDLUP
.*
.FOUND   ANOP  ,
&MACPCNT SETA  &J            PAX INDEX FOR THIS KEYWORD
&MACPVAL SETA  &PAX(&J)
         AGO   .MEND
.*
.NOTFUND MNOTE 8,'MACBYTE: TERM &REQ NOT FOUND'
&MACPERR SETB  1
         AGO   .MEND
.BALERR  MNOTE 8,'MACBYTE: PARM AND PAX COUNTS DO NOT MATCH'
.MEND    MEND  ,
