         MACRO
&NM      QUE   &Q,&Q1,&Q2,&Q3,&SIZE=,&OUT=
         GBLA  &QUECNT,&QUEIN,&QUESIZ,&QUECON,&QIO(64)
         LCLA  &I,&J,&K,&L,&QC
         LCLC  &C,&D,&E
         LCLC  &BIG(10)
         LCLA  &BIGQ(10),&BM
         LCLA  &MAX
&BM      SETA  5             NUMBER OF THREE DIGIT Q NAMES DEFINED
&BIG(1)  SETC  'OUT'
&BIG(2)  SETC  'OLD'
&BIG(3)  SETC  'ASB'
&BIG(4)  SETC  'RJE'
&BIG(5)  SETC  'TSO'         TSO BATCHED READER QUEUE
&BIGQ(1) SETA  0
&BIGQ(2) SETA  1
&BIGQ(3) SETA  2
&BIGQ(4) SETA  39
&BIGQ(5) SETA  55
&MAX     SETA  18
         AIF   ('&OUT' EQ '').NOUT
&QUEIN   SETA  1
         AIF   ('&OUT' NE 'IN').NOUT
&QUEIN   SETA  0
.NOUT    ANOP
         AIF   (&QUESIZ NE 1).BIG
&MAX     SETA  9
.BIG     ANOP
&I       SETA  N'&SYSLIST
&J       SETA  1
         AIF   (&QUECNT GT &MAX).MEXIT
         AIF   (&QUECNT GT 0).CHECK
         AIF   ('&NM' NE '').DOEQ
         MNOTE 8,'FIRST ''QUE'' MUST HAVE A NAME'
&QUECNT  SETA  &MAX+1
&QUECON  SETA  0             RESET CONCAT
         AGO   .MEXIT
.DOEQ    AIF   ('&SIZE' EQ '').DOCL
&MAX     SETA  9
&QUESIZ  SETA  1
.DOCL    ANOP
&NM      DC    &MAX.CL4' ' .  RESERVE STORAGE FOR ZAPS
         ORG   &NM .          BUMP BACK TO START
&K       SETA  1
.QZERO   AIF   (&K GT 64).CHECK
&QIO(&K) SETA  0
&K       SETA  &K+1
         AGO   .QZERO
.CHECK   AIF   ('&Q' EQ '').MISSING
         AIF   ('&Q' EQ 'END').END
         AIF   ('&Q'(1,1) LT 'A').SPECIAL
&QUECON  SETA  0             RESET CONCAT
.LOOP    ANOP
&QUECNT  SETA  &QUECNT+1
         AIF   (&QUECNT GT &MAX).OFLOW
&K       SETA  K'&SYSLIST(&J)
         AIF   (&K EQ 1).ONE
         AIF   (&K EQ 3).THREE
.MISSING MNOTE 4,'BAD OR MISSING PARAMETER'
         AGO   .MEXIT
.THREE   ANOP
&K       SETA  1
.THL     AIF   ('&SYSLIST(&J)' EQ '&BIG(&K)').F3
&K       SETA  &K+1
         AIF   (&K LE &BM).THL
         AGO   .MISSING
.F3      DC    C'&SYSLIST(&J)',AL1(&BIGQ(&K))
         AIF   (&K EQ 1).SETOUT
&L       SETA  &BIGQ(&K)+1
&QIO(&L) SETA  1
         AGO   .BUMP
.SETOUT  ANOP
&QUEIN   SETA  1
.BUMP    ANOP
&J       SETA  &J+1
         AIF   (&J GT &I).MEXIT
         AGO   .LOOP
.ONE     AIF   (&QUEIN EQ 1).OUT
&K       SETA  1
.FIT     AIF   ('&SYSLIST(&J)' EQ 'ABCDEFGHIJKLMNO'(&K,1)).FI1
&K       SETA  &K+1
         AIF   (&K GT 15).MISSING
         AGO   .FIT
.FI1     ANOP
&K       SETA  &K+39
.F1      DC    C' ',C'&SYSLIST(&J)',C' ',AL1(&K)
&L       SETA  &K+1
&QIO(&L) SETA  1
         AGO   .BUMP
.OUT     ANOP
&K       SETA  1
.FOT     AIF   ('&SYSLIST(&J)' EQ                                      X
               'ABCDEFGHIJKLMNOPQRSTUVWXYZ0123456789'(&K,1)).FO1
&K       SETA  &K+1
         AIF   (&K GT 36).MISSING
         AGO   .FOT
.FO1     ANOP
&K       SETA  &K+2
         AGO   .F1
.END     AIF   ('&NM' EQ '').END1
&NM      EQU   * .           END OF QUE TABLE
.END1    ANOP
         ORG
&QUECNT  SETA  0
&QUEIN   SETA  0
&QUESIZ  SETA  0
&QUECON  SETA  0             RESET CONCAT
&K       SETA  1
&I       SETA  1
.WORD    ANOP
&E       SETC  ''
&J       SETA  1
.BYTE    ANOP
&L SETA &QIO(&K)*8+&QIO(&K+1)*4+&QIO(&K+2)*2+&QIO(&K+3)+1
&E       SETC  '&E'.'0123456789ABCDEF'(&L,1)
&K       SETA  &K+4
&J       SETA  &J+1
         AIF   (&J LE 8).BYTE
&I       SETA  &I+1
&C       SETC  '&D'
&D       SETC  '&E'
         AIF   (&I LE 2).WORD
         DC    X'&C&D' . QIO TABLE (BIT=0 => NO I/O)
.MEXIT   MEXIT
.OFLOW   MNOTE 4,'ONLY &MAX DISPLAY FIELDS ALLOWED'
         AGO   .END1
.SPECIAL AIF   ('&Q' EQ '#').CONCAT
&QUECON  SETA  1             PERMIT CONCATENATIONS
         AIF   ('&Q' EQ '?' AND &I EQ 4).VAR
         AIF   (&I NE 3).MISSING
&D       SETC  '&Q1'
&E       SETC  '&Q2'
         AIF   ('&Q' EQ '*' OR '&Q' EQ '$').S1
         AGO   .MISSING
.S1      ANOP
&QUECNT  SETA  &QUECNT+1
         AIF   (&QUECNT GT &MAX).OFLOW
&QC      SETA  0
.SV      AIF   (&QUEIN EQ 1).SOUT
&K       SETA  1
&L       SETA  1
.SI1     AIF   ('&D' EQ 'ABCDEFGHIJKLMNO'(&K,1)).SI2
&K       SETA  &K+1
         AIF   (&K GT 15).MISSING
         AGO   .SI1
.SI2     AIF   ('&E' EQ 'ABCDEFGHIJKLMNO'(&L,1)).SI3
&L       SETA  &L+1
         AIF   (&L GT 15).MISSING
         AGO   .SI2
.SI3     ANOP
&K       SETA  &K+39
&L       SETA  &L+39
.SCOM    AIF   (&K GT &L).MISSING
         DC    C'&Q',AL1(&QC,&K,&L)
&L       SETA  &L+1
.SCOML   ANOP
&K       SETA  &K+1
         AIF   (&K GT &L).MEXIT
&QIO(&K) SETA  1
         AGO   .SCOML
         AGO   .MEXIT
.SOUT    ANOP
&K       SETA  1
&L       SETA  1
.SO1     AIF   ('&D' EQ 'ABCDEFGHIJKLMNOPQRSTUVWXYZ0123456789'(&K,1)).X
&K       SETA  &K+1
         AIF   (&K GT 36).MISSING
         AGO   .SO1
.X       AIF   ('&E' EQ 'ABCDEFGHIJKLMNOPQRSTUVWXYZ0123456789'(&L,1)).Y
&L       SETA  &L+1
         AIF   (&L GT 36).MISSING
         AGO   .X
.Y       ANOP
&K       SETA  &K+2
&L       SETA  &L+2
         AGO   .SCOM
.VAR     ANOP
&C       SETC  '&Q1'
&D       SETC  '&Q2'
&E       SETC  '&Q3'
         AIF   (&C LT 1 OR &C GT &MAX).MISSING
&QC      SETA  &MAX-&QUECNT
         AIF   (&QC LE 0).OFLOW
         AIF   (&QC LT &C).NOROOM
&QC      SETA  &C
         AGO   .V1
.NOROOM  MNOTE 4,'REQUEST FOR ''&C'' FIELDS TRUNCATED TO ''&QC'''
.V1      ANOP
&QUECNT  SETA  &QUECNT+&QC
         AGO   .SV
.CONCAT  AIF   (&QUECON EQ 0).MISSING
.*       AIF   (&I NE 2 AND &I NE 3).MISSING
         AIF   (&I NE 2).MISSING
&QC      SETA  1
&J       SETA  1
.CONCLP  AIF   (&J GE &I).SCOM
&J       SETA  &J+1
&L       SETA  2
.CONCLS  AIF   ('&SYSLIST(&J)' EQ '&BIG(&L)').CONCLT
&L       SETA  &L+1
         AIF   (&L LE &BM).CONCLS
         AGO   .MISSING
.CONCLT  ANOP  ,
&L       SETA  &BIGQ(&L)
         AIF   (&J NE 2).CONCLP
&K       SETA  &L
         AGO   .CONCLP
         MEND
