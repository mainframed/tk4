         MACRO ,
&NM     GETREGS &F,&L,&D     EXTRACT CALLER'S REGISTER(S) ADDED GP13291
.*
.*       Retrieve register(s) from caller's save area (using normal
.*       OS calling conventions).
.*
.*   &F IS FIRST REGISTER TO RESTORE (0-12)
.*   &L IS LAST REGISTER TO RESTORE  (0-12)
.*   &D IS REGISTER TO RESTORE TO; DEFAULT IF OMITTED IS &F
.*
         LCLC  &OF,&OL,&DF,&DL
&DF      SETC  '&D(1)'
         AIF   ('&DF' NE '').HAVEDR
&DF      SETC  '&F(1)'
.HAVEDR  ANOP  ,
&OF      SETC  '&F(1)'
&OL      SETC  '&L(1)'
&DL      SETC  '(&OL-&OF)+&DF'
&NM      L     &DF,4(,R13)   CALLER'S SAVE AREA
         AIF   ('&OL' EQ '').ONE
         AIF   ('&OF' EQ '&OL').ONE
         LM    &DF,&DL,20+4*&OF.(&DF)
         MEXIT ,
.ONE     L     &DF,20+4*&OF.(,&DF)
         MEND  ,
