         MACRO
&NM      MVSDSAB &DDNAME=,&DCBPTR=,&DSABPTR=,&TCBPTR=,&RETCODE=,       *
               &RSNCODE=,&MF=
         GBLB  &MVSESA       IF ESA, USE IBM'S MACRO AND SERVICE
         GBLC  &MACPLAB      FIRST LABEL EXPANDED, IF ANY
.**********************************************************************
.*                                                                    *
.*   THIS MACRO LINKS TO LOCAL SUBROUTINE SUBDSAB FOR EARLY SYSTEMS   *
.*   (E.G., MVS 3.8J). FOR ESA AND LATER, IT INVOKES IBM'S GETDSAB    *
.*   SERVICE                                                          *
.*                                                                    *
.**********************************************************************
         LCLC  &MF1,&MF2,&MF3,&LLN,&@REQ,&@TCB,&@RSN,&@RET
         LCLA  &I,&J,&K,&#REQ
&MF1     SETC  '&MF(1)'
&MF2     SETC  '&MF(2)'
&MF3     SETC  '&MF(3)'
&LLN     SETC  '5'           NUMBER OF WORDS IN LIST
         AIF   (NOT &MVSESA).USELOCL
&NM      GETDSAB &SYSLIST(1),DDNAME=&DDNAME,DCBPTR=&DCBPTR, *
               DSABPTR=&DSABPTR,TCBPTR=&TCBPTR,                        *
               RETCODE=&RETCODE,RSNCODE=&RSNCODE,MF=&MF
         MEXIT ,
.USELOCL ANOP ,
         AIF   ('&MF1' EQ 'L').LIST
&MACPLAB SETC  '&NM'         LABEL TO BE EXPANDED
         AIF   ('&MF1' EQ 'S' OR '&MF1' EQ '').STND
         AIF   ('&MF1' EQ 'E').EXEC
         MNOTE 8,'MF=&MF1 NOT A SUPPORTED OPTION'
         MEXIT ,
.*
.LIST    AIF   ('&MF3' NE '').LISTEX
&MF3     SETC  '0A'          DEFAULT ALIGNMENT
.LISTEX  AIF   ('&MF3' EQ '0F' OR '&MF3' EQ '0D' OR                    *
               '&MF3' EQ '0A').LISTAOK
         MNOTE 4,'MF= &MF3 NOT SUPPORTED; USING 0F'
&MF3     SETC  '0A'          FORCED ALIGNMENT
.LISTAOK AIF   ('&SYSLIST(1)' NE '').LISTBAD
         AIF   ('&DDNAME' NE '').LISTBAD
         AIF   ('&DCBPTR' NE '').LISTBAD
         AIF   ('&DSABPTR' NE '').LISTBAD
         AIF   ('&TCBPTR' NE '').LISTBAD
         AIF   ('&RETCODE' NE '').LISTBAD
         AIF   ('&RSNCODE' EQ '').LISTEXP
.LISTBAD MNOTE 4,'KEYWORD SETTINGS IGNORED FOR MF=L'
.LISTEXP ANOP  ,
&MF2     DS    &MF3
&NM      MACPARM MODE=LABEL  EXPAND ADDITIONAL LABEL, IF NEEDED
         DC    &LLN.AL4(0)
         MEXIT ,
.*
.STND    ANOP  ,
         MACPARM R1,*+4*&LLN+4,OP=BAL
         DC    &LLN.AL4(0)
         AGO   .EXECC
.*
.EXEC    AIF   ('&MF2' NE '').EXECR1
         MNOTE 8,'MF=(E,LIST) NEEDS LIST ADDRESS'
         MEXIT ,
.EXECR1  MACPARM R1,&MF2
.EXECC   MACPARM 0(4*&LLN,R1),0(R1),OP=XC  CLEAR LIST
         AIF   ('&DCBPTR' EQ '' AND '&DDNAME' EQ '').NODUPE
         MNOTE 8,'DCBPTR AND DDNAME ARE MUTUALLY EXCLUSIVE'
         MEXIT ,
.EXCDCB  MNOTE 8,'FIRST/NEXT AND DCBPTR= ARE MUTUALLY EXCLUSIVE'
         MEXIT ,
.EXCDDN  MNOTE 8,'FIRST/NEXT AND DDNAME= ARE MUTUALLY EXCLUSIVE'
         MEXIT ,
.NODUPE  AIF   ('&SYSLIST(1)' NE '' AND '&DCBPTR' NE '').EXCDCB
         AIF   ('&SYSLIST(1)' NE '' AND '&DDNAME' NE '').EXCDDN
&#REQ    SETA  1             GET FIRST DSAB
&@REQ    SETC  '0'           NO ADDRESS NEEDED
         AIF   ('&SYSLIST(1)' EQ 'FIRST').OTHR   LOOK FOR OTHERS
&#REQ    SETA  2             GET NEXT DSAB
&@REQ    SETC  '0'           NO ADDRESS NEEDED
         AIF   ('&SYSLIST(1)' EQ 'NEXT').OTHR   LOOK FOR OTHERS
         AIF   ('&SYSLIST(1)' EQ '').TREQ   LOOK FOR NON-CHAINED
         MNOTE 8,'UNSUPPORTED POSITIONAL: &SYSLIST(1)'
         MEXIT ,
.TREQ    AIF   ('&DDNAME' NE '').SETDCB
&#REQ    SETA  3             GET DSAB BY DDNAME
&@REQ    SETC  '&DDNAME'     DDN ADDRESS NEEDED
         AGO   .OTHR
.SETDCB  ANOP  ,
&#REQ    SETA  4             GET DSAB BY DCB
&@REQ    SETC  '&DCBPTR'     DCB ADDRESS NEEDED
.OTHR    ANOP  ,
         MVI   0(R1),&#REQ   SET REQUEST TYPE
         MACPLOP &@REQ,4(R1)   PASS REQUEST ADDRESS OR 0
         MACPLOP &DSABPTR,8(R1)
         MACPLOP &TCBPTR,12(R1),NULL=SKIP
         MACPARM R15,=V(SUBDSAB),OP=L
         MACPARM R14,R15,OP=BALR,OPR=BALR
         MACPARM R15,&RETCODE,OP=ST,NULL=SKIP
         MACPARM R0,&RSNCODE,OP=ST,NULL=SKIP
         MEND  ,
