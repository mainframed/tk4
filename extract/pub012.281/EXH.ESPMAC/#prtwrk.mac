         MACRO ,
&NM    #PRTWRK  &DDNAME=SYSPRINT,&MAXLPP=60,&PFX=PR1,&@TITLE=@HEADERS, *
               &FLAGS=0,&WIDTH=132,&REPCH=0
.*  This macro defines work space for one print file. The definition
.*     is used with the #PRT macro and the SUBPRT subroutine.
.*  A unique DDNAME is required, and a unique prefix. Only eight
.*   concurrent print files are supported, and it is suggested to use
.*   prefixes of PR1 to PR8 for convenience.  The name of this work
.*   area (invocation name field) must be placed into the @PRTWORK
.*   list at this printer number's offset using a #PRT TYPE=OPEN call.
.*  Title specifies the name of a variable length list of words. The
.*   first word specifies the address where the 4 byte page number
.*   will be updated; the next words the first, second, etc. header
.*   lines (first of these must have a page eject); the list is
.*   terminated by a word of zeroes.
.*  MAXLPP should be set to the desired lines per page, but not less
.*   than 15 and not more than 200
.*  FLAGS should be set (with the appropriate prefix):
.*     =pr1FGPUN   punch file (no translation) - not tested
.*     =pr1FGTRN   inhibit translation of unprintable characters
.*     =pr1FGPRT   MCC ION printers only - use portrait mode
.*  WIDTH=132 - provided for future expansion (ibm 3800 = 204)
.*  REPCH= self-defining character - sets the replacement character
.*     for unprintable characters for all open files (only one table
.*     is defined for all files. defaults to reverse slash \
&NM      DS    0D            WORK AREA FOR PRINTER &PFX (PRT#)
&PFX.@HEAD DC  A(&@TITLE)    ADDRESS OF TITLE LIST
         DC    3A(0)           RESERVED FOR FUTURE EXPANSION
         SPACE 1
&PFX.DCB DCB   DDNAME=&DDNAME,DSORG=PS,MACRF=PM EXLST=PRTEXLST
&PFX.MAXTX DC  Y(&WIDTH)     MAXIMUM TEXT WIDTH
&PFX.MAXLN DC  Y(&MAXLPP)    MAXIMUM LINES PER PAGE
&PFX.CURLN DC  Y(1+&MAXLPP)  CURRENT LINE POSITION
&PFX.PAGE  DC  Y(0)          PAGE NUMBER
&PFX.FORCC DC  X'00'         FORCED CC OVERRIDE ON NEXT PRINT
&PFX.FLAGS DC  AL1(&FLAGS)   PROCESSING FLAGS
&PFX.FGPUN EQU X'80'           PUNCH FILE; NO CC
&PFX.FGTRN EQU X'40'           PRINT; INHIBIT TRANSLATE
&PFX.FGPRT EQU X'20'  IRS MCC SPECIFIC - SET ION PRT PORTRAIT MODE
&PFX.REPCH DC  AL1(&REPCH)   UNPRINTABLE CHARACTER REPLACEMENT
&PFX.RDW   DC  Y(5,0)        RECORD DESCRIPTOR FOR RECFM=V
&PFX.CC    DC  X'8B'         CARRIAGE CONTROL
.*FX.TRL   DC  C'1'          3800 OPTCD=J TABLE REFERENCE CHARACTER
&PFX.TEXT  DC  CL(&WIDTH)' ' DATA RECORD
         DC    3X'00'          RESERVED
         MEND  ,
