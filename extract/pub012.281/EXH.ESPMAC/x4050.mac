         MACRO ,
&NM      X4050 &JDE=DUPLEX,&JDL=DFAULT,                 ADDED ON 89143 *
               &PAPER=LETTER,&FEED=MAIN,&MAXLEN=150,                   *
               &PMODE=LAND,&DUPLEX=YES,&SEP=WIDE,                      *
               &OVERFIX=YES,&CHARS=,&HCOUNT=1,&TCOUNT=1,&FLASH=,       *
               &PREJES2=0,&PRESAM=0,&POSTJES=0,&POSTSAM=0,&MF=D
.**********************************************************************
.*                                                                   **
.*   MACRO TO DEFINE/CORRELATE XEROX 4050 DJDE SETUP                 **
.*     USED BY LOCAL JES2 MODS AND LOCAL WRITER ROUTINES             **
.*                                                                   **
.**********************************************************************
         LCLB  &B0,&B1,&B2,&B3,&B4,&B5,&B6,&B7
         LCLB  &B8,&B9,&B10,&B11,&B12,&B13,&B14,&B15
&B0      SETB  ('&OVERFIX' EQ 'YES')
&B1      SETB  ('&CHARS' EQ 'DUMP')                              89302
&B4      SETB  ('&SEP' EQ 'NARROW')                              89157
&B8      SETB  ('&DUPLEX' EQ 'YES')
&B12     SETB  ('&PMODE' EQ 'PORTRAIT')
         AIF   ('&MF' EQ 'D').MAP
.*
&NM      START 0             BEGIN DEFINITION MODULE
         DC    B'&B0&B1&B2&B3&B4&B5&B6&B7'
         DC    B'&B8&B9&B10&B11&B12&B13&B14&B15'
         DC    B'0'
         DC    B'0'
         DC    B'0'
         DC    B'0'
         DC    AL1(&MAXLEN)                                      89157
         DC    AL1(&HCOUNT*16+&TCOUNT)                           89157
         DC    A(&NM+40,&PREJES2,&POSTJES,&PRESAM,&POSTSAM)
         DC    3A(0)         SPARES
         DC    CL8'&JDL ',CL8'&JDE ',CL8'&PAPER '
         DC    CL8'&FEED ',CL8'&FLASH '                          89355
         MEXIT ,
.MAP     ANOP  ,
X4DSECT  DSECT ,
X4F1     DS    X             LOCAL GOODIES
X4FOFIX  EQU   X'80'           FIX OVERPRINT TO DARKEN OUTPUT
X4FODUMP EQU   X'40'           FIX CHARS=DUMP OUTPUT             89302
X4FCARD  EQU   X'08'           80-BYTE SEPARATOR MODE            89157
X4F2     DS    X             RENDITION
X4FDUPX  EQU   X'80'           DUPLEX (0=SIMPLEX)
X4FPORT  EQU   X'08'           PORTRAIT (0=LANDSCAPE)
X4F3     DS    X
X4F4     DS    X
X4F5     DS    X
X4MAXLEN DS    AL1(&MAXLEN)     MAX WIDTH TO PROCESS             89157
X4HEAD#  DS    AL1(&HCOUNT*16+&TCOUNT)   HEADER/TRAILER PAGES    89157
X4@SET   DS    A             ADDRESS OF BASIC SETUP TEXT
X4@PREJ  DS    A             ADDRESS OF JES2 PREFIX
X4@POSJ  DS    A             ADDRESS OF JES2 POST-AMBLE
X4@PRES  DS    A             ADDRESS OF SAM PREFIX (ON-LINE)
X4@POSS  DS    A             ADDRESS OF SAM POST-AMBLE
         DS    3A            SPARES                              89143
*        FLOATING SETUP                                          89143
X4JDLSCT DSECT ,                                                GP99354
X4JDL    DS    CL8           JDL/JSL                             89143
X4JDE    DS    CL8           JDE WITHIN JDL                      89143
X4PAPER  DS    CL8           TYPE OF PAPER                       89143
X4FEED   DS    CL8           FEED MODE                           89143
X4FLASH  DS    CL8           FORMS FLASH (IBM CL4, XEROX CL6)    89355
*        OPTIONAL SETUP COMMANDS                                 89143
X4CMDSCT DSECT ,                                                GP99354
X4CMD    DS    X             COMMAND LENGTH
X4CFG    DS    X             COMMAND FLAGS
X4CTX    DS    0C            COMMAND TEXT
         MEND  ,
