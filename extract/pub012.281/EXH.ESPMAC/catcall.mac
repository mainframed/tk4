         MACRO ,
&NM      CATCALL &FUN,&ARG,&PFX=CSP,&MODE=(V,BASSM),&PARM=PARM
.*--------------------------------------------------------------------*
.*                                                                    *
.*  CATCALL - USED TO INVOKE THE SUBCAT SERVICE FOR SIMPLE AND        *
.*    MASKED CATALOG LOOKUP. 'FUN' IS LOOK (SINGLE REQUEST),          *
.*    INIT, THEN LOOP, FOR MASKED REQUEST, CLOSE TO FREE STORAGE.     *
.*                                                                    *
.*--------------------------------------------------------------------*
         GBLB  &MACPNUL,&MVSXA
         GBLC  &MACPLAB
         LCLA  &I,&J,&K
         LCLC  &C,&D,&E,&SUBNM
&SUBNM   SETC  'UBICF'       ICF CATALOG INTERFACE
         AIF   (&MVSXA).SYSXA  AT LEAST MVS XA ?
&SUBNM   SETC  'UBCAT'       VSAM & CVOL CATALOG INTERFACE
.SYSXA   ANOP  ,
&MACPLAB SETC  '&NM'
&C       SETC  '&FUN'.'    '
&C       SETC  '&C'(1,4)
&J       SETA  1
.FUNLOOP AIF   ('&C' EQ 'CLOSLOOKINITLOOP'(&J,4)).HAVEFUN
&J       SETA  &J+4
         AIF   (&J LT 16).FUNLOOP
&J       SETA  0                                                GP09149
         AIF   ('&C' EQ 'END ').HAVEFUN    ALTERNATE FOR CLOSE  GP09149
         MNOTE 8,'CATCALL: INVALID FUNCTION REQUEST &FUN'
.HAVEFUN MACPARM &PFX.$REQ,&J/4,OP=MVI
         MACPARM R14,&ARG,NULL=SKIP
         AIF   (&MACPNUL).NOARG
         MACPARM R14,&PFX.@REQ,OP=ST
.NOARG   AIF   ('&MODE(1)' EQ '').DOV
         AIF   ('&MODE(1)' NE 'V').NOTV
.DOV     MACPARM R15,=V(&SUBNM),OP=L
         O     R15,=X'80000000'    GET HIGH
         AGO   .INVOKE
.NOTV    AIF   ('&MODE(1)' NE 'A').NOTA
         MACPARM R15,=A(&SUBNM),OP=L
         O     R15,=X'80000000'    GET HIGH
         AGO   .INVOKE
.NOTA    AIF   ('&MODE(1)' NE '@').NOTD
         MACPARM R15,@&SUBNM,OP=L
         AGO   .INVOKE
.NOTD    MNOTE 8,'CATCALL: LOAD ADDRESS MODE &MODE(1) NOT RECOGNIZED'
.INVOKE  MACPARM R1,&PFX.&PARM,NULL=SKIP  PASS WORK AREA
         AIF   (&MVSXA).AM31  NEWFLANGLED 370 INSTRUCTIONS
&C       SETC  'BALR'
&E       SETC  'R14'         RETURN REGISTER
         AIF   (N'&MODE LE 1).REGOP2
&D       SETC  '&MODE(2)'
         AIF   ('&D' EQ 'BAKR' OR '&D' EQ 'BASSM').REGOP2
         AIF   ('&D' EQ 'BASR' OR '&D' EQ 'BALR').REGOP2
         AIF   ('&D' EQ 'BSM').REGOP2
         AGO   .BADMODE
.AM31    ANOP  ,
&C       SETC  'BAKR'
&E       SETC  '0'           NO RETURN REGISTER
         AIF   ('&D' EQ 'BAKR').REGOP2
&C       SETC  'BASSM'
&E       SETC  'R14'         RETURN REGISTER
         AIF   (N'&MODE LE 1).REGOP2
&D       SETC  '&MODE(2)'
         AIF   ('&D' EQ 'BASSM').REGOP2
&C       SETC  'BSM'
         AIF   ('&D' EQ 'BSM').REGOP2
&C       SETC  'BASR'
         AIF   ('&D' EQ 'BASR').REGOP2
&C       SETC  'BALR'
         AIF   ('&D' EQ 'BALR').REGOP2
.BADMODE MNOTE 8,'CATCALL: BRANCH TYPE MODE &D NOT RECOGNIZED'
.REGOP2  MACPARM R14,(R15),OP=&C,OPR=&C
         MEND  ,
