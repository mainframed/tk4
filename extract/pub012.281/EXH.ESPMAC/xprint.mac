         MACRO
&NM      XPRINT &MSG,&LEN,&AD=@XPRINT,&MODE=PRINT
         GBLC  &MACPLAB
         LCLC  &NAM
         LCLA  &OFF
&MACPLAB SETC  '&NM'
         AIF   ('&MODE' EQ 'PRINT').OLDPRT
&OFF     SETA  28           OFFSET FOR OPEN POINTER
         AIF   ('&MODE' EQ 'OPEN').BAL  NO PARMS
&OFF     SETA  32           OFFSET FOR CLOSE
         AIF   ('&MODE' EQ 'CLOSE').BAL  NO PARMS
&OFF     SETA  36           OFFSET FOR RESERVE
         AIF   ('&MODE' EQ 'RES' OR '&MODE' EQ 'RESERVE').COUNT
&OFF     SETA  40           OFFSET FOR SPACE
         AIF   ('&MODE' EQ 'SPC' OR '&MODE' EQ 'SPACE').COUNT
         MNOTE 8,'XPRINT: UNRECOGNIZED MODE=&MODE'
.COUNT   MACPARM R1,&MSG,NULL=1  DEFAULT COUNT TO 1
         AGO   .BAL          JOIN COMMON
.OLDPRT  AIF   ('&MSG' NE '').OK
         MNOTE 4,'MSG ADDR MISSING'
         MEXIT
.BLOO    MNOTE 12,'LEN=(1) INVALID'
.OK      ANOP
         MACPARM  R1,&MSG
         AIF   ('&LEN' EQ '').BAL
         AIF   ('&LEN'(1,1) EQ '(').LR
         MACPARM  1(R1),&LEN,OP=MVI   SET LENGTH
         AGO   .BAL
.LR      AIF   ('&LEN' EQ '(1)' OR '&LEN' EQ '(R1)').BLOO
         MACPARM  &LEN(1),0(R1),OP=STH   SET LENGTH
.BAL     MACPARM  R15,&AD,NULL==V(EXORPRNT),OP=L  GET PRINT ROUTINE
         AIF   (&OFF EQ 0).BAS
         MACPARM R15,&OFF.(,R15),OP=L  GET ENTRY ADDRESS
.BAS     MACPARM  R14,(R15),OP=BAL,OPR=BALR     PRINT A LINE
         MEND
