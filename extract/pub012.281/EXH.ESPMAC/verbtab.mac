         MACRO ,
&NM      VERBTAB &STR,&LB,&BASE=,&LEN=                UPDATED ON 81169
         GBLA  &TABMBLN                                          81169
         GBLC  &TABMBAS
         LCLA  &I,&J,&K,&L
         LCLC  &H                                                81169
&K       SETA  K'&STR-1
         AIF   (&TABMBLN NE 0).HADLEN                            81169
&TABMBLN SETA  3             SET DEFAULT                         81169
         AIF   (T'&LEN EQ 'O').HADLEN                            81169
&TABMBLN SETA  &LEN                                              81169
.HADLEN  AIF   ('&STR' EQ '').PARMA                              81169
         AIF   ('&STR'(1,1) NE '''').COMLEN
&I       SETA  2
&J       SETA  &K-1
&K       SETA  &J
.LOOP    AIF   ('&STR'(&I,2) EQ '''''').SK2
         AIF   ('&STR'(&I,2) EQ '&&').SK2
&I       SETA  &I+1
         AGO   .INC
.SK2     ANOP  ,
&I       SETA  &I+2
&K       SETA  &K-1
.INC     AIF   (&I LE &J).LOOP
&K       SETA  &K-1
         AIF   (&K GE 0).COMLEN
&K       SETA  0
.COMLEN  AIF   ('&BASE' EQ '').NBAS
         AIF   ('&BASE' NE '*').UBAS
&TABMBAS SETC  '&SYSECT'
         AGO   .NBAS
.UBAS    ANOP  ,
&TABMBAS SETC  '&BASE'
.NBAS    AIF   ('&TABMBAS' NE '').BASOK
&TABMBAS SETC  '&SYSECT'     DEFAULT BASE TO CURRENT
.BASOK   AIF   ('&STR' EQ '*END').LAST
         AIF   ('&LB' NE '').THEX                                81169
.PARMA   MNOTE 8,'MISSING OR CONFLICTING PARAMETERS'
         MEXIT ,
.THEX    AIF   (&K LT 1).TO                                      81169
         AIF   ('&STR'(1,1) NE '*').TO                           81169
&L       SETA  &K            SUPPLIED LENGTH-1                   81169
&K       SETA  (&K+1)/2-1    NEW TEXT LENGTH                     81169
&H       SETC  '&STR'(2,&L)                                      81169
&NM      DC    AL1(&K),AL&TABMBLN.(&LB-&TABMBAS),X'&H'
         MEXIT ,                                                 81169
.TO      AIF   ('&STR'(1,1) EQ '''').TOAP
&NM      DC    AL1(&K),AL&TABMBLN.(&LB-&TABMBAS),C'&STR'
         MEXIT ,
.TOAP    ANOP  ,
&NM      DC    AL1(&K),AL&TABMBLN.(&LB-&TABMBAS),C&STR
         MEXIT ,
.LAST    ANOP  ,
&NM      DC    X'FF'         END OF TABLE
         MEND  ,
