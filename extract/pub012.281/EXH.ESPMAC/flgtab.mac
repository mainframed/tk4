         MACRO
&NM      FLGTAB &MASK,&TEXT,&LEN,&MLEN=
.*--------------------------------------------------------------------*
.*                                                                    *
.*  THIS MACRO GENERATES A TABLE OF FLAG BITS AND MATCHING TEXT       *
.*  &MLEN (ON THE FIRST ENTRY) SPECIFIES THE BIT MASK LENGTH IN BYTES *
.*  &TEXT SPECIFIES THE QUOTED OR UNQUOTED TEXT FOR THAT BIT MASK     *
.*  &LEN OPTIONALLY OVERRIDES THE CALCULATED TEXT LENGTH              *
.*                                                                    *
.*--------------------------------------------------------------------*
         GBLC  &ZZFGTLN      REMEMBER LENGTH GLOBALLY
         LCLA  &I,&K,&L
         LCLB  &QUO
         LCLC  &Q1,&Q2
.*
.*  CHECK END REQUEST
.*
         AIF   ('&MASK' EQ '*END').END
.*
&QUO     SETB  ('&TEXT'(1,1) EQ '''')
         AIF   (&QUO).NOFRAME
&Q1      SETC  ''''
&Q2      SETC  ''''
.*
.*  SET GLOBAL LENGTH AS NEEDED
.*
.NOFRAME AIF   ('&MLEN' EQ '').NOTMLEN
&ZZFGTLN SETC  '&MLEN'
.NOTMLEN AIF   ('&ZZFGTLN' NE '').GOTMLEN
&ZZFGTLN SETC  '1'           DEFAULT MASK LENGTH IS 1
.*
.*  WHEN USER SPECIFIES A LENGTH, JUST EXPAND THE REQUEST
.*
.GOTMLEN AIF   ('&LEN' EQ '').COMLEN
&NM      DC    AL1(&LEN-1),AL(&ZZFGTLN)(&MASK),CL(&LEN)&Q1&TEXT&Q2
         MEXIT ,
.*
.*  CALCULATE THE LENGTH OF THE TEXT ITEM
.*
.COMLEN  AIF   (&QUO).QUOADJ
&L       SETA  K'&TEXT
&I       SETA  &L
&K       SETA  1
         AGO   .LOOP
.QUOADJ  ANOP  ,
&L       SETA  K'&TEXT-2
&I       SETA  &L-1
&K       SETA  2
.*
.*  SCAN FOR DOUBLE QUOTES, AND DECREMENT LENGTH BY ONE
.*
.LOOP    AIF   (&K GE &I).EXPQ
         AIF   ('&TEXT'(&K,2) EQ '''''').DOUB
&K       SETA  &K+1
         AGO   .LOOP
.DOUB    ANOP
&K       SETA  &K+2
&L       SETA  &L-1
         AGO   .LOOP
.*
.*  GENERATE WITH FORCED LENGTH
.*
.EXPQ    ANOP
&NM      DC    AL1(&L-1),AL(&ZZFGTLN)(&MASK),CL(&L)&Q1&TEXT&Q2
         MEXIT ,
.END     ANOP  ,
&NM      DC    AL1(255)      END OF FLAG TABLE ENTRIES
         MEND  ,
