         MACRO ,
&NM      XCLDEL &ADDR,&ACTION=NOW
.*
.*   XCLDEL REMOVES A CLEANUP ELEMENT TO THE EXWCLEAN CHAIN.
.*   IN ADDITION TO THE DELETION FROM THE CHAIN, THE TERMINAL
.*   ACTION APPROPRIATE TO ITS TYPE IS ALSO PERFORMED:
.*   01 - CLOSE THE XIO DCB, UNALLOC, ETC., THEN FREE
.*   00 - FREEMAIN THE STORAGE
.*
.*   THE POSITIONAL OPERAND IS THE ADDRESS OF THE ELEMENT TO
.*   BE DELETED.
.*
.*   ACTION= MAY BE SET OTHER THAN NOW TO SKIP NORMAL CLEANUP.
.*   IN THAT CASE THE CALLER IS RESPONSIBLE FOR PROPER CLEANUP.
.*
.*   NOTE THAT THE ELEMENT ADDRESS IS COPIED TO R7 TO MATCH THE
.*   ADDRESS USED BY XIO.
.*
         LCLC  &ZZ
&ZZ      SETC  'ZZ'.'&SYSNDX'
&NM      STM   R0,R7,12(R13)      SAVE SOME REGISTERS
         LA    R15,4              SET WARNING CODE
         MACPARM R2,&ADDR         LOAD ELEMENT ADDRESS
         LA    R2,0(,R2)          CLEAN HIGH
         LTR   R2,R2              ANY SUPPLIED?
         BZ    &ZZ.X                NO; JUST RETURN R15=4
         LA    R7,EXWCLEAN        POINT TO CHAIN HEAD
&ZZ.L    LR    R1,R7              REMEMBER PREVIOUS ADDRESS
         ICM   R7,15,0(R7)        GET NEXT, IF ANY
         BZ    &ZZ.X                NOT FOUND; RETURN R15=4
         CR    R2,R7              MATCHING ELEMENT?
         BNE   &ZZ.L                YES
         MVC   0(4,R1),0(R7)      UNCHAIN THE REQUESTED ELEMENT
         XC    0(4,R7),0(R7)      SIGNAL NOT ON CHAIN
         AIF   ('&ACTION' NE 'NOW').LATER
         CLI   8(R7),1            DASD?
         BL    &ZZ.F                NO; FREE STORAGE ONLY
*DEFER*  XIO   CLEAN,(R7)         USE SPECIAL ENTRY TO CLEAN UP
&ZZ.F    L     R0,4(,R7)          LOAD ELEMENT SUBPOOL/LENGTH
         FREEMAIN R,LV=(0),A=(R7)   AND FREE IT
         SLR   R15,R15            CLEAR RETURN
.LATER   ANOP  ,
&ZZ.X    LM    R0,R7,12(R13)      0 OR ADDRESS IN R15
.MEND    MEND  ,
