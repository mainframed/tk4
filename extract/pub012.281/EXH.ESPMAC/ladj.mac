         MACRO ,
&NM      LADJ  &CH,&LEN,&WK=R15,&MASK=C' '  DELETE BLANKS
.*--------------------------------------------------------------------*
.*                                                                    *
.*  THIS MACRO CREATES CODE TO LEFT-JUSTIFY A FIELD.                  *
.*  &FIELD MUST NOT BE A REGISTER SPECIFICATION, BUT A RELOCATABLE    *
.*  &LEN MAY BE ABSOLUTE, OR A REGISTER                               *
.*    &FIELD+LEN MUST BE A TRAILING FILL CHARACTER (NORMALLY BLANK)   *
.*  &WK IS A WORK REGISTER, NORMALLY R15                              *
.*  &MASK SPECIFIES EITHER BLANK OR ZERO (C' ' OR C'0'). SHIFTING     *
.*    STOPS WHEN OTHER THAN THE EXCLUDED BITS ARE ON.                 *
.*                                                                    *
.*--------------------------------------------------------------------*
         GBLC  &MACPLAB                                         GP03245
         LCLC  &L,&TAG,&FIELD                                   GP03016
         LCLC  &D,&B         BASE/DISPLACEMENT                  GP12307
         LCLA  &I,&J,&K                                         GP12307
&TAG     SETC  'ZZJ'.'&SYSNDX'                                  GP03016
&L       SETC  'L'''
&FIELD   SETC  '&CH'                                            GP12307
&K       SETA  K'&CH                                            GP12307
         AIF   ('&FIELD' EQ '').BOO
         AIF   (&K LT 3).NOREG                                  GP12307
         AIF   ('&CH'(1,1) NE '(' OR '&CH'(2,1) EQ '(').NOREG   GP12307
       AIF ('&CH'(&K,1) NE ')' OR '&CH'(&K-1,1) EQ ')').NOREG   GP12307
&FIELD   SETC  '0'.'&CH'     CHANGE (REG) TO 0(REG)             GP12304
.NOREG   AIF   ('&LEN' EQ '').DEF
         AIF   (T'&LEN NE 'N').MAC                              GP03016
&NM      LA    &WK,&LEN-1    NO. OF BYTES TO MOVE
         AGO   .COM
.MAC     ANOP  ,                                                GP03016
&NM      MACPARM &WK,&LEN                                       GP03016
&MACPLAB MACPARM &WK,0,OP=BCTR,OPR=BCTR                         GP03016
         AGO   .COM
.DEF     ANOP  ,
&NM      LA    &WK,&L&FIELD-1  NO. OF BYTES TO MOVE
.COM     ANOP  ,                                                GP12307
&TAG.L   TM    &FIELD,X'FF'-&MASK LEADING BLANK OR ZERO ?       GP03016
         BNZ   &TAG.X        RETURN                             GP03016
         EX    &WK,&TAG.M    SHIFT LEFT ONE POSITION            GP03016
         BCT   &WK,&TAG.L    REPEAT FOR ALL                     GP03016
         B     &TAG.X        DONE                               GP03016
&K       SETA  K'&FIELD                                         GP12307
         AIF   ('&FIELD'(&K,1) NE ')').SYMBOL                   GP12307
.*  PARSE xxx ( yyy ) - CHANGE TO xxx ( 1 , yyy )               GP12307
&I       SETA  &K                                               GP12307
&J       SETA  1                                                GP12307
.LOOP    AIF   (&I LE 1).ERRTO                                  GP12307
&I       SETA  &I-1                                             GP12307
         AIF   ('&FIELD'(&I,1) EQ '(').HAVLEFT                  GP12307
         AIF   ('&FIELD'(&I,1) EQ ')').HAVRITE                  GP12307
         AGO   .LOOP                                            GP12307
.HAVRITE ANOP  ,                                                GP12307
&J       SETA  &J+1                                             GP12307
         AGO   .LOOP                                            GP12307
.HAVLEFT ANOP  ,                                                GP12307
&J       SETA  &J-1                                             GP12307
         AIF   (&J GT 0).LOOP                                   GP12307
         AIF   (&I LE 1).SYMBOL   ((XYZ-RST))                   GP12307
&D       SETC  '&FIELD'(1,&I-1)                                 GP12307
&B       SETC  '&FIELD'(&I+1,&K-&I-1)                           GP12307
&TAG.M   MVC   &D.(0,&B),1+&FIELD                               GP12307
&TAG.X   DS    0H                                               GP12307
         MEXIT ,                                                GP12307
.SYMBOL  ANOP  ,             STASH LENGTH IN MVC                GP12307
&TAG.M   MVC   &FIELD.(0),1+&FIELD     SHIFT LEFT               GP12307
&TAG.X   DS    0H                                               GP03016
         MEXIT ,
.ERRTO   MNOTE 4,'LADJ - FIELD NAME MALFORMED'                  GP12307
         MEXIT ,                                                GP12307
.BOO     MNOTE 4,'LADJ - FIELD NAME OMITTED'
         AIF   ('&NM' EQ '').MEND                               GP03016
&NM      DS    0H            BUT AT LEAST DEFINE THE LABEL      GP03016
.MEND    MEND  ,
