         MACRO
&NM      XXTRKEY  &QUY,&TEXT,&CON=NO,&FOLD=                     GP06266
.*
.*   See EXHKDFLT and EXHCCXTR for use
.*
         GBLB  &PFKONE,&PFKEND,&PFKFOLD                         GP06266
         GBLC  &KEY(40),&HEX(40),&KVL(40),&SECT,&LBL(40)        GP06266
         GBLA  &IND(40),&FLG(40)                                GP06266
         LCLA  &I,&J,&K,&L,&F                                   GP06266
         LCLB  &NOC,&ISPFK                                      GP06266
&PFKFOLD SETB  (&PFKFOLD OR '&FOLD' EQ 'YES' OR '&FOLD' EQ 'Y') GP06266
&PFKFOLD SETB  (&PFKFOLD AND NOT ('&FOLD' EQ 'NO' OR '&FOLD' EQ 'N'))
         AIF   (NOT &PFKEND).OK
         MNOTE 8,'XXTRKEY: OUT OF SEQUENCE'
         MEXIT ,
.OK      AIF   ('&QUY' EQ '*END' OR '&QUY' EQ 'END').LAST
         AIF   ('&QUY' EQ '' OR '&TEXT' EQ '').BOOBOO
         AIF   (&PFKONE).INIT
         AIF   (K'&SYSECT EQ 8).SECTOK
.BADSECT MNOTE 4,'XXTRKEY: CONTROL SECTION NAME INVALID'
         AGO   .CONT
.SECTOK  AIF   ('&SYSECT'(1,4) NE 'EXHK').BADSECT
&SECT    SETC  '&SYSECT'(5,4)
         AIF   ('&SECT'(1,1) NE 'C').CONT
&SECT    SETC  'CRT '.'&SECT'(2,3)
.CONT    ANOP  ,
&PFKONE  SETB  1
&HEX(11) SETC  '0B'
&HEX(12) SETC  '0C'
&HEX(13) SETC  '0D'
&HEX(14) SETC  '0E'
&HEX(16) SETC  '10'
&HEX(17) SETC  '11'          PFK 1
&HEX(18) SETC  '12'
&HEX(19) SETC  '13'
&HEX(20) SETC  '14'
&HEX(21) SETC  '15'
&HEX(22) SETC  '16'
&HEX(23) SETC  '17'
&HEX(24) SETC  '18'
&HEX(25) SETC  '19'
&HEX(26) SETC  '1A'
&HEX(27) SETC  '1B'
&HEX(28) SETC  '1C'          PFK12
&HEX(29) SETC  '31'          PFK13                              GP06266
&HEX(30) SETC  '32'                                             GP06266
&HEX(31) SETC  '33'                                             GP06266
&HEX(32) SETC  '34'                                             GP06266
&HEX(33) SETC  '35'                                             GP06266
&HEX(34) SETC  '36'                                             GP06266
&HEX(35) SETC  '37'                                             GP06266
&HEX(36) SETC  '38'                                             GP06266
&HEX(37) SETC  '39'                                             GP06266
&HEX(38) SETC  '3A'                                             GP06266
&HEX(39) SETC  '3B'                                             GP06266
&HEX(40) SETC  '3C'          PFK24                              GP06266
&IND(01) SETA  14            ALIAS FOR PA2
&IND(02) SETA  02
&IND(03) SETA  03
&IND(04) SETA  04
&IND(05) SETA  05
&IND(06) SETA  06
&IND(07) SETA  07
&IND(08) SETA  08
&IND(09) SETA  09
&IND(10) SETA  10
&IND(11) SETA  11
&IND(12) SETA  12
&IND(13) SETA  13
&IND(14) SETA  14
&IND(15) SETA  15
&IND(16) SETA  16
&IND(17) SETA  17
&IND(18) SETA  18
&IND(19) SETA  19
&IND(20) SETA  20
&IND(21) SETA  21
&IND(22) SETA  22
&IND(23) SETA  23
&IND(24) SETA  24
&IND(25) SETA  25
&IND(26) SETA  26
&IND(27) SETA  27
&IND(28) SETA  28                                               GP06266
&IND(29) SETA  29                                               GP06266
&IND(30) SETA  30                                               GP06266
&IND(31) SETA  31                                               GP06266
&IND(32) SETA  32                                               GP06266
&IND(33) SETA  33                                               GP06266
&IND(34) SETA  34                                               GP06266
&IND(35) SETA  35                                               GP06266
&IND(36) SETA  36                                               GP06266
&IND(37) SETA  37                                               GP06266
&IND(38) SETA  38                                               GP06266
&IND(39) SETA  39                                               GP06266
&IND(40) SETA  40                                               GP06266
&FLG(11) SETA  32            SET UNUSED, AVAILABLE FOR ZAP      GP06259
&FLG(12) SETA  32                                               GP06259
&FLG(13) SETA  32                                               GP06259
&FLG(14) SETA  32                                               GP06259
&FLG(16) SETA  32                                               GP06259
&FLG(17) SETA  32                                               GP06259
&FLG(18) SETA  32                                               GP06259
&FLG(19) SETA  32                                               GP06259
&FLG(20) SETA  32                                               GP06259
&FLG(21) SETA  32                                               GP06259
&FLG(22) SETA  32                                               GP06259
&FLG(23) SETA  32                                               GP06259
&FLG(24) SETA  32                                               GP06259
&FLG(25) SETA  32                                               GP06259
&FLG(26) SETA  32                                               GP06259
&FLG(27) SETA  32                                               GP06259
&FLG(28) SETA  32                                               GP06266
&FLG(29) SETA  32                                               GP06266
&FLG(30) SETA  32                                               GP06266
&FLG(31) SETA  32                                               GP06266
&FLG(32) SETA  32                                               GP06266
&FLG(33) SETA  32                                               GP06266
&FLG(34) SETA  32                                               GP06266
&FLG(35) SETA  32                                               GP06266
&FLG(36) SETA  32                                               GP06266
&FLG(37) SETA  32                                               GP06266
&FLG(38) SETA  32                                               GP06266
&FLG(39) SETA  32                                               GP06266
&FLG(40) SETA  32                                               GP06266
&KVL(11) SETC  ' HELP XTR'
&KVL(12) SETC  ' HELP XTR'
&KVL(13) SETC  ' HELP XTR'
&KVL(14) SETC  ' HELP XTR'
&KVL(16) SETC  ' HELP XTR'
&KVL(17) SETC  ' HELP XTR'
&KVL(18) SETC  ' HELP XTR'
&KVL(19) SETC  ' HELP XTR'
&KVL(20) SETC  ' HELP XTR'
&KVL(21) SETC  ' HELP XTR'
&KVL(22) SETC  ' HELP XTR'
&KVL(23) SETC  ' HELP XTR'
&KVL(24) SETC  ' HELP XTR'
&KVL(25) SETC  ' HELP XTR'
&KVL(26) SETC  ' HELP XTR'
&KVL(27) SETC  ' HELP XTR'
&KVL(28) SETC  ' HELP XTR'
&KVL(29) SETC  ' HELP XTR'                                      GP06266
&KVL(30) SETC  ' HELP XTR'                                      GP06266
&KVL(31) SETC  ' HELP XTR'                                      GP06266
&KVL(32) SETC  ' HELP XTR'                                      GP06266
&KVL(33) SETC  ' HELP XTR'                                      GP06266
&KVL(34) SETC  ' HELP XTR'                                      GP06266
&KVL(35) SETC  ' HELP XTR'                                      GP06266
&KVL(36) SETC  ' HELP XTR'                                      GP06266
&KVL(37) SETC  ' HELP XTR'                                      GP06266
&KVL(38) SETC  ' HELP XTR'                                      GP06266
&KVL(39) SETC  ' HELP XTR'                                      GP06266
&KVL(40) SETC  ' HELP XTR'                                      GP06266
&KEY(01) SETC  'CANCEL'      ALIAS FOR PA2
&KEY(11) SETC  'PA3'
&KEY(12) SETC  'PA1'
&KEY(13) SETC  'CLEAR'
&KEY(14) SETC  'PA2'
&KEY(16) SETC  'TEST'
&KEY(17) SETC  'PF1'
&KEY(18) SETC  'PF2'
&KEY(19) SETC  'PF3'
&KEY(20) SETC  'PF4'
&KEY(21) SETC  'PF5'
&KEY(22) SETC  'PF6'
&KEY(23) SETC  'PF7'
&KEY(24) SETC  'PF8'
&KEY(25) SETC  'PF9'
&KEY(26) SETC  'PF10'
&KEY(27) SETC  'PF11'
&KEY(28) SETC  'PF12'
&KEY(29) SETC  'PF13'                                           GP06266
&KEY(30) SETC  'PF14'                                           GP06266
&KEY(31) SETC  'PF15'                                           GP06266
&KEY(32) SETC  'PF16'                                           GP06266
&KEY(33) SETC  'PF17'                                           GP06266
&KEY(34) SETC  'PF18'                                           GP06266
&KEY(35) SETC  'PF19'                                           GP06266
&KEY(36) SETC  'PF20'                                           GP06266
&KEY(37) SETC  'PF21'                                           GP06266
&KEY(38) SETC  'PF22'                                           GP06266
&KEY(39) SETC  'PF23'                                           GP06266
&KEY(40) SETC  'PF24'                                           GP06266
         AIF   ('&QUY' EQ '*END' OR '&QUY' EQ 'END').LAST
.INIT    AIF   ('&CON' EQ 'NO').INITCN
&NOC     SETB  1
         AIF  ('&CON' EQ '' OR '&CON' EQ 'YES' OR '&CON' EQ 'Y').INITCN
         MNOTE 2,'XXTRKEY: CON=&CON INVALID - YES ASSUMED'
.INITCN  ANOP  ,
&I       SETA  0
.INITL   AIF   (&I GT 39).BOOBOO
&I       SETA  &I+1
         AIF   ('&KEY(&I)' NE '&QUY').INITL
&I       SETA  &IND(&I)
&ISPFK   SETB  (&PFKFOLD AND &I GE 17 AND &I LE 28)             GP06266
&F       SETA  &I+12         FOR FOLD                           GP06266
         AIF   ('&KVL(&I)' EQ ' HELP XTR').INIT1
         MNOTE 2,'XXTRKEY: &KEY(&I) KEY ALREADY DEFINED'
         MEXIT ,
.INIT1   AIF   (NOT &ISPFK).INIT2                               GP06266
         AIF   ('&KVL(&F)' EQ ' HELP XTR').INIT2                GP06266
&ISPFK   SETB  0                                                GP06266
.INIT2   ANOP  ,                                                GP06266
&J       SETA  K'&TEXT
         AIF   ('&TEXT'(1,1) EQ '''').QUOTE
         AIF   (&J LE 72).UNQLOK
         MNOTE 2,'XXTRKEY: TEXT LENGTH > 72; TRUNCATED'         GP06266
&J       SETA  72                                               GP06266
.UNQLOK  ANOP  ,                                                GP06266
&L       SETA  &J
&LBL(&I) SETC  '&NM'
&KVL(&I) SETC  '&TEXT'
&FLG(&I) SETA  &NOC*128+1                                       GP06259
         AIF   (NOT &ISPFK).SAVE                                GP06266
&KVL(&F) SETC  '&TEXT'                                          GP06266
&FLG(&F) SETA  &NOC*128+1                                       GP06259
         AGO   .SAVE                                            GP06266
.BOOBOO  AIF   ('&TEXT' EQ '').BOOB
         AIF   ('&TEXT'(1,1) NE '''').BOOB
         MNOTE 8,'XXTRKEY: INVALID :  KEY=''&QUY'', VALUE='&TEXT''
         MEXIT ,
.BOOB    ANOP  ,
         MNOTE 8,'XXTRKEY: INVALID :  KEY=''&QUY'', VALUE=''&TEXT'''
         MEXIT ,
.QUOTE   ANOP  ,
&L       SETA  &J-2
&J       SETA  &L
&K       SETA  2
.LOOP    AIF   (&K GE &J).EXPQ
         AIF   ('&TEXT'(&K,2) EQ '&&').DOUB
         AIF   ('&TEXT'(&K,2) EQ '''''').DOUB
&K       SETA  &K+1
         AGO   .LOOP
.DOUB    ANOP  ,
&K       SETA  &K+2
&L       SETA  &L-1
         AGO   .LOOP
.EXPQ    AIF   (&L LE 72).QOTLOK                                GP06266
         MNOTE 2,'XXTRKEY: TEXT LENGTH > 72; TRUNCATED'         GP06266
&L       SETA  72                                               GP06266
.QOTLOK  ANOP  ,                                                GP06266
&LBL(&I) SETC  '&NM'
&KVL(&I) SETC  '&TEXT'(2,&J)
&FLG(&I) SETA  &NOC*128+1                                       GP06259
         AIF   (NOT &ISPFK).SAVE                                GP06266
&KVL(&F) SETC  '&TEXT'(2,&J)                                    GP06266
&FLG(&F) SETA  &NOC*128+1                                       GP06259
.SAVE    ANOP  ,
.MEXIT   MEXIT ,
.*
.LAST    AIF   (&PFKONE).LEAST
         MNOTE 8,'XXTRKEY: NO DEFINITIONS SUPPLIED'
         MEXIT ,
.LEAST   ANOP  ,
&I       SETA  0
.DEFLUP  AIF   (&I GT 39).DOEND
&I       SETA  &I+1
         AIF   (&FLG(&I) LT 1).DEFLUP                           GP06259
&LBL(&I) DC    AL1(X'&HEX(&I)',&FLG(&I)),CL6'&KEY(&I) ',CL72'&KVL(&I) '
         AGO   .DEFLUP
.DOEND   ANOP  ,
&NM      DC    X'00FF'       END OF LIST                        GP06266
         MEND  ,
