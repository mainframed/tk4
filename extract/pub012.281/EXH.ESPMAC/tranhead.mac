         MACRO
&NM      TRANHEAD &DOLL=C';'                               ADDED 79299
         COPY  OPTIONS
         LCLA  &I
         LCLC  &N1
&N1      SETC  '&NM'
         AIF   ('&N1' EQ '').BOOBOO
&I       SETA  K'&NM-4
         AIF   (&I LT 1).BOOBOO
         AIF   ('&N1'(1,4) NE 'EXHT').BOOBOO
&N1      SETC  '&N1'(5,&I)
&NM      START 0
         USING *,R15
         NUSE  SQSP,R13
         NUSE  WORK,R11
*        THIS MODULE PROVIDES TRANSLATION CODE AND TRANSLATE TABLES
*        FOR SPECIFIC DEVICES.  THE CODE ASSUMES THAT THE TRANSLATE
*        INSTRUCTION MAY BE USED; E.G. APL AND SIMILAR KEYBOARDS
*        WITH TWO-CHARACTER SHIFT SEQUENCES ARE NOT SUPPORTED.
*
*        INPUT R2 - BUFFER TO BE TRANSLATED  R3 - BUFFER LENGTH
*              R0 - FUNCTION SWITCH 0/OUT,DATA  2/OUT,CONTROL
*                                   4/IN,VAR. CASE  6/IN,UPPER CASE
*              R15 - BASE ADDRESS OF THIS MODULE
*              R14 - RETURN ADDRESS
*          R0-R3 ARE DESTROYED
*
         B     TRANSLAT      GO TO TRANSLATION CODE
         SPACE 1
         DC    CL4'&N1 '     MODULE ID
TRANPOIN DC    Y(OUTDATA-&NM)  OUTPUT TABLE; DATA ONLY
         DC    Y(OUTCON-&NM)   OUTPUT TABLE; CONTROL AND DATA
TRANULOW DC    Y(INUPLOW-&NM)  INPUT TABLE; UPPER+LOWER CASE
         DC    Y(INUPPER-&NM)  INPUT TABLE; UPPER CASE ONLY
TRANSDOL DC    AL1(&DOLL)    ALTERNATE FOR $ IN COMMANDS         87176
         DC    XL3'0'          SPARE                             87176
         DC    3A(0)         FOR EXPANSION
         SPACE 1
TRANSLAT CH    R0,=Y(TRANULOW-TRANPOIN)  UPLOW INPUT ?
         BNE   TRANSLAS      NO
         TM    SQTRANS,X'80'  UPLOW FLAG SET ?
         BNZ   TRANSLAS      YES; LEAVE LOWER CASE
         LA    R0,TRANULOW+2-TRANPOIN  SET UPPER CASE ONLY
TRANSLAS LR    R1,R0         COPY TABLE OFFSET
         LH    R1,TRANPOIN(R1)  GET TABLE OFFSET IN MODULE
         AR    R1,R15        GET TABLE ADDRESS
         LTR   R3,R3         VALID LENGTH ?
         BNPR  R14           NO; RETURN
         LR    R0,R14        SAVE RETURN ADDRESS
         LA    R14,255       MAKE LENGTH VALUE FOR EXECUTE
TRANLOOP CH    R3,=H'256'    MORE TO DO ?
         BNL   *+8           YES
         LR    R14,R3
         BCTR  R14,0         SET FOR EXECUTE
         EX    R14,TRANTR    TRANSLATE
         LA    R2,1(R2,R14)  BUMP BUFFER
         BCTR  R3,0          DECREMENT LENGTH
         SR    R3,R14        MORE TO DO ?
         BP    TRANLOOP
         LR    R14,R0        REGAIN EXIT ADDRESS
         BR    R14           RETURN TO CALLER
TRANTR   TR    0(0,R2),0(R1)  TRANSLATE BUFFER
         SPACE 1
         LTORG ,
         PUSH  PRINT
         PRINT NOGEN
&NM      CSECT ,
         MSECT ,
&NM      CSECT ,
         POP   PRINT
.MEXIT   MEXIT
.BOOBOO  MNOTE 12,'NAME MISSING OR INVALID'
         MEND
