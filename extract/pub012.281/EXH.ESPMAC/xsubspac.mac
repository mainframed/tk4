         MACRO
         XSUBSPAC &TRANS=YES
         SPACE 2
*        SUBROUTINE TO HANDLE DISPLAY OUTPUT
*
SPINIT   STH   R0,SPINDENT        SAVE INDENTATION VALUE
         LH    R10,LSIZE     GET LINE SIZE
         SR    R10,R0        GET DATA LENGTH
         STH   R10,SPDATEL     SAVE DATA LENGTH
         MVC   SPCURR,BUFAD     GET TOP OF SCREEN
         B     SPLINEUP      FORCE LINE ADVANCE
         SPACE
SPLINE   L     R10,SPDATA       GET FIRST DATA ENTRY
         CLC   BLANKS,0(R10)     WAS ANYTHING PLACED ON THIS LINE ?
         BE    SPLINEUS      NO, USE LINE AS IS
         SPACE
SPLINEUP L     R10,SPCURR     GET START OF CURRENT LINE
         AH    R10,LSIZE     GET NEXT LINE ADDRESS
         MVC   0(80,R10),BLANKS      CLEAR NEW LINE
         ST    R10,SPCURR      STASH NEW LINE ADDRESS
         AH    R10,LSIZE        BUMP TO LINE AFTER THAT
         ST    R10,SPLEND     SAVE END OF THIS LINE
         L     R10,SPCURR      RESTORE
         AH    R10,SPINDENT     GET START OF DATA ON THIS LINE
         ST    R10,SPDATA     AND SAVE
         SPACE
SPLINEUS L     R10,SPCURR      GET START OF CURRENT LINE
         C     R10,MSGAD      IS IT END OF SCREEN ?
         BR    R14           RETURN WITH CONDITION CODE SET
         SPACE 2
*        CODE TO MOVE / TEST DISPLAY OUTPUT ITEM
*        (14) - LENGTH    (15)  SOURCE ADDR. OR 0 FOR TEST
*        (10)  PRESUMED CURRENT POINTER
*        RETURN -  R9 - PAGE FULL; 12(R13)=1 NO MOVE; 12(R13)=0 MOVED
*                  4(9) - DATA MOVED; 12(R13)=0   - 12(R13)=1 SPFITS
*
SPFITS   SR    R15,R15       SET FOR TEST / ADVANCE ONLY
         SPACE
SPMOVE   STM   R2,R3,28(R13)     SAVE
         MVI   12(R13),1     PROVISIONALLY SIGNAL NO MOVE
         CH    R14,SPDATEL      FITS ON ONE LINE ?
         BNH   *+8           YES, OK
         LH    R14,SPDATEL     ELSE USE ONLY WHAT FITS
         C     R10,MSGAD     IS SCREEN FULL ?
         BNLR  R9
         LR    R3,R14        PREVENT LENGTH FROM GETTING LOST
         LA    R2,0(R10,R14)      NEW END POSITION WITHOUT SPACES
         C     R2,SPLEND      FITS ON THIS LINE ?
         BNH   SPMVC         YES, MOVE IT
         BALS  R14,SPLINEUP      SPACE TO NEXT LINE
         BL    *+10          ROOM LEFT ON SCREEN
         LM    R2,R3,28(R13)      RESTORE
         BR    R9            SIGNAL NO ROOM LEFT ON SCREEN
         L     R10,SPDATA     POINT TO FIRST DATA ENTRY
         LA    R2,0(R3,R10)     NEW END POSITION
         SPACE
SPMVC    LR    R1,R10        SAVE FOR EX
         LA    R10,1(,R2)    NEW END POSITION INCLUDING TRAILING BLANK
         LTR   R15,R15       WAS THIS A 'FITS' TEST ONLY ?
         BZ    SPRET         YES, EXIT
         BCTR  R3,0          FOR EXECUTE
         EX    R3,SPEXMVC       MOVE TO SCREEN
         MVI   12(R13),0     SIGNAL DATA MOVED
         AIF   ('&TRANS' EQ 'NO').SKPTRNS                       GP04234
         ICM   R2,15,SQTRANS TRANSLATE TABLE AVAILABLE ?        GP04234
         BZ    SPRET         NO; SKIP                           GP04234
         AH    R2,8(,R2)     ADD OFFSET TO OUTPUT TABLE         GP04234
         EX    R3,SPEXTRN    MAKE PRINTABLE PER TERM TYPE       GP04234
.SKPTRNS ANOP  ,
SPRET    LM    R2,R3,28(R13)       RESTORE
         C     R10,SPLEND    STILL ON THIS LINE ?
         BL    4(,R9)        YES, TAKE GOOD EXIT
         BALS  R14,SPLINEUP      ELSE GO TO NEXT LINE
         BL    *+4+2+2       ROOM ON SCREEN                      90354
         LTR   R15,R15       WAS THIS A 'FITS' TEST ONLY ?       90354
         BNZR  R9            NO; RETURN SCREEN FULL              90354
         L     R10,SPDATA     POINT TO NEW DATA POSITION
         B     4(,R9)         TAKE GOOD EXIT
         SPACE 2
SPEXMVC  MVC   0(0,R1),0(R15)     MOVE STUFF TO SCREEN
         AIF   ('&TRANS' EQ 'NO').SKPTRN2                       GP04234
SPEXTRN  TR    0(0,R1),0(R2)      MAKE SURE IT'S PREENTABLE     GP04234
.SKPTRN2 ANOP  ,                                                GP04234
SPCURR   DC    A(0)          START OF CURRENT LINE
SPDATA   DC    A(0)          START OF DATA PORTION OF CURRENT LINE
SPINDENT DC    H'0'          LENGTH OF INDENTATION
SPDATEL  DC    H'0'          LENGTH OF REMAINING LINE PORTION
SPLEND   DC    A(0)          END OF CURRENT LINE
         MEND
