         MACRO ,
         MACDEFXT &NAME,&TYPE                            ADDED  GP05250
.*
.*  (POOR) REPLACEMENT FOR D' ATTRIBUTE TO ALLOW ASSEMBLY UNDER ASM XF
.*
         GBLC  &ZZEXTNM(100) NAMES OF PREVIOUSLY DEFINED EXTERNALS
         GBLA  &ZZEXTFG      0 ON ERROR; 1 IF ADDED; 2 IF OLD
         GBLA  &ZZEXT##      NUMBER OF EXTERNALS DEFINED
.*
         LCLC  &WHAT   TYPE: EXTRN, WXTRN, OR 'OTHER' (NAME SAVED ONLY)
         LCLA  &I,&J
&ZZEXTFG SETA  0             SET ERROR
         AIF   ('&NAME' EQ '').MEND    NULL SUBSTITUTE?
         AIF   ('&TYPE' EQ 'OTHER').TYPOK
&WHAT    SETC  'WXTRN'       MOST FREQUENT ?
         AIF   ('&TYPE' EQ 'WXTRN').TYPOK
&WHAT    SETC  'EXTRN'       DEFAULT
         AIF   (T'&TYPE EQ 'O' OR '&TYPE' EQ 'EXTRN').TYPOK
         MNOTE 8,'MACDEFXT: UNSUPPORTED TYPE &TYPE - NOT EXRTN, WXTRN'
.TYPOK   AIF   (&I GE &ZZEXT##).NEW
&I       SETA  &I+1
         AIF   ('&ZZEXTNM(&I)' NE '&NAME').TYPOK
&ZZEXTFG SETA  2             SET OLD
         MEXIT ,             PREVIOUSLY DEFINED - NO ACTION
.*
.FULL    MNOTE 4,'MACDEFXT: NAME TABLE FULL - NAME NOT SAVED'
         MEXIT ,             PREVIOUSLY DEFINED - NO ACTION
.*
.NEW     AIF   ('&TYPE' EQ 'OTHER').JUSTADD
         &WHAT &NAME
.JUSTADD AIF   (&ZZEXT## GT 99).FULL
&ZZEXT## SETA  &ZZEXT##+1
&ZZEXTNM(&ZZEXT##) SETC '&NAME'   REMEMBER THE NAME
&ZZEXTFG SETA  1             SET NEW
.MEND    MEND  ,             PREVIOUSLY DEFINED - NO ACTION
