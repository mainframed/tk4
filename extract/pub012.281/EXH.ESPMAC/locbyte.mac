         MACRO ,
&NM      LOCBYTE &TEXT,&WK=R14,&W2=R15,&BYTE=C' ',&LEN=,  ADDED GP03080*
               &END=                                            GP03199
.*--------------------------------------------------------------------*
.*                                                                    *
.*  LOCBYTE SETS UP REGISTERS FOR A SEARCH STRING FOR BYTE (SRST)     *
.*    OPERATION.                                                      *
.*  &TEXT     SPECIFIES THE ADDRESS OF THE TEXT STRING TO BE SEARCHED *
.*    USE EITHER LEN= OR END=                                         *
.*  &END      SPECIFIES THE TEXT END ADDRESS + 1                      *
.*  &LEN      SPECIFIES THE TEXT LENGTH; DEFAULT IS L'&TEXT           *
.*  &WK (14)  WORK REGISTER                                           *
.*  &W2 (15)  RESULT REGISTER (TEXT+LEN IF NOTHING FOUND)             *
.*  &BYTE     SELF-DEFINING TERM FOR SEARCH CHARACTER                 *
.*                                                                    *
.*  WHEN DONE,                                                        *
.*                                                                    *
.*         CONDITION CODE 1 (BH) NO MATCH FOUND                       *
.*         CONDITION CODE 2 (BL) MATCH FOUND; ADDRESS IN &W2          *
.*                                                                    *
.*  SAMPLE USE:                                                       *
.*                                                                    *
.*       LOCBYTE INBUFF      BUFFER, CARD IMAGE, ETC. BUFFER          *
.*       BH    NOTFOUND        NOTHING FOUND                          *
.*       BCTR  R15,0     E.G., SPACE TO LAST BYTE                     *
.*                                                                    *
.*--------------------------------------------------------------------*
         LCLC  &L,&LAB
&L       SETC  'L'''
&LAB     SETC  'ZZ'.'&SYSNDX'
&NM      MACPARM R0,&BYTE                                       GP03199
         MACPARM &WK,&TEXT   LOAD TEXT ADDRESS
         AIF   ('&END' EQ '').HAVELEN                           GP03199
         MACPARM &W2,&END    LOAD END ADDRESS+1                 GP03199
         AIF   ('&LEN' EQ '').DONELEN                           GP03199
         MNOTE 8,'LOCBYTE: END= AND LEN= ARE MUTUALLY EXCLUSIVE'
.HAVELEN MACPARM &W2,&LEN,NULL=&L&TEXT                          GP03199
         MACPARM &W2,(&WK),OP=AR,OPR=AR
.DONELEN ANOP  ,                                                GP03199
&LAB.LP  SRST  &W2,(&WK)                                        GP04234
         BO    &LAB.LP
.DONE    MEND  ,
