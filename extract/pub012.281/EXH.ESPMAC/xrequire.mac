         MACRO
&NM      XREQUIRE &OPTS,&B=EXCXCB,&MINLS=,&MINLN=               GP10206
         GBLC  &MACPLAB
.**********************************************************************
.*   EXHIBIT MACRO TO TEST (AND FAIL) IMPROPER MODULE INVOCATION     **
.*     (WAS NAMED RESTRICT)                                          **
.**********************************************************************
         LCLA  &I,&M
         LCLB  &TRUE,&F(36)                                     GP07007
         LCLC  &C,&J                                            GP10213
&M       SETA  N'&SYSLIST
&J       SETC  '&SYSNDX'
&I       SETA  0
&MACPLAB SETC  '&NM'
.*
.*  LOOP THROUGH POSITIONAL OPERANDS
.*
.LOOP    AIF   (&I GE &M).LEND
&I       SETA  &I+1
&C       SETC  '&SYSLIST(&I)'
         AIF   ('&C' EQ '').LOOP
.*
.*  CHECK FOR SYSTEM AND ENVIRONMENT
.*
&F(2)    SETB  (('&C' EQ 'PCP') OR &F(2))
&F(3)    SETB  (('&C' EQ 'MFT') OR &F(3))
&F(4)    SETB  (('&C' EQ 'MVT') OR &F(4))
&F(6)    SETB  (('&C' EQ 'MP') OR &F(6))
&F(7)    SETB  (('&C' EQ 'DAT') OR &F(7))
&F(17)   SETB  (('&C' EQ 'VM') OR &F(17))                        79194
.*
&F(9)    SETB  (('&C' EQ 'TSO') OR &F(9))                        *TSO*
&F(10)   SETB  (('&C' EQ 'NOTSO') OR &F(10))                     *TSO*
.*
&F(11)   SETB  (('&C' EQ 'DEBUG') OR &F(11))
&F(30)   SETB  (('&C' EQ 'DRDEBUG') OR &F(30))                  GP07007
.*
&F(12)   SETB  (('&C' EQ 'HASP') OR &F(12))
&F(12)   SETB  (('&C' EQ 'JES2') OR &F(12))                      89211
.*
.*  NOTE THAT THE ACCESS PROTECTION FUNCTIONS ARE NOW HANDLED BY AN
.*  OPTIONAL RACF INTERFACE (MODULE EXHASIGN), AND THE FUNCTIONS OTHER
.*  THAN KEY ARE DEPRECATED.
.*
.*  CON,CAN OR CON,PAS ALLOW ACCESS TO A SECONDARY, UNLOCKED CONSOLE
.*  UNL ALLOWS OPERATOR ACCESS TO AN UNLOCKED TERMINAL
.*  KEY REQWUIRES SYSTEM PRIVILEGES FOR ACCESS.
.*
&F(13)   SETB  (('&C' EQ 'CON') OR &F(13))
&F(14)   SETB  (('&C' EQ 'CAN') OR &F(14))                      GP15116
&F(14)   SETB  (('&C' EQ 'PAS') OR &F(14))
&F(15)   SETB  (('&C' EQ 'KEY') OR &F(15))
&F(16)   SETB  (('&C' EQ 'UNL') OR &F(16))
.*
&F(18)   SETB  (('&C' EQ 'GAME') OR ('&C' EQ 'GAMES') OR &F(18))
&F(19)   SETB  (('&C' EQ 'KEY0') OR &F(19))  ->WITH DEBUG ONLY  GP11260
.*
.*  NOTE THAT NUMERIC SIZES ARE DEPRECATED. USE MINLS AND MINLN
.*  INSTEAD, UNLESS THE FUNCTION CANNOT ACCEPT CUSTOM SIZES.
.*
&F(21)   SETB  (('&C' EQ '12') OR &F(21))
&F(22)   SETB  (('&C' EQ '24') OR &F(22))
&F(27)   SETB  (('&C' EQ '40') OR &F(27))
&F(28)   SETB  (('&C' EQ '80') OR &F(28))
&F(29)   SETB  (('&C' EQ '132') OR &F(29))                       89211
&F(31)   SETB  (('&C' EQ 'CRT') OR &F(31))                      GP13288
         AGO   .LOOP
.LEND    AIF   (NOT &F(31)).NO31                                GP13288
         MACPARM SQSIMFG,SQSICRT+SQSIFSC,OP=TM   REAL CRT?      GP13288
         MACPARM &B,MODE=ONE,OP=BNO,OPR=BNOR,NULL=EXCXCB        GP13288
.*
.*  CHECK FOR SYSTEM AND ENVIRONMENT
.*  FAIL IF SPECIFIED ENTRY MATCHED
.*
.NO31    ANOP  ,                                                GP13288
&TRUE    SETB (&F(2) OR &F(3) OR &F(4) OR &F(6) OR &F(7))
         AIF   (NOT &TRUE).SK1
         NEED  CVT
      MACPARM  WRKSYS,&F(2)*CVT1SSS+&F(3)*CVT2SPS+&F(4)*CVT4MS1+&F(6)*C*
               VT4MPS+&F(7)*CVT6DAT,OP=TM  TEST SYSTEM BITS
         MACPARM &B,MODE=ONE,OP=BNZ,OPR=BNZR,NULL=EXCXCB        GP10206
.*
.*   ACCESS AUTHORITY CHECKING:
.*   CON - IF ALONE, TESTS FOR SECONDARY CONSOLE (EXCONCAN)
.*         WITH CAN IS IGNORED
.*   KEY - TESTS FOR EXHPRIV FLAG (SYSTEMS)
.*   UNL - TESTS FOR OPERATOR UNLOCK
.*   CAN - TESTS FOR UNLOCKED SECONDARY CONSOLE
.*   PAS - ALIAS FOR CAN
.*     TESTS ARE ORED
.*
.SK1     AIF   (&F(14) OR &F(15) OR &F(16)).NOSUPT              GP15116
         AIF   (NOT &F(13)).NOSUPT                              GP15116
         MACPARM EXCPRIV,EXCONSOL,OP=TM CONTROL DESK CRT ?      GP15116
         MACPARM &B,MODE=ONE,OP=BZ,OPR=BZR,NULL=EXCXCB          GP13221
         AGO   .SK2          SKIP ACCESS TESTS                  GP15116
.NOSUPT  AIF   (NOT &F(15) AND NOT &F(16)).TEST14               GP15116
         MACPARM EXCPRIV,EXCPSYS+&F(16)*EXCAUTH,OP=TM           GP15116
         AIF   (&F(14)).DO14                                    GP15116
         MACPARM &B,MODE=ONE,OP=BZ,OPR=BZ,NULL=EXCXCB           GP15116
         AGO   .SK2                                             GP15116
.DO14    MACPARM ZZX&J.K,MODE=ONE,OP=BNZ,OPR=BNZ                GP15116
.TEST14  AIF   (NOT &F(14)).SK2                                 GP15116
         MACPARM  EXCPRIV,EXCONSOL+EXCONCAN,OP=TM CUST SUPPORT? GP15116
.N15     MACPARM &B,MODE=ONE,OP=BNO,OPR=BNOR,NULL=EXCXCB        GP15116
         AIF   (NOT &F(15) AND NOT &F(16)).SK2                  GP15116
ZZX&J.K  DS    0H .          BYPASS CON/CAN FOR KEY/UNL         GP15116
.*
.SK2     AIF   (NOT &F(12)).SKCRT
         MACPARM HASPHCT+1(3),ZEROES,OP=CLC
         MACPARM &B,MODE=ONE,OP=BE,OPR=BER,NULL=EXCXCB          GP10206
.SKCRT   AIF   (NOT &F(21) AND NOT &F(22)).SKCB
         MACPARM LNO+1,LN24+LN32,OP=TM . MORE THAN 15 LINES ?    78051
         AIF   (&F(22)).SKC2
         MACPARM &B,MODE=ONE,OP=BZ,OPR=BZR,NULL=EXCXCB          GP10206
         AGO   .SKCB
.SKC2    MACPARM &B,MODE=ONE,OP=BNZ,OPR=BNZR,NULL=EXCXCB        GP10206
.SKCB    AIF   (NOT &F(27) AND NOT &F(28) AND NOT &F(29)).SKOTH  89211
         MACPARM LSIZE+1,80,OP=CLI  40 OR 80 BYTE CRT ?          88249
         BC    2*&F(29)+8*&F(28)+4*&F(27),&B                     91364
        MACPARM 2*&F(29)+8*&F(28)+4*&F(27),&B,OP=BC,OPR=BCR,NULL=EXCXCB
.SKOTH   AIF   (NOT &F(11)).SKNDEB
         AIF   (NOT &F(9)).SKBOPT  SKIP COMBINED TEST            78216
         MACPARM OPTS,DEBUG+OPTSO,OP=TM DEBUG OR TSO ?           78216
         MACPARM &B,MODE=ONE,OP=BNZ,OPR=BNZR,NULL=EXCXCB        GP10206
         AGO   .SKTSO                                            78216
.SKBOPT  ANOP  ,                                                 78216
         MACPARM OPTS,DEBUG,OP=TM . RUNNING DEBUG MODE ?
         MACPARM &B,MODE=ONE,OP=BNZ,OPR=BNZR,NULL=EXCXCB        GP10206
.SKNDEB  AIF   (NOT &F(9) AND NOT &F(10)).SKTSO                  *TSO*
         MACPARM OPTS,OPTSO,OP=TM . RUNNING UNDER TSO ?          *TSO*
         AIF   (NOT &F(9)).NTTSO                                 *TSO*
         MACPARM &B,MODE=ONE,OP=BNZ,OPR=BNZR,NULL=EXCXCB        GP10206
         AGO   .SKTSO                                            *TSO*
.NTTSO   ANOP  ,                                                 *TSO*
         MACPARM &B,MODE=ONE,OP=BZ,OPR=BZR,NULL=EXCXCB          GP10206
.SKTSO   AIF   (NOT &F(17) AND NOT &F(18)).NOSS18                79194
         MACPARM SQSAVPO,SQSAFVM*&F(17)+SQSNGAM*&F(18),OP=TM     79194
         MACPARM &B,MODE=ONE,OP=BNZ,OPR=BNZR,NULL=EXCXCB        GP10206
.NOSS18  AIF   (NOT &F(19)).NOSAVP     NO KEY0 CHECK            GP11260
         MACPARM OPTS,DEBUG,OP=TM . RUNNING DEBUG MODE ?        GP11260
         MACPARM &B,MODE=ONE,OP=BZ,OPR=BZR,NULL=EXCXCB          GP11260
         TESTAUTH KEY=YES,FCTN=0,RBLEVEL=1  KEY ZERO ?          GP11260
         LTR   R15,R15                                          GP11260
         MACPARM &B,MODE=ONE,OP=BNZ,OPR=BNZR,NULL=EXCXCB        GP11260
.NOSAVP  AIF   (NOT &F(30)).SS30
         TM    DRFLAG,DRDEBUG  DASD FUNCTION IN DEBUG MODE ?    GP07007
         MACPARM &B,MODE=ONE,OP=BNZ,OPR=BNZR,NULL=EXCXCB        GP10206
.SS30    AIF   (T'&MINLS EQ 'O').SS31                           GP10206
         CLI   LSIZE+1,&MINLS                                   GP10206
         MACPARM &B,MODE=ONE,OP=BL,OPR=BLR,NULL=EXCXCB          GP10206
.SS31    AIF   (T'&MINLN EQ 'O').SS32                           GP10206
         CLI   LNO+1,&MINLN                                     GP10206
         MACPARM &B,MODE=ONE,OP=BL,OPR=BLR,NULL=EXCXCB          GP10206
.SS32    ANOP  ,                                                GP10206
         MEND  ,
