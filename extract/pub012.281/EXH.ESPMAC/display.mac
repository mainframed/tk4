         MACRO
&NM      DISPLAY &MOD,&FL,&LL,&FC,&LC,&TIM,&W=                   88249
         GBLC  &ID
         GBLA  &DSPCTR,&DSPWID                                   88249
         GBLC  &SYSTEM                                            *TSM*
         LCLA  &IDFLAG
         LCLA  &LAST                                              *TSM*
&LAST    SETA  7             ASSUME OS/360                        *TSM*
         AIF   ('&SYSTEM' EQ 'MFT' OR '&SYSTEM' EQ 'MVT').NOTVS   *TSM*
&LAST    SETA  8             SET FOR OS/VS1-OS/VS2                *TSM*
.NOTVS   AIF   ('&W' EQ '').WNONEW                               88249
&DSPWID  SETA  &W            SET NEW WIDTH                       88249
.WNONEW  AIF   (&DSPWID NE 0).WHAVE                              88249
&DSPWID  SETA  80            SET DEFAULT WIDTH                   88249
.WHAVE   AIF   ('&MOD' EQ '' OR '&FL' EQ '' OR '&LL' EQ '' OR '&FC' EQ *
               '' OR '&LC' EQ '' OR '&TIM' EQ '').BOO
         AIF   (K'&MOD NE 4).BOO
&DSPCTR  SETA  &DSPCTR+1
         AIF   ('&MOD'(1,2) NE 'ZZ').CNTR
         AIF   ('&ID' NE 'YES').NOID
&IDFLAG  SETA  1
.NOID    AIF   (&DSPCTR EQ &LAST+1).ZZ8                           *TSM*
.MORE    DC    0H'0',CL4'Z@@@',AL1(0,0,0,0,255,255) .  OMITTED ENTRY
&DSPCTR  SETA  &DSPCTR+1
         AIF   (&DSPCTR LE &LAST).MORE                            *TSM*
         MNOTE *,'ROOM FOR MORE ENTRIES'
.ZZ8     ANOP
&NM      DC    0H'0',CL4'&MOD',AL1(0,&IDFLAG,255,255,0,0)  .   END OF
&DSPCTR  SETA  0             FOR NEXT TABLE
         AGO   .MEXIT
.MANY    MNOTE 8,'TOO MANY ENTRIES'
         AGO   .MEXIT
.BOO     MNOTE 8,'INVALID PARAMETER'
         AGO   .MEXIT
.CNTR    AIF   (&DSPCTR GT &LAST).MANY                            *TSM*
&NM      DC    0H'0',CL4'&MOD',AL2(&FL*&DSPWID+&FC-&DSPWID-1,&LL*&DSPWI*
               D+&LC-&DSPWID-1),AL1(&TIM,&TIM)  DISPLAY ENTRY    88249
.MEXIT   MEND
