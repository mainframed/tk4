         MACRO ,
&NM      ARM   &ACT,&AR=
.*--------------------------------------------------------------------*
.*                                                                    *
.*   THIS MACRO SETS AR MODE ON OR OFF, SETS MATCHING GLOBAL,         *
.*   AND OPTIONALLY LOADS/STORES CONTIGUOUS ACCESS REGISTERS          *
.*                                                                    *
.*   ARM  ON                 BEGIN ACCESS REGISTER MODE               *
.*   ARM  OFF                END ACCESS REGISTER MODE                 *
.*                                                                    *
.*     OPTIONAL:  AR=ADDR          LAM/STAM AR0,AR15,ADDR             *
.*     OPTIONAL:  AR=(R,ADDR)      LAM/STAM AR0,AR15,ADDR             *
.*     OPTIONAL:  AR=(RS,RT,ADDR)  LAM/STAM RS,RT,ADDR                *
.*                                                                    *
.*--------------------------------------------------------------------*
         GBLB  &MVSXA                                           GP04234
         LCLC  &R1,&R2,&ADDR
         LCLB  &EXPAR
         LCLA  &K
&K       SETA  N'&AR
         AIF   (&K EQ 0).NOK
&EXPAR   SETB  1
         AIF   (&K EQ 1).OK1
         AIF   (&K EQ 2).OK2
         AIF   (&K EQ 3).OK3
&EXPAR   SETB  0
  MNOTE 8,'ARM: TOO MANY OPERANDS AR=&AR'
         AGO   .NOK
.OK1     ANOP  ,
&R1      SETC  'R0'
&R2      SETC  'R15'
&ADDR    SETC  '&AR(1)'
         AGO   .NOK
.OK2     ANOP  ,
&R1      SETC  '&AR(1)'
&R2      SETC  '&AR(1)'
&ADDR    SETC  '&AR(2)'
         AGO   .NOK
.OK3     ANOP  ,
&R1      SETC  '&AR(1)'
&R2      SETC  '&AR(2)'
&ADDR    SETC  '&AR(3)'
.NOK     AIF   (&MVSXA).ENOUGH                                  GP04234
&NM      MACPARM MODE=LBL                                       GP04234
         MEXIT ,                                                GP04234
.ENOUGH  AIF   ('&ACT' EQ 'ON' OR '&ACT' EQ 'S' OR '&ACT' EQ 'SEC' OR  *
               '&ACT' EQ '512' OR '&ACT' EQ 'X''200''').ARON
         AIF   ('&ACT' EQ 'OFF' OR'&ACT' EQ 'P' OR '&ACT' EQ 'PRI' OR  *
               '&ACT' EQ '0' OR '&ACT' EQ 'X''0''').AROF
 MNOTE 8,'ARM: INVALID ACTION &ACT; SPECIFY ON OR OFF'
.ARON    AIF   (NOT &EXPAR).NOLAM
&NM      MACPARM &R1,&R2,&ADDR,OP=LAM,MODE=THREE
.NOLAM   MACPARM 512,OP=SAC,MODE=ONE
         SYSSTATE ASCENV=AR
         MEXIT ,
.AROF    ANOP  ,
&NM      MACPARM 0,OP=SAC,MODE=ONE
         SYSSTATE ASCENV=P
         AIF   (NOT &EXPAR).NOSTAM
         MACPARM &R1,&R2,&ADDR,OP=LAM,MODE=THREE
.NOSTAM  MEND  ,
