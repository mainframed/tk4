         MACRO ,
&NM      #PRT  &AD,&LN,&TYPE=VCON,&DEV=0,&CC=YES
         GBLB  &ZZ#PRTF
         GBLC  &MACPLAB,&ZZ#PRTN(10),&ZZ#PRTI(10)
.**********************************************************************
.*                                                                    *
.*   #PRT GENERATES CALLS TO SUBROUTINE SUBPRT FOR PRINTING ON UP TO  *
.*   EIGHT OPEN PRINT STREAMS (DEV=1 OR DEV=(3,7)...                  *
.*     MULTIPLE PRINTERS ARE VALID EXCEPT WITH TYPE=OPEN              *
.*   PRINT DDs ARE ASSOCIATED WITH #PRTWRK MACROS                     *
.*                                                                    *
.**********************************************************************
         LCLA  &I,&J,&K,&N
         LCLB  &B0,&B1,&B2,&B3,&B4,&B5,&B6,&B7
         LCLC  &PT,&PD,&OP1,&L,&M,&DV,&FN
&MACPLAB SETC  '&NM'
&L       SETC  'L'''
&M       SETC  '&SYSNDX'
&DV      SETC  '&DEV'
&OP1     SETC  '&AD'
&FN      SETC  '0'
         AIF   ('&CC' NE 'NO').YESCC
&FN      SETC  '128'
.YESCC   AIF   (T'&DEV EQ 'N').DEVASIS
         AIF   (N'&DEV EQ 1).DEVASIS
.DEVLOOP AIF   (&J GE N'&DEV).DEVDONE
&J       SETA  &J+1
         AIF   ('&DEV(&J)' GE '1' OR '&DEV(&J)' LE '8').DEVOK
         MNOTE 8,'#PRT: DEV=&DEV(&J) UNACCEPTABLE'
.DEVOK   ANOP  ,
&B0      SETB  (&B0 OR ('&DEV(&J)' EQ '8'))
&B1      SETB  (&B1 OR ('&DEV(&J)' EQ '7'))
&B2      SETB  (&B2 OR ('&DEV(&J)' EQ '6'))
&B3      SETB  (&B3 OR ('&DEV(&J)' EQ '5'))
&B4      SETB  (&B4 OR ('&DEV(&J)' EQ '4'))
&B5      SETB  (&B5 OR ('&DEV(&J)' EQ '3'))
&B6      SETB  (&B6 OR ('&DEV(&J)' EQ '2'))
&B7      SETB  (&B7 OR ('&DEV(&J)' EQ '1'))
         AGO   .DEVLOOP
.*
.DEVDONE ANOP  ,
&DV      SETC  '128*&B0+64*&B1+32*&B2+16*&B3+8*&B4+4*&B5+2*&B6+&B7'
.DEVASIS AIF   (&ZZ#PRTF).NOT1ST
&ZZ#PRTN(01) SETC 'VCON'
&ZZ#PRTI(01) SETC '0'
&ZZ#PRTN(02) SETC 'SPACE'
&ZZ#PRTI(02) SETC '1'
&ZZ#PRTN(03) SETC 'SKIP'
&ZZ#PRTI(03) SETC '1'
&ZZ#PRTN(04) SETC 'KEEP'
&ZZ#PRTI(04) SETC '2'
&ZZ#PRTN(05) SETC 'RESERVE'
&ZZ#PRTI(05) SETC '2'
&ZZ#PRTN(06) SETC 'OPEN'
&ZZ#PRTI(06) SETC '3'
&ZZ#PRTN(07) SETC 'CLOSE'
&ZZ#PRTI(07) SETC '4'
&ZZ#PRTN(08) SETC 'TEXT'
&ZZ#PRTI(08) SETC '5'
&ZZ#PRTN(09) SETC 'PRINTF'
&ZZ#PRTI(09) SETC '5'
&ZZ#PRTN(10) SETC 'BCON'
&ZZ#PRTI(10) SETC '6'
&ZZ#PRTF SETB  1             FIRST TIME STUFF DONE
.NOT1ST  AIF   (T'&AD NE 'O').HAVEAD
         AIF   ('&TYPE' EQ 'CLOSE').HAVEAD
         MNOTE 8,'#PRT: FIRST OPERAND REQUIRED (ADDR/TEXT/PRTWRK)'
         MEXIT ,
.HAVEAD  AIF   (&I GE 10).BADTYPE
&I       SETA  &I+1
         AIF   ('&TYPE' NE '&ZZ#PRTN(&I)').HAVEAD
&PT      SETC  '&ZZ#PRTI(&I)'
         AIF   ('&TYPE' EQ 'CLOSE').HAVTYPE
         AIF   ('&AD'(1,1) NE '''').HAVTYPE
         AIF   ('&PT' EQ '0' OR '&PT' EQ '5' OR '&PT' EQ '6').DEFTEXT
         MNOTE 4,'#PRT: TYPE=&TYPE INVALID WITH LITERAL STRING'
&PT      SETC  '6'           DO LITERAL AS BCON
.BADTYPE AIF   ('&AD'(1,1) EQ '''').DEFTEXT
         MNOTE 8,'#PRT: TYPE=&TYPE INVALID'
         MEXIT ,
.*
.DEFTEXT ANOP  ,
&PT      SETC  '6'           DO LITERAL AS BCON
         MACPARM ZZZ&M.C,OP=B,MODE=ONE
ZZZ&M.L  BCON  &AD           DEFINE LITERAL WITH LENGTH
&MACPLAB SETC  'ZZZ'.'&M'.'C'
&OP1     SETC  'ZZZ'.'&M'.'L'
.*
.HAVTYPE AIF   ('&PT' NE '5').NOTLEN
         MACPARM R0,&LN,NULL=&L&AD     GET TEXT LENGTH
         MACPARM R0,16,OP=SLL          LEFT JUSTIFY
         MACPARM R0,3,=AL1(&PT,&DV),OP=ICM,MODE=THREE  SET FUNC/DEV
         AGO   .SETADD
.*
.NOTLEN  MACPARM R0,=AL1(0,0,&PT,&DV),OP=L
.SETADD  AIF   ('&FN' EQ '' OR '&FN' EQ '0').SETFUN             GP09275
         MACPARM R0,8,=AL1(&FN),OP=ICM,MODE=THREE  SET FUNCTION CODE
.SETFUN  MACPARM R1,&OP1,NULL=SKIP     GET STORAGE ADDRESS
         MACPARM R15,=V(SUBPRT),OP=L    GET SUBROUTINE ADDRESS
         MACPARM R14,R15,OP=BALR,OPR=BALR  CALL SUBROUTINE
.MEND    MEND ,
