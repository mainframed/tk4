         MACRO ,
&NM      SUBRET &RREG,&TYPE=BR,&RS=,&RE=,&OR=8,&VECT=0
         GBLC  &ZZSUBR1,&ZZSUBR2                                GP00020
         LCLC  &RR                                              GP00020
.*  SUBRET ENDS A MATCHING SUBHEAD USING SUBROUTINE
.*    &RREG SPECIFIES THE RETURN REGISTER (DEFAULT IS R14)
.*    &TYPE MAY SPECIFY POP (RELOAD, NO BRANCH), BR FOR BRANCH,
.*      OR BSM FOR A BSM 0,&&REG RETURN
.*    &RS AND &RE MAY OVERRIDE THE RESTORE REGISTERS, BUT NOTE THAT
.*      THIS WILL WORK ONLY IF RE IS LESS THAN THE SUBHEAD SECOND REG.
.*    &VECT SPECIFIES AN INTEGER RETURN OFFSET (CALLER'S RETURN + N)
.*
.*  SUBHEAD BEGINS A SUBROUTINE, USING A SAVE AREA STACK
.*    THE MACRO MUST PHYSICALLY PRECEDE THE MATCHING SUBRET
.*    MACRO FOR CORRECT REGISTER RESTORE.
.*  BY SPECIFYING &F AND &L APPROPRIATELY, (E.G., R2,R0),
.*    ALTERED REGISTER VALUES (IN THIS CASE, R1) CAN BE RETURNED
.*    TO THE CALLER
&RR      SETC  '&RREG(1)'                                       GP00020
         AIF   ('&RR' NE '').HAVER                              GP00020
&RR      SETC  'R14'                                            GP00020
.HAVER   AIF   ('&RS' EQ '').HAVE1                              GP00020
&ZZSUBR1 SETC  '&RS'                                            GP00020
.HAVE1   AIF   ('&RE' EQ '').HAVE2                              GP00020
&ZZSUBR2 SETC  '&RE'                                            GP00020
.HAVE2   ANOP  ,                                                GP00020
&NM      SH    R13,=Y(LOCSAVE1-LOCSAVE)  POP THE STACK
         LM    &ZZSUBR1,&ZZSUBR2,&OR.(R13)  RESTORE CALLER'S REGISTERS
         AIF   ('&RR' EQ 'POP').MEND                            GP00020
         AIF   ('&TYPE' EQ 'POP' OR '&TYPE' EQ 'NO').MEND
         AIF   ('&TYPE' EQ 'BSM').BSM
         AIF   ('&VECT' EQ '0').BR
         B     &VECT.(,&RR)  VECTORED RETURN
         MEXIT ,
.BR      BR    &RR           RETURN TO CALLER
         MEXIT ,
.BSM     AIF   ('&VECT' EQ '0').BSM0
         AL    &RR,=A(&VECT) ADJUST RETURN ADDRESS
.BSM0    RET31 &RR           RETURN IN CALLER'S MODE
.MEND    MEND  ,
