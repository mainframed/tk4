         MACRO ,
       MAPCOMM &DSECT=
         COPY  OPTIONS
         AIF   ('&DSECT' NE 'NO').DS
EXHBCOMM DS    0D            DEFINE FOR EXHATALK ONLY
         AGO   .COMM
.DS      ANOP  ,
EXHBCOMM DSECT ,
.COMM    USING EXHBCOMM,R8
         USING EXHBWORK,R11
         USING EXHBSQSP,R13
         NEED  WORK,SQSP
         SPACE ,
EXCNEW   BAL   R14,BLANKER   CLEAR SCREEN                       GP99018
EXCYES   LA    R2,YES        SET FOR STANDARD RESPONSE
         LA    R3,L'YES      AND LENGTH
EXCSLOP  XPAGE (R2),(R3)     MOVE MESSAGE TO BUFFER AND WRITE IT OUT
         SPACE 2
EXCLOOP  NI    EXCPRIV,255-EXCPLOOP     PREVENT WRONG WAIT TIME
         BAL   R9,EXCPEWRD   WAIT, THEN READ INPUT INTO STANDARD BUF
         B     EXCEXIT       EXIT IF NO INPUT
         SPACE 1
EXCIN    DS    0H            INPUT ANALYSIS
         AIF   ('&CRT' NE '3270').XINP
         CLI   REPLY,C' '    ALREADY DID ATTN CHECK ?
         BH    EXCINP        YES, SKIP SECOND TRY
         ICM   R15,15,SQXINAN  INPUT ANALYSIS?                  GP04234
         BZ    EXCINOAN      NO                                 GP04234
         BALR  R14,R15       INVOKE IT                          GP04234
EXCINOAN LA    R1,SQPFK      GET PF KEY DEFINITIONS
         LA    R15,(SQPAKE-SQPFK)/6    GET NUMBER
EXCINPL  CLC   0(1,R1),REPLY     THIS ONE ?
         BE    EXCINPT       YES, MOVE IT
         LA    R1,6(,R1)     ELSE TRY NEXT
         BCT   R15,EXCINPL   REPEAT; IF LAST, POINTS TO CL5' '
         TM    SQSAVPR,EXCUNFG2  CRT RESTRICTED TO KEYS ?
         BZ    EXCINPT       NO, PROCEED
         TM    EXCPRIV,EXCAUTH+EXCPSYS+EXCONCAN  UNLOCKED BEFORE ?
         BZ    EXCSYNT       NO; FAIL ENTER KEY
EXCINPT  MVC   REPLY(5),1(R1)     MOVE EQUIVALENT INTO REPLY
.XINP    ANOP  ,
.* 8 BYTE OVERLAY FOR EXTENDED COMMAND SCAN
EXCINP   LA    R5,REPLY      GET INPUT BUFFER
         LA    R6,REPLAST     GET LAST BYTE ADDR.
         MVC   INVOKE(8),EXCNMPAT     MOVE COMMON BLDL MASK/NAME
         LA    R4,EXCOPQ     GET TABLE OF FUNCTIONS IN THIS MODULE
EXCPEQU  BAL   R14,VERBLOOK  IS IT AN IMMEDIATE FUNCTION ?      GP99018
           B   EXCXC         NOT IMMEDIATE - PROCESS FURTHER
         LR    R1,R4         SAVE VERB ADDR. FOR EXC@$CM
         B     0(R8,R15)     BRANCH TO IMMEDIATE ROUTINE
         SPACE 1
EXCSEQU  BCTR  R5,0          ONE EQU IS ESCAPE TO COMMAND MODE  GP10213
         MVI   0(R5),C' '    ELIMINATE IT                       GP10213
         LA    R4,EXCOP      USE NORMAL TABLE                   GP10213
         B     EXCPEQU       AND RESUME SCAN                    GP10213
         SPACE 1
EXC@TST  LA    R4,EXCXCT@    GET SYSTEM FUNCTION COMMAND TABLE
         TM    EXCPRIV,EXCPSYS      PRIVILEGED USER (KEY MODE) ?
         BNZ   EXC@$CM       DO COMMON CODE FOR SPECIAL XCTLS   GP15302
         LA    R2,AUTHERR    GET ERROR MESSAGE                  GP15302
         LA    R3,L'AUTHERR                                     GP15302
         B     EXCSLOP       AND WRITE IT OUT                   GP15302
         SPACE 2
EXC8BLD  OC    ASPVT+1(3),ASPVT+1  IS ASP AVAILABLE ?
         BZ    EXCSYNT       NO, TOO BAD
*        LA    R4,EXCXCT8    ASP COMMAND TABLE                   83345
         LA    R1,EXCOPASP     USE '8' FOR NAME
         B     EXC@$CM-4     DO COMMON CODE FOR SPECIAL XCTLS    83345
EXC$BLD  OC    HASPHCT+1(3),HASPHCT+1   IS HASP UP ?
         BZ    EXCSYNT       NO, TOO BAD
         LA    R1,EXCOPHSP   POINT TO HASP/JES2 COMMAND          83345
         LA    R4,EXCXCT$    HASP COMMAND TABLE
EXC@$CM  MVC   INVOKE+4(1),4(R1)      MOVE SPECIAL CHARACTER FOR NAME
         B     EXCCBLV       CHECK FOR COMMAND IN TABLE
EXCXC    CLI   0(R5),C' '      ANY INPUT AT ALL ?
         BE    EXCEXIT        NO, TREAT AS TERMINATION REQUEST
         LA    R4,EXCXCTT      GET STANDARD LOOKUP TABLE
EXCCBLV  BAL   R14,VERBLOOK  IS COMMAND IN TABLE ?              GP99018
          B    EXCDFLT       NO, USE COMMAND ITSELF FOR XCTL NAME
EXCBLDL  MVC   INVOKE+5(3),1(R4)  MOVE SPECIAL MODULE NAME
EXCXCTL  MVI   INVOKE+22,0   ZERO THE LKED ATTRIBUTE BYTE POSITION
*        BLDL  0,BLDL        BLDL
*OLD*    XR    R1,R1         NO DCB
*OLD*    LA    R0,BLDL       PASS BLDL LIST
*OLD*    SVC   18            DO BLDL
         LA    R0,BLDL       POINT TO BLDL REQUEST              GP04234
         BAL   R14,SQBLDLS   INVOKE BLDL SERVICE                GP04234
EXCXCTST TM    INVOKE+22,EXHF1ATT  MODULE RENT AND EXECUTABLE ?  82157
         BNO   EXCXCB        NO, TELL USER
         MVC   NEWMODSZ+1(3),EXHFSIZ-EXHBWENT+INVOKE COPY MODULE SIZE
         L     R1,EXCSTGMX     GET AVAILABLE STORAGE            GP04234
         AL    R1,EXCSZMOD     ADD CURRENT MODULE SIZE          GP02230
         CLI   INVOKE+4,C'X'   XCTL REQUEST ?
         BNL   EXCXCTLB      YES-JUST OVERLAY; DON'T ADD EXTRA   79215
         AL    R1,EXCSZLOD   ADD SIZE OF MODULE TO BE DELETED   GP02230
         L     R0,EXCSTGLN   GET SUBPOOL/LENGTH OF STORAGE      GP04234
         N     R0,EXWMSK24   KILL SUBPOOL                       GP04234
         ALR   R1,R0         ADD SIZE OF FREEMAIN TO BE DONE    GP02230
EXCXCTLB CL    R1,NEWMODSZ   WILL NEW ONE FIT ?                 GP04234
         BNL   EXCXCXC       YES, DO XCTL                       GP02230
EXCXCB   BAL   R14,ONUNIT      FORCE EXIT                       GP99018
         L     R2,MSGAD      GET MESSAGE AREA
         MVC   0(L'EXCUNVC,R2),EXCUNVC    MOVE UNAVAILABLE MSG
         MVC   SMILOFF+1(8,R2),INVOKE      ADD VARIABLE TEXT
         LA    R3,L'EXCUNVC        SET MSG LENGTH
         XC    REPLY,REPLY  CREAM INVALID COMMAND               GP02232
         BAS   R9,EXCWAIT2  AND MAKE SURE ECBS GET RESET        GP02232
         B     EXCSLOP       WRITE IT OUT                       GP02232
         SPACE 1
EXCXCXC  BAL   R14,ONUNITB   TAKE USER EXIT, IF ANY             GP99018
         ST    R5,REPPTR     SAVE ADDRESS OF BYTE AFTER REPLY VERB
         L     R1,EXCSTGMX    GET AVAILABLE STORAGE             GP04234
         AL    R1,EXCSZMOD    ADD CURRENT MODLE SIZE            GP02230
         MVC   EXCSZMOD+1(3),EXHFSIZ-EXHBWENT+INVOKE  MOVE NEW SIZE
         SL    R1,EXCSZMOD    NEW FREE SIZE                     GP02230
         ST    R1,EXCSTGMX   SAVE FOR LOAD AND GETMAIN REQUESTS GP04234
         LA    R10,INVOKE    GET BLDL ENTRY ADDRESS
         B     XCTL          GO TO DO XCTL TO REQUESTED MODULE
         SPACE 1
EXCSVCT  TM    EXCPRIV,EXCAUTH     USER IN KEY OR UNLOCK MODE ?
         BZ    EXCSYNT       NO, BOO
EXCSVC   LA    R1,EXCMGCR      SET FOR OS COMMAND PROCESSOR MODULE
EXCSVCM  MVC   INVOKE(8),0(R1)      MOVE XCTL MODULE NAME
         B     EXCXCTL       VERIFY AND XCTL
         SPACE 1
EXCSYNT  LA    R2,SYNTERR    GET ERROR MESSAGE
         LA    R3,L'SYNTERR
         B     EXCSLOP       AND WRITE IT OUT
         SPACE 2
EXCDFLT  LR    R1,R5         GET FIRST INPUT BYTE
         LA    R2,9          STOP SCAN AFTER 9 BYTES
EXCDFL1  CLI   0(R1),C' '     HIT TRAILING BLANK YET ?
         BE    EXCDFL2       YES, CHECK LENGTH
         LA    R1,1(,R1)     BUMP TO NEXT BYTE
         BCT   R2,EXCDFL1    TRY AGAIN
         B     EXCSYNT       MORE THAN 8 BYTES IN LENGTH - BOO
EXCDFL2  ST    R1,REPPTR     SAVE FIRST BYTE AFTER VERB
         ST    R5,DB         SAVE FIRST BYTE OF VERB
         LA    R1,8          USE TO GET LENGTH - 1
         SR    R1,R2         LENGTH - 1
         BM    EXCSYNT       TOO SHORT ?
         CH    R1,H2         IS IT MORE THAN THREE BYTES ?
         BH    EXCTRUN       YES, USE FIRST THREE BYTES ONLY
         EX    R1,EXCBNMV    MOVE INTO BUFFER, WITH TRAILING 999
         L     R5,REPPTR     GET NEXT BYTE ADDRESS FOR PASSING
         B     EXCXCTL       NAME TO BLDL
         SPACE 1
EXCTRUN  L     R5,REPPTR     GET NEW ENTRY BYTE ADDRESS
         L     R4,DB         GET FIRST BYTE OF VERB
         BCTR  R4,0          FOR MOVE
         B     EXCBLDL       GO TO BLDL
         SPACE 1
EXCSOP   LA    R2,OP2BAD     WRITE BAD OPERAND MESSAGE
         LA    R3,L'OP2BAD    GET LENGTH
         B     EXCSLOP       TELL USER
         SPACE 2
.*   EXCCMLK PERFORMS A LOOKUP, AND BRANCHES ACCORDINGLY.
.*   1) MATCH ON A USER BTAB ENTRY (POINTED TO BY R4) BRANCHES
.*   2) NO MATCH - FALLS THROUGH TO EXCCMD
.*                                                              GP15151
 DEBPRT **********************DEBUG********************
EXCCMLK  XLOOK T=(R4)                                           GP15151
 DEBPRT **********************DEBUG********************
         SPACE 1
.*   EXCCMD IS AN ADDITION MEANT TO COMBINE MULTIPLE TESTS IN MANY
.*     MODULES.
.*   1) REPLY EXHAUSTED - GO TO EXCNEW
.*   2) QUIT, EXIT, PA1 - GO TO EXCNEW
.*   3) PF3/PF15 - GO TO EXCNEW IF REPLY IS NULL
.*   4) PF3/PF15 - TREAT AS $JCL IF REPLY IS NOT NULL
.*   5) LEADING = SIGN - BLANK AND RETRY
.*   6) NO MATCH - TREAT AS COMMAND INPUT
.*                                                              GP15151
EXCCMD   XLOOK T=EXCCMDTB    CHECK INPUT                        GP15151
         CLI   0(R5),C' '    INPUT EXHAUSTED?                   GP15151
         BH    EXCIN           NO; TREAT AS COMMAND             GP15151
         B     EXCNEW          ELSE PROMPT FOR INPUT
         SPACE 1
EXCQPF3  CLC   BLANKS,0(R5)  ANY INPUT?                         GP15151
         BE    EXCYES          NO; PROMPT FOR IT                GP15151
         SPACE 1                                                GP15151
EXCCMDBK MVI   0(R5),C' '    IGNORE EQUAL SIGN (CMD REDIRECT)   GP15154
         B     EXCCMD          AND RESCAN                       GP15154
         SPACE 1                                                GP15151
EXCCMDTB BTAB  *PF3,EXCQPF3,BASE=*                              GP15151
         BTAB  *PF15,EXCQPF3      ANY REPLY?                    GP15151
         BTAB  *PA1,EXCNEW   PA1 - SOLICIT INPUT                GP15151
         BTAB  '=',EXCCMDBK  FORCED COMMAND - BLANK OUT         GP15154
         BTAB  *END                                             GP15151
         SPACE 3
*   EXCLOAD IS ENTERED TO LOAD A SUBROUTINE WITH A NAME OF EXHCS...
*   THE LAST THREE BYTES OF THE DESIRED MODULE NAME MUST BE MOVED
*   TO EXCINVOK+5. A BLANK OR LOWER IN EXCINVOK+5 SHOWS 'DELETE'
*   REGISTER 0 RETURNS ENTRY POINT IF LOADED, ELSE = 0
*   ANY PRIOR LOAD IS FREED.  A PRIOR GETMAIN SHOULD HAVE BEEN
*   FREED TO PREVENT 804 ABEND ON LOAD. AN INCORRECT SEQUENCE OF
*   GETMAIN AND LOADS MAY RESULT IN STORAGE FRAGMENTATION, THUS
*   INVALIDATING THE STORAGE SIZE CHECK. NORMAL SEQ. SHOULD BE
*   LOAD, GETMAIN, FREEMAIN, DELETE, LOAD, GETMAIN, ETC.
*
EXCLOAD  CLI   EXCEPLO,C' '    WAS THERE A PRIOR LOAD ?
         BE    EXCLOD1       NO
DEBUG9   DELETE EPLOC=EXCEPLO  FREE IT UP
         L     R1,EXCSTGMX   GET AVAILABLE STORAGE              GP04234
         AL    R1,EXCSZLOD   GET PREV. LOADED SIZE              GP02230
         MVC   EXCSZLOD,ZERO     CLEAR CURRENT SIZE             GP04234
         ST    R1,EXCSTGMX    SET AVAILABLE STORAGE             GP02230
         MVI   EXCEPLO,C' '
EXCLOD1  MVC   EXCINVOK(4),EXCNMPAT     MOVE BASIC NAME         GP02232
         MVI   EXCINVOK+4,C'S'     MAKE SUBROUTINE
         MVI   EXCINVOK+22,0     ZERO LKED ATTRIBUTES
         CLI   EXCINVOK+5,C' '     DELETE ONLY ?
         BNH   EXCLOD2       YES, SKIP LOAD
*        BLDL  0,EXCSBLDL    NO, PROCEED WITH THE BLDL
*OLD*    XR    R1,R1         NO DCB
*OLD*    LA    R0,EXCSBLDL   PASS BLDL LIST
*OLD*    SVC   18            DO BLDL
         LA    R0,EXCSBLDL   REQUEST LIST FOR LOAD              GP04234
         BAL   R14,SQBLDLS   INVOKE BLDL SERVICE                GP04234
EXCLOD2  XR    R0,R0         PROVISIONALLY SIGNAL NO LOAD
         TM    EXCINVOK+22,EXHF1ATT   MODULE RENT + EXEC ?       82157
         BNOR  R9            NO, RETURN WITH GPR 0 = 0
         L     R1,EXCSTGMX   GET AVAILABLE STORAGE SIZE         GP04234
         CLC   EXCSTGMX+1(3),EXHFSIZ-EXHBWENT+EXCINVOK   WILL IT FIT ?
         BLR   R9            NO, RETURN WITH ZERO CODE          GP02230
         MVC   EXCSZLOD+1(3),EXHFSIZ-EXHBWENT+EXCINVOK  NEW SIZE LOADED
         SL    R1,EXCSZLOD    GET NEW STORAGE AVAILABLE         GP02230
         ST    R1,EXCSTGMX   AND SAVE FOR GETMAIN, IF ANY       GP02230
EXCLOD5  MVC   EXCEPLO,EXCINVOK     SAVE MODULE NAME             85317
         LA    R0,EXCINVOK    GET BLDL ENTRY
DEBUG10  DS    0H            LOAD INTERCEPT                     GP99018
*        LOAD  DE=(0)        LOAD IT                            GP99018
         SR    R1,R1         NO DCB ADDRESS                     GP99018
         LNR   R0,R0         SIGNAL DE MODE                     GP99018
         SVC   8             LOAD                               GP99018
         ST    R0,EXCLODAD     SAVE ENTRY POINT ADDRESS
         BR    R9            RETURN TO USER, (0) HAS ENTRY POINT ADDRES
         SPACE 3
*   EXCGET ACQUIRES STORAGE, USER MUST HAVE STORED LENGTH IN DB WITH
*   ST.  RETURN ADDRESS IS IN REG 1. AN 804 OR 80A ABEND MAY
*   RESULT IF STORAGE IS FRAGMENTED.
*
EXCGET   OC    EXCSTGAD,EXCSTGAD  ANY GETMAIN IN EFFECT ?
         BZ    EXCGET1       NO, SKIP FREEMAIN
         L     R1,EXCSTGLN   GET PREVIOUS ACQUIRED SIZE         GP04234
         N     R1,EXWMSK24   KILL SUBPOOL                       GP04234
         AL    R1,EXCSTGMX    ADD TO AVAILABLE STORAGE          GP02230
         ST    R1,EXCSTGMX     ADD TO GET NEW AMOUNT AVAILABLE  GP02230
         LM    R0,R1,EXCSTGSP   GET SUBPOOL, LENGTH AND ADDRESS GP04234
*DEBUG11 FREEMAIN R,LV=(0),A=(1)  FREE THE STORAGE
DEBUG11  SVC   10            ISSUE FREEMAIN
EXCGET1  XR    R1,R1         SIGNAL NO GETMAIN
         XC    EXCSTGLN+1(7),EXCSTGLN+1  INDICATE FREEMAIN DONE GP04234
         L     R15,DB        LOAD SIZE REQUESTED                GP04234
         LTR   R15,R15       ANY SPACE REQUESTED ?
         BNPR  R9            NO REQUEST, RETURN AFTER FREEMAIN ONLY
         L     R0,EXCSTGMX    GET AVAILABLE STORAGE             GP02230
         SR    R0,R15        SUBTRACT REQUEST                   GP02230
         BMR   R9            NOT ENOUGH SPACE, RETURN           GP02230
         ST    R0,EXCSTGMX    SAVE AMOUNT                       GP02230
         STCM  R15,7,EXCSTGLN+1  MOVE REQUESTED LENGTH          GP04234
         L     R0,EXCSTGSP   GET LENGTH AND SUBPOOL             GP04234
DEBUG12 GETMAIN R,LV=(0)     GET THE STORAGE
         ST    R1,EXCSTGAD    SAVE THE ADDRESS                  GP04234
         BR    R9            RETURN
         SPACE 2
EXCLOCK  NI    EXCPRIV,EXCONSOL     TURN OTHER FLAGS OFF
         L     R1,EXCTIMLO   RESET SHORT WAIT MODE              GP04234
         B     EXCHOLDM      GO TO SET TIMER INTERVAL
         SPACE 2
EXCHOLD  L     R1,EXCTIMHI    SET HOLD MODE FOR THINKER-TYPE    GP04234
EXCHOLDM ST    R1,EXCTIMER    SET NEW TIMER DELAY VALUE         GP04234
         B     EXCYES        RETURN, REDISPLAY SCREEN
         SPACE 2
EXCMODF  MVI   MODOPT,MODMODCR   INDICATE MODIFY FROM CRT
EXCMODX  LA    R1,EXITMOD     SET FOR XCTL TO MODIFY/TERMINATION MODULE
         B     EXCSVCM       GO TO MOVE NAME AND XCTL
         SPACE 2
EXCSLOOP OI    EXCPRIV,EXCPLOOP     SET LOOP OPTION
         SH    R5,H4         BUMP TO START OF 'LOOP' INPUT
         MVC   0(4,R5),BLANKS      ERASE IT
         B     EXCIN         PROCESS REMAINING INPUT AS THOUGH NO LOOP
         EJECT
*   THIS ROUTINE FINDS A VERB IN A LOOK-UP TABLE
*   REG. 4  TABLE ADDRESS - RETURNED WITH TABLE ENTRY
*   REG. 5  CURRENT BUFFER ADDRESS - BUMPED TO AFTER VERB
*   REG. 6  LAST INPUT BYTE AVAILABLE
*         0(14)  NOT FOUND
*         4(14)  FOUND, REG. 15 HAS DISPLACEMENT
*
VERBLOOK XR    R15,R15       ZERO IC REGISTER
         MVI   DB,0          CLEAR HIGH BYTE FOR LOAD
VERB1    CLI   0(R5),C' '    LOOK FOR NON-BLANK INPUT
         BNE   VERB2         OK
         LA    R5,1(,R5)     SKIP TO NEXT BYTE
         CR    R5,R6         REACHED END OF INPUT BUFFER
         BL    VERB1         NO, LOOK FOR NON-BLANK
         BR    R14           RETURN, BUFFER EXHAUSTED
         SPACE 1
VERB3    LA    R4,5(R15,R4)     BUMP TO NEXT TABLE ENTRY
VERB2    CLI   0(R4),X'FF'    END OF TABLE ?
         BER   R14           YES, VERB NOT FOUND
         IC    R15,0(,R4)    LENGTH - 1  OF TABLE ENTRY
         EX    R15,VERBCHEK    SAME VERB ?
         BNE   VERB3         NO, KEEP LOOKING
         MVC   DB+1(3),1(R4)  MOVE DISPLACEMENT TABLE ENTRY
         LA    R5,1(R15,R5)  POINT TO FIRST BYTE PAST VERB
         L     R15,DB        LOAD DISPLACEMENT ADDRESS
         B     4(,R14)       RETURN FOUND CONDITION
         EJECT ,
*   SMALL I/O RELATED SUBROUTINES
*
*   EXCWAIT             WAITS FOR USER ATTENTION
*         CALLED ON REG 9, RETURN TO (9) - NO ATTN WITHIN INTERVAL
*   RETURN TO (9)+4 - ATTENTION RECEIVED
*
EXCWAIT  ICM   R15,15,SQATTNR   ATTENTION ROUTINE?              GP04234
         BZ    EXCWAITO      NO; DO OLD STYLE TIMED WAIT        GP04234
         BAL   R14,SQXATTN   INVOKE ATTN CODE                   GP04234
         B     EXCWAIT1      TEST RESULT                        GP04234
EXCWAITO LA    R1,EXCTIMER   GET NORMAL WAIT TIME (WAS IODELAY) GP13069
         TM    EXCPRIV,EXCPLOOP     LOOP MODE ?
         BZ    *+8           NO, LEAVE IT
         LA    R1,SQTEIR     USE LOOP TIME FROM 'TE' IN 'SQSP'
*     STIMER   REAL,STIMEXIT,BINTVL=(1)     RESPONSE WAIT TIME REQ.
         L     R0,EXCSTMEX   LOAD 'REAL' FLAGS/ EXIT ADDRESS
         SVC   47            ISSUE STIMER - EXIT IS STIMEX
         WAIT  1,ECBLIST=ECBLIST  WAIT FOR ATTN OR TIMER
         L     R1,CANECBA    LOAD CANCEL ECB ADDRESS
         TM    0(R1),X'40'   CANCEL REQUESTED ?
         BNZ   EXCEXIT       YES - QUIT IMMEDIATELY
         TM    TIMECB,X'40'    TIMER EXPIRED ?
         BNZ   EXCWAIT1        YES, DON'T NEED TO CANCEL
       TTIMER  CANCEL          CANCEL TIMER
EXCWAIT1 TM    ATTNECB,X'40'  GOT ATTENTION ?
         BZ    EXCWAIT2       NO, HOLLER
         LA    R9,4(,R9)     SIGNAL ATTENTION
EXCWAIT2 XC    ATTNECB(8),ATTNECB   CLEAR THE ECBS
         AIF   ('&CRT' NE '3270').WAT
         L     R1,SQIQPARM      GET UCB ADDRESS
         MVC   26(2,R1),H4     SET FOR READ INITIAL PENDING
.WAT     ANOP  ,
         BR    R9            RETURN - NO ATTN
         SPACE 3
*   EXCREAD  READS INPUT INTO A SPECIFIED BUFFER.
*   (2) = INPUT ADDRESS;   (3) = MAXIMUM LENGTH
*   BR 9     I/O ERROR
*   B  4(9)             NO INPUT, NO SMI
*   B  8(9)             GOOD INPUT
*
EXCREAD  DS    0H            READ ROUTINE ENTRY - DEVICE DEPENDENT CODE
         AIF   ('&CRT' NE '3270').SETC60
         ST    R2,READCCW+8      SET INPUT ADDRESS
         MVI   READCCW+8,6     RESTORE 'READ MODIFIED' OPCODE
         LPR   R15,R3        GET INPUT LENGTH                   GP03089
         STH   R15,READCCW+14   SPECIFY INPUT LENGTH            GP03089
         BCTR  R15,0         MINUS 1 FOR EX
         EX    R15,XC        CLEAR REPLY INPUT AREA
         AGO   .SETCC
.SETC60  ANOP  ,
         ST    R2,READCCW      SET INPUT ADDRESS
         MVI   READCCW,2     SET 'READ DS MI' CCW OP-CODE
         LPR   R15,R3        GET INPUT LENGTH                   GP03089
         STH   R15,READCCW+6    SPECIFY INPUT LENGTH            GP03089
         BCTR  R15,0         MINUS 1 FOR EXECUTE
         EX    R15,XC        CLEAR REPLY AREA
.SETCC   LA    R1,READCCW     GET CCW ADDRESS
         SPACE 2
EXCIOCOM ST    R1,SQIOBCCW     STASH CCW CHAIN ADDRESS IN IOB
         LR    R15,R9        SAVE RETURN OVER RESET CALL          76039
         BAL   R9,EXCWAIT2    RESET ATTN; I/O JUST MESSED UP SCRN 76039
         LR    R9,R15        RESTORE RETURN ADDRESS               76039
DEBUG1   DS    0H            'EXORCISE' DEBUG POINT
*OLD*    EXCP  SQIOB     READING, RITING AND RYTHMIC TIC
         BAL   R14,SQEXCPS   INVOKE I/O ROUTINE                 GP04234
         WAIT  ECBLIST=CRTECB  WAIT FOR COMPLETION               77113
         AIF   ('&CRT' NE '3270').NOGONG                         75293
         L     R15,BCCAD     GET PREFIX BYTES                    75293
         NI    0(R15),255-4     TURN OFF ALARM BIT               75293
.NOGONG  CLI   WECB,X'7F'    NORMAL ?                            75293
         BNER  R9            NO, SIGNAL I/O ERROR
         LR    R15,R3        GET MAX LENGTH (FORCE RETURN IF R3<0)
         SH    R15,SQIOBCSW+6    SUBTRACT BYTES NOT I/O'ED
         BNP   4(,R9)        NO, NO INPUT (FORCED FOR WRITE)
         AIF   ('&CRT' NE '3270').REP60
         AIF   ('&SYSTEM' EQ 'MVS').AMVS78                       82199
EXCREADI NC    0(6,R2),EXCRMSK    MASK AID, CURS, ATB, ETC.
         CLI   0(R2),X'1D'     WAS IT AN ENTER ?
         BNE   EXCREADO       NO, CHECK FOR TEST-REQ. KEY
         MVI   0(R2),C' '     YES, REPLACE BY BLANK FOR 2260 COMPAT.
         CLI   6(R2),0       WAS ANY TEXT READ IN ?
         BE    4(,R9)        NO, TREAT AS ENTER ONLY
EXCREADO NI    EXCPRIV,255-EXCPLOOP    INPUT - RESET LOOP MODE
         CLI   0(R2),1       FUNKY TEST-REQ INPUT ?
         BNE   EXCREADP      NO
         MVC   3(3,R2),4(R2)     SHIFT TO COMPENSATE FOR STUPID I/P
         MVI   6(R2),X'40'     REPLACE ATB ADDR BYTE BY BLANK
         MVI   0(R2),X'10'     PUT IN DOCUMENTED TEST-REQ. CODE
         NI    5(R2),X'3F'     MASK FOR ADDRESS CALCULATION
         IC    R1,5(,R2)     GET ATB ADDR. + 1
         LA    R1,1(,R1)     FAKE ONE MORE TO ALLOW FOR MVC
         STC   R1,5(,R2)     STASH IT BACK
         CLI   1(R2),1920/64     GARBAGE IN CURSOR ADDRESS ?
         BL    *+10          NO, USE IT AS IS
         XC    1(5,R2),1(R2)     CLEAR ENTIRE INPUT BUFFER
EXCREADP TM    0(R2),X'50'     ENTER OR PFK WITH TEXT ?
         BZ    EXCREADT      NO, SKIP CURSOR CHECK
         AGO   .CMVS78                                           82199
.AMVS78  ANOP  ,                                                 82199
EXCREADI CLI   0(R2),1       TEST-REQUEST SOH/STX SEQUENCE ?     82199
         BNE   EXCREADN      NO                                  82199
         XC    0(5,R2),0(R2)  KILL PREFIX                        83340
         MVI   6(R2),C' '    MAKE PRETTY                         82199
         MVI   0(R2),X'F0'   SET DOCUMENTED AID                  82199
         AIF   (&MVS).INP3290                                   GP10228
EXCREADN MVC   EXCRAWIN,0(R2)  PRESERVE AID, CURSOR, ETC. AS IS GP10213
         NC    0(6,R2),EXCRMSK   MASK AID, CURS, ATB, ETC.       82199
         XI    0(R2),X'20'   CONVERT TO OLD CODE                 82199
         CLI   0(R2),X'1D'   WAS IT AN ENTER ?                   82199
         BNE   EXCREADO      NO                                  82199
         MVI   0(R2),C' '    YES, REPLACE BY BLANK FOR 2260 CODE 82199
         CLI   6(R2),0       WAS ANY TEXT READ IN ?              82199
         BE    4(,R9)        NO, TREAT AS ENTER ONLY             82199
EXCREADO NI    EXCPRIV,255-EXCPLOOP    INPUT - RESET LOOP MODE   82199
EXCREADP TM    0(R2),X'50'   ENTER OR PFK WITH TEXT ?            82199
         BZ    EXCREADT      NO, SKIP CURSOR CHECK               82199
         CLI   0(R2),X'1E'   L-PEN ?                             82199
         BE    8(,R9)        YES; DON'T CLOBBER                  82199
.CMVS78  XR    R0,R0         CLEAR FOR IC                        82199
         XR    R1,R1         CLEAR FOR ADD
         LA    R15,2         FOR TWO BYTE ADDRSS
         LR    R14,R2        POINT TO CURSOR ADDRESS BYTE 1 -1
EXCREADQ IC    R0,1(,R14)    GET CURSOR ADDR. BYTE
         SLA   R1,6          SHIFT PREVIOUS ADDRESS OVER
         AR    R1,R0         ADD CURSOR POSITION IN
         IC    R0,4(,R14)    GET (ATB+1) ADDRESS BYTE
         SR    R1,R0         SUBTRACT - EVENTUALLY GETS RELATIVE POSTN
         LA    R14,1(,R14)   POINT TO NEXT ADDRESS BYTE
         BCT   R15,EXCREADQ    DO FOR BOTH ADDRESS BYTES
         LTR   R1,R1         WAS CURSOR IN FIELD ?
         AGO   .COM3290                                          93044
.INP3290 ANOP  ,   RECODED FOR 14/16 BIT ADDRESS SUPPORT         93044
.*   NOTE THIS MUST EXPAND TO THE SAME LENGTH AS THE OLD CODE IN ORDER
.*   TO AVOID HAVING TO RE-ASSEMBLE EVERYTHING                   93044
EXCREADN MVC   EXCRAWIN,0(R2)  PRESERVE AID, CURSOR, ETC. AS IS GP10213
         NI    0(R2),X'3F'   MASK AID                            93044
         NOPR  0             MAINTAIN PRIOR LENGTH               93044
         XI    0(R2),X'20'   CONVERT TO OLD CODE                 93044
         CLI   0(R2),X'1D'   WAS IT AN ENTER ?                   93044
         BNE   EXCREADO      NO                                  93044
         MVI   0(R2),C' '    YES, REPLACE BY BLANK FOR 2260 CODE 93044
         CLI   6(R2),0       WAS ANY TEXT READ IN ?              93044
         BE    4(,R9)        NO, TREAT AS ENTER ONLY             93044
EXCREADO NI    EXCPRIV,255-EXCPLOOP    INPUT - RESET LOOP MODE   93044
EXCREADP TM    0(R2),X'50'   ENTER OR PFK WITH TEXT ?            93044
         BZ    EXCREADT      NO, SKIP CURSOR CHECK               93044
         CLI   0(R2),X'1E'   L-PEN ?                             93044
         BE    8(,R9)        YES; DON'T CLOBBER                  93044
         MVC   DB(3),0(R2)   MOVE AID AND FIRST ADDRESS          93044
         BAL   R14,EXWFRSBA  CONVERT TO BINARY ADDRESS           93044
         LR    R15,R0        SAVE OVER NEXT CALL                 93044
         MVC   DB(3),3(R2)   MOVE ATB AND FIELD ADDRESS          93044
         BAL   R14,EXWFRSBA  CONVERT CURSOR ADDRESS              93044
         SLR   R1,R1         PRESET FOR NO INPUT                 93044
         SR    R15,R0        GET LENGTH OF INPUT                 93044
         BNP   EXCREADT      NONE; SHOULD NOT HAVE GOTTEN HERE   93044
         NOP   0             MAINTAIN OLD LENGTH                 93044
         LTR   R1,R15        SWITCH AND TEST                     93044
.COM3290 BM    EXCREADT      NO
         LA    R15,0(R2,R3)     POINT TO LAST POSSIBLE INPUT BYTE + 1
         LA    R14,6(R1,R2)    FIRST I/P POSITION TO CLEAR
         SR    R15,R14       NO. OF BYTES TO CLEAR
         BNP   EXCREADT      SKIP IF NONE
         MVI   0(R14),C' '     BLANK FIRST BYTE
         SH    R15,H1        ALLOW FOR EX
         BM    EXCREADT      NONE
         STC   R15,*+5       KLUTZY
         MVC   1(0,R14),0(R14)     CLEAR REMAINDER OF INPUT
EXCREADT MVC   1(5,R2),BLANKS     CLEAR ADDR, ATB, ETC.
         LA    R2,1(,R2)     PREVENT OC OF AID
         SH    R3,H2         SET LENGTH TO OC - 1
         AGO   .REP67
.REP60   SH    R3,H1         ADJUST LENGTH FOR EX
.REP67   LA    R15,L'BLANKS-1     GET LENGTH FOR EX
         LA    R14,L'BLANKS     LENGTH FOR SUB
EXCREADU BM    8(,R9)        SKIP IF DONE
         TM    SQSEDFG,SQSELOW    KEEP MIXED CASE ?             GP10213
         BZ    EXCREADW      NO; UPPER CASE                     GP10213
         ZI    SQSEDFG,SQSELOW    RESET IT                      GP10213
         B     8(,R9)        AND RETURN                         GP10213
EXCREADW CR    R3,R15        ANY LEFT TO BLANK
         BL    EXCREADV      YES - SINGLE OC
         EX    R15,OC        UPPER CASE
         AR    R2,R14        NEXT I/P ADDR.
         SR    R3,R14        REMAINING LENGTH
         B     EXCREADU      TRY MORE
EXCREADV EX    R3,OC         CONVERT TO UPPER CASE FROM HEX ZEROES
         B     8(,R9)        TAKE GOOD RETURN
         AIF   ('&CRT' NE '3270').REP67B
         AIF   ('&SYSTEM' NE 'MVS').NMVS78                       82199
EXCRMSK  DC    X'3F3F3F003F3F'  MASK FOR AID/CURSOR/ATB/ADDR     82199
         AGO   .REP67B                                           82199
.NMVS78  ANOP  ,                                                 82199
EXCRMSK  DC    X'1F3F3F003F3F'    MASK FOR AID, CURS AD, ATB, ATB AD
.REP67B  ANOP  ,
         SPACE 3
*   EXCPAGE WRITES A FULL PAGE + A MESSAGE
*   (2) POINTS TO MESSAGE (SMI FORMAT)
*   (3) HAS MESSAGE LENGTH ( 0 < (3) < 81 )
*
EXCPAGE  L     R14,MSGAD     GET ADDRESS OF MESSAGE BUFFER
         LR    R15,R3        GET LENGTH
         BCTR  R15,0         MINUS FOR EX
         EX    R15,IOPAGMVC     MOVE TEXT TO MESSAGE AREA
EXCPAGNM AH    R3,BUFSIZ     GET LENGTH OF SCREEN SIZE + MESSAGE
         L     R1,PAGECCWA   GET ADDRESS OF PAGE CCW
         B     EXCRITE+4     BRANCH TO COMMON WRITE
         SPACE 1
EXCRITE  L     R1,RITECCWA   GET ADDRESS OF WRITE CCW
         AH    R3,BUFPFX     ADD CONTROL BYTE LENGTH
         STH   R3,PAGELEN    STASH LENGTH IN WRITE CCW
         LNR   R3,R3         FORCE 4(R9) RETURN
         B     EXCIOCOM      DO EXCP WRITE
         SPACE 2
*   ONUNIT FREES GOTTEN STORAGE AND DELETES LOADED MODULES PRIOR
*   TO LETTING A MODULE YIELD CONTROL. OPTIONALLY A USER EXIT
*   MAY BE TAKEN (REQUESTED BY CALL TO 'ONUNIT'). THE 'ONUNIT'
*   MUST BE RE-REQUESTED FOLLOWING EACH EXIT FROM IT, OR CONTROL
*   WILL BE GIVEN TO EXHBCOMM.
*
ONUNIT   MVI   ONUNITO,C'X'     FORCE EXIT                      GP99018
ONUNITB  L     R15,ONUNIT@     LOAD EXIT ADDRESS                GP99018
         CLI   INVOKE+4,C'X'     XCTL TO XCTL MODULE ?
         BL    ONUNITE       NO; CHECK FOR EXIT AND FREE/DELETE  79215
         CLI   ONUNITO,C'X'        XCTL=YES OPTION ?            GP99018
         BNER  R14           YES;  IGNORE FREEMAIN/DELETE
ONUNITE  MVC   ONUNIT@,ZEROES      WIPE OUT ONUNIT WORD         GP99018
         MVI   ONUNITO,0           WIPE OUT ONUNIT FLAG         GP99018
         LTR   R15,R15       ANY EXIT ADDRESS ?
         BNZR  R15           YES, TAKE THE EXIT
ONUNITF  ST    R9,EXCSAV09   SAVE OVER CALLS                    GP02232
         MVI   EXCINVOK+5,C' '    REQUEST DELETION
         MVC   DB(4),ZERO    SIGNAL FREEMAIN ONLY               GP05095
         ST    R14,EXCSAV14  SAVE RETURN REGISTER OVER SVCS     GP02232
         BAL   R9,EXCGET     FREEMAIN GOTTEN STORAGE            GP99018
         BAL   R9,EXCLOAD    DELETE LOADED ROUTINE, IF ANY      GP99018
ONUNITG  L     R9,EXCSAV09   RESTORE RETURN REGISTER            GP99100
         XC    SQSPICA+4(8),SQSPICA+4    CLEAR SPIE EXIT        GP99100
         L     R14,EXCSAV14  RESTORE RETURN REGISTER            GP99100
         B     EXWFRSTG      FREE ALL STORAGE AND RETURN TO CALLER
         SPACE 3
EXCPOUT  LA    R2,EXMORE     MORE MESSAGE
         LA    R3,L'EXMORE    LENGTH
         TM    EXCPRIV,EXCPLOOP      LOOP MODE ?
         BZ    *+8           NO, LEAVE 'MORE' MESSAGE
         LA    R2,EXMOLOOP     USE LOOP MESSAGE INSTEAD
EXCPELL  ST    R9,DB+4       SAVE OVER CALLS
         XPAGE (R2),(R3)     WRITE PAGE AND MESSAGE
         L     R9,DB+4       RESTORE RETURN FOR SPECIAL ENTRY
EXCPEWRD ST    R9,DB+4       SAVE OVER CALLS
         XWAIT END=EXCPLTST     TEST FOR LOOP MODE IF NO RESPONSE
         XREAD ,             STANDARD REPLY INPUT
          B    EXCPENT       'ENTER' ONLY
         L     R9,DB+4       RESTORE
         B     4(,R9)        TAKE 'INPUT' XIT
EXCPLTST TM    EXCPRIV,EXCPLOOP      LOOP MODE ?
         BZ    EXCEXIT       NO; EXIT IF NO RESPONSE
EXCPENT  L     R9,DB+4       RESTORE
         BR    R9            RETURN - EOB EXIT
         EJECT ,
*   TERMINATION CODE
*
EXCRERR  BAL   R14,ONUNIT      FORCE EXIT                       GP99018
         LA    R2,READERR    GET READ ERROR MESSAGE
         LA    R3,L'READERR
         B     EXCSLOP       TELL USER
         SPACE 2
EXCWERR  MVI   MODOPT,MODIOERC     SHOW I/O ERROR
         SPACE 2
EXCEXIT  BAL   R14,ONUNIT      FORCE EXIT                       GP99018
         BAL   R14,ONUNITF      FORCE FREE/DELETE               GP99018
         OI    MODOPT,MODCREND      SIGNAL END TALK TO MODS
         B     EXCMODX       SET NAME AND EXIT TO RETURN CLEANUP MODULE
         SPACE 3
         AIF   (&SVS OR &MVS).NUBLAN                            GP15151
BLANKER  L     R15,BUFAD     GET START BUFFER ADDRESS
BLANKER1 MVC   0(80,R15),BLANKS       CLEAR A LINE
         LA    R15,80(,R15)    ADVANCE TO NEXT LINE
         C     R15,MSGAD      PASSED MESSAGE LINE YET ?
         BNH   BLANKER1      NO, DO ANOTHER LINE
         BR    R14           RETURN
         AGO   .DNBLAN                                          GP15151
.NUBLAN  ANOP  ,                                                GP15151
BLANKER  L     R15,BUFAD     GET START BUFFER ADDRESS           GP15151
BLANKER1 STM   R0,R1,BLANKERS   SAVE SOME                       GP15151
         LR    R0,R15        COPY START                         GP15151
         L     R1,MSGAD        AND END OF SCREEN                GP15151
         AH    R1,LSIZE      ADD ROOM FOR PROMPT                GP15151
         SR    R1,R15        SIZE TO CLEAR                      GP15151
         L     R15,BLANKERB  SET FOR BLANK FILL                 GP15151
         MVCL  R0,R14        BLANK AREA                         GP15151
         LM    R0,R1,BLANKERS   SAVE SOME                       GP15151
         BR    R14           RETURN TO CALLER                   GP15151
BLANKERS DC    2A(0)         SAVE AREA                          GP15151
BLANKERB DC    X'40000000'   MVC BLANK                          GP15151
.DNBLAN  EJECT ,
*   DATA AREA
*
VERBCHEK CLC   0(0,R5),4(R4)    COMPARE VERB TO TABLE  ENTRY
XC       XC    0(0,R2),0(R2)    CLEAR REPLY BUFFER
OC       OC    0(0,R2),BLANKS CONVERT REPLY TO UPPER CASE
EXCBNMV  MVC   INVOKE+5(0),0(R5)  MOVE LONG VERB
IOPAGMVC MVC   0(0,R14),0(R2)     MOVE TEXT FOR EXCPAGE
ONUNIT@  DC    A(0)           ADDRESS OF USER EXIT              GP99018
EXCSTMEX DC    A(0)          STIMER FLAGS/EXIT ADDRESS
EXCSTGLN DC    0F'0'   1/3   LENGTH OF GOTTEN STORAGE           GP04234
EXCSTGSP DC    AL1(X'FC'),AL3(0)  SUB-POOL 252 FOR GOTTEN STG   GP04234
EXCSTGAD DC    A(0)    3/3   ADDRESS OF GOTTEN STORAGE          GP04234
EXCSZMOD DC    F'0'          CURRENT MODULE SIZE                GP04234
EXCSZLOD DC    F'0'    1/2   CURRENT LOADED MODULE SIZE         GP04234
EXCLODAD DC    A(0)    2/2   ENTRY POINT ADDRESS OF LOADED MODULES
EXCEPLO  DC    CL8' '         NAME OF LOADED MODULE OR C' '
REPPTR   DC    F'0'          ADDRESS OF PARAMETER FOLLOWING VERB
EXCSBLDL DC    0A(0),AL2(1,58)     BLDL PREFIX
EXCINVOK DC    CL58'EXHCS###'  BLDL LIST ENTRY FOR LOADING SUBROUTINES
EXCRAWIN DC    XL6'0'        RAW INPUT PREFIX (AID,CURSOR,ETC)  GP10213
EXCSTGMX DC    F'0'          AMOUNT OF STORAGE AVAIL. FOR LOAD/GETMAIN
IODELAY  DC    0F'0'         STIMER VAL (NORM. 30 SECONDS IN 1/100THS)
EXCTIMER  DC    F'3000'       MAX. WAIT OF 30 SECONDS
EXCTIMLO DC    F'3000'       WORD  TO RESET EXCTIMER
EXCTIMHI DC    A(15*60*100)  MAX. WAIT OF 15 MINUTES
EXITMOD  DC    C'EXHAMODS'   BLDL NAME FOR EXIT FROM COMM.
EXCMGCR  DC    C'EXHC#MGC'   SVC 34 / MGCR COMMAND MODULE
EXCPRIV  DC    X'0'          PRIVILEGED  OPERATION BYTE
EXCAUTH  EQU   X'01'         AUTHORIZATION FLAG
EXCPSYS  EQU   X'02'         PRIVILEGED FUNCTION UNLOCK BIT
EXCONCAN EQU   X'04'         UNLOCKED NON-OPERATOR CONSOLE
EXCONSOL EQU   X'08'         THIS IS A SECONDARY (NON-OPERATOR) CRT
EXCUNFG  EQU   X'30'         STORAGE UNFRAG. REQ AND DONE
EXCUNFG1 EQU   X'10'         STORAGE UNFRAGMENTATION REQUESTED
EXCUNFG2 EQU   X'20'         STORAGE UNFRAG. MODULE EXECUTED
EXCPLOOP EQU   X'40'         LOOP MODE SWITCH
EXCFAIL  EQU   X'80'         BAD UNLOCK TRY FLAG
EXCUNVC  SMI   ' EXHCC999 UNAVAILABLE  '    DON'T CLOBBER MSG
BLDLMSK  EQU   EXCUNVC+SMILOFF+1,8,C'C'   VAR. TEXT ADDRESS     GP99018
EXCOPQ   BTAB  '=',EXCSEQU     EQUAL - COMMAND ESCAPE           GP10213
EXCOP    BTAB  'LOCK ',EXCLOCK,BASE=EXHBCOMM
         BTAB  '/',EXCSVC      OS COMMAND REQUEST
         BTAB  '#',EXCSVCT     PRIVILEGED OS COMMAND REQ.
EXCOPHSP BTAB  '$',EXC$BLD     HASP REQUEST (MAY BE REPLACED)    83345
         AIF   ('&SYSTEM' NE 'MVS').MMVS78                       82199
         BTAB  'Ö',EXC$BLD   TREAT CENT SIGN AS HASP REQUEST     82199
.MMVS78  ANOP  ,                                                 82199
EXCOPASP BTAB  '8',EXC8BLD     ASP REQUEST
         BTAB  '*',EXC8BLD      ALTERNATE ASP FORM
         BTAB  '@',EXC@TST     PRIVILEGED MODULE REQUEST
         BTAB  'HOLD ',EXCHOLD
         BTAB  LOOP,EXCSLOOP     LOOP MODE - PRECEDES VALID INPUT
         BTAB  'MOD ',EXCMODF     MODIFY COMMAND
         BTAB  'F ',EXCMODF    SHORT MODIFY
         BTAB  '^CLR',EXCNEW   WIPE SCREEN
         BTAB  '^CAN',EXCLOCK     LOCK AND SHORT WAIT MODE
         BTAB  '^PA1',EXCHOLD     SET HOLD MODE
         BTAB  'END ',EXCEXIT  EXIT
LOOKDUMM BTAB  *END          EMPTY TABLE LABEL
         SPACE 1
YES      SMI   ' YES ',SMI=6F    'YES ?' MESSAGE
SYNTERR  SMI   'Unacceptable ',SMI=5A  TELL IN A FORCEFUL WAY
AUTHERR  SMI   'Unauthorized ',SMI=5A  TELL IN A FORCEFUL WAY   GP15302
READERR  SMI   'I/O ERR - RETRY ',SMI=6F   MAYBE ?
OP2BAD   SMI   'Invalid Op ',SMI=5A     TELL THE DUMMY
EXMORE   SMI   ' MORE ',SMI=6F       MORE ?  MESSAGE
EXMOLOOP SMI   ' LOOP ',SMI=6F      LOOP ?  MESSAGE
         SPACE 1
EXCXCTT XTAB  'KEY=',KEY    UNLOCK PRIVILEGED SYSTEM FUNCTIONS
EXCXCT@  DS    0X            NO ENTRIES IN PRIVILEGED MODULE TABLE
EXCXCT8  DS    0X            NO ENTRIES IN ASP MODULE TABLE
EXCXCT$  DS    0X            NO SPECIAL HASP ENTRIES
         XTAB  *END          END OF LIST
ONUNITO  DC    C'X'          ONUNIT CLEAN-UP FLAG               GP99018
EXCNMPAT DC    CL8'EXHCC999'   (BLDLMSK GETS CLOBBERED?)        GP02232
EXCSAV09 DC    F'0'         SAVE RETURN REGISTER                GP02232
EXCSAV14 DC    F'0'         SAVE RETURN REGISTER                GP02232
         SPACE 3                                                GP05132
*   SIMPLE COMPARE SUBROUTINE, INVOKED BY MACRO XMASK           GP05132
*   DATA ADDR R1, LEN R0, MASK ADDR R14, LEN 15, RET R9         GP05132
*                                                               GP05132
EXCOMPAR CMASK ,             TAKE THE DEFAULTS                  GP05132
         SPACE 3                                                GP05132
*   SIMPLE PARSING SUBROUTINE, INVOKED BY MACRO XPARM           GP05132
*   DATA ADDRESS R5, END R6, OUTPUT R3, LEN R4, STOP CHARS R0   GP05132
*    (MAY BE INVOKED AFTER XLOOK TO GET PARM)                   GP05132
*
*    RETURNS: R0 - FIELD LENGTH     R5 - NEXT SCAN START        GP05132
*                                                               GP05132
*   OPTION BITS, PRESET IN DB+3                                 GP05132
*    80 - NO ERROR IF INPUT EXHAUSTED                           GP05132
*    40 - NO ERROR IF LENGTH IS ZERO                            GP05132
*    20 - IF NO MASK BYTES, RETURN FULL LENGTH                  GP05132
*    10 - ACCEPT QUOTED STRING IN QUOTED FORM                   GP08313
*    08 - ACCEPT QUOTED STRING BUT STRIP QUOTES                 GP08313
*    04 - VALIDATE INTEGER; RETURN VALUE IN R2                  GP05132
*    02 - VALIDATE HEX; CONVERT TO HEX IN USER VARIABLE         GP05132
*                                                               GP05132
*   RETURN TO R14 - NO MATCH; REASON BYTE IN DB                 GP05132
*   RETURN TO 4(R14) - VALUE STASHED                            GP05132
*                                                               GP05132
*   DB+0  C'9' - OPERAND TOO LONG, OR MISSING TERMINATION       GP05132
*         C'2' - OPERAND IS DUPLICATE (OUTPUT NOT BLANK/ZERO)   GP05132
*         C'0' - OPERAND IS NULL                                GP05132
*         C'N' - HEX OR INT REQUEST, BUT NOT VALID              GP05132
*         C'E' - INPUT STRING EXHAUSTED                         GP05132
*         C' ' - SUCCESSFUL PARSE                               GP05132
*                                                               GP05132
*   DB+1  X'20'  STRING CONTAINS MASK BYTES (* - ? %)           GP05132
*                                                               GP05132
*   DB+2  C      STOP CHARACTER (BLANK, COMMA, ETC)             GP05132
*                                                               GP05132
EXCPARM0 L     R0,EXCPSTOP   LOAD DEFAULT STOPPERS              GP05132
EXCPARM  ST    R0,EXCPRMDB   SAVE STOPPERS FOR A WHILE          GP08313
         XC    DB+1(2),DB+1  CLEAR OTHER RETURN FIELDS          GP05132
         SR    R0,R0         PRESET FOR NO DATA MOVED           GP05132
         SR    R1,R1         ENSURE ZERO HIGH BYTE              GP05132
         LR    R15,R6        FINAL SCAN ADDRESS                 GP05132
         SR    R15,R5        RESIDUAL TEXT LENGTH               GP05132
         BP    EXCMASK0      NOT EXHAUSTED                      GP05132
EXCMASQ0 MVI   DB,C'E'       SET INPUT EXHAUSTED                GP05132
         TM    DB+3,X'80'    ACCEPT EXHAUSTED INPUT ?           GP05132
         BZR   R14           RETURN NULL AS ERROR               GP05132
         B     EXCMASKX      RETURN AS LENGTH 0                 GP05132
EXCMASK0 BCTR  R15,0         SET LENGTH FOR EXECUTE             GP05132
         EX    R15,EXPRNBLK  LOOK FOR NON-BLANK                 GP05132
         BZ    EXCMASQ0      RETURN NULL                        GP05132
         MVI   DB,C'2'       SET FOR DUPLICATE                  GP05132
         CLI   0(R3),C' '    ALREADY FILLED?                    GP05132
         BHR   R14           YES; RETURN                        GP05132
         LR    R5,R1         SET NEW SCAN POSITION              GP05132
         LR    R15,R6        FINAL SCAN ADDRESS                 GP05132
         SR    R15,R5        RESIDUAL TEXT LENGTH               GP05132
         BNP   EXCMASQ0      HUH ?                              GP05132
         BCTR  R15,0         SET LENGTH FOR EXECUTE             GP05132
         SR    R2,R2         CLEAR FOR INSERT                   GP05132
         TM    DB+3,X'18'    PROCESS QUOTED STRINGS ?           GP08313
         BZ    EXCPQNON      NO                                 GP08313
         CLI   0(R5),C'"'    NORMAL QUOTE ?                     GP08313
         BE    EXCPQUOT      YES; SPECIAL HANDLING              GP08313
         CLI   0(R5),C''''   ABNORMAL QUOTE ?                   GP08313
         BNE   EXCPQNON      NO; NORMAL STRING                  GP08313
EXCPQUOT STM   R3,R4,36(R13)   SAVE CALLER'S OUTPUT ADDR/LEN    GP08313
         SR    R0,R0         CLEAR ACCUMULATED LENGTH           GP08313
         SR    R15,R15                                          GP08313
         IC    R15,0(,R5)    LOAD QUOTE                         GP08313
         XC    EXCTRTST,EXCTRTST  CLEAR TRT STOPPER ARRAY       GP08313
         STC   R15,EXCTRTST(R15)  SAVE STOPPER                  GP08313
         LA    R5,1(,R5)     POINT PAST OPENING QUOTE           GP08313
EXCPQLUP MVI   DB,C'9'       SHOW TOO LONG OR NOT TERMINATED    GP08313
         LR    R15,R6        COPY END ADDRESS                   GP08313
         SR    R15,R5        REMAINING TEXT TO SCAN             GP08313
         BNP   EXCPQRET      NONE - OOPS                        GP08313
         BCTR  R15,0           EX LENGTH                        GP08313
         EX    R15,EXPRMTRT  LOOK FOR ENDING QUOTE              GP08313
         BZ    EXCPQRET      RETURN ERROR                       GP08313
         LR    R15,R1        COPY STOP ADDRESS                  GP08313
         SR    R15,R5        LESS START                         GP08313
         TM    DB+3,X'08'    STRIP QUOTES ?                     GP08313
         BNZ   EXCPQMVC      YES; TEST AND MOVE                 GP08313
         BCTR  R5,0          INCLUDE QUOTE IN MOVE              GP08313
         LA    R15,2(,R15)   ADJUST LENGTH                      GP08313
EXCPQMVC AR    R0,R15        SET RETURNED LENGTH                GP08313
         CR    R0,R4         WILL IT FIT ?                      GP08313
         BH    EXCPQRET      NO; RETURN ERROR                   GP08313
         BCTR  R15,0           EX LENGTH                        GP08313
         EX    R15,EXPRMMVC  MOVE TEXT                          GP08313
         LA    R3,1(R15,R3)  NEXT DESTINATION                   GP08313
         LA    R5,1(,R1)     POINT PAST STOPPER                 GP08313
         TRT   0(1,R5),EXCTRTST   DOUBLED STOPPER ?             GP08313
         BZ    EXCPQEND      NO; LOOK FOR FIELD SEPARATOR       GP08313
         TM    DB+3,X'08'    STRIP QUOTES ?                     GP08313
         BZ    EXCPQLUP      NO; REPEAT SCAN AND MOVE           GP08313
         AH    R0,H1         UP LENGTH                          GP08313
         CR    R0,R4         WILL IT FIT ?                      GP08313
         BH    EXCPQRET      NO; FAIL                           GP08313
         MVC   0(1,R3),0(R5)   CONVERT DOUBLE TO SINGLE QUOTE   GP08313
         LA    R3,1(,R3)     ADJUST OUTPUT                      GP08313
         LA    R5,1(,R5)     ALSO FIX INPUT                     GP08313
         B     EXCPQLUP      PROCESS REST                       GP08313
EXCPQEND L     R2,EXCPRMDB   RELOAD STOPPERS                    GP08313
         XC    EXCTRTST,EXCTRTST  CLEAR TRT STOPPER ARRAY       GP08313
         LA    R15,4         SUPPORT UP TO 4 STOP CHARACTERS    GP08313
EXCPQSLP SRDL  R2,8          MOVE CHARACTER TO R1               GP08313
         SRL   R3,24         RIGHT JUSTIFY AND CLEAR            GP08313
         STC   R3,EXCTRTST(R3)  SAVE STOPPER                    GP08313
         BCT   R15,EXCPQSLP  DO ALL FOUR                        GP08313
         MVI   DB,C'9'       PRESET ERROR                       GP08313
         TRT   0(1,R5),EXCTRTST  CHECK FOR STOPPER              GP08313
         BZ    EXCPQRET      STOPPER MISSING                    GP08313
         MVI   DB,C' '       GOOD RETURN                        GP08313
         STC   R2,DB+2       RETURN STOP CHARACTER              GP08313
         LA    R5,1(,R1)     ADVANCE PAST STOPPER               GP08313
         LM    R3,R4,36(R13)   RESTORE CALLER'S REGISTERS       GP08313
         LTR   R0,R0         USABLE LENGTH ?                    GP08313
         BP    4(,R14)       YES; RETURN STRING                 GP08313
         TM    DB+3,X'40'    ACCEPT NULL ENTRY ?                GP08313
         BNZ   4(,R14)       YES; TAKE GOOD RETURN ANYWAY       GP08313
         MVI   DB,C'0'       SIGNAL ZERO LENGTH STRING          GP08313
         BR    R14           RETURN ERROR                       GP08313
EXCPQRET LM    R3,R4,36(R13) RESTORE CALLER'S REGISTERS         GP08313
         AR    R0,R15        EXCESS LENGTH, MORE OR LESS        GP08313
         BR    R14           RETURN ERROR                       GP08313
         SPACE 1
EXCPQNON XC    EXCTRTST,EXCTRTST  CLEAR TRT STOPPER ARRAY       GP05132
         L     R0,EXCPRMDB   RELOAD STOPPERS                    GP08313
         LA    R2,4          SUPPORT UP TO 4 STOP CHARACTERS    GP05132
EXCMASKL SRDL  R0,8          MOVE CHARACTER TO R1               GP05132
         SRL   R1,24         RIGHT JUSTIFY AND CLEAR            GP05132
         STC   R1,EXCTRTST(R1)  SAVE STOPPER                    GP05132
         BCT   R2,EXCMASKL   DO ALL FOUR                        GP05132
         LA    R1,1(,R6)     STOP ADDRESS IF NO STOPPER         GP05132
         EX    R15,EXPRMTRT  LOOK FOR STOPPER                   GP05132
         STC   R2,DB+2       RETURN STOP CHARACTER              GP05132
         MVI   DB,C'0'       SHOW ITEM ZERO LENGTH              GP05132
         LR    R15,R1        COPY STOPPER                       GP05132
         SR    R15,R5          LESS START                       GP05132
         BP    EXCMASKM      HAVE TEXT                          GP05132
         SR    R0,R0         SHOULD NEVER GET NEGATIVE?         GP05132
         TM    DB+3,X'40'    ACCEPT NULL VARIABLE?              GP05132
         BZR   R14           TREAT NULL AS ERROR                GP05132
         LA    R5,1(,R1)     SKIP STOPPER                       GP05132
         B     EXCMASKX      RETURN AS LENGTH 0                 GP05132
EXCMASKM MVI   DB,C'9'       SET TOO LONG                       GP05132
         LR    R0,R15        RETURN LENGTH OF FIELD             GP05281
         BCTR  R15,0         LENGTH FOR EXECUTE                 GP05132
         TM    DB+3,X'04'    LOOKING FOR INTEGER ?              GP05281
         BNZ   EXCPTINT      YES; VERIFY AND CONVERT            GP05281
         CR    R15,R4        COMPARE TO MAX                     GP05132
         BNLR  R14           TOO LONG - TOO BAD                 GP05132
         EX    R15,EXPRMMVC  MOVE TO PARM AREA                  GP05132
         TM    DB+3,X'02'    LOOKING FOR HEX DATA?              GP06358
         BNZ   EXCPTHEX      YES; VERIFY AND CONVERT            GP06358
         XC    EXCTRTST,EXCTRTST  CLEAR TRT AREA AGAIN          GP05132
         MVI   EXCTRTST+C'*',C'*'  UNIVERSAL MATCH - FIELD      GP05132
         MVI   EXCTRTST+C'-',C'-'  UNIVERSAL MATCH - FIELD      GP05132
*2260*   MVI   EXCTRTST+C'"',C'"'  UNIVERSAL MATCH - BYTE       GP05132
         MVI   EXCTRTST+C'%',C'%'  UNIVERSAL MATCH - BYTE       GP05132
         MVI   EXCTRTST+C'?',C'?'  UNIVERSAL MATCH - BYTE       GP05132
         OI    DB+1,X'20'    PRESET FOR MASK FOUND              GP05132
         EX    R15,EXPRMTRT  CHECK FOR MASK BYTES               GP05132
         BNZ   EXCMASKY      YES; ONE OR MORE. USE LEN OF MASK  GP05132
         NI    DB+1,255-X'20'  RESET MASK FOUND                 GP05132
         TM    DB+3,X'20'    CHECK FOR MASK BYTES?              GP05132
         BZ    EXCMASKY      NO; RETURN ACTUAL LENGTH           GP05132
         LR    R0,R4         RETURN LENGTH OF FIELD             GP05132
         B     EXCMASKY      AND GET OUT                        GP05281
EXCPTHEX MVI   EXCTRTST,X'FF'     MAKE STOPPERS                 GP08313
         MVC   EXCTRTST+1(255),EXCTRTST   PROPAGATE             GP08313
         XC    EXCTRTST+C'0'(10),EXCTRTST+C'0'  ACCEPT 0-9      GP08313
         XC    EXCTRTST+C'A'(6),EXCTRTST+C'A'  ACCEPT A-F       GP06358
         XC    EXCTRTST+C'a'(6),EXCTRTST+C'a'  ACCEPT a-f ALSO  GP06358
         MVI   DB,C'N'       SET NON-HEX                        GP06358
         EX    R15,EXPRMTRT  VALID HEX ?                        GP06358
         BNZR  R14           RETURN ERROR                       GP06358
         LA    R2,14         MAX VALUE THAT WORKS               GP06358
         CR    R15,R2        IS MAX HIGHER ?                    GP06358
         BNL   *+4+2         YES; CAP IT                        GP06358
         LR    R2,R15        USE SHORTER LENGTH                 GP06358
         LA    R1,1(,R2)     FIX LENGTH TO +1                   GP06358
         XC    EXCTRTST,EXCTRTST   CLEAR AGAIN                  GP06358
         MVC   EXCTRTST+C'a'(6),EXCDCAF  CONVERSION VALUES      GP06358
         MVC   EXCTRTST+C'A'(6),EXCDCAF  CONVERSION VALUES      GP06358
         MVC   EXCTRTST+C'1'(9),EXCDC19  CONVERSION VALUES      GP06358
         EX    R15,EXPRMMVX  MOVE USER'S DATA                   GP06358
         EX    R15,EXPRMTRX  CONVERT EBCDIC TO HEX 00-0F        GP06358
         EX    R1,EXPRMPHX   PACK HEX VALUE                     GP06358
         LM    R1,R2,EXCTRTST+8   GET UP TO 7 BYTES; RIGHT-JUST GP06358
         B     EXCMASKY      AND DO GOOD RETURN                 GP06358
EXCPTINT BCTR  R4,0          SET FOR EXECUTE                    GP13306
         MIN   R4,(R15)      USE SHORTER VALUE                  GP13306
         EX    R4,EXPRMMVC   MOVE (PARTIAL?) VALUE              GP13306
         CH    R15,EXCPTMAX  NOT TOO LONG FOR INTEGER ?         GP05281
         BHR   R14           TOO LONG                           GP05281
         MVI   DB,C'N'       SET NON-NUMERIC                    GP05281
         MVC   EXCTRTST,EXCTRTST-1   PROPAGATE                  GP13306
         XC    EXCTRTST+C'0'(10),EXCTRTST+C'0'  ACCEPT 0-9      GP08313
         EX    R15,EXPRMTRT  TEST FOR NUMERICS ONLY             GP08313
         BNZR  R14           NOT VALID - RETURN                 GP05281
         EX    R15,EXPRMPAK  CONVERT TO PACKED FORM             GP05281
         CVB   R2,EXCPRMDB   CONVERT TO BINARY IN R2 RETURN     GP05281
         SPACE 1                                                GP05281
EXCMASKY LA    R5,2(R15,R5)  SKIP SEPARATOR                     GP05132
EXCMASKX MVI   DB,C' '       SHOW SUCCESSFUL                    GP05132
         B     4(,R14)       TAKE GOOD RETURN                   GP05132
EXCPRMDB DC    D'0'    1/2   LOCAL WORK AREA                    GP05281
EXCPSTOP DC    0A(0),CL4'   ,'    STOP ON COMMA AND BLANK       GP05132
EXPRNBLK TRT   0(*-*,R5),EXCTRTNB  LOOK FOR NON-BLANK           GP05132
EXPRMTRT TRT   0(*-*,R5),EXCTRTST  LOOK FOR STOPPER             GP05132
EXPRMMVC MVC   0(*-*,R3),0(R5)     MOVE PARM                    GP05132
EXPRMPAK PACK  EXCPRMDB,0(0,R5)  PACK NUMERIC INPUT             GP05281
EXPRMPHX PACK  EXCTRTST+8(9),EXCTRTST+00(0)  PACK HEX           GP06358
EXPRMTRX TR    EXCTRTST+00(0),EXCTRTST  CONVERT EBCDIC TO 0-F   GP06358
EXPRMMVX MVC   EXCTRTST+00(0),0(R3)   COPY USER'S DATA          GP06358
EXCPTMAX DC    H'9'     1/2  MAXIMUM INTEGER DIGITS             GP05281
EXCTRTST DC    XL256'0' 2/2  STOPPERS FILLED IN LATER           GP05132
EXCTRTNB DC    256AL1(*-EXCTRTNB)  STOP ON ALL EXCEPT NULL      GP05132
         ORG   EXCTRTNB+C' '   AND SPACE                        GP05132
         DC    AL1(0)                                           GP05132
         ORG   EXCTRTNB+256                                     GP05132
EXCDCAF  DC    X'0A0B0C0D0E0F'   HEX CONVERT A-F                GP06358
EXCDC19  DC    X'010203040506070809' CONVERT 1-9                GP06358
         SPACE 1
EXHBCMEN DS    0D           END OF MODULE
         SIZER ,COMM
         MEND  ,
