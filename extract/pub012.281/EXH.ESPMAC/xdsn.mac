         MACRO ,
&NM      XDSN  &IN,&ERR=,&DSN=DRDSN,&MEM=,&CMEM=          ADDED GP08090
.*
.*   REPLACEMENT FOR INDSN MACRO - NEW PREFIX CODE IN MAPWORK
.*
.*   WHEN INPUT IS 'REPLY', THEN R5 AND R6 ARE USED AND RESTORED
.*   DSN= SPECIFIES 44 BYTE OUTPUT DATA SET NAME FIELD
.*   MEM= SPECIFIES AN 8 BYTE MEMBER NAME FIELD
.*   CMEM= SPECIFIES A MEMBER FIELD THAT IS USED ONLY WHEN THE INPUT
.*         CONTAINS A MEMBER NAME
.*   ERR= SPECIFIES A LABEL BRANCHED TO ON ERROR
.*
         GBLC  &MACPLAB,&MACPSIZ
         LCLA  &I,&J,&K
         LCLC  &C,&L,&WR
&L       SETC  'L'''
&WR      SETC  'R4'
&MACPLAB SETC  '&NM'
         AIF   ('&IN' NE '').HAVEIN
.MISSIN  MNOTE 8,'XDSN: INPUT DSN REQUIRED'
         MEXIT ,
.HAVEIN  AIF   ('&IN' NE 'REPLY').NOTREP
         MACPARM R0,(R5)     COPY CURRENT CURSER
         MACPARM R1,(R6)     COPY END
         MACPARM R1,(R0),OPR=SR  INPUT LENGTH
         AGO   .DONEIN
.NOTREP  AIF   (N'&IN GT 1).EXPLEN
         MACPARM R0,&IN,QUOTE=LA   LOAD INPUT ADDRESS
         MACPARM R1,&MACPSIZ   IMPLICIT LENGTH
         AGO   .DONEIN
.EXPLEN  MACPARM R0,&IN(1),QUOTE=LA   LOAD ADDRESS
         MACPARM R1,&IN(2)   LOAD LENGTH
.DONEIN  MACPARM R15,EXW@DSN,OP=L   GET DSN PROCESSOR
         MACPARM R14,R15,OP=BALR,OPR=BALR  INVOKE IT
         AIF   ('&IN' NE 'REPLY').NOREST
         MACPARM R5,DB2,OP=L   RESTORE SCAN POINTER
.NOREST  MACPARM R15,R15,OP=LTR,OPR=LTR
         MACPARM &ERR,MODE=1,OP=BNZ,OPR=BNZR,NULL=SKIP
         AIF   ('&DSN' EQ '' AND '&MEM' EQ '' AND '&CMEM' EQ '').MEND
         MACPARM &WR,R0,OP=LR,OPR=LR
         AIF   ('&DSN' EQ '').NODSN
         MACPARM R14,&DSN
         MVC   0(44,R14),0(&WR)    MOVE RETURNED DSN
.NODSN   AIF   ('&MEM' EQ '').CMEM
         MACPARM R14,&MEM
         MVC   0(8,R14),44(&WR)    MOVE MEMBER OR BLANKS
.CMEM    AIF   ('&CMEM' EQ '').MEND
         MACPARM R14,&CMEM
         CLI   44(&WR),C' '   MEMBER NAME SUPPLIED ?
         BNH   *+4+6         NO; SKIP
         MVC   0(8,R14),44(&WR)    MOVE NON-BLANK MEMBER
.MEND    MEND  ,
