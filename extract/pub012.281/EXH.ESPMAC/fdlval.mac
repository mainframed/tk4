         MACRO ,                                       ADDED ON GP12303
&NM      FDLVAL &ATTR1,&ATTR2,&FROM=
.*
.*   INNER MACRO FOR FDLINE,FDLITEM, AND FDLSET TO DERIVE BIT
.*   COMBINATIONS FOR ATTRIBUTE CONTROL BYTES
.*
.*   ATTRIBUTES:
.*     COLOR, HIGH-LIGHTING, AND INPUT VS. OUTPUT
.*     THE FIRST, AND ONLY FIRST, OPERAND MAY BE A LIST, SIMILAR TO
.*       FDLINE AND FDLITEM ENTRIES.
.*
.*   IN, OUT, INT(ENSE OUT),
.*   RED, BLUE, GREEN, CYAN, TURQ(OISE), WHITE, YELLOW
.*   BLACK (NON-DISPLAY) - FORCES INPUT MODE
.*
.*   THIS MACRO GENERATES NEITHER THE LINE NOR FLAG FIELDS; USE FDLINE
.*   OR FDLITEM FOR THOSE
.*
.*
         GBLB  &MACBIT0,&MACBIT1,&MACBIT2,&MACBIT3
         GBLB  &MACBIT4,&MACBIT5,&MACBIT6,&MACBIT7
         GBLB  &MACPERR,&MACPNUL
.*
         LCLC  &C,&D,&MOD
         LCLB  &F0,&F1,&F2,&F3,&F4,&F5,&F6,&F7
         LCLB  &INP,&INT,&BLACK,&UCOL,&UHI
         LCLA  &I,&J,&K,&L,&M,&N,&NN,&NJ                        GP12357
&N       SETA  N'&SYSLIST
         AIF   (&N GE 1).FIELD    HAVE MULTIPLE ARGUMENTS
         MNOTE 4,'FDLVAL: NO ATTRIBUTES SUPPLIED'
&MACPERR SETB  1
&MACPNUL SETB  1
         MEXIT ,
.*
.FIELD   ANOP  ,
&MOD     SETC  '&FROM'
         AIF   ('&MOD' NE '').OKMOD
&MOD     SETC  'FDLVAL'
.OKMOD   ANOP  ,
&MACBIT0 SETB  0
&MACBIT1 SETB  0
&MACBIT2 SETB  0
&MACBIT3 SETB  0
&MACBIT4 SETB  0
&MACBIT5 SETB  0
&MACBIT6 SETB  0
&MACBIT7 SETB  0
.*
&J       SETA  0             START WITH FIRST
&L       SETA  N'&SYSLIST    NUMBER OF ARGUMENTS
&F0      SETB  0             MUST BE OFF
&F1      SETB  0             MUST BE OFF (RANGE 01-3F)
&F2      SETB  0
&F3      SETB  0   DFLT 1 0  OUTPUT  ;  1 1  OUT INTENSE
&F4      SETB  0        0 1  INPUT   ;  0 0  NON-DISPLAY
&F5      SETB  0             COLOR: GREEN
&F6      SETB  0             COLOR: RED
&F7      SETB  0             COLOR: BLUE
&INP     SETB  0             INPUT
&INT     SETB  0             INTENSE
&BLACK   SETB  0             NON-DISPLAY
.FDLOOM  AIF   (&J GE &L).EXPFLD
&J       SETA  &J+1
&NJ      SETA  0                                                GP12357
&NN      SETA  N'&SYSLIST(&J)                                   GP12357
.FDLOOP  AIF   (&NJ GE &NN).FDLOOM                              GP12357
&NJ      SETA  &NJ+1                                            GP12357
.*                                                              GP12357
&C       SETC  '&SYSLIST(&J,&NJ)'                               GP12357
         AIF   ('&C' EQ '').FDLOOP
         AIF   ('&C' EQ 'REVERSE').REVERSE
         AIF   ('&C' EQ 'BLINK').BLINK
         AIF   ('&C' EQ 'UNDER').UNDER
         AIF   ('&C' EQ 'DFLT').NULL
         AIF   ('&C' EQ 'BL').BLUE
         AIF   ('&C' EQ 'RE').RED
         AIF   ('&C' EQ 'PI').PINK
         AIF   ('&C' EQ 'MA').PINK
         AIF   ('&C' EQ 'GR').GREEN
         AIF   ('&C' EQ 'CY').TURQ
         AIF   ('&C' EQ 'TU').TURQ
         AIF   ('&C' EQ 'YE').YELLOW
         AIF   ('&C' EQ 'WH').WHITE
         AIF   ('&C' EQ 'BK').BLACK
         AIF   ('&C' EQ 'ND').BLACK
         AIF   ('&C' EQ 'IN').INPUT
         AIF   ('&C' EQ 'BLUE').BLUE
         AIF   ('&C' EQ 'RED').RED
         AIF   ('&C' EQ 'PINK').PINK
         AIF   ('&C' EQ 'MAGENTA').PINK
         AIF   ('&C' EQ 'GREEN').GREEN
         AIF   ('&C' EQ 'CYAN').TURQ
         AIF   ('&C' EQ 'TURQ').TURQ
         AIF   ('&C' EQ 'TURQOISE').TURQ
         AIF   ('&C' EQ 'YELLOW').YELLOW
         AIF   ('&C' EQ 'WHITE').WHITE
         AIF   ('&C' EQ 'BLACK').BLACK
         AIF   ('&C' EQ 'INPUT').INPUT
         AIF   ('&C' EQ 'OUT').OUTPUT
         AIF   ('&C' EQ 'INT').INTENSE
         AIF   ('&C' EQ 'INTENSE').INTENSE
&I       SETA  &J-1
       MNOTE 8,'&MOD: ATTRIBUTE &I INVALID - &C NOT A VALID ENTRY'
         AGO   .FDLOOP
.BLUE    AIF   (&UCOL).DUPE
         AIF   (&UHI).CONFL
&UCOL    SETB  1
&F5      SETB  0
&F6      SETB  0
&F7      SETB  1
         AGO   .FDLOOP
.*
.RED     AIF   (&UCOL).DUPE
         AIF   (&UHI).CONFL
&UCOL    SETB  1
&F5      SETB  0
&F6      SETB  1
&F7      SETB  0
         AGO   .FDLOOP
.*
.PINK    AIF   (&UCOL).DUPE
         AIF   (&UHI).CONFL
&UCOL    SETB  1
&F5      SETB  0
&F6      SETB  1
&F7      SETB  1
         AGO   .FDLOOP
.*
.GREEN   AIF   (&UCOL).DUPE
         AIF   (&UHI).CONFL
&UCOL    SETB  1
&F5      SETB  1
&F6      SETB  0
&F7      SETB  0
         AGO   .FDLOOP
.*
.TURQ    AIF   (&UCOL).DUPE
         AIF   (&UHI).CONFL
&UCOL    SETB  1
&F5      SETB  1
&F6      SETB  0
&F7      SETB  1
         AGO   .FDLOOP
.*
.YELLOW  AIF   (&UCOL).DUPE
         AIF   (&UHI).CONFL
&UCOL    SETB  1
&F5      SETB  1
&F6      SETB  1
&F7      SETB  0
         AGO   .FDLOOP
.*
.WHITE   AIF   (&UCOL).DUPE
         AIF   (&UHI).CONFL
&UCOL    SETB  1
&F5      SETB  1
&F6      SETB  1
&F7      SETB  1
         AGO   .FDLOOP
.*
.BLACK   AIF   (&UCOL).DUPE
         AIF   (&UHI).CONFL
&UCOL    SETB  1
&F3      SETB  0
&F4      SETB  0
&F5      SETB  0
&F6      SETB  0
&F7      SETB  1             FORCE NON-ZERO
&BLACK   SETB  1
         AGO   .FDLOOP
.*
.BLINK   AIF   (&UCOL).CONFL
&UHI     SETB  1
&F2      SETB  1
&F7      SETB  1
         AGO   .FDLOOP
.*
.REVERSE AIF   (&UCOL).CONFL
&UHI     SETB  1
&F2      SETB  1
&F6      SETB  1
         AGO   .FDLOOP
.*
.UNDER   AIF   (&UCOL).CONFL
&UHI     SETB  1
&F2      SETB  1
&F5      SETB  1
         AGO   .FDLOOP
.*
.NULL    AIF   (&UCOL).CONFL
&UHI     SETB  1
&F2      SETB  1
&F5      SETB  0
&F6      SETB  0
&F7      SETB  0
         AGO   .FDLOOP
.*
.INPUT   ANOP
&F3      SETB  0
&F4      SETB  1
&INP     SETB  1
         AIF   (&INT).CONFII
         AGO   .FDLOOP
.*
.OUTPUT  ANOP  ,
&F3      SETB  1
&F4      SETB  0
         AGO   .FDLOOP
.*
.INTENSE ANOP  ,
&F4      SETB  1
&INT     SETB  1
         AIF   (&INP).CONFII
&F3      SETB  1
         AGO   .FDLOOP
.CONFII  MNOTE *,'&MOD: INTENSE INPUT NOT SUPPORTED'
         AGO   .FDLOOP
.*
.CONFL MNOTE 8,'&MOD: CONFLICT - COLOR AND HIGH-LIGHTING NOT SUPPORTED'
         AGO   .FDLOOP
.*
.DUPE    MNOTE 8,'&MOD: ERROR - COLOR PREVIOUSLY SPECIFIED'
         AGO   .FDLOOP
.*
.EXPFLD  AIF   (NOT &BLACK).EXPFLD1
&F4      SETB  0             FORCED INPUT MODE
         AIF   (NOT &F3).EXPFLD2
         MNOTE 4,'&MOD: CONFLICTING NON-DISPLAY AND OUTPUT ATTR &I'
.EXPFLD1 AIF   (&F3 OR &F4).EXPFLD2
&F3      SETB  1             OUTPUT IS DEFAULT
.*
.EXPFLD2 ANOP  ,
&MACBIT0 SETB  (&F0)
&MACBIT1 SETB  (&F1)
&MACBIT2 SETB  (&F2)
&MACBIT3 SETB  (&F3)
&MACBIT4 SETB  (&F4)
&MACBIT5 SETB  (&F5)
&MACBIT6 SETB  (&F6)
&MACBIT7 SETB  (&F7)
 MNOTE 0,'&MOD: &F0 &F1 &F2 &F3 &F4 &F5 &F6 &F7 '
.MEND    MEND  ,
