         MACRO
&NM      #WAIT &ECBLIST,&R=,&B=
.*   THIS MACRO WAITS ON AN ECB LIST, AND RETURNS THE OFFSET (0- )
.*   OF THE LOWEST POSTED ECB IN THE LIST.
.*   R= MAY SPECIFY A REGISTER 2-14 THAT WILL POINT TO THE POSTED
.*      ECB
.*   B= MAY SPECIFY THE NAME OF A TABLE CONTAINING 4-BYTE INSTRUCTIONS
.*      SUCH THAT THE TABLE+OFFSET ENTRY IS BRANCHED TO.
.*   OMITTING B AND R CAUSES THE MACRO TO RETURN THE OFFSET IN R15
.*
.*   THIS MACRO WAS SUGGESTED BY THE BTAM TWAIT MACRO, BUT IS SHORTER.
.*
         LCLC  &M
&M       SETC  '&SYSNDX'
&NM      MACPARM R1,&ECBLIST,NULL=(R1) LOAD LIST ADDRESS
ZZ&M.N   MACPARM R0,(R1),OP=LR
ZZ&M.L   L     R15,0(,R1)    GET NEXT ECB
         TM    0(R15),X'40'  IS COMPLETE BIT ON?
         BNZ   ZZ&M.X          YES; USE THIS
         LA    R1,4(,R1)     GO TO NEXT LIST ENTRY
         LTR   R15,R15       IF NOT, CHECK LIST END
         BNM   ZZ&M.L          AND REPEAT FOR NEXT ECB
.*
.*       WAIT  ,ECBLIST=(1)
ZZ&M.W   LCR   R1,R0         RELOAD ECB LIST ADDRESS
         LA    R0,1          SINGLE EVENT
         SVC   1             WAIT
         LCR   R1,R1
         B     ZZ&M.N          AND RETRY
.*
ZZ&M.X   MACPARM &R,0(,R15),NULL=SKIP  LOAD ECB ADDRESS IF REQUESTED
         MACPARM R15,(R1),OP=LR  ECB ENTRY WITHOUT HIGH BIT
         SR    R15,R0        LESS BASE
         AIF   (T'&B EQ 'O').MEND
         B     &B.(R15)        BRANCH TO MATCHING ADDRESS
.MEND    MEND
