         MACRO
&NM      RESTRICT &OPTS,&B=EXCXCB
         GBLC  &MACPLAB
.**********************************************************************
.*                                                                   **
.*   EXHIBIT MACRO TO TEST (AND FAIL) IMPROPER MODULE INVOCATION     **
.*                                                                   **
.**********************************************************************
         LCLA  &I,&J,&M
         LCLB  &TRUE,&F(36)                                     GP07007
         LCLC  &HUH
&M       SETA  N'&SYSLIST
&J       SETA  &SYSNDX
&I       SETA  0
&MACPLAB SETC  '&NM'
.LOOP    AIF   (&I GE &M).LEND
&I       SETA  &I+1
&HUH     SETC  '&SYSLIST(&I)'
         AIF   ('&HUH' EQ '').LOOP
&F(2) SETB (('&HUH' EQ 'PCP') OR &F(2))
&F(3) SETB (('&HUH' EQ 'MFT') OR &F(3))
&F(4) SETB (('&HUH' EQ 'MVT') OR &F(4))
&F(6) SETB (('&HUH' EQ 'MP') OR &F(6))
&F(7) SETB (('&HUH' EQ 'DAT') OR &F(7))
&F(9)    SETB  (('&HUH' EQ 'TSO') OR &F(9))                      *TSO*
&F(10)   SETB  (('&HUH' EQ 'NOTSO') OR &F(10))                   *TSO*
&F(11)   SETB  (('&HUH' EQ 'DEBUG') OR &F(11))
&F(12) SETB (('&HUH' EQ 'HASP') OR &F(12))
&F(12) SETB (('&HUH' EQ 'JES2') OR &F(12))                       89211
&F(13) SETB (('&HUH' EQ 'CON') OR &F(13))
&F(14) SETB (('&HUH' EQ 'PAS') OR &F(14))
&F(15) SETB (('&HUH' EQ 'KEY') OR &F(15))
&F(16) SETB (('&HUH' EQ 'UNL') OR &F(16))
&F(17)   SETB  (('&HUH' EQ 'VM') OR &F(17))                      79194
&F(18)   SETB  (('&HUH' EQ 'GAME') OR ('&HUH' EQ 'GAMES') OR &F(18))
&F(21)   SETB  (('&HUH' EQ '12') OR &F(21))
&F(22)   SETB  (('&HUH' EQ '24') OR &F(22))
&F(27)   SETB  (('&HUH' EQ '40') OR &F(27))
&F(28)   SETB (('&HUH' EQ '80') OR &F(28))
&F(29)   SETB (('&HUH' EQ '132') OR &F(29))                      89211
&F(30)   SETB (('&HUH' EQ 'DRDEBUG') OR &F(30))                 GP07007
         AGO   .LOOP
.LEND    ANOP
&TRUE    SETB (&F(2) OR &F(3) OR &F(4) OR &F(6) OR &F(7))
         AIF   (NOT &TRUE).SK1
         NEED  CVT
&NM   MACPARM  WRKSYS,&F(2)*CVT1SSS+&F(3)*CVT2SPS+&F(4)*CVT4MS1+&F(6)*C*
               VT4MPS+&F(7)*CVT6DAT,OP=TM  TEST SYSTEM BITS
         BNZ   &B .          ISSUE UNAVAILABLE MESSAGE
.SK1     ANOP
&TRUE    SETB (&F(13) OR &F(14) OR &F(16) OR &F(15))
         AIF   (NOT &TRUE).SK2
         MACPARM EXCPRIV,EXCPSYS,OP=TM KEY MODE ?
&TRUE    SETB (&F(13) OR &F(14) OR &F(16))                       *TSM*
         AIF   (NOT &TRUE).N15                                   *TSM*
         BO    ZXCR&J .      SKIP ALL TESTS IF IN KEY MODE
         AIF   (NOT &F(13) OR NOT &F(14)).N13
         TM    EXCPRIV,EXCONSOL .  CONTROL DESK ?
         BZ    *+12 .        NO; SKIP THE NEXT TEST
         TM    EXCPRIV,EXCONCAN .   SUPPLIED PASSWORD ?
.N15     BZ    &B                NO, KILL IT                     *GPP*
&F(13)   SETB  0
&F(14)   SETB  0
.N13     ANOP
         AIF   (NOT &F(16) AND NOT &F(14)).N16
         TM    EXCPRIV,EXCAUTH*&F(16)+EXCONCAN*&F(14) .  PASS/UNL ?
         BZ    &B .          NO, TELL
.N16     AIF   (NOT &F(13)).SS2
         TM    EXCPRIV,EXCONSOL .  CONTROL DESK CRT ?
         BNZ   &B .          YES, DISALLOW
.SS2     ANOP
ZXCR&J   EQU   * .           BYPASS IF IN KEY MODE
.SK2     AIF   (NOT &F(12)).SKCRT
         MACPARM HASPHCT+1(3),ZEROES,OP=CLC
         BE    &B .          HASP NOT UP
.SKCRT   AIF   (NOT &F(21) AND NOT &F(22)).SKCB
         MACPARM LNO+1,LN24+LN32,OP=TM . MORE THAN 15 LINES ?    78051
         AIF   (&F(22)).SKC2
         BZ    &B .          REJECT 12 LINES
         AGO   .SKCB
.SKC2    BNZ   &B .          REJECT MORE THAN 15 LINES           78051
.SKCB    AIF   (NOT &F(27) AND NOT &F(28) AND NOT &F(29)).SKOTH  89211
         MACPARM LSIZE+1,80,OP=CLI  40 OR 80 BYTE CRT ?          88249
         BC    2*&F(29)+8*&F(28)+4*&F(27),&B                     91364
.SKOTH   AIF   (NOT &F(11)).SKNDEB
         AIF   (NOT &F(9)).SKBOPT  SKIP COMBINED TEST            78216
         MACPARM OPTS,DEBUG+OPTSO,OP=TM DEBUG OR TSO ?           78216
         BNZ   &B            EITHER; SKIP                        78216
         AGO   .SKTSO                                            78216
.SKBOPT  ANOP  ,                                                 78216
         MACPARM OPTS,DEBUG,OP=TM . RUNNING DEBUG MODE ?
         BNZ   &B .          REJECT DEBUG MODE
.SKNDEB  AIF   (NOT &F(9) AND NOT &F(10)).SKTSO                  *TSO*
         MACPARM OPTS,OPTSO,OP=TM . RUNNING UNDER TSO ?          *TSO*
         AIF   (NOT &F(9)).NTTSO                                 *TSO*
         BNZ   &B .          REJECT TSO                          *TSO*
         AGO   .SKTSO                                            *TSO*
.NTTSO   ANOP  ,                                                 *TSO*
         BZ    &B .          REJECT UNLESS TSO                   *TSO*
.SKTSO   AIF   (NOT &F(17) AND NOT &F(18)).NOSAVP                79194
         MACPARM SQSAVPO,SQSAFVM*&F(17)+SQSNGAM*&F(18),OP=TM     79194
         BNZ   &B .          REJECT VM/GAME REQUEST              79194
.NOSAVP  AIF   (NOT &F(30)).SS30
         TM    DRFLAG,DRDEBUG  DASD FUNCTION IN DEBUG MODE ?    GP07007
         BNZ   &B .          YES, DISALLOW                      GP07007
.SS30    ANOP  ,                                                GP07007
         MEND  ,
