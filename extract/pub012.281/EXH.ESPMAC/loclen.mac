         MACRO ,
&NM      LOCLEN &AD,&LN,&BYTE=C' '                     ADDED ON GP09333
         LCLC  &L
&L       SETC  'L'''
.*  LOCLEN SUGGESTED BY FINDLBLK, AS PUBLISHED ON ASSEMBLER-LIST
.*    BY CLEMENT VICTOR CLARKE
.*
&NM      MACPARM R15,&AD     LOAD STRING ADDRESS
         MACPARM R1,&LN,NULL=&L&AD   LOAD THE LENGTH
         MACPARM R0,=4&BYTE,OP=L     SEARCH ARGUMENT
         MACPARM R14,=H'-4',OP=LH    DECREMENT
         CH    R1,=H'4'      LONG ENOUGH FOR WORD TEST?
         BL    ZZ&SYSNDX.W   USE BYTE COMPARE
         AR    R1,R15        END ADDRESS
         AR    R1,R14        LAST WORD
         CLM   R0,15,0(R1)   LAST FOUR MATCH ?
         BNE   ZZ&SYSNDX.X   NO; USE BYTE COMPARE
         NR    R1,R14        TRUNCATE TO WORD BOUNDARY
ZZ&SYSNDX.L C  R0,0(,R1)     WORD MATCHED?
         BNE   ZZ&SYSNDX.X   NO; USE BYTE COMPARE
         BXH   R1,R14,ZZ&SYSNDX.L   TRY AGAIN
ZZ&SYSNDX.V SR R1,R1
         B     ZZ&SYSNDX.Z   EXIT
ZZ&SYSNDX.W LTR R1,R1        ANY USABLE LENGTH ?
         BNP   ZZ&SYSNDX.V   NO; RETURN 0
         AR    R1,R15        END ADDRESS
         AR    R1,R14        LAST WORD
ZZ&SYSNDX.X LA R1,3(,R1)     ADVANCE TO END
         LH    R14,=H'-1'    DECREMENT BY 1
ZZ&SYSNDX.Y CLI 0(R1),&BYTE   MATCH ?
         BNE   *+8           NO; DONE
         BXH   R1,R14,ZZ&SYSNDX.Y  TRY AGAIN
         SR    R1,R15        CALCULATE LENGTH - 1
         SR    R1,R14        TRUE LENGTH
ZZ&SYSNDX.Z DS 0H
         MEND  ,
