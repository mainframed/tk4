         MACRO ,
&NM      SERVSORT &DSECT=YES,&PFX=SOP,&LOW=0,&HIGH=0,&SEQ=C'A',        *
               &COO=0,&COL=0,&LEN=0,&LAST=,&MF=L         ADDED ON 82024
.*  CANCELING THE &RET PARAMETER PRODUCES IN-LINE CODE, BUT NO ERROR
.*    INDICATION.
         LCLC  &P,&NAME,&FUN,&LIST                              GP15129
&NAME    SETC  '&NM'
&P       SETC  'SOP'
&FUN     SETC  '&MF(1)'
&LIST    SETC  '&MF(2)'
         AIF   ('&NAME' NE '').HAVENM
&NAME    SETC  'SERVSORT'
.HAVENM  AIF   ('&DSECT' NE 'YES' OR '&FUN' NE 'L').NOSECT      GP15129
&NAME    DSECT ,
         AGO   .TESTP
.NOSECT  AIF   ('&NM' EQ '').TESTP
&NM      DS    0A .
.TESTP   AIF   ('&PFX' EQ '').HAVEP
&P       SETC  '&PFX'
.HAVEP   ANOP  ,
         AIF   ('&FUN' EQ 'L').LIST                             GP15129
&NM      MACPARM R1,&LIST,NULL=&P.LOW       LIST START          GP15129
         MACLAST 0(,R1),&LOW                START ENTRY ADDRESS GP15129
         MACLAST 4(,R1),&HIGH&LAST,NULL=SKIP  END ADDRESS/COUNT GP15129
         AIF   (T'&LAST EQ 'O').HAVENUM                         GP15129
         OI    4(R1),X'80'   INDICATE END ADDRESS               GP15129
.HAVENUM MACLAST 8(,R1),&SEQ,OP=STC    'A' OR 'D' SEQUENCE      GP15129
         MACLAST 9(,R1),&COO,OP=STC    KEY OFFSET               GP15129
         MACLAST 10(,R1),&COL,OP=STC   KEY LENGTH               GP15129
         MACLAST 11(,R1),&LEN,OP=STC   ENTRY LENGTH             GP15129
         AIF   ('&FUN' EQ 'E').CALL                             GP15129
.LIST    ANOP  ,                                                GP15129
&P.LOW   DC    A(&LOW)       LOWEST ENTRY ADDRESS
&P.FAD   EQU   X'80'         ON IN HIGH TO SIGNAL ADDRESS
&P.HIGH  DC    A(&HIGH)      ADDRESS (-) / ELSE # OF ENTRIES
&P.SEQ   DC    AL1(&SEQ)     SORT SEQUENCE (C'A' OR C'D')
&P.COO   DC    AL1(&COO)     COMPARE OFFSET
&P.COL   DC    AL1(&COL)     COMPARE LENGTH
&P.LEN   DC    AL1(&LEN)     ENTRY LENGTH
         AIF   ('&MF' EQ 'L').MEND                              GP15129
.CALL    SERVCALL SORTH,(R1) SORT                               GP15129
.MEND    MEND  ,
