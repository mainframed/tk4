         MACRO
&L       WTCLR &TXT,&FLAG=(PFX,IMM),&DESC=2,&PAD=,                78310*
               &TYPE=BUILD,&MSGSECT=                              78310
.*       TXT   TEXT TO BE DISPLAYED, DELIMITED BY APOSTROPHES
.*       NAME  LABEL TO BE GENERATED ON DC
.*       FLAG  LIST OF ONE OR MORE OPTIONS:
.*             UCMID                    ROUTE TO SPECIFIC CONSOLE
.*             ML                       MULTI-LINE WTO
.*             PFX                      PREFIX '&PFX' FROM WTBLD
.*                                      SUFFIX '&SFX' FROM WTBLD
.*             WTOR                     CONSTRUCT WTOR
.*             IMM                      ISSUE SVC 35
.*       PAD   SPACE TO BE ALLOWED FOLLOWING TEXT
.*       DESC  DESCRIPTOR FOR WTO/WTOR
.*
.*             AT LEAST ONE OF TXT, NAME MUST BE SPECIFIED
.*
.*       TYPE  TYPE OF OPERATION TO BE PERFORMED:
.*             BUILD                    BUILD NEW WTO/WTOR
.*             EXTEND                   ADD TO EXISTING WTO/WTOR
         GBLC  &WTOSECT,&MACPLAB                                  82171
         LCLA  &I,&J,&ILINE,&ITXT
         LCLB  &OPTS(5)
         LCLC  &LB,&LD,&LP,&LQ
         LCLC  &ALFABET,&LTR
         LCLC  &ENTRY
         LCLC  &FLAGS(5),&OPTION,&OPTSC
&MACPLAB SETC  '&L'                                               82171
         AIF   ('&WTOSECT' EQ '').SECTSET                         78310
         AIF   ('&MSGSECT' EQ '&WTOSECT').SECTOK                  78310
         AIF   ('&MSGSECT' EQ '').SECTOK                          78310
         MNOTE 8,'MSGSECT=&MSGSECT INCONSISTENT'                  78310
         MNOTE 8,'MSGSECT=&WTOSECT PREVIOUSLY SPECIFIED'          78310
         AGO   .SECTOK                                            78310
.SECTSET ANOP  ,                                                  78310
&WTOSECT SETC  '&MSGSECT'                                         78310
         AIF   ('&WTOSECT' NE '').SECTOK                          78310
&WTOSECT SETC  'MSGCSECT'                                         78310
.SECTOK  ANOP  ,                                                  78310
&ITXT    SETA  1
&FLAGS(1) SETC 'UCMID'
&FLAGS(2) SETC 'ML'
&FLAGS(3) SETC 'WTOR'
&FLAGS(4) SETC 'PFX'
&FLAGS(5) SETC 'IMM'
&ALFABET SETC  'ABCDEFGHIJKLMNOPQRSTUVWXYZ'
&LD      SETC  '$'
&LQ      SETC  'L'''
.NXTLINE ANOP
&ILINE   SETA  (&ITXT+1)/2
&LTR     SETC  '&ALFABET'(&ILINE,1)
&OPTSC   SETC  '+$SCON'
         AIF   ('&SYSLIST(&ITXT)'(1,2) EQ 'S(').NAMEOK
&OPTSC   SETC  ''
&LB      SETC  '&SYSLIST(&ITXT,2)'
         AIF   ('&SYSLIST(&ITXT,2)' NE '').NAMEOK
         AIF   ('&SYSLIST(&ITXT)' NE '').GENAME
         MNOTE 12,'PARAMETER # &ITXT INVALID'
         MNOTE *,'LINE &ILINE MUST SPECIFY EITHER NAME OR TEXT'
         MEXIT
.*
.GENAME  ANOP
&LB      SETC  '@&SYSNDX'.'&LTR'
.NAMEOK  AIF   (N'&SYSLIST EQ 1 OR &ITXT GT 1).NOML
&OPTS(2) SETB  1
&OPTION  SETC  '$ML'
&LP      SETC  '+'
.NOML    AIF   ('&OPTSC' NE '' OR '&SYSLIST(&ITXT,1)' EQ '').GENOP
&WTOSECT CSECT ,                                                  78310
&LB      DC    C&SYSLIST(&ITXT,1)
&SYSECT  CSECT ,                                                  78310
.GENOP   AIF   (&ITXT GT 1).NOT1ST
         AIF   (&I EQ N'&FLAG).OPTDONE
&I       SETA  &I+1
&J       SETA  1
.GENLOOP AIF   ('&FLAG(&I)' EQ '&FLAGS(&J)').SETOPT
&J       SETA  &J+1
         AIF   (&J LE 5).GENLOOP
         MNOTE 12,'&FLAG(&I) IS INVALID OPTION'
         MEXIT
.*
.*             CHECK FOR OPTION PREVIOUSLY SET
.SETOPT  AIF   (&OPTS(&J)).GENOP
.*             SET OPTION AND FLAG BIT
&OPTION  SETC  '&OPTION'.'&LP'.'&LD&FLAG(&I)'
&OPTS(&J) SETB 1
&LP      SETC  '+'
         AGO   .GENOP
.OPTDONE AIF   ('&OPTION' NE '').TESTOPT
&OPTION  SETC  '0'
.*
.TESTOPT AIF   (&OPTS(2) AND &OPTS(3)).BUM1ML
         AIF   (NOT &OPTS(3) OR '&TYPE' NE 'EXTEND').TESTYPE
         MNOTE 12,'TYPE=EXTEND ILLEGAL WITH WTOR - TYPE=BUILD ASSUMED'
&ENTRY   SETC  'WTOBUILD'
         AGO   .BAL
.BUM1ML  MNOTE 12,'ML ILLEGAL WITH WTOR - MACRO TERMINATED'
         MEXIT
.*
.TESTYPE ANOP
&ENTRY   SETC  'WTOEXTND'
         AIF   ('&TYPE' EQ 'EXTEND').BAL
&ENTRY   SETC  'WTOBUILD'
         AIF   ('&TYPE' EQ 'BUILD').BAL
         MNOTE 12,'TYPE=&TYPE INVALID - TYPE=BUILD ASSUMED'
.BAL     AIF   ('&PAD' EQ '' OR '&PAD' EQ '0').NOPAD              82171
         MACPARM R0,&PAD                                          82171
&ENTRY   SETC  '&ENTRY'.'+2'
.NOPAD   ANOP
         MACPARM R14,&ENTRY,OP=BAL
         AIF   ('&TYPE' EQ 'EXTEND').NOT1ST
         WTLST &DESC,TYPE=DESC
.NOT1ST  AIF   ('&OPTSC' NE '').DCSCON
         DC    AL1(&LQ.&LB-1,&OPTION&OPTSC)
         DC    AL2(&LB-&WTOSECT)                                  78310
         AGO   .TSTML
.DCSCON  DC    AL1(0,&OPTION&OPTSC)
         DC    &SYSLIST(&ITXT)
.TSTML   AIF   (NOT &OPTS(2)).BYBY
         WTLTP &SYSLIST(&ITXT+1),&ILINE
.*
.BMPITXT ANOP
&ITXT    SETA  &ITXT+2
         AIF   (&ITXT LT N'&SYSLIST).NXTLINE
         AIF   (&ITXT GT N'&SYSLIST).DONE
         MNOTE 12,'NUMBER OF PARAMETERS MUST BE ODD'
         MNOTE *,'# &ITXT = &SYSLIST(&ITXT) IGNORED'
.DONE    DC    X'FFFF'
.BYBY    MEND
