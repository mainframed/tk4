RDFLT    TITLE 'E X H R D F L T  ***  LPA RESIDENT CODE'
         COPY  OPTIONS
         SPACE 1
***********************************************************************
*                                                                     *
*        COPYRIGHT 1993  EXPERT SYSTEM PROGRAMMING, INC.              *
*        COPYRIGHT 2003,2005  EXPERT SYSTEM PROGRAMMING               *
*                        176 OLD STAGE COACH ROAD                     *
*                        BRADFORD, VT 05033-8844                      *
*                                                                     *
*                    ALL RIGHTS RESERVED                              *
*                                                                     *
***********************************************************************
*  MAINTENANCE:                                                       *
*  2004-12-15   GYP  ADAPTED FOR MVS 3.8J USE (W/HERCULES)            *
*                                                                     *
***********************************************************************
         EJECT ,
***********************************************************************
*                                                                     *
*    MODULE EXHRDFLT SHOULD BE ADDED TO THE LINK-PACK AREA EITHER BY  *
*    COPYING IT TO SYS1.LPALIB OR A LOCAL LPALIB; OR IT MAY BE ADDED  *
*    TO A LINKLIB AND SPECIFIED IN AN IEALPA-- PARMLIB MEMBER.        *
*                                                                     *
*    CORRECT PERFORMANCE ALSO REQUIRES AN ENTRY IN IEFSSN-- IN PARM-  *
*    LIB FOR EXHI. THE SSN ENTRY IS THE GLOBAL ANCHOR FOR WORK SPACE. *
*    IF THE SSN ENTRY IS NOT FOUND, EXHAINIT WILL BUILD ONE.          *
*                                                                     *
*    THIS MODULE IS NEW FOR EXHIBIT X/A, AND REPLACES THE EXHR----    *
*    SERIES OF LPA RESIDENT MODULES.                                  *
*                                                                     *
***********************************************************************
         SPACE 1
         LCLB  &LOCO
&LOCO    SETB  ('&LOCAL' EQ 'PID')
EXHRDFLT START 0             RE-ENTRANT, REFRESHABLE
EXHRDFLT AMODE 31            OUR WORK AREA IS ABOVE THE LINE
EXHRDFLT RMODE 24            BELOW UNTIL INDSN IS FIXED
         SPLEVEL SET         SET DEFAULT ASSEMBLY SP LEVEL
         USING EXHRDFLT,R10  BASE SET BY CALLER
         NUSE  SQSP,R13      DECLARE SQS WORK AREA
         NUSE  WORK,R11      DECLARE DISPLAY WORK AREA
         SPACE 1
         B     BEGIN         RESCALL LOOP (+0)
         B     SCREENUP      RESCALL SCREEN (+4)                 79159
         B     INIT          RESCALL INIT (+8)                   80042
 NOP 0   B     LOCSCRN       (WYLBUR) RETURN SCREEN ADDRESS (+12)
         B     SLOWOUT2        RESERVED - TAKE ERROR RETURN
         B 2   INDSN         DSN PREFIX /NOW IN EXWORK          GP08300
         SPACE 1                                                 79159
*        INITIALIZATION CALL.  BASE=R10(PASSED),+R11,+R13        79159
         SPACE 1                                                 79159
INIT     STM   R14,R12,12(R13)  SAVE REGISTERS
         MVC   DYNWORK(ENQLEN),ENQPAT  MAKE PATTERN              93017
         ENQ   MF=(E,DYNWORK)  PRESERVE EVERYTHING               93017
***********************************************************************
*                                                                     *
*   FIND THE REQUIRED SCREEN ENTRY.  ESTABLISH NECESSARY RESOURCES,   *
*      INCLUDING SSCT, EXR WORK AREA AND EXK FOR THIS SCREEN GROUP.   *
*                                                                     *
***********************************************************************
         PUSH  USING
         L     R8,CVTPTR     GET THE CVT
         ICM   R8,15,CVTJESCT-CVTMAP(R8)  GET THE JES VECTOR TABLE
         USING JESCT,R8      DECLARE IT
         BZ    INITBACK      FAIL
         LA    R7,JESSSCT-JESCT-(SSCTSCTA-SSCT)(,R8)  HEAD OF CHAIN
INITSSF  LR    R6,R7         SAVE ANCHOR
         USING SSCT,R7       DECLARE SSCVT
         ICM   R7,15,SSCTSCTA  GET NEXT SSCT
         BZ    INITSSG       NONE; MAKE ONE
         CLC   =C'EXHI',SSCTSNAM  OUR SSN ?
         BNE   INITSSF
         B     INITSSCT      HAVE IT
INITSSG  GETMAIN RC,SP=241,LV=SSCTSIZE  SHOULD BE KEY 1
         BXH   R15,R15,INITBACK  TOO BAD
         LR    R7,R1         COPY ADDRESS
         XC    0(SSCTSIZE,R1),0(R1)  CLEAR IT
         MVC   SSCTSNAM,=C'EXHI'  IDENTIFY IT
         MVC   SSCTID,SSCTID-SSCT(R6)  COPY ID
         MVI   SSCTFLG1,SSCTUPSS  USE JES SYSOUT SERVICES
         SLR   R2,R2         JUST IN CASE
INITSSAD CS    R2,R1,SSCTSCTA-SSCT(R6)  MAKE END OF CHAIN        93044
         BE    INITSS#       WILL WONDERS NEVER CEASE
         ICM   R6,15,SSCTSCTA-SSCT(R6)  GET NEWLY ADDED ELEMENT
         BNZ   INITSSAD      SEE IF IT IS LAST
         FREEMAIN R,LV=SSCTSIZE,SP=241,A=(R7)                    93045
         B     INITBACK      HUH ?
INITSS#  L     R1,=X'00010000'  JESNRSS IS LEFT HALFWORD         93045
         AIF   (NOT &MVSSP).NOSS#                               GP04234
         CSADD JESNRSS,INC=(R1)  UPDATE SUBSYSTEM COUNT          93045
.NOSS#   ANOP  ,                                                GP04234
INITSSCT ST    R7,EXWSSCT    SAVE SSCT ADDRESS
         ST    R10,EXWSUS2   SAVE MODULE ADDRESS
         ICM   R6,15,SSCTSUSE  DO WE HAVE A WORK AREA ?
         BNZ   INITSUSE      YES; WHOOPPEE
         GETMAIN RC,LV=EXRSIZE,SP=241 LOC=(ANY,ANY)  PAGED CSA  GP04234
         LR    R6,R1         COPY
         USING EXRBASE,R6    DECLARE BASE VALUE
         XC    0(EXRSIZE,R1),0(R1)  CLEAR IT
         MVI   EXRSPLEN,241  SET SUBPOOL
         MVI   EXRSPLEN+L'EXRSPLEN-1,EXRSIZE  SAVE SIZE
         MVI   EXRBROAD,C' '                                     93019
         MVC   EXRBROAD+1(L'EXRBROAD-1),EXRBROAD  BLANK MSG      93019
         MVI   EXRBROAD+77,X'11'  MAKE SBA                       93019
         ST    R6,SSCTSUSE   ROOT OF ALL EVIL ?                  93017
INITSUSE ST    R6,EXWSUSE    SAVE MAIN WORK AREA
*---------------------------------------------------------------------*
*   RESET GLOBAL HALT/SHUTDOWN/QUIESCE                                *
*---------------------------------------------------------------------*
         MVI   EXRGSHUT,0    RESET FLAGS                        GP05095
         XC    EXRGSHAS,EXRGSHAS  ALSO RESET OWNER              GP05095
         BAL   R14,LOCPAGE   SEE IF PROPER SCREEN ENTRY EXISTS
         BNZ   INITOVER      YES; USE IT
         LH    R3,BUFSIZ     GET SCREEN SIZE REQUIRED
         LA    R3,EXKSIZE(,R3)  PLUS PREFIX
         GETMAIN RC,LV=(R3),SP=241 LOC=(ANY,ANY)   PAGED CSA    GP04234
         LR    R2,R1         COPY TO BASE
         CLRL  (R2),(R3)     CLEAR AREA
         USING EXKBASE,R2    DECLARE IT
         ST    R3,EXKSPLEN   SET LENGTH
         MVI   EXKSPLEN,241  SET SUBPOOL
         MVC   EXKSCRSZ(L'LNO+L'LSIZE),LNO  LENGTH AND WIDTH OF SCREEN
         MVC   EXKGROUP,EXWNMGRP  SAVE GROUP NAME
         MVC   EXKJESNM,EXWJ2NAM  SAVE SUBSYSTEM NAME
         MVC   EXKCHAIN,EXRSCRIN  PUT AT HEAD OF CHAIN
         ST    R2,EXRSCRIN   SET HEAD
         POP   USING
         USING EXKBASE,R2    DECLARE RETURNED ELEMENT
INITOVER XC    EXKFLAGS,EXKFLAGS  RESET ALL FLAGS                83010
         USING EXKBASE,R2    DECLARE IT
         XC    EXKXC(EXKXCLEN),EXKXC   CLEAR PREVIOUS VALUES     82347
         MVI   EXKBL,C' '     INITIALIZE 'BLANK' FIELDS          79160
         MVC   EXKBL+1(EXKBLLEN-1),EXKBL  BLANK SPECIAL FIELDS   79160
         MVI   EXKTIME,X'FF'  MAKE LAST TIME VERY NEGATIVE       79160
         ST    R2,EXWSSGRP   SAVE SCREEN ENTRY                   93018
         LA    R9,4(,R9)     TAKE GOOD RETURN
INITBACK DEQ   MF=(E,DYNWORK)  DEQUEUE FROM INITIALIZATION       93017
         LM    R14,R8,12(R13)  RESTORE REGISTERS
         RET31 R9            RETURN
         DROP  R2                                                87281
         SPACE 1
*        THIS CODE IS ENTERED AT THE BEGINNING OF EXHDZZZZ
*        IF THE CURRENT USER HAS ENQ CONTROL, THE SCREEN AND
*        RELATED VALUES (CYCLE, ETC) ARE SAVED FOR RE-USE.
*        IF THE CALLER LACKS CONTROL, THE PREVIOUS SCREEN IS
*        COPIED TO THE CALLER'S BUFFER; COUNTERS ARE UPDATED.
*              WITH THE LOOP CALL, THIS PERMITS MULTIPLE
*              EXHIBIT TASKS RUNNING AS A GROUP (SIMILAR OPTIONS)
*              TO AVOID THE OVERHEAD OF GOING THROUGH THE DISPLAY
*              CODE; ONLY ONE DOES THE COMPUTATION, THE OTHERS
*              COPY THE SCREEN DATA.
         SPACE 1
SCREENUP BAS   R14,LOCPAGE   FIND SCREEN WORK AREA
         BZ    SLOWOUT2      NONE FOUND
         L     R4,MSGAD      MAKE COMMON WORK AREA
         USING TEMPWORK,R4   DECLARE IT
         ST    R5,SAVER5     SAVE REGISTER 5                     87281
         LR    R5,R2         LOAD RETURNED AREA
         USING EXKBASE,R5    DECLARE IT
         MVC   ENQLIST(ENQLEN),ENQPAT  MAKE DEQ LIST
         LA    R14,EXKGROUP  POINT TO GROUP/SIZE
         ST    R14,ENQLIST+8  REPLACE MINOR NAME
         L     R14,BUFAD     GET SCREEN OF THIS TASK             79159
         LH    R15,BUFSIZ    GET SCREEN SIZE
         LR    R3,R15        COPY
         LA    R2,EXKSCRN   GET RESIDENT SCREEN
         TM    EXRESAD,X'80' DOES THIS USER HAVE CONTROL ?
         BZ    COMMUPD       NO; JUST UPDATE COUNTERS&SCREEN     79165
         NI    EXRESAD,X'7F' RESET CONTROL FLAG
         XR    R2,R14        FLIP FOR COMMON SUBROUTINE          80042
         XR    R14,R2        (SAVES STORAGE IN OS)               80042
         XR    R2,R14                                            80042
         BAS   R1,COMMOVE    GO TO COMMON MOVER                  80042
         LA    R2,INVOKEZ    GET DZZZZ
         USING EXHBWENT,R2
         MVC   EXKDZZ,EXHFSTRT-14   SAVE CPU VALUES
         MVC   EXKHASP(SQTRANS-HASPHCT),HASPHCT  SAVE HASP       83227
         SH    R2,=AL2(INVOKED-INVOKE)  GET DPA ADDRESS
         MVC   EXKDPA,EXHFSTRT-10  SAVE PAGING VALUES
         AIF   (NOT &LOCO).NOLO1                                 80042
         MVC   EXKXSAVE,USERXSAV  SAVE USER DATA
.NOLO1   TIME  BIN           GET SCREEN UPDATE TIME
         ST    R0,EXKTIME    AND SAVE IT
         DEQ   MF=(E,ENQLIST)     RELEASE RESOURCE
         B     SLOWOUT       GO TO TAKE SLOW EXIT                87281
         SPACE 1
*        COPYING TASK - UPDATE SCREEN AND COUNTERS
*
COMMUPD  LA    R1,COMMCOMM   SET RETURN FROM IN-LINE SUBROUTINE  80042
COMMOVE  LMVC  R14,R2        COPY OLD (CURRENT?) SCREEN          80042
         L     R3,EXWSUSE    GET WORK AREA ADDRESS BACK
         USING EXRBASE,R3    DECLARE IT
         CLC   EXRBROAD+2(L'EXRBROAD-2-2-3),BLANKS  ANY TEXT ?   88251
         BER   R1            NO; SKIP                            88251
         L     R14,BUFAD     GET SCREEN ADDRESS                  80042
         AH    R14,LSIZE     SKIP A LINE                         88250
COMMULUP AH    R14,LSIZE     GET NEXT LINE                       88250
         C     R14,MSGAD     END OF SCREEN ?                     80042
         BNLR  R1            YES; NO ROOM FOR MESSAGE            80042
         CLC   2(L'EXRBROAD-7,R14),EXRBROAD+2  ALREADY ON ?      90268
         BER   R1            YES; JUST RETURN                    90268
         CLC   0(L'EXRBROAD,R14),BLANKS  CLEAR LINE ?            80042
         BNE   COMMULUP      NO                                  80042
         TM    OPTS,DEBUG+OPTNCRT  NON-3270 ?                    80042
         BNZ   COMMUPAR      MOVE MSG WITHOUT ORDERS             80042
         TM    SQSIMFG,SQSICRT+SQSIFSC  FULL-SCREEN CAPABILITY?
         BNO   COMMUPAR      NO; NO ORDERS
         MVC   0(L'EXRBROAD,R14),EXRBROAD  MOVE MSG              80042
         LA    R15,L'EXRBROAD(,R14)  GET END MSG ADDRESS
         S     R15,BUFAD      GET SCREEN OFFSET TO NEXT LINE
         STCM  R15,3,L'EXRBROAD-2(R14)  STASH AS IS
         TM    L'EXRBROAD-2(R14),X'F0'  NEED 14 OR 16 BITS ?     88250
         BNZ   COMMUPAR      YES; LEAVE AS IS                    88250
         SRL   R15,6          HIGH BUFFER ADDRESS
         STC   R15,L'EXRBROAD-2(,R14)  SET IT
         NI    L'EXRBROAD-1(R14),X'3F'  MASK                     80042
         TR    L'EXRBROAD-2(2,R14),CONADDR  MAKE EBCDIC          80042
         BR    R1            RETURN                              80042
COMMUPAR MVC   2(L'EXRBROAD-2-2-3,R14),EXRBROAD+2  TEXT ONLY     80042
.NOBDCST BR    R1            RETURN                              80042
COMMCOMM LA    R2,INVOKEZ    GET DZZZZ
         USING EXHBWENT,R2
         MVC   EXHFSTRT-14(14),EXKDZZ   SET CPU VALUES
         MVC   HASPHCT(SQTRANS-HASPHCT),EXKHASP  HELP HASP       83227
         SH    R2,=AL2(INVOKED-INVOKE)  GET DPA ADDRESS
         MVC   EXHFSTRT-10(10),EXKDPA  SET PAGING VALUES
         AIF   (NOT &LOCO).NOLO2                                 80042
         MVC   USERXSAV,EXKXSAVE  SET USER DATA
.NOLO2   RET31 R9            RETURN WITH FAST PATH
         DROP  R5                                                87281
         SPACE 2
*        THIS CODE IS ENTERED PRIOR TO BEGINNING THE DISPLAY
*        LOOP.  THE CODE TESTS WHETHER AN OLD, BUT RECENT SCREEN
*        IS AVAILABLE.  IF SO, THE DISPLAY CYCLE IS SKIPPED.
*        ELSE THE ENQ IS KEPT UNTIL DZZZZ RELEASES IT ON ANOTHER
*        RESCALL (AN ABEND WOULD ALSO RELEASE IT).
*
BEGIN    BAS   R14,LOCPAGE   FIND THE SCREEN WORK AREA
         BZ    SLOWOUT2      NO AREA FOUND
         L     R4,MSGAD      MAKE WORK SPACE
         USING TEMPWORK,R4     DECLARE IT
         ST    R5,SAVER5     SAVE REGISTER 5                     87281
         LR    R5,R2         COPY RETURN
         MVC   ENQLIST(ENQLEN),ENQPAT  MAKE ENQ LIST
         USING EXKBASE,R5    DECLARE SCREEN AREA
         LA    R14,EXKGROUP  POINT TO GROUP/SIZE
         ST    R14,ENQLIST+8  REPLACE MINOR NAME
         ENQ   MF=(E,ENQLIST)     ENQUEUE
         TIME  BIN           GET CURRENT TIME
         C     R0,EXKTIME    IS IT LOWER THAN PREVIOUS ?
         BNL   *+8           NO
         A     R0,=A(24*60*60*100)  ADD A DAY
         L     R1,EXKTIME    GET LAST UPDATE TIME
         A     R1,DELAY      ADD LOOP TIME
         CR    R0,R1         TIME TO DO FULL UPDATE ?
         BH    BEGINUP       YES
         DEQ   MF=(E,ENQLIST)     RELEASE RESOURCE
         B     COMMCOMM      UPDATE COUNTERS ONLY
         SPACE 1
BEGINUP  OI    EXRESAD,X'80' SHOW TASK IN CONTROL
SLOWOUT  L     R5,SAVER5     RESTORE R5                          87281
SLOWOUT2 LA    R9,4(,R9)     SET FOR SLOW EXIT
         RET31 R9            TAKE SLOW EXIT
         DROP  R5                                                87281
         SPACE 1
LOCPAGE  SLR   R2,R2         SIGNAL NOTHING GOTTEN
         ICM   R1,15,EXWSSCT  DID THE SSN ADDRESS GET STASHED ?
         BZR   R14           NO; TOO BAD (UNLESS INITIALIZATION)
         ICM   R1,15,SSCTSUSE-SSCT(R1)  IS THERE A WORK AREA ?
         BZR   R14           NO; TOO BAD
         ICM   R2,15,EXRSCRIN-EXRBASE(R1)  SCREEN CHAIN ?
         BZR   R14           NO; TOO BAD
         USING EXKBASE,R2    DECLARE
LOCPAGEL CLC   LNO(L'LNO+L'LSIZE),EXKSCRSZ  CORRECT DIMENSION ?
         BNE   LOCPAGEB      NO; BUMP TO NEXT
         CLC   EXKJESNM,EXWJ2NAM  SAME SUB-SYSTEM ?
         BE    LOCPAGEM      YES
LOCPAGEB ICM   R2,15,EXKCHAIN  TRY AGAIN
         BNZ   LOCPAGEL
         BR    R14           NOT FOUND
LOCPAGEM CR    R2,R14        SET NON-ZERO RETURN CODE
         BR    R14           RETURN FOUND
         DROP  R2
         SPACE 1
         LTORG ,
         EJECT ,
         PRINT NOGEN
EXHRDFLT CSECT ,             KEEP MSECT HONEST
         MSECT ,
         PRINT GEN                                               79159
EXHRDFLT CSECT ,             RESTORE                             79159
         SPACE 1                                                 79159
MAJOR    DC    CL8'SYSEXHIB'  MAJOR ENQ NAME
MINOR    DC    CL8'EXHRDFLT'  MINOR NAME
ENQPAT   ENQ   (MAJOR,MINOR,E,8,SYSTEM),RET=HAVE,MF=L  ENQ PATTERN
ENQLEN   EQU   *-ENQPAT      LENGTH OF PATTERN
         SPACE 1
CONADDR  DC    X'40C1C2C3C4C5C6C7C8C94A4B4C4D4E4F'   0 - 15      80042
         DC    X'50D1D2D3D4D5D6D7D8D95A5B5C5D5E5F'  16 - 31      80042
         DC    X'6061E2E3E4E5E6E7E8E96A6B6C6D6E6F'  32 - 47      80042
         DC    X'F0F1F2F3F4F5F6F7F8F97A7B7C7D7E7F'  48 - 63      80042
         SPACE 1                                                 80042
TEMPWORK DSECT ,             MSGAD WORK AREA
SAVER5   DS    A             SAVE R5                             87281
ENQLIST  DC    XL(ENQLEN)'0'  ROOM FOR ENQ LIST
         SPACE 1
         MAPEXK ,
         SPACE 1
         MAPEXR ,
         SPACE 1
         IEFJSCVT ,          SSN ENTRY MAPPING
         SPACE 1
         IEFJESCT ,
         SPACE 1
         PRINT NOGEN
         CVT   DSECT=YES
         SPACE 1
         MSECT ,
         END   ,
