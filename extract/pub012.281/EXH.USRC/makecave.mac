MAKECAVE TITLE 'M A K E C A V E  ***  CONVERT ADVENTURE GAME FILE'
         COPY  OPTIONGB
         SPACE 1
         SYSPARM LIST=YES
         SPACE 2
***********************************************************************
*                                                                     *
*                                                                     *
*        COPYRIGHT 2002  EXPERT SYSTEM PROGRAMMING                    *
*                        176 OLD STAGE COACH ROAD                     *
*                        BRADFORD, VT 05033-8844                      *
*                                                                     *
*                    ALL RIGHTS RESERVED                              *
*                                                                     *
***********************************************************************
         EJECT ,
         PRINT &PRTSOR
***********************************************************************
**                                                                   **
**  MAKECAVE - EXHIBIT UTILITY PROGRAM TO MAKE ADVENTURERS HONEST ?  **
**                                                                   **
**  THIS MODULE READS THE PATTERN CAVE DEFINITION FILE, AND WRITES   **
**  OUT A NEW ONE WITH SCRAMBLED DATA, AS EXPECTED BY EXHCCADV.      **
**                                                                   **
***********************************************************************
*
MAKECAVE PGMHEAD ZERO12,BASE=R12,BNDRY=PAGE,PARM=R9, REPLACD ON GP02231*
               AM=24,RM=24
         SPACE 1
         SERVINIT ,
         SERVLOAD @PRINTER,@INPREAD
         PRTOPEN SYSPRINT,OPT=ABEND
         LOAD  EPLOC==CL8'EXHCSD64'  LOAD PATTERN MODULE
         LR    R7,R0         SAVE ADDRESS
         PRTOPEN SYSPUNCH,OPT=ABEND,DEV=2  SCRAMBLED OUTPUT FILE
         PRTL  '#CAVE CREATION UTILITY',TITLE=1
         INPOPEN SYSIN,OPT=ABEND
         SR    R5,R5         CLEAR CARD COUNTER
         SPACE 1
INLINED  INPGET ,            GET NEXT INPUT LINE
         BXH   R15,R15,ENDSYN  QUIT ON EODAD OR ERROR
         MVC   LINE,0(R1)    COPY CARD IMAGE
         XC    LINE,0(R7)    MANGLE THE CARD IMAGE
         PRTF  LINE,DEV=2,CC=NO    'PUNCH' THE OUTPUT
         LA    R5,1(,R5)     UP CARD COUNT
         B     INLINED
ENDSYN   ST    R5,DB         SAVE CARD COUNT
         PRTLIST STATLINE    TELL USER
         LTR   R5,R5         DID IT GO WELL ?
         BP    EXIT          YES
         OICC  8             ELSE SET ERROR RETURN CODE
         SPACE 1
EXIT     SERVTERM ,
         L     R9,RETCODE
         PGMEXIT RC=(R9)     RETURN WITH CC
         SPACE 2
SYSPRINT PRTWORK SYSPRINT,TITLE=3
SYSPUNCH PUNWORK NEWCAVE,WIDTH=80
SYSIN    INPWORK OLDCAVE,SYSIN,EODAD=ENDSYN
         SPACE 1                                                 94297
STATLINE FDOPT NL,CC=C'0'
         FD    DB,4,I,PAD
         FD    'CARD IMAGES PROCESSED'
         FD    *END                                              94297
         SPACE 1
SAVE     DSECT ,
DB       DS    D
         SERVDEFS ,
PROFG    DS    X
LINE     DS    CL80          WORK SPACE FOR CARD TRANSLATION
SAVEEND  EQU   *
         SPACE 1
         CVT   DSECT=YES
         IHACDE ,
         END   ,
