//ALGSAMP3 JOB (ALGOL),
//             'ALGOL Sample Program',
//             CLASS=A,
//             MSGCLASS=A,
//             REGION=8500K,COND=(0,NE),MSGLEVEL=(1,1)
//********************************************************************
//*
//* Name: SYS2.JCLLIB(ALGSAMP3)
//*
//* Desc: IBM Algol F Level 2.1 IVP
//*
//********************************************************************
//*
//*       360S-AL-531 Algol F Compiler
//*            and
//*       360S-LM-532 Algol F Library
//*
//IVP3   EXEC  ALGOFCLG
//ALGOL.SYSIN  DD  *
 'BEGIN'
 'COMMENT'
 //////////////////////////////////////////////////////////
 // NAME: PETER M. MAURER
 // PROGRAM: SIEVE OF ERATOSTHENES
 // DUE: NEVER
 // LANGUAGE: ALGOL 60 ALA IBM 360
 //
 // Changes:
 // - Juergen Winkelmann, 2014/07/12, Performance and Output
 //   Optimization
 //////////////////////////////////////////////////////////
 ;
 'COMMENT' DEFINE THE SIEVE DATA STRUCTURE ;
 'BOOLEAN' 'ARRAY' CANDIDATES[0:8400000];
 'INTEGER' I,J,K,LIMIT;
 'COMMENT' SET LINE-LENGTH=120,SET LINES-PER-PAGE=62,OPEN;
 SYSACT(1,6,120);
 SYSACT(1,8,62);
 SYSACT(1,12,1);
 ININTEGER(0,LIMIT);
 'FOR' I := 0 'STEP' 1 'UNTIL' LIMIT 'DO'
 'BEGIN'
 'COMMENT' EVERYTHING IS POTENTIALLY PRIME
 UNTIL PROVEN OTHERWISE ;
 CANDIDATES[I] := 'TRUE';
 'END';
 'COMMENT' NEITHER 1 NOR 0 IS PRIME, SO FLAG THEM OFF ;
 CANDIDATES[0] := 'FALSE';
 CANDIDATES[1] := 'FALSE';
 'COMMENT' START THE SIEVE WITH THE INTEGER 0 ;
 I := 0;
 K := 0;
 'FOR' I := I 'WHILE' K 'LESS' LIMIT 'DO'
 'BEGIN'
 'COMMENT' ADVANCE TO THE NEXT UN-CROSSED OUT. ;
 'COMMENT' THIS NUMBER MUST BE A PRIME;
 'FOR' I := I 'WHILE' K 'LESS' LIMIT
 'AND' 'NOT' CANDIDATES[I] 'DO'
 'BEGIN'
 I := I+1;
 K := I*I;
 'END';
 'COMMENT' INSURE AGAINST RUNNING OFF THE END;
 'IF' K 'LESS' LIMIT 'THEN'
 'BEGIN'
 'COMMENT' CROSS OUT ALL MULTIPLES OF THE PRIME.;
 'FOR' K := K 'WHILE' K 'LESS' LIMIT 'DO'
 'BEGIN'
 CANDIDATES[K] := 'FALSE';
 K := K+I;
 'END';
 'COMMENT' ADVANCE TO THE NEXT CANDIDATE ;
 I := I+1;
 K := I*I;
 'END'
 'END';
 'COMMENT' ALL UNCROSSED OUT NUMBERS ARE PRIME;
 'COMMENT' PRINT ALL PRIMES ;
 J := 0;
 K := 0;
 'FOR' I := 0 'STEP' 1 'UNTIL' LIMIT-1 'DO'
 'BEGIN'
 'IF' CANDIDATES[I] 'THEN'
 'BEGIN'
 J := J + 1;
 K := K + 1;
 OUTINTEGER(1,I);
 'IF' J 'EQUAL' 9 'THEN' J := 0;
 'END';
 'END';
 'IF' J 'NOTEQUAL' 0 'THEN' SYSACT(1,14,1);
 SYSACT(1,14,1);
 OUTINTEGER(1,K);
 OUTSTRING(1,'(' primes found')');
 SYSACT(1,14,1);
 'END'
/*
//GO.SYSIN  DD *
  100000
/*
