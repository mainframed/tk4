000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID. KSDSLOAD.
000300 AUTHOR. JAY MOSELEY.
000400 DATE-WRITTEN. NOVEMBER, 2001.
000500 DATE-COMPILED.
000600
000700* ************************************************************* *
000800* THIS PROGRAM TESTS THE VSAMIO ROUTINE BY LOADING A KSDS       *
000900* CLUSTER SEQUENTIALLY WITH RECORDS FROM A SEQUENTIAL DATASET.  *
001000* ************************************************************* *
001100
001200 ENVIRONMENT DIVISION.
001300 CONFIGURATION SECTION.
001400 SOURCE-COMPUTER. IBM-370.
001500 OBJECT-COMPUTER. IBM-370.
001600
001700 INPUT-OUTPUT SECTION.
001800 FILE-CONTROL.
001900
002000     SELECT RECORD-IMAGES
002100         ASSIGN TO DA-2314-S-IMAGES.
002200
002300 DATA DIVISION.
002400 FILE SECTION.
002500 FD  RECORD-IMAGES
002600     LABEL RECORDS ARE STANDARD
002700     BLOCK CONTAINS 10 RECORDS
002800     DATA RECORD IS RECORD-IMAGE.
002900
003000 01  RECORD-IMAGE                PIC X(80).
003100
003200 WORKING-STORAGE SECTION.
003300 77  END-OF-FILE-SWITCH          PIC X(1)    VALUE 'N'.
003400     88  END-OF-FILE                         VALUE 'Y'.
003500
003600 77  RECORD-COUNTER              PIC S9(8)   VALUE +0.
003700 77  COUNTER-EDIT                PIC  ZZ,ZZZ,ZZ9.
003800
003900 01  VSIO-PARAMETER-VALUES       COPY VSAMIO.
004000 01  KSDSF01                     COPY VSAMIOFB.
004100 01  KSDS-RECORD                 PIC X(80).
004200
004300 PROCEDURE DIVISION.
004400
004500 000-INITIATE.
004600
004700     DISPLAY 'KSDSLOAD: WRITE KSDS SEQUENTIALLY'.
004800     DISPLAY '---------------------------------'.
004900     DISPLAY ' '.
005000
005100     OPEN INPUT RECORD-IMAGES.
005200
005300     MOVE 'KSDSF01' TO VSIO-DDNAME.
005400     MOVE VSIO-KSDS TO VSIO-ORGANIZATION.
005500     MOVE VSIO-SEQUENTIAL TO VSIO-ACCESS.
005600     MOVE VSIO-OUTPUT TO VSIO-MODE.
005700     MOVE +80 TO VSIO-RECORD-LENGTH.
005800     MOVE +0 TO VSIO-KEY-POSITION.
005900     MOVE +10 TO VSIO-KEY-LENGTH.
006000     MOVE VSIO-OPEN TO VSIO-COMMAND.
006100     CALL 'VSAMIO' USING VSIO-PARAMETER-BLOCK, KSDSF01,
006200                         KSDS-RECORD.
006300*    END-CALL.
006400     IF NOT VSIO-SUCCESS
006500         DISPLAY 'VSAMIO ERROR OCCURRED DURING '
006600                 VSIO-COMMAND
006700         EXHIBIT NAMED VSIO-RETURN-CODE,
006800         EXHIBIT NAMED VSIO-VSAM-RETURN-CODE,
006900                       VSIO-VSAM-FUNCTION-CODE,
007000                       VSIO-VSAM-FEEDBACK-CODE
007100         STOP RUN.
007200*    END-IF.
007300
007400 010-PROCESS.
007500
007600     PERFORM 110-PROCESS-DATA
007700        THRU 119-EXIT
007800       UNTIL END-OF-FILE
007900          OR (NOT VSIO-SUCCESS).
008000*    END-PERFORM.
008100
008200 020-TERMINATE.
008300
008400     CLOSE RECORD-IMAGES.
008500
008600     MOVE VSIO-CLOSE TO VSIO-COMMAND.
008700     CALL 'VSAMIO' USING VSIO-PARAMETER-BLOCK, KSDSF01,
008800                         KSDS-RECORD.
008900*    END-CALL.
009000     IF NOT VSIO-SUCCESS
009100         DISPLAY 'VSAMIO ERROR OCCURRED DURING '
009200                 VSIO-COMMAND
009300         EXHIBIT NAMED VSIO-RETURN-CODE,
009400         EXHIBIT NAMED VSIO-VSAM-RETURN-CODE,
009500                       VSIO-VSAM-FUNCTION-CODE,
009600                       VSIO-VSAM-FEEDBACK-CODE.
009700*    END-IF.
009800
009900     MOVE RECORD-COUNTER TO COUNTER-EDIT.
010000     DISPLAY COUNTER-EDIT ' RECORDS WERE LOADED SUCCESSFULLY'.
010100
010200     STOP RUN.
010300
010400 110-PROCESS-DATA.
010500     READ RECORD-IMAGES
010600         AT END
010700             MOVE 'Y' TO END-OF-FILE-SWITCH.
010800*    END-READ.
010900
011000     IF NOT END-OF-FILE
011100         PERFORM 120-ADD-RECORD THRU 129-EXIT.
011200*    END-IF.
011300
011400 119-EXIT.
011500     EXIT.
011600
011700 120-ADD-RECORD.
011800
011900     MOVE RECORD-IMAGE TO KSDS-RECORD.
012000
012100     MOVE VSIO-WRITE TO VSIO-COMMAND.
012200     CALL 'VSAMIO' USING VSIO-PARAMETER-BLOCK, KSDSF01,
012300                         KSDS-RECORD.
012400*    END-CALL.
012500
012600     IF VSIO-SUCCESS
012700         ADD +1 TO RECORD-COUNTER
012800         GO TO 129-EXIT.
012900*    END-IF.
013000
013100     IF VSIO-LOGIC-ERROR
013200     AND VSIO-NO-MORE-SPACE
013300         DISPLAY 'INSUFFICIENT SPACE DEFINED IN '
013400                 'CLUSTER TO CONTAIN ALL RECORDS - '
013500                 'LOADING TERMINATED'
013600         GO TO 129-EXIT.
013700*    END-IF.
013800
013900     IF VSIO-LOGIC-ERROR
014000     AND VSIO-SEQUENCE-ERROR
014100         DISPLAY 'KEY SEQUENCE ERROR DURING LOAD'
014200         DISPLAY 'RECORD BYPASSED: ' RECORD-IMAGE
014300         MOVE +0 TO VSIO-RETURN-CODE
014400         GO TO 129-EXIT.
014500*    END-IF.
014600
014700     DISPLAY 'VSAMIO ERROR OCCURRED DURING '
014800             VSIO-COMMAND.
014900     EXHIBIT NAMED VSIO-RETURN-CODE.
015000     EXHIBIT NAMED VSIO-VSAM-RETURN-CODE,
015100                   VSIO-VSAM-FUNCTION-CODE,
015200                   VSIO-VSAM-FEEDBACK-CODE.
015300*    END-IF.
015400
015500 129-EXIT.
015600     EXIT.
015700
015800
