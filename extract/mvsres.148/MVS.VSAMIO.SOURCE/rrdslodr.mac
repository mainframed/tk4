000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID. RRDSLODR.
000300 AUTHOR. JAY MOSELEY.
000400 DATE-WRITTEN. NOVEMBER, 2001.
000500 DATE-COMPILED.
000600
000700* ************************************************************* *
000800* THIS PROGRAM TESTS THE VSAMIO ROUTINE BY LOADING AN RRDS      *
000900* CLUSTER DIRECTLY WITH RECORDS FROM A SEQUENTIAL DATASET. THE  *
001000* RRN IS TAKEN FROM THE SEQUENCE PART OF THE RECORD AND THERE   *
001100* ARE GAPS IN THE TEST DATA.                                    *
001200* ************************************************************* *
001300
001400 ENVIRONMENT DIVISION.
001500 CONFIGURATION SECTION.
001600 SOURCE-COMPUTER. IBM-370.
001700 OBJECT-COMPUTER. IBM-370.
001800
001900 INPUT-OUTPUT SECTION.
002000 FILE-CONTROL.
002100
002200     SELECT RECORD-IMAGES
002300         ASSIGN TO DA-2314-S-IMAGES.
002400
002500 DATA DIVISION.
002600 FILE SECTION.
002700 FD  RECORD-IMAGES
002800     LABEL RECORDS ARE STANDARD
002900     BLOCK CONTAINS 10 RECORDS
003000     DATA RECORD IS RECORD-IMAGE.
003100
003200 01  RECORD-IMAGE                PIC X(80).
003300
003400 WORKING-STORAGE SECTION.
003500 77  END-OF-FILE-SWITCH          PIC X(1)    VALUE 'N'.
003600     88  END-OF-FILE                         VALUE 'Y'.
003700
003800 77  RECORD-COUNTER              PIC S9(8)   VALUE +0.
003900 77  COUNTER-EDIT                PIC  ZZ,ZZZ,ZZ9.
004000
004100 01  VSIO-PARAMETER-VALUES       COPY VSAMIO.
004200 01  RRDSF01                     COPY VSAMIOFB.
004300 01  RRDS-RECORD.
004400     02  FILLER                  PIC X(07).
004500     02  RRDS-RRN                PIC 9(03).
004600     02  FILLER                  PIC X(70).
004700
004800 PROCEDURE DIVISION.
004900
005000 000-INITIATE.
005100
005200     DISPLAY 'RRDSLODR: WRITE RRDS DIRECT (VACANT SLOTS)'.
005300     DISPLAY '------------------------------------------'.
005400     DISPLAY ' '.
005500
005600     OPEN INPUT RECORD-IMAGES.
005700
005800     MOVE 'RRDSF01' TO VSIO-DDNAME.
005900     MOVE VSIO-RRDS TO VSIO-ORGANIZATION.
006000     MOVE VSIO-DIRECT TO VSIO-ACCESS.
006100     MOVE VSIO-OUTPUT TO VSIO-MODE.
006200     MOVE +80 TO VSIO-RECORD-LENGTH.
006300     MOVE +0 TO VSIO-KEY-LENGTH, VSIO-KEY-POSITION.
006400     MOVE VSIO-OPEN TO VSIO-COMMAND.
006500     CALL 'VSAMIO' USING VSIO-PARAMETER-BLOCK, RRDSF01,
006600                         RRDS-RECORD.
006700*    END-CALL.
006800     IF NOT VSIO-SUCCESS
006900         DISPLAY 'VSAMIO ERROR OCCURRED DURING '
007000                 VSIO-COMMAND
007100         EXHIBIT NAMED VSIO-RETURN-CODE,
007200         EXHIBIT NAMED VSIO-VSAM-RETURN-CODE,
007300                       VSIO-VSAM-FUNCTION-CODE,
007400                       VSIO-VSAM-FEEDBACK-CODE
007500         STOP RUN.
007600*    END-IF.
007700
007800 010-PROCESS.
007900
008000     PERFORM 110-PROCESS-DATA
008100        THRU 119-EXIT
008200       UNTIL END-OF-FILE
008300          OR (NOT VSIO-SUCCESS).
008400*    END-PERFORM.
008500
008600 020-TERMINATE.
008700
008800     CLOSE RECORD-IMAGES.
008900
009000     MOVE VSIO-CLOSE TO VSIO-COMMAND.
009100     CALL 'VSAMIO' USING VSIO-PARAMETER-BLOCK, RRDSF01,
009200                         RRDS-RECORD.
009300*    END-CALL.
009400     IF NOT VSIO-SUCCESS
009500         DISPLAY 'VSAMIO ERROR OCCURRED DURING '
009600                 VSIO-COMMAND
009700         EXHIBIT NAMED VSIO-RETURN-CODE,
009800         EXHIBIT NAMED VSIO-VSAM-RETURN-CODE,
009900                       VSIO-VSAM-FUNCTION-CODE,
010000                       VSIO-VSAM-FEEDBACK-CODE.
010100*    END-IF.
010200
010300     MOVE RECORD-COUNTER TO COUNTER-EDIT.
010400     DISPLAY COUNTER-EDIT ' RECORDS WERE LOADED SUCCESSFULLY'.
010500
010600     STOP RUN.
010700
010800 110-PROCESS-DATA.
010900     READ RECORD-IMAGES
011000         AT END
011100             MOVE 'Y' TO END-OF-FILE-SWITCH.
011200*    END-READ.
011300
011400     IF NOT END-OF-FILE
011500
011600         MOVE RECORD-IMAGE TO RRDS-RECORD
011700         MOVE RRDS-RRN TO VSIO-RELATIVE-RECORD
011800
011900         MOVE VSIO-WRITE TO VSIO-COMMAND
012000         CALL 'VSAMIO' USING VSIO-PARAMETER-BLOCK, RRDSF01,
012100                         RRDS-RECORD
012200*        END-CALL
012300
012400         IF VSIO-SUCCESS
012500             ADD +1 TO RECORD-COUNTER
012600         ELSE
012700             IF VSIO-LOGIC-ERROR
012800            AND VSIO-NO-MORE-SPACE
012900                DISPLAY 'INSUFFICIENT SPACE DEFINED IN CLUSTER'
013000                        'TO CONTAIN ALL RECORDS - LOADING '
013100                        'TERMINATED'
013200             ELSE
013300                 DISPLAY 'VSAMIO ERROR OCCURRED DURING '
013400                         VSIO-COMMAND
013500                 EXHIBIT NAMED VSIO-RETURN-CODE,
013600                 EXHIBIT NAMED VSIO-VSAM-RETURN-CODE,
013700                               VSIO-VSAM-FUNCTION-CODE,
013800                               VSIO-VSAM-FEEDBACK-CODE.
013900*            END-IF
014000*        END-IF
014100*    END-IF.
014200
014300 119-EXIT.
014400     EXIT.
014500
014600
