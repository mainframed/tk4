000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID. RRDSRAND.
000300 AUTHOR. JAY MOSELEY.
000400 DATE-WRITTEN. NOVEMBER, 2001.
000500 DATE-COMPILED.
000600
000700* ************************************************************* *
000800* THIS PROGRAM TESTS THE VSIO ROUTINE BY ACCESSING AN RRDS      *
000900* CLUSTER RANDOMLY AND ADDING/UPDATING/DELETING RECORDS.        *
001000* ************************************************************* *
001100
001200 ENVIRONMENT DIVISION.
001300 CONFIGURATION SECTION.
001400 SOURCE-COMPUTER. IBM-370.
001500 OBJECT-COMPUTER. IBM-370.
001600
001700 INPUT-OUTPUT SECTION.
001800 FILE-CONTROL.
001900
002000     SELECT RECORD-IMAGES
002100         ASSIGN TO UR-2540R-S-SYSIN.
002200
002300 DATA DIVISION.
002400 FILE SECTION.
002500
002600 FD  RECORD-IMAGES
002700     LABEL RECORDS ARE OMITTED
002800     BLOCK CONTAINS 0 RECORDS
002900     DATA RECORD IS RECORD-IMAGE.
003000
003100 01  RECORD-IMAGE.
003200     02  RI-ACTION               PIC X(01).
003300         88  ACTION-IS-ADD                   VALUE 'A'.
003400         88  ACTION-IS-CHANGE                VALUE 'C'.
003500         88  ACTION-IS-DELETE                VALUE 'D'.
003600         88  ACTION-IS-VALID                 VALUE 'A', 'C', 'D'.
003700     02  FILLER                  PIC X(01).
003800     02  RI-IMAGE                PIC X(78).
003900
004000 WORKING-STORAGE SECTION.
004100 77  END-OF-FILE-SWITCH          PIC X(1)    VALUE 'N'.
004200     88  END-OF-FILE                         VALUE 'Y'.
004300
004400 77  RECORD-COUNTER              PIC S9(8)   VALUE +0.
004500 77  COUNTER-EDIT                PIC ZZ,ZZZ,ZZ9.
004600
004700 01  VSIO-PARAMETER-VALUES       COPY VSAMIO.
004800 01  RRDSF01                     COPY VSAMIOFB.
004900 01  RRDS-RECORD.
005000     02  RR-KEY.
005100         03  RRK-HIGH            PIC 9(7).
005200         03  RRK-LOW             PIC 9(3).
005300     02  FILLER                  PIC X(70).
005400
005500 PROCEDURE DIVISION.
005600
005700 000-INITIATE.
005800
005900     DISPLAY 'RRDSRAND: READ/REWRITE RRDS DIRECT'.
006000     DISPLAY '----------------------------------'.
006100     DISPLAY ' '.
006200
006300     OPEN INPUT RECORD-IMAGES.
006400
006500     MOVE 'RRDSF01' TO VSIO-DDNAME.
006600     MOVE VSIO-RRDS TO VSIO-ORGANIZATION.
006700     MOVE VSIO-DIRECT TO VSIO-ACCESS.
006800     MOVE VSIO-INPUT-OUTPUT TO VSIO-MODE.
006900     MOVE +80 TO VSIO-RECORD-LENGTH.
007000     MOVE VSIO-OPEN TO VSIO-COMMAND.
007100     CALL 'VSAMIO' USING VSIO-PARAMETER-BLOCK, RRDSF01,
007200                         RRDS-RECORD.
007300*    END-CALL.
007400     IF NOT VSIO-SUCCESS
007500         DISPLAY 'VSAMIO ERROR OCCURRED DURING '
007600                 VSIO-COMMAND
007700         EXHIBIT NAMED VSIO-RETURN-CODE,
007800         EXHIBIT NAMED VSIO-VSAM-RETURN-CODE,
007900                       VSIO-VSAM-FUNCTION-CODE,
008000                       VSIO-VSAM-FEEDBACK-CODE
008100         STOP RUN.
008200*    END-IF.
008300
008400 010-PROCESS.
008500
008600     PERFORM 110-PROCESS-UPDATES
008700        THRU 119-EXIT
008800       UNTIL END-OF-FILE.
008900*    END-PERFORM.
009000
009100 020-TERMINATE.
009200
009300     MOVE VSIO-CLOSE TO VSIO-COMMAND.
009400     CALL 'VSAMIO' USING VSIO-PARAMETER-BLOCK, RRDSF01,
009500                         RRDS-RECORD.
009600*    END-CALL.
009700     IF NOT VSIO-SUCCESS
009800         DISPLAY 'VSAMIO ERROR OCCURRED DURING '
009900                 VSIO-COMMAND
010000         EXHIBIT NAMED VSIO-RETURN-CODE,
010100         EXHIBIT NAMED VSIO-VSAM-RETURN-CODE,
010200                       VSIO-VSAM-FUNCTION-CODE,
010300                       VSIO-VSAM-FEEDBACK-CODE.
010400*    END-IF.
010500
010600     STOP RUN.
010700
010800 110-PROCESS-UPDATES.
010900
011000     READ RECORD-IMAGES
011100         AT END
011200             MOVE 'Y' TO END-OF-FILE-SWITCH.
011300*    END-READ.
011400
011500     IF END-OF-FILE
011600         GO TO 119-EXIT.
011700*    END-IF.
011800
011900     ADD +1 TO RECORD-COUNTER.
012000
012100     IF NOT ACTION-IS-VALID
012200         MOVE RECORD-COUNTER TO COUNTER-EDIT
012300         DISPLAY COUNTER-EDIT ': ' RECORD-IMAGE
012400         DISPLAY '            *** ACTION INVALID: IGNORED'
012500         DISPLAY ' '
012600         GO TO 119-EXIT.
012700*    END-IF.
012800
012900     IF ACTION-IS-ADD
013000         PERFORM 120-ADD-PROCESS THRU 129-EXIT.
013100*    END-IF.
013200
013300     IF ACTION-IS-CHANGE
013400         PERFORM 130-CHANGE-PROCESS THRU 139-EXIT.
013500*    END-IF.
013600
013700     IF ACTION-IS-DELETE
013800         PERFORM 140-DELETE-PROCESS THRU 149-EXIT.
013900*    END-IF.
014000
014100 119-EXIT.
014200     EXIT.
014300
014400 120-ADD-PROCESS.
014500
014600     MOVE RI-IMAGE TO RRDS-RECORD.
014700     MOVE RRK-LOW TO VSIO-RELATIVE-RECORD.
014800     MOVE VSIO-WRITE TO VSIO-COMMAND.
014900     MOVE VSIO-RELATIVE-RECORD TO COUNTER-EDIT.
015000     DISPLAY COUNTER-EDIT ': ADDING: ' RRDS-RECORD.
015100     CALL 'VSAMIO' USING VSIO-PARAMETER-BLOCK, RRDSF01,
015200                         RRDS-RECORD.
015300*    END-CALL.
015400
015500     IF VSIO-SUCCESS
015600         DISPLAY '               RECORD ADDED'
015700     ELSE
015800         IF VSIO-LOGIC-ERROR
015900        AND VSIO-DUPLICATE-RECORD
016000             MOVE VSIO-RELATIVE-RECORD TO COUNTER-EDIT
016100             DISPLAY '            *** DUPLICATE RECORD ON FILE'
016200         ELSE
016300             DISPLAY 'VSAMIO ERROR OCCURRED DURING '
016400                     VSIO-COMMAND
016500             EXHIBIT NAMED VSIO-RETURN-CODE,
016600             EXHIBIT NAMED VSIO-VSAM-RETURN-CODE,
016700                           VSIO-VSAM-FUNCTION-CODE,
016800                           VSIO-VSAM-FEEDBACK-CODE.
016900*        END-IF
017000*    END-IF.
017100
017200     DISPLAY ' '.
017300
017400 129-EXIT.
017500     EXIT.
017600
017700 130-CHANGE-PROCESS.
017800
017900     PERFORM 150-READ-RECORD THRU 159-EXIT.
018000
018100     IF VSIO-LOGIC-ERROR
018200     AND VSIO-RECORD-NOT-FOUND
018300         MOVE VSIO-RELATIVE-RECORD TO COUNTER-EDIT
018400         DISPLAY COUNTER-EDIT ' *** RECORD NOT FOUND'
018500         DISPLAY ' '
018600         GO TO 139-EXIT.
018700*    END-IF.
018800
018900     MOVE VSIO-RELATIVE-RECORD TO COUNTER-EDIT.
019000     DISPLAY COUNTER-EDIT ': ' RRDS-RECORD.
019100     DISPLAY '               RECORD BEFORE CHANGE'.
019200
019300     MOVE RI-IMAGE TO RRDS-RECORD.
019400     MOVE VSIO-REWRITE TO VSIO-COMMAND.
019500     CALL 'VSAMIO' USING VSIO-PARAMETER-BLOCK, RRDSF01,
019600                         RRDS-RECORD.
019700*    END-CALL.
019800
019900     IF VSIO-SUCCESS
020000         MOVE VSIO-RELATIVE-RECORD TO COUNTER-EDIT
020100         DISPLAY COUNTER-EDIT ': ' RRDS-RECORD
020200         DISPLAY '               RECORD AFTER CHANGE'
020300     ELSE
020400         DISPLAY 'VSAMIO ERROR OCCURRED DURING '
020500                 VSIO-COMMAND
020600         EXHIBIT NAMED VSIO-RETURN-CODE,
020700         EXHIBIT NAMED VSIO-VSAM-RETURN-CODE,
020800                       VSIO-VSAM-FUNCTION-CODE,
020900                       VSIO-VSAM-FEEDBACK-CODE
021000*    END-IF
021100
021200     DISPLAY ' '.
021300
021400 139-EXIT.
021500     EXIT.
021600
021700 140-DELETE-PROCESS.
021800
021900     PERFORM 150-READ-RECORD THRU 159-EXIT.
022000
022100     IF VSIO-LOGIC-ERROR
022200     AND VSIO-RECORD-NOT-FOUND
022300         MOVE VSIO-RELATIVE-RECORD TO COUNTER-EDIT
022400         DISPLAY COUNTER-EDIT ' *** RECORD NOT FOUND'
022500         DISPLAY ' '
022600         GO TO 149-EXIT.
022700*    END-IF.
022800
022900     MOVE VSIO-RELATIVE-RECORD TO COUNTER-EDIT.
023000     DISPLAY COUNTER-EDIT ': ' RRDS-RECORD.
023100     DISPLAY '               RECORD BEFORE DELETE'.
023200
023300     MOVE VSIO-DELETE TO VSIO-COMMAND.
023400     CALL 'VSAMIO' USING VSIO-PARAMETER-BLOCK, RRDSF01,
023500                         RRDS-RECORD.
023600*    END-CALL.
023700
023800     IF VSIO-SUCCESS
023900         DISPLAY '             DELETED '
024000     ELSE
024100         DISPLAY 'VSAMIO ERROR OCCURRED DURING '
024200                 VSIO-COMMAND
024300         EXHIBIT NAMED VSIO-RETURN-CODE,
024400         EXHIBIT NAMED VSIO-VSAM-RETURN-CODE,
024500                       VSIO-VSAM-FUNCTION-CODE,
024600                       VSIO-VSAM-FEEDBACK-CODE.
024700*    END-IF.
024800
024900     DISPLAY ' '.
025000
025100 149-EXIT.
025200     EXIT.
025300
025400 150-READ-RECORD.
025500
025600     MOVE RI-IMAGE TO RRDS-RECORD.
025700     MOVE RRK-LOW TO VSIO-RELATIVE-RECORD.
025800     MOVE VSIO-READ TO VSIO-COMMAND.
025900     CALL 'VSAMIO' USING VSIO-PARAMETER-BLOCK, RRDSF01,
026000                         RRDS-RECORD.
026100*    END-CALL.
026200
026300     IF NOT VSIO-SUCCESS
026400         IF VSIO-LOGIC-ERROR
026500        AND VSIO-RECORD-NOT-FOUND
026600            NEXT SENTENCE
026700         ELSE
026800             DISPLAY 'VSAMIO ERROR OCCURRED DURING '
026900                     VSIO-COMMAND
027000             EXHIBIT NAMED VSIO-RETURN-CODE,
027100             EXHIBIT NAMED VSIO-VSAM-RETURN-CODE,
027200                           VSIO-VSAM-FUNCTION-CODE,
027300                           VSIO-VSAM-FEEDBACK-CODE.
027400*        END-IF
027500*    END-IF.
027600
027700 159-EXIT.
027800     EXIT.
027900
