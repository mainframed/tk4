000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID. RRDSLODS.
000300 AUTHOR. JAY MOSELEY.
000400 DATE-WRITTEN. NOVEMBER, 2001.
000500 DATE-COMPILED.
000600
000700* ************************************************************* *
000800* THIS PROGRAM TESTS THE VSAMIO ROUTINE BY LOADING AN RRDS      *
000900* CLUSTER SEQUENTIALLY WITH RECORDS FROM A SEQUENTIAL DATASET.  *
001000* ************************************************************* *
001100
001200 ENVIRONMENT DIVISION.
001300 CONFIGURATION SECTION.
001400 SOURCE-COMPUTER. IBM-370.
001500 OBJECT-COMPUTER. IBM-370.
001600
001700 INPUT-OUTPUT SECTION.
001800 FILE-CONTROL.
001900
002000     SELECT RECORD-IMAGES
002100         ASSIGN TO DA-2314-S-IMAGES.
002200
002300 DATA DIVISION.
002400 FILE SECTION.
002500 FD  RECORD-IMAGES
002600     LABEL RECORDS ARE STANDARD
002700     BLOCK CONTAINS 10 RECORDS
002800     DATA RECORD IS RECORD-IMAGE.
002900
003000 01  RECORD-IMAGE                PIC X(80).
003100
003200 WORKING-STORAGE SECTION.
003300 77  END-OF-FILE-SWITCH          PIC X(1)    VALUE 'N'.
003400     88  END-OF-FILE                         VALUE 'Y'.
003500
003600 77  RELATIVE-RECORD-NUMBER      PIC S9(8)   VALUE +0.
003700 77  RECORD-COUNTER              PIC S9(8)   VALUE +0.
003800 77  COUNTER-EDIT                PIC  ZZ,ZZZ,ZZ9.
003900
004000 01  VSIO-PARAMETER-VALUES       COPY VSAMIO.
004100 01  RRDSF01                     COPY VSAMIOFB.
004200 01  RRDS-RECORD                 PIC X(80).
004300
004400 PROCEDURE DIVISION.
004500
004600 000-INITIATE.
004700
004800     DISPLAY 'RRDSLODS: WRITE RRDS SEQUENTIALLY'.
004900     DISPLAY '---------------------------------'.
005000     DISPLAY ' '.
005100
005200     OPEN INPUT RECORD-IMAGES.
005300
005400     MOVE 'RRDSF01' TO VSIO-DDNAME.
005500     MOVE VSIO-RRDS TO VSIO-ORGANIZATION.
005600     MOVE VSIO-SEQUENTIAL TO VSIO-ACCESS.
005700     MOVE VSIO-OUTPUT TO VSIO-MODE.
005800     MOVE +80 TO VSIO-RECORD-LENGTH.
005900     MOVE +0 TO VSIO-KEY-LENGTH, VSIO-KEY-POSITION.
006000     MOVE VSIO-OPEN TO VSIO-COMMAND.
006100     CALL 'VSAMIO' USING VSIO-PARAMETER-BLOCK, RRDSF01,
006200                         RRDS-RECORD.
006300*    END-CALL.
006400     IF NOT VSIO-SUCCESS
006500         DISPLAY 'VSAMIO ERROR OCCURRED DURING '
006600                 VSIO-COMMAND
006700         EXHIBIT NAMED VSIO-RETURN-CODE,
006800         EXHIBIT NAMED VSIO-VSAM-RETURN-CODE,
006900                       VSIO-VSAM-FUNCTION-CODE,
007000                       VSIO-VSAM-FEEDBACK-CODE
007100         STOP RUN.
007200*    END-IF.
007300
007400 010-PROCESS.
007500
007600     PERFORM 110-PROCESS-DATA
007700        THRU 119-EXIT
007800       UNTIL END-OF-FILE
007900          OR (NOT VSIO-SUCCESS).
008000*    END-PERFORM.
008100
008200 020-TERMINATE.
008300
008400     CLOSE RECORD-IMAGES.
008500
008600     MOVE VSIO-CLOSE TO VSIO-COMMAND.
008700     CALL 'VSAMIO' USING VSIO-PARAMETER-BLOCK, RRDSF01,
008800                         RRDS-RECORD.
008900*    END-CALL.
009000     IF NOT VSIO-SUCCESS
009100         DISPLAY 'VSAMIO ERROR OCCURRED DURING '
009200                 VSIO-COMMAND
009300         EXHIBIT NAMED VSIO-RETURN-CODE,
009400         EXHIBIT NAMED VSIO-VSAM-RETURN-CODE,
009500                       VSIO-VSAM-FUNCTION-CODE,
009600                       VSIO-VSAM-FEEDBACK-CODE.
009700*    END-IF.
009800
009900     MOVE RECORD-COUNTER TO COUNTER-EDIT.
010000     DISPLAY COUNTER-EDIT ' RECORDS WERE LOADED SUCCESSFULLY'.
010100
010200     STOP RUN.
010300
010400 110-PROCESS-DATA.
010500     READ RECORD-IMAGES
010600         AT END
010700             MOVE 'Y' TO END-OF-FILE-SWITCH.
010800*    END-READ.
010900
011000     IF NOT END-OF-FILE
011100
011200         ADD +1 TO RELATIVE-RECORD-NUMBER
011300         MOVE RELATIVE-RECORD-NUMBER TO VSIO-RELATIVE-RECORD
011400         MOVE RECORD-IMAGE TO RRDS-RECORD
011500
011600         MOVE VSIO-WRITE TO VSIO-COMMAND
011700         CALL 'VSAMIO' USING VSIO-PARAMETER-BLOCK, RRDSF01,
011800                         RRDS-RECORD
011900*        END-CALL
012000
012100         IF VSIO-SUCCESS
012200             ADD +1 TO RECORD-COUNTER
012300         ELSE
012400             IF VSIO-LOGIC-ERROR
012500            AND VSIO-NO-MORE-SPACE
012600                DISPLAY 'INSUFFICIENT SPACE DEFINED IN CLUSTER'
012700                        'TO CONTAIN ALL RECORDS - LOADING '
012800                        'TERMINATED'
012900             ELSE
013000                 DISPLAY 'VSAMIO ERROR OCCURRED DURING '
013100                         VSIO-COMMAND
013200                 EXHIBIT NAMED VSIO-RETURN-CODE,
013300                 EXHIBIT NAMED VSIO-VSAM-RETURN-CODE,
013400                               VSIO-VSAM-FUNCTION-CODE,
013500                               VSIO-VSAM-FEEDBACK-CODE.
013600*            END-IF
013700*        END-IF
013800*    END-IF.
013900
014000 119-EXIT.
014100     EXIT.
014200
014300
