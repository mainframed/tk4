000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID. KSDSLVAR.
000300 AUTHOR. JAY MOSELEY.
000400 DATE-WRITTEN. NOVEMBER, 2001.
000500 DATE-COMPILED.
000600
000700* ************************************************************* *
000800* THIS PROGRAM TESTS THE VSAMIO ROUTINE BY LOADING A KSDS       *
000900* CONTAINING VARIABLE LENGTH RECORDS.                           *
001000* ************************************************************* *
001100
001200 ENVIRONMENT DIVISION.
001300 CONFIGURATION SECTION.
001400 SOURCE-COMPUTER. IBM-370.
001500 OBJECT-COMPUTER. IBM-370.
001600
001700 INPUT-OUTPUT SECTION.
001800 FILE-CONTROL.
001900
002000     SELECT RECORD-IMAGES
002100         ASSIGN TO UR-2540R-S-SYSIN.
002200
002300 DATA DIVISION.
002400 FILE SECTION.
002500 FD  RECORD-IMAGES
002600     LABEL RECORDS ARE OMITTED
002700     BLOCK CONTAINS 0 RECORDS
002800     DATA RECORDS IS RECORD-IMAGE.
002900
003000 01  RECORD-IMAGE.
003100     02  FILLER                  PIC X(07).
003200     02  RI-COLUMN-8             PIC X(01).
003300     02  FILLER                  PIC X(72).
003400
003500 WORKING-STORAGE SECTION.
003600 77  END-OF-FILE-SWITCH          PIC X(1)    VALUE 'N'.
003700     88  END-OF-FILE                         VALUE 'Y'.
003800
003900* ************************************************************** *
004000* TWO ALTERNATING CARD FORMATS ARE READ FROM SYSIN, WITH A PAIR  *
004100* PROVIDING THE DATA TO ADD A STUDENT AND ALL COURSES ENROLLED.  *
004200* BASED ON THE FORMAT, THE CARD READ IS MOVED TO THE APPROPRIATE *
004300* GROUP LEVEL ITEM BELOW FOR PROCESSING.                         *
004400* ************************************************************** *
004500 01  CARD-IMAGES.
004600     02  CI-STUDENT-INFO.
004700         03  CISI-STUDENT-ID     PIC X(07).
004800         03  CISI-NAME           PIC X(22).
004900         03  CISI-ADDRESS        PIC X(25).
005000         03  CISI-CITY           PIC X(15).
005100         03  CISI-STATE          PIC X(02).
005200         03  CISI-ZIPCODE        PIC 9(05).
005300         03  CISI-GENDER         PIC X(01).
005400         03  CISI-MAJOR          PIC X(03).
005500     02  CI-COURSE-INFO.
005600         03  CICI-STUDENT-ID     PIC X(07).
005700         03  CICI-COURSE-TABLE   OCCURS 8 TIMES
005800                                 INDEXED BY CICI-COURSE-IX.
005900             04  FILLER          PIC X(01).
006000             04  CICI-COURSE-ID  PIC 9(06).
006100         03  FILLER              PIC X(17).
006200
006300 01  VSIO-PARAMETER-VALUES       COPY VSAMIO.
006400 01  STUDENT-MASTER-FILE         COPY VSAMIOFB.
006500* ************************************************************** *
006600* SINCE THE STUDENT DATASET CONTAINS VARIABLE LENGTH RECORDS,    *
006700* TWO FORMATS ARE DEFINED BELOW, ONE FOR THE 83 BYTE HEADER      *
006800* RECORD AND ONE FOR THE 17 BYTE TRAILER SEGMENTS.               *
006900* ************************************************************** *
007000 01  STUDENT-INFO-RECORD.
007100     02  SIR-STUDENT-ID          PIC X(07).
007200     02  SIR-KEY-ID              PIC 9(03).
007300     02  SIR-NAME                PIC X(22).
007400     02  SIR-ADDRESS             PIC X(25).
007500     02  SIR-CITY                PIC X(15).
007600     02  SIR-STATE               PIC X(02).
007700     02  SIR-ZIPCODE             PIC 9(05).
007800     02  SIR-GENDER              PIC X(01).
007900     02  SIR-MAJOR               PIC X(03).
008000 01  COURSE-INFO-RECORD.
008100     02  CIR-STUDENT-ID          PIC X(07).
008200     02  CIR-KEY-ID              PIC 9(03).
008300     02  CIR-COURSE-ID           PIC 9(06).
008400     02  CIR-GRADE               PIC S9(3)V99 COMP-3.
008500
008600 PROCEDURE DIVISION.
008700
008800 000-INITIATE.
008900
009000     DISPLAY 'KSDSLVAR: LOAD VARIABLE LENGTH KSDS'.
009100     DISPLAY '-----------------------------------'.
009200     DISPLAY ' '.
009300
009400     OPEN INPUT RECORD-IMAGES.
009500
009600     MOVE 'STMASTR' TO VSIO-DDNAME.
009700     MOVE VSIO-KSDS TO VSIO-ORGANIZATION.
009800     MOVE VSIO-SEQUENTIAL TO VSIO-ACCESS.
009900     MOVE VSIO-OUTPUT TO VSIO-MODE.
010000     MOVE +83 TO VSIO-RECORD-LENGTH.
010100     MOVE +0 TO VSIO-KEY-POSITION.
010200     MOVE +10 TO VSIO-KEY-LENGTH.
010300     MOVE VSIO-OPEN TO VSIO-COMMAND.
010400     CALL 'VSAMIO' USING VSIO-PARAMETER-BLOCK,
010500                         STUDENT-MASTER-FILE,
010600                         STUDENT-INFO-RECORD.
010700*    END-CALL.
010800     IF NOT VSIO-SUCCESS
010900         PERFORM 500-DISPLAY-UNKNOWN-ERROR THRU 509-EXIT
011000         STOP RUN.
011100*    END-IF.
011200
011300 010-PROCESS.
011400
011500     PERFORM 110-PROCESS-DATA
011600        THRU 119-EXIT
011700       UNTIL END-OF-FILE
011800          OR (NOT VSIO-SUCCESS).
011900*    END-PERFORM.
012000
012100 020-TERMINATE.
012200
012300     CLOSE RECORD-IMAGES.
012400
012500     MOVE VSIO-CLOSE TO VSIO-COMMAND.
012600     CALL 'VSAMIO' USING VSIO-PARAMETER-BLOCK,
012700                         STUDENT-MASTER-FILE,
012800                         STUDENT-INFO-RECORD.
012900*    END-CALL.
013000     IF NOT VSIO-SUCCESS
013100         PERFORM 500-DISPLAY-UNKNOWN-ERROR THRU 509-EXIT.
013200*    END-IF.
013300
013400     STOP RUN.
013500
013600 110-PROCESS-DATA.
013700     READ RECORD-IMAGES
013800         AT END
013900             MOVE 'Y' TO END-OF-FILE-SWITCH.
014000*    END-READ.
014100
014200     IF NOT END-OF-FILE
014300         PERFORM 120-ADD-RECORD THRU 129-EXIT.
014400*    END-IF.
014500
014600 119-EXIT.
014700     EXIT.
014800
014900 120-ADD-RECORD.
015000
015100     IF RI-COLUMN-8 NOT EQUAL SPACE
015200         MOVE RECORD-IMAGE TO CI-STUDENT-INFO
015300         PERFORM 130-ADD-STUDENT THRU 139-EXIT
015400     ELSE
015500         MOVE RECORD-IMAGE TO CI-COURSE-INFO
015600         PERFORM 140-ADD-COURSES THRU 149-EXIT.
015700*    END-IF.
015800
015900 129-EXIT.
016000     EXIT.
016100
016200 130-ADD-STUDENT.
016300
016400     MOVE CISI-STUDENT-ID TO SIR-STUDENT-ID.
016500     MOVE ZERO            TO SIR-KEY-ID.
016600     MOVE CISI-NAME       TO SIR-NAME.
016700     MOVE CISI-ADDRESS    TO SIR-ADDRESS.
016800     MOVE CISI-CITY       TO SIR-CITY.
016900     MOVE CISI-STATE      TO SIR-STATE.
017000     MOVE CISI-ZIPCODE    TO SIR-ZIPCODE.
017100     MOVE CISI-GENDER     TO SIR-GENDER.
017200     MOVE CISI-MAJOR      TO SIR-MAJOR.
017300
017400     MOVE +83 TO VSIO-RECORD-LENGTH.
017500     MOVE VSIO-WRITE TO VSIO-COMMAND.
017600     CALL 'VSAMIO' USING VSIO-PARAMETER-BLOCK,
017700                         STUDENT-MASTER-FILE,
017800                         STUDENT-INFO-RECORD.
017900*    END-CALL.
018000
018100     IF VSIO-SUCCESS
018200         GO TO 139-EXIT.
018300*    END-IF.
018400
018500     IF VSIO-LOGIC-ERROR
018600     AND VSIO-NO-MORE-SPACE
018700         DISPLAY 'INSUFFICIENT SPACE DEFINED IN '
018800                 'CLUSTER TO CONTAIN ALL RECORDS - '
018900                 'LOADING TERMINATED'
019000         GO TO 139-EXIT.
019100*    END-IF.
019200
019300     IF VSIO-LOGIC-ERROR
019400     AND VSIO-SEQUENCE-ERROR
019500         DISPLAY 'KEY SEQUENCE ERROR DURING LOAD'
019600         DISPLAY 'RECORD BYPASSED: (STUDENT) '
019700                 SIR-STUDENT-ID '-' SIR-KEY-ID ' ' SIR-NAME
019800         MOVE +0 TO VSIO-RETURN-CODE
019900         GO TO 139-EXIT.
020000*    END-IF.
020100
020200     PERFORM 500-DISPLAY-UNKNOWN-ERROR THRU 509-EXIT.
020300
020400 139-EXIT.
020500     EXIT.
020600
020700 140-ADD-COURSES.
020800
020900     MOVE ZERO TO CIR-KEY-ID.
021000     PERFORM 150-ADD-A-COURSE THRU 159-EXIT
021100       VARYING CICI-COURSE-IX
021200         FROM +1 BY +1
021300         UNTIL CICI-COURSE-IX > +8.
021400*    END-PERFORM.
021500
021600 149-EXIT.
021700     EXIT.
021800
021900 150-ADD-A-COURSE.
022000
022100     IF CICI-COURSE-ID (CICI-COURSE-IX) EQUAL ZEROS
022200         GO TO 159-EXIT.
022300*    END-IF.
022400
022500     MOVE CICI-STUDENT-ID TO CIR-STUDENT-ID.
022600     ADD 1 TO CIR-KEY-ID.
022700     MOVE CICI-COURSE-ID (CICI-COURSE-IX) TO CIR-COURSE-ID.
022800     MOVE +0 TO CIR-GRADE.
022900
023000     MOVE +19 TO VSIO-RECORD-LENGTH.
023100     MOVE VSIO-WRITE TO VSIO-COMMAND.
023200     CALL 'VSAMIO' USING VSIO-PARAMETER-BLOCK,
023300                         STUDENT-MASTER-FILE,
023400                         COURSE-INFO-RECORD.
023500*    END-CALL.
023600
023700     IF VSIO-SUCCESS
023800         GO TO 159-EXIT.
023900*    END-IF.
024000
024100     IF VSIO-LOGIC-ERROR
024200     AND VSIO-NO-MORE-SPACE
024300         DISPLAY 'INSUFFICIENT SPACE DEFINED IN '
024400                 'CLUSTER TO CONTAIN ALL RECORDS - '
024500                 'LOADING TERMINATED'
024600         GO TO 159-EXIT.
024700*    END-IF.
024800
024900     IF VSIO-LOGIC-ERROR
025000     AND VSIO-SEQUENCE-ERROR
025100         DISPLAY 'KEY SEQUENCE ERROR DURING LOAD'
025200         DISPLAY 'RECORD BYPASSED: (COURSE) '
025300                 CIR-STUDENT-ID '-' CIR-KEY-ID '-' CIR-COURSE-ID
025400         MOVE +0 TO VSIO-RETURN-CODE
025500         GO TO 159-EXIT.
025600*    END-IF.
025700
025800     PERFORM 500-DISPLAY-UNKNOWN-ERROR THRU 509-EXIT.
025900
026000 159-EXIT.
026100     EXIT.
026200
026300 500-DISPLAY-UNKNOWN-ERROR.
026400
026500     DISPLAY 'UNEXPECTED VSAMIO ERROR OCCURRED DURING '
026600             VSIO-COMMAND.
026700     EXHIBIT NAMED VSIO-RETURN-CODE.
026800     EXHIBIT NAMED VSIO-VSAM-RETURN-CODE,
026900                   VSIO-VSAM-FUNCTION-CODE,
027000                   VSIO-VSAM-FEEDBACK-CODE.
027100
027200 509-EXIT.
027300     EXIT.
