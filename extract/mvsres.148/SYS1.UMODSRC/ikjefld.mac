IKJEFLD  CSECT                  TSO pre prompt logon exit
         PRINT NOGEN
         TITLE 'IKJEFLD: Identify and initialize TSO terminal'
*
***********************************************************************
*                                                                     *
* NAME: IKJEFLD                                                       *
*                                                                     *
* TYPE: Assembler Source                                              *
*                                                                     *
* DESC: TSO pre prompt logon exit                                     *
*                                                                     *
* FUNCTION: - issue GTSIZE to retrieve terminal dimensions            *
*                                                                     *
*           - obtain access method and terminal id from TSB           *
*                                                                     *
*           - for TCAM 3270 terminals use the GTSIZE result and       *
*              o issue erase/write alternate if screen > 24 lines     *
*              o issue erase/write if screen <= 24 lines              *
*                                                                     *
*           - issue message                                           *
*                                                                     *
*             LGN001I TSO logon in progress at MTHD terminal TSBTRMID *
*                                                                     *
*              o MTHD is the access method TCAM or VTAM               *
*              o TSBTRMID is the terminal id                          *
*                                                                     *
* REQUIREMENTS: As IKJEFLD gets called unauthorized it needs to       *
*               authorize itself to be able to access the TSB. To     *
*               achieve this the authorization SVC 244 available in   *
*               most MVS Tur(n)key systems is used. If RAC is in      *
*               place and a RAC protected version of SVC 244 is in    *
*               use, the userid or group under which the master       *
*               scheduler is running must be authorized to access it. *
*                                                                     *
* HISTORY: 1979/10/01 Margaret Gardner   - initial implementation of  *
*                     GTE Laboratories     TCAM terminal initializa-  *
*                                          tion                       *
*                                                                     *
*          2012/02/25 Juergen Winkelmann - major rewrite to issue WTO *
*                     ETH Zuerich          to identify terminal id    *
*                                                                     *
*          2012/03/28 Juergen Winkelmann - issue initial erase/write  *
*                     ETH Zuerich          alternate to all TCAM      *
*                                          terminals gen'ed with      *
*                                          more than 24 lines.        *
*                                                                     *
***********************************************************************
*
         STM   R14,R12,12(R13)  save registers
         LR    R12,R15          establish ..
         USING IKJEFLD,R12                 .. addressability
         B     BEGIN            branch around id
         DC    CL8'IKJEFLD '    module name
         DC    CL8'ZJW0005 '    usermod id
BEGIN    GETMAIN RU,LV=LWORKA   get storage
         ST    R13,4(,R1)       chain the ..
         ST    R1,8(,R13)                  .. save areas
         LR    R13,R1           establish own save area
         USING WORKAREA,R13     addressability of work area
         GTSIZE ,               screen size?
         LTR   R15,R15          did GTSIZE work?
         BNZ   ERR               -> no, set up for write/error
         LR    R11,R0           remember number of lines
         MVC   LGN001I(LMSG),LGN001IL copy WTO list to work area
         L     R10,PSAAOLD-PSA(0) point to ASCB from PSA
         L     R10,ASCBTSB-ASCB(,R10) point to TSB from ASCB
         BSPAUTH ON             authorize ourselves
         MODESET KEY=ZERO       enter key zero to access TSB
         USING TSB,R10          addressability of TSB
         MVC   TERMID(8),TSBTRMID get terminal id from TSB
         MVC   ATTRIB(1),TSBFLG5 get fifth flag byte from TSB
         DROP  R10              TSB no longer needed
         MODESET KEY=NZERO      back to user key
         BSPAUTH OFF            authorization no longer needed
         TM    ATTRIB,TSBVTAM   VTAM terminal?
         BNO   TCAM              -> no, it is TCAM
         MVC   ACCESS,=CL2'VT'  indicate VTAM
         B     WTO              issue message and exit
TCAM     MVC   ACCESS,=CL2'TC'  indicate TCAM
         LA    R2,ERALT         set for mod 4
         CL    R11,MOD2         24 lines or less?
         BNL   TPUT             if not, TPUT erase/write alt
         CL    R11,NO3270       number of lines = 0?
         BE    WTO              must be non-3270, don't bother
         LA    R2,ERASE         set for plain erase/write
TPUT     TPUT  (R2),3,FULLSCR   TPUT initial screen setting
WTO      BSPAUTH ON             authorize ourselves
         MODESET MODE=SUP       enter supervisor state to issue ..
         WTO   MF=(E,LGN001I)    .. LGN001I message without '+'
         MODESET MODE=PROB      back to problem state
         BSPAUTH OFF            authorization no longer needed
RETURN   LR    R1,R13           get own save area address
         L     R13,4(,R13)      restore caller's save area address
         FREEMAIN RU,A=(R1),LV=LWORKA free save area storage
         LM    R14,R12,12(R13)  restore caller's regs
         SLR   R15,R15          set return code of zero
         BR    R14              return to caller
ERR      LA    R2,MSG1          set up error msg
         TPUT  (R2),3,FULLSCR   TPUT error
         B     RETURN           has unit prefix been filled?
ERALT    DC    X'277E4040'      erase/write alternate command
ERASE    DC    X'27F54040'      erase/write command
MSG1     DC    X'27F14040'      indication of error
NO3270   DC    F'0'             no of lines = 0 --> not a 3270 terminal
MOD2     DC    F'25'            mod 2 3270 no of lines + 1 for BNL
LGN001IL WTO   'LGN001I TSO logon in progress at xxAM terminal xxxxxxxxX
               ',MF=L           logon message
LMSG     EQU   *-LGN001IL       length of WTO list
         LTORG ,                literals go here
WORKAREA DSECT                  work area
SAVEAREA DS    18F              save area
LGN001I  DS    0F               logon message
         ORG   LGN001I+37       position of access method indicator
ACCESS   DS    CL2              access method indicator in logon msg
         ORG   LGN001I+51       terminal id position in logon msg
TERMID   DS    CL8              terminal id
         ORG   LGN001I+LMSG     position to end of WTO list
ATTRIB   DS    XL1              TSBFLG5 goes here
LWORKA   EQU   *-WORKAREA       size of work area
         YREGS                  register equates
         IHAPSA ,               map the PSA
         IHAASCB ,              map the ASCB
         IKJTSB ,               map the TSB
         END   IKJEFLD          end of program
