         MACRO
&NAME    MHPUT &WORK=,&RESERVE=
         GBLB  &IEDQZA(64) .                                    SA62998
         LCLA  &LEN,&RESERV
         LCLB  &NOGO   .                                       @SA75452
         LCLC  &WORKARE,&WORKREG
.*  CHANGE ACTIVITY AS FOLLOWS
.******************* MICROFICHE FLAGS *********************** SUPT CODE
.*C 485000-490000                                               SA56318
.*A056001,100100-100400                                         SA62998
.*D100350                                                       SA66910
.*A060000,108000,371000,460000,490000                          @SA75452
.*C100100-100300,560000,860000                                 @SA75452
.*C100100                                                      @OS76999
&LEN     SETA  4
         AIF   (&IEDQZA(5) OR &IEDQZA(14)).BADGRP              @OS76999
         AGO   .CKWORK                                         @OS76999
.BADGRP  ANOP                                                  @OS76999
         MNOTE 12,'*** IHB070 SEQUENCE ERROR - MUST FOLLOW INHDR OR INBX
               UF OR OUTHDR OR OUTBUF MACRO' .                 @SA75452
&NOGO    SETB  1 .                                             @SA75452
.CKWORK  ANOP                                                   SA62998
         AIF   ('&WORK' NE '').CKWK
         MNOTE 12,'***  IHB001  WORK OPERAND REQUIRED - NOT SPECIFIED'
&NOGO    SETB  1 .                                             @SA75452
         AGO   .RESERV
.CKWK    ANOP
         AIF   ('&WORK'(1,1) EQ '(').REGNO
&WORKARE SETC  '&WORK'
&LEN     SETA  &LEN+4
         AIF   ('&WORK'(1,1) GT 'Z').BADWORK
         AGO   .RESERV
.REGNO   ANOP
         AIF   ('&WORK'(K'&WORK,1) EQ ')').CKREG
         AGO   .BADWORK
.CKREG   ANOP
&WORKREG SETC  '&WORK'(2,K'&WORK-2)
         AIF   ('&WORKREG'(1,1) LT '0').RESERV
         AIF   ('&WORKREG' GE '2' AND '&WORKREG' LE '12').RESERV
.BADWORK ANOP
         MNOTE 12,'***  IHB300  WORK OPERAND INVALID AS SPECIFIED'
&NOGO    SETB  1 .                                             @SA75452
.RESERV  ANOP
         AIF   ('&RESERVE'  EQ '').NORES
&RESERV  SETA  &RESERVE
         AIF   (&RESERV GE 0).TST221
         MNOTE 12,'***  IHB300  RESERVE OPERAND INVALID AS SPECIFIED'
&NOGO    SETB  1 .                                             @SA75452
         AGO   .BAL
.TST221  AIF   (&RESERV LE 221).BAL
&RESERV  SETA  221
         MNOTE 4,'*** IED907 RESERVE OPERAND VALUE TOO LARGE - 221 WAS *
               USED'                                            SA56318
&NOGO    SETB  1 .                                             @SA75452
         AGO   .BAL
.NORES   ANOP
&RESERV  SETA  255
.BAL     AIF   (&NOGO EQ 1).MEND .                             @SA75452
         CNOP  0,4                                             @SA75452
&NAME    DS    0H
         BAL   1,*+8+&LEN .             BRANCH AROUND CNSTNTS  @PI06030
         DC    AL1(1) .                 MHPUT ID
         DC    AL1(&LEN) .              PARM LENGTH
         AIF   ('&WORKREG' NE '').WKREGDC
&WORKREG SETC  '0'
.WKREGDC ANOP
         DC    AL1(&WORKREG) .          WORK REG IF SPECIFIED
         DC    AL1(&RESERV) .           NO OF RESRV CHAR IF SPEC
         AIF   ('&WORKARE' EQ '').LOAD
         DC    A(&WORKARE) .            ADR OF WORK AREA IF SPEC
.LOAD    ANOP                                                  @YM06905
         DC    V(IEDQGP)                MHGET/MHPUT ROUTINE    @YM06905
         L     15,*-4 .                 MHGET/MHPUT RTNE ADDR  @YM06030
         BALR  14,15
.MEND    SPACE 2    .                                          @SA75452
         MEND   .                                              @SA75452
