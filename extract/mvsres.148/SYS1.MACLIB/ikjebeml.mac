         MACRO
&NAME    IKJEBEML  &MNUM,&RLEV2,&INS1,&INS2,&MF=
         LCLC  &REG
&REG     SETC  '1'
         AIF   ('&MF(1)' EQ 'E').TAG1
&NAME    DS    0F
         AIF   (N'&MNUM GT 0).TAG4
         MNOTE 4,'MESSAGE NUMBER MISSING'
         AGO   .EXIT
.TAG4    ANOP
         DC    AL2(&MNUM)
         AIF   (N'&RLEV2 EQ 0).TAG6
         DC    AL2(&RLEV2) REL LEVEL 2 MESSAGE NUMBER
         AGO   .TAG8
.TAG6    ANOP
         DC   AL2(0) NO LEVEL 2 MESSAGE
.TAG8    ANOP
         AIF   (N'&INS1 EQ 0).TAG10
         DC    A(&INS1) PTR TO INSERTION, LEVEL 1
         AGO   .TAG12
.TAG10   ANOP
         DC    A(0) NO LEVEL 1 INSERTION
.TAG12   ANOP
         AIF   (N'&INS2 EQ 0).TAG14
         DC    A(&INS2) PTR TO LEVEL 2 INSERTION
         AGO   .EXIT
.TAG14   ANOP
         DC    A(0) NO LEVEL 2 INSERTION
         AGO   .EXIT
.TAG1    ANOP
&NAME    DS    0H
         AIF   ('&MF(2)' EQ '(1)').TAG17
         AIF   ('&MF(2)'(1,1) EQ '(').TAG15
         LA    &REG,&MF(2) POINT TO LIST AREA
         AGO   .TAG17
.TAG15   ANOP
         LR    &REG,&MF(2)
.TAG17   ANOP
         AIF   (N'&MNUM EQ 0).TAG16
         AIF   ('&MNUM'(1,1) NE '(').TAG21
         STH   &MNUM,0(0,&REG) STORE MESSAGE NUMBER
         AGO   .TAG16
.TAG21   ANOP
         LA    0,&MNUM
         STH   0,0(&REG) STORE MESSAGE NUMBER
.TAG16   ANOP
         AIF   (N'&RLEV2 EQ 0).TAG18
         AIF   ('&RLEV2'(1,1) NE '(').TAG23
         STH   &RLEV2,2(0,&REG) STORE REL SECOND LEVEL NUMBER
         AGO   .TAG18
.TAG23   ANOP
         LA    0,&RLEV2
         STH   0,2(&REG) STORE REL LEVEL 2 NUMBER
.TAG18   ANOP
         AIF   (N'&INS1 EQ 0).TAG20
         AIF   ('&INS1'(1,1) EQ '(').TAG30
         LA    0,&INS1
         ST    0,4(&REG) STORE PTR TO INSERTION, LEVEL 1
         AGO   .TAG20
.TAG30   ANOP
         ST    &INS1,4(,&REG) STORE PTR TO INSERTION, LEVEL 1
.TAG20   ANOP
         AIF   (N'&INS2 EQ 0).TAG22
         AIF   ('&INS2'(1,1) EQ '(').TAG31
         LA    0,&INS2
         ST    0,8(&REG) STORE PTR TO INSERTION, LEVEL 2
         AGO   .TAG22
.TAG31   ANOP
         ST    &INS2,8(,&REG) STORE PTR TO INSERTION, LEVEL 2
.TAG22   ANOP
         IKJEBESH (COMMREG),IKJEBEMS,PARAM=((COMMREG),(1)),MF=(E,(1))
.EXIT    ANOP
         MEND
