         MACRO
&LINNME  LINEGRP &TERM=,&DDNAME=,&LINENO=,&UNITNO=1,&TRANTAB=,&TERMNO=,X
               &CODE=,&DIAL=YES,&INTVL=,&FEATURE=,&ADDR=,&SCREEN=,     X
               &GROUP=,&FULLSCR=NO,&USS=SCS,&LUNO=,&NCPFM=5,   @Y17XAYPX
               &NCPTS=5,&PUFM=0,&PUTS=1,&LUFM=0,&LUTS=1,       @Y17XAYPX
               &SESSION=,&LUTYPE=                              @Y17XAYP
.*
.*       OS/VS2 TCAM LEVEL 10                                  @Y17XAYP
.* A609000                                                     @OY19814
.* A609000,609200                                              @OZ32496
.*A055000,609500                                               @OZ34694
.*C609200                                                      @OZ34694
.*C604750-604800                                               @OZ44396
.*D604750,604780                                               @OZ56383
.*
.*
.*FUNCTION -- AFTER EXAMINING THE CALLER'S PARAMETERS FOR VALIDITY,
.*   VALUES ARE PLACED IN GLOBAL VARIABLES TO REFLECT THE CALLER'S
.*   SPECIFICATIONS FOR THIS LINE GROUP.  THE VALUES IN THE GLOBAL
.*   VARIABLES WILL BE USED LATER BY THE 'TSOMCP' MACRO TO GOVERN
.*   GENERATION OF THE TCAM MCP MACRO STATEMENTS.
.*
.*ENTRY POINTS -- NOT APPLICABLE
.*
.*INPUT -- MACRO CALL PARAMETERS
.*
.*OUTPUT -- VALUES IN GLOBAL VARIABLES TO BE USED LATER BY THE
.*   'TSOMCP' MACRO.  MNOTE MESSAGES IF INVALID PARAMETERS ARE
.*   DETECTED.
.*
.*EXTERNAL REFERENCES -- NOT APPLICABLE
.*
.*EXITS, NORMAL -- NORMAL EXIT OCCURS IF NO SERIOUS ERRORS IN THE
.*   CALLER'S PARAMETERS ARE DETECTED.
.*
.*EXITS,ERROR -- UPON DETECTION OF A SERIOUS ERROR, THE 'QUITSW'
.*   IS SET TO 1 AND FURTHER PROCESSING IS BYPASSED.
.*
.*TABLES/WORK AREAS -- SEE DESCRIPTIONS ACCOMPANYING VARIABLE
.*   DECLARATIONS.
.*
.*ATTRIBUTES -- NOT APPLICABLE
.*
.*CHARACTER CODE DEPENDENCY -- NONE
.*
.*NOTES -- NONE
.*
.*
.*       GLOBAL VARIABLES
.*
         GBLA  &NLG                NUMBER OF LINE GROUP MACROS ISSUED
         GBLA  &LNO(63)            CONTAINS LINENO= PARAMETER VALUES
         GBLA  &UNO(63)            CONTAINS UNITNO= PARAMETER VALUES
         GBLA  &INT(63)            CONTAINS INTVL= PARAMETER VALUES
         GBLA  &NFM(63)            CONTAINS NCPFM= PARAMETER   @Y17XAYP
         GBLA  &NTS(63)            CONTAINS NCPTS= PARAMETER   @Y17XAYP
         GBLA  &PFM(63)            CONTAINS PUFM= PARAMETER    @Y17XAYP
         GBLA  &PTS(63)            CONTAINS PUTS= PARAMETER    @Y17XAYP
         GBLA  &LFM(63)            CONTAINS LUFM= PARAMETER    @Y17XAYP
         GBLA  &LTS(63)            CONTAINS LUTS= PARAMETER    @Y17XAYP
         GBLA  &LUTYP(63)          CONTAINS THE LU TYPE        @YM08503
         GBLA  &ADRPT              POINTER TO LAST ADDR PARM GROUP
         GBLA  &SCRPT              POINTER TO LAST SCREEN PARM GROUP
         GBLA  &SCRH(2500)         CONTAINS SCREEN HEIGHT
         GBLA  &SCRW(2500)         CONTAINS SCREEN WIDTH
         GBLA  &TRMNO(2500)        CONTAINS NO. OF TERMINALS/LINE
         GBLA  &DDCT                    COUNT OF UNIQUE DDNAMES  Y06327
         GBLA  &RNNUMB             COUNT OF 3705'S               Y06327
         GBLA  &RNLINES            NO. OF 3705 LINES             Y06327
         GBLA  &RNTERMS            NO. OF 3705 TERMINALS         Y06327
         GBLA  &NONLU                   NON SNA OR 327C TERMS  @OZ34694
         GBLA  &LUTERMS            NO. OF LU                   @Y17XAYP
         GBLB  &T3705                  3705 LINE GROUP           Y06327
         GBLB  &T270X                  NON-3705 LINE GROUP       Y06327
         GBLB  &LGRPSW                 LINEGRP ISSUED SWITCH
         GBLB  &MCPSW                  TSOMCP  ISSUED SWITCH
         GBLB  &QUITSW                 GENERATION QUIT SWITCH
         GBLB  &BISYNC                 BISYNCHRONOUS LINE
         GBLB  &SNA                SNA MACROS REQUIRED         @Y17XAYP
         GBLB  &FSCR                    FULL SCREEN REQ. FLAG    Y06327
         GBLB  &SCRALL(2500)            FULL SCREEN READ SWITCHESY06327
         GBLB  &LUSB(2500)         LU/LU SESSION SWITCHES      @Y17XAYP
         GBLB  &USSB(2500)         USS=3270 SWITCHES           @Y17XAYP
         GBLB  &DIALSW(63)             DIAL LINE SWITCHES
         GBLB  &FETAB(189)             FEATURE= SWITCHES
.*                                     (3 X 63) = 189
         GBLC  &GRPNAME(63)        CONTAINS GROUP= PARAMETER     Y06327
         GBLC  &T(63)              CONTAINS TERM= PARAMETER VALUES
         GBLC  &DDNME(63)          CONTAINS DDNAME= PARAMETER VALUES
         GBLC  &TTAB(252)          CONTAINS TRANTAB= PARAMETER VALUES
         GBLC  &CTAB(252)          CONTAINS CODE= PARAMETER VALUES
.*                                     (4 X 63) = 252
         GBLC  &ADCHAR(2500)       CONTAINS ADDR= PARAMETER VALUES
.*
.*       LOCAL VARIABLES
.*
         LCLA  &A
         LCLA  &B
         LCLA  &C
         LCLA  &D
         LCLA  &E                                                Y06327
         LCLA  &G                  WORK VARIABLE               @Y17XAYP
         LCLA  &H                  WORK VARIABLE               @Y17XAYP
         LCLA  &K                  WORK VARIABLE               @Y17XAYP
         LCLA  &I                  WORK VARIABLE               @Y17XAYP
         LCLA  &LUNOS              COUNT OF LU THIS LINEGRP    @Y17XAYP
         LCLB  &BADTERM                TERM= BAD
         LCLB  &BADDDNM                DDNAME= BAD
         LCLB  &BADLNO                 LINENO= BAD
         LCLB  &BADUNO                 UNITNO= BAD
         LCLB  &BADTTAB                TRANTAB= BAD
         LCLB  &BADCODE                CODE= BAD
         LCLB  &BADDIAL                DIAL= BAD
         LCLB  &BADINTV                INTVL= BAD
         LCLB  &BADFE                  FEATURE= BAD
         LCLB  &QTDFTSW                QUOTE DEFAULT SW
         LCLB  &SWBC               BC41 TRAN TABLE SPECIFIED
         LCLB  &SWEB               EB41 TRAN TABLE SPECIFIED
         LCLB  &SWEBAS             EBCD OR ASCI TABLE SPECIFIED
         LCLB  &SW2260             INDICATES 2260/65 LINE GROUP
         LCLB  &SW3270             INDICATES 3270 LINE GROUP
         LCLC  &F                  HOLDS A FEATURE SUB-LIST MEMBER
.*
.*       TEST FOR MACRO CALL OUT OF ORDER OR TOO MANY LINEGRP MACRO
.*       CALLS ISSUED.
.*
&NLG     SETA  &NLG+1
&I       SETA  &NLG                     SHORT FORM FOR NLG
         AIF   (NOT &MCPSW).A0         TSOMCP MACRO ISSUED?
         MNOTE 16,'IKJ54101I LINEGRP MACRO MUST PRECEDE TSOMCP MACRO'
&QUITSW  SETB  1                       YES, SET QUIT SW.
.A0      ANOP
&LGRPSW  SETB  1                       SET LINEGRP ISSUED SW.
         AIF   (&NLG LE 63).A1         MAX. LINEGRP COUNT EXCEEDED?
         MNOTE 16,'IKJ54102I MORE THAN 63 LINEGRP MACROS'
&QUITSW  SETB  1                       YES, SET QUIT SW.
         MEXIT
.*
.*       ARE REQUIRED OPERANDS SPECIFIED
.*
.A1      AIF   ('&TERM' NE '' AND '&DDNAME' NE '' AND '&LINENO' NE '').*
               A2                                                Y06327
         MNOTE 12,'IKJ54103I REQUIRED OPERANDS(S) NOT SPECIFIED'
&QUITSW  SETB  1                       NO, SET QUIT SW.
         MEXIT
.A2      ANOP                                                    Y06327
         AIF   ('&GROUP' EQ '').GRP1 3705 LINE GROUP             Y06327
&T3705   SETB  1                   YES. SET 3705 SWITCH          Y06327
         AGO   .GRP2                                             Y06327
.GRP1    ANOP                                                    Y06327
&T270X   SETB  1                   NO. SET NON-3705 SWITCH       Y06327
.GRP2    ANOP                                                    Y06327
         AIF   (&T270X).GRP3       270X SW ON FOLLOWED BY        Y06327
         AGO   .GROUP                                            Y06327
.GRP3    ANOP                                                    Y06327
         AIF   ('&GROUP' EQ '').GROUP 3705 LINE GROUP CAUSES     Y06327
.*                                 ERROR MESSAGE                 Y06327
         MNOTE 16,'IKJ60000I 3705 LINE GROUPS MUST BE FIRST'     Y06327
&QUITSW  SETB  1                                                 Y06327
         MEXIT                                                   Y06327
.*  -------------------------------------------------------------------
.*  -----      GROUP= KEYWORD ANALYSIS     ----------------------------
.*  -------------------------------------------------------------------
.GROUP   ANOP                                                    Y06327
         AIF   ('&GROUP' EQ '').GRP5 3705 LINE GROUP             Y06327
         AIF   (K'&GROUP GT 8 OR N'&GROUP GT 1).GRP6 YES. IS IT VALID
.*                                                               Y06327
&A       SETA  1                                                 Y06327
&B       SETA  K'&GROUP                                          Y06327
.OK      ANOP                                                    Y06327
         AIF   ('&GROUP'(&A,1) EQ '$' OR '&GROUP'(&A,1) EQ '#' OR      X
               '&GROUP'(&A,1) EQ '@').OK1                        Y06327
         AIF   ('&GROUP'(&A,1) GE 'A' AND '&GROUP'(&A,1) LE 'Z').OK1
.*                                                               Y06327
         AIF   ('&GROUP'(&A,1) GE '0' AND '&GROUP'(&A,1) LE '9' AND &A X
               NE 1).OK1                                         Y06327
.GRP6    ANOP                                                    Y06327
         MNOTE 16,'IKJ54104I &GROUP INVALID FOR GROUP OPERAND'   Y06327
&QUITSW  SETB  1                                                 Y06327
         MEXIT                                                   Y06327
.OK1     ANOP                                                    Y06327
&A       SETA  &A+1                                              Y06327
         AIF   (&A GT 8 OR '&A' GT '&B').GRP4                    Y06327
         AGO   .OK                                               Y06327
.GRP4    ANOP                                                    Y06327
&GRPNAME(&I) SETC '&GROUP'         FOR 3705 SET GROUP= IN ARRAY  Y06327
&SNA     SETB  1                                               @YM08015
         AGO   .TERMCK                                           Y06327
.GRP5    ANOP                                                    Y06327
&GRPNAME(&I) SETC ''               FOR NON-3705 SET '' IN ARRAY  Y06327
.*  -------------------------------------------------------------------
.*  -----      TERM= KEYWORD ANALYSIS       ---------------------------
.*  -------------------------------------------------------------------
.TERMCK  ANOP                                                    Y06327
         AIF   ('&GROUP' EQ '').TERM CONINUE IF NOT 3705         Y06327
         AIF   (('&TERM'(1,3) EQ '226') OR ('&TERM'(4,1) EQ 'L')).T4
.*                                 2260 OR LOCAL DEVICES NOT     Y06327
.*                                 SUPPORTED ON 3705             Y06327
.TERM    AIF   (K'&TERM GT 4 OR N'&TERM GT 1).T4    TERM=KEYWORD VALID?
         AIF   ('&TERM' EQ '1050').T1   BRANCH IF TERM=1050
         AIF   ('&TERM' EQ '2741').T1   BRANCH IF TERM=2741
         AIF   ('&TERM' EQ '5041').T1   BRANCH IF TERM=5041
         AIF   ('&TERM' EQ '3335').T1   BRANCH IF TERM=3335
         AIF   ('&TERM' EQ '226R').T1   BRANCH IF TERM=226R
         AIF   ('&TERM' EQ '226L').T1   BRANCH IF TERM=226L
         AIF   ('&TERM' EQ '2265').T1   BRANCH IF TERM=2265
         AIF   ('&TERM' EQ '327L').T2   BRANCH IF TERM=327L
         AIF   ('&TERM' EQ '327R').T2   BRANCH IF TERM=327R
         AIF   ('&TERM' EQ '327S').T2   BRANCH IF TERM=327S
         AIF   ('&GROUP' EQ '').T4   327C AND PUNT NCP ONLY    @YM07280
         AIF   ('&TERM' EQ '327C').T2   BRANCH IF TERM=327C    @Y17XAYP
         AIF   ('&TERM' EQ 'PUNT').T1   BRANCH IF TERM=PUNT    @Y17XAYP
.T4      MNOTE 16,'IKJ54104I &TERM INVALID FOR TERM OPERAND'
&BADTERM SETB  1                       INDICATE BAD TERM= KEYWORD.
&QUITSW  SETB  1                       SET QUIT SW.
         AGO   .DDNME              GO CHECK DD NAME PARAMETER
.T2      ANOP
&BISYNC  SETB  1                   BISYNCHRONOUS LINE
.T1      ANOP
&T(&I)   SETC  '&TERM'             PUT VALUE IN ARRAY
.*  -------------------------------------------------------------------
.*  -----      DDNAME= KEYWORD ANALYSIS     ---------------------------
.*  -------------------------------------------------------------------
.DDNME   ANOP
         AIF   (K'&DDNAME GT 8 OR N'&DDNAME GT 1).DD3   KEYWORD VALID?
.*                                                               Y06327
&A       SETA  1                                                 Y06327
&B       SETA  K'&DDNAME                                         Y06327
.OKA     ANOP                                                    Y06327
         AIF   ('&DDNAME'(&A,1) EQ '$' OR '&DDNAME'(&A,1) EQ '#' OR    X
               '&DDNAME'(&A,1) EQ '@').OKB                       Y06327
         AIF   ('&DDNAME'(&A,1) GE 'A' AND '&DDNAME'(&A,1) LE 'Z').OKB
.*                                                               Y06327
         AIF   ('&DDNAME'(&A,1) GE '0' AND '&DDNAME'(&A,1) LE '9' AND  X
               &A NE 1).OKB                                      Y06327
.DD3     ANOP                                                    Y06327
         MNOTE 16,'IKJ54104I &DDNAME INVALID FOR DDNAME OPERAND'
&BADDDNM SETB  1                       INDICATE BAD DDNAME= KEYWORD.
&QUITSW  SETB 1                        NO, SET QUIT SW,
         AGO   .LNO                    BR TO TEST LINENO.
.OKB     ANOP                                                    Y06327
&A       SETA  &A+1                                              Y06327
         AIF   (&A GT 8 OR '&A' GT '&B').DD1                     Y06327
         AGO   .OKA                                              Y06327
.DD1     ANOP
&DDNME(&I) SETC '&DDNAME'              SAVE NAME.
         AIF   ('&GROUP' EQ '').DD4     BYPASS IF NOT 3705       Y06327
         AIF   (&NLG EQ 1).DD2                                   Y06327
         AIF   ('&DDNME(&NLG)' EQ '&DDNME(&NLG-1)').LNO          Y06327
.DD2     ANOP                                                    Y06327
&RNNUMB  SETA  &RNNUMB+1           COUNT 3705 UNITS              Y06327
.DD4     ANOP                                                    Y06327
&DDCT    SETA  &DDCT+1                  UPDATE DDNAME COUNT      Y06327
.*  -------------------------------------------------------------------
.*  -----      LINENO= KEYWORD ANALYSIS     ---------------------------
.*  -------------------------------------------------------------------
.LNO     ANOP
         AIF   (K'&LINENO GT 2 OR N'&LINENO GT 1).L1     KEYWORD VALID?
         AIF   (T'&LINENO NE 'N').L1
         AIF   (&LINENO LE 51 AND &LINENO GT 0).L2
.L1      MNOTE 16,'IKJ54104I &LINENO INVALID FOR LINENO OPERAND'
&BADLNO  SETB  1                       INDICATE BAD LINENO= KEYWORD.
&QUITSW  SETB  1                       NO, SET QUIT SW,
         AGO   .UNO                    BR TO TEST UNITNO.
.L2      ANOP
&LNO(&I) SETA  &LINENO                 SAVE LINENO.
         AIF   ('&GROUP' EQ '').UNO     BYPASS IF NOT 3705       Y06327
&RNLINES SETA  &RNLINES+&LNO(&I)   COUNT 3705 LINES              Y06327
.*  -------------------------------------------------------------------
.*  -----      UNITNO= KEYWORD ANALYSIS     ---------------------------
.*  -------------------------------------------------------------------
.UNO     ANOP
         AIF   (N'&UNITNO GT 1).U1     UNITNO= KEYWORD VALID?
         AIF   (T'&UNITNO EQ 'N').U2
.U1      MNOTE 16,'IKJ54104I &UNITNO INVALID FOR UNITNO OPERAND'
&BADUNO  SETB  1                       INDICATE BAD UNITNO= KEYWORD.
&QUITSW  SETB  1                       NO, SET QUIT SW,
         AGO   .TTAB                   BR TO TEST TRANTAB.
.U2      ANOP
         AIF   (&UNITNO GT 0 AND &UNITNO LT 256).U3  OPERAND IN RANGE
         MNOTE 4,'IKJ54110I UNITNO OPERAND OUT OF RANGE, DEFAULT VALUE -
               USED'
&UNO(&I) SETA  1                        USE DEFAULT VALUE
         AGO   .TTAB                    BR TO TEST TRANTAB
.U3      ANOP
&UNO(&I) SETA  &UNITNO                 SAVE UNITNO.
.*  -------------------------------------------------------------------
.*  -----      TRANTAB= KEYWORD ANALYSIS    ---------------------------
.*  -------------------------------------------------------------------
.TTAB    ANOP
         AIF   (&BADTERM).TTQUIT
         AIF   ('&GROUP' EQ '').TTAB1   BYPASS IF NOT 3705       Y06327
         AIF   ('&TRANTAB' EQ '').TT3   TEST FOR 3705 DEFAULT    Y06327
         AIF   (N'&TRANTAB GT 1).TTAB2
         AIF   ('&TRANTAB(1)' EQ 'EBCD').TT3                     Y06327
.TTAB2   ANOP                                                    Y06327
         MNOTE 4,'IKJ60001I TRANTAB INVALID FOR 3705 TERMINAL. DEFAULT X
               VALUE USED'                                       Y06327
         AGO   .TT3                                              Y06327
.TTAB1   ANOP                                                    Y06327
         AIF   ('&TRANTAB' EQ '').TTABDFT   TEST TRANTAB FOR DEFAULT.
&C       SETA  N'&TRANTAB               GET NO. OF SUBLIST OPERANDS
.*
.*
.*       SET UP FOR LOOP TO CHECK TABLE VALIDITY & STORE IN ARRAY
.*
&A       SETA  (&NLG-1)*4          SET ARRAY POINTER
&B       SETA  0                   SET SUBLIST POINTER
&D       SETA  1                   SET ARRAY OFFSET POINTER
.*
.TTLOOP1 ANOP                      LOOP EXECUTED ONCE/TRANSLATE TABLE
&B       SETA  &B+1                INCREMENT LOOP COUNTER
         AIF   (&B GT &C).CODE     BR IF NO MORE TABLES
.*
.*       SELECT CHECKING ROUTINE ACCORDING TO TERMINAL TYPE
.*
         AIF   ('&TERM' EQ '5041').TT5041
         AIF   ('&TERM' EQ '1050').TT5041
         AIF   ('&TERM' EQ '2741').TT2741
         AIF   ('&TERM' EQ '3335').TT3335
         AIF   ('&TERM' EQ '226L').TT226L
         AIF   ('&TERM' EQ '226R').TT226X
         AIF   ('&TERM' EQ '2265').TT226X
         AIF   ('&TERM' EQ '327L').TT226L
         AIF   ('&TERM' EQ '327R').TT327X
         AIF   ('&TERM' EQ '327S').TT327X
.*
.*       5041 & 1050
.*
.TT5041  ANOP
         AIF   ('&TRANTAB(&B)' EQ '1050').TTEB41
         AIF   ('&TERM' EQ '1050').TTBAD
.*
.*       2741
.*
.TT2741  AIF   ('&TRANTAB(&B)' EQ 'EB41').TTEB41
         AIF   ('&TRANTAB(&B)' EQ 'BC41').TTBC41
         AIF   ('&TRANTAB(&B)' EQ 'CR41').TTOK
         AGO   .TTBAD
.*
.*       3335
.*
.TT3335  ANOP
         AIF   ('&TRANTAB(&B)' EQ 'TTYB').TTOK
         AIF   ('&TRANTAB(&B)' EQ 'TTYC').TTOK
         AGO   .TTBAD
.*
.*       226L
.*
.TT226L  ANOP
         AIF   ('&TRANTAB(&B)' EQ 'EBCD').TTOK
         AGO   .TTBAD
.*
.*       226X
.*
.TT226X  ANOP
         AIF   ('&TRANTAB(&B)' EQ '2260').TTOK
         AIF   ('&TRANTAB(&B)' EQ '2265').TTOK
.*
.*       327R
.*
.TT327X  ANOP
         AIF   ('&TRANTAB(&B)' EQ 'EBCD').TT1
         AIF   ('&TRANTAB(&B)' NE 'ASCI').TTBAD
.TT1     ANOP
         AIF   (NOT &SWEBAS).TT2
         MNOTE 4,'IKJ54113I EBCD OR ASCI (BUT NOT BOTH) ALLOWED FOR THI-
               S TERMINAL TYPE, DEFAULT VALUE USED'
         AGO   .TTABDFT
.TT2     ANOP
&SWEBAS  SETB  1
         AGO   .TTOK
.*
.TTBAD   ANOP
         MNOTE 12,'IKJ54105I TRANTAB=&TRANTAB(&B) INVALID FOR THIS TERM-
               INAL TYPE'
&BADTTAB SETB  1                   INDICATE TRANTAB PARAMETER BAD
&QUITSW  SETB  1                   INDICATE SERIOUS ERROR
         AGO   .TTLOOP1            GO CHECK NEXT SUB-OPERAND
.*
.*       PUT TRANSLATE TABLE NAME INTO ARRAY
.*
.TTOK    ANOP
&TTAB(&A+&D) SETC '&TRANTAB(&B)'   INSERT TBL NAME IN ARRAY
&D       SETA  &D+1                BUMP ARRAY OFFSET POINTER
         AGO   .TTLOOP1            GO PROCESS NEXT SUB-OPERAND
.*
.TTEB41  ANOP
&SWEB    SETB  1                   INDICATE EB41 TABLE SPECIFIED
         AGO   .TTOK               GO PUT TABLE INTO ARRAY
.*
.TTBC41  ANOP
&SWBC    SETB  1                   INDICATE BC41 TABLE SPECIFIED
         AGO   .TTOK               GO PUT TABLE INTO ARRAY
.TTQUIT ANOP
&BADTTAB SETB  1                       INDICATE BAD TRANTAB= KEYWORD.
&QUITSW  SETB  1                       SET QUIT SW.
         AGO   .CODE
.*
.*    PROCESS TRANTAB DEFAULT VALUES.
.*
.TTABDFT AIF   ('&TERM' NE '1050').T2741  TERM=1050 SPECIFIED?
&A       SETA  (&NLG-1)*4              INITILIZE INDEX A.
&TTAB(&A+1) SETC '1050'                YES, DEFAULT TO TAB 1050.
         AGO   .CODE
.T2741   AIF   ('&TERM' NE '2741').T5041  TERM=2741 SPECIFIED?
&A       SETA  (&NLG-1)*4              INITILIZE INDEX A.
&TTAB(&A+1) SETC 'CR41'                 YES, DEFAULT TO TAB CR41,
&TTAB(&A+2) SETC 'EB41'                                 TAB EB41,
         AGO   .CODE
.T5041   AIF   ('&TERM' NE '5041').T3335  TERM=5041 SPECIFIED?
&A       SETA  (&NLG-1)*4              INITILIZE INDEX A.
&TTAB(&A+1) SETC '1050'                 YES, DEFAULT TO TAB 1050,
&TTAB(&A+2) SETC 'CR41'                                 TAB CR41.
&TTAB(&A+3) SETC 'EB41'                                 TAB EB41,
         AGO   .CODE
.T3335   AIF   ('&TERM' NE '3335').T2260  TERM=3335 SPECIFIED?
&A       SETA  (&NLG-1)*4              INITILIZE INDEX A.
&TTAB(&A+1) SETC 'TTYB'                 YES, DEFAULT TO TAB TTYB,
         AGO   .CODE
.T2260   AIF   ('&TERM' NE '226R').T226L  TERM=226R SPECIFIED?
&TTAB((&NLG-1)*4+1) SETC '2260'         SET 2260 DEFAULT
         AGO   .CODE
.T226L   AIF   ('&TERM' NE '226L').T2265  TERM=226L SPECIFIED?
&TTAB((&NLG-1)*4+1) SETC 'EBCD'         SET 2260 LOCAL DEFAULT
         AGO   .CODE
.T2265   AIF   ('&TERM' NE '2265').T3270  TERM=265 SPECIFIED?
&TTAB((&NLG-1)*4+1) SETC '2265'         SET 2265 DEFAULT
         AGO   .CODE
.T3270   ANOP
         AIF   ('&TERM' EQ '327L').TT3  TERM=327L SPECIFIED?
         AIF   ('&TERM' EQ '327R').TT3  TERM=327R SPECIFIED
         AIF   ('&TERM' NE '327S').TTQUIT
.TT3     ANOP
&TTAB((&NLG-1)*4+1) SETC 'EBCD'         SET 3270 DEFAULT
.*  -------------------------------------------------------------------
.*  -----      CODE= KEYWORD ANALYSIS    ------------------------------
.*  -------------------------------------------------------------------
.CODE    ANOP
         AIF   (&BADTTAB).CODER1        TRANTAB SPECIFIED CORRECTLY?
         AIF   ('&CODE' EQ '').CODEDFT TEST CODE FOR DEFAULT.
.*
.*             NOT DEFAULT, TEST FOR VALIDITY.
.*
         AIF   ('&TTAB((&NLG-1)*4+2)' EQ '').CODER2  BR IF ONLY 1 TBL
&C       SETA  N'&CODE                 GET NO. OF SUBLIST OPERANDS.
         AIF   (&C GT 4).CODER3        GT 4 OPERANDS SPECIFIED?
.*
.*   TEST CHARACTER COUNT OF EACH SUBLIST OPERAND.
.*
&D       SETA  1
.CLOOP1  AIF   (K'&CODE(&D) GT 8).CODER4  GT 8 CHARACTERS SPECIFIED?
         AIF   (&D EQ &C).CODEOK       MORE SUB-OPERANDS? NO, BR.
&D       SETA  &D+1                    INIT. TO TEST NEXT SUB-OPERAND
         AGO   .CLOOP1                 BR TO TEST AGAIN.
.CODER1  ANOP
         AGO   .CQUIT
.CODER2  MNOTE 4,'IKJ54106I CODE OPERAND SHOULD BE SPECIFIED ONLY IF   X
                MORE THAN ONE TRANSLATE TABLE IS SPECIFIED'
         AGO   .DIAL
.CODER3  MNOTE 12,'IKJ54107I CODE OPERAND CONTAINS MORE THAN 4 SUBLIST X
                OPERANDS'
         AGO   .CQUIT
.CODER4  MNOTE 12,'IKJ54108I CODE SUBOPERAND TOO LONG'
.CQUIT   ANOP
&BADCODE SETB  1                        INDICATE BAD CODE= KEYWORD.
&QUITSW  SETB  1                       SET QUIT SW.
         AGO   .DIAL                    BR TO TEST DIAL.
.*
.*   PROCESS CODE VALUES INTO TABLE.
.*
.CODEOK  ANOP
&A       SETA  (&NLG-1)*4               INITILIZE INDEX A.
&B       SETA  0                   INITIALIZE INDEX B
.CLOOP2  ANOP
&B       SETA  &B+1                     INCREMENT INDEX B.
         AIF   ('&CODE(&B)' EQ '').CODNULL  SKIP NULL OPERANDS
&CTAB(&A+1) SETC '&CODE(&B)'           SAVE CODE SUB-OPERAND IN TABLE.
&A       SETA  &A+1                    YES, INCREMENT INDEX A.
.CODNULL ANOP
         AIF   (N'&CODE LT (&B+1)).DIAL MORE SUB-OPERANDS? NO, BR.
         AGO   .CLOOP2
.*
.*   PROCESS CODE DEFAULT VALUES.
.*
.CODEDFT ANOP
         AIF   (NOT &SWBC  OR NOT &SWEB).CODESET
&QTDFTSW SETB  1                   INDICATE QUOTE IS DEFAULT VALUE
.CODESET ANOP
&A       SETA  (&NLG-1)*4               INITILIZE INDEX A.
         AIF   (&QTDFTSW).C1           DEFAULT OF (") TO BE USED.
&CTAB(&A+1) SETC 'LOGON'               NO, USE 'LOGON'.
         AGO   .DIAL                    BR TO TEST DIAL.
.C1      ANOP
&CTAB(&A+1) SETC '"LOGON'          SET DEFAULT FOR BC41/EB41
.*  -------------------------------------------------------------------
.*  -----      DIAL= KEYWORD ANALYSIS.     ----------------------------
.*  -------------------------------------------------------------------
.DIAL    ANOP
         AIF   (N'&DIAL GT 1).D1       TEST FOR VALID KEYWORD.
         AIF   ('&DIAL' EQ 'YES' OR '&DIAL' EQ 'NO').D2
.D1      MNOTE 16,'IKJ54104I &DIAL INVALID FOR DIAL OPERAND'
         AGO   .DQUIT
.D2      AIF   (NOT &BADTERM).D3       TERM= KEYWORD BAD?
         AGO   .DQUIT
.D3      AIF   ('&DIAL' EQ 'YES').D4   DIAL=YES TO BE USED?
         AIF   ('&TERM' EQ '5041' OR '&TERM' EQ '3335').D5
         AGO   .INTVL                  BR TO TEST INTVL.
.D4      ANOP
         AIF   ('&TERM'(1,3) EQ '226' OR '&TERM'(1,3) EQ '327').D5
.*                                 2260'S AND 3270'S ARE NON-SWITCHED
&DIALSW(&I) SETB 1                     INDICATE DIAL=YES SPECIFIED.
         AGO   .INTVL                  BR TO TEST INTVL.
.D5      MNOTE 12,'IKJ54109I DIAL OPERAND NOT CONSISTANT WITH TERM     X
                OPERAND'
.DQUIT   ANOP
&BADDIAL SETB  1                       INDICATE BAD DIAL=KEYWORD.
&QUITSW SETB  1                        SET QUIT SW.
.*  -------------------------------------------------------------------
.*  ----- INTVL= KEYWORD ANALYSIS     ---------------------------------
.*  -------------------------------------------------------------------
.INTVL ANOP
         AIF   ('&GROUP' EQ '').I0 BYPASS IF NOT 3705            Y06327
         AIF   ('&INTVL' EQ '').USS     BYPASS IF NOT CODED    @Y17XAYP
         MNOTE 4,'IKJ60002I INTVL OPERAND NOT USED FOR 3705. IGNORED'
         AGO   .USS                                            @Y17XAYP
.I0      ANOP                                                    Y06327
         AIF   (N'&INTVL GT 1).I1      TEST FOR VALID KEYWORD.
         AIF   ('&INTVL' EQ '').I3  IF OMITTED, GO SET DEFAULT
         AIF   (&INTVL GE 0 AND &INTVL LE 255).I2
.I1      MNOTE 4,'IKJ54110I INTVL OPERAND OUT OF RANGE, DEFAULT VALUE U-
               SED'
.I3      ANOP
&INT(&I) SETA  0                   USE DEFAULT VALUE
         AIF   ('&TERM' EQ '2741').USS                         @Y17XAYP
         AIF   ('&TERM' EQ '226L').USS                         @Y17XAYP
         AIF   ('&TERM' EQ '327L').USS                         @Y17XAYP
         AIF   ('&TERM' EQ '3335').USS                         @Y17XAYP
&INT(&I) SETA  2                   SET DEFAULT FOR POLLED TERMINALS
         AGO   .USS                    BR TO TEST USS          @Y17XAYP
.I2      ANOP
&INT(&I) SETA  &INTVL                  SAVE INTVL VALUE.
.USS     AIF   ('&GROUP' EQ '').FEATURE BYPASS IF NOT NCP      @Y17XAYP
         AIF   ('&TERM' NE 'PUNT').NCPFM   BYPASS PUNT ONLY OP @Y17XAYP
         AIF   ('&LUTYPE' NE '').CKTYP   LUTYPE REQD FOR PUNT  @YM08503
         MNOTE 12,'IKJ54103I REQUIRED OPERANDS(S) NOT SPECIFIED'
         AGO   .CKUSS                                          @YM08503
.CKTYP   ANOP                                                  @YM08503
         AIF   (K'&LUTYPE GT 1 OR N'&LUTYPE GT 1).ERTYP        @YM08503
         AIF   (T'&LUTYPE NE 'N').ERTYP  VALID LUTYPE OPERAND  @YM08503
         AIF   (&LUTYPE LT 0 OR &LUTYPE GT 2).ERTYP            @YM08503
&LUTYP(&I) SETA &LUTYPE    SET LU TYPE FOR TSOMCP              @YM08503
         AGO   .CKUSS                                          @YM08503
.ERTYP   MNOTE 8,'IKJ54104I &LUTYPE INVALID FOR LUTYPE OPERAND'
.CKUSS   ANOP                                                  @YM08503
         AIF   ('&USS' EQ '').CKSES USS NOT SPECIFIED          @Y17XAYP
         AIF   ('&USS' NE '3270').CKSCS NOT USS USE 3270 DATA  @Y17XAYP
         AGO   .CKSES                                          @Y17XAYP
.CKSCS   AIF   ('&USS' NE 'SCS').ERUSS INDICATE INVALID USS    @Y17XAYP
         AGO   .CKSES                                          @Y17XAYP
.ERUSS   MNOTE 8,'IKJ54104I &USS INVALID FOR USS OPERAND'      @Y17XAYP
.CKSES   AIF   ('&TERM' NE 'PUNT').FEATURE BYPASS IF NOT NCP   @Y17XAYP
         AIF   ('&SESSION' EQ '').NCPFM SESSION NOT SPECIFIED  @Y17XAYP
         AIF   (N'&SESSION GT 1).ERSES  NO. OPERANDS VALID     @Y17XAYP
         AIF   ('&SESSION' NE 'YES' AND '&SESSION' NE 'NO').ERSES
         AGO   .NCPFM                                          @Y17XAYP
.ERSES   MNOTE 4,'IKJ54110I SESSION OPERAND OUT OF RANGE, DEFAULT VALUEX
               USED'                                           @Y17XAYP
.NCPFM   AIF   (K'&NCPFM GT 1 OR N'&NCPFM GT 1).NFMER          @Y17XAYP
         AIF   (T'&NCPFM NE 'N').NFMER VALID NCPFM OPERAND     @Y17XAYP
         AIF   (&NCPFM LT 0 OR &NCPFM GT 5).NFMER              @Y17XAYP
&NFM(&I) SETA  &NCPFM              SET NCP FM PROFILE          @Y17XAYP
         AGO   .NCPTS              CK NCPTS OPERAND            @Y17XAYP
.NFMER   MNOTE 4,'IKJ54110I NCPFM OUT OF RANGE, DEFAULT VALUE USED'
&NFM(&I) SETA  5                   DEFAULT NCPFM VALUE         @Y17XAYP
.NCPTS   AIF   (K'&NCPTS GT 1 OR N'&NCPTS GT 1).NTSER          @Y17XAYP
         AIF   (T'&NCPTS NE 'N').NTSER VALID NCPTS OPERAND     @Y17XAYP
         AIF   (&NCPTS LT 0 OR &NCPTS GT 5).NTSER              @Y17XAYP
&NTS(&I) SETA  &NCPTS              SET NCP TS PROFILE          @Y17XAYP
         AIF   ('&TERM' NE 'PUNT').FEATURE                     @Y17XAYP
         AGO   .PUFM               CK NCPTS OPERAND            @Y17XAYP
.NTSER   MNOTE 4,'IKJ54110I NCPTS OUT OF RANGE, DEFAULT VALUE USED'
&NTS(&I) SETA  5                   DEFAULT NCPTS VALUE         @Y17XAYP
         AIF   ('&TERM' NE 'PUNT').FEATURE                     @Y17XAYP
.PUFM    AIF   (K'&PUFM GT 1 OR N'&PUFM GT 1).PFMER            @Y17XAYP
         AIF   (T'&PUFM NE 'N').PFMER VALID PUFM OPERAND       @Y17XAYP
         AIF   (&PUFM LT 0 OR &PUFM GT 5).PFMER                @Y17XAYP
&PFM(&I) SETA  &PUFM               SET PU FM PROFILE           @Y17XAYP
         AGO   .PUTS               CK PUTS OPERAND             @Y17XAYP
.PFMER   MNOTE 4,'IKJ54110I PUFM OUT OF RANGE, DEFAULT VALUE USED'
&PFM(&I) SETA  0                   DEFAULT PUFM VALUE          @Y17XAYP
.PUTS    AIF   (K'&PUTS GT 1 OR N'&PUTS GT 1).PTSER            @Y17XAYP
         AIF   (T'&PUTS NE 'N').PTSER   VALID PUTS OPERAND     @Y17XAYP
         AIF   (&PUTS LT 0 OR &PUTS GT 5).PTSER                @Y17XAYP
&PTS(&I) SETA  &PUTS               SET PU TS PROFILE           @Y17XAYP
         AGO   .LUFM               CK LUFM OPERAND             @Y17XAYP
.PTSER   MNOTE 4,'IKJ54110I PUTS OUT OF RANGE, DEFAULT VALUE USED'
&PTS(&I) SETA  1                   DEFAULT PUTS VALUE          @Y17XAYP
.LUFM    AIF   (K'&LUFM GT 1 OR N'&LUFM GT 1).LFMER            @Y17XAYP
         AIF   (T'&LUFM NE 'N').LFMER   VALID LUFM OPERAND     @Y17XAYP
         AIF   (&LUFM LT 0 OR &LUFM GT 5).LFMER                @Y17XAYP
&LFM(&I) SETA  &LUFM               SET LU FM PROFILE           @Y17XAYP
         AGO   .LUTS               CK LUTS OPERAND             @Y17XAYP
.LFMER   MNOTE 4,'IKJ54110I LUFM OUT OF RANGE, DEFAULT VALUE USED'
&LFM(&I) SETA  0                   DEFAULT LUFM VALUE          @Y17XAYP
.LUTS    AIF   (K'&LUTS GT 1 OR N'&LUTS GT 1).LTSER            @Y17XAYP
         AIF   (T'&LUTS NE 'N').LTSER   VALID LUTS OPERAND     @Y17XAYP
         AIF   (&LUTS LT 0 OR &LUTS GT 5).LTSER                @Y17XAYP
&LTS(&I) SETA  &LUTS               SET LU TS PROFILE           @Y17XAYP
         AGO   .FEATURE            CK FEATURE OPERAND          @Y17XAYP
.LTSER   MNOTE 4,'IKJ54110I LUTS OUT OF RANGE, DEFAULT VALUE USED'
&LTS(&I) SETA  1                   DEFAULT LUTS VALUE          @Y17XAYP
.FEATURE AIF   (N'&FEATURE GT 3).FEER1 TEST FOR EXCESSIVE OPERANDS.
         AIF   (&BADTERM).FEER2        TEST FOR BAD TERM= KEYWORD.
.*
.*       ESTABLISH DEFAULT ASSUMPTIONS
.*
&A       SETA  (&NLG-1)*3          SET TABLE GROUP POINTER
         AIF   ('&TERM' EQ '2741' OR '&TERM' EQ '3335').F1
         AIF   ('&TERM' EQ '226L' OR '&TERM' EQ '226R').F226
         AIF   ('&TERM' EQ '327L' OR '&TERM' EQ '327R').F327
         AIF   ('&TERM' EQ '327S').F327
         AIF   ('&TERM' EQ '327C').F327  3270 CLUSTER          @Y17XAYP
         AIF   ('&TERM' EQ 'PUNT').FPUNT SNA PU                @Y17XAYP
         AIF   ('&TERM' NE '2265').NOBATN
.F226    ANOP
&SW2260  SETB  1                   INDICATE 2260/65 LINE GROUP
         AGO   .F2                 GO PROCESS FEATURE SUBLIST
.F327    ANOP
&SW3270  SETB  1                   INDICATE 3270 LINE GROUP
         AGO   .F2
.NOBATN  ANOP
&FETAB(&A+2) SETB 1
         AGO   .F2
.FPUNT   ANOP                                                  @Y17XAYP
&SNA     SETB  1                   INCLUDE SNA MACROS          @Y17XAYP
&FETAB(&A+1)   SETB 1                                          @Y17XAYP
&FETAB(&A+2)   SETB 1                                          @Y17XAYP
         AGO   .F2                                             @1Y17XAP
.F1      ANOP
&FETAB(&A+1) SETB 1
&FETAB(&A+2) SETB 1
&FETAB(&A+3) SETB 1
.F2      ANOP
.*
.*       PROCESS FEATURE SUBLIST, SETTING TABLE SWITCHES IF INDICATED
.*       FEATURES ARE VALID.
.*
         AIF   ('&FEATURE' EQ '').TERMNO  BR IF DEFAULTS WANTED
&B       SETA  1              SET SUB LIST POINTER
.FLOOP   ANOP
&F       SETC  '&FEATURE(&B)'
         AIF   ('&F' NE 'BREAK').FL1
         AIF   (&SW2260 OR &SW3270).FEER3
&FETAB(&A+1) SETB 1
         AGO   .FTEST
.FL1     AIF   ('&F' NE 'NOBREAK').FL2
         AIF   ('&TERM' EQ '3335').FEER3
&FETAB(&A+1) SETB 0
         AGO   .FTEST
.FL2     AIF   ('&F' NE 'ATTN').FL3
         AIF   (&SW2260 OR &SW3270).FEER3
&FETAB(&A+2) SETB 1
         AGO   .FTEST
.FL3     AIF   ('&F' NE 'NOATTN').FL4
         AIF   ('&TERM' EQ '3335').FEER3
&FETAB(&A+2) SETB 0
         AGO   .FTEST
.FL4     AIF   ('&F' NE 'TOSUPPR').FEER3
         AIF   (&SW2260 OR &SW3270).FEER3
&FETAB(&A+3) SETB 1
.FTEST   ANOP
&B       SETA  &B+1
         AIF   (&B LE N'&FEATURE).FLOOP
         AGO   .TERMNO
.FEER1   MNOTE 12,'IKJ54111I FEATURE OPERAND CONTAINS TOO MANY SUBLIST -
                OPERANDS'
         AGO   .FQUIT
.FEER2   ANOP
         AGO   .FQUIT
.FEER3   MNOTE 4,'IKJ54112I SUBLIST OPERAND &B OF FEATURE OPERAND IS IN-
               VALID'
         AGO   .FTEST              GO CONTINUE TESTING
.FQUIT   ANOP
&BADFE   SETB  1                       INDICATE BAD FEATURE= KEYWORD.
&QUITSW  SETB  1                       SET QUIT SW.
.TERMNO  ANOP
.*
.*
&A       SETA  1                   SET TERMNO VALUE SELECTOR   @Y17XAYP
&B       SETA  0                   CLEAR TERMINAL COUNT
&G       SETA  0                   CUMLATIVE LU COUNT          @Y17XAYP
&H       SETA  1                   SET LUNO OPERAND SELECTOR   @Y17XAYP
.TCNTLOP ANOP                      LOOP EXECUTED ONCE/LINE IN LINEGRP
&K       SETA  1                   SET LUNO PER PUNO COUNT     @Y17XAYP
.CNTTRM  AIF   (&A GT N'&TERMNO).COUNT1    IS DEFAULT REQUIRED @Y17XAYP
         AIF   ('&TERMNO(&A)' EQ '').COUNT1 DEFAULT REQUIRED   @Y17XAYP
&B       SETA  &B+&TERMNO(&A)      UPDATE TERMINAL COUNT
&C       SETA  N'&TERMNO           NO. TERMNO OPERANDS         @Y17XAYP
         AGO   .LCNTLOP            GO TO LOOP TEST             @Y17XAYP
.COUNT1  ANOP
&B       SETA  &B+1                UPDATE TERMINAL COUNT BY 1
.LCNTLOP AIF   ('&LUNO' EQ '').ADD1 LUNO SPECIFIED             @Y17XAYP
         AIF   (&H GT N'&LUNO).ADD1 DEFAULT REQUIRED           @Y17XAYP
         AIF   ('&LUNO(&H)' EQ '').ADD1 DEFAULT REQUIRED       @Y17XAYP
&G       SETA  &G+&LUNO(&H)        UPDATE LU COUNT             @Y17XAYP
         AGO   .LCNTTST            CK FOR MORE LUNO TO ADD     @Y17XAYP
.ADD1    ANOP                                                  @Y17XAYP
&G       SETA  &G+1                UPDATE LU COUNT BY 1        @Y17XAYP
.LCNTTST ANOP                      CK FOR MORE LUNO TO ADD     @Y17XAYP
&H       SETA  &H+1                SELECT NEXT LUNO VALUE      @Y17XAYP
&K       SETA  &K+1                COUNT LUNO PER PUNO-TERMNO  @Y17XAYP
.LNTST   ANOP                                                  @Y17XAYP
&A       SETA  &A+1                GO ADD NEXT LINE            @Y17XAYP
         AIF   (&A LE &LNO(&NLG)).TCNTLOP
         AIF   ('&GROUP' EQ '').RNCT    BYPASS IF NOT 3705       Y06327
&RNTERMS SETA  &RNTERMS+&B         COUNT 3705 TERMINALS          Y06327
         AIF   ('&TERM' EQ '327C').CNTCLU COUNT 3270 CLUSTERS  @OZ32496
         AIF   ('&TERM' NE 'PUNT').CNTNON NOT SNA TERMINAL     @OZ34694
.CNTCLU  ANOP                                                  @OZ32496
&LUTERMS SETA  &LUTERMS+&G         COUNT LU ENTRIES            @Y17XAYP
         AGO   .RNCT                                           @OZ34694
.CNTNON  ANOP                                                  @OZ34694
&NONLU   SETA  &NONLU+&B                COUNT NON SNA TERMS    @OZ34694
.RNCT    ANOP                                                    Y06327
         AIF   (&NLG NE 1).TRM1    BRANCH UNLESS THIS IS FIRST CALL
&ADRPT   SETA  1                   INITIALIZE ADDR CHARS ARRAY POINTER
&SCRPT   SETA  1                   INITIALIZE SCREEN ARRAY POINTER
         AGO   .TRM2               GO TEST PARAMETER VALIDITY
.TRM1    ANOP
&B       SETA  0                   SET LINE COUNTER            @Y17XAYP
&C       SETA  0                   TERMNO VALUE                @Y17XAYP
&D       SETA  0                   LUNO SELECTOR               @Y17XAYP
&E       SETA  0                   ACCUMULATED LUNO COUNT      @Y17XAYP
&G       SETA  0                                               @Y17XAYP
&H       SETA  0                   ACCUMULATED LU COUNT        @Y17XAYP
.TRMLP1  ANOP                      LOOP ONCE/LINE IN LAST LINEGRP
&C       SETA  &TRMNO(&SCRPT+&B+&D)  GET TERMNO VALUE          @Y17XAYP
&E       SETA  &E+&C               ACCUMULATE COUNT LU ENTRY   @Y17XAYP
.LULOP   ANOP                                                  @Y17CAYP
&D       SETA  &D+1                INCREMENT LUNO SELECTOR     @Y17XAYP
&H       SETA  &H+&TRMNO(&SCRPT+&B+&D) ACCUMULATE COUNT OF LU  @Y17XAYP
&G       SETA  &G+1                INCREMENT LUNO SELECTOR     @Y17XAYP
         AIF   (&G LT &C).LULOP    MORE LUNO ENTRIES/TERMNO    @Y17XAYP
&B       SETA  &B+1                SELECT NEXT TERMNO ENTRY    @Y17XAYP
&G       SETA  0                   RESET LUNO CNT THIS TERMNO  @Y17XAYP
         AIF   (&B LT &LNO(&NLG-1)).TRMLP1 BR IF MORE LINES    @Y17XAYP
&SCRPT   SETA &SCRPT+&LNO(&NLG-1)+&E                           @Y17XAYP
.*                                 UPDATE TRMNO ARRAY PTR      @Y17XAYP
&ADRPT   SETA  &ADRPT+&H           UPDATE SCR AND ADR ARRAY PTR@Y17XAYP
.*       CHECK PARAMETER VALIDITY
.*
.TRM2    AIF   ('&TERMNO' EQ '').TRMDEF                        @Y17XAYP
         AIF   ('&TERM' EQ '1050').TRM3 TERMNO VALID FOR 1050    Y06327
         AIF   (&SW2260 OR &SW3270).TRM3
.*                                 TERMO VALID FOR 2260/2265/3270
         AIF   ('&TERM' EQ 'PUNT').TRM3 TERMNO VALID FOR PUNT  @Y17XAYP
         MNOTE 4,'IKJ54114I TERMNO OPERAND NOT ALLOWED FOR THIS TERMINA-
               L TYPE'
         AGO   .TRMDEF             GO GENERATE DEFAULT VALUES
.TRM3    AIF   (&BADLNO).ALLDONE                               @Y17XAYP
.TRM3B   AIF   (N'&TERMNO LE &LNO(&NLG)).TRM4                  @Y17XAYP
         MNOTE 8,'IKJ54115I TERMNO OPERAND REFERENCES NON-EXISTENT LINE-
               S'
.*
.*       MOVE PARAMETER VALUES INTO ARRAY
.*
.TRM4    ANOP
&A       SETA  N'&TERMNO           SET NUMBER OF OPERANDS TO MOVE
.TRM5    ANOP                      ENTER HERE IF NOT TO MOVE ALL
&B       SETA  1                   SET LINE LOOP CONTROL       @Y17XAYP
&G       SETA  0                   NO. LUNO OPERANDS MOVED     @Y17XAYP
&H       SETA  0                   SET LUNO SELECTOR           @Y17XAYP
.TRMMOV  AIF   (&B GT &A).TRMONE   BR IF DEFAULT NEEDED        @Y17XAYP
         AIF   ('&TERMNO(&B)' EQ '').TRMONE  BR IF DEFAULT NEEDED
&TRMNO(&SCRPT+&B+&H-1) SETA &TERMNO(&B) MVOE OPERAND TO ARRAY  @Y17XAYP
         AGO   .TRM6               GO TO LOOP TEST
.TRMONE  ANOP
&TRMNO(&SCRPT+&B+&H-1) SETA 1      PUT DEFAULT VALUE IN ARRAY  @Y17XAYP
.TRM6    ANOP
&H       SETA  &H+1                INCREMENT LUNO SELECTOR     @Y17XAYP
         AIF   (&H GT N'&LUNO).LUONE DEFAULT NEEDED            @Y17XAYP
         AIF   ('&LUNO(&H)' EQ '').LUONE DEFAULT NEEDED        @Y17XAYP
&TRMNO(&SCRPT+&B+&H-1) SETA &LUNO(&H) MOVE LUNO TO ARRAY       @Y17XAYP
         AGO   .LUNO1                                          @Y17XAYP
.LUONE   ANOP                                                  @Y17XAYP
&TRMNO(&SCRPT+&B+&H-1) SETA 1      MOVE DEFAULT TO ARRAY       @Y17XAYP
.LUNO1   ANOP                                                  @Y17XAYP
&G       SETA  &G+1                ADD 1 TO LUNO COUNT         @Y17XAYP
.LUCHK   AIF   ('&TERMNO' EQ '').LUDEF    DEFAULT REQUIRED     @Y17XAYP
         AIF   ('&TERMNO(&B)' EQ '').LUDEF DEFAULT REQUIRED    @Y17XAYP
         AIF   (&G LT &TERMNO(&B)).TRM6 MORE LUNO THIS TERMNO  @Y17XAYP
.LUDEF   ANOP                                                  @Y17XAYP
&G       SETA  0                   RESET NO. LUNOMOVED/TERMNO  @Y17XAYP
&B       SETA  &B+1                BUMP LOOP CONTROL VARIABLE
         AIF   (&B LE &LNO(&NLG)).TRMMOV  LOOP BACK IF MORE LINES
         AGO   .SCREEN             GO PROCESS SCREEN PARAMETER
.TRMDEF  ANOP                      SET DEFAULT ONE TERMINAL/LINE
&A       SETA  0                   ASK TO MOVE NO OPERANDS
         AGO   .TRM5               GO MOVE DEFAULT VALUES INTO ARRAY
.SCREEN  AIF   ('&FULLSCR' EQ 'YES' OR '&FULLSCR' EQ 'NO').SCRF1 Y06327
.*                                      VALID OPERAND            Y06327
         MNOTE 16,'IKJ54104I &FULLSCR INVALID FOR FULLSCR OPERAND'
.*                                                               Y06327
&QUITSW  SETB  1                                                 Y06327
         MEXIT                                                   Y06327
.SCRF1   AIF   ('&FULLSCR' EQ 'YES' AND '&TERM' EQ 'PUNT').FSCR1
.*                                 FULLSCR VALID FOR PUNT      @Y17XAYP
         AIF   ('&FULLSCR' EQ 'YES' AND &SW3270 NE 1).SCRF2      Y06327
.*                                 FULLSCR VALID FOR 3270      @Y17XAYP
         AIF   ('&FULLSCR' EQ 'NO').SCRF3 SET FLAG IF FULLSCR REQY06327
.FSCR1   ANOP                                                  @Y17XAYP
&FSCR    SETB  1                                                 Y06327
         AGO   .SCRF3                                            Y06327
.SCRF2   MNOTE 4,'IKJ54114I FULLSCR OPERAND NOT ALLOWED FOR THIS TERMINX
               AL TYPE'                                        @Y17XAYP
.SCRF3   AIF   ('&SCREEN' EQ '').SCRDEF BR IF DEFAULT VALUES NEEDED
         AIF   (&SW2260 OR &SW3270).SCR1
.*                                      BR IF VALID TERMINAL TYPE
         AIF   ('&TERM' EQ 'PUNT').SCR1 SCREEN VALID FOR PUNT  @Y17XAYP
         MNOTE 4,'IKJ54114I SCREEN OPERAND NOT ALLOWED FOR THIS TERMINA-
               L TYPE'
         AGO   .SCRF6                                          @Y17XAYP
.SCR1    AIF   (N'&SCREEN EQ 2).SCR2    BR IF NO. OF OPERANDS OK
         MNOTE 4,'IKJ54116I SCREEN OPERAND MUST BE A TWO-OPERAND SUBLIS-
               T'
         AGO   .SCRDEF                  GO GENERATE DEFAULT VALUES
.*
.*       CHECK FOR STANDARD VALUES
.*
.SCR2    ANOP
&A       SETA  &SCREEN(1)
&B       SETA  &SCREEN(2)
         AIF   (&A EQ 12 AND &B EQ 40).SCRSTD
         AIF   (&A EQ 12 AND &B EQ 80).SCR3
         AIF   (&A EQ 6 AND &B EQ 40).SCR3
         AIF   (&A EQ 15 AND &B EQ 64).SCR3
         AIF   (&A EQ 24 AND &B EQ 80).SCR4
         AGO   .SCRNSTD
.SCR3    AIF   (&SW2260).SCRSTD                                @Y17XAYP
         AGO   .SCRNSTD
.SCR4    AIF   (&SW3270).SCRSTD                                @Y17XAYP
         AIF   ('&TERM' EQ 'PUNT').SCRSTD  ANY SCRSZ VALID     @YM07281
.SCRNSTD MNOTE 4,'IKJ54117I NON-STANDARD SCREEN DIMENSIONS -- ACCEPTED'
.*
         AGO   .SCRSTD             USE VALUES SPECIFIED          Y06327
.SCRDEF  AIF   (NOT &SW2260).SCRDEF1    GENERATE FOR 2260/65   @Y17XAYP
&A       SETA  12                       SET DEFAULT HEIGHT
&B       SETA  80                       SET DEFAULT WIDTH
         AGO   .SCRSTD
.SCRDEF1 AIF   (NOT &SW3270).ZERODEF    ZER SCREEN SIZE        @Y17XAYP
&A       SETA  24                       SET DEFAULT HEIGHT
&B       SETA  80                       SET DEFAULT WIDTH
         AGO   .SCRSTD                                         @Y17XAYP
.ZERODEF ANOP                                                  @Y17XAYP
&A       SETA  0                        SET 0 HEIGHT           @Y17XAYP
&B       SETA  0                        SET 0 WIDTH            @Y17XAYP
.*
.SCRSTD  ANOP
.*
.*       INSERT SCREEN DIMENSIONS IN ARRAYS
.*
&D       SETA  0                        TERMNO ENTRY SELECTOR  @Y17XAYP
&C       SETA  0                        LU ENTRY INDEX         @Y17XAYP
&E       SETA  0                        LUNO ENTRY SELECTOR    @Y17XAYP
&K       SETA  0                        LUNO COUNTER           @Y17XAYP
&G       SETA  0                        LU ENTRY COUNTER       @Y17XAYP
&H       SETA  0                        TERMNO VALUE           @Y17XAYP
.SCRLOOP ANOP                           LOOP ONCE/LINE IN LINE GROUP
&H       SETA  &TRMNO(&SCRPT+&D+&E)     GET TERMNO VALUE       @Y17XAYP
.MOVSCR  ANOP                                                  @Y17XAYP
.*                                                               Y06327
&SCRH(&ADRPT+&C) SETA &A                SET SCREEN HEIGHT
&SCRW(&ADRPT+&C) SETA &B                SET SCREEN WIDTH
         AIF   ('&FULLSCR' EQ 'YES' AND &SW3270 EQ 1).SCRF5      Y06327
         AIF   ('&FULLSCR' EQ 'YES' AND '&TERM' EQ 'PUNT').SCRF5
.*                                                             @Y17XAYP
         AGO   .SCRF6                                            Y06327
.SCRF5   ANOP                                                    Y06327
&SCRALL(&ADRPT+&C) SETB 1               SET FULL SCREEN FLAG     Y06327
.SCRF6   ANOP                                                    Y06327
.*       LU/LU SESSION ACTIVE                                  @Y17XAYP
         AIF   ('&TERM' NE 'PUNT').ADDR GO CHK ADDR            @Y17XAYP
&LUSB(&ADRPT+&C) SETB 1                 ACTIVATE LU/LU SESSION @Y17XAYP
         AIF   ('&TERM' EQ 'PUNT' AND '&USS' NE '3270').ADDR   @Y17XAYP
&USSB(&ADRPT+&C) SETB 1                 INDICATE USS=3270      @Y17XAYP
.ADDR    AIF   ('&GROUP' EQ '').ADDR0   BYPASS IF NOT 3705     @Y17XAYP
         AIF   ('&ADDR' EQ '').ADDR0    BYPASS IF NOT SPECIFIED  Y06327
         MNOTE 4,'IKJ60002I ADDR OPERAND NOT USED FOR 3705. IGNORED'
         AGO   .ALLDONE
.ADDR0   AIF   ('&ADDR' EQ '').ALLDONE                         @Y17XAYP
         AIF   (N'&ADDR GT 1).ADER3
         AIF   ('&TERM' NE '1050' AND '&TERM' NE '5041').ADDR226
         AIF   (K'&ADDR NE 2).ADER2
         AGO   .PUTADDR
.ADDR226 AIF   ('&TERM' EQ '226R' OR '&TERM' EQ '2265').ADDR1  @Y17XAYP
         AIF   ('&TERM' EQ '327R' OR '&TERM' EQ '327S').ADDR1
         AGO   .ADER1
.ADDR1   AIF   (K'&ADDR NE 4).ADER2                            @Y17XAYP
.*
.*       PUT ADDR CHARS INTO ARRAY
.*
.PUTADDR ANOP
&ADCHAR(&ADRPT+&C) SETC '&ADDR'    PUT PARAMETER VALUE IN ARRAY
         AGO   .ALLDONE                                        @Y17XAYP
.*
.*       ADDR PARAMETER ERROR MESSAGES
.*
.ADER1   MNOTE 4,'IKJ54114I ADDR OPERAND NOT ALLOWED FOR THIS TERMINAL -
                TYPE'
         AGO   .ALLDONE
.ADER2   MNOTE 4,'IKJ54118I ADDR OPERAND INCORRECT LENGTH -- IGNORED'
         AGO   .ALLDONE
.ADER3   MNOTE 8,'IKJ54104I &ADDR INVALID FOR ADDR OPERAND'
         AGO   .ALLDONE
.ALLDONE ANOP
&G       SETA  &G+1                     COUNT LU ENTRY         @Y17XAYP
&C       SETA  &C+1                     INCREMENT LU ENTRY INDX@Y17XAYP
         AIF   ('&GROUP' EQ '').NOTLU   IS THIS NCP TERM       @Y17XAYP
.NOTLU   AIF   (&G LT &TRMNO(&SCRPT+&E+&D+1)).MOVSCR           @Y17XAYP
.*                                      MORE LU ENTRIES LUNO   @Y17XAYP
&G       SETA  0                        CLEAR LU ENTRY COUNTER @Y17XAYP
&E       SETA  &E+1                     INCREMENT LUNO SELECTOR@Y17XAYP
&K       SETA  &K+1                     INCREMENT LUNO COUNTER @Y17XAYP
         AIF   (&K LT &H).MOVSCR        MORE LUNO ENTRY/TERMNO @Y17XAYP
&D       SETA  &D+1                     INCREMENT TERMNO SELECTOR
&K       SETA  0                        RESET LUNO COUNTER     @Y17XAYP
         AIF   (&D LT &LNO(&NLG)).SCRLOOP LOOP IF MORE LINES     Y06327
.*
         MEND
