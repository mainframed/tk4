         MACRO
&NAME    IEDBUFTR &ID=USER,&TRACESW=
.*000000-999999                                                @OY19405
.* C020000                                                     @OZ43081
.*C020000,022500,025000                                        @OZ43668
         GBLA  &IEDQZE(64),&IEDQZL(256)
         GBLB  &IEDQZA(64)
         LCLC  &KEY,&UID
         LCLA  &LEN,&L
         AIF   (NOT &IEDQZA(5)).MHNORM
.MHERROR ANOP
         MNOTE 12,'*** IHB070 SEQUENCE ERROR-MUST FOLLOW INHEADER, INBUX
               F, OUTHEADER, OR OUTBUF MACROS ONLY'
         AGO   .MEND
.MHNORM  ANOP
&LEN     SETA  10
         AIF   (K'&ID GT 4).ERR
         AIF   ('&ID' EQ '').SETID
&UID     SETC  '&ID'
         AGO   .CKSW
.ERR     ANOP
&KEY     SETC  'ID'
.MSG     ANOP
         MNOTE 12,'IHB304 &KEY SPECIFIED TOO MANY CHARACTERS'
         AGO   .MEND
.SETID   ANOP
&UID     SETC  'USER'
.CKSW    ANOP
&L       SETA  1
         AIF   ('&TRACESW' EQ '').DF    NO SW SPECIFIED, ON ALWAYS
         AIF   (K'&TRACESW GT 8).ERR2
&L       SETA  &L+1
         AGO   .DF
.ERR2    ANOP
&KEY     SETC  'TRACESW'
         AGO   .MSG
.DF      ANOP
         AIF   (&L NE 2).CONT
&LEN     SETA  &LEN+4
.CONT    ANOP
         IEDQVCON 70,IEDQBUFF
&NAME    BAL   1,*+&LEN+4               POINT R1 AT PARM LIST  @OZ43668
         DC    AL1(&IEDQZE(48)),AL1(&LEN) IEDQNWMH VCON AND PARM LENGTH
         DC    AL1(&IEDQZL(6))          IEDQBUFF VCON
         DC    AL1(0)                   ALIGNMENT FILLER
         DC    CL4'&UID'                USER ID
         DC    AL1(&L,0)                TRACESW INDICATOR      @OZ43668
         AIF   (&L NE 2).SKIP
         IEDQVCON 31,IEDQAE
         DC    AL1(&IEDQZE(31)),AL1(4),AL1(&TRACESW),AL1(08) AE PARM
.SKIP    ANOP
         L     15,IEDUI                 LOAD BASE FOR UI       @OZ43668
         BALR  14,15                    GO TO UI
.MEND    ANOP
         MEND
