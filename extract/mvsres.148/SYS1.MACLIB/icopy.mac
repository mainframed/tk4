         MACRO
&NAME    ICOPY &DDN,&RLN,&WORK,&TINDEX=NO                      @OZ09941
.*D030000                                                       SA51790
.*C090000-140000,470000-480000                                  SA51790
.*C020000,410000                                               @OZ09941
.*A040000,460000,680000                                        @OZ09941
         AIF   (N'&SYSLIST NE 3).ERR0
         AIF   ('&TINDEX' NE 'NO' AND '&TINDEX' NE 'YES').ERR6 @OZ09941
         AIF   ('&DDN' EQ '' OR '&RLN' EQ '' OR '&WORK' EQ '').ERR0
         AIF   ('&DDN' EQ '(0)').ERR1
         AIF   ('&WORK' EQ '(0)' OR '&WORK' EQ '(1)').ERR2
&NAME    DS    0H
.*             VERSION DATED JUNE 1,1976                       @OZ09941
         AIF   ('&RLN' EQ '(0)').DDNAME
         AIF   ('&RLN'(1,1) NE '(').LA
         LR    0,&RLN(1) .              RELATIVE LINE NUMBER
         AGO   .DDNAME
.LA      ANOP
         AIF   ('&RLN' LE '0' OR '&RLN' GE '256').ERR4
         LA    0,&RLN .                 RELATIVE LINE NUMBER
.DDNAME  ANOP
         AIF   ('&DDN' EQ '(1)').SHIFT
         AIF   ('&DDN'(1,1) NE '(').LA1
         LR    1,&DDN(1) .              ADDRESS OF DDNAME
         AGO   .SHIFT
.LA1     ANOP
         AIF   (K'&DDN GT 8).ERR3
         AIF   (('&DDN'(1,1) GE 'A' AND '&DDN'(1,1) LE 'Z') OR         X
               '&DDN'(1,1) EQ '$' OR '&DDN'(1,1) EQ '@' OR             X
               '&DDN'(1,1) EQ '#').DDOK
         AGO   .ERR3
.DDOK    ANOP
         BAL   1,*+12 .                 ADDRESS OF DDNAME
         DC    CL8'&DDN' .              DDNAME
.SHIFT   ANOP
         SLL   1,8 .                    POSITION ADDR OF DDNAME
         SLDL  0,24 .                   POSITION RLN AND ADDR OF
*                                       DDNAME IN REGISTER 0
         AIF   ('&WORK'(1,1) NE '(').LA2
         LA    1,0(,&WORK(1))           ADDR OF WORKAREA TO R1 @OZ09941
         AGO   .CALL
.LA2     ANOP
         AIF   (K'&WORK GT 8).ERR5
         LA    1,&WORK .                ADDRESS OF WORKAREA TO R1
.CALL    ANOP
         AIF   ('&TINDEX' EQ 'NO').CALL1                       @OZ09941
         CNOP  0,4                                             @OZ09941
         B     *+8                                             @OZ09941
         DC    X'80000000'                                     @OZ09941
         O     1,*-4                    SET EXTENDED ICOPY BIT @OZ09941
.CALL1   ANOP                                                  @OZ09941
         LINK  EP=IEDQE4                                        SA51790
         MEXIT
.ERR0    ANOP
         MNOTE 12,'*** IHB004 REQUIRED OPERAND(S) NOT SPECIFIED'
         MEXIT
.ERR1    ANOP
         MNOTE 12,'*** IHB078 DDNAME OPERAND REGISTER NOTATION         *
               INVALID . (0)'
         MEXIT
.ERR2    ANOP
         MNOTE 12,'*** IHB078 WORKAREA OPERAND REGISTER NOTATION       *
               INVALID - (0) OR (1)'
         MEXIT
.ERR3    ANOP
         MNOTE 12,'*** IHB002 INVALID DDNAME OPERAND SPECIFIED'
         MEXIT
.ERR4    ANOP
         MNOTE 12,'*** IHB002 INVALID RLN OPERAND SPECIFIED'
         MEXIT
.ERR5    ANOP
         MNOTE 12,'*** IHB002 INVALID WORKAREA OPERAND SPECIFIED'
         MEXIT                                                 @OZ09941
.ERR6    ANOP                                                  @OZ09941
         MNOTE 12,'*** IHB002 INVALID TINDEX OPERAND SPECIFIED'
         MEND
