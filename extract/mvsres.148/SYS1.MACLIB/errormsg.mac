         MACRO
&NAME    ERRORMSG &MASK,&DEST=,&DATA=,&EXIT=,&CONNECT=OR,              X
               &HEADER=YES,&TASK=                              @Y17XANQ
.* &TASKNME - TASK NAME                                        @Y17XAMN
.* &TASKNUM - NUMBER OF CHARACTERS IN TASK CHAR STRING         @Y17XAMN
.* &IEDQZA(32) - TOTE MH INDICATOR                             @Y17XAMN
         GBLA  &IEDQZD,&IEDQZE(64),&IEDQZK
         GBLB  &IEDQZA(64)
         LCLA  &LEN,&BIT,&D,&ALG,&U,&TASKNUM                   @Y17XANQ
         LCLB  &NOGO
         LCLC  &C,&TASKNME
.*C552000                                                       SA58454
.*A440000,480000                                                SA59170
.*C472000                                                       SA59170
.*A016000,040000,160000,256000,328000,392000,432000            @Y17XAMN
.*A432000,486000,544000,592000,664000,832000,912000            @Y17XAMN
.*C264000,352000,656000,840000,864000,920000                   @Y17XAMN
.*A256000,261110                                               @Y17XANQ
.*C016800,040000,164000,260800,261100,336000,376000,597000     @Y17XANQ
.*C840000,920000                                               @Y17XANQ
.*D068000,256300-257500,258100-260500,594000                   @Y17XANQ
.*C704000                                                      @OX19417
.* DUMMY APAR                                                  @OZ27328
.*C096000                                                      @OY19729
         SPACE
&C       SETC  '&SYSECT'
&D       SETA  0
         AIF   ('&HEADER' EQ 'NO').TSOCHK                      @OY19729
         AIF   (('&HEADER' EQ 'YES') AND (NOT &IEDQZA(9))).TSOCHK
         MNOTE 12,'***  IHB312  MUST FOLLOW STARTMH MACRO WITH TSOMH=NO*
               '
         AGO   .MEND                                                TSO
.TSOCHK  ANOP                                                       TSO
         AIF   (&IEDQZA(5)).CHECK
         MNOTE 12,'***  IHB070  SEQUENCE ERROR-MUST FOLLOW INMSG OR OUT*
               MSG MACRO'
&NOGO    SETB  1
.CHECK   AIF   ('&DATA' NE '').MASKCHK                         @Y17XANQ
         MNOTE 12,'***  IHB004  REQUIRED OPERAND(S) NOT SPECIFIED'
&NOGO    SETB  1
.MASKCHK AIF   ('&MASK' EQ '').NXT
         IEDQMASK &MASK,5
         AIF   (&IEDQZA(24)).ERR
         AIF   ('&MASK'(1,1) NE 'X').NXT
         AIF   ('&MASK'(2,1) EQ 'L' AND '&MASK'(3,1) NE '5').ERR
         AIF   ('&MASK'(2,1) NE 'L' AND K'&MASK NE 13).ERR
         AGO   .NXT
.ERR     MNOTE 12,'***  IHB300  MASK OPERAND INVALID AS SPECIFIED'
&NOGO    SETB  1
.NXT     AIF   ('&TASK' EQ '' OR '&DEST' EQ '').ERRNXT         @Y17XANQ
         MNOTE 4,'*** IED600 TASK AND DEST OPERANDS ARE MUTUALLY EXCLUS*
               IVE, DEST OPERAND IGNORED'                      @Y17XANQ
.ERRNXT  AIF   ('&TASK' EQ '').ERRNT                           @Y17XAMN
         AIF   ('&TASK' NE 'TOTE').ERRNTOT                     @Y17XANQ
         AIF   (&IEDQZA(32)).TTOTE                             @Y17XANQ
         MNOTE 12,'*** IED601 TASK OPERAND DISALLOWED FOR USER MH '
&NOGO    SETB  1                                               @Y17XANQ
         AGO   .DATACHK                                        @Y17XANQ
.TTOTE   ANOP                                                  @Y17XANQ
&TASKNME SETC  'TOTE'                                          @Y17XAMN
&TASKNUM SETA  4                                               @Y17XAMN
         AGO   .DATACHK                                        @Y17XAMN
.ERRNTOT ANOP                                                  @Y17XAMN
         MNOTE 12,'*** IED602 INVALID TASK OPERAND'            @Y17XAMN
&NOGO    SETB  1                                               @Y17XAMN
         AGO   .DATACHK                                        @Y17XAMN
.ERRNT   AIF   ('&DEST' EQ '').DATACHK                         @Y17XAMN
         AIF   ('&DEST' EQ 'ORIGIN' OR '&DEST' EQ 'DESTIN').DATACHK
         IEDQCHAR 8,&DEST
&D       SETA  &IEDQZD
         AIF   (&IEDQZA(24)).DESTERR
         AIF   (&IEDQZD LE 8).DATACHK
.DESTERR MNOTE 12,'***  IHB300  DESTINATION OPERAND INVALID AS SPECIFIE*
               D'
&NOGO    SETB  1
.DATACHK AIF   ('&DATA'(K'&DATA,1) EQ ')').DATAERR             @Y17XANQ
         AIF   ('&DATA'(K'&DATA,1) NE '''' AND K'&DATA GT 8).DATAERR
         AIF   ('&DATA'(K'&DATA,1) NE '''').ERRHDR
         IEDQCHAR 255,&DATA
         AIF   (&IEDQZA(24)).DATAERR
         AIF   (&IEDQZD LE 255).ERRHDR                         @Y17XANQ
.DATAERR MNOTE 12,'***  IHB300  DATA OPERAND INVALID AS SPECIFIED'
&NOGO    SETB  1
.ERRHDR  ANOP                                                  @Y17XAMN
&BIT     SETA  &BIT+2                                          @Y17XAMN
         AIF   ('&HEADER' EQ 'YES').ERROP                      @Y17XAMN
         AIF   ('&HEADER' EQ 'NO').ERRHL                       @Y17XAMN
         MNOTE 4,'*** IED604 INVALED HEADER OPERAND,DEFAULT ASSUMED.'
         AGO   .ERROP                                          @Y17XAMN
.ERRHL   ANOP                                                  @Y17XAMN
&BIT     SETA  &BIT-2                                          @Y17XAMN
.ERROP   AIF   ('&EXIT' EQ '').RTNCHK1
         AIF   (K'&EXIT LE 8).RTNCHK
         MNOTE 12,'***  IHB300  EXIT OPERAND INVALID AS SPECIFIED'
&NOGO    SETB  1
         AGO   .RTNCHK1                                        @Y17XAMN
.RTNCHK  ANOP                                                  @Y17XAMN
&U       SETA  4                                               @Y17XAMN
.RTNCHK1 AIF   ('&CONNECT' EQ 'AND' OR '&CONNECT' EQ 'OR').OP
         AIF   ('&CONNECT' EQ 'NAND').OP                        SA59170
         MNOTE 12,'***  IHB300  CONNECT OPERAND INVALID AS SPECIFIED'
&NOGO    SETB  1
.OP      AIF   (&NOGO).MEND
         AIF   ('&CONNECT' NE 'AND').CKNAND                     SA59170
&ALG     SETA  1
         AGO   .ORG                                             SA59170
.CKNAND  AIF   ('&CONNECT' NE 'NAND').ORG                       SA59170
&ALG     SETA  2                                                SA59170
.ORG     AIF   ('&DEST' EQ '').SMV
         AIF   ('&DEST' EQ 'ORIGIN' OR '&DEST' EQ 'DESTIN' OR '&DEST'(K*
               '&DEST,1) EQ '''').SMV
         IEDQVCON (7,3,31,38),(IEDQAZ,IEDQAT,IEDQAE,IEDQAF)
         AGO   .TEST
.SMV     IEDQVCON (7,3,38),(IEDQAZ,IEDQAT,IEDQAF)
.TEST    AIF   (NOT &IEDQZA(1)).AT
&IEDQZA(18) SETB 1
.AT      AIF   ('&MASK' EQ '0' OR '&MASK' EQ 'XL5''0''').SET    SA58454
         AIF   ('&MASK' EQ 'X''0000000000''').SET               SA58454
         AIF   ('&MASK' EQ 'XL5''0000000000''').SET             SA58454
         AIF   ('&MASK' EQ 'XL5''00''' OR '&MASK' EQ '').SET    SA58454
         AGO   .SKP                                             SA58454
.SET     ANOP                                                   SA58454
&BIT     SETA  &BIT+1
.SKP     ANOP
         AIF   (&BIT NE 1 AND &BIT NE 3).M
&NAME    DC    AL1(&IEDQZE(7)+&BIT),AL1(16+&ALG+&U),AL1(1),AL1(0)
         AGO   .ERRTKG                                         @Y17XAMN
.M       ANOP                                                  @Y17XAMN
&NAME    DC    AL1(&IEDQZE(7)+&BIT),AL1(20+&ALG+&U),AL1(1)     @Y17XANQ
&LEN     SETA  0
         AIF   ('&MASK'(1,1) EQ 'X').XM
         DC    FL5'&MASK' .             ERROR MASK
         AGO   .ERRTKG                                         @Y17XAMN
.XM      DC    &MASK .                  ERROR MASK
.ERRTKG  AIF   ('&TASK' EQ '').DEST                            @Y17XAMN
         DC    C'T',AL3(IEDQMISC+&IEDQZK)                      @Y17XAMN
IEDQMISC CSECT                                                 @Y17XAMN
         DC    AL1(&TASKNUM)                                   @Y17XAMN
         DC    C'&TASKNME'                                     @Y17XAMN
&C       CSECT                                                 @Y17XAMN
&IEDQZK  SETA  &IEDQZK+&TASKNUM+1                              @Y17XAMN
         AGO   .DATA                                           @Y17XAMN
.DEST    AIF   ('&DEST' EQ '').ORGDST
         AIF   ('&DEST' EQ 'ORIGIN').S
         AIF   ('&DEST' EQ 'DESTIN').D
         AIF   ('&DEST'(K'&DEST,1) EQ '''').DST
         DC    C'O',AL1(&IEDQZE(31)),AL1(&DEST),AL1(15)        @OX19417
         AGO   .DATA
.DST     ANOP
&LEN     SETA  1
IEDQMISC CSECT
         DC    AL1(&D),&DEST .          DESTINATION
         AIF   ('&DATA'(K'&DATA,1) NE '''').OUT
         DC    &DATA .            ERROR MESSAGE
.OUT     ANOP
&C       CSECT
         DC    C'N',AL3(IEDQMISC+&IEDQZK)
&IEDQZK  SETA  &IEDQZK+1+&D
         AGO   .DATA
.ORGDST  AIF   (&IEDQZA(2)).D
.S       DC    C'S',AL3(0) .            SEND TO SOURCE
         AGO   .DATA
.D       DC    C'D',AL3(0) .            SEND TO DESTINATION
.DATA    DC    AL1(&IEDQZE(3)),AL1(8+&U),AL1(&IEDQZE(38)),AL1(0)
         AIF   ('&DATA'(K'&DATA,1) EQ '''').CD
         DC    AL1(0),AL3(&DATA) .      ERROR MESSAGE
         AGO   .ERRNTK                                         @Y17XAMN
.CD      AIF   (&LEN EQ 1).DCV
IEDQMISC CSECT
         DC    &DATA .                  ERROR MESSAGE
&C       CSECT
.DCV     DC    AL1(&IEDQZD),AL3(IEDQMISC+&IEDQZK)
&IEDQZK  SETA  &IEDQZK+&IEDQZD
.ERRNTK  AIF   ('&EXIT' EQ '').MEND                            @Y17XANQ
         DC    AL4(&EXIT) .             ADDRESS OF EXIT ROUTINE
.MEND    SPACE 2
         MEND
