         MACRO
&LABEL   IKJTERM &TYPNAM,&OPTION,&OPTION2,&OPTION3,&OPTION4,&PROMPT=,  *
               &RSVWD=,&DEFAULT=,&TYPE=ANY,&SBSCRPT=,&VALIDCK=,&HELP=
.********************************************************************
.*
.* TITLE  -  IKJTERM MACRO (SYMBOLIC DEBUG PROJECT)
.*
.* FUNCTION  -  THE IKJTERM MACRO IS USED BY COMMAND PROCESSORS TO
.*              DEFINE TO PARSE THE EXPECTED FORMAT OF A TERM-TYPE
.*              PARAMETER.
.*
.* INPUT  -  THE PROTOTYPE STATEMENT IS AS DEFINED ABOVE. IT'S
.*           PARAMETERS REPRESENT THE FOLLOWING:
.*               &LABEL   - STATEMENT LABEL IS REQUIRED
.*               &TYPNAM  - A PARAMETER TYPE IS REQUIRED
.*               &OPTION,&OPTION2,OPTION3, AND &OPTION4  -
.*                          FOUR POSSIBLE POSITIONAL PARAMETERS,
.*                          LIST,RANGE,SBSCRPT, AND ASIS/UPPERCASE.
.*               &RSVWD   - SPECIFIES THE PCE-LABEL WHICH WILL
.*                          DESCRIBE POSSIBLE LITERAL CONSTANTS.
.*               &TYPE    - INDICATES THE TYPE OF TERM TO BE
.*                          SCANNED FOR - VARIABLE,CONSTANT,
.*                          STATEMENT NUMBER, OR ANY(VARIABLE OR
.*                          CONSTANT). DEFAULT IS ANY.
.*               &SBSCRPT - SPECIFIES THE PCE-LABEL WHICH WILL
.*                          DESCRIBE THE POSSIBLE SUBSCRIPT ON
.*                          THIS TERM.
.*               &HELP    - SUPPLIES SECOND-LEVEL MESSAGES.
.*               &VALIDCK - SPECIFIES THE ADDRESS OF A USER-SUPPLIED
.*                          VALIDITY CHECK EXIT ROUTINE.
.*               &PROMPT  - SUPPLIES DATA TO BE USED IF PROMPTING FOR
.*                          A MISSING PARAMETER.
.*               &DEFAULT - SUPPLIES VALUE TO BE USED IF THE PARAMETER
.*                          IS NOT ENTERED.
.*
.* OUTPUT  -  THIS MACRO PRODUCES A PARAMETER CONTROL ENTRY (PCE) TO BE
.*            PASSED TO THE PARSE SERVICE ROUTINE.
.*            IF AN ERROR IS ENCOUNTERED IN PROCESSING THE MACRO
.*            INSTRUCTION ENTERED BY THE CP, AN APPROPRIATE ERROR
.*            MESSAGE IS PRINTED AND THE PCE IS NOT GENERATED.
.*
.********************************************************************
.*
.* THE FOLLOWING CODE DEFINES VARIABLES TO BE USED IN THE MACRO
.*     EXPANSION.
.*        GLOBALS   -   IKJCSNM - CSECT NAME FROM IKJPARM MCARO LABEL
.*                      IKJDSNM - DSECT NAME FOR PDL
.*        LOCALS(ARITHMETIC)  -  T - USED AS COUNTER FOR HELP MSG.
.*                               END - SECOND COUNTER FOR HELP MSG.
.*                               PDESIZE - AMOUNT OF SPACE FOR PDE
.*              (BINARY)      -  FLAGS FOR VARIOUS OPTIONS AND TO
.*                               INDICATE THAT NO PCE IS TO BE
.*                               GENERATED.
.*              (CHARACTER)   -  QUOTE - USED WHEN QUOTES ARE MISSING
.*                               FROM &TYPNAM
.*
         GBLC  &IKJCSNM,&IKJDSNM
         LCLA  &T,&PDESIZE,&END
         LCLB  &FLIST,&FRANGE,&FASIS,&FPRPT,&FDFLT,&FHELP,&FVALID
         LCLB  &FENDBIT,&FSUBSC,&FRSVWD,&FSTMT,&FVAR,&FCNST,&FANY
         LCLB  &SUBTRM
         LCLC  &QUOTE
&FENDBIT SETB  0
.*
.* DETERMINE IF EXTRANEOUS POSITIONAL PARAMETERS HAVE BEEN SPECIFIED.
.*
         AIF   (N'&SYSLIST LT 6).GOOD
         MNOTE 8,'NO MORE THAN 5 POSITIONAL PARAMETERS ALLOWED'
&FENDBIT SETB  1
.*
.* DETERMINE IF TYPE NAME HAS BEEN ENTERED.
.*
.GOOD    AIF   ('&TYPNAM' EQ 'LIST' OR '&TYPNAM' EQ 'RANGE' OR         *
               '&TYPNAM' EQ 'ASIS' OR '&TYPNAM' EQ 'UPPERCASE' OR      *
               '&TYPNAM' EQ '').BAD
         AGO   .GOOD1
.BAD     MNOTE 8,'PARAMETER TYPE MISSING BUT REQUIRED'
&FENDBIT SETB  1
.*
.* DETERMINE IF CSECT NAME IS MISSING.
.*
.GOOD1   AIF   ('&IKJCSNM' NE '').GOOD2
         MNOTE 8,'CSECT NAME (FROM IKJPARM MACRO LABEL) IS MISSING'
&FENDBIT SETB  1
.*
.* DETERMINE IF STATEMENT LABEL WAS ENTERED.
.*
.GOOD2   AIF   ('&LABEL' NE '').GOOD3
         MNOTE 8,'STATEMENT LABEL MISSING BUT REQUIRED'
&FENDBIT SETB  1
.GOOD3   ANOP
.*
.* THE FOLLOWING CODE SETS FLAGS INDICATING THE OPTIONS SPECIFIED
.*   ON POSITIONAL PARAMETERS.
.*
&FLIST   SETB  ('&OPTION' EQ 'LIST' OR '&OPTION2' EQ 'LIST' OR         *
               '&OPTION3' EQ 'LIST' OR '&OPTION4' EQ 'LIST')
&FRANGE  SETB  ('&OPTION' EQ 'RANGE' OR '&OPTION2' EQ 'RANGE' OR       *
               '&OPTION3' EQ 'RANGE' OR '&OPTION4' EQ 'RANGE')
&FASIS   SETB  ('&OPTION' EQ 'ASIS' OR '&OPTION2' EQ 'ASIS' OR         *
               '&OPTION3' EQ 'ASIS' OR '&OPTION4' EQ 'ASIS')
&SUBTRM   SETB  ('&OPTION' EQ 'SBSCRPT' OR '&OPTION2' EQ 'SBSCRPT' OR  *
               '&OPTION3' EQ 'SBSCRPT' OR '&OPTION4' EQ 'SBSCRPT')
.*
.* THE FOLLOWING CODE DETERMINES IF INCORRECT POSITIONAL PARAMETERS
.*   HAVE BEEN ENTERED.
.*
         AIF   ('&OPTION' EQ 'LIST' OR '&OPTION' EQ 'RANGE' OR         *
               '&OPTION' EQ 'ASIS' OR '&OPTION' EQ 'UPPERCASE' OR      *
               '&OPTION' EQ '' OR '&OPTION' EQ 'SBSCRPT').OPT1
         MNOTE 8,'''&OPTION'' IS AN INVALID POSITIONAL PARAMETER'
&FENDBIT SETB  1
.OPT1    AIF   ('&OPTION2' EQ 'LIST' OR '&OPTION2' EQ 'RANGE' OR       *
               '&OPTION2' EQ 'ASIS' OR '&OPTION2' EQ 'UPPERCASE' OR    *
               '&OPTION2' EQ '' OR '&OPTION2' EQ 'SBSCRPT').OPT2
         MNOTE 8,'''&OPTION2'' IS AN INVALID POSITIONAL PARAMETER'
&FENDBIT SETB  1
.OPT2    AIF   ('&OPTION3' EQ 'LIST' OR '&OPTION3' EQ 'RANGE' OR       *
               '&OPTION3' EQ 'ASIS' OR '&OPTION3' EQ 'UPPERCASE' OR    *
               '&OPTION3' EQ '' OR '&OPTION3' EQ 'SBSCRPT').OPT3
         MNOTE 8,'''&OPTION3'' IS AN INVALID POSITIONAL PARAMETER'
&FENDBIT SETB  1
.OPT3    AIF   ('&OPTION4' EQ 'LIST' OR '&OPTION4' EQ 'RANGE' OR       *
               '&OPTION4' EQ 'ASIS' OR '&OPTION4' EQ 'UPPERCASE' OR    *
               '&OPTION4' EQ '' OR '&OPTION4' EQ 'SBSCRPT').OPT4
         MNOTE 8,'''&OPTION4'' IS AN INVALID POSITIONAL PARAMETER'
&FENDBIT SETB  1
.*
.* UPPERCASE AND ASIS CANNOT BOTH BE SPECIFIED.
.*
.OPT4    AIF   (('&OPTION' EQ 'UPPERCASE' OR '&OPTION2' EQ             *
               'UPPERCASE' OR '&OPTION3' EQ 'UPPERCASE') AND           *
               &FASIS).ERROR3
         AGO   .A1
.ERROR3  MNOTE 8,'BOTH ''UPPERCASE'' AND ''ASIS'' CANNOT BE SPECIFIED'
&FENDBIT SETB  1
.A1      ANOP
.*
.* THE FOLLOWING CODE SETS FLAGS INDICATING KEYWORD OPTIONS.
.*
&FPRPT   SETB  ('&PROMPT' NE '')
&FDFLT   SETB  ('&DEFAULT' NE '')
&FHELP   SETB  ('&HELP' NE '')
&FVALID  SETB  ('&VALIDCK' NE '')
&FSUBSC  SETB  ('&SBSCRPT' NE '')
&FRSVWD  SETB  ('&RSVWD' NE '')
&FSTMT   SETB  ('&TYPE' EQ 'STMT')
&FVAR    SETB  ('&TYPE' EQ 'VAR')
&FCNST   SETB  ('&TYPE' EQ 'CNST')
&FANY    SETB  ('&TYPE' EQ 'ANY')
.*
         AIF  (&FRANGE AND &SUBTRM).BAD3
         AGO   .GOODI4
.BAD3    ANOP
         MNOTE 4,'RANGE OPTION ILLEGAL WITH "SBSCRPT". IGNORED'
&FRANGE  SETB  0
.GOODI4  AIF   (&FLIST AND &SUBTRM).BAD4
         AGO   .GOODI5
.BAD4    ANOP
         MNOTE 4,'LIST OPTION ILLEGAL WITH "SBSCRPT". IGNORED.'
&FLIST   SETB  0
.GOODI5  AIF (NOT(&SUBTRM AND &FSUBSC)).GOODI6
         MNOTE 8,'"SBSCRPT" OPTION ILLEGAL WITH "SBSCRPT=LABEL" OPTION'
&FENDBIT SETB  1
.GOODI6  ANOP
.*
.* THE ONLY VALID 'TYPE' PARAMETERS ARE VAR,STMT,CNST, AND ANY
.*
         AIF   ('&TYPE' NE 'STMT' AND '&TYPE' NE 'VAR' AND '&TYPE' NE  *
               'CNST' AND '&TYPE' NE 'ANY').ERROR
         AGO   .GOOD9
.ERROR   MNOTE 8,'INVALID ''TYPE'' PARAMETER'
&FENDBIT SETB  1
.*
.* PROMPT AND DEFAULT CANNOT BOTH BE SPECIFIED.
.*
.GOOD9   AIF   (NOT(&FDFLT AND &FPRPT)).GOOD4
         MNOTE 8,'PARAMETER CANNOT BOTH DEFAULT AND REQUIRE PROMPTING'
&FENDBIT SETB  1
.*
.* RSVWD IS IGNORED IF TYPE=VAR OR TYPE=STMT.
.*
.GOOD4   AIF   (('&TYPE' EQ 'VAR' OR '&TYPE' EQ 'STMT') AND            *
               &FRSVWD).ERROR1
         AGO   .GOOD5
.ERROR1  MNOTE 4,'''RSVWD'' IGNORED WITH ''&TYPE'''
&FRSVWD  SETB  0
.*
.* SBSCRPT IS IGNORED IF TYPE=STMT OR TYPE=CNST.
.*
.GOOD5   AIF   (('&TYPE' EQ 'STMT' OR '&TYPE' EQ 'CNST') AND           *
               &FSUBSC).ERROR2
         AGO   .GOOD6
.ERROR2  MNOTE 4,'''SBSCRPT'' IGNORED WITH ''&TYPE'''
&FSUBSC  SETB  0
.*
.* IF AN ERROR HAS BEEN ENCOUNTERED DO NOT GENERATE PCE.
.*
.GOOD6   AIF   (NOT &FENDBIT).GEN
         MEXIT
.GEN     ANOP
&IKJCSNM CSECT ,
* FLAGS - BIT3=PROMPT,    BIT4=DEFAULT, BIT6=HELP,
*         BIT7=VALIDCK,   BIT8=LIST,    BIT9=ASIS,    BIT10=RANGE,
*         BIT11=SBSCRPT,  BIT12=RSVWD
DENT&SYSNDX EQU *
&LABEL   EQU   *
         DC    BL2'110&FPRPT&FDFLT.0&FHELP&FVALID&FLIST&FASIS&FRANGE&FS*
               UBSC&FRSVWD.000'      FLAGS
         DC    AL2(DEND&SYSNDX-DENT&SYSNDX)      LENGTH OF PCE
         AIF   (&SUBTRM).GOODI7
         DC    AL2(LAB&SYSNDX-&IKJDSNM)          OFFSET TO PDE
         AGO   .GOODI8
.GOODI7  DC   AL2(0)        NO PDE OFFSET - PREVIOUSLY ALLOCATED
.GOODI8  ANOP
* TYPES - BIT0=STMT, BIT1=VAR, BIT2=CNST, BIT3=ANY,BIT4=SBSCRPT
         DC    BL1'&FSTMT&FVAR&FCNST&FANY&SUBTRM.000'   TYPE SPECIFIED
.*
.* THE FOLLOWING CODE SUPPLIES QUOTES TO &TYPNAM IF THEY ARE MISSING.
.*
&QUOTE   SETC  '&TYPNAM'(1,1)
         AIF   ('&QUOTE' EQ '''').QUOT
&QUOTE   SETC  ''''
         DC    AL2(IKJ@&SYSNDX-*),AL2(18),C&QUOTE&TYPNAM&QUOTE    PARAM*
               ETER TYPE MESSAGE SEGMENT
         AGO   .EQUGEN1
.QUOT    ANOP
         DC    AL2(IKJ@&SYSNDX-*),AL2(18),C&TYPNAM    PARAMETER TYPE ME*
               SSAGE SEGMENT
.EQUGEN1 ANOP
IKJ@&SYSNDX EQU *
         AIF   (&FPRPT).PRPT      PROMPT SPECIFIED
         AIF   (NOT &FDFLT).NPRDF
         DC    AL1(IKJ$&SYSNDX-*-2),C&DEFAULT        DEFAULT DATA
         AGO   .EQUGEN
.PRPT    DC     AL1(IKJ$&SYSNDX-*-2),C&PROMPT        PROMPT DATA
.EQUGEN  ANOP
IKJ$&SYSNDX EQU *
.NPRDF   ANOP
         AIF   (NOT &FSUBSC).NOSUBSC
         DC    AL2(&SBSCRPT-&IKJCSNM)    OFFSET TO SUBSCRIPT PCE
.NOSUBSC AIF   (NOT &FRSVWD).NORSWD
         DC    AL2(&RSVWD-&IKJCSNM)       OFFSET TO RESERVE WORD PCE
.NORSWD  ANOP
IKJA&SYSNDX EQU *
         AIF   (NOT &FHELP).NOHELP     IS HELP SPECIFIED
         DC    AL2(IKJB&SYSNDX-*)      LENGTH OF HELP DATA
&T       SETA  1
.HELPLP  ANOP
&T       SETA  &T+1
         AIF   (&T EQ 256).HELPER
         AIF   (&T LE N'&HELP).HELPLP
         AGO   .HELPCT
.HELPER  MNOTE 4,'ONLY 255 LEVELS OF HELP AVAILABLE. OTHERS IGNORED'
&T       SETA  &T+1
.HELPCT  ANOP
&T       SETA  &T-1
         DC    AL1(&T)             NUMBER OF HELP MESSAGES
&END     SETA  &T
&T       SETA  1
.HELPLPA ANOP
         DC    AL2(IKJ&SYSNDX.&T-*),AL2(00),C&HELP(&T) HELP MSG SEGMENT
IKJ&SYSNDX.&T EQU *
&T       SETA  &T+1
         AIF   (&T LE &END).HELPLPA
IKJB&SYSNDX EQU *
.NOHELP  AIF   (NOT &FVALID).NOVALID     IS VALIDCK SPECIFIED
         DC    AL3(&VALIDCK)       VALIDCK ROUTINE ADDRESS
.NOVALID ANOP
DEND&SYSNDX EQU *
         AIF (&SUBTRM).NOALLOC
&IKJDSNM DSECT
&PDESIZE SETA  41
         AIF   (&FLIST AND &FRANGE AND &FSUBSC).DSECT
&PDESIZE SETA  40
         AIF   (&FRANGE AND &FSUBSC).DSECT
&PDESIZE SETA  21
         AIF   (&FLIST AND &FSUBSC).DSECT
&PDESIZE SETA  20
         AIF   (&FSUBSC).DSECT
&PDESIZE SETA  11
         AIF   (&FLIST AND &FRANGE).DSECT  IS LIST AND RANGE SPECIFIED
&PDESIZE SETA  10
         AIF   (&FRANGE).DSECT
&PDESIZE SETA  6
         AIF   (&FLIST).DSECT
&PDESIZE SETA  5
.DSECT   ANOP
LAB&SYSNDX   DS    &PDESIZE.F      ALLOCATE PDE SPACE
.NOALLOC  ANOP
&SYSECT  CSECT ,               RESUME USERS CSECT
         MEXIT
         MEND
