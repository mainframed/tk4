         MACRO
&NAME    ORIGIN &LEN,&FORM=                                      S22026
         GBLA  &IEDQZE(64)
         GBLB  &IEDQZA(64)
         LCLA  &A                                                S22026
         LCLB  &NOGO
         LCLC  &C                                                S22026
.*D420000-434000                                                 S22026
.*C020000,060000,453000,540000                                   S22026
.*A045000,064000-076000,420000-448000                            S22026
.*             VERSION DATED OCTOBER 27,1971                     S22026
         SPACE
&IEDQZE(40) SETA 0
         AIF   (NOT &IEDQZA(9)).TSOCHK                              TSO
         MNOTE 12,'***  IHB312  MUST FOLLOW STARTMH MACRO WITH TSOMH=NO*
               '
&NOGO    SETB  1
.TSOCHK  AIF   (&IEDQZA(3)).CHECK
         MNOTE 12,'***  IHB070  SEQUENCE ERROR-MUST FOLLOW INHDR MACRO'
&NOGO    SETB  1
.CHECK   AIF   ('&LEN' EQ '').ORG
         AIF   ('&LEN' EQ 'X''FF''').ORG
         AIF   (K'&LEN EQ 1 AND '&LEN' GT '0' AND '&LEN' LE '8').ORG
         AIF   (K'&LEN EQ 1).ERR
         AIF   ('&LEN'(1,1) NE 'X' OR '&LEN'(K'&LEN,1) NE '''').ERR
         AIF   ('&LEN'(2,1) EQ 'L').XL
         AIF   ('&LEN'(2,1) NE '''' OR K'&LEN LT 4 OR K'&LEN GT 5).ERR
         AIF   ('&LEN'(K'&LEN-1,1) LE '0' OR '&LEN'(K'&LEN-1,1) GT '8')*
               .ERR
         AIF   (K'&LEN EQ 5 AND '&LEN'(3,1) NE '0').ERR
         AGO   .ORG
.XL      AIF   (K'&LEN LT 6 OR K'&LEN GT 7).ERR
         AIF   ('&LEN'(3,2) NE '1''').ERR
         AIF   ('&LEN'(K'&LEN-1,1) LE '0' OR '&LEN'(K'&LEN-1,1) GT '8')*
               .ERR
         AIF   (K'&LEN EQ 7 AND '&LEN'(5,1) NE '0').ERR
         AGO   .ORG
.ERR     MNOTE 12,'***  IHB300  LENGTH OPERAND INVALID AS SPECIFIED'
&NOGO    SETB  1                                                 S22026
.ORG     ANOP                                                    S22026
         AIF   (NOT &IEDQZA(13)).NOCONC GO SET C TO IEDQAM       S22026
&C       SETC  'IEDQBM'                 SET FOR IEDQVCON FOR BM  S22026
         AIF   ('&FORM' EQ '').CONTINU                           S22026
         AIF   ('&FORM' EQ 'NAME').SETNAM                        S22026
         AIF   ('&FORM' EQ 'ID').SETID                           S22026
         MNOTE 12,'*** IHB002 INVALID FORM OPERAND SPECIFIED- &FORM'
.*                                                               S22026
         AGO   .MEND                                             S22026
.SETNAM  ANOP                                                    S22026
&A       SETA  1                        SET FORM=NAME BIT        S22026
         AGO   .CONTINU                                          S22026
.SETID   ANOP                                                    S22026
&A       SETA  2                        SET FORM=ID BIT          S22026
         AGO   .CONTINU                                          S22026
.NOCONC  ANOP                                                    S22026
&C       SETC  'IEDQAM'                 SET FOR IEDQVCON FOR AM  S22026
         AIF   ('&FORM' EQ '').CONTINU                           S22026
         MNOTE 8,'*** IHB233 INCONSISTENT WITH FEATURE OPERAND OF THE IX
               NTRO MACRO' .                                     S22026
&NOGO    SETB  1 .                                               S22026
         AIF   ('&FORM' EQ 'NAME' OR '&FORM' EQ 'ID').CONTINU    S22026
         MNOTE 4,'*** IHB002 INVALID FORM OPERAND SPECIFIED- &FORM'
.*                                                               S22026
.CONTINU AIF   (&NOGO).MEND                                      S22026
         IEDQVCON 22,&C                                          S22026
         CNOP  2,4 .                                             S22025
&NAME    BAL   1,*+10 .                                          S22025
         DC    AL1(&IEDQZE(22)+&A),AL1(6) .                      S22026
         DC    AL1(&IEDQZE(30)),AL1(4),AL1(0)
         AIF   ('&LEN' EQ '').DEFAULT
         AIF   ('&LEN'(1,1) EQ 'X').HEX
         DC    AL1(&LEN) .              LENGTH OF ORIGIN FIELD
         AGO   .OUT
.DEFAULT DC    XL1'FF' .                VARIABLE LENGTH ORIGIN FLD
         AGO   .OUT
.HEX     DC    &LEN    .                                         S22026
.OUT     L     15,IEDUI .               TO ORIGIN ROUTINE        S22026
         BALR  14,15
.MEND    SPACE 2
         MEND
