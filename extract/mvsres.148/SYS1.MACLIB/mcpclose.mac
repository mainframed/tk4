         MACRO
&NAME    MCPCLOSE &CODE,&PASSWRD=
         AIF   ('&PASSWRD' EQ '').NOPASS
         AIF   (K'&PASSWRD LT 9).OK
         MNOTE 12,'*** IHB002 INVALID PASSWORD OPERAND SPECIFIED'
         MEXIT
.ERRMSG  ANOP
         MNOTE 12,'*** IHB002 INVALID KEYWORD OPERAND SPECIFIED'
         MEXIT
.NOPASS  ANOP
&NAME    DS    0H
         SR    0,0 .                    INDICATE NO PASSWORD
         AGO   .PARM
.OK      ANOP
&NAME    DS    0H
         BAL   0,*+12 .                 SET ADDR OF PASSWORD IN R0
         DC    CL8'&PASSWRD' .          UNSCRAMBLED PASSWORD
.PARM    ANOP
         CNOP  0,4
         BAL   1,*+44 .                 SET CIB ADDR IN PARM REG
IEDQ&SYSNDX DS 0F .                     COMMAND INPUT BUFFER
         DC    F'0' .                   OPERATOR CONTROL QCB ADDR
         DC    XL1'E0' .                CIB QUEUING PRIORITY
         DC    XL3'00' .                LINK FIELD
         AIF   ('&CODE' EQ '').DEFAULT
         AIF   ('&CODE' EQ 'FLUSH').DEFAULT
         AIF   ('&CODE' NE 'QUICK').ERRMSG
         DC    XL1'10' .                VERB CODE FOR QUICK CLOSE
         AGO   .NEXT
.DEFAULT ANOP
         DC    XL1'11' .                VERB CODE FOR FLUSH CLOSE
.NEXT    ANOP
         DC    XL1'1C' .                CIB LENGTH
         DC    XL1'00' .                RELATIVE LINE NUMBER
         DC    XL1'00' .                COMPLETION CODE
         DC    A(IEDQ&SYSNDX+32) .      ECB ADDRESS
         DC    XL8'00' .                TERMINAL NAME
         DC    XL4'00' .                WORKAREA ADDRESS
         DC    A(IEDQ&SYSNDX) .         ADDRESS OF CIB
         DC    F'0' .                   PCB WORKAREA/ECB
         DC    F'0' .                   ADDRESS OF CIB LENGTH
         LINK  EP=IEDQET
         MEND
