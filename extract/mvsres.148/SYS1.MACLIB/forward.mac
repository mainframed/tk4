         MACRO
&NAME    FORWARD &DEST=,&EOA=,&EXIT=,&THRESH=,&TASK=           @YM04688
.*A-000000-999999                                              @Z37X9M0
.*A903000,910000                                                SA56312
.*C126000                                                       SA57691
.*A112000,A133000                                              @YA11171
.*A014000,056000,059000,102000,102100                          @Y17XAMN
.*C014000,059000,099300                                        @Y17XAMN
.*D060000-061000,063000,066000                                 @Y17XAMN
.*C003180                                                      @OX19417
.* DUMMY APAR                                                  @OZ27328
.* A003440                                                     @OZ48773
         GBLA  &IEDQZD,&IEDQZE(64),&IEDQZK
         GBLB  &IEDQZA(64)
         GBLC  &IEDQZB
         LCLA  &BIT,&LEN,&STATUS,&AA,&CT,&D                    @YM06054
         LCLA  &E,&G .                                           S22027
         LCLB  &FLG,&HEX,&TH                                     S22025
         LCLC  &C,&A                                             S22025
.* &QCBNM IS ADDED AS A LCLC VARIABLE FOR TASK = 'TOTE'        @Y17XAMN
.* OR 'SSCP'                                                   @Y17XAMN
         LCLC  &REG,&QCBNM .                                   @Y17XAMN
         SPACE
&LEN     SETA  12
         AIF   (&IEDQZA(3)).TSOCHK
         MNOTE 12,'***  IHB070  SEQUENCE ERROR-MUST FOLLOW INHDR MACRO'
         AGO   .MEND
.TSOCHK  ANOP                                                       TSO
         AIF   (NOT &IEDQZA(9)).TRYTSK                         @Y17XAMN
         MNOTE 12,'***  IHB312  MUST FOLLOW STARTMH MACRO WITH TSOMH=NO*
               '
         AGO   .MEND                                                TSO
.* IS THE TASK OPERAND CODED? IF NO, CONTINUE WITH FORWARD     @Y17XAMN
.* LOGIC AT .TRYTHR                                            @Y17XAMN
.TRYTSK  ANOP                                                  @Y17XAMN
         AIF   ('&TASK' EQ '').TRYTHR                          @Y17XAMN
.* IS ANY OTHER OPERAND CODED? IF NOT, CONTINUE WITH TASK      @Y17XAMN
.* OPERAND FIELD INTERROGATION.                                @Y17XAMN
         AIF   ('&THRESH' NE '' OR '&DEST' NE '' OR '&EOA' NE '').ERR2
         AIF   ('&EXIT' EQ '').TSKCHK                          @YM04688
.ERR2    MNOTE 4,'*** IED700 UNNECESSARY OPERANDS ARE CODED , WILL BE IG
               GNORED'                                         @Y17XAMN
.* IS TASK = SSCP? IF NO, CHECK FOR TASK TOTE.                 @Y17XAMN
.TSKCHK  ANOP                                                  @Y17XAMN
&QCBNM   SETC  'SSCP'                                          @Y17XAMN
&IEDQZD  SETA  4                                               @Y17XAMN
         AIF   ('&TASK' NE 'SSCP').TK1                         @Y17XAMN
         AIF   (&IEDQZA(30)).TSKSSC                            @Y17XAMN
         MNOTE 12,'*** IED701 INVALID USE OF TASK OPERAND, NO SSCP MH.'
         AGO   .MEND                                           @Y17XAMN
.* IS TASK = TOTE? IF NO, CHECK FOR TASK = REG(NUMBER).        @Y17XAMN
.TK1     ANOP                                                  @Y17XAMN
&QCBNM   SETC  'TOTE'                                          @Y17XAMN
&IEDQZD  SETA  4                                               @Y17XAMN
         AIF   ('&TASK' NE 'TOTE').TK2                         @Y17XAMN
         AIF   (&IEDQZA(30) OR &IEDQZA(32)).TSKSSC             @Y17XAMN
         MNOTE 12,'*** IED703 INVALID USE OF TASK OPERAND, NO TOTE OR S*
               SCP MH.'                                        @Y17XAMN
         AGO   .MEND                                           @Y17XAMN
.* IS TASK = REG(NUMBER)? IF YES, CHECK FOR A SSCP OR TOTE MH. @Y17XAMN
.TK2     ANOP                                                  @Y17XAMN
         AIF   ('&TASK'(1,1) NE '(').ERR3                      @YM06054
         AIF   (&IEDQZA(30)).TSKMH                             @Y17XAMN
         MNOTE 12,'*** IED702 TASK=(NUMBER) MUST ONLY BE USED BY A SSCP*
                MH.'                                           @YM06054
         AGO   .MEND                                           @Y17XAMN
.* IS TASK = REG(NUMBER) SYNTACTICALLY CORRECT AND             @Y17XAMN
.* PROPERLY DEFINED.                                           @Y17XAMN
.TSKMH   ANOP                                                  @Y17XAMN
&E       SETA  K'&TASK                                         @Y17XAMN
         AIF   ('&TASK'(&E,1) NE ')').ERR4                     @Y17XAMN
&E       SETA  &E-2                                            @YM06054
&REG     SETC  '&TASK'(2,&E)                                   @YM06054
         CNOP  0,4                                             @YM06054
         ST    &REG,*+12                                       @YM06054
&STATUS  SETA  &STATUS+1                                       @YM06054
         AGO   .TSKGEN                                         @Y17XAMN
.ERR3    MNOTE 12,'***IED704 INVALID USE OF TASK OPERAND,UNDEFINED OPERA
               AND FIELD.'                                     @Y17XAMN
         AGO   .MEND                                           @Y17XAMN
.ERR4    MNOTE 12,'*** IED705 INVALID USE OF TASK OPERAND, REG USED INCO
               ORRECTLY.'                                      @Y17XAMN
         AGO   .MEND                                           @Y17XAMN
.TSKSSC  ANOP                                                  @Y17XAMN
.* CALL IEDQVCON AND BUILD THE FIRST 4 BYTES OF PARAMETERS     @Y17XAMN
.* LIST.                                                       @Y17XAMN
.TSKGEN  ANOP                                                  @Y17XAMN
&LEN     SETA  8                        PARAMETER LIST LENGTH  @YM06054
         IEDQVCON 42,IEDQA5                                    @Y17XAMN
         CNOP  0,4                                             @Y17XAMN
&NAME    BAL   1,*+&LEN+4                                      @YM06054
         DC    AL1(&IEDQZE(42)),AL1(&LEN)                      @Y17XAMN
         DC    AL1(0),AL1(&STATUS)                             @YM06054
         AIF   ('&REG' EQ '').TSKTAB                           @YM06054
         DS    A(0)                     ADDR OF USER'S TASK INFO
         AGO   .TSKEXT                                         @YM06054
.TSKTAB  ANOP                                                  @YM06054
.* UPDATE IEDQMISC DSECT,GENERATE LENGTH BYTE AND ADDRESS      @Y17XAMN
.* INTO PARAMETER LIST.                                        @Y17XAMN
&C       SETC  '&SYSECT'                                       @Y17XAMN
IEDQMISC CSECT                                                 @Y17XAMN
         DC    C'&QCBNM'                                       @Y17XAMN
&C       CSECT                                                 @Y17XAMN
         DC    AL1(&IEDQZD)                                    @Y17XAMN
         DC    AL3(IEDQMISC+&IEDQZK)                           @Y17XAMN
&IEDQZK  SETA  &IEDQZK+&IEDQZD                                 @Y17XAMN
.* BUILD LINKAGE TO UI.                                        @Y17XAMN
.TSKEXT  ANOP                                                  @Y17XAMN
         L     15,IEDUI                                        @Y17XAMN
         BALR  14,15                                           @Y17XAMN
         AGO   .MEND                                           @Y17XAMN
.TRYTHR  ANOP                                                  @Y17XAMN
         AIF   ('&THRESH' EQ '').TRY                             S22025
         AIF   ('&THRESH'(1,2) NE 'X''').BBB .                   S22025
         AIF   (K'&THRESH EQ 7 AND '&THRESH'(7,1) EQ '''').CCC . S22025
.AAA     MNOTE 12,'*** IHB300 INVALID THRESH OPERAND'
         AGO   .MEND                                             S22025
.CCC     ANOP                                                    S22025
&TH      SETB  1 .                                               S22025
&HEX     SETB  1 .                                               S22025
&CT      SETA  &CT+4 .                                           S22025
&BIT     SETA  &BIT+4 .                                          S22025
         AGO   .TRY .                                            S22025
.BBB     AIF   ('&THRESH'(1,4) NE 'XL2''').DEC .                 S22025
         AIF   (K'&THRESH NE 9 OR '&THRESH'(9,1) NE '''').AAA .  S22025
&TH      SETB  1 .                                               S22025
&HEX     SETB  1 .                                               S22025
&CT      SETA  &CT+4 .                                           S22025
&BIT     SETA  &BIT+4 .                                          S22025
         AGO   .TRY .                                            S22025
.DEC     ANOP .                                                  S22025
&A       SETC  '&THRESH' .                                       S22025
&AA      SETA  &A .                                              S22025
         AIF   (&AA GT 65535).AAA .                              S22025
&CT      SETA  &CT+4 .                                           S22025
&TH      SETB  1 .                                               S22025
&BIT     SETA  &BIT+4 .                                          S22025
.TRY     AIF   ('&DEST' NE 'PUT').TEST
         IEDQVCON 42,IEDQA5                                      S22025
         AIF   (&TH).THPT                                      @YA11171
         CNOP  0,4                                               S22025
&NAME    BAL   1,*+6                                            SA57691
         DC    AL1(&IEDQZE(42)),AL1(2)                           S22025
         AGO   .DSTPT                                          @YA11171
.THPT    CNOP 0,4                                              @YA11171
&NAME    BAL   1,*+8                                           @YA11171
         DC    AL1(&IEDQZE(42)+1),AL1(4)                       @YA11171
         AIF   (&HEX).XTHR                                     @YA11171
         DC    AL2(&THRESH)                                    @YA11171
         AGO   .DSTPT                                          @YA11171
.XTHR    DC    &THRESH                                         @YA11171
.DSTPT   ANOP                                                  @YA11171
         L     15,IEDUI                                          S22025
         BALR  14,15
         AGO   .MEND
.TEST    AIF   ('&DEST' EQ '').ASTST1   DEST=** IS DEFAULT     @YM04688
         AIF   ('&DEST'(1,4) NE 'REG(').ASTST .                  S22027
&E       SETA  K'&DEST .                                         S22027
         AIF   ('&DEST'(&E,1) NE ')').ERR1 .                     S22027
&E       SETA  &E-5 .                                            S22027
&REG     SETC  '&DEST'(5,&E) .                                   S22027
         AIF   ('&DEST'(5,1) EQ '0').ERR1 .                      S22027
         AIF   ('&REG'(1,1) LT '0').SETBIT                       S22027
         AIF   ('&REG' LT '2' OR '&REG' GT '12').ERR1            S22027
.SETBIT  ANOP                                                    S22027
&BIT     SETA  &BIT+1 .                                          S22027
&FLG     SETB  1 .                                               S22027
         AGO   .HERE .                                           S22027
.ASTST   ANOP .                                                  S22027
         AIF   ('&DEST' NE '**').T1
.ASTST1  ANOP                           DEST=** IS DEFAULT     @YM04688
&BIT     SETA  &BIT+32
&LEN     SETA  &LEN+4
         AGO   .HERE
.T1      AIF   ('&DEST'(1,1) EQ '(').PAR
         AIF   ('&DEST' EQ 'ORIGIN').ORIG1 .                     S22027
.GO      IEDQCHAR 8,&DEST
         AIF   (&IEDQZA(24)).ERR1
&D       SETA  &IEDQZD
         AIF   (&IEDQZD LE 8).T2
.ERR1    MNOTE 12,'***  IHB300  DESTINATION OPERAND INVALID AS SPECIFIE*
               D'
         AGO   .MEND
.T2      AIF   ('&DEST'(K'&DEST,1) EQ '''').SETS
         AIF   ('&DEST'(1,1) GE 'A' AND '&DEST'(1,1) LE 'Z').SETO
         AIF   ('&DEST'(1,1) NE '$' AND '&DEST'(1,1) NE '@' AND '&DEST'*
               (1,1) NE '#').ERR1
.SETO    ANOP
&BIT     SETA  &BIT+64
&FLG     SETB  1
&LEN     SETA  &LEN+4
         AGO   .HERE
.SETS    ANOP
&BIT     SETA  &BIT+128
&FLG     SETB  1
         AGO   .HERE
.ORIG1   ANOP .                                                  S22027
&BIT     SETA  &BIT+2 .                                          S22027
         AGO   .HERE .                                           S22027
.PAR     AIF   (K'&DEST NE 3).ERR1
         AIF   ('&DEST'(K'&DEST,1) NE ')').ERR1
         AIF   ('&DEST'(2,1) LT '1' OR '&DEST'(2,1) GE '9').ERR1
&BIT     SETA  &BIT+32
&LEN     SETA  &LEN+4
.HERE    AIF   ('&EOA' EQ '').EXITCHK
.CHEOA   IEDQCHAR 8,&EOA
&BIT     SETA  &BIT+8
&LEN     SETA  &LEN+4
         AIF   (&IEDQZA(24)).ERR
         AIF   (&IEDQZD LE 8).EXITCHK
.ERR     MNOTE 12,'***  IHB300  EOA OPERAND INVALID AS SPECIFIED'
         AGO   .MEND
.EXITCHK AIF   ('&EXIT' EQ '').OK
&BIT     SETA  &BIT+16
&LEN     SETA  &LEN+4
         AIF   (K'&EXIT LE 8).OK
         MNOTE 12,'***  IHB108  EXIT OPERAND INVALID AS SPECIFIED'
         AGO   .MEND
.OK      AIF   ('&DEST' EQ '').V2                              @YM04688
         AIF   ('&DEST'(K'&DEST,1) EQ '''').V1
         AIF   ('&DEST' EQ 'ORIGIN').V1 .                        S22027
         AIF   ('&DEST'(1,4) EQ 'REG(').V1 .                     S22027
         AIF   ('&DEST' EQ '**' OR '&DEST'(1,1) EQ '(').V2
         IEDQVCON (42,31),(IEDQA5,IEDQAE)
         AGO   .BAL
.V1      IEDQVCON 42,IEDQA5
         AGO   .BAL
.V2      AIF   ('&EOA' EQ '').V1
         IEDQVCON (42,21),(IEDQA5,IEDQBA)
.BAL     CNOP  0,4
&NAME    BAL   1,*+4+&LEN+&CT                                    S22025
         DC    AL1(&IEDQZE(42))
         AIF   (&LEN EQ 24).SC
         AIF   (&LEN EQ 20).SE
         AIF   (&LEN EQ 12).SF
         AIF   ('&EXIT' EQ '').SF
.SE      DC    AL1(8+&CT),AL1(&BIT) .                            S22025
         AIF   ('&EOA' EQ '').SZ
         DC    AL1(&IEDQZE(21))
         AGO   .E
.SZ      DC    AL1(0)
         AGO   .E
.SC      DC    AL1(12+&CT),AL1(&BIT),AL1(&IEDQZE(21)) .          S22025
         AGO   .E
.SF      DC    AL1(4+&CT),AL1(&BIT),AL1(&IEDQZE(21))             S22025
.E       AIF   ('&EOA' EQ '').X
&C       SETC  '&SYSECT'
IEDQMISC CSECT
         AIF   ('&EOA'(K'&EOA,1) EQ '''').SETE
         DC    C'&EOA'
         AGO   .C
.SETE    DC    &EOA
.C       ANOP
&C       CSECT
         DC    AL1(&IEDQZD),AL3(IEDQMISC+&IEDQZK)
&IEDQZK  SETA  &IEDQZK+&IEDQZD
.X       AIF   ('&EXIT' EQ '').CKTRSH .                          S22025
         DC    AL4(&EXIT)
.CKTRSH  AIF   (NOT &TH).SI .                                    S22025
         AIF   (&HEX).XDC .                                      S22025
         DC    AL2(&THRESH),AL2(0) .                             S22025
         AGO   .SI                                               S22025
.XDC     DC    &THRESH,AL2(0) .                                  S22025
.SI      AIF   ('&DEST' EQ '').NSB                             @YM04688
         AIF   ('&DEST'(K'&DEST,1) EQ '''').SB
         AIF   ('&DEST' EQ 'ORIGIN').SB .                        S22027
         AIF   ('&DEST'(1,4) EQ 'REG(').SB .                     S22027
         AIF   ('&DEST' NE '**' AND '&DEST'(1,1) NE '(').SL
.NSB     DC    AL1(&IEDQZE(30)),X'04',AL1(15)                  @OX19417
         AIF   ('&DEST' EQ '**' OR '&DEST' EQ '').FF           @YM04688
&C       SETC  '&DEST'(2,1)
&LEN     SETA  &C
         DC    AL1(&LEN)
         AGO   .SB
.FF      DC    X'FF'
         AGO   .SB
.SL      DC    AL1(&IEDQZE(31)),X'04',AL1(&DEST),AL1(15)       @YM08438
.SB      DC    AL1(8+&FLG),AL1(8),AL1(0)
         AIF   ('&DEST' EQ '').SR                              @YM04688
         AIF   ('&DEST'(1,4) EQ 'REG(').REG .                    S22027
         AIF   ('&DEST'(K'&DEST,1) NE '''').SR
         DC    AL1(&D)
&C       SETC  '&SYSECT'
IEDQMISC CSECT
         DC    &DEST
&C       CSECT
         DC    A(IEDQMISC+&IEDQZK)
&IEDQZK  SETA  &IEDQZK+&D
         AGO   .SV
.REG     ANOP .                                                  S22027
         DC    AL1(0) .                 TERM NAME LENGTH         S22027
         DC    AL4(0) .                 TERM NAME ADR            S22027
         IC    15,0(,&REG) .                                     S22027
         STC   15,*-9 .                                          S22027
         LA    &REG,1(,&REG) .                                   S22027
         ST    &REG,*-16 .                                       S22027
         BCTR  &REG,0                                          @OZ48773
         AGO   .SV .                                             S22027
.SR      DC    XL5'00'
.SV      L     15,IEDUI
         BALR  14,15
         AIF   ('&EOA' EQ '').MEND
         B     *+6                                              SA56312
IEDR&SYSNDX    DC   H'0'                                        SA56312
         STH   15,IEDR&SYSNDX                                   SA56312
         SETSCAN &EOA
         LTR   15,15                                            SA56312
               BNZ  *+8                                         SA56312
         LH    15,IEDR&SYSNDX                                   SA56312
.MEND    SPACE 2
         MEND
