         MACRO
&LABEL   IKJPOSIT &TYPNAM,&OP,&OP1,&OP2,&OP3,&OP4,&OP5,&OP6,           *
               &PROMPT=,&DEFAULT=,&HELP=,&VALIDCK=
.*                                                               Y02666
.* RELEASE 20 SUPPORT CODE -- 20035
.* RELEASE 2.0 SUPPORT CODE -- Y02666
         GBLC  &IKJCSNM,&IKJDSNM
         LCLA  &TYPENO,&PCLSIZ,&T,&END
         LCLB  &FLIST,&FASIS,&FPRPT,&FDFLT,&FHELP,&FRANGE,&FVALID,&FEND*
               BIT,&FQSTR,&FUSID,&FVOLSER,&FDDNAM
.*                                                               Y02666
&FENDBIT SETB  0
.*                                                               Y02666
         AIF   (N'&SYSLIST LT 9).GOOD
.*                                                               Y02666
         MNOTE 8,'NO MORE THAN 8 POSITIONAL PARAMETERS ALLOWED'
.*                                                               Y02666
&FENDBIT SETB  1
.GOOD    ANOP
         AIF   ('&IKJCSNM' NE '').GOOD2
         MNOTE 8,'CSECT NAME (FROM IKJPARM MACRO LABEL) IS MISSING'
&FENDBIT SETB  1
.GOOD2   ANOP
&FLIST SETB ('&OP' EQ 'LIST' OR '&OP1' EQ 'LIST' OR '&OP2' EQ 'LIST'   *
               OR '&OP3' EQ 'LIST' OR '&OP4' EQ 'LIST'                 *
               OR '&OP5' EQ 'LIST' OR 'OP6' EQ 'LIST')
.*                                                               Y02666
&FASIS SETB ('&OP' EQ 'ASIS' OR '&OP1' EQ 'ASIS' OR '&OP2' EQ 'ASIS'   *
               OR '&OP3' EQ 'ASIS' OR '&OP4' EQ 'ASIS' OR '&OP5'       *
               EQ 'ASIS' OR '&OP6' EQ 'ASIS')
.*                                                               Y02666
&FRANGE SETB ('&OP' EQ 'RANGE' OR '&OP1' EQ 'RANGE' OR '&OP2'          *
               EQ 'RANGE' OR '&OP3' EQ 'RANGE' OR '&OP4' EQ            *
               'RANGE' OR '&OP5' EQ 'RANGE' OR '&OP6' EQ 'RANGE')
.*                                                               Y02666
&FVOLSER SETB ('&OP' EQ 'VOLSER' OR '&OP1' EQ 'VOLSER' OR '&OP2'       *
               EQ 'VOLSER' OR '&OP3' EQ 'VOLSER' OR '&OP4' EQ 'VOLSER' *
               OR '&OP5' EQ 'VOLSER' OR '&OP6' EQ 'VOLSER')
.*                                                               Y02666
&FQSTR SETB ('&OP' EQ 'SQSTRING' OR '&OP1' EQ 'SQSTRING' OR '&OP2' EQ  *
               'SQSTRING' OR '&OP3' EQ 'SQSTRING' OR '&OP4' EQ 'SQSTRIN*
               G' OR '&OP5' EQ 'SQSTRING' OR '&OP6' EQ 'SQSTRING')
.*                                                               Y02666
&FUSID   SETB  ('&OP' EQ 'USID' OR '&OP1' EQ 'USID' OR                 *
               '&OP2' EQ 'USID' OR '&OP3' EQ 'USID' OR '&OP4' EQ       *
               'USID' OR '&OP5' EQ 'USID' OR '&OP6' EQ 'USID')
.*                                                               Y02666
&FDDNAM SETB ('&OP' EQ 'DDNAM' OR '&OP1' EQ 'DDNAM' OR '&OP2' EQ       *
               'DDNAM' OR '&OP3' EQ 'DDNAM' OR '&OP4' EQ 'DDNAM' OR    *
               '&OP5' EQ 'DDNAM' OR '&OP6' EQ 'DDNAM')
.*                                                               Y02666
         AIF   ('&OP' EQ '' OR '&OP' EQ 'LIST' OR '&OP' EQ             *
               'USID' OR '&OP' EQ 'RANGE' OR '&OP' EQ 'ASIS' OR        *
               '&OP' EQ 'UPPERCASE' OR '&OP' EQ 'SQSTRING').OPT
         AIF   ('&OP' EQ 'VOLSER' OR '&OP' EQ 'DDNAM').OPT
.*                                                               Y02666
         MNOTE 8,'''&OP'' IS AN INVALID POSITIONAL PARAMETER'
&FENDBIT SETB  1
.OPT     AIF   ('&OP1' EQ '' OR '&OP1' EQ 'LIST' OR '&OP1'             *
               EQ 'RANGE' OR '&OP1' EQ 'ASIS' OR                       *
               '&OP1' EQ 'USID' OR '&OP1' EQ 'UPPERCASE').OPT2
         AIF   ('&OP1' EQ 'SQSTRING' OR '&OP1' EQ 'VOLSER'             *
               OR '&OP1' EQ 'DDNAM').OPT2
.*                                                               Y02666
         MNOTE 8,'''&OP1'' IS AN INVALID POSITIONAL PARAMETER'
&FENDBIT SETB  1
.OPT2    AIF   ('&OP2' EQ '' OR '&OP2' EQ 'LIST' OR '&OP2'             *
               EQ 'RANGE' OR '&OP2' EQ 'ASIS' OR                       *
               '&OP2' EQ 'USID' OR '&OP2' EQ 'UPPERCASE').OPT3
.*                                                               Y02666
         AIF    ('&OP2' EQ 'SQSTRING' OR '&OP2' EQ 'VOLSER' OR         *
               '&OP2' EQ 'DDNAM').OPT3
         MNOTE 8,'''&OP2'' IS AN INVALID POSITIONAL PARAMETER'
&FENDBIT SETB  1
.OPT3    AIF   ('&OP3' EQ '' OR '&OP3' EQ 'LIST' OR '&OP3'             *
               EQ 'RANGE' OR '&OP3' EQ 'ASIS' OR                       *
               '&OP3' EQ 'USID' OR '&OP3' EQ 'UPPERCASE').OPT4
         AIF   ('&OP3' EQ 'SQSTRING').OPT4
         AIF   ('&OP3' EQ 'VOLSER' OR '&OP3' EQ 'DDNAM').OPT4
.*                                                               Y02666
         MNOTE 8,'''&OP3'' IS AN INVALID POSITIONAL PARAMETER'
&FENDBIT SETB  1
.OPT4    AIF   ('&OP4' EQ '' OR '&OP4' EQ 'LIST' OR '&OP4'             *
               EQ 'RANGE' OR '&OP4' EQ 'ASIS' OR                       *
               '&OP4' EQ 'USID' OR '&OP4' EQ 'UPPERCASE').OPT1
         AIF   ('&OP4' EQ 'SQSTRING').OPT1
         AIF   ('&OP4' EQ 'VOLSER' OR '&OP4' EQ 'DDNAM').OPT1
.*                                                               Y02666
         MNOTE 8,'''&OP4'' IS AN INVALID POSITIONAL PARAMETER'
&FENDBIT SETB  1
.OPT1    AIF   ('&OP5' EQ '' OR '&OP5' EQ 'LIST' OR '&OP5'             *
               EQ 'RANGE' OR '&OP5' EQ 'ASIS' OR                       *
               '&OP5' EQ 'USID' OR '&OP5' EQ 'UPPERCASE').OPT5
         AIF   ('&OP5' EQ 'SQSTRING').OPT5
         AIF   ('&OP5' EQ 'VOLSER' OR '&OP5' EQ 'DDNAM').OPT5
.*                                                               Y02666
         MNOTE 8,'''&OP5'' IS AN INVALID POSITIONAL PARAMETER'
&FENDBIT SETB  1
.OPT5     AIF   ('&OP6' EQ '' OR '&OP6' EQ 'LIST' OR '&OP6'            *
               EQ 'RANGE' OR '&OP6' EQ 'ASIS' OR                       *
               '&OP6' EQ 'USID' OR '&OP6' EQ 'UPPERCASE').OPT6
          AIF   ('&OP6' EQ 'SQSTRING').OPT6
          AIF   ('&OP6' EQ 'VOLSER' OR '&OP6' EQ 'DDNAM').OPT6
.*                                                               Y02666
         MNOTE 8,'''&OP6'' IS AN INVALID POSITIONAL PARAMETER'
&FENDBIT SETB  1
.OPT6    AIF   (('&OP' EQ 'UPPERCASE' OR '&OP1' EQ 'UPPERCASE'         *
                OR '&OP2' EQ 'UPPERCASE' OR '&OP3' EQ                  *
                'UPPERCASE') AND &FASIS).ERROR0
         AIF   (('&OP4' EQ 'UPPERCASE') AND &FASIS).ERROR0
         AIF   (('&OP5' EQ 'UPPERCASE' OR '&OP6' EQ 'UPPERCASE') AND   *
                &FASIS).ERROR0
         AGO   .A7
.ERROR0  MNOTE 8,'BOTH ''UPPERCASE'' AND ''ASIS'' CANNOT BE SPECIFIED'
&FENDBIT SETB  1
.A7      ANOP
         AIF   (NOT(&FUSID OR &FVOLSER OR &FDDNAM)).A0
         AIF   ('&TYPNAM' EQ 'DSNAME' OR '&TYPNAM' EQ 'DSTHING').A0
         AIF     (NOT &FUSID).A6
.ERROR7 MNOTE 4,'''USID'' OPTION ILLEGAL WITH &TYPNAM - IGNORED'
&FUSID  SETB  0
&FENDBIT SETB  1
.A6      ANOP
         AIF     (NOT &FVOLSER).A10
.ERROR8 MNOTE 4,'''VOLSER'' OPTION ILLEGAL WITH &TYPNAM - IGNORED'
&FVOLSER  SETB  0
&FENDBIT SETB  1
.A10     ANOP
         AIF     (NOT &FDDNAM).A0
.ERROR9 MNOTE 4,'''DDNAM'' OPTION ILLEGAL WITH &TYPNAM - IGNORED'
&FDDNAM  SETB  0
&FENDBIT SETB  1
.A0      ANOP
         AIF   (('&TYPNAM' EQ 'DELIMITER' OR '&TYPNAM' EQ 'SPACE')     *
               AND ('&PROMPT' NE '' OR '&DEFAULT' NE '' OR '&HELP' NE  *
               '' OR '&VALIDCK' NE '')).ERROR3
&FPRPT   SETB  ('&PROMPT' NE '')
&FDFLT   SETB  ('&DEFAULT' NE '')
&FHELP   SETB  ('&HELP' NE '')
&FVALID  SETB  ('&VALIDCK' NE '')
         AGO   .A1
.ERROR3  MNOTE  4,'PROMPT, DEFAULT, HELP, VALIDCK IGNORED WITH &TYPNAM'
.A1      AIF   (('&TYPNAM' NE 'ADDRESS' AND '&TYPNAM' NE 'VALUE')      *
               AND &FRANGE).RANGE
         AGO   .A15
.RANGE   MNOTE 4,'''RANGE'' OPTION ILLEGAL WITH &TYPNAM - IGNORED'
&FRANGE  SETB  0
.A15     AIF   (&FDFLT AND &FPRPT).ERROR1
         AGO   .A2
.ERROR1  MNOTE 8,'PARAMETER CANNOT BOTH DEFAULT AND REQUIRE PROMPTING'
&FENDBIT SETB  1
.A2      AIF   ('&LABEL' EQ '').ERROR2
         AGO   .A3
.ERROR2  MNOTE 8,'STATEMENT LABEL MISSING BUT REQUIRED'
&FENDBIT SETB  1
.A3      AIF   (('&TYPNAM' NE 'STRING') AND &FQSTR).ERROR4
.*                                                               Y02666
         AGO   .A4
.ERROR4  MNOTE 8,'''SQSTRING'' OPTION ILLEGAL WITH &TYPNAM'
.*                                                               Y02666
&FENDBIT  SETB  1
.A4      AIF   (NOT &FLIST OR ( '&TYPNAM' NE 'DELIMITER' AND '&TYPNAM' *
               NE 'STRING' AND '&TYPNAM' NE 'QSTRING' AND '&TYPNAM'    *
               NE 'SPACE')).ACCEPT1
         MNOTE 4,'''LIST'' OPTION ILLEGAL WITH &TYPNAM - IGNORED'
&FLIST   SETB  0
.ACCEPT1 AIF   (NOT &FASIS OR ( '&TYPNAM' NE  'DELIMITER' AND          *
               '&TYPNAM' NE 'SPACE')).ACCEPT2
         MNOTE 4,'''ASIS'' OPTION ILLEGAL WITH &TYPNAM - IGNORED'
&FASIS   SETB  0
.ACCEPT2 ANOP
         AIF   ('&OP' NE 'UPPERCASE' AND '&OP2' NE 'UPPERCASE'         *
               AND '&OP3' NE 'UPPERCASE' OR ('&TYPNAM' NE              *
               'DELIMITER' AND '&TYPNAM' NE 'SPACE')).ACCEPT4
         AIF   ('&OP4' NE 'UPPERCASE' AND '&OP5' NE                    *
               'UPPERCASE' AND '&OP6' NE 'UPPERCASE' OR ('&TYPNAM' NE  *
               'DELIMITER' AND '&TYPNAM' NE 'SPACE')).ACCEPT4
         MNOTE 4,'''UPPERCASE'' OPTION ILLEGAL WITH &TYPNAM - IGNORED'
.ACCEPT4 ANOP
         AIF   (NOT &FVALID OR ('&TYPNAM' NE 'STRING')).ACCEPT3
         MNOTE 4,'''VALIDCK'' OPTION ILLEGAL WITH &TYPNAM - IGNORED'
&FVALID  SETB  0
.ACCEPT3 ANOP
&TYPENO  SETA  1
         AIF   ('&TYPNAM' EQ 'DELIMITER').TYPED
&TYPENO  SETA  10
         AIF   ('&TYPNAM' EQ 'SPACE').TYPED
&TYPENO  SETA  3
&PCLSIZ  SETA  2
         AIF   ('&TYPNAM' EQ 'VALUE').TYPED
&TYPENO  SETA  2
         AIF   ('&TYPNAM' EQ 'STRING').TYPED
&TYPENO  SETA  5
         AIF   ('&TYPNAM' EQ 'PSTRING').TYPED
&TYPENO  SETA  9
         AIF   ('&TYPNAM' EQ 'QSTRING').TYPED
&PCLSIZ  SETA  4
&TYPENO  SETA  6
         AIF   ('&TYPNAM' EQ 'USERID').TYPED
&PCLSIZ  SETA  6                                               @Z40RPKQ
&TYPENO  SETA  12                                              @Z40RPKQ
         AIF   ('&TYPNAM' EQ 'UID2PSWD').TYPED                 @Z40RPKQ
&TYPENO  SETA  4
&PCLSIZ  SETA  9
         AIF   ('&TYPNAM' EQ 'ADDRESS').TYPED
&PCLSIZ  SETA  6
&TYPENO  SETA  7
         AIF   ('&TYPNAM' EQ 'DSNAME').TYPED
&TYPENO  SETA  8
         AIF   ('&TYPNAM' EQ 'DSTHING').TYPED
.*                                                               Y02666
&PCLSIZ  SETA  4                                                 YM4738
.*                                                               Y02666
&TYPENO  SETA  11
.*                                                               Y02666
         AIF   ('&TYPNAM' EQ 'JOBNAME').TYPED
         AIF   ('&TYPNAM' EQ '').NULTYPE
         AGO   .ILLTYPE
.NULTYPE MNOTE 8,'POSITIONAL PARAMETER TYPE FIELD MISSING BUT REQUIRED'
&FENDBIT SETB  1
         AGO   .TYPED
.ILLTYPE ANOP
         MNOTE 8,'ILLEGAL POSITIONAL TYPE - ''&TYPNAM'''
.END     MEXIT
.TYPED   AIF   (&FENDBIT).END
&IKJCSNM CSECT ,                       RESUME PCL CSECT
POST&SYSNDX EQU *
         DC    BL2'001&FPRPT&FDFLT.0&FHELP&FVALID&FLIST&FASIS&FRANGE.0&*
               FQSTR&FUSID&FVOLSER&FDDNAM'
         DC    AL2(PEND&SYSNDX-POST&SYSNDX) LENGTH OF PCE
         DC    AL2(&LABEL-&IKJDSNM)    OFFSET TO PDE
         DC    AL1(&TYPENO)            TYPE OF POSITIONAL
         AIF   (&FPRPT).PRPT           PROMPT
         AIF   (NOT &FDFLT).NPRDF      NEITHER
         DC    AL1(IKJ@&SYSNDX-*-2),C&DEFAULT DEFAULT DATA
         AGO   .EQUGEN
.PRPT    DC    AL1(IKJ@&SYSNDX-*-2),C&PROMPT PROMPT DATA
.EQUGEN  ANOP
IKJ@&SYSNDX EQU *
.NPRDF   ANOP
         AIF   (NOT &FHELP).NOHELP     IS HELP SPECIFIED
         DC    AL2(IKJ$&SYSNDX-*)      LENGTH OF HELP DATA
&T       SETA  1
.HELPLP  ANOP
&T       SETA  &T+1                    NEXT HELP
         AIF   (&T EQ 257).HELPER
         AIF   (&T LE N'&HELP).HELPLP
         AGO   .HELPCT
.HELPER  MNOTE 4,'ONLY 255 LEVELS OF HELP AVAILABLE. OTHERS IGNORED'
&T       SETA  &T+1                    PROVIDE FOR DECREMENT
.HELPCT  ANOP
&T       SETA  &T-1
         DC    AL1(&T)                 NUMBER OF HELP MESSAGES
&END     SETA  &T
&T       SETA  1
.HELPLPA ANOP
         DC    AL2(IKJ&SYSNDX.&T-*),AL2(00),C&HELP(&T) HELP MSG SEGMENT
IKJ&SYSNDX.&T EQU *
&T       SETA  &T+1
         AIF   (&T LE &END).HELPLPA
IKJ$&SYSNDX EQU *
.NOHELP  AIF   (NOT &FVALID).NOVALID   IS VALIDCK SPECIFIED
         DC    AL3(&VALIDCK)           VALIDCK ROUTINE ADDRESS
.NOVALID ANOP
PEND&SYSNDX EQU *
&IKJDSNM DSECT
&LABEL   DS    &PCLSIZ.A               ALLOCATE PDE SPACE
         AIF   (NOT &FRANGE).NRNGGEN
         DS    &PCLSIZ.A               ALLOCATE ADDITIONAL PDE SPACE
.NRNGGEN ANOP
         AIF   (NOT &FLIST).NLSTGEN
         DS    A                       FOR LIST CHAIN POINTER
.NLSTGEN ANOP
&SYSECT  CSECT ,                       RESUME USERS CSECT
         MEXIT
         MEND
