         MACRO
&NAME    TGOTO &MH=
         GBLA  &IEDQZE(64)
         GBLB  &IEDQZA(64)
         LCLA  &BIT
         LCLB  &NOGO
         LCLC  &C
.*A22026                                                         S22026
.*A522000-528000                                                 S22029
.*C190000-200000,410000,420000,450000                            S22029
.*             VERSION DATED JANUARY 11, 1977                  @YM08438
         SPACE
         AIF   (&IEDQZA(13)).TSOCHK
         MNOTE 8,'*** IHB233 INCONSISTENT WITH FEATURE OPERAND OF THE IX
               NTRO MACRO' .
&NOGO    SETB  1
.TSOCHK  AIF   (NOT &IEDQZA(9)).SEQCHK
         MNOTE 12,'*** IHB312 MUST FOLLOW STARTMH MACRO WITH TSOMH=NO'
&NOGO    SETB  1
.SEQCHK  AIF   (&IEDQZA(1) AND &IEDQZA(6)).OMITCHK
         MNOTE 12,'*** IHB070 SEQUENCE ERROR - MUST FOLLOW INHDR OR INBX
               UF MACRO' .                                       S22029
&NOGO    SETB  1
.OMITCHK AIF   ('&MH' NE '').CONTINU
         MNOTE 12,'*** IHB001 MH OPERAND REQ''D- NOT SPECIFIED' .
         AGO   .MEND
.CONTINU ANOP
         IEDQCHAR 8,&MH
         AIF   (NOT &IEDQZA(24)).TEST
.ERR     MNOTE 12,'*** IHB300 MH OPERAND INVALID AS SPECIFIED' .
         AGO   .MEND
.TEST    AIF   ('&MH'(K'&MH,1) EQ '''').BLDPARM
         AIF   ('&MH'(1,1) GE 'A' AND '&MH'(1,1) LE 'Z').SETOPT
         AIF   ('&MH'(1,1) NE '$' AND '&MH'(1,1) NE '@' AND '&MH'(1,1) *
               NE '#').ERR
.SETOPT  AIF   (&NOGO).MEND
&BIT     SETA  &BIT+128
         IEDQVCON (50,31),(IEDQBP,IEDQAE)
         AGO   .BAL
.BLDPARM AIF   (&NOGO).MEND
         IEDQVCON 50,IEDQBP
.BAL     CNOP  0,4
&NAME    BAL   1,*+16 .                 BRANCH AROUND PARMLIST   S22029
         DC    AL1(&IEDQZE(50)),X'0C',AL1(&BIT),X'00' BP PRMLIST S22029
         AIF   ('&MH'(K'&MH,1) EQ '''').NOTOPT
         DC    AL1(&IEDQZE(31)),X'04',AL1(&MH),AL1(15)         @YM08438
         AGO   .GENSTMH                                        @PI06957
.NOTOPT  ANOP
         AIF   ('&MH'(2,1) EQ 'L').CLN
&C       SETC  '&MH'(3,K'&MH-3)
         AGO   .DC
.CLN     ANOP
&C       SETC  '&MH'(5,K'&MH-5)
.DC      DC    AL4(&C) .                MH ADDRESS
.GENSTMH DC    V(IEDQAA) .              ADDR OF STARTMH CODE   @PI06957
         L     15,IEDUI
         BALR  14,15
.MEND    SPACE 2
         MEND
