         MACRO
&NAME    SCREEN &MOD,&CHARS,&BLANK=YES,&RETRV=NO                 S99228
.*C070000                                                        A42385
.*A160000,380000                                                 A42385
.*A168000,262000-266000,380100-380400,451000-455000              S99228
.*C020000,270000,330000,370000,460000                            S99228
         GBLA  &IEDQZD,&IEDQZE(64)
         GBLB  &IEDQZA(64)
         GBLC  &IEDQZC
         LCLB  &IX,&NOGO
.*             VERSION DATED OCTOBER 23, 1970
.*             VERSION DATED FEBRUARY 7, 1972                    A42385
         SPACE
         AIF   (NOT &IEDQZA(9)).TSO001                              TSO
         MNOTE 12,'***  IHB312  MUST FOLLOW STARTMH MACRO WITH TSOMH=NO*
               '                                                    TSO
&NOGO    SETB  1                                                    TSO
.TSO001  AIF   (&IEDQZA(4)).OP                                      TSO
         MNOTE 12,'***  IHB070  SEQUENCE ERROR - MUST FOLLOW OUTHDR MAC*
               RO'
&NOGO    SETB  1
.OP      AIF   ('&MOD' EQ '').CH
&IX      SETB  1
         AIF   ('&MOD' EQ 'WDC' OR '&MOD' EQ 'WRE' OR '&MOD' EQ 'WLA').*
               CH
         AIF   ('&MOD' EQ 'XDC' OR '&MOD' EQ 'XRE' OR '&MOD' EQ        *
               'XLA').CH                                         A42385
         AIF   ('&MOD' EQ 'EAU').CH                              S99228
         MNOTE 12,'***  IHB300  COMMAND OPERAND INVALID AS SPECIFIED'
&NOGO    SETB  1
.CH      AIF   ('&CHARS' EQ '').AY
         IEDQCHAR 8,&CHARS
         AIF   (&IEDQZA(24)).ERR
         AIF   (&IEDQZD LE 8).AY
.ERR     MNOTE 12,'***  IHB300  CHARACTER OPERAND INVALID AS SPECIFIED'
&NOGO    SETB  1
.AY      AIF  ('&RETRV' EQ 'YES' OR '&RETRV' EQ 'NO').OK         S99228
         MNOTE 12,'***IHB002 RETRV OPERAND SPECIFIED &RETRV'     S99228
&NOGO    SETB  1                                                 S99228
.OK      AIF   (&NOGO).MEND                                      S99228
         AIF   ('&NAME' EQ '').VCON
&NAME    DS    0H
.VCON    IEDQVCON 25,IEDQAY
         AIF   ('&CHARS' EQ '').NOCH
         IEDQSCAN &IEDQZD,&CHARS,&BLANK
         AIF   (&IEDQZA(24)).MEND
         LTR   15,15
         BNP   *+26                                              S99228
         L     1,IEDADBUF
         STH   15,24(,1)
.NOCH    BAL   1,*+8                                             S99228
         DC    AL1(&IEDQZE(25)+&IX)
         AIF   ('&MOD' NE 'EAU').CKXDC                           S99228
         DC    AL1(3)              ERASE ALL UNPROTECTED         S99228
         AGO   .L                                                S99228
.CKXDC   ANOP                                                    S99228
         AIF   ('&MOD' NE 'XDC').RELB                            A42385
         DC    AL1(16+0) .              WRITE DISPLAY W/O KR     A42385
         AGO   .L                                                A42385
.RELB    AIF   ('&MOD' NE 'XRE').RELC                            A42385
         DC    AL1(16+2) .              WRITE ERASE W/O KR       A42385
         AGO   .L                                                A42385
.RELC    AIF   ('&MOD' NE 'XLA').RELD                            A42385
         DC    AL1(16+1) .              WRITE AT LINE ADDR W/O KRA42385
         AGO   .L                                                A42385
.RELD    ANOP                                                    A42385
         AIF   ('&MOD' NE '' AND '&MOD' NE 'WDC').RELA
         DC    AL1(0) .                 WRITE DISPLAY
         AGO   .L
.RELA    AIF   ('&MOD' EQ 'WLA').LA
         DC    AL1(2) .                 WRITE ERASE
         AGO   .L
.LA      DC    AL1(1) .                 WRITE AT LINE ADDRESS
.L       AIF   ('&RETRV' NE 'YES').RETNO                         S99228
         DC    AL1(128)             RETRIEVE=YES                 S99228
         AGO   .E                                                S99228
.RETNO   ANOP                                                    S99228
         DC    AL1(0)               RETRIEVE NOT = YES           S99228
.E       L     15,IEDUI                                          S99228
         BALR  14,15
.MEND    SPACE 2
         MEND
