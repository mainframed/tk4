         MACRO
&NAME    IEDDFC &COMMAND,&CHARS,&BLANK=YES
.*A000000-999999                                               @X50X9MH
.*C110000-120000                                               @Y17XAMG
         GBLA  &IEDQZD
         GBLA  &IEDQZE(64)
         GBLB  &IEDQZA(64)
         LCLB  &NOGO
         LCLC  &CODE
.*
.* START MH ISSUED?
         AIF   (&IEDQZA(46)).CKOUTHR                           @Y17XAMG
         MNOTE 12,'***IHB312 MUST FOLLOW STARTMH MACRO'        @Y17XAMG
&NOGO    SETB  1
.*
.* MACRO ISSUED IN OUTHDR?
.CKOUTHR AIF   (&IEDQZA(4)).CKCMD
         MNOTE 12,'***IHB070 SEQUENCE ERROR - MUST FOLLOW OUTHDR MACRO'
&NOGO    SETB  1
.*
.* VALID COMMAND SPECIFIED ?
.CKCMD   ANOP
.*
.* SET CODE FOR BID
&CODE    SETC  'C8'
         AIF   ('&COMMAND' EQ 'BID').CKCHAR
         MNOTE 12,'***IHB300 &COMMAND OPERAND INVALID AS SPECIFIED'
&NOGO    SETB  1
.*
.* CONCHARS SPECIFIED?
.CKCHAR  AIF   ('&CHARS' EQ '').ERRCHK
         IEDQCHAR 8,&CHARS
         AIF   (NOT &IEDQZA(24)).ERRCHK
         MNOTE 12,'***IHB300 &CHARS OPERAND INVALID AS SPECIFIED'
&NOGO    SETB  1
.*
.ERRCHK  AIF   (&NOGO).EXIT
&NAME    DS    0H
         IEDQVCON 63,IEDIAS
         AIF   ('&CHARS' EQ '').NOCHAR
         IEDQSCAN &IEDQZD,&CHARS,&BLANK
         AIF   (&IEDQZA(24)).EXIT
         LTR   15,15              CONCHARS FOUND
         BNP   *+26               BRANCH IF NO
         L     1,IEDADBUF         GET BUFFER ADDRESS           @XM10716
         STH   15,24(,1)          UPDATE SCAN POINTER
.NOCHAR  ANOP
         BAL   1,*+8              BRANCH AROUND CONSTANTS
         DC    AL1(&IEDQZE(63))   INDEX TO IEDIAS
         DC    AL1(3)             LENGTH OF PARM LIST
         DC    XL1'&CODE'         COMMAND CODE
         DS    0H
         L     15,IEDUI
         BALR  14,15
.EXIT    ANOP
         MEND
