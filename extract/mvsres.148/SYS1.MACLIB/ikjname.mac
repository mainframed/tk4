         MACRO
         IKJNAME &NAME,&SUBFLD=,&INSERT=,&ALIAS=
.* RELEASE 20 SUPPORT CODE - - 20035
.* RELEASE 2.0 SUPPORT CODE -- Y02666
         GBLC  &IKJCSNM
         LCLA  &T,&END
         LCLB  &FSUBF,&FINSERT,&FALIAS,&EXITBIT
         LCLC  &QUOTE
&FSUBF   SETB  ('&SUBFLD' NE '')       SUBFIELD FLAG
&FINSERT SETB  ('&INSERT' NE '')       INSERT DATA FLAG          Y01993
&FALIAS  SETB  ('&ALIAS' NE '')        KEYWORD NAME ALIAS FLAG   Y02666
         AIF   (N'&SYSLIST LT 2).A0
         MNOTE 8,'NO MORE THAN 1 POSITIONAL PARAMETERS ALLOWED'
&EXITBIT SETB  1
.A0      AIF   ('&NAME' NE '').A1
         MNOTE 8,'NAME OF KEYWORD IS MISSING'
&EXITBIT SETB  1
.A1      AIF   ('&IKJCSNM' NE '').GOOD2
         MNOTE 8,'CSECT NAME (FROM IKJPARM MACRO LABEL) IS MISSING'
.END     MEXIT
.GOOD2   AIF   (&EXITBIT).END
&IKJCSNM CSECT ,                       RESUME PCL CSECT
NAME&SYSNDX EQU *
         DC    BL2'01100&FSUBF.00000&FINSERT.00&FALIAS.0' FLAGS
         DC    AL2(NEND&SYSNDX-NAME&SYSNDX) LENGTH OF PCE
.*
.* THE FOLLOWING CODE SUPPLIES QUOTES TO &NAME IF THEY ARE MISSING.
.*
&QUOTE   SETC  '&NAME'(1,1)
         AIF   ('&QUOTE' EQ '''').QUOT
&QUOTE   SETC  ''''
         DC    AL1(IKJ@&SYSNDX-*-2),C&QUOTE&NAME&QUOTE NAME VALID FOR K*
               EYWORD
         AGO   .QUOTED
.QUOT    ANOP
         DC    AL1(IKJ@&SYSNDX-*-2),C&NAME NAME VALID FOR KEYWORD
.QUOTED  ANOP
IKJ@&SYSNDX EQU *
         AIF   (NOT &FSUBF).NOSUBF
         DC    AL2(&SUBFLD-&IKJCSNM)   OFFSET TO SUBFIELD
.NOSUBF  AIF   (NOT &FINSERT).NOINSRT
         DC    AL1(IKJ$&SYSNDX-*-2),C&INSERT INSERT DATA
IKJ$&SYSNDX EQU *
.*
.* THE FOLLOWING CODE PROCESSES KEYWORD (ABBREVIATIONS) ALIASES  Y02666
.*
.NOINSRT AIF   (NOT &FALIAS).NOALIAS    ARE ALIASES SPECIFIED?   Y02666
&T       SETA  1
.ALOOP   ANOP
&T       SETA  &T+1                     NEXT ALIAS               Y02666
         AIF   (&T EQ 34).AMSG
         AIF   (&T LE N'&ALIAS).ALOOP
         AGO   .ACOUNT
.AMSG    MNOTE 4,'ONLY 32 ALIASES ARE ALLOWED. OTHERS IGNORED'
.ACOUNT  ANOP
&T       SETA  &T-1
         DC    AL1(&T)            NUMBER OF ALIASES
&END     SETA  &T
&T       SETA  1
.ALIASET ANOP
         DC    AL1(AL&SYSNDX.&T-*-1),C&ALIAS(&T)
AL&SYSNDX.&T EQU *
&T       SETA  &T+1
         AIF   (&T LE &END).ALIASET
IKJB&SYSNDX EQU *
.NOALIAS ANOP
NEND&SYSNDX EQU *
&SYSECT  CSECT ,                       RESUME USERS CSECT
         MEXIT
         MEND
