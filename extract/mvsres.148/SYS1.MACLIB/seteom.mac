         MACRO
&NAME    SETEOM &ENDCHAR=,&LENGTH=,&PROCESS=NO,&REMOVE=NO,&EOM=  S99238
         GBLA  &IEDQZE(64),&IEDQZD,&IEDQZK
         GBLB  &IEDQZA(64)
         LCLA  &LNGRS,&LNGMSG,&PARMLN,&ADDR,&CH,&CTR
         LCLB  &B(8),&QCB
         LCLC  &TEMP,&TLEN,&C
.* CHANGE ACTIVITY AS FOLLOWS
.******************* MICROFICHE FLAGS *********************** SUPT CODE
.*A 108500                                                       S99238
.*A 103000                                                      SA57357
.*C 264000                                                      SA57325
.*C618000                                                       SA60306
         AIF   (&IEDQZA(14)).MM
         MNOTE 12,'*** IHB070 SEQUENCE ERROR - MUST BE IN INBLOCK SUBGRX
               OUP'
         AGO   .END
.MM      ANOP
         AIF   (NOT &IEDQZA(35)).BA4
         MNOTE 12,'*** IHB234 SETEOM PREVIOUSLY CODED IN THIS INBLOCK SX
               UBGROUP'
         AGO   .END
.BA4     ANOP
         AIF   (&IEDQZA(48)).BA5                                SA57357
         MNOTE 12,'*** IED920 LMD=YES NOT CODED ON STARTMH MACRO'
         AGO   .END                                            SA57357
.BA5     ANOP                                                  SA57357
&IEDQZA(35) SETB 1
         AIF   ('&EOM' EQ '').CKEND                              S99238
&TEMP    SETC  'EOM'                                             S99238
         AIF   ('&EOM' NE 'ETB').ERR1                            S99238
&B(7)    SETB  1                                                 S99238
         AIF   ('&ENDCHAR' NE '' OR '&LENGTH' NE '').ERR2        S99238
         AIF   ('&PROCESS' NE 'YES').ERR2                        S99238
         AIF   ('&REMOVE' EQ 'NO').CKPRO                         S99238
.ERR2    MNOTE 12,'*** IHB066 INCONSITENT OPERANDS SPECIFIED'    S99238
         AGO   .END                                              S99238
.CKEND   ANOP                                                    S99238
         AIF   ('&ENDCHAR' EQ '').CKLNG
&TEMP    SETC  'ENDCHAR'
.LOOP    ANOP
&CTR     SETA  &CTR+1
         AIF   ('&ENDCHAR'(&CTR,1) EQ ' ').ERR1
         AIF   (&CTR LT K'&ENDCHAR).LOOP
&B(1)    SETB  1
         AIF   ('&ENDCHAR'(1,2) NE 'C''').CKRS1
         AIF   ('&ENDCHAR'(K'&ENDCHAR,1) NE '''' OR K'&ENDCHAR GT      X
               11).ERR1
&LNGRS   SETA  (K'&ENDCHAR-3)
         AGO   .CKLNG
.CKRS1   AIF   ('&ENDCHAR'(1,2) NE 'CL').CKRS2
         AIF   ('&ENDCHAR'(4,1) NE '''' OR '&ENDCHAR'(K'&ENDCHAR,1) NE X
               '''').OPFD1
&C       SETC  '&ENDCHAR'(3,1)
&CH      SETA  &C
         AIF   (K'&ENDCHAR GT 13 OR &CH NE (K'&ENDCHAR-5)).ERR1
&LNGRS   SETA  (K'&ENDCHAR-5)
         AGO   .CKLNG
.CKRS2   AIF   ('&ENDCHAR'(1,2) NE 'X''').CKRS3
         AIF   ('&ENDCHAR'(K'&ENDCHAR,1) NE '''' OR K'&ENDCHAR GT      X
               19).ERR1
&LNGRS   SETA  ((K'&ENDCHAR-3)/2)
         AGO   .CKLNG
.CKRS3   AIF   ('&ENDCHAR'(1,2) NE 'XL').OPFD1
         AIF   ('&ENDCHAR'(4,1) NE '''' OR '&ENDCHAR'(K'&ENDCHAR,1) NE X
               '''').OPFD1
&C       SETC  '&ENDCHAR'(3,1)
&CH      SETA  &C
&CH      SETA  (&CH*2)
         AIF   (K'&ENDCHAR GT 21 OR &CH NE (K'&ENDCHAR-5)).ERR1
&LNGRS   SETA  ((K'&ENDCHAR-5)/2)
         AGO   .CKLNG
.OPFD1   ANOP
&B(2)    SETB  1
         AIF   (K'&ENDCHAR LE 8).CKLNG
.ERR1    MNOTE 12,'*** IHB300 INVALID &TEMP   OPERAND SPECIFIED'
         AGO   .END
.CKLNG   AIF   ('&LENGTH' NE '').CONLNG
         AIF   (&B(1)).CKPRO
         MNOTE 12,'*** IHB001 REQUIRED OPERAND MISSING'
         AGO   .END
.CONLNG  ANOP
         AIF   (N'&LENGTH EQ 2).TWOOP
&TEMP    SETC  'LENGTH'
         AGO   .ERR1
.TWOOP   ANOP
&B(3)    SETB  1
&TLEN    SETC  '&LENGTH(1)'(1,1)
         AIF   ('&TLEN' GE '0' AND '&TLEN' LE '9').INT
.OPFD2   ANOP
&B(4)    SETB  1
         AGO   .OPFD3
.INT     ANOP
&TLEN    SETC  '&LENGTH(1)'(1,K'&LENGTH(1))
&LNGMSG  SETA  &TLEN
         AIF   (&LNGMSG GT 0 AND &LNGMSG LT 65536).BA1
&TEMP    SETC  'LENGTH'
         AGO   .ERR1
.BA1     ANOP
.OPFD3   ANOP
.CKPRO   AIF   ('&PROCESS' NE 'YES').PRONO
&B(5)    SETB  1
&QCB     SETB  1
&PARMLN  SETA  32
         AGO   .CKREM
.PRONO   ANOP
         AIF   ('&PROCESS' EQ 'NO').BA2
&TEMP    SETC  'PROCESS'
         AGO   .ERR1
.BA2     ANOP
&PARMLN  SETA  20
.CKREM   AIF   ('&REMOVE' NE 'YES').EXP
&B(6)    SETB  1
        AGO   .BA3
.EXP     ANOP
         AIF   ('&REMOVE' EQ 'NO').BA3
&TEMP    SETC  'REMOVE'
         AGO   .ERR1
.BA3     ANOP
         IEDQVCON (53,16),(IEDQBO,IEDQAO)
         IEDQVCON  55,IEDQBN
         AIF   (NOT &B(3)).NOTLEN
         IEDQVCON  54,IEDQBR
.NOTLEN  ANOP
         IEDQVCON (31,38),(IEDQAE,IEDQAF)
         CNOP  0,4
&NAME    BAL   1,*+4+&PARMLN                                    SA60306
         DC    AL1(&IEDQZE(53)),AL1(&PARMLN)
         DC    BL1'&B(8)&B(7)&B(6)&B(5)&B(4)&B(3)&B(2)&B(1)'
         DC    AL1(&IEDQZE(31))
         DC    AL1(&IEDQZE(55)),AL1(&IEDQZE(54))
         DC    AL1(&IEDQZE(38)),AL1(&IEDQZE(16))
         DC    AL4(0) .                 HOLD QCB
.SKIP2   AIF   (NOT &B(3)).DC4
         AIF   (&B(4)).DC3
         DC    AL2(&LNGMSG),AL1(&LENGTH(2)),AL1(0)
         AGO   .SKIP3
.DC3     DC    AL1(0),AL1(&LENGTH(1)),AL1(&LENGTH(2)),AL1(0)
         AGO   .SKIP3
.DC4     DC    AL4(0)
.SKIP3   AIF   (NOT &B(1)).BB
         AIF   (&B(2)).SKIP4
         IEDQCHAR 8,&ENDCHAR
         AIF   (NOT &IEDQZA(24)).SKIP5
&TEMP    SETC  'ENDCHAR'
         AGO   .ERR1
.SKIP5   AIF   (&IEDQZD LE 8).SKIP6
&TEMP    SETC  'ENDCHAR'
         AGO   .ERR1
.SKIP6   ANOP
&C       SETC  '&SYSECT'
IEDQMISC CSECT
         DC    AL1(&IEDQZD),&ENDCHAR
&C       CSECT
         DC    AL1(&IEDQZD),AL3(IEDQMISC+&IEDQZK)
&IEDQZK  SETA  &IEDQZK+&IEDQZD+1
         AGO   .AA
.SKIP4   DC    AL1(&ENDCHAR),AL3(0)
         AGO   .AA
.BB      DC    AL4(0)
.AA      ANOP
         AIF   (NOT (&QCB)).SKIP7
         DC    3AL4(0) .                QCB
.SKIP7   L     15,IEDUI
         BALR  14,15
         LR    15,1                     RETURN CODE
.END     SPACE 2
         MEND
