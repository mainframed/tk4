         MACRO
&MESS    IKJTSMSG  &PARTS,&ID,&SECID
         LCLA  &A
         LCLA  &B
         LCLA  &C
         LCLC  &LP
         LCLC  &LEN
         SPACE 2
&A       SETA  1
&B       SETA  N'&PARTS
&LEN     SETC  'A'.'&SYSNDX'
         AIF   (T'&ID NE 'O').PRID
         AIF   (T'&PARTS NE 'O').ID
         DC    CL4'    '           INDICATES END OF MSG CSECT
         AGO   .OUT
.ID      ANOP
         MNOTE 8,'SPECIFY MESSAGE ID'
.PRID    ANOP
&MESS    DC    CL4'&ID'            ID
         AIF   (T'&SECID NE 'O').SECOND
         DC    CL4'    '           NO FOLLOWING MESSAGE
         AGO   .NB
.SECOND  ANOP
         DC    CL4'&SECID'         ID OF FOLLOWING MSG
.NB      ANOP
         DC    AL2(&LEN-*+8)       TOTAL LENGTH
         DC    AL1(&B)             NB OF PARTS TO MSG
.LOOP    ANOP
         AIF   (T'&PARTS(&A) NE 'O').SKP
         DC    AL1(0)              A VARIABLE
         AGO   .INC
.SKP     ANOP
&LP  SETC  '&LEN'.'&A'
         DC    AL1(&LP-*-1)        LENGTH OF MSG PART
         AIF   ('&PARTS(&A)'(1,1) EQ '''').QUO
         MNOTE 12,'QUOTES MISSING ON OPERAND'
.QUO     ANOP
         DC    C&PARTS(&A)         MSG PART
&LP      EQU   *
.INC     ANOP
&A       SETA  &A+1
         AIF   (&A LE N'&PARTS).LOOP
&LEN     EQU   *
.OUT     ANOP
         SPACE 4
         MEND
