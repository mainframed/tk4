         MACRO
&NAME    IEDFMT &IN=,&OUT=,&TABLE=
.*A-000000-999999                                              @Y17XAMN
.*C001600                                                      @OZ26306
.*C004820,004850                                               @OZ38896
.*C004820                                                      @OY22215
.* &IN - EQUALS THE LABEL OF A 256 BYTE WORK AREA TO RECEIVE THE
.*       RU TO BE CONVERTED.
.* &OUT - EQUALS THE LABEL OF A 256 BYTE WORK AREA TO RECEIVE THE
.*        CONVERTED OUTPUT.
.* &TABLE - EQUALS THE LABEL OF A CONVERSION TABLE, WHICH IS EXTERNAL
.*          TO THE MCP CSECT AND DEFINED BY MEANS OF SYSDEF MACROS.
         GBLA  &IEDQZE(64),&IEDQZL(256)
         GBLB  &IEDQZA(64)
         LCLA  &LEN
         LCLB  &NOGO,&FLAG(8)
         LCLC  &REG,&LABEL,&REG1
&LEN     SETA  16             LENGTH OF PARMETER LIST
&LABEL   SETC  'IED#&SYSNDX'  FOR EXTERNAL LABEL               @OZ26306
         AIF   (&IEDQZA(30)).FMTSSC
         MNOTE 12,'*** IEDF03 INVALID USE OF IEDFMT MACRO, NO SSCP MH.'
&NOGO    SETB  1
.FMTSSC  AIF   (NOT &IEDQZA(5) AND NOT &IEDQZA(14)).FMTBGN
         MNOTE 12,'*** IED070 SEQUENCE ERROR-MUST FOLLOW INHDR ORINBUF *
               OR OUTHDR OR OUTBUF MACRO.'
&NOGO    SETB  1
.FMTBGN  AIF   ('&IN' NE '' AND '&OUT' NE '' AND '&TABLE' NE '').FMTINR
         MNOTE 12,'*** IEDF00 OPERANDS IN=,OUT=,AND TABLE= MUST BE CODE*
               D.'
&NOGO    SETB  1
.FMTINR  AIF   ('&IN'(1,1) NE '(').FMTLBLI
&REG1    SETC  '&IN'(2,K'&IN-2)
         AIF   ('&REG1'(1,1) LT '0').FMTSTT8
         AIF   ('&REG1' GE '2' AND '&REG1' LE '12').FMTSTT8
         MNOTE 12,'*** IEDF01 INVALID USE OF IN OPERAND,REG USED INCORR*
               ECTLY.'
&NOGO    SETB  1
.FMTSTT8 ANOP
&FLAG(8) SETB  1                        SET IN=(REGISTER) FLAG
.FMTLBLI ANOP
         AIF   ('&OUT'(1,1) NE '(').FMTLBLO
&REG     SETC  '&OUT'(2,K'&OUT-2)
         AIF   ('&REG'(1,1) LT '0').FMTSTT7
         AIF   ('&REG' GE '2' AND '&REG' LE '12').FMTSTT7
         MNOTE 12,'*** IEDF02 INVALID USE OF OUT OPERAND,REG USED INCOR*
               RECTLY.'
&NOGO    SETB  1
.FMTSTT7 ANOP
&FLAG(7) SETB  1                        SET OUT=(REG) FLAG
.FMTLBLO ANOP
         AIF   (&NOGO).MEND1            NOGO FLAG SET
         AIF   ('&IN'(1,1) NE '(').FMNOREG                     @YM06170
&NAME    XC    2(2,&REG1),2(&REG1)      CLEAR DATA LEN FIELD   @OY22215
         AGO   .FMISCMD                                        @YM06170
.FMNOREG ANOP                                                  @YM06170
&NAME    XC    &IN+2(2),&IN+2           CLEAR DATA LEN FIELD   @OZ38896
.FMISCMD ANOP                                                  @YM06170
         MHGET WORK=&IN
         LTR   15,15                    GOOD RETURN FROM MHGET
         BNZ   &LABEL                   TERMINATE TESTING
         IEDQVCON 66,IEDQFM
         BAL   1,*+4+&LEN               BRANCH AROUND PARM
         DC    AL1(&IEDQZE(48)),AL1(&LEN) 1ST LEV IDX/PARM LEN.
         DC    AL1(&IEDQZL(2))          IEDQFM INDEX
         DC    AL1(0)                   STATUS ALWAYS ZERO
         DC    V(&TABLE)                CONVERSION TABLE ADDRESS
         AIF   (&FLAG(8)).FMTOREG
         DC    AL4(&IN)                 INPUT LABEL ADDRESS
         AGO   .FMTOLBL
.FMTOREG ANOP
         DC    AL4(0)                   INPUT ADDRESS IN REG
.FMTOLBL AIF   (&FLAG(7)).FMTIREG
         DC    AL4(&OUT)                OUTPUT LABEL ADDRESS
         AGO   .FMTSTOR
.FMTIREG ANOP
         DC    AL4(0)                   OUTPUT ADDRESS IN REG
.FMTSTOR AIF   (NOT &FLAG(8)).FMTIST
         ST    &REG1,8(1)               PUT ADDRESS IN INPUT PARM
.FMTIST  AIF   (NOT &FLAG(7)).FMTOST
         ST    &REG,12(1)               PUT ADDRESS IN OUTPUT PARM
.FMTOST  ANOP
         L     15,IEDUI                 BRANCH TO IEDUI
         BALR  14,15
         LTR   15,15                    GOOD RETURN FROM IEDQFM
         BNZ   &LABEL                   TERMINATE TESTING
         MHPUT WORK=&OUT
.MEND    ANOP
&LABEL   EQU   *
.MEND1   MEND
