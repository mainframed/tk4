         MACRO
&NAME    IEDQTO  &OPDATA,&X=(M,N,O,P,Q,R,S,T),&AA=(A,B,C,D,E,F)
.*                                                             @OZ44387
         GBLC  &IEDQOP(255),&IEDQSV(8),&IEDTNME                @OZ44387
         GBLB  &IEDQZA(64)
         GBLA  &IEDQAR(64)
         LCLC  &S,&T,&U,&V
         LCLA  &A,&B,&C,&D,&SX,&NN,&DS
.*C520000                                                      @OZ39371
.*A280000                                                      @OZ39371
.*C020000-030000                                               @OZ44387
.*A280000,280500                                               @OZ44387
.*PACKAGE ERROR                                                @OZ49699
&A       SETA  N'&OPDATA
&DS      SETA  &IEDQAR(5)
         AIF   (&IEDQAR(5) NE 0).NOCS
&IEDQSV(3)  SETC  'IED'.'&IEDQAR(6)'.'&X(&IEDQAR(9))'
.NOCS    AIF   ('&OPDATA(&A)' EQ '').AD
&IEDQAR(5) SETA 0
         AIF   (&IEDQAR(3) NE 0).CKN
         SPACE 2
         MNOTE 12,'*** IHB317 OPDATA INVALID- NO OPTION FIELDS SPECIFIE*
               D'
         SPACE 2
         MEXIT
.CKN     AIF   (&IEDQAR(3) GE &DS+&A).XX
         SPACE 2
.ER      MNOTE 12,'*** IHB306 TOO MANY OPDATA PARAMETERS'
&A       SETA  &IEDQAR(3)-&DS
.XX      ANOP
&D       SETA  &DS
&SX      SETA  &IEDQAR(6)
&NN      SETA  &IEDQAR(9)
IEDQOPT  CSECT
         AIF   ('&NAME' EQ '&IEDTNME').LOOP                    @OZ44387
         DS    0F                      ALIGN TO FULL WORD      @OZ39371
&IEDTNME SETC  '&NAME'                                         @OZ44387
.LOOP    ANOP
&D       SETA  &D+1
&B       SETA  &B+1
         AIF   ('&OPDATA(&B)' EQ '').NXT
&S       SETC  'IED'.'&IEDQAR(6)'.'&X(&IEDQAR(9))'
&IEDQAR(6)  SETA  &IEDQAR(6)+1
         AIF   (&IEDQAR(6) LE 9999).OK
&IEDQAR(6)  SETA  0
&IEDQAR(9)  SETA  &IEDQAR(9)+1
.OK      ANOP
&T       SETC  '&IEDQOP(&D)'(1,1)
&C       SETA  &T
&T       SETC  ')'
         AIF   ('&IEDQOP(&D)'(&C+2,1) EQ '(').GO
&T       SETC  ''''
.GO      ANOP
&U       SETC  '&IEDQOP(&D)'(2,&C+1)
&S       DC    &U&OPDATA(&B)&T
.NXT     AIF   (&B LT &A).LOOP
IEDQTRMC CSECT
&B       SETA  0
         AIF   (&DS NE 0).CON
         DC    AL1(&A) .                NUMBER OF OPTION FIELDS
         DC    AL2((&IEDQSV(3)-IEDQOPT)/4) OFFSET TO OPTION    @OZ39371
*                                       TABLE IN FULL WORDS    @OZ39371
&D       SETA  0
.LOP2    ANOP
&D       SETA  &D+1
&B       SETA  &B+1
         AIF   ('&OPDATA(&B)' NE '').GO2
         DC    X'FF' .                  OPTION FIELD OMMITTED
         AGO   .NX2
.AD      ANOP
&A       SETA  &A-1
&IEDQAR(5)  SETA  &IEDQAR(5)+&A
         AIF   (&IEDQAR(3) GT &DS+&A).XX
&IEDQAR(5)  SETA  0
         AGO   .ER
.CON     ANOP
&V       SETC  'IED'.'&IEDQAR(4)'.'&AA(&IEDQAR(7))'
         ORG   &V+17
         DC    AL1(&DS+&A)
         ORG
&D       SETA  &DS
         AGO   .LOP2
.GO2     ANOP
&S       SETC  'IED'.'&SX'.'&X(&NN)'
&SX      SETA  &SX+1
         AIF   (&SX LE 9999).K2
&SX      SETA  0
&NN      SETA  &NN+1
.K2      DC    AL1(&S-&IEDQSV(3))
.NX2     AIF   (&B LT &A).LOP2
         MEND
