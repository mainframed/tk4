*%BTMHJN:  MACRO KEYS(DOEND);                                  @OY36208
*GOTO PLSHJN;                      /* BILINGUAL HEADER
         MACRO
&NAME    BTMHJN &LABEL,&HJN,&NOINFLB                           @OY36208
.**********************************************************************
.*                                                                    *
.*  $MAC(BTMHJN)          PROD(BTAM):                          @OY36208
.*                                                                    *
.*  DESCRIPTIVE NAME = MODULE IDENTIFIER MACRO                        *
.*                                                                    *
.*  FUNCTION = INVOKE THE BTMHJN MACRO TO IDENTIFY A MODULE    @OY36208
.*                                                                    *
.*  NOTES =                                                           *
.*    MACRO LIST POSITIONAL PARAMETERS:                               *
.*        1ST - PROVIDES THE BRANCH-TO SYMBOL                         *
.*                (IF OMITTED NO BRANCH IS GENERATED)                 *
.*        2ND - IGNORED (HJN IS NOW ALWAYS PROVIDED)                  *
.*        3RD - SUPPRESSES GENERATION OF AN IDENTIFIER LABEL          *
.*                                                                    *
.*    PLS INVOCATION EXAMPLES:                                        *
.*                                                                    *
.*        ?BTMHJN(PROCNAME) DOEND;                             @OY36208
.*                                                                    *
.*        WHERE:  PROCNAME IS THE PROCEDURE NAME                      *
.*                DOEND SPECIFIES DO-END PAIR TO BE SUPPLIED          *
.*                                                                    *
.*        ?BTMHJN(PROCNAME);                                   @OY36208
.*                                                                    *
.*        WHERE:  PROCNAME IS THE PROCEDURE NAME                      *
.*                NO DO-END PAIR TO BE SUPPLIED                       *
.*                                                                    *
.*  EXTERNAL REFERENCES = NONE                                        *
.*                                                                    *
.*  CHANGE ACTIVITY =                                                 *
.*    TOOK MACRO IEDHJN FROM TCAM AND NAMED TO BTMHJN          @OY36208
.* C002700,011200-012800         REMOVED ALL REFERENCES TO     @OY42916
.* C013600-013800,016100-016120  TCAM, TCAM APARS, AND         @OY42916
.* C016160-016200,018000         COMMENT LINES THAT DO NOT     @OY42916
.* C025700,027510                APPLY TO BTAM.                @OY42916
.* D004000-005500,010000-010002                                @OY42916
.*                                                                    *
.**********************************************************************
         GBLB  &INFLBL               INFO LABEL WAS GENERATED
         LCLA  &K                    &K=LENGTH OF SYSPARM
         LCLC  &HJA,&HJB,&HJC        &HJC=PTF/LEVEL IDENTIFIER
&HJC     SETC  '       '             DEFAULT PTF/LEVEL (BLANKS)
&K       SETA  K'&SYSPARM            SYSPARM LENGTH
&HJA     SETC  '&SYSPARM'(1,4)
&HJB     SETC  '&SYSPARM'(5,4)
         AIF   (&K LT 15).CODE       NO PTF/LEVEL VALUE PASSED
&HJC     SETC  '&SYSPARM'(9,7)       SET PTF/LEVEL VALUE
.CODE    ANOP
         AIF   ('&LABEL' EQ '').NOBRNCH
         B     &LABEL .              BRANCH AROUND CONSTANTS
         SPACE 1
.NOBRNCH ANOP
         AIF   ('&NAME' EQ '').NOLABEL
         AIF   (&INFLBL OR ('&NOINFLB' NE '')).LABEL
&INFLBL  SETB  1                     INDICATE INFO LBL GENERATED
MODINF   EQU   *                     MODULE ID LABEL
.LABEL   ANOP                        GENERATE MODULE IDENT
.****                         MODULE IDENT NOW 8 CHARACTERS
         DC    CL8'&NAME' .          MODULE IDENTIFIER
.NOLABEL ANOP
         DC    X'&HJA' .               DATE OF MODIFICATION
.****                         HJN NOW PROVIDED UNCONDITIONALLY
         DC    X'&HJB' .               HJN OF MODIFICATION
.DATE    ANOP
.****                         PTF/LEVEL INSERTED
         DC    C' ',C'&HJC' .       DEVELOPMENT OR PTF LEVEL
         DS    0H
         AIF   ('&LABEL' EQ '').NOLBL
         SPACE 1
&LABEL   DS    0H
.NOLBL   ANOP
         MEND
*                                      END OF ASSEMBLER PORTION      */
*PLSHJN:;
*DCL PARM CHAR;
* DCL INVOC    CHAR;                /* INVOCATION STRING             */
*   INVOC=MACNAME××MACLIST××' '××MACKEYS; /* SET INVOCATION STRING   */
*   PARM=MACLIST(1);
*   IF DOEND^='' THEN               /* IF DO-END PAIR TO BE CODED    */
*     ANS('DO;'××COMMENT(INVOC)) COL(5);/* SUPPLY DO STATEMENT      */
*ANS('GEN NOREFS NOSETS;') COL(2);
*ANS('PARM') COL(1);
*ANS('BTMHJN AFTERHJN,HJN') COL(10);
*ANS('MODULE IDENTIFICATION') COL(40);
*ANS('@ENDGEN') COL(2);
*   IF DOEND^='' THEN               /* IF DO-END PAIR TO BE CODED    */
*     ANS('END;') COL(5);           /* SUPPLY END STATEMENT          */
*ANS('@SPACE;') COL(5);
*%END BTMHJN;
