         MACRO
&NAME    IEDHALT &MASK,&CONNECT=,&CHARS=,&FLUSH=NO             @Y17XAMG
.*A-000000-999999                                              @X31X8M0
.* CHANGE ACTIVITY AS FOLLOWS:
.*A014000,742000                                               @Z30X8MJ
.*C063000,672000                                               @Z30X8MJ
.*A175000,322000,350000,420000                                 @Y17XAMG
.*C049000                                                      @Y17XAMG
         GBLA  &IEDQZD,&IEDQZK,&IEDQZE(64)
         GBLB  &IEDQZA(64)
         LCLA  &LEN
         LCLB  &NOGO,&IN,&CH,&O
         LCLB  &CON,&BIT,&PRMFL,&PRMST,&PRMOP                  @Y17XAMG
         LCLC  &TEMP,&C
.*             VERSION DATED AUGUST 6, 1974
         SPACE
&C       SETC  '&SYSECT'
&LEN     SETA  4                                               @Y17XAMG
         AIF   (&IEDQZA(46)).SEQ1
.ERR1    ANOP
         MNOTE 12,'***  IHB070  SEQUENCE ERROR-MUST FOLLOW STARTMH MACR*
               O'
&NOGO    SETB  1
.SEQ1    ANOP
         AIF   (&IEDQZA(3) OR &IEDQZA(5)).SEQ2
.ERR2    ANOP
         MNOTE 12,'***  IHB070  SEQUENCE ERROR-MUST FOLLOW INHDR,INMSG,*
                OR OUTMSG MACRO'
         AGO   .MEND
.SEQ2    ANOP
         AIF   (&IEDQZA(3) AND NOT &IEDQZA(1)).ERR2
         AIF   ('&FLUSH' EQ '').FLOK                           @Y17XAMG
         AIF   ('&FLUSH' NE 'YES').FLNO                        @Y17XAMG
&PRMFL   SETB  1  SET FLUSH FLAG                               @Y17XAMG
         AGO   .FLOK                                           @Y17XAMG
.FLNO    AIF   ('&FLUSH' EQ 'NO').FLOK                         @Y17XAMG
&TEMP    SETC  'FLUSH'                                         @Y17XAMG
         AGO   .ERR3   GO SEND MNOTE AND EXIT                  @Y17XAMG
.FLOK    AIF   (&IEDQZA(5)).MSG                                @Y17XAMG
.HDR     ANOP
         AIF   ('&MASK' NE '' OR '&CONNECT' NE '').ERR4
         AIF   ('&CHARS' EQ '').UNCOND
&TEMP    SETC  'CHARS'
         IEDQCHAR 8,&CHARS
         AIF   (NOT &IEDQZA(24)).CHARS
.ERR3    ANOP
         MNOTE 12,'*** IHB300 &TEMP    OPERAND INVALID AS SPECIFIED'
         AGO   .MEND
.ERR4    ANOP
         MNOTE 12,'***  IHB066  INCONSISTENT OPERANDS'
         AGO   .MEND
.CHARS   ANOP
         AIF   ('&CHARS'(K'&CHARS,1) NE '''').OPT
         AIF   ('&CHARS'(1,1) NE 'C' AND '&CHARS'(1,1) NE 'X').ERR3
         AIF   ('&CHARS'(2,1) EQ '''').CHARSOK
         AIF   ('&CHARS'(2,1) NE 'L' OR  '&CHARS'(4,1) NE '''').ERR3
.CHARSOK ANOP
&CH      SETB   1
&LEN     SETA  &LEN+4                                          @Y17XAMG
&PRMST   SETB  1 INDICATE CHARACTER STRING ADDRESS IN PARMLIST @Y17XAMG
         AGO   .UNCOND
.OPT     ANOP
&O       SETB  1
&LEN     SETA  &LEN+2
&PRMOP   SETB  1 INDICATE CHARACTER STRING IN OPTION FIELD     @Y17XAMG
         CNOP  2,4                                             @Y17XAMG
         AGO   .SEQ3
.UNCOND  ANOP
         CNOP  0,4                                             @Y17XAMG
.SEQ3    ANOP
&NAME    DS    0H
         IEDQVCON 61,IEDIAH
         BAL   1,*+&LEN+4
         DC    AL1(&IEDQZE(61))
         DC    AL1(&LEN)
         DC    BL1'&PRMFL&PRMST&PRMOP.00000' OPTION BYTE       @Y17XAMG
         DC    XL1'0'                                          @Y17XAMG
         AIF   (NOT &O).NOTO
         IEDQVCON 31,IEDQAE
         DC    AL1(&IEDQZE(31))
         DC    AL1(&CHARS)
         AGO   .NOTCH
.NOTO    ANOP
         AIF   (NOT &CH).NOTCH
         DC    AL1(&IEDQZD)
         DC    AL3(IEDQMISC+&IEDQZK)
IEDQMISC CSECT
         DC    &CHARS
&C       CSECT
&IEDQZK  SETA  &IEDQZK+&IEDQZD
.NOTCH   ANOP
         L     15,IEDUI
         BALR  14,15
         AGO   .MEND
.MSG     ANOP
         AIF   ('&CHARS' NE '').ERR4
         AIF   ('&MASK' EQ '').MSKOK
.MSK1    ANOP
         IEDQMASK &MASK,5
         AIF   (&IEDQZA(24)).ERR5
         AIF   ('&MASK'(1,1) NE 'X').MSKOK
         AIF   ('&MASK'(2,1) EQ 'L' AND '&MASK'(3,1) NE '5').ERR5
         AIF   ('&MASK'(2,1) EQ 'L' AND K'&MASK GT 5 AND K'&MASK LT 16)-
               .MSKOK
         AIF   ('&MASK'(2,1) EQ '''' AND K'&MASK EQ 13).MSKOK
.ERR5    ANOP
         MNOTE 12,'***  IHB300  MASK OPERAND INVALID AS SPECIFIED'
&NOGO    SETB  1                                               @Z30X8MJ
.MSKOK   ANOP
&CON     SETB  1
         AIF   ('&CONNECT' EQ '' OR '&CONNECT' EQ 'AND' OR '&CONNECT'  *
               EQ 'OR').CON1
         MNOTE 12,'***  IHB300  CONNECT OPERAND INVALID AS SPECIFIED'
         AGO   .MEND
.CON1    ANOP
         AIF   (&NOGO).MEND
         AIF   ('&CONNECT' EQ 'AND').CON2
&CON     SETB  0
.CON2    ANOP
         IEDQVCON 61,IEDIAH
         AIF   ('&MASK' NE '0' AND '&MASK' NE 'X''0000000000''' AND '&M*
               ASK' NE 'XL5''0000000000''' AND '&MASK' NE '').GEN1
&BIT     SETB  1
.GEN1    ANOP
         DC    AL1(&IEDQZE(61)+&BIT)
         DC    AL1(8+&CON)
         DC    BL1'&PRMFL&PRMST&PRMOP.00000' OPTION BYTE       @Y17XAMG
         AIF   (&BIT).GEN2
         AIF   ('&MASK'(1,1) EQ 'X').GEN3
         DC    FL5'&MASK' .             ERROR MASK
         AGO   .MEND
.GEN2    ANOP
         DC    XL5'00' .                ERROR MASK
         AGO   .MEND
.GEN3    ANOP
         DC    &MASK .                  ERROR MASK
.MEND    ANOP
         SPACE
         MEND
