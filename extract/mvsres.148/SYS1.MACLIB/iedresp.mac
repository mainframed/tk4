         MACRO
&NAME    IEDRESP &MASK,&CONNECT=OR,&SENSE=,&CLEAR=YES,&UPDATE=ADD,&COUNX
               T=,&BLANK=,&OVERIDE=NO,&HOLD=NO,&SNAVALS=
.*A000000-999999
.*C150030                                                      @OY16455
.*A120030,420030,422530,440030                                 @OZ41631
.*C020030,130030,150030,170030,320030-340030,390030            @OZ41631
.*C080030-090030                                               @OZ41631
         GBLA  &IEDQZE(64),&IEDQZL(256)
         GBLB  &IEDQZA(64),&IEDNO
         GBLC  &IEDQSV(8)
         LCLA  &FLG,&OPT,&LEN
         LCLA  &OPS
         LCLC  &OP,&OPSN
         LCLB  &X,&Y,&VALOP
         SPACE
         IEDQVCON 65,IEDQRS                                    @YM07693
&OP      SETC  '0'                  ASSUME OPFIELD NOT CODED
&IEDNO   SETB  0                    CLEAR NOGO SWITCH
&FLG     SETA  &FLG+&IEDQZA(4)      SET FLAG IF OUTHDR         @OZ41631
        AIF    (&IEDQZA(1) OR &IEDQZA(4)).CKHO  INMH OR OUTHDR @OZ41631
         MNOTE 12,'*** IHB070  SEQUENCE ERROR - MUST FOLLOW INMSG,INHDRX
               ,INBUF OR OUTHDR MACRO'                         @OZ41631
&IEDNO   SETB  1                    SET NOGO
.CKHO    AIF   ('&HOLD' NE 'YES').CKY  CHECK IF YES            @OZ41631
&FLG     SETA  &FLG+2                  INDICATE HOLD=YES       @OZ41631
         AIF   (&IEDQZA(5) OR  &IEDQZA(4)).REHL IF INMSG/OUTHDR@OZ41631
         AIF   ('&CONNECT' EQ 'OR' AND '&SENSE' EQ '' AND '&OVERIDE' EQX
                'NO').SBOK                                     @OZ41631
         MNOTE 4,'*** IHB070 EXTRANEOUS OPERANDS IGNORED'      @OZ41631
         AGO   .OK1                  BYPASS OTHER CHECKS       @OZ41631
.REHL    ANOP                                                  @OZ41631
&FLG     SETA  &FLG-2                  INDICATE HOLD=NO        @OZ41631
.CKY     AIF   ('&HOLD' EQ 'NO').SBOK IF HOLD=NO               @OZ41631
         MNOTE 4,'*** IHB303 INVALID OPERAND HOLD=&HOLD, NO ASSUMED'
.SBOK    AIF   ('&SENSE' EQ '').NOSEN  IF SENSE OMITTED        @OZ41631
&FLG     SETA  &FLG+32              SETUP FLAG FOR SENSE=
&X       SETB  1                    INDICATE SENSE= CODED
         AIF   ('&SENSE'(K'&SENSE,1) NE '''').CKRG    HEX NOTATION
     AIF   (K'&SENSE EQ 13 AND '&SENSE'(1,4) EQ 'XL4''').XSENS @YM07693
     AIF   (K'&SENSE EQ 11 AND '&SENSE'(1,2) EQ 'X''').XSENS   @YM07693
         MNOTE 12,'*** IHB300 SENSE OPERAND INVALID AS SPECIFIED'
&IEDNO   SETB  1                    SET NOGO
.CKRG    AIF   ('&SENSE'(1,1) EQ '(').RGSENS  REGISTER NOTATION
         IEDQVCON 31,IEDQAE                                    @YM07693
&OPT     SETA  &IEDQZE(31)          SAVE INDEX TO IEDQAE
&OP      SETC  '&SENSE'             SAVE OPFIOELD NAME
         AGO   .XSENS
.RGSENS  ANOP
&Y       SETB  1                    INDICATE SENSE=(REGISTER)
         AIF   (NOT &IEDQZA(5)).XSENS   NOT IN INMSG           @OZ41631
         MNOTE 12,'*** IED904 SENSE= (REG) SHOULD ONLY BE USED IN THE  *
               INBUF, INHDR OR OUTHDR SUBGROUP'                @OZ41631
&IEDNO   SETB  1                    SET NOGO
.XSENS   ANOP
&LEN     SETA  &LEN+4               ALLOW FOR SENSE=
         AGO   .NOOK
.NOSEN   AIF   (&IEDQZA(5) OR '&HOLD' EQ 'YES' OR &IEDQZA(4)).NOOK
         MNOTE 12,'*** IHB002 SENSE OPERAND REQ''D, NOT SPECIFIED'
&IEDNO   SETB  1                    SET NOGO
.NOOK    ANOP
         AIF   ('&OVERIDE' EQ 'NO').OK1                        @OZ41631
&FLG     SETA  64+&FLG              SET OVERRIDE FLAG          @OZ41631
         AIF   ('&OVERIDE' EQ 'YES').OK1                       @OZ41631
   MNOTE 4,'*** OVERIDE=&OVERIDE IS INVALID,  YES ASSUMED ***' @OZ41631
.OK1     ANOP                                                  @OZ41631
         AIF   (&IEDQZA(1) AND '&SNAVALS' NE '').BADVALS       @OZ41631
         AIF   (&IEDQZA(4) AND '&SNAVALS' EQ '').BADVALS       @OZ41631
&VALOP   SETB  1
         IEDQVCON 31,IEDQAE                                    @OZ41631
&OPS     SETA  &IEDQZE(31)                                     @OZ41631
&OPSN    SETC  '&SNAVALS'                                      @OZ41631
         AGO   .SNACON                                         @OZ41631
.BADVALS ANOP                                                  @OZ41631
         MNOTE 12,'*** IHB300 SNAVALS OPERAND INVALID AS SPECIFIED'
&IEDNO   SETB  1                                               @OZ41631
.SNACON  ANOP                                                  @OZ41631
         AIF   (NOT &IEDQZA(4)).REGGEN                         @OZ41631
&NAME    IEDQJN &MASK,&CONNECT,&CLEAR,&UPDATE,,&COUNT,,&BLANK,R,PASS=(&X
               IEDQZL(1),&OPT,&OP,&FLG,&X,&OPS,&OPSN,,&VALOP)
         AGO   .BYJN                                           @OZ41631
.REGGEN  ANOP                                                  @OZ41631
&NAME    IEDQJN &MASK,&CONNECT,&CLEAR,&UPDATE,,&COUNT,,&BLANK,R,PASS=(&X
               IEDQZL(1),&OPT,&OP,&FLG,&X)                     @YM08558
.BYJN    ANOP                                                  @OZ41631
         AIF   (&IEDNO).MEXIT       IF NOGO SET
         AIF   (NOT &X).GEN         IF SENSE OMITTED
         AIF   ('&SENSE'(K'&SENSE,1) NE '''').GNA0  IF SENSE NOT HEX
         DC    &SENSE
         DS    0H                                              @YM07693
         AGO   .GEN
.GNA0    DC    AL4(0)                                          @YM07693
         AIF   (NOT &Y).GEN         IF SENSE NOT REG NOTATION
         MVC   *-4(4),0(&SENSE(1))
.GEN     AIF   (&IEDQZA(5)).MEXIT   IF IN/OUTMSG SUBGROUPS
         L     15,IEDUI
         BALR  14,15
         AIF   ('&IEDQSV(6)' EQ '').MEXIT  IF CONCHARS NOT USED
&IEDQSV(6)  EQU *
.MEXIT   SPACE
         MEND
