         MACRO
&NAM     TSOMCP &OPTIONS=,&TRACE=0,&LNUNITS=,&UNITSIZ=60,              X
               &DTRACE=0,&OLTEST=0,                                    X
               &PLCBNO=,&BTRACE=,&IPLTXID=,&AUTOIPL=,          @ZM46739X
               &SIBCNT=,&SUBAREA=,&MAXSUBA=,&TTRACE=,          @ZM46739X
               &IEDHALT=,                                      @Y17XAYPX
               &DMPNAME=,&AUTODMP=,&THRESH=(255,1,1,1),        @OY18934X
               &INHDR=,&INBUF=,&INMSG=,&OUTHDR=,&OUTBUF=,      @OY18934X
               &OUTMSG=,&LOGSCAN=12                            @OY18934
.**********************************************************************
.*                                                                    *
.*       MACRO NAME = TSOMCP                                          *
.*                                                                    *
.*       DESCRIPTIVE NAME = TCAM/TSO SOURCE GENERATOR                 *
.*                                                                    *
.*       COPYRIGHT = 'NONE'                                           *
.*                                                                    *
.*       STATUS: CHANGE LEVEL 10.0                                    *
.*                                                                    *
.* CHANGE ACTIVITY = AS FOLLOWS:
.* C655200                                                      OY03474
.*                                                              @Y06327
.*                                                              @YM2220
.* C426300                                                     @YA07414
.*C653100-653800                                               @YA04034
.*A875700,876400                                               @YA03674
.* A445200                                                     @ZA03733
.* C653100-653800                                              @ZA03733
.* D375200,404600-406000                                       @YA10436
.* C849800                                                     @YA11714
.* D875700-876700                                              @YA11714
.* C411200-411220,411260-411300,411380                         @OZ15632
.* C345800,393400                                              @OZ16520
.* D410940-411460                                              @OZ16520
.* C338800                                                     @OZ16602
.*A189700                                                      @OY16449
.*C076300,198100-200280,316000-316100,340900,343000-343700     @OY16449
.*C631065,631079                                               @OY16449
.*A630000                                                      @OY18058
.*C625800                                                      @OY18058
.*A067900,247800,250600,334600                                 @OY19258
.*C198800-199600                                               @OY19259
.* A078400,849100,874360                                       @OZ27297
.* C802200                                                     @OY19814
.*C003500,183400,186200,340200-343700,877300,881300            @OY18934
.*A079800,193200                                               @OY18934
.*A630907                                                      @OY20324
.*C315000-315700                                               @OY19429
.*A107100                                                      @OY20431
.*C318500-320600                                               @OY20431
.*C200680,204400                                               @OY20379
.*A102900,209700                                               @OY20834
.*C216640-216660,216711-216712,217000                          @OY20834
.*C630957-630958,631223                                        @OZ33108
.*A033600                                                      @OZ34694
.*C802200                                                      @OZ34694
.*A102200                                                      @OY21130
.*C216680-216707,216711,217000                                 @OY21130
.*D103200                                                      @OY21130
.*C145600,148400                                               @OZ45570
.*C861700                                                      @OZ48953
.*D392700-393400,352100,368200                                 @OZ50273
.*C356300                                                      @OZ50273
.* C148400                                                     @OZ58371
.*
.*
.*FUNCTION -- THIS MACRO USES THE VALUES PLACED IN GLOBAL VARIABLES
.*   BY PREVIOUS LINEGRP AND LIST MACROS AND THE VALUES SPECIFIED
.*   IN ITS OWN MACRO PARAMETERS TO GENERATE THE TCAM SOURCE
.*   STATEMENTS WHICH CONSTITUTE A MESSAGE CONTROL PROGRAM.
.*
.*ENTRY POINTS -- NOT APPLICABLE
.*
.*INPUT -- VALUES PLACED IN GLOBAL VARIABLES BY PREVIOUS MACRO CALLS
.*   AND MACRO CALL PARAMETER VALUES SPECIFIED BY THE USER.
.*
.*OUTPUT -- TCAM MACRO STATEMENTS.  MNOTES IF ERRORS ARE DETECTED.
.*
.*EXTERNAL REFERENCES -- NOT APPLICABLE
.*
.*EXITS, NORMAL -- NORMAL EXIT OCCURS AFTER A COMPLETE SOURCE MESSAGE
.*   CONTROL PROGRAM HAS BEEN GENERATED.
.*
.*EXITS, ERROR -- IF PREVIOUS MACROS ENCOUNTERED SERIOUS
.*   ERRORS (QUITSW = 1) A MNOTE WITH SEVERITY 16 IS ISSUED
.*   AND GENERATION IS BYPASSED.
.*
.*TABLES/WORK AREAS -- SEE DESCRIPTIONS ACCOMPANYING VARIABLE
.*   DECLARATIONS.
.*
.*ATTRIBUTES -- NOT APPLICABLE
.*
.*CHARACTER CODE DEPENDENCY -- NONE
.*
.*NOTES -- NONE
.*
.*
.*       GLOBAL VARIABLES
.*
         GBLB  &T3705              3705 LINE GROUP               Y06327
         GBLB  &T270X              NON-3705 LINE GROUP           Y06327
         GBLA  &RNNUMB             COUNT OF 3705'S               Y06327
         GBLA  &RNLINES            NO. OF 3705 LINES             Y06327
         GBLA  &RNTERMS            NO. OF 3705 TERMINALS         Y06327
         GBLA  &LUTERMS            NO. NCP TERM-LU             @Y17XAYP
         GBLA  &NLG                NUMBER OF LINEGRP MACROS ISSUED
         GBLA  &NONLU                   NON SNA OR 327C TERMS  @OZ34694
         GBLB  &MCPSW              INDICATES TSOMCP MACRO ISSUED
         GBLB  &QUITSW             ON MEANS SERIOUS ERROR OCCURRED
         GBLB  &BISYNC             BISYNCHRONOUS LINE
         GBLB  &SNA                SNA RESOURCE INCLUDED       @Y17XAYP
         GBLB  &FSCR               FULL SCREEN REQ FLAG          Y06327
         GBLB  &USSB(2500)         USS CHARACTER SETSWITCHES   @Y17XAYP
         GBLB  &SCRALL(2500)            FULL SCREEN READ SWITCHESY06327
         GBLB  &LUSB(2500)         LU/LU SESSION SWITCHES      @Y17XAYP
         GBLA  &DDCT                    COUNT OF UNIQUE DDNAMES  Y06327
         GBLA  &ADRPT              ADCHAR ARRAY POINTER
         GBLA  &SCRPT              USED AS POINTER TO ARRAYS WITH
.*                                 ONE ENTRY PER LINE
         GBLA  &NFM(63)            CONTAINS NCPFM= PARAMETER   @YM07290
         GBLA  &NTS(63)            CONTAINS NCPTS= PARAMETER   @YM07290
         GBLA  &PFM(63)            CONTAINS PUFM= PARAMETER    @YM07290
         GBLA  &PTS(63)            CONTAINS PUTS= PARAMETER    @YM07290
         GBLA  &LFM(63)            CONTAINS LUFM= PARAMETER    @YM07290
         GBLA  &LTS(63)            CONTAINS LUTS= PARAMETER    @YM07290
         GBLA  &LUTYP(63)          CONTAINS THE LU TYPE        @YM08503
         GBLA  &LNO(63)            NUMBER OF LINES PER GROUP
.*                                 (ONE ENTRY PER LINEGRP)
         GBLA  &UNO(63)            NO. OF UNITS PER BUFFER
.*                                 (ONE ENTRY PER LINEGRP)
         GBLA  &INT(63)            POLLING INTERVAL
.*                                 (ONE ENTRY PER LINEGRP)
         GBLB  &DIALSW(63)         NETWORK TYPE INDICATORS
.*                                 (ONE ENTRY PER LINEGRP)
         GBLC  &T(63)              TERMINAL TYPE
.*                                 (ONE ENTRY PER LINEGRP)
         GBLC  &DDNME(63)          DDNAMES OF LINE GROUPS
.*                                 (ONE ENTRY PER LINEGRP)
         GBLC  &GRPNAME(63)        GROUP NAME OF 3705 LINE GROUP Y06327
.*                                 (ONE ENTRY PER LINE GROUP)    Y06327
         GBLB  &FETAB(189)         FEATURES PRESENT INDICATORS
.*                                 (THREE ENTRIES PER LINEGRP)
         GBLC  &TTAB(252)          TRANSLATION TABLES
.*                                 (FOUR ENTRIES PER LINEGRP)
         GBLC  &CTAB(252)          CODE SELECTION CHAR SEQUENCES
.*                                 (FOUR ENTRIES PER LINEGRP)
         GBLA  &SCRH(2500)         SCREEN HEIGHT
.*                                 (ONE ENTRY PER LINE)
         GBLA  &SCRW(2500)         SCREEN WIDTH
.*                                 (ONE ENTRY PER LINE)
         GBLA  &TRMNO(2500)        NO. OF TERMINALS PER LINE
.*                                 (ONE ENTRY PER LINE)
         GBLC  &ADCHAR(2500)       ADDRESSING CHARACTERS
.*                                 (ONE ENTRY PER TERMINAL)
         GBLC  &HALT               IEDHALT OPERAND             @Y17XAYP
.*
.*       LOCAL VARIABLES
.*
         LCLA  &PNCHCO             COUNT FOR PUNCH CONTINUE     @YM8503
         LCLA  &BTRCNO             NO. OF BTU TRACE ENTRIES      Y06327
         LCLA  &LC                 3705 TERMINAL LINE COUNTER    Y06327
         LCLA  &RN                 DCB SUFFIX FOR 3705           Y06327
         LCLB  &FLG                FLAG                          Y06327
         LCLC  &DMP                TEMPORARY STORAGE             Y06327
         LCLC  &IPL                TEMPORARY STORAGE             Y06327
         LCLC  &GNAME              GROUP MACRO LABEL             Y06327
         LCLC  &LNAME              3705 TERMINAL LINE NAME       Y06327
         LCLC  &CNAME              CLUSTER OR PUNT NAME        @Y17XAYP
         LCLC  &FEATURE            3705 FEATURE                  Y06327
         LCLC  &BRACK                   BRACKET OPTION         @OY19258
         LCLA  &PLCB               3705 PLCBS                    Y06327
         LCLC  &GTRANS             TRANSTAB FOR GROUP MACRO      Y06327
         LCLC  &OPDT               OPTION FIELD CHARS            Y06327
         LCLC  &OPDT1              OPTION FIELD DATA             Y06327
         LCLC  &OPDT2              OPTION BIND IMAGE NAME      @YM08503
         LCLA  &LNEPT              LINE ARRAY POINTER            Y06327
         LCLC  &AUTOI              AUTOIPL PARAMETER             Y06327
         LCLC  &AUTOD              AUTODMP PARAMETER             Y06327
         LCLA  &SIBCT              SIBCOUNT                    @Y17XAYP
         LCLA  &MAXSA              MAXSUBAREA                  @Y17XAYP
         LCLA  &SUBA               SUBAREA                     @Y17XAYP
         LCLC  &TRM                TERM TYPE                   @Y17XAYP
         LCLA  &OLTST              NO. OF ON-LINE TEST PROCEDURES
         LCLC  &CRSREF             INDICATES CROSSRF OPTION DESIRED
         LCLB  &PROMPT             ON MEANS PROMPT USER FOR TCAM PARMS
         LCLA  &TRCNO              NO. OF TRACE TABLE ENTRIES WANTED
         LCLA  &DTRCN1             NO. OF DISPATCHER TRACE ENT.@OY16449
         LCLC  &DTRCN2             ON, OFF, POST SWITCH        @OY16449
         LCLA  &LNUNNO             NO. OF BASIC UNITS IN BUFFER POOL
         LCLA  &UNSIZ              SIZE OF TCAM BASIC UNIT
         LCLB  &CPIDSW             ON MEANS CPU.ID. CONSTANT NEEDED
         LCLB  &CPID1              ON MEANS CPU ID  EVEN PARITY@OZ27297
         LCLB  &CPID2              ON MEANS CPU ID ODD PARITY  @OZ27297
         LCLA  &ADDON              LNUNITS ADDON VALUE FOR 3270  Y06327
.*                                 TERMINALS                     Y06327
         LCLA  &DISPLAY            LARGEST 3270 SCREEN SIZE      Y06327
.*                                 PER LINE                      Y06327
         LCLC  &C                  COMMA                         Y06327
         LCLC  &D                  PERIOD                        Y06327
         LCLA  &TH,&THS1,&THS2,&THS3,&THS4                     @OY18934
         LCLA  &E                  WORK VARIABLE               @Y17XAYP
         LCLA  &G                  WORK VARIABLE               @Y17XAYP
         LCLA  &H                  WORK VARIABLE               @Y17XAYP
         LCLA  &I                  WORK VARIABLE
         LCLA  &J                  WORK VARIABLE
         LCLA  &K                  WORK VARIABLE
         LCLA  &L                  WORK VARIABLE
         LCLA  &M                  WORK VARIABLE
         LCLA  &N                  WORK VARIABLE
         LCLA  &MM                 WORK VARIABLE
         LCLA  &Q                  WORK VARIABLE
         LCLC  &LUACT              LU/LU ACTIVATE OPERAND      @Y17XAYP
         LCLC  &X                  WORK VARIABLE
         LCLC  &Y                  WORK VARIABLE
         LCLA  &Z                  WORK VARIABLE
         LCLC  &EOT                CONTAINS EOT CHARACTER
         LCLC  &TBL(32)            3270 POLLING/ADDRESSING CHARACTERS
.*                                 LOOKUP TABLE. THE TABLE HAS 32
.*                                 ENTRIES. EACH ENTRY HAS 4 ELEMENTS.
.*                                 EACH ELEMENT HAS TWO CHARACTERS.
.*                                 1. POLLING CHARACTERS (EBCDIC)
.*                                 2. CORRESSPONDING ADDRESSING CHARS.
.*                                 3. POLLING CHARACTERS (ASCII)
.*                                 4. CORRESPONDING ADDRESSING CHARS.
         LCLC  &QBYP               CONTAINS QBY PARAMETER
         LCLC  &TYP                INTRO ENVIRON TYPE          @Y17XAYP
         LCLC  &L2741(4)           2741 TRANSLATE TABLE ARRAY
         LCLC  &L1050(4)           1050 TRANSLATE TABLE ARRAY
         LCLA  &N2741              2741 TRANSLATE TABLE ARRAY COUNTER
         LCLA  &N1050              1050 TRANSLATE TABLE ARRAY COUNTER
         LCLC  &FEAT(3)            CONTAINS FEATURE DESCRIPTORS
         LCLC  &TNAME              TEMP HOLD AREA FOR CHAR EXPRESSION
         LCLA  &TN                 COUNTER OF TERMINAL/LINE MACRO NAMES
         LCLA  &IN                 COUNTER FOR INVLIST MACRO NAMES
         LCLC  &CM(3)              SUBLIST SEPARATORS, COMMAS OR NULLS
         LCLC  &CS(36)             WILL CONTAIN ALPHAMERIC CHAR SET
.*                                 (26 ALPHABETIC, 10 NUMERIC)
         LCLA  &SCSPT                   SCREEN SIZE ARRAY PTR  @OY21130
         LCLC  &CP(3)              CONTINUATION POSITIONERS -- THESE
.*                                 ARE SET TO EITHER '' OR '     '.
.*                                 THEIR FUNCTION IS TO TAKE UP THE
.*                                 RIGHT AMOUNT OF SPACE IN THE PUNCHED
.*                                 SOURCE STATEMENTS SUCH THAT THERE
.*                                 WILL BE A CONTINUATION CHAR IN
.*                                 COLUMN 72.
         LCLA  &TOTERMS            TOTAL OF 3705 TERMINALS     @OY20431
.*                                 PLUS 3705 LU'S              @OY20431
.*
         ACTR  (&NLG+6)*4096       ADJUST ACTR                   Y06327
.*
.*       INITIALIZE ALPHAMERIC CHARACTER SET ARRAY
.*
&CS(1)   SETC  'A'
&CS(2)   SETC  'B'
&CS(3)   SETC  'C'
&CS(4)   SETC  'D'
&CS(5)   SETC  'E'
&CS(6)   SETC  'F'
&CS(7)   SETC  'G'
&CS(8)   SETC  'H'
&CS(9)   SETC  'I'
&CS(10)  SETC  'J'
&CS(11)  SETC  'K'
&CS(12)  SETC  'L'
&CS(13)  SETC  'M'
&CS(14)  SETC  'N'
&CS(15)  SETC  'O'
&CS(16)  SETC  'P'
&CS(17)  SETC  'Q'
&CS(18)  SETC  'R'
&CS(19)  SETC  'S'
&CS(20)  SETC  'T'
&CS(21)  SETC  'U'
&CS(22)  SETC  'V'
&CS(23)  SETC  'W'
&CS(24)  SETC  'X'
&CS(25)  SETC  'Y'
&CS(26)  SETC  'Z'
&CS(27)  SETC  '0'
&CS(28)  SETC  '1'
&CS(29)  SETC  '2'
&CS(30)  SETC  '3'
&CS(31)  SETC  '4'
&CS(32)  SETC  '5'
&CS(33)  SETC  '6'
&CS(34)  SETC  '7'
&CS(35)  SETC  '8'
&CS(36)  SETC  '9'
.*
.*       INITIALIZE 3270 POLLING/ADDRESSING CHARACTER LOOK-UP TABLE
.*
&TBL(1)  SETC  '4060202D'
&TBL(2)  SETC  'C161412F'
&TBL(3)  SETC  'C2E24253'
&TBL(4)  SETC  'C3E34354'
&TBL(5)  SETC  'C4E44455'
&TBL(6)  SETC  'C5E54556'
&TBL(7)  SETC  'C6E64657'
&TBL(8)  SETC  'C7E74758'
&TBL(9)  SETC  'C8E84859'
&TBL(10) SETC  'C9E9495A'
&TBL(11) SETC  '4A6A5B7C'                                      @OZ45570
&TBL(12) SETC  '4B6B2E2C'
&TBL(13) SETC  '4C6C3C25'
&TBL(14) SETC  '4D6D285F'
&TBL(15) SETC  '4E6E2B3E'                                      @OZ58371
&TBL(16) SETC  '4F6F213F'
&TBL(17) SETC  '50F02630'
&TBL(18) SETC  'D1F14A31'
&TBL(19) SETC  'D2F24B32'
&TBL(20) SETC  'D3F34C33'
&TBL(21) SETC  'D4F44D34'
&TBL(22) SETC  'D5F54E35'
&TBL(23) SETC  'D6F64F36'
&TBL(24) SETC  'D7F75037'
&TBL(25) SETC  'D8F85138'
&TBL(26) SETC  'D9F95239'
&TBL(27) SETC  '5A7A5D3A'
&TBL(28) SETC  '5B7B2423'
&TBL(29) SETC  '5C7C2A40'
&TBL(30) SETC  '5D7D2927'
&TBL(31) SETC  '5E7E3B3D'
&TBL(32) SETC  '5F7F5E22'
.*
.*       CHECK FOR MULTIPLE TSOMCP MACRO CALLS
.*
         AIF   (NOT &MCPSW).SETMCP
         MNOTE 16,'IKJ54125I MORE THAN ONE TSOMCP MACRO CALL ISSUED'
         MEXIT
.SETMCP  ANOP
&MCPSW   SETB  1                   INDICATE A TSOMCP MACRO CALL ISSUED
&HALT    SETC  '&IEDHALT'                                      @Y17XAYP
.*
.*       TEST FOR LINEGRP MACRO CALL HAVING BEEN ISSUED
.*
         AIF   (&NLG GT 0).LGISUD
         MNOTE 16,'IKJ54126I NO LINEGRP MACRO ISSUED BEFORE TSOMCP MACRO
               O'
         MEXIT
.LGISUD  ANOP
.*
.*       CHECK PARAMETER VALIDITY
.*
&OLTST   SETA  &OLTEST             NO. OF ON-LINE TEST PROCEDURES
         AIF   (&OLTST EQ 0).CHKXREF
         AIF   (&OLTST LE 255 AND &OLTST GE 10).CHKXREF
         MNOTE 4,'IKJ54110I OLTEST OPERAND OUT OF RANGE, DEFAULT USED'
&OLTST   SETA  0                        SET DEFAULT
.CHKXREF ANOP
&CRSREF  SETC  '0'                 SET DEFAULT
         AIF   ('&OPTIONS' EQ '').CKTHRSH                      @OY18934
&I       SETA  0                   INITIALIZE SUB-OPERAND SELECTOR
.OPLOOP  ANOP
&I       SETA  &I+1                BUMP SUB-OPERAND SELECTOR
         AIF   (&I GT N'&OPTIONS).CKTHRSH                      @OY18934
         AIF   ('&OPTIONS(&I)' EQ 'XREF').SETCR
         AIF   ('&OPTIONS(&I)' EQ 'PROMPT').SETPR
         MNOTE 4,'IKJ54104I &OPTIONS(&I) INVALID FOR OPTIONS OPERAND'
         AGO   .OPLOOP             CONTINUE SCANNING
.SETCR   ANOP
&N       SETA  &ADRPT-1            SET TERMINAL COUNT          @OY16449
&M       SETA  0                   SET LOOP VARIBLE            @OY16449
.XREFLOP ANOP                      LOOP ONCE/LINE IN LAST L.G. @OY16449
&N       SETA  &N+&TRMNO(&SCRPT+&M) UPDATE TERMINAL COUNT      @OY16449
&M       SETA  &M+1                BUMP LOOP CONTROL VARIABLE  @OY16449
         AIF   (&M LT &LNO(&NLG)).XREFLOP BR IF MORE LINES     @OY16449
&CRSREF  SETC  '&N'                SET NO. OF XREF ENTRIES
         AGO   .OPLOOP             CONTINUE SCANNING
.SETPR   ANOP
&PROMPT  SETB  1                   INDICATE PROMPT REQUESTED
         AGO   .OPLOOP             CONTINUE SCANNING
.CKTHRSH ANOP                                                  @OY18934
&TH      SETA  0                                               @OY18934
.TH1     ANOP                                                  @OY18934
&TH      SETA  &TH+1                                           @OY18934
         AIF   (T'&THRESH(&TH) NE 'N').TH2                     @OY18934
         AIF   (&THRESH(&TH) GT 0 AND &THRESH(&TH) LT 256).TH3 @OY18934
.TH2     MNOTE 4,'IKJ54110I THRESH OPERAND OUT OF RANGE, DEFAULT USED'
&THS1    SETA  255                                             @OY18934
&THS2    SETA  1                                               @OY18934
&THS3    SETA  1                                               @OY18934
&THS4    SETA  1                                               @OY18934
         AGO   .CKTR                                           @OY18934
.TH3     AIF   (&TH NE 4).TH1                                  @OY18934
&THS1    SETA  &THRESH(1)                                      @OY18934
&THS2    SETA  &THRESH(2)                                      @OY18934
&THS3    SETA  &THRESH(3)                                      @OY18934
&THS4    SETA  &THRESH(4)                                      @OY18934
.CKTR    ANOP
&TRCNO   SETA  &TRACE
         AIF   (&TRCNO LE 65535).CKDTR
         MNOTE 4,'IKJ54110I TRACE OPERAND OUT OF RANGE, DEFAULT USED'
&TRCNO   SETA  0
.CKDTR   ANOP
&I       SETA  1                   INITIALIZE SUB-OPERAND SEL  @OY16449
&DTRCN1  SETA  &DTRACE(&I)         SET NO. OF TRACE ENTRIES    @OY16449
         AIF   (&DTRCN1 LE 65535).CKUN                         @OY16449
         MNOTE 4,'IKJ54110I DTRACE OPERAND OUT OF RANGE, DEFAULT USED'
&DTRCN1  SETA  0                                               @OY16449
.CKUN    ANOP                                                  @OY16449
&I       SETA  2                   BUMP TO NEXT OPERAND        @OY16449
         AIF   (&I GT N'&DTRACE).ENDDT  ONLY ONE OPERAND       @OY19259
&DTRCN2  SETC  '(&DTRCN1,&DTRACE(&I))'  SET DTRACE OPERAND     @OY19259
         AGO   .CKUNSIZ                                        @OY19259
.ENDDT   ANOP                                                  @OY19259
&DTRCN2  SETC  '&DTRCN1'                SET DTRACE OPERAND     @OY19259
.CKUNSIZ ANOP                                                  @OY19259
&UNSIZ   SETA  &UNITSIZ
         AIF   (&UNSIZ LT 41).BADUNS
         AIF   (&T3705 EQ 1 AND &UNSIZ LT 44).BADUNS             Y06327
         AIF   (&UNSIZ LE 255).CKLN                              Y06327
.BADUNS  MNOTE 4,'IKJ54110I UNITSIZ OPERAND OUT OF RANGE,DEFAULT USED'
&UNSIZ   SETA  60                  SET DEFAULT BASIC UNIT SIZE
.CKLN    AIF   ('&LNUNITS' EQ '').LNUDEF  CHK IF DEFAULT WANTED@Y17XAYP
&LNUNNO  SETA  &LNUNITS
         AIF   (&LNUNNO GT 65535).BADLNU
         AIF   (&LNUNNO GT 0).GOTEST                             Y06327
.BADLNU  MNOTE 4,'IKJ54110I LNUNITS OPERAND OUT OF RANGE,DEFAULT USED'
.LNUDEF  AIF   (&QUITSW).GOTEST    IF QUITSW SET, BYPASS         Y06327
.*
.*       THE FOLLOWING ROUTINE COMPUTES THE DEFAULT VALUE FOR LNUNITS
.*       USING THE PARAMETERS SUPPLIED IN PREVIOUS LINEGRP MACRO CALLS
.*
&I       SETA  1                   SET LOOP INDEX
&LNUNNO  SETA  0
&LNEPT   SETA  1                   INITIALIZE LINE ARRAY POINTER Y06327
&SCRPT   SETA  1                   INITIALIZE SCREEN ARRAY PTR @Y17XAYP
&SCSPT   SETA  1                                               @OY20834
.LNULOOP AIF   (&I GT &NLG).TRUNC  BR IF ALL LINE GROUPS       @Y17XAYP
&K       SETA  20                  ASSUME 2 BUFFERS/TERMINAL
&ADDON   SETA  0                   ADDON VALUE INITIALIZED       Y06327
&N       SETA  0                   INITIALIZE TERMINAL COUNT     Y06327
         AIF   ('&T(&I)' EQ 'PUNT').M25 2.5 MULTIPLIER         @Y17XAYP
         AIF   ('&T(&I)'(1,3) EQ '327').M25                    @Y17XAYP
         AIF   ('&T(&I)'(1,3) NE '226').LNULOP2
.M25     ANOP                                                  @Y17XAYP
&K       SETA  25                  ASSUME 2.5 BFRS/TERM FOR 2260/65
.*                                 AND FOR 3270
.LNULOP2 ANOP
.*
.*       THE FOLLOWING ROUTINE COMPUTES AN ADDON VALUE PER LINE, TO
.*       LNUNITS, FOR 3270 DISPLAY TERMINALS. THIS SHOULD PROVIDE
.*       SUFFICIENT LNUNITS FOR FULL SCREEN OPERATIONS.
.*
&DISPLAY SETA  1                                                 Y06327
&Q       SETA  0                   LUNO COUNTER                @Y17XAYP
&L       SETA  0                   LU COUNT                    @Y17XAYP
&E       SETA  0                   LUNO ENTRY SELECTOR         @Y17XAYP
&J       SETA  0                   TERMNO SELECTOR             @Y17XAYP
.TRMLOOP ANOP                                                  @Y17XAYP
&M       SETA  &TRMNO(&SCRPT+&E+&J) GET TERMNO VALUE           @Y17XAYP
         AIF   ('&T(&I)' EQ 'PUNT').CKSCR                      @Y17XAYP
         AIF   ('&T(&I)'(1,3) NE '327').LNUA BR NOT 3270         Y06327
.CKSCR   AIF   (((&SCRH(&SCSPT+&L))*(&SCRW(&SCSPT+&L))) LE &DISPLAY).LNX
               UA                                              @OY20834
&DISPLAY SETA  &SCRH(&SCSPT+&L)*&SCRW(&SCSPT+&L) LARGEST SCREEN@OY20834
.LNUA    ANOP                                                    Y06327
&L       SETA  &L+1                COUNT LU ENTRY              @Y17XAYP
         AIF   (&L LT &TRMNO(&SCRPT+&E+&J+1)).CKSCR            @Y17XAYP
.*                                 MORE LU ENTRIES/LUNO        @Y17XAYP
&L       SETA  0                   CLEAR LU ENTRY COUNT        @Y17XAYP
&E       SETA  &E+1                INCREMENT LUNO SELECTOR     @Y17XAYP
&Q       SETA  &Q+1                INCREMENT LUNO COUNTER      @Y17XAYP
&N       SETA  &N+1                CUMULATIVE LUCOUNT          @Y17XAYP
         AIF   (&Q LT &M).CKSCR    MORE LUNO ENTRIES/TERMNO    @Y17XAYP
&J       SETA  &J+1                INCREMENT TERMNO SELECTOR   @Y17XAYP
&Q       SETA  0                   ZERO LUNO COUNTER           @Y17XAYP
         AIF   (&J LT &LNO(&I)).TRMLOOP    MORE TERMNO/LINEGRP @Y17XAYP
&SCSPT   SETA  &SCSPT+&J                SET ARRAY PTR          @OY21130
&SCRPT   SETA  &SCRPT+&E+&J                                    @OY20834
         AIF   ('&T(&I)' EQ 'PUNT').ADIT USE ADDON VALUE       @OY20834
         AIF   ('&T(&I)'(1,3) NE '327').LNULOP3 BR NOT 3270    @Y17XAYP
.ADIT    ANOP                                                  @Y17XAYP
&ADDON   SETA  &ADDON+((&DISPLAY+48)*10)/(&UNSIZ*&UNO(&I))       Y06327
.*                                 48 = PREFIX(MIN 41,44) + DBLWD ALIGN
.LNULOP3 ANOP                                                    Y06327
&LNUNNO  SETA  &LNUNNO+&ADDON*&J+&K*&N*&UNO(&I)                @OY21130
&I       SETA  &I+1
         AGO   .LNULOOP
.TRUNC   ANOP
&LNUNNO  SETA  (&LNUNNO+5)/10      ROUND & TRUNCATE
.GOTEST  ANOP
.*
.*       TEST FOR SERIOUS ERRORS IN PREVIOUS MACROS
.*
.TSTQUIT AIF   (NOT &QUITSW).INTRO
         MNOTE 16,'IKJ54127I SERIOUS ERROR IN PREVIOUS MACRO CALL PREVEN
               NTS FURTHER GENERATION'
         MEXIT
.*
.*********************************************************************
.*       INTRO
.*********************************************************************
.*
.INTRO   AIF   ('&NAM' EQ '').NERR      NAME REQUIRED          @Y17XAYP
         AIF   (K'&NAM GT 8 OR N'&NAM GT 1).NERR  NAME VALID     Y06327
&I       SETA  1                   CHARACTER COUNTER             Y06327
&J       SETA  K'&NAM              LENGTH OF NAME                Y06327
.NAMEOK  AIF   ('&NAM'(&I,1) EQ '$' OR '&NAM'(&I,1) EQ '#'             X
               OR '&NAM'(&I,1) EQ '@').OKNME                   @Y17XAYP
         AIF   ('&NAM'(&I,1) GE 'A' AND '&NAM'(&I,1) LE 'Z').OKNME
         AIF   ('&NAM'(&I,1) GE '0' AND '&NAM'(&I,1) LE '9'            X
               AND &I NE 1).OKNME                                Y06327
.NERR    MNOTE 16,'IKJ54104I &NAM INVALID FOR NAME OPERAND'      Y06327
         MEXIT                                                   Y06327
.OKNME   ANOP                                                    Y06327
&I       SETA  &I+1                UPDATE CHARACTER COUNTER      Y06327
         AIF   (&I GT 8 OR '&I' GT '&J').INTR1                   Y06327
         AGO   .NAMEOK                                           Y06327
.INTR1   ANOP                                                    Y06327
&FEATURE SETC  'MIXD'              270X/3705 MCP                 Y06327
&BRACK   SETC  'YES'                    BRACKET OPTION         @OY19258
         AIF   (&T3705 EQ 1 AND &T270X EQ 1).INTRO1 BOTH         Y06327
&FEATURE SETC  'ONLY'              3705 MCP                      Y06327
         AIF   (&T270X NE 1 AND &T3705 EQ 1).INTRO1 3705 ONLY    Y06327
&FEATURE SETC  'NO'                270X MCP                      Y06327
&BRACK   SETC  'NO'                     BRACKET OPTION         @OY19258
         AGO   .PLCB               BYPASS IF NOT 3705            Y06327
.*
.**********************************************************************
.*       IPLTXID= KEYWORD ANALYSIS
.**********************************************************************
.*
.INTRO1  AIF   ('&AUTOIPL' EQ '').AUTOI AUTOIPL DEFAULT        @Y17XAYP
         AIF   (N'&AUTOIPL EQ &RNNUMB).AUTOI SUBLIST VALID       Y06327
         MNOTE 16,'IKJ54104I &AUTOIPL INVALID FOR AUTOIPL OPERAND'
&QUITSW  SETB  1                   SET QUIT SWITCH               Y06327
.AUTOI   AIF   ('&IPLTXID' EQ '').DMP   BYPASS IF NOT CODED    @Y17XAYP
         AIF   (N'&IPLTXID EQ &RNNUMB).IPL CORRECT NUMBER OF IPL Y06327
         AGO   .IPLBD              NO. EXIT                      Y06327
.IPL     ANOP                                                    Y06327
&K       SETA  1                   OUTER LOOP COUNTER            Y06327
.IPLGO   ANOP                                                    Y06327
&I       SETA  1                   CHARACTER COUNTER             Y06327
&J       SETA  K'&IPLTXID(&K)      LENGTH OF SUBOPERAND          Y06327
         AIF   (&J EQ 0).IPLSUB    SKIP OMITTED SUBOPERANDS      Y06327
         AIF   (&J GT 7).IPLBD     SUBOPERAND LENGTH MAX 7       Y06327
&IPL     SETC  '&IPLTXID(&K)'      GET PROPER SUBOPERAND         Y06327
.OKI     AIF   ('&IPL'(&I,1) EQ '$' OR '&IPL'(&I,1) EQ '#'             X
               OR '&IPL'(&I,1) EQ '@').OKIPL                   @Y17XAYP
         AIF   ('&IPL'(&I,1) GE 'A' AND '&IPL'(&I,1) LE 'Z').OKIPL
.*                                                               Y06327
         AIF   ('&IPL'(&I,1) GE '0' AND '&IPL'(&I,1) LE '9' AND &I     X
               NE 1).OKIPL                                       Y06327
.IPLBD   MNOTE 16,'IKJ54104I &IPLTXID INVALID FOR IPLTXID OPERAND'
&QUITSW  SETB  1                                                 Y06327
         AGO   .DMP                                              Y06327
.OKIPL   ANOP                                                    Y06327
&I       SETA  &I+1                SET FOR NEXT CHARACTER        Y06327
         AIF   (&I GT 7 OR '&I' GT '&J').IPLSUB                  Y06327
         AGO   .OKI                                              Y06327
.IPLSUB  ANOP                                                    Y06327
&K       SETA  &K+1                UPDATE OUTER LOOP COUNTER     Y06327
         AIF   ('&K' GT '&RNNUMB').DMP                           Y06327
         AGO   .IPLGO                                            Y06327
.*
.**********************************************************************
.*       DMPNAME= KEYWORD ANALYSIS
.**********************************************************************
.*
.DMP     AIF   ('&AUTODMP' EQ '').AUTOD AUTODMP DEFAULT        @Y17XAYP
         AIF   (N'&AUTODMP EQ &RNNUMB).AUTOD SUBLIST VALID       Y06327
         MNOTE 16,'IKJ54104I &AUTODMP INVALID FOR AUTODMP OPERAND'
&QUITSW  SETB  1                   SET QUIT SWITCH               Y06327
.AUTOD   AIF   ('&DMPNAME' EQ '').PLCB  BYPASS IF NOT CODED    @Y17XAYP
         AIF   (N'&DMPNAME EQ &RNNUMB).DMP1 CORRECT NUMBER OF OP Y06327
         AGO   .DMPBD              NO. EXIT                      Y06327
.DMP1    ANOP                                                    Y06327
&K       SETA  1                   OUTER LOOP CONTROL            Y06327
.DMPGO   ANOP                                                    Y06327
&I       SETA  1                   CHARACTER COUNTER             Y06327
&J       SETA  K'&DMPNAME(&K)      LENGTH OF SUBOPERAND          Y06327
         AIF   (&J EQ 0).DMPSUB    SKIP OMITTED SUBOPERAND       Y06327
         AIF   (&J GT 8).DMPBD     MAX SUBOPERAND LENGTH 8       Y06327
&DMP     SETC  '&DMPNAME(&K)'      GET PROPER SUBOPERAND         Y06327
.OKD     AIF   ('&DMP'(&I,1) EQ '$' OR '&DMP'(&I,1) EQ '#'             X
               OR '&DMP'(&I,1) EQ '@').OK                      @Y17XAYP
         AIF   ('&DMP'(&I,1) GE 'A' AND '&DMP'(&I,1) LE 'Z').OK  Y06327
         AIF   ('&DMP'(&I,1) GE '0' AND '&DMP'(&I,1) LE '9'            X
               AND &I NE 1).OK                                   Y06327
.DMPBD   MNOTE 16,'IKJ54104I &DMPNAME INVALID FOR DMPNAME OPERAND'
&QUITSW  SETB  1                                                 Y06327
         AGO   .PLCB                                             Y06327
.OK      ANOP                                                    Y06327
&I       SETA  &I+1                                              Y06327
         AIF   (&I GT 8 OR '&I' GT '&J').DMPSUB                  Y06327
         AGO   .OKD                                              Y06327
.DMPSUB  ANOP                                                    Y06327
&K       SETA  &K+1                                              Y06327
         AIF   ('&K' GT '&RNNUMB').PLCB                          Y06327
         AGO   .DMPGO                                            Y06327
.**********************************************************************
.*       PLCBNO= AND BTRACE= KEYWORDS ANALYSIS
.**********************************************************************
.*
.PLCB    AIF   (NOT &QUITSW).PLCBGO                            @Y17XAYP
         MEXIT                                                   Y06327
.PLCBGO  ANOP                                                    Y06327
 PUNCH '&NAM CSECT'
&TYP     SETC  'TSO'               ASSUME NO SNA RESOURCE      @Y17XAYP
.TSO     ANOP                                                  @Y17XAYP
 PUNCH '         INTRO ENVIRON=&TYP,KEYLEN=&UNSIZ,LNUNITS=&LNUNNO,CIB=2X
               ,  .................'                           @OY19429
 PUNCH '               ABEFMT=(SIB,RVT,DCT,LGB,DCB,DEB,SCB,PLCB,LCB,   X
                  .........'
 PUNCH '               QCB,OPT,TTE),                           @OY16449X
                  ..........'                                  @OY16449
         AIF   (NOT &T3705).NO3705      BYPASS IF NO 3705        Y06327
         AIF   ('&PLCBNO' EQ '').PLCBDEF DEFAULT IF NOT CODED    Y06327
&PLCB    SETA  &PLCBNO             GET NUMBER OF PLCB'S          Y06327
         AIF   ('&PLCB' GE '&RNLINES').PLCBOK                  @OY20431
&TOTERMS SETA  &RNTERMS+&LUTERMS   3705 TERMINAL/LU TOTAL      @OY20431
         AIF   ('&PLCB' LE '&TOTERMS').PLCBOK                  @OY20431
.PLCB2   MNOTE 4,'IKJ60003I PLCBNO MUST BE GE NUMBER OF 3705 LINES OR LX
               E NUMBER OF 3705 TERMINALS -- DEFAULT VALUE USED' Y06327
.*                                                               Y06327
.PLCBDEF ANOP                                                    Y06327
&PLCB    SETA  &RNLINES            DEFAULT PLCBNO                Y06327
.PLCBOK  AIF   ('&BTRACE' NE '' AND '&TTRACE' NE '').UBTRCE    @ZM46739
         AIF   ('&BTRACE' EQ '' AND '&TTRACE' EQ '').USEDEF    @ZM46739
         AIF   ('&BTRACE' EQ '').UTTRCE IF NO BTRACE USE TTRACE@ZM46739
.UBTRCE  ANOP                                                  @ZM46739
&BTRCNO  SETA  &BTRACE                  SET VALUE              @ZM46739
         AIF   (&BTRCNO LE 65535).TBTU  VALUE WITHIN RANGE     @ZM46739
         MNOTE 4,'IKJ54110I BTRACE OPERAND OUT OF RANGE, DEFAULT USED'
         AGO   .USEDEF                                         @ZM46739
.UTTRCE  ANOP                                                  @ZM46739
&BTRCNO  SETA  &TTRACE                  SET VALUE              @ZM46739
         AIF   (&BTRCNO LE 65535).TBTU  VALUE WITHIN RANGE     @ZM46739
         MNOTE 4,'IKJ54110I TTRACE OPERAND OUT OF RANGE, DEFAULT USED'
.*                                                             @ZM46739
.USEDEF  ANOP                                                  @ZM46739
&BTRCNO  SETA  0                        SET FOR DEFAULT        @ZM46739
.TBTU    ANOP                                                    Y06327
 PUNCH '               FEATURE=(,,,,&FEATURE.3705),BTRACE=&BTRCNO,PLCBNX
               O=&PLCB, ...............'                         Y06327
         AGO   .GO3705                                           Y06327
.NO3705  ANOP                                                    Y06327
 PUNCH '               FEATURE=(,,,,NO3705),                           X
                    ......'                                      Y06327
.GO3705  ANOP                                                    Y06327
 PUNCH '               BRACKET=&BRACK,                                 X
               ................'                               @OY19258
&X       SETC  'STSP'              ASSUME START/STOP LINE
         AIF   (NOT &BISYNC).LINE2
&X       SETC  'BOTH'              SET LINETYPE TO BOTH FOR BISYNC LINE
.LINE2   ANOP
 PUNCH '               CROSSRF=&CRSREF,TRACE=&TRCNO,LINETYP=&X,        -
                        .........'                             @OZ16602
         AIF   (NOT &T3705).LINE3 SKIP NCP OPERANDS            @Y17XAYP
         AIF   (T'&SIBCNT NE 'N').BADSIB  VALID SIB SOUNT      @Y17XAYP
         AIF   (&SIBCNT LT 0 OR &SIBCNT GT 65535).BADSIB       @Y17XAYP
&SIBCT   SETA  &SIBCNT             SET SIB COUNT               @Y17XAYP
         AGO   .CKSAL              CK VALID SUBAREA LENGTH     @Y17XAYP
.BADSIB  ANOP                                                  @Y17XAYP
&SIBCT   SETA  0                   SET DEFAULT SIB COUNT       @Y17XAYP
         MNOTE 4,'IKJ54110I SIBCNT OPERAND OUT OF RANGE, DEFAULT USED'
.CKSAL   AIF   (T'&MAXSUBA NE 'N').BADSAL VALID SUBAREA LEN    @Y17XAYP
&MAXSA   SETA  &MAXSUBA            SET MAX SUBAREA VALUE       @YM08080
         AGO   .CKSA               CHECK SUBAREA VALUE         @Y17XAYP
.BADSAL  MNOTE 4,'IKJ54110I MAXSUBA OPERAND OUT OF RANGE,DEFAULT USED'
&MAXSA   SETA  0                   SET DEFAULT MAXSUBAREA      @Y17XAYP
.CKSA    AIF   (T'&SUBAREA NE 'N').BADSA  VALID SUBAREA        @Y17XAYP
&SUBA    SETA  &SUBAREA            SET SUBAREA VALUE           @Y17XAYP
         AGO .LINE2A                                           @Y17XAYP
.BADSA   MNOTE 4,'IKJ54110I SUBAREA OPERAND OUT OF RANGE, DEFAULT USED'
&SUBA    SETA  0                   SET DEFAULT SUBAREA         @Y17XAYP
.LINE2A  ANOP                                                  @Y17XAYP
 PUNCH '               SIBCNT=&SIBCT,SUBAREA=&SUBA,MAXSUBA=&MAXSA,     X
                            .............'                     @Y17XAYP
.LINE3   ANOP                                                  @OY18934
&D       SETC  '....'                                          @OY18934
&L       SETA  K'&DTRACE+K'&OLTEST+K'&NAM                      @OY18934
         AIF   (&L LT 9).SET6                                  @OY18934
         AIF   (&L LT 16).PCHIT                                @OY18934
&D       SETC  '..'                                            @OY18934
         AGO   .PCHIT                                          @OY18934
.SET6    ANOP                                                  @OY18934
&D       SETC  '......'                                        @OY18934
.PCHIT   ANOP                                                  @OY18934
 PUNCH '               DTRACE=&DTRCN2,OLTEST=&OLTST,PROGID=&NAM,       X
                 &D&D&D&D'                                     @OY18934
         AIF   (NOT &PROMPT).NOPRMPT                           @OY18934
 PUNCH '               THRESH=(&THS1,&THS2,&THS3,&THS4)'       @OY18934
         AGO   .OPEN                                           @OY18934
.NOPRMPT ANOP                                                  @OY18934
 PUNCH '               STARTUP=C,THRESH=(&THS1,&THS2,&THS3,&THS4)'
.OPEN    ANOP
 PUNCH '         LTR   15,15'
 PUNCH '         BNZ   IEDAYBYE'    INTRO FAILED               @OZ16520
.*
.*********************************************************************
.*       OPEN'S
.*********************************************************************
.*
 PUNCH '         LA    3,DCBL1'
 PUNCH '         LA    4,DCBL2-DCBL1'
 PUNCH '         LA    5,DCBLAST'
 PUNCH 'OPENLOOP LR    1,3'
 PUNCH '         OPEN  MF=(E,(1))'
 PUNCH '         USING IHADCB,6'
 PUNCH '         L     6,0(,3)'
 PUNCH '         TM    DCBOFLGS,X''10'''
 PUNCH '         BO    OPENNEXT'                               @OZ50273
         AIF   ('&DMPNAME' EQ '').NOOPD  NO DUMP DATASET TO OPEN Y06327
 PUNCH '         TM    DCBDSORG,X''40'''
 PUNCH '         BO    OPNDMP'
.NOOPD   ANOP                                                    Y06327
 PUNCH '         MVC   OPENWTO+34(8),DCBDDNAM'
 PUNCH 'OPENWTO  WTO   ''IKJ403I LINE GROUP FOR DD          NOT OPENED'-
               ',DESC=6, -'
 PUNCH '               ROUTCDE=2'
 PUNCH '         B     OPENNEXT'                                 YM2220
         AIF   ('&DMPNAME' EQ '').NOOPD1 NO DUMPDATASET TO OPEN  Y06327
 PUNCH 'OPNDMP   MVC   OPENWTR+17(8),DCBDDNAM'
 PUNCH 'OPENWTR  WTO   ''IED183I          DUMP DCB SPECIFICATION ERROR'X
               ',DESC=6, -'
 PUNCH '               ROUTCDE=2'
 PUNCH '         B     OPENNEXT'                                 YM2220
.NOOPD1  ANOP                                                    Y06327
 PUNCH 'OPENNEXT BXLE  3,4,OPENLOOP'                             YM2220
 PUNCH '         DROP  6'
 PUNCH '         B     OPENDONE'
&FLG     SETB  0                   CLEAR FLAG                    Y06327
 PUNCH 'DCBL1    DS    0F'
.OPDCB   ANOP                                                    Y06327
&I       SETA  1
.OPNLOOP AIF   (&I EQ &DDCT).OPNLAST                           @Y17XAYP
 PUNCH '         OPEN  (DCB&I,(INOUT)),MF=L'
         AGO   .OPLTST
.OPNLAST ANOP
 PUNCH 'DCBLAST  OPEN  (DCB&I,(INOUT)),MF=L'
.OPLTST  AIF   (&I NE 1).OPLTST2                               @Y17XAYP
 PUNCH 'DCBL2    EQU   *'
.OPLTST2 ANOP
&I       SETA  &I+1
         AIF   (&I LE &DDCT).OPNLOOP                             Y06327
 PUNCH 'OPENDONE DS    0H'
.*
.*********************************************************************
.*       READY
.*********************************************************************
.*
 PUNCH '         READY'
.*
.*********************************************************************
.*       CLOSE'S
.*********************************************************************
.*
&I       SETA  1
         AIF   ('&DMPNAME' EQ '').CLSLOOP BYPASS IF NO DUMP      Y06327
&RN      SETA  1                   INITIALIZE DCB COUNTER        Y06327
.CLSDMP  ANOP                                                    Y06327
&RN      SETA  &RN+1               UPDATE DCB POINTER            Y06327
         AIF   ('&RN' GT '&RNNUMB').CLSLOOP                      Y06327
         AGO   .CLSDMP                                           Y06327
.CLSLOOP ANOP
 PUNCH '         CLOSE (DCB&I)'
&I       SETA  &I+1
         AIF   (&I LE &DDCT).CLSLOOP                             Y06327
.*
.*********************************************************************
.*       RETURN
.*********************************************************************
.*
 PUNCH 'IEDAYBYE L     13,4(,13)'
 PUNCH '         RETURN (14,12)'
.**********************************************************************
.*             NCP DCB AND NCP DUMP DCB
.**********************************************************************
&I       SETA  1                   LINEGRP LOOP CONTROL        @Y17XAYP
&RN      SETA  0                   SET DCB COUNTER             @Y17XAYP
.NCPDCB4 AIF   ('&GRPNAME(&I)' EQ '').NCPDCB1  NCP GROUP       @Y17XAYP
         AIF   (&I EQ 1).NCPDCB2   FIRST DCB                   @Y17XAYP
         AIF   ('&DDNME(&I)' EQ '&DDNME(&I-1)').ILOOP          @Y17XAYP
.*                                 SMAE NCP                    @Y17XAYP
.NCPDCB2 ANOP                                                  @Y17XAYP
&RN      SETA  &RN+1               INCREMENT DCB COUNTER       @Y17XAYP
 PUNCH 'DCB&RN   DCB   DSORG=TR,MACRF=(G,P),DDNAME=&DDNME(&I)' @Y17XAYP
         AIF   ('&DMPNAME(&RN)' EQ '').ILOOP                   @Y17XAYP
 PUNCH 'DCBD&RN  DCB   DSORG=PS,MACRF=(WP),BLKSIZE=512,RECFM=F,LRECL=51X
               2,  ........'                                   @Y17XAYP
 PUNCH '               DDNAME=&DMPNAME(&RN)'                   @Y17XAYP
         AGO   .ILOOP              CK MORE LINEGRPS            @Y17XAYP
.NCPDCB1 ANOP                                                  @Y17XAYP
&RN      SETA  &RN+1               INCREMENT DCB COUNT         @Y17XAYP
.ILOOP   ANOP                                                  @Y17XAYP
&I       SETA  &I+1                INCREMENT LINEGRP COUNTER   @Y17XAYP
         AIF   (&I LE &NLG).NCPDCB4                            @Y17XAYP
.*
.*********************************************************************
.*       TTABLE
.*********************************************************************
.*
 PUNCH '         TTABLE LAST=TLAST,MAXLEN=8'
.*
.*********************************************************************
.*       OPTION
.*********************************************************************
.*
         AIF   (NOT &FSCR).NOFS    IF FULLSCR REQUESTED
 PUNCH 'IEDQFSCR OPTION XL1'            FULLSCR OPTION FIELD
.NOFS    ANOP
 PUNCH 'IEDQCAN  OPTION A'              TRANS TABLE OPTION     @YA07414
         AIF   (NOT &SNA).SKPMODE    ANY SNA RESOURCES         @YM08503
 PUNCH 'IEDLMODE OPTION CL8'            BIND IMAGE OPTION FLD  @YM08503
.SKPMODE ANOP                                                  @YM08503
.**********************************************************************
.*             NCP TERMINAL AND SSCP TERMINAL
.**********************************************************************
&I       SETA  1                   SET LINEGRP LOOP CONTROL    @Y17XAYP
&RN      SETA  0                   SET DCB COUNTER             @Y17XAYP
.NCPTRM1 AIF   ('&GRPNAME(&I)' EQ '').NCPTRM4   NOT NCP GROUP  @Y17XAYP
         AIF   (&I EQ 1).NCPTRM2 FIRST GROUP BYPASS DDNAME CK  @Y17XAYP
         AIF   ('&DDNME(&I)' EQ '&DDNME(&I-1)').DB12           @Y17XAYP
.NCPTRM2 ANOP                                                  @Y17XAYP
&RN      SETA  &RN+1               INCREMENT DCB COUNTER       @Y17XAYP
.NCPTRM3 AIF   ('&AUTOIPL(&RN)' EQ '').NOI AUTOIPL SUBLIST     @Y17XAYP
         AIF   ('&AUTOIPL(&RN)' EQ 'YES' OR                            X
               '&AUTOIPL(&RN)' EQ 'NO').I1 OPERANDS VALID      @Y17XAYP
         MNOTE 4,'IKJ60004I AUTOIPL SUBLIST OPERAND &AUTOIPL(&RN) INCORX
               RECT -- IGNORED'                                @Y17XAYP
.NOI     ANOP                                                  @Y17XAYP
&AUTOI   SETC  'NO'                DEFAULT TO NO AUTOIPL       @Y17XAYP
         AGO   .I2                                             @Y17XAYP
.I1      ANOP                                                  @Y17XAYP
&AUTOI   SETC  '&AUTOIPL(&RN)'                                 @Y17XAYP
.I2      AIF   ('&AUTODMP(&RN)' EQ '').NOD AUTODMP SUBLIST     @Y17XAYP
         AIF   ('&AUTODMP(&RN)' EQ 'YES' OR                            X
               '&AUTODMP(&RN)' EQ 'NO').D1 OPERANDS VALID      @Y17XAYP
         MNOTE 4,'IKJ60004I AUTODMP SUBLIST OPERAND &AUTODMP(&RN) INCORX
               RECT -- IGNORED'                                @Y17XAYP
.NOD     ANOP                                                  @Y17XAYP
&AUTOD   SETC  'NO'                DEFAULT TO NO AUTODMP       @Y17XAYP
         AGO   .D2                                             @Y17XAYP
.D1      ANOP                                                  @Y17XAYP
&AUTOD   SETC  '&AUTODMP(&RN)'                                 @Y17XAYP
.D2      ANOP                                                  @Y17XAYP
&C       SETC  ','                 ASSUME DCB CONTINUATION     @Y17XAYP
&D       SETC  '.'                                             @Y17XAYP
         AIF   ('&DMPNAME(&RN)' NE '' OR '&IPLTXID(&RN)' NE '' OR      X
               '&AUTOIPL(&RN)' NE '').DB6                      @Y17XAYP
&C       SETC  ' '                 NO CONTINUATION             @Y17XAYP
&D       SETC  ' '                                             @Y17XAYP
.DB6     ANOP                                                  @Y17XAYP
 PUNCH 'NCP&RN    TERMINAL TERM=LNCP,DCB=DCB&RN&C              @ZM46739X
                       &D&D&D&D&D&D&D&D&D&D'                   @ZM46739
.*PUNCH'               FMPROF=&NFM(&I),TSPROF=&NTS(&I)&C       @ZM46739
.*                               &D&D&D&D&D&D&D&D&D&D'         @ZM46739
&C       SETC  ' '                 ASSUME NO                   @Y17XAYP
&D       SETC  ' '                 CONTINUATION                @Y17XAYP
         AIF   ('&IPLTXID(&RN)' EQ '').DB10                    @Y17XAYP
         AIF   ('&AUTOIPL(&RN)' EQ '' AND '&DMPNAME(&RN)' EQ '').DB11
&C       SETC  ','                 ASSUME                      @Y17XAYP
&D       SETC  '.'                 CONTINUATION                @Y17XAYP
.DB11    ANOP                                                  @Y17XAYP
 PUNCH '               IPLTXID=&IPLTXID(&RN)&C                         X
                             &D&D&D&D&D&D&D&D'                 @Y17XAYP
&C       SETC  ' '                 ASSUME NO                   @Y17XAYP
&D       SETC  ' '                 CONTINUATION                @Y17XAYP
.DB10    ANOP                                                  @Y17XAYP
         AIF   ('&AUTOIPL(&RN)' EQ '').DB14                    @OY18058
         AIF   ('&DMPNAME(&RN)' EQ '').DB13                    @Y17XAYP
&C       SETC  ','                 ASSUME                      @Y17XAYP
&D       SETC  '.'                 CONTINUATION                @Y17XAYP
.DB13    ANOP                                                  @Y17XAYP
 PUNCH '               IPLAUTO=&AUTOI&C                                X
                          &D&D&D'                              @Y17XAYP
.DB14    ANOP                                                  @OY18058
         AIF   ('&DMPNAME(&RN)' EQ '').DB12                    @Y17XAYP
 PUNCH '               DUMPDCB=DCBD&RN,DMPAUTO=&AUTOD'         @Y17XAYP
         AGO   .DB12                                           @Y17XAYP
.NCPTRM4 ANOP                                                  @Y17XAYP
&RN      SETA  &RN+1               INCREMENT DCB COUNTER       @Y17XAYP
.DB12    ANOP                                                  @Y17XAYP
&I       SETA  &I+1                INCREMENT LINEGRP CTR       @Y17XAYP
         AIF   (&I LE &NLG).NCPTRM1                            @Y17XAYP
         AIF   (NOT &SNA).NOSSCP  NO SNA RESOURCES             @Y17XAYP
 PUNCH 'SSCP    TERMINAL TERM=SSCP'                            @Y17XAYP
.NOSSCP  ANOP
.*
.**********************************************************************
.*       GROUP MACROS
.**********************************************************************
.*
&SCRPT   SETA  1                   INITIALIZE SCREEN ARRAY POINTER
&LNEPT   SETA  1                   INITIALIZE LINE ARRAY POINTER Y06327
&TN      SETA  1000                INITIALIZE MACRO COUNTER
&I       SETA  1                   SET OUTER LOOP CONTROL
&RN      SETA  0                   INITIALIZE DCB COUNTER        Y06327
.GRPLIN  AIF   ('&GRPNAME(&I)' EQ '').GRP2 BYPASS IF NOT 3705  @Y17XAYP
         AIF   (&I EQ 1).GRP3      BYPASS EQ DDNAME CHECK        Y06327
.*                                 IF FIRST LINE GROUP           Y06327
         AIF   ('&DDNME(&I)' EQ '&DDNME(&I-1)').GRP1  EQUAL      Y06327
.*                                 DDNAMES MEANS SAME 3705       Y06327
.GRP3    ANOP                                                    Y06327
&RN      SETA  &RN+1               UPDATE DCB COUNTER            Y06327
.GRP1    ANOP                                                    Y06327
&GNAME   SETC  '&GRPNAME(&I)'                                    Y06327
&GTRANS  SETC  '&TTAB((&I-1)*4+1)' GET NAME OF TRANSLATE TABLE   Y06327
 PUNCH '&GNAME GROUP MH=TSOMH,TRANS=&GTRANS,                           X
                     ...........'                                Y06327
&N       SETA  2                   DEFAULT BUFOUT                Y06327
         AIF   ('&T(&I)'(1,3) NE '327').GRP4  BUFOUT=1 FOR       Y06327
.*                                 3705/3270                     Y06327
&N       SETA  1                                                 Y06327
         AIF   ('&T(&I)'(1,4) NE '327L').GRP4  BYPASS IF NOT   @ZA03733
.*                                 3270 LOCAL                  @ZA03733
 PUNCH '               PCI=(,N),                               @ZA03733X
                       X'          PCI=(,N) FOR 3270 LOCAL     @ZA03733
.GRP4    ANOP                                                    Y06327
 PUNCH '               DCB=DCB&RN,BUFOUT=&N,BUFSIZE=&UNSIZ,BUFMAX=2'
.*
.**********************************************************************
.*       3705 LINES
.**********************************************************************
.*
&LC      SETA  1                   INITIALIZE LINE CTR           Y06327
.LCINCR  ANOP                                                    Y06327
&TRM     SETC  '&T(&I)'            SET TERM TYPE               @Y17XAYP
         AIF   (&LC GT &LNO(&I)).TERMLIN ALL DONE WITH 3705 LINESY06327
         AIF   ('&GRPNAME(&I)' EQ '').TERMLIN                  @Y17XAYP
&TN      SETA  &TN+1               INCREMENT TTABLE ENTRY COUNT  Y06327
&LNAME   SETC  'L&TN'              BUILD TTABLE ENTRY NAME       Y06327
&TRM     SETC  'LINE'                                          @Y17XAYP
.BLDLN   ANOP                                                  @Y17XAYP
 PUNCH '&LNAME   TERMINAL GROUP=&GRPNAME(&I),RLN=&LC,TERM=&TRM'
.*                                                             @Y17XAYP
.INLNCT  ANOP                                                  @Y17XAYP
&LC      SETA  &LC+1               INCREMENT LINE COUNTER        Y06327
         AGO   .LCINCR             PROCESS MORE 3705 LINES       Y06327
.GRP2    ANOP                                                    Y06327
&RN      SETA  &RN+1               UPDATE DCB COUNTER            Y06327
.*********************************************************************
.*       TERMINAL'S
.*********************************************************************
.*
.TERMLIN ANOP                      LOOP EXECUTED ONCE FOR EACH LINEGRP
.*
.*       BUILD DESCRIPTIVE NAMES OF FEATURES
.*
&J       SETA  (&I-1)*3            SET FEATUR TABLE OFFSET VALUE
         AIF   (&FETAB(&J+1)).TL1  TEST PRESENCE OF BREAK FEATURE
&FEAT(1) SETC  'NOBREAK'
         AGO   .TL2
.TL1     ANOP
&FEAT(1) SETC  'BREAK'
.TL2     AIF   (&FETAB(&J+2)).TL3  TEST PRESENCE OF ATTN FEATURE
&FEAT(2) SETC  'NOATTN'
         AGO   .TL4
.TL3     ANOP
&FEAT(2) SETC  'ATTN'
.TL4     AIF   (&FETAB(&J+3)).TL5  TEST PRESENCE OF TOSUPPR FEATURE
&FEAT(3) SETC  ''
         AGO   .TL6
.TL5     ANOP
&FEAT(3) SETC  ',TOSUPPR'
.TL6     ANOP
.*
.*       BUILD MACRO LABEL
.*
&J       SETA  1                   INITIALIZE LINE COUNTER     @Y17XAYP
&H       SETA  1                   INITIALIZE LUNO COUNTER     @Y17XAYP
&K       SETA  0                   INITIALIZE TERM/PU COUNTER  @Y17XAYP
&M       SETA  0                        SET LU COUNT           @OY20324
.TLINLOP ANOP                      LOOP EXECUTED ONCE PER LINE
&TRM     SETC  '&T(&I)'            SET TERMINAL TYPE           @Y17XAYP
         AIF   ('&GRPNAME(&I)' EQ '').LUNOL  NCP LINEGRP       @Y17XAYP
         AIF   ('&T(&I)' EQ 'PUNT').PPUNT IS THIS A NCP        @Y17XAYP
         AIF   ('&T(&I)' NE '327C').LUNOL THIS NOT A CLUSTER   @Y17XAYP
&TN      SETA  &TN+1               SET MACRO NUMBER            @ZM46739
&CNAME   SETC  'T&TN'              SET MACRO NAME              @ZM46739
&K       SETA  &K+1                INCREMENT TERM COUNTER      @ZM46739
 PUNCH '&CNAME   TERMINAL TERM=&TRM,QBY=T,RLN=&J,GROUP=&GRPNAME(&I)'
.*                                                             @YM07798
&M       SETA  0                                               @YM08503
         AGO   .LUNOL                                          @Y17XAYP
.PPUNT   ANOP                                                  @Y17XAYP
&TN      SETA  &TN+1               SET MACRO NUMBER            @ZM46739
&CNAME   SETC  'T&TN'              SET MACRO NAME              @ZM46739
&K       SETA  &K+1                INCREMENT TERM COUNTER      @ZM46739
 PUNCH '&CNAME   TERMINAL TERM=&TRM'                           @ZM46739
&M       SETA  0                                               @YM08503
.LUNOL   ANOP                                                  @Y17XAYP
         AIF   ('&T(&I)' EQ 'PUNT').PLUNT IS THIS FOR A LU     @Y17XAYP
         AIF   ('&T(&I)' EQ '327C').PREM IS THIS A CLUSTER     @Y17XAYP
&K       SETA  &K+1                INCREMENT TERMNO COUNT      @YM07381
         AGO   .PNCHLU                                         @Y17XAYP
.PLUNT   ANOP                                                  @Y17XAYP
&TRM     SETC  'LUNT'              SET TERM TYPE FOR LU        @Y17XAYP
         AGO   .PNCHLU                                         @Y17XAYP
.PREM    ANOP                                                  @Y17XAYP
&TRM     SETC  '327R'              SET TERM TYPE FOR REMOTE    @Y17XAYP
.PNCHLU  ANOP                                                  @Y17XAYP
&M       SETA  &M+1                INCREMENT LU COUNT          @Y17XAYP
         AIF   (&I EQ &NLG AND &J EQ &LNO(&I) AND &K EQ &TRMNO(&LNEPT) X
               AND &M EQ &TRMNO(&LNEPT+&H)                             X
               AND &I EQ &NLG).TLAST    LAST MACRO             @OZ33108
&TN      SETA  &TN+1               INCREMENT MACRO COUNT       @Y17XAYP
&TNAME   SETC  'T&TN'              SET TERMINAL NAME           @Y17XAYP
         AGO   .TL10                                           @Y17XAYP
.TLAST   ANOP
&TNAME   SETC  'TLAST'             BUILD SPECIAL NAME FOR LAST ENTRY
.TL10    ANOP
&CRSREF  SETC  'NO'                ASSUME NON-SWITCHED LINE
&QBYP    SETC  'QBY=T,'            INCLUDE QBY PARAMETER
         AIF   (NOT &DIALSW(&I)).LINE
&CRSREF  SETC  'YES'               INDICATE SWITCHED LINE
&QBYP    SETC  '      '            OMIT QBY PARAMETER
.*
.*       BUILD FIRST LINE OF TERMINAL MACRO
.*
.LINE    AIF   ('&GRPNAME(&I)' EQ '').CARD1 NOT 3705           @Y17XAYP
 PUNCH '&TNAME   TERMINAL GROUP=&GRPNAME(&I),QBY=T,RLN=&J,TERM=&TRM,QUEX
               UES=TS,  .............'                           Y06327
         AGO   .CARD1A                                           Y06327
.CARD1   ANOP                                                    Y06327
 PUNCH '&TNAME   TERMINAL &QBYP.DCB=DCB&RN,RLN=&J,TERM=&TRM,QUEUES=TS, X
                        .........'
.*
.*       PUNCH SECOND LINE OF TERMINAL MACRO
.*
.CARD1A  AIF   ('&TTAB((&I-1)*4+2)' NE '' OR '&SCRALL(&SCRPT)' NE      X
               '0').OPT1           NEED AN OPTION FIELD INITIAL@Y17XAYP
         AIF   ('&TRM' EQ 'LUNT').OPT1    PUT BIND IMAGE OPTN  @YM08503
&PNCHCO  SETA  0                                               @YM08503
         AGO   .OPT3                                             Y06327
.OPT1    ANOP                                                    Y06327
&OPDT    SETC  'OPDATA='                                         Y06327
&OPDT2   SETC  '),'           ASSUME NO BIND OPTION FIELD      @YM08503
&PNCHCO  SETA  2                                               @YM08503
         AIF   ('&TRM' NE 'LUNT').CKTRAN     NO BIND IMAGE RQD @YM08503
&PNCHCO  SETA  11                                              @YM08503
         AIF   (&LUTYP(&I) EQ 0).LU0     IS LU TYPE ZERO       @YM08503
         AIF   (&LUTYP(&I) EQ 1).LU1     IS LU TYPE ONE        @YM08503
         AIF   (&LUTYP(&I) EQ 2).LU2     IS LU TYPE TWO        @YM08503
         AGO   .CKTRAN                                         @YM08503
.LU0     ANOP                LU TYPE 0                         @YM08503
&OPDT2   SETC  ',IEDTSOB0),' LU TYPE 0 BIND IMAGE NAME         @YM08503
         AGO   .CKTRAN                                         @YM08503
.LU1     ANOP                LU TYPE 1                         @YM08503
&OPDT2   SETC  ',IEDTSOB1),' LU TYPE 1 BIND IMAGE NAME         @YM08503
         AGO   .CKTRAN                                         @YM08503
.LU2     ANOP                LU TYPE 2                         @YM08503
&OPDT2   SETC  ',IEDTSOB2),' LU TYPE 2 BIND IMAGE NAME         @YM08503
.CKTRAN  ANOP                                                  @YM08503
&OPDT1   SETC  '(0,0'              BOTH OPTION FIELDS INITIAL. @YM08503
&PNCHCO  SETA  &PNCHCO+4                                       @YM08503
         AIF   ('&TTAB((&I-1)*4+2)' NE '' AND '&SCRALL(&SCRPT)' NE '0')X
               .TSCRN              BOTH FIELDS INITIALIZED       Y06327
         AIF   (&FSCR).TSCRN       OK IF FULLSCR                 Y06327
&OPDT1   SETC  '(0'                INITIALIZE  FIELD           @YM08503
&PNCHCO  SETA  &PNCHCO-2                                       @YM08503
.*                                 IF NO FULLSCR                 Y06327
         AGO   .TSCRN                                            Y06327
.OPT3    ANOP                                                    Y06327
&OPDT    SETC  '       '                                         Y06327
&OPDT1   SETC  '      '                                          Y06327
.TSCRN   AIF   ('&GRPNAME(&I)' EQ '').CARD2 NOT 3705           @Y17XAYP
&CRSREF  SETC  'NO'                                              Y06327
.CARD2   AIF   (&SCRH(&SCRPT) EQ 0 AND &SCRW(&SCRPT) EQ 0).TNOSCR
 PUNCH '               UTERM=&CRSREF,SCRSIZE=(&SCRH(&SCRPT),&SCRW(&SCRPX
               T)),                              ....'           Y06327
         AIF   ('&T(&I)'(1,3) NE '327' AND '&T(&I)' NE 'PUNT').NOBUF
         AIF   (&SCRH(&SCRPT) EQ 24 AND &SCRW(&SCRPT) EQ 80).LGBUF
         AIF   (&SCRH(&SCRPT) EQ 12 AND &SCRW(&SCRPT) EQ 40).SMBUF
         AGO   .NOBUF              NO BUFSIZE NEEDED             Y06327
.LGBUF   ANOP                      LARGE SCREEN BUFSIZE          Y06327
 PUNCH '               BUFSIZE=1968,                                   X
                     ....'
         AGO   .NOBUF                                            Y06327
.SMBUF   ANOP                      SMALL SCREEN BUFSIZE          Y06327
 PUNCH '               BUFSIZE=528,                                    X
                     ....'
.NOBUF   AIF   ('&OPDT' EQ '       ').TPUN3                    @Y17XAYP
         AIF   (&PNCHCO GT 9).BND                              @YM08503
 PUNCH '               &OPDT&OPDT1&OPDT2                               X
                  ...........'                                 @OY16449
         AGO   .TPUN3                                          @YM08503
.BND     ANOP
 PUNCH '               &OPDT&OPDT1&OPDT2                               X
               ........'                                         Y06327
         AGO   .TPUN3              GO PUNCH 3RD LINE
.TNOSCR  ANOP
 PUNCH '               UTERM=&CRSREF,                                  X
                         .....'                                  Y06327
         AIF   ('&OPDT' EQ '       ').TPUN3                      Y06327
         AIF   (&PNCHCO GT 9).BND1                             @YM08503
 PUNCH '               &OPDT&OPDT1&OPDT2                               X
                   ...........'                                @OY16449
         AGO   .TPUN3                                          @YM08503
.BND1    ANOP
 PUNCH '               &OPDT&OPDT1&OPDT2                               X
               ........'                                         Y06327
.TPUN3   AIF   ('&GRPNAME(&I)' NE '').TLPNOAD BYPASS FOR 3705  @Y17XAYP
.*
.*       PUNCH THIRD LINE OF TERMINAL MACRO
.*
&Y       SETC  ''
         AIF   ('&ADCHAR(&SCRPT)' EQ '').TLNOAD TEST           @Y17XAYP
.*                                 FOR NULL ADDR.                Y06327
&X       SETC  '&ADCHAR(&SCRPT)' ASSUME NOT 1050 OR            @Y17XAYP
.*                                 226R/5 OR 327R                Y06327
         AIF   ('&T(&I)' EQ '327R' OR '&T(&I)' EQ '327S').T3270
         AIF   ('&T(&I)' NE '226R' AND '&T(&I)' NE '2265').T1050
&X       SETC  '&X'(1,4)'A0'
         AGO   .TLPAST
.T3270   ANOP
&Z       SETA  1                        INITIALIZE PTR FOR TABLE LOOKUP
         AIF   ('&TTAB((&I-1)*4+1)' EQ 'ASCI').ASCI CHECK TRANTAB
.EBCD    ANOP
&Q       SETA  1                        POSITION IN TABLE ELEMENT
&Y       SETC  '2D'
         AGO   .SEARCH
.ASCI    ANOP
&Q       SETA  5                        POSITION IN TABLE ELEMENT
&Y       SETC  '05'
.SEARCH  AIF   ('&X'(1,2) EQ '&TBL(&Z)'(&Q,2)).FOUND           @Y17XAYP
&Z       SETA  &Z+1                     POINTS AT NEXT ENTRY
         AIF   (&Z LE 32).SEARCH
         MNOTE 8,'IKJ54128I ADDRESSING CHARACTERS INVALID OR MISSING FOX
               R TERMINAL IN LINE GROUP &I'
&X       SETC  '        '
         AGO   .TLPNOAD                                          Y06327
.FOUND   ANOP
&X       SETC  '&TBL(&Z)'(&Q+2,2)'&TBL(&Z)'(&Q+2,2)'&X'(3,2)'&X'(3,2)
.*                                 REPLICATING ADDRESSING CHARS
         AGO   .TLPAST
.T1050   ANOP
         AIF   ('&T(&I)' NE '1050' AND '&T(&I)' NE '5041').TLPAST
&X       SETC  '&X'(1,2)'13'       ADD COMPONENT SELECTOR
.TLPAST  ANOP
 PUNCH '               ADDR=&X&Y,FEATURE=(&FEAT(1),&FEAT(2)&FEAT(3))'
         AGO   .TL11
.TLNOAD  AIF   ('&T(&I)'(1,3) NE '226' AND '&T(&I)'(1,3) NE '327' AND '&
               &T(&I)' NE '1050').TLPNOAD                      @Y17XAYP
         AIF   ('&T(&I)' EQ '327L').TLPNOAD
         AIF   ('&T(&I)' EQ '226L').TLPNOAD
         MNOTE 8,'IKJ54128I ADDRESSING CHARACTERS INVALID OR MISSING FOX
               R TERMINAL IN LINE GROUP &I'
.TLPNOAD AIF   ('&T(&I)' NE 'PUNT').SKPUSS                     @Y17XAYP
         AIF   (&LUSB(&SCRPT) EQ 1).ACTLU ACTIVATE LU/LU SES   @Y17XAYP
&LUACT   SETC  'NO'                                            @Y17XAYP
         AGO   .PACT               PUNCH ACTIVE CARD           @Y17XAYP
.ACTLU   ANOP                                                  @Y17XAYP
&LUACT   SETC  'YES'               ACTIVATE LU/LU SESSION      @Y17XAYP
.PACT    ANOP                                                  @Y17XAYP
 PUNCH '               ACTIVE=(YES,&LUACT),                            X
                       .....'                                  @Y17XAYP
 PUNCH '               TCMSESN=LUTERM,                                 X
                     .....'                                    @Y17XAYP
.*PUNCH'               FMPROF=&LFM(&I),TSPROF=&LTS(&I),        @ZM46739
.*                                 .....'                      @ZM46739
.SKPACT  AIF   (&USSB(&SCRPT) NE 1).SETSCS USS 3270 SPECIFIED  @Y17XAYP
 PUNCH '               USS=3270,                                       X
                    .......'                                   @Y17XAYP
         AGO   .SKPUSS                                         @Y17XAYP
.SETSCS  ANOP                                                  @Y17XAYP
 PUNCH '               USS=SCS,                                        X
                     .......'                                  @Y17XAYP
.SKPUSS  ANOP                                                  @Y17XAYP
 PUNCH '               FEATURE=(&FEAT(1),&FEAT(2)&FEAT(3))'
.TL11    ANOP
.*                                                             @ZM46739
&SCRPT   SETA  &SCRPT+1            BUMP SCREEN ARRAY POINTER
         AIF   (&M LT &TRMNO(&LNEPT+&H)).LUNOL  MORE LU        @Y17XAYP
&M       SETA  0                   RESET LU/LUNO  COUNT        @Y17XAYP
&H       SETA  &H+1                INCREMENT LUNO COUNTER      @Y17XAYP
         AIF   (&H LE &TRMNO(&LNEPT)).TLINLOP  MORE LUNO       @Y17XAYP
&K       SETA  0                                               @OZ33108
&LNEPT   SETA  &LNEPT+&H           SET PTR TO NEXT TERMNO      @Y17XAYP
&H       SETA  1                   RESET LUNO COUNTER          @Y17XAYP
&J       SETA  &J+1                INCREMENT LINE COUNTER      @Y17XAYP
         AIF   (&J LE &LNO(&I)).TLINLOP MORE LINES/LINEGRP     @Y17XAYP
.*       OUTER LOOP TEST
.TLOUT   ANOP
&I       SETA  &I+1                INCREMENT OUTER LOOP INDEX
         AIF   (&I LE &NLG).GRPLIN BR IF MORE LINE GROUPS        Y06327
.*
.*********************************************************************
.*       TSINPUT
.*********************************************************************
.*
 PUNCH '         TSINPUT'
.*
.*********************************************************************
.*       DCB'S
.*********************************************************************
.*
&IN      SETA  0                   INITIALIZE INVLIST COUNTER
&I       SETA  1                   SET LOOP CONTROL VARIABLE
&RN      SETA  0                   INITIALIZE 3705 DCB CTR       Y06327
.DCBLOOP AIF   ('&GRPNAME(&I)' EQ '').DCB2 BYPASS IF NOT 3705  @Y17XAYP
         AIF   (&I EQ 1).DCB1      BYPASS EQ DDNAME CHECK        Y06327
.*                                 IF FIRST LINE GROUP           Y06327
         AIF   ('&DDNME(&I)' EQ '&DDNME(&I-1)').DCBTEST EQUAL    Y06327
.*                                 DDNAMES MEANS SAME 3705       Y06327
.DCB1    ANOP                                                    Y06327
&RN      SETA  &RN+1               UPDATE 3705 DCB COUNTER       Y06327
         AGO   .DCBTEST                                          Y06327
.DCB2    ANOP                                                    Y06327
&RN      SETA  &RN+1               UPDATE DCB COUNTER            Y06327
&J       SETA  1                   ASSUME BUFIN=1
         AIF   ('&T(&I)'(1,3) NE '226' AND '&T(&I)'(1,3) NE '327').PUNDX
               CB
&J       SETA  2                   BUFIN=2 FOR 2260/65 AND FOR 3270
.PUNDCB  ANOP
 PUNCH 'DCB&RN   DCB   DSORG=TX,MACRF=(G,P),INTVL=&INT(&I),CPRI=S, ....X
               ...................'
 PUNCH '               DDNAME=&DDNME(&I),MH=TSOMH,                     X
                         ........'                               Y06327
 PUNCH '               BUFIN=&J,BUFOUT=2,                              X
                     .......'
&X       SETC  '&TTAB((&I-1)*4+1)'  SET SCT TABLE NAME
&K       SETA  &UNSIZ*&UNO(&I)     COMPUTE BUFFER SIZE
&J       SETA  &J+1                COMPUTE BUFMAX
         AIF   ('&TTAB((&I-1)*4+2)' EQ '').DB1 BR IF ONLY 1 TRAN TBL
&TNAME   SETC  'TL&RN'             BUILD NAME OF TRANLIST        Y06327
         AIF   ('&T(&I)' NE '5041').DB2   BR IF NOT 5041 LINE GROUP
&X       SETC  '1050'              1050 IS SCT FOR 5041 LINE GROUPS
         AGO   .DB2
.DB1     ANOP
&TNAME   SETC  '&TTAB((&I-1)*4+1)' GET NAME OF TRANSLATE TABLE
.DB2     ANOP
&OPDT1   SETC  '(A,A)'             SET DEFAULT PCI               Y06327
         AIF  ('&T(&I)' NE '327L' AND '&T(&I)' NE '226L').DB20 @ZA03733
&OPDT1   SETC  '(N,N)'             SET PCI FOR LOCAL DEVICE    @ZA03733
.DB20    ANOP                                                    Y06327
 PUNCH '               BUFSIZE=&K,BUFMAX=&J,PCI=&OPDT1,                X
                     .....'                                     OY03474
 PUNCH '               TRANS=&TNAME,                                   X
                     ......'                                    OY03474
&IN      SETA  &IN+1               BUMP INVLIST COUNTER
.*
.*       BRANCH IF SEVERAL INVLIST SUB-OPERANDS
         AIF   (&LNO(&I) GT 1).DB3
.*
.*       COMPUTE VALUES FOR SELECTION OF CHARACTERS IN THE
.*       ALPHAMERIC CHARACTER SET ARRAY.  THESE TWO SELECTED CHARACTERS
.*       WILL BE CONCATENATED TO FORM THE INVLIST MACRO NAME.
&L       SETA  (&IN-1)/36+1        SET 1ST CHARACTER SELECTOR
&M       SETA  &IN-(&L-1)*36       SET 2ND CHAR SELECTOR
 PUNCH '               SCT=&X,INVLIST=(&CS(&L)&CS(&M))'
         AGO   .DCBTEST            GO TEST FOR MORE LINE GROUPS
.DB3     ANOP
&L       SETA  (&IN-1)/36+1        SET 1ST CHARACTER SELECTOR
&M       SETA  &IN-(&L-1)*36       SET 2ND CHAR SELECTOR
 PUNCH '               SCT=&X,INVLIST=(&CS(&L)&CS(&M),,,               X
                             .........'
.*
.*       SET UP LOOP TO GENERATE REST OF INVLIST SUB-OPERANDS
.*
&J       SETA  2                   INITIALIZE LINE COUNTER
.DCBINLP ANOP
&IN      SETA  &IN+1               BUMP INVLIST COUNTER
.*       BRANCH IF LAST SUB-OPERAND COMING UP NEXT
         AIF   (&J EQ &LNO(&I)).DB4
.*
.*       COMPUTE VALUES FOR SELECTION OF CHARACTERS IN THE
.*       ALPHAMERIC CHARACTER SET ARRAY.  THESE TWO SELECTED CHARACTERS
.*       WILL BE CONCATENATED TO FORM THE INVLIST MACRO NAME.
&L       SETA  (&IN-1)/36+1        SET 1ST CHARACTER SELECTOR
&M       SETA  &IN-(&L-1)*36       SET 2ND CHAR SELECTOR
 PUNCH '               &CS(&L)&CS(&M),,,                               X
                                   X'
&J       SETA  &J+1                BUMP LINE COUNTER
         AGO   .DCBINLP
.DB4     ANOP
&L       SETA  (&IN-1)/36+1        SET 1ST CHARACTER SELECTOR
&M       SETA  &IN-(&L-1)*36       SET 2ND CHAR SELECTOR
 PUNCH '               &CS(&L)&CS(&M))' PUNCH LAST SUBOPERAND
.*
.*       DCB GENERATION LOOP TEST
.*
.DCBTEST ANOP
&I       SETA  &I+1                INCREMENT LOOP CONTROL VARIABLE
         AIF   (&I LE &NLG).DCBLOOP BR IF MORE LINE GROUPS
.*
.*********************************************************************
.*       TRANLIST'S
.*********************************************************************
.*
&I       SETA  1                   SET OUTER LOOP INDEX VARIABLE
&RN      SETA  0                   INITIALIZE DCB COUNTER        Y06327
.TRLOOP  AIF   ('&GRPNAME(&I)' EQ '').TRAN2  BYPASS IF NOT 3705@Y17XAYP
         AIF   (&I EQ 1).TRAN1     BYPASS EQ DDNAME CHECK        Y06327
.*                                 IF FIRST LINE GROUP           Y06327
         AIF   ('&DDNME(&I)' EQ '&DDNME(&I-1)').TRLPTST  EQUAL   Y06327
.*                                 DDNAMES MEANS SAME 3705       Y06327
.TRAN1   ANOP                                                    Y06327
&RN      SETA  &RN+1               UPDATE DCB COUNTER            Y06327
         AGO   .TRLPTST                                          Y06327
.TRAN2   ANOP                                                    Y06327
&RN      SETA  &RN+1               UPDATE DCB COUNTER            Y06327
&J       SETA  (&I-1)*4+1          PT TO 1ST OF GROUP IN &TTAB, &CTAB
         AIF   ('&TTAB(&J+1)' EQ '').TRLPTST BE IF TRANLIST UNNECESSARY
.*
.*       THIS ROUTINE SCANS THE TRANSLATE TABLE NAMES OF A LINEGRP
.*       LOOKING FOR 2741 OR 1050 TABLES.  WHEN (AND IF) FOUND THEY ARE
.*       PLACED IN  ARRAYS  FROM WHICH THE L2741= AND L1050=
.*       PARAMETERS WILL LATER BE GENERATED IF BOTH 2740'S AND 1050'S
.*       ARE INVOLVED IN THE LINE GROUP.
.*
&L2741(1) SETC  ''                 INITIALIZE ARRAYS
&L2741(2) SETC  ''
&L2741(3) SETC  ''
&L2741(4) SETC  ''
&L1050(1) SETC  ''
&L1050(2) SETC  ''
&L1050(3) SETC  ''
&L1050(4) SETC  ''
&N2741   SETA  0                   INITIALIZE ARRAY COUNTERS
&N1050   SETA  0
&K       SETA  0                   SET LOOP CONTROL VARIABLE
.*       SKIP NULL TABLE ENTRIES
.TRLP2   AIF   ('&TTAB(&J+&K)' EQ '').TRLP2T
         AIF   ('&TTAB(&J+&K)'(3,2) NE '41').TRLP3
&N2741   SETA  &N2741+1
&L2741(&N2741) SETC '&TTAB(&J+&K)'
         AGO   .TRLP2T             GO TO SCANNING LOOP TEST
.TRLP3   AIF   ('&TTAB(&J+&K)'(3,2) NE '50').TRLP2T
&N1050   SETA  &N1050+1
&L1050(&N1050) SETC '&TTAB(&J+&K)'
.TRLP2T  ANOP
&K       SETA  &K+1
         AIF   (&K LE 3).TRLP2     BR UNTIL ALL TBL NAMES ARE CHECKED
         AIF   (&N2741 NE 0 AND &N1050 NE 0).TRLBOTH
.*
.*       THIS ROUTINE IS EXECUTED UNLESS THE LINE GROUP HAS BOTH
.*       2740 AND 1050 TERMINALS.  THIS ROUTINE GENERATES THE
.*       LIST= PARAMETER.
.*
&CM(2)   SETC  ''                  INITIALIZE SUBLIST SEPARATORS
&CM(3)   SETC  ''
&CP(2)   SETC  '     '             INITIALIZE CONTINUATION POSITIONERS
&CP(3)   SETC  '     '
.*       MAKE SUBLIST SEPARATORS COMMAS IF THERE ARE, IN FACT,
.*       OPERANDS TO SEPARATE.  ALSO SET THE CONTINUATION POSITIONERS
.*       TO THE PROPER LENGTH SO THAT A CONTINUATION CHARACTER WILL
.*       APPEAR IN COLUMN 72.
         AIF   ('&TTAB(&J+2)' EQ '').TRL1
&CM(2)   SETC  ','
&CP(2)   SETC  ''
.TRL1    AIF   ('&TTAB(&J+3)' EQ '').TRL2
&CM(3)   SETC  ','
&CP(3)   SETC  ''
.TRL2    ANOP
 PUNCH 'TL&RN    TRANLIST LIST=(&TTAB(&J),&TTAB(&J+1)&CM(2)&TTAB(&J+2)&X
               CM(3)&TTAB(&J+3)),&CP(2)&CP(3)                 .........X
               ........'
         AGO   .TRLCHAR            GO GENERATE CHARS= PARAMETER
.*
.*       THIS ROUTINE GENERATES THE L2741= PARAMETER
.*
.TRLBOTH ANOP
&CM(1)   SETC  ''                  INITIALIZE SUBLIST SEPARATORS
&CM(2)   SETC  ''
&CM(3)   SETC  ''
&CP(1)   SETC  '     '             INITIALIZE CONTINUATION POSITIONERS
&CP(2)   SETC  '     '
&CP(3)   SETC  '     '
.*       MAKE SUBLIST SEPARATORS COMMAS IF THERE ARE, IN FACT,
.*       OPERANDS TO SEPARATE.  ALSO SET THE CONTINUATION POSITIONERS
.*       TO THE PROPER LENGTH SO THAT A CONTINUATION CHARACTER WILL
.*       APPEAR IN COLUMN 72.
         AIF   (&N2741 LT 2).TRL3
&CM(1)   SETC  ','
&CP(1)   SETC  ''
         AIF   (&N2741 LT 3).TRL3
&CM(2)   SETC  ','
&CP(2)   SETC  ''
         AIF   (&N2741 LT 4).TRL3
&CM(3)   SETC  ','
&CP(3)   SETC  ''
.TRL3    ANOP
 PUNCH 'TL&RN    TRANLIST L2741=(&L2741(1)&CM(1)&L2741(2)&CM(2)&L2741(3X
               )&CM(3)&L2741(4)),&CP(1)&CP(2)&CP(3)                 ...X
               .............'
.*
.*       THIS ROUTINE GENERATES THE L1050= PARAMETER
.*
&CM(1)   SETC  ''                  INITIALIZE SUBLIST SEPARATORS
&CM(2)   SETC  ''
&CM(3)   SETC  ''
&CP(1)   SETC  '     '             INITIALIZE CONTINUATION POSITIONERS
&CP(2)   SETC  '     '
&CP(3)   SETC  '     '
.*       MAKE SUBLIST SEPARATORS COMMAS IF THERE ARE, IN FACT,
.*       OPERANDS TO SEPARATE.  ALSO SET THE CONTINUATION POSITIONERS
.*       TO THE PROPER LENGTH SO THAT A CONTINUATION CHARACTER WILL
.*       APPEAR IN COLUMN 72.
         AIF   (&N1050 LT 2).TRL4
&CM(1)   SETC  ','
&CP(1)   SETC  ''
         AIF   (&N1050 LT 3).TRL4
&CM(2)   SETC  ','
&CP(2)   SETC  ''
         AIF   (&N1050 LT 4).TRL4
&CM(3)   SETC  ','
&CP(3)   SETC  ''
.TRL4    ANOP
 PUNCH '               L1050=(&L1050(1)&CM(1)&L1050(2)&CM(2)&L1050(3)&CX
               M(3)&L1050(4)),&CP(1)&CP(2)&CP(3)                    ...X
               .............'
.*
.*       THIS ROUTINE GENERATES THE CHARS= PARAMETERS
.*
.TRLCHAR ANOP
&CM(1)   SETC  ''                  INITIALIZE SUBLIST SEPARATORS
&CM(2)   SETC  ''
&CM(3)   SETC  ''
.*       MAKE SUBLIST SEPARATORS COMMAS IF THERE ARE, IN FACT,
.*       OPERANDS TO SEPARATE
         AIF   ('&CTAB(&J+1)' EQ '').TRL5
&CM(1)   SETC  ','
         AIF   ('&CTAB(&J+2)' EQ '').TRL5
&CM(2)   SETC  ','
         AIF   ('&CTAB(&J+3)' EQ '').TRL5
&CM(3)   SETC  ','
.TRL5    ANOP
 PUNCH '               CHARS=(&CTAB(&J)&CM(1)&CTAB(&J+1)&CM(2)&CTAB(&J+X
               2)&CM(3)&CTAB(&J+3)),OPFLD=IEDQCAN'               Y06327
.*
.*       TRANLIST LOOP TEST
.*
.TRLPTST ANOP
&I       SETA  &I+1
         AIF   (&I LE &NLG).TRLOOP BR IF MORE LINE GROUPS TO HANDLE
.*
.*********************************************************************
.*       INVLIST'S
.*********************************************************************
.*
&SCRPT   SETA  1                   INITIALIZE TRMNO ARRAY POINTER
&ADRPT   SETA  0                   INITIALIZE ADCHAR ARRAY POINTER
&IN      SETA  1                   INITIALIZE INVLIST COUNTER
&K       SETA  1000                INITIALIZE TTABLE NAME COUNTER
         AIF   (NOT &T3705).IL270X      IF 3705                  Y06327
&K       SETA  &RNLINES+&RNTERMS+&LUTERMS+1000   INIT PST 3705 @YM08503
&ADRPT   SETA  &ADRPT+&LUTERMS+&NONLU   ADJUST ADCHAR ARRAY    @OZ34694
.*                                 POINTER FOR THE 3705          Y06327
&SCRPT   SETA  &SCRPT+&RNLINES+&RNTERMS   TRMNO ARRAY POINTER    Y06327
.*                                 FOR THE 3705                  Y06327
.IL270X  ANOP                                                    Y06327
&I       SETA  1                   SET OUTER LOOP CONTROL VARIABLE
.IVL1    ANOP                      LOOP EXECUTED ONCE FOR EACH LINEGRP
         AIF   ('&GRPNAME(&I)' NE '').IVL1T INVLIST NOT          Y06327
.*                                 NEEDED FOR 3705               Y06327
&J       SETA  1                   SET INNER LOOP CONTROL VARIABLE
&EOT     SETC  ''
.IVL2    ANOP                      LOOP EXECUTED ONCE FOR EACH LINE
&N       SETA  1                   SET INNERMOST LOOP COUNTER
.IVL3    AIF   (&J GT &LNO(&I)).IVL1T BR IF ALL LINES IN LINEGRP DONE
         AIF   (&N GT &TRMNO(&SCRPT)).IVL2T  BR IF ALL TERM'S ON LINE
         AIF   (&N LT &TRMNO(&SCRPT)).IL1  BR IF NOT LAST TERM ON LINE
         AIF   (&J LT &LNO(&I)).IL1 BR IF NOT LAST LINE IN LINEGRP
         AIF   (&I LT &NLG).IL1    BR IF NOT LAST LINE IN WHOLE SYSTEM
&TNAME   SETC  'TLAST'             BUILD SPECIAL NAME FOR LAST LINE
         AGO   .IL2                SKIP NORMAL NAME BUILDING
.IL1     ANOP
&K       SETA  &K+1                BUMP TTABLE NAME COUNTER
&TNAME   SETC  'T&K'               BUILD TTABLE ENTRY NAME
.IL2     ANOP
&ADRPT   SETA  &ADRPT+1            BUMP ADCHAR ARRAY POINTER
&Y       SETC  ''
&X       SETC  '&ADCHAR(&ADRPT)'   ASSUME NOT 1050 OR 226R/5 OR 327R
         AIF   ('&X' EQ '').ILPAST  SKIP PROCESSING IF NULL
         AIF   ('&T(&I)' EQ '327R' OR '&T(&I)' EQ '327S').IL327R
         AIF   ('&T(&I)' NE '226R' AND '&T(&I)' NE '2265').IL1050
&X       SETC  '&X'(1,4)'40'
         AGO   .ILPAST3
.IL327R  ANOP
&X       SETC  '&X'(1,2)'&X'(1,2)'&X'(3,2)'&X'(3,2)
.*                                 REPLICATING ADDRESSING CHARS
         AIF   ('&TTAB((&I-1)*4+1)' EQ 'ASCI').ASCITAB
&Y       SETC  '2D'                     SET ENQ FOR EBCD
&EOT     SETC  ',EOT=37'
         AGO   .ILPAST3
.ASCITAB ANOP
&Y       SETC  '05'                     SET ENQ FOR ASCI
&EOT     SETC  ',EOT=04'
         AGO   .ILPAST3
.IL1050  AIF   ('&T(&I)' NE '1050' AND '&T(&I)' NE '5041').ILPAST
&X       SETC  '&X'(1,2)'15'       ADD COMPONENT SELECTOR
         AGO   .ILPAST3
.ILPAST  AIF   ('&T(&I)' NE '226L' AND '&T(&I)' NE '327L').ILPAST2
.*                                 TEST FOR 226L AND 327L
         AIF   ('&T(&I)' EQ '327L').IL327L
&X       SETC  '02'                SET 226L INVLIST VALUE
         AGO   .ILPAST3
.IL327L  ANOP
&X       SETC  '06'                SET 327L INVLIST VALUE
         AGO   .ILPAST3
.ILPAST2 AIF   ('&X' NE '').ILPAST3                            @Y17XAYP
&X       SETC  'DF'                SET DEFAULT VALUE
.ILPAST3 ANOP
.*       COMPUTE VALUES FOR SELECTION OF CHARACTERS IN THE
.*       ALPHAMERIC CHARACTER SET ARRAY.  THESE TWO SELECTED CHARACTERS
.*       WILL BE CONCATENATED TO FORM THE INVLIST MACRO NAME.
&L       SETA  (&IN-1)/36+1        SET 1ST CHARACTER SELECTOR
&M       SETA  &IN-(&L-1)*36       SET 2ND CHAR SELECTOR
         AIF   ('&T(&I)' NE '3335').IVLNTWX  BR IF NOT TWX
&IN      SETA  &IN+1               BUMP INVLIST MACRO COUNTER
         AIF   ('&TTAB((&I-1)*4+1)' NE 'TTYB').TTYBBYP         @OZ27297
 PUNCH '&CS(&L)&CS(&M)       INVLIST ORDER=(&TNAME+89),CPUID=CPIDE'
&CPID1   SETB  1                                               @OZ27297
&CPIDSW  SETB  1                                               @OZ27297
         AGO   .IVLPND                                         @OZ27297
.TTYBBYP ANOP                                                  @OZ27297
&CPID2   SETB  1                                               @OZ27297
 PUNCH '&CS(&L)&CS(&M)       INVLIST ORDER=(&TNAME+89),CPUID=CPID'
&CPIDSW  SETB  1                                               @YA11714
         AGO   .IVLPND             GO TO LOOP MODIFICATION
.IVLNTWX ANOP
&MM      SETA  (&N-((&N-1)/14)*14) CONVERT TO MODULO 14         SA59196
         AIF   (&N GE &TRMNO(&SCRPT)).ILEOL BR IF LAST INVLIST ENTRY
         AIF   (&MM EQ 14).ILEOG   BR IF END OF GROUP OF 14     SA59196
         AIF   (&N GT 1 AND &MM NE 1).ILPMID  BR IF NOT FIRST OR LAST
 PUNCH '&CS(&L)&CS(&M)       INVLIST ORDER=(&TNAME+&X&Y,               X
                              .........'
         AGO   .IVLPND             GO TO LOOP MODIFICATION
.ILPMID  ANOP                      PUNCH NOT-FIRST/NOT-LAST ENTRY
 PUNCH '               &TNAME+&X&Y,                                    X
                  .........'
         AGO   .IVLPND             GO TO LOOP MODIFICATION
.ILEOG   ANOP
&Y       SETC  '&Y'.','            ADD COMMA
 PUNCH '               &TNAME+&X&Y)'                           @OZ48953
         AGO   .IVLPND             GO TO LOOP MODIFICATION     @OZ48953
.ILEOL   ANOP                      END OF THIS INVLIST MACRO
&IN      SETA  &IN+1               BUMP INVLIST MACRO COUNTER
         AIF   (&MM EQ 1).ILFAL    BR IF 1ST & LAST ENTRY
.ILPLAST ANOP
 PUNCH '               &TNAME+&X&Y),AUTO=NO&EOT'               @YA14062
         AGO   .IVLPND             GO TO LOOP MODIFICATION
.ILFAL   ANOP                      PUNCH FIRST-AND-LAST ENTRY
 PUNCH '&CS(&L)&CS(&M)       INVLIST ORDER=(&TNAME+&X&Y),AUTO=NO&EOT'
.IVLPND  ANOP
&N       SETA  &N+1                BUMP INNERMOST LOOP COUNTER
         AGO   .IVL3               GO TO INNERMOST LOOP TEST
.IVL2T   ANOP
&SCRPT   SETA  &SCRPT+&TRMNO(&SCRPT)+1    TRMNO ARRAY PTR      @Y17XAYP
&J       SETA  &J+1                BUMP INNER LOOP CONTROL VARIABLE
         AGO   .IVL2               GO INCREMENT INNERMOST LOOP CTR
.IVL1T   ANOP
&I       SETA  &I+1                BUMP OUTER LOOP CONTROL VARIABLE
         AIF   (&I LE &NLG).IVL1   BR IF ALL LINEGRP'S NOT HANDLED
         AIF   (NOT &CPIDSW).CKSSCP                            @Y17XAYP
         AIF   (NOT &CPID1).ODDPRTY                            @OZ27297
 PUNCH 'CPIDE    DC    X''0288C9'''                            @OZ27297
.ODDPRTY ANOP                                                  @OZ27297
         AIF   (NOT &CPID2).CKSSCP                             @OZ27297
 PUNCH 'CPID      DC     X''0289C9'''
.CKSSCP  ANOP                                                  @Y17XAYP
         AIF   (&SNA NE 1).OLDTSMH  NO SSCP MH REQUIRED        @Y17XAYP
 PUNCH '         IEDMHGEN SSCP=YES'                            @Y17XAYP
TSOMH    TSOMH IEDHALT=&IEDHALT,LU=YES,INHDR=&INHDR,INBUF=&INBUF,      X
               INMSG=&INMSG,OUTHDR=&OUTHDR,OUTBUF=&OUTBUF,             X
               OUTMSG=&OUTMSG,LOGSCAN=&LOGSCAN                 @OY18934
         AGO    .PNDCBD                                        @Y17XAYP
.OLDTSMH ANOP                                                  @Y17XAYP
TSOMH    TSOMH INHDR=&INHDR,INBUF=&INBUF,INMSG=&INMSG,OUTBUF=&OUTBUF,  X
               OUTHDR=&OUTHDR,OUTMSG=&OUTMSG,LOGSCAN=&LOGSCAN  @OY18934
.PNDCBD  ANOP                                                  @Y17XAYP
 PUNCH '         DCBD  DSORG=TX'
.ALLTHRU ANOP
 PUNCH '         END'
         MEND
