         MACRO
&NAME    MRELEASE &TERM,&PASSWRD=
         AIF   ('&TERM' NE '').GO
         MNOTE 12,'*** IHB001  TERMNAME OPERAND REQ''D-NOT SPECIFIED'
         MEXIT
.GO      ANOP
         AIF   (K'&TERM LT 9).OK2
         AIF   ('&TERM'(1,1) EQ '(').OK2
         MNOTE 12,'*** IHB002 INVALID TERMNAME OPERAND SPECIFIED'
         MEXIT
.OK2     ANOP
&NAME    DS    0H
         AIF   ('&TERM'(1,1) NE '(').PASS
.REG     ANOP
         AIF   ('&TERM' EQ '(1)').SETCIB
         LR    1,&TERM(1) .             ADDRESS OF TERMNAME
.SETCIB  ANOP
         MVC   IEDQ&SYSNDX+16(8),0(1) . MOVE TERMNAME INTO CIB
.PASS    ANOP
         AIF   ('&PASSWRD' EQ '').NOPASS
         AIF   (K'&PASSWRD LT 9).OK4
         MNOTE 12,'*** IHB002 INVALID PASSWORD OPERAND SPECIFIED'
         MEXIT
.OK4     ANOP
         BAL   0,*+12 .                 ADDRESS OF PASSWORD
         DC    CL8'&PASSWRD' .          UNSCRAMBLED PASSWORD
         AGO   .LINK
.NOPASS  ANOP
         SR    0,0 .                    INDICATE NO PASSWORD
.LINK    ANOP
         CNOP  0,4
         BAL   1,*+44 .                 SET CIB ADDR IN PARM REG
IEDQ&SYSNDX DS 0F .                     COMMAND INPUT BUFFER
         DC    F'0' .                   OPERATOR CONTROL QCB ADDR
         DC    XL1'E0' .                CIB QUEUING PRIORITY
         DC    XL3'00' .                LINK FIELD
         DC    XL1'87' .                VERB CODE FOR LINE RELEASE
         DC    XL1'1C' .                CIB LENGTH
         DC    XL1'00' .                RELATIVE LINE NUMBER
         DC    XL1'00' .                COMPLETION CODE
         DC    A(IEDQ&SYSNDX+32) .      ECB ADDRESS
         AIF   ('&TERM'(1,1) EQ '(').ZERO
         DC    CL8'&TERM' .             TERMINAL NAME
         AGO   .OK5
.ZERO    ANOP
         DC    XL8'00' .                TERMINAL NAME
.OK5     ANOP
         DC    XL4'00' .                WORKAREA ADDRESS
         DC    A(IEDQ&SYSNDX) .         ADDRESS OF CIB
         DC    F'0' .                   PCB WORKAREA/ECB
         DC    F'0' .                   ADDRESS OF CIB LENGTH
         LINK  EP=IEDQET
         MEND
