         MACRO
&NAME    STATTN &LINES=,&TENS=,&INPUT=
.**********************************************************************
.*                                                                    *
.*RELEASE 23 DELETIONS                                                *
.*                                                                    *
.*RELEASE 22 DELETIONS                                                *
.*                                                                    *
.*RELEASE 21 DELETIONS                                                *
.*                                                                    *
.*STATUS - CHANGE LEVEL 000                                           *
.*                                                                    *
.*FUNCTION - THE STATTN MACRO IS USED TO SPECIFY THE CONDITIONS UNDER *
.*    WHICH AN ATTENTION IS TO BE SIMULATED BY THE TERMINAL HANDLER.  *
.*    THE SIMULATION ATTENTION MAY NOT BE USED FOR THE LINE DELETE    *
.*   CHARACTER ON INPUT.                                              *
.*                                                                    *
.*       THE STANDARD FORM OF THE EXPANSION IS BELOW                  *
.*&NAME  B     *+6                     BRANCH PAST DC'S               *
.*       DC    AL1(10)                  LINES INTEGER                 *
.*       DC    AL1(5)                   TENS INTEGER                  *
.*       SR    0,0                     CLEAR REGISTER                 *
.*       LH    0,*-4                   PUT 2 BYTE PARM. IN REG. 0     *
.*       LA    1,8                      LOAD ENTRY CODE               *
.*       SLL   1,24                     PUT ABOVE IN HIGH ORDER BYTE  *
.*       OR    0,1                      PUT ENTRY CODE IN REG 0       *
.*       CNOP  0,4                                                    *
.*       B     *+8                     BRANCH PAST PARAMETER          *
.*       DC    AL1(244)                 FLAG BYTE                     *
.*       DC    AL3(ADDR)                INPUT ADDRESS                 *
.*       L     1,*-4                   PUT PARAMETER IN REG. 1        *
.*       SVC   94                       ISSUE STATUS SVC              *
.*                                                                    *
.**********************************************************************
         LCLA  &SW
       AIF   ('&LINES' EQ '' AND '&TENS' EQ '' AND '&INPUT' EQ '').ERR
         AIF   ('&LINES' EQ '').NULLNES
&SW      SETA  X'80'
         AIF   ('&LINES'(1,1) EQ '(' OR '&LINES' EQ '0').LINES0
         AIF   (&LINES GT 255 OR &LINES LT 0).ERROR
&NAME    B     *+6                     BRANCH PAST DC'S
         DC    AL1(&LINES)              LINES INTEGER
         AGO   .CKTENS
.LINES0  ANOP
&NAME    B     *+6                     BRANCH PAST DC'S
         DC    X'00'                   LINES=0,IN REG.,NOT SPECIFIED
         AGO   .CKTENS
.NULLNES ANOP
&SW      SETA  X'00'
         AGO   .LINES0
.CKTENS  AIF   ('&TENS' EQ '').TENS0
&SW      SETA  &SW+X'40'
         AIF   ('&TENS'(1,1) EQ '(' OR '&TENS' EQ '0').TENS0
         AIF   (&TENS GT 255 OR &TENS LT 0).ERROR2
         DC    AL1(&TENS)               TENS INTEGER
         AGO   .LOAD0
.TENS0   ANOP
         DC    X'00'                   TENS=0,IN REG.,NOT SPECIFIED
.LOAD0   LH    0,*-2                   PUT 2 BYTE PARM. IN REG. 0
         SLL   0,16                    CLEAR HIGH ORDER
         SRL   0,16                    PORTION OF REG. 0
         AIF   ('&LINES' EQ '').CKTREG
         AIF   ('&LINES'(1,1) NE '(').CKTREG
         LR    1,&LINES                PUT LINES INTEGER IN REG. 1
         SLL   1,24                     TRUNCATE IF MORE THAN 255
         SRL   1,16                     PUT INTEGER IN 3RD BYTE
         OR    0,1                      PUT LINES INTEGER IN REG 0
.CKTREG  AIF   ('&TENS' EQ '').ENTRYCD
         AIF   ('&TENS'(1,1) NE '(').ENTRYCD
         LR    1,&TENS                 PUT TENS INTEGER IN REG. 1
         SLL   1,24                     TRUNCATE IF MORE THAN 255
         SRL   1,24                     PUT INTEGER IN LOW ORDER BYTE
         OR    0,1                      PUT TENS INTEGER IN REG 0
.ENTRYCD LA    1,8                      LOAD ENTRY CODE
         SLL   1,24                     PUT ABOVE IN HIGH ORDER BYTE
         OR    0,1                      PUT ENTRY CODE IN REG 0
.CKINPUT AIF   ('&INPUT' EQ '').LOADSW
&SW      SETA  &SW+X'20'
.LOADSW  ANOP
         CNOP  0,4
         B     *+8                     BRANCH PAST PARAMETER
         DC    AL1(&SW)                FLAG BYTE
         AIF   ('&INPUT' EQ '').INPUT0
         AIF   ('&INPUT'(1,1) EQ '(').INPUT0
         DC    AL3(&INPUT)             INPUT ADDRESS
         AGO   .LOAD1
.INPUT0  ANOP
         DC    AL3(0)
.LOAD1   L     1,*-4                   PUT PARAMETER IN REG. 1
         AIF   ('&INPUT' EQ '').SVC
         AIF   ('&INPUT'(1,1) NE '(').SVC
         LA    &INPUT,0(&INPUT)        CLEAR HIGH ORDER BYTE
         OR    1,&INPUT                PUT PARAMETER IN REG. 1
.SVC     SVC   94                      ISSUE SVC
         MEXIT
         IHBERMAC   24
.ERR     MEXIT
.ERROR   IHBERMAC 54,,&LINES
         MEXIT
.ERROR2  IHBERMAC 54,,&TENS
         MEND
