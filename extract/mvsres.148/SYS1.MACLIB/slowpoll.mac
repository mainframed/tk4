         MACRO
&NAME    SLOWPOLL &MASK,&CONNECT=OR,&SECONDS=60
.*
.* VERSION = 00, LEVEL = 00
.*
         GBLA  &IEDQZE(64)
         GBLB  &IEDQZA(64)
         LCLA  &ALG
         LCLB  &NOGO
.*
.*
.**********************************************************************
.*                                                                    *
.* THE SLOWPOLL MACRO -                                               *
.*   * DELAYS FURTHER POLLING WHEN A LINE ERROR OCCURS,               *
.*   * IS OPTIONAL IN AN INMESSAGE SUBGROUP OF AN MH (AND NOT         *
.*     PERMITTED IN ANY OTHER SUBGROUP),                              *
.*   * MAY BE USED MORE THAN ONCE IN A SUBGROUP.                      *
.*                                                                    *
.* SLOWPOLL SUSPENDS FURTHER POLLING ON A GIVEN LINE WHEN ERRORS      *
.* SPECIFIED BY THE ERROR MASK HAVE OCCURRED. THE BITS SPECIFIED BY   *
.* THE ERROR MASK OPERAND ARE COMPARED TO THE SETTING OF THE BITS IN  *
.* THE MESSAGE ERROR RECORD FOR THIS MESSAGE - IF SPECIFIED BITS IN   *
.* THE MESSAGE ERROR RECORD ARE ON, THE POLLING WILL BE SUSPENDED. BY *
.* SPECIFYING AN ALL-ZERO MASK, OR BY OMITTING THE MASK OPERAND, THE  *
.* ACTION WILL BE UNCONDITIONAL. POLLING WILL RESUME AFTER THE LENGTH *
.* OF TIME SPECIFIED IN THE SECONDS OPERAND.                          *
.*                                                                    *
.* ****************************************************************** *
.* *        *        *                 .   .             .       .  * *
.* *(SYMBOL)*SLOWPOLL*(MASK)(,CONNECT=. AND .)(,SECONDS=. INTEGER .)* *
.* *        *        *                . OR  .           . 60      . * *
.* *        *        *                 .## .             .##     .  * *
.* ****************************************************************** *
.*                                                                    *
.*   SYMBOL -                                                         *
.*     FUNCTION - NAME OF THE MACRO.                                  *
.*     DEFAULT - NONE. SPECIFICATION IS OPTIONAL.                     *
.*     FORMAT - MUST CONFORM TO THE RULES FOR ASSEMBLER LANGUAGE      *
.*              SYMBOLS (SEE THE SYMBOL ENTRY IN THE GLOSSARY).       *
.*                                                                    *
.*   MASK -                                                           *
.*     FUNCTION - SPECIFIES THE FIVE-BYTE BIT CONFIGURATION USED TO   *
.*                TEST THE MESSAGE ERROR RECORD FOR THE MESSAGE (THE  *
.*                MESSAGE ERROR RECORD IS DESCRIBED IN APPENDIX B).   *
.*     DEFAULT - NONE. SPECIFICATION OPTIONAL.                        *
.*     FORMAT - DECIMAL OR HEXADECIMAL. IF HEXADECIMAL FORMAT IS      *
.*              USED, FRAMING CHARACTERS MUST BE USED. IF X'' IS      *
.*              USED, LEADING ZEROES MUST BE CODED, IF XL5'' IS USED, *
.*              LEADING ZEROES MAY BE OMITTED.                        *
.*     MAXIMUM - 16777215 OR A HEXADECIMAL FIELD FIVE BYTES IN        *
.*               LENGTH.                                              *
.*     NOTES - OMITTING THIS OPERAND OR SPECIFYING AN ALL-ZERO MASK   *
.*             CAUSES UNCONDITIONAL EXECUTION.                        *
.*                                                                    *
.*            .   .                                                   *
.*   CONNECT=. AND . -                                                *
.*           . OR  .                                                  *
.*            .## .                                                   *
.*     FUNCTION - SPECIFIES THE TYPE OF LOGICAL CONNECTION TO BE MADE *
.*                BETWEEN THE MASK AND THE MESSAGE ERROR RECORD.      *
.*     DEFAULT - CONNECT=OR.                                          *
.*     FORMAT - AND OR OR.                                            *
.*     NOTES - AND SPECIFIES THAT THE MACRO IS TO BE EXECUTED ONLY IF *
.*             ALL OF THE BITS SPECIFIED BY MASK ARE ON IN THE        *
.*             MESSAGE ERROR RECORD. OR SPECIFIES THAT THE MACRO IS   *
.*             TO BE EXECUTED IF ANY BIT SPECIFIED BY MASK IS ON IN   *
.*             THE MESSAGE ERROR RECORD.                              *
.*                                                                    *
.*            .       .                                               *
.*   SECONDS=. INTEGER . -                                            *
.*           . 60      .                                              *
.*            .##     .                                               *
.*     FUNCTION - SPECIFIES THE LENGTH OF TIME, EXPRESSED IN SECONDS, *
.*                DURING WHICH POLLING ON THE LINE WILL BE SUSPENDED. *
.*     DEFAULT - SECONDS=60.                                          *
.*     FORMAT - DECIMAL.                                              *
.*     MINIMUM - 1.                                                   *
.*     MAXIMUM - 65535.                                               *
.*                                                                    *
.**********************************************************************
.*
.*
         AIF   (NOT &IEDQZA(9)).B20
         MNOTE 12,'***  IHB312  MUST FOLLOW STARTMH MACRO WITH TSOMH=NO*
               '
         AGO   .MEND
.*
.B20     AIF   (&IEDQZA(5)).B30
         MNOTE 12,'***  IHB070  SEQUENCE ERROR-MUST FOLLOW INMSG OR OUT*
               MSG MACRO'
&NOGO    SETB  1
.B30     AIF   ('&MASK' EQ '').B50
         IEDQMASK &MASK,5
         AIF   (&IEDQZA(24)).B40
         AIF   ('&MASK'(1,1) NE 'X').B50
         AIF   ('&MASK'(2,1) EQ 'L' AND '&MASK'(3,1) NE '5').B40
         AIF   ('&MASK'(2,1) NE 'L' AND K'&MASK NE 13).B40
         AGO   .B50
.*
.B40     MNOTE 12,'***  IHB300  MASK OPERAND INVALID AS SPECIFIED'
&NOGO    SETB  1
.B50     AIF   (&SECONDS GT 0 AND &SECONDS LT 65536).B60
         MNOTE 12,'***  IHB300  SECONDS OPERAND INVALID AS SPECIFIED'
&NOGO    SETB  1
.B60     AIF   ('&CONNECT' EQ 'AND' OR '&CONNECT' EQ 'OR').B70
         MNOTE 12,'***  IHB300  CONNECT OPERAND INVALID AS SPECIFIED'
&NOGO    SETB  1
.B70     AIF   (&NOGO).MEND
         SPACE 1
         IEDQVCON 60,IEDQB4
         AIF   ('&MASK' NE '0' AND '&MASK' NE 'X''0000000000''' AND '&M*
               ASK' NE 'XL5''0000000000''' AND '&MASK' NE '').C50
&NAME    DC    AL1(&IEDQZE(60)+1),AL1(8),AL2(0)
         AGO   .C80
.*
.C50     AIF   ('&CONNECT' EQ 'OR').C60
&ALG     SETA  1
.C60     ANOP
&NAME    DC    AL1(&IEDQZE(60)),AL1(12+&ALG),AL1(0)
         AIF   ('&MASK'(1,1) EQ 'X').C70
         DC    FL5'&MASK'          ERROR MASK
         AGO   .C80
.*
.C70     DC    &MASK               ERROR MASK
.C80     DC    AL2(&SECONDS),AL2(0)
.MEND    SPACE 1
         MEND
