         MACRO
&NAME    IEDSHOW &KY,&STATION=**,&RESULT=,&FORMAT=NO,&TARGET=
.*C006900                                                      @YM08126
.*A001300,001800,002300,002800,004700                          @OZ41631
.*C004500                                                      @OZ41631
.* A004745                                                     @OX26340
.*C006200                                                      @OZ50546
.*C004100                                                      @OZ50600
.*A008600                                                      @OZ50600
         GBLA  &IEDQZE(64),&IEDQZK,&IEDQZL(256)
         GBLB  &IEDQZA(64)
         LCLB  &NO
         LCLC  &KYT(10),&SAV
         LCLA  &CB(10),&OFF(10),&LEN(10),&I,&FLG,&LNG,&FMM
         SPACE 1
&KYT(1)  SETC  'TSTATUS'
&KYT(2)  SETC  'ORIGIN'
&KYT(3)  SETC  'DESTIN'
&KYT(4)  SETC  'DATASIZE'
&KYT(5)  SETC  'CURTERM'         CURRENTLY CONNECTED
&KYT(9)  SETC  'SNAVALS'                SNA RH AND SEQUENCE NO @OZ41631
&CB(1)   SETA  4
&CB(2)   SETA  1
&CB(3)   SETA  1
&CB(4)   SETA  1
&CB(5)   SETA  2                  LCB
&CB(9)   SETA  1                  BUFFER                       @OZ41631
&OFF(1)  SETA  255
&OFF(2)  SETA  16
&OFF(3)  SETA  40
&OFF(4)  SETA  18
&OFF(5)  SETA  90           LCB TTCIN
&OFF(9)  SETA  0                        OFFSET PRF1RH          @OZ41631
&LEN(1)  SETA  0
&LEN(2)  SETA  1
&LEN(3)  SETA  1
&LEN(4)  SETA  1
&LEN(5)  SETA  1            LENGTH IS 2
&LEN(9)  SETA  5                        MOVE LENGTH            @OZ41631
&LNG     SETA  12
&SAV     SETC  '0'
         AIF   ('&KY' EQ '').MN1
.LOOP    AIF   (&I GE 10).MN1
&I       SETA  &I+1
         AIF   ('&KY' NE '&KYT(&I)').LOOP
         AIF   ('&STATION' EQ '**').CKFM STATION= NOT SPECIFIED
&FMM     SETA  &FMM+1
         AIF   (&I NE 1).MNZ            STATION= INVALID UNLESS TSTATUS
&LNG     SETA  &LNG+4
.CKST    AIF   ('&STATION'(1,1) EQ '(' OR '&STATION' EQ '''').CKFM
&SAV     SETC  '&STATION'
.CKFM    ANOP                                                  @OZ50600
         AIF   (&I EQ 1 AND '&STATION' EQ '**' AND (NOT &IEDQZA(46))).MX
               N4                                              @OZ50600
.STOK    ANOP                                                  @OZ50600
         AIF   ('&FORMAT' EQ 'NO').CKRS                        @OZ50600
         AIF   ('&FORMAT' NE 'YES').MN3
         AIF   (&I EQ 1 OR  &I EQ 4).MN3  NO FORMAT TSTATUS/BUFSIZE
&FLG     SETA  &FLG+1
.CKRS    ANOP                                                  @OZ41631
&NAME    CNOP  0,4                                             @OZ41631
         AIF   ('&RESULT' NE '' AND '&TARGET' EQ '').NOS1      @OZ41631
         AIF   ('&RESULT' EQ '' AND '&TARGET' NE '').NOS2      @OZ41631
         MNOTE 12,'*** IHB300  RESULT OR TARGET INVALID AS SPECIFIED'
&NO      SETB  1
.NOS1    ANOP
         B     *+4+4                                           @OZ41631
S&SYSNDX DC    A(&RESULT)                                      @OZ41631
         AGO   .REGRES                                         @OZ41631
.NOS2    ANOP                                                  @OZ41631
         B     *+4+4                                           @OZ41631
S&SYSNDX DC    A(&TARGET)
*        LOCOPT &TARGET,,STATION=&STATION                      @OZ41631
         LOCOPT &TARGET,,STATION=&STATION
         AIF   (&IEDQZA(61)).REGRES                            @OZ41631
         LTR   15,15                                           @OZ41631
         BNZ   *+12                                            @OZ41631
         LA    15,12                                           @OZ41631
         B     SHOW&SYSNDX                                     @OZ41631
         ST    15,S&SYSNDX                                     @OZ41631
.REGRES  ANOP                                                  @OZ41631
         AIF   (&I NE 9).NOS            NOT SNAVALS            @OX26340
&FLG     SETA  &FLG+2                   INDICATE NEGATIVE PRF  @OZ41631
.NOS     AIF   (&IEDQZA(46)).CKMH
&LNG     SETA  &LNG+4
         AIF   (&IEDQZA(7)).NOMH
         MNOTE 12,'***IHB070 SEQUENCE ERROR MUST FOLLOW INTRO'
&NO      SETB  1
         AGO   .SBOK
.CKMH    AIF   (NOT &IEDQZA(5)).SBOK
         MNOTE 12,'*** IHB07O SEQUENCE ERROR - MUST FOLLOW INHDR, INBUFX
               , INBLOCK, OUTHDR OF OUTBUF MACRO'
&NO      SETB  1
.SBOK    IEDQVCON 67,IEDQBS
.NOMH    AIF   (&NO).MEND
         AIF   ('&STATION'(1,1) NE '(').GNBL
         ST    &STATION(1),*+20                                @OZ50546
.GNBL    BAL   1,*+&LNG+4
         AIF   (&IEDQZA(46)).GNRDC
         DC    AL1(255,&LNG,0,&FMM)
         AGO   .GNCB
.GNRDC   DC    AL1(&IEDQZE(48),&LNG,&IEDQZL(3),&FMM)
.GNCB    DC    AL1(&FLG,&CB(&I),&OFF(&I),&LEN(&I)),A(S&SYSNDX) @OZ41631
         AIF   (&I NE 1 OR '&STATION' EQ '**').GNUI            @YM08126
         AIF   ('&STATION'(1,1) NE '''').GNAO
         DC    A(IEDQMISC+&IEDQZK)
&SAV     SETC  '&SYSECT'
IEDQMISC CSECT
         DC    C&STATION
&SAV     CSECT
&IEDQZK  SETA  &IEDQZK+K'&STATION
         AGO   .GNUI
.MN1     MNOTE 12,'*** IHB300   FIELD OPERAND INVALID AS SPECIFIED'
&NO      SETB  1
         AGO   .CKFM
.MNZ     MNOTE 12,'***  IHB300  STATION OPERAND INVALID AS SPECIFIED'
&NO      SETB  1
         AGO   .CKST
.MN3     MNOTE 12,'***  IHB300  FORMAT OPERAND INVALID AS SPECIFIED'
&NO      SETB  1
         AGO   .CKRS
.MN4     MNOTE 4,'***  IHB300  TSTATUS STATION=** CODED OUTSIDE MH - MUX
               ST HAVE BUFFER FORMAT TO LOCATE LCB'            @OZ50600
         AGO   .STOK                                           @OZ50600
.GNAO    DC    A(&SAV)
.GNUI    AIF   (&IEDQZA(46)).GOMH
         DC    V(IEDQBS)
         L     15,*-4
         BAL   14,4(,15)
         AGO   .MEND
.GOMH    L     15,IEDUI
         BALR  14,15
.MEND    SPACE 2
SHOW&SYSNDX EQU *                                              @OZ41631
         MEND
