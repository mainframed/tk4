         MACRO
&NAME    PATH  &SW,&OP,&CHARS,&BLANK=YES
         GBLA  &IEDQZD
         GBLB  &IEDQZA(64)
         GBLC  &IEDQZB,&IEDQZC
         LCLA  &A
         LCLB  &NOGO
         LCLC  &PARM,&C
.*             VERSION DATED OCTOBER 16, 1970
.*C110000                                                      @OZ50351
         SPACE
&PARM    SETC  'IEDW'.'&SYSNDX'        SET UP LABEL NAME       @OZ50351
         AIF   ((&IEDQZA(1) OR &IEDQZA(2)) AND NOT &IEDQZA(5)).CHECK
         MNOTE 12,'***  IHB070  SEQUENCE ERROR-MUST FOLLOW INHDR, INBUF*
               , OUTHDR OR OUTBUF MACRO'
&NOGO    SETB  1
.CHECK   AIF   ('&SW' EQ '').SWERR
         IEDQMASK &SW,1
         AIF   (NOT &IEDQZA(24)).CHARS
.SWERR   MNOTE 12,'***  IHB300  SWITCH OPERAND INVALID AS SPECIFIED'
&NOGO    SETB  1
.CHARS   AIF   ('&CHARS' EQ '').ORG
         AIF   (NOT &IEDQZA(9)).TSO001                              TSO
         MNOTE 12,'***  IHB314  CHARACTER OPERAND RESTRICTED WITH TSO M*
               H'
         AGO   .MEND                                                TSO
.TSO001  IEDQCHAR 8,&CHARS
         AIF   (&IEDQZA(24)).ERR
         AIF   (&IEDQZD LE 8).ORG
.ERR     MNOTE 12,'***  IHB300  CHARACTER OPERAND INVALID AS SPECIFIED'
&NOGO    SETB  1
.ORG     AIF   (&NOGO).MEND
         AIF   ('&NAME' EQ '').NON
&NAME    DS    0H
.NON     AIF   ('&CHARS' EQ '').DOIT
         IEDQSCAN &IEDQZD,&CHARS,&BLANK
         AIF   (&IEDQZA(24)).MEND
         LTR   15,15
         BNP   &PARM
         L     1,IEDADBUF .             GET BUFFER ADDRESS
         STH   15,24(,1) .              PUT SCAN IN BUFFER
.DOIT    LOCOPT &OP
         AIF   (&IEDQZA(61)).MEND
         LTR   15,15 .                  OPTION FIELD FOUND
         BZ    *+8 .                    NO - DO NOT SET SWITCH
         AIF   ('&SW'(K'&SW,1) EQ '''').IC
&C       SETC  '&SW'
&A       SETA  &C
         MVI   0(15),&A .               SET SWITCH
         AGO   .MEND
.IC      MVI   0(15),&SW .              SET SWITCH
.MEND    SPACE 2
&PARM    EQU   *
         MEND
