         MACRO
&NAME    FULLSCR &CHARS,&BLANK=YES,&TRIGGER=,&LCOUNT=00,&CARRIER=00,&FUX
               LLO=IEDQFSCR,&EXPFLS=NO                         @OZ35823
         GBLA  &IEDQZD,&IEDQZE(64),&IEDQZL(256)
         GBLB  &IEDQZA(64)
         LCLB  &ERROR
         LCLB  &TROPT,&LCOPT,&CAOPT,&CHRPRES
         LCLB  &EXPY                                           @OZ35823
         LCLA  &LEN
         LCLC  &LABEL,&TRIGERC
.*C000000-999999                                               @OY18039
.*A003500,011000,050500,051000,051500                          @OZ35823
.*C001500                                                      @OZ35823
         AIF   (&IEDQZA(9)).TSOMH
         MNOTE 12,'*** IHB312  MUST FOLLOW STARTMH MACRO WITH TSOMH=YESX
               '
         AGO   .MEND
.TSOMH   ANOP
         AIF   (&IEDQZA(3) OR &IEDQZA(5)).MHERROR
         AGO   .MHNORM
.MHERROR ANOP
         MNOTE 12,'*** IHB070  SEQUENCE ERROR-MUST FOLLOW INBUF, OUTHEAX
               DER, OR OUTBUF MACROS'
&ERROR   SETB  1
.MHNORM  ANOP
         AIF   (('&EXPFLS' EQ 'YES') OR ('&EXPFLS' EQ 'NO')).EXPGOOD
         MNOTE 12,'*** IHB300 EXPFLS OPERAND INVALID AS SPECIFIED'
&ERROR   SETB  1
         AGO   .GOODCOM                                        @OZ35823
.EXPGOOD ANOP                                                  @OZ35823
         AIF   ('&EXPFLS' EQ 'NO').EXPNORM                     @OZ35823
         AIF   ('&CHARS' EQ '' AND '&TRIGGER' EQ '').REGEXP    @OZ35823
         MNOTE 12,'*** IHB300 TRIGGER AND CHARS OPERANDS INVALID AS SPEX
               CIFIED'
&ERROR   SETB  1                                               @OZ35823
         AGO   .GOODCOM                                        @OZ35823
.REGEXP  ANOP                                                  @OZ35823
         AIF   ('&LCOUNT' EQ '00' AND '&CARRIER' EQ '00').GOODEXP
         MNOTE 12,'*** IHB300 LCOUNT AND CARRIER OPERANDS INVALID AS SPX
               ECIFIED'
&ERROR   SETB  1                                               @OZ35823
         AGO   .GOODCOM                                        @OZ35823
.GOODEXP ANOP                                                  @OZ35823
&EXPY    SETB  1                                               @OZ35823
         AGO   .GOODCOM                                        @OZ35823
.EXPNORM ANOP                                                  @OZ35823
         AIF   (('&TRIGGER' NE '' AND '&CHARS' EQ '') OR ('&TRIGGER' EQX
                '' AND '&CHARS' NE '')).GOODCOM
         AIF   ('&TRIGGER' EQ '' AND '&CHARS' EQ '' AND NOT &IEDQZA(2))X
               .GOODCOM
         MNOTE 12,'*** IHB300 TRIGGER AND CHARS OPERANDS INVALID AS SPEX
               CIFIED'
&ERROR   SETB  1
.GOODCOM ANOP
         AIF   (&ERROR).MEND
&LABEL   SETC  'IEDM'.'&SYSNDX'
         AIF   ('&CHARS' EQ '').SUBGRP
         IEDQCHAR 8,&CHARS
         AIF   (NOT &IEDQZA(24)).CHRNORM
         MNOTE 12,'*** IHB300 &CHARS OPERAND INVALID AS SPECIFIED'
&ERROR   SETB  1
.CHRNORM ANOP
         SPACE 1
         IEDQSCAN &IEDQZD,&CHARS,&BLANK
         AIF   (&IEDQZA(24)).MEND
&CHRPRES SETB  1
         LTR   15,15                    CONCHARS FOUND
         LA    15,4                     SET DEFAULT RETURN CODE
         BNP   &LABEL                   BRANCH IF NO
         L     1,IEDADBUF               GET BUFFER ADDRESS
         STH   15,24(,1)                UPDATE SCAN POINTER
.SUBGRP  ANOP
&LEN     SETA  11
&TRIGERC SETC  'X''6D'''
         AIF   ('&TRIGGER' EQ '').LCCHECK
&TRIGERC SETC  '&TRIGGER'
         AIF   ('&TRIGGER'(1,1) EQ 'X' AND '&TRIGGER'(K'&TRIGGER,1) EQ X
               '''').NOTROPT
&TROPT   SETB  1
&LEN     SETA  &LEN+4
         AGO   .LCCHECK
.NOTROPT ANOP
         AIF   (K'&TRIGGER NE 5).TRERROR
         AIF   ('&TRIGGER'(2,1) NE '''' AND '&TRIGGER'(5,1) NE '''').TRX
               ERROR
         AGO   .TRSET
.TRERROR ANOP
         MNOTE 8,'*** IHB289  TRIGGER VALUE MUST BE A HEXADECIMAL DIGITX
               '
&ERROR   SETB  1
         AGO   .LCCHECK
.TRSET   ANOP
         AIF   ('&TRIGGER' NE 'X''00''' AND '&TRIGGER' NE 'X''FF''' ANDX
                &IEDQZA(2)).OUTERR
         AGO   .LCCHECK
.OUTERR  ANOP
         MNOTE 12,'*** IHB300 &TRIGGER OPERAND INVALID AS SPECIFIED'
&ERROR   SETB  1
.LCCHECK ANOP
         AIF   (T'&LCOUNT EQ 'N').LCLIM
&LCOPT   SETB  1
&LEN     SETA  &LEN+4
         AGO   .CACHECK
.LCLIM   ANOP
         AIF   (&LCOUNT LE 24).CACHECK
         MNOTE 8,'*** IHB290  LCOUNT VALUE MUST BE LESS THAN OR EQUAL TX
               O DECIMAL 24'
&ERROR   SETB  1
.CACHECK ANOP
         AIF   (T'&CARRIER EQ 'N').CALIM
&CAOPT   SETB  1
&LEN     SETA  &LEN+4
         AGO   .ALCHECK
.CALIM   ANOP
         AIF   (&CARRIER LE 80).ALCHECK
         MNOTE 8,'*** IHB291  CARRIAGE VALUE MUST BE LESS THAN OR EQUALX
                TO DECIMAL 80'
&ERROR   SETB  1
.ALCHECK ANOP
         AIF   (&ERROR).MEND
         SPACE 1
         IEDQVCON 69,IEDQFSC
         BAL   1,*+5+&LEN
         DC    AL1(&IEDQZE(48)),AL1(&LEN)
         DC    AL1(&IEDQZL(5))
         AIF   (NOT &EXPY).REGFS                               @OZ35823
         DC    BL1'&LCOPT&CAOPT&TROPT&CHRPRES&EXPY.000'
         AGO   .EXPFS                                          @OZ35823
.REGFS   ANOP                                                  @OZ35823
         DC    BL1'&LCOPT&CAOPT&TROPT&CHRPRES.0000'
.EXPFS   ANOP                                                  @OZ35823
         AIF   (&LCOPT).LOPTION
         AIF   (NOT &EXPY).REGLC                               @OZ35823
         DC    AL1(2)                                          @OZ35823
         AGO   .DOCA                                           @OZ35823
.REGLC   ANOP                                                  @OZ35823
         DC    AL1(&LCOUNT)
         AGO   .DOCA
.LOPTION ANOP
         DC    AL1(0)
.DOCA    ANOP
         AIF   (&CAOPT).COPTION
         DC    AL1(&CARRIER)
         AGO   .CKOPT
.COPTION ANOP
         DC    AL1(0)
.CKOPT   ANOP
         AIF   (&TROPT OR &CHRPRES).TOPTION
         DC    &TRIGERC
         AGO   .ALDON
.TOPTION ANOP
         DC    AL1(0)
.ALDON   ANOP
         IEDQVCON 31,IEDQAE
         DC    AL1(&IEDQZE(31)),AL1(4),AL1(&FULLO),AL1(03)
         AIF   (NOT &LCOPT).CAGEN
         DC    AL1(&IEDQZE(31)),AL1(4),AL1(&LCOUNT),AL1(05)
.CAGEN   ANOP
         AIF   (NOT &CAOPT).TRGEN
         DC    AL1(&IEDQZE(31)),AL1(4),AL1(&CARRIER),AL1(05)
.TRGEN   ANOP
         AIF   (NOT &TROPT).GENCODE
         DC    AL1(&IEDQZE(31)),AL1(4),AL1(&TRIGGER),AL1(05)
.GENCODE ANOP
         DC    XL1'00'
         L     15,IEDUI
         BALR  14,15
&LABEL   EQU   *
         SPACE 2
.MEND    ANOP
         MEND
