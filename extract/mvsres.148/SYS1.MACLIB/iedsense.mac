         MACRO
&NAME    IEDSENSE  &AREA=
         GBLB  &IEDQZA(64)
         GBLA  &IEDQZE(64)
         LCLB  &NOGO,&SYMBOLC
         LCLA  &LENGTH
         LCLC  &AREANME,&AREAREG
         SPACE 4
.* A000000-999999                                             @X50X9MM
.* A370000                                                    @ZM41043
.* C750000                                                    @ZM41044
.* D280000-320000                                             @Y17XAMG
.* LENGTH IS THE TOTAL LENGTH OF THE PARAMETER LIST
.* AREANME  IS THE SYMBOLIC NAME TO INDICATE THE USER AREA
.* NOGO IS A SWITCH INDICATE THAT IEDSENSE AS SPECIFIED IS INCORRECT
.* AREAREG IS THE REGISTER WHICH CONTAINS THE ADDRESS OF THE AREA
.* SYMBOLC  IS A BIT TO INDICATE THAT THE USER AREA ADDRESS IS
.*               SPECIFIED BY A SYMBOLIC NAME.
.* IEDSENSE MACRO MUST FOLLOW STARTMH MACRO WITH TSOMH=NO SPECIFIED
.* THIS MACRO CAN ONLY BE CODED IN THE FOLLOWING SUBGROUPS: INHDR,
.*              INBLOCK,INBUF,OUTHDR,AND OUTBUF MACROS.
         SPACE 4
&LENGTH  SETA  2
         AIF   (&IEDQZA(46)).STARTMH
         MNOTE 12,'*** IHB070 SEQUENCE ERROR - MUST FOLLOW STARTMH MACR*
                O'
&NOGO    SETB  1
.STARTMH ANOP
         AIF   (NOT &IEDQZA(5)).SUBGRP
         MNOTE 12,'*** IHB070 SEQUENCE ERROR - MUST FOLLOW INHDR,INBUF,*
                INBLOCK,OUTDHR OR OUTBUF MACRO'
&NOGO    SETB  1
         AGO   .CKOPRND                                        @ZM41043
.SUBGRP  ANOP
         AIF   (&IEDQZA(6) OR &IEDQZA(1)).CKOPRND
         MNOTE 12,'*** IHB070 SEQUENCE ERROR - MUST FOLLOW INHDR,INBUF,*
                INBLOCK,OUTHDR OR OUTBUF MACRO'
&NOGO    SETB  1
.CKOPRND ANOP
         AIF   ('&AREA' NE '').CONTCKS
         MNOTE 12,'*** IHB004 REQUIRED OPERAND NOT SPECIFIED'
&NOGO    SETB  1
         AGO   .VALDFRM
.CONTCKS ANOP
         AIF   ('&AREA'(1,1) EQ '(').REGISTR
         AIF   ('&AREA'(1,1) LE 'Z').VALDNME
         MNOTE 12,'*** IHB300 AREA OPERAND INVALID AS SPECIFIED'
&NOGO    SETB  1
.VALDNME ANOP
&AREANME SETC '&AREA'
&SYMBOLC SETB  1
&LENGTH  SETA  &LENGTH+4
         AGO   .VALDFRM
.REGISTR ANOP
&AREAREG SETC  '&AREA'(2,K'&AREA-2)
         AIF   ('&AREA'(2,1) LE 'Z').VALDREG
         AIF   ('&AREAREG' GE '2' AND '&AREAREG' LE '12').VALDREG
.BADFRMT ANOP
         MNOTE 12,'*** IHB300 AREA OPERAND INVALID AS SPECIFIED'
&NOGO    SETB  1
         AGO   .VALDFRM
.VALDREG ANOP
&LENGTH  SETA  &LENGTH+2
.VALDFRM ANOP
         AIF   (&NOGO).MEND
&NAME    DS    0H
         IEDQVCON 19,IEDSNS .           MODULE TO DO THE FUNCT @YM08466
         CNOP  2,4
         BAL   1,*+&LENGTH+4 .
         DC    AL1(&IEDQZE(19)) .       OFFSET IN AVTMSGS      @YM08466
         DC    AL1(&LENGTH) .           LENGTH OF THE PARAMETER LIST
         AIF   (&SYMBOLC).GENACON
         DC    AL1(&AREAREG) .          REG WHICH CONTAINS ADDRESS
*                                       OF AREA IF SPECIFIED
         DC     X'00'                   CLEAR UNUSED BYTE
         AGO   .GETOUT
.GENACON ANOP
         DC    A(&AREANME) .            ADDRESS OF AREA IF SPECIFIED
.GETOUT  ANOP
         L     15,IEDUI
         BALR  14,15
.MEND    ANOP
         MEND
