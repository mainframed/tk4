         MACRO
&MACNAM  STTRAN &FLG,&TAB=,&NAME=,&TCHAR=,&SCHAR=,&LIST=,&MF=I
.**********************************************************************
.*                                                                    *
.* STATUS - CHANGE LEVEL 000                                          *
.*                                                                    *
.* NAME - STTRAN                                                      *
.*                                                                    *
.* FUNCTION - THE STTRAN MACRO IS USED TO:                            *
.*            1-SET UP THE USE OF A USER DEFINED TRANSLATE TABLE      *
.*            2-DISCONTINUE THE USE OF A USER DEFINED TRANSLATE TABLE *
.*            3-ESTABLISH SPECIFIC TRANSLATIONS IN A USER TABLE OR    *
.*              A DEFAULT TABLE, CHARACTER BY CHARACTER               *
.*            4-DISCONTINUE PREVIOUS SPECIFIC CHARACTER TRANSLATIONS  *
.*                                                                    *
.*       THE MACRO PROTOTYPE IS LISTED ABOVE                          *
.*                                                                    *
.*       THE STANDARD FORM OF EXPANSION FOR MF=I:                     *
.*                                                                    *
.*         STTRAN TAB=MYTAB,NAME=TABNAME                              *
.*         CNOP  0,4                                                  *
.*         BAL   1,*+16                  BRANCH AROUND PARAMETERS     *
.*         DC    A(MYTAB)                ADDRESS OF TABLE             *
.*         DC    A(TABNAME)              ADDRESS OF NAME              *
.*         DC    A(0)                    ADDRESS OF LIST PARAMETERS   *
.*         LA    0,3840+32               SET ENTRY CODE AND FLAGS     *
.*         SLL   0,16                    SHIFT TO HIGH ORDER BYTES    *
.*         SVC   94                      TERMINAL CONTROL MACROS SVC  *
.*                                                                    *
.*       THE STANDARD FORM OF EXPANSION FOR MF=L:                     *
.*                                                                    *
.*         STTRAN LIST=LISTPARM,MF=L                                  *
.*         DS    0F                                                   *
.*         DC    A(0)                    ADDRESS OF TABLE             *
.*         DC    A(0)                    ADDRESS OF NAME              *
.*         DC    A(LISTPARM)             ADDRESS OF LIST PARAMETERS   *
.*                                                                    *
.*       THE STANDARD FORM OF EXPANSION FOR MF=E:                     *
.*                                                                    *
.*         STTRAN NOCHAR,NAME=TABNAME,MF=(E,MYLIST)                   *
.*         LA    1,MYLIST                LOAD PARAMETER REG1          *
.*         LA    0,TABNAME               LOAD ADDRESS OF NAME         *
.*         ST    0,4(,1)                 STORE NAME ADDR IN PARM LIST *
.*         LA    0,3840+64               SET ENTRY CODE AND FLAGS     *
.*         SLL   0,16                    SHIFT TO HIGH ORDER BYTES    *
.*         SVC   94                      TERMINAL CONTROL MACROS SVC  *
.*                                                                    *
.**********************************************************************
         LCLA  &SYSFLG
         LCLA  &SYSEC
&SYSEC   SETA  3840
         AIF   ('&MF' EQ 'I' AND '&FLG' EQ '' AND '&TAB' EQ '' AND '&TC*
               HAR' EQ '' AND '&LIST' EQ '' AND '&NAME' EQ '' AND '&SCH*
               AR' EQ '').ERROR1
         AIF   ('&FLG' EQ 'NOTRAN' AND ('&TAB' NE '' OR '&NAME' NE '' O*
               R '&TCHAR' NE '' OR '&SCHAR' NE '')).ERROR1
         AIF   ('&FLG' EQ 'NOTRAN').TOPMAC
         AIF   ('&FLG' EQ 'NOCHAR' AND ('&TAB' NE '' OR '&TCHAR' NE '' *
               OR '&SCHAR' NE '')).ERROR1
         AIF   ('&FLG' EQ 'NOCHAR' AND '&NAME' EQ '' AND '&MF' EQ 'I').*
               ERROR2
         AIF   ('&FLG' EQ 'NOCHAR').TOPMAC
         AIF   ('&FLG' NE '').ERROR3
         AIF   ('&TAB' NE '' AND '&NAME' EQ '' AND '&MF' EQ 'I').ERROR2
         AIF   ('&TCHAR' EQ '' AND '&SCHAR' NE '' AND '&MF' NE 'L').ERR*
               OR4
         AIF   ('&TCHAR' NE '' AND '&SCHAR' EQ '' AND '&MF' NE 'L').ERR*
               OR5
         AIF   ('&NAME' NE '' AND ('&TAB' EQ '' OR '&FLG' EQ 'NOCHAR') *
               AND '&MF' EQ 'I').ERROR6
.TOPMAC  ANOP
         AIF   ('&FLG' EQ 'NOTRAN' AND '&LIST' EQ '' AND '&MF' NE 'L').*
               NOTRN
         AIF   ('&MF' EQ 'L').LNAME
         AIF   ('&MF(1)' EQ 'E').ENAME
         AIF   ('&MF' NE 'I').ERROR1
&MACNAM  CNOP  0,4
         BAL   1,*+16                   BRANCH AROUND PARAMETERS
         AGO   .DCONST
.ENAME   ANOP
         IHBOPLST ,,&MACNAM,MF=&MF
         AGO   .STADDS
.LNAME   ANOP
&MACNAM  DS    0F
.DCONST  AIF   ('&TAB' EQ '').TREG
         AIF   ('&TAB'(1,1) EQ '(').TREG
         DC    A(&TAB)                  ADDRESS OF TABLE
         AGO   .DNAME
.TREG    ANOP
         DC    A(0)                     ADDRESS OF TABLE
.DNAME   AIF   ('&NAME' EQ '').NREG
         AIF   ('&NAME'(1,1) EQ '(').NREG
         DC    A(&NAME)                 ADDRESS OF NAME
         AGO   .DLIST
.NREG    ANOP
         DC    A(0)                     ADDRESS OF NAME
.DLIST   AIF   ('&LIST' EQ '').LREG
         AIF   ('&LIST'(1,1) EQ '(').LREG
         DC    A(&LIST)                 ADDRESS OF LIST PARAMETERS
         AGO   .CKMF
.LREG    ANOP
         DC    A(0)                     ADDRESS OF LIST PARAMETERS
.CKMF    AIF   ('&MF' EQ 'L').LEND
.STADDS  ANOP
         AIF   ('&TAB' EQ '').STNAM
         AIF   ('&TAB'(1,1) NE '(' AND '&MF' EQ 'I').STNAM
         AIF   ('&TAB'(1,1) NE '(').STTABA
         ST    &TAB(1),0(,1)            STORE TABLE ADDR IN PARM LIST
         AGO   .STNAM
.STTABA  ANOP
         LA    0,&TAB                   LOAD ADDRESS OF TABLE
         ST    0,0(,1)                  STORE TABLE ADDR IN PARM LIST
.STNAM   ANOP
         AIF   ('&NAME' EQ '').STLIST
         AIF   ('&NAME'(1,1) NE '(' AND '&MF' EQ 'I').STLIST
         AIF   ('&NAME'(1,1) NE '(').STNAMA
         ST    &NAME(1),4(,1)           STORE NAME ADDR IN PARM LIST
         AGO   .STLIST
.STNAMA  ANOP
         LA    0,&NAME                  LOAD ADDRESS OF NAME
         ST    0,4(,1)                  STORE NAME ADDR IN PARM LIST
.STLIST  ANOP
         AIF   ('&LIST' EQ '').LOAD0
         AIF   ('&LIST'(1,1) NE '(' AND '&MF' EQ 'I').LOAD0
         AIF   ('&LIST'(1,1) NE '(').STLSTA
         ST    &LIST(1),8(,1)           STORE LIST ADDR IN PARM LIST
         AGO   .LOAD0
.STLSTA  ANOP
         LA    0,&LIST                  LOAD ADDRESS OF LIST PARMS
         ST    0,8(,1)                  STORE LIST ADDR IN PARM LIST
.NOTRN   ANOP
         SR    1,1                      ZERO REGISTER 1
.LOAD0   ANOP
         AIF   ('&FLG' EQ '').CKCHARS
         AIF   ('&FLG' EQ 'NOCHAR').STNOCH
&SYSFLG  SETA  128
         AGO   .STREG0
.STNOCH  ANOP
&SYSFLG  SETA  64
         AGO   .STREG0
.CKCHARS AIF   ('&TCHAR' EQ '').NOFLG
&SYSFLG  SETA  32
         AGO   .STREG0
.NOFLG   ANOP
&SYSFLG  SETA  0
.STREG0  ANOP
         LA    0,&SYSEC+&SYSFLG         SET ENTRY CODE AND FLAGS
         SLL   0,16                     SHIFT TO HIGH ORDER BYTES
         AIF   ('&TCHAR' EQ '').STSVC
         AIF   ('&TCHAR'(1,1) EQ '(').TCREG
         ICM   0,2,&TCHAR               SET TERMINAL CHARACTER
         AGO   .STSCHAR
.TCREG   ANOP
         ICM   0,2,0(&TCHAR(1))         SET TERMINAL CHARACTER
.STSCHAR AIF   ('&SCHAR'(1,1) EQ '(').SCREG
         ICM   0,1,&SCHAR               SET SYSTEM CHARACTER
         AGO   .STSVC
.SCREG   ANOP
         ICM   0,1,0(&SCHAR(1))         SET SYSTEM CHARACTER
.STSVC   ANOP
         SVC   94                       TERMINAL CONTROL MACROS SVC
         MEXIT
.LEND    AIF   ('&TCHAR' EQ '' AND '&SCHAR' EQ '').END
         IHBERMAC 1016,TCHAR/SCHAR,MF-L
.END     MEXIT
.ERROR1  ANOP
         IHBERMAC 54,,,
         MEXIT
.ERROR2  ANOP
         IHBERMAC 1006,NAME
         MEXIT
.ERROR3  ANOP
         IHBERMAC 1008,&FLG,1
         MEXIT
.ERROR4  ANOP
         IHBERMAC 1010,SCHAR,TCHAR
         MEXIT
.ERROR5  ANOP
         IHBERMAC 1010,TCHAR,SCHAR
         MEXIT
.ERROR6  ANOP
         IHBERMAC 1010,NAME,TAB/NOCHAR
         MEXIT
         MEND
