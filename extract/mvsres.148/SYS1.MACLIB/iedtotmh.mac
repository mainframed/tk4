         MACRO
         IEDTOTMH
         GBLB  &IEDQRN(8)
.*A000000-999999                                               @Y17XAUV
.*D870000                                                      @OZ27198
.*A426000                                                      @OX21177
*    ... STARTMH MH=TOTE,LU=YES         LU=TO INSURE ADDR IN MH TAB
IEDTOTMH STARTMH MH=TOTE,LU=YES
*    ... INHDR ...
         INHDR
*    ... SETSCAN 0,BLANK=NO
         SETSCAN 0,BLANK=NO
         LTR   15,15                    ZERO LENGTH BUFFER     @YM08076
         BM    IEDTOT10                 YES - BRANCH TO INMSG  @YM08076
         LR    6,15                     COPY ADDR OF DATA
*    ... IEDSHOW TSTATUS,RESULT=IEDTOTMG+4
         IEDSHOW TSTATUS,RESULT=IEDTOTMG+4
         TM    IEDTOTMG+7,X'40'
         BZ    IEDTOT01                 BRANCH NOT TEST FLOW
*    ... IEDRH RHIND=(+DFC)
         IEDRH RHIND=(+DFC)
         LTR   15,15                    DFC CMND ?
         BNZ   IEDTOT22                 NO - BRANCH
         CLI   1(6),X'04'               LUSTAT ?               @YM08076
         BNE   IEDTOT22                 NO - DO NOT RELEASE QUEUE
*    ... IEDRELS                        TERM NOW ABLE TO RECIEVE OUTPUT
         IEDRELS
IEDTOT22 DS    0H
*    ... FORWARD TASK=TOTE
         FORWARD TASK=TOTE
         L     6,IEDADBUF               BUFFER ADDRESS
         L     3,12(,6)                 LCB ADDRESS
         NI    71(3),X'FE'              RESET 'RESPONSE OWED' BIT
         NI    104(3),X'FB'             RESET 'RESP TO RTR OWED'
         NI    112(3),X'7F'             RESET 'RESP TO CANCEL OWED'
         B     IEDTOT02                 BRANCH TO INBUF
IEDTOT01 DS    0H                       CONTROL FLOW
*    ... IEDRH RHIND=(+EXR)
         IEDRH RHIND=(+EXR)
         LTR   15,15
         BNZ   IEDTOT03                 BRANCH IF NOT EXR
*    ... TERRSET
         TERRSET
         B     IEDTOT10                 BRANCH TO INMSG
IEDTOT03 DS    0H
*    ... IEDRH RHIND=(+DFC)
         IEDRH RHIND=(+DFC)
         LTR   15,15
         BZ    IEDTOT04                 BRANCH IF DFC
*    ... IEDRH RHIND=(+BB)
         IEDRH RHIND=(+BB)
         LTR   15,15
         BNZ   IEDTOT20
*    ... TERRSET                        ERROR ON BB - UNAUTORIZED INPUT
         TERRSET
         B     IEDTOT10
IEDTOT20 DS    0H
*    ... SETSCAN C'IEDTOTE/',BLANK=NO,MOVE=RETURN
         SETSCAN C'IEDTOTE/',BLANK=NO,MOVE=RETURN
         LTR   15,15                    WAS STRING FOUND ?
         BZ    IEDTOT09                 NO - CHECK IF 3270
*    ... SETSCAN C'IEDTOTE/',BLANK=NO
         SETSCAN C'IEDTOTE/',BLANK=NO
         B     IEDTOT18
IEDTOT09 TM    IEDTOTMG+7,X'02'         3270 ?
         BNO   IEDTOT18                 NO - BRANCH            @YM08175
         CLI   1(6),X'7D'               TEST ADDRESS ID BYTE
         BNE   IEDTOT02                 BRANCH IF NOT 'ENTER'
*    ... SETSCAN 3,BLANK=NO             BUMP PAST AID,CUR,CUR
         SETSCAN 3,BLANK=NO
IEDTOT18 DS    0H
*    ... FORWARD TASK=TOTE
         FORWARD TASK=TOTE
         B     IEDTOT02
IEDTOT04 DS    0H
*    ... MSGTYPE TABLE=IEDTOTBL,EXIT=IEDTOT02
         MSGTYPE TABLE=IEDTOTBL,EXIT=IEDTOT02
IEDTOTBL DS    0H
*    ... TYPETABL X'C2',ROUTINE=IEDTOT05     RSHUTD
         TYPETABL X'C2',ROUTINE=IEDTOT05     RSHUTD
*    ... TYPETABL X'05',ROUTINE=IEDTOT06     RTR
         TYPETABL X'05',ROUTINE=IEDTOT06     RTR
*    ... TYPETABL X'C9',ROUTINE=IEDTOT05     SIGNAL
         TYPETABL X'C9',ROUTINE=IEDTOT05     SIGNAL
*    ... TYPETABL X'04',ROUTINE=IEDTOT08     LUSTAT
         TYPETABL X'04',ROUTINE=IEDTOT08     LUSTAT
*    ... TYPETABL X'83',ROUTINE=IEDTOT18     CANCEL
         TYPETABL X'83',ROUTINE=IEDTOT18     CANCEL
IEDTOT05 DS    0H
*    ... TERRSET
         TERRSET
*    ... SETSCAN  1,POINT=BACK,BLANK=NO
         SETSCAN  1,POINT=BACK,BLANK=NO
         B     IEDTOT10
IEDTOT06 DS    0H
*    ... IEDRELS
         IEDRELS
*    ... SETSCAN 1,POINT=BACK,BLANK=NO
         SETSCAN 1,POINT=BACK,BLANK=NO
         B     IEDTOT02
IEDTOT08 DS    0H
*    ... MSGTYPE X'0001'
         MSGTYPE X'0001'
*    ... IEDRELS
         IEDRELS
*    ... SETSCAN  2,POINT=BACK,BLANK=NO
         SETSCAN  2,POINT=BACK,BLANK=NO
*    ... MSGTYPE X'081C'
         MSGTYPE X'081C'
*    ... TERRSET
         TERRSET
*    ... IEDRELS
         IEDRELS
*    ... SETSCAN  2,POINT=BACK,BLANK=NO
         SETSCAN  2,POINT=BACK,BLANK=NO
*    ... MSGTYPE
         MSGTYPE
IEDTOT02 DS    0H
*    ... INBUF ...
         INBUF
*    ... IEDRH RHIND=(+CHNGDIR)
         IEDRH RHIND=(+CHNGDIR)
         LTR   15,15
         BNZ   IEDTOT10
*    ... IEDRELS
         IEDRELS
IEDTOT10 EQU   *
*    ... INMSG ...
         INMSG
*    ... ERRORMSG X'0000080000',DATA=IEDTOTMG,EXIT=IEDTOTEX,HEADER=NO, C
               TASK=TOTE
         ERRORMSG X'0000080000',DATA=IEDTOTMG,EXIT=IEDTOTEX,HEADER=NO, C
               TASK=TOTE
*    ... INEND ...
         INEND
*    ... OUTHDR ...
         OUTHDR
*    ... IEDSHOW TSTATUS,RESULT=IEDTOTMG+4
         IEDSHOW TSTATUS,RESULT=IEDTOTMG+4
         AIF   (&IEDQRN(3)).ONLNA1
         AIF   (NOT &IEDQRN(4)).NOSNA1                         @OX21177
         CLI   IEDTOTMG+4,X'40'
         BNL   IEDTOT31                 BRANCH NOT SNA
.ONLNA1  ANOP
*    ... SETSCAN 0,BLANK=NO             LOCATE FIRST BYTE OF DATA
         SETSCAN 0,BLANK=NO
         LTR   15,15                    ZERO LENGTH BUFFER ?   @YM08076
         BNM   IEDTOT30                 NO - BRANCH            @YM08076
         TM    IEDTOTMG+7,X'40'         TEST FLOW ?            @YM08076
         BO    IEDTOT41                 YES - GO TO TEST OUTMSG@YM08076
         B     IEDTOT51                 NO - TO CONTROL OUTMSG @YM08076
IEDTOT30 L     2,IEDADBUF               BUFFER
         LA    3,8                      PREFIX SIZE
         L     4,12(,2)                 LCB
         SLR   2,3                      BACK UP TO PREFIX
         LH    3,32(2)                  SCAN POINTER
         LA    3,20(3,2)                1ST DATA BYTE
         MVC   0(3,2),0(3)              MOVE RH
         SLR   5,5
         IC    5,28(4)                  ISIZE
         LA    5,3(,5)                  BUMP
         STC   5,28(4)                  RESTORE ISIZE
*    ... SETSCAN 3,BLANK=NO             BUMP PAST R4
         SETSCAN 3,BLANK=NO             BUMP PAST R4
         TM    2(2),X'20'               CHANGE DIRECTION ON ?  @YM08076
         BZ    IEDTOT42                 NO - BRANCH
*    ... HOLD
         HOLD
IEDTOT42 TM    IEDTOTMG+7,X'40'
         BO    IEDTOT41                 BRANCH NOT CONTROL
         TM    IEDTOTMG+7,X'02'         IS THIS A 3270
         BO    IEDTOT52                 BRANCH YES
*    ... MSGEDIT ((I,X'15',1,)),LAST=YES
IEDTOT60 MSGEDIT ((I,X'15',1,)),LAST=YES
         B     IEDTOT51
IEDTOT52 MVC   1(2,3),=X'F13F'          WRITE COMMAND SEQUENCE
*    ... SETSCAN 2,POINT=BACK,BLANK=NO
         SETSCAN 2,POINT=BACK,BLANK=NO
         SH    5,=H'2'                  DECREMENT ISIZE
         STC   5,28(4)                   AND STORE
*    ... MSGEDIT ((I,X'4013',1,)),LAST=YES BLANK,INSERT CURSOR
IEDTOT70 MSGEDIT ((I,X'4013',1,)),LAST=YES
*    ... OUTMSG                         OUTMSG FOR SNA CONTROL FLOW
IEDTOT51 OUTMSG                                                @YM08076
*    ... HOLD     X'0004000002'
         HOLD     X'0004000002'
*    ... HOLD     X'0004000012'
         HOLD     X'0004000012'
*    ... ERRORMSG X'0004052600',DATA=IEDTOTMG,EXIT=IEDTOTEX,HEADER=NO, C
               TASK=TOTE
         ERRORMSG X'0004052600',DATA=IEDTOTMG,EXIT=IEDTOTEX,HEADER=NO, C
               TASK=TOTE
*    ..  OUTMSG                         OUTMSG FOR SNA TEST FLOW
IEDTOT41 OUTMSG                                                @YM08076
*    ... HOLD     X'0004000002'
         HOLD     X'0004000002'
*    ... HOLD     X'0004000012'
         HOLD     X'0004000012'
*    ... ERRORMSG X'0000002000',DATA=IEDTOTM2,EXIT=IEDTOTEX,HEADER=NO, C
               TASK=TOTE
         ERRORMSG X'0000002000',DATA=IEDTOTM2,EXIT=IEDTOTEX,HEADER=NO, C
               TASK=TOTE
*    ... ERRORMSG X'0000002000',DATA=IEDTOTMG,EXIT=IEDTOTEX,HEADER=NO, C
               TASK=TOTE,CONNECT=NAND
         ERRORMSG X'0000002000',DATA=IEDTOTMG,EXIT=IEDTOTEX,HEADER=NO, C
               TASK=TOTE,CONNECT=NAND
         AIF   (&IEDQRN(3)).ONLNA2
.NOSNA1  ANOP
*    ... OUTMSG ...                     OUTMSG FOR PRE-SNA
IEDTOT31 OUTMSG                                                @YM08076
*    ... ERRORMSG X'01040301FF',DATA=IEDTOTMG,EXIT=IEDTOTEX,HEADER=NO, C
               TASK=TOTE
         ERRORMSG X'01040301FF',DATA=IEDTOTMG,EXIT=IEDTOTEX,HEADER=NO, C
               TASK=TOTE
.ONLNA2  ANOP
*    ... OUTEND ...
         OUTEND
IEDTOTEX DS    0H
         LR    11,14              SAVE RETURN ADDRESS
         LR    12,15              COPY ENTRY POINT ADDRESS
         LR    5,13               COPY SAVE AREA ADDRESS
         LA    13,IEDSAVE4        SAVE AREA FOR IEDSHOW MACRO
         LR    9,1                COPY BUFFER ADDRESS          @YM08459
         USING IEDTOTEX,12        SET UP BASE REG FOR EXIT
         AIF   (NOT &IEDQRN(4)).NOSNA11
*    ... IEDSHOW TSTATUS,RESULT=IEDTOTMG+4
         IEDSHOW TSTATUS,RESULT=IEDTOTMG+4
.NOSNA11 ANOP
*    ... IEDSHOW CURTERM,RESULT=IEDTOTMG+2
         IEDSHOW CURTERM,RESULT=IEDTOTMG+2
         MVI   IEDTOTMG+4,0       CLEAR BYTE 4
         LTR   6,6
         AIF   (&IEDQRN(3)).ONLSN1
         BM    IEDTOTE6           BRANCH IF PRE-SNA TERMINAL   @YM08534
.ONLSN1  BP    IEDTOTE7           BRANCH IF NEGATIVE RESP.     @YM08534
         ICM   6,7,=X'838000'     SET RH FOR POSITIVE RESP.    @YM08534
IEDTOTE7 OI    IEDTOTMG+4,X'80'   SET FLAG INDICATING SNA
         TM    IEDTOTMG+7,X'40'   IS THIS A TEST DEVICE ?
         BO    IEDTOTE5           YES - BRANCH
         L     6,12(9)            LCB ADDRESS                  @YM08459
         L     7,92(6)            SCB ADDRESS                  @YM08139
         MVC   IEDTOTMG+5(4),16(7) FIRST 4 BYTES OF ERR RECORD @YM08139
         MVC   IEDTOTMG+9(1),39(6) SNA MINOR SENSE             @YM08139
         B     IEDTOTE3           BRANCH AROUND TEST DEV SETUP @YM08139
IEDTOTE5 OI    IEDTOTMG+4,X'40'   SET FLAG INDICATING TEST DEVICE
         STCM  6,7,IEDTOTMG+5     SET RH IN MESSAGE
         STCM  7,15,IEDTOTMG+8    SET SENSE
IEDTOTE3 DS    0H
         AIF   (&IEDQRN(3)).ONLSN3
         B     IEDTOTE4           GO RETURN
IEDTOTE6 L     6,12(9)            LCB ADDRESS                  @YM08459
         L     7,92(6)            SCB ADDRESS                  @YM08076
         MVC   IEDTOTMG+5(4),16(7) FIRST 4 BYTES OF ERROR RECORD
         MVC   IEDTOTMG+9(1),34(6) ERROR RECORD SENSE BYTE
IEDTOTE4 DS    0H
.ONLSN3  LR    14,11              RESTORE RETURN ADDRESS
         LR    1,9                RESTORE BUFFER ADDRESS       @YM08459
         LR    13,5               RESTORE SAVE AREA ADDRESS
         BR    14                 RETURN TO MESSAGE HANDLER
         SPACE 2
IEDTOTMG DC    AL1(11)            LENGTH OF MESSAGE
         DC    X'01'              TOTE ERRORMSG TYPE 1
         DC    C'0000000000'
         AIF   (NOT &IEDQRN(4)).NOSNA33
IEDTOTM2 DC    AL1(11)            LENGTH OF MESSAGE
         DC    X'02'              TOTE ERRORMSG TYPE 2 - BIND ERROR
         DC    C'0000000000'
.NOSNA33 ANOP
         LTORG
         MEND
