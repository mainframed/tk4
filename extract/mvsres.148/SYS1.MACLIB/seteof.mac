         MACRO
&NAME    SETEOF &CHARS,&BLANK=YES
         GBLA  &IEDQZD
         GBLB  &IEDQZA(64)
         GBLC  &IEDQZC
         LCLB  &NOGO
.*             VERSION DATED OCTOBER 16, 1970
         SPACE
         AIF   (NOT &IEDQZA(9)).TSOCHK
         MNOTE 12,'***  IHB312  MUST FOLLOW STARTMH MACRO WITH TSOMH=NO*
               '
&NOGO    SETB  1
.TSOCHK  AIF   (&IEDQZA(4)).CHK
         MNOTE 12,'***  IHB070  SEQUENCE ERROR - MUST FOLLOW OUTHDR MAC*
               RO'
&NOGO    SETB  1
.CHK     AIF   ('&CHARS' EQ '').NM
         IEDQCHAR 8,&CHARS
         AIF   (&IEDQZA(24)).ERR
         AIF   (&IEDQZD LE 8).NM
.ERR     MNOTE 12,'***  IHB300  CHARACTER OPERAND INVALID AS SPECIFIED'
&NOGO    SETB  1
.NM      AIF   (&NOGO).MEND
         AIF   ('&NAME' EQ '').NON
&NAME    DS    0H
.NON     AIF   ('&CHARS' EQ '').DOIT
         IEDQSCAN &IEDQZD,&CHARS,&BLANK
         AIF   (&IEDQZA(24)).MEND
         LTR   15,15 .                  TEST RETURN CODE
         BNP   *+16 .                   NEGATIVE OR ZERO - NO ACTION
         L     1,IEDADBUF .             GET BUFFER ADDRESS
         STH   15,24(,1) .              PUT SCAN POINTER IN BUFFER
         AGO   .OI
.DOIT    L     1,IEDADBUF .             GET BUFFER ADDRESS
.OI      OI    20(1),X'04' .            SET EOF INDICATOR ON
.MEND    SPACE 2
         MEND
