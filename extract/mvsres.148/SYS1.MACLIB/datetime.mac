         MACRO
&NAME    DATETIME &DATE=YES,&TIME=YES
         GBLA  &IEDQZE(64)
         GBLB  &IEDQZA(64)
         LCLA &CNT
.*             VERSION DATED AUGUST 19, 1970
         SPACE
         AIF   (NOT &IEDQZA(9)).TSOCHK                              TSO
         MNOTE 12,'***  IHB312  MUST FOLLOW STARTMH MACRO WITH TSOMH=NO*
               '
         AGO   .MEND                                                TSO
.TSOCHK  AIF   (&IEDQZA(3) OR &IEDQZA(4)).BOTH
         MNOTE 12,'***  IHB070  SEQUENCE ERROR-MUST FOLLOW INHDR OR OUT*
               HDR MACRO'
         AGO   .MEND
.BOTH    AIF   ('&DATE' EQ 'NO' AND '&TIME' EQ 'NO').MNOTE
         AGO   .ORGDTTM
.MNOTE   MNOTE 12,'***  IHB066  INCONSISTENT OPERANDS'
         AGO   .MEND
.ORGDTTM IEDQVCON (11,38),(IEDQAC,IEDQAF)
         AIF   ('&DATE' EQ 'YES' OR '&DATE' EQ 'NO').TIME
.ERR     MNOTE 12,'***  IHB300  DATE OR TIME OPERAND INVALID AS SPECIFI*
               ED'
         AGO   .MEND
.TIME    AIF   ('&TIME' NE 'YES' AND '&TIME' NE 'NO').ERR
&CNT     SETA  0
         AIF   ('&DATE' EQ 'NO').SET
&CNT     SETA  7
.SET     AIF   ('&TIME' EQ 'NO').OUT
&CNT     SETA  &CNT+9
.OUT     ANOP
&NAME    BAL   1,*+6
         DC    AL1(&IEDQZE(38)+3),AL1(&CNT)
         L     15,IEDUI .               TO EXPAND
         BALR  14,15
         LTR   15,15 .                  EXPAND SUCCESSFUL
         BNZ   *+16 .                   NO - DO NOT EXECUTE
         BAL   1,*+6
         DC    AL1(&IEDQZE(11)),AL1(&CNT)
         L     15,IEDUI .               TO DATETIME
         BALR  14,15
.MEND    SPACE 2
         MEND
