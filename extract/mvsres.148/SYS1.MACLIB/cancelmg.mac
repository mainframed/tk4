         MACRO
&NAME    CANCELMG &MASK,&CONNECT=OR,&LEVEL=MSG,&BLANK=         @Y17XAMQ
         GBLA  &IEDQZE(64),&IEDQZD                             @Y17XAMQ
         GBLB  &IEDQZA(64)
         LCLA  &CNT1,&CNT2,&CNT3,&CNT4,&CNT5,&CNT6               S22025
         LCLA  &MSK                                              S22025
         LCLB  &SW1,&SW2                                         S22025
         LCLB  &BIT,&ALG,&NOGO,&MASKMN
         LCLB  &WRKB(2)                                          S22025
         LCLC  &WRKC1,&WRKC2                                     S22025
         LCLC  &WRKC                                             S22025
         LCLC  &LETR                                             S22025
.*A300000,560000,640000,800000                                   S22025
.*A827700,828600,829200,830100,831000                            S22025
.*A080000,100000,460000,535000,740000,808000,810000              S22025
.*C122000,300000,308000-312000,640000,780000-800000,802000-804000S22025
.*C304000,109000,122000,814000,820000                            S22025
.*C040000,120000,560000,640000                                   S22025
.*A500000                                                      @Y17XAMQ
.*C040000,060000                                               @Y17XAMQ
.*A504800                                                      @OZ38082
.*C501200,501800                                               @OZ43499
.*A504800                                                      @OZ48948
.*C501200,501800                                               @OZ48948
         SPACE
         AIF   (&IEDQZA(1) AND &IEDQZA(5)).OK
         AIF   (&IEDQZA(4)).OUTH OUTHEADER SPECIFICATION
         MNOTE 12,'***  IHB070  SEQ ERR-MUST FOLLOW INMSG OR OUTHDR'
&NOGO    SETB  1
.OK      AIF   (NOT &IEDQZA(18)).MASK
         MNOTE 12,'***  IHB070  SEQUENCE ERROR - MUST BE FIRST MACRO IN*
                INMSG IF SPECIFIED'
&NOGO    SETB  1
.MASK    ANOP                                                    S22025
         AIF   ('&LEVEL' NE 'BLK').DATGUD                        S22025
         AIF   (('&LEVEL' EQ 'BLK') AND ('&CONNECT' EQ 'OR' OR         *
               '&CONNECT' EQ '')).DATGUD                         S22025
         MNOTE 12,'***  IHB300  CONNECT OPERAND INVALID AS SPECIFIED   *
               WITH LEVEL=BLK'                                   S22025
&NOGO    SETB  1
.DATGUD  ANOP                                                    S22025
         AIF   ('&MASK' EQ '').AND                               S22025
.SETMSK  ANOP                                                    S22025
         IEDQMASK &MASK,5
         AIF   (NOT &IEDQZA(24)).CNCL
.ERR     AIF   (&MASKMN).AND
         MNOTE 12,'***  IHB300  MASK OPERAND INVALID AS SPECIFIED'
&MASKMN  SETB  1
&NOGO    SETB  1
.CNCL    AIF   ('&MASK'(1,1) NE 'X').AND
         AIF   ('&MASK'(2,1) EQ 'L' AND '&MASK'(3,1) NE '5').ERR
         AIF   ('&MASK'(2,1) NE 'L' AND K'&MASK NE 13).ERR
.AND     AIF   ('&CONNECT' EQ 'AND' OR '&CONNECT' EQ 'OR').OP
         AIF   ('&CONNECT' EQ '').NXT                            S22025
         MNOTE 12,'***  IHB300  CONNECT OPERAND INVALID AS SPECIFIED'
         AGO   .MEND
.OUTH    AIF   ('&CONNECT' EQ 'OR' AND '&LEVEL' EQ 'MSG').OHOK @Y17XAMQ
         MNOTE 4,'***   IHB300  EXTRANEOUS OPERANDS IGNORED'   @Y17XAMQ
.OHOK    ANOP                                                  @Y17XAMQ
&MSK     SETA  20                                              @OZ48948
         AIF   ('&MASK' EQ '').OHNOM                           @Y17XAMQ
&MSK     SETA  52                                              @OZ48948
         IEDQCHAR 8,&MASK                                      @Y17XAMQ
         AIF   (NOT &IEDQZA(24) AND &IEDQZD LE 8).OHNOM        @Y17XAMQ
         MNOTE 12,'***  IHB300  CHARACTER OPRND INVAL AS SPEC' @Y17XAMQ
         AGO   .MEND                                           @Y17XAMQ
.OHNOM   ANOP                                                  @Y17XAMQ
&NAME    CNOP   0,4                                            @Y17XAMQ
         L     1,IEDADBUF               GET BUFFER ADDRESS     @Y17XAMQ
         L     15,12(1)                 GET LCB ADDRESS        @Y17XAMQ
         CLI   32(15),0                 IS IT A PLCB           @Y17XAMQ
         BNE   *+&MSK                   BRANCH IF NOT          @Y17XAMQ
         TM    71(15),X'02'             IS THIS A DIAL LCB     @OZ48948
         BO    *+8                      YES, DON'T SET LCBNEGRP@OZ48948
         OI    71(15),X'08'             NEG RESPONSE TO POLL   @OZ38082
         AIF   ('&MASK' EQ '').OHGNM                           @Y17XAMQ
         IEDQSCAN &IEDQZD,&MASK,&BLANK                         @Y17XAMQ
         AIF   (&IEDQZA(24)).MEND                              @Y17XAMQ
         LTR   15,15                    CHAR STRING FOUND      @Y17XAMQ
         BNP   *+16                     BRANCH ON NO           @Y17XAMQ
         L     1,IEDADBUF               GET BUFFER ADDRESS     @Y17XAMQ
         STH   15,24(,1)                STORE SCAN POINTER     @Y17XAMQ
.OHGNM   OI    20(1),X'80'              SET STOPMSG FLAG       @Y17XAMQ
         AGO   .MEND                                           @Y17XAMQ
.OP      AIF   (&NOGO).MEND
         AIF   ('&CONNECT' EQ 'OR').NXT
&ALG     SETB  1
.NXT     AIF   ('&LEVEL' EQ 'MSG' OR '&LEVEL' EQ 'BLK').CKMSG    S22025
         AIF   ('&LEVEL' EQ '').MESG                             S22025
         MNOTE 12,'*** IHB300 LEVEL OPERAND INVALID AS SPECIFIED'
         AGO   .MEND                                             S22025
.CKMSG   AIF   ('&LEVEL' EQ 'MSG').MESG                          S22025
         IEDQVCON 57,IEDQBU                                      S22025
         AGO   .MSK                                              S22025
.MESG    IEDQVCON 2,IEDQAR                                       S22025
.MSK     ANOP                                                    S22025
         AIF   ('&MASK' NE '0' AND '&MASK' NE 'X''0000000000''' AND '&M*
               ASK' NE 'XL5''0000000000''' AND '&MASK' NE '').SKP
&BIT     SETB  1
.SKP     AIF   ('&LEVEL' EQ 'BLK').SDC                           S22025
         AIF   (NOT &BIT).DC                                     S22025
&NAME    DC    AL1(&IEDQZE(2)+&BIT),AL1(4+&ALG),H'0'
         AGO   .MEND
.DC      ANOP
&NAME    DC    AL1(&IEDQZE(2)+&BIT),AL1(&ALG+8),AL1(0)
         AIF   ('&MASK' EQ '').MENDY                             S22025
         AIF   ('&MASK'(1,1) EQ 'X').HEX
&MSK     SETA  &MASK                                             S22025
         AIF   ('&LEVEL' NE 'BLK').MERDIS                        S22025
&MSK     SETA  &MSK/X'010000'*X'010000'-&MSK/X'4000'*X'4000'+&MSK+X'C00*
               0'                                                S22025
.MERDIS  ANOP                                                    S22025
         DC    FL5'&MSK' .              ERROR MASK               S22025
         AGO   .MEND                                             S22025
.GENMSK  ANOP                                                    S22025
         DC    XL5'000000C000'          ERROR MASK               S22025
         AGO   .MEND                                             S22025
.SDC     ANOP                                                    S22025
&BIT     SETB  0
&NAME    DC    AL1(&IEDQZE(57)+&BIT),AL1(&ALG+8),AL1(0)          S22025
         AIF   ('&MASK' EQ '').GENMSK                            S22025
         AIF   ('&MASK'(1,1) EQ 'X').HEX                         S22025
&MSK     SETA  &MASK                                             S22025
&MSK     SETA  &MSK/X'010000'*X'010000'-&MSK/X'4000'*X'4000'+&MSK+X'C00*
               0'                                                S22025
.MERDI2  ANOP                                                    S22025
         DC    FL5'&MSK' .              ERROR MASK               S22025
         AGO   .MEND                                             S22025
.MENDY   ANOP                                                    S22025
         AIF   ('&LEVEL' EQ 'BLK').GENMSK                        S22025
.HEX     ANOP                                                    S22025
.XL5     ANOP                                                    S22025
         AIF   ('&LEVEL' NE 'BLK').TSKTSK                        S22025
&CNT1    SETA  2                        SET COUNTER              S22025
         AIF   ('&MASK'(2,2) EQ 'L5').XL5OK CHECK FOR XL5        S22025
         AIF   ('&MASK'(&CNT1,1) EQ '''').COUNT                  S22025
.BDNEWS  ANOP                                                    S22025
         MNOTE 12,'***  IHB300  MASK INVALID AS SPECIFIED '      S22025
         AGO   .MEND                                             S22025
.TSKTSK  ANOP                                                    S22025
         DC    &MASK                    ERROR MASK               S22025
         AGO   .MEND                                             S22025
.XL5OK   ANOP                                                    S22025
&CNT1    SETA  &CNT1+2                  RESET TO POSITION OF     S22025
.*                                      FIRST APOSTROPHE IF XL5  S22025
.*                                      IS SPECIFED              S22025
         AIF   ('&MASK'(&CNT1,1) EQ '''').COUNT                  S22025
         AGO   .BDNEWS                                           S22025
.COUNT   ANOP                                                    S22025
&CNT2    SETA  &CNT1                                             S22025
.LOOP    ANOP                                                    S22025
&CNT2    SETA  &CNT2+1                  INCREMENT COUNT TO NEXT  S22025
.*                                      OPERAND POSITION         S22025
         AIF   ('&MASK'(&CNT2,1) EQ '''').STPLP CHK THIS  CHARTR S22025
         AIF   (&CNT2 GT 15).BDNEWS     COUNT GREATER THAN NO.   S22025
.*                                      OF MAX NO. OF  CHARACTERSS22025
         AGO   .LOOP                    BACK TO TOP OF LOOP      S22025
.STPLP   ANOP                                                    S22025
&CNT3    SETA  (&CNT2-&CNT1)-1          GET NUMBER OF CHARACTERS S22025
.*                                      USER SPECIFIED           S22025
         AIF   (&CNT3 GE 1).GDSHOW      ATLEAST ONE              S22025
         AGO   .BDNEWS                  NONE FORGET IT           S22025
.GDSHOW  ANOP                                                    S22025
         AIF   (&CNT3 GT 4).SET00       MORE THAN FOUR           S22025
.*                                      LESS THAN FOUR           S22025
&CNT4    SETA  5-&CNT3                  SET COUNT FOR CHARACTER  S22025
.*                                      WORK AREA POSITION       S22025
         AIF   (&CNT3 EQ 4).USE4                                 S22025
&WRKC1   SETC  '000'(1,4-&CNT3)         MOVE IN PADDING CARACTERSS22025
&LETR    SETC  '&MASK'(&CNT1+1,&CNT3)
&WRKC1   SETC  '&WRKC1'(1,4-&CNT3).'&LETR'(1,&CNT3)
.PAD6    ANOP                                                    S22025
&WRKC2   SETC  '000000'                PAD WORK AREA             S22025
         AGO   .OUT1                    ALMOST DONE              S22025
.USE4    ANOP                                                    S22025
&WRKC1   SETC  '&MASK'(&CNT1+1,&CNT3)                            S22025
         AGO   .PAD6                                             S22025
.USE6    ANOP                                                    S22025
&WRKC2   SETC  '&MASK'(&CNT1+1,&CNT5)                            S22025
         AGO   .DOREST                                           S22025
.SET00   ANOP                                                    S22025
&CNT5    SETA  &CNT3-4                  SET POSITION OF 4TH BYTE S22025
.*                                      OF WORK AREA             S22025
         AIF   (&CNT5 EQ 6).USE6                                 S22025
&WRKC2   SETC  '000000'(1,6-&CNT5)      PAD WORK AREA            S22025
&LETR    SETC  '&MASK'(&CNT1+1,&CNT5)
&WRKC2   SETC '&WRKC2'(1,6-&CNT5).'&LETR'(1,&CNT5)
.DOREST  ANOP                                                    S22025
&WRKC1   SETC  '&MASK'(&CNT1+1+&CNT3-4,4)                        S22025
.OUT1    ANOP                                                    S22025
&WRKB(1) SETB ('&WRKC1'(1,1) EQ '2' OR '&WRKC1'(1,1) EQ '3' OR '&WRKC1'*
               (1,1) EQ '6' OR '&WRKC1'(1,1) EQ '7' OR '&WRKC1'(1,1) EQ*
                'A' OR '&WRKC1'(1,1) EQ 'B' OR '&WRKC1'(1,1) EQ 'E' )
&WRKB(1) SETB ('&WRKC1'(1,1) EQ 'F' OR &WRKB(1) EQ 1)            S22025
&WRKB(2) SETB ('&WRKC1'(1,1) EQ '1' OR '&WRKC1'(1,1) EQ '3' OR '&WRKC1'*
               (1,1) EQ '5' OR '&WRKC1'(1,1) EQ '7' OR '&WRKC1'(1,1) EQ*
                '9' OR '&WRKC1'(1,1) EQ 'B' OR '&WRKC1'(1,1) EQ 'D' )
&WRKB(2) SETB ('&WRKC1'(1,1) EQ 'F' OR &WRKB(2) EQ 1)            S22025
&WRKC1   SETC  'CDEF'(1+2*&WRKB(1)+&WRKB(2),1).'&WRKC1'(2,3)     S22025
         DC    XL5'&WRKC2&WRKC1.' .     ERROR MASK               S22025
.MEND    SPACE 2
         MEND
