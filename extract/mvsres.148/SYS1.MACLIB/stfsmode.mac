         MACRO
&NAME    STFSMODE &A,&INITIAL=NO,&RSHWKEY=64,&NOEDIT=NO         ZP60009
.**********************************************************************
.*                                                                    *
.* STATUS - OS/VS2 TSO/VTAM DYNAMIC RESHOW LEVEL 2.0                  *
.*                                                                    *
.* NAME - STFSMODE  (SET FULL SCREEN MODE)                            *
.*                                                                    *
.* FUNCTION - THE STFSMODE MACRO IS USED TO SPECIFY WHETHER OR NOT    *
.*            AN APPLICATION IS PRESENTING DISPLAYS TO A TSO/VTAM     *
.*            DISPLAY TERMINAL IN FULL SCREEN MODE.                   *
.*            WHEN INITIALLY SETTING THE MODE ON, INITIAL=YES WILL    *
.*            PREVENT PAGING FOR THE FIRST FULL SCREEN TPUT ISSUED BY *
.*            THE APPLICATION.                                        *
.*            RSHWKEY IS USED TO SPECIFY WHICH PFKEY IS TO BE USED    *
.*            AS THE RESHOW KEY.  E.G., RSHWKEY=3 MEANS THAT PFKEY 3  *
.*            IS TO BE THE RESHOW KEY.  WHEN STFSMODE IS OFF,         *
.*            RSHWKEY IS NOT TO BE SPECIFIED.  WHEN STFSMODE IS ON    *
.*            AND NO RSHWKEY HAS BEEN SPECIFIED, RSHWKEY WILL DEFAULT *
.*            TO ZERO.                                                *
.*                                                                    *
.*       THE MACRO PROTOTYPE IS ABOVE                                 *
.*                                                                    *
.*       THE STANDARD FORM OF THE EXPANSION IS BELOW                  *
.*&NAME  LA    1,128+64                 FLAGS FOR MODE/INITIAL CALL   *
.*       SLL   1,24                     SHIFT TO HIGH ORDER BYTE      *
.*       SR    0,0                      CLEAR REGISTER                *
.*       LA    0,10                     LOAD RSHWKEY                  *
.*       OR    1,0                      RSHWKEY IN RIGHTMOST BYTE     *
.*       LA    0,18                     LOAD ENTRY CODE               *
.*       SLL   0,24                     SHIFT TO HIGH ORDER BYTE      *
.*       SVC   94                       TERMINAL CONTROL MACRO SVC    *
.*                                                                    *
.**********************************************************************
         LCLA  &SYSFS
         LCLA  &SYSINIT
         LCLA  &SYSNEDT                                         ZP60009
&SYSFS   SETA  128
&SYSINIT SETA  0
&SYSNEDT SETA  0                                                ZP60009
         AIF   ('&INITIAL' NE 'YES' AND '&INITIAL' NE 'NO').ERROR1
         AIF   ('&NOEDIT' NE 'YES' AND '&NOEDIT' NE 'NO').ERROR5  ZP6-9
         AIF   ('&NOEDIT' EQ 'YES' AND '&A' EQ 'OFF').ERROR4    ZP60009
         AIF   (&RSHWKEY GT 0 AND &RSHWKEY LT 25).TESTFS        ZP60009
         AIF   (&RSHWKEY NE 64).ERROR2
         AGO   .NEXT
.TESTFS  ANOP
         AIF   ('&A' EQ 'OFF').ERROR3
.NEXT    ANOP
         AIF   ('&INITIAL' EQ 'NO').TESTNE                      ZP60009
&SYSINIT SETA  64
.TESTNE  ANOP                                                   ZP60009
         AIF   ('&NOEDIT' EQ 'NO').TESTA                        ZP60009
&SYSNEDT SETA  32                                               ZP60009
.TESTA   ANOP
         AIF   ('&A' EQ 'ON' OR '&A' EQ '').FSON
         AIF   ('&A' EQ 'OFF').FSOFF
.ERROR   IHBERMAC 36,,&A
         MEXIT
.ERROR1  IHBERMAC 54,,INITIAL                                   ZP60009
         MEXIT
.ERROR2  IHBERMAC 54,,RSHWKEY
         MEXIT
.ERROR3  IHBERMAC 1020,RSHWKEY,&A
         MEXIT
.ERROR4  IHBERMAC 1020,NOEDIT,&A                                ZP60009
         MEXIT                                                  ZP60009
.ERROR5  IHBERMAC 54,,NOEDIT                                    ZP60009
         MEXIT                                                  ZP60009
.FSON    ANOP
&NAME    LA    1,&SYSFS+&SYSINIT+&SYSNEDT  MODE/INITIAL/NOEDIT  ZP60009
         SLL   1,24                     SHIFT TO HIGH ORDER BYTE
         AGO   .PFCD
.FSOFF   ANOP
&NAME    SR    1,1                      CLEAR PARAMETER REGISTER
.PFCD    ANOP
         AIF   ('&A' EQ 'OFF').ENTRYCD
         AIF   (&RSHWKEY EQ 64).ENTRYCD
         SR    0,0                      CLEAR REGISTER
         LA    0,&RSHWKEY               LOAD RSHWKEY
         OR    1,0                      RSHWKEY IN RIGHTMOST BYTE
.ENTRYCD ANOP
         LA    0,18                     LOAD ENTRY CODE
         SLL   0,24                     SHIFT TO HIGH ORDER BYTE
         SVC   94                       TERMINAL CONTROL MACRO SVC
         MEND
