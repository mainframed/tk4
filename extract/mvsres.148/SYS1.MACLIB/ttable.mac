         MACRO
&NAME    TTABLE &LAST=,&MAXLEN=,&Y=(4,2,2,2,4,2,1,0),&S=(1,1,0,0,0,0,1,*
               0),&OLTERM=0                                      S99528
.*A-000000-999999                                              @Y10X5O0
         GBLA  &IEDQAR(64),&IEDNCT                               S99528
         GBLB  &IEDQZA(64)
         GBLC  &IEDQSV(8)
         LCLA  &A
.*       VERSION DATED SEPTEMBER 12, 1974                      @Z30X8OE
.*A685000                                                        X01004
.*C690000                                                        X01004
.*C030000-XXXXXX,190000-XXXXXX                                   S99528
.*A235000                                                        S22025
.*C190200                                                       SA56318
.*A030000,260000,450000,500000,680000                          @Z30X8OE
.*C071000,370000,390000-440000,610000-660000                   @Z30X8OE
         AIF   (NOT &IEDQZA(55)).SP
         MNOTE 12,'***IHB067 TTABLE MACRO PREVIOUSLY USED'
         MEXIT
.SP      SPACE 2
         AIF   ('&LAST' EQ '').ERR2
&IEDQAR(1)  SETA  K'&LAST+3
         AIF   ('&MAXLEN' EQ '').GEN
         AIF   ('&MAXLEN' LT '1' OR '&MAXLEN' GT '8').ERR
         AIF   (K'&LAST EQ &MAXLEN).GEN
         AIF   (K'&LAST GT &MAXLEN).ERR
&IEDQAR(1)  SETA  &MAXLEN+3
.GEN     AIF   (&IEDQAR(1) LT 4 OR &IEDQAR(1) GT 11).ERR3
         AIF   (&OLTERM LE 255).CON                              S99528
         MNOTE 12,'*** IED904 OLTERM GREATER THAN 255'          SA56318
         MEXIT
&IEDNCT  SETA  0                                                 S99528
.CON     ANOP                                                    S99528
&IEDNCT  SETA  &OLTERM                                           S99528
&IEDQZA(55)  SETB  1
&IEDQAR(7)  SETA  1
&IEDQAR(8)  SETA  1
&IEDQAR(9)  SETA  1
&IEDQAR(44) SETA 1                                               S22025
&IEDQSV(5)  SETC  '&LAST'
&IEDQSV(4)  SETC  '&SYSECT'
IEDQTNT  CSECT
         N     1,78(15)                 CLEAR SIGN PROPAGATION @Z30X8OE
         AIF   (&IEDQAR(1) EQ 4 OR &IEDQAR(1) EQ 8).SL
         AIF   (&IEDQAR(1) NE 6 AND &IEDQAR(1) NE 10).CY
         AR    1,1 .                    DOUBLE
.CY      LR    0,1 .                    SAVE
         AIF   (&IEDQAR(1) NE 6).SL
.SE      AR    0,0  .                   DOUBLE
.AR      AR    1,0 .                    COMBINE
         AIF   (&IEDQAR(1) NE 11).DN
         AR    0,0 .                    DOUBLE
         AR    1,0 .                    COMBINE
.DN      ANOP                                                  @Z30X8OE
&A       SETA  &Y(&IEDQAR(1)-3)
         LA    1,79(1,15)               TRM OFFSET             @Z30X8OE
         ICM   1,7,0(1)                 TRM ENTRY ADDRESS      @Z30X8OE
         LR    0,1                     COPY ADDRESS            @Z30X8OE
         BR    14 .                     RETURN TO CALLER
         DC    5H'0'                                           @Z30X8OE
         DS    &A.H
&NAME    DC    H'0'
         DC    AL1(&IEDQAR(1)-3) .      LENGTH OF TERM NAMES
         DC    AL3(&LAST) .             MIDDLE ENTRY
         DC    H'00'
         N     9,78(7)                  CLEAR SIGN PROPAGATION @ZM30196
         AIF   (&IEDQAR(1) EQ 4 OR &IEDQAR(1) EQ 8).SLX
         AIF   (&IEDQAR(1) NE 6 AND &IEDQAR(1) NE 10).CYX
         AR    9,9 .                    DOUBLE
.CYX     LR    8,9 .                    SAVE
         AIF   (&IEDQAR(1) NE 6).SLX
.SEX     AR    8,8 .                    DOUBLE
.ARX     AR    9,8 .                    COMBINE
         AIF   (&IEDQAR(1) NE 11).DNX
         AR    8,8 .                    DOUBLE
         AR    9,8 .                    COMBINE
.DNX     LA    8,79(9,7)                TRM OFFSET             @Z30X8OE
         ICM   8,7,0(8)                 TRM ENTRY ADDRESS      @Z30X8OE
         BR    6 .                      RETURN TO CALLER
         DS    &A.H
         DC    4H'0'                                           @Z30X8OE
         DC    X'FFFF'                                         @Z30X8OE
IEDQTRMC CSECT                                                   X01004
         DC    A(IEDQSCBC,IEDQTRME)                              X01004
         SPACE 2
         MEXIT
.SL      SLL   1,3-&S(&IEDQAR(1)-3)
         AIF   (&IEDQAR(1) EQ 4 OR &IEDQAR(1) EQ 8).DN
         AIF   (&IEDQAR(1) NE 7).AR
         SR    1,0 .                    DECREMENT
         AGO   .DN
.SLX     SLL   9,3-&S(&IEDQAR(1)-3)
         AIF   (&IEDQAR(1) EQ 4 OR &IEDQAR(1) EQ 8).DNX
         AIF   (&IEDQAR(1) NE 7).ARX
         SR    9,8                      DECREMENT
         AGO   .DNX
.ERR     MNOTE 4,'*** IHB002 INVALID MAXLEN OPERAND SPECIFIED- &MAXLEN'
         AGO   .GEN
.ERR2    MNOTE 12,'*** IHB001 LAST OPERAND REQ''D- NOT SPECIFIED'
         SPACE 2
         MEXIT
.ERR3    ANOP
&IEDQAR(1) SETA &IEDQAR(1)-3
         MNOTE 12,'*** IHB002 INVALID LENGTH OF TERMINAL NAMES SPECI-  *
                        FIED- &IEDQAR(1)'
         SPACE 2
         MEND
