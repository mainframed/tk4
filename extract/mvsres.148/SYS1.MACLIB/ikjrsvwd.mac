             MACRO
&LABEL   IKJRSVWD &TYPNAM,&HELP=,&PROMPT=,&DEFAULT=
.*********************************************************************
.*
.* TITLE  -  IKJRSVWD MACRO (SYMBOLIC DEBUG PROJECT)
.*
.* FUNCTION  -  THE IKJRSVWD MACRO IS USED BY COMMAND PROCESSORS TO
.*              DEFINE TO PARSE THE EXPECTED FORMAT OF A RESERVE
.*              WORD PARAMETER.
.*
.* INPUT  -  THE PROTOTYPE STATEMENT IS AS DEFINED ABOVE. IT'S
.*           PARAMETERS REPRESENT THE FOLLOWING:
.*                &LABEL   -  A STATEMENT LABEL IS REQUIRED.
.*                &TYPNAM  -  PARAMETER TYPE
.*                &PROMPT= -  PROMPT DATA, IF ENTERED
.*                &DEFAULT=  -  DEFAULT, IF ENTERED
.*                &HELP=  -  HELP DATA, IF ENTERED
.*
.* OUTPUT  -  THIS MACRO PRODUCES A PARAMETER CONTROL ENTRY (PCE) TO
.*            BE PASSED TO THE PARSE SERVICE ROUTINE.
.*            IF AN ERROR IS ENCOUNTERED IN PROCESSING THE MACRO
.*            INSTRUCTION ENTERED BY THE CP, AN APPROPRIATE ERROR
.*            MESSAGE IS PRINTED AND THE PCE IS NOT GENERATED.
.*
.*********************************************************************
.*
.* THE FOLLOWING CODE DEFINES VARIABLES TO BE USED IN THE MACRO
.*   EXPANSION:
.*           GLOBALS:   IKJCSNM - CSECT NAME FROM IKJPARM MACRO LABEL.
.*                      IKJDSNM - DSECT NAME FOR PDL
.*           LOCALS(BINARY):  FENDBIT - INDICATES THE PCE IS NOT TO
.*                                      BE GENERATED.
.*
         GBLC  &IKJDSNM,&IKJCSNM
         LCLA  &T,&END
         LCLB  &FENDBIT,&FIGCON,&SARSVWD,&FPRPT,&FDFLT,&FHELP
         LCLC  &QUOTE
&FENDBIT SETB  0
.*
.* DETERMINE THAT THERE IS NO MORE THAN 1 POSITIONAL PARAMETER.
.*
         AIF   (N'&SYSLIST LT 2).GOOD
         MNOTE 8,'NO MORE THAN ONE POSITIONAL PARAMETER ALLOWED'
&FENDBIT SETB  1
.*
.* DETERMINE IF STATEMENT LABEL WAS ENTERED.
.*
.GOOD    AIF   ('&LABEL' NE '').GOOD1
.ERROR1  ANOP
         MNOTE 8,'STATEMENT LABEL MISSING BUT REQUIRED'
&FENDBIT SETB  1
.*
.* DETERMINE IF CSECT NAME IS MISSING.
.*
.GOOD1   AIF   ('&IKJCSNM' NE '').GOOD2
         MNOTE 8,'CSECT NAME (FROM IKJPARM MACRO LABEL) IS MISSING'
&FENDBIT SETB  1
.GOOD2   AIF   (N'&SYSLIST NE 0).TYPE2
&FIGCON  SETB  1
         AGO   .OKGOBLD
.TYPE2   ANOP
&FPRPT   SETB  ('&PROMPT' NE '')
&FDFLT   SETB  ('&DEFAULT' NE '')
&FHELP   SETB  ('&HELP' NE '')
         AIF   (NOT (&FPRPT AND &FDFLT)).PCEBLD
         MNOTE 8,'PARAMETER CANNOT BOTH DEFAULT AND REQUIRE PROMPTING'
&FENDBIT SETB  1
.PCEBLD  AIF   ('&TYPNAM' NE '').OKGOBLD
         MNOTE 8,'POSITIONAL PARAMETER TYPE FIELD MISSING BUT REQUIRED'
&FENDBIT SETB  1
.OKGOBLD AIF   (&FENDBIT).END
&IKJCSNM CSECT ,             RESUME PCL CSECT
RSVW&SYSNDX EQU  *
&LABEL   EQU   *
         DC    BL2'101&FPRPT&FDFLT.0&FHELP.0&FIGCON.0000000'
         AIF   (N'&SYSLIST NE 0).CONT
         DC    AL2(RSVD&SYSNDX-RSVW&SYSNDX)  LENGTH OF PCE
         DC    AL2(0)    NO PDE OFFSET - NO PDE BUILT
RSVD&SYSNDX  EQU  *
         AGO   .EXIT2
.CONT    DC    AL2(RSVR&SYSNDX-RSVW&SYSNDX)  LENGTH OF PCE
         DC    AL2(LAB&SYSNDX-&IKJDSNM)          OFFSET TO THE PDE
.*
.* THE FOLLOWING CODE SUPPLIES QUOTES TO &TYPNAM IF THEY ARE MISSING
.*
&QUOTE   SETC  '&TYPNAM'(1,1)
         AIF   ('&QUOTE' EQ '''').QUOT
&QUOTE   SETC  ''''
         DC    AL2(IKK@&SYSNDX-*),AL2(18),C&QUOTE&TYPNAM&QUOTE   PARAME*
               TER TYPE MESSAGE SEGMENT
         AGO   .EQUGEN1
.QUOT    ANOP
         DC    AL2(IKK@&SYSNDX-*),AL2(18),C&TYPNAM  PARAMETER TYPE MESS*
               AGE SEGMENT
.EQUGEN1 ANOP
IKK@&SYSNDX EQU *
         AIF   (&FPRPT).PRPT           PROMPT
         AIF   (NOT &FDFLT).NPRDF      NEITHER
         DC    AL1(IKJ@&SYSNDX-*-2),C&DEFAULT DEFAULT DATA
         AGO   .EQUGEN
.PRPT    DC    AL1(IKJ@&SYSNDX-*-2),C&PROMPT PROMPT DATA
.EQUGEN  ANOP
IKJ@&SYSNDX EQU *
.NPRDF   ANOP
         AIF   (NOT &FHELP).NOHELP     IS HELP SPECIFIED
         DC    AL2(IKJ$&SYSNDX-*)      LENGTH OF HELP DATA
&T       SETA  1
.HELPLP  ANOP
&T       SETA  &T+1                    NEXT HELP
         AIF   (&T EQ 256).HELPER
         AIF   (&T LE N'&HELP).HELPLP
         AGO   .HELPCT
.HELPER  MNOTE 4,'ONLY 255 LEVELS OF HELP AVAILABLE. OTHERS IGNORED'
.HELPCT  ANOP
&T       SETA  &T-1
         DC    AL1(&T)                 NUMBER OF HELP MESSAGES
&END     SETA  &T
&T       SETA  1
.HELPLPA ANOP
         DC    AL2(IKJ&SYSNDX.&T-*),AL2(00),C&HELP(&T) HELP MSG SEGMENT
IKJ&SYSNDX.&T EQU *
&T       SETA  &T+1
         AIF   (&T LE &END).HELPLPA
IKJ$&SYSNDX EQU *
.NOHELP  ANOP
         AIF    (N'&SYSLIST NE 0).GOOD3
         AGO   .EXIT2
.END     MEXIT
.GOOD3   ANOP
RSVR&SYSNDX EQU *
&IKJDSNM DSECT
LAB&SYSNDX   DS    2F                  ALLOCATE PDL SPACE
.EXIT2   ANOP
&SYSECT  CSECT ,                   RESUME USERS CSECT
         MEXIT
         MEND
