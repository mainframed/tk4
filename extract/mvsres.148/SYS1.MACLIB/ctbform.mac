         MACRO
&NAME    CTBFORM &OP,&DVCID=YES,&ENDCHAR=YES,&INSERT=YES
         GBLB  &IEDQZA(64)
         GBLA  &IEDQZE(64)
         LCLB  &NOGO,&SW(8)
.*  &SW(4) MUST ALWAYS BE ZERO                                   S22027
         LCLB  &B
         LCLA  &I
.*A22026                                                         S22026
.*       CTBFORM VERSION DATED MAY 5,1972                        S22027
         SPACE 1
         AIF   (&IEDQZA(13)).OK1
         MNOTE 8,'*** IHB233 INCONSISTENT WITH FEATURE OPERAND OF THE IX
               NTRO MACRO' .
&NOGO    SETB  1
.OK1     ANOP
         AIF   (NOT &IEDQZA(9)).NOTSO
         MNOTE 12,'*** IHB312 MUST FOLLOW STARTMH MACRO WITH TSOMH=NO'
&NOGO    SETB  1
.NOTSO   ANOP
         AIF   (&IEDQZA(2) AND NOT &IEDQZA(4) AND NOT &IEDQZA(5)).OUTOK
         MNOTE 12,'*** IHB070 SEQUENCE ERROR- MUST FOLLOW OUTBUF MACRO'
&NOGO    SETB  1
.OUTOK   ANOP
         AIF   ('&OP' EQ '').DVCID
&SW(1)   SETB  1
         AIF   ('&OP'(K'&OP,1) EQ ')' OR '&OP'(K'&OP,1) EQ '''').OPERR
         AIF   (K'&OP GT 8).OPERR
         AIF   (('&OP'(1,1) GE 'A' AND '&OP'(1,1) LE 'Z') OR '&OP'(1,1)*
               EQ '$' OR '&OP'(1,1) EQ '#' OR '&OP'(1,1) EQ '@').DVCID
.OPERR   MNOTE 12,'*** IHB300 &OP OPERAND INVALID AS SPECIFIED'  S22027
&NOGO    SETB  1
&SW(1)   SETB  0
.DVCID   ANOP
&SW(2)   SETB  ('&DVCID' EQ 'YES')
         AIF   ('&DVCID' EQ 'YES' OR '&DVCID' EQ 'NO').DVDOK
&NOGO    SETB  1
         MNOTE 12,'***  IHB300  DVCID OPERAND INVALID AS SPECIFIED'
.DVDOK   ANOP
&SW(3)   SETB  ('&ENDCHAR' EQ 'YES')
         AIF   ('&ENDCHAR' EQ 'YES' OR '&ENDCHAR' EQ 'NO').CKINS S22027
         MNOTE 12,'*** IHB300 ENDCHAR OPERAND INVALID AS SPECIFIED'
&NOGO    SETB  1
.CKINS   ANOP                                                    S22027
&SW(5)   SETB  ('&INSERT' EQ 'NO')                               S22027
         AIF   ('&INSERT' EQ 'YES' OR '&INSERT' EQ 'NO').CKIFOP  S22027
         MNOTE 12,'*** IHB300 INSERT OPERAND INVALID AS SPECIFIED'
*                                                                S22027
&NOGO    SETB  1                                                 S22027
.CKIFOP  ANOP                                                    S22027
         AIF   (&SW(1)).CKGEN                                    S22027
&SW(5)   SETB  0                                                 S22027
.CKGEN   AIF   (&NOGO).MEND
         AIF   (&SW(1) AND (&SW(2) OR &SW(3))).ALL
         AIF   (NOT &SW(1) AND (&SW(2) OR &SW(3))).NOAE
         IEDQVCON 51,IEDQGH
         AGO   .GEN
.ALL     ANOP
         IEDQVCON (51,31,16,38),(IEDQGH,IEDQAE,IEDQAO,IEDQAF)
         AGO   .GEN
.NOAE    ANOP
         IEDQVCON (51,16,38),(IEDQGH,IEDQAO,IEDQAF)
.GEN     ANOP
&B       SETB  (&SW(1) OR &SW(2) OR &SW(3))
&I       SETA  4+2*&SW(1)+2*&B
&NAME    BAL   1,*+&I+4
         DC    AL1(&IEDQZE(51)),AL1(&I),BL1'&SW(8)&SW(7)&SW(6)&SW(5)&SW*
               (4)&SW(3)&SW(2)&SW(1)',AL1(0)
         AIF   (NOT &SW(1) AND NOT &SW(2) AND NOT &SW(3)).NOAF1
         DC    AL1(&IEDQZE(38)),AL1(&IEDQZE(16))
.NOAF1   ANOP
         AIF   (NOT &SW(1)).NOOPFLD
         DC    AL1(&IEDQZE(31)),AL1(&OP)
.NOOPFLD ANOP
         L     15,IEDUI
         BALR  14,15
&IEDQZA(25) SETB 1
.MEND    SPACE 2
         MEND
