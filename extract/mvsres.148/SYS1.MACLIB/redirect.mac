         MACRO
&NAME    REDIRECT &MASK,&DEST=ORIGIN,&CONNECT=OR,&EXIT=0        X30X5MT
         GBLA  &IEDQZD,&IEDQZE(64),&IEDQZK
         GBLB  &IEDQZA(64)
         LCLA  &BIT
         LCLB  &NOGO,&ALG
         LCLC  &C
.********************** MICROFICHE FLAGS ******************************
.*               VERSION DATED AUGUST 4,1975                    X30X5MT
.*C020000,140000,400000                                         X30X5MT
.*A550000,570000,580000,590000,790000                           X30X5MT
.**********************************************************************
         SPACE
&C       SETC  '&SYSECT'
         AIF   (NOT &IEDQZA(9)).TSOCHK                              TSO
         MNOTE 12,'***  IHB312  MUST FOLLOW STARTMH MACRO WITH TSOMH=NO*
               '
         AGO   .MEND1                                           X30X5MT
.TSOCHK  AIF   (&IEDQZA(5)).CHECK
         MNOTE 12,'***  IHB070  SEQUENCE ERROR-MUST FOLLOW INMSG OR OUT*
               MSG MACRO'
&NOGO    SETB  1
.CHECK   AIF   ('&MASK' EQ '').NXT
         IEDQMASK &MASK,5
         AIF   (&IEDQZA(24)).ERR
         AIF   ('&MASK'(1,1) NE 'X').NXT
         AIF   ('&MASK'(2,1) EQ 'L' AND '&MASK'(3,1) NE '5').ERR
         AIF   ('&MASK'(2,1) NE 'L' AND K'&MASK NE 13).ERR
         AGO   .NXT
.ERR     MNOTE 12,'***  IHB300  MASK OPERAND INVALID AS SPECIFIED'
&NOGO    SETB  1
.NXT     AIF   ('&DEST' EQ '' OR '&DEST' EQ 'ORIGIN' OR '&DEST' EQ 'DES*
               TIN').AND
         IEDQCHAR 8,&DEST
         AIF   (&IEDQZA(24)).DESTERR
         AIF   (&IEDQZD GT 8).DESTERR
         AIF   ('&DEST'(K'&DEST,1) EQ '''').AND
         AIF   (('&DEST'(1,1) GE 'A' AND '&DEST'(1,1) LE 'Z') OR '&DEST*
               '(1,1) EQ '$' OR '&DEST'(1,1) EQ '#' OR '&DEST'(1,1) EQ *
               '@').AND
.DESTERR MNOTE 12,'***  IHB300  DESTINATION OPERAND INVALID AS SPECIFIE*
               D'
&NOGO    SETB  1
.AND     AIF   ('&CONNECT' EQ 'AND' OR '&CONNECT' EQ 'OR').OP
         MNOTE 12,'***  IHB300  CONNECT OPERAND INVALID AS SPECIFIED'
&NOGO    SETB  1
.OP      AIF   ('&EXIT' NE '').OP1                              X30X5MT
         MNOTE 12,'***IHB002 INVALID EXIT OPERAND SPECIFIED-  ' X30X5MT
&NOGO    SETB  1                                                X30X5MT
.OP1     AIF   (&NOGO).MEND1                                    X30X5MT
         AIF   ('&CONNECT' EQ 'OR').ORG
&ALG     SETB  1
.ORG     AIF   ('&DEST' EQ '' OR '&DEST' EQ 'ORIGIN' OR '&DEST'(K'&DEST*
               ,1) EQ '''').SMV
         IEDQVCON (7,31),(IEDQAZ,IEDQAE)
         AGO   .TEST
.SMV     IEDQVCON 7,IEDQAZ
.TEST    AIF   (NOT &IEDQZA(1)).PARM
&IEDQZA(18) SETB 1
.PARM    AIF   ('&MASK' NE '0' AND '&MASK' NE 'X''0000000000''' AND '&M*
               ASK' NE 'XL5''0000000000''' AND '&MASK' NE '').SKP
&BIT     SETA  1
.SKP     ANOP
&BIT     SETA  &BIT+2
         AIF   (&BIT NE 1 AND &BIT NE 3).S
         AIF   ('&EXIT' NE '0').EXP                             X30X5MT
&NAME    DC    AL1(&IEDQZE(7)+&BIT),AL1(8+&ALG),H'0'
         AGO   .DEST
.EXP     ANOP                                                   X30X5MT
&NAME    DC    AL1(&IEDQZE(7)+&BIT),AL1(12+&ALG),AL1(2),AL1(0)  X30X5MT
         AGO   .DEST                                            X30X5MT
.S       ANOP
         AIF   ('&EXIT' NE '0').EXP1                            X30X5MT
&NAME    DC    AL1(&IEDQZE(7)+&BIT),AL1(12+&ALG),AL1(0)
         AGO   .NXT1                                            X30X5MT
.EXP1    ANOP                                                   X30X5MT
&NAME    DC    AL1(&IEDQZE(7)+&BIT),AL1(16+&ALG),AL1(2)         X30X5MT
.NXT1    ANOP                                                   X30X5MT
         AIF   ('&MASK'(1,1) EQ 'X').XM
         DC    FL5'&MASK' .             ERROR MASK
         AGO   .DEST
.XM      DC    &MASK .                  ERROR MASK
.DEST    AIF   ('&DEST' NE 'ORIGIN').OPT
         DC    C'S',AL3(0) .            REDIRECT TO ORIGIN
         AGO   .MEND
.OPT     AIF   ('&DEST' NE '' AND '&DEST' NE 'DESTIN').OPN
         DC    C'D',AL3(0) .            REDIRECT TO DESTINATION
         AGO   .MEND
.OPN     AIF   ('&DEST'(K'&DEST,1) EQ '''').BUF
         DC    C'O',AL1(&IEDQZE(31)),AL1(&DEST),AL1(15)        @YM08438
         AGO   .MEND
.BUF     DC    C'N' .                   REDIRECT PER SPECIFIED DEST
IEDQMISC CSECT
         DC    AL1(&IEDQZD),&DEST .     DESTINATION
&C       CSECT
         DC    AL3(IEDQMISC+&IEDQZK)
&IEDQZK  SETA  &IEDQZK+1+&IEDQZD
.MEND    SPACE 2
         AIF   ('&EXIT' EQ '0').MEND1                           X30X5MT
         DC    V(&EXIT)                                         X30X5MT
.MEND1   ANOP                                                   X30X5MT
         MEND
