         MACRO
         IEDQVCON &SUB,&RTN
.*A-000000-999999                                              @Y17XAMN
.* &IEDQZL(64) - INDEX VALUE INTO IEDQMSGS
.* &IEDQZL(256) - INDEX VALUE INTO IEDQNVCN
.* &IEDQZLC - NEXT AVAILABLE INDEX INTO IEDQNVCN
.* &IN - INDEX INTO IEDQZL
         GBLA  &IEDQZE(64),&IEDQZL(256),&IEDQZLC
         LCLA  &IX,&IN
         LCLC  &C
.LOOP    ANOP
.* CHECK IF SECOND LEVEL INDEX TABLE IS NEEDED (&SUB>64).
&IX      SETA  &IX+1                    INDEX INTO SUB ENTRIES
         AIF   (&SUB(&IX) GT 64).CONNUT SUB>64?
.* CHECK IF FIRST LEVEL INDEX POSITION HAS BEEN ASSIGNED.
         AIF   (&IEDQZE(&SUB(&IX)) NE 0).NEXT
&C       SETC  '&SYSECT'                ASSIGN POSITION
IEDQMSGS CSECT
         DC    V(&RTN(&IX))
&C       CSECT
&IEDQZE(&SUB(&IX)) SETA &IEDQZE(1)
&IEDQZE(1) SETA &IEDQZE(1)+4
         AGO   .NEXT                                           @YM06460
.* CHECK IF CODE TO SETUP SECOND LEVEL POINTER TO MODULE
.* DESIRED HAS BEEN GENERATED AND BRANCH TO THAT MODULE.
.CONNUT  AIF   (&IEDQZLC NE 0).CONOVCN
&C       SETC  '&SYSECT'                GENERATE THE CODE
IEDQMSGS CSECT
         DC    A(IEDQNWMH)
&IEDQZE(48) SETA &IEDQZE(1)
&IEDQZE(1) SETA &IEDQZE(1)+4
IEDQNWMH CSECT
         LTR   15,15                    FROM BD                @YM06135
         BZ    16(12)                   NO, BRANCH             @YM06135
*        CODE EXECUTED WHEN ENTERING FROM IEDQBD               @YM06135
         SR    15,15                                           @YM06135
         IC    15,2(8)                  USE REG 8 FOR PARM LIST ADDR
         B     20(12)                                          @YM06135
*        CODE EXECUTED WHEN ENTERING FROM IEDQUI               @YM06135
         IC    15,2(1)                  USE REG 1 FOR PARM LIST ADDR
*        COMMONLY EXECUTED CODE                                @YM06135
         SLL   15,2                     CALCULATE DISPL TO ADDR
         L     12,IEDQNVCN-4-IEDQNWMH(15,12) LOAD ROUTINE ADDR @YM05674
         SR    15,15                    ZERO REGISTER 15
         BR    12                       BRANCH TO ROUTINE
IEDQNVCN DS    0F
&C       CSECT
&IEDQZLC SETA  1                        SET PRESENT INDEX
.CONOVCN ANOP
&IN      SETA  &SUB(&IX)-64             DETERMINE 2ND LVL NUM
.* CHECK IF SECOND LEVEL INDEX POSITION HAS BEEN ASSIGNED.
         AIF   (&IEDQZL(&IN) NE 0).NEXT
&C       SETC  '&SYSECT'                ASSIGN POSITION
IEDQNWMH CSECT
         DC    V(&RTN(&IX))
&C       CSECT
&IEDQZL(&IN) SETA &IEDQZLC
&IEDQZLC SETA &IEDQZLC+1                SET NEXT INDEX
.NEXT    AIF   (&IX LT N'&SUB).LOOP     MORE SUB ENTRIES?
         MEND
