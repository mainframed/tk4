         MACRO
&NAME    GROUP &BUFOUT=2,&BUFMAX=2,&MH=,&DCB=,&BUFSIZE=,&INVLIST=,     *
               &TRANS=EBCD,&RESERVE=,&PCI=(,A),&OPACING=,              *
               &PACK=NO                                        @Y17XAFF
.*A-000000-999999                                              @X31X8F0
.*C018000,042000,063000,234000,324000-326600,684000-71400      @Y17XAFF
.*C744000,804000-805000,594000                                 @Y17XAFF
.*A018000,063000,234000,246000,744000,594000                   @Y17XAFF
.*A238200,246000,732480                                        @OZ40805
         GBLA  &IEDQAR(64)
         GBLA  &IEDQRL
         GBLA  &KEYLN                                          @OZ36390
         GBLB  &IEDQNO,&IEDQZA(64),&IEDQRN(8)
         GBLB  &IEDQCOD(8)                                     @Y17XAFF
         GBLB  &IEDQZZ1(8)                                     @OX21221
         GBLC  &IEDQSV(8)
         GBLC  &IEDQGN
         LCLA  &B,&C,&D,&S,&L,&TRNDX
         LCLA  &M,&N,&OPACE                                    @Y17XAFF
         LCLB  &P                                              @Y17XAFF
         LCLC  &NMPR,&TRNM
.*C864600,868200                                                 Y05330
.*D078000                                                        Y05330
.*C018000,096000,216000,732000,750000,804000                     X03039
.*A060000,324000                                                 X03039
.*D864000-868200                                                 X03039
.*C234000,258000,261000,282000,324800,325800                   @Z30X8FG
.*C654000                                                      @SA05843
.*A042000                                                      @OX21221
.*C864500,865000                                               @OX21221
.*A030000                                                      @OZ36390
.*C300000                                                      @OZ36390
.*C236400                                                      @OZ37416
         SPACE 1
         AIF   (&IEDQRN(1)).A00
         MNOTE 12,'***  IED914  GROUP MUST FOLLOW INTRO MACRO WITH FEAT*
               URE=(,,,,MIXD3705/ONLY3705) OPERAND'              X03039
&IEDQNO  SETB  1
.A00     ANOP
         AIF   (&IEDQZA(55)).A01   CHECK TO SEE IF TTABLE CODED
         AIF   ('&IEDQZA(54)' EQ '&IEDQZA(55)' AND '&IEDQZA(53)'       *
               EQ '&IEDQZA(55)').A02
         MNOTE 12,'***  IHB315  SEQUENCE ERROR-MUST PRECEDE LAST ENTRY *
               SPEC. BY TTABLE MACRO'
         AGO   .A03
.A02     MNOTE 12,'***  IHB070  SEQUENCE ERROR-MUST FOLLOW TTABLE MACRO*
               '
.A03     ANOP
&IEDQNO  SETB  1
.A01     AIF   ('&NAME' NE '').A04
         MNOTE 12,'***  IHB076  MACRO NAME FIELD BLANK-NAME REQUIRED'
&IEDQNO  SETB  1
.A04     AIF   (K'&NAME LE &IEDQAR(1)-3).A05
&S       SETA  &IEDQAR(1)-3
         MNOTE 4,'***  IHB302  NAME FIELD LONG-TRUNCATED TO &S CHARACTE*
               RS'
.A05     AIF   ('&MH' NE '').A06 MH REQUIRED                     X03039
         MNOTE 12,'***  IHB004  REQUIRED OPERAND(S) NOT SPECIFIED'
&IEDQNO  SETB  1
.*                                                             @Y17XAFF
.*       CHECK OUT OPACING OPERAND                             @Y17XAFF
.*                                                             @Y17XAFF
.A06     AIF   ('&OPACING' EQ '').A069  IS OPCING OMITTED      @Y17XAFF
         AIF   (N'&OPACING EQ 1).A062   ARE THERE 1 PARAMETERS @Y17XAFF
.A061    MNOTE 4,'*** IED999 INVALID OPACING OPERAND, WILL USE BUFOUT  *
               AND BUFMAX.'                                    @Y17XAFF
         AGO   .A069                                           @Y17XAFF
.*       ARE OPERANDS NUMERIC                                  @Y17XAFF
.A062    AIF   (T'&OPACING(1) NE 'N').A061                     @Y17XAFF
.*                                                             @Y17XAFF
.*       CHECK FOR VALID RANGE                                 @Y17XAFF
         AIF   (&OPACING(1) LT 1 OR &OPACING(1) GT 15).A061    @OZ37416
.*       SET VALUES                                            @Y17XAFF
&OPACE   SETA  &OPACING(1)                                     @Y17XAFF
         AGO   .A07A                    BRANCH AROUND BUFMAX   @Y17XAFF
.*                                           AND BUFOUT        @Y17XAFF
.A069    AIF   (&BUFOUT LT 1 OR &BUFOUT GT 15).A071            @Y17XAFF
         AIF   ('&PCI' EQ '(,N)').SKIPMAX    BUFMAX NOT NEEDED @OZ40805
         AIF   (&BUFMAX LT 2 OR &BUFMAX GT 15).A07
         AIF   (&BUFMAX LT &BUFOUT).A07
.SKIPMAX ANOP                                                  @OZ40805
.*       SET VALUES                                            @Y17XAFF
&M       SETA  &BUFOUT                                         @Y17XAFF
&N       SETA  &BUFMAX                                         @Y17XAFF
         AGO   .A07A
.A07     MNOTE 12,'IHB002 INVALID BUFMAX OPERAND SPECIFIED - &BUFMAX'
.*                                                             @Z30X8FG
         AGO   .A072                                           @Z30X8FG
.A071    MNOTE 12,'IHB002 INVALID BUFOUT OPERAND SPECIFIED - &BUFOUT'
.*                                                             @Z30X8FG
.A072    ANOP                                                  @Z30X8FG
&IEDQNO  SETB  1
.A07A    AIF   ('&BUFSIZE' EQ '').A08
         AIF   (&BUFSIZE GT 35 AND &BUFSIZE LT 65536).A09
         MNOTE 12,'IHB002 INVALID BUFSIZE OPERAND SPECIFIED - &BUFSIZE'
.*                                                             @Z30X8FG
&IEDQNO  SETB  1
.A08     ANOP
&B       SETA  &KEYLN                   BFSZ DEFAULT TO KEYLEN @OZ36390
         AGO   .A10
.A09     ANOP
&B       SETA  &BUFSIZE
.*                                                             @Y17XAFF
.*       CHECK OUT PACK OPERAND                                @Y17XAFF
.*                                                             @Y17XAFF
.A10     AIF   ('&PACK' EQ 'NO').A106                          @Y17XAFF
         AIF   ('&PACK' EQ 'YES').A105                         @Y17XAFF
         MNOTE 4,'*** IED999 INVALID PACK OPERAND, WILL ASSUME PACK=NO *
               '                                               @Y17XAFF
         AGO   .A106                                           @Y17XAFF
.A105    ANOP                                                  @Y17XAFF
&P       SETB  1                        SET CTL BLOCK VALUE    @Y17XAFF
.A106    ANOP                                                  @Y17XAFF
&NMPR    SETC  'IEDQ'
&TRNDX   SETA  25
         AIF   ('&TRANS' EQ 'EBCD').SETTR
         AIF   ('&TRANS' NE 'EBCF').A15
&NMPR    SETC  'IEDN'
         AGO   .SETTR
.A15     ANOP
&TRNDX   SETA  0
.SETTR   ANOP
&TRNM    SETC  '&TRNDX'
         AIF   (&IEDQNO).EXIT
&IEDQRL  SETA  1
&IEDQCOD(1) SETB  1                     INDICATE GROUP IS CODED@Y17XAFF
&IEDQCOD(2) SETB  0                     RESET LINE AND PUNT    @Y17XAFF
&IEDQCOD(3) SETB  0                          CODED INDICATORS  @Y17XAFF
&IEDQCOD(6) SETB  0                                            @Y17XAFF
&IEDQGN  SETC  '&NAME'
&IEDQAR(4) SETA &IEDQAR(4)+1
         AIF   (&IEDQAR(4) LE 9999).SP
&IEDQAR(4) SETA 0
&IEDQAR(7) SETA &IEDQAR(7)+1
.SP      SPACE 1
IEDQTNT  CSECT
&L       SETA  &IEDQAR(1)-3
         DC    CL&L'&NAME'  .   ENTRY NAME
         DC    AL3(&NAME.+21)                                  @SA05843
         SPACE 1
&IEDQSV(4)  CSECT
&IEDQSV(1) SETC '&NAME'
&C       SETA  N'&INVLIST
         ORG   *-17 .                   ELIMINATE UNUSED AREA  @Y17XAFF
&NAME    DS    0F .                     ORIGIN ON WORD BOUNDARY@Y17XAFF
         ORG   *+17 .                   TO ORIGIN GENERATION   @Y17XAFF
         DC    X'00' .                  GROUP TYPE             @Y17XAFF
         DC    AL2(0) .                 GRP SUBSTRUCT POINTER  @Y17XAFF
         DC    AL1(&M+16) .             BUFOUT OR OPACING(1)   @PI03727
         DC    AL3(&MH) ADDRESS OF MH
         DC    AL1(0) NUMBER OF LINES IN GROUP
         AIF   ('&PCI(2)' NE 'N').CHKA                           X03039
         AIF   (&OPACE EQ 0).PCINOK     IF PCI EQ N AND OPACING EQ 0
.*                                       MUST FORCE PCI=A      @Y17XAFF
         MNOTE 8,'*** IED995 PCI=N AND OPACING GT 0 ARE MUTUALLY EXCLUS*
               IVE, HAVE DEFAULTED TO PCI=A.'                  @Y17XAFF
         AGO   .PCIA                                           @Y17XAFF
.PCINOK  ANOP                                                  @Y17XAFF
         AIF   ('&BUFMAX' EQ '2').CONT                         @OZ40805
         MNOTE 4,'BUFMAX NOT REQUIRED WITH PCI=(,N)- BUFMAX IGNORED AS *
               SPECIFIED'                                      @OZ40805
.CONT    ANOP                                                  @OZ40805
         DC    XL1'04' .                PCI=NO                   X03039
         AGO   .DSORG                                            X03039
.CHKA    ANOP                                                    X03039
         AIF   ('&PCI(2)' EQ 'A').PCIA                           X03039
         MNOTE 4,'IHB303 INVALID OPERAND PCI, (,A) ASSUMED'      X03039
.PCIA    ANOP                                                    X03039
         DC    XL1'10' .                PCI=ADD                  X03039
.DSORG   ANOP                                                    X03039
         DC    BL2'1000000000000000' DSORG
         DC    AL1(&N) .                BUFMAX OR OPACING(2)   @Y17XAFF
         DC    B'&P.0000000' .          FLAGS                  @Y17XAFF
         DC    AL1(&OPACE) .            OPACING VALUE          @Y17XAFF
         DC    AL1(0) .                 RESERVED               @Y17XAFF
         DC    BL1'0' FLAGS
         AIF   (&TRNDX EQ 0).A10B
         DC    VL3(&NMPR&TRNM) ADDRESS OF TRANS TABLE
         AGO   .A10A
.A10B    DC    AL3(&TRANS) ADDRESS OF TRANS TABLE
.A10A    ANOP
         DC    A(0) RESERVED
         DC    CL8'&NAME' GROUP NAME
         DC    XL1'10' OFLGS                                   @PI03723
         DC    VL3(IEDIAU) ADDRESS OF SCT                        X03039
         DC    AL1(&C) ILCT
         DC    AL1(0) RESERVED
         DC    AL2(&B) BUFSIZE
         DC    AL1(0) .                 RESERVED               @Y17XAFF
         DC    AL1(0) .                 RESERVED               @Y17XAFF
         DC    AL1(0)                   RESERVE                  X03039
         DC    AL1(0)                   RESERVE                  X03039
&D       SETA  1
.A12     AIF   ('&INVLIST(&D)' NE '').A13
         DC    A(0) ADDRESS OF INVLIST
         AIF   (&C EQ 0).A11
         AGO   .A14
.A13     DC    A(&INVLIST(&D)) ADDRESS OF INVLIST
.A14     AIF   (&D EQ &C).A11
&D       SETA  &D+1
         AGO   .A12
.A11     SPACE 2
         AIF   (&C EQ 0 OR &IEDQZZ1(4)).EXIT1                  @OX21221
&IEDQZZ1(4) SETB 1                                             @OX21221
IEDNSAVT CSECT .                                               @YM05633
         ORG   IEDID .                                         @YM05633
         DC    V(IEDNID) .             BSC ID VERIFICATION RTN @YM05633
         ORG
.EXIT1   ANOP                                                  @YM05633
IEDQTRMC CSECT                                                   Y05330
         SPACE 2
.EXIT    ANOP
         MEND
