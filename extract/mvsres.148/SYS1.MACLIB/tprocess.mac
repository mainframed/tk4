         MACRO
&NAME    TPROCESS   &PCB=,&QUEUES=,&ALTDEST=,&OPDATA=,&SECTERM=NO,     *
               &SYNC=NO,&CKPTSYN=NO,&X=(A,B,C,D,E,F),&SECURE=NO, S22024*
               &QS=(DN,DR,MO,MR,MN,34,18,66,82,98),&LEVEL=(0),         *
               &RECDEL=00,&DATE=NO,&QBACK=NO,&LU=NO,&POS       @OX21220
         GBLB  &IEDQZA(64),&IEDQTTF(64),&IEDQFL(16)            @PI06895
         GBLA  &IEDQAR(64)
         GBLC  &IEDQSV(8)
         LCLA  &A,&L
         LCLB  &NO,&B(8)
         LCLC  &V
.*A553000                                                        S22027
.*C035000,553000                                                 S22027
.*A840000                                                        S99528
.*A080000,493000,500000                                          S22029
.*A490000,497000                        CHANGED 10/15/71         A44888
.*C868000                                                        S21101
.*A519000-524000,556000                                          S22025
.*C035000,511000                                                 S22025
.*D707000                                                        S22025
.*A605000,843000,897000-902000                                   X01004
.*C511000,882000                                                 X01004
.*D350000,518000,854000-875000,903---                            X01004
.*C581000                                                        Y01004
.*A847000                                                        X02004
.*A602000                                                       SA63593
.*A840100,843100,882000,900000                                   Y05330
.*C000014                                                        Y05330
.*C002500,553060,553900,555100,833000
.*D0060000
.*A083600,53120
.*C580000                                                      @PI02863
.*A696000                                                      @OZ26494
.*C580000,582000                                               @G36XRFF
.*C002500                                                      @OX21220
.*A008000,136000                                               @OX21220
.*C652000                                                      @OZ48579
.*PACKAGE ERROR                                                @OZ49699
&IEDQFL(9) SETB 0                                                S22025
         AIF   ('&POS' NE '').INVPO                            @OX21220
         AIF   (&IEDQAR(10) EQ 0).C0
         AIF   ('&NAME' EQ '&IEDQSV(1)' AND &IEDQAR(10) EQ 2).OPT
         IEDQGCH 2
.C0      ANOP                                                  @YM06883
&IEDQFL(14) SETB 1                WANT TPROCESS DEV DEP INFO   @PI06895
&IEDQFL(1)  SETB 1                WANT BUFSIZE DEV DEP INFO    @PI06895
&IEDQAR(17) SETA 0                      INIT BUFSIZE VALUE     @YM06883
         AIF   (&IEDQZA(55)).C1
         AIF   (&IEDQZA(54) EQ &IEDQZA(55) AND &IEDQZA(53) EQ &IEDQZA(5*
               4)).NOTAB
         MNOTE 12,'*** IHB315 SEQUENCE ERROR- MUST PRECEDE LAST ENTRY  *
                SPECIFIED BY TTABLE MACRO'
         AGO   .SETNO
.INVPO   MNOTE 12,'*** IHB027 EXECSSIVE POSITIONAL PARAMETER SPECIFIED-*
                &POS'                                          @OX21220
         AGO   .SETNO                                          @OX21220
.NOTAB   MNOTE 12,'*** IHB070 SEQUENCE ERROR- MUST FOLLOW TTABLE MACRO'
.SETNO   ANOP
&NO      SETB  1
.C1      AIF   ('&NAME' NE '').C1A
         MNOTE 12,'*** IHB076 MACRO NAME FIELD BLANK- NAME REQUIRED'
&NO      SETB  1
.C1A     AIF   ('&PCB' NE '').C2
         MNOTE 12,'*** IHB004 REQUIRED OPERAND(S) NOT SPECIFIED'
         SPACE 2
         MEXIT
.C2      AIF   (&NO).C3
         AIF   (K'&NAME LE &IEDQAR(1)-3).C3
&A       SETA  &IEDQAR(1)-3
         MNOTE 4,'*** IHB302 NAME FIELD LONG- TRUNCATED TO &A CHARS'
         SPACE 2
&A       SETA  0
.C3      AIF   ('&CKPTSYN' EQ 'NO').C3A
&B(6)    SETB  1
         AIF   ('&CKPTSYN' EQ 'YES').C3B
         MNOTE 12,'*** IHB002 INVALID CKPTSYN OPERAND SPECIFIED- &CKPTS*
               YN'
&NO      SETB  1
         AGO   .C3B
.C3A     AIF   ('&SYNC' EQ 'NO').C3B
&B(6)    SETB  1
         AIF   ('&SYNC' EQ 'YES').C3B
         MNOTE 12,'*** IHB002 INVALID SYNC OPERAND SPECIFIED- &SYNC'
&NO      SETB  1
.C3B     AIF   ('&QUEUES' EQ '').PUT                           @PI06895
&B(5)    SETB  1
.C4      AIF   (&A GT 4).ERR3
&A       SETA  &A+1
         AIF   ('&QUEUES' NE '&QS(&A)').C4
&A       SETA  &QS(&A+5)
         AIF   (&A LE 34).C5
         AIF   (NOT &IEDQZA(42)).ERR2
.C5      AIF   (K'&RECDEL EQ 2).C6
         MNOTE 12,'*** IHB002 INVALID RECDEL OPERAND SPECIFIED- &RECDEL*
               '
&NO      SETB  1
.C6      AIF   (&L+1 GT N'&LEVEL-1).C7
&L       SETA  &L+1
         AIF   ('&LEVEL(&L+1)' GT '&LEVEL(&L)').C6
         MNOTE 12,'*** IHB002 INVALID LEVEL OPERAND SPECIFIED- &LEVEL(&*
               L)'
&NO      SETB  1
         AGO   .C6
.C7      AIF   (&NO).EXT
&IEDQZA(39) SETB 1                                               A44888
&IEDQZA(17) SETB 1                                               S22029
         IEDQTQ &PCB,&A,0,0,&LEVEL
&IEDQZA(39) SETB 0                                               A44888
&IEDQAR(5) SETA 0                                               SA51075
&IEDQZA(17) SETB 0                                               S22029
&NAME    IEDQTT
&B(7)    SETB  ('&OPDATA' NE '')                                 X01004
.C7A     AIF   ('&DATE' EQ 'NO').GEN                             S22025
&IEDQTTF(1) SETB 1                      SET DATE=YES INDICATOR @Y17XAFF
         AIF   ('&DATE' EQ 'YES').GEN                            S22025
         MNOTE 12,'*** IHB002 INVALID DATE OPERAND SPECIFIED - &DATE'
&NO      SETB  1                                                 S22025
.GEN     DC    BL1'0010&B(5)&B(6)&B(7)&B(8)' . TRMSTATE        @Y17XAFF
         DC    AL3(&IEDQSV(2)),2H'01'   TRMDESTQ,INSEQ,OUTSQ   @PI06895
         AIF   ('&ALTDEST' EQ '').NAL
&A       SETA  82-&IEDQAR(1)
         DC    AL2((&ALTDEST-IEDQTNT-&A)/&IEDQAR(1))
.ST      AIF   ('&SECURE' EQ 'NO').L1                          @PI06895
         AIF   ('&SECURE' NE 'YES').SECERR                       S22024
&IEDQZA(43) SETB 1                      INDICATE SECURE=YES    @Y17XAFF
         AGO   .L1                                               S22024
.SECERR  MNOTE 4,'*** IHB086 SECURE INVALID KEYWORD, NO ASSUMED' S22024
.L1      AIF   ('&LU' EQ 'NO').L10     VALIDATE LU OPERAND     @PI06895
         AIF   ('&LU' EQ 'YES').L5                             @Y17XAFF
         MNOTE 4,'*** IED930 INVALID LU OPERAND,LU=NO ASSUMED' @Y17XAFF
         AGO   .L10                                            @Y17XAFF
.L5      ANOP                                                  @Y17XAFF
&IEDQZA(49) SETB 1                      INDICATE LU=YES CODED  @Y17XAFF
.L10     AIF   ('&QBACK' EQ 'NO').ST1A                         @PI06895
         AIF   ('&QBACK' NE 'YES').QBERR                         S22027
         DC    BL2'&IEDQFL(1).000000000000&IEDQFL(14).00'      @Y17XAFF
.*                                      DEV DEP FLD FLAGS      @Y17XAFF
         DC    XL3'0',XL1'01',XL1'&RECDEL' .                     S22024
         AGO   .ST1                                              S22027
.QBERR   MNOTE 12,'*** IHB300 QBACK OPERAND INVALID AS SPECIFIED'
.ST1A    DC    BL2'&IEDQFL(1).000000000000&IEDQFL(14).00'      @PI06895
.*                                      DEV DEP FLD FLAGS      @Y17XAFF
         DC    XL4'0',XL1'&RECDEL' .                             S22024
.ST1     AIF   (&IEDQZA(53)).ST2                                 Y02027
&IEDQZA(53) SETB  1
&IEDQSV(4) CSECT .                                               Y02027
         ORG   IEDOCQPT
         DC    V(IEDQEU)                OPEN/CLOSE QCB ADDRESS @G36XRFF
         ORG
.ST2     ANOP                                                    Y02027
&IEDQZA(54)  SETB  1
IEDQSTCS CSECT .                                                SA63593
IEDQTRMC CSECT                                                   X01004
         ORG   &IEDQSV(2)+28
&V       SETC  'IED'.'&IEDQAR(4)'.'&X(&IEDQAR(7))'
         DC    A(&V)
         ORG
         AIF   (N'&OPDATA EQ 0).CSE
.OPT     ANOP                                                  @OZ48579
&NAME    IEDQTO  &OPDATA                                       @OZ48579
         AIF   (&IEDQAR(5) EQ 0).CSE
&IEDQAR(10) SETA 2
         AGO   .EXT
.NAL     DC    AL2(0) .                NO ALTERNATE DESTINATION
         AGO   .ST
.PUT     ANOP
         AIF   ('&LU' EQ 'YES').BADPUT                         @OZ26494
&IEDQZA(38)  SETB  1
&A       SETA  2
         AIF   ('&SECTERM' EQ 'NO').C5
&B(8)    SETB  1
         AIF   ('&SECTERM' EQ 'YES').C5
         MNOTE 12,'*** IHB002 INVALID SECTERM OPERAND SPECIFIED- &SECTE*
               RM'
&NO      SETB  1
         AGO   .C5
.BADPUT  MNOTE 12,'*** IHB345 QUEUES OPERAND REQUIRED WHEN LU=YES IS SPX
               ECIFIED'                                        @OZ26494
&NO      SETB  1                                               @OZ26494
         AGO   .C5                                             @OZ26494
.ERR2    MNOTE 12,'*** IHB318 QUEUES OPERAND &QUEUES INVALID WITH MSUNI*
               TS=0'
&NO      SETB  1
         AGO   .C5
.ERR3    MNOTE 12,'*** IHB002 INVALID QUEUES OPERAND SPECIFIED- &QUEUES*
               '
&NO      SETB  1
         AGO   .C5
.CSE     ANOP                                                  @Y17XAFF
.*       GO GENERATE DEV DEP FIELDS                            @Y17XAFF
         IEDQGCH                                               @Y17XAXF
         AIF   ('&NAME' NE '&IEDQSV(5)').EXT
         IEDNOL ,                                                S99528
         AIF   (&IEDQAR(14) NE 0).GOTSCB                         Y05330
&IEDQAR(14) SETA 1                                               Y05330
.GOTSCB  ANOP                                                    Y05330
IEDQSCBC CSECT                                                   Y05330
IEDQTRME DC    AL2(&IEDQAR(11)),AL2((&IEDQAR(14)+7)/8)           X01004
&IEDQZA(55) SETB   0
IEDQTNT  CSECT                                                   X02004
         DC    A(IEDQTRMC)                                       X02004
         AIF   (&IEDQAR(3) EQ 0).CKR                             X01004
IEDQOPC  CSECT
         DC    X'FF'                    END OF TABLE             Y05330
IEDQOPT  CSECT
IEDQOPTN DC    AL2(&IEDQAR(15))
.CKR     ANOP                                                    X01004
&IEDQSV(4) CSECT                                                 X01004
&A       SETA  0                                                 Y05330
&L       SETA  0                                                 Y05330
IEDOCQE  EQU   255                      INDICATE IEDQEU TO BE    Y05330
*                                       LOADED BY OPEN           Y05330
         AIF   (&IEDQAR(33) EQ 0 AND &IEDQAR(34) EQ 0).CXXG      Y05330
&A       SETA  (&IEDQAR(34)*4+1)                                 Y05330
&L       SETA  (&IEDQAR(33)*4+3)                                 Y05330
.CXXG    ANOP                                                    Y05330
IEDQNADR EQU   &L                                                Y05330
IEDQNADN EQU   &A                                                Y05330
.EXT     SPACE 2
         MEND
