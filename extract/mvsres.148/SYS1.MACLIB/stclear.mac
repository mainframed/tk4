         MACRO
&NAME    STCLEAR &STRING=               PROTOTYPE
.**********************************************************************
.*                                                                    *
.*RELEASE 23 DELETIONS                                                *
.*                                                                    *
.*RELEASE 22 DELETIONS                                                *
.*                                                                    *
.*RELEASE 21 DELETIONS                                                *
.*                                                                    *
.*STATUS - CHANGE LEVEL 000                                           *
.*                                                                    *
.*NAME - STCLEAR                                                      *
.*                                                                    *
.*FUNCTION - THIS MACRO IS USED TO SPECIFY THE CHARACTER STRING THAT  *
.*   WILL BE USED TO REQUEST THAT A DISPLAY STATION SCREEN CAN BE     *
.*   ERASED.                                                          *
.*                                                                    *
.*       THE MACRO PROTOTYPE IS ABOVE                                 *
.*                                                                    *
.*       THE STANDARD FORM OF THE EXPANSION IS BELOW                  *
.*NAME   LA    1,CHAR                   PUT ADDR OF CHAR STRING IN REG*
.*       LA    0,16                     LOAD ENTRY CODE               *
.*       SLL   0,24                     PUT ENTRY CODE IN HIGH ORDER  *
.*       SVC   94                       ISSUE STATUS SVC              *
.*                                                                    *
.**********************************************************************
         AIF   ('&STRING' EQ '').ERROR
         AIF   ('&STRING' EQ '0').ZERO
         AIF   ('&STRING'(1,1) EQ '(').REG
&NAME    LA    1,&STRING .              PUT ADDR OF CHAR STRING IN REG
         AGO   .ENTRYCD .
.REG     ANOP
&NAME    LR    1,&STRING .              PUT ADDR OF CHAR STRING IN REG
         AGO   .ENTRYCD
.ZERO    ANOP
&NAME    SR    1,1 .                    ZERO OUT REG
.ENTRYCD LA    0,16 .                   LOAD ENTRY CODE
         SLL   0,24 .                   PUT ABOVE IN HIGH ORDER
         SVC   94 .                     ISSUE STATUS SVC
         AGO   .EXIT
.ERROR   IHBERMAC 24
.EXIT    ANOP
         MEND
