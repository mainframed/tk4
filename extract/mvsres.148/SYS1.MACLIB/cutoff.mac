         MACRO
&NAME     CUTOFF &LEN
         GBLA  &IEDQZE(64)
         GBLB  &IEDQZA(64)
         LCLA  &L
         LCLB  &NOGO
         LCLC  &C
.*  CHANGE ACTIVITY AS FOLLOWS
.******************* MICROFICHE FLAGS *********************** SUPT CODE
.*C 140000,160000,200000                                        SA63017
.******     TCAM  LEVEL  5B    *********
         AIF   (&IEDQZA(14)).CHECK                              SA63017
         AIF   (&IEDQZA(1) AND NOT &IEDQZA(3) AND NOT &IEDQZA(5)).CHECK
         MNOTE 12,'***  IHB070  SEQUENCE ERROR-MUST FOLLOW INBLOCK OR I*
               NBUF MACRO'                                      SA63017
&NOGO    SETB  1
.CHECK   AIF   ('&LEN' EQ '').LENERR
         AIF   ('&LEN'(K'&LEN,1) EQ ')').LENERR
         AIF   (K'&LEN EQ 1 AND '&LEN' EQ '0').LENERR
         AIF   ('&LEN' EQ 'X''0000''' OR '&LEN' EQ 'XL2''0000''' OR '&L*
               EN' EQ 'X''00''' OR '&LEN' EQ 'XL1''00''').LENERR
         IEDQMASK &LEN,2
         AIF   (NOT &IEDQZA(24)).ORG
.LENERR  MNOTE 12,'***  IHB300  LENGTH OPERAND INVALID AS SPECIFIED'
&NOGO    SETB  1
.ORG     AIF   (&NOGO).MEND
         IEDQVCON 10,IEDQAU
&NAME    BAL   1,*+8 .                  SKIP AROUND PARMLIST
         DC    AL1(&IEDQZE(10)),AL1(4)
         AIF   ('&LEN'(K'&LEN,1) EQ '''').HEX
         DC    AL2(&LEN) .              MAXIMUM MESSAGE LENGTH
         AGO   .OUT
.HEX     AIF   ('&LEN'(2,1) EQ 'L').XL
&L       SETA  (K'&LEN-3+1)/2
         AGO   .DC
.XL      ANOP
&C       SETC  '&LEN'(3,1)
&L       SETA  &C
.DC      AIF   (&L EQ 2).SET
         DC    AL1(0)
.SET     DC    &LEN .                   MAXIMUM MESSAGE LENGTH
.OUT     L     15,IEDUI .               TO CUTOFF ROUTINE
         BALR  14,15
.MEND    SPACE 2
         MEND
