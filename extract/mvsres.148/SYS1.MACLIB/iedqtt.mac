         MACRO
&NAME    IEDQTT &A=(A,B,C,D,E,F),&X=0,&TAC=0,&ACTV=,&GPCU=,    @SA05843X
               &ALIAS=,&TERM=,&SPECOUT=,&UTERM=,&TCMSESN=      @Y17XAFF
.*A-000000-999999                                              @X31X8F0
         GBLA  &IEDQAR(64)
         GBLB  &IEDQFL(16) .                                     S22025
         GBLB  &IEDQCOD(8),&IEDQZA(64)                         @Y17XAFF
         GBLC  &IEDQSV(8),&IEDQCTM .                             S22025
         LCLA  &B,&L                                             Y05330
         LCLC  &TYPE                                           @Y17XAFF
         LCLB  &B0,&B1,&B2,&B3,&B4,&B5,&B6,&B7                 @Y17XAFF
.*C060000,110000-130000,420000                                   S22025
.*A400000-410000                                                 S22025
.*C840000                                                        X02004
.*A380000,780000                                                 Y05330
.*C060000,180000                                                 Y05330
.*D846000-850000                                                 X03039
.*A060000                                                      @Z30X8FG
.*A062000,853000                                               @X50X9FH
.*C060000,190000,856000                                        @X50X9FH
.*C063000,190000,842000-862000                                 @Y17XAFF
.*A120000                                                      @Y17XAFF
.*C060000                                                      @SA05843
.*C849980,850040                                               @OZ33565
&IEDQAR(4)  SETA  &IEDQAR(4)+1
         AIF   (&IEDQAR(4) LE 9999).SP
&IEDQAR(4)  SETA  0
&IEDQAR(7)  SETA  &IEDQAR(7)+1
.SP      SPACE 1
&L       SETA  N'&ALIAS+1                                        Y05330
IEDQTNT  CSECT
&B       SETA  &IEDQAR(1)-3
&IEDQSV(1)  SETC  'IED'.'&IEDQAR(4)'.'&A(&IEDQAR(7))'
         AIF   (&IEDQFL(9) AND &TAC NE 0).NCTRM .                S22025
&IEDQCTM SETC '&IEDQSV(1)' .                                     S22025
.NCTRM   AIF   ('&X' NE '0').SPC .                               S22025
&NAME    DC    CL&B'&NAME'  .   ENTRY NAME
         AGO    .COM
.SPC     ANOP
&IEDQSV(1)  SETC  '&NAME'
&IEDQSV(8)  SETC  'IED&IEDQAR(4).&A(&IEDQAR(7))'
IED&IEDQAR(4).&A(&IEDQAR(7)) DC  CL&B'&NAME'
.COM     DC    AL3(&IEDQSV(1))
&L       SETA  &L-1                                              Y05330
         AIF   (&L EQ 0).DECAL                                   Y05330
         AIF   ('&ALIAS(&L)'(1,1) NE '''').CLC                   Y05330
         DC    XL&B*2&ALIAS(&L)         HEX ALIAS                Y05330
         AGO   .COM                                              Y05330
.CLC     ANOP                                                    Y05330
&ALIAS(&L) DC  CL&B'&ALIAS(&L)'         ALIAS NAME               Y05330
         AGO   .COM                                              Y05330
.DECAL   ANOP                                                    Y05330
         SPACE 1
IEDQTRMC CSECT                                                   X02004
.*                                                             @Y17XAFF
.*       CREATE TTE PREFIX IF NECESSARY                        @Y17XAFF
.*                                                             @Y17XAFF
         AIF   (NOT &IEDQZA(63)).A100                          @Y17XAFF
         CNOP  2,4                                             @Y17XAFF
.*       SETUP TRMBYTE4                                        @Y17XAFF
&B6      SETB  0                        INITIALIZE BIT 6       @Y17XAFF
.*       IS IT PRE-SNA                                         @Y17XAFF
         AIF   ('&TERM' EQ 'LNCP' OR '&TERM' EQ 'SSCP' OR              *
               '&TERM' EQ 'PUNT' OR '&TERM' EQ 'LUNT').A001    @Y17XAFF
.*       SETUP TRMSPOUT                                        @Y17XAFF
&B6      SETB  ('&SPECOUT' EQ 'YES' AND '&UTERM' EQ 'NO')      @Y17XAFF
.A001    ANOP                                                  @Y17XAFF
         DC    B'000000&B6.0' .         TRMBYTE4               @Y17XAFF
.*       SETUP TRMBYTE3                                        @Y17XAFF
&B0      SETB  0                        INITIALIZE BIT 0       @Y17XAFF
&B1      SETB  0                        INITIALIZE BIT 1       @Y17XAFF
&B2      SETB  0                        INITIALIZE BIT 2       @Y17XAFF
&B7      SETB  0                        INITIALIZE BIT 7       @Y17XAFF
.*       SETUP TRMLUTM                                         @Y17XAFF
         AIF   ('&TERM' NE 'LUNT' OR '&ACTV(2)' NE 'YES').A003 @XM05694
         AIF   ('&TCMSESN' EQ 'LUTERM').A002                   @XM05694
         MNOTE 4,'IED997 IF AUTO BIND IS SPECIFIED, WILL FORCE TCMSESN=*
               LUTERM FOR LU.'                                 @XM05694
.A002    ANOP                                                  @XM05694
&B2      SETB  1                                               @XM05694
         AGO   .A004                                           @XM05694
.A003    ANOP                                                  @XM05694
&B2      SETB  ('&TERM' EQ 'LUNT' AND ('&TCMSESN' EQ 'LUTERM'          *
               OR '&ACTV(2)' EQ 'YES'))                        @XM05694
.A004    ANOP
.*       SETUP TRMLUIT                                         @Y17XAFF
&B1      SETB  ('&TERM' EQ 'LUNT' AND '&TCMSESN' EQ 'LUINIT')  @Y17XAFF
         AIF   (NOT &B1).A005           LUINIT IMPLIES LUTERM  @Y17XAFF
&B2      SETB  1                        SET LUTERM BIT         @Y17XAFF
.*       SETUP TRMAUTO                                         @Y17XAFF
.A005    ANOP                                                  @Y17XAFF
&B7      SETB  ('&TERM' EQ 'LUNT')                             @Y17XAFF
&B0      SETB  ('&TERM' EQ 'LUNT' AND '&ACTV(2)'   EQ 'YES')   @Y17XAFF
         AIF   ('&TERM' NE 'LNCP').A010                        @YM08462
&B2      SETB  ('&ACTV(1)'   EQ 'YES' OR T'&ACTV EQ 'O')       @YM08462
.A010    ANOP                                                  @Y17XAFF
         DC    B'&B0&B1&B2.0000&B7' .   TRMBYTE3               @Y17XAFF
.*       SETUP TRMBYTE2                                        @Y17XAFF
&B0      SETB  0                        INITIALIZE BIT 0       @Y17XAFF
&B1      SETB  0                        INITIALIZE BIT 1       @Y17XAFF
.*       SETUP TRMREMB                                         @Y17XAFF
         AIF   ('&TERM' EQ 'SSCP').A020 NO ACTV CHK FOR SSCP   @PI02855
         AIF   (&IEDQCOD(2)).A014 .     IF LINE'S BEEN CODED   @Y17XAFF
.*                                        LOOK ONLY AT ACTV(1) @Y17XAFF
&B1      SETB  ('&UTERM' EQ 'YES')                             @PI02855
         AIF   (&B1).A020               IF UTERM EQ YES DO NOT @PI02855
.*                                       CHECK ACTV            @PI02855
&B1      SETB  (('&ACTV(1)' EQ 'YES' OR T'&ACTV EQ 'O') AND '&UTERM'   *
               NE 'IDLE')                                      @OZ33565
         AGO   .A020 .                  OTHERWISE LOOK AT UTERM@Y17XAFF
.A014    ANOP                             ALSO                 @Y17XAFF
&B1      SETB  ('&ACTV(1)' EQ 'YES' OR T'&ACTV EQ 'O')         @Y17XAFF
         AIF   ('&UTERM' NE 'IDLE' OR '&TERM' NE 'LINE').A020 @ZM04782
         MNOTE 4,'*** IED998 IMPROPER USE OF UTERM=IDLE.  OPERAND IGNOR*
               ED.'                                            @Y17XAFF
.A020    ANOP                                                  @Y17XAFF
.*       SETUP TRMRSACT                                        @Y17XAFF
&B0      SETB  ('&TERM' EQ 'SSCP')                             @Y17XAFF
         AIF   ('&TERM' EQ 'SSCP' OR '&TERM' EQ 'LUNT' OR              *
                '&TERM' EQ 'PUNT' OR '&TERM' EQ 'LINE' OR              *
                '&TERM' EQ 'LNCP').A025                        @Y17XAFF
         AIF   ('&UTERM' NE 'NO' AND NOT &IEDQCOD(2)).A025     @Y17XAFF
&B0      SETB  (&B1)                    MAKE TRMRSACT THE      @Y17XAFF
&B1      SETB  0                         ACTIVE=YES BIT IF IT  @Y17XAFF
.A025    ANOP                            IS A NCP PRE-SNA TERM @Y17XAFF
         DC    B'&B0&B1.000000' .       TRMBYTE2               @Y17XAFF
.*       SETUP TRMBYTE1                                        @Y17XAFF
         DC    B'00000000' .            TRMBYTE1               @Y17XAFF
         DC    H'0' .                   TRMCOHRT               @Y17XAFF
.*       SETUP TRMBYTE0                                        @Y17XAFF
&B1      SETB  0 .                      INITIALIZE BIT 1       @Y17XAFF
&B2      SETB  0 .                      INITIALIZE BIT 2       @Y17XAFF
&B6      SETB  0                        INITIALIZE BIT 6       @Y17XAFF
&B7      SETB  0                        INITIALIZE BIT 7       @Y17XAFF
.*       SETUP TRMINPUT                                        @Y17XAFF
&B1      SETB  ('&TERM' EQ 'LUNT' OR '&TERM' EQ '327C')        @Y17XAFF
.*       SETUP TRMOUTPT                                        @Y17XAFF
&B2      SETB  ('&TERM' EQ 'LUNT')                             @Y17XAFF
.*       SETUP TRMBUFRD                                        @Y17XAFF
&B7      SETB  (('&TERM' EQ '274I' OR '&TERM' EQ '274J' OR '&TERM' EQ  *
               '277A' OR '&TERM' EQ '277B') AND ('&UTERM' EQ 'NO' OR   *
               &IEDQCOD(2)))                                   @Y17XAFF
.*       SETUP TRMSNA                                          @Y17XAFF
&B6      SETB  ('&TERM' EQ 'LNCP' OR '&TERM' EQ 'PUNT' OR '&TERM' EQ   *
               'LUNT' OR '&TERM' EQ 'SSCP')                    @Y17XAFF
         DC    B'0&B1&B2.000&B6&B7' .   TRMBYTE0               @Y17XAFF
.*                                                             @Y17XAFF
         DC    AL3(0) .                 TRMSIBPT/TRMGPCNT/     @Y17XAFF
*                                        TRMCOHTG              @Y17XAFF
         DC    XL1'00' .                TRMRLMCT               @Y17XAFF
.*                                                             @Y17XAFF
.*       DEFINE TRMTYPE
.*                                                             @Y17XAFF
&TYPE    SETC  '4F'                     INITIALIZE TO TRMPSNA  @Y17XAFF
         AIF   ('&UTERM' EQ 'NO').A030                         @Y17XAFF
         AIF   (&IEDQCOD(2)).A030                              @Y17XAFF
&IEDQCOD(6) SETB 1                      CODED UTERM= FOR       @Y17XAFF
.*                                        LINE DEFINITION      @Y17XAFF
&TYPE    SETC  '20'                     SET TRMNSDLC           @Y17XAFF
         AGO   .A040                                           @Y17XAFF
.A030    AIF   ('&TERM' NE 'LNCP').A031                        @Y17XAFF
&TYPE    SETC  '0F'                     SET TRMLNCP            @Y17XAFF
         AGO   .A040                                           @Y17XAFF
.A031    AIF   ('&TERM' NE 'PUNT').A032                        @Y17XAFF
&TYPE    SETC  '18'                     SET TRMPUNT            @Y17XAFF
         AGO   .A040                                           @Y17XAFF
.A032    AIF   ('&TERM' NE 'LUNT').A033                        @Y17XAFF
&TYPE    SETC  '30'                     SET TRMLUNT            @Y17XAFF
         AGO   .A040                                           @Y17XAFF
.A033    AIF   ('&TERM' NE 'LINE').A034                        @Y17XAFF
         AIF   (NOT &IEDQCOD(6)).A0331  CANNOT INTERMIX LINE   @Y17XAFF
.*                                        DEFINITIONS          @Y17XAFF
         MNOTE 8,'*** IED996 ALL LINES WITHIN GROUP SHOULD BE DEFINED  *
               WITH TERM=LINE OR UTERM=YES'                    @Y17XAFF
.A0331   ANOP                                                  @Y17XAFF
&TYPE    SETC  '20'                     SET TRMNSDLC           @Y17XAFF
         AGO   .A040                                           @Y17XAFF
.A034    AIF   ('&TERM' NE 'SSCP').A039                        @Y17XAFF
&TYPE    SETC  'FF'                     SET TRMSSCP            @Y17XAFF
.A039    AIF   ('&TERM' NE '327C').A040                        @Y17XAFF
&TYPE    SETC  '40'                     SET TRMCLUST           @Y17XAFF
.A040    ANOP                                                  @Y17XAFF
         DC    XL1'&TYPE' .             TRMTYPE                @Y17XAFF
.*                                                             @Y17XAFF
         DC    H'0' .                   TRMSUBST               @Y17XAFF
.A100    ANOP                                                  @Y17XAFF
&IEDQSV(1)  DS  0F
&IEDQSV(1) SETC '&NAME'
         MEND
