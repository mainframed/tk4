         MACRO
&LABEL  BSPBEG &BASE=3,               , default base register          *
               &BASE2=,               , second base register           *
               &BASE3=,               , third base register            *
               &BASE4=,               , fourth base register           *
               &HEADER=               , header in title
         COPY  BSPGLBLS               , get name of globals
         COPY  BSPSGLBL               , set global values
&BSPCSCT SETC  'BSPTEMP'              , default pgm name
         AIF   ('&LABEL' EQ '').NOLAB , BIF no label given
&BSPCSCT SETC  '&LABEL'               , else set CSECT name from label
.NOLAB   ANOP                         ,
&H       SETC  ' ===> '               , first part of header
         AIF   ('&HEADER' EQ '').NOHEAD
         AIF   ('&HEADER'(1,1) EQ '''').HEADAPO
&H       SETC  '&HEADER'
         AGO   .NOHEAD
.HEADAPO ANOP
&H       SETC  '&HEADER'(2,K'&HEADER-2)
.NOHEAD  ANOP
&SAVE    SETC  'BSPA'.'&SYSNDX'       , Save Area Label
&SAVEEND SETC  'BSPB'.'&SYSNDX'       , Save Area label, end
&BEG     SETC  'BSPC'.'&SYSNDX'       , Program begin label
&BYP     SETC  'BSPD'.'&SYSNDX'       , End of eyecatcher
&LEN     SETC  'BSPE'.'&SYSNDX'       , Length of eyecatcher
         @     &BSPCSCT               , PGM name in block letters
&BSPCSCT BOX   'Module name................... &BSPCSCT',              -
               'Written by.................... &BSPAUTH',              -
               'Assembly data................. &SYSDATE',              -
               'Assembly time................. &SYSTIME',              -
               TITLE=' HOUSEKEEPING ',                                 -
               HEAD=&H
         SPACE                        ,
         REGISTER                     ,
         EJECT                        ,
&BSPCSCT CSECT                        , Start of module
         USING &BSPCSCT,R15           , Temporary addressabilty
         STM   R14,R12,12(R13)        , Save all registers
         B     &BYP                   , Branch around eyecatcher
&BEG     DS    0H                     , Eyecatcher starts here
         DC    AL2(&BYP-&BEG)         , Length of eyecatcher
         DC    CL8'&BSPCSCT'          , CSECT name
         DC    C' VER. &BSPVER..&BSPMOD ' , Version and Mod Level
         DC    C'&SYSDATE._&SYSTIME'  , ASSEMBLY DATE AND TIME
         DC    C' &BSPAUTH'           , Author
&BYP     DS    0H                     , Eyecatcher ends here
         LR    &BASE,R15              , Load first base from entry
         AIF   ('&BASE2' EQ '').@USING1
         LR    &BASE2,&BASE             Set
         LA    &BASE2,4095(&BASE2)         second
         LA    &BASE2,1(&BASE2)                  base register
         AIF   ('&BASE3' EQ '').@USING2
         LR    &BASE3,&BASE2            SET
         LA    &BASE3,4095(&BASE3)         UP
         LA    &BASE3,1(&BASE3)              THIRD BASE REG
         AIF   ('&BASE4' EQ '').@USING3
         LR    &BASE4,&BASE3            THIS IS
         LA    &BASE4,4095(&BASE4)             THE FOURTH
         LA    &BASE4,1(&BASE4)                           BASE REG
         USING &BSPCSCT,&BASE,&BASE2,&BASE3,&BASE4 ADDRESABILITY
         AGO   .@BASE00
.@USING3 ANOP
         USING &BSPCSCT,&BASE,&BASE2,&BASE3 MAKE IT ADDRESSABLE
         AGO   .@BASE00
.@USING2 ANOP
         USING &BSPCSCT,&BASE,&BASE2    MAKE ADDRESSABILITY PERMANENT
         AGO   .@BASE00
.@USING1 ANOP
         USING &BSPCSCT,&BASE           ESTABLISH ADDRESSABILITY
.@BASE00 ANOP                         ,
         DROP  R15                    , Not needed any more
         LR    R15,R13                , Old save area pointer
         CNOP  0,4                    , Align to fullword boundary
         ST    R15,&SAVE+4            , Store old savearea address
         BAL   R13,&SAVEEND           , Load new save area address
&SAVE    DC    9CL8'&BSPCSCT'         , Save area
&SAVEEND ST    R13,8(0,R15)           , Store into old save area
         MEXIT
         MEND
