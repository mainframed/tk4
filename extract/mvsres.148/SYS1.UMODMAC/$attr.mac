         MACRO
         $ATTR  &ATTR
.**********************************************************************
.*
.*  Name:  $ATTR
.*
.*  Type:  Assembler Macro
.*
.*  Desc:  Generates 3270 attribute bytes
.*
.*  Syntax   : $ATTR (list of attributes) Can be:
.*                    (PROT,NUM,MDT,HI,NONDISP,SKIP)
.*                    (UNPROT,ALPHA,NOMDT)
.*
.**********************************************************************
         COPY  $GLOBALS
         LCLA  &I1,&BIT(8)
&I1      SETA  1
.LOOP    ANOP
.*   PROCESS PROT ATTRIBUTE
         AIF ('&ATTR(&I1)' NE 'PROT').UNPROT
&BIT(3)  SETA  1
         AGO    .NXTONE
.*   PROCESS UNPROT ATTRIBUTE
.UNPROT  AIF ('&ATTR(&I1)' NE 'UNPROT').NUM
&BIT(3)  SETA  0
         AGO    .NXTONE
.*   PROCESS NUM ATTRIBUTE
.NUM     AIF ('&ATTR(&I1)' NE 'NUM').ALPHA
&BIT(4)  SETA  1
         AGO    .NXTONE
.*   PROCESS ALPHA ATTRIBUTE
.ALPHA   AIF ('&ATTR(&I1)' NE 'ALPHA').SKIP
&BIT(4)  SETA  0
         AGO    .NXTONE
.*   PROCESS SKIP ATTRIBUTE
.SKIP    AIF ('&ATTR(&I1)' NE 'SKIP').NONDISP
&BIT(3)  SETA  1
&BIT(4)  SETA  1
         AGO    .NXTONE
.*   PROCESS NONDISP ATTRIBUTE
.NONDISP AIF ('&ATTR(&I1)' NE 'NONDISP').HI
&BIT(5)  SETA  1
&BIT(6)  SETA  1
         AGO    .NXTONE
.*   PROCESS HI ATTRIBUTE
.HI      AIF ('&ATTR(&I1)' NE 'HI').MDT
&BIT(5)  SETA  1
         AGO    .NXTONE
.*   PROCESS MDT ATTRIBUTE
.MDT     AIF ('&ATTR(&I1)' NE 'MDT').NOMDT
&BIT(8)  SETA  1
         AGO    .NXTONE
.*   PROCESS NOMDT ATTRIBUTE
.NOMDT   AIF ('&ATTR(&I1)' NE 'NOMDT').ERROR
&BIT(8)  SETA  0
         AGO    .NXTONE
.ERROR   MNOTE 8,'&ATTR(&I1) IS AN INVALID ATTRIBUTE OPTION'
.NXTONE  ANOP
&I1      SETA  &I1+1
         AIF (&I1 LE N'&ATTR).LOOP
.*   CALCULATE VALUE OF OPTION BYTE
&I1      SETA &BIT(8)+&BIT(6)*4+&BIT(5)*8+&BIT(4)*16+&BIT(3)*32+1
         AIF (&I1 LE 64).GETCHAR
         MNOTE 8,'&I1 IS GREATE THAN 64'
&RC      SETA  4
         MEXIT
.*   LOOK UP CHAR FOR CALCULATED OPTION BYTE.
.GETCHAR ANOP
&ATTRIB  SETC  '&TAB3270(&I1)'
&RC      SETA  0
         MEND
