//IFLOADRN JOB CLASS=A,MSGCLASS=X,MSGLEVEL=(1,1)
//ASM EXEC ASMFC,PARM.ASM='DECK',REGION.ASM=512K
//ASM.SYSUT1 DD UNIT=SYSDA
//ASM.SYSUT2 DD UNIT=SYSDA
//ASM.SYSUT3 DD UNIT=SYSDA
//ASM.SYSPUNCH  DD  DSN=&&TEMP,DISP=(MOD,PASS),SPACE=(CYL,(1,1)),
//             UNIT=SYSDA,DCB=(DSORG=PS,RECFM=FB,LRECL=80,BLKSIZE=800)
//ASM.SYSIN DD *
IFLOADRN START
* (C) COPYRIGHT 2007 MHP (IKJ1234I AT YAHOO DOT COM)
         PRINT GEN
**********************************************************************
* SETUP
**********************************************************************
         STM   14,12,12(13)
         LR    12,15
         USING IFLOADRN,12
         ST    13,SAVE13
         LA    13,SAVEA
         ST    1,SAVER1
*        WTO   'IFL001I  IFLOADRN STARTED',ROUTCDE=2,DESC=5
         SLR   15,15
         ST    15,SAVE15
**********************************************************************
* TEST IF LOCAL OR REMOTE NCP
**********************************************************************
         L     1,SAVER1
         L     15,12(,1)      PTR TO VTAMPRMS
         L     1,8(,15)       DCB PTR IF LOCAL NCP OR 0 IF REMOTE NCP
         LTR   1,1
         BNZ   IFL990         EXIT IF LOCAL NCP
**********************************************************************
* SETUP PLIST AND INVOKE ISTINC32
**********************************************************************
         MVC   ISTINC32(4),12(15)
         MVC   C32PLIST+0(4),0(15)    COPY NCSPL ADDR TO PLIST
         LA    1,DATABUF
         ST    1,C32PLIST+4
         MVC   C32PLIST+8(2),=X'8000'  SET MORE FLAG=1
         MVC   C32PLIST+10(2),=H'256'  SET DATA BUFFER LENGTH
         LA    1,EPADDR
         ST    1,C32PLIST+12    SET "ENTRY POINT ADDRESS"
         OI    C32PLIST+12,X'80'  SET END OF PLIST FLAG
         L     15,ISTINC32      ADDRESS OF ROUTINE
         LA    15,0(,15)
         LA    1,C32PLIST       ADDRESS OF PLIST
         BALR  14,15            CALL ISTINC32
         ST    15,SAVE15
         LTR   15,15
         BNZ   IFL900
**********************************************************************
* INVOKE ISTINC32 FOR FINAL RU
**********************************************************************
         MVC   C32PLIST+8(2),=X'0000'  SET MORE FLAG=0
         L     15,ISTINC32      ADDRESS OF ROUTINE
         LA    15,0(,15)
         LA    1,C32PLIST       ADDRESS OF PLIST
         BALR  14,15            CALL ISTINC32
         ST    15,SAVE15
         LTR   15,15
         BZ    IFL990
IFL900   DS    0H
         WTO   'IFL002I  IFLOADRN FAILURE IN ISTAPC32',ROUTCDE=2,DESC=5
IFL990   DS    0H
         WTO   'IFL003I  IFLOADRN COMPLETED',ROUTCDE=2,DESC=5
         L     15,SAVE15
         L     13,SAVE13
         L     14,12(,13)
         LM    0,12,20(13)
         DROP  12
*         SLR   15,15
         BR    14
         DS    0F
C32PLIST DS    4F
SAVE13   DS    1F
SAVE15   DS    1F
SAVER1   DS    1F
ISTINC32 DS    1F
SAVEA    DS    18F
EPADDR   DC    X'12345678'      FAKE EP ADDR
DATABUF  DS    0X
          DC    X'000102030405060708090A0B0C0D0E0F'
          DC    X'101112131415161718191A1B1C1D1E1F'
          DC    X'202122232425262728292A2B2C2D2E2F'
          DC    X'303132333435363738393A3B3C3D3E3F'
          DC    X'404142434445464748494A4B4C4D4E4F'
          DC    X'505152535455565758595A5B5C5D5E5F'
          DC    X'606162636465666768696A6B6C6D6E6F'
          DC    X'707172737475767778797A7B7C7D7E7F'
          DC    X'808182838485868788898A8B8C8D8E8F'
          DC    X'909192939495969798999A9B9C9D9E9F'
          DC    X'A0A1A2A3A4A5A6A7A8A9AAABACADAEAF'
          DC    X'B0B1B2B3B4B5B6B7B8B9BABBBCBDBEBF'
          DC    X'C0C1C2C3C4C5C6C7C8C9CACBCCCDCECF'
          DC    X'D0D1D2D3D4D5D6D7D8D9DADBDCDDDEDF'
          DC    X'E0E1E2E3E4E5E6E7E8E9EAEBECEDEEEF'
          DC    X'F0F1F2F3F4F5F6F7F8F9FAFBFCFDFEFF'
         END   IFLOADRN
/*
//LKED   EXEC  PGM=IEWL,PARM='XREF,LIST,LET,CALL,AC=0',REGION=512K
//*            COND=(4,LT,ASM2)
//SYSLIN DD    DSNAME=&&TEMP,DISP=(OLD,DELETE)
//SYSLMOD DD DISP=SHR,DSN=SYS1.LINKLIB(IFLOADRN)
//SYSUT1 DD    DSNAME=&SYSUT1,UNIT=(SYSDA),
//             SPACE=(1024,(50,20))
//SYSPRINT DD  SYSOUT=A
//SYSLIB DD DUMMY
//
