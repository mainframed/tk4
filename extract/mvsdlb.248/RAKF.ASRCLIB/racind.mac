         TITLE 'Set or Clear RACF Indicator of VSAM Catalog Entries'
RACIND   CSECT
         PRINT NOGEN
*
**********************************************************************
*                                                                    *
* NAME: RACIND                                                       *
*                                                                    *
* TYPE: Assembler Source                                             *
*                                                                    *
* DESC: Set or Clear RACF Indicator of VSAM Catalog Entries          *
*                                                                    *
* FUNTION: Act upon control statements read from SYSIN to set or     *
*          clear the RACF indicator of VSAM catalog entries. The     *
*          following control statements are valid:                   *
*                                                                    *
*          ----+----1----+----2----+----3----+----4----+----5----+   *
*          CATALOG   name of catalog to search for entries           *
*          RACON     name of entry to indicate                       *
*          RACOFF    name of entry to unindicate                     *
*          * Comment                                                 *
*                                                                    *
*          Any number of control statements is allowed. The first    *
*          none comment statement must be a CATALOG statement. A     *
*          CATALOG statement remains active until a new CATALOG      *
*          statement replaces it. The SYSIN dataset must have fixed  *
*          length records with LRECL=80, padded to the right with    *
*          blanks.                                                   *
*                                                                    *
* REQUIREMENTS: //SYSIN DD    defining the input control statements  *
*               //SYSPRINT DD defining the print output listing      *
*               The utility must be run from an authorized library   *
*                                                                    *
**********************************************************************
*
* initialize
*
         SAVE  (14,12),,RACIND_&SYSDATE._&SYSTIME
         USING RACIND,R15          establish => program EP
         ST    R13,SAVEAREA+4      save HSA
         LA    R11,SAVEAREA        establish => savearea
         ST    R11,8(R13)          save LSA
         LR    R13,R11             setup => our savearea
         USING SAVEAREA,R13        new addressability
         DROP  R15                 program EP no longer needed
         B     CONTINUE            branch around savearea
SAVEAREA DS    18F                 savearea
CONTINUE OPEN  (SYSIN,(INPUT))     open control statement dataset
         OPEN  (SYSPRINT,(OUTPUT)) open output dataset
         RACHECK ENTITY=RAKFADM,CLASS='FACILITY',ATTR=READ authorize
         LTR    R15,R15            RAKFADM granted?
         BNZ    NOAUTH              no, talk dirrty and exit
         MODESET MODE=SUP,KEY=ZERO authorize ourselves
         LA    R12,PL              address CTGPL
         USING CTGPL,R12           tell assembler
         LA    R11,FL              address CTGFL
         USING CTGFL,R11           tell assembler
*
* setup catalog parameter and catalog field parameter lists
*
         MVI   CTGNOFLD,X'01'      number of fields in CTGPL
         LA    R1,ENTNAME          entry name address
         ST    R1,CTGENT           store entry name address in CTGPL
         LA    R1,ENTCAT           catalog name address
         ST    R1,CTGCAT           store catalog name address in CTGPL
         OI    CTGOPTN1,CTGNAME+CTGCNAME indicate names provided
         ST    R11,CTGFIELD        store field list address in CTGPL
         LA    R1,WA               CTGPL work area address
         ST    R1,CTGWKA           store work area address in CTGPL
         MVI   CTGFLDNO,X'01'      number of fields in CTGFL
         LA    R1,SECFLAGS         field name address
         ST    R1,CTGFLDNM         store field name address in CTGFL
         LA    R1,FLDWA            CTGFL work area address
         ST    R1,CTGFLDWA         store work area address in CTGFL
*
* read and interpret control statement
*
NEXTENT  GET   SYSIN,INPUT         read control statement
         CLI   INPUT,C'*'          comment line?
         BE    NEXTENT              yes, read next control statement
         CLC   INPUT(10),=C'CATALOG   ' CATALOG statement?
         BE    SETCAT               yes, replace current catalog name
         CLC   INPUT(10),=C'RACON     ' RACON statement?
         BE    SETENT               yes, replace current entry name
         CLC   INPUT(10),=C'RACOFF    ' RACOFF statement?
         BE    SETENT               yes, replace current entry name
         B     INVSTMT              alas master, what do you want?
*
* clear work areas
*
ENTOK    LA    R4,WA               CTGPL work area address
         LA    R2,3(,R4)           MVCL target is second byte of WA
         LA    R4,2(,R4)           MVCL source is first byte of WA
         MVI   0(R4),X'00'         clear source
         LA    R3,WAL-3            length of MVCL target
         LA    R5,1                length of MVCL source with X'00' pad
         MVCL  R2,R4               clear CTGPL work area
         LA    R4,FLDWA            CTGFL work area address
         LA    R2,3(,R4)           MVCL target is second byte of WA
         LA    R4,2(,R4)           MVCL source is first byte of WA
         MVI   0(R4),X'00'         clear source
         LA    R3,FLDWAL-3         length of MVCL target
         LA    R5,1                length of MVCL source with X'00' pad
         MVCL  R2,R4               clear CTGFL work area
*
* locate entry
*
         NI    CTGOPTN3,X'00'      clear function indicator
         OI    CTGOPTN3,CTGLOC+CTGAM0 indicate locate via CTGPL
         LR    R1,R12              CTGPL address
         SVC   26                  locate entry
         LTR   R15,R15             found?
         BNZ   CATERR              no, issue catalog error message
*
* set or clear RACF indicator
*
         L     R5,CTGFLPT          get SECFLAGS address
         CLC   INPUT(10),=C'RACOFF    ' request type?
         BE    RACOFF              process RACOFF
         OI    0(R5),B'10000000'   RACON:  set RACF indicator
         B     UPDATE              go update
RACOFF   NI    0(R5),B'01111111'   RACOFF: clear RACF indicator
*
* update entry
*
UPDATE   NI    CTGOPTN3,X'00'      clear function indicator
         OI    CTGOPTN3,CTGUPDAT+CTGAM0 indicate update via CTGPL
         LR    R1,R12              CTGPL address
         SVC   26                  update entry
         LTR   R15,R15             success?
         BNZ   CATERR              no, issue catalog error message
         MVC   OUTPUT(132),BLANK   clear output record
         MVC   OUTPUT(37),=C'IND003I RACF indicator turned xxx for'
         MVC   OUTPUT+30(3),INPUT+3             build ..
         MVC   OUTPUT+38(44),ENTNAME                .. message
         PUT   SYSPRINT,OUTPUT     print message
         B     NEXTENT             process next entry
*
* return
*
RETURN   MODESET MODE=PROB,KEY=NZERO return to problem state
EXIT     CLOSE (SYSIN)             close control statement dataset
         CLOSE (SYSPRINT)          close output dataset
         L     R15,MAXRC           get this run's maximum return code
         L     R13,SAVEAREA+4      get caller's savearea
         L     R14,12(,R13)        restore ..
         LM    R0,R12,20(R13)                .. regs
         BR    R14                 return
*
* process control statements
*
SETCAT   CLI   INPUT+10,C' '       process CATALOG statement
         BE    INVCAT              first char blank, ignore statement
         MVC   ENTCAT(44),INPUT+10 set new catalog
         MVC   OUTPUT(132),BLANK   clear output record
         MVC   OUTPUT(21),=C'IND001I using catalog' build ..
         MVC   OUTPUT+22(44),ENTCAT                     .. message
         PUT   SYSPRINT,OUTPUT     print message
         B     NEXTENT             process next entry
SETENT   CLI   INPUT+10,C' '       process RACxxx statement
         BE    INVENT              first char blank, ignore statement
         CLI   ENTCAT,C' '         catalog defined?
         BE    NOCAT                no, ignore statement
         MVC   ENTNAME(44),INPUT+10 set entry name
         MVC   OUTPUT(132),BLANK   clear output record
         MVC   OUTPUT(18),=C'IND002I processing'    build ..
         MVC   OUTPUT+19(44),ENTNAME                    .. message
         PUT   SYSPRINT,OUTPUT     print message
         B     ENTOK               process entry
*
* end of SYSIN dataset reached
*
DONE     MVC   OUTPUT(132),BLANK   clear output record
         MVC   OUTPUT(31),=C'IND004I processing ended at EOD' message
         PUT   SYSPRINT,OUTPUT     print message
         B     RETURN              exit
*
* error messages
*
INVCAT   MVC   OUTPUT(132),BLANK   clear output record
         MVC   OUTPUT(33),=C'IND005E CATALOG statement invalid' message
         PUT   SYSPRINT,OUTPUT     print message
         B     NEXTENT             process next entry
INVENT   MVC   OUTPUT(132),BLANK   clear output record
         MVC   OUTPUT(32),=C'IND006E RACxxx statement invalid' build ..
         MVC   OUTPUT+11(3),INPUT+3                          .. message
         PUT   SYSPRINT,OUTPUT     print message
         B     NEXTENT             process next entry
NOCAT    MVC   OUTPUT(132),BLANK   clear output record
         MVC   OUTPUT(45),=C'IND007E no catalog defined, statement ignoX
               red'                build message
         PUT   SYSPRINT,OUTPUT     print message
         B     NEXTENT             process next entry
INVSTMT  MVC   OUTPUT(132),BLANK   clear output record
         MVC   OUTPUT(33),=C'IND008E invalid statement ignored' message
         PUT   SYSPRINT,OUTPUT     print message
         B     NEXTENT             process next entry
CATERR   C     R15,MAXRC           RC higher than MAXRC?
         BNH   CATLOW               no, don't update MAXRC
         ST    R15,MAXRC            yes, update MAXRC
CATLOW   MVC   OUTPUT(132),BLANK   clear output record
         MVC   OUTPUT(72),=C'IND009E ** VSAM catalog return code is rrrX
                - reason code is IGG0CLxx-nnn' build message
         MVC   OUTPUT+66(2),CTGFDBK move module id into message
         CVD   R15,ERRDEC          convert return code to decimal
         L     R5,ERRDEC+4         get low order decimal word
         SLL   R5,4                separate last digit from sign
         ST    R5,ERRDEC           store in high order decimal word
         UNPK  ERREBC(7),ERRDEC(4) convert to EBCDIC
         MVC   OUTPUT+39(3),ERREBC+3 move return code into message
         LH    R5,CTGREAS1         get reason code
         CVD   R5,ERRDEC           convert reason code to decimal
         L     R5,ERRDEC+4         get low order decimal word
         SLL   R5,4                separate last digit from sign
         ST    R5,ERRDEC           store in high order decimal word
         UNPK  ERREBC(7),ERRDEC(4) convert to EBCDIC
         MVC   OUTPUT+69(3),ERREBC+3 move reason code into message
         PUT   SYSPRINT,OUTPUT     print message
         B     NEXTENT             process next entry
NOAUTH   MVC   OUTPUT(132),BLANK   clear output record
         MVC   OUTPUT(21),=C'IND010T access denied' build message
         PUT   SYSPRINT,OUTPUT     print message
         LA    R15,12              set ..
         ST    R15,MAXRC                 .. return code ..
         B     EXIT                                       .. and exit
*
* data area
*
ENTNAME  DS    CL44                name of entry to modify
ENTCAT   DC    CL44' '             name of catalog to search for entry
SECFLAGS DC    CL8'SECFLAGS'       field name for CTGFL
MAXRC    DC    F'0'                maximum catalog return code
PL       DC    8F'0'               catalog parameter list
FL       DC    6F'0'               catalog field list
WA       DC    AL2(WAL)            CTGPL ..
         DS    1022X                       .. work area
WAL      EQU   *-WA                length of CTGPL work area
FLDWA    DC    AL2(FLDWAL)         CTGFL ..
         DS    1022X                       .. work area
FLDWAL   EQU   *-FLDWA             length of CTGFL work area
RAKFADM  DC    CL39'RAKFADM'       facility name to authorize
INPUT    DS    CL80                control statement
BLANK    DC    C' '                blank to clear output record
OUTPUT   DS    CL132               output record
ERRDEC   DS    D                   return/reason codes decimal
ERREBC   DS    CL8                 return/reason codes EBCDIC
SYSIN    DCB   DDNAME=SYSIN,MACRF=GM,DSORG=PS,EODAD=DONE,              X
               LRECL=80,RECFM=FB,BLKSIZE=800      control statements
SYSPRINT DCB   DDNAME=SYSPRINT,MACRF=PM,DSORG=PS,                      X
               LRECL=132,RECFM=FB,BLKSIZE=1320    messages
         LTORG ,                   all literals go here
*
* equates
*
         YREGS                     register equates
*
* parameter list mappings
*
         IEZCTGPL                  catalog parameter list
         IEZCTGFL                  catalog field parameter list
         END   RACIND              end of program
