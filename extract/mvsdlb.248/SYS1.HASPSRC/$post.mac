         MACRO -- $POST -- POST AN EVENT COMPLETE
&NAME    $POST &EWF,&EVENT
         LCLA  &I,&J,&K
         LCLC  &B(10),&N,&MASK,&OFF
         $CHEK &EWF,&EVENT
&N       SETC  '&NAME'                                         @OZ27300
         AIF   ('&EWF' EQ '$HASPECF').R
&B(1)    SETC  'POST'
&B(2)    SETC  'OPER'
&B(3)    SETC  'IO'
&B(4)    SETC  'WORK'
&B(5)    SETC  'HOLD'
&MASK    SETC  ''
         AIF   ('&EVENT'(1,1) NE '(').ID
&J       SETA  0
.IA      ANOP
&J       SETA  &J+1
         AIF   (&J GT 5).IF
&I       SETA  1
.IB      AIF   ('&SYSLIST(2,&I)' EQ '&B(&J)').IC
&I       SETA  &I+1
         AIF   (&I LE N'&SYSLIST(2)).IB
         AGO   .IA
.IC      ANOP
&MASK    SETC  '&MASK'.'-$EWF'.'&B(&J)'
         AGO   .IA
.ID      ANOP
&J       SETA  0
.IE      ANOP
&J       SETA  &J+1
         AIF   (&J GT 5).IF
         AIF   ('&EVENT' NE '&B(&J)').IE
&MASK    SETC  '-$EWF'.'&EVENT'
.IF      AIF   ('&EWF'(1,1) EQ '(').IG
&NAME    NI    PCEEWF-PCEDSECT+&EWF,255&MASK RESET INHIBITS
         AGO   .IH
.IG      ANOP
&NAME    NI    PCEEWF-PCEDSECT&EWF,255&MASK RESET INHIBITS
.IH      BNZ   *+20                SKIP QUEUEING IF INHIBITED
         STM   LINK,R3,$POSTSAV    SAVE REGISTERS
         AIF   ('&EWF'(1,1) EQ '(').II
         LA    R1,&EWF             POINT TO PCE
         AGO   .IJ
.II      LA    R1,0&EWF            POINT TO PCE
.IJ      BAL   LINK,$POST          QUEUE THE PCE ON READY QUEUE
         LM    LINK,R3,$POSTSAV    RESTORE REGISTERS
         MEXIT
.R       AIF   ('&EVENT'(1,1) NE '(').RG
&B(1)    SETC  'CKPW'
&B(2)    SETC  'SMF'
&B(3)    SETC  'CMB'
&B(4)    SETC  'CKPT'
&B(5)    SETC  'UNIT'
&B(6)    SETC  'JOB'
&B(7)    SETC  'TRAK'
&B(8)    SETC  'JOT'
&B(9)    SETC  'BUF'
&B(10)   SETC  'IMAG'
&K       SETA  8
&MASK    SETC  ''
*                                  THIS CARD DELETED BY APAR   @OZ27300
&J       SETA  0
.RA      ANOP
&J       SETA  &J+1
         AIF   (&J GT &K).RE
&I       SETA  1
.RB      AIF   ('&SYSLIST(2,&I)' EQ '&B(&J)').RC
&I       SETA  &I+1
         AIF   (&I LE N'&SYSLIST(2)).RB
         AGO   .RA
.RC      AIF   ('&MASK' NE '').RD
&OFF     SETC  '&B(&J)'
.RD      ANOP
         AIF   ('&B(&J)' NE 'CKPW').RD1                        @OZ27300
&N       OI    $STATUS,$CKPTW      SET CKPT-WRITE REQUEST      @OZ27300
&N       SETC  ''                                              @OZ27300
.RD1     ANOP                                                  @OZ27300
&MASK    SETC  '&MASK'.'-$EWF'.'&B(&J)'
         AGO   .RA
.RE      AIF   ('&MASK' EQ '').RF
&N       NI    $HASPECF+$EWB&OFF,255&MASK RESET EVENTS
&N       SETC  ''
&MASK    SETC  ''
.RF      ANOP
&J       SETA  &K
&K       SETA  &K+2
         AIF   (&K LE 10).RA
         MEXIT
.RG      ANOP
         AIF   ('&EVENT' NE 'CKPW').RH                         @OZ27300
&N       OI    $STATUS,$CKPTW      SET CKPT-WRITE REQUEST      @OZ27300
&N       SETC  ''                                              @OZ27300
.RH      ANOP                                                  @OZ27300
&N       NI    $HASPECF+$EWB&EVENT,FF-$EWF&EVENT RESET EVENT   @OZ27300
         MEND
