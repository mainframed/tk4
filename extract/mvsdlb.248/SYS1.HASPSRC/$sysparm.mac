         MACRO -- $SYSPARM -- RETRIEVE SYSTEM GLOBAL &SYSPARM
         $SYSPARM
         GBLC  &PRINT,&GEN,&DATA,&DOC,&LIST
         LCLC  &ASMOPT(5)
         LCLA  &CNT,&CTR,&STRNG,&LNTH,&SUB
&ASMOPT(1)     SETC    '&SYSLIST(1,1)'
&ASMOPT(2)     SETC    '&SYSLIST(1,2)'
&ASMOPT(3)     SETC    '&SYSLIST(1,3)'
&ASMOPT(4)     SETC    '&SYSLIST(1,4)'
&ASMOPT(5)     SETC    '&SYSLIST(1,5)'
         AIF   (K'&SYSPARM EQ 0).XIT
         AIF   ('&SYSPARM'(1,1) EQ '(').MORE
         MNOTE 2,'SYSPARM SYNTAX ERROR--MUST BE ENCLOSED IN PARANS'
         MNOTE *,'SYSPARM IS ''&SYSPARM'' '
         MEXIT
.MORE    ANOP
&CNT     SETA  K'&SYSPARM
&CTR     SETA  1
&SUB     SETA  1
&STRNG   SETA  &CTR+1
.LOOP    AIF   ('&SYSPARM'(&CTR,1) EQ ',' OR &CTR EQ &CNT).FOUND
&CTR     SETA  &CTR+1
         AGO   .LOOP
.FOUND   ANOP
&LNTH    SETA  &CTR-&STRNG
         AIF   (&LNTH EQ 0).NULL
&ASMOPT(&SUB) SETC '&SYSPARM'(&STRNG,&LNTH)
.NULL    ANOP
&SUB     SETA  &SUB+1
         AIF   (&SUB GT 5 OR &CTR EQ &CNT).XIT
&CTR     SETA  &CTR+1
&STRNG   SETA  &CTR
         AGO   .LOOP
.XIT     ANOP
&PRINT   SETC  '&ASMOPT(1)'
&GEN     SETC  '&ASMOPT(2)'
&DATA    SETC  '&ASMOPT(3)'
&DOC     SETC  '&ASMOPT(4)'
&LIST    SETC  '&ASMOPT(5)'
         AIF   ('&PRINT' EQ 'ON').SPACE
&LIST    SETC  'NO'
.SPACE   SPACE 3
         MNOTE *,'                            &&PRINT SET TO &PRINT'
         MNOTE *,'                            &&GEN   SET TO &GEN'
         MNOTE *,'                            &&DATA  SET TO &DATA'
         MNOTE *,'                            &&DOC   SET TO &DOC'
         MNOTE *,'                            &&LIST  SET TO &LIST'
         MEND
