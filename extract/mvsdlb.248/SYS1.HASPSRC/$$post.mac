         MACRO -- $$POST -- POST HASP PCE AND HASP TASK
&N       $$POST &TYPE=,&ELMT=,&R11=SSVT
         LCLC  &S
&S       SETC  '&N'
         AIF   ('&R11' EQ 'SSVT').A
&S       L     R11,$SSVT           POINT TO SSVT
&S       SETC  ''
.A       AIF   ('&ELMT' EQ '').D
         AIF   ('&TYPE' EQ '').B
         MNOTE 8,'ELMT AND TYPE OPERANDS ARE MUTUALLY EXCLUSIVE'
         MEXIT
.B       AIF   ('&ELMT'(1,1) EQ '(').C
&S       LA    R1,&ELMT-SSVT(,R11) POINT TO POST ELEMENT
         AGO   .H
.C       AIF   ('&ELMT' EQ '(R1)').I
&S       LR    R1,&ELMT(1)         POINT TO POST ELEMENT
         AGO   .H
.D       AIF   ('&TYPE' EQ '').G
         AIF   ('&TYPE' EQ '(').E
&S       LA    R0,$EWF&TYPE*((255-$EWB&TYPE)/255*255+1)
         AGO   .F
.E       AIF   ('&TYPE' EQ '(R0)').G
&S       LR    R0,&TYPE(1)         SET POST MASK
.F       ANOP
&S       SETC  ''
.G       ANOP
&S       SLR   R1,R1               SIGNAL NO ELEMENT
.H       ANOP
&S       SETC  ''
.I       ANOP
&S       L     R15,$SVPOST-SSVT(,R11) POINT TO $$POST ROUTINE
         BALR  R2,R15              ENTER IT
         AIF   ('&R11' EQ 'SSVT').XIT
         L     R11,$SVHCT-SSVT(,R11) POINT TO HCT
.XIT     MEND
