         MACRO -- $SJB -- HASP SUBSYSTEM JOB BLOCK DSECT
         $SJB  &DOC=NO
         AIF   ('&DOC' NE 'YES').A
.A       TITLE 'HASP SUBSYSTEM JOB BLOCK (SJB) DSECT'
SJBDSECT DSECT
SJBSAVE  DS    18F
*
*        TSO CANCEL/STATUS SERVICE FIELDS
*
         ORG   SJBSAVE+8
SJBTCMND DS    0D                  FIRST OF CANCEL COMMAND
SJBTAREA DS    F                   WORK AREA GOTTEN
SJBTCCE  DS    F                   ADDRESS OF CCE
SJBTJOBN DS    CL8                 JOB NAME
SJBTJOBI DS    F                   JOB ID OR ZERO ( BINARY FORM )
SJBTDIMP DS    H                   SIZE OF EXTENTION
SJBTDIMR DS    H                   SIZE USED OR REQUIRED
SJBTFUNC DS    H                   REQUESTED FUNCTION
SJBTRETB DS    H                   SSOBRETN CODE
SJBTRETR DS    H                   R15 RETURN CODE
SJBTF    DS    C                   FLAGS
SJBTFCAN EQU   X'80'               SVC 34 REQUIRED TO CANCEL
SJBTFLGS DS    C                   FLAGS FROM SSOB
SJBTULEN DS    C                   USER ID LENGTH
         ORG
SJBID    DS    CL4'SJB'            SJB IDENTIFIER
SJBLENG  DS    AL2(*-*)            CONTROL BLOCK LENGTH              R4
SJBFLG1  DS    X                   FIRST FLAG BYTE ---
SJB1XBMC EQU   X'80'               XBM CONTINUATION
SJB1SJID EQU   X'40'               SELECT JOB BY ID - SJBJOBID
SJB1NORE EQU   X'20'               DISALLOW JOB RE-ENQUEUE
SJB1XBM  EQU   X'10'               XEQ BATCH MONITOR OPERATING
SJB1XBWT EQU   X'08'               BATCH MONITOR AWAITS JOB
SJB1EJOB EQU   X'04'               HASP-RESTART JOB ($E JOB)
SJB1XBMR EQU   X'02'               XBM TERMINATING
SJB1XBMJ EQU   X'01'               NORMAL JOB CAUSED XBM TERMINATION
SJBFLG2  DS    X                   SECOND FLAG BYTE ---
SJB2PNIT EQU   X'80'               STOP THE INITIATOR
SJB2JNFD EQU   X'40'               JOB-BY NUMBER NOT FOUND
SJB2EOM  EQU   X'20'               END-OF-MEMORY DETECTED
SJB2CNCL EQU   X'10'               CANCEL AFTER SWA CREATE
SJB2CONV EQU   X'08'               SJB CREATED FOR CONVERTER
SJB2HOLD EQU   X'04'               HOLD JOB AFTER RE-QUEUE           R4
SJB2JNL  EQU   X'02'               JOB IS JOURNALLED                 R4
SJB2INIT EQU   X'01'               INITIATOR FLAG                   R41
SJBSSIB  DS    A                   POINTER TO SSIB
SJBSJB   DS    A                   SJB CHAIN FROM $SVHAVT
SJBSDB   DS    A                   POINTER TO CHAIN OF SDBS
SJBJKEY  DS    A                   HDBDSKEY FOR THIS JOB
SJBJCT   DS    A                   ADDRESS OF JCT FOR JOB
SJBJCTRK DS    F                   JCT TRACK ADDRESS
SJBTCBP  DS    A                   ADDRESS OF INIT OR STC TCB  @OZ53355
SJBJQOFF DS    F                   OFFSET OF JQE WITHIN JOB QUEUE
SJBSSNM  DS    CL4                 SUBSYSTEM ID OF OWNER       @OZ15844
SJBIOT   DS    A                   ADR OF FIRST REGULAR IOT
SJBSPIOT DS    A                   ADR OF FIRST SPIN IOT
SJBOCT   DS    A                   ADR OF OUTPUT CONTROL TABLE
SJBSECB  DS    0A                  STOP-ECB ADR FOR CREATED-ID JOB
SJBPIT   DS    A                   ADDRESS OF PIT IN HASP
SJBSTQE  DS    3F                  EXECUTION TIMER QUEUE ELEMENT
SJBXSTIM DS    F                   TIME ESTIMATE EXCESSION AMOUNT
SJBESTLN DS    F                   PRINT EXCESSION LIMIT
SJBESTPU DS    F                   PUNCH EXCESSION LIMIT
SJBXBSDB DS    A                   ADDRESS OF XBM INPUT SDB
SJBXBJNM DS    CL8                 NAME OF BATCH DRIVER JOB
SJBCSCB  DS    A                   ADDRESS OF CSCB
SJBECB   DS    A                   ECB FOR SJBIOB & JOB SELECT
SJBECBP  DS    A                   POINTER TO SJB'S ECB
SJBASCBP DS    A                   POINTER TO RELATED ASCB
SJBERRET DS    A                   POINTER TO ERROR RETURN
SJBPATID DS    H                   EBCDIC PIT ID
SJBPRIO  DS    X                   HASP EXECUTION SELECTION PRTY
         DS    C                   RESERVED FOR FUTURE USE
SJBXQFN1 DS    H                   HASPXEQ FUNCTION INDICATOR
SJBFLG3  DS    B                   TERMINATION FLAG ONE ---
SJB3CLS  EQU   X'80'               CLOSE ALL SUBSYSTEM DATA SETS
SJB3FSDB EQU   X'40'               FREE ALL SDBS
SJB3TERM EQU   X'20'               TERMINATE THE JOB
SJB3PPOU EQU   X'10'               PURGE PARTIAL OUTPUT
SJB3RSV4 EQU   X'08'               RESERVED
SJB3CKPT EQU   X'04'               WRITE IOTS, JCT
SJB3FIOT EQU   X'02'               FREE ALL IOTS
SJB3FJCT EQU   X'01'               FREE JCT
SJBFLG4  DS    B                   TERMINATION FLAG TWO ---
SJB4MEND EQU   X'80'               MSG 'ENDED'
SJB4MTRM EQU   X'40'               MSG 'TERMINATED'
SJB4MREQ EQU   X'20'               MSG 'RE-ENQUEUED'
SJB4MREX EQU   X'10'               MSG 'QUEUED FOR RE-EXECUTION'
SJB4FSJB EQU   X'08'               FREE THE SJB
SJB4MRQH EQU   X'04'               MSG 'RE-ENQUEUED AND HELD'
SJB4RSV6 EQU   X'02'               RESERVED                    @OZ56375
SJB4TERM EQU   X'01'               BATCH JOB HAS TERMINATED    @OZ64778
SJBQUEUE DS    A                   ADDRESS OF $SVJ QUEUE HEADER
SJBXQCHN DS    A                   HASPXEQ CHAINING WORD
         DS    F                   RESERVED
SJBLOCKH DS    0D                  SJB LOCKING DOUBLEWORD
SJBTCB   DS    A                   LOCK-HOLDING TCB ADDRESS
SJBNEXTL DS    A                   0 - SJB IS UNLOCKED
*                                  - - SJB LOCKED, NO WAIT CHAIN
*                                  + - SJB LOCKED, ADR OF WAITER
SJBLOGQ  DS    A                   HASP JOB LOG MESSAGE QUEUE
SJBPSOP  DS    A                   PROCESS SYSOUT CONTROL BLOCK
SJBJCLAS DS    C                   HASP JOB CLASS
SJBLKFG  DS    X                   SERIALIZATION FLAG          @OZ40161
SJBFIRST EQU   X'80'               FIRST $SVJPCLS REQUEST      @OZ35293
SJBDRAIN EQU   X'40'               PIT HAS BEEN DRAINED        @OZ73682
SJBPRIM  EQU   X'20'               PRIMARY INITIATOR SJB       @OZ56375
SJBASID  DS    H                   USERS ASID
SJBJOBID DS    CL8'JOBNNNNN'       JOB IDENTIFIER - EBCDIC, NUMERIC
SJBJOBNM DS    CL8'JOBNAMEX'       JOBNAME FROM JOB CARD
SJBFLOW  DS    0F                  FLOW BYTES FOR ABNORMAL PROCESSING
SJBTFLOW DS    X'0'                SJB HAS BEEN ON QUEUE INDICATOR
SJBTFFG  DS    X'0'                TSO PROCESSING FLOW
SJBTFFGM EQU   X'80'               ECB STATUS MEANINGFULL
SJBTFFGG EQU   X'40'               PSO GOTTEN THIS ENTRY TO PSO
SJBTFFGP EQU   X'20'               PSO PURGE REQUESTED
SJBTFFGC EQU   X'10'               CANCEL/STATUS EOT FUNCTION GOING
         DS    X                   RESERVED
         DS    X                   RESERVED
SJBTCHN  DS    A                   TSU CHAIN FIELD
SJBFWORD DS    F                   WORK AREA FOR HCBIO RTNS    @OZ78293
*        SJBFWORD MUST NEVER BE SEPARATED FROM SJBDBLWK        @OZ78293
SJBDBLWK DS    D                   DOUBLEWORD WORKAREA
*
*              INPUT/OUTPUT BLOCK IN SJB
*
         DS    0D                  DOUBLE WORD ALIGNMENT
SJBIOB   DS    0XL(40+4*8)         IOB FOR JOB CONTROL BLOCKS
SJBIFLG1 DS    X                   IOB - FLAG BYTE
         DS    X
SJBISNS0 DS    X                   IOB - FIRST SENSE BYTE
SJBISNS1 DS    X                   IOB - SECOND SENSE BYTE
SJBIECB  DS    0A                  IOB - EVENT CONTROL BLOCK ADR
SJBICMP  DS    X                   IOB - COMPLETION CODE
         DS    AL3(SJBECB)         IOB - ECB POINTER
SJBISIO  DS    X                   IOB - SIO CONDITION CODE
SJBICSW  DS    0XL7                IOB - CHANNEL STATUS WORD
SJBICSWA DS    AL3                 IOB - CSW ADDRESS PORTION
SJBIST0  DS    X                   IOB - FIRST STATUS BYTE
SJBIST1  DS    X                   IOB - SECOND STATUS BYTE
SJBILEN  DS    H                   IOB - RESIDUAL LENGTH
SJBIST   DS    A(SJBCCW1)          IOB - CHANNEL PROGRAN ADDRESS
SJBIDCB  DS    A(SJBDCB)           IOB - DCB ADDRESS
SJBIRS   DS    A                   IOB - RESTART CHANNEL PROGRAM ADR
         DS    A
SJBIFDAD DS    D                   IOB - FULL DISK ADDRESS
SJBCCW1  CCW   X'03',*+5,X'60',1   SET SECTOR/NO-OP
SJBCCW2  CCW   X'31',SJBIFDAD+3,X'60',5  SEARCH ID EQUAL
SJBCCW3  CCW   X'08',*-8,0,1       TIC *-8
SJBCCW4  CCW   X'05',0,0,*-*       WRITE/READ DATA                   R4
*
*              ACB FOR HASP JOB LOG DATASET
*
SJBLACB  ACB   MACRF=(ADR,SEQ,OUT)
*
*              DEB FOR HASP JOB LOG DATASET
*
SJBLDEB  DC    7A(0),A(SJBLACB)
*
*              RPL FOR HASP JOB LOG DATASET
*
SJBLRPL  RPL   ACB=SJBLACB
*
*              ACB FOR INTERNAL TEXT DATASET
*
SJBIACB  ACB   MACRF=(ADR,SEQ,IN)
*
*              DEB FOR INTERNAL TEXT DATASET
*
SJBIDEB  DC    7A(0),A(SJBIACB)
*
*              ACB FOR SYSTEM MESSAGES DATASET
*
SJBMACB  ACB   MACRF=(ADR,SEQ,OUT)
*
*              DEB FOR SYSTEM MESSAGES DATASET
*
SJBMDEB  DC    7A(0),A(SJBMACB)
*
*              ACB FOR JOURNAL DATASET
*
SJBJACB  ACB   MACRF=(ADR,SEQ,DIR,IN,OUT)
*
*              DEB FOR JOURNAL DATASET
*
SJBJDEB  DC    7A(0),A(SJBJACB)
*
*              DCB FOR SPOOL DATASET
*
         ORG   *-40
SJBDCB   DS    0F                  SYS1.HASPACE DATA CONTROL BLOCK
         ORG   *+40
         DS    3A
*
*              DEB FOR SPOOL DATASET
*
SJBDEB   EQU   *                   SYS1.HASPACE DATA EXTENT BLOCK    R4
&SYSECT  CSECT
         MEND
