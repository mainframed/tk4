         MACRO -- $JCAN -- CANCEL JOB
&N       $JCAN &JQE=(R1),&ENTER=YES,&TYPE=,&NOTJOB=,&NOP=*+4,          C
               &OK=,&DUMP=NO
         LCLC  &C
&C       SETC  '&N'
         AIF   ('&TYPE' EQ '').F
         AIF   ('&TYPE' NE 'CANXEQ' OR '&DUMP' EQ 'NO').A
&C       LA    R0,64               SET CANCEL (DUMP) OPTION
         AGO   .E
.A       AIF   ('&TYPE' NE 'CANXEQ').B
&C       SLR   R0,R0               SET CANCEL EXECUTION OPTION
         AGO   .E
.B       AIF   ('&TYPE' NE 'CANALL' OR '&DUMP' EQ 'NO').C
&C       LA    R0,65               SET CANCEL ALL (DUMP) OPTION
         AGO   .E
.C       AIF   ('&TYPE' NE 'CANALL').D
&C       LA    R0,1                SET CANCEL ALL OPTION
         AGO   .E
.D       AIF   ('&TYPE' NE 'STOP').G
&C       LA    R0,2                SET STOP JOB OPTION
.E       ANOP
&C       SETC  ''
.F       AIF   ('&ENTER' NE 'YES').XIT
&C       $DECODE &JQE
         L     R15,$JCAN           POINT TO SERVICE ROUTINE
         BALR  R14,R15             ENTER IT
         B     &NOTJOB             CANCEL REJECTED FOR STC AND TSU JOB
         B     &NOP                CANCEL REJECTED FOR JOB IN OUTPUT
         AIF   ('&OK' EQ '').XIT
         B     &OK                 JOB CANCELLED OR PURGED
         MEXIT
.G       MNOTE 12,'TYPE MUST BE CANXEQ, CANALL, OR STOP'
.XIT     MEND
