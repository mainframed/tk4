         MACRO -- $MID -- ASSIGN HASP MESSAGE ID FOR WTO
         $MID  &NUM
         GBLC  &MID
&MID     SETC  '$HASP&NUM'.' '
.*
.*             ADD MESSAGE ID TO THE &LBL TABLE
.*
         GBLB  &LBLB               ON IF &LBL TABLE IS FULL
         GBLC  &LBL(256)           ROOM FOR 256 $MID MACROES
         LCLA  &N,&ALPH            INDICES
         LCLC  &LB                 TEXT TO ENTER INTO &LBL TBL
         AIF   (&LBLB).OUTZ        EXIT IF TABLE FULL
&LB      SETC  '&NUM'              ASSUME FIRST ID OCCURRENCE
.LBLAGN  ANOP                      RE-ENTER HERE IF NOT FIRST
&N       SETA  0                   SET TABLE SEARCH INDEX TO 0
.LBLGO   ANOP                      SEARCH LOOP STARTS HERE
&N       SETA  &N+1                UP THE SEARCH INDEX
         AIF   (&N GT 256).OUTN    EXIT IF TABLE FULL
         AIF   ('&LBL(&N)' EQ '').LBLSPOT  BRANCH IF EMPTY SPOT
         AIF   ('&LB' NE '&LBL(&N)').LBLGO  CONTINUE UNLESS EQUAL
&ALPH    SETA  &ALPH+1             GO TO NEXT LETTER OF THE ALPH
         AIF   (&ALPH GT 26).OUTA  IF OUT OF ALPHABET, MNOTE
&LB      SETC  '&NUM'.'ABCDEFGHIJKLMNOPQRSTUVWXYZ'(&ALPH,1)
         AGO   .LBLAGN             RESTART THE SEARCH
.LBLSPOT ANOP                      EMPTY SLOT FOUND
&LBL(&N) SETC  '&LB'               FILL IN THE SLOT
         AIF   (K'&LB GT 4).OUTK   IF TOO LONG WE CANT DO LABEL
$MID&LB  DC    0AL4($MID&LB)       MESSAGE IDENTIFIER
.OUTZ    ANOP                      END OF &LBL TABLE CODE
.*
.*             &LBL TABLE ADDITION COMPLETE
.*
         AGO   .XIT                RETURN FROM MACRO
.*
.*             &LBL TABLE ERROR MESSAGES
.*
.OUTN    MNOTE 0,'MORE THAN 256 OCCURRENCES OF $MID'
&LBLB    SETB  1                   ONE-TIME MESSAGE
         AGO   .OUTZ
.*
.OUTA    MNOTE 0,'MORE THAN 27 OCCURRENCES OF MESSAGE IDENTIFIER &NUM'
         AGO   .OUTZ
.*
.OUTK    MNOTE 0,'CANNOT CREATE SYMBOL FOR $MID&LB'
         AGO   .OUTZ
.*
.*             END OF &LBL TABLE MESSAGES
.*
.XIT     MEND
