         MACRO -- $PSO -- HASP PROCESS SYSOUT WORK AREA DSECT
         $PSO  &DOC=NO
         GBLB  &PDDBD
         AIF   (&PDDBD).PDDB
         $PDDB DOC=&DOC            GENERATE HASP PDDB DSECT
.PDDB    ANOP
         AIF   ('&DOC' NE 'YES').A
.A       TITLE 'HASP PROCESS SYSOUT WORK AREA (PSO) DSECT'
***********************************************************************
*                                                                     *
*        THE PROCESS SYSOUT WORK AREA (PSO) DSECT DESCRIBES A CONTROL *
*        BLOCK RESIDING IN THE COMMON STORAGE AREA (CSA) WHICH        *
*        CONTAINS INFORMATION REQUIRED FOR THE PROCESSING OF THE      *
*        FIRST AND SUBSEQUENT PROCESS SYSOUT REQUESTS FOR EACH        *
*        REQUEST SERIES.  THE AREA FROM PSOUFLG THROUGH PSOWTRC       *
*        PROVIDES A ONE-FOR-ONE MAPPING OF THOSE FIELDS IN THE SSOB   *
*        EXTENSION FOR PROCESS SYSOUT FOLLOWING THE EXTENSION LENGTH  *
*        FIELD, SSSOLEN.                                              *
*                                                                     *
*        THE PSO IS OBTAINED BY THE ROUTINE IN HASPSSSM, WHICH FIRST  *
*        HANDLES PROCESS SYSOUT REQUESTS, WHEN IT ENCOUNTERS THE      *
*        INITIAL REQUEST OF A SERIES.  THE PSO IS FREED BY THIS       *
*        ROUTINE WHENEVER THE JES2 PROCESS SYSOUT PROCESSOR POSTS IT  *
*        WITH THE PSOFDONE BIT SET IN PSOFLG2.  THIS ROUTINE WILL     *
*        ALSO FREE THE PSO IF THE RETURN CODE STORED BY THE JES2      *
*        PROCESS SYSOUT PROCESSOR IN PSORETN IS NON-ZERO, UNLESS THE  *
*        HIGH-ORDER BIT IS SET IN THE SJB FIELD SJBPSOP.              *
*                                                                     *
***********************************************************************
         SPACE 2
PSODSECT DSECT                     PSO PARAMETER LIST DSECT
         SPACE 1
PSONEXT  DS    0A                  ADDR OF PSO FOR NEXT $O REQUEST
PSOCCE   DS    A                   ADDRESS OF CCE
PSOCRDT  DS    F                   ELLIGIBILITY CUT-OFF DATE        R41
PSOPDDB  DS    CL(PDBLENG)         DATA SET PDDB
         SPACE 1
         CNOP  2,4
PSOUFLG  DS    B                   GROUP REQUEST OPTIONS BYTE
         DS    C                   RESERVED
PSOFLG1  DS    B                   DATA SET SELECTION CONTROL FLAGS
PSOFLG2  DS    B                   DATA SET DISPOSITION AND CTL FLAGS
PSOCOPY  DS    H                   NUMBER OF DATA SET COPIES
PSOJOBN  DS    CL8                 JOB NAME
PSOJOBI  DS    CL8                 HASP JOB ID (JOBNNNNN)
PSOCLS   DS    C                   NEW SYSOUT CLASS (GROUP REQ ONLY)
         DS    CL3                 RESERVED
PSODEST  DS    CL8                 REMOTE USER ID FOR SELECTION
PSOPGMN  DS    CL8                 USER WRITER NAME
PSORBA   DS    CL8                 RBA OF SYSOUT DATA SET
PSODSN   DS    CL44                SYSOUT DATA SET NAME
         ORG   PSODSN              REDEFINE DSNAME
         DC    C'HASP.'            SUB-SYSTEM IDENTIFIER
PSOJOBNM DC    0CL8'JOBNNNNN',C'JOB'  JOB ID
PSOJOBNR DC    C'NNNNN',C'.PS'     JOB NUMBER PORTION OF JOB ID
PSODSNR  DC    C'NNNN'             DATA SET NUMBER
         ORG   ,
PSOFORM  DS    CL4                 DATA SET FORM NUMBER
PSOCLAS  DS    CL8                 CLASS(ES) TO BE PROCESSED
PSOWTRC  DC    A(PSOECB)           ADDR OF EXTERNAL WTR PARAMETER AREA
PSODSID  DC    CL8'CCCCCCCC'       DATA SET IDENT CHAR STRING  @Y30O
PSOPARML EQU   *-PSOUFLG           LENGTH OF SSOB PARAMETER AREA
         EJECT
         SPACE 5
PSOTCB   DS    A                   TCB ADDRESS OF LAST PSO USER
PSORETN  DS    XL4                 SUB-SYSTEM RETURN CODE
PSOHQT   DS    XL4                 TRACK (OR STORAGE) ADDRESS OF HQT
PSOSJB   DS    A                   ADDRESS OF SJB
         SPACE 1
*              START OF EXTERNAL WRITER PARAMETER AREA
PSOECB   DS    F                   ECB FOR EXTERNAL WRITER WAIT ON JOT
PSOECBP  DC    0A(PSOECB)          START OF XMPOST PARAMETER LIST
PSORDRON DS    XL4                 TIME ON INPUT PROCESSOR
PSORDTON DS    XL4                 DATE ON INPUT PROCESSOR
PSOUSEID DS    CL8                 USER ID
*              END OF EXTERNAL WRITER PARAMETER AREA
         SPACE 1
PSOWKOFF DS    A                   OFFSET OF WORK JOE          @OZ40028
PSOCHOFF DS    A                   OFFSET OF CHAR JOE          @OZ40028
PSOLINK  DS    0A                  SAVE AREA FOR LINK REGISTER
PSOCKOFF DS    A                   OFFSET OF CKPT JOE          @OZ40028
PSOJBKEY DS    XL4                 JOB IDENTIFIER KEY
         SPACE 1
PSOJQEP  DS    A                   HASP JQE OFFSET
PSOJOBNO DS    H                   HASP JOB NUMBER (BINARY)
PSOROUTE DS    H                   SELECTION ROUTE CODE (BINARY)
PSOIOTTR DS    F                   IOT TTR OF SELECTED HQR     @OZ31509
         SPACE 1
         DS    0D
PSOLNGTH EQU   *-PSODSECT          LENGTH OF PSO DSECT
&SYSECT  CSECT                     END OF PSO DSECT
         TITLE 'HASP PSO STATUS BIT DEFINITIONS'
*                             PSORETN
         SPACE 3
PSOFRTOK EQU   SSSORTOK            EVERYTHING IS OK
PSOFEODS EQU   SSSOEODS            NO MORE DATASETS TO SELECT
PSOFNJOB EQU   SSSONJOB            JOB NOT FOUND
PSOFINVA EQU   SSSOINVA            INVALID SEARCH ARGUMENTS
PSOFUNAV EQU   SSSOUNAV            UNABLE TO PROCESS NOW
PSOFDUPJ EQU   SSSODUPJ            DUPLICATE JOBNAMES
PSOFINVJ EQU   SSSOINVJ            INVALID JOBNAME/JOBID COMBINATION
         SPACE 5
*                             PSOUFLG
         SPACE 3
PSOFSETC EQU   SSSOSETC            ALTER SYSOUT CLASS TO PSOCLS
PSOFDELC EQU   SSSODELC            DELETE SELECTED DATA SETS
PSOFROUT EQU   SSSOROUT            ROUTE SELECTED DATA SETS TO PSODEST
PSOFHOLD EQU   SSSOHOLD            HOLD SELECTED DATA SETS
PSOFRLSE EQU   SSSORLSE            RELEASE SELECTED DATA SETS
         SPACE 5
*                             PSOFLG1
         SPACE 3
PSOFHLD  EQU   SSSOHLD             SELECT HELD SYSOUT DATA SETS
PSOFCLAS EQU   SSSOSCLS            USE CLASS
PSOFDEST EQU   SSSODST             USE REMOTE DESTINATION
PSOFJOBN EQU   SSSOSJBN            USE JOB NAME
PSOFJOBI EQU   SSSOSJBI            USE JOB ID
PSOFPGM  EQU   SSSOSPGM            USE USER WRITER PROGRAM NAME
PSOFFORM EQU   SSSOSFRM            USE FORM NUMBER
         SPACE 5
*                             PSOFLG2
         SPACE 3
PSOFDONE EQU   SSSOCTRL            PROCESSING COMPLETED
PSOFCKPT EQU   SSSOCHKP            CHECKPOINT USING RBA FIELD
PSOFPURG EQU   X'02'               PSO PURGED FLAG             @OZ40277
PSOF$O   EQU   X'01'               $O INITIATED GROUP REQUEST
         MEND
