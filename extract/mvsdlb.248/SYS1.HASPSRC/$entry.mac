         MACRO -- $ENTRY -- PROVIDE ENTRY TO HASP SEGMENT
&SEG     $ENTRY &DUMMY,&CSECT=NO,&BASE=BASE3,&ENTRY=YES,&POINTER=   R41
         GBLC  &VERSION
         LCLC  &REGS
         LCLA  &N
&N       SETA  16
         AIF   ('&POINTER' EQ '').A0                                R41
         AIF   ('&BASE' EQ '').A0                                   R41
         MNOTE 8,'POINTER NOT ALLOWED WITH BASE'                    R41
         MEXIT                                                      R41
.A0      ANOP                                                       R41
         AIF   ('&SEG' NE '').A
         MNOTE 8,'NAME FIELD MUST BE PROVIDED'
         MEXIT
.A       AIF   ('&CSECT' EQ 'YES').B
&SEG     DS    0D                  INSURE DOUBLE-WORD ALIGNMENT
         AIF   ('&ENTRY' NE 'YES').C
         ENTRY &SEG                PROVIDE ENTRY TO HASP SEGMENT
         AGO   .C
.B       ANOP
&SEG     CSECT                     CREATE NEW HASP CSECT
&N       SETA  &N+4
         AIF   ('&BASE' EQ '').F
&N       SETA  &N+4
.C       AIF   ('&BASE' NE 'BASE3').D
         ST    LINK,PCERETN        SAVE RETURN ADDRESS
&N       SETA  &N-4
.D       ANOP
&REGS    SETC  '&BASE'
         AIF   ('&BASE'(1,1) NE '(').E
&REGS    SETC  '&BASE'(2,K'&BASE-2)
.E       ANOP
         USING &SEG,&REGS          PROVIDE SEGMENT ADDRESSABILITY
         B     *+&N                BRANCH AROUND SEGMENT IDENTIFICATION
.F       ANOP
         AIF   ('&POINTER' EQ '').H                                 R41
         AIF   ('&POINTER'(1,1) EQ '(').G                           R41
         DC    A(&POINTER)         TABLE POINTER                    R41
         AGO   .H                                                   R41
.G       DC    AL1(&POINTER(1))    TABLE LENGTH                     R41
         DC    AL3(&POINTER(2))    TABLE POINTER                    R41
.H       ANOP                                                       R41
&N       SETA  &N+4-((&N+4)/8)*8
         DC    &N.C'$',CL8'&SEG'   HASP SEGMENT IDENTIFICATION
         AIF   ('&CSECT' NE 'YES').END
         DC    CL8'&VERSION'       HASP VERSION NUMBER
.END     MEND
