         MACRO -- $WTO -- WRITE TO OPERATOR                          R4
&NAME    $WTO  &MSG,&L,&MF=,&JOB=YES,&WAIT=YES,&CMB=,&RMT=,&UCM=,    R4C
               &ROUTE=$LOG,&TYPE=,&CLASS=$NORMAL,&PRI=$ST            R4
         LCLA  &E,&F                                                 R4
         LCLC  &A,&B,&D                                              R4
&A       SETC  '$WTO'              ASSUME CMB=NO                     R4
&B       SETC  '&L'                ASSUME L IN PARAMETER LIST        R4
&D       SETC  '&ROUTE'            ASSUME LOGICAL ROUTINGS           R4
&E       SETA  X'1000'             ASSUME NORMAL DESCRIPTOR CODES    R4
         AIF   ('&CMB' NE 'YES').A                                   R4
&A       SETC  '$WTOC'             SET CMB=YES                       R4
.A       AIF   ('&L' EQ '').AA                                       R4
         AIF   ('&L'(1,1) NE '(').B                                  R4
.AA      ANOP                                                        R4
&B       SETC  '*-*'               SET L DYNAMICALLY                 R4
.B       AIF   ('&MF' EQ 'LX').C                                     R4
&F       SETA  X'01'               INDICATE SHORT FORM REQUEST       R4
.C       AIF   ('&JOB' NE 'YES').D                                   R4
&F       SETA  &F+X'04'            SET JOB=YES                       R4
.D       AIF   ('&WAIT' EQ 'YES').E                                  R4
&F       SETA  &F+X'02'            SET WAIT=NO                       R4
.E       AIF   ('&TYPE' NE 'SVC34').F                                R4
&F       SETA  &F+X'80'            SET COMMAND PRESENT               R4
.F       AIF   ('&UCM' NE 'YES').G                                   R4
&F       SETA  &F+X'10'            SET UCMID WILL BE DYNAMICALLY SET R4
&D       SETC  '*-*'               NEGATE LOGICAL ROUTINGS           R4
&E       SETA  0                   NEGATE DESCRIPTOR CODES           R4
         AGO   .H                                                    R4
.G       AIF   ('&RMT' NE 'YES').H                                   R4
&F       SETA  &F+X'40'            REMOTE WILL BE DYNAMICALLY SET    R4
&D       SETC  '*-*'               NEGATE LOGICAL ROUTINGS           R4
&E       SETA  0                   NEGATE DESCRIPTOR CODES           R4
.H       AIF   ('&MF' EQ 'L').O                                      R4
         AIF   ('&MF' EQ 'LX').P                                     R4
         $CHEK X,&MSG              NORMAL AND MF=(E,X) REQUIRES MSG  R4
&NAME    $DECODE &MSG              R1 POINTS TO MESSAGE DATA         R4
         AIF   ('&MF(1)' EQ 'E').I                                   R4
         AIF   ('&MF(1)' EQ 'EX').K                                  R4
         $CHEK X,&L                NORMAL REQUIRES LENGTH            R4
         AIF   ('&L'(1,1) NE '(').HA                                 R4
         $DECODE  ,&L              R0=LENGTH                   @OZ25131
&F       SETA  &F+X'08'            INDICATE LENGTH IN R0             R4
.HA      L     R15,&A              POINT TO SERVICE ROUTINE          R4
         BALR  LINK,R15            ENTER SERVICE ROUTINE             R4
         DC    AL1(&F,&CLASS+&PRI,&D,&B)                             R4
         AGO   .N                                                    R4
.I       AIF   ('&L' EQ '').J                                        R4
         $DECODE ,&L               SET MSG LENGTH IN R0              R4
         O     R0,&MF(2)           SET PARAMETER REGISTER            R4
         AGO   .MA                                                   R4
.J       L     R0,&MF(2)           SET PARAMETER REGISTER            R4
         AGO   .MA                                                   R4
.K       AIF   ('&L' EQ '').M                                        R4
         AIF   ('&L'(1,1) EQ '(').L                                  R4
         MVI   &MF(2)+3,&L         SET MSG LENGTH                    R4
         AGO   .M                                                    R4
.L       STC   &L(1),&MF(2)+3      SET MSG LENGTH                    R4
.M       LA    R0,&MF(2)           POINT TO PARAMETERS               R4
.MA      L     R15,&A              POINT TO SERVICE ROUTINE          R4
         BAL   LINK,0(,R15)        ENTER SERVICE ROUTINE             R4
.N       AIF   ('&WAIT' EQ 'YES' OR '&WAIT' EQ 'NO').END             R4
         BE    &WAIT               BRANCH IF $WTO NOT SUCCESSFUL     R4
         MEXIT                                                       R4
.O       CNOP  0,4                 INSURE FULL WORD ALIGNMENT        R4
&NAME    DC    AL1(&F,&CLASS+&PRI,&D,&B)                             R4
         MEXIT                                                       R4
.P       AIF   ('&CLASS' NE '$DOMACT').Q                             R4
&E       SETA  X'4000'             SET ACTION DESCRIPTOR CODES       R4
.Q       ANOP                                                        R4
&NAME    DC    AL1(&F,&CLASS+&PRI,0,&B),AL2(*-*,&E,&D,0,0)           R4
.END     MEND                                                        R4
