PRPU     TITLE 'HASP PRINT/PUNCH SERVICE PROLOG'
***********************************************************************
*                                                                     *
* MODULE NAME = HASJES20 ( HASPPRPU CSECT )                           *
*                                                                     *
* DESCRIPTIVE NAME = JES2 PRINT/PUNCH PROCESSOR                       *
*                                                                     *
* COPYRIGHT = NONE                                                    *
*                                                                     *
* STATUS = OS/VS2 MVS   --  SEE &VERSION (BELOW) FOR JES2 LEVEL       *
*                                                                     *
* FUNCTION = THE HASPPRPU CSECT PROVIDES SELECTIVE PROCESSING OF      *
*            JOB SYSOUT OUTPUT WITH INTERFACES TO THE CONSOLE         *
*            OPERATOR FOR DIRECTION.                                  *
*                                                                     *
* NOTES = SEE BELOW                                                   *
*                                                                     *
*    DEPENDENCES = IF A 3211 PRINTER IS TO BE USED AS AN OUTPUT       *
*                  DEVICE, THE PRINT POSITION INDEXING FEATURE        *
*                  MUST BE INSTALLED.                                 *
*                                                                     *
*                  THE SYSTEM LIBRARY SYS1.IMAGELIB MUST CONTAIN      *
*                  ANY UCS OR FCB IMAGES SPECIFIED BY USER JCL.       *
*                  IF NOT, THE OPERATOR WILL GET A DIAGNOSTIC         *
*                  MESSAGE AND BE REQUIRED TO SPECIFY AN IMAGE        *
*                  NAME WHICH IS IN SYS1.IMAGELIB.                    *
*                                                                     *
*                  ALL FCB IMAGES OR CARRIAGE TAPES USED WITH JES2    *
*                  MUST CONTAIN A CHANNEL 1 PUNCH IN LINE POSITION    *
*                  1 FOR PROPER FORMS ALLIGNMENT.                     *
*                                                                     *
*                  3525 PRINT CCW'S DIRECTED TO A 3525 WITHOUT THE    *
*                  PRINT FEATURE WILL BE CONVERTED TO PUNCH CCW'S.    *
*                                                                     *
*                  A REQUEST TO INTERPRET CARDS ON A 3525 PUNCH       *
*                  WILL BE IGNORED IF THE PRINT FEATURE IS NOT        *
*                  INSTALLED.                                         *
*                                                                     *
*    RESTRICTIONS = IF THE FUNC=I INTERPRET FEATURE IS TO BE USED     *
*                   WITH A 3525 PUNCH, THE JES2 INITIALIZATION        *
*                   PARAMETER &NOPUCCW MUST BE AT LEAST 2.            *
*                                                                     *
*                   ALL DATA AREAS PRINTED/PUNCHED-FROM MUST BE       *
*                   PAGE-FIXED FOR EXCPVR PROCESSING.                 *
*                                                                     *
*    REGISTER CONVENTIONS = R0  =       = PARAMETER REGISTER          *
*                           R1  =       = PARAMETER REGISTER          *
*                           R2  = PW    = WORK REGISTER               *
*                           R3  = PBUF  = BUFFER ADDRESSABILITY       *
*                           R4  = PC1   = CCW WORK REGISTER 1         *
*                           R5  = PC2   = CCW WORK REGISTER 2         *
*                           R6  = BASE4 = PROCESSOR ADDRESSABILITY    *
*                           R7  = PL    = SECONDARY LINK REGISTER     *
*                           R8  = BASE3 = PROCESSOR ADDRESSABILITY    *
*                           R9  =       = RESERVED                    *
*                           R10 = JCT   = JCT ADDRESSABILITY          *
*                           R11 = BASE1 = HCT ADDRESSABILITY          *
*                           R12 = BASE2 = PROCESSOR ADDRESSABILITY    *
*                           R13 = SAVE  = PCE ADDRESSABILITY          *
*                           R14 = LINK  = PRIMARY LINK REGISTER       *
*                           R15 =       = RETURN REGISTER             *
*                           SEE ENTRY POINT DESCRIPTIONS FOR          *
*                           EXCEPTIONS.                               *
*                                                                     *
*    PATCH LABEL = NONE                                               *
*                                                                     *
* MODULE TYPE = PROCEDURE, TABLE ( CSECT TYPE )                       *
*                                                                     *
*    PROCESSOR = OS/VS ASSEMBLER                                      *
*                                                                     *
*    MODULE SIZE = SEE $DLENGTH MACRO EXPANSION(S) AT END OF ASSEMBLY *
*                                                                     *
*    ATTRIBUTES = READ ONLY, EXCEPT WHEN DEBUG OPTION SELECTED, AND   *
*                 HASP REENTRANT                                      *
*                                                                     *
* ENTRY POINT = HASPIMAG                                              *
*                                                                     *
*    PURPOSE = THIS ENTRY IS IDENTIFIED AND ATTACHED BY MODULE        *
*              HASPINIT.  ITS FUNCTION IS TO LOAD FCB AND UCS IMAGES  *
*              FROM SYS1.IMAGELIB WITHOUT CAUSING THE MAIN JES2 TASK  *
*              TO WAIT.                                               *
*                                                                     *
*    LINKAGE = THIS SUBTASK IS ACTIVATED WHEN NEEDED BY A JES2 PRINT  *
*              PUNCH PROCESSOR BY A OS/VS POST WITH A BUFFER ADDRESS  *
*              AS THE POST CODE.  THIS BUFFER CONTAINS A BLDL LIST    *
*              FOR THE REQUIRED IMAGE.  WHEN THE IMAGE IS LOADED,     *
*              THE SUBTASK MOVES IT TO THE BUFFER AND USES $$POST     *
*              TO ALERT THE WAITING PRINT/PUNCH PROCESSOR.  THE       *
*              LOADED IMAGE IS THEN DELETED AND THE SUBTASK WAITS.    *
*                                                                     *
* ENTRY POINT = HASPHOPE                                              *
*                                                                     *
*    PURPOSE = THIS IS THE ENTRY POINT TO THE JES2 OUTPUT PROCESSOR.  *
*              THE OUTPUT PROCESSOR CONVERTS THE OUTPUT REQUIREMENTS  *
*              FOR A JOB DESCRIBED BY THE PDDB'S IN THE JOB'S OUTPUT  *
*              IOT(S) TO JOE(S).  AS EACH JOE IS ADDED TO THE JOT, IT *
*              IMMEDIATELY BECOMES AVAILABLE FOR SELECTION BY A       *
*              PRINT/PUNCH PROCESSOR.  IF ANY SPIN DATA SET IOT'S     *
*              ARE ON THE $UNSPUNQ, THEIR REQUIREMENTS ARE ADDED TO   *
*              THE JOT IF SPACE IS AVAILABLE.                         *
*                                                                     *
*    LINKAGE = VIA $WAIT AND $POST OBEYING JES2 CONVENTIONS.          *
*                                                                     *
* ENTRY POINT = HASPPPI1                                              *
*                                                                     *
*    PURPOSE = THIS IS THE ENTRY POINT TO THE JES2 PRINT/PUNCH        *
*              PROCESSOR.  THIS PROCESSOR AQUIRES OUTPUT WORK FROM    *
*              THE JOT USING ITS DCT AS A PARAMETER LIST.  THE DATA   *
*              SETS REPRESENTED ARE READ FORM THE SPOOL VOLUME,       *
*              DEBLOCKED, AND PRINTED/PUNCHED TO THE USER             *
*              SPECIFICATIONS.  WHEN ALL OUTPUT WORK FOR A JOB HAS    *
*              BEEN COMPLETED, THE JOB IS MOVED TO THE PURGE QUEUE.   *
*                                                                     *
*    LINKAGE = VIA $WAIT AND $POST OBEYING JES2 CONVENTIONS.          *
*                                                                     *
* INPUT = SEE ENTRY POINT DESCRIPTION                                 *
*                                                                     *
* OUTPUT = SEE ENTRY POINT DESCRIPTION                                *
*                                                                     *
* EXIT NORMAL = SEE ENTRY POINT DESCRIPTION                           *
*                                                                     *
* EXIT ERROR = USES JES2 MACRO $DISTERR TO INFORM THE OPERATOR        *
*                                                                     *
* EXTERNAL REFERENCES = SEE BELOW                                     *
*                                                                     *
*    ROUTINES = SEE MACROS FOR SERVICES USED                          *
*                                                                     *
*    DATA AREAS = SEE $HASPCB MACRO EXPANSION                         *
*                                                                     *
*    CONTROL BLOCKS - SEE $OUTWORK MACRO EXPANSION FOR PCE WORK       *
*                     AREA EXTENSION FOR ENTRY POINT HASPHOPE         *
*                                                                     *
*                     SEE $PPPWORK MACRO EXPANSION FOR PCE WORK       *
*                     AREA EXTENSION FOR ENTRY POINT HASPPPI1         *
*                                                                     *
* TABLES - NONE                                                       *
*                                                                     *
* MACROS = JES2   - $$POST, $#ADD, $#BLD, $#CKPT, $#GET, $#JCT,       *
*                   $#PUT, $#REM, $DISTERR, $DOM, $EXCP, $EXTP,       *
*                   $FREEBUF, $FREUNIT, $GETBUF, $GETSMFB, $GETUNIT,  *
*                   $IOERROR, $PGSRVC, $POST, $PURGE, $QGET, $QLOC,   *
*                   $QPUT, $QUESMFB, $TIME, $WAIT, $WTO               *
*                                                                     *
* MACROS = SYSTEM - BLDL, DELETE, ESTAE, IMGLIB, LOAD, POST, RETURN,  *
*                   SAVE, SETPRT, SETRP, TIME, WAIT                   *
*                                                                     *
* CHANGE ACTIVITY                                                     *
*                                                                     *
*     RELEASE 4.0 = OZ02418,OZ02421,OZ02422,OZ02442,OZ02443,OZ02444,  *
*                   OZ03343,OZ04338,OZ04340,OZ04967,OZ04969,OZ05777,  *
*                   OZ05780,OZ06730,OZ06740,OZ06748,OZ07433,OZ07447,  *
*                   OZ08187,OZ08230,OZ09020,OZ09042,OZ09064,OZ09079,  *
*                   OZ09082,OZ10324                                   *
*                                                                     *
*     RELEASE 4.1 = OZ09073,OZ10376,OZ10377,OZ11743,OZ11760,OZ11775,  *
*                   OZ11790,OZ12298,OZ12301,OZ12302,OZ13254,OZ14412,  *
*                   OZ14424,OZ14447,OZ14914,OZ15250,OZ15275,OZ16691   *
*                                                              @G38ESBB
*     EJE1103     = @G38ESBB 3800 PRINTER ENHANCEMENTS         @G38ESBB
*                                                                     *
***********************************************************************
         TITLE 'HASP CONTROL BLOCK GENERATION MACRO'
         SPACE 5
*
*       $HASPCB                    GENERATE HASP CONTROL BLOCK
*
         SPACE 1
         MACRO
         $HASPCB &DOC=NO,&LIST=NO
         GBLC  &PRINT,&GEN,&DATA
         PUSH  PRINT
         PRINT &PRINT
         $CVT  LIST=&LIST          GENERATE OS CVT DSECT             R4
         $DCB  LIST=&LIST          GENERATE OS DCB DSECT
         $DEB  LIST=&LIST          GENERATE OS DEB DSECT
         $UCB  LIST=&LIST          GENERATE OS UCB DSECT
         $SDWA LIST=&LIST          GENERATE OS SDWA DSECT            R4
         $SETPRT LIST=&LIST        GENERATE OS SPPARM DSECT          R4
         $TED  DOC=&DOC            GENERATE HASP TED DSECT
         $TAB  DOC=&DOC            GENERATE HASP TAB DSECT           R4
         $PCIE DOC=&DOC            GENERATE HASP PCIE DSECT          R4
         $SVT  DOC=&DOC            GENERATE HASP SSVT DSECT
         $SJB  DOC=&DOC            GENERATE HASP SJB DSECT
         $HCT  DOC=&DOC            GENERATE HASP HCT DSECT
         $PCE  DOC=&DOC            GENERATE HASP PCE DSECT
         $LRC  DOC=&DOC            GENERATE HASP LRC DSECT
         $BUFFER DOC=&DOC          GENERATE HASP BUFFER DSECT
         $SMF  DOC=&DOC            GENERATE HASP SMF DSECT
         $JQE  DOC=&DOC            GENERATE HASP JQE DSECT
         $JOE  DOC=&DOC            GENERATE HASP JOE DSECT
         $JOT  DOC=&DOC            GENERATE HASP JOT DSECT
         $QSE  DOC=&DOC            GENERATE HASP QSE DSECT
         $JCT  DOC=&DOC            GENERATE HASP JCT DSECT
         $CAT  DOC=&DOC            GENERATE HASP CAT DSECT
         $PDDB DOC=&DOC            GENERATE HASP PDDB DSECT
         $IOT  DOC=&DOC            GENERATE HASP IOT DSECT
         $RAT  DOC=&DOC            GENERATE HASP RAT DSECT
         $DCT  DOC=&DOC            GENERATE HASP DCT DSECT
         $FMH  DOC=&DOC            GENERATE SNA FM HEADER DSECT      R4
         $BFW  DOC=&DOC            GENERATE HASP BFW DSECT     @G38ESBB
         $PQH  DOC=&DOC            GENERATE HASP PQH DSECT     @G38ESBB
         $PQE  DOC=&DOC            GENERATE HASP PQE DSECT     @G38ESBB
         $OUTWORK DOC=&DOC         GENERATE HASP OUTWORK DSECT
         $PPPWORK DOC=&DOC         GENERATE HASP PPPWORK DSECT
         $DTE  DOC=&DOC            GENERATE HASP DTE DSECT     @OZ38805
         SPACE 1
         POP   PRINT
         PRINT &GEN,&DATA          SET ASSEMBLY PRINT OPTIONS
         MEND
 TITLE 'HASPPRPU REGISTER SAVE/RETURN (PSAVE/PRETURN) MACROS'  @G38ESBB
*                                                              @G38ESBB
*****    PSAVE                     REGISTER SAVE MACRO         @G38ESBB
*****                                                          @G38ESBB
         MACRO                                                 @G38ESBB
&NAME    PSAVE &ALL                                            @G38ESBB
&NAME    STM   R14,R1,$CSAVREG     SAVE WORK REGS              @G38ESBB
         LA    R15,PSAVE           ADDRESS PSAVE ROUTINE       @G38ESBB
         AIF   ('&ALL' EQ 'ALL').PSVALL                        @G38ESBB
         BALR  R14,R15             *** SAVE BASE2 AND PL ***   @G38ESBB
         AGO   .PSAVX                                          @G38ESBB
.PSVALL  BAL   R14,0(,R15)         *** SAVE ALL REGISTERS ***  @G38ESBB
.PSAVX   LM    R14,R1,$CSAVREG     RESTORE WORK REGS           @G38ESBB
         MEND                                                  @G38ESBB
         SPACE 2                                               @G38ESBB
*                                                              @G38ESBB
*****    PRETURN                   REGISTER RESTORE/RETURN     @G38ESBB
*****                                   MACRO                  @G38ESBB
         MACRO                                                 @G38ESBB
&NAME    PRETURN ,                                             @G38ESBB
&NAME    STM   R14,R1,$CSAVREG     SAVE WORK REGISTERS         @G38ESBB
         LA    R15,PRETURN         POINT TO PRETURN RTN        @G38ESBB
         BALR  R14,R15             *** RESTORE SAVED REGS ***  @G38ESBB
         MEND                                                  @G38ESBB
 TITLE 'HASPPRPU MESSAGE (PMSG) MACRO'                         @G38ESBB
*                                                              @G38ESBB
*****    PMSG                      $WTO MSG TEXT CREATE MACRO  @G38ESBB
*****                                                          @G38ESBB
         MACRO                                                 @G38ESBB
         PMSG  &AREA,&LENGTH,&TEXT MACRO PARAMETERS            @G38ESBB
         LCLA  &CT                 DECLARE                     @G38ESBB
         LCLA  &AL                  LOCAL                      @G38ESBB
         LCLA  &ALSUM                                          @G38ESBB
         LCLC  &CL                   VARIABLES                 @G38ESBB
         LCLC  &CLSUM                                          @G38ESBB
&CT      SETA  1                   INITIALIZE LOCAL            @G38ESBB
.A       AIF   (&CT EQ N'&TEXT+1).Y           DETERMINE TYPE   @G38ESBB
&AL      SETA  0                                               @G38ESBB
&CL      SETC  ''                                              @G38ESBB
         AIF   ('&TEXT(&CT)'(1,2) NE 'C''').B  OF TEXT STRING  @G38ESBB
&AL      SETA  K'&TEXT(&CT)-3                   IN ORDER TO    @G38ESBB
         AGO   .C                                COMPUTE       @G38ESBB
.B       AIF   ('&TEXT(&CT)'(1,2) NE 'X''').F     CORRECT      @G38ESBB
&AL      SETA  (K'&TEXT(&CT)-3)/2                  LENGTH      @G38ESBB
.C       AIF   ('&CLSUM' EQ '').D                              @G38ESBB
         MVC   &AREA+&CLSUM+&ALSUM.(&AL),=&TEXT(&CT)           @G38ESBB
         AGO   .E                                              @G38ESBB
.D       MVC   &AREA+&ALSUM.(&AL),=&TEXT(&CT) MESSAGE TEXT     @G38ESBB
.E       ANOP                                                  @G38ESBB
&ALSUM   SETA  &ALSUM+&AL           OFFSET AND                 @G38ESBB
         AGO   .X                                              @G38ESBB
.F       ANOP                                                  @G38ESBB
&CL      SETC  'L''&TEXT(&CT)'     IF NOT TEXT STRING USE      @G38ESBB
         AIF   ('&CLSUM' EQ '').G                              @G38ESBB
         MVC   &AREA+&CLSUM+&ALSUM.(&CL),&TEXT(&CT)            @G38ESBB
         AGO   .H                                              @G38ESBB
.G       MVC   &AREA+&ALSUM.(&CL),&TEXT(&CT) IMPLICIT LENGTH   @G38ESBB
.H       AIF   ('&CLSUM' EQ '').I                              @G38ESBB
&CLSUM   SETC  '&CLSUM+&CL'                                    @G38ESBB
         AGO   .X                                              @G38ESBB
.I       ANOP                                                  @G38ESBB
&CLSUM   SETC  '&CL'                                           @G38ESBB
.X       ANOP                                                  @G38ESBB
&CT      SETA  &CT+1               BUMP COUNTER AND            @G38ESBB
         AGO   .A                    CONTINUE LOOP             @G38ESBB
.Y       AIF   (K'&LENGTH EQ 0).Z                              @G38ESBB
&LENGTH  EQU   &CLSUM+&ALSUM       MESSAGE TEXT LENGTH         @G38ESBB
.Z       MEND                                                  @G38ESBB
         TITLE 'HASP OUTPUT PROCESSOR EXECUTIVE'
******  $DTE  ******               DEFINE DAUGHTER TASK ELEME  @OZ38805
         MACRO                                                 @OZ38805
         $DTE  &DOC=NO                                         @OZ38805
         TITLE 'HASP DAUGHTER TASK ELEMENT (DTE) DSECT'        @OZ38805
         SPACE 5                                               @OZ38805
DTEDSECT DSECT                                                 @OZ38805
DTETCB   DS    A                   SUB-TASK TCB ADDRESS        @OZ38805
DTETECB  DS    F                   SUB-TASK TERMINATION ECB    @OZ38805
DTEWECB  DS    F                   SUB-TASK WORK ECB           @OZ38805
&SYSECT  CSECT                     END OF DTE DSECT            @OZ38805
         MEND                                                  @OZ38805
         SPACE 5
HASPPRPU START 0                   HASP PRINT/PUNCH SERVICE
         SPACE 5
         COPY  $HASPGEN            COPY HASPGEN PARAMETERS
         TITLE 'HASP CONTROL BLOCKS'
         SPACE 5
HASPPRPU $ENTRY BASE=,CSECT=YES    PROVIDE PROCESSOR IDENTIFICATION
         SPACE 5
*
*                             DOCUMENTATION OPTIONS FOR THIS ASSEMBLY
*
         SPACE 3
        $SYSPARM (OFF,GEN,NODATA,NO,NO)
         SPACE 5
*
*                             GENERATE HASP CONTROL BLOCKS
*
         SPACE 3
        $HASPCB DOC=&DOC,LIST=&LIST  GENERATE HASP CONTROL BLOCKS
         TITLE 'HASP OUTPUT PROCESSOR -- MAIN ENTRY'                 R4
***********************************************************************
*                                                                     *
*              HOPE REGISTER DEFINITIONS                              *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         SPACE 3
JOE      EQU   6                   JOB OUTPUT ELEMENT BASE
RNP      EQU   7                   NON-PROCESS RETURN REGISTER
JOT      EQU   8                   JOB OUTPUT TABLE BASE
         SPACE 15                                                    R4
***********************************************************************
*                                                                     *
*        HASP OUTPUT PROCESSOR -- MAIN ENTRY POINT                    *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
HASPHOPE $ENTRY BASE=BASE2         HOPE MAIN ENTRY                   R4
         TITLE 'HASP OUTPUT PROCESSOR -- SPIN DATA SET PROCESSING'   R4
***********************************************************************
*                                                                     *
*        ADD ANY STACKED SPIN DATA SETS TO THE JOT IF POSSIBLE        *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
OPINIT   L     JOT,$AQSE           LOAD QSE BASE ADDRESS       @OZ27300
         USING QSEDSECT,JOT        ACTIVATE QSE ADDRESSABILITY
         MVC   PCEJQE,$ZEROS       CLEAR JQE ADDRESS           @OZ32566
        $QSUSE                     REQUEST ACCESS TO CHECKPOINT DATA
         OC    $UNSPUNQ,$UNSPUNQ   ANY QUEUED SPIN IOTS
         BZ    OPSPIN3             BRANCH IF NO
         L     R1,$JOTABLE         ADDRESS JOB OUTPUT TABLE
         USING JOTDSECT,R1         ACTIVATE JOT ADDRESSABILITY
         LH    R2,JOTFREC          NUMBER OF AVAILABLE JOES
         BCTR  R2,R0               JOE FOR A CHAR-JOE
         BCTR  R2,R0               JOE FOR A WORK-JOE
         CH    R2,$MINJOES         BELOW MINIMUM FREE LIMIT...       R4
         BL    OPSPIN3             BRANCH IF YES
         DROP  R1                  SUSPEND JOT ADDRESSABILITY
         SPACE 1                                                     R4
***********************************************************************
*                                                                     *
*        GET A BUFFER FOR THE SPIN IOT IF ONE NOT OWNED               *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         ICM   R1,15,OPIOTBUF      IOT BUFFER ALREADY GOTTEN
         BNZ   OPSPIN1             BRANCH IF YES
        $GETBUF ,                  GET A BUFFER FOR THE IOT
         ST    R1,OPIOTBUF         SAVE BUFFER ADDRESS
         BNZ   OPSPIN1             BRANCH IF BUFFER AVAILABLE
        $WAIT  BUF                 WAIT FOR A FREE BUFFER
         B     HASPHOPE            GO TRY AGAIN
         EJECT                                                       R4
***********************************************************************
*                                                                     *
*        READ FIRST SPIN IOT FROM THE UNSPUN QUEUE                    *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
OPSPIN1  DS    0H
         L     R0,$UNSPUNQ         GET IOT TRACK ADDRESS
         MVI   PCEDEVTP,PCEDARD    SET DCT FOR READ
         BAL   RNP,OPIOCK          READ AND CHECK IOT
         BZ    OPSPIN2             BRANCH IF READ GOOD
OPSPNIOT $DISTERR                  INDICATE CONTROL BLOCK ERROR
        $QSUSE                     REQUEST ACCESS TO CHECKPOINT DATA
         XC    $UNSPUNQ,$UNSPUNQ   CLEAR SPIN IOT QUEUE
         B     OPSPIN3             EXIT FROM SPIN LOOP
         SPACE 1                                                     R4
***********************************************************************
*                                                                     *
*        IF UNSPUN QUEUE HAS CHANGED - START OVER                     *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
OPSPIN2  DS    0H
        $QSUSE                     REQUEST ACCESS TO CHECKPOINT DATA
         L     R1,OPIOTBUF         ADDRESS IOT BUFFER
         USING IOTDSECT,R1         ACTIVATE IOT ADDRESSABILITY
         CLC   $UNSPUNQ,PCESEEK    HAS SPIN QUEUE CHANGED            R4
         BNE   HASPHOPE            BRANCH IF YES
         CLC   PCESEEK,IOTTRACK    VALIDATE IOT                      R4
         BNE   OPSPNIOT            BRANCH IF IOT BAD                 R4
         L     R2,$IOTPDDB         POINT TO                          R4
         ALR   R2,R1                1ST PDDB IN IOT                  R4
         USING PDBDSECT,R2         ACTIVATE PDDB ADDRESSABILITY
         MVC   OPCLASS,PDBCLASS    SAVE SYSOUT CLASS OF PDDB
         L     R3,IOTJQOFF         HOLD JQE OFFSET
         LTR   JCT,R3              COPY JQE OFFSET             @OZ60988
         BNP   OPSPNIOT            BR IF INVALID OFFSET        @OZ60988
         LH    R0,$MAXJOBS         COMPUTE MAXIMUM             @OZ60988
         BCTR  R0,0                 JQE                        @OZ60988
         MH    R0,=Y(JQELNGTH)       LENGTH                    @OZ60988
         CLR   JCT,R0              BR IF INVALID               @OZ60988
         BH    OPSPNIOT             JQE OFFSET                 @OZ60988
         AL    JCT,$JOBQPTR        ADD JOB QUEUE BASE
         USING JQEDSECT,JCT        ACTIVATE JQE ADDRESSABILITY
         TM    JQEFLAGS,QUEPURGE   HAS $CJOB BEEN ISSUED
         BO    OPSPNCJ             BRANCH IF YES
         DROP  R1,R2               SUSPEND IOT,PDDB ADDRESSABILITY
         SPACE 1                                               @OZ60988
***********************************************************************
*                                                                     *
*        BUILD A PROTOTYPE JOE PAIR FROM THE PDDB IN THE SPIN IOT     *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
        $#BLD  JOES=OPWORK,PDDB=(R2),JQE=(R3) CONVERT PDDB TO JOES   R4
         LA    JOE,OPWORK          ADDRESS WORK-JOE
         USING JOEDSECT,JOE        ACTIVATE JOE ADDRESSABILITY
         L     R1,OPIOTBUF         ADDRESS SPIN IOT
         USING IOTDSECT,R1         ACTIVATE IOT ADDRESSABILITY
         MVC   JOEIOTTR,IOTTRACK   SET SPIN IOT TRACK ADDRESS
         MVI   JOEFLAG,$JOESPIN    SET SPIN FLAG IN WORK-JOE
         DROP  R1,JOE              SUSPEND IOT,JOE ADDRESSABILITY
         SPACE 1                                                     R4
***********************************************************************
*                                                                     *
*        ATTEMPT TO ADD THE PROTOTYPE JOE PAIR TO THE JOT             *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         LA    R0,OPWORK           ADDRESS WORK-JOE PROTOTYPE
         LA    R1,OPCHAR           ADDRESS CHAR-JOE PROTOTYPE
        $#ADD  WORK=(R0),CHAR=(R1) ADD TO THE JOT                    R4
         BNZ   OPSPIN3             BRANCH IF JOT FULL
         EJECT                                                       R4
***********************************************************************
*                                                                     *
*        DE-QUEUE IOT FROM $UNSPUNQ -- RE-QUEUE JOB FOR $PURGE        *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
OPSPNCJ  DS    0H
         L     R1,OPIOTBUF         ADDRESS IOT
         USING IOTDSECT,R1         ACTIVATE IOT ADDRESSABILITY
         MVC   $UNSPUNQ,IOTSPIOT   DEQUEUE SPIN IOT FROM CHAIN
         L     R2,$IOTPDDB         POINT TO                          R4
         ALR   R2,R1                1ST PDDB IN IOT                  R4
         USING PDBDSECT,R2         ACTIVATE PDDB ADDRESSABILITY
         NI    PDBFLAG1,255-PDB1HOLD TURN OFF HOLD FLAG
         TM    JQEFLAGS,QUEPURGE   HAS $CJOB BEEN ISSUED...    @OZ27300
         BO    *+8                 BR IF YES                   @OZ27300
         OI    PDBFLAG1,PDB1PSO    TURN ON PSO FLAG
         DROP  R1,R2               SUSPEND IOT,PDDB ADDRESSABILITY
         MVI   PCEDEVTP,PCEDAWR    SET DCT FOR WRITE
         L     R0,PCESEEK          GET IOT MTTR
         BAL   RNP,OPIOCK          WRITE AND CHECK IOT
        $QSUSE                     REQUEST ACCESS TO CHECKPOINT DATA
         LH    R1,JQEHLDCT         DECREMENT                   @OZ27300
         SL    R1,=A(X'10')         JOB HELD                   @OZ27300
         STH   R1,JQEHLDCT           DATASET COUNT             @OZ27300
         SRA   R1,4                ANY MORE NON-HELD OUTPUT... @OZ27300
         BNZ   *+8                 BR IF YES                   @OZ27300
         NI    JQEHLDCT+1,FF-JQEHLDDS  RESET 3540-HOLD FLAG    @OZ27300
          $QCKPT (JCT)             CHECKPOINT THE JQE          @OZ20010
         TM    JQEFLAGS,QUEPURGE   HAS $CJOB BEEN ISSUED
         BNO   HASPHOPE            BRANCH IF NO
*                                  THIS CARD DELETED BY APAR   @OZ27300
         CLC   JQEJOE,$ZEROS       ANY NON-HELD OUTPUT...      @OZ27300
         BNE   OPINIT              BR IF YES                   @OZ27300
         CLC   JQEHLDCT,$ZEROS     ANY MORE HELD OUTPUT...     @OZ27300
         BNE   OPINIT              BR IF YES                   @OZ27300
         CLI   JQETYPE,$HARDCPY    IS JOB IN THE $HARDCPY QUEUE
         BNE   HASPHOPE            BRANCH IF NO
        $QPUT  (JCT),$PURGE        MOVE THE JOB TO $PURGE
         B     HASPHOPE            GO TO NEXT SPIN DATA SET
         DROP  JCT                 SUSPEND JQE ADDRESSABILITY
         TITLE 'HASP OUTPUT PROCESSOR -- MAIN PROCESSOR'             R4
***********************************************************************
*                                                                     *
*        *** MAIN PROCESSOR ***        LOCATE A JOB                   *
*                                                                     *
*        FREE THE SPIN IOT BUFFER AND ATTEMPT TO GET A JOB            *
*                                                                     *
***********************************************************************
OPSPIN3  DS    0H
         ICM   R1,15,OPIOTBUF      IS THERE AN IOT BUFFER TO FREE
         BZ    OPQLOC              BRANCH IF NO
        $FREEBUF (R1)              FREE IOT BUFFER
         XC    OPIOTBUF,OPIOTBUF   ZERO IOT BUFFER POINTER
OPQLOC   DS    0H
         OC    QSEOPCKP,QSEOPCKP   IS THIS A RESTART
         BZ    OPQGET              BRANCH IF NO
        $QLOC  QSEOPJNO            LOCATE JOB QUEUE ELEMENT
         BZ    OPQGET              BRANCH IF JOB NOT FOUND
         USING JQEDSECT,R1         ACTIVATE JQE ADDRESSABILITY
         OC    JQEFLAGS,$SIDBUSY   SET JOB QUEUE ELEMENT BUSY
         DROP  R1                  SUSPEND JQE ADDRESSABILITY
          $QCKPT (R1)              CHECKPOINT THE JQE          @OZ20010
         B     OPJOB               GO PROCESS PARTIAL JOB
OPQGET   DS    0H
         USING JCTDSECT,JCT        ACTIVATE JCT ADDRESSABILITY
        $QGET  $OUTPUT             ATTEMPT TO GET A JOB
         BNZ   OPSETUP             BRANCH IF NEW JOB FOUND
*
*        $QGET NON-PROCESS EXIT ROUTINE
*
        $WAIT  JOB,INHIBIT=NO      $WAIT FOR JOB TO BE QUEUED
         B     HASPHOPE            TRY AGAIN
         EJECT                                                       R4
***********************************************************************
*                                                                     *
*        SETUP TO PROCESS NEW JOB                                     *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
OPSETUP  DS    0H
         SLR   R0,R0               CLEAR REGISTER
         STH   R0,QSEOPCKP         SET PARTIAL JOE COUNT ZERO
OPJOB    DS    0H
        $ACTIVE R=R14              INDICATE PROCESSOR ACTIVE
         ST    R1,PCEJQE           STORE JQE ADDRESS           @OZ32566
         USING JQEDSECT,R1         ACTIVATE JQE ADDRESSABILITY
         MVC   QSEOPJNO,JQEJOBNO   SET JOB NUMBER FOR WARM START
         DROP  R1                  SUSPEND JQE ADDRESSABILITY
         LR    R2,R1               HOLD A(JOB QUEUE ELEMENT)
        $TIME                      GET SIGN-ON TIME/DATE
         STM   R0,R1,OPTIMEON      SAVE IN PCE FOR JCT UPDATE
         SPACE 1                                                     R4
***********************************************************************
*                                                                     *
*        GET ADDRESS OF JCT IN BUFFER FROM JCT MANAGER                *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         SLR   JOE,JOE             CLEAR JOE REG IN CASE OF JCT ERR  R4
         LR    JCT,R2              SET JCT = A(JOB QUEUE ELEMENT)
        $#JCT  READ,REFRESH=YES    GET ADDR OF UPDATED JCT BUFF     R41
         ST    JCT,OPJCTBUF        SAVE JCT BUFFER ADDRESS
         BZ    OPNOJCT             BRANCH IF READ WAS IN ERROR
         SPACE 1                                                     R4
***********************************************************************
*                                                                     *
*        JCT VALID - DETERMINE IF THIS JOB HAD THE NOTIFY OPTION      *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
JCTOK    DS    0H
         OC    QSEOPCKP,QSEOPCKP   IS THIS A RESTART
         BNZ   OPNOTX              BRANCH IF YES - SKIP NOTIFY
         TM    JCTTSUID,255-C' '   WAS NOTIFY REQUESTED...           R4
         BZ    OPNOTX              BR IF NO TO SKIP NOTIFY           R4
*              THIS LINE HAS BEEN DELETED BY OZ60576           @OZ60576
*              THIS LINE HAS BEEN DELETED BY OZ60576           @OZ60576
         EJECT                                                       R4
***********************************************************************
*                                                                     *
*        SCAN JOB QUEUE FOR TS USER TO NOTIFY                         *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
        $QSUSE                     REQUEST ACCESS TO CHECKPOINT DATA
         L     R1,=V($QINDEX)      LOCATE
         LA    R2,CATTSUCL-(255-QUECLASS) HEAD
         IC    R2,0(R1,R2)         OF THE TSU
         LA    R2,$JQHEADS-2-QUECHAIN(R2) EXECUTION QUEUE
         EJECT                                                       R4
         SPACE 1                                                     R4
OPJQELP  LH    R2,QUECHAIN(,R2)    GET NEXT JQE OFFSET               R4
         N     R2,=F'65535'        CLEAR LEFT HALFWORD
         BZ    OPENDQ              BRANCH IF END OF JQES
         SLL   R2,2                EXPAND TO FULL ADDRESS OFFSET
         AL    R2,$JOBQPTR         ADD JOB QUEUE BASE
         USING JQEDSECT,R2         ACTIVATE JQE ADDRESSABILITY
         TM    JQEFLAGS,QUEBUSY    IS THIS USER ACTIVE
         BZ    OPJQELP             BRANCH IF NO - SKIP
         CLC   JQEJNAME(7),JCTTSUID IS THIS THE RIGHT USER
         BNE   OPJQELP             BRANCH IF NO - SKIP
         MVC   PCEWA(1),JQEFLAGS   COPY JQE BUSY FLAGS
         NI    PCEWA,QUEBUSY       ISOLATE BUSY FLAGS
         CLC   PCEWA(1),$SIDBUSY   IS THE JOB BUSY ON THIS SYSTEM
         BE    OPTSWTO             BRANCH IF YES
         SPACE 1                                                     R4
         DROP  R2                  SUSPEND JQE ADDRESSABILITY
         SLR   R2,R2               CLEAR REGISTER 2
         IC    R2,PCEWA            COPY BUSY FLAGS
         BCTR  R2,0                DECREMENT BY 1
         LA    R3,1                SETUP FOR BUSY TO AFFINITY SHIFT
         SLL   R3,0(R2)            CREATE AFFINITY BIT
         L     R1,PCEJQE           ADDR OF CURRENT JOB'S JQE   @OZ69909
         SPACE 1                                               @OZ69909
         USING JQEDSECT,R1         ACTIVATE JQE ADDRESSABILITY
         SPACE 1                                               @OZ69909
         TM    JQEFLAGS,QUEPURGE+QUEOPCAN TEST FOR $CJ,P       @OZ69909
         BNO   OPUPAFF             NO, ALTER AFF AND QPUT      @OZ69909
         SPACE 1                                               @OZ69909
OPTSAFF  MH    R2,=AL2(QSELEN)     TIMES ELEMENT SIZE          @OZ69909
         L     R15,$QSE1           LOAD ADDR OF IST QSE        @OZ69909
         ALR   R2,R15              ADD 1ST QSE PLUS DISP       @OZ69909
         TM    QSESTAT-QSEDSECT(R2),QSEACTIV SYSTEM ACTIVE..   @OZ69909
         BZ    OPNOTX              NO, BYPASS MESSAGE          @OZ69909
         SPACE 1                                               @OZ69909
OPUPAFF  NI    JQEFLAG2,FF-QUESYSAF  RESET CURRENT AFFINITY    @OZ69909
         STC   R3,PCEWA            STORE NEW AFFINITY
         OC    JQEFLAG2,PCEWA      MOVE IN NEW AFFINITY
        $QPUT  (R1),$OUTPUT        REQUEUE JQE TO $OUTPUT
        $#JCT  FREE                RELEASE JCT BUFFER
        $DORMANT                   INDICATE PROCESSOR INACTIVE
         B     HASPHOPE            TRY TO SELECT OTHER WORK
*              THIS LINE DELETED BY APAR OZ69909               @OZ69909
         SPACE 1                                                     R4
OPENDQ   DS    0H
         CLI   JCTTSUAF,0          SEE IF TSO SOURCE AFFINITY       R41
         BE    OPTSWTO               PRESENT, BRANCH IF NOT         R41
         CLC   JCTTSUAF,$SIDAFF    SOURCE AFFINITY FOR THIS SYSTEM
         BE    OPTSWTO             BRANCH IF YES
         IC    R3,JCTTSUAF         SELECT SOURCE AFFINITY
         L     R1,PCEJQE           ADDR OF CURRENT JOB'S JQE   @OZ69909
         TM    JQEFLAGS,QUEPURGE+QUEOPCAN TEST FOR $CJ,P       @OZ69909
         BNO   OPUPAFF             NO, ALTER AFF AND QPUT      @OZ69909
         LR    R15,R3              LOAD SOURCE AFFINITY        @OZ69909
         SLR   R2,R2               ZERO INCREMENT REG          @OZ69909
         LA    R2,1(,R2)           COUNT 1 FOR EACH AFF BIT    @OZ69909
         SRA   R15,1               SHIFT OUT ONE BIT           @OZ69909
         BNZ   *-8                 AND CNT FOR EACH BIT MOVED  @OZ69909
         BCTR  R2,0                LESS ONE FOR DISPLACEMENT   @OZ69909
         B     OPTSAFF             ALTER AFFINITY AND $QPUT
         DROP  R1                  SUSPEND JQE ADDRESSABILITY  @OZ69909
         EJECT                                                       R4
*
*        TS USER BUSY ON THIS SYSTEM - USE $WTO TO NOTIFY
*
OPTSWTO  DS    0H
         MVC   M165LOC,MSG165      SET BASIC MESSAGE FORMAT          R4
         MVC   M165JNAM,JCTJNAME   INSERT JOBNAME                    R4
         MVC   M165JBID,JCTJOBID     AND JOB NUMBER                  R4
         LA    R1,M165JNAM-1       SET PTR TO JOBNAME - 1            R4
OPSCNJ   DS    0H
         LA    R1,1(,R1)           INCREMENT POINTER                 R4
         CLI   0(R1),C' '          IS THIS CHARACTER BLANK
         BNE   OPSCNJ              IF NOT TRY AGAIN
         TM    JCTJTFLG,JCTJTJF+JCTJTCF  ENDED BY CC SET
         BNO   OPENDMSG            IF NOT BRANCH TO END MESG
         CLI   JCTPSN1,C' '        IS STEPNAME BLANK
         BE    OPSCNJS2            IF YES SKIP STEPNAME              R4
         MVC   1(8,R1),JCTPSN1     INSERT STEPNAME
OPSCNJS1 DS    0H
         LA    R1,1(,R1)           INCREMENT POINTER                 R4
         CLI   0(R1),C' '          IS THIS CHARACTER BLANK
         BNE   OPSCNJS1            IF NOT TRY AGAIN
OPSCNJS2 DS    0H                                                    R4
         CLI   JCTPSN2,C' '        IS STEP NAME BLANK...             R4
         BE    OPSCNJS4            IF YES SKIP STEPNAME              R4
         MVC   1(8,R1),JCTPSN2     INSERT STEPNAME
OPSCNJS3 DS    0H                                                    R4
         LA    R1,1(,R1)           INCREMENT POINTER                 R4
         CLI   0(R1),C' '          IS THIS CHARACTER BLANK
         BNE   OPSCNJS3            IF NOT TRY AGAIN                  R4
OPSCNJS4 DS    0H                                                    R4
         MVC   1(9,R1),=C'ENDED CC='  SET REASON                     R4
         LA    R1,10(,R1)          SET PTR TO CONDITION CODE         R4
         LH    R2,JCTJTCC          GET CONDITION CODE
         CVD   R2,0(,R1)           CONVERT TO DECIMAL                R4
         UNPK  0(4,R1),0(8,R1)     UNPACK FOUR LOW DIGITS
         OI    3(R1),X'F0'         SET ZONE TO X'F'
         LA    R1,4(,R1)           SET POINTER TO NEXT FIELD         R4
         B     OPLOUSM2            SET LOGON USERID MESSAGE          R4
         EJECT                                                       R4
OPENDMSG DS    0H
         MVC   1(5,R1),=C'ENDED'   SET ENDED MESSAGE
         LA    R1,6(,R1)           SET PTR TO NEXT FIELD             R4
         SPACE 1                                               @OZ74919
*************************************************************  @OZ74919
*                                                           *  @OZ74919
*        SET JCL ERROR MESSAGE AND REMOVE APOSTROPHES IN    *  @OZ74919
*        THE JOBNAME TO AVOID TRUNCATION OF MESSAGE.        *  @OZ74919
*                                                           *  @OZ74919
*************************************************************  @OZ74919
         SPACE 1                                               @OZ74919
         CLI   JCTJTFLG,JCTJTJF    IS IT JCL ERROR
         BE    OPJCLMSG            YES, SET JCL MESSAGE        @OZ74919
         OC    JCTCNVRC,JCTCNVRC   IS IT JCL ERROR
         BZ    OPJOBCAN            NO,SEE IF CANCELED          @OZ74919
         SPACE 1                                               @OZ74919
OPJCLMSG MVC   0(11,R1),=C'- JCL ERROR'  SET REASON            @OZ74919
         LA    R2,M165JNAM-1       LOAD ADDRESS OF JOB NAME    @OZ74919
         LA    R0,M165JNAM+7       LOAD ADDR OF LAST CHAR      @OZ74919
         SPACE 1                                               @OZ74919
OPEDJBNM LA    R2,1(,R2)           ADDRESS NEXT CHARACTER      @OZ74919
         CR    R2,R0               PAST THE END OF JOBNAME     @OZ74919
         BH    OPLOUSM              YES, SET LOGON USERID MSG  @OZ74919
         CLI   0(R2),C''''         IS CHARACTER AN APOSTROPHE  @OZ74919
         BNE   OPEDJBNM             BR IF NOT AN APOSTROPHE    @OZ74919
         MVI   0(R2),C'"'          SUBSTITUTE WITH QUOTE       @OZ74919
         B     OPEDJBNM            BR TO EDIT NEXT CHARACTER   @OZ74919
         SPACE 1                                               @OZ74919
*************************************************************  @OZ74919
*                                                           *  @OZ74919
*        SET CANCEL MESSAGE                                 *  @OZ74919
*                                                           *  @OZ74919
*************************************************************  @OZ74919
         SPACE 1                                               @OZ74919
OPJOBCAN L     R2,PCEJQE           ADDRESS CURRENT JQE         @OZ74919
         USING JQEDSECT,R2        ESTABLISH JQE ADDRESSABILITY
         TM    JQEFLAGS,QUEOPCAN+QUEPURGE $CJ OR $PJ ISSUED..  @OZ60576
         BZ    OPTABND                     NO, TEST ABEND      @OZ60576
         TM    JQEFLAGS,QUEOPCAN   $CJ ISSUED ...              @OZ60576
         BO    OPTCANMS             YES, ISSUE CANCEL MSG      @OZ60576
         CLC   JCTXEQON,$ZEROES    $PJ ISSUED IN INPUT ...     @OZ60576
         BNE   OPTABND              NO, CHECK IF ABEND         @OZ60576
OPTCANMS MVC   0(11,R1),=C'- CANCELLED' SET REASON             @OZ60576
         B     OPLOUSM             SET LOGON USERID
OPTABND  TM    JCTJTFLG,JCTJTABD   WAS JOB ABENDED             @OZ60576
         BZ    OPLOUSM2            NO, SET LOGON USERID MESSAGE      R4
         MVC   0(11,R1),=C'- ABENDED  '   SET REASON
OPLOUSM  DS    0H                                                    R4
         LA    R1,11(,R1)          INCREMENT PTR TO NEXT FIELD       R4
OPLOUSM2 DS   0H                                                     R4
         MVC   0(14,R1),=C''',LOGON,USER=('  SET USER ID             R4
         LA    R1,14(,R1)          INCREMENT POINTER                 R4
         MVC   0(7,R1),JCTTSUID    SET USERID
OPSCNUID DS    0H
         LA    R1,1(,R1)           INCREMENT POINTER                 R4
         CLI   0(R1),C' '          WAS THIS CHARACTER A BLANK
         BNE   OPSCNUID            IF NOT TRY NEXT
         MVI   0(R1),C')'          TERMINATE USERID WITH ')'
        $WTO   M165LOC,L'MSG165,JOB=NO,TYPE=SVC34 ISSUE 'SEND' CMD   R4
*              THIS LINE HAS BEEN DELETED BY OZ60576           @OZ60576
         EJECT                                                       R4
***********************************************************************
*                                                                     *
*        INITIALIZE PRIMARY PDDB TO JOE CONVERSION LOOP               *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
OPNOTX   DS    0H
         L     R1,PCEJQE           ADDRESS OF JQE              @OZ60576
         TM    QUEFLAGS(R1),QUEPURGE IF $CJ,P ISSUED           @OZ60576
         BO    OPQPUT                 BYPASS JOE CREATION      @OZ60576
         MVC   OPJOBCPY,JCTCPYCT   JOB LEVEL COPY COUNT
         CLI   OPJOBCPY,X'00'      IS THE JOB COPY COUNT ZERO
         BNE   *+8                 BRANCH IF NO
         MVI   OPJOBCPY,X'01'      FORCE COUNT TO ONE
         CLC   OPJOBCPY,$JCOPYLM   TOO MANY JOB COPIES REQUESTED     R4
         BNH   SKIP70              BR IF NO                          R4
         MVC   OPJOBCPY,$JCOPYLM   USE MAXIMUM                       R4
SKIP70   MVC   OPMSGCLS,JCTMCLAS   JOB MESSAGE CLASS
         MVC   OPJBKEY,JCTJBKEY    HOLD JOB KEY FOR VALIDITY CHECK
*
*        READ CHAIN OF IOT'S
*
         SLR   R3,R3               CLEAR PREVIOUS BUFFER POINTER
        $GETBUF WAIT=YES           GET BUFFER FOR IOT                R4
         ST    R1,OPIOTBUF         SAVE IOT BUFFER ADDRESS           R4
         LA    R2,1(0,0)           START COUNT OF IOT'S        @OZ56154
         L     R0,JCTIOT           GET ADDRESS OF 1ST REGULAR IOT
OPIOTRD  LR    JCT,R1              LOAD BASE FOR IOT                 R4
         USING IOTDSECT,JCT        ACTIVATE IOT ADDRESSABILITY
         MVI   PCEDEVTP,PCEDARD    INDICATE READ OPERATION
         BAL   RNP,OPIOCK          CALL I/O AND CHECK ROUTINE
         BZ    *+10                BRANCH IF GOOD READ
         XC    IOTJBKEY,IOTJBKEY   ZERO IOT JOB KEY
         CLC   IOTJBKEY,OPJBKEY    IS THIS IOT VALID
         BE    IOTOK               BRANCH IF YES
*
*        IOT JOB KEY DOES NOT MATCH JCT JOB KEY
*
OPIOTER  $DISTERR                  INDICATE CONTROL BLOCK ERROR
        $FREEBUF (JCT)             FREE BUFFER OF INVALID IOT
         LTR   JCT,R3              IS AT LEAST 1 IOT VALID
         BNZ   PDBSCAN             PROCESS WHAT IS LEFT
         ST    R3,OPIOTBUF         SET BUFFER ADDRESS TO ZERO
         B     OPQPUT              TERMINATE
         EJECT                                                       R4
***********************************************************************
*                                                                     *
*        IOT JOB KEY MATCHES JCT JOB KEY                              *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
IOTOK    DS    0H
         ICM   R3,15,IOTIOTTR      LOAD AND TEST NEXT IOT TRACK      R4
         BZ    PDBSCAN             BRANCH IF NO MORE IOT'S
*              THIS LINE DELETED BY APAR                     * @OZ69283
        $GETBUF PDDNOBUF           GET BUFFER FOR IOT          @OZ56154
         LA    R2,1(,R2)           INCREMENT COUNT OF IOT'S    @OZ56154
         ST    R1,IOTIOT           CHAIN TO PREVIOUS IOT
         LR    R0,R3               TTR FOR OPIOCK                    R4
         LR    R3,JCT              HOLD PREVIOUS IOT BASE
         B     OPIOTRD             CONTINUE READING IOT'S
         SPACE 1                                               @OZ56154
*************************************************************  @OZ56154
*                                                           *  @OZ56154
*    NO BUFFERS AVAILABLE - NOTIFY OPERATOR AND HOLD JOB    *  @OZ56154
*                                                           *  @OZ56154
*************************************************************  @OZ56154
         SPACE 1                                               @OZ56154
PDDNOBUF LR    R1,R10              RESTORE POINTER TO IOT      @OZ56154
         ICM   R0,15,IOTIOTTR      GET TTR OF NEXT IOT         @OZ56154
         BZ    PDDNBMSG            BRANCH IF ALL READ          @OZ56154
         LA    R2,1(,R2)           ADD THIS IOT TO COUNT       @OZ56154
         MVI   PCEDEVTP,PCEDARD    REQUEST READ OPERATION      @OZ56154
         BAL   R7,OPIOCK           CALL FOR I/O AND CHECK      @OZ56154
         BZ    *+10                BRANCH IF GOOD READ         @OZ56154
         XC    IOTJBKEY,IOTJBKEY   ZERO KEY FIELD              @OZ56154
         CLC   IOTJBKEY,OPJBKEY    IS IOT VALID                @OZ56154
         BE    PDDNOBUF            BRANCH IF SO                @OZ56154
         SPACE 1                                               @OZ56154
PDDRDERR $DISTERR                  INDICATE ERROR              @OZ56154
         SPACE 1                                               @OZ56154
PDDNBMSG XC    IOTIOT,IOTIOT       SET LAST INCORE IOT         @OZ69283
         L     R10,OPJCTBUF        POINT TO JCT                @OZ69283
         PUSH  USING               SAVE CURRENT USING          @OZ56154
         USING JCTDSECT,R10                                    @OZ56154
         LA    R1,JCTWORK          POINT TO JCTWORK            @OZ56154
         USING JCTWORK,R1                                      @OZ56154
         MVC   JCTWORK(L'MSG183),MSG183  MOVE MESSAGE          @OZ69283
         CVD   R2,$DOUBLE          READY COUNT FOR PRINT       @OZ56154
         UNPK  $DOUBLE(5),$DOUBLE+5(3) UNPACK BUFFER COUNT     @OZ56154
         OI    $DOUBLE+4,X'F0'     MAKE PRINTABLE              @OZ56154
         MVC   BUFREQ-MSG183(,R1),$DOUBLE+1 INSERT IN MESSAGE  @OZ56154
         LH    R2,$NUMBUF          GET NUMBER OF BUFFERS       @OZ56154
         CVD   R2,$DOUBLE          READY COUNT FOR PRINT       @OZ56154
         UNPK  $DOUBLE(5),$DOUBLE+5(3) UNPACK BUFFER COUNT     @OZ56154
         OI    $DOUBLE+4,X'F0'     MAKE PRINTABLE              @OZ56154
         MVC   BUFDEF-MSG183(,R1),$DOUBLE+1 INSERT IN MESSAGE  @OZ56154
         SPACE 1                                               @OZ56154
        $WTO   JCTWORK,L'MSG183,JOB=YES,ROUTE=$MAIN+$LOG,      @OZ56154X
               CLASS=$ACTION                                   @OZ56154
         SPACE 1                                               @OZ56154
        $#JCT  FREE                RELEASE JCT BUFFER          @OZ56154
         SPACE 1                                               @OZ56154
        $QSUSE                     ENQUEUE ON SHARED RESOURCE  @OZ56154
         L     R1,PCEJQE           ADDRESS JQE                 @OZ56154
         OI    JQEFLAGS-JQEDSECT(R1),QUEHOLD1   MARK JOB HELD  @OZ56154
         NI    JQEFLAGS-JQEDSECT(R1),FF-QUEBUSY  & NOT BUSY    @OZ56154
         SPACE 1                                               @OZ56154
        $QCKPT (R1)                REQUEST CHECKPOINT          @OZ56154
         B     OPTERM              GO TO RELEASE IOT'S         @OZ56154
         POP   USING                                           @OZ56154
         EJECT                                                 @OZ56154
***********************************************************************
*                                                                     *
*        ALL VALID IOTS HAVE BEEN READ - START PDDB SCAN              *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
PDBSCAN  DS    0H
         XC    IOTIOT,IOTIOT       ZERO CHAIN POINTER OF LAST IOT
         XC    OPCKPT,OPCKPT       ZERO CHECKPOINT JOE INDEX
         L     JCT,OPIOTBUF        ADDRESS IOT BUFFER
PDBIOT   DS    0H
         L     R2,$IOTPDDB         POINT TO                          R4
         ALR   R2,JCT               1ST PDDB IN IOT                  R4
         USING PDBDSECT,R2         ACTIVATE PDDB ADDRESSABILITY
         L     R3,IOTPDDBP         OFFSET AFTER LAST PDDB
         AR    R3,JCT              ADD CURRENT BUFFER ORIGIN
         ST    R3,OPDBEND          SAVE IN PCE
PDBNEXT  DS    0H
         XC    OPDDB,OPDDB         CLEAR RESTART POINTER
         C     R2,OPDBEND          END OF PDDBS ID THIS IOT
         BNE   PDBPDB              BRANCH IF NO
         ICM   JCT,15,IOTIOT       LOAD AND TEST IOT CHAIN
         BZ    OPQPUT              BRANCH IF NO MORE IOTS
         B     PDBIOT              SETUP NEW IOT ADDRESSES
PDBPDB   DS    0H
         TM    PDBFLAG1,PDB1NULL+PDB1NSOT IS PDDB NULL/NOT SYSOUT
         BZ    PDBJOE              BRANCH IF NO - BUILD JOES
         LA    R2,PDBLENG(,R2)     STEP TO THE NEXT PDDB
         B     PDBNEXT             CONTINUE SCAN
         EJECT                                                       R4
***********************************************************************
*                                                                     *
*        BUILD CHAR-JOE AND WORK-JOE FROM PDDB                        *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
PDBJOE   MVC   OPCLASS,PDBCLASS    GET SYSOUT CLASS FROM PDDB        R4
         USING JOEDSECT,JOE        ACTIVATE JOE ADDRESSABILITY
         L     R3,PCEJQE           ADDRESS OF JQE              @OZ32566
         SL    R3,$JOBQPTR         MINUS JOB QUEUE ORIGIN
         LA    JOE,OPCHAR          ADDRESS CHAR-JOE
        $#BLD  JOES=OPWORK,PDDB=(R2),JQE=(R3) CONVERT PDDB TO JOES   R4
         SPACE 1                                                     R4
         OI    PDBFLAG1,PDB1NULL   SHOW 1ST PDDB PROCESSED          R41
         SPACE 1                                                     R4
***********************************************************************
*                                                                     *
*        SCAN REMAINING PDDB'S FOR A CHARACTERISTICS MATCH            *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
DDBNEXT  DS    0H
         LA    R2,PDBLENG(,R2)     STEP TO THE NEXT PDDB
DDBIOT   DS    0H
         CL    R2,OPDBEND          END OF PDDB'S IN THIS IOT
         BNE   DDBPDB              BRANCH IF NO
         ICM   JCT,15,IOTIOT       LOAD AND TEST IOT CHAIN
         BZ    DDBEND              BRANCH IF NO MORE IOTS
         L     R2,$IOTPDDB         POINT TO                          R4
         ALR   R2,JCT               1ST PDDB IN IOT                  R4
         L     R3,IOTPDDBP         OFFSET AFTER LAST PDDB
         ALR   R3,JCT              ADD BUFFER ORIGIN
         ST    R3,OPDBEND          SAVE IN PCE
         B     DDBIOT              PROCESS PDDBS IN THIS IOT
DDBPDB   DS    0H
         TM    PDBFLAG1,PDB1NULL+PDB1NSOT IS PDDB NULL/NOT SYSOUT
         BNZ   DDBNEXT             BRANCH IF YES
*
*        CHECK SYSOUT CLASS
*
         CLC   PDBCLASS,OPCLASS    DOES SYSOUT CLASS MATCH
         BNE   DDBSTEP             BRANCH IF NO
         EJECT                                                      R41
*
*        CHECK REMAINING CHARACTERISTICS
*
         LA    JOE,OPWORK          ADDRESS WORK JOE                  R4
         TM    JOEFLAG2,$JOEDMND   TEST FOR DEMAND-SETUP OPTION      R4
         LA    JOE,OPCHAR          ADDRESS CHAR-JOE
         BZ    SKIP80              SKIP CLASS TEST IF NOT CHOSEN     R4
         CLC   PDBCLASS,OPMSGCLS   DOES SYSOUT CLASS = MSGCLASS
         BE    OPDEMAND            BRANCH IF YES - DEMAND SETUP
SKIP80   CLC   JOEFORM(12),PDBFORMS CHECK FORMS, FCB, UCS
         BNE   DDBSTEP             BRANCH IF NOT MATCHING
         CLC   JOEFLASH,PDBFLASH   CHECK FLASH FRAME ID              R4
         BNE   DDBSTEP             BRANCH IF NOT MATCHING            R4
         TM    PDBFLAG2,PDB2BRST   VERIFY                            R4
         BZ    SKIP90               THAT                             R4
         TM    JOECFLAG,$JOEBRST     PDDB                            R4
         BZ    DDBSTEP                BURSTER                        R4
         B     OPDEMAND                SPECIFICATION                 R4
SKIP90   TM    JOECFLAG,$JOEBRST        MATCHES                      R4
         BO    DDBSTEP                   CHAR-JOE                    R4
         SPACE 1                                                    R41
OPDEMAND CLC   JOEWTRID,PDBWTRID   CHECK EXTERNAL WRITER NAME       R41
         BNE   DDBSTEP             BRANCH IF NOT MATCHING
         LA    JOE,OPWORK          ADDRESS WORK-JOE
         CLC   JOEDEST,PDBDEST     CHECK DESTINATION
         BNE   DDBSTEP             BRANCH IF NOT MATCHING
         SPACE 1                                                    R41
OPMATCH  SLR   R0,R0               CLEAR WORK REGISTER              R41
         SLR   R1,R1               CLEAR COPY-GROUPS SUM            R41
         LA    R5,8                SET MAX COPY-GROUPS              R41
         SPACE 1                                                    R41
OPCOPLUP IC    R0,PDBCOPYG-1(R5)   COMPUTE TOTAL                    R41
         ALR   R1,R0                COPY COUNT OF                   R41
         BCT   R5,OPCOPLUP           3800 COPY-GROUPS               R41
         CLM   R1,1,PDBCOPYS       USE NORMAL                       R41
         BNL   OPRECCT              COPY COUNT                      R41
         IC    R1,PDBCOPYS           IF LARGER                      R41
         SPACE 1                                                    R41
OPRECCT  L     R0,PDBRECCT         ADD LINE/CARD                    R41
         MR    R0,R0                COUNT OF PDDB                   R41
         AL    R1,JOERECCT           TO WORK-JOE                    R41
         ST    R1,JOERECCT            TOTAL                         R41
         OI    PDBFLAG1,PDB1NULL   SET PDDB PROCESSED BIT
         B     DDBNEXT             GO PROCESS NEXT PDDB
         SPACE 1                                                    R41
***********************************************************************
*                                                                     *
*        CHARACTERISTICS DO NOT MATCH -- CHECK NEXT PDDB              *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
DDBSTEP  OC    OPDDB,OPDDB         RESTART POINT SELECTED...        R41
         BNZ   DDBNEXT             BRANCH IF YES
         ST    R2,OPDDB            SET PDDB AS RESTART POINT
         ST    JCT,OPIOT           AND SAVE IOT BUFFER ADDRESS
         B     DDBNEXT             CONTINUE SCAN OF PDDB'S
         EJECT                                                      R41
***********************************************************************
*                                                                     *
*        ALL MATCHING PDDB'S HAVE BEEN PROCESSED                      *
*                                                                     *
*        ADD A WORK-JOE FOR EACH JOB COPY                             *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
DDBEND   SLR   RNP,RNP             GET JOB LEVEL                    R41
         IC    RNP,OPJOBCPY         COPY COUNT                      R41
         SPACE 1                                                    R41
OPJCOPY  LH    R5,OPCKPT           GET INDEX OF LAST JOE ADDED      R41
         LA    R5,1(,R5)           INCR FOR THIS JOE                R41
         CH    R5,QSEOPCKP         WAS THIS JOE ALREADY ADDED...    R41
         BNH   OPSKIP              BR IF YES                        R41
         SPACE 1                                                    R41
         USING JQEDSECT,R1         PROVIDE JQE ADDRESSABILITY       R41
         SPACE 1                                                    R41
OPJOTADD L     R1,PCEJQE           ADDRESS OF JQE              @OZ32566
         TM    JQEFLAGS,QUEPURGE   HAS $CJOB BEEN ISSUED...         R41
         BO    OPQPUT              BR IF YES                        R41
         SPACE 1                                                    R41
         DROP  R1                  DROP JQE ADDRESSABILITY          R41
         SPACE 1                                                    R41
         LA    R0,OPWORK           PROTOTYPE WORK-JOE               R41
         LA    R1,OPCHAR           PROTOTYPE CHAR-JOE               R41
        $#ADD  WORK=(R0),CHAR=(R1) ADD WORK TO JOT                  R41
         BZ    OPADOK              BR IF ADD SUCCESSFUL             R41
        $DORMANT                   ALLOW $PJES2 WHILE $WAITING      R41
        $WAIT  JOT                 WAIT FOR JOT SERVICE             R41
        $ACTIVE                    SHOW HASPHOPE ACTIVE AGAIN       R41
         B     OPJOTADD            TRY AGAIN                        R41
         SPACE 1                                                    R41
OPADOK   STH   R5,QSEOPCKP         SAVE COUNT FOR WARM START        R41
         SPACE 1                                                    R41
OPSKIP   STH   R5,OPCKPT           SAVE COUNT IN PCE                R41
         BCT   RNP,OPJCOPY         ADD A JOE FOR ALL JOB COPIES     R41
         EJECT                                                      R41
***********************************************************************
*                                                                     *
*        SELECT PDDB WITH NEW CHARACTERISTICS                         *
*                                                                     *
***********************************************************************
         SPACE 1                                                    R41
         ICM   R2,15,OPDDB         IS RESTART ADDRESS 0
         BZ    OPQPUT              BRANCH IF YES
         L     JCT,OPIOT           RESTART IOT BASE
         L     R3,IOTPDDBP         OFFSET AFTER LAST PDDB
         ALR   R3,JCT              ADD BUFFER ORIGIN
         ST    R3,OPDBEND          SAVE IN PCE
         B     PDBNEXT             CONTINUE PDDB SCAN
         SPACE 3                                                    R41
***********************************************************************
*                                                                     *
*        ALL PDDBS HAVE BEEN PROCESSED - MAKE JOB AVAILABLE           *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
OPQPUT   DS    0H
         L     JCT,OPJCTBUF        ADDRESS JCT IN BUFFER
         USING JCTDSECT,JCT        ACTIVATE JCT ADDRESSABILITY
         MVC   JCTOUTON(8),OPTIMEON HOPE SIGN-ON TIME/DATE
        $TIME                      SIGN-OFF TIME/DATE
         STM   R0,R1,JCTOUTOF      HOPE SIGN-OFF TIME/DATE
         MVC   JCTOTSID,$SID       SET SID FOR TYPE 26 SMF RECORD
        $#JCT  WRITE               COPY JCT TO SPOOL
        $#JCT  FREE                RELEASE JCT BUFFER
         EJECT                                                      R41
***********************************************************************
*                                                                     *
*        IF NO JOES OR HQRS MOVE JOB TO $PURGE, ELSE $HARDCPY         *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
OPNOJCT  DS    0H
         L     R1,PCEJQE           ADDRESS OF JQE              @OZ32566
         LA    R0,$PURGE           SET NEXT QUEUE AS $PURGE
         USING JQEDSECT,R1         ACTIVATE JQE ADDRESSABILITY
        $QSUSE                     REQUEST ACCESS TO CHECKPOINT DATA
         CLC   JQEJOE,$ZEROS       ANY NON-HELD OUTPUT...      @OZ27300
         BNE   *+14                BR IF YES                   @OZ27300
         CLC   JQEHLDCT,$ZEROS     ANY HELD OUTPUT...          @OZ27300
         DROP  R1                  SUSPEND JQE ADDRESSABILITY
         BE    *+8                 BR IF NO                    @OZ27300
         LA    R0,$HARDCPY         SET NEXT QUEUE AS $HARDCPY
        $QPUT  (R1),(R0)           MOVE JOB TO NEXT QUEUE
         MVC   PCEJQE,$ZEROS       CLEAR JQE ADDRESS           @OZ32566
         EJECT                                                       R4
         SPACE 3                                                     R4
***********************************************************************
*                                                                     *
*        FREE CHAIN OF IOT BUFFERS USED DURING JOE CREATION           *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
OPTERM   DS    0H
         USING IOTDSECT,JCT        ACTIVATE IOT ADDRESSABILITY
         ICM   JCT,15,OPIOTBUF     LOAD AND TEST BUFFER POINTER
         BZ    OPPURGE             BRANCH IF ZERO
         MVC   OPIOTBUF,IOTIOT     COPY NEXT BUFFER POINTER
        $FREEBUF (JCT)             FREE IOT BUFFER CHAIN
         B     OPTERM              CYCLE THROUGH ALL IOT BUFFERS
OPPURGE  DS    0H
         SLR   R0,R0               CLEAR REGISTER
         STH   R0,QSEOPJNO         CLEAR JOB NUMBER
         STH   R0,QSEOPCKP         CLEAR PARTIAL JOE COUNT
        $DORMANT                   INDICATE PROCESSOR DORMANT
         B     HASPHOPE            TRY TO GET ANOTHER JOB
         DROP  JOE                 SUSPEND JOE ADDRESSABILITY
         TITLE 'HASP OUTPUT PROCESSOR -- BUFFER READ/WRITE AND CHECK SUC
               BROUTINE'                                             R4
***********************************************************************
*                                                                     *
*        BUFFER READ/WRITE AND CHECK SUBROUTINE                       *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
OPIOCK   DS    0H
         USING BUFDSECT,R1         ACTIVATE BUFFER ADDRESSABILITY
         LA    R15,IOBCCW1         RESET IOBSTART
         ST    R15,IOBSTART         TO FIRST CCW IN CHAIN
         DROP  R1                  SUSPEND BUFFER ADDRESSABILITY
         ST    R1,PCEBUFAD         STORE BUFFER ADDRESS IN DCT
         ST    R0,PCESEEK          STORE TRACK ADDRESS IN DCT
         LA    R1,PCEDADCT         SETUP DCT ADDRESS FOR $EXCP
        $EXCP  (R1),WAIT=YES       INITIATE I/O AND WAIT             R4
         BMR   RNP                 RETURN IF ERROR WITH NON-ZERO CC  R4
         SR    R15,R15             SET GOOD RETURN CODE
         BR    RNP                 RETURN TO CALLER
         DROP  BASE2               SUSPEND LOCAL ADDRESSABILITY
         TITLE 'HASP OUTPUT PROCESSOR -- LITERAL POOL AND CONSTANTS' R4
         LTORG
         SPACE 2                                                     R4
        $MID   165                 GENERATE MESSAGE ID
MSG165   DC    CL87'IDSE ''&MID.JOBNNNNN  JOBNAMES'            @OZ49921
         ORG   MSG165
         DC    X'165F'             MESSAGE ID
         ORG   ,
         SPACE 1                                                     R4
M165LOC  EQU   JCTWORK,L'MSG165    LOCATION FOR MESSAGE              R4
M165JNAM EQU   M165LOC+25,8        LOCATION FOR JOB NAME             R4
M165JBID EQU   M165LOC+15,8        LOCATION FOR JOBID                R4
         SPACE 2                                               @OZ56154
MSG183   $MSG  183,' HELD, BUFFER SHORTAGE DURING OUTPUT - NNNN REQUIREX
               D, NNNN DEFINED'                                @OZ69283
         SPACE 1                                               @OZ56154
BUFREQ   EQU   MSG183+41,4                                     @OZ69283
BUFDEF   EQU   MSG183+56,4                                     @OZ69283
         TITLE 'HASP PRINT/PUNCH SERVICE PROCESSOR'
***********************************************************************
*                                                                     *
*              PRINT/PUNCH REGISTER DEFINITIONS                       *
*                                                                     *
***********************************************************************
         SPACE 3
PW       EQU   WA                  WORK REGISTER
PBUF     EQU   WB                  BUFFER POINTER
PC1      EQU   WC                  CCW REGISTER 1                    R4
PC2      EQU   WD                  CCW REGISTER 2
BASE4    EQU   WE                  PRPU THIRD LOCAL BASE REGISTER    R4
PL       EQU   WF                  INTERNAL LINKAGE REGISTER
         SPACE 5
***********************************************************************
*                                                                     *
*              PPFLAG SWITCH DEFINITIONS                              *
*                                                                     *
***********************************************************************
         SPACE 3
PPWSW    EQU   X'80'               PRINT/PUNCH WRITE SWITCH
PPDELSW  EQU   X'40'               PRINT/PUNCH SUSPEND SWITCH
PPDALOC  EQU   X'20'               PRINT/PUNCH ALLOCATION IOT
PRDELSW  EQU   X'10'               PRINT/PUNCH TERMINATION SWITCH
PPFUNCI  EQU   X'08'               PUNCH INTERPRET REQUESTED
PPRDERR  EQU   X'04'               PRINT/PUNCH DATA READ ERROR
PPJCTIOT EQU   X'02'               PRINT/PUNCH JCT/IOT READ ERROR
PPNEWS   EQU   X'01'               PRINT JES2-NEWS PROCESS SWITCH   R41
         EJECT                                                 @OZ19494
***********************************************************************
*                                                                     *
*              PPFLAG2 SWITCH DEFINITIONS                             *
*                                                                     *
***********************************************************************
         SPACE 2                                                     R4
PPTCEL   EQU   X'80'               TRACK-CELL DE-SPOOLING SWITCH     R4
PPRSW    EQU   X'40'               PRINT/PUNCH READ SWITCH           R4
PPCKPT   EQU   X'20'               PRINT/PUNCH CKPT-NEEDED SWITCH    R4
PPCKPTA  EQU   X'10'               PRINT/PUNCH CKPT-ALLOWED SWITCH   R4
PPCIWAIT EQU   X'08'               PRINT/PUNCH PCI WAIT SWITCH       R4
PPOPTJ   EQU   X'04'               PRINTER OPTCD=J SWITCH            R4
PPFDS    EQU   X'02'               FIRST SYSOUT DATA SET SWITCH      R4
PSMFDSER EQU   X'01'               DATA BUFFER ERROR FLAG FOR SMF    R4
         SPACE 2                                               @OZ19494
************************************************************** @OZ19494
*                                                            * @OZ19494
*              PPFLAG3 SWITCH DEFINITIONS                    * @OZ19494
*                                                            * @OZ19494
************************************************************** @OZ19494
         SPACE 2                                               @OZ19494
PPTRUNC  EQU   X'80'               TRUNCATE OUTPUT             @OZ19494
PPDVNAVL EQU   X'20'               DEVICE NO LONGER AVAILABLE  @OZ51930
PP38CKPT EQU   X'10'               3800 CHECKPOINT FLAG        @OZ51592
PP3800S  EQU   X'08'               3800 REPOSITION BIT         @OZ51592
PPQSPND  EQU   X'04'               3800 PPQ SUSPEND BIT        @OZ48003
PP3800R  EQU   X'02'               3800 RESTART BIT            @G38ESBB
PPINIT   EQU   X'01'               FIRST USE BIT               @OZ34384
         SPACE 2                                               @OZ61672
***********************************************************************
*                                                                     *
*              PPFLAG4 SWITCH DEFINITIONS                             *
*                                                                     *
***********************************************************************
         SPACE 2                                               @OZ61672
PP38PSI  EQU   X'80'               3800 PATH SET INDICATOR     @OZ61672
PP3081D  EQU   X'40'               WCS PATH INDICATOR          @OZ61672
PPREPOBS EQU   X'10'               REPOSITION DUE TO BSPACE    @OZ78256
         SPACE 2                                               @OZ61672
         TITLE 'HASP PRINT/PUNCH SERVICE -- PROCESSOR INITIALIZATION'
***********************************************************************
*                                                                     *
*        HASP PRINT/PUNCH PROCESSOR -- MAIN ENTRY POINT               *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
HASPPPI1 $ENTRY BASE=(BASE2,BASE3,BASE4)  PRINT/PUNCH MAIN ENTRY     R4
         SPACE 3                                                     R4
         USING BUFDSECT,PBUF       ACTIVATE BUFFER ADDRESSABILITY
         USING JCTDSECT,JCT        ACTIVATE JCT ADDRESSABILITY
         USING DCTDSECT,R1         ACTIVATE DCT ADDRESSABILITY
         SPACE 3                                                     R4
         LA    BASE3,2048(,BASE2)  SETUP SECOND LOCAL
         LA    BASE3,2048(,BASE3)   BASE REGISTER
         LA    BASE4,2048(,BASE3)  SETUP THIRD LOCAL                 R4
         LA    BASE4,2048(,BASE4)   BASE REGISTER                    R4
         SPACE 1                                               @OZ19494
         L     R1,$HASPECB         INITIALIZE                  @OZ19494
         LA    R1,$SVBLANK(,R1)     CCWS REQUIRING             @OZ19494
         STCM  R1,7,PUCCWBL+1        BLANKS IN                 @OZ19494
         STCM  R1,7,PUSPACCW+1        FIXED-STORAGE            @OZ19494
         SPACE 3                                               @OZ19494
PRPUINIT DS    0H                                              @OZ19494
         XC    PBUFADDR,PBUFADDR   CLEAR PRIMARY BUFFER ADDRESS     R41
         XC    PDDBSKIP,PDDBSKIP   CLEAR REPOSITIONING COUNTERS FOR  R4
         XC    PBUFSKIP,PBUFSKIP    CARDS/LINES AND BUFFER           R4
         XC    PCKPT,PCKPT         CLEAR CHECKPOINT DATA AREA        R4
         XC    PPCKPTR,PPCKPTR     CLEAR CHECKPOINT DATA POINTER     R4
         SPACE 1                                                     R4
         MVC   PCEJQE,$ZEROS       CLEAR JQE ADDRESS           @OZ32566
         SPACE 1                                               @OZ19494
                                   PRINT OFF - SECTION DELETED @OZ19494
*                                  THIS CARD DELETED BY APAR   @OZ19494
                                   PRINT ON -- SECTION DELETED @OZ19494
         MVI   PSMFDCI,0           CLEAR SMF FLAGS             @OZ32566
         EJECT                                                       R4
***********************************************************************
*                                                                     *
*        TRY TO GET AN IDLE PRINTER OR PUNCH                          *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
PGETUNIT $GETUNIT PCEDCT           ATTEMPT ACQUIRE PRT/PUN DCT @OZ32566
         BNZ   PGOTUNIT            BR IF DEVICE AVAILABLE            R4
         SPACE 1                                                     R4
***********************************************************************
*                                                                     *
*        DEVICE NOT AVAILABLE -- ISSUE $WAIT                          *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         TM    PCEID,PCERJEID      TEST FOR REMOTE TERMINAL          R4
         BO    PRJEDEV             BR IF SO                          R4
         CLI   PDEVTYP3,UCB3800    IS DEVICE A 3800 PRINTER    @G38ESBB
         BNE   PWTUNIT             BR IF NOT                   @G38ESBB
         L     R1,PCEDCT           GET DCT ADDRESS             @G38ESBB
         TM    DCTSTAT-DCTDSECT(R1),DCTDRAIN TEST FOR $P DEV   @G38ESBB
         BZ    PWTUNIT             BR IF NOT                   @G38ESBB
         SPACE 1                                               @OZ49145
         USING PQHDSECT,PW         PROVIDE PQH ADDRESSABILITY  @OZ49145
         L     PW,PQHADR           GET PQH ADDRESS             @OZ49145
         OI    PQHFLAG,PQHDRAIN    INDICATE PPQ DRAIN          @OZ49145
         L     R15,=A(PPQMGR)      CALL PPQMGR FOR TOTAL       @OZ49145
         BALR  PL,R15               PURGE OF PPQ               @OZ49145
         DROP  PW                  SUSPEND PQH ADDRESSABILITY  @OZ49145
         SPACE 1                                               @G38ESBB
PWTUNIT $WAIT  UNIT,INHIBIT=NO     WAIT FOR U/R DEVICE         @G38ESBB
         B     PGETUNIT            THEN BR TO TRY AGAIN              R4
         SPACE 1                                                     R4
PRJEDEV $WAIT  WORK                $WAIT FOR RJE DEVICE              R4
         B     PGETUNIT            THEN BR TO TRY AGAIN              R4
         SPACE 1                                                     R4
***********************************************************************
*                                                                     *
*        GOT A PRINTER OR PUNCH -- SHOW PROCESSOR ACTIVE              *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
PGOTUNIT MVI   PPFLAG,0            CLEAR PRINT/PUNCH FLAGS          R41
         MVI   PPFLAG2,0           CLEAR PRINT/PUNCH FLAGS           R4
         NI    PPFLAG3,PPINIT      CLEAR ALL BUT PPINIT        @OZ41128
         CLI   DCTDEVTP,DCTPUN     IF DEVICE                         R4
         BNE   SKIP100              IS LOCAL PUNCH,                  R4
         XC    PULMTTR(7),PULMTTR  CLEAR PUNCH RESTART POINTER       R4
         SPACE 1                                                     R4
SKIP100  MVI   DCTFLAGS,0          RESET OPERATOR COMMANDS
        $ACTIVE R=PW               INDICATE PROCESSOR ACTIVE
         B     PTSTINIT            GO TEST FIRST USE OF UNIT   @OZ34870
         EJECT                                                       R4
***********************************************************************
*                                                                     *
*        FOR REMOTE PRINTERS -- PRINT ANY SPOOLED MESSAGES            *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
PTSTRMT  DS    0H                  *                           @OZ34870
         TM    PCEID,PCELCLID+PCEPUSID  TEST PROCESSOR TYPE
         BNZ   PGETJOB             BR IF NOT REMOTE PRINTER    @OZ34870
         TM    DCTPPSW,DCTPPSWS    SUPPRESS SEP PAGE
         BO    PGETJOB             BRANCH IF YES
         CLI   $SPOLMSG,0          IF NO MSG SPOOL BUFFERS,          R4
         BE    PGETJOB              BR TO GET A JOB                  R4
         CLI   DCTDEVTP,DCTRCON    IF NOT A CONSOLE DCT        @OZ26040
         BNE   PSNACHK               GO CHECK FOR SNA REMOTE   @OZ26040
PGETRAT  DS    0H                                              @OZ26040
         ICM   R1,7,DCTDCB+1       REMOTE STILL SIGNED ON...   @OZ31782
         BZ    PNOJOB              NO, FREE UP RESOURCES       @OZ31782
         L     R1,MDCTRAT            TO RAT                    @OZ26040
         B     PMSGS               GO CHECK FOR MESSAGES       @OZ26040
PSNACHK  DS    0H                                              @OZ26040
         TM    MDCTTYPE,DCTPLU1    IF NOT AN SNA REMOTE             R41
         BNO   PGETRAT             GET RAT ADDRESSABLILITY     @OZ26040
         ICM   R1,7,DCTDCB+1       REMOTE STILL SIGNED ON...   @OZ31782
         BZ    PNOJOB              NO, FREE UP RESOURCES       @OZ31782
         L     R1,MDCTRAT           TO RAT                          R41
         USING RATDSECT,R1         EST. RAT ADDRESSABILITY     @OZ26040
         TM    RATCONF,RATCONFC    IF RMT HAS A CONSOLE,       @OZ33415
         BO    PGETJOB               GO GET A JOB              @OZ26040
PMSGS    DS    0H                                                   R41
         $QSUSE                    REQUEST ACCESS TO SHARED Q  @OZ26040
         SLR   R14,R14               GET NUMBER OF REMOTE      @OZ26040
         IC    R14,RATCONRT+1          WITH CONSOLE            @OZ26040
         BCTR  R14,0                     LESS 1 FOR OFFSET     @OZ26040
         LR    R15,R14             SAVE REMOTE NUMBER (-1)     @OZ26040
         MH    R15,=Y(RATTLE)      CALCULATE RAT DISPLACEMENT  @OZ26040
         A     R15,$RATABLE        GET ADDRESS OF RAT ENTRY    @OZ26040
         CLR   R15,R1              IF CONSOLE AND PRINTER ARE  @OZ26040
         BE    PMSGDSP               SAME RMT, DE-SPOOL MSGS   @OZ26040
         L     R15,RATLDCT-RATDSECT(,R15) IF CONSOLE'S REMOTE  @OZ26040
         LTR   R15,R15             SIGNED ON THIS SYSTEM,      @OZ26040
         BNZ   PGETJOB               DON'T PRINT MESSAGES      @OZ26040
*              THIS LINE DELETED BY APAR NUMBER                @OZ45379
         LR    R15,R14             THREE TIMES                 @OZ26040
         ALR   R15,R14               REMOTE NUMBER             @OZ26040
         ALR   R15,R14                 MINUS ONE               @OZ26040
         SLR   R14,R14             COMPUTE BIT AND             @OZ26040
         D     R14,=F'8'             BYTE OFFSET               @OZ26040
         AL    R15,$RMTSON             TO REMOTE SIGNON BITS   @OZ26040
         ICM   R15,12,0(R15)       PICK UP BITS                @OZ26040
         SLL   R15,0(R14)           ISOLATE BITS FOR           @OZ26040
         SRL   R15,29                  THIS REMOTE             @OZ26040
         LTR   R15,R15             IF RMT SIGNED ON ANY SYSTM  @OZ26040
         BNZ   PGETJOB              THEN DON'T PRINT MESSAGES  @OZ26040
PMSGDSP  DS    0H                                              @OZ26040
         SLR   PBUF,PBUF           SET NO BUFFER INDICATION    @OZ26040
         SLR   JCT,JCT             SET NO JOB INDICATION       @OZ26040
         MVC   PPKEY,=C'MSPOOL'    SET SPECIAL JOB/DATA KEY    @OZ26040
         SLR   PW,PW               INITIALIZE                  @OZ26040
         BCTR  PW,0                 LINE COUNT
         ST    PW,PRLINECT           LIMIT
PRSMTEST DS    0H
*              THIS LINE DELETED BY APAR NUMBER                @OZ26040
*              THIS LINE DELETED BY APAR NUMBER                @OZ45379
         SLR   R15,R15             MULTIPLY                          R4
         IC    R15,RATCONRT+1       REMOTE                     @OZ45379
         LA    PW,0(R15,R15)         NUMBER                          R4
         ALR   PW,R15                 BY THREE                       R4
         USING DCTDSECT,R1         RESTORE DCT ADDRESSIBILITY  @OZ45379
         AL    PW,$MSPOOLQ         ADD MESSAGE QUEUE BASE ADDRESS    R4
         CLC   0(1,PW),1(PW)       PRINTED VERSUS WRITTEN QUEUES
         BE    PRMDONE             BRANCH IF NO SPOOL MESSAGES
        $GETBUF WAIT=YES           GET A DATA BUFFER                 R4
         ST    R1,PBUFSAVE         SAVE BUFFER ADDRESS
         ST    R1,PBUFADDR         SAVE BUFFER ADDRESS               R4
         LR    PBUF,R1             ESTABLISH BUFFER ADDRESSABILITY
         ST    PBUF,PINIOB         INITIALIZE INPUT IOB ADDRESS      R4
         MVI   PBUFOPT,1           FORCE SINGLE BUFFERING            R4
         EJECT                                                       R4
         L     R1,PCEDCT           RELOAD PRINTER DCT ADDRESS  @OZ32566
         TM    MDCTTYPE,DCTPSNA    TEST FOR SNA REMOTES              R4
         BNO   *+14                NO, SKIP---                       R4
         MVC   PRMTSSEL,MDCTSEL    SAVE RMT SELECT BYTE IN WORK AREA R4
         MVI   MDCTSEL,FMHCNSLE+DCTPOUTB USE OUTBOUND CONSOLE SELECT R4
        $EXTP  OPEN,(R1)           OPEN REMOTE FOR SPOOL MESSAGES    R4
         BZ    PRMFINI             OPEN FAILED, BR -- TERMINATE      R4
         SPACE 2                                                     R4
PRFNDMSG DS    0H                                                    R4
        $QSUSE                     REQUEST ACCESS TO SHARED QUEUES   R4
         L     R1,PCEDCT           LOAD DCT ADDR IN CASE DESTR @OZ32566
         TM    MDCTSTAT,DCTABORT   TEST DCT STATUS             @OZ59095
         BO    PRMDONE             BR IF ABORTING              @OZ59095
         ICM   R1,B'0111',DCTDCB+1 GET LINE DCT ADDRESS        @OZ45379
         BZ    PRMDONE             BRANCH IF NOT SIGNED ON     @OZ45379
         L     R1,MDCTRAT          GET RAT ADDRESS             @OZ45379
         SLR   R15,R15             MULTIPLY                          R4
         IC    R15,RATCONRT+1-RATDSECT(,R1)    REMOTE          @OZ45379
         LA    PW,0(R15,R15)         NUMBER                          R4
         ALR   PW,R15                 BY THREE                       R4
         AL    PW,$MSPOOLQ         ADD MESSAGE QUEUE BASE            R4
         CLC   0(1,PW),1(PW)       CHECK PRINTED VS. WRITTEN QUEUES  R4
         BE    PRMDONE             BRANCH IF NO MESSAGES TO PRINT    R4
         MVC   PJCTBUF(2),0(PW)    SAVE POINTERS TO PRINTED/WRITTEN  R4
         MVC   0(1,PW),1(PW)       SHOW ALL PRINTED                  R4
        $POST  $HASPECF,CKPW       POST CHECKPOINT PROCESSOR         R4
         EJECT                                                       R4
***********************************************************************
*                                                                     *
*        INITIALIZE PROCESSOR TO PRINT A SINGLE SPOOL MESSAGE BUFFER  *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
PRMTMSG  DS    0H                  *
         MVI   PBFAVAIL,1          SHOW ONLY 1 DATA BUFFER AVAILABLE R4
         SLR   PW,PW               CLEAR WORK REGISTER
         IC    PW,PJCTBUF          GET PRINTED RECORD NUMBER
         LA    PW,1(,PW)           ADD ONE FOR NEXT VALID BUFFER
         STC   PW,PJCTBUF          STORE FOR LATER
         SLR   R15,R15             IS RECORD                         R4
         IC    R15,$SPOLMSG         NUMBER                           R4
         LA    PW,1(,PW)             GREATER THAN                    R4
         SR    PW,R15                 &SPOLMSG - 1                   R4
         BNP   *+8                 BRANCH IF NO
         MVI   PJCTBUF,0           SET RECORD NUMBER TO ZERO
         SPACE 1                                                     R4
***********************************************************************
*                                                                     *
*        COMPUTE MTTR FROM RECORD NUMBER                              *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         L     R1,PCEDCT           ADDRESS PRINTER DCT         @OZ32566
         TM    MDCTSTAT,DCTABORT   TEST DCT STATUS             @OZ59095
         BO    PRMDONE             BR IF ABORTING              @OZ59095
         ICM   R1,B'0111',DCTDCB+1 GET LINE DCT ADDRESS        @OZ45379
         BZ    PRMDONE             BRANCH IF NOT SIGNED ON     @OZ45379
         L     R1,MDCTRAT          GET RAT ADDRESS             @OZ45379
         SLR   PW,PW               MULTIPLY                          R4
         IC    PW,RATCONRT+1-RATDSECT(,R1)    &SPOLMSG         @OZ45379
         SLR   R14,R14               BY REMOTE                       R4
         MR    R14,PW                NUMBER                          R4
         IC    R14,PJCTBUF         GET RELATIVE RECORD NUMBER
         ALR   R15,R14             ADD TO REMOTE SPACE BASE
         L     PW,$TEDADDR         ADDRESS FIRST TRACK EXTENT DATA
         LH    PW,TNRT-TEDDSECT(,PW) GET NUMBER OF TRACKS/RECORD
         SLR   R14,R14             CLEAR WORK REGISTER
         DR    R14,PW              DIVIDE RECORD # BY RECS/TRACK
         SLL   R15,8               MOVE TO TT IN MTTR
         LA    R14,1(,R14)         STEP PAST RECORD ZERO
         ALR   R15,R14             ADD REMAINING RECORD NUMBER
         L     R14,$DACKPT         GET BASE MTTR                     R4
         LH    R14,2(,R14)          FOR SPOOL MESSAGES               R4
         SLL   R14,8               MOVE TO TT IN MTTR
         ALR   R15,R14             ADD TO CURRENT RECORD OFFSET
         BAL   PL,PRDBUF           INITIATE READ OF NEXT DATA BLOCK
         BAL   PL,PRDCHK           CHECK READ
         CLI   BUFECBCC,X'7F'      TEST COMPLETION CODE
         BNE   PRSMBEOB            BRANCH IF IN ERROR
         XC    HDBNXTRK,HDBNXTRK   CLEAR CHAIN TRACK
         MVC   PCEEJRCB,PCCW+2     SET 1ST RCB DISPLACEMENT    @OZ19494
         B     P1STBLK             PRINT DATA BLOCK            @OZ19494
         EJECT                                                       R4
***********************************************************************
*                                                                     *
*        END OF, OR ERROR IN SPOOLED MESSAGE BUFFER                   *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
PRSMBEOB DS    0H
         MVI   PPFLAG,0            RESET ANY ERROR INDICATIONS
         CLC   PJCTBUF(1),PJCTBUF+1 END OF CURRENT SERIES
         BNE   PRMTMSG             BRANCH IF NO
         B     PRFNDMSG            CHECK FOR ADDITIONAL MESSAGES     R4
         SPACE 1                                                     R4
***********************************************************************
*                                                                     *
*        END OF ALL SPOOLED MESSAGE BUFFERS FOR THIS REMOTE           *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
PRMDONE  DS    0H
         LTR   PBUF,PBUF           WERE ANY SPOOLED MESSAGES PRINTED
         BZ    PGETJOB             BRANCH IF NO
         LM    PC1,PC2,PRCCWSP     LOAD 3 SPACE CCW
         BAL   PL,PPPUT            ADD CCW TO CHAIN
         BAL   PL,PPPUT            ADD CCW TO CHAIN
         BAL   PL,PPPUT            ADD CCW TO CHAIN
         BAL   PL,PPPUT            ADD CCW TO CHAIN
         L     R1,PCEDCT           GET ADDRESS OF REMOTE DCT   @OZ32566
         TM    DCTFLAGS,DCTRSTRT   DID RTAM FIND AN ERROR....  @OZ59095
         BNO   PRMCLOSE            NO, CLOSE NORMALLY          @OZ59095
         NI    DCTFLAGS,255-DCTRSTRT RESET ERROR INDICATION    @OZ59095
         $EXTP NCLOSE,(R1)         TERMINATE WITH ERROR        @OZ59095
         B     PRMFINI             FINISH CONSOLE CLEANUP      @OZ59095
PRMCLOSE DS    0H                                              @OZ59095
        $EXTP  CLOSE,(R1)          CLOSE REMOTE DCT                  R4
PRMFINI  TM    MDCTTYPE,DCTPSNA    TEST FOR SNA REMOTES              R4
         BNO   *+10                NO, SKIP NEXT                     R4
         MVC   MDCTSEL,PRMTSSEL    RESTORE ORIG. SNA DEVICE SELECT   R4
         $FREEBUF (PBUF)           FREE HASP BUFFER                  R4
         TM    $PRTOPTS,$RPRBOPT   TEST REMOTE PRT BUFFERING OPTION  R4
         BZ    PNOJOB              BR IF SINGLE BUFFERING            R4
         MVI   PBUFOPT,2            ELSE FORCE DOUBLE BUFFERING      R4
         B     PNOJOB              THEN BR TO FREE THE UNIT          R4
         EJECT                                                 @OZ61672
PTSTINIT DS    0H                                              @OZ34384
         TM    PCEID,PCEPUSID      IS DEVICE A PUNCH...        @OZ34384
         BO    PGETJOB             BR IF YES, GET A JOB        @OZ34384
         CLI   PDEVTYPE+3,UCB3800  NON-IMPACT PRINTER...       @OZ34384
         BE    PTST38MD            BRANCH IF YES               @OZ61672
         TM    PPFLAG3,PPINIT      FIRST USE OF UNIT...        @OZ34384
         BO    PTSTRMT             BR IF NO, CONTINUE INIT     @OZ34870
         OI    PPFLAG3,PPINIT      TURN ON FIRST-USE FLAG      @OZ34384
         L     R15,=A(PINITSU)     CALL IMAGE                  @OZ34384
         BALR  PL,R15                READ ROUTINE              @OZ34384
         B     PTSTRMT             FINISHED, CONTINUE INIT     @OZ34870
         DROP  R1                  SUSPEND DCT ADDRESSABILITY
         SPACE 1                                               @OZ61672
***********************************************************************
*                                                                     *
*        FOR 3800 PRINTERS, ESTABLISH MICROCODE PATH                  *
*                                                                     *
***********************************************************************
         SPACE 1                                               @OZ61672
PTST38MD TM    PPFLAG4,PP38PSI     PATH ALREADY SET...         @OZ61672
         BO    PGETJOB             BRANCH IF YES               @OZ65116
         L     R15,=A(PPSETMC)     CALL SUBROUTINE TO          @OZ61672
         BALR  PL,R15               ESTABLISH MICROCODE MODE   @OZ61672
         SPACE 1                                               @OZ65116
         EJECT
***********************************************************************
*                                                                     *
*        ATTEMPT TO GET WORK FROM THE JOB OUTPUT TABLE (JOT)          *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
PGETJOB  DS    0H
         L     R1,PCEDCT           GET PRINTER/PUNCH           @OZ32566
         LA    R1,0(,R1)             DCT ADDRESS               @OZ26040
         CLI   DCTDEVTP-DCTDSECT(R1),DCTRCON IF A CONSOLE DCT       R41
         BE    PNOJOB                         DONT LOOK FOR JOB     R41
        $#GET  DCT=(R1)            IS THERE WORK
         BNZ   PGOTJOB             BRANCH IF WORK FOUND
         SPACE 1                                                     R4
***********************************************************************
*                                                                     *
*        NO WORK IS CURRENTLY AVAILABLE - FREE RESOURCES              *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         CLI   PDEVTYP3,UCB3800    IS DEVICE A 3800 PRINTER    @G38ESBB
         BNE   PNOJOB              BR IF NOT                   @G38ESBB
         L     R15,=A(PALLOC)      CALL RESOURCE ALLOCATION    @G38ESBB
         BALR  PL,R15               BEFORE CLEARPRINT I/O      @G38ESBB
         L     PBUF,PBUFADDR       ADDRESS BUFFER              @G38ESBB
         MVC   PWKJOE,$ZEROS       SHOW ZERO WORK JOE FOR I/O  @G38ESBB
         LA    JCT,JCT             SHOW NON-ZERO JCT FOR I/O   @G38ESBB
         LM    PC1,PC2,PCCWCP      LOAD CLEAR PRINT CCW        @G38ESBB
         BAL   PL,PPPUT            ADD CCW TO AREA             @G38ESBB
         BAL   PL,PPWRITE          WRITE CCW AREA              @G38ESBB
         BAL   PL,PPCHECK          WAIT FOR I/O TO COMPLETE    @G38ESBB
         L     R1,PCEDCT           GET PRINT/PUNCH             @OZ61672
         LA    R1,0(,R1)            DCT ADDRESS                @OZ61672
         L     R15,=A(PPRSTMC)     CALL SUBROUTINE TO          @OZ65116
         BALR  PL,R15               RESET MICROCODE MODE       @OZ65116
         L     R15,=A(PDEALLOC)    DEALLOCATE RESOURCES        @OZ61672
         BALR  PL,R15               FOR CLEARPRINT I/O         @OZ61672
        $QSUSE                     OBTAIN SHARED QUEUES        @OZ65116
        $#GET  DCT=(R1),HAVE=NO    WORK ADDED DURING I/O...    @OZ65116
         BNZ   PTST38MD            BRANCH IF WORK FOUND        @OZ65116
         EJECT                                                 @OZ46470
PNOJOB   L     PW,PCEDCT           GET ADDR OF PRINT/PUNCH DCT @G38ESBB
         SPACE 1                                                     R4
         USING DCTDSECT,PW         ESTABLISH DCT ADDRESSABILITY
         SPACE 1                                                     R4
         OI    DCTSTAT,DCTHOLD     SET DEVICE UNAVAILABLE            R4
        $FREUNIT (PW)              FREE DEVICE
         TM    DCTSTAT,DCTDRAIN    IS THIS DEVICE DRAINED...   @OZ74217
         BO    PDORMANT            IF YES, DECR ACTIVE COUNT   @OZ74217
         CLI   PDEVTYP3,UCB3800    IS DEVICE A 3800 PRINTER    @G38ESBB
         BE    PINACTIV            BR IF YES, BYPASS DORMANT   @OZ46470
        $DORMANT                   INDICATE PROCESSOR INACTIVE
         TM    PCEID,PCERJEID      TEST PROCESSOR TYPE
         BO    PRPUINIT            START ALL OVER IF REMOTE    @OZ19494
*              THIS LINE DELETED BY APAR NUMBER                @OZ46470
         SPACE 1                                               @G38ESBB
*              THIS LINE DELETED BY APAR NUMBER                @OZ46470
*              THIS LINE DELETED BY APAR NUMBER                @OZ46470
         EJECT                                                       R4
***********************************************************************
*                                                                     *
*        ISSUE DEVICE INACTIVE MESSAGE TO THE OPERATOR                *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
PINACTIV TM    DCTPPSW,DCTPPSWI    INACTIVE MESSAGE ISSUED     @G38ESBB
         BO    PJOTWAIT            BRANCH IF YES
         OI    DCTPPSW,DCTPPSWI    SET INACTIVE MESSAGE ISSUED
        $MID   160                                                   R4
         MVC   PMESSAGE(2),=X'160F' INACTIVE MESSAGE ID
         MVC   PMESSAGE+2(8),DCTDEVN GET DEVICE NAME
         MVC   PMESSAGE+10(18),=CL18' INACTIVE - CLASS='
         SLR   R15,R15             SET                               R4
         IC    R15,$NUMCLAS         CLASS                            R4
         BCTR  R15,0                 FROM                            R4
         EX    R15,PMOVMSG            Q=                             R4
         LA    R0,29(,R15)         GET TOTAL MESSAGE SIZE            R4
        $WTO   PMESSAGE,(R0),JOB=NO,  ISSUE MESSAGE                  R4C
               ROUTE=$LOG+$UR,CLASS=$TRIVIA,PRI=$ST
         SPACE 3                                                     R4
***********************************************************************
*                                                                     *
*        WAIT FOR WORK TO BE ADDED TO THE JOB OUTPUT TABLE            *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
PJOTWAIT DS    0H
        $WAIT  JOT,INHIBIT=NO      WAIT FOR WORK OR DRAIN      @G38ESBB
         TM    DCTSTAT,DCTCIP      IS CMD IN PROGRESS...       @OZ56835
         BO    PJOTWAIT            YES, WAIT AGAIN             @OZ56835
         NI    DCTSTAT,FF-DCTHOLD  RELEASE DCT                 @OZ61672
         CLI   PDEVTYP3,UCB3800    TEST FOR 3800               @OZ61672
         BNE   PRPUINIT            BRANCH IF NOT               @OZ61672
         B     PDORMANT            DECREMENT $ACTIVE COUNT     @OZ65116
         SPACE 1                                               @OZ65116
*              THIS LINE DELETED BY APAR OZ65116               @OZ65116
*              THIS LINE DELETED BY APAR OZ65116               @OZ65116
*              THIS LINE DELETED BY APAR OZ65116               @OZ65116
         SPACE 1                                                     R4
PMOVMSG  MVC   PMESSAGE+28(*-*),DCTCLASS  *** EXECUTE ONLY ***       R4
         SPACE 1                                                     R4
         DROP  PW                  DROP DCT ADDRESSABILITY
         EJECT
***********************************************************************
*                                                                     *
*        A JOB HAS BEEN GOTTEN FROM THE JOB OUTPUT TABLE              *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
PGOTJOB  DS    0H
         ST    R0,PCHJOE           SAVE CHAR-JOE ADDRESS
         ST    R1,PCEJQE           STORE JQE ADDRESS           @OZ32566
         ST    R15,PWKJOE          SAVE WORK-JOE ADDRESS
         LR    JCT,R1              SET JCT = A(JOB QUEUE ELEMENT)
        $TIME  ,                   GET TIME OF DAY
         STM   R0,R1,PTIMEON       PRPU SIGN-ON TIME/DATE
         MVI   PDCTFLAG,0          CLEAR PRPU COPY OF DCTFLAGS       R4
         OI    PPFLAG2,PPFDS       SHOW 1ST DATA SET BEING PROCESSED R4
         SPACE 1                                                     R4
***********************************************************************
*                                                                     *
*        IF NOT LOCAL -- INITIALIZE REMOTE PRINTER OR PUNCH           *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         TM    PCEID,PCELCLID      TEST PROCESSOR TYPE
         BO    PLOCAL              BRANCH IF LOCAL
        $EXTP  OPEN,PCEDCT         OPEN REMOTE FOR PRT OR PNCH @OZ32566
         BNZ   PGOTJB1             OPEN SUCCEEDED              @OZ25817
         L     R1,PCEDCT           GET ADDRESS TO THE DCT      @OZ19494
         OI    DCTSTAT-DCTDSECT(R1),DCTHOLD TURN ON DCT HOLD   @OZ19494
         USING JOEDSECT,R1         PROVIDE JOE ADDRESSABILITY  @OZ25817
         L     R1,PWKJOE           MAKE WORKJOE ADDRESSABLE    @OZ25817
         TM    JOEFLAG,$JOECKV     IS CHKPT-JOE DATA VALID?    @OZ25817
         BZ    PJOEPUT             NO, BR TO REQUEUE WK-JOE    @OZ25817
         LH    R15,JOECKPT         HALF-WD OFFSET OF CKPT-JOE  @OZ25817
         N     R15,=F'65535'       CLEAR LEFT HALFWORD         @OZ25817
         SLL   R15,2               EXPANDTO BYTE OFFSET        @OZ25817
         AL    R15,$JOTABLE        ADD JOB OUTPUT TABLE ORG.   @OZ25817
         ST    R15,PCKJOE          SAVE CKPT-JOE @ IN PCE      @OZ25817
         MVC   PCKJOE(1),JOEFLAG     SAVE WK-JOE FLAGS IN PCE  @OZ25817
         B     PJPUTI              RETURN TO JOT WITH CKPT     @OZ25817
         DROP  R1                                              @OZ25817
         EJECT                                                 @G38ESBB
*************************************************************  @OZ25817
*                                                              @OZ25817
*        OPEN SUCCEEDED                                        @OZ25817
*                                                              @OZ25817
*************************************************************  @OZ25817
PGOTJB1  DS    0H                                              @OZ25817
        $GETBUF WAIT=YES           GET A DATA BUFFER                 R4
         ST    R1,PBUFSAVE         SAVE BUFFER ADDRESS               R4
         CLI   PBUFOPT,2           TEST FOR DOUBLE BUFFERING         R4
         BNE   PRMTNOSB            BR IF NOT                         R4
        $GETBUF WAIT=YES           GET SECOND DATA BUFFER            R4
         SPACE 1                                                     R4
PRMTNOSB LR    PBUF,R1             ACTIVATE BUFFER ADDRESSABILITY    R4
         ST    PBUF,PBUFADDR       SAVE BUFFER ADDRESS               R4
         ST    PBUF,PINIOB         INITIALIZE INPUT IOB POINTER      R4
         B     PLOCJOE             BR AROUND LOCAL DEV INIT    @G38ESBB
         EJECT                                                       R4
***********************************************************************
*                                                                     *
*        LOCAL DEVICE INITIALIZATION                                  *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
PLOCAL   DS    0H
         L     PW,PCEDCT           GET ADDRESS OF PRT/PNCH DCT @OZ32566
         SPACE 1                                                     R4
         USING DCTDSECT,PW         ESTABLISH DCT ADDRESSABILITY
         SPACE 1                                                     R4
         NI    DCTPPSW,255-DCTPPSWI RESET INACTIVE MESSAGE SWITCH
         SPACE 1                                                     R4
***********************************************************************
*                                                                     *
*        DETERMINE DE-SPOOLING METHOD    (SINGLE OR TRACK-CELL)       *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         LA    PC2,1               INITIALIZE TRACK-CELL SIZE TO 1   R4
         TM    PCEID,PCEPRSID      TEST PROCESSOR TYPE               R4
         BZ    PGETBUFS            FORCE SINGLE IF NOT LOCAL PRINTER R4
         TM    DCTPPFL,DCTTCEL     TEST FOR DESPOOL=TRKCEL SPECIFIED R4
         BZ    PGETBUFS            BRANCH IF NO                      R4
         CLI   $TCELSIZ,1          IF TRACK-CELL SIZE IS NOT GT 1    R4
         BNH   PGETBUFS             THEN FORCE DESPOOL=SINGLE        R4
         L     R15,PWKJOE          PICK UP WORK-JOE ADDRESS          R4
         SPACE 1                                                     R4
         USING JOEDSECT,R15        ACTIVATE JOE ADDRESSABILITY       R4
         SPACE 1                                                     R4
         TM    JOEFLAG2,$JOETCEL   WAS DATA SET TRACK-CELL'ED ...    R4
         BZ    PGETBUFS            FORCE DESPOOL=SINGLE IF NOT       R4
         OI    PPFLAG2,PPTCEL      INDICATE TRACK-CELL DESPOOLING    R4
*        DELETED                                               @G38ESBB
         SPACE 1                                                     R4
         DROP  R15,PW              SUSPEND JOE/DCT ADDRESSABILITY    R4
         EJECT                                                       R4
***************************************************************@G38ESBB
*                                                              @G38ESBB
*        CALL SUBROUTINE TO ALLOCATE PRINT/PUNCH RESOURCES     @G38ESBB
*                                                              @G38ESBB
***************************************************************@G38ESBB
         SPACE 1                                               @G38ESBB
PGETBUFS L     R15,=A(PALLOC)      CALL SUBROUTINE TO          @G38ESBB
         BALR  PL,R15                  ALLOCATE RESOURCE       @G38ESBB
         L     PBUF,PBUFADDR       ADDRESS BUFFER              @G38ESBB
         SPACE 1                                               @G38ESBB
         PRINT OFF                 THIS SECTION DELETED BY     @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
         PRINT ON                  THIS SECTION DELETED BY     @G38ESBB
         EJECT                                                       R4
***********************************************************************
*                                                                     *
*        LOCATE CHECKPOINT-JOE                                        *
*                                                                     *
***********************************************************************
         SPACE 1                                                    R41
         USING JOEDSECT,R1         PROVIDE JOE ADDRESSABILITY       R41
         SPACE 1                                                    R41
PLOCJOE  L     R1,PWKJOE           ADDRESS WORK JOE            @G38ESBB
         LH    R14,JOECKPT         HALFWORD OFFSET OF CKPT-JOE
         N     R14,=F'65535'       CLEAR LEFT HALFWORD
         SLL   R14,2               EXPAND TO BYTE OFFSET             R4
         AL    R14,$JOTABLE        ADD JOB OUTPUT TABLE ORIGIN
         ST    R14,PCKJOE          SAVE A(CKPT-JOE) IN PCE
         MVC   PCKJOE(1),JOEFLAG   COPY WORK-JOE FLAGS
         SPACE 3                                                    R41
***********************************************************************
*                                                                     *
*        GET ADDRESS OF JCT IN BUFFER FROM JCT MANAGER                *
*                                                                     *
***********************************************************************
         SPACE 1                                                    R41
        $#JCT  READ                GET ADDR OF JCT BUFFER           R41
         ST    JCT,PJCTBUF         SAVE JCT BUFFER ADDRESS          R41
         BNZ   PATIMAGE            BRANCH IF SUCCESSFUL        @OZ57316
         OI    PDCTFLAG,DCTDELET   ELSE, SET DELETE FLAG       @OZ57316
         B     PRPUEXIT            GO TO EXIT - BAD JCT        @OZ57316
         EJECT                                                 @OZ26939
***************************************************************@OZ26939
*                                                              @OZ26939
*        ATTACH HASPIMAG SUBTASK FOR PRINTERS                  @OZ26939
*                                                              @OZ26939
***************************************************************@OZ26939
         SPACE 1                                               @OZ26939
PATIMAGE TM    PCEID,PCEPUSID      TEST PROCESSOR TYPE         @OZ57316
         BO    PCLDSTRT            BR IF PUNCH                 @OZ26939
         L     R1,PRIMGDTE          ELSE GET HASPIMAG DTE ADDR @OZ26939
         OC    0(4,R1),0(R1)       SUBTASK ALREADY ACTIVE...   @OZ26939
         BNZ   PCLDSTRT            BR IF YES                   @OZ26939
         L     R15,=A(PIMAGLST)     ELSE MOVE ATTACH PARMS     @OZ26939
         MVC   $CSAVREG(PIMAGLL),0(R15)  TO WORK AREA          @OZ26939
         LR    PW,R1               SAVE DTE ADDRESS            @OZ26939
         ATTACH ECB=4(,R1),MF=(E,(1)),SF=(E,$CSAVREG)          @OZ26939
         STCM  R1,7,1(PW)          STORE TCB ADDRESS INTO DTE  @OZ36651
         SPACE 1                                               @OZ26939
PATWAIT $WAIT  IMAG                WAIT FOR SUBTASK TO INIT    @OZ26939
         TM    0(PW),X'80'         HASPIMAG INITIALIZED...     @OZ26939
         BNO   PATWAIT             BR IF NOT YET               @OZ26939
         SPACE 1                                               @OZ26939
         EJECT                                                       R4
         EJECT                                                 @G38ESBB
***************************************************************@G38ESBB
*                                                              @G38ESBB
*        CALL SUBROUTINE TO SETUP PRINT/PUNCH HEADER           @G38ESBB
*                                                              @G38ESBB
***************************************************************@G38ESBB
         SPACE 1                                               @G38ESBB
PCLDSTRT L     R15,=A(PHEADER)     CALL SUBROUTINE TO          @G38ESBB
         BALR  PL,R15                   SETUP PRINT HEADER     @G38ESBB
         B     PCLDSTRC(R15)       BRANCH ON RETURN CODE       @OZ75372
         SPACE 1                                               @OZ75372
PCLDSTRC B     PCLDSTOK            0  NORMAL RETURN            @OZ75372
         B     PPABORT             4  OPER TERMINATE           @OZ75372
         B     PPDSEND             8  3800 JAM OR CANCEL KEY   @OZ75372
         B     PPIODRN             12 3800 ERROR IN SETUP      @OZ75372
         SPACE 1                                               @OZ75372
         PRINT OFF                 THIS SECTION DELETED BY     @OZ75372
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
         EJECT                                                       R4
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
         PRINT ON                  THIS SECTION DELETED BY     @G38ESBB
         SPACE 1                                               @G38ESBB
PCLDSTOK TM    PPFLAG,PPDELSW+PRDELSW TEST FOR DELETION        @OZ75372
         BO    PCKPTNIT            YES, BYPASS NEWS            @OZ41172
         TM    PPFLAG,PPNEWS       NEWS DATA SET...            @G38ESBB
         BZ    PCKPTNIT            BR IF NO                    @G38ESBB
         B     PNEWSGO             ELSE GO PRINT NEWS          @G38ESBB
         SPACE 1                                               @G38ESBB
PENDNEWS DS    0H                  * RETURN HERE AFTER NEWS *  @OZ45008
         BAL   PL,PRDTCHK          CHECK FOR READ COMPLETE     @OZ45008
         LM    PC1,PC2,PRCCWEJ     LOAD EJECT CCW              @OZ45008
         BAL   PL,PPPUT            SKIP                        @G38ESBB
         BAL   PL,PPWRITE           TO TOP                     @G38ESBB
         BAL   PL,PPCHECK            OF PAGE                   @G38ESBB
         NI    PPFLAG,FF-PPNEWS-PPRDERR RESET NEWS AND ER FLGS @G38ESBB
         EJECT                                                      R41
***********************************************************************
*                                                                     *
*        INITIALIZE PRPU CHECKPOINT DATA AREA                         *
*                                                                     *
*        COPY CKPT-JOE TO PRPU CHECKPOINT DATA AREA ON WARM START     *
*                                                                     *
***********************************************************************
         SPACE 1                                                    R41
         USING JOEDSECT,R1         PROVIDE JOE ADDRESSABILITY       R41
         SPACE 1                                                    R41
PCKPTNIT MVC   PPJOBKEY,JCTJBKEY   SET JOB KEY                      R41
         TM    PCKJOE,$JOECKV      WARM START...                    R41
         BO    PWARMNIT            BR IF YES                        R41
         XC    PCKPT,PCKPT         CLEAR PRPU CKPT DATA AREA        R41
         L     PW,$IOTPDDB         SET INITIAL                      R41
         STH   PW,PDDBDISP          PDDB DISPLACEMENT               R41
         MVC   PCEIOTTR,JCTIOT     SET IOT MTTR                     R41
         SPACE 1                                                    R41
         L     R1,PWKJOE           ADDRESS WORK-JOE                 R41
         TM    JOEFLAG,$JOESPIN    SPIN JOE...                      R41
         BZ    PPAGSIZE            BR IF NO                         R41
         MVC   PCEIOTTR,JOEIOTTR    ELSE SET SPIN-IOT MTTR          R41
         OI    PSMFDCI,SMFSPIN     SET SPIN SMF FLAG                R41
         B     PPAGSIZE            CONTINUE                         R41
         SPACE 1                                                    R41
PWARMNIT L     R1,PCKJOE           ADDRESS CKPT-JOE                 R41
         MVC   PCKPT,JOECKPP       REFRESH PRPU CKPT DATA AREA @OZ27300
         MVC   PBUFSKIP,PCEJBOFF   SET BUFFER OFFSET FOR RESTART    R41
         OI    PSMFDCI,SMFINTRC    SET WARM-START SMF FLAG          R41
         SPACE 1                                                    R41
         DROP  R1                  SUSPEND JOE ADDRESSABILITY       R41
         SPACE 1                                                    R41
***********************************************************************
*                                                                     *
*        SETUP MAXIMUM PHYSICAL PAGE SIZE FOR PRINTERS         @OZ19494
*                                                                     *
***********************************************************************
         SPACE 1                                                    R41
PPAGSIZE SLR   PW,PW               GET JOB'S                        R41
         IC    PW,JCTLINCT          PAGE SIZE (LINE COUNT)          R41
         BCTR  PW,0                SET PHYSICAL PAGE SIZE      @OZ19494
         ST    PW,PRLINECT          FOR PRINTERS.  PUNCH       @OZ19494
*                                    PAGE SIZE SET FROM        @OZ19494
*                                     CKPTLNS IN PGOTPDDB      @OZ19494
*                                      ROUTINE.                @OZ19494
*                                  THIS LINE DELETED BY APAR   @OZ19494
         SPACE 1                                                    R41
         TM    PPFLAG,PPDELSW      DELETION DURING PRPU INIT...     R41
         BZ    PENDINIT            BRANCH IF NO                @OZ37777
         TM    PPFLAG,PRDELSW      TEST FOR TERMINATION        @OZ37777
         BO    PPDONE              BRANCH IF SO TO TERMINATE   @OZ37777
         NI    PPFLAG,255-PPDELSW  ELSE IGNORE $B, $F          @OZ37777
         TITLE 'HASP PRINT/PUNCH SERVICE -- DATA SET SELECTION' OZ39705
***********************************************************************
*                                                                     *
*        PROCESSOR INITIALIZATION COMPLETE - READ/CHECK/VERIFY IOT    *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
PENDINIT DS    0H
         L     R15,PCEIOTTR        GET IOT MTTR
         BAL   PL,PRDBUF           INITIATE READ OF IOT
         BAL   PL,PRDCHK           CHECK READ
         LR    JCT,PBUF            ADDRESS IOT IN BUFFER
         USING IOTDSECT,JCT        ACTIVATE IOT ADDRESSABILITY
         TM    PPFLAG,PPRDERR      TEST FOR I/O ERROR ON READ
         BO    PIOTPRE             BR IF YES                         R4
         CLC   IOTJBKEY,PPJOBKEY   IS THIS IOT VALID
         BE    PPIOTOK             BRANCH IF YES
PIOTPRE  $DISTERR                  INDICATE CONTROL BLOCK ERROR
         OI    PPFLAG,PPJCTIOT+PRDELSW REASON FOR TERMINATION
         B     PPDONE              ABORT JOB
PPIOTOK  DS    0H
         TM    IOTFLAG1,IOT1SPIN   SPIN DATA SET...                  R4
         BNO   *+8                 BRANCH IF NO - NO $PURGE AT EOJ
         OI    PPFLAG,PPDALOC      SET $PURGE REQUIRED FLAG
         LH    PC1,PDDBDISP        GET NEXT PDDB DISPLACEMENT
         SPACE 3                                                     R4
***********************************************************************
*                                                                     *
*        SELECT NEW PDDB -- GO READ NEXT IOT IF NONE LEFT             *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         USING JCTDSECT,R15        PROVIDE JCT ADDRESSABILITY        R4
         USING PDBDSECT,PC2        PROVIDE PDDB ADDRESSABILITY       R4
         SPACE 1                                                     R4
PDDBNEXT DS    0H
         L     R15,PJCTBUF         ADDRESS JCT IN BUFFER
         LA    PC2,0(PC1,JCT)      GENERATE ABSOLUTE ADDRESS
         CL    PC1,IOTPDDBP        END OF PDDB'S IN THIS IOT
         BNE   PPPDB               BRANCH IF NO
         TM    IOTFLAG1,IOT1SPIN   IS THIS A SPIN/HOLD IOT
         BO    PPDONE              BRANCH IF YES
         ICM   R15,15,IOTIOTTR     LOAD AND TEST NEXT TRACK
         BZ    PPDONE              BRANCH IF NO MORE IOTS
         ST    R15,PCEIOTTR        SAVE NEW TRACK IN PCE
         L     R14,$IOTPDDB        SET INITIAL PDDB OFFSET           R4
         STH   R14,PDDBDISP        IN CKPT AREA OF PCE
         B     PENDINIT            READ NEXT IOT
         EJECT                                                       R4
***********************************************************************
*                                                                     *
*        VERIFY THAT PDDB MATCHES WORK/CHAR-JOE -- ELSE SKIP IT       *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
PPPDB    DS    0H
         TM    PDBFLAG1,PDB1NULL+PDB1NSOT IS PDDB NULL/NOT SYSOUT
         BNZ   PDDBSRCH            BRANCH IF YES
*
*        CHECK SYSOUT CLASS
*
         L     PW,PWKJOE           ADDRESS WORK JOE                  R4
         USING JOEDSECT,PW         ACTIVATE JOE ADDRESSABILITY       R4
         CLC   PDBCLASS,JOEPDBCL   DOES CLASS MATCH JOE              R4
         BNE   PDDBSRCH            BRANCH IF NO
*
*        CHECK REMAINING CHARACTERISTICS
*
         TM    JOEFLAG2,$JOEDMND   TEST FOR DEMAND-SETUP OPTION      R4
         L     PW,PCHJOE           ADDRESS CHAR-JOE
         BZ    SKIP130             SKIP CLASS TEST IF NOT CHOSEN     R4
         CLC   PDBCLASS,JCTMCLAS   DOES SYSOUT CLASS = MSGCLASS
         BE    PPDEMAND            BRANCH IF YES - DEMAND SETUP
SKIP130  CLC   JOEFORM(12),PDBFORMS CHECK FORMS, FCB, UCS
         BNE   PDDBSRCH            BRANCH IF NOT MATCHING
         CLC   JOEFLASH,PDBFLASH   CHECK OVERLAY-FRAME               R4
         BNE   PDDBSRCH            IF NOT MATCHING, BRANCH           R4
         TM    PDBFLAG2,PDB2BRST   VERIFY                            R4
         BZ    SKIP140              THAT                             R4
         TM    JOECFLAG,$JOEBRST     PDDB                            R4
         BZ    PDDBSRCH               BURSTER                        R4
         B     PPDEMAND                SPECIFICATION                 R4
SKIP140  TM    JOECFLAG,$JOEBRST        MATCHES                      R4
         BO    PDDBSRCH                  CHAR-JOE                    R4
PPDEMAND CLC   JOEWTRID,PDBWTRID   CHECK EXTERNAL WRITER NAME        R4
         BNE   PDDBSRCH            BRANCH IF NOT MATCHING
         L     PW,PWKJOE           ADDRESS WORK-JOE
         CLC   JOEDEST,PDBDEST     CHECK DESTINATION
         BE    PGOTPDDB            BR IF SAME -- DATA SET SELECTED   R4
         SPACE 1                                                     R4
***********************************************************************
*                                                                     *
*        PDDB DOES NOT MATCH WORK/CHAR-JOE -- STEP TO NEXT            *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
PDDBSRCH DS    0H                                                    R4
         LH    PC1,PDDBDISP        GET CURRENT PDDB OFFSET           R4
         LA    PC1,PDBLENG(,PC1)   STEP TO NEXT PDDB                 R4
         STH   PC1,PDDBDISP        SAVE NEW PDDB OFFSET IN CKPT AREA R4
         B     PDDBNEXT            GO TEST NEW PDDB                  R4
         SPACE 1                                                     R4
         DROP  PW,R15              SUSPEND JOE/JCT ADDRESSABILITY    R4
         EJECT                                                       R4
***********************************************************************
*                                                                     *
*        PDDB MATCHES -- SETUP PRPU WORK AREA FOR DATA SET            *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         USING DCTDSECT,R15        PROVIDE DCT ADDRESSABILITY  @OZ19494
         SPACE 1                                               @OZ19494
PGOTPDDB DS    0H                                                    R4
         MVC   PMESSAGE(12),PDBFORMS SAVE SETUP
         MVC   PRINDEX,PDBINDEX    SAVE 3211 INDEX VALUE
*              THIS LINE DELETED BY APAR NUMBER                @OZ33895
         MVC   PPDSKEY,PDBDSKEY    SAVE DATA SET KEY
         L     R15,PCEDCT          ADDRESS PRINTER DCT         @OZ32566
         MVC   DCTACPTN,DCTDCPTN   SET DEFAULT CPT NUMBER      @OZ19494
         CLI   PDBCPTN,X'FF'       IF PDDB CPT NUMBER NOT SPECIFIED R41
         BE    PCKPTPL              USE DEFAULT                @OZ19494
         MVC   DCTACPTN,PDBCPTN    USE CPT NUMBER IN PDDB      @OZ19494
         SPACE 1                                               @OZ19494
PCKPTPL  MVC   PCKPTPSV,DCTCKPTP   INITIALIZE CKPTPGS SAVE     @OZ19494
         MVC   PCKPTP,DCTCKPTP      AREA AND COUNTER FROM DCT  @OZ19494
         MVC   PCKPTLSV,DCTCKPTL   INITIALIZE CKPTLNS SAVE     @OZ19494
         MVC   PCKPTL,DCTCKPTL      AREA AND COUNTER FROM DCT  @OZ19494
         CLC   PDBCKPTP,=X'FFFF'   TEST PDDB CKPTPGS VALUE     @OZ19494
         BE    PCKPTLNS            BRANCH IF NOT SPECIFIED     @OZ19494
         MVC   PCKPTPSV,PDBCKPTP   INITIALIZE CKPTPGS SAVE     @OZ19494
         MVC   PCKPTP,PDBCKPTP      AREA AND COUNTER FROM PDDB @OZ19494
PCKPTLNS CLC   PDBCKPTL,=X'FFFF'   TEST PDDB CKPTLNS VALUE     @OZ19494
         BE    PUNLPAGE            BRANCH IF NOT SPECIFIED     @OZ19494
         TM    PCEID,PCEPRSID      TEST PROCESSOR TYPE         @OZ19494
         BO    PCKPTL1             BRANCH IF PRINT PROCESSOR   @OZ19494
         CLC   PDBCKPTL,=H'0'      TEST CKPTLNS FOR A PUNCH    @OZ19494
         BE    PUNLPAG1            ILLEGAL IF 0, USE DCT VALUE @OZ19494
PCKPTL1  MVC   PCKPTLSV,PDBCKPTL   INITIALIZE CKPTLNS SAVE     @OZ19494
         MVC   PCKPTL,PDBCKPTL      AREA AND COUNTER FROM PDDB @OZ19494
PUNLPAGE TM    PCEID,PCEPRSID      TEST PROCESSOR TYPE         @OZ19494
         BO    PSYSOUT             BRANCH IF PRINTER           @OZ19494
PUNLPAG1 LH    R1,PCKPTL           FOR PUNCHES, LOGICAL        @OZ19494
         BCTR  R1,0                 PAGE SIZE (CKPTLNS)        @OZ19494
         ST    R1,PRLINECT           EQUALS PHYSICAL PAGE SIZE @OZ19494
         SPACE 1                                               @OZ19494
         DROP  R15                 SUSPEND DCT ADDRESSABILITY  @OZ19494
         EJECT                                                 @OZ19494
PSYSOUT  MVI   PPDIRID,X'00'       INDICATE SYSOUT PDIR        @OZ19494
         MVC   PPDSRCT(4),PDBRECCT SAVE PDDB DATASET REC COUNT @OZ19494
         TM    PCKJOE,$JOECKV      SAVE 1ST MTTR               @OZ19494
         BO    PSAVMTTR             OF DATA SET IF             @G38ESBB
         MVC   PCEJMTTR,PDBMTTR      COLD START - ELSE USE CKPT JOE  R4
         SPACE 1                                                     R4
PSAVMTTR MVC   PPLC,PDBMTTR        SAVE BEGIN DATA SET MTTR    @G38ESBB
         TM    PDBFLAG2,PDB2TCEL   PDDB SPECIFY TRACK-CELL...  @G38ESBB
         BO    PTSTNIPR            BR IF YES                         R4
         TM    PPFLAG2,PPTCEL      TEST FOR PREVIOUS TRACK-CELL'ING  R4
         BZ    PTSTNIPR            BR IF NO                          R4
*                                                                    R4
*        NON-TRACK-CELL'ED PDDB DETECTED IN TRACK-CELL'ED JOE        R4
*                                                                    R4
        $FREEBUF PINIOB            FREE-UP INPUT CCW BUFFER          R4
         NI    PPFLAG2,255-PPTCEL  RESET DESPOOLING METHOD TO SINGLE R4
         ST    PBUF,PINIOB         USE IOB IN DATA BUFFER            R4
         SPACE 1                                                     R4
         CLI   PBUFOPT,2           TEST BUFFER OPTION                R4
         BL    PNOTCEL1            BR IF NOT DOUBLE                  R4
         L     R1,PBUFSAVE         ADDRESS SECONDARY BUFFER CHAIN    R4
         L     R1,BUFCHAIN-BUFDSECT(,R1)  FREE ALL BUT 1ST           R4
        $FREEBUF (R1),MULTIPLE             BUFFER IN CHAIN           R4
         SPACE 1                                                     R4
PNOTCEL1 L     R1,PBUFADDR         ADDRESS PRIMARY BUFFER CHAIN      R4
         L     R1,BUFCHAIN-BUFDSECT(,R1)  FREE ALL BUT 1ST           R4
        $FREEBUF (R1),MULTIPLE             BUFFER IN CHAIN           R4
         SPACE 1                                                     R4
         EJECT                                                       R4
***********************************************************************
*                                                                     *
*        SETUP FOR 3800 PRINTERS                                      *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
PTSTNIPR DS    0H                                                    R4
         B     PSMFTST             CHECK IF SMF6 NEEDED        @OZ33895
PSETNIPR DS    0H                                              @OZ33895
         MVC   PFLASHC,PDBFLSHC    MOVE FLASH COUNT TO WORK AREA     R4
         XC    PCOPYGRP,PCOPYGRP   ZERO COPY GROUPS IN WORK AREA     R4
         MVI   SPFLAG,0            CLEAR SETUP FLAGS                 R4
         TM    PDBFLAG2,PDB2BRST   TEST FOR BURSTING                 R4
         BZ    PNOBURST            BR IF NO                          R4
         OI    SPFLAG,SPBURST      SET BURST SETUP FLAG              R4
PNOBURST DS    0H                                                    R4
         MVC   SPFORMS(2*4),PDBFORMS  FORMS/FCB                      R4
         MVC   SPCHAR1(6*4+2*1),PDBCHAR1 CHAR1-4/FLSH/MODF/FLSHC/TRC R4
         CLI   PDBCOPYG,0          TST FOR NULL COPY GROUPS          R4
         BE    PNIPSET1            BR IF YES                         R4
         SLR   R0,R0               CLEAR                             R4
         SLR   R1,R1                WORK                             R4
         SLR   PW,PW                 REGISTERS                       R4
         LA    PL,8                MAX COPY GROUPS                   R4
         SPACE 1                                                     R4
PCGROUPS IC    R0,PDBCOPYG(R1)     INITIALIZE                        R4
         ALR   PW,R0                COPY-GROUPS                      R4
         CH    PW,=H'255'            ENSURING THAT                   R4
         BH    PCGT255                TOTAL                          R4
         STC   R0,PCOPYGRP(R1)         DOES NOT                      R4
         LA    R1,1(,R1)                EXCEED                       R4
         BCT   PL,PCGROUPS               255                         R4
         STC   PW,PPDSCPY                 DATA SET                   R4
         B     PNIPSET1                    COPIES                    R4
         SPACE 1                                                     R4
PCGT255  SH    PW,=H'255'          IF EXCESSION,                     R4
         SLR   R0,PW                AJUST FOR                        R4
         STC   R0,PCOPYGRP(R1)       ONLY 255                        R4
         MVI   PPDSCPY,255            COPIES                         R4
         SPACE 1                                                     R4
         EJECT                                                       R4
PNIPSET1 DS    0H                                                    R4
         SLR   R1,R1               DETERMINE                         R4
         LA    R0,3                 MAXIMUM                          R4
         LA    PL,PDBCHAR2           VALUE                           R4
PCNTRC   CLC   =C'****',0(PL)         ALLOWED                        R4
         BE    SKIP160                 FOR            ( OPTCD=J )    R4
         LA    R1,1(,R1)                IMBEDDED                     R4
         LA    PL,4(,PL)                 TABLE-                      R4
         BCT   R0,PCNTRC                  REFERENCE-                 R4
SKIP160  STC   R1,PRMAXTRC                 CHARACTERS (TRC)          R4
         SPACE 1                                                     R4
         TM    PCKJOE,$JOECKV      TEST FOR WARM START               R4
         BO    PNIPWARM            BR IF YES                         R4
         MVI   SPCOPYS,1           ELSE, INIT STARTING COPY NUMBER   R4
         MVI   SPCOPYN,1           ASSUME 1 COPY                     R4
         CLI   PCOPYGRP,0          TEST FOR COPY GROUPS SPECIFIED    R4
         BE    SKIP170             BR IF NOT--ASSUMPTION OK          R4
         MVC   SPCOPYN,PCOPYGRP    ELSE, USE 1ST TRANSMISSION COUNT  R4
SKIP170  MVI   PDDBCPYG,0          RESET COPY GROUP OFFSET           R4
         B     PPDBSETF            GO SET PPFLAGS              @OZ33895
         SPACE 3                                                     R4
***********************************************************************
*                                                                     *
*        SETUP FOR 3800 DATA SET WARM START                           *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
PNIPWARM DS    0H                                                    R4
         LA    R1,1                ASSUME 1 COPY THIS TRANSMISSION   R4
         CLI   PCOPYGRP,0          TEST FOR COPY GROUPS SPECIFIED    R4
         BE    SKIP180             BR IF NOT - ASSUMPTION OK         R4
         IC    R1,PDDBCPYG         ELSE, PICK UP OFFSET TO           R4
         IC    R1,PCOPYGRP(R1)      COPY GROUP AND GET COUNT         R4
SKIP180  STC   R1,SPCOPYN          MOVE NO. COPIES TO SETUP LIST     R4
         MVI   SPFLASHC,0          ASSUME NO FLASHING                R4
         IC    R1,PPRCPYCT         GET TOTAL COPIES ALREADY PRINTED  R4
         SLR   R0,R0               COMPUTE NUMBER                    R4
         IC    R0,PFLASHC           OF COPIES LEFT TO                R4
         SR    R0,R1                 BE FLASHED                      R4
         BP    PSETFLC             BR IF MORE FLASHING         @OZ38238
         OI    SPFLAG,SPNOFLSH     ELSE TURN OFF FLASHING      @OZ38238
         B     PSETCPYN            BR TO CONTINUE              @OZ38238
         SPACE 1                                               @OZ38238
PSETFLC  STC   R0,SPFLASHC         MOVE REMAINING FLASH COUNT  @OZ38238
         SPACE 1                                               @OZ38238
PSETCPYN LA    R1,1(,R1)           INDICATE STARTING           @OZ38238
         STC   R1,SPCOPYS           COPY NUMBER                      R4
         B     PPDBSETF            GO SET PPFLAGS              @OZ33895
         SPACE 1                                                     R4
         EJECT                                                       R4
***********************************************************************
*                                                                     *
*        WRITE A NEW TYPE 6 SMF RECORD IF PERTINENT DATA HAS CHANGED  *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
PSMFTST  CLI   $NUMSMFB,2          TEST SMF BUFFER COUNT             R4
         BL    PPDBFLGS            BR IF SMF NOT SUPPORTED           R4
         SPACE 1                                                     R4
         L     PW,PCEDCT           PROVIDE DCT                 @OZ32566
         USING DCTDSECT,PW          ADDRESSABILITY                   R4
         SPACE 1                                                     R4
         TM    PCKJOE,$JOECKV      TEST FOR WARM START               R4
         BO    PPDBFLGS            BR IF YES                         R4
         TM    PPFLAG2,PPFDS       TEST FOR 1ST DATA SET             R4
         BO    PPDBFLGS            BR IF YES                         R4
         SPACE 1                                                     R4
         TM    PSMFDCI,SMFINTRC+SMFOPCC WARM-START/CARR. OVERRIDE... R4
         BNZ   PNEW6               SMF6 IF EITHER                    R4
         TM    PPFLAG2,PSMFDSER    DATA BUFFER ERROR...              R4
         BO    PNEW6               SMF6 IF YES                       R4
         SPACE 1                                                     R4
         CLC   DCTFORMS,PDBFORMS   FORMS MATCH...                    R4
         BNE   PNEW6               SMF6 IF NOT                       R4
         SPACE 1                                                     R4
         CLC   DCTFCB,PDBFCB       FCB MATCH...                      R4
         BE    PSMFTPUN            BR IF YES                        R41
         CLC   PDBFCB,=CL4'****'   TEST FOR DEFAULT                  R4
         BNE   PNEW6               SMF6 IF NOT                       R4
         TM    DCTPPSW,DCTPPSWB    IS DEVICE FCB STANDARD...         R4
         BNZ   PNEW6               SMF6 IF NOT                       R4
         SPACE 1                                                     R4
PSMFTPUN CLI   PDEVTYPE+3,UCB3525  TEST FOR 3525 PUNCH               R4
         BNE   PSMFTUCS            BR IF NOT                        R41
         TM    PDEVTYPE+1,X'30'    ARE PRINT FEATURES AVAIL..  @OZ45069
         BZ    PSMFTUCS            BR IF NOT                        R41
         LA    R1,PBFUNCI          COMPARE FUNC=I                    R4
         BAL   PL,PBITSAME          SPECIFICATIONS                   R4
         SPACE 1                                                    R41
PSMFTUCS CLI   PDEVTYPE+3,UCB3800  TEST FOR 3800 PRINTER            R41
         BE    PSMFNIP             BR IF YES                        R41
         CLC   DCTUCS,PDBUCS       UCS MATCH...                     R41
         BE    PPDBFLGS            BR IF YES                        R41
         CLC   PDBUCS,=C'****'     TEST FOR DEFAULT                 R41
         BNE   PNEW6               SMF6 IF NOT                      R41
         TM    DCTPPSW,DCTPPSWU    IS DEVICE UCS STANDARD...   @OZ28771
         BNZ   PNEW6               SMF6 IF NOT                      R41
         B     PPDBFLGS            NEW SMF TYPE 6 NOT NEEDED        R41
         EJECT                                                      R41
PSMFNIP  CLC   =C'****',PDBCHAR1   IF CHAR1 NOT SPECIFIED,     @OZ31424
         BNE   *+10                 THEN UPDATE IN-STORAGE     @OZ31424
         MVC   PDBCHAR1,DCTUCS       PDDB WITH PRINTER DEFAULT @OZ31424
         LA    R0,4                CHECK 4 PDDB CHAR FIELDS    @OZ31424
         LA    R15,4                WITH 4 DCT  CHAR FIELDS    @OZ31424
         LA    R1,PDBCHAR1         R1 = POINTER TO PDDB CHARS  @OZ31424
         SPACE 1                                               @OZ31424
PSCHLUP1 LA    PL,DCTCHAR1         PL = POINTER TO DCT CHARS   @OZ31424
         SPACE 1                                               @OZ31424
PSCHLUP2 CLC   0(4,PL),0(R1)       DOES THIS                   @OZ31424
         BE    PSCHNEXT             PDDB CHAR                  @OZ31424
         LA    PL,4(,PL)             MATCH ANY                 @OZ31424
         BCT   R0,PSCHLUP2            DCT CHAR FIELD...        @OZ31424
         B     PNEW6               NEW SMF6 IF NOT             @OZ31424
         SPACE 1                                               @OZ31424
PSCHNEXT LA    R1,4(,R1)           LOOP, CHECKING              @OZ31424
         CLC   =C'****',0(R1)       ALL SPECIFIED              @OZ31424
         BE    *+8                   PDDB CHAR FIELDS.         @OZ31424
         BCT   R15,PSCHLUP1        FALL THROUGH IF ALL MATCH   @OZ31424
         CLC   DCTFLASH(2*4),PDBFLASH  FLASH, MODIFY MATCH...  @OZ31424
         BNE   PNEW6               SMF6 IF NOT                 @OZ31424
         CLC   PCOPYGRP,PDBCOPYG   COPY GROUPS MATCH...              R4
         BNE   PNEW6               SMF6 IF NOT                       R4
         CLC   PFLASHC,PDBFLSHC    FLASH COUNTS MATCH...             R4
         BNE   PNEW6               SMF6 IF NOT                       R4
         SPACE 1                                                     R4
         LA    R1,PBOPTJ           COMPARE OPTCD=J                   R4
         BAL   PL,PBITSAME          SPECIFICATIONS                   R4
         LA    R1,PBBURST          COMPARE BURSTER                   R4
         BAL   PL,PBITSAME          SPECIFICATIONS                   R4
         SPACE 1                                                     R4
         B     PPDBFLGS            BR IF NEW TYPE 6 NOT NEEDED       R4
         SPACE 5
PBFUNCI  TM    PDBFUNC,X'80'       FUNC=I        *** EXECUTE ***     R4
         TM    PPFLAG,PPFUNCI                    ***   ONLY  ***     R4
         SPACE 1                                                     R4
PBOPTJ   TM    PDBFLAG2,PDB2OPTJ   DCB=OPTCD=J   *** EXECUTE ***     R4
         TM    PPFLAG2,PPOPTJ                    ***   ONLY  ***     R4
         SPACE 1                                                     R4
PBBURST  TM    PDBFLAG2,PDB2BRST   BURST=Y/N     *** EXECUTE ***     R4
         TM    DCTPPSW2,DCTNIBRS                 ***   ONLY  ***     R4
         SPACE 2                                                     R4
*                                                                    R4
*        PBITSAME -- RETURNS TO CALLER IF FLAGS MATCH                R4
*                                                                    R4
         SPACE 1                                                     R4
PBITSAME EX    0,0(,R1)            TEST                              R4
         BZ    SKIP200              IF                               R4
         EX    0,4(,R1)              BOTH                            R4
         BZ    PNEW6                  FLAG                           R4
         BR    PL                      BITS                          R4
SKIP200  EX    0,4(,R1)                 MATCH                        R4
         BZR   PL                  RETURN -- BITS MATCH. ELSE,       R4
         EJECT
***********************************************************************
*                                                                     *
*        NEW TYPE 6 NEEDED -- CALL PPSMF6 AND RESET DATA              *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
PNEW6    L     R15,=A(PPSMF6)      GENERATE NEW                @OZ32776
         BALR  PL,R15               TYPE-6 SMF RECORD          @OZ32776
         SPACE 1                                                     R4
        $TIME  ,                   GET CURRENT TIME/DATE             R4
         STM   R0,R1,PTIMEON       RESET PRPU START TIME/DATE        R4
         XC    PPLNCDCT,PPLNCDCT         CURRENT RECORD COUNT        R4
         XC    PRPAGECT,PRPAGECT         CURRENT PAGE COUNT          R4
         XC    PPJNDS,PPJNDS             CURRENT DATA SET COUNT      R4
         XC    PSMFDCI,PSMFDCI           DATA SET CONTROL INDICATORS R4
         NI    PPFLAG2,255-PSMFDSER      DATA BUFFER ERROR FLAG      R4
         SPACE 1                                               @OZ33895
PPDBFLGS MVC   PPDSCPY,PDBCOPYS    SAVE DATA SET COUNT         @OZ33895
         CLI   PDEVTYPE+3,UCB3800  TEST FOR 3800 PRINTER       @OZ33895
         BE    PSETNIPR            IF SO, SET UP WORK AREA     @OZ33895
         EJECT                                                       R4
***********************************************************************
*                                                                     *
*        SET FUNC=I AND OPTCD=J FLAGS                                 *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
PPDBSETF NI    PPFLAG,255-PPFUNCI  SET 3525                    @OZ33895
         TM    PDBFUNC,X'80'        PUNCH-INTERPRET                  R4
         BZ    SKIP210               FLAG IF                         R4
         OI    PPFLAG,PPFUNCI         REQUESTED                      R4
SKIP210  NI    PPFLAG2,255-PPOPTJ  SET 3800                          R4
         TM    PDBFLAG2,PDB2OPTJ    DCB=OPTCD=J                      R4
         BZ    PDDBFCHK              FLAG IF                         R4
         OI    PPFLAG2,PPOPTJ         REQUESTED                      R4
         SPACE 1                                                     R4
         DROP  PC2                 SUSPEND PDDB ADDRESSABILITY       R4
         SPACE 3                                                     R4
***********************************************************************
*                                                                     *
*        SETUP DEVICE BY PDDB SPECIFICATIONS                          *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
PDDBFCHK DS    0H                                                    R4
         LA    R1,PMESSAGE         ADDRESS NEW SETUP SPECIFICATION
         L     R15,=A(PRPUDSV)     CALL DEVICE                       R4
         BALR  PL,R15               SETUP VERIFICATION               R4
         B     PDDBDSVR(R15)       BRANCH ON RETURN CODE       @OZ75372
         SPACE 1                                               @OZ75372
PDDBDSVR B     PTESTFDS            0  NORMAL RETURN            @OZ75372
         B     PPDONE              4  OPER TERMINATE           @OZ75372
         B     PDDSVJAM            8  3800 JAM OR CANCEL KEY   @OZ75372
         B     PPIODRN             12 3800 ERROR IN SETUP      @OZ75372
         SPACE 1                                               @OZ75372
PDDSVJAM L     R15,=A(PLOCATE)     CALL THE LOCATE ROUTINE     @OZ75372
         BALR  PL,R15                   TO GET ORIGIN PQE      @G38ESBB
         B     PPDSEND             PROCESS PAPERJAM/CANCEL KEY @G38ESBB
         EJECT                                                 @OZ75372
***********************************************************************
*                                                                     *
*        OFFSET-STACK 3800 BURSTER DATA SETS                          *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
PTESTFDS TM    PPFLAG2,PPFDS       TEST FOR FIRST DATA SET     @G38ESBB
         BO    PWARMTST            BR IF YES                         R4
         CLI   PDEVTYPE+3,UCB3800  TEST FOR 3800 PRINTER             R4
         BNE   PWARMTST            BR IF NOT                         R4
         CLC   PPDSKEY,=AL2(IOTPDBOD/PDBLENG)  SYSTEM PDDB...       R41
         BNH   PWARMTST            SKIP OFFSET-STACK IF YES         R41
         LM    PC1,PC2,PCCWOFST    SELECT OFFSET-STACK CCW           R4
         BAL   PL,PPPUT2           ADD TO CHAIN                @OZ51441
         SPACE 1                                                     R4
PWARMTST DS    0H                                                    R4
         TM    PCKJOE,$JOECKV      IS THIS A DATASET WARM START
         BO    PFASTRT             BRANCH IF YES
         EJECT                                                       R4
***********************************************************************
*                                                                     *
*        RESTART POINT FOR DATA SET COPIES                            *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
PNXTCPY  DS    0H
         IC    PL,PPJNDS           GET DATA SET COUNTER
         LA    PL,1(,PL)           INCREMENT BY ONE
         STC   PL,PPJNDS           STORE NEW COUNTER VALUE
         SPACE 1                                                     R4
***********************************************************************
*                                                                     *
*        IF FIRST DATA SET -- INITIALIZE CHECKPOINT-JOE               *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
PCKINIT  DS    0H
         MVC   PCEEJRCB,PCCW+2     1ST RCB DISPLACEMENT
         XC    PDDBPGCT,PDDBPGCT   CLEAR DATASET PAGE COUNT
         TM    PPFLAG2,PPFDS       TEST FOR 1ST DATA SET             R4
         BZ    PFASTRT             BR IF NOT                         R4
         CLI   PDEVTYP3,UCB3800    TEST FOR 3800 PRINTER       @G38ESBB
         BE    PFASTRT1            YES, BYPASS CKPT JOE INIT   @G38ESBB
         SPACE 1                                                     R4
        $QSUSE                     REQUEST ACCESS TO CHECKPOINT DATA
         SPACE 1                                                     R4
***********************************************************************
*                                                                     *
*        INITIALIZE AND CHECKPOINT THE CKPT-JOE                       *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         USING JOEDSECT,PL         PROVIDE JOE ADDRESSABILITY        R4
         SPACE 1                                                     R4
         L     PL,PCKJOE           ADDRESS CKPT-JOE                  R4
         MVC   JOECKPP,PCKPT        AND INITIALIZE IT          @OZ27300
         SPACE 1                                                     R4
        $#CKPT JOE=0(,PL),TYPE=A   CKPT THE CKPT-JOE                 R4
         EJECT                                                       R4
***********************************************************************
*                                                                     *
*        SHOW CKPT-JOE IS VALID -- CHECKPOINT THE WORK-JOE            *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         L     PL,PWKJOE           ADDRESS WORK-JOE
         OI    JOEFLAG,$JOECKV     SET CKPT-JOE VALID FLAG
        $#CKPT JOE=0(,PL),TYPE=A   CKPT THE WORK-JOE                 R4
         SPACE 1                                                     R4
         DROP  PL                  SUSPEND JOE ADDRESSABILITY
         SPACE 1                                               @G38ESBB
***************************************************************@G38ESBB
*                                                              @G38ESBB
*        FOR 3800 PRINTER, ASSIGN DATA SET PQE (PQED)          @G38ESBB
*                                                              @G38ESBB
***************************************************************@G38ESBB
         SPACE 1                                               @G38ESBB
PFASTRT  CLI   PDEVTYP3,UCB3800    TEST FOR 3800 PRINTER       @G38ESBB
         BNE   PFASTRT3            NO, BRANCH                  @G38ESBB
         SPACE 1                                               @G38ESBB
PFASTRT1 TM    PPFLAG3,PP3800R+PP3800S  REPOSITIONING...       @OZ51592
         BNZ   PFASTRT2            YES, BYPASS PQE ALLOC       @OZ51592
         L     R15,=A(PQEDINIT)    CALL PQEDINIT TO ACQUIRE    @G38ESBB
         BALR  PL,R15               AND INIT DATA SET PQE      @G38ESBB
         BZ    PPDONE              BRANCH IF NOT SUCCESSFUL    @OZ48003
PFASTRT2 NI    PCKJOE,FF-$JOECKV   RESET WARMSTART BIT         @G38ESBB
         NI    PPFLAG3,FF-PP3800S  RESET REPOSITION FLAG       @OZ51592
         B     PBSFSGO             GO REJOIN COMMON CODE       @G38ESBB
         SPACE 1                                               @G38ESBB
         SPACE 1                                                     R4
***********************************************************************
*                                                                     *
*        RESET WARM START INDICATION IN WORK AREA                     *
*                                                                     *
*        MOVE A COPY OF THE CKPT-JOE TO THE PP BUFFER                 *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
PFASTRT3 DS    0H                                              @G38ESBB
         NI    PCKJOE,255-$JOECKV  RESET WARM START BIT              R4
         TM    PCEID,PCERJEID      TEST PROCESSOR TYPE               R4
         BO    PRMTPDIR            BR IF REMOTE                     R41
         LM    PC1,PC2,POUTCCWA         INITIALIZE                   R4
         AH    PC1,PCCWLAST              PPBUF                       R4
         AH    PC2,PCCWLAST               COPIES                     R4
         MVC   PCIESIZE(L'PCKPT,PC1),PCKPT OF                        R4
         MVC   PCIESIZE(L'PCKPT,PC2),PCKPT  CKPT-JOE                 R4
         OI    PCISGNAL-PCIDSECT(PC1),PCICKPT NEW CKPT EXISTS  @OZ66860
         B     PBSPINIT            BR TO CONTINUE                   R41
         EJECT                                                      R41
***********************************************************************
*                                                                     *
*        CALL PPDIR SUBROUTINE IF REMOTE SETUP HEADER IS NEEDED       *
*                                                                     *
***********************************************************************
         SPACE 1                                                    R41
         USING DCTDSECT,PW         PROVIDE DCT ADDRESSABILITY       R41
         SPACE 1                                                    R41
PRMTPDIR L     PW,PCEDCT           ADDRESS OF REMOTE DCT       @OZ32566
         TM    MDCTFEAT,DCTPSHDR   NEED SETUP HEADER...             R41
         BZ    PBSPINIT            BR IF NO                         R41
         L     R15,=A(PPDIR)        ELSE CALL PDIR                  R41
         BALR  PL,R15                SUBROUTINE                     R41
         SPACE 1                                                    R41
         DROP  PW                  SUSPEND DCT ADDRESSABILITY       R41
         SPACE 2                                                     R4
***********************************************************************
*                                                                     *
*        INITIALIZE BACKSPACE TABLE                                   *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
PBSPINIT DS    0H                                                    R4
         LH    R15,$BSPSIZ         IF NO BSP                         R4
         LTR   R15,R15              TABLE ENTRIES,                   R4
         BZ    PBSFSGO               BR TO PRIME THE BUFFERS         R4
         L     PW,PDDBPGCT         SET FIRST
         LA    PW,1(,PW)            BACKSPACE
         ST    PW,PBSPGCT            FRAME PAGE
         SH    R15,=H'7'           RESET                             R4
         LA    R15,PBSPTBL(R15)     LAST                             R4
         XC    0(7,R15),0(R15)       ENTRY                           R4
         TITLE 'HASP PRINT/PUNCH SERVICE -- MAIN PROCESSOR'          R4
***********************************************************************
*                                                                     *
*        MAIN PRINT/PUNCH LOOP INITIALIZATION                         *
*                                                                     *
*        RESTART POINT FOR $F/$B PROCESSING                           *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
PBSFSGO  DS    0H                  $F/$B RESTART POINT
         SLR   R1,R1               INITIALIZE CHECKPOINT       @OZ66860
         ST    R1,PPCKPTR           DATA POINTER                     R4
         STCK  $DOUBLE             SET START TIME FOR          @G38ESBB
         MVC   PCECLOCK,$DOUBLE     CHECKPOINT INTERVAL        @G38ESBB
         OI    PPFLAG2,PPCKPTA      AND ALLOW CHECKPOINTS            R4
         MVI   PUNLINE,X'FD'       SET FOR HIGHEST PRINT LINE CCW
         MVI   PRTRCCW,255         RESET TRC SET MEMORY        @OZ40326
         TM    PPFLAG3,PP3800R     ARE WE REPOSITIONING...     @G38ESBB
         BO    PDSPLTST            YES,BYPASS LINE COUNT RESET @G38ESBB
         SPACE 1                                                    R41
PNEWSGO  XC    PPLC,PPLC           CLEAR PAGE LINE COUNTER          R41
         EJECT ,                                               @OZ27703
************************************************************** @OZ27703
* INITIALIZE PBUFSKIP TO PROPER VALUE IF THIS IS TRACKCELLED * @OZ27703
* PROCESSOR.  THIS IS DONE BY CALCULATING ALL RECORDS IN A   * @OZ27703
* TRACK CELL IN TURN AND CHECKING FOR A MATCH ON THE INPUT   * @OZ27703
* MTTR VALUE.  WHEN THIS MATCH OCCURS, THE PBUFSKIP VALUE    * @OZ27703
* WILL BE SET TO THE CORRECT VALUE.                          * @OZ27703
*                                                            * @OZ27703
* REGISTER USAGE - - - - - - - -                             * @OZ27703
* R15 - - WORK AND COUNTER FOR RECORDS IN THE CELL           * @OZ27703
* R14 - - SUB PERMUTATION NUMBER                             * @OZ27703
* PL  - - WORKING R VALUE                                    * @OZ27703
* R0  - - RECORDS PER TRACK THIS SPOOL DEVICE                * @OZ27703
* R1  - - RECORD INCREMENT VALUE                             * @OZ27703
************************************************************** @OZ27703
         SPACE 2                                               @OZ27703
PDSPLTST TM    PPFLAG2,PPTCEL      TEST DE-SPOOLING METHOD     @G38ESBB
         BZ    PGO100               DON'T CALCULATE PBUFSKIP   @OZ27703
         SPACE 1                                               @OZ27703
*              LOOP INITIALIZATION                             @OZ27703
         XR    R15,R15             CLEAR WORK REG TO ZERO      @OZ27703
         LR    R1,R15              CLEAR RECINCR REG TO ZERO   @OZ27703
         LA    PL,1                SET INITIAL R TO 1          @OZ27703
         LR    R14,PL              SET INITIAL SPN TO 1        @OZ27703
         IC    R15,PCEJMTTR        FROM M VALUE                @OZ27703
         MH    R15,=AL2(TEDSIZ)     POINT TO                   @OZ27703
         AL    R15,$TEDADDR          PROPER TED                @OZ27703
         LH    R0,TNRT-TEDDSECT(,R15) GET RECORDS PER TRACK    @OZ27703
         LR    R15,R1              CLEAR COUNT TO ZERO         @OZ27703
         IC    R1,$RECINCR         GET RECORD INCREMENT VALUE  @OZ27703
         SPACE 1                                               @OZ27703
PGO001   CLM   PL,1,PCEJMTTR+3     DOES R MATCH                @OZ27703
         BE    PGO099              BR IF YES, EXIT             @OZ27703
         SPACE 1                                               @OZ27703
         LA    R15,1(,R15)         BUMP COUNT BY 1             @OZ27703
         CLM   R15,1,$TCELSIZ      IS FULL CELL ACCOUNTED FOR  @OZ27703
         BL    PGO002              BR IF NO, KEEP ON COUNTING  @OZ27703
         XR    R15,R15             RESET COUNT TO ZERO         @OZ27703
PGO002   AR    PL,R1               BUMP R BY RECINCR           @OZ27703
         CR    PL,R0               CHECK IF R FITS ON TRACK    @OZ27703
         BNH   PGO001              BR IF YES, LOOP BACK        @OZ27703
         SPACE 1                                               @OZ27703
         LA    R14,1(,R14)         BUMP SPN                    @OZ27703
         LR    PL,R14              AND USE AS NEW R            @OZ27703
         B     PGO001              GO CHECK R                  @OZ27703
         SPACE 1                                               @OZ27703
PGO099   STC   R15,PBUFSKIP        SET SKIP VALUE              @OZ27703
PGO100   DS    0H                  RESUME                      @OZ27703
         EJECT ,                                               @OZ27703
***********************************************************************
*                                                                     *
*        DE-SPOOL INITIAL BUFFER OR TRACK-CELL                        *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         L     R15,PCEJMTTR        1ST TRACK OF DATA SET             R4
         IC    R1,PBUFOPT          INITIALIZE AVAILABLE INPUT        R4
         STC   R1,PBFAVAIL          BUFFER COUNT TO BUFFERING OPTION R4
         BAL   PL,PRDTCEL          READ FIRST DATA BLOCK(S)          R4
         SPACE 1                                                     R4
         TM    PPFLAG,PPDELSW      TEST FOR DELETION                 R4
         BO    PPDSEND             BR IF YES                         R4
         BAL   PL,PRDTCHK          ELSE, WAIT FOR I/O TO COMPLETE    R4
         B     P1STBLK              AND GO PROCESS 1ST DATA BUFFER   R4
         SPACE 1                                                     R4
***********************************************************************
*                                                                     *
*        WAIT FOR SPOOL INPUT TO COMPLETE                             *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
PCHKNBLK DS    0H                                                    R4
         TM    PPFLAG,PPDELSW      TEST FOR SUSPENSION/TERMINATION   R4
         BO    PPDSEND             BR IF YES                         R4
         BAL   PL,PRDTCHK           ELSE, CHECK INPUT I/O            R4
         EJECT                                                       R4
***********************************************************************
*                                                                     *
*        PROCESS NEW DATA SET BUFFER                                  *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
*                                  THIS LINE DELETED BY APAR   @OZ19494
*                                  THIS LINE DELETED BY APAR   @OZ19494
         SPACE 1                                                     R4
P1STBLK  DS    0H                                                    R4
         TM    PPFLAG2,PPTCEL      TEST DE-SPOOLING METHOD           R4
         BZ    SKIP220             BRANCH IF SINGLE                  R4
         OC    PPFLAG,BUFECBCC     COPY STATUS OF NEW BUFFER         R4
         B     PVALCHK              AND GO CHECK IT                  R4
SKIP220  CLC   HDBKEY,PPKEY        IS THIS DATA BUFFER VALID         R4
         BE    PVALCHK             BR IF YES                         R4
         OI    PPFLAG,PPRDERR      SET DATA BUFFER VALIDITY ERROR    R4
         SPACE 1                                                     R4
***********************************************************************
*                                                                     *
*        EXIT IF BUFFER READ/VALIDITY ERROR OR SUSPENSION             *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
PVALCHK  DS    0H                                                    R4
         TM    PPFLAG,PPRDERR+PPDELSW SUSPEND OR READ ERROR
         BNZ   PCPERR              BRANCH IF YES               @OZ56911
         LM    PC1,PC2,PCCW        SETUP CCW IN PC1 AND PC2
         ICM   PC1,3,PCEEJRCB      GET RCB DISPLACEMENT
         ALR   PC1,PBUF            ADJUST FOR THIS BUFFER
         LH    R1,PCEEJRCB         GET CURRENT RCB DISPL
         SPACE 1                                                     R4
***********************************************************************
*                                                                     *
*        PROCESS NEW LOGICAL RECORD -- TEST FOR END-OF-BLOCK          *
*                                                                     *
*        IF DATA BUFFER IS AVAILABLE -- BEGIN TO DE-SPOOL INTO IT     *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
PNXTCCW  DS    0H
         STH   R1,PCEEJRCB         SAVE EJECT RCB DISPL
         CLI   PBFAVAIL,0          TEST FOR AVAILABLE INPUT BUFFER   R4
         BE    PALLACTV            SKIP IF ALL ARE ACTIVE            R4
        $COUNT R=PW                ********************************* R4
         BAL   PL,PRDTCNXT         BEGIN INPUT OF NEXT DATA BLOCK(S) R4
PALLACTV DS    0H                                                    R4
         TM    0(PC1),X'FF'        TEST RECORD CONTROL BYTE
         BO    PCPEND              BRANCH IF END OF BLOCK
         EJECT                                                       R4
         ICM   PC2,2,=X'00'        ENSURE LENGTH RESET               R4
         IC    PC2,0(,PC1)         SET TEXT LENGTH                   R4
         LR    PW,PC1              COPY LRC ADDRESS                  R4
         SPACE 1                                                     R4
         USING LRCDSECT,PW         PROVIDE LRC ADDRESSABILITY        R4
         SPACE 1                                                     R4
         TM    LRCFLAG1,LRC1SPAN   TEST FOR SPANNED RECORD           R4
         BZ    PNOSPAN             BR IF NO                          R4
         SPACE 1                                                     R4
***********************************************************************
*                                                                     *
*        SPANNED RECORD -- PRINT/PUNCH ONLY FIRST SEGMENT             *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         ICM   PC2,3,LRCSEGL       GET SEGMENT LENGTH                R4
         LA    PC1,LRCSTEXT        ASSUME NOT 1ST SEGMENT            R4
         TM    LRCFLAG1,LRC1SBGN   TEST ASSUMPTION                   R4
         BZ    PNXTRCB             BR IF VALID TO SKIP SEGMENT       R4
         LA    PC1,LRCSFTXT        POINT TO START OF 1ST-SEG TEXT    R4
         STM   PC1,PC2,PCCWORK     SAVE CCW FOR ANALYSIS             R4
         B     PFSPCK               AND BR TO ANALYZE                R4
         SPACE 1                                                     R4
         DROP  PW                  KILL LRC ADDRESSABILITY           R4
         SPACE 1                                                     R4
***********************************************************************
*                                                                     *
*        PICK UP USER CARRIAGE CONTROL (IF ANY) -- TEST FOR ASA       *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
PNOSPAN  DS    0H                  *                                 R4
         LA    PC1,3(,PC1)         TEXT ADDRESS (NO CC)
         TM    1(PW),LRC1CCTL      IS USER CC SPECIFIED
         BNO   *+8                 <*** BRANCH IF NO
         LA    PC1,1(,PC1)            * STEP OVER CC
         STM   PC1,PC2,PCCWORK        * STORE CCW FOR ANALYSIS
         BNO   *+10                <*** BRANCH IF NO CC
         MVC   PCCWORK(1),3(PW)    MOVE IN USER CC
         TM    1(PW),LRC1ONUL      IS NOT SYSOUT BIT SET
         BO    PNXTRCB             BRANCH IF YES
         TM    1(PW),LRC1CCTL+LRC1TASA IS CC ASA
         BNO   PFSPCK              BRANCH IF NO - START ANALYSIS
         EJECT                                                       R4
***********************************************************************
*                                                                     *
*        CONVERT ASA CARRIAGE CONTROL TO PROPER IMMEDIATE CCW         *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
PASA001  LA    PL,PASANUM          NUMBER OF ASA TBL ENTRIES   @OZ70401
         CLI   PDEVTYPE+3,X'0C'    IS DEVICE A 3525 PUNCH...   @OZ28353
         BE    PASA005             BR. YES...USE 3525 TABLE    @OZ28353
         L     PC2,=A(PASAMCH)     ASA TO MCH CONVERSION TABLE @OZ32776
         SPACE 1                                               @OZ32776
PASA002  DS    0H                  *
         CLC   PCCWORK(1),0(PC2)   DOES USER CC MATCH ASA ENTRY
         BE    PASA004             BRANCH IF YES                     R4
         LA    PC2,2(,PC2)         STEP TO NEXT TABLE ENTRY
         BCT   PL,PASA002          CYCLE THROUGH TABLE         @OZ70401
         L     PC2,PCCWORK+4       RESTORE 2ND HALF OF CCW           R4
         OC    PCCWORK+6(2),PCCWORK+6 CC NOT FOUND -- CHECK DATA LEN R4
         BZ    PNXTRCB             IGNORE REC IF BAD CC & NO DATA    R4
         TM    PCEID,PCEPRSID      CHECK PROCESSOR TYPE              R4
         BO    PASA003             BRANCH IF PRINT                   R4
         CLI   PDEVTYPE+3,X'0C'    IS DEVICE A 3525 PUNCH...   @OZ33253
         BE    PASA006             BRANCH IF YES               @OZ33253
         MVI   PCCWORK,X'41'       DEFAULT PUNCH CC = STACKER 1      R4
         B     PNIMDCMD            BRANCH TO COUNT RECORDS           R4
PASA005  DS    0H                  *                           @OZ28353
         L     PC2,=A(PASAMCH2)    3525 ASA TO MCH TABLE       @OZ32776
*                                  THIS LINE DELETED BY APAR   @OZ32776
         B     PASA002             CONTINUE CONVERSION         @OZ28353
PASA006  DS    0H                                              @OZ33253
         MVI   PCCWORK,X'01'       DEFAULT 3525 CC = STACK 1   @OZ33253
         B     PNIMDCMD            BRANCH TO COUNT RECORDS     @OZ33253
PASA003  DS    0H                  *                                 R4
         L     PC2,=A(PASAMCH)     DEFAULT TO SINGLE SPACE     @OZ32776
PASA004  DS    0H                  *                                 R4
         MVC   PCCWORK(1),1(PC2)   SELECT MCH CC VALUE
         L     PC2,PCCWORK+4       RESTORE SECOND PART OF CCW
         TM    PCEID,PCEPUSID      CHECK PROCESSOR TYPE              R4
         BO    PNIMDCMD            NO ASA IMMEDIATE CMDS FOR PUNCH   R4
PFSPCK   DS    0H                  *
         TM    PCCWORK,X'02'       TEST COMMAND TYPE
         BZ    PNIMDCMD            BRANCH IF NOT IMMEDIATE
         L     PC2,PCCW+4          SET COUNT TO ZERO
         TM    1(PW),LRC1CCTL+LRC1TASA  TEST FOR ASA           @OZ70401
         BO    PNOCOUNT            BRANCH IF YES               @OZ70401
         EJECT                                                       R4
***********************************************************************
*                                                                     *
*        UPDATE TOTAL LINE OR CARD COUNT FOR THIS JOE                 *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
PNIMDCMD DS    0H                  CMND IS NOT IMMEDIATE
         CLC   PDDBSKIP,PDDBPGCT   SKIP COUNT VS PAGE COUNT
         BNH   PCOUNT              COUNT IF NOT HIGH           @OZ78256
         TM    PPFLAG4,PPREPOBS    REPO FOR 3800 BSPACE...     @OZ78256
         BNO   PNOCOUNT            NO, DON'T COUNT             @OZ78256
PCOUNT   SLR   R2,R2               ZERO WORK REGISTER          @OZ78256
         IC    PW,PREVCPYN         UPDATE RECORD COUNT         @OZ35930
         AL    PW,PPLNCDCT          BY NUMBER OF COPIES        @OZ35930
         ST    PW,PPLNCDCT           THIS TRANSMISSION         @OZ35930
         SPACE 1                                                     R4
***********************************************************************
*                                                                     *
*        IF PUNCH -- UPDATE DATA SET CARD COUNT                       *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
PNOCOUNT DS    0H                  *
         TM    PCEID,PCEPRSID      TEST PROCESSOR TYPE
         BO    PRINT               BRANCH IF PRINT
         CLI   PDEVTYPE+3,X'0C'    IS DEVICE A 3525...         @OZ28353
         BNE   PUSTKR1             BR. NO...DONT TEST          @OZ28353
         CLI   PCCWORK,X'01'       IS CC STACKER ONE...        @OZ28353
         BE    PUSTKR2             BR. YES...CONTINUE TESTS    @OZ28353
PUSTKR1  DS    0H                  *                           @OZ28353
         CLI   PCCWORK,X'41'       IS CC PUNCH STACKER 2
         BE    *+12                BRANCH IF YES
         CLI   PCCWORK,X'81'       IS CC PUNCH STACKER 3
         BNE   PUNPRT              BRANCH IF NO
PUSTKR2  DS    0H                  *                           @OZ28353
         MVI   PUNLINE,X'05'       RESET PUNCH-PRINT LINE NO.
         TM    PPFLAG,PPDELSW      CHECK FOR SUSPENSION        @OZ29138
         BO    PPDSEND             BRANCH IF YES               @OZ29138
         L     PW,PDDBPGCT         UPDATE
         LA    PW,1(,PW)            PUNCH
         ST    PW,PDDBPGCT           CARD COUNT
         L     PW,PPLC             UPDATE                            R4
         LA    PW,1(,PW)            LOGICAL                          R4
         ST    PW,PPLC               PAGE COUNT                      R4
         B     PUPDTBSP            GO UPDATE BSP TABLE               R4
         SPACE 1                                                     R4
***********************************************************************
*                                                                     *
*        CC IS NOT A PUNCH COMMAND - CHECK FOR 3525 PRINT             *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
PUNPRT   DS    0H
         TM    PCCWORK,X'05'       IS CC A 3525 PRINT-LINE
         BNO   PUBADCC             BRANCH IF NO
         CLI   PDEVTYPE+3,X'0C'    IS DEVICE A 3525
         BNE   PNXTRCB             BRANCH IF NO                @OZ40272
         TM    PPFLAG,PPFUNCI      WAS INTERPRET REQUESTED
         BO    PRNOPRNT            BRANCH IF YES - SKIP PRINT CCW
         TM    PDEVTYPE+1,X'30'    IS EITHER PRINT FEATURE IN
         BZ    PUBADCC             BRANCH IF NO
         EJECT                                                       R4
***********************************************************************
*                                                                     *
*        CHECK FOR VALID 3525 PRINT-LINE COMMAND                      *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         CLC   PCCWORK(1),PUNLINE  IS LINE NUMBER IN SEQUENCE
         BNH   PUBADCC             BRANCH IF NO
         SLR   PW,PW               CLEAR REG PW
         IC    PW,PCCWORK          GET PRINT COMMAND
         SRL   PW,3                CONVERT TO PRINT LINE NUMBER
         CLM   PW,1,=X'03'         DOES LINE REQUIRE ML PRINT
         BNH   *+12                BRANCH IF NO
         TM    PDEVTYPE+1,X'10'    IS ML PRINT FEATURE INSTALLED
         BZ    PUBADCC             BRANCH IF NO
         MVC   PUNLINE,PCCWORK     SET NEW LINE NUMBER
         B     PUPDTBSP            GO UPDATE BSP TABLE               R4
         SPACE 1                                                     R4
***********************************************************************
*                                                                     *
*        FOR NO USER CC, BAD USER CC, BAD PRINT LINE - USE CC=41      *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
PUBADCC  DS    0H                  *
         CLI   PDEVTYPE+3,X'0C'    IS DEV A 3525 PUNCH         @OZ45115
         BE    PUBADCC2            BR IF YES,DEFAULT STACK 1   @OZ45115
         MVI   PCCWORK,X'41'       SET CC PUNCH STACKER 2
         B     PNOCOUNT            UPDATE CARD COUNTERS
PUBADCC2 MVI   PCCWORK,X'01'       DEFAULT 3525 CC=STACK 1     @OZ45115
         B     PNOCOUNT            UPDATE CARD COUNTERS        @OZ45115
         EJECT                                                       R4
***********************************************************************
*                                                                     *
*        CHECK VALIDITY OF PRINTER CCW COMMAND                        *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
PRINT    DS    0H
         TM    PDCTFLAG,DCTSPACE   TEST FOR FORCE SINGLE SPACE
         BZ    PRNSPACE            BRANCH IF NOT SINGLE SPACING
         TM    PSMFDCI,SMFOPCC     TEST CARRIAGE OVERRIDE      @OZ55801
         BO    PRLOOP              BR. YES...NOT FIRST TIME    @OZ55801
         TM    PPFLAG3,PP3800R     TEST FOR 3800 RESTART       @G38ESBB
         BO    PRLOOP              BRANCH IF YES               @G38ESBB
         LM    PC1,PC2,PRCCWSP1    LOAD IMMEDIATE SPACE CCW    @OZ55801
         BAL   PL,PPPUT            ADD CCW TO CHAIN            @OZ55801
         LM    PC1,PC2,PCCWORK     RELOAD CURRENT COMMAND CCW  @OZ55801
         PRINT OFF        THESE LINES DELETED BY APAR NUMBER   @OZ55801
*              THIS LINE DELETED BY APAR NUMBER                @OZ55801
         PRINT ON         THESE LINES DELETED BY APAR NUMBER   @OZ55801
PRLOOP   DS    0H                                              @OZ55801
         OI    PSMFDCI,SMFOPCC     SET OPERATOR CARRIAGE OVERRIDE
         TM    PCCWORK,X'02'       TEST FOR IMMEDIATE COMMAND
         BO    PRNOPRNT            BRANCH IF YES
         IC    PL,PDCTFLAG         GET SPACE CONTROL BITS
         SLL   PL,3                POSITION FOR WRITE/SPACE
         STC   PL,PCCWORK          STORE INTO CCW
         OI    PCCWORK,X'01'       SET WRITE BIT
         NI    PCCWORK,X'19'       TURN OFF ALL EXTRAS
         B     PRGOODCC            CONTINUE PROCESSING
         SPACE 1                                                     R4
PRNSPACE TM    PCCWORK,X'01'       TEST LOW ORDER BIT OF COMMAND
         BZ    PRBADCC             BIT MUST BE ONE OR COMMAND IS BAD
         TM    PCCWORK,X'84'       TEST BITS 0 AND 5
         BM    PRSKIP              BRANCH IF EJECT OR INVALID
         TM    PCCWORK,X'64'       NO, TEST BITS 1, 2, AND 5
         BZ    PRGOODCC            BITS MUST BE ZERO OR COMMAND IS BAD
         CLI   PCCWORK,X'73'       TEST FOR BLOCK DATA CHECK COMMAND
         BE    PRNOPRNT            IGNORE IF BLOCK DATA CHECK
         SPACE 1                                                     R4
PRBADCC  LH    PW,PCEEJRCB         GET LRC OFFSET IN BUFFER    @OZ69106
         ALR   PW,PBUF             ADD BUFFER ORIGIN           @OZ69106
         NI    1(PW),FF-LRC1TASA   RESET ASA BIT               @OZ69106
         SPACE 1                                               @OZ69106
PRBADSKP NI    PCCWORK,X'02'       CONVERT BAD COMMAND         @OZ69106
         OI    PCCWORK,X'09'        TO SINGLE SPACE            @OZ69106
         SPACE 1                                                     R4
PRGOODCC SLR   PL,PL               CLEAR REGISTER
         IC    PL,PCCWORK          PICK UP COMMAND
         SRL   PL,3                SHIFT OUT LOW-ORDER BITS
         AL    PL,PPLC             INCREMENT LINE                    R4
         ST    PL,PPLC              COUNT BY SPACE VALUE             R4
         CL    PL,PRLINECT         COMPARE LINE COUNT WITH MAXIMUM   R4
         BNH   PPCKLNS             BRANCH IF NOT HIGH          @OZ19494
         NI    PCCWORK,X'02'       CONVERT COMMAND
         OI    PCCWORK,X'89'       TO EJECT
         SPACE 1                                                     R4
PRSKIP   DS    0H
         CLI   PCCWORK,X'89'       TEST COMMAND VALIDITY
         BL    PRBADCC             BRANCH IF INVALID (LOW)
         BH    PRN8940             BRANCH IF NOT EJECT
         LH    PW,PCEEJRCB         GET RCB OFFSET
         ALR   PW,PBUF             ADD BUFFER ORIGIN
         CLC   0(2,PW),=X'01A0'    CHECK FOR PRINT
         BNE   PRN8940             BRANCH IF NO
         CLC   3(2,PW),=X'8940'     AND SKIP TO CHNL 1
         BNE   PRN8940             BRANCH IF NO
         OI    PCCWORK,X'02'       CONVERT TO 8B
         SPACE 1                                               @OZ69106
***********************************************************************
*                                                                     *
*        UPDATE DATA SET PAGE COUNT                                   *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
PRN8940  DS    0H
         CLI   PCCWORK,X'E3'       TEST COMMAND VALIDITY
         BH    PRBADCC             BRANCH IF INVALID (HIGH)
         ICM   PW,8,PCCWORK        GET COMMAND CODE            @OZ69106
         SLL   PW,1                ISOLATE CHANNEL             @OZ69106
         SRL   PW,28                SKIP VALUE                 @OZ69106
         LA    R1,1                VALIDATE                    @OZ69106
         SLL   R1,0(PW)             CHANNEL                    @OZ69106
         STH   R1,$DOUBLE            SKIP                      @OZ69106
         NC    $DOUBLE(2),PFCBMAP     VALUE                    @OZ69106
         BZ    PRBADSKP            BRANCH IF NOT DEFINED       @OZ69106
         XC    PPLC,PPLC           CLEAR PAGE LINE COUNTER           R4
         TM    PPFLAG,PPDELSW      CHECK FOR SUSPENSION        @OZ29138
         BO    PPDSEND             BRANCH IF YES               @OZ29138
         CLI   PCCWORK,X'8B'       TEST FOR IMM SKIP TO CHAN 1 @OZ19494
         BNE   PRNPGCT             BRANCH IF NOT               @OZ19494
         L     R1,PCEDCT           GET DCT ADDRESS             @OZ19494
         TM    DCTPPFL-DCTDSECT(R1),DCTEJECT  IF ALREADY AT    @OZ19494
         BO    PRNOPRNT            TOP OF PAGE, DO NOT UPDATE  @OZ19494
PRNPGCT  L     PW,PDDBPGCT         UPDATE                      @OZ19494
         LA    PW,1(,PW)            DATASET
         ST    PW,PDDBPGCT           PAGE COUNT
         EJECT                                                 @OZ69106
***********************************************************************
*                                                                     *
*        PUSH DOWN BACKSPACE TABLE -- ADD NEW ENTRY                   *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
PUPDTBSP DS    0H                                                    R4
         CLI   PDEVTYP3,UCB3800    TEST FOR 3800 PRINTER       @G38ESBB
         BE    PSKPBSP             YES, BYPASS BSP TABLE CODE  @G38ESBB
         LH    R15,$BSPSIZ         IF NO BSP                         R4
         LTR   R15,R15              TABLE ENTRIES,                   R4
         BZ    PSKPBSP               BR TO CHECK SKIP COUNT          R4
         CLC   PDDBPGCT,PBSPGCT    IS THIS A FRAME PAGE
         BNE   PSKPBSP             BRANCH IF NO
         SLR   PW,PW               STEP TO                           R4
         IC    PW,$BSPGCT           NEXT                             R4
         AL    PW,PBSPGCT            PAGE FRAME                      R4
         ST    PW,PBSPGCT          SAVE FOR LATER
         LA    PW,PBSPTBL          POINT TO BACKSPACE TABLE          R4
         SH    R15,=H'8'           TABLE LENGTH - 1, LESS 1 ENTRY    R4
         BM    SKIP230             BR IF 1-ENTRY TABLE               R4
         EX    R15,PPBSPUSH         ELSE PUSH DOWN TABLE ENTRIES     R4
SKIP230  ALR   PW,R15              POINT TO LAST ENTRY - 1           R4
         MVC   1(4,PW),PCEJMTTR    SAVE BUFFER MTTR                  R4
         MVC   5(2,PW),PCEEJRCB    SAVE LINE RCB                     R4
         MVC   7(1,PW),PCEJBOFF    SAVE BUFFER OFFSET                R4
         B     PSKPBSP             THEN BR TO CHECK SKIP COUNT       R4
         SPACE 1                                                     R4
PPBSPUSH MVC   0(*-*,PW),7(PW)     *** EXECUTE ONLY ***              R4
         EJECT                                                 @OZ78256
PSKPBSP  CLC   PDDBSKIP,PDDBPGCT   COMPARE SKIP COUNT WITH PG COUNT  R4
         BNL   PSETEJ              BRANCH IF REPOSITIONING     @OZ19494
         TM    PCEID,PCEPRSID      TEST PROCESSOR TYPE
         BO    PPCHKPT             BRANCH IF PRINT
         L     R1,PCEDCT           GET ADDR OF RMT PRT/PCH DCT @OZ32566
         NI    DCTPPFL-DCTDSECT(R1),255-DCTEJECT CLR PCH LOGCL EJECT R4
         L     PW,PPLC             TEST FOR END OF                   R4
         CL    PW,PRLINECT          PUNCH LOGICAL PAGE               R4
         BNH   PRZCL               BRANCH IF NO
         XC    PPLC,PPLC           CLEAR PUNCH LOGICAL PAGE COUNT    R4
         OI    DCTPPFL-DCTDSECT(R1),DCTEJECT SET PUNCH LOGICAL EJECT R4
         B     PPCHKPT             GO UPDATE SMF PAGE COUNT    @OZ19494
         SPACE 1                                               @OZ19494
PSETEJ   TM    PPFLAG4,PPREPOBS    REPO FOR 3800 BSPACE...     @OZ78256
         BNO   PSETEJ1             CONTINUE IF NOT             @OZ78256
         SLR   R2,R2               ELSE, COUNT                 @OZ78256
         IC    R2,PREVCPYN          THE PAGES SKIPPED          @OZ78256
         AL    R2,PRPAGECT           DURING THE                @OZ78256
         ST    R2,PRPAGECT            BACKSPACE                @OZ78256
PSETEJ1  CLI   PCCWORK,X'8B'       TEST FOR IMM SKIP TO CHAN 1 @OZ78256
         BE    *+12                BRANCH IF YES               @OZ19494
         CLI   PCCWORK,X'89'       TEST FOR SKIP TO CHAN 1     @OZ19494
         BNE   PRNOPRNT            BRANCH IF NOT               @OZ19494
         L     R1,PCEDCT           GET DCT ADDRESS             @OZ19494
         OI    DCTPPFL-DCTDSECT(R1),DCTEJECT  AT TOP OF PAGE   @OZ19494
         B     PRNOPRNT            CONTINUE                    @OZ19494
         EJECT                                                       R4
***********************************************************************
*                                                                     *
*        NEW PAGE -- UPDATE TOTAL PAGE COUNT FOR THIS JOE             *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
*                                  THIS LINE DELETED BY APAR   @OZ19494
*                                  THIS LINE DELETED BY APAR   @OZ19494
*                                  THIS LINE DELETED BY APAR   @OZ19494
*                                  THIS LINE DELETED BY APAR   @OZ19494
PPCHKPT  DS    0H                                              @OZ19494
         SLR   PW,PW                                           @OZ19494
         IC    PW,PREVCPYN         UPDATE PAGE COUNT           @OZ35930
         AL    PW,PRPAGECT          WITH NUMBER OF COPIES      @OZ35930
         ST    PW,PRPAGECT           THIS TRANSMISSION         @OZ35930
*                                  THIS LINE DELETED BY APAR   @OZ29138
*                                  THIS LINE DELETED BY APAR   @OZ29138
         B     PPCKPGS             EXAMINE CKPTPGS             @OZ19494
         SPACE 1                                                     R4
************************************************************** @OZ19494
*                                                            * @OZ19494
*        EXAMINE CKPTLNS -- TEST FOR END OF LOGICAL PAGE     * @OZ19494
*                                                            * @OZ19494
*        NOTE: ROUTINE USED ONLY FOR PRINTERS.               * @OZ19494
*              CKPTLNS=PRLINECT FOR PUNCHES                  * @OZ19494
*                                                            * @OZ19494
************************************************************** @OZ19494
         SPACE 1                                               @OZ19494
PPCKLNS  DS    0H                                              @OZ19494
         LTR   JCT,JCT             IS THIS A SPOOLED MESSAGE   @OZ19494
         BZ    PRNOVFL             YES, NO TRUNCS OR CKPTS     @OZ19494
         LH    PW,PCKPTL           IF CKPTLNS=0, THEN LOG PAGE @OZ19494
         LTR   PW,PW                SIZE IS DETERMINED BY CHAN @OZ19494
         BZ    PRNOVFL                SKIPS AND LINE COUNT     @OZ19494
         SLR   PL,PL               CLEAR WORK REGISTER         @OZ19494
         IC    PL,PCCWORK          PICK UP COMMAND             @OZ19494
         SRL   PL,3                GET SPACE BITS, DECREMENT   @OZ19494
         SR    PW,PL                CKPTLNS BY NO. OF SPACES   @OZ19494
         STH   PW,PCKPTL           STORE NEW VALUE             @OZ19494
         BP    PRNOVFL             BR IF NOT AT LOG PAGE BNDRY @OZ19494
         CLC   PDDBSKIP,PDDBPGCT   COMPARE SKIP WITH PG COUNT  @OZ19494
         BH    PPCKPGS2            BRANCH IF REPOSITIONING     @OZ19494
         EJECT                                                 @OZ19494
************************************************************** @OZ19494
*                                                            * @OZ19494
*        EXAMINE CKPTPGS                                     * @OZ19494
*                                                            * @OZ19494
*        FOR REMOTES -- TRUNCATE BUFFERS AND SCHEDULE CKPT   * @OZ19494
*                       AT EACH CKPTPGS BOUNDARY             * @OZ19494
*                                                            * @OZ19494
*        FOR LOCALS  -- TRUNCATE CCW BUFFER, UPDATE COPY OF  * @OZ19494
*                       CKPT-JOE, AND SCHEDULE CHECKPOINT AT * @OZ19494
*                       EACH CKPTPGS BOUNDARY.               * @OZ19494
*                       FOR 3800, CREATE PQEC FOR CHECKPOINT * @G38ESBB
*                       INFORMATION                          * @G38ESBB
*                                                            * @OZ19494
*        NOTE: FOR LOCAL DEVICES,  IF CKPTS ARE NOT FORCED   * @OZ19494
*              THEN THE CCW BUFFER WILL NOT BE TRUNCATED.    * @OZ19494
*                                                            * @OZ19494
************************************************************** @OZ19494
         SPACE 1                                               @OZ19494
PPCKPGS  DS    0H                                              @OZ19494
         LH    PW,PCKPTP           GET CKPTPGS COUNTER         @OZ19494
         BCTR  PW,0                DECREMENT COUNTER           @OZ19494
         STH   PW,PCKPTP           STORE NEW VALUE             @OZ19494
         LTR   PW,PW               TEST COUNTER AND BRANCH     @OZ19494
         BNZ   PPCKPGS2             IF NOT AT CKPTPGS BOUNDARY @OZ19494
         MVC   PCKPTP,PCKPTPSV     RESET CKPTPGS COUNTER       @OZ19494
         TM    PCEID,PCELCLID      TEST PROCESSOR TYPE         @OZ19494
         BO    PLCLCKPT            DO NOT TRUNCATE IF LOCAL    @OZ19494
         OI    PPFLAG3,PPTRUNC     SET TRUNC REQUIRED FLAG     @OZ19494
         TM    PCEID,PCERJEID      TEST PROCESSOR TYPE         @OZ19494
         BO    PPCKPGS2            BRANCH IF REMOTE            @OZ19494
*              THIS LINE DELETED BY APAR NUMBER                @OZ19494
*              THIS LINE DELETED BY APAR NUMBER                @OZ19494
         SPACE 1                                                     R4
PLCLCKPT DS    0H                                                    R4
         CLI   PDEVTYP3,UCB3800    TEST FOR 3800 PRINTER       @G38ESBB
         BNE   PLCKPT1             NO, BRANCH                  @G38ESBB
         TM    PPFLAG,PPNEWS       NEWS DATA SET               @G38ESBB
         BO    PPCKPGS2            YES, BYPASS PQEC ALLOCATION @G38ESBB
         OI    PPFLAG3,PP38CKPT    SET FLAG FOR PQEC ALLOC     @OZ51592
         B     PPCKPGS2             AND CONTINUE               @OZ51592
         SPACE 1                                               @OZ51592
         USING PQHDSECT,PW         SET PQH ADDRESSABILITY      @OZ48003
PCKP3800 NI    PPFLAG3,FF-PP38CKPT RESET CHECKPOINT FLAG       @OZ51592
         L     PW,PQHADR           ADDRESS PQH                 @OZ48003
         OI    PQHAFLAG,PQHALOC    SET ALLOCATION FLAG         @OZ48003
         L     R15,=A(PQECINIT)    CALL PQECINIT TO            @G38ESBB
         BALR  PL,R15               ALLOCATE PQEC              @G38ESBB
         BZ    PPDONE              BRANCH IF NOT SUCCESSFUL    @OZ48003
         DROP  PW                  SUSPEND PQH ADDRESSABILITY  @OZ48003
         SPACE 1                                               @OZ48003
         L     R15,=A(PPGIDIO)     CALL PPGIDIO TO SOLICIT     @G38ESBB
         BALR  PL,R15               ID FOR PQEC                @G38ESBB
         B     PRNOPRNT            GO REJOIN COMMON CODE       @OZ51592
         EJECT                                                 @OZ48003
PLCKPT1  L     R1,POUTCCWA         GET POINTER                 @G38ESBB
         AH    R1,PCCWLAST          TO CKPT-JOE MINUS PCIESIZE       R4
         OI    PCISGNAL-PCIDSECT(R1),PCICKPT  SHOW NEW CKPT PRESENT  R4
         MVC   PCIESIZE(L'PCKPT,R1),PCKPT UPDATE COPY OF CKPT-JOE    R4
         SPACE 1                                               @OZ19494
PPCKPGS2 MVC   PCKPTL,PCKPTLSV     RESET CKPTLNS COUNTER       @OZ19494
         EJECT                                                       R4
***********************************************************************
*                                                                     *
*        IF &PRTRANS=YES -- TRANSLATE ALL UNPRINTABLES TO BLANKS      *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
PRNOVFL  CLC   PDDBSKIP,PDDBPGCT   COMPARE SKIP COUNT WITH PAGE COUNT
         BNH   PRNTRANS            DON'T PRINT IF HIGH         @OZ19494
         L     R1,PCEDCT           GET DCT ADDRESS             @OZ19494
         NI    DCTPPFL-DCTDSECT(R1),255-DCTEJECT RESET CH 1 SW @OZ19494
         B     PRNOPRNT            CONTINUE                    @OZ19494
         SPACE 1                                               @OZ19494
PRNTRANS TM    $PRTOPTS,$PRTRANS   TEST FOR PRINT TRANSLATE    @OZ19494
         BZ    PRZCL               BR IF NO                          R4
         TM    PCEID,PCEPUSID      TEST PROCESSOR TYPE
         BO    PRZCL               BRANCH IF PUNCH
         CLC   PDEVTYPE+2(2),=AL1(UCB3UREC,UCB3211)  LOCAL 3211...   R4
         BE    PRZCL               BRANCH IF YES
         CLC   PDEVTYPE+2(2),=AL1(UCB3UREC,UCB3800)  3800 PRINTER... R4
         BE    PRZCL               BRANCH IF YES                     R4
         CLC   PDEVBYT2(2),=AL1(UCB3UREC,UCB3203)  LOCAL 3203  @OZ40627
         BE    PRZCL               BRANCH IF A 3203            @OZ40627
         CLM   PC2,2,=X'00'        LENGTH EXCEED MAXIMUM...    @OZ35113
         BNE   *+12                BR IF YES                   @OZ35113
         CLM   PC2,1,=X'00'        IS THE COUNT ZERO
         BE    PRZCL               BRANCH IF YES - SKIP TRANSLATE
         LA    R1,0(PC1,PC2)       R1 = ADDRESS OF NEXT RCB
         SLR   R1,PBUF             GENERATE OFFSET OF RCB
         CH    R1,$BUFLENG         TEST FOR END OF BUFFER            R4
         BNL   PRZCL               BRANCH IF OUT OF BUFFER
         LA    R1,X'FF'(PC2)       R1 = COMMAND BYTE COUNT - 1
         L     R15,=A(PTRTBL)      TRANSLATE UNPRINTABLES      @OZ32776
         CLM   PC2,2,=X'00'        LENGTH EXCEED MAXIMUM...    @OZ35113
         BE    *+8                 BR IF NO                    @OZ35113
         ICM   R1,3,=X'00FE'       SET LENGTH TO MAXIMUM       @OZ35113
         EX    R1,PRXTR             TO BLANKS                  @OZ32776
         EJECT                                                       R4
***********************************************************************
*                                                                     *
*        IF OPTCD=J -- SELECT APPROPRIATE 3800 TRANSLATE TABLE        *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
PRZCL    L     PC1,PCCWORK         PICK UP MODIFIED COMMAND          R4
         TM    PPFLAG2,PPOPTJ      TEST FOR OPTCD=J SPECIFIED        R4
         BZ    PRCHAIN             BRANCH IF NO                      R4
         CL    PC2,PCCW+4          BYPASS IF FIRST PASS FOR         R41
         BE    PRCHAIN              ASA CC (OR INVALID)             R41
         CLI   PDEVTYPE+3,UCB3800  TEST FOR 3800 PRINTER            R41
         BNE   PRZCL2              BR IF OPTCD=J NOT SUPPORTED      R41
         NI    0(PC1),X'0F'        USE BINARY PART OF TRC            R4
         SLR   R1,R1               PICK-UP                          R41
         CLC   PRMAXTRC,0(PC1)      TABLE REF CHAR                  R41
         BL    PRCHKTRC              OR USE ZERO                    R41
         IC    R1,0(PC1)              IF INVALID                    R41
         SPACE 1                                                    R41
PRCHKTRC LA    R1,PXTABCCW(R1)     ADDR OF SELECT CCW OP CODE       R41
         CLC   PRTRCCW,0(R1)       TABLE ALREADY SELECTED...        R41
         BE    PRZCL2              SKIP UNNECESSARY CCW IF YES      R41
         STM   PC1,PC2,PBLKWORK    SAVE 'DATA' CCW                  R41
         LM    PC1,PC2,PRCCWEJ     CONSTRUCT SELECT-                R41
         ICM   PC1,8,0(R1)          TRANSLATE-TABLE CCW             R41
         BAL   PL,PPPUT2           ADD CCW TO CHAIN            @OZ51441
         STCM  PC1,8,PRTRCCW       SAVE NEW SELECT CCW OP CODE      R41
         LM    PC1,PC2,PBLKWORK    RESTORE 'DATA' CCW               R41
         SPACE 1                                                    R41
PRZCL2   AL    PC1,=F'1'           ACCOUNT FOR TABLE ID              R4
         SL    PC2,=F'1'           ACCOUNT FOR TABLE ID              R4
         SPACE 1                                                    R41
*              THIS LINE DELETED BY APAR NUMBER                @OZ25297
*              THIS LINE DELETED BY APAR NUMBER                @OZ25297
*              THIS LINE DELETED BY APAR NUMBER                @OZ25297
*              THIS LINE DELETED BY APAR NUMBER                @OZ25297
*              THIS LINE DELETED BY APAR NUMBER                @OZ25297
*              THIS LINE DELETED BY APAR NUMBER                @OZ25297
         EJECT                                                 @OZ19494
***********************************************************************
*                                                                     *
*        ADD NEW PRINT/PUNCH COMMAND TO CCW CHAIN                     *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
PRCHAIN  DS    0H                                                    R4
         BAL   PL,PPPUT            ADD CCW TO CHAIN
         TM    PPFLAG3,PP38CKPT    3800 PQEC NEEDED...         @OZ51592
         BO    PCKP3800            BRANCH IF YES               @OZ51592
         TM    PPFLAG3,PPTRUNC     BUFFER TRUNC REQUIRED       @OZ19494
         BZ    PPPFUNCI            BRANCH IF NO                @OZ19494
         TM    PCEID,PCELCLID      TEST PROCESSOR TYPE         @OZ19494
         BO    PRCHAINL            BRANCH IF LOCAL             @OZ19494
         OI    PPFLAG2,PPCKPT      SET CKPT NEEDED FLAG        @OZ19494
         LA    R1,PCKPT            POINT TO CHECKPOINT DATA    @OZ19494
         ST    R1,PPCKPTR          SAVE FOR PPPCKPT            @OZ19494
         ICM   PC1,8,=X'FF'        BUFFER TRUNC COMMAND        @OZ19494
         BAL   PL,PPPUT2           FORCE RTAM TO FLUSH BUFFERS @OZ19494
*                                  AND WAIT FOR RESPONSE       @OZ19494
         NI    PPFLAG3,255-PPTRUNC RESET TRUNC REQUIRED FLAG   @OZ19494
         B     PRNOPRNT            CONTINUE                    @OZ19494
         SPACE 1                                               @OZ19494
PRCHAINL BAL   PL,PPWRITE          SCHEDULE CCW BUFR FOR I/O   @OZ19494
         NI    PPFLAG3,255-PPTRUNC RESET TRUNC REQUIRED FLAG   @OZ19494
         EJECT                                                 @OZ19494
***********************************************************************
*                                                                     *
*        IF FUNC=I -- PRODUCE APPROPRIATE 3525 PRINT-LINE CCW'S       *
*                                                                     *
*        LINE 1 -- CARD COLS.  1-64 -- STARTING IN PRINT POSITION 1   *
*        LINE 3 -- CARD COLS. 65-80 -- STARTING IN PRINT POSITION 49  *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
PPPFUNCI DS    0H                                              @OZ19494
         TM    PPFLAG,PPFUNCI      INTERPRET REQUESTED
         BNO   PRNOPRNT            BRANCH IF NO
         CLI   PDEVTYPE+3,X'0C'    IS DEVICE A 3525
         BNE   PRNOPRNT            BRANCH IF NO
         TM    PDEVTYPE+1,X'30'    IS EITHER PRINT FEATURE IN
         BZ    PRNOPRNT            BRANCH IF NO
         SPACE 1                                                     R4
         OI    PSMFDCI,SMFPUPRT    SET 3525 INTERPRET SMF FLAG       R4
         SPACE 1                                                     R4
         STM   PC1,PC2,PCCWORK     SAVE ORIGINAL PUNCH CCW
         ICM   PC1,8,=X'0D'        CONVERT CCW TO PRINT LINE 1
         BAL   PL,PPPUT            ADD CCW TO CHAIN
         CLC   PCCWORK+6(2),=H'64' IS TEXT OVER 64 CHARACTERS...     R4
         BNH   PRNOPRNT            BR IF NOT                         R4
         L     PL,PCCWPT           PICK UP CURRENT CCW POINTER
         LA    PL,2*8(,PL)         VALUE AFTER 2 CCW'S ADDED         R4
         SL    PL,POUTCCWA         CONVERT TO CCW CHAIN OFFSET       R4
         CH    PL,PCCWLAST         IS THERE ROOM IN CHAIN            R4
         BL    *+8                 BRANCH IF YES
         BAL   PL,PPWRITE          FORCE CCW CHAIN TO BE CLEARED
         LM    PC1,PC2,PUSPACCW    LOAD DATA CHAIN PRINT LINE 3
         BAL   PL,PPPUT            ADD CCW TO CHAIN
         LM    PC1,PC2,PCCWORK     RESTORE ORIGINAL PUNCH CCW
         LA    PC1,0(,PC1)         CLEAR CCW COMMAND
         SH    PC2,=H'64'          COMPUTE RESIDUAL COUNT OVER 64
         AH    PC1,=H'64'          COMPUTE NEW STARTING ADDRESS
         ICM   PC1,8,=X'1D'        CONVERT CCW TO PRINT LINE 3
         BAL   PL,PPPUT            ADD CCW TO CHAIN
         LM    PC1,PC2,PCCWORK     RESTORE ORIGINAL PUNCH CCW
         EJECT                                                       R4
***********************************************************************
*                                                                     *
*        RE-CYCLE RECORD IF CARRIAGE CONTROL WAS ASA                  *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
PRNOPRNT TM    PPFLAG3,PP3800R     TEST FOR 3800 RESTART       @G38ESBB
         BZ    PRECYCLE            BR IF NOT                   @G38ESBB
         L     R15,=A(PMAPFCB)     ADDRESS MAP FCB ROUTINE     @G38ESBB
         BALR  PL,R15              CALL 3800 FCB MAPPING RTN   @G38ESBB
         BNZ   PPDONE              BR TO TERMINATE             @G38ESBB
         SPACE 1                                               @G38ESBB
PRECYCLE TM    PCEID,PCEPUSID      IS THIS A PUNCH PROCESSOR   @G38ESBB
         BO    PNXTRCB             BRANCH IF YES
         LH    PW,PCEEJRCB         GET LRC DISPLACEMENT
         ALR   PW,PBUF             ADD BUFFER ORIGIN
         MVI   PCCWORK,X'01'       SET CC TO WRITE NO-SPACE
         LM    PC1,PC2,PCCWORK     RESTORE CCW REGISTERS
         TM    1(PW),LRC1CCTL+LRC1TASA ASA CARRIAGE CONTROL
         BNO   PNXTRCB             BRANCH IF NO
         NI    1(PW),255-LRC1TASA  RESET ASA BIT
         B     PFSPCK              START CCW ANALYSIS
         SPACE 1                                                     R4
***********************************************************************
*                                                                     *
*        STEP TO NEXT LOGICAL RECORD CONTROL BLOCK                    *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
PNXTRCB  LA    PC1,0(PC1,PC2)      TEXT ADDRESS + TEXT LENGTH        R4
         LR    R1,PC1              COPY NEXT RCB ADDRESS
         SLR   R1,PBUF             GENERATE OFFSET OF RCB
         CH    R1,$BUFLENG         TEST FOR END OF BUFFER            R4
         BL    PNXTCCW             BRANCH IF RCB IS IN BUFFER
         EJECT                                                 @OZ46351
***********************************************************************
*                                                                     *
*        END OF DATA BUFFER                                           *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
PCPEND   ICM   R15,15,HDBNXTRK     LOAD AND TEST CHAIN TRACK         R4
         BNZ   PSAVCHN             BRANCH IF NOT END OF DS     @G38ESBB
PCPERR   TM    PPFLAG3,PP3800R     3800 REPOSITIONING...       @OZ56911
         BZ    PPDSEND             BR IF NOT TO TERMINATE      @G38ESBB
         L     PW,PQHADR           GET PQH ADDRESS             @G38ESBB
         L     R0,PQHMAPV-PQHDSECT(,PW) GET FCB MAPPING VALUE  @OZ53047
         LTR   R0,R0               TEST MAPPING VALUE          @OZ51936
         BNM   PCALLRCP            BR IF NOT $F,D              @G38ESBB
         AH    R0,=H'2'            ADD 1 FOR MSG PAGE, AND     @G38ESBBC
                                   ADD 1 FOR -1 MAPPING ORIGIN @G38ESBB
         SPACE 1                                               @G38ESBB
PCALLRCP L     R15,=A(PRECOMP)     CALL RECOMPUTE ROUTINE      @G38ESBB
         BALR  PL,R15               TO ADJUST FOR COMMAND      @G38ESBB
         MVC   PQHMAPV-PQHDSECT(,PW),$ZEROS ZERO MAPPING CNT   @OZ51936
         ICM   R1,15,PQHFCB-PQHDSECT(PW) GET FCB BUFFER ADDR   @OZ51936
         BZ    PNOFCBUF            BRANCH IF NONE              @OZ51936
        $FREEBUF (R1)              FREE FCB BUFFER             @OZ51936
         MVC   PQHFCB-PQHDSECT(,PW),$ZEROS CLEAR BUFFER ADDR   @OZ51936
PNOFCBUF NI    PPFLAG3,FF-PP3800R  RESET 3800 REPRO INDICATOR  @OZ51936
         L     R1,PQHLAST-PQHDSECT(,PW)  GET LAST PQE          @OZ46351
         CR    R1,PW               TEST FOR EMPTY PPQ          @OZ46351
         BE    PRSTSKP             BRANCH IF YES               @OZ46351
         USING PQEDSECT,R1         SET PQE ADDRESSABILITY      @OZ46351
         TM    PQECFLAG,PQECLPG    TEST FOR LAST PAGE PQEC     @OZ46351
         BZ    PPDSEND             BRANCH IF NOT               @OZ46351
         L     R1,PQECPQED         POINT TO PQED               @OZ46351
         TM    PQEDFLAG,PQEDLAST   TEST FOR LAST DATASET       @OZ46351
         BZ    PPDSEND             BRANCH IF NOT               @OZ46351
         DROP  R1                  SUSPEND PQE ADDRESSABILITY  @OZ46351
PRSTSKP  XC    PDDBSKIP,PDDBSKIP   ZERO SKIP COUNT             @OZ46351
         B     PPDONE              BRANCH TO TERMINATE         @OZ46351
         SPACE 1                                               @G38ESBB
PSAVCHN  ST    R15,PCEJMTTR        SAVE CHAIN TRACK FOR CKPT   @G38ESBB
         MVC   PCEEJRCB,PCCW+2     SET 1ST RCB DISPLACEMENT    @OZ19494
         TM    PPFLAG2,PPTCEL      TEST DE-SPOOLING METHOD           R4
         BZ    PENDTCEL             BRANCH IF SINGLE                 R4
         SLR   PL,PL               INCREMENT                         R4
         IC    PL,PCEJBOFF          BUFFER OFFSET                    R4
         LA    PL,1(,PL)             WITHIN THIS                     R4
         STC   PL,PCEJBOFF            TRACK-CELL                     R4
         L     R1,PBUFADDR         ADDR OF FIRST BUFFER IN CHAIN     R4
         CH    PL,BUFCHNCT-BUFDSECT(,R1) TEST FOR END OF TRACK-CELL  R4
         BNL   PENDTCEL            BRANCH IF YES                     R4
         L     PBUF,BUFCHAIN       ESTABLISH ADDRESSABILITY ON       R4
         B     P1STBLK              NEXT BUFFER AND GO PROCESS @OZ19494
         EJECT                                                       R4
***********************************************************************
*                                                                     *
*        PROCESSING COMPLETE FOR CURRENT BUFFER OR TRACK-CELL         *
*                                                                     *
*        SETUP TO DE-SPOOL NEXT DATA BUFFER(S)                        *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
PENDTCEL DS    0H                                                    R4
         TM    PCEID,PCERJEID      TEST PROCESSOR TYPE               R4
         BO    SKIP250             BR IF REMOTE                      R4
         CLC   PDDBSKIP,PDDBPGCT   COMPARE SKIP AND PAGE COUNTERS    R4
         BNH   PNOTSKIP            BR IF SKIP COUNT NOT HIGH         R4
SKIP250  IC    R1,PBFAVAIL         INCREMENT NUMBER                  R4
         LA    R1,1(,R1)            OF AVAILABLE                     R4
         STC   R1,PBFAVAIL           INPUT BUFFERS                   R4
         B     PCHREAD             GO FOR NEXT INPUT BUFFER          R4
         SPACE 1                                                     R4
***********************************************************************
*                                                                     *
*        INDICATE IN PCIE THAT THE FINAL BUFFER HAS BEEN PROCESSED    *
*                                                                     *
* NOTE - WHEN THE OUTPUT FOR THIS BUFFER IS COMPLETE, PPCHECK WILL    *
*        INCREMENT THE AVAILABLE BUFFER COUNT  (PBFAVAIL)             *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
PNOTSKIP DS    0H                                                    R4
         L     R1,POUTCCWA         IF CCW AREA IS EMPTY,             R4
         CL    R1,PCCWPT            INSERT A NOP                     R4
         BNH   PSETFINL              TO ASSURE                       R4
         LM    PC1,PC2,PCCWNOP        INDICATION IS                  R4
         ICM   PC2,B'1000',=X'60'      SEEN BY                       R4
         BAL   PL,PPPUT                 PPCHECK                      R4
         L     R1,POUTCCWA         RELOAD CCW AREA ADDRESS           R4
         SPACE 1                                                     R4
         USING PCIDSECT,R1         INDICATE THAT EXECUTION           R4
PSETFINL AH    R1,PCCWLAST          OF THIS CCW AREA                 R4
         OI    PCISGNAL,PCIFNLBF     WILL CAUSE A DATA               R4
         DROP  R1                     BUFFER TO BECOME AVAILABLE     R4
         SPACE 1                                                     R4
         BAL   PL,PPWRITE          FORCE CCW AREA SWAP               R4
         SPACE 1                                                     R4
***********************************************************************
*                                                                     *
*        IF READ HAS ALREADY BEEN STAGED -- GO WAIT FOR COMPLETION    *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
PCHREAD  TM    PPFLAG2,PPRSW       TEST FOR READ IN PROGRESS         R4
         BO    PCHKNBLK            BRANCH IF YES                     R4
         EJECT                                                       R4
***********************************************************************
*                                                                     *
*        IF ANY BUFFERS ARE AVAILABLE  -- STAGE NEXT READ             *
*        ELSE -- WAIT FOR PPCHECK TO MAKE A BUFFER AVAILABLE          *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
PCHAVAIL CLI   PBFAVAIL,0          TEST FOR AVAILABLE INPUT BUFFER   R4
         BH    PSTGREAD            STAGE NEXT READ IF YES            R4
         TM    PPFLAG,PPWSW        TEST FOR WRITE IN PROGRESS        R4
         BZ    PLOSTINT            BRANCH IF NO                      R4
         BAL   PL,PCIWAIT          WAIT FOR OUTPUT I/O               R4
         B     PCHAVAIL             AND TRY AGAIN                    R4
         SPACE 1                                                     R4
PLOSTINT IC    R1,PBUFOPT          RESET AVAILABLE INPUT BUFFER      R4
         STC   R1,PBFAVAIL          COUNT TO BUFFERING OPTION        R4
        $COUNT                     * RECORD USAGE *                  R4
         SPACE 1                                                     R4
***********************************************************************
*                                                                     *
*        BEGIN THE NEXT DE-SPOOLING OPERATION -- GO WAIT COMPLETION   *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
PSTGREAD BAL   PL,PRDTCNXT         STAGE READ FOR NEXT BUFFER(S)     R4
        $COUNT                     ********************************  R4
         B     PCHKNBLK            GO CHECK AND PROCESS IT           R4
         TITLE 'HASP PRINT/PUNCH SERVICE -- DATA SET TERMINATION'
***********************************************************************
*                                                                     *
*        ***  DATA SET TERMINATION  ***                               *
*                                                                     *
*        SPECIAL TERMINATION FOR RMT SPOOLED MESSAGES AND JES2-NEWS   *
*                                                              @G38ESBB
*        DETERMINE IF 3800 COMMAND ISSUED AND CALL COMMAND     @G38ESBB
*        ROUTINE TO PROCESS ACCORDINGLY.                       @G38ESBB
*                                                                     *
***********************************************************************
         SPACE 1                                               @G38ESBB
PPDSEND  CLI   PDEVTYP3,UCB3800    TEST FOR 3800 PRINTER       @G38ESBB
         BNE   PPDSEND1            BR IF NOT                   @G38ESBB
         CLC   PSAVAREA,$ZEROS     SAVE AREA = LEVEL 0...      @G38ESBB
         BE    PPCHKNEW            YES, BRANCH                 @G38ESBB
         MVC   PSAVAREA,PSAV1ST    RESTORE FIRST LEVEL SAVE    @G38ESBB
         L     R1,PSAVAREA         ADDRESS FIRST SAVE AREA     @G38ESBB
         CLI   0(R1),PSAVEALL      SAVE ALL SPECIFIED...       @G38ESBB
         BE    PRETLNG             YES, BRANCH                 @G38ESBB
         MVC   2*4(4,R1),=A(PPCHKNEW) RETURN TO HERE           @G38ESBB
         PRETURN ,                 RESTORE REGS AND RETURN     @G38ESBB
         SPACE 1                                               @G38ESBB
PRETLNG  MVC   4+PL*4(4,R1),=A(PPCHKNEW) RETURN TO HERE        @G38ESBB
         PRETURN ,                 RESTORE REGS AND RETURN     @G38ESBB
         SPACE 1                                               @G38ESBB
PPCHKNEW TM    PPFLAG,PPNEWS       NEWS DATA SET...            @G38ESBB
         BO    PPDSEND1            YES, BYPASS PQE PROCESSING  @G38ESBB
         TM    PPFLAG3,PP3800R     3800 COMMAND PROCESSING     @G38ESBB
         BZ    PPDSEND0            BR IF NOT                   @G38ESBB
         L     R15,=A(P3800CMD)    CALL 3800 COMMAND           @G38ESBB
         BALR  PL,R15                   PROCESSING             @G38ESBB
         USING PQHDSECT,PW         PROVIDE PQH ADDRESSABILITY  @G38ESBB
         TM    PPFLAG,PRDELSW      WAS CMD FOR TERMINATION     @G38ESBB
         BZ    PTSTFD              BR IF NOT                   @G38ESBB
         L     PW,PQHADR           ADDRESS PQH                 @G38ESBB
         NI    PPFLAG,FF-PRDELSW   RESET TERMINATION INDIC     @G38ESBB
         CLC   PQHMAPV,$ZEROS      MAPPING NEEDED...           @G38ESBB
         BE    PPDETERM            NO,GO DETERMINE TERM ACTION @G38ESBB
         B     PENDINIT            BR TO RESTART POINT         @G38ESBB
         SPACE 1                                               @G38ESBB
PPDETERM TM    PQHFLAG,PQH2CMD     TEST FOR DOUBLE COMMAND     @G38ESBB
         BO    PPABORT             ABORT JOB IF YES            @G38ESBB
         B     PPDONE              OTHERWISE, GO TERMINATE     @G38ESBB
         SPACE 1                                               @G38ESBB
PPDSEND2 NI    PCKJOE,FF-$JOECKV   RESET WARMSTART BIT         @OZ51866
         B     PPDSEND1            CONTINUE D.S. TERMINATION   @OZ51866
         EJECT                                                 @OZ51866
PTSTFD   L     PW,PQHADR           GET PQH ADDRESS             @G38ESBB
         CLC   PQHMAPV,PFDSET      NEED TO GET NEW DS          @G38ESBB
         BNE   PENDINIT            BR IF NOT TO RESTART        @G38ESBB
         XC    PQHMAPV,PQHMAPV     ELSE, CLEAR MAPPING VALUE   @OZ56969
         B     PENDINIT            BRANCH TO RESTART DATASET   @OZ56969
         EJECT                                                 @OZ48003
PPDSEND0 L     R15,=A(PQECINIT)    CREATE PQEC FOR             @G38ESBB
         BALR  PL,R15               END OF DATA SET            @G38ESBB
         BZ    PPDONE              BRANCH IF NOT SUCCESSFUL    @OZ48003
         L     R15,=A(PPGIDIO)     CALL PPGIDIO TO SOLICIT     @G38ESBB
         BALR  PL,R15               ID FOR PQE                 @G38ESBB
         L     PW,PQHADR           ADDRESS PQH                 @G38ESBB
         L     R1,PQHLAST          ADDRESS LPG PQEC            @G38ESBB
         OI    PQECFLAG-PQEDSECT(R1),PQECLPG SET END OF DS     @G38ESBB
         SPACE 1                                               @G38ESBB
         DROP  PW                  DROP PQH ADDRESSABILITY     @G38ESBB
         SPACE 1                                               @G38ESBB
PPDSEND1 BAL   PL,PPWRITE          FORCE EXEC OF LAST CCW AREA @G38ESBB
         LTR   JCT,JCT             IS THIS A SPOOL MESSAGE PRINTER
         BZ    PRSMBEOB            BRANCH IF YES
         TM    PPFLAG,PPNEWS       WAS THIS JES2-NEWS DATA SET...   R41
         BO    PENDNEWS            RETURN TO MAIN LINE IF YES       R41
         TM    PCEID,PCELCLID      TEST PROCESSOR TYPE         @OZ78498
         BO    PPDSEND3            BRANCH IF LOCAL             @OZ78498
         ICM   PC1,B'1000',=X'FF'  SEND BUFFER TRUNCATE        @OZ78498
         BAL   PL,PPPUT2            AND WAIT FOR RESP          @OZ78498
         SPACE 1                                               @OZ78498
***********************************************************************
*                                                                     *
*        WRITE PROGRAMMER MESSAGE IF DATA SET I/O OR VALIDITY ERROR   *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
PPDSEND3 NI    PPFLAG2,FF-PPCKPTA  SUSPEND CHECKPOINTS         @OZ78498
         TM    PPFLAG,PPRDERR      DATA BUFFER READ ERROR
         BNO   PPIOTCK             BRANCH IF NO
        $MID   185                                                   R4
         LA    R1,=C'$HASP185 '    MESSAGE ID                        R4
         LA    R14,=C' TERMINATED '  MESSAGE TEXT              @OZ48259
         L     R15,=A(PRMSG)       ISSUE MSG TO OPERATOR       @OZ19494
         BALR  PL,R15               AND ADD IT TO OUTPUT       @OZ48259
         SPACE 1                                                     R4
         OI    PPFLAG2,PSMFDSER    SET DATA SET ERROR FLAG FOR SMF   R4
         SPACE 1                                                     R4
***********************************************************************
*                                                                     *
*        RE-READ IOT                                                  *
*                                                                     *
*        SEE IF IOT IS ALREADY BEING DE-SPOOLED                       *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
PPIOTCK  DS    0H                                                    R4
         NI    PPFLAG,255-PPRDERR  CLEAR ERROR CONDITION, IF ANY     R4
         TM    PPFLAG2,PPRSW       TEST FOR READ IN PROGRESS         R4
         BZ    PPIOTCK1            BR IF NOT                         R4
         CLC   PCESEEK(4),PCEIOTTR TEST FOR IOT READ IN PROGRESS     R4
         BE    PPIOTCK3            BR IF YES                         R4
         BAL   PL,PRDTCHK           ELSE, CLEAR INPUT I/O            R4
         EJECT                                                       R4
***********************************************************************
*                                                                     *
*        SEE IF ANY BUFFERS ARE AVAILABLE FOR DE-SPOOLING THE IOT     *
*                                                                     *
*        NOTE - IF NONE AVAILABLE -- WAIT FOR PPCHECK TO FREE ONE     *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
PPIOTCK1 CLI   PBFAVAIL,0          TEST FOR AVAILABLE INPUT BUFFER   R4
         BH    PPIOTCK2            BRANCH IF YES                     R4
         TM    PPFLAG,PPWSW        TEST FOR WRITE OUTSTANDING        R4
         BZ    PPIOTCK2            BRANCH IF NO                      R4
         BAL   PL,PCIWAIT          WAIT FOR AVAILABLE INPUT BUFFER   R4
         B     PPIOTCK1             AND TRY AGAIN                    R4
         SPACE 1                                                     R4
***********************************************************************
*                                                                     *
*        BUFFER AVAILABLE -- READ AND VERIFY THE IOT                  *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
PPIOTCK2 L     R15,PCEIOTTR        GET IOT TRACK ADDRESS             R4
         BAL   PL,PRDBUF           INITIATE READ OF IOT              R4
PPIOTCK3 BAL   PL,PRDCHK           CHECK READ                        R4
         TM    PPFLAG,PRDELSW      TEST FOR TERMINATION
         BO    PPDONE              BRANCH IF YES
         LR    JCT,PBUF            ADDRESS IOT IN BUFFER
         TM    PPFLAG,PPRDERR      TEST FOR I/O ERROR ON READ
         BO    PIOTPOST            BR IF YES                         R4
         CLC   IOTJBKEY,PPJOBKEY   IS THIS IOT VALID
         BE    PRSUSTST            BRANCH IF YES
         SPACE 1                                                     R4
***********************************************************************
*                                                                     *
*        IF INPUT OR VALIDITY ERROR -- TELL OPERATOR AND KILL OUTPUT  *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
PIOTPOST $DISTERR                  INDICATE CONTROL BLOCK ERROR
         OI    PPFLAG,PPJCTIOT+PRDELSW REASON FOR TERMINATION
         B     PPDONE              ABORT JOB
         EJECT
***********************************************************************
*                                                                     *
*        IF $F OR $B COMMAND -- INFORM OPERATOR AND PROGRAMMER        *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
PRSUSTST BAL   PL,PPCHECK          FINAL I/O. CHK OPER CMDS    @OZ29138
         TM    PPFLAG,PRDELSW      CHECK FOR TERMINATION       @OZ49577
         BO    PPDONE              BRANCH IF YES               @OZ49577
         TM    PPFLAG,PPDELSW      CHECK FOR SUSPENSION        @OZ29138
         BZ    PRLOGCK             BRANCH IF NO
         NI    PPFLAG,255-PPDELSW  RESET SUSPEND FLAG
         MVC   PCKPTP,PCKPTPSV     RESET CKPTPGS COUNTER       @OZ19494
         MVC   PCKPTL,PCKPTLSV     RESET CKPTLNS COUNTER       @OZ19494
         MVC   PCEFORM(4),PDDBPGCT SAVE CURRENT PAGE COUNT     @OZ19494
         USING DCTDSECT,R1         DCT ADDRESSABILITY          @OZ19494
         L     R1,PCEDCT           GET DCT ADDRESS             @OZ19494
         TM    DCTPPSW2,DCTBFCKP   REPOSITION FROM LAST CKPT   @OZ19494
         BZ    PPADJUST            BRANCH IF NO                @OZ19494
         NI    DCTPPSW2,255-DCTBFCKP  RESET $B/$F CKPT FLAG    @OZ19494
         DROP  R1                  SUSPEND DCT ADDRESSABILITY  @OZ19494
         SPACE 1                                               @OZ19494
         USING JOEDSECT,R1         CKPT-JOE ADDRESSABILITY     @OZ19494
         L     R1,PCKJOE           GET CKPT-JOE ADDRESS        @OZ19494
         CLC   PDDBDISP,JOEPDDB    INSURE THAT                 @OZ19494
         BNE   PRSUSTS1             AT LEAST ONE CHECKPOINT    @OZ19494
         CLC   PCEIOTTR,JOEIOTTR     HAS BEEN TAKEN FOR        @OZ19494
         BNE   PRSUSTS1               THE CURRENT DATASET      @OZ19494
         CLC   PDDBPGCT,JOEPPCT    COMPARE CURRENT PAGE COUNT  @OZ19494
         BNL   PRSUSTS2            WITH CKPT BR NOT LESS       @OZ19494
PRSUSTS1 L     PL,PMAXPAGE         IF NO CKPT FOR THE DATASET  @OZ19494
         LNR   PL,PL                OR THE CURRENT PAGE COUNT  @OZ19494
         ST    PL,PFSBSCT            IS LESS THAN THE CKPT     @OZ19494
         B     PPADJUST               PAGE COUNT THEN $B DSET  @OZ19494
         SPACE 1                                               @OZ19494
PRSUSTS2 MVC   PCEJMTTR,JOEMTTR    REFRESH                     @OZ19494
         MVC   PCEEJRCB,JOEJRCB     PPWORK FIELDS FROM         @OZ19494
         MVC   PDDBPGCT,JOEPPCT      LAST CKECKPOINT           @OZ19494
         DROP  R1                  SUSPEND JOE ADDRESSABILITY  @OZ19494
         EJECT                                                 @OZ19494
PPADJUST L     R1,PDDBPGCT         GET CURRENT PAGE COUNT      @OZ19494
         L     R15,PFSBSCT         GET $B/$F PAGES             @OZ19494
         LTR   R15,R15             DETERMINE IF $B OR $F       @OZ19494
         BP    PPFWD               BRANCH IF $F                @OZ19494
         LA    R14,=C' BACKSPACED ' SET UP FOR $B MESSAGE      @OZ48259
         AR    R1,R15              BACKUP PAGE COUNT           @OZ19494
         BNM   PSETPAGE            BRANCH IF VALID PAGE COUNT  @OZ19494
         SLR   R1,R1               SETUP FOR $B,D              @OZ19494
         B     PSETPAGE            CONTINUE                    @OZ19494
         SPACE 1                                               @OZ19494
PPFWD    LA    R14,=C' FWD-SPACED ' SET UP FOR $F MESSAGE      @OZ48259
         AR    R1,R15              ADD IN PAGE COUNT           @OZ19494
         SPACE 1                                               @OZ19494
PSETPAGE ST    R1,PDDBSKIP         SAVE NEW SKIP COUNT         @OZ19494
         SPACE 1                                               @OZ19494
        $MID   170                                             @OZ19494
         LA    R1,=C'$HASP170 '    POINT TO MESSAGE ID         @OZ19494
         L     R15,=A(PRMSG)       ISSUE MSG TO OPERATOR       @OZ19494
         BALR  PL,R15                                          @OZ48259
         SPACE 1                                               @OZ19494
***********************************************************************
*                                                                     *
*        GO TERMINATE DATA SET IF $F DEVICE,DSET WAS REQUESTED        *
*                                                                     *
*        CAUSED BY OPERATOR COMMAND OR PRINTER 'CANCEL' BUTTON        *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         CLC   PDDBSKIP,PMAXPAGE   TEST SKIP COUNT             @OZ19494
         BNL   PRLOGCK             BRANCH IF $F DATASET        @OZ19494
         SPACE 1                                                     R4
***********************************************************************
*                                                                     *
*        SKIP TO NEW PAGE, OR ADD A BLANK CARD -- THEN RE-POSITION    *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         LM    PC1,PC2,PRCCWEJ     SELECT PAGE-EJECT CCW             R4
         TM    PCEID,PCEPRSID      TEST PROCESSOR TYPE
         BO    *+8                 BRANCH IF PRINT
         LM    PC1,PC2,PUCCWBL     SELECT BLANK CARD CCW
         BAL   PL,PPPUT            ADD CCW TO CHAIN
         BAL   PL,PPWRITE           INITIATE WRITE
         BAL   PL,PPCHECK            AND CHECK
         XC    PULMTTR(7),PULMTTR  CLEAR PUNCH RESTART POINTER       R4
         CLC   PDDBSKIP,PCEFORM    SKIP COUNT VS. PAGE COUNT   @OZ19494
         BH    PBSFSGO             $F DEVICE,N
         LH    R15,$BSPSIZ         IF NO BSP                         R4
         LTR   R15,R15              TABLE ENTRIES,                   R4
         BZ    PBSPBEG               BR TO RESTART DS               R41
         EJECT
***********************************************************************
*                                                                     *
*        $B COMMAND - SEARCH BACKSPACE TABLE FOR RESTART POINT        *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         SH    R15,=H'8'           TABLE LENGTH - 1, LESS 1 ENTRY    R4
         LA    R1,PBSPTBL          POINT TO 1ST TABLE ENTRY          R4
         SPACE 1                                                     R4
PBSPSRCH ALR   R1,R15              POINT TO LAST TABLE ENTRY - 1     R4
         SLR   PL,PL               GET                               R4
         IC    PL,$BSPGCT           PAGE FRAME                       R4
         LNR   PL,PL                 FOR LAST                        R4
         A     PL,PBSPGCT             ENTRY                          R4
         BNP   PBSPBEG             BR IF START OF DATA SET           R4
         OC    5(2,R1),5(R1)       TEST FOR VALID ENTRY              R4
         BZ    PBSPBEG             BR IF NO -- RESTART DS            R4
         ST    PL,PBSPGCT          SAVE FOR LATER
*                                  THIS LINE DELETED BY APAR   @OZ19494
*                                  THIS LINE DELETED BY APAR   @OZ19494
         MVC   PCEFORM(7),1(R1)    SAVE BACKSPACE TABLE ENTRY  @OZ19494
         LA    R1,PBSPTBL          POINT TO 1ST TABLE ENTRY          R4
         LTR   R15,R15             IF SINGLE-ENTRY TABLE,            R4
         BM    PBSPINVL             DON'T POP UP TABLE ENTRIES      R41
         EX    R15,PPOP1           POP UP BSP TABLE                 R41
         EX    R15,PPOP2            TO PREVIOUS ENTRY               R41
         SPACE 1                                                    R41
PBSPINVL DS    0H                                              @OZ29138
         CL    PL,PDDBSKIP         IS PAGE BEFORE BSP POINT
         BNH   PBSPFND             BR IF CORRECT PAGE FRAME    @OZ29138
         XC    0(7,R1),0(R1)       INVALIDATE UNUSED ENTRY     @OZ29138
         B     PBSPSRCH            GO CHECK NEXT FRAME         @OZ29138
PBSPFND  DS    0H                                              @OZ29138
         MVC   PCEJMTTR,PCEFORM    SET BUFFER MTTR             @OZ19494
         MVC   PCEEJRCB,PCEFORM+4  SET LINE RCB                @OZ19494
         MVC   PBUFSKIP,PCEFORM+6  SET BUFFER OFFSET           @OZ19494
         ST    PL,PDDBPGCT         SET AS NEW PAGE COUNTER
         SLR   PL,PL               GET                         @OZ29138
         IC    PL,$BSPGCT           PAGE FRAME AND BUMP        @OZ29138
         A     PL,PDDBPGCT            BY FRAME SIZE            @OZ29138
         ST    PL,PBSPGCT          SAVE FOR NEXT TABLE UPDATE  @OZ29138
         LTR   R15,R15             IF SINGLE ENTRY, NO NEED    @OZ19494
         BM    PBSFSGO               TO RESTORE TABLE          @OZ19494
         EX    R15,PPOP3           RESTORE BACKSPACE TABLE     @OZ19494
         LA    PL,0(R15,R1)        POINT TO LAST ENTRY-1       @OZ19494
         MVC   1(7,PL),PCEFORM     RESTORE LAST TABLE ENTRY    @OZ19494
         B     PBSFSGO             GO TO RESTART PROCESS
         SPACE 1                                                     R4
PPOP1    MVC   BUFSTART(*-*),0(R1) *** EXECUTE ONLY ***             R41
PPOP2    MVC   7(*-*,R1),BUFSTART  *** EXECUTE ONLY ***             R41
PPOP3    MVC   0(*-*,R1),BUFSTART  *** EXECUTE ONLY ***        @OZ19494
         SPACE 1                                               @OZ19494
PBSPBEG  CLC   PDDBPGCT,PDDBSKIP   COMPARE PAGE AND SKIP COUNT @OZ19494
         BNE   PBSPBEG1            START FROM TOP IF NOT EQUAL @OZ19494
         ICM   PL,15,PFSBSCT       IF $F/$B PAGE COUNT ZERO    @OZ19494
         BZ    PBSFSGO              RESTART POSITION SET       @OZ19494
PBSPBEG1 LH    PC1,PDDBDISP        GET PDDB OFFSET             @OZ19494
         LA    PC2,0(PC1,JCT)      GET PDDB ADDRESS                  R4
         MVC   PCEJMTTR,PDBMTTR-PDBDSECT(PC2)  RESET 1ST MTTR        R4
         MVI   PBUFSKIP,0          RESET TRACK-CELL BUFFER OFFSET    R4
         B     PCKINIT             BACKSPACE TO START OF DATA SET    R4
         EJECT
***********************************************************************
*                                                                     *
*        PRINT HASP JOB STATISTICS AFTER LOG DATA SET                 *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
PRLOGCK  SLR   R0,R0               RESET PAGE COUNT            @OZ28599
         ST    R0,PDDBPGCT          AND SAVE IT                @OZ28599
         TM    PCEID,PCEPUSID      IS THIS A PUNCH PROCESSOR
         BO    PNEXTDDB            BRANCH IF YES
         LH    PC1,PDDBDISP        GET PDDB OFFSET INTO THE IOT
         LA    PC2,0(PC1,JCT)      ADD IOT BASE
         TM    PDBFLAG1-PDBDSECT(PC2),PDB1LOG IS THIS THE LOG PDDB
         BNO   PNEXTDDB            BRANCH IF NO
         L     JCT,PJCTBUF         ADDRESS JCT BUFFER
         USING JCTDSECT,JCT        ACTIVATE JCT ADDRESSABILITY
         ICM   R1,15,JCTXEQOF      JCT COMPLETE...             @OZ43026
         BNZ   PRNUJCT             BR IF YES, NO REFRESH       @OZ43026
         L     JCT,PCEJQE          GET JQE ADDRESS             @OZ43026
        $#JCT  READ,REFRESH=YES    GET JCT BUFFER ADDRESS      @OZ43026
         BZ    PRERJCT             BR IF ERROR, USE OLD JCT    @OZ43026
         ST    JCT,PJCTBUF         SAVE REFRESHED JCT BUF ADD  @OZ43026
        $#JCT  FREE                FREE JCT (REDUCE USE COUNT) @OZ43026
         B     PRNUJCT             JCT REFRESHED, CONTINUE     @OZ43026
PRERJCT  DS    0H                                              @OZ43026
         L     JCT,PJCTBUF         RESTORE JCT ADDRESS         @OZ43026
PRNUJCT  DS    0H                                              @OZ43026
         MVC   PMESSAGE,PJOBSTAT   SETUP STATISTICS TITLE
         L     R15,=A(PCOMMENT)    WRITE STATISTICS HEADER     @OZ19494
         BALR  PL,R15              -----------------------     @G38ESBB
         ICM   R1,15,JCTXDTON      OBTAIN EXECUTION DATE            R41
         BZ    PRNODATE            BR IF NONE                       R41
         L     R15,=A(PPDATE)       ELSE CONVERT DATE          @OZ19494
         BALR  R14,R15               TO ' DD MMM YY'           @OZ19494
         MVC   PMESSAGE+10(L'PXEQDATE),PXEQDATE  FORMAT MESSAGE     R41
         L     R15,=A(PCOMMENT)    WRITE EXECUTION DATE        @OZ19494
         BALR  PL,R15              --------------------        @G38ESBB
PRNODATE DS    0H                                                   R41
         MVC   PMESSAGE,PRDRSTAT   SETUP CARDS READ
         L     R1,JCTCARDS         GET INPUT CARD COUNT
         CVD   R1,PCCWORK          CONVERT TO DECIMAL
         ED    PMESSAGE(10),PCCWORK+4 EDIT INPUT CARD COUNT          R4
         L     R15,=A(PCOMMENT)    WRITE CARDS READ            @OZ19494
         BALR  PL,R15              ----------------            @G38ESBB
         MVC   PMESSAGE,PPRTSTAT   SETUP SYSOUT PRINT
         L     R1,JCTLINES         GET SYSOUT LINE COUNT
         CVD   R1,PCCWORK          CONVERT TO DECIMAL
         ED    PMESSAGE(10),PCCWORK+4 EDIT SYSOUT RECORD COUNT       R4
         L     R15,=A(PCOMMENT)    WRITE PRINT RECORD COUNT    @OZ19494
         BALR  PL,R15              ------------------------    @G38ESBB
         MVC   PMESSAGE,PPUNSTAT   SETUP SYSOUT PUNCH
         L     R1,JCTPUNCH         GET SYSOUT PUNCH COUNT
         CVD   R1,PCCWORK          CONVERT TO DECIMAL
         ED    PMESSAGE(10),PCCWORK+4 EDIT SYSOUT RECORD COUNT       R4
         L     R15,=A(PCOMMENT)    WRITE PUNCH RECORD COUNT    @OZ19494
         BALR  PL,R15              ------------------------    @G38ESBB
         MVC   PMESSAGE,PXEQSTAT   SETUP EXECUTION TIME
         SLR   R0,R0               ZERO TIME ACCUMULATOR             R4
         SLR   R1,R1               ZERO TIME ACCUMULATOR             R4
         CLC   JCTXDTON,JCTXDTOF   COMPARE START AND STOP DATES      R4
         BE    PRDATEQ             BRANCH IF SAME DAY                R4
         BL    PRDATELO            BRANCH IF SPANNED AT LEAST 1 DAY  R4
PRDATEHI DS    0H                                                    R4
         MVC   PMESSAGE+1(9),=C'(UNKNOWN)' JOB STOPPED BEFORE START  R4
         B     PRXTIME             BRANCH TO PRINT MESSAGE           R4
         EJECT                                                       R4
PRDATELO DS    0H                                                    R4
         TM    JCTXDTON+3,X'0F'    VALIDATE START DATE (00YYDDDF)    R4
         BNO   PRDATEHI            ERR MSG IF DATE NOT STORED        R4
         TM    JCTXDTOF+3,X'0F'    VALIDATE STOP DATE                R4
         BNO   PRDATEHI            ERR MSG IF DATE NOT STORED        R4
         CLC   JCTXDTON(2),JCTXDTOF COMPARE YEAR PORTIONS ONLY       R4
         BE    PRYEAREQ            BRANCH IF SAME YEAR               R4
         BH    PRDATEHI            ERR MSG IF YEARS BACKWARD         R4
         ZAP   PCCWORK,JCTXDTON    ISOLATE START YEAR                R4
         SRP   PCCWORK,64-3,0        AS PACKED DECIMAL NUMBER        R4
         CVB   PL,PCCWORK          CONVERT START YEAR TO BINARY,     R4
         N     PL,=F'3'              TEST FOR LEAP YEAR        @OZ40773
         BNZ   SKIP270             SKIP IF NOT LEAP YEAR             R4
         LA    R1,1                ALLOW FOR 366-DAY YEAR            R4
SKIP270  LA    R1,365(,R1)         ADD 365 DAYS FOR YEAR CHANGE      R4
PRYEAREQ DS    0H                                                    R4
         ZAP   PCCWORK,JCTXDTOF+2(2) COMPUTE DIFFERENCE              R4
         SP    PCCWORK,JCTXDTON+2(2)   BETWEEN JOB START             R4
         CVB   PL,PCCWORK              DAY AND JOB STOP DAY          R4
         AR    R1,PL               ADD TO ADJUSTMENT FOR YEAR CHANGE R4
         M     R0,=A(24*60*60*100) CONVERT DAYS TO HUNDREDTHS OF SEC R4
PRDATEQ  DS    0H                                                    R4
         AL    R1,JCTXEQOF         ADD STOP TIME                     R4
         BC    12,SKIP280          SKIP IF NO CARRY                  R4
         AL    R0,=F'1'            ADJUST HIGH END                   R4
SKIP280  SL    R1,JCTXEQON         SUBTRACT START TIME               R4
         BC    3,SKIP290           SKIP IF CARRY                     R4
         BCTR  R0,0                ADJUST HIGH END                   R4
SKIP290  LTR   R0,R0               CHECK HIGH END                    R4
         BM    PRDATEHI            ERR MSG IF TOTAL TIME NEGATIVE    R4
         BP    PRBIGMIN            BRANCH IF TOO LARGE FOR EDIT      R4
         CL    R1,=A(9999999*60)   CHECK LOW END                     R4
         BH    PRBIGMIN            BRANCH IF TOO LARGE FOR EDIT      R4
         D     R0,=F'60'           CONVERT TO HUNDREDTHS OF MINUTES  R4
         B     PRTIMCVD            BRANCH TO CVD AND EDIT            R4
PRBIGMIN DS    0H                                                    R4
         D     R0,=A(60*100)       CONVERT TIME TO WHOLE MINUTES     R4
         MVC   PMESSAGE(10),PRDRSTAT REPLACE EDIT PATTERN            R4
PRTIMCVD DS    0H                                                    R4
         CVD   R1,PCCWORK          CONVERT TO DECIMAL                R4
         ED    PMESSAGE(10),PCCWORK+4 EDIT EXECUTION TIME            R4
PRXTIME  DS    0H                                                    R4
         L     R15,=A(PCOMMENT)    WRITE EXECUTION TIME        @OZ19494
         BALR  PL,R15              --------------------        @G38ESBB
         DROP  JCT                 SUSPEND JCT ADDRESSABILITY
         LR    JCT,PBUF            RESTORE IOT BASE ADDRESS
         EJECT
***********************************************************************
*                                                                     *
*        PREPARE FOR SELECTION OF THE NEXT DATA SET                   *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
PNEXTDDB DS    0H
         XC    PDDBSKIP,PDDBSKIP   CLEAR SKIP COUNT
         L     R1,PCEDCT           ADDRESS PRINT/PUNCH DCT     @OZ32566
         USING DCTDSECT,R1         ACTIVATE DCT ADDRESSABILITY
         NI    DCTFLAGS,255-DCTSPACE RESET SPACE CONTROL SWITCHES
         DROP  R1                  SUSPEND DCT ADDRESSABILITY
         NI    PDCTFLAG,255-DCTSPACE RESET SPACE CONTROL SWITCHES
         LM    PC1,PC2,PRCCWEJ     SELECT SKIP TO CH 1 CCW
         TM    PCEID,PCEPRSID      TEST PROCESSOR TYPE
         BO    *+8                 BRANCH IF PRINT
         LM    PC1,PC2,PUCCWBL     SELECT BLANK CARD CCW
         BAL   PL,PPPUT            PUT CCW ONTO CHAIN
         BAL   PL,PPWRITE           SEND CHAIN TO DEVICE
         BAL   PL,PPCHECK            CHECK WRITE
         SLR   PW,PW               CLEAR WORK REGISTER         @OZ61152
         IC    PW,PREVCPYN         UPDATE TOTAL PAGE COUNT     @OZ61152
         AL    PW,PRPAGECT          WITH NUMBER OF COPIES      @OZ61152
         ST    PW,PRPAGECT           THIS TRANSMISSION         @OZ61152
         XC    PULMTTR(7),PULMTTR  CLEAR PUNCH RESTART POINTER       R4
         SPACE 1                                                     R4
***********************************************************************
*                                                                     *
*        RE-CYCLE SAME DATA SET UNTIL DS COPIES IS SATISFIED          *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         USING PDBDSECT,PC1        PROVIDE PDDB ADDRESSABILITY       R4
         SPACE 1                                                     R4
PRCPYTST DS    0H
         SPACE 1                                                     R4
         NI    PPFLAG2,255-PPFDS   RESET 1ST DATA SET SWITCH         R4
         SPACE 1                                                     R4
         LH    PC1,PDDBDISP        PDDB OFFSET INTO THE IOT          R4
         LA    PC1,0(PC1,PBUF)     ADD IOT BASE                      R4
         MVC   PCEJMTTR,PDBMTTR    RESET 1ST MTTR OF DATA SET        R4
         SPACE 1                                                     R4
         CLI   PDEVTYPE+3,UCB3800  TEST FOR 3800 PRINTER             R4
         BE    PRCPYGRP            HANDLE COPY GROUPS IF YES         R4
         SPACE 1                                                     R4
         IC    PW,PPRCPYCT         INCREMENT
         LA    PW,1(,PW)           COPY
         STC   PW,PPRCPYCT         COUNT
         CLC   PPDSCPY,PPRCPYCT    ENOUGH COPIES OF DATA SET
         BH    PNXTCPY             BR IF ANOTHER COPY NEEDED         R4
         SPACE 1                                                     R4
PRCPYEND DS    0H                                                    R4
         MVI   PPRCPYCT,X'00'      SET COPY COUNT TO ZERO
         B     PDDBSRCH            FIND NEXT MATCHING PDDB
         EJECT                                                       R4
***********************************************************************
*                                                                     *
*        HANDLE COPIES (COPY GROUPS) AND FLASH COUNTS FOR 3800        *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
PRCPYGRP DS    0H                                                    R4
         SLR   PW,PW               CLEAR WORK REGISTER               R4
         LA    R1,1                ASSUME 1 COPY JUST PRINTED        R4
         CLI   PCOPYGRP,0          TEST FOR COPY GROUPS              R4
         BE    SKIP300             BR IF NOT - ASSUMPTION CORRECT    R4
         IC    R1,PDDBCPYG         ELSE, USE COUNT                   R4
         IC    R1,PCOPYGRP(R1)      OF COPIES JUST PRINTED           R4
SKIP300  IC    PW,PPRCPYCT         UPDATE TOTAL                      R4
         LA    PW,0(R1,PW)          NUMBER OF                        R4
         STC   PW,PPRCPYCT           COPIES PRINTED                  R4
         CLM   PW,1,PPDSCPY        TEST FOR ALL COPIES PRINTED       R4
         BNL   PRCPYEND            BR IF YES                         R4
         LA    R1,1(,PW)           PLACE NEW STARTING                R4
         STC   R1,SPCOPYS           COPY NO. INTO SETUP LIST         R4
         MVI   SPFLASHC,0          ASSUME NO MORE FLASHING           R4
         CLI   PDBFLASH,C'*'       FLASHING...                 @OZ18407
         BE    PRCPYN              BR IF NO                    @OZ18407
         IC    R1,PDBFLSHC         GET NO. COPIES NEEDING FLASH      R4
         SR    R1,PW               SUBTRACT NUMBER ALREADY FLASHED   R4
         BNP   PRCPYN              BR IF DONE FLASHING         @OZ18407
         STC   R1,SPFLASHC          ELSE SET REMAINING COUNT   @OZ18407
         SPACE 1                                               @OZ18407
PRCPYN   MVI   SPCOPYN,1           ASSUME 1 COPY THIS XMISSION @OZ18407
         CLI   PCOPYGRP,0          TEST FOR COPY GROUPS              R4
         BE    PTSTRXMT            BR IF NO                         R41
         SLR   PW,PW               ELSE, UPDATE                      R4
         IC    PW,PDDBCPYG          COPY GROUP OFFSET                R4
         LA    PW,1(,PW)             IN CKPT-AREA AND                R4
         STC   PW,PDDBCPYG            PLACE NUMBER                   R4
         IC    PW,PCOPYGRP(PW)         OF COPIES INTO                R4
         STC   PW,SPCOPYN               SETUP LIST                   R4
         B     PSETRXMT            BR TO SETUP FOR RE-XMIT          R41
         SPACE 1                                                    R41
PTSTRXMT CLI   PDBMODF,C'*'        USING COPY-MOD...                R41
         BNE   PSETRXMT            BR IF YES                        R41
         CLI   PDBFLASH,C'*'       FLASHING...                      R41
         BE    PRCPYOFS            BR IF NO                         R41
         CLC   PDBFLSHC,PPRCPYCT   TURN-OFF FLASHING...        @OZ18407
         BNE   PRCPYOFS            BR IF NO                    @OZ18407
         EJECT                                                 @OZ75372
PSETRXMT MVI   SPFLAG,SPREXMIT     SET RETRANSMISSION INDICATION    R41
         L     R15,=A(P3800DSV)    CALL 3800                         R4
         BALR  PL,R15               DEVICE SETUP VERIFICATION        R4
         B     PSETRXMR(R15)       BRANCH ON RETURN CODE       @OZ75372
         SPACE 1                                               @OZ75372
PSETRXMR B     PRCPYOFS            0  NORMAL RETURN            @OZ75372
         B     PPDONE              4  OPER TERMINATE           @OZ75372
         B     PSETRXMJ            8  3800 JAM OR CANCEL KEY   @OZ75372
         B     PPIODRN             12 3800 ERROR IN SETUP      @OZ75372
         SPACE 1                                               @OZ75372
PSETRXMJ L     R15,=A(PLOCATE)     CALL THE LOCATE ROUTINE     @OZ75372
         BALR  PL,R15               TO GET THE ORIGIN PQE      @OZ75372
         B     PPDSEND             PROCESS JAM OR CANCEL KEY   @OZ75372
         SPACE 1                                               @OZ75372
PRCPYOFS LM    PC1,PC2,PCCWOFST    ADD OFFSET STACKER          @OZ75372
         BAL   PL,PPPUT2            CCW TO CHAIN               @OZ51441
         ICM   PC1,8,PXTABCCW      SELECT                      @OZ24675
         BAL   PL,PPPUT2            CHAR1                      @OZ51441
         B     PNXTCPY             BR FOR NEXT COPY (COPIES)         R4
         SPACE 1                                                     R4
         DROP  PC1                 SUSPEND PDDB ADDRESSABILITY       R4
         TITLE 'HASP PRINT/PUNCH SERVICE -- PPPUT SUBROUTINE'        R4
***********************************************************************
*                                                                     *
*        PPPUT -- SUBROUTINE TO ADD A NEW PRINT OR PUNCH COMMAND TO   *
*                 THE CURRENT CCW AREA                                *
*                                                                     *
*        PC1,PC2 - 8 BYTE CHANNEL COMMAND WORD                        *
*        PL      - RETURN ADDRESS                                     *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
***********************************************************************
*                                                                     *
*        ELIMINATE EXTRANEOUS PAGE EJECTS                             *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         USING DCTDSECT,R1         PROVIDE DCT ADDRESSABILITY        R4
         SPACE 1                                                     R4
PPPUT    TM    PCEID,PCEPRSID      TEST PROCESSOR TYPE               R4
         BNO   PPPUT2              BRANCH IF NOT PRINTER             R4
         L     R1,PCEDCT           LOAD ADDRESS OF PRINTER DCT @OZ32566
         CLM   PC1,8,=X'89'        TEST CHANNEL COMMAND CODE
         BL    PPPUT1              BR IF NOT POSSIBLE EJECT
         BE    PCHAN1              BR IF PRINT, SKIP TO CHANNEL 1    R4
         CLM   PC1,8,=X'8B'        TEST CHANNEL COMMAND CODE
         BNE   PPPUT1              BRANCH IF NOT IMED SKP CHNL 1
         TM    DCTPPFL,DCTEJECT    CHECK IF ALREADY AT CHANNEL 1     R4
         BOR   PL                  RETURN IF YES                     R4
         SPACE 1                                                     R4
PCHAN1   OI    DCTPPFL,DCTEJECT    SHOW PRINTER EJECTED
         B     PPPUT2                AND CONTINUE
PPPUT1   NI    DCTPPFL,255-DCTEJECT RESET CHANNEL 1 SWITCH
         SPACE 1                                                     R4
***********************************************************************
*                                                                     *
*        ALTERNATE PPPUT ENTRY POINT                                  *
*                                                                     *
*        IF REMOTE -- SEND CCW TO LINE MANAGER                        *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
PPPUT2   TM    PCEID,PCERJEID      TEST PROCESSOR TYPE               R4
         BZ    PTESTCHN            BR IF NOT REMOTE TERMINAL         R4
         STM   PC1,PC2,POUTCCWA    PASS CCW TO                       R4
         L     R1,PCEDCT           GET PRINT/PUNCH DCT ADDRESS @OZ32566
         LA    R0,POUTCCWA         LOAD ADDRESS OF CCW               R4
*                                  THIS LINE DELETED BY APAR   @OZ19494
        $EXTP  PUT,(R1),(R0)       PASS REQUEST TO RTAM        @OZ19494
         BNZ   PPCHECK             BRANCH IF SUCCESSFUL        @OZ19494
         NI    PPFLAG2,255-PPCKPTA DON'T ALLOW CHECKPOINTS     @OZ19494
         B     PPCHECK             CHECK OPERATOR COMMANDS           R4
         EJECT                                                       R4
***********************************************************************
*                                                                     *
*        SEE IF OK TO ADD NEW CCW TO THIS CCW AREA                    *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         USING PCIDSECT,PW         PROVIDE PCIE ADDRESSABILITY       R4
         SPACE 1                                                     R4
PTESTCHN LH    PW,PCCWLAST         GET OFFSET TO PCIE                R4
         AL    PW,POUTCCWA         ADD BASE TO OBTAIN TRUE ADDRESS   R4
         SPACE 1                                                     R4
         TM    PCISGNAL,PCIACTIV   USE IS OK IF THIS                 R4
         BZ    PPCHAIN              CCW AREA IS NOT ACTIVE,          R4
         TM    PPFLAG,PPWSW          OR IF A WRITE                   R4
         BZ    PPCHAIN                IS NOT IN PROGRESS             R4
         SPACE 1                                                     R4
         ST    PL,PLSAVE           ELSE, WAIT                        R4
         BAL   PL,PCIWAIT           FOR A PCI                        R4
         L     PL,PLSAVE             AND                             R4
         B     PTESTCHN               TRY AGAIN                      R4
         SPACE 1                                                     R4
         DROP  R1,PW               SUSPEND DCT/PCIE ADDRESSABILITY  R41
         SPACE 1                                                     R4
***********************************************************************
*                                                                     *
*        TRANSLATE VIRTUAL DATA ADDRESS TO REAL AND ADD CCW TO CHAIN  *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
PPCHAIN  DS    0H
         L     PW,PCCWPT           PICK UP CURRENT CCW POINTER       R4
         LA    PW,8(,PW)           BUMP CCW POINTER
         STM   PC1,PC2,0(PW)       ADD CCW TO CHAIN
         LA    R1,0(,PC1)          LOAD CCW ADDRESS            @OZ68244
         LTR   R1,R1               IS IT ZERO DATA ADDRESS     @OZ68244
         BNZ   *+6                 BRANCH IF NOT               @OZ68244
         LR    R1,PW               MAKE ADDRESS NON-ZERO       @OZ68244
         LRA   R1,0(,R1)           CONVERT VIRT TO REAL ADDR   @OZ68244
         STCM  R1,7,1(PW)           AND UPDATE IN CHAIN              R4
         EJECT                                                 @OZ68244
***********************************************************************
*                                                                     *
*        MERGE IMMEDIATE CCW WITH PREVIOUS PRINT, NO SPACE CCW        *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         OC    6(2,PW),6(PW)       TEST BYTE COUNT
         BNZ   PPENDCHK            BRANCH IF NOT ZERO
         TM    0(PW),X'02'         IS THIS AN IMMED CMD...     @OZ25297
         BO    PPCHAIN1            YES, CHK IF COMBINE         @OZ25297
         MVI   5(PW),C' '          SET BLANK SOURCE            @OZ25297
         LRA   R1,5(,PW)           GET REAL ADDRESS OF BLANK   @OZ25297
         STCM  R1,7,1(PW)           IN CCW                     @OZ25297
         B     PPNOMERG              AND SET LENGTH TO 1       @OZ25297
PPCHAIN1 DS    0H                                              @OZ25297
         L     R1,POUTCCWA         ADDRESS 1ST CCW IN CHAIN          R4
         CLR   PW,R1               IS CURRENT CCW AFTER 1ST
         BNH   PPNOMERG            BR IF NOT                         R4
         L     R1,PCCWPT           R1 = ADDRESS OF LAST CCW
         CLI   0(R1),X'01'         TEST LAST COMMAND
         BNE   PPNOMERG            BR IF NOT PRINT, NO SPACE         R4
         NI    0(PW),X'FD'         REMOVE IMMEDIATE BIT              R4
         MVC   0(1,R1),0(PW)       MOVE COMMAND TO PREVIOUS CCW      R4
         BR    PL                  RETURN -- NO CCW ADDED            R4
         SPACE 1                                                     R4
PPNOMERG MVI   7(PW),X'01'         FORCE BYTE COUNT NON-ZERO
         EJECT                                                       R4
***********************************************************************
*                                                                     *
*        UPDATE CCW POINTER AND PUNCH RESTART POINTERS                *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
PPENDCHK ST    PW,PCCWPT           SAVE CCW POINTER
         TM    PCEID,PCEPRSID      TEST PROCESSOR TYPE
         BO    PNOTPUN             BRANCH IF PRINT
         TM    0(PW),X'05'         IS CCW A 3525 PRINT-LINE
         BO    PNOTPUN             BRANCH IF YES - DON'T CKPT
         L     R1,POUTIOB          ADDRESS OF OUTPUT IOB             R4
         MVC   PPBNMTTR-BUFDSECT(4,R1),PCEJMTTR  UPDATE PUNCH        R4
         MVC   PPBNRCB-BUFDSECT(2,R1),PCEEJRCB    RESTART            R4
         MVC   PPBNBOFF-BUFDSECT(1,R1),PCEJBOFF    POINTERS          R4
         SPACE 1                                                     R4
***********************************************************************
*                                                                     *
*        USE PPWRITE IF CCW AREA IS FULL                              *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
PNOTPUN  DS    0H
         CLI   PDEVTYP3,UCB3800    TEST FOR 3800 PRINTER       @G38ESBB
         BNE   *+8                 NO, BRANCH                  @G38ESBB
         LA    PW,RPISIBSZ+L'PCCWNOP(,PW) INCLUDE RPI,SIB,NOP  @G38ESBB
         LA    PW,8(,PW)           COMPUTE OFFSET OF NEXT            R4
         SL    PW,POUTCCWA          SLOT IN CCW CHAIN                R4
         CH    PW,PCCWLAST         TEST FOR END OF CCW CHAIN         R4
         BLR   PL                  RETURN IF NOT                     R4
         TITLE 'HASP PRINT/PUNCH SERVICE -- PPWRITE SUBROUTINE'      R4
***********************************************************************
*                                                                     *
*        PPWRITE -- SUBROUTINE TO SCHEDULE A NEW CCW AREA FOR OUTPUT  *
*                                                                     *
*        PL    - RETURN ADDRESS                                       *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         DROP  PBUF                ACTIVATE LOCAL ADDRESSABILITY ON  R4
         USING BUFDSECT,R15         OUTPUT IOB (BUFFER PREFIX)       R4
         SPACE 1                                                     R4
         CNOP  0,8                                                   R4
PPWRITE  ST    PL,PLSAVE           SAVE RETURN ADDRESS               R4
         SPACE 1                                                     R4
***********************************************************************
*                                                                     *
*        RETURN IF REMOTE OR IF CCW AREA IS EMPTY                     *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         TM    PCEID,PCERJEID      TEST PROCESSOR TYPE               R4
         BOR   PL                  RETURN IF REMOTE                  R4
         L     PW,PCCWPT           PICK UP CCW POINTER               R4
         L     R1,POUTCCWA         ADDR OF CCW AREA                  R4
         CLR   PW,R1               RETURN IF                         R4
         BLR   PL                   NO CCW'S                         R4
         CLI   PDEVTYP3,UCB3800    TEST FOR 3800 PRINTER       @G38ESBB
         BE    PSETPCI             YES, BRANCH                 @G38ESBB
         L     R15,POUTCCWN        GET NEW CCW CHAIN ADDRESS   @OZ29138
         AH    R15,PCCWLAST        POINT TO THE PCIE,          @OZ29138
         LA    R15,PCIESIZE(,R15)   AND THE CCW CHKPT AREA.    @OZ29138
         MVC   JOEPPCT-JOECKPP(,R15),PDDBPGCT  UPDATE PAGE CNT @OZ27300
         SPACE 1                                                     R4
***********************************************************************
*                                                                     *
*        SET PCI AND INITIALIZE PCIE AT END OF NEW CCW AREA           *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         USING PCIDSECT,R1         PROVIDE PCIE ADDRESSABILITY       R4
         SPACE 1                                                     R4
PSETPCI  L     R15,POUTIOB         ADDRESS OUTPUT IOB          @G38ESBB
         OI    4(R1),X'08'         SET PCI AT TOP OF NEW AREA        R4
         AH    R1,PCCWLAST         COMPUTE ADDR OF PCIE              R4
         LR    R0,R1               SAVE PCIE ADDR              @OZ47150
         OI    PCISGNAL,PCIACTIV+PCIBUSY  SHOW PCIE ACTIVE AND BUSY  R4
         NI    PCI1FLGS,255-X'40'  TURN OFF COMMAND CHAINING         R4
         ST    PW,PPBLVCCN         SAVE POINTER TO LAST VALID CCW    R4
         EJECT                                                 @OZ47150
***********************************************************************
*                                                                     *
*        IF CCW AREA IS NOT FULL -- ADD A TIC TO THE PCIE             *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         CLI   PDEVTYP3,UCB3800    TEST FOR 3800 PRINTER       @G38ESBB
         BNE   PNXTSLOT            NO, BRANCH                  @G38ESBB
         SH    R1,=Y(L'PCCWSIB)    POINT TO SIB CCW            @G38ESBB
         ST    R1,PPBLVCCN         SET LAST VALID CCW TO SIB   @G38ESBB
         SH    R1,=Y(L'PCCWXORD)   ADDRESS THE RPI             @G38ESBB
         LR    R0,R1               SAVE RPI ADDR               @OZ47150
         SH    R0,=Y(L'PCCWNOP)    POINT TO NOP CCW            @OZ47150
         SPACE 1                                               @OZ47150
PNXTSLOT LA    PW,8(,PW)           ADDR OF NEXT SLOT IN CHAIN  @G38ESBB
         CLR   PW,R0               COMPARE WITH END            @OZ47150
         BE    PPWSWAP             BRANCH IF EQUAL (FLUSH WITH PCIE) R4
         LRA   R1,0(,R1)           CONVERT VIRT PCIE ADDRESS TO REAL R4
         MVC   0(8,PW),PCNOPTIC+8  ADD A TIC ON THE CHAIN            R4
         STCM  R1,7,1(PW)           AND POINT IT TO THE PCIE         R4
         EJECT                                                       R4
***********************************************************************
*                                                                     *
*        SWAP CCW AREA POINTERS                                       *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
PPWSWAP  DS    0H                                                    R4
        $COUNT                     ******* RECORD USAGE *******      R4
         LM    R0,R1,POUTCCWA      SWAP                              R4
         ST    R0,POUTCCWN          CCW AREA                         R4
         ST    R1,POUTCCWA           POINTERS                        R4
         ST    R0,PPBCCWNX         NEW CHAIN ADDR INTO BUFFER PREFIX R4
         LR    PW,R1               RESET POINTER                     R4
         SH    PW,=H'8'             TO FIRST CCW                     R4
         ST    PW,PCCWPT             IN NEXT AREA                    R4
         SPACE 1                                                     R4
***********************************************************************
*                                                                     *
*        IF WRITE IN PROGRESS -- PREPARE TO CHAIN NEW CCW AREA ON     *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         TM    PPFLAG,PPWSW        TEST FOR WRITE IN PROGRESS        R4
         BZ    PWEXCPVR            IF NOT - GO START ONE             R4
         AH    R1,PCCWLAST         ADDRESS OF PCIE ON ACTIVE CHAIN   R4
         L     R0,PCI1CCW+4        RIGHT HALF OF PCIE NOP CCW        R4
         N     R0,=A(X'FFFFFFFF'-PCIABORT) ABORT OFF FOR COMP  @G38ESBB
         O     R0,=A(PCIBUSY)      SET BUSY FLAG FOR COMPARE         R4
         LR    PW,R0               COPY AND BUILD A                  R4
         ICM   PW,8,=X'60'          CHAINED NOP  (CC+SLI)            R4
         EJECT                                                       R4
***********************************************************************
*                                                                     *
*        ATTEMPT TO SET THE COMMAND-CHAINING BIT IN THE PCIE AT       *
*                THE END OF THE CURRENTLY ACTIVE CHAIN                *
*                                                                     *
*       IF SUCCESSFUL, THE TIC WILL PASS CONTROL TO THE NEW CHAIN     *
*              IF NOT, WAIT FOR THE CHE AND RE-ISSUE I/O              *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         CS    R0,PW,PCI1CCW+4     APPEND NEW CHAIN TO ACTIVE CHAIN  R4
         BE    PPWRETRN            BR IF SUCCESSFUL                  R4
         CLI   PDEVTYP3,UCB3800    IS DEVICE A 3800            @G38ESBB
         BNE   PWAITCE             BR IF NOT                   @G38ESBB
         NI    PCISGNAL,FF-PCIABORT RESET ABORT IT             @G38ESBB
         SPACE 1                                               @G38ESBB
PWAITCE  BAL   PL,PPCHECK          WAIT FOR CHANNEL-END        @G38ESBB
         SPACE 1                                                     R4
         DROP  R1                  SUSPEND PCIE ADDRESSABILITY       R4
         SPACE 1                                                     R4
***********************************************************************
*                                                                     *
*        SETUP FOR OUTPUT I/O AND CALL $EXCP (VR)                     *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
PWEXCPVR DS    0H                                                    R4
         L     R15,POUTIOB         RE-ESTABLISH IOB ADDRESSABILITY   R4
         L     R1,POUTCCWN         ADDRESS OF NEW CHANNEL PROGRAM    R4
         ST    R1,IOBSTART         PLACE INTO IOB FOR IOS            R4
         NI    4(R1),255-X'08'     RESET PCI FLAG                    R4
         TM    PCEID,PCERJEID+PCEPRSID  TEST PROCESSOR TYPE    @OZ32288
         BNZ   PRTNOTPU            BRANCH IF NOT LOCAL PUNCH   @OZ32288
         LR    R14,R1              PRESET                      @OZ32288
         SH    R14,=H'8'            ERROR                      @OZ32288
         ST    R14,PUERRPT           POINTER                   @OZ32288
PRTNOTPU AH    R1,PCCWLAST         OBTAIN PCIE ADDRESS         @OZ32288
         ST    R1,PPBPCIE          PLACE INTO IOB FOR APPENDAGE      R4
         MVC   PPBLVCCC,PPBLVCCN   LAST VALID CCW ADDR INTO IOB      R4
         MVC   PPBCMTTR(7),PPBNMTTR UPDATE PUNCH RESTART POINTERS    R4
         MVI   PPBFLAG1,X'00'      CLEAR CSW SAVE              @OZ29106
         L     R1,PCEDCT           PICK UP DCT ADDRESS         @OZ32566
        $EXCP  (R1),TYPE=VR        INITIATE WRITE (EXCPVR)           R4
        $COUNT                     ******* RECORD USAGE *******      R4
         SPACE 1                                                     R4
         OI    PPFLAG,PPWSW        INDICATE WRITE HAS BEEN STAGED    R4
         SPACE 2                                                     R4
PPWRETRN DS    0H                                                    R4
         L     PL,PLSAVE           RESTORE LINK REGISTER             R4
         BR    PL                   AND RETURN                       R4
         SPACE 1                                               @G38ESBB
         PRINT OFF                 THIS SECTION DELETED BY     @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
         PRINT ON                  THIS SECTION DELETED BY     @G38ESBB
         TITLE 'HASP PRINT/PUNCH SERVICE -- PUNCH ERROR RECOVERY'    R4
***********************************************************************
*                                                                     *
*        ***  PUNCH ERROR RECOVERY AND RESTART  ***                   *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
PUNCHERR DS    0H
         L     R14,PPBPCIE         GET CURRENT PCIE POINTER    @OZ45731
         CR    PW,R14              IS CCW ADDRESS ABOVE...     @OZ45731
         BH    PCISIMUL            YES, GO SIMULATE PCI        @OZ45731
         SH    R14,PCCWLAST        GET FIRST CCW ADDRESS       @OZ45731
         CR    PW,R14              IS CCW WITHIN CURRENT AREA  @OZ45731
         BL    PCISIMUL            NO, GO SIMULATE PCI.        @OZ45731
*              THIS LINE DELETED BY APAR OZ45731               @OZ45731
*              THIS LINE DELETED BY APAR OZ45731               @OZ45731
*              THIS LINE DELETED BY APAR OZ45731               @OZ45731
*              THIS LINE DELETED BY APAR OZ45731               @OZ45731
         SPACE 1                                                     R4
*              THIS LINE DELETED BY APAR OZ45731               @OZ45731
         L     R1,PPBPCIE          ADDRESS 1ST POSSIBLE              R4
*              THIS LINE DELETED BY APAR OZ64163               @OZ64163
*              THIS LINE DELETED BY APAR OZ64163               @OZ64163
*              THIS LINE DELETED BY APAR OZ64163               @OZ64163
         SH    R1,PCCWLAST          CCW IN CHAIN                     R4
         SL    PW,=F'16'           DECREMENT RESTART ADDRESS
         CLI   PDEVTYPE+3,X'02'    TEST PUNCH DEVICE TYPE
         BE    PURANGE             BRANCH IF 2540P
         LA    PW,8(,PW)           INCR RESTART ADDRESS BY 8   @OZ40462
         CLI   PDEVTYPE+3,X'0C'    TEST PUNCH DEVICE TYPE
         BE    PU3525              BRANCH IF 3525
*        THIS LINE DELETED BY APAR                             @OZ40462
         B     PURANGE             BRANCH TO CHECK RESTART ADDRESS
PU3525   DS    0H
         CR    PW,R1               IS RESTART ADDRESS BEFORE CCW'S   R4
         BL    PUBACKUP            BRANCH IF YES
         TM    0(PW),X'05'         IS CCW A PUNCH CCW
         BM    PURANGE             BRANCH IF YES
         SL    PW,=F'8'            DECREMENT RESTART ADDRESS
         B     PU3525              CONTINUE BACKWARD SCAN
PUBACKUP DS    0H
         OC    PULMTTR(6),PULMTTR  IS RESTART POINTER ZERO
         BNZ   PURSTRT             BRANCH IF RESTART POINTER NOT ZERO
         LR    PW,R1               SET RESTART ADDRESS TO 1ST CCW
         B     PURANGE             GO RANGE CHECK ADDRESS
PURSTRT  DS    0H
         L     PL,POUTCCWA         TURN OFF FINAL              @OZ64163
         AH    PL,PCCWLAST          BUFFER FLAG IN CURRENT     @OZ64163
         NI    PCISGNAL-PCIDSECT(PL),FF-PCIFNLBF    BUFFER     @OZ64163
         L     PL,POUTCCWN         TURN OFF FINAL              @OZ64163
         AH    PL,PCCWLAST          BUFFER FLAG IN NEXT        @OZ64163
         NI    PCISGNAL-PCIDSECT(PL),FF-PCIFNLBF    BUFFER     @OZ64163
         MVC   PCEJMTTR,PULMTTR    SET RESTART BUFFER MTTR
         MVC   PCEEJRCB,PULRCB     SET RESTART RCB OFFSET
         MVC   PBUFSKIP,PULBOFF    SET RESTART BUFFER OFFSET         R4
         TM    PPFLAG2,PPRSW       READ IN PROGRESS .....      @OZ45731
         BZ    *+8                 NO, RESTART..               @OZ45731
         BAL   PL,PRDTCHK          WAIT FOR COMPLETION.        @OZ45731
         LA    PL,PBSFSGO          SETUP RESTART LOCATION
         CLI   PDEVTYPE+3,X'0C'    TEST PUNCH DEVICE TYPE      @OZ38034
         BNE   PPWINIT             BRANCH IF NOT 3525          @OZ38034
         L     R1,PRPUUCB          ADDRESS 3525 UCB            @OZ38034
         MODESET EXTKEY=ZERO       SET ZERO KEY TO ALTER UCB   @OZ38034
         OI    UCBFL1-UCBDSECT(R1),UCBNOTRD SET NOT READY      @OZ38034
         MODESET EXTKEY=HASP       RETURN TO NORMAL KEY        @OZ38034
         B     PPWINIT             EXIT TO COMPLETE RESTART          R4
         SPACE 1                                                     R4
PURANGE  DS    0H
         CR    PW,R1               IS RESTART ADDRESS BEFORE CCW'S   R4
         BL    PUBACKUP            BRANCH IF YES
         CL    PW,PUERRPT          IS RESTART ADDRESS BEFORE LAST
         BNH   PURETRY             BRANCH IF YES
*              THIS LINE DELETED BY APAR OZ45731               @OZ45731
*              THIS LINE DELETED BY APAR OZ45731               @OZ45731
*              THIS LINE DELETED BY APAR OZ45731               @OZ45731
*              THIS LINE DELETED BY APAR OZ45731               @OZ45731
*              THIS LINE DELETED BY APAR OZ45731               @OZ45731
*              THIS LINE DELETED BY APAR OZ45731               @OZ45731
*              THIS LINE DELETED BY APAR OZ45731               @OZ45731
*              THIS LINE DELETED BY APAR OZ45731               @OZ45731
*              THIS LINE DELETED BY APAR OZ45731               @OZ45731
         ST    PW,PUERRPT          NEW ERROR -- SAVE CCW FOR TEST
         TITLE 'HASP PRINT/PUNCH SERVICE -- CHANNEL PROGRAM RESTART' R4
***********************************************************************
*                                                                     *
*        RESTART PRINT/PUNCH CHANNEL PROGRAM AFTER ERROR              *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
PRESTRT  TM    PCEID,PCEPRSID      IS THIS A PRINTER PCE...    @OZ35391
         BO    PPRTHALT            YES..ISSUE MSG AND STOP.    @OZ35391
         TM    IOBCSW+4,X'3F'      TEST FOR CHANNEL FAILURES   @OZ35391
         BZ    PUREST              CONTINUE IF NONE.           @OZ35391
PPRTHALT L     R1,PCEDCT           GET DCT ADDR.               @OZ35391
         LR    R0,JCT              SAVE JCT REG ACROSS $WTO    @OZ35391
         OI    DCTFLAGS-DCTDSECT(R1),DCTSTOP STOP THE DEVICE   @OZ35391
         L     JCT,PJCTBUF         LOAD JCT FOR $WTO           @OZ35391
         SPACE 1                                               @OZ35391
         $MID  191                                             @OZ35391
         MVC   PMESSAGE(2),=X'191F' MOVE MSG ID                @OZ35391
         MVC   PMESSAGE+2(8),DCTDEVN-DCTDSECT(R1) MOVE DEVICE  @OZ35391
         MVC   PMESSAGE+10(29),PURMSG MOVE MSG TEXT            @OZ35391
         $WTO  PMESSAGE,39,JOB=YES, ISSUE PRINTER HALTED MSG   @OZ35391*
               ROUTE=$LOG+$UR,CLASS=$DOMACT,PRI=$ST            @OZ35391
         LR    JCT,R0              RESTORE JCT REG CONTENTS    @OZ35391
         LR    R0,R1               SAVE ADDR FOR $DOM          @OZ35391
         SPACE 1                                               @OZ35391
PUREIFC  L     R1,PCEDCT           LOAD DCT ADDRESS            @OZ35391
         USING DCTDSECT,R1         ESTABLISH DCT ADDRESS       @OZ35391
*        THIS LINE DELETED BY APAR ===>                        @OZ35391
         TM    DCTFLAGS,DCTSTOP    TEST FOR $S DEVICE          @OZ35391
         BZ    PRESTDOM            BRANCH IF TRUE START        @OZ35391
*              THIS LINE DELETED BY APAR OZ61556               @OZ61556
         DROP  R1                                              @OZ35391
         $WAIT IO                  WAIT FOR POST FROM COMM     @OZ35391
         B     PUREIFC             TEST FOR $S DEVICE          @OZ35391
         SPACE 1                                               @OZ35391
PRESTDOM $DOM  CMB=(R0)            DELETE OPERATOR MESSAGE     @OZ35391
         ST    PL,PPSAVE2+4        SAVE RETURN ADDRESS         @OZ51011
         L     R15,=A(PPCMDCK)     CALL SUBROUTINE TO PROCESS  @OZ51011
         BALR  PL,R15               SYNCHRONOUS OPERATOR CMDS  @OZ51011
         L     PL,PPSAVE2+4        RESTORE RETURN ADDRESS      @OZ51011
         L     R15,POUTIOB         PICK UP OUTPUT IOB ADDR     @OZ35391
         SPACE 1                                               @OZ35391
PUREST   ST    PW,IOBSTART         RESTART CCW LIST            @OZ35391
         EJECT                                                 @OZ35391
***********************************************************************
*                                                                     *
*        RESTART PRINT/PUNCH CHANNEL PROGRAM AT IOBSTART              *
*                                                                     *
*        FOR 3525 PUNCH -- MAKE DEVICE 'NOT-READY'                    *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
PURETRY  DS    0H
         CLI   PDEVTYPE+3,X'0C'    TEST DEVICE TYPE
         BNE   PUREXCP             BRANCH IF NOT A 3525 PUNCH
         L     R1,PRPUUCB          ADDRESS 3525 UCB                 R41
        MODESET EXTKEY=ZERO        GET ZERO KEY TO CHANGE THE UCB
         OI    UCBFL1-UCBDSECT(R1),UCBNOTRD  SET NOT-READY          R41
        MODESET EXTKEY=HASP        RETURN TO NORMAL HASP KEY
         SPACE 1                                                     R4
***********************************************************************
*                                                                     *
*        RESET PCI FLAG (IF ANY) IN RESTART CCW                       *
*                                                                     *
*        RE-ISSUE $EXCP (VR) AND RE-CHECK COMPLETION                  *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
PUREXCP  L     R1,IOBSTART         RESET PCI FLAG                    R4
         MVI   PPBFLAG1,X'00'      CLEAR CSW SAVE              @OZ29106
         NI    4(R1),255-X'08'      IN RESTART CCW                   R4
         SPACE 1                                                     R4
         L     R1,PCEDCT           ADDRESS PRINTER/PUNCH DCT   @OZ32566
        $EXCP  (R1),TYPE=VR        RESTART I/O                       R4
         B     PPPWAIT             GO WAIT FOR I/O COMPLETION        R4
PURMSG   DC    CL29' HALTED/POSSIBLE DATA LOST   '             @OZ35391
         TITLE 'HASP PRINT/PUNCH SERVICE -- ERROR DETECTION AND CORRECTC
               ION'                                                  R4
***********************************************************************
*                                                                     *
*        PPCHECK SUBROUTINE -- DETECT PRINT/PUNCH ERRORS AND RECOVER  *
*                                                                     *
*        PL    - RETURN ADDRESS                                       *
*                                                                     *
***********************************************************************
         SPACE 2                                                     R4
***********************************************************************
*                                                                     *
*        ENTRY POINT FOR PCI WAITS                                    *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
PCIWAIT  DS    0H                  ENTRY FOR PCI WAITS               R4
         L     R15,POUTIOB         PICK UP OUTPUT IOB ADDRESSABILITY R4
         TM    PPFLAG,PPWSW        TEST FOR WRITE IN PROGRESS        R4
         BZ    PPCHECK             BR IF NOT                         R4
         TM    BUFECBCC,X'7F'      HAS I/O COMPLETED...              R4
         BNZ   PPCHECK             BR IF YES                         R4
         OI    PPFLAG2,PPCIWAIT    INDICATE WAIT FOR PCI             R4
         L     R14,PPBPCIE         PICK UP ACTIVE PCIE ADDRESS       R4
         USING PCIDSECT,R14        ESTABLISH PCIE ADDRESSABILITY     R4
         TM    PCISGNAL,PCIACTIV   IS PCIE ON ACTIVE CHAIN...        R4
         BZ    PPPWAIT             WAIT IF NOT                       R4
         TM    PCISGNAL,PCIBUSY    HAS PCIE EXECUTED...              R4
         BZ    PPCHECK             BR IF YES                         R4
         DROP  R14                 SUSPEND PCIE ADDRESSABILITY       R4
         SPACE 1                                                     R4
***********************************************************************
*                                                                     *
*        $WAIT PRINT/PUNCH PROCESSOR FOR AN I/O POST                  *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
PPPWAIT  DS    0H
        $WAIT  IO                  WAIT FOR IO POST
         SPACE 1                                                     R4
***********************************************************************
*                                                                     *
*        MAIN PPCHECK ENTRY POINT                                     *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
PPCHECK  DS    0H
         LTR   JCT,JCT             IS THIS A SPOOL MESSAGE PRINTER
         BZR   PL                  BRANCH IF YES
         TM    PCEID,PCERJEID      TEST PROCESSOR TYPE
         BO    PCOMTEST            BRANCH IF REMOTE
         TM    PPFLAG,PPWSW        TEST FOR WRITE INITIATED
         BO    PCOMTEST            BR IF YES                   @G38ESBB
         TM    PPFLAG3,PP3800R     3800 REPOSITIONING...       @G38ESBB
         BOR   PL                  RETURN IF YES,NOTHING TO DO @G38ESBB
         B     PPPCKPT             BRANCH AROUND COMMANDS      @G38ESBB
         EJECT                                                       R4
***********************************************************************
*                                                                     *
*        FIRST -- CHECK FOR OPERATOR CONSOLE ACTION                   *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
PCOMTEST DS    0H
         L     PW,PCEDCT           GET ADDRESS OF PRT/PNCH DCT @OZ32566
         USING DCTDSECT,PW         ESTABLISH DCT ADDRESSABILITY
         TM    PPFLAG3,PP3800R     3800 REPOSITIONING...       @G38ESBB
         BZ    PCOMTST1            NO, BRANCH                  @G38ESBB
         TM    DCTFLAGS,DCTDELET+DCTRSTRT+DCTRPT+DCTBKSP  CMD  @G38ESBB
         BZ    PPIOTST             BR IF NOT RECURSIVE COMMAND @G38ESBB
        $MID   152                                             @G38ESBB
         PMSG  PMESSAGE,M152L,(X'152F',DCTDEVN,C' COMMAND REJECTED')   C
                                   MOVE MESSAGE TEXT           @G38ESBB
        $WTO   PMESSAGE,M152L,JOB=NO, INFORM OPERATOR          @G38ESBBC
               ROUTE=$LOG+$UR,CLASS=$NORMAL,PRI+$ST            @G38ESBB
         B     PPIOTEST            BR TO PROCESS PCIE          @G38ESBB
         SPACE 1                                               @G38ESBB
PCOMTST1 NI    PDCTFLAG,FF-DCTSPACE RESET OP CARRIAGE CONTROL  @G38ESBB
         OC    PDCTFLAG,DCTFLAGS   STACK OPERATOR FLAGS
*
*        $C/$E/$I DEVICE                                             R4
*
         TM    DCTFLAGS,DCTDELET+DCTRSTRT NEW $C, $E, OR $I
         BZ    POPCANJ             BRANCH IF NO
         TM    PPFLAG,PRDELSW      PREVIOUS $C, $E, OR $I
         BZ    PRDEL               BR IF NO, SET TERM FLAG     @G38ESBB
         CLI   PDEVTYP3,UCB3800    IS DEVICE A 3800 PRINTER    @G38ESBB
         BNE   PPABORT             NO, GO TERMINATE            @G38ESBB
         B     PPIOTEST            PROHIBIT DOUBLE COMMANDS    @OZ46351
*              THIS LINE DELETED BY APAR NUMBER                @OZ46351
*              THIS LINE DELETED BY APAR NUMBER                @OZ46351
         EJECT                                                 @G38ESBB
*
*        $CJOB,PURGE  (WHEN ISSUED ON ANOTHER SYSTEM)                R4
*
POPCANJ  DS    0H
         L     R1,PCEJQE           ADDRESS OF JQE              @OZ32566
         LTR   R1,R1               NO JOB IF TRAILING DATA...  @G38ESBB
         BZ    POPSPACE            BR IF SO                    @G38ESBB
         USING JQEDSECT,R1         ACTIVATE JQE ADDRESSABILITY
         TM    JQEFLAGS,QUEOPCAN+QUEPURGE $CJOB,PURGE ISSUED
         BNO   POPSPACE            BRANCH IF NO
         DROP  R1                  SUSPEND JQE ADDRESSABILITY
         OI    PDCTFLAG,DCTDELET   SET TERMINATION REASON
         B     PRDEL               GO COMPLETE TERMINATION
*
*        $B/$F DEVICE,PAGES OR ,DSET                                 R4
*
POPSPACE DS    0H
         TM    DCTFLAGS,DCTBKSP    BSP/FSP
         BZ    POPRPT              BRANCH IF NO
         CLI   PDEVTYP3,UCB3800    TEST FOR 3800 PRINTER       @G38ESBB
         BE    PPDEL               BR IF YES                   @G38ESBB
         NI    PDCTFLAG,255-DCTBKSP RESET FS/BS FLAG
         TM    PPFLAG,PPNEWS       IGNORE $B/$F IF                  R41
         BO    POPRPT               PRINTING JES2-NEWS              R41
         NI    DCTPPFL,255-DCTEJECT  RESET TOP-OF-PAGE FLAG          R4
         TM    PCEID,PCERJEID+PCEPUSID  RJE DEVICE OR PUNCH... @OZ29138
         BNZ   PPDEL                    BR IF YES              @OZ29138
         CLC   DCTCSW,=F'0'        DID INT. REQUIRED OCCUR     @OZ29138
         BE    PPDEL               BR IF NO, SET SUSPEND FLAG  @OZ29138
         ST    PL,PMESSAGE         SAVE LINKAGE REGISTER       @OZ29138
         L     R15,=A(PPCNTPGE)    GET PAGE COUNT ROUTINE ADDR @OZ29138
         BALR  PL,R15              GO AND COUNT PAGES          @OZ29138
         L     PL,PMESSAGE         RESTORE LINKAGE REGISTER    @OZ29138
         B     PPDEL               SET SUSPEND FLAG
         EJECT
*
*        $N DEVICE
*
POPRPT   DS    0H
         TM    PDCTFLAG,DCTRPT     TEST FOR $N
         BNO   PPIOTEST            BRANCH IF NO
         CLI   PDEVTYP3,UCB3800    IS DEVICE A 3800 PRINTER    @G38ESBB
         BE    PPDEL               BR IF YES                   @G38ESBB
         TM    PPFLAG,PRDELSW      PREVIOUS $C, $E, $I         @OZ29256
         BO    PPIOTST             BRANCH IF YES               @OZ29256
         SPACE ,                                               @OZ58550
         PRINT OFF                THIS SECTION DELETED BY      @OZ58550
*        THIS LINE DELETED BY APAR NUMBER                      @OZ58550
*        THIS LINE DELETED BY APAR NUMBER                      @OZ58550
*        THIS LINE DELETED BY APAR NUMBER                      @OZ58550
*        THIS LINE DELETED BY APAR NUMBER                      @OZ58550
*        THIS LINE DELETED BY APAR NUMBER                      @OZ58550
*        THIS LINE DELETED BY APAR NUMBER                      @OZ58550
*        THIS LINE DELETED BY APAR NUMBER                      @OZ58550
*        THIS LINE DELETED BY APAR NUMBER                      @OZ58550
         PRINT ON                 THIS SECTION DELETED BY      @OZ58550
*
*        ADD A COPY OF THE WORK-JOE TO THE JOB OUTPUT TABLE
*
PREPADD  DS    0H
        $#ADD  WORK=PWKJOE,CHAR=PCHJOE COPY JOE BACK INTO QUEUE      R4
         BNZ   PPIOTEST            BRANCH IF $#ADD FAILED
*
*        WRITE OPERATOR MESSAGE IF ADD SUCCESSFUL
*
PREPWTO  DS    0H
         NI    PDCTFLAG,255-DCTRPT RESET $N FLAG
        $MID   170                                                   R4
         MVC   PMESSAGE(2),=X'170F' MESSAGE NUMBER
         MVC   PMESSAGE+2(8),DCTDEVN DEVICE NAME
         MVC   PMESSAGE+10(12),=CL12' REPEATED' OPERATOR MESSAGE
        $WTO   PMESSAGE,22,        INFORM THE OPERATOR                 C
               ROUTE=$LOG+$UR,CLASS=$NORMAL,PRI=$ST,JOB=NO
         B     PPIOTEST            CHECK THE IO STATUS
         SPACE 1                                                     R4
***********************************************************************
*                                                                     *
*        INDICATE PROCESSOR SUSPENSION AND/OR TERMINATION             *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
PRDEL    OI    PPFLAG,PRDELSW      TERMINATION - $C, $E, $I
PPDEL    OI    PPFLAG,PPDELSW      SUSPENSION  - $B, $F
         NI    PPFLAG2,255-PPCKPTA DON'T ALLOW CHECKPOINTS     @OZ19494
         CLI   PDEVTYP3,UCB3800    IS DEVICE A 3800 PRINTER... @G38ESBB
         BNE   PPIOTEST            BR IF NOT                   @G38ESBB
         L     R1,PQHADR           ADDRESS PQH                 @G38ESBB
         OI    PQHFLAG-PQHDSECT(R1),PQHXFER SET XFER COMMAND   @G38ESBB
         EJECT
***********************************************************************
*                                                                     *
*        CHECK FOR ACTIVE PCIE EXECUTION                              *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         USING PCIDSECT,R14        PROVIDE PCIE ADDRESSABILITY       R4
PPIOTEST DS    0H                                                    R4
         NI    DCTFLAGS,255-DCTDELET-DCTRSTRT-DCTRPT-DCTBKSP RESET   R4
PPIOTST  DS    0H                                              @OZ29256
         TM    PCEID,PCERJEID      TEST PROCESSOR TYPE               R4
         BO    PPZTEST             BR IF REMOTE                      R4
         SPACE 1                                                     R4
         L     R15,POUTIOB         PICK UP OUTPUT IOB ADDRESSABILITY R4
         L     R14,PPBPCIE         PICK UP ACTIVE PCIE ADDRESS       R4
         SPACE 1                                                     R4
PPCICHK  DS    0H                                                    R4
         CLI   BUFECBCC,X'41'      LAST ELEMENT IN ERROR...    @OZ45731
         BE    PPZTEST             DO NOT UPDATE CHECKPOINT    @OZ45731
         TM    PCISGNAL,PCIBUSY    HAS PCI ELEMENT EXECUTED...       R4
         BO    PPZTEST             BR IF STILL BUSY                  R4
         CLI   PDEVTYP3,UCB3800    IS DEVICE A 3800 PRINTER    @G38ESBB
         BE    PPCIUPDT            BR IF YES                   @G38ESBB
         XC    DCTCSW(4),DCTCSW    CLEAR INT. REQUIRED INDIC   @OZ29138
         SPACE 1                                                     R4
***********************************************************************
*                                                                     *
*        PCIE HAS BEEN EXECUTED -- UPDATE BUFFER AVAILABLE COUNT      *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
PPCIUPDT DS    0H                                                    R4
         CLC   DCTCSW,$ZEROS       TEST FOR INTV REQD          @OZ54610
         BNE   PPCUPJOE            BRANCH IF YES               @OZ54610
         NI    PCISGNAL,255-PCIACTIV   FLAG PCIE OFF ACTIVE CHAIN    R4
         TM    PCISGNAL,PCIFNLBF   TEST IF DATA BUFFER(S) FINISHED   R4
         BZ    PPCUPJOE            BR IF NOT                         R4
         IC    R1,PBFAVAIL         INCREMENT NUMBER OF               R4
         LA    R1,1(,R1)            BUFFERS AVAILABLE FOR            R4
         STC   R1,PBFAVAIL           DE-SPOOLING (TO FORCE READ)     R4
         NI    PCISGNAL,255-PCIFNLBF RESET FLAG                      R4
         EJECT                                                 @G38ESBB
***************************************************************@G38ESBB
*                                                              @G38ESBB
*        FOR 3800 PRINTER, CALL PQECOMP SUBROUTINE TO          @G38ESBB
*        COMPLETE ANY PQE'S ASSIGNED DURING THIS CCW AREA      @G38ESBB
*                                                              @G38ESBB
***************************************************************@G38ESBB
         SPACE 1                                               @G38ESBB
PPCUPJOE CLI   PDEVTYP3,UCB3800    TEST FOR 3800 PRINTER       @G38ESBB
         BNE   PPCKPTCH            NO, BRANCH                  @G38ESBB
         SLR   R0,R0               CLEAR REGISTER              @G38ESBB
         IC    R0,BFWPQECT-BFWDSECT+PCIESIZE(,R14) GET COUNT   @G38ESBBC
                                   OF ID'S SOLICITED IN AREA   @G38ESBB
         L     PW,PQHADR           ADDRESS PQH                 @G38ESBB
         ST    PL,PQHSAVE1-PQHDSECT(,PW) SAVE RETURN ADDRESS   @G38ESBB
         L     R15,=A(PQECOMP)     CALL SUBROUTINE TO COMPLETE @G38ESBB
         BALR  PL,R15               PQE'S ASSIGNED IN CCW AREA @G38ESBB
         L     R15,POUTIOB         RESTORE IOB ADDRESS         @G38ESBB
         L     R14,PPBPCIE         RESTORE PCIE ADDRESS        @G38ESBB
         L     PL,PQHSAVE1-PQHDSECT(,PW) RESTORE RETURN ADR    @G38ESBB
         L     PW,PCEDCT           RESTORE DCT ADDRESS         @G38ESBB
         SPACE 1                                                     R4
***********************************************************************
*                                                                     *
*        SHOW CHECKPOINT NEEDED                                       *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
PPCKPTCH DS    0H                  CHECK IF CKPT NEEDED        @G38ESBB
         TM    PPFLAG2,PPCKPTA     ARE CHECKPOINTS ALLOWED           R4
         BZ    PPCNCKPT            BR IF NO                          R4
         TM    PCISGNAL,PCICKPT    IS NEW CKPT DATA PRESENT...       R4
         BZ    PPCNCKPT            BR IF NO                          R4
         NI    PCISGNAL,255-PCICKPT  RESET NEW CKPT FLAG             R4
         LA    R1,PCIESIZE(,R14)   POINT TO CHECKPOINT DATA          R4
         ST    R1,PPCKPTR          SAVE FOR PPPCKPT                  R4
         OI    PPFLAG2,PPCKPT      INDICATE CKPT-DATA NEEDS UPDATE   R4
         EJECT                                                       R4
***********************************************************************
*                                                                     *
*        IF PCIE NOT LAST ON CHAIN -- UPDATE CCW-AREAS INFORMATION    *
*                                                                     *
***********************************************************************
         SPACE 2                                                     R4
PPCNCKPT TM    PCEID,PCERJEID+PCEPRSID   TEST PROCESSOR TYPE   @OZ30757
         BNZ   PPCHKCC             BR IF NOT LOCAL PUNCH       @OZ30757
         L     R1,PPBCCWNX         PRESET                      @OZ30757
         SH    R1,=H'8'             PUNCH                      @OZ30757
         ST    R1,PUERRPT            ERROR POINTER             @OZ30757
         MVC   PULMTTR(7),PPBCMTTR    AND MTTR                 @OZ30757
         SPACE 1                                               @OZ30757
PPCHKCC  TM    PCI1FLGS,X'40'      CHECK NEXT CCW AREA READY   @OZ30757
         BZ    PPZTEST             BR IF COMMAND-CHAIN NOT ON  @OZ30757
         L     R1,PPBCCWNX         UPDATE IOBSTART TO REFLECT  @OZ30757
         ST    R1,IOBSTART          EXECUTION IN NEXT AREA     @OZ30757
         AH    R1,PCCWLAST         COMPUTE NEW PCIE ADDR AND   @OZ30757
         ST    R1,PPBPCIE           UPDATE PPBPCIE TO SHOW IT  @OZ30757
         L     R1,PPBLVCCN         UPDATE PTR TO LAST VALID    @OZ30757
         ST    R1,PPBLVCCC          CHAN CMND IN CURRENT AREA  @OZ30757
         MVC   PPBCMTTR(7),PPBNMTTR UPDATE RESTART POINTER     @OZ30757
         EJECT                                                       R4
***********************************************************************
*                                                                     *
*        IF $Z DEVICE -- $WAIT FOR OPERATOR ACTION                    *
*                                                                     *
*        CHECK FOR LOCAL PRINT/PUNCH I/O COMPLETION                   *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
PPZTEST  TM    DCTFLAGS,DCTSTOP    TEST FOR OPERATOR $Z              R4
         BO    PPPWAIT             BRANCH IF YES
         TM    PCEID,PCERJEID      TEST PROCESSOR TYPE               R4
         BO    PPWINIT             BR IF REMOTE                      R4
         SPACE 1                                                     R4
         TM    BUFECBCC,X'7F'      TEST I/O COMPLETION CODE
         BZ    PCITEST             BRANCH IF NOT CHANNEL END         R4
         BO    PPCGOOD             BRANCH IF NO ERRORS               R4
         SPACE 1                                                     R4
***********************************************************************
*                                                                     *
*        ERROR DETECTED -- START ANALYSIS                             *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         L     R1,PRPUUCB          ADDRESS DEVICE UCB
         TM    UCBFLB-UCBDSECT(R1),UCBIORST TEST UCB DEVICE STATUS
         BZ    PDEVOK              BRANCH IF M/P HAS NOT ABORTED
         CLC   $RELSE,=C'02'       TEST OS/VS2 RELEASE NUMBER  @OZ35278
         BE    PPIODRN             BR IF RELEASE 2                   R4
         L     R1,CVTPTR           GET ADDRESS OF CVT                R4
         CLC   CVTCRCA-CVT(,R1),=XL4'0'  TEST RECOVERY MODE          R4
         BNE   PDEVOK              BR IF DEVICE STILL ACCESSIBLE     R4
PPIODRN  DS    0H                                                    R4
         L     R15,=A(PDEVABRT)    CALL ABORT ROUTINE TO       @OZ51930
         BALR  PL,R15               CLEAN UP CONTROL BLKS      @OZ51930
         B     PPABORT             SUSPEND USE OF THE DEVICE
         SPACE 1                                                     R4
PDEVOK   DS    0H
        $COUNT                     ********************************* R4
         CLI   PDEVTYP3,UCB3800    TEST FOR 3800 PRINTER       @OZ51011
         BNE   PDEVOKA             BRANCH IF NOT               @OZ51011
         TM    DCTPPSW2,DCTCKJAM   PAPER JAM OR CANCEL KEY...  @OZ51011
         BZ    PDEVOKA             BRANCH IF NOT               @OZ51011
         NI    PPFLAG,FF-PRDELSW   RESET TERMINATION SWITCH    @OZ51011
         OI    PPFLAG,PPDELSW      SET SUSPENSION SWITCH       @OZ51011
         B     P3800LOC            LOCATE ORIGIN PQE           @OZ51011
         EJECT                                                 @OZ51011
PDEVOKA  DS    0H                                              @OZ51011
         TM    PPFLAG,PPDELSW      TEST FOR OPERATOR ACTION    @OZ51011
         BZ    SKIP330             BRANCH IF NONE              @OZ51011
         TM    PCEID,PCEPUSID      TEST FOR PUNCH DEVICE       @OZ51011
         BZ    PPWINIT             BRANCH IF NOT               @OZ51011
         TM    PPFLAG,PRDELSW      TEST FOR TERMINATION        @OZ51011
         BO    PPWINIT             BRANCH IF YES               @OZ51011
         SPACE 1                                                     R4
         DROP  PW                  SUSPEND DCT ADDRESSABILITY
         EJECT                                                       R4
***********************************************************************
*                                                                     *
*        FOR PRINTERS - IF INVALID CSW -- RETRY USING IOBSTART        *
*                       IF VALID CSW   -- RESTART AT CCW AFTER ERROR  *
*                       IF IN PCIE     -- SIMULATE PCI INTERRUPTION   *
*                                                                     *
***********************************************************************
SKIP330  DS    0H                                              @OZ40165
         TM    IOBCSW+4,X'3F'      CHANNEL ERRORS ? ....       @OZ40165
         BZ    SKIP331             ..NO CONTINUE               @OZ40165
         L     PW,IOBSTART         GET VALID RESTART ADDR      @OZ40165
         LA    PW,0(,PW)           CLEAR HIGH-ORDER BYTE       @OZ40165
         TM    PCEID,PCEPUSID       TEST PROCESSOR TYPE        @OZ40165
         BO    PUNCHERR            BR IF PUNCH                 @OZ40165
         B     PPRTHALT            BR IF PRINTER               @OZ40165
SKIP331  DS    0H                                              @OZ40165
         L     PW,IOBCSW-1         GET COMMAND ADDR FROM CSW   @OZ40165
         LA    PW,0(,PW)           CLEAR HIGH-ORDER BYTE
         CLM   PW,7,IOBSTART+1     IS CSW VALID...             @OZ53086
         BNL   PPDEVTYP            YES, GO TEST FOR PUNCH      @OZ53086
         ICM   PW,7,IOBSTART+1      ELSE USE IOBSTART          @OZ53086
PPDEVTYP DS    0H                                              @OZ53086
         TM    PCEID,PCEPUSID      TEST PROCESSOR TYPE               R4
         BO    PUNCHERR            BR IF PUNCH                       R4
         SPACE 1                                                     R4
         CLC   IOBCSW(3),IOBSTART+1  COMPARE CSW WITH FIRST CCW      R4
         BNH   PPIOMSG             BR IF ZERO OR INVALID.      @OZ44279
         CL    PW,PPBLVCCC         COMP RESTART CCW WITH END OF AREA R4
         BH    PCHKPCIE            BRANCH IF CSW BELOW AREA    @G38ESBB
         SPACE 1                                               @G38ESBB
         CLI   0(PW),PTICCMD       RESTART ON TIC...           @G38ESBB
         BNE   PRESTRT             NO, GO RESTART CHANNEL PGM  @G38ESBB
         L     PW,0(,PW)           ALTER RESTART CSW           @G38ESBB
         N     PW,PCVTREAL          TO POINT TO CCW            @G38ESBB
         AL    PW,POUTIOB            POINTED TO BY TIC         @G38ESBB
         B     PRESTRT             GO RESTART CHANNEL PROGRAM  @G38ESBB
         SPACE 1                                                     R4
PCHKPCIE L     R14,PPBPCIE         ADDRESS THE PCIE            @G38ESBB
         LA    R1,PCIESIZE(,R14)   IF RESTART CCW IS                 R4
         CLR   PW,R1                OUT OF ACTIVE AREA,              R4
         BNL   PPIOMSG               RESTART AT IOBSTART.      @OZ44279
         XC    IOBCSW(3),IOBCSW    CLEAR CSW TO FORCE RETRY    @OZ45731
         SPACE 1                                                     R4
PCISIMUL DS    0H                                              @OZ44279
         TM    PCEID,PCEPRSID      IS THIS A PRINTER...        @OZ44279
         BZ    SKIP332             BRANCH IF NOT.              @OZ44279
         S     PW,=F'8'            BACK UP RESTART ADDRESS.    @OZ44279
         MVI   0(PW),NOP           NOP LAST COMMAND.           @OZ44279
         B     PRESTRT             BRANCH TO HALT PRINTER.     @OZ44279
SKIP332  DS    0H                                              @OZ44279
         L     PW,PCEDCT           RESTORE DCT POINTER.        @OZ44279
         NI    PCISGNAL,255-PCIBUSY  SIMULATE PCI INTERRUPTION       R4
*              THIS LINE DELETED BY APAR OZ45731               @OZ45731
         TM    PCI1FLGS,X'40'      SEE IF NEXT CCW AREA IS READY     R4
         BO    PPCIUPDT            BR IF YES -- RECYCLE              R4
         B     PPWINIT              ELSE GO TO CLEAN UP              R4
         SPACE 1                                               @OZ44279
PPIOMSG  DS    0H                                              @OZ44279
         L     PW,IOBSTART         SET REGISTER FOR RESTART.   @OZ44279
         B     PRESTRT             BR TO HALT PRINTER          @OZ44279
NOP      EQU   3                                               @OZ44279
         EJECT                                                       R4
***********************************************************************
*                                                                     *
*        NORMAL I/O COMPLETION                                        *
*                                                                     *
*        IF CHE NOT ON EXPECTED PCIE -- HANDLE LOST INTERRUPTS        *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
PPCGOOD  DS    0H                                                    R4
         CL    R14,PPBPCIE         TEST FOR PCIE SWAP                R4
         BE    PPWINIT             BR IF ALL I/O COMPLETE            R4
         L     R14,PPBPCIE         ELSE, ADDRESS NEW PCIE            R4
         TM    PCISGNAL,PCIBUSY    HAS THIS NEW PCIE EXECUTED...     R4
         BO    PUREXCP             RESTART AT IOBSTART IF NOT        R4
         B     PPCIUPDT            ELSE, PROCESS NEW PCIE            R4
         SPACE 1                                                     R4
         DROP  R14                 SUSPEND PCIE ADDRESSABILITY       R4
         SPACE 2                                                     R4
***************************************************************@G38ESBB
*                                                              @G38ESBB
*        FOR 3800 PRINTERS, SEE IF INTERVENTION REQUIRED       @G38ESBB
*        DETERMINE COMMAND PROCESSING OR RESTART I/O IF NOT    @G38ESBB
*                                                              @G38ESBB
***************************************************************@G38ESBB
         SPACE 1                                               @G38ESBB
PPWINIT  CLI   PDEVTYP3,UCB3800    IS DEVICE A 3800 PRINTER    @G38ESBB
         BNE   PPWINIT2            BR IF NOT                   @G38ESBB
         TM    PPFLAG3,PP3800R     COMMAND IN PROGRESS...      @G38ESBB
         BO    PTSTRST             YES, BYPASS LOCATE          @OZ46290
         L     R15,PQHADR          ADDRESS PQH                 @G38ESBB
         TM    PQHFLAG-PQHDSECT(R15),PQHXFER XFER COMMAND...   @G38ESBB
         BZ    PTSTRST             BR IF NOT                   @G38ESBB
         SPACE 1                                               @G38ESBB
P3800LOC L     R15,PQHADR          ADDRESS PQH                 @G38ESBB
         NI    PQHFLAG-PQHDSECT(R15),FF-PQHXFER RESET CMD FLAG @G38ESBB
         ST    PL,PQHSAVE1-PQHDSECT(,R15) SAVE RETURN ADDRESS  @G38ESBB
         L     R15,=A(PLOCATE)     FIND PAGE ID AND PQE ON     @G38ESBB
         BALR  PL,R15                   3800 PAPER LINE        @G38ESBB
         L     PL,PQHADR           ADDRESS PQH                 @G38ESBB
         L     PL,PQHSAVE1-PQHDSECT(,PL) RESTORE RETURN ADR    @G38ESBB
         TM    PPFLAG3,PP3800R     IS THE COMMAND PROCESSABLE  @G38ESBB
         BZ    PTSTRST             BR IF NOT                   @G38ESBB
         NI    PPFLAG,FF-PPWSW     INDICATE WRITE COMPLETED    @G38ESBB
         B     PPDSEND             GO PROCESS COMMAND          @G38ESBB
         SPACE 1                                               @G38ESBB
PTSTRST  L     PW,PQHADR           ADDRESS PQH                 @G38ESBB
         TM    PQHFLAG-PQHDSECT(PW),PQH2CMD DOUBLE COMMAND...  @G38ESBB
         BO    PPABORT             YES, GO ABORT JOB           @G38ESBB
         L     R15,POUTIOB         RESTORE OUTPUT BUFFER ADDR  @G38ESBB
         L     PW,PCEDCT           RESTORE DCT                 @G38ESBB
         USING DCTDSECT,PW              ADDRESSABILITY         @G38ESBB
         CLC   DCTCSW,$ZEROS       INTERVENTION REQUIRED...    @G38ESBB
         BE    PPWCKIO             BRANCH IF NOT               @OZ48114
         L     R14,PPBPCIE         ELSE SETUP TO RESTART I/O   @G38ESBB
         USING PCIDSECT,R14        PCI ADDRESSABILITY          @G38ESBB
         OI    PCISGNAL,PCIBUSY+PCIACTIV IND AREA BUSY/ACTIVE  @G38ESBB
         L     R1,PQHADR           GET PQH ADDRESS             @G38ESBB
         OC    PCI1FLGS,PQHPCICH-PQHDSECT(R1) RESET CHAINING   @G38ESBB
         NI    PCISGNAL,FF-PCIABORT  RESET ABORT FLAG          @OZ46290
         L     R1,DCTCSW           GET RESTART CSW             @G38ESBB
         LA    R1,0(,R1)           CLEAR HIGH ORDER BYTE       @G38ESBB
         SH    R1,PCCWLENG         BACK UP TO PREVIOUS CCW     @G38ESBB
         CLI   0(R1),PTICCMD       IS RESTART CCW A TIC...     @G38ESBB
         BNE   PZEROCSW            NO, GO RESTART FROM DCTCSW  @G38ESBB
         SH    R1,PCCWLENG         BACKUP BEFORE TIC CCW       @G38ESBB
         SPACE 1                                               @G38ESBB
PZEROCSW ST    R1,IOBSTART         SET UP RESTART ADDRESS      @G38ESBB
         XC    DCTCSW,DCTCSW       ZERO INT REQ INDICATOR      @G38ESBB
         B     PURETRY             GO RESTART I/O              @G38ESBB
         DROP  PW                  DROP DCT ADDRESSABILITY     @G38ESBB
         SPACE 1                                               @G38ESBB
PPWCKIO  TM    BUFECBCC,X'7F'      IS ERROR RCVY REQD...       @OZ48114
         BM    SKIP330             BRANCH IF YES               @OZ48114
         SPACE 1                                               @OZ48114
PPWINIT2 NI    PPFLAG,FF-PPWSW     INDICATE WRITE COMPLETED    @G38ESBB
         B     PPPCKPT             GO TO CHECKPOINT ROUTINE          R4
         SPACE 1                                                     R4
PCITEST  CLI   PDEVTYP3,UCB3800    TEST FOR 3800               @G38ESBB
         BNE   PCITEST1            BR IF NOT                   @G38ESBB
         TM    PPFLAG,PPDELSW      TEST FOR COMMAND            @G38ESBB
         BZ    PCITEST1            BR IF NOT                   @G38ESBB
         NI    PPFLAG2,FF-PPCIWAIT INDIC WAIT FOR CE           @G38ESBB
         SPACE 1                                               @G38ESBB
PCITEST1 TM    PPFLAG2,PPCIWAIT    PCI-WAIT INDICATION         @G38ESBB
         BZ    PPPWAIT             BR IF NO                          R4
         SPACE 1                                               @G38ESBB
*              DELETED                                         @G38ESBB
*              DELETED                                         @G38ESBB
         TITLE 'HASP PRINT/PUNCH SERVICE -- PROCESSOR CHECKPOINT'    R4
***********************************************************************
*                                                                     *
*        FOR IMPACT PRINTER, PERFORM CHECKPOINT                @G38ESBB
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
PPPCKPT  NI    PPFLAG2,255-PPCIWAIT  RESET PCI-WAIT INDICATION       R4
         TM    PPFLAG2,PPCKPT      CKPT DATA NEED UPDATING...       R41
         BZR   PL                  RETURN IF NOT                    R41
         NI    PPFLAG2,255-PPCKPT  RESET CKPT-NEEDED FLAG            R4
         SPACE 1                                                    R41
PPPCKPT1 TM    PPFLAG2,PPCKPTA     CHECKPOINTS ALLOWED...           R41
         BZR   PL                  RETURN IF NOT                    R41
         SPACE 1                                               @G38ESBB
         LR    R1,PL               SAVE RETURN REGISTER        @G38ESBB
         L     R15,=A(PGETQS)      CALL CKPT                   @G38ESBB
         BALR  PL,R15               ACCESS ROUTINE             @G38ESBB
         LR    PL,R1               RESTORE RETURN REGISTER     @G38ESBB
         BNZR  PL                  Q'S NOT OWNED, RETURN       @G38ESBB
         SPACE 1                                               @G38ESBB
         PRINT OFF                 THIS SECTION DELETED BY     @G38ESBB
*              DELETED                                         @G38ESBB
*              DELETED                                         @G38ESBB
*              DELETED                                         @G38ESBB
*              DELETED                                         @G38ESBB
*              DELETED                                         @G38ESBB
*              DELETED                                         @G38ESBB
*              DELETED                                         @G38ESBB
         PRINT ON                  THIS SECTION DELETED BY     @G38ESBB
         SPACE 1                                               @G38ESBB
         L     R1,PCKJOE           ADDRESS OF CKPT-JOE               R4
         USING JOEDSECT,R1         ACTIVATE JOE ADDRESSABILITY       R4
         L     PW,PPCKPTR          PICK-UP CKPT DATA POINTER         R4
         MVC   JOECKPP,0(PW)        AND UPDATE CKPT-JOE        @OZ27300
         DROP  R1                  SUSPEND JOE ADDRESSABILITY        R4
        $#CKPT JOE=0(,R1),TYPE=A   SCHEDULE CKPT-JOE FOR CHECKPOINT  R4
         BR    PL                   AND RETURN                       R4
         SPACE 1                                               @G38ESBB
         DROP  R15                 DROP BUF ADR                @G38ESBB
         USING BUFDSECT,PBUF       RE-ESTABLISH BUF ADR        @G38ESBB
         TITLE 'HASP PRINT/PUNCH SERVICE -- TRACK-CELL READ ROUTINES'
***********************************************************************
*                                                                     *
*        PRDTCNXT - READ NEXT TRACK-CELL OR IOT                       *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
PRDTCNXT DS    0H                                                    R4
         TM    PPFLAG2,PPTCEL      TEST DESPOOLING METHOD            R4
         BZ    PRDNXTB             BRANCH IF SINGLE                  R4
         ICM   R15,15,PCENXTRK     TEST FOR ZERO CHAIN               R4
         BZ    SKIP340             BRANCH IF LAST BLOCK READ         R4
         TM    PPFLAG,PPDELSW+PPRDERR TEST FOR SUSPEND OR READ ERROR R4
         BZ    PRDTCEL             BRANCH IF NO                      R4
SKIP340  TM    PPFLAG,PPNEWS       RETURN IF END OF            @OZ25573
         BOR   PL                   JES2-NEWS DATA SET         @OZ25573
         L     R15,PCEIOTTR        GET IOT MTTR                @OZ25573
         B     PRDBUF              GO TO SINGLE BUFFER READ ROUTINE  R4
         SPACE 2                                                     R4
***********************************************************************
*                                                                     *
*        PRDTCEL - READ ENTIRE TRACK-CELL                             *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
PRDTCEL  DS    0H                                                    R4
         TM    PPFLAG2,PPTCEL      TEST DESPOOLING METHOD            R4
         BZ    PRDBUF              BRANCH IF SINGLE                  R4
         ST    PL,PLSAVE           SAVE RETURN REGISTER              R4
         STM   PW,PBUF,PCEWA       SAVE WORKING REGISTERS            R4
         L     PBUF,PINIOB         ACTIVATE INPUT IOB ADDRESSABILITY R4
         ST    R15,PCESEEK         SAVE BUFFER MTTR                  R4
         STCM  R15,8,IOBXTENT      SET IOB EXTENT FIELD TO (M)       R4
         SLR   PL,PL               EXPAND $RECINCR                   R4
         IC    PL,$RECINCR          TO A FULLWORD                    R4
         SLR   R0,R0               COMPUTE                           R4
         SLR   R1,R1                SUB-                             R4
         IC    R1,PCESEEK+3          PERMUTATION                     R4
         DR    R0,PL                  NUMBER            R0 = 000P    R4
         LTR   R0,R0                   OF                            R4
         BNZ   SKIP350                  INITIAL                      R4
         IC    R0,$RECINCR               BUFFER                      R4
SKIP350  STC   R0,PCESEEK          REPLACE MTTR WITH PTTR            R4
         LR    R0,R15              R0 = MTTR            . MTTR       R4
         SRL   R15,24              COMPUTE ADDRESS      . 000M       R4
         MH    R15,=AL2(TEDSIZ)     OF TED FOR THIS     . M * TEDSIZ R4
         AL    R15,$TEDADDR          EXTENT             . TED ENTRY  R4
         USING TEDDSECT,R15        ACTIVATE TED ADDRESSABILITY       R4
         SPACE 1                                                     R4
         SLR   PW,PW               CLEAR TRACK-CELL BUFFER COUNT     R4
         IC    PW,PBUFSKIP         PICK UP RESTART POINT (IF ANY)    R4
         SPACE 1                                                     R4
         L     R14,PBUFSAVE        GET INPUT BUFFER-HEAD             R4
         L     R1,PINMTTRT         GET ADDR OF MTTR/BFR-ADDR TBL     R4
         EJECT                                                       R4
*                                                                    R4
*        BUILD TABLE OF MTTR'S AND CORRESPONDING BUFFER ADDRS        R4
*                                                                    R4
PRDINSRT ST    R0,0(,R1)           PLACE MTTR AND BUFFER-            R4
         ST    R14,4(,R1)           ADDRESS INTO THIS ENTRY          R4
         MVI   BUFECBCC-BUFDSECT(R14),0 RESET ANY ERROR CONDITIONS   R4
         LA    PW,1(,PW)           INCREMENT BUFFER COUNT            R4
         CLM   PW,1,$TCELSIZ       TEST FOR END OF TRACK-CELL        R4
         BNL   PRDTSORT            BR IF YES -- TABLE BUILT          R4
         ALR   R0,PL               R0 = MTTR + $RECINCR              R4
PRECHECK CLM   R0,1,TNRT+1         TEST RECORD NUMBER                R4
         BNH   PRDINCR             BRANCH IF STILL ON TRACK          R4
         CLC   PCESEEK(1),$RECINCR IF RECORD WAS LAST ON TRACK,      R4
         BNL   PRDTSORT             THEN HANDLE -STUNTED- TRACK-CELL R4
         IC    R0,PCESEEK          GET BEGINNING RECORD NUMBER,      R4
         AL    R0,=F'1'             UP IT BY 1, AND SAVE NEW         R4
         STC   R0,PCESEEK            BEGINNING RECORD NUMBER         R4
         B     PRECHECK            RE-CHECK NEW MTTR                 R4
PRDINCR  LA    R1,2*4(,R1)         INCR TO NEXT TABLE EnTRY, AND     R4
         L     R14,BUFCHAIN-BUFDSECT(,R14) STEP TO NEXT BUFFER       R4
         B     PRDINSRT            LOOP UNTIL TABLE BUILT            R4
         DROP  R15                 SUSPEND TED ADDRESSABILITY        R4
*                                                                    R4
*        MTTR/BUFAD TABLE COMPLETE -- SORT IT                        R4
*                                                                    R4
PRDTSORT DS    0H                                                    R4
         L     R14,PBUFSAVE        ADDRESS OF FIRST BUFFER IN CHAIN  R4
         SLR   R15,R15             OBTAIN RESTART                    R4
         IC    R15,PBUFSKIP         BUFFER OFFSET                    R4
         SLR   PW,R15              COMPUTE ACTUAL NUMBER OF BUFFERS  R4
         STC   R15,BUFCHOFF-BUFDSECT(,R14) SAVE OFFSET OF 1ST BUFFER R4
         STH   PW,BUFCHNCT-BUFDSECT(,R14) INSERT BUFFER CHAIN COUNT  R4
         MVI   PBUFSKIP,0          RESET RESTART BUFFER OFFSET       R4
         LR    R15,PW              DECREMENT BUFFER                  R4
         BCT   R15,SKIP360          COUNT BY 1 AND SKIP              R4
         B     PRDBCCWS              THE SORT IF ONLY 1              R4
         SPACE 1                                                     R4
SKIP360  L     R1,PINMTTRT         ADDRESS OF MTTR/BUFAD TABLE       R4
         LA    R14,8               SET BXLE INCREMENT VALUE          R4
         SLL   R15,3               SET BXLE LIMIT VALUE              R4
         ALR   R15,R1               = R1 + 8*(COUNT-1). (LAST ENTRY) R4
         SPACE 1                                                     R4
PRLOOP1  CLR   R1,R15              IF END OF TABLE,                  R4
         BE    PRDBCCWS             BYPASS LAST COMPARE              R4
PRLOOP1R LA    PL,8(,R1)           SET/RESET COMPARE POINTER         R4
PRLOOP2  CLC   3(1,R1),3(PL)       TEST CURRENT REC NBR WITH NEXT    R4
         BH    PRSWAP              BR IF HIGH                        R4
         BXLE  PL,R14,PRLOOP2      INCR AND LOOP FOR COMPARE         R4
         BXLE  R1,R14,PRLOOP1      INCR AND LOOP FOR ENTIRE TABLE    R4
         SPACE 1                                                     R4
PRSWAP   XC    0(8,R1),0(PL)       SWAP POSITIONS OF MTTRS           R4
         XC    0(8,PL),0(R1)        AND BUFFER ADDRESSES WHICH       R4
         XC    0(8,R1),0(PL)         WERE OUT OF ORDER               R4
         B     PRLOOP1R            RESTART CURRENT COMPARE           R4
         EJECT                                                       R4
*                                                                    R4
*        BUILD SET-SECTOR CCW AT IOBCCW1 (IF SUPPORTED)              R4
*                                                                    R4
*        BUILD INPUT CCW'S STARTING AT IOBCCW2                       R4
*                                                                    R4
PRDBCCWS DS    0H                                                    R4
         SLR   R15,R15             OBTAIN                            R4
         IC    R15,0(,R1)           POINTER                          R4
         MH    R15,=AL2(TEDSIZ)      TO                              R4
         AL    R15,$TEDADDR           TRACK-EXTENT-DATA              R4
         USING TEDDSECT,R15        ACTIVATE TED ADDRESSABILITY       R4
         MVC   PCEWC,TNTC          SAVE NUMBER OF TRACKS/CYLINDER    R4
         SPACE 1                                                     R4
PRDSECTR DS    0H                                                    R4
         L     R1,PINMTTRT         POINT TO MTTR/BUFAD TABLE         R4
         TM    $RUNOPTS,$RPS       TEST FOR RPS IN SYSTEM            R4
         BZ    PRDBUILD            BR IF NOT                         R4
         L     PL,TRPS             GET ADDRESS OF RPS TABLE          R4
         MVI   IOBCCW1,X'03'       SET FIRST CHANNEL COMMAND TO NOP  R4
         LTR   PL,PL               TEST IF EXTENT SUPPORTS RPS       R4
         BZ    PRDBUILD            BR IF NO -- SKIP RPS COMPUTATION  R4
         MVI   IOBCCW1,X'23'       SET CHANNEL COMMAND TO SET SECTOR R4
         SLR   R14,R14             CLEAR REGISTER FOR IC             R4
         IC    R14,3(,R1)          GET RECORD NUMBER                 R4
         IC    R14,0(R14,PL)       GET CORRESPONDING SECTOR NUMBER   R4
         STC   R14,IOBCCW1+5       PUT SECTOR NUMBER IN SET-SECTOR   R4
         DROP  R15                 SUSPEND TED ADDRESSABILITY        R4
         SPACE 1                                                     R4
PRDBUILD DS    0H                                                    R4
         LA    PL,IOBCCW2          POINT TO CCW-BUILD AREA           R4
         MVI   IOBCCW4,6           SET OP-CODE TO READ-DATA         R41
         OI    IOBCCW4+4,X'40'     SET COMMAND-CHAIN BIT IN SKELETON R4
         B     SKIP370             FIRST SET OF CCWS ARE BUILT       R4
         SPACE 1                                                     R4
PRDSKEL  LA    PL,3*8(,PL)         POINT TO NEXT CCW-BUILD AREA      R4
         MVC   0(3*8,PL),IOBCCW2       SRCH/TIC/READ CCW SKELETON    R4
SKIP370  L     R14,4(,R1)              ADDRESS OF DATA               R4
         LA    R14,BUFSTART-BUFDSECT(,R14) PORTION OF BUFFER         R4
         LA    R0,3(,R1)               POINT TO CCHHR                R4
         STCM   R0,7,0*8+1(PL)         SRCH TARGET   (CCHHR)         R4
         STCM   PL,7,1*8+1(PL)         TIC TARGET    (SRCH CCW)      R4
         STCM  R14,7,2*8+1(PL)         READ TARGET   (BUF ADDR)      R4
         SPACE 1                                                     R4
         L     R14,0(,R1)          R14 = MTTR                        R4
         LA    R14,0(,R14)         R14 = 0TTR                        R4
         SPACE 1                                                     R4
*                                    MBBCCHHR FROM MTTR CONVERSION   R4
         STC   R14,7(,R1)           -------R                         R4
         SRDL  R14,40               ISOLATE TT IN R14/R15            R4
         D     R14,PCEWC            COMPUTE CYLINDER AND HEAD        R4
         STCM  R14,3,5(R1)          -----HHR     HEAD                R4
         STCM  R15,3,3(R1)          ---CCHHR   CYLINDER              R4
         XC    0(3,R1),0(R1)        000CCHHR     MBB                 R4
         LA    R1,8(,R1)           POINT TO NEXT ENTRY               R4
         BCT   PW,PRDSKEL          LOOP FOR ENTIRE TABLE             R4
         EJECT                                                       R4
*                                                                    R4
*        ISSUE EXCP FOR THIS TRACK-CELL                              R4
*                                                                    R4
         NI    2*8+4(PL),255-X'40' ZERO COMMAND-CHAIN IN LAST CCW    R4
         ST    PBUF,PCEBUFAD       IOB ADDRESS TO DA DCT             R4
         LA    R0,IOBCCW1          RESET IOBSTART                    R4
         ST    R0,IOBSTART          FOR READ                         R4
         L     R1,PINMTTRT         SET IOBSEEK TO 1ST BUFFER'S       R4
         MVC   IOBSEEK(7),1(R1)     TRACK ADDRESS                    R4
         MVC   PCESEEK,$HASPDCB    SET UP DA DCT SO THAT $EXCP       R4
         MVI   PCEDEVTP,DCTINT      WILL NOT CONVERT PCESEEK         R4
         LA    R1,PCEDADCT         POINT TO IOB                      R4
        $EXCP  (R1)                ISSUE READ FOR TRACK-CELL         R4
         OI    PPFLAG2,PPRSW       INDICATE READ IN PROGRESS         R4
         IC    PL,PBFAVAIL         DECREMENT NUMBER                  R4
         BCTR  PL,0                 OF AVAILABLE                     R4
         STC   PL,PBFAVAIL           INPUT BUFFERS                   R4
         LM    PW,PBUF,PCEWA       RESTORE WORKING REGISTERS         R4
         L     PL,PLSAVE           RESTORE RETURN REGISTER           R4
         BR    PL                   AND RETURN                       R4
         EJECT                                                       R4
***********************************************************************
*                                                                     *
*        PRDTCHK - WAIT FOR, AND CHECK COMPLETION OF TRACK-CELL READ  *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
PRDTCHK  DS    0H                                                    R4
         TM    PPFLAG2,PPTCEL      TEST DESPOOLING METHOD            R4
         BZ    PRDCHK              BR IF SINGLE                      R4
         ST    PL,PLSAVE           SAVE RETURN REGISTER              R4
         L     PBUF,PINIOB         ACTIVATE INPUT IOB ADDRESSABILITY R4
         L     R1,PBUFSAVE         SWAP                              R4
         MVC   PBUFSAVE,PBUFADDR    TRACK-CELL                       R4
         ST    R1,PBUFADDR           BUFFER POINTERS                 R4
         NI    IOBCCW4+4,255-X'40' TURN OFF COMMAND CHAINING BIT     R4
PRDTCIO  DS    0H                                                    R4
         TM    BUFECBCC,X'7F'      TEST I/O COMPLETION               R4
         BO    PRDTSCAN            BRANCH IF NO ERRORS               R4
         BNZ   PRDTCERR            BRANCH IF ERROR                   R4
        $WAIT  IO                  WAIT FOR CHANNEL END              R4
         B     PRDTCIO              AND CHECK AGAIN                  R4
         SPACE 1                                                     R4
PRDTCERR DS    0H                                                    R4
         L     PL,IOBCSW-1         OBTAIN POINTER                    R4
         LA    PL,0(,PL)            TO FAILING                       R4
         SH    PL,=H'8'              CHANNEL COMMAND                 R4
         LA    R15,3*8             ADDRESS OF                        R4
         MH    R15,BUFCHNCT-BUFDSECT(,R1) LAST POSSIBLE              R4
         LA    R15,IOBCCW1-BUFDSECT(R15,PBUF) READ COMMAND           R4
         CLM   PL,7,IOBSTART+1     VALIDATE CSW                      R4
         BL    SKIP380              CONTENTS AND                     R4
         CLR   PL,R15                USE IOBSTART                    R4
         BNH   SKIP390                IF CSW IS                @OZ39281
SKIP380  L     PL,IOBSTART             OUT OF RANGE                  R4
         LA    PL,0(,PL)           CLEAR SIO CONDITION CODE    @OZ39281
SKIP390  LA    R14,8               BXLE INCREMENT VALUE              R4
PRDTLOCR CLI   0(PL),6             LOCATE READ                       R4
         BE    SKIP400              CCW FOR BUFFER                   R4
         BXLE  PL,R14,PRDTLOCR       IN ERROR                        R4
         SPACE 1                                                     R4
SKIP400  L     R1,0(,PL)           EXTRACT BUFFER ADDRESS            R4
         SL    R1,=A(BUFSTART-BUFDSECT)  ADJUST TO START OF BUFFER   R4
         OI    BUFECBCC-BUFDSECT(R1),PPRDERR SET READ ERROR FLAG     R4
         CLR   PL,R15              WAS ERROR BUFFER LAST IN CHAIN    R4
         BNE   *+6                 BR IF NOT                         R4
         SLR   PL,PL                ELSE CLEAR RESTART ADDRESS       R4
        $IOERROR (PBUF)            RECORD THE BAD NEWS               R4
         LTR   PL,PL               TEST FOR POSSIBLE RESTART         R4
         BZ    PRDTSCAN            BR IF NO -- ALL BUFFERS READ      R4
         LA    PL,8(,PL)           GET RESTART CCW ADDRESS           R4
         ST    PL,IOBSTART          AND PLACE IN IOB                 R4
         L     R1,0(,PL)           GET POINTER TO SEARCH TARGET      R4
         MVC   IOBSEEK+2(5),0(R1)  MOVE CCHHR INTO IOB               R4
         LA    R1,PCEDADCT         PICK UP DA DCT ADDRESS            R4
        $EXCP  (R1)                RESTART CHANNEL PROGRAM           R4
         L     R1,PBUFADDR         PICK UP BUFFER CHAIN HEAD ADDRESS R4
         B     PRDTCIO              AND GO CHECK ON I/O              R4
         SPACE 3                                                     R4
*                                                                    R4
*        ALL BUFFERS HAVE BEEN READ (OR SKIPPED)                     R4
*                                                                    R4
*        VALIDATE CHAIN AND OBTAIN MTTR OF NEXT TRACK-CELL           R4
*                                                                    R4
PRDTSCAN DS    0H                                                    R4
         L     PBUF,PBUFADDR       ADDRESSABILITY ON 1ST BUFFER      R4
         LH    R15,BUFCHNCT        NUMBER OF BUFFERS IN THIS CHAIN   R4
         LA    PBUF,PBUFADDR-(BUFCHAIN-BUFDSECT) INITIAL POSITIONING R4
PRDTSCN1 L     PBUF,BUFCHAIN       ADDRESSABILITY ON NEXT BUFFER     R4
         CLC   HDBKEY,PPKEY        DO DATA AND JOB KEYS MATCH        R4
         BNE   PRDTRDER            BR IF NOT                         R4
         TM    BUFECBCC,PPRDERR    DID READ FOR THIS BUFFER FAIL     R4
         BO    PRDTUPDT            BR IF YES                         R4
         OC    HDBNXTRK,HDBNXTRK   TEST FOR LAST BUFFER IN DATA SET  R4
         BZ    PRDTUPDT            BR IF YES                         R4
         BCT   R15,PRDTSCN1        CHECK ALL BUFFERS IN CHAIN        R4
         MVC   PCENXTRK,HDBNXTRK   MTTR OF NEXT TRACK-CELL           R4
         L     PBUF,PBUFADDR       ESTAB. DATA BUFFER ADDRESSABILITY R4
         LH    R1,BUFCHNCT         OBTAIN BUFFER COUNT               R4
         B     PRDTCRET             AND RETURN                       R4
         SPACE 1                                                     R4
*                                                                    R4
*        BUFFER IN CHAIN IS NOT VALID -- UPDATE CHAIN COUNT          R4
*                                                                    R4
PRDTRDER OI    BUFECBCC,PPRDERR    INDICATE BUFFER VALIDITY ERROR    R4
PRDTUPDT XC    PCENXTRK,PCENXTRK   FORCE END OF                      R4
         XC    HDBNXTRK,HDBNXTRK    BUFFER CHAIN                     R4
         L     PBUF,PBUFADDR       ESTAB. DATA BUFFER ADDRESSABILITY R4
         LH    R1,BUFCHNCT         COUNT OF BUFFERS IN CHAIN         R4
         BCTR  R15,0               COMPUTE NUMBER OF                 R4
         SLR   R1,R15               VALID BUFFERS IN CHAIN           R4
         SPACE 1                                                     R4
PRDTCRET IC    R15,BUFCHOFF        UPDATE BUFFER CHAIN COUNT         R4
         ALR   R1,R15               TO REFLECT RESTART               R4
         STH   R1,BUFCHNCT           OFFSET                          R4
         STC   R15,PCEJBOFF        INIT TRACK-CELL BUFFER OFFSET     R4
         NI    PPFLAG2,255-PPRSW   INDICATE READ COMPLETED           R4
         L     PL,PLSAVE           RESTORE RETURN REGISTER           R4
         BR    PL                   AND RETURN TO CALLER             R4
         TITLE 'HASP PRINT/PUNCH SERVICE -- SINGLE DATA BUFFER READ ROUC
               TINES'                                                R4
***********************************************************************
*                                                                     *
*        SINGLE BUFFER READ ROUTINES -- PRDNXTB -- PRDBUF -- PRDCHK   *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
*
*        READ NEXT DATA BLOCK - IOT IF CHAIN TRACK IS ZERO
*
PRDNXTB  ICM   R15,15,HDBNXTRK     IS CHAIN TRACK ZERO
         BZ    *+12                BRANCH IF LAST BLOCK
         TM    PPFLAG,PPDELSW+PPRDERR  SUSPEND OR READ ERROR
         BZ    PRDBUF              BRANCH IF NO
         TM    PPFLAG,PPNEWS       RETURN IF END OF                 R41
         BOR   PL                   JES2-NEWS DATA SET              R41
         L     R15,PCEIOTTR        GET IOT TRACK ADDRESS
         DROP  PBUF                KILL BUFFER ADDRESSABILITY
*
*        READ DATA BUFFER FROM SPOOL PACK
*
PRDBUF   L     R1,PINIOB           PICK UP INPUT IOB ADDRESS         R4
         L     LINK,PBUFSAVE       PICK UP DATA BUFFER ADDRESS       R4
         ST    R1,PCEBUFAD         IOB ADDRESS TO DA DCT             R4
         USING BUFDSECT,R1         ESTABLISH BUFFER ADDRESSABILITY
         LA    LINK,BUFSTART-BUFDSECT(,LINK) START OF DATA AREA      R4
         STCM  LINK,7,IOBCCW4+1    PLACE INTO READ CCW               R4
         LA    LINK,IOBSEEK+2      PLACE SEEK TARGET ADDRESS         R4
         STCM  LINK,7,IOBCCW2+1     INTO SEARCH CCW                  R4
         LA    R0,IOBCCW1          RESET IOBSTART
         ST    R0,IOBSTART         FOR READ
         MVI   PCEDEVTP,PCEDARD    SET DA DCT TO READ                R4
         LA    R1,PCEDADCT         SETUP DCT ADDRESS FOR $EXCP
         ST    R15,PCESEEK         STORE TRACK ADDRESS IN DCT
        $EXCP  (R1)                INITIATE READ
         OI    PPFLAG2,PPRSW       INDICATE READ IN PROGRESS         R4
         IC    R1,PBFAVAIL         DECREMENT NUMBER                  R4
         BCTR  R1,0                 OF AVAILABLE                     R4
         STC   R1,PBFAVAIL           INPUT BUFFERS                   R4
         BR    PL                  RETURN
               EJECT                                                 R4
*
*        WAIT/CHECK SPOOL READ COMPLETION                            R4
*
PRDCHK   DS    0H                  *
         L     PBUF,PBUFSAVE       SWAP PRIMARY                      R4
         MVC   PBUFSAVE,PBUFADDR    AND SECONDARY                    R4
         ST    PBUF,PBUFADDR         BUFFER POINTERS                 R4
         L     R1,PINIOB           PICK UP INPUT IOB ADDRESSABILITY  R4
PRDRECHK TM    BUFECBCC,X'7F'      TEST FOR I/O COMPLETE
         BO    PRDRETRN            BRANCH IF READ SUCCESSFUL         R4
         BNZ   PRDERROR            BRANCH IF ERROR DETECTED
        $WAIT  IO                  WAIT FOR I/O POST
         B     PRDRECHK            GO BACK AND TRY AGAIN
PRDERROR DS    0H
         OI    PPFLAG,PPRDERR      READ ERROR, SET FLAG
        $IOERROR PINIOB            LOG I/O ERROR                     R4
         SPACE 1                                                     R4
PRDRETRN DS    0H                                                    R4
         NI    PPFLAG2,255-PPRSW   INDICATE READ COMPLETED           R4
         BR    PL                  AND RETURN
         DROP  R1                  KILL LOCAL BUFFER ADDRESSABILITY  R4
         USING BUFDSECT,PBUF       ESTAB. DATA BUFFER ADDRESSABILITY R4
         TITLE 'HASP PRINT/PUNCH SERVICE -- PROCESSOR TERMINATION'
***********************************************************************
*                                                                     *
*        CHECK TERMINATION REASON - PUNCH BLANK TO FLUSH PUNCH        *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
PPDONE   DS    0H
         NI    PPFLAG,255-PPDELSW  RESET DELETE FLAG
         TM    PPFLAG,PRDELSW      IS THIS A NORMAL TERMINATION
         BZ    PRTRAILR            BRANCH IF YES
         TM    PCEID,PCEPRSID      IS THIS A PRINT PROCESSOR
         BO    PRABEOJ             BRANCH IF YES
         LM    PC1,PC2,PUCCWBL     LOAD BLANK CARD CCW
         BAL   PL,PPPUT            ADD CCW TO CHAIN
         BAL   PL,PPWRITE           INITIATE WRITE
         BAL   PL,PPCHECK            AND CHECK
         SPACE 1                                                     R4
***********************************************************************
*                                                                     *
*        PROVIDE REASON FOR TERMINATION ON PROGRAMMERS LISTING        *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
PRABEOJ  DS    0H                  *
        $MID   170                                                   R4
         LA    R1,=C'$HASP170 '    MESSAGE ID                        R4
         TM    PDCTFLAG,DCTRSTRT+DCTBKSP $I - (INTERRUPT)
         BNO   PGMRST              BRANCH IF NO
         LA    R14,=C' INTERRUPTED' MESSAGE TEXT               @OZ48259
         OI    PSMFDCI,SMFINTRP    SET SMF FLAG
         B     PGMRMSG             GO WRITE MESSAGE
         SPACE 1                                                     R4
PGMRST   DS    0H
         TM    PDCTFLAG,DCTRSTRT   $E - (RESTART)
         BNO   PGMDEL              BRANCH IF NO
         LA    R14,=C' RESTARTED  ' MESSAGE TEXT               @OZ48259
         OI    PSMFDCI,SMFRESTR    SET SMF FLAG
         B     PGMRMSG             GO WRITE MESSAGE
         SPACE 1                                                     R4
PGMDEL   DS    0H
         TM    PDCTFLAG,DCTDELET   $C - (CANCEL)
         BNO   PGMTRM              BRANCH IF NO
         LA    R14,=C' DELETED    ' MESSAGE TEXT               @OZ48259
         OI    PSMFDCI,SMFOPSTP    SET SMF FLAG
         B     PGMRMSG             GO WRITE MESSAGE
         SPACE 1                                                     R4
PGMTRM   DS    0H
        $MID   185                                                   R4
         LA    R1,=C'$HASP185 '    MESSAGE ID                        R4
         LA    R14,=C' TERMINATED ' MESSAGE TEXT               @OZ48259
         SPACE 1                                               @OZ19494
PGMRMSG  L     R15,=A(PRMSG)       ISSUE MSG TO OPERATOR       @OZ19494
         BALR  PL,R15               AND ADD IT TO OUTPUT       @OZ48259
         EJECT
***********************************************************************
*                                                                     *
*        SETUP DEVICE BY CHAR-JOE AND PRINT TRAILER PAGE              *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         USING JOEDSECT,R1         PROVIDE CHAR-JOE ADDRESSABILITY   R4
         SPACE 1                                                     R4
PRTRAILR DS    0H
         L     R15,=A(PPSMF6)      GENERATE FINAL              @OZ32776
         BAL   PL,0(,R15)           TYPE-6 SMF RCD (IN BUFFER) @OZ34616
         L     R1,PCHJOE           ADDRESS CHAR-JOE
         CLI   PDEVTYPE+3,UCB3800  TEST FOR 3800 PRINTER             R4
         BNE   PTSETUP             BR IF NOT                         R4
         MVC   SPFORMS(2*4),JOEFORM USE JOE FORMS AND FCB ID         R4
         MVC   SPFLASH,JOEFLASH    SET FLASH (SEP WON'T FLASH)      R41
         MVC   SPMODF,=C'****'     RESET COPY MODIFICATION           R4
         MVI   SPCOPYN,1           FORCE ONLY 1 COPY OF TRAILER      R4
         MVI   SPCOPYS,1           INDICATE STARTING COPY NUMBER     R4
         MVI   SPFLAG,SPSEP        INIT FLAGS FOR SEP PAGE          R41
         TM    JOECFLAG,$JOEBRST   SET FOR                           R4
         BZ    PTRSETUP             NOBURST OR                      R41
         OI    SPFLAG,SPBURST        BURST                           R4
         B     PTRSETUP            ENTER COMMON SETUP                R4
         SPACE 1                                                     R4
PTSETUP  LA    R1,JOEFORM          ADDRESS IMPACT PRINTER SETUP DATA R4
         MVI   PRINDEX,X'81'       RESET 3211 INDEX TO 1
         B     PTRSTUP             SKIP NON-IMPACT READ CHECK  @OZ46202
         SPACE 1                                                     R4
PTRSETUP TM    PPFLAG2,PPRSW       IF A READ IS                @G38ESBB
         BZ    PTRSTUP              OUTSTANDING, CLEAR         @G38ESBB
         BAL   PL,PRDTCHK            ALL INPUT I/O             @G38ESBB
         SPACE 1                                               @G38ESBB
PTRSTUP  L     R15,=A(PRPUDSV)     CALL DEVICE                 @G38ESBB
         BALR  PL,R15               SETUP VERIFICATION         @G38ESBB
         B     PTRDSVR(R15)        BRANCH ON RETURN CODE       @OZ75372
         SPACE 1                                               @OZ75372
PTRDSVR  B     PRINTTR             0  NORMAL RETURN            @OZ75372
         B     PRINTTR             4  OPER TERMINATE           @OZ75372
         B     PTDSVJAM            8  3800 JAM OR CANCEL KEY   @OZ75372
         B     PPIODRN             12 3800 ERROR IN SETUP      @OZ75372
         SPACE 1                                               @OZ75372
PTDSVJAM L     R15,=A(PLOCATE)     CALL THE LOCATE ROUTINE     @OZ75372
         BALR  PL,R15                   TO GET ORIGIN PQE      @G38ESBB
         B     PPDSEND             PROCESS PAPERJAM/CANCEL KEY @G38ESBB
         SPACE 1                                                     R4
         DROP  R1                  SUSPEND JOE ADDRESSABILITY        R4
         EJECT                                                 @OZ75372
***********************************************************************
*                                                                     *
*        PRODUCE PRINT TRAILER PAGE                                   *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         USING DCTDSECT,R1         PROVIDE DCT ADDRESSABILITY        R4
         SPACE 1                                                     R4
PRINTTR  TM    PCEID,PCEPUSID      IS THIS A PUNCH PROCESSOR   @G38ESBB
         BO    PFINLSMF            BR IF YES                   @OZ34616
         L     R1,PCEDCT           ADDRESS PRINT DCT           @OZ32566
         MVI   DCTACPTN,0          DISABLE COMPACTION FOR SEP       R41
         TM    DCTPPSW,DCTPPSWS    SUPPRESS SEPARATOR PAGE...        R4
         BO    PEDGMARK            BR IF YES                        R41
         LA    R1,=CL5' END'       TRAILER PAGE ID
         L     R15,=A(PRINTID)     PRODUCE PRINTER             @OZ19494
         BALR  PL,R15               TRAILER PAGE               @G38ESBB
         EJECT                                                       R4
***********************************************************************
*                                                                     *
*        ADD 3800 EDGE-MARK CCW IF REQUESTED BY DEVICE                *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
PEDGMARK CLI   PDEVTYPE+3,UCB3800  TEST FOR 3800 PRINTER            R41
         BNE   PFINLSMF            BR IF NOT                   @OZ34616
         L     R1,PCEDCT           ADDRESS PRINTER DCT         @OZ32566
         TM    DCTPPSW2,DCTNIMRK   TEST FOR EDGE-MARKING             R4
         BZ    PSETPQED            BRANCH IF NOT               @OZ46315
         LM    PC1,PC2,PCCWEM      LOAD EDGE-MARK CCW                R4
         BAL   PL,PPPUT2           PLACE ON CHAIN              @OZ51441
         SLR   PC1,PC1             WRITE ONE                   @OZ46315
         ICM   PC1,8,=X'89'         BLANK PAGE                 @OZ46315
         L     PC2,PCCW+4            FOR EDGE MARKING          @OZ46315
         BAL   PL,PPPUT            PLACE ON CHAIN              @OZ46315
         SPACE 1                                               @G38ESBB
***********************************************************************
*                                                              @G38ESBB
*        INDICATE END OF JOE IN PAGE QUEUE                     @G38ESBB
*                                                              @G38ESBB
***********************************************************************
         SPACE 1                                               @G38ESBB
         BAL   PL,PPWRITE          INITIATE OUTPUT             @G38ESBB
         BAL   PL,PPCHECK          WAIT FOR OUTPUT             @G38ESBB
PSETPQED L     PW,PQHADR           ADDRESS THE PQH             @OZ46315
         TM    PQHFLAG-PQHDSECT(PW),PQHDSVC  RESET NEEDED...   @OZ44633
         BZ    PGETLAST            BRANCH IF NOT               @OZ44633
         NI    PQHFLAG-PQHDSECT(PW),FF-PQHDSVC    RESET CMD    @OZ44633
         NI    PDCTFLAG,FF-DCTDELET-DCTRSTRT-DCTBKSP  FLAGS    @OZ44633
PGETLAST L     R1,PQHLAST-PQHDSECT(,PW) ADDRESS LAST PQE       @OZ44633
         LA    R15,PQHFIRST-(PQENEXT-PQEDSECT)-PQHDSECT(,PW)   @G38ESBBC
                                   GET PQE0                    @G38ESBB
         SPACE 1                                               @G38ESBB
PTSTLAST CLR   R1,R15              END OF PPQ...               @G38ESBB
         BE    PSETDEL             YES, SET DELETE FLAG        @OZ57316
         CLI   PQETYPE-PQEDSECT(R1),PQEC IS THIS A LAST PG PQE @G38ESBB
         BE    PQEDONE             BR IF YES, IND LAST DS      @G38ESBB
         L     R1,PQEPREV-PQEDSECT(,R1) GET PREVIOUS PQE       @G38ESBB
         B     PTSTLAST            BR TO TEST FOR PQEC         @G38ESBB
         EJECT                                                 @OZ57316
PQEDONE  L     R1,PQECPQED-PQEDSECT(,R1) ADDRESS PQED          @G38ESBB
         OI    PQEDFLAG-PQEDSECT(R1),PQEDLAST LAST DS OF JOE   @G38ESBB
         CLC   PQEDWJOE-PQEDSECT(,R1),PWKJOE TEST JOE ADDR     @OZ57316
         BE    PTSTRPT             BRANCH IF CURRENT JOE       @OZ57316
PSETDEL  OI    PDCTFLAG,DCTDELET   ELSE, SET DELETE FLAG       @OZ57316
         B     PFINLSMF            GO WRITE SMF 6 RECORD       @OZ57316
         SPACE 1                                               @OZ57316
PTSTRPT  TM    PDCTFLAG,DCTRPT     NEED TO DEFER $N CMD...     @OZ57316
         BZ    PFINLSMF            NO, BRANCH                  @G38ESBB
         NI    PDCTFLAG,FF-DCTRPT  RESET COMMAND FLAG          @G38ESBB
         OI    PQEDFLAG-PQEDSECT(R1),PQEDRPT SET DEFERRED $N   @G38ESBB
         IC    R1,PQHCMDCT-PQHDSECT(,PW) GET DEFERRED CMD CT   @G38ESBB
         LA    R1,1(,R1)           INCREMENT DEFERRED CMD CT   @G38ESBB
         STC   R1,PQHCMDCT-PQHDSECT(,PW) SAVE NEW COUNT        @G38ESBB
*                                  THIS LINE DELETED BY APAR   @OZ46142
         SPACE 1                                               @OZ57316
PFINLSMF L     R1,PPSMFBUF         OBTAIN FINAL TYPE-6         @OZ34616
         LTR   R1,R1                SMF RECORD BUFFER          @OZ34616
         BZ    PRPUEXIT            BR IF NO RECORD AVAILABLE   @OZ46610
        $QUESMFB                    ELSE QUEUE THE RECORD      @OZ34616
         MVC   PPSMFBUF,$ZEROS     CLEAR SMF BUFFER POINTER    @OZ34616
         B     PRPUEXIT             AND BRANCH TO CONTINUE     @OZ46610
         SPACE 1                                                     R4
         DROP  R1                  SUSPEND DCT ADDRESSABILITY        R4
         EJECT                                                       R4
***********************************************************************
*                                                                     *
*        CALL PPSMF6 TO BUILD AND WRITE FINAL TYPE 6 SMF RECORD       *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
PPABORT  CLC   PSAVAREA,$ZEROS     SAVE AREA = LEVEL 0...      @G38ESBB
         BE    PTRMSMF6            YES, BRANCH                 @G38ESBB
         MVC   PSAVAREA,PSAV1ST    RESTORE FIRST LEVEL SAVE    @G38ESBB
         L     R1,PSAVAREA         ADDRESS FIRST SAVE AREA     @G38ESBB
         CLI   0(R1),PSAVEALL      SAVE ALL SPECIFIED...       @G38ESBB
         BE    PLNGRET             YES, BRANCH                 @G38ESBB
         MVC   2*4(4,R1),=A(PTRMSMF6) ALTER RET ADR TO HERE    @G38ESBB
         PRETURN ,                 RESTORE REGS AND RETURN     @G38ESBB
         SPACE 1                                               @G38ESBB
PLNGRET  MVC   4+PL*4(4,R1),=A(PTRMSMF6) ALTER RET ADR TO HERE @G38ESBB
         PRETURN ,                 RESTORE REGS AND RETURN     @G38ESBB
         SPACE 1                                               @G38ESBB
PTRMSMF6 CLI   PDEVTYP3,UCB3800    TEST FOR 3800 PRINTER       @OZ45081
         BNE   PTRMSMF             NO, BRANCH                  @OZ45081
         TM    PDCTFLAG,DCTDELET+DCTRSTRT+DCTBKSP 3800 CMD...  @OZ45081
         BNZ   PTRMRSET            YES, BYPASS SMF RECORD      @OZ45081
         TM    PPFLAG3,PPDVNAVL    IS DEVICE AVAILABLE...      @OZ51930
         BO    PTRMRSET            BRANCH IF NOT               @OZ51930
         SPACE 1                                               @OZ45081
PTRMSMF  L     R15,=A(PPSMF6)      GENERATE TYPE 6             @OZ45081
         BALR  PL,R15               SMF RECORD                 @OZ32776
         CLI   PDEVTYP3,UCB3800    TEST FOR 3800E PRINTER      @G38ESBB
         BNE   PRPUEXIT            NO, BRANCH                  @OZ46610
         SPACE 1                                               @OZ45081
PTRMRSET L     PW,PQHADR           ADDRESS PQH                 @OZ45081
         NI    PQHFLAG-PQHDSECT(PW),FF-PQH2CMD RESET CMD FLAG  @G38ESBB
         SPACE 2                                                     R4
*              THIS LINE DELETED BY APAR OZ46610               @OZ46610
         PRINT OFF                                             @OZ46610
*              THIS LINE DELETED BY APAR OZ46610               @OZ46610
*              THIS LINE DELETED BY APAR OZ46610               @OZ46610
*              THIS LINE DELETED BY APAR OZ46610               @OZ46610
         SPACE 1                                                     R4
         USING DCTDSECT,R1         PROVIDE DCT ADDRESSABILITY        R4
         SPACE 1                                                     R4
*              THIS LINE DELETED BY APAR OZ46610               @OZ46610
*              THIS LINE DELETED BY APAR OZ46610               @OZ46610
*              THIS LINE DELETED BY APAR OZ46610               @OZ46610
*              THIS LINE DELETED BY APAR OZ46610               @OZ46610
*              THIS LINE DELETED BY APAR OZ46610               @OZ46610
*              THIS LINE DELETED BY APAR OZ46610               @OZ46610
*              THIS LINE DELETED BY APAR OZ46610               @OZ46610
*              THIS LINE DELETED BY APAR OZ46610               @OZ46610
*              THIS LINE DELETED BY APAR OZ46610               @OZ46610
*              THIS LINE DELETED BY APAR OZ46610               @OZ46610
*              THIS LINE DELETED BY APAR OZ46610               @OZ46610
*              THIS LINE DELETED BY APAR OZ46610               @OZ46610
*              THIS LINE DELETED BY APAR OZ46610               @OZ46610
*              THIS LINE DELETED BY APAR OZ46610               @OZ46610
*              THIS LINE DELETED BY APAR OZ46610               @OZ46610
*              THIS LINE DELETED BY APAR OZ46610               @OZ46610
*              THIS LINE DELETED BY APAR OZ46610               @OZ46610
*              THIS LINE DELETED BY APAR OZ46610               @OZ46610
*              THIS LINE DELETED BY APAR OZ46610               @OZ46610
*              THIS LINE DELETED BY APAR OZ46610               @OZ46610
         SPACE 1                                               @G38ESBB
         PRINT OFF                 THIS SECTION DELETED BY     @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
         PRINT ON                  THIS SECTION DELETED BY     @G38ESBB
         EJECT
************************************************************** @OZ19494
*                                                            * @OZ19494
*        ISSUE $EXTP CLOSE FOR REMOTE PROCESSOR              * @OZ19494
*                                                            * @OZ19494
************************************************************** @OZ19494
         SPACE 1                                               @OZ19494
PRPUEXIT TM    PCEID,PCELCLID      TEST FOR LOCAL PROCESSOR    @OZ19494
         BO    PJCTFREE            BRANCH IF YES               @OZ19494
         L     R1,PCEDCT           ADDRESS REMOTE DCT          @OZ19494
         USING DCTDSECT,R1         ACTIVATE DCT ADDRESSABILITY @OZ19494
         OI    DCTSTAT,DCTHOLD     SET DEVICE UNAVAILABLE      @OZ19494
         TM    PDCTFLAG,DCTDELET+DCTRSTRT  CHECK FOR $C OR $E  @OZ19494
         BZ    PPCLOSE             BR IF NO, DO NORMAL CLOSE   @OZ19494
         TM    PDCTFLAG,DCTRSTRT+DCTBKSP  CHECK FOR $I         @OZ19494
         BNO   PPNCLOSE            BR IF NO, USE NEG. CLOSE    @OZ19494
PPCLOSE  ICM   PC1,8,=X'FF'        TRUNCATE LAST BUFFER,       @OZ19494
         NI    PPFLAG,255-PRDELSW   RESET TERMINATION FLAG     @OZ19494
         BAL   PL,PPPUT2             AND CHECK OPERATOR CMDS   @OZ19494
         L     R1,PCEDCT           RESTORE DCT PTR FOR CLOSE   @OZ19494
         $EXTP CLOSE,(R1)          CLOSE REMOTE DEVICE         @OZ19494
         B     PPCLSCK             CONTINUE                    @OZ19494
         SPACE 1                                               @OZ19494
PPNCLOSE $EXTP NCLOSE,(R1)         NEGATIVE CLOSE RMT DEVICE   @OZ19494
         NI    DCTFLAGS,255-DCTSTOP RESET STOP                 @OZ43394
PPCLSCK  NI    PPFLAG,255-PRDELSW  RESET TERMINATION FLAG      @OZ19494
         BAL   PL,PPCHECK          CHECK FOR OPERATOR COMMANDS @OZ19494
         SPACE 1                                               @OZ19494
         DROP  R1                  SUSPEND DCT ADDRESSABILITY  @OZ19494
         EJECT                                                 @OZ19494
************************************************************** @OZ19494
*                                                            * @OZ19494
*        RELEASE JCT AND DISPOSE OF JOB OUTPUT ELEMENT       * @OZ19494
*                                                            * @OZ19494
************************************************************** @OZ19494
         SPACE 1                                               @OZ19494
PJCTFREE L     JCT,PJCTBUF         ADDRESS JCT BUFFER          @OZ19494
        $#JCT  FREE                RELEASE JCT BUFFER
         XC    PJCTBUF,PJCTBUF     INDICATE NO JCT             @G38ESBB
         SPACE 1                                                     R4
***********************************************************************
*                                                                     *
*        IF THERE IS A $N STACKED - REQUEUE JOE (NO CKPT DATA)        *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
PJOEOPN  TM    PDCTFLAG,DCTRPT     $N - (REPEAT)                     R4
         BZ    PJOEOPI             BRANCH IF NO
PJOEPUT $#PUT  WORK=PWKJOE         RETURN TO JOT - NO CHECKPOINT     R4
         B     PPNOJOE             BRANCH TO CONTINUE                R4
         SPACE 1                                                     R4
***********************************************************************
*                                                                     *
*        TERMINATION FOR $I - REQUEUE JOE (CKPT DATA)                 *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
PJOEOPI  TM    PDCTFLAG,DCTRSTRT+DCTBKSP  $I - (INTERRUPT)           R4
         BNO   PJOEOPE             BRANCH IF NO
         TM    PCEID,PCELCLID      LOCAL DEVICE...             @OZ47402
         BNO   PJPUTI              BRANCH IF NOT.              @OZ47402
         CLI   PDEVTYP3,UCB3800    IS THIS A D/T3800....       @OZ46954
         BE    PJPUTI              DO NOT UPDATE JOE           @OZ46954
         $QSUSE                    REQUEST ACCESS TO CHKPT     @OZ46954
         L     R14,PCKJOE          GET CHKPT JOE POINTER       @OZ46954
         USING JOEDSECT,R14                                    @OZ46954
         ICM   R15,15,PPCKPTR      GET CURRENT CHKPT DATA      @OZ46954
         BZ    PJPUTI              BRANCH IF NONE AVAILABLE.   @OZ46954
         MVC   JOECKPP,0(R15)      UPDATE CHKPT JOE            @OZ46954
PJPUTI   DS    0H                                              @OZ46954
         $#PUT WORK=PWKJOE,PRC=PCKJOE RETURN CHKPT JOE TO JOT  @OZ46954
         DROP  R14                                             @OZ46954
         B     PPNOJOE             BRANCH TO CONTINUE                R4
         EJECT                                                       R4
***********************************************************************
*                                                                     *
*        TERMINATION FOR $E - REQUEUE JOE (NO CKPT DATA)              *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
PJOEOPE  TM    PDCTFLAG,DCTRSTRT   $E - (RESTART)                    R4
         BO    PJOEPUT             BRANCH IF YES                     R4
         TM    PDCTFLAG,DCTDELET   IS COMMAND $C...            @G38ESBB
         BO    PJOEIOE             YES, GO REMOVE JOE          @G38ESBB
         CLI   PDEVTYP3,UCB3800     ELSE, BYPASS JOE REMOVAL   @G38ESBB
         BE    PPNOJOE              IF 3800 PRINTER            @G38ESBB
         SPACE ,                                               @OZ58550
         PRINT OFF                THIS SECTION DELETED BY      @OZ58550
*        THIS LINE DELETED BY APAR NUMBER                      @OZ58550
*        THIS LINE DELETED BY APAR NUMBER                      @OZ58550
*        THIS LINE DELETED BY APAR NUMBER                      @OZ58550
*        THIS LINE DELETED BY APAR NUMBER                      @OZ58550
*        THIS LINE DELETED BY APAR NUMBER                      @OZ58550
*        THIS LINE DELETED BY APAR NUMBER                      @OZ58550
*        THIS LINE DELETED BY APAR NUMBER                      @OZ58550
*        THIS LINE DELETED BY APAR NUMBER                      @OZ58550
*        THIS LINE DELETED BY APAR NUMBER                      @OZ58550
*        THIS LINE DELETED BY APAR NUMBER                      @OZ58550
*        THIS LINE DELETED BY APAR NUMBER                      @OZ58550
*        THIS LINE DELETED BY APAR NUMBER                      @OZ58550
*        THIS LINE DELETED BY APAR NUMBER                      @OZ58550
*        THIS LINE DELETED BY APAR NUMBER                      @OZ58550
*        THIS LINE DELETED BY APAR NUMBER                      @OZ58550
*        THIS LINE DELETED BY APAR NUMBER                      @OZ58550
         PRINT ON                 THIS SECTION DELETED BY      @OZ58550
         SPACE 1                                               @OZ38851
         SPACE 1                                               @OZ46610
************************************************************   @OZ46610
*        RESET SPIN IOT ALLOCATION BIT AND PURGE DATA      *   @OZ46610
*                      SET TRACKS                          *   @OZ46610
************************************************************   @OZ46610
         SPACE 1                                               @OZ46610
PJOEIOE  DS    0H                                              @OZ46610
         TM    PPFLAG,PPDALOC      IS THIS AN ALLOCATION IOT   @OZ44633
         BZ    PJOEIO2             BRANCH IF NOT               @OZ44633
         L     R1,PCEDCT           LOAD PR/PU DCT BASE         @OZ46610
         USING DCTDSECT,R1                                     @OZ46610
         CLI   DCTBUFCT,0          IS UNIT RECORD I/O HUNG     @OZ46610
         BNE   PJOEIO2             YES, CAN'T PURGE.           @OZ46610
         DROP  R1                                              @OZ46610
         L     R0,PCEIOTTR         ADDRESS IOT FOR SUBROUTINE  @OZ46610
         L     R15,=A(PURSPDS)     CALL SUBROUTINE TO PURGE    @OZ46610
         BALR  PL,R15              SPIN DATA SET SPOOL SPACE   @OZ46610
PJOEIO2  DS    0H                                              @OZ46610
         SPACE 1                                               @OZ46610
************************************************************   @OZ46610
*        TERMINATION FOR $C - DELETE JOE                   *   @OZ46610
*        TERMINATION FOR I/O ERROR READING DATA OR         *   @OZ46610
*        CONTROL BLOCKS                                    *   @OZ46610
************************************************************   @OZ46610
         SPACE 1                                               @OZ46610
         $#REM WORK=PWKJOE         REMOVE WORK-JOE FROM JOT    @OZ46610
         SPACE 1                                                     R4
PPNOJOE  MVC   PCEJQE,$ZEROS       CLEAR JQE ADDRESS           @OZ32566
         TM    PCEID,PCERJEID      TEST PROCESSOR TYPE         @OZ19494
         BO    PFRESRCE            IF REMOTE, FREE RESOURCES   @OZ19494
         SPACE 1                                               @OZ19494
         PRINT OFF                 THIS SECTION DELETED BY     @OZ19494
*                                  THIS CARD DELETED BY APAR   @OZ19494
*                                  THIS CARD DELETED BY APAR   @OZ19494
*                                  THIS CARD DELETED BY APAR   @OZ19494
*                                  THIS CARD DELETED BY APAR   @OZ19494
*                                  THIS CARD DELETED BY APAR   @OZ19494
*                                  THIS CARD DELETED BY APAR   @OZ19494
*                                  THIS CARD DELETED BY APAR   @OZ19494
*                                  THIS CARD DELETED BY APAR   @OZ19494
*                                  THIS CARD DELETED BY APAR   @OZ19494
*                                  THIS CARD DELETED BY APAR   @OZ19494
*                                  THIS CARD DELETED BY APAR   @OZ19494
         PRINT ON                  THIS SECTION DELETED BY     @OZ19494
         SPACE 1                                               @G38ESBB
         PRINT OFF                 THIS SECTION DELETED BY     @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
         PRINT ON                  THIS SECTION DELETED BY     @G38ESBB
         EJECT                                                       R4
***********************************************************************
*                                                                     *
*        CLEAR ALL INPUT I/O                                   @G38ESBB
*                                                                     *
*        COMPLETE ALL OUTPUT I/O                               @G38ESBB
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
PFRESRCE DS    0H                                                    R4
         TM    PPFLAG2,PPRSW       IF A READ IS                      R4
         BZ    PCLRIO              OUTSTANDING, CLEAR          @G38ESBB
         BAL   PL,PRDTCHK            ALL INPUT I/O                   R4
         SPACE 1                                                     R4
         USING DCTDSECT,R1         PROVIDE DCT ADDRESSABILITY  @G38ESBB
         SPACE 1                                               @G38ESBB
PCLRIO   TM    PCEID,PCERJEID      TEST PROCESSOR TYPE         @G38ESBB
         BO    PFREUNIT            BR IF REMOTE                @G38ESBB
         L     R1,PCEDCT           ADDRESS PRINT/PUNCH DCT     @G38ESBB
         CLI   DCTBUFCT,0          HAS ALL OUTPUT COMPLETED... @G38ESBB
         BE    PTDRAIN             BR IF YES                   @G38ESBB
        $WAIT  IO                  ELSE,WAIT FOR I/O TO FINISH @G38ESBB
         B     PCLRIO              AND TRY AGAIN               @G38ESBB
         SPACE 1                                               @G38ESBB
PTDRAIN  CLI   PDEVTYP3,UCB3800    IS DEVICE A 3800 PRINTER    @G38ESBB
         BNE   PTSTDR              BRANCH IF NOT               @OZ65116
         TM    PPFLAG3,PPQSPND     SHOULD DEVICE DRAIN...      @OZ48003
         BZ    PTDRNFLG            BRANCH IF NOT               @OZ48003
         OI    DCTSTAT,DCTDRAIN    SET DRAIN FLAG              @OZ48003
         SPACE 1                                               @OZ48003
PTDRNFLG TM    DCTSTAT,DCTDRAIN    DRAIN DETECTED...           @OZ48003
         BO    PDRAINCP            BRANCH IF YES               @OZ48003
         TM    DCTPPFL,DCTPAUSE    OPERATOR REQUESTED PAUSE    @G38ESBB
         BZ    PFREUNIT            BR IF NO                    @G38ESBB
         SPACE 1                                               @G38ESBB
PDRAINCP LA    JCT,JCT             SHOW NON-ZERO JCT FOR I/O   @G38ESBB
         MVC   PWKJOE,$ZEROS       SHOW ZERO WORK JOE FOR I/O  @G38ESBB
         TM    PPFLAG3,PPDVNAVL    IS DEVICE AVAILABLE...      @OZ51930
         BO    PFREUNIT            BRANCH IF NOT               @OZ51930
         LM    PC1,PC2,PCCWCP      LOAD CLEAR PRINT CCW        @G38ESBB
         BAL   PL,PPPUT            ADD CCW TO AREA             @G38ESBB
         BAL   PL,PPWRITE          WRITE CCW TO AREA           @G38ESBB
         BAL   PL,PPCHECK          WAIT FOR I/O TO COMPLETE    @G38ESBB
         SPACE 1                                               @OZ61672
         L     R15,=A(PPRSTMC)     CALL SUBROUTINE TO          @OZ65116
         BALR  PL,R15               RESET MICROCODE PATH       @OZ65116
         L     R1,PCEDCT           RESTORE DCT ADDRESS         @OZ65116
         SPACE 1                                               @OZ65116
PTSTDR   TM    DCTSTAT,DCTDRAIN    DRAIN INDICATED...          @OZ65116
         BO    PFREUNIT            BRANCH IF YES               @OZ65116
         EJECT                                                 @OZ61672
PDRMSG   TM    DCTPPFL,DCTPAUSE    PAUSE ISSUED                @G38ESBB
         BZ    PFREUNIT            BR IF NO                    @G38ESBB
         OI    DCTSTAT,DCTPAUSE    PAUSE THE DEVICE            @G38ESBB
        $MID   175                 MESSAGE IDENTIFIER          @G38ESBB
         PMSG  PMESSAGE,M175L,(X'175F',DCTDEVN,C' PAUSED')     @G38ESBBC
                                   MOVE MESSAGE TEXT           @G38ESBB
        $WTO   PMESSAGE,M175L,     INFORM OPERATOR             @G38ESBBC
               ROUTE=$LOG+$UR,CLASS=$NORMAL,PRI=$ST,JOB=NO     @G38ESBB
         SPACE 1                                               @G38ESBB
         DROP  R1                  SUSPEND DCT                 @G38ESBB
         SPACE 1                                               @G38ESBB
***************************************************************@G38ESBB
*                                                              @G38ESBB
*        FREE PRINT/PUNCH/REMOTE DCT                           @G38ESBB
*                                                              @G38ESBB
***************************************************************@G38ESBB
         SPACE 1                                               @G38ESBB
PFREUNIT $FREUNIT PCEDCT           FREE PRINT/PUNCH DCT        @G38ESBB
         SPACE 1                                               @G38ESBB
***************************************************************@G38ESBB
*                                                              @G38ESBB
*        CALL RESOURCE DEALLOCATION ROUTINE                    @G38ESBB
*                                                              @G38ESBB
***************************************************************@G38ESBB
         SPACE 1                                               @G38ESBB
         L     R15,=A(PDEALLOC)    CALL RESOURCE               @G38ESBB
         BALR  PL,R15               DEALLOCATION ROUTINE       @G38ESBB
         SPACE 1                                               @G38ESBB
***************************************************************@G38ESBB
*                                                              @G38ESBB
*        INDICATE THAT THE PRINT/PUNCH PROCESSOR IS DORMANT    @G38ESBB
*                                                              @G38ESBB
***************************************************************@G38ESBB
         SPACE 1                                               @G38ESBB
PDORMANT DS    0H                                              @G38ESBB
        $DORMANT                   INIDCATE PROCESSOR INACTIVE @G38ESBB
         B     PRPUINIT            BR TO RESTART PROCESSOR     @G38ESBB
         SPACE 1                                               @OZ48003
         PRINT OFF                 THIS SECTION DELETED BY     @OZ48003
*              THIS LINE DELETED BY APAR NUMBER                @OZ48003
*              THIS LINE DELETED BY APAR NUMBER                @OZ48003
*              THIS LINE DELETED BY APAR NUMBER                @OZ48003
*              THIS LINE DELETED BY APAR NUMBER                @OZ48003
*              THIS LINE DELETED BY APAR NUMBER                @OZ48003
*              THIS LINE DELETED BY APAR NUMBER                @OZ48003
*              THIS LINE DELETED BY APAR NUMBER                @OZ48003
*              THIS LINE DELETED BY APAR NUMBER                @OZ48003
*              THIS LINE DELETED BY APAR NUMBER                @OZ48003
*              THIS LINE DELETED BY APAR NUMBER                @OZ48003
*              THIS LINE DELETED BY APAR NUMBER                @OZ48003
*              THIS LINE DELETED BY APAR NUMBER                @OZ48003
*              THIS LINE DELETED BY APAR NUMBER                @OZ48003
*              THIS LINE DELETED BY APAR NUMBER                @OZ48003
*              THIS LINE DELETED BY APAR NUMBER                @OZ48003
*              THIS LINE DELETED BY APAR NUMBER                @OZ48003
*              THIS LINE DELETED BY APAR NUMBER                @OZ48003
*              THIS LINE DELETED BY APAR NUMBER                @OZ48003
*              THIS LINE DELETED BY APAR NUMBER                @OZ48003
*              THIS LINE DELETED BY APAR NUMBER                @OZ48003
*              THIS LINE DELETED BY APAR NUMBER                @OZ48003
*              THIS LINE DELETED BY APAR NUMBER                @OZ48003
*              THIS LINE DELETED BY APAR NUMBER                @OZ48003
*              THIS LINE DELETED BY APAR NUMBER                @OZ48003
*              THIS LINE DELETED BY APAR NUMBER                @OZ48003
*              THIS LINE DELETED BY APAR NUMBER                @OZ48003
*              THIS LINE DELETED BY APAR NUMBER                @OZ48003
*              THIS LINE DELETED BY APAR NUMBER                @OZ48003
*              THIS LINE DELETED BY APAR NUMBER                @OZ48003
*              THIS LINE DELETED BY APAR NUMBER                @OZ48003
*              THIS LINE DELETED BY APAR NUMBER                @OZ48003
*              THIS LINE DELETED BY APAR NUMBER                @OZ48003
*              THIS LINE DELETED BY APAR NUMBER                @OZ48003
*              THIS LINE DELETED BY APAR NUMBER                @OZ48003
*              THIS LINE DELETED BY APAR NUMBER                @OZ48003
*              THIS LINE DELETED BY APAR NUMBER                @OZ48003
*              THIS LINE DELETED BY APAR NUMBER                @OZ48003
*              THIS LINE DELETED BY APAR NUMBER                @OZ48003
*              THIS LINE DELETED BY APAR NUMBER                @OZ48003
*              THIS LINE DELETED BY APAR NUMBER                @OZ48003
*              THIS LINE DELETED BY APAR NUMBER                @OZ48003
*              THIS LINE DELETED BY APAR NUMBER                @OZ48003
*              THIS LINE DELETED BY APAR NUMBER                @OZ48003
*              THIS LINE DELETED BY APAR NUMBER                @OZ48003
*              THIS LINE DELETED BY APAR NUMBER                @OZ48003
*              THIS LINE DELETED BY APAR NUMBER                @OZ48003
*              THIS LINE DELETED BY APAR NUMBER                @OZ48003
*              THIS LINE DELETED BY APAR NUMBER                @OZ48003
*              THIS LINE DELETED BY APAR NUMBER                @OZ48003
*              THIS LINE DELETED BY APAR NUMBER                @OZ48003
*              THIS LINE DELETED BY APAR NUMBER                @OZ48003
*              THIS LINE DELETED BY APAR NUMBER                @OZ48003
*              THIS LINE DELETED BY APAR NUMBER                @OZ48003
*              THIS LINE DELETED BY APAR NUMBER                @OZ48003
*              THIS LINE DELETED BY APAR NUMBER                @OZ48003
*              THIS LINE DELETED BY APAR NUMBER                @OZ48003
*              THIS LINE DELETED BY APAR NUMBER                @OZ48003
*              THIS LINE DELETED BY APAR NUMBER                @OZ48003
*              THIS LINE DELETED BY APAR NUMBER                @OZ48003
*              THIS LINE DELETED BY APAR NUMBER                @OZ48003
*              THIS LINE DELETED BY APAR NUMBER                @OZ48003
*              THIS LINE DELETED BY APAR NUMBER                @OZ48003
*              THIS LINE DELETED BY APAR NUMBER                @OZ48003
*              THIS LINE DELETED BY APAR NUMBER                @OZ48003
*              THIS LINE DELETED BY APAR NUMBER                @OZ48003
*              THIS LINE DELETED BY APAR NUMBER                @OZ48003
*              THIS LINE DELETED BY APAR NUMBER                @OZ48003
*              THIS LINE DELETED BY APAR NUMBER                @OZ48003
*              THIS LINE DELETED BY APAR NUMBER                @OZ48003
*              THIS LINE DELETED BY APAR NUMBER                @OZ48003
*              THIS LINE DELETED BY APAR NUMBER                @OZ48003
*              THIS LINE DELETED BY APAR NUMBER                @OZ48003
         PRINT ON                  THIS SECTION DELETED BY     @OZ48003
 TITLE 'HASP PRINT/PUNCH SERVICE -- SMF BUFFER FREE ROUTINE'   @G38ESBB
***************************************************************@G38ESBB
*                                                              @G38ESBB
*        RETURN SMF BUFFER TO THE SMF BUFFER FREE QUEUE        @G38ESBB
*                                                              @G38ESBB
***************************************************************@G38ESBB
         SPACE 1                                               @G38ESBB
PFRESMFB L     R0,$SMFFREE         GET SMF BUF QUEUE HEAD      @G38ESBB
         ST    R0,0(,R1)           ADD PASSED SMF              @G38ESBB
         CS    R0,R1,$SMFFREE       BUFFER TO                  @G38ESBB
         BNE   PFRESMFB              QUEUE HEAD                @G38ESBB
        $POST  $HASPECF,SMF        NOTIFY RESOURCE AVAILABLE   @G38ESBB
         BR    LINK                RETURN                      @G38ESBB
 TITLE 'HASPPRPU REGISTER SAVE/RETURN ROUTINES'                @G38ESBB
***************************************************************@G38ESBB
*                                                              @G38ESBB
*        PSAVE -- OBTAIN NEW AREA AND SAVE REGISTERS           @G38ESBB
*                                                              @G38ESBB
*        R0,R1 WORK REGISTERS                                  @G38ESBB
*        R14 - RETURN ADR (HI-BIT OFF = ONLY SAVE BASE2,PL)    @G38ESBB
*        R15 - ENTRY ADDRESS                                   @G38ESBB
*                                                              @G38ESBB
* NOTE - REGS R14 THRU R1 ARE SAVED AND RESTORED BY THE        @G38ESBB
*             PSAVE MACRO EXPANSION USING $CSAVREG             @G38ESBB
*                                                              @G38ESBB
***************************************************************@G38ESBB
         SPACE 1                                               @G38ESBB
PSAVE    L     R0,PSAVAREA         POINT TO PREVIOUS SAVE AREA @G38ESBB
         LTR   R0,R0               IS THIS FIRST SAVE LEVEL... @G38ESBB
         BNZ   PSAVNOT1            BR IF NO                    @G38ESBB
         L     R1,PSAV1ST          ELSE POINT TO 1ST SAVE AREA @G38ESBB
         B     PSAVCOMN             AND BR TO CONTINUE         @G38ESBB
         SPACE 1                                               @G38ESBB
PSAVNOT1 LR    R1,R0               POINT                       @G38ESBB
         L     R1,0(,R1)            PAST                       @G38ESBB
         SRL   R1,24                 PREVIOUS                  @G38ESBB
         ALR   R1,R0                  SAVE AREA                @G38ESBB
         SPACE 1                                               @G38ESBB
PSAVCOMN ST    R1,PSAVAREA         UPDATE SAVE AREA POINTER    @G38ESBB
         ST    R0,0(,R1)           BACK-CHAIN PREVIOUS AREA    @G38ESBB
         LTR   R14,R14             SAVE ALL REGISTERS...       @G38ESBB
         BNM   PSAVSHOR            BR IF NO                    @G38ESBB
         MVI   0(R1),(1+16)*4       ELSE SHOW ALL REGS SAVED   @G38ESBB
         MVC   4+(R0*4)(2*4,R1),$CSAVREG+2*4    SAVE R0,R1     @G38ESBB
         STM   R2,R13,4+(R2*4)(R1)              SAVE R2-R13    @G38ESBB
         MVC   4+(R14*4)(2*4,R1),$CSAVREG       SAVE R14,R15   @G38ESBB
         BR    R14                 RETURN TO CALLER            @G38ESBB
         SPACE 1                                               @G38ESBB
PSAVSHOR MVI   0(R1),(1+2)*4       SHOW 2 REGISTERS SAVED      @G38ESBB
         ST    BASE2,4(,R1)            SAVE BASE2              @G38ESBB
         ST    PL,2*4(,R1)             SAVE PL                 @G38ESBB
         BR    R14                 RETURN TO CALLER            @G38ESBB
         EJECT                                                 @G38ESBB
***************************************************************@G38ESBB
*                                                              @G38ESBB
*        PRETURN - RESTORE REGS AND RETURN TO PREVIOUS LEVEL   @G38ESBB
*                                                              @G38ESBB
*        R14 - RETURN ADR (HI-BYTE CONTAINS CC SET BY BALR)    @G38ESBB
*        R15 - ENTRY ADDRESS                                   @G38ESBB
*                                                              @G38ESBB
* NOTE - CONDITION CODE ON ENTRY IS RESTORED BEFORE RETURNING  @G38ESBB
*                                                              @G38ESBB
***************************************************************@G38ESBB
         SPACE 1                                               @G38ESBB
PRETURN  L     R1,PSAVAREA         POINT TO CURRENT SAVE AREA  @G38ESBB
         MVC   PSAVAREA+1(3),1(R1) UPDATE SAVE AREA POINTER    @G38ESBB
         CLI   0(R1),PSAVEALL      ALL REGISTERS SAVED...      @G38ESBB
         BE    PRETALL             BR IF YES                   @G38ESBB
         SPM   R14                 RESTORE CC                  @G38ESBB
         L     BASE2,4(,R1)        RETORE BASE2                @G38ESBB
         L     PL,8(,R1)           RESTORE PL                  @G38ESBB
         BR    PL                  RETURN TO PREVIOUS LEVEL    @G38ESBB
         SPACE 1                                               @G38ESBB
PRETALL  SPM   R14                 RESTORE CC                  @G38ESBB
         LM    R0,R15,4+(R0*4)(R1) RESTORE R0 THRU R15         @G38ESBB
         BR    PL                  RETURN TO PREVIOUS LEVEL    @G38ESBB
 TITLE 'HASP PRINT/PUNCH SERVICE -- VARIABLE STORAGE'          @G38ESBB
*                             MISCELLANEOUS CONSTANTS          @OZ19494
         SPACE 1                                               @OZ19494
PJOBSTAT DC    CL39'------ JES2 JOB STATISTICS ------'         @OZ19494
BFWSKEL  DC    XL3'F10000'         BFW SKELETON                @OZ61672
         DC    XL2'F200'             CORRESPONDS DIRECTLY      @OZ61672
         DC    XL2'F300'               TO EXECUTE ORDER        @OZ61672
         DC    XL3'F40000'               MAPPING IN $BFW       @OZ61672
PFDSET   DC    X'7FFFFFFF'         END OF DS PQE INDICATOR     @OZ53047
PCLRHALF DC    F'65535'            CLEAR LEFT HALFWORD         @G38ESBB
PCVTREAL DC    XL4'00000FFF'       CONVERT TO REAL ADDRESS     @G38ESBB
PQELIMIT DC    XL4'00000FFF'       ISOLATE 3800 PAGE IDS       @G38ESBB
PCCWLENG DC    H'08'               CCW LENGTH                  @G38ESBB
PH6LPI   DC    H'03'               LINES IN HALF INCH AT 6 LPI @G38ESBB
PFCBTOP  DC    H'01'               TOP OF FCB                  @G38ESBB
PFSPDSET DC    X'FFFFFFFF'         $FPRTN,D                    @OZ53047
PMAXSKIP DC    XL4'7FFFFFFF'       INDICATE NO PRINTING        @G38ESBB
PXEQDATE DC    CL29' JOB EXECUTION DATE'                       @OZ19494
PRDRSTAT DC    X'40206B2020206B202120',CL29' CARDS READ'       @OZ19494
PPRTSTAT DC    X'40206B2020206B202120',CL29' SYSOUT PRINT RECORDS'   R4
PPUNSTAT DC    X'40206B2020206B202120',CL29' SYSOUT PUNCH RECORDS'   R4
PXEQSTAT DC    X'4020206B2021204B2020',CL29' MINUTES EXECUTION TIME' R4
         SPACE 1                                               @OZ19494
*                             CCW SKELETONS                    @OZ19494
         SPACE 1                                               @OZ19494
PDIRCCW  CCW   X'FE',BUFSTART-BUFDSECT,X'60',68 PDIR CCW       @OZ19494
PCCWSIB  CCW   X'14',0,X'60',12    SENSE INT BUF CCW           @G38ESBB
PCCWXORD CCW   X'33',0,X'60',2     EXECUTE ORDER CCW           @G38ESBB
PRCCWEJ  CCW   X'8B',0,X'60',1                                 @OZ19494
PRCCWID  CCW   X'09',*-*,X'60',132                             @OZ19494
PUCCW    CCW   X'41',*-*,X'60',80                              @OZ19494
PCCW     CCW   *-*,HDBSTART-BUFDSECT,X'60',*-*                 @OZ19494
PUCCWBL  CCW   X'01',*-*,X'60',1     BLANK SPACER CCW.         @OZ19494
PRCCWSP  CCW   X'1B',0,X'60',1                                 @OZ19494
PRCCWSP1 CCW   X'0B',0,X'60',1     SPACE-1-IMMEDIATE           @OZ19494
PRCCWCOM CCW   X'09',*-*,X'60',L'PMESSAGE                      @OZ19494
PCCWNOP  CCW   X'03',0,X'20',64    COUNT USED AS A BLANK       @OZ19494
PUNFOLD  CCW   X'23',0,X'60',1     3211 UNFOLD CONTROL CCW     @OZ19494
PUCSGATE CCW   X'EB',0,X'60',1     1403 UCS GATE CONTROL CCW   @OZ19494
PUCSBDC  CCW   X'73',0,X'60',1     BLOCK DATA CHECK CCW        @OZ19494
PUCSLOAD CCW   X'FB',BUFSTART+1-BUFDSECT,X'60',240 LOAD UCS CCW@OZ19494
PFCBLOAD CCW   X'63',BUFSTART+2-BUFDSECT,X'60',0 LOAD FCB CCW  @OZ19494
PUSPACCW CCW   X'1D',*-*,X'A0',48    BLANK SPACER CCW.         @OZ19494
PCCWEM   CCW   X'17',0,X'60',1     EDGE MARK CCW               @OZ19494
PCCWCP   CCW   X'87',0,X'60',1     CLEAR PRINT CCW             @OZ19494
PCCWOFST CCW   X'07',0,X'60',1     OFFSET-STACK (EOT)          @OZ19494
         SPACE 1                                               @OZ19494
PCNOPTIC DC    X'03',AL3(*-*),X'20',AL1(0,PCIEPRPU,0) PCIE     @OZ19494
         DC    X'08',AL3(*-*),A(*-*)                  TEMPLATE @OZ19494
         SPACE 1                                               @OZ19494
PRXTR    TR    0(*-*,PC1),0(R15)   *** EXECUTED ***  &PRTRANS  @OZ19494
PMAXPAGE DC    A(X'3FFFFFFF')      $F DATASET PAGE COUNT       @OZ19494
         SPACE 1                                               @G38ESBB
*                             MISCELLANEOUS EQUATES            @G38ESBB
         SPACE 1                                               @G38ESBB
RPISIBSZ EQU   L'PCCWXORD+L'PCCWSIB LENGTH OF RPI/SIB CCW'S    @G38ESBB
PTICCMD  EQU   X'08'               TIC COMMAND                 @G38ESBB
PVALCMD  EQU   X'10'               VALID FCB MAPPING COMMAND   @G38ESBB
PSKIPCMD EQU   X'20'               CHANNEL SKIP COMMAND        @G38ESBB
PSAVBITS EQU   X'0F'               ISOLATE SPACE/SKIP BITS     @G38ESBB
PDEVTYP3 EQU   PDEVTYPE+UCBTBYT4-UCBTYP UCB DEVICE TYPE        @G38ESBB
PWRTNOSP EQU   X'00'               WRITE NO SPACE COMMAND      @G38ESBB
PCONVIMM EQU   X'02'               CONVERT TO IMMEDIATE CMD    @G38ESBB
PSAVEALL EQU   68                  SAVE ALL REGISTERS          @G38ESBB
         EJECT                                                 @OZ19494
***************************************************************@OZ19494
*                                                              @OZ19494
*        PRINT/PUNCH PROCESSOR LITERAL POOL                    @OZ19494
*                                                              @OZ19494
***************************************************************@OZ19494
         SPACE 1                                               @OZ19494
         DS    0D                                              @OZ19494
         LTORG                                                 @OZ19494
         SPACE 3                                               @OZ19494
* * * * * * * * *  END OF ADDRESSABILITY  * * * * * * * * *    @OZ19494
         SPACE 2                                               @OZ19494
                                   PRINT OFF - SECTION DELETED @OZ19494
*                                  THIS LINE DELETED BY APAR   @OZ32776
*                                  THIS LINE DELETED BY APAR   @OZ32776
*                                  THIS LINE DELETED BY APAR   @OZ32776
*                                  THIS LINE DELETED BY APAR   @OZ32776
*                                  THIS LINE DELETED BY APAR   @OZ32776
*                                  THIS LINE DELETED BY APAR   @OZ32776
*                                  THIS LINE DELETED BY APAR   @OZ32776
*                                  THIS LINE DELETED BY APAR   @OZ32776
*                                  THIS LINE DELETED BY APAR   @OZ32776
*                                  THIS LINE DELETED BY APAR   @OZ32776
*                                  THIS LINE DELETED BY APAR   @OZ32776
*                                  THIS LINE DELETED BY APAR   @OZ32776
*                                  THIS LINE DELETED BY APAR   @OZ32776
*                                  THIS LINE DELETED BY APAR   @OZ32776
*                                  THIS LINE DELETED BY APAR   @OZ32776
*                                  THIS LINE DELETED BY APAR   @OZ32776
*                                  THIS LINE DELETED BY APAR   @OZ32776
*                                  THIS LINE DELETED BY APAR   @OZ32776
*                                  THIS LINE DELETED BY APAR   @OZ32776
*                                  THIS LINE DELETED BY APAR   @OZ32776
*                                  THIS LINE DELETED BY APAR   @OZ32776
*                                  THIS LINE DELETED BY APAR   @OZ32776
*                                  THIS LINE DELETED BY APAR   @OZ32776
*                                  THIS LINE DELETED BY APAR   @OZ32776
*                                  THIS LINE DELETED BY APAR   @OZ32776
*                                  THIS LINE DELETED BY APAR   @OZ32776
*                                  THIS LINE DELETED BY APAR   @OZ32776
*                                  THIS LINE DELETED BY APAR   @OZ32776
*                                  THIS LINE DELETED BY APAR   @OZ32776
*                                  THIS LINE DELETED BY APAR   @OZ32776
*                                  THIS LINE DELETED BY APAR   @OZ32776
*                                  THIS LINE DELETED BY APAR   @OZ32776
*                                  THIS LINE DELETED BY APAR   @OZ32776
*                                  THIS LINE DELETED BY APAR   @OZ32776
*                                  THIS LINE DELETED BY APAR   @OZ32776
*                                  THIS LINE DELETED BY APAR   @OZ32776
*                                  THIS LINE DELETED BY APAR   @OZ32776
*                                  THIS LINE DELETED BY APAR   @OZ32776
*                                  THIS LINE DELETED BY APAR   @OZ32776
*                                  THIS LINE DELETED BY APAR   @OZ32776
*                                  THIS LINE DELETED BY APAR   @OZ32776
*                                  THIS LINE DELETED BY APAR   @OZ32776
*                                  THIS LINE DELETED BY APAR   @OZ32776
*                                  THIS LINE DELETED BY APAR   @OZ32776
*                                  THIS LINE DELETED BY APAR   @OZ32776
*                                  THIS LINE DELETED BY APAR   @OZ32776
*                                  THIS LINE DELETED BY APAR   @OZ32776
*                                  THIS LINE DELETED BY APAR   @OZ32776
*                                  THIS LINE DELETED BY APAR   @OZ32776
*                                  THIS LINE DELETED BY APAR   @OZ32776
*                                  THIS LINE DELETED BY APAR   @OZ32776
*                                  THIS LINE DELETED BY APAR   @OZ32776
*                                  THIS LINE DELETED BY APAR   @OZ32776
*                                  THIS LINE DELETED BY APAR   @OZ32776
*                                  THIS LINE DELETED BY APAR   @OZ32776
*                                  THIS LINE DELETED BY APAR   @OZ32776
*                                  THIS LINE DELETED BY APAR   @OZ32776
*                                  THIS LINE DELETED BY APAR   @OZ32776
*                                  THIS LINE DELETED BY APAR   @OZ32776
*                                  THIS LINE DELETED BY APAR   @OZ32776
*                                  THIS LINE DELETED BY APAR   @OZ32776
*                                  THIS LINE DELETED BY APAR   @OZ32776
*                                  THIS LINE DELETED BY APAR   @OZ32776
*                                  THIS LINE DELETED BY APAR   @OZ32776
*                                  THIS LINE DELETED BY APAR   @OZ32776
*                                  THIS LINE DELETED BY APAR   @OZ32776
*                                  THIS LINE DELETED BY APAR   @OZ32776
*                                  THIS LINE DELETED BY APAR   @OZ32776
*                                  THIS LINE DELETED BY APAR   @OZ32776
*                                  THIS LINE DELETED BY APAR   @OZ32776
*                                  THIS LINE DELETED BY APAR   @OZ32776
         PRINT ON                  THIS SECTION DELETED BY     @OZ32776
         TITLE 'HASP PRINT/PUNCH SERVICE -- SEPARATOR PAGE ROUTINE'
***********************************************************************
*                                                                     *
*        SEPARATOR PAGE ROUTINE                                       *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         USING PRINTID,BASE2       PROVIDE LOCAL ADDRESSABILITY@OZ19494
         SPACE 1                                               @OZ19494
PRINTID  PSAVE ,                   SAVE LINKAGE AND BASE REGS  @G38ESBB
*        DELETED                                               @G38ESBB
         LR    BASE2,R15           LOAD BASE REGISTER          @OZ19494
         L     JCT,PJCTBUF         PICK UP JCT ADDRESS               R4
         USING JCTDSECT,JCT        ACTIVATE JCT ADDRESSABILITY
         ST    R1,PCEFORM          SAVE ADDRESS OF ID TYPE
         NI    PPFLAG,255-PPDELSW  RESET SUSPEND SWITCH
         CLI   PDEVTYPE+3,UCB3800  TEST FOR 3800 PRINTER             R4
         BNE   BLKLCNT             BR IF NOT                         R4
         CLI   $PRIDCT,0           TEST FOR ZERO LINE COUNT          R4
         BE    PRNOID              BR IF YES                         R4
         LM    PC1,PC2,PCCWEM      SELECT                            R4
         ICM   PC1,8,PXTABCCW       DEFAULT CHARACTER SET           R41
         BAL   PL,PPPUT2             FOR ID PAGES              @OZ51441
BLKLCNT  DS    0H                                                    R4
         SLR   PL,PL               REMOTE SEPARATOR                  R4
         IC    PL,$TPIDCT           LINE COUNT                       R4
         TM    PCEID,PCELCLID      TEST FOR LOCAL PRINTER
         BZ    BLKRMT              BRANCH IF NOT LOCAL
         IC    PL,$PRIDCT          LOCAL SEPARATOR LINE COUNT        R4
BLKRMT   DS    0H
         LTR   PL,PL               IS LINE COUNT ZERO
         BZ    PRNOID              BRANCH IF YES
         SPACE 1                                                    R41
         TM    PCEID,PCERJEID      TEST PROCESSOR TYPE              R41
         BZ    PBLKTST             BR IF NOT REMOTE                 R41
         L     R1,PCEDCT           ADDRESS PRINTER DCT         @OZ32566
         TM    MDCTFEAT-DCTDSECT(R1),DCTPSHDR IF PTR DOES NOT REQ   R41
         BNO   PBLKTST                         SETUP HDR, SKIP PDIR R41
         MVI   PPDIRID,X'01'       INDICATE SEPARATOR PDIR          R41
         ST    PL,PPDSRCT          USE SEPARATOR LINE CNT FROM HCT  R41
*                                  THIS LINE DELETED BY        @OZ63941
*                                  THIS LINE DELETED BY        @OZ63941
         L     R15,=A(PPDIR)       POINT TO SEPARATOR ROUTINE       R41
         BALR  PL,R15              PUT SEPARATOR PDIR               R41
         L     PL,PPDSRCT          RESTORE SEPARATOR LINE COUNT     R41
         EJECT                                                 @OZ63941
PBLKTST  DS    0H                                                   R41
         LA    R1,BUFSTART-BUFDSECT  STARTING LINE DISPLACEMENT      R4
         CLM   PL,1,=AL1(30)       AT LEAST 30 LINES REQUESTED
         BL    BLKSKIP             BRANCH IF NO
         MVC   PCCWORK,JCTJNAME    JOB NAME FROM JOB CARD            R4
         LA    PL,BLKPRT           POINT TO BLOCK LETTER ROUTINE    R41
         BALR  PL,PL               TO PRINT JOBNAME NON-SLANTED,    R41
         NOPR  0                    CHANGE 'BALR' AND 'NOPR'        R41C
                                     TO 'BAL PL,0(,PL)'             R41
         SPACE 1                                               @OZ63941
         LM    PC1,PC2,PRCCWSP     LOAD SPACE CCW
         BAL   PL,PPPUT            ADD CCW TO CHAIN
         XC    PCCWORK,PCCWORK     CLEAR WORK AREA
         MVC   PCCWORK(1),JCTJOBID GET JOB TYPE
         MVC   PCCWORK+1(4),JCTJOBID+4 AND JOB NUMBER
SKIP490  CLI   PCCWORK+1,C' '          LEFT-                         R4
         BNE   SKIP480                  JUSTIFY                      R4
         MVC   PCCWORK+1(4),PCCWORK+2    JOB                         R4
         B     SKIP490                    NUMBER                     R4
SKIP480  L     PL,PWKJOE           ADDRESS WORK-JOE                  R4
         USING JOEDSECT,PL         ACTIVATE JOE ADDRESSABILITY       R4
         MVC   PCCWORK+6(1),JOECURCL  GET SYSOUT CLASS               R4
         MVI   PCCWORK+7,C' '      SET TRAILING BLANK                R4
         L     R1,PLNDISPL         GET NEW LINE DISPLACEMENT         R4
         LA    PL,BLKPRT           POINT TO BLOCK LETTER ROUTINE    R41
         BAL   PL,0(,PL)           TO PRINT JOBID/CLASS SLANTED,    R41C
                                    CHANGE 'BAL' TO 'BALR PL,PL'    R41C
                                     AND 'NOPR 0'                   R41
         SPACE 1                                                    R41
         LM    PC1,PC2,PRCCWSP     LOAD SPACE CCW
         ICM   PC1,8,=X'13'        SET CCW TO DOUBLE SPACE
         BAL   PL,PPPUT            ADD CCW TO CHAIN
         L     R1,PLNDISPL         GET NEW LINE DISPLACEMENT         R4
         EJECT                                                 @OZ63941
BLKSKIP  DS    0H
         LA    PC1,0(R1,PBUF)      GET NEW LINE BUFFER ADDRESS       R4
         L     R1,PCEFORM          ID PAGE TYPE
         MVC   0(6,PC1),=C'***** ' PP001 FRAME CHARACTERS            R4
         MVC   6(121,PC1),5(PC1)   BLANK MIDDLE OF LINE              R4
         MVC   128(4,PC1),0(PC1)   PP129 FRAME CHARACTERS            R4
         L     PL,PWKJOE           ADDRESS WORK-JOE                  R4
         MVC   4(1,PC1),JOECURCL   PP005 SYSOUT CLASS                R4
         MVC   127(1,PC1),JOECURCL PP128 SYSOUT CLASS                R4
         DROP  PL                  DROP JOE ADDRESSABILITY           R4
         MVC   7(5,PC1),0(R1)      PP008 START/END/CONT              R4
         MVC   120(5,PC1),0(R1)    PP121 START/END/CONT              R4
         MVC   14(8,PC1),JCTJOBID  PP015 JOB NUMBER                  R4
         MVC   110(8,PC1),JCTJOBID PP111 JOB NUMBER                  R4
         MVC   24(8,PC1),JCTJNAME  PP025 JOB NAME                    R4
         MVC   100(3,PC1),=C'SYS'  PP101 SYS                         R4
         MVC   104(4,PC1),$SID     PP105 SYSTEM ID                   R4
         MVC   34(20,PC1),JCTPNAME PP035 PROGRAMMER NAME             R4
         MVC   56(4,PC1),=C'ROOM'  PP057 ROOM                        R4
         MVC   61(4,PC1),JCTROOMN  PP062 ROOM NUMBER                 R4
         TIME  DEC                 GET TIME AND DATE
         L     R15,=A(PTIMASK)     MOVE EDIT MASK              @OZ32776
         MVC   67(11,PC1),0(R15)    FOR TIME                   @OZ32776
         CL    R0,=X'12000000'     TEST TIME
         BL    PMORNING            BRANCH IF AM
         MVI   76(PC1),C'P'        CHANGE FROM AM TO PM              R4
         SL    R0,=X'12000000'     SUBTRACT TWELVE HOURS
         EJECT                                                      R41
PMORNING ST    R0,PCCWORK          STORE ADJUSTED TIME
         CLI   PCCWORK,X'00'       TEST FOR ZERO HOURS
         BNE   *+8                 BRANCH IF NOT
         MVI   PCCWORK,X'12'       CONVERT ZERO TO TWELVE
         TM    PCCWORK,X'08'       TEST FOR ADJUSTMENT ERROR
         BZ    *+8                 BRANCH IF NO ERROR
         NI    PCCWORK,X'09'       CORRECT FOR BINARY SUBTRACT ERROR
         ED    66(9,PC1),PCCWORK   PP068 HH.MM.SS                    R4
         LA    R15,PPDATE          CONVERT DATE                @OZ19494
         BALR  R14,R15              TO ' DD MMM YY'            @OZ19494
         MVC   67+11(10,PC1),PMESSAGE  PP080 DAY MONTH YEAR         R41
         L     PL,PCEDCT           ADDRESS PRINT DCT           @OZ32566
         USING DCTDSECT,PL         ACTIVATE DCT ADDRESSABILITY
         MVC   90(8,PC1),DCTDEVN   PP091 DEVICE NAME                 R4
         DROP  PL                  SUSPEND DCT ADDRESSABILITY
PRIDOUT  DS    0H                  END OF SETUP
         TM    $PRTOPTS,$PRTRANS   IF TRANSLATION NOT REQ'D,         R4
         BZ    PRIDNOTR             DON'T DO IT                @OZ32776
         CLC   PDEVTYPE+2(2),=AL1(UCB3UREC,UCB3211)  LOCAL 3211...   R4
         BE    PRIDNOTR            BR IF YES                   @OZ32776
         CLC   PDEVTYPE+2(2),=AL1(UCB3UREC,UCB3800)  3800 PRINTER... R4
         BE    PRIDNOTR            BR IF YES                   @OZ32776
         CLC   PDEVBYT2(2),=AL1(UCB3UREC,UCB3203)   LOCAL 3203 @OZ40627
         BE    PRIDNOTR            BR IF A 3203                @OZ40627
         L     R15,=A(PTRTBL)      TRANSLATE UNPRINTABLES      @OZ32776
         TR    0(132,PC1),0(R15)    TO BLANKS                  @OZ32776
PRIDNOTR DS    0H                                              @OZ32776
         SLR   JCT,JCT             INITIALIZE LINE COUNT       @OZ32776
         IC    JCT,$TPIDCT          FOR REMOTE SEPARATOR             R4
         TM    PCEID,PCELCLID      TEST FOR LOCAL PRINT
         BZ    PRIEJECT            BRANCH IF NOT LOCAL
         IC    JCT,$PRIDCT         LOCAL SEPARATOR LINE COUNT        R4
PRIEJECT DS    0H
         CLM   JCT,1,=AL1(30)      TEST FOR BLOCK LETTERS USED       R4
         BL    *+8                 BRANCH IF NO
         SH    JCT,=H'29'          ACCOUNT FOR BLOCK LETTER LINES    R4
         AL    PC1,PRCCWID         CONSTRUCT                         R4
         L     PC2,PRCCWID+4        PRINT CCW                        R4
         BAL   PL,PPPUT            ADD CCW TO CHAIN
         BCT   JCT,PPPUT           GENERATE SEPARATOR PAGE           R4
         L     JCT,PJCTBUF         RELOAD JCT ADDRESS                R4
         TM    PPFLAG,PPNEWS       JES2-NEWS AVAILABLE...           R41
         BO    PRNOID              SKIP EJECT IF YES                R41
         LM    PC1,PC2,PRCCWEJ     LOAD EJECT CCW
         BAL   PL,PPPUT            ADD CCW TO CHAIN
PRNOID   BAL   PL,PPWRITE          INITIATE WRITE
         BAL   PL,PPCHECK           AND CHECK
         PRETURN ,                 RESTORE REGS AND RETURN     @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
         TITLE 'HASP PRINT/PUNCH SERVICE -- BLOCK LETTER ROUTINE'
BLKPRT   ST    PL,PCEFORM+4        SAVE RETURN REGISTER              R4
         MVC   PBLKWORK,PCCWORK    SAVE TEXT                         R4
         OC    PCCWORK(8),=8X'C0'  SHIFT ALL TO 4TH QUADRANT
         TR    PCCWORK(8),BLOCKTR-192 TRANSLATE TO INDEX VALUE
         SLR   R4,R4               LINE 0 OF 12
BLKBLD   STH   R4,PCEFORM+8        SAVE LINE COUNTER                 R4
         ST    R1,PLNDISPL         SAVE NEW LINE DISPLACEMENT        R4
         ALR   R1,PBUF             GET NEW LINE BUFFER ADDRESS       R4
         MVI   0(R1),C' '          FILL LINE                         R4
         MVC   1(131,R1),0(R1)      WITH BLANKS                      R4
         LA    R7,7                SCAN FOR                          R4
BLKCENTR LA    R15,PBLKWORK(R7)      LAST                            R4
         CLI   0(R15),C' '            NON-                           R4
         BNE   SKIP510                 BLANK                         R4
         BCT   R7,BLKCENTR              CHARACTER                    R4
SKIP510  LA    R7,1(,R7)           COMPUTE                           R4
         MH    R7,=H'7'             BEGINNING                        R4
         SH    R7,=H'67'             PRINT POSITION                  R4
         LCR   R7,R7                  TO CENTER                      R4
         LA    R5,0(R7,R1)             BLOCK LETTERS                 R4
         TM    PCEFORM+4,X'80'     TEST FOR SLANTING OPTION          R4
         BO    SKIP520             BR IF NO                          R4
         LA    R5,6(,R5)           ELSE, SLANT                       R4
         SRL   R4,1                 BLOCK                            R4
         SLR   R5,R4                 LETTERS                         R4
SKIP520  SLR   R4,R4               SET FOR A LETTER INDEX OF 0       R4
BLKLUP   IC    R7,PBLKWORK(R4)     USE RELATIVE TEXT LETTER          R4
         STC   R7,$POSTSAV          TO FORM BLOCK TEXT               R4
         LA    R7,PCCWORK(R4)      CURRENT LETTER INDEX
         SLR   R15,R15             CLEAR REGISTER
         ICM   R15,1,0(R7)         GET TRANSLATED LETTER INDEX
         BZ    BLKSTOR             BRANCH IF INDEX ZERO
         BCTR  R15,0               DECREMENT BY ONE
         MH    R15,=H'24'          CONVERT TO DISPLACEMENT
         AH    R15,PCEFORM+8       SELECT FOR LINE WITHIN LETTER
         LA    R15,BLOCKA(R15)     LETTER MASK ADDRESS
         ICM   R15,12,0(R15)       LETTER MASK BITS
BLKSTOR  LA    R7,12               BLOCK WIDTH OF 12                 R4
BLKLOOP  ALR   R15,R15             SHIFT LEFT AND TEST HIGH BIT      R4
         BC    12,SKIP530          BRANCH IF OFF                     R4
         MVC   0(1,R5),$POSTSAV    OVERSTORE BLANK TO FORM BLOCK     R4
SKIP530  LA    R5,1(,R5)           INCREMENT COL NUMBER
         BCT   R7,BLKLOOP          BRANCH TO FILL 12 COL'S
         LA    R5,2(,R5)           2 BLANKS BETWEEN BLOCKS
         LA    R4,1(,R4)           STEP TO NEXT LETTER INDEX
         CL    R4,=F'8'            HAVE WE DONE 8 BLOCKS
         BL    BLKLUP              BRANCH IF NO
         LM    PC1,PC2,PRCCWID     GET PRINT CCW
         ALR   PC1,PBUF            ADD BUFFER ORIGIN
         AL    PC1,PLNDISPL         AND LINE DISPLACEMENT            R4
         BAL   PL,PPPUT            ADD CCW TO CHAIN
         L     R1,PLNDISPL         GET NEW                           R4
         LA    R1,132(,R1)          LINE DISPLACEMENT                R4
         LA    R4,132(,R1)         CHECK FOR ROOM                    R4
         CH    R4,$BUFLENG          IN BUFFER                        R4
         BNH   SKIP540             BR IF YES                         R4
         BAL   PL,PPWRITE          FORCE WRITE
         BAL   PL,PPCHECK          CHECK WRITE
         LA    R1,BUFSTART-BUFDSECT  SET STARTING LINE DISPLACEMENT  R4
SKIP540  LH    R4,PCEFORM+8        GET LINE COUNTER
         LA    R4,2(,R4)           STEP TO NEXT LINE
         CH    R4,=H'24'           LAST LINE FINISHED
         BL    BLKBLD              BRANCH IF NO
         ST    R1,PLNDISPL         SAVE NEW LINE DISPLACEMENT        R4
         L     PL,PCEFORM+4        LOAD RETURN REGISTER
         BR    PL                  RETURN TO CALLER
         TITLE 'HASP PRINT/PUNCH SERVICE -- DATE SUBROUTINE'        R41
***********************************************************************
*                                                                     *
*        PPDATE - SUBROUTINE TO FORMAT THE DATE                       *
*                                                                     *
* INPUT  R1    - DATE IN THE FORM 00YYDDDC                            *
*        R14   - RETURN ADDRESS                                @OZ19494
*        R15   - ENTRY ADDRESS                                 @OZ19494
*                                                                     *
* OUTPUT                                                       @OZ19494
*        PMESSAGE    - FORMATTED DATE IN THE FORM ' DD MMM YY'        *
*                                                                     *
***********************************************************************
         SPACE 1                                                    R41
         USING PPDATE,BASE2        PROVIDE LOCAL ADDRESSABILITY@OZ19494
         SPACE 1                                               @OZ19494
PPDATE   LTR   R1,R1               VALID DATE...               @OZ19494
         BZR   R14                 RETURN IF NO                @OZ19494
         ST    BASE2,PPSAVE2       SAVE PREVIOUS BASE REG      @OZ19494
         LR    BASE2,R15           LOAD BASE REGISTER          @OZ19494
         ST    R1,PCCWORK+4        STORE DATE                       R41
         L     PW,=A(PYEARTAB)     COPY DATE CONVERSION TABLE  @OZ32776
         MVC   PMONTHS,0(PW)        FOR POSSIBLE UPDATE        @OZ32776
         TM    PCCWORK+5,X'01'     AJUST                            R41
         BO    PTDEDYR              TABLE                           R41
         TM    PCCWORK+5,X'12'       ON                             R41
         BM    PTDEDYR                LEAP                          R41
         MVI   PFEB,29                 YEARS                        R41
         SPACE 1                                                    R41
PTDEDYR  MVC   PMESSAGE+7(3),=X'402120'  GET PATTERN                R41
         ED    PMESSAGE+7(3),PCCWORK+5    AND EDIT THE YEAR (YY)    R41
         XC    PCCWORK(6),PCCWORK  CLEAR ALL BUT JULIAN DAY         R41
         SLR   R0,R0               CLEAR FOR IC                     R41
         CVB   R1,PCCWORK          CONVERT TO BINARY DAY            R41
         LA    PW,PMONTHS-4        ADDR OF DATE CONVERSION TABLE    R41
         SPACE 1                                                    R41
PTDATLUP SLR   R1,R0               CONVERT                          R41
         LA    PW,4(,PW)            JULIAN DAY                      R41
         IC    R0,0(,PW)             TO                             R41
         CLR   R0,R1                  STANDARD DAY                  R41
         BL    PTDATLUP                *  (R1)                      R41
         SPACE 1                                                    R41
         CVD   R1,PCCWORK          CONVERT TO DECIMAL DAY           R41
         MVI   PMESSAGE,C' '       CLEAR 1ST BYTE OF AREA           R41
         UNPK  PMESSAGE+1(2),PCCWORK+6(2)  PLACE DAY (DD)           R41
         OI    PMESSAGE+2,X'F0'             INTO PMESSAGE           R41
         MVI   PMESSAGE+3,C' '     INSERT DELIMITER                 R41
         MVC   PMESSAGE+4(3),1(PW)  MOVE EBCDIC MONTH (MMM)         R41
         L     BASE2,PPSAVE2       RESTORE PREVIOUS BASE REG   @OZ19494
         BR    R14                  AND RETURN TO CALLER       @OZ19494
         TITLE 'HASP PRINT/PUNCH SERVICE -- MESSAGE AND COMMENT SUBROUTC
               INES'                                                 R4
***********************************************************************
*                                                                     *
*        PRMSG/PCOMMENT -- ADD MSG TO OUTPUT AND SEND TO OPER  @OZ19494
*                                                                     *
* ENTRY  R1  - POINTER TO MSG-ID                               @OZ19494
*        PL  - RETURN ADDRESS                                  @OZ48259
*        R14 - POINTER TO 12-BYTE MESSAGE TEXT                 @OZ48259
*        R15 - ENTRY ADDRESS                                   @OZ19494
*                                                              @OZ19494
***********************************************************************
         SPACE 1                                                     R4
         USING PRMSG,BASE2         PROVIDE LOCAL ADDRESSABILITY@OZ19494
         SPACE 1                                               @OZ19494
PRMSG    DS    0H                                              @OZ48259
         PSAVE ,                   SAVE LINK & BASE REGS       @OZ48259
         LR    BASE2,R15           LOAD BASE REGISTER          @OZ19494
         MVI   PMESSAGE,C' '                     BLANK OUT           R4
         MVC   PMESSAGE+1(L'PMESSAGE-1),PMESSAGE  MESSAGE AREA       R4
         MVC   PMESSAGE(9),0(R1)   MOVE IN MESSAGE ID                R4
         PACK  PMSGNO,5(3,R1)      SAVE ID IN PACKED FORM            R4
         L     R1,PCEDCT           GET DCT ADDRESS             @OZ32566
         MVC   PMESSAGE+9(8),DCTDEVN-DCTDSECT(R1)  FILL IN DEV NAME  R4
         MVC   PMESSAGE+17(12),0(R14) MOVE IN MESSAGE TEXT     @OZ48259
         TM    $RUNOPTS,$MSGID     IF MESSAGE IDS REQUESTED,         R4
         BO    PRMOUT               BR TO INCLUDE MSG ID       @OZ19494
         MVC   PMESSAGE+1(29),PMESSAGE+8 ELSE OVERLAY MSG ID        R41
PRMOUT   LA    R15,PCOMMENT        ADD MESSAGE                 @OZ19494
         ICM   R15,8,=X'80'         AND EJECT                  @OZ63941
         BALR  PL,R15               TO OUTPUT                  @G38ESBB
         LA    R1,PMESSAGE+7       POINT TO MESSAGE TEXT - 2         R4
         TM    $RUNOPTS,$MSGID     IF MESSAGE IDS REQUESTED,         R4
         BO    SKIP560              BR TO INSERT MESSAGE NUMBER      R4
         LA    R1,PMESSAGE           ELSE FIRST UPDATE TEXT ADDRESS  R4
SKIP560  MVC   0(2,R1),PMSGNO      MOVE PACKED MSG NUMBER TO MSG     R4
        $WTO   (R1),22,ROUTE=$LOG+$UR,  INFORM OPERATOR              R4C
               CLASS=$NORMAL,PRI=$ST,JOB=NO                          R4
         PRETURN ,                 RESTORE REGS & RETURN       @OZ48259
*              THIS LINE DELETED BY APAR OZ48259               @OZ48259
*              THIS LINE DELETED BY APAR OZ48259               @OZ48259
         EJECT                                                      R41
***********************************************************************
*                                                                     *
*        PCOMMENT -- ADD MSG TO OUTPUT ONLY                    @OZ19494
*                                                                     *
* ENTRY  PL    - RETURN ADDRESS                                @G38ESBB
*        R15   - ENTRY ADDRESS                                 @OZ19494
*                                                              @OZ19494
***********************************************************************
         SPACE 1                                                    R41
         USING PCOMMENT,BASE2      PROVIDE LOCAL ADDRESSABILITY@OZ19494
         SPACE 1                                               @OZ19494
PCOMMENT TM    PCEID,PCEPUSID      TEST PROCESSOR TYPE         @OZ19494
         BOR   PL                  RETURN IF PUNCH             @G38ESBB
*        DELETED                                               @G38ESBB
         PSAVE ,                   SAVE LINKAGE AND BASE REGS  @G38ESBB
         LR    BASE2,R15           LOAD BASE REGISTER          @OZ19494
         LM    PC1,PC2,PRCCWSP     LOAD SPACE CCW
         ICM   PC1,8,=X'13'        FORCE DOUBLE SPACING             R41
         L     PW,PPLC             INCREMENT                         R4
         LA    PW,4(,PW)            PAGE                             R4
         ST    PW,PPLC               LINE COUNT                      R4
         CL    PW,PRLINECT         COMPARE WITH MAXIMUM              R4
         BNH   PRCOMSP             BRANCH IF NOT HIGH
         LA    PW,1                RESET PAGE                        R4
         ST    PW,PPLC              LINE COUNTER                     R4
         LM    PC1,PC2,PRCCWEJ     LOAD EJECT CCW
         SPACE 1                                                    R41
PRCOMSP  MVC   PBUFSAVA,BUFSTART   SAVE BUFFER DATA            @OZ42418
         MVC   BUFSTART(L'PMESSAGE),PMESSAGE  MOVE MSG TO BUF  @OZ42418
*                                  THIS LINE DELETED BY APAR   @OZ42418
         BAL   PL,PPPUT            ADD CCW TO CHAIN            @G38ESBB
         SPACE 1                                               @G38ESBB
         LA    R1,BUFSTART         FIXED-STORAGE PRINT AREA    @G38ESBB
         LM    PC1,PC2,PRCCWCOM    LOAD PRINT CCW
         OR    PC1,R1              UPDATE DATA-ADDRESS               R4
         LTR   BASE2,BASE2         TEST FOR EJECT              @OZ63941
         BNM   *+8                 BRANCH IF NOT               @OZ63941
         ICM   PC1,8,=X'89'        SET WRITE AND EJECT         @OZ63941
         BAL   PL,PPPUT            ADD CCW TO CHAIN
         BAL   PL,PPWRITE          INITIATE WRITE
         BAL   PL,PPCHECK           AND CHECK
         SPACE 1                                                    R41
         MVC   PMESSAGE,BUFSTART   RESTORE MESSAGE             @OZ42418
         MVC   BUFSTART(L'PBUFSAVA),PBUFSAVA  RESTORE BUFFER   @OZ42418
*                                  THIS LINE DELETED BY APAR   @OZ42418
         SPACE 1                                                    R41
         PRETURN ,                 RESTORE REGS AND RETURN     @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
         EJECT                                                 @OZ19494
                                   PRINT OFF - SECTION DELETED @OZ19494
*                                  THIS LINE DELETED BY APAR   @OZ19494
                                   PRINT ON -- SECTION DELETED @OZ19494
         PRINT OFF                 THIS SECTION DELETED BY     @OZ32776
*                                  THIS LINE DELETED BY APAR   @OZ32776
*                                  THIS LINE DELETED BY APAR   @OZ32776
*                                  THIS LINE DELETED BY APAR   @OZ32776
*                                  THIS LINE DELETED BY APAR   @OZ32776
*                                  THIS LINE DELETED BY APAR   @OZ32776
*                                  THIS LINE DELETED BY APAR   @OZ32776
*                                  THIS LINE DELETED BY APAR   @OZ32776
*                                  THIS LINE DELETED BY APAR   @OZ32776
*                                  THIS LINE DELETED BY APAR   @OZ32776
*                                  THIS LINE DELETED BY APAR   @OZ32776
*                                  THIS LINE DELETED BY APAR   @OZ32776
*                                  THIS LINE DELETED BY APAR   @OZ32776
*                                  THIS LINE DELETED BY APAR   @OZ32776
*                                  THIS LINE DELETED BY APAR   @OZ32776
*                                  THIS LINE DELETED BY APAR   @OZ32776
*                                  THIS LINE DELETED BY APAR   @OZ32776
*                                  THIS LINE DELETED BY APAR   @OZ32776
*                                  THIS LINE DELETED BY APAR   @OZ32776
*                                  THIS LINE DELETED BY APAR   @OZ32776
         PRINT ON                  THIS SECTION DELETED BY     @OZ32776
         SPACE 1                                               @OZ28353
                                   PRINT OFF - SECTION DELETED @OZ19494
*                                  THIS LINE DELETED BY APAR   @OZ19494
*                                  THIS LINE DELETED BY APAR   @OZ19494
*                                  THIS LINE DELETED BY APAR   @OZ19494
*                                  THIS LINE DELETED BY APAR   @OZ19494
*                                  THIS LINE DELETED BY APAR   @OZ19494
*                                  THIS LINE DELETED BY APAR   @OZ19494
*                                  THIS LINE DELETED BY APAR   @OZ19494
*                                  THIS LINE DELETED BY APAR   @OZ19494
*                                  THIS LINE DELETED BY APAR   @OZ19494
*                                  THIS LINE DELETED BY APAR   @OZ19494
*                                  THIS LINE DELETED BY APAR   @OZ19494
*                                  THIS LINE DELETED BY APAR   @OZ19494
*                                  THIS LINE DELETED BY APAR   @OZ19494
*                                  THIS LINE DELETED BY APAR   @OZ19494
*                                  THIS LINE DELETED BY APAR   @OZ19494
*                                  THIS LINE DELETED BY APAR   @OZ19494
*                                  THIS LINE DELETED BY APAR   @OZ19494
*                                  THIS LINE DELETED BY APAR   @OZ19494
*                                  THIS LINE DELETED BY APAR   @OZ19494
*                                  THIS LINE DELETED BY APAR   @OZ19494
*                                  THIS LINE DELETED BY APAR   @OZ19494
*                                  THIS LINE DELETED BY APAR   @OZ19494
*                                  THIS LINE DELETED BY APAR   @OZ19494
*                                  THIS LINE DELETED BY APAR   @OZ19494
*                                  THIS LINE DELETED BY APAR   @OZ19494
*                                  THIS LINE DELETED BY APAR   @OZ19494
*                                  THIS LINE DELETED BY APAR   @OZ19494
*                                  THIS LINE DELETED BY APAR   @OZ19494
*                                  THIS LINE DELETED BY APAR   @OZ19494
*                                  THIS LINE DELETED BY APAR   @OZ19494
*                                  THIS LINE DELETED BY APAR   @OZ19494
*                                  THIS LINE DELETED BY APAR   @OZ19494
*                                  THIS LINE DELETED BY APAR   @OZ19494
*                                  THIS LINE DELETED BY APAR   @OZ19494
*                                  THIS LINE DELETED BY APAR   @OZ19494
*                                  THIS LINE DELETED BY APAR   @OZ19494
*                                  THIS LINE DELETED BY APAR   @OZ19494
*                                  THIS LINE DELETED BY APAR   @OZ19494
*                                  THIS LINE DELETED BY APAR   @OZ19494
*                                  THIS LINE DELETED BY APAR   @OZ19494
*                                  THIS LINE DELETED BY APAR   @OZ19494
*                                  THIS LINE DELETED BY APAR   @OZ19494
*                                  THIS LINE DELETED BY APAR   @OZ19494
*                                  THIS LINE DELETED BY APAR   @OZ19494
*                                  THIS LINE DELETED BY APAR   @OZ19494
*                                  THIS LINE DELETED BY APAR   @OZ19494
*                                  THIS LINE DELETED BY APAR   @OZ19494
                                   PRINT ON -- SECTION DELETED @OZ19494
         PRINT OFF                 THIS SECTION DELETED BY     @OZ32776
*                                  THIS LINE DELETED BY APAR   @OZ32776
*                                  THIS LINE DELETED BY APAR   @OZ32776
*                                  THIS LINE DELETED BY APAR   @OZ32776
*                                  THIS LINE DELETED BY APAR   @OZ32776
*                                  THIS LINE DELETED BY APAR   @OZ32776
*                                  THIS LINE DELETED BY APAR   @OZ32776
*                                  THIS LINE DELETED BY APAR   @OZ32776
*                                  THIS LINE DELETED BY APAR   @OZ32776
*                                  THIS LINE DELETED BY APAR   @OZ32776
*                                  THIS LINE DELETED BY APAR   @OZ32776
*                                  THIS LINE DELETED BY APAR   @OZ32776
*                                  THIS LINE DELETED BY APAR   @OZ32776
*                                  THIS LINE DELETED BY APAR   @OZ32776
*                                  THIS LINE DELETED BY APAR   @OZ32776
*                                  THIS LINE DELETED BY APAR   @OZ32776
*                                  THIS LINE DELETED BY APAR   @OZ32776
*                                  THIS LINE DELETED BY APAR   @OZ32776
         PRINT ON                  THIS SECTION DELETED BY     @OZ32776
 TITLE 'HASP PRINT/PUNCH SERVICE -- MISCELLANEOUS TABLES'      @OZ19494
***********************************************************************
*                                                                     *
*        BLOCK LETTER TABLE                                           *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
BLOCKTR  DC    X'0001020304050607080900000000000000'
         DC    X'0A0B0C0D0E0F1011120013000000000000'
         DC    X'1415161718191A1B000000000000'
         DC    X'1C1D1E1F202122232425002627000000'
         SPACE 2                                                     R4
BLOCKA   DC    X'7FE0FFF0C030C030C030FFF0FFF0C030C030C030C030C030'
BLOCKB   DC    X'FFE0FFF0C030C030C060FFC0FFC0C060C030C030FFF0FFE0'
BLOCKC   DC    X'7FE0FFF0C030C000C000C000C000C000C000C030FFF07FE0'
BLOCKD   DC    X'FF80FFC0C060C030C030C030C030C030C030C060FFC0FF80'
BLOCKE   DC    X'FFF0FFF0C000C000C000FF00FF00C000C000C000FFF0FFF0'
BLOCKF   DC    X'FFF0FFF0C000C000C000FF00FF00C000C000C000C000C000'
BLOCKG   DC    X'7FE0FFF0C030C000C000C000C1F0C1F0C030C030FFF07FE0'
BLOCKH   DC    X'C030C030C030C030C030FFF0FFF0C030C030C030C030C030'
BKOCKI   DC    X'7FE07FE0060006000600060006000600060006007FE07FE0'
BLOCKJ   DC    X'3FF03FF0030003000300030003000300C300C300FF007E00'
BLOCKK   DC    X'C030C060C0C0C180C300FE00FE00C300C180C0C0C060C030'
BLOCKL   DC    X'C000C000C000C000C000C000C000C000C000C000FFF0FFF0'
BLOCKM   DC    X'C030E070F0F0D9B0CF30C630C030C030C030C030C030C030'
BLOCKN   DC    X'C030E030F030D830CC30C630C330C1B0C0F0C070C030C010'
BLOCKO   DC    X'FFF0FFF0C030C030C030C030C030C030C030C030FFF0FFF0'
BLOCKP   DC    X'FFE0FFF0C030C030C030FFF0FFE0C000C000C000C000C000'
BLOCKQ   DC    X'7FE0FFF0C030C030C030C030C030C330C1B0C0F0FFE07FB0'
BLOCKR   DC    X'FFE0FFF0C030C030C030FFF0FFE0C300C180C0C0C060C030'
BLOCK$   DC    X'06007FE0FFF0C630E6007FC03FE00670C630FFF07FE00600'
BLOCKS   DC    X'7FE0FFF0C030C000E0007FC03FE000700030C030FFF07FE0'
BLOCKT   DC    X'FFF0FFF00600060006000600060006000600060006000600'
BLOCKU   DC    X'C030C030C030C030C030C030C030C030C030C030FFF07FE0'
BLOCKV   DC    X'C030C030C030C030C030C030C030606030C019800F000600'
BLOCKW   DC    X'C030C030C030C030C030C030C630CF30D9B0F0F0E070C030'
BLOCKX   DC    X'C030C030606030C019800F000F00198030C06060C030C030'
BLOCKY   DC    X'C030C030606030C019800F00060006000600060006000600'
BLOCKZ   DC    X'FFF0FFF0006000C001801FC01FC00C00180030007FF0FFF0'
BLOCK0   DC    X'3FC07FE0C0F0C1B0C330C630CC30D830F030E0307FE03FC0'
BLOCK1   DC    X'06000E001E0006000600060006000600060006007FE07FE0'
BLOCK2   DC    X'7FE0FFF0C0300030003000600180060018006000FFF0FFF0'
BLOCK3   DC    X'7FE0FFF0C0300030003001E001E000300030C030FFF07FE0'
BLOCK4   DC    X'038007800D80198031807FF0FFF001800180018001800180'
BLOCK5   DC    X'FFF0FFF0C000C000C000FF80FFC0006000300030FFF0FFE0'
BLOCK6   DC    X'7FE0FFF0C030C000C000FFE0FFF0C030C030C030FFF07FE0'
BLOCK7   DC    X'FFF0FFE0C0C0018003000600060006000600060006000600'
BLOCK8   DC    X'7FE0FFF0C030C03060603FC03FC06060C030C030FFF07FE0'
BLOCK9   DC    X'7FE0FFF0C030C030C030FFF0FFF000300030C030FFF07FE0'
BLOCK#   DC    X'30C030C0FFF0FFF030C030C030C030C0FFF0FFF030C030C0'
BLOCK@   DC    X'3FC07FE0C030003000301E303F306330C330C3307FE03FC0'
         EJECT                                                 @OZ32776
***************************************************************@OZ32776
*                                                              @OZ32776
*        &PRTRANS TRANSLATE TABLE                              @OZ32776
*                                                              @OZ32776
*        TRANSLATE LOWER-CASE TO UPPER-CASE                    @OZ32776
*        TRANSLATE INVALID PN-TRAIN CHARACTERS TO BLANKS       @OZ32776
*                                                              @OZ32776
***************************************************************@OZ32776
         SPACE 1                                               @OZ32776
PTRTBL   DC    C'                                '  TRANSLATE  @OZ32776
         DC    C'                                '  TABLE      @OZ32776
         DC    C'           .<(+&&          $*);^' USED TO    @OZ32776
         DC    C'-/         ,%_>?          :#@''="' TRANSLATE  @OZ32776
         DC    C' ABCDEFGHI       JKLMNOPQR      '  ALL        @OZ32776
         DC    C'  STUVWXYZ      0123456789      '  ILLEGAL    @OZ32776
         DC    C' ABCDEFGHI       JKLMNOPQR      '  CHARACTERS @OZ32776
         DC    C'  STUVWXYZ      0123456789      '  TO BLANKS  @OZ32776
         SPACE 5                                               @OZ32776
***************************************************************@OZ32776
*                                                              @OZ32776
*        MISCELLANEOUS CONSTANTS NOT DIRECTLY ADDRESSABLE      @OZ32776
*                                                              @OZ32776
***************************************************************@OZ32776
         SPACE 1                                               @OZ32776
PTIMASK  DC    X'21204B20204B202040C1D4'  TIME MASK            @OZ32776
PYEARTAB DC    AL1(31),C'JAN',AL1(28),C'FEB'  JULIAN           @OZ32776
         DC    AL1(31),C'MAR',AL1(30),C'APR'   TO DAY          @OZ32776
         DC    AL1(31),C'MAY',AL1(30),C'JUN'    AND            @OZ32776
         DC    AL1(31),C'JUL',AL1(31),C'AUG'     MONTH         @OZ32776
         DC    AL1(30),C'SEP',AL1(31),C'OCT'      CONVERSION   @OZ32776
         DC    AL1(30),C'NOV',AL1(255),C'DEC'      TABLE       @OZ32776
         SPACE 1                                               @OZ32776
PMONTHS  EQU   $CSAVREG,12*4       AREA FOR COPY OF ABOVE TABLE@OZ32776
PFEB     EQU   PMONTHS+4           ENTRY FOR FEBRUARY          @OZ32776
         SPACE 1                                               @OZ32776
PASAMCH  DC    X'400BF18BF013601BF293F39BF4A3F5ABF6B3F7BBF8C3F9CBC1D3C2C
               DBC3E34E03E541E681000B' ASA TO MACHINE CNVRT    @OZ33326
PASANUM  EQU   (*-PASAMCH)/2                                   @OZ32776
         SPACE 1                                               @OZ32776
PASAMCH2 DC    X'400BF18BF013601BF293F39BF4A3F5ABF6B3F7BBF8C3F9CBC1D3C2C
               DBC3E34E03E501E641000B' 3525 ASA TO MCH CNVRT   @OZ33326
         TITLE 'HASP PRINT SERVICE  --  PAGE COUNT ROUTINE'    @OZ29138
****************************************************************OZ29138
*                                                              *OZ29138
*        CALCULATE WHERE THE INTERVENTION REQURIED (PAPER JAM, *OZ29138
*        FORMS CHECK, ETC.) OCCURED FOR DATA SET REPOSITIONING *OZ29138
*                                                              *OZ29138
****************************************************************OZ29138
         SPACE 3                                               @OZ29138
PPCNTPGE DS    0H                                              @OZ29138
         LR    R14,R15             GET SUBROUTINE ENTRY ADDR   @OZ29138
         SPACE 1                                               @OZ29138
         USING PPCNTPGE,R14        ESTAB. SUBROUTINE BASE      @OZ29138
         USING DCTDSECT,PW         ESTAB. DCT ADDRESSABILITY   @OZ29138
         SPACE 1                                               @OZ29138
PPCKIO   L     R15,POUTIOB         GET OUTPUT CCW IOB ADDR     @OZ29138
         TM    BUFECBCC-BUFDSECT(R15),X'7F' I/O COMPLETE...    @OZ29138
         BNZ   PPCKCSW             BR IF YES                   @OZ29138
        $WAIT  IO                  WAIT FOR I/O POST           @OZ29138
         B     PPCKIO              ENSURE I/O HAS STOPPED      @OZ29138
         SPACE 1                                               @OZ29138
*        FIND OUT WHICH CCW CHAIN THE CSW BELONGS TO           @OZ29138
         SPACE 1                                               @OZ29138
PPCKCSW  CLC   DCTCSW+1(3),POUTCCWA+1 IS CSW IN ACTIVE AREA... @OZ29138
         BNH   PNXTCHN             BR IF NO, CHECK OTHER       @OZ29138
         L     R1,POUTCCWA         GET ACTIVE AREA POINTER     @OZ29138
         AH    R1,PCCWLAST         POINT TO THE PCIE,          @OZ29138
         LA    R1,PCIESIZE(,R1)     AND THE CCW CHKPT AREA.    @OZ29138
         CLM   R1,7,DCTCSW+1       IS CSW IN ACTIVE AREA...    @OZ29138
         BNH   PNXTCHN             BR IF CSW ADDR IS BEYOND END@OZ29138
         L     R1,POUTCCWA         GET START OF CCW CHAIN ADDR @OZ29138
         B     PFNDEND             FIND END OF CCW AREA        @OZ29138
PNXTCHN  DS    0H                                              @OZ29138
         CLC   DCTCSW+1(3),POUTCCWN+1 IS IT IN THIS AREA...    @OZ29138
         BNH   PPGERET             BR IF NOT THIS AREA EITHER  @OZ29138
         L     R1,POUTCCWN         GET NEXT AREA POINTER       @OZ29138
         AH    R1,PCCWLAST         POINT TO THE PCIE,          @OZ29138
         LA    R1,PCIESIZE(,R1)     AND THE CCW CHKPT AREA.    @OZ29138
         CLM   R1,7,DCTCSW+1       IS CSW IN THIS AREA...      @OZ29138
         BNH   PPGERET             BR IF CSW ADDR IS BEYOND END@OZ29138
         L     R1,POUTCCWN         GET START OF CCW CHAIN ADDR @OZ29138
         SPACE 1                                               @OZ29138
*        CSW IS VALID, FIND THE END OF THE CCW CHAIN.          @OZ29138
         SPACE 1                                               @OZ29138
PFNDEND  DS    0H                                              @OZ29138
         L     R0,DCTCSW           GET CSW ADDRESS AND         @OZ29138
         SH    R0,=H'8'             DECREMENT BY 8             @OZ29138
         LR    R15,R1              GET START OF CCW CHAIN      @OZ29138
         AH    R15,PCCWLAST        POINT TO THE LAST CCW+8     @OZ29138
         LR    R2,R1               GET START OF CCW CHAIN      @OZ29138
PINCR8   LA    R2,8(,R2)           INCREMENT BY ONE CCW        @OZ29138
         CR    R15,R2              END OF CHAIN...             @OZ29138
         BE    PFULLCHN            BR IF NOT A SHORT CHAIN     @OZ29138
         CLI   0(R2),X'08'         IS CCW A TIC (SHORT CHAIN)  @OZ29138
         BNE   PINCR8              BR IF NO, CHECK NEXT CCW    @OZ29138
PFULLCHN DS    0H                                              @OZ29138
         SPACE 1                                               @OZ29138
*        COUNT PAGES FROM END OF CCW CHAIN TO THE CSW          @OZ29138
         SPACE 1                                               @OZ29138
         SLR   R15,R15             CLEAR REGISTER FOR COUNT    @OZ29138
PCNTPGE  DS    0H                                              @OZ29138
         CR    R2,R0               CCW EQUAL TO INT REQ CCW... @OZ29138
         BNH   PCHNEND             BR IF TOP CONTINUE COUNTING @OZ29138
         CLI   0(R2),X'89'         IS CCW A CARRIAGE SKIP...   @OZ29138
         BL    *+8                 BR IF NO, NOT A PAGE        @OZ29138
         LA    R15,1(,R15)         ADD PAGE TO ACCUMULATOR     @OZ29138
         SH    R2,=H'8'            DECREMENT TO THE NEXT CCW   @OZ29138
         B     PCNTPGE             GO CHECK THE NEXT CCW       @OZ29138
         SPACE 1                                               @OZ29138
*        END OF PAGE COUNTING, NOW UPDATE PAGE COUNT IN PCE.   @OZ29138
         SPACE 1                                               @OZ29138
PCHNEND  DS    0H                                              @OZ29138
         LNR   R15,R15             COMPLEMENT FOR SUBTRACT     @OZ29138
         AH    R1,PCCWLAST         POINT TO THE PCIE,          @OZ29138
         LA    R1,PCIESIZE(,R1)     AND THE CCW CHKPT AREA.    @OZ29138
         A     R15,JOEPPCT-JOECKPP(,R1) UPDATE FROM JOE CNT    @OZ27300
         ST    R15,PDDBPGCT        UPDATE CURRENT PAGE COUNT   @OZ29138
         SPACE 1                                               @OZ29138
         SLR   R1,R1               DOES A                      @OZ29138
         LH    R1,$BSPSIZ           BACKSPACE                  @OZ29138
         LTR   R1,R1                 TABLE EXIST...            @OZ29138
         BZ    PPGERET             BR IF NO, RETURN            @OZ29138
         SH    R1,=H'7'            DECREMENT BY ONE ENTRY      @OZ29138
         LA    R1,PBSPTBL(R1)      GET ADDRESS OF LAST ENTRY   @OZ29138
         XC    0(7,R1),0(R1)       INVALIDATE BACKSPACE TABLE  @OZ29138
         SPACE 1                                               @OZ29138
PPGERET  DS    0H                                              @OZ29138
         L     PW,PCEDCT           REFRESH DCT POINTER         @OZ29138
         BR    PL                  RETURN TO CALLER            @OZ29138
         SPACE 1                                               @OZ29138
         DROP  PW,R14                                          @OZ29138
         TITLE 'HASP PRINT/PUNCH SERVICE -- TYPE 6 SMF RECORD' @OZ32776
***************************************************************@OZ32776
*                                                              @OZ32776
*        PPSMF6 -- SUBROUTINE TO GENERATE TYPE 6 SMF RECORD    @OZ32776
*                                                              @OZ32776
*        PL  - RETURN ADDRESS (HI-BIT ON MEANS DON'T $QUESMFB) @OZ34616
*        R15 - ENTRY ADDRESS                                   @OZ32776
*                                                              @OZ32776
***************************************************************@OZ32776
         SPACE 1                                               @OZ32776
         USING SMFDSECT,R1         PROVIDE SMF ADDRESSABILITY  @OZ32776
         USING JCTDSECT,JCT        PROVIDE JCT ADDRESSABILITY  @OZ32776
         USING PPSMF6,BASE2        PROVIDE LOCAL ADDR'BILITY   @OZ32776
         SPACE 1                                               @OZ32776
PPSMF6   DS    0H                                              @OZ32776
         PSAVE ALL                 SAVE CALLER'S REGISTERS     @OZ53654
*        DELETED                                               @G38ESBB
         LR    BASE2,R15           LOAD BASE REGISTER          @OZ32776
         L     R1,PPSMFBUF         OBTAIN FINAL TYPE 6         @OZ34616
         LTR   R1,R1                SMF RECORD BUFFER          @OZ34616
         BNZ   PPSMF6Q             BR IF NO RECORD AVAILABLE   @OZ34616
         CLI   $NUMSMFB,2          TEST SMF BUFFER COUNT       @OZ32776
         BL    PPSMFRET            RETURN IF SMF NOT SUPPORTED @OZ32776
         L     JCT,PJCTBUF         ADDRESS JCT BUFFER          @OZ32776
         TM    JCTSMFLG,JCTNOTY6   SHOULD TYPE 6 BE BYPASSED...@OZ32776
         BO    PPSMFRET            RETURN IF YES               @OZ32776
         SPACE 1                                               @OZ32776
         CLI   PDEVTYP3,UCB3800    TEST FOR 3800 PRINTER       @G38ESBB
         BNE   PPSMFGT             NO, GO GET WITH WAIT=YES    @G38ESBB
        $GETSMFB WAIT=NO           GET AN SMF BUFFER           @G38ESBB
         BNZ   PPSMFBLD            BRANCH IF SUCCESSFUL        @G38ESBB
         LM    PC1,PC2,PCCWCP      ISSUE CLEARPRINT CCW        @G38ESBB
         BAL   PL,PPPUT             TO TRY TO FORCE            @G38ESBB
         BAL   PL,PPWRITE            FREEING OF AN             @G38ESBB
         BAL   PL,PPCHECK             SMF BUFFER               @G38ESBB
         SPACE 1                                               @G38ESBB
PPSMFGT $GETSMFB WAIT=YES          GET AN SMF BUFFER           @G38ESBB
         ST    R1,PPSMFBUF         SAVE SMF BUFFER ADDRESS     @OZ34616
         SPACE 1                                               @OZ32776
***************************************************************@OZ32776
*                                                              @OZ32776
*        BUILD BASE SECTION                                    @OZ32776
*                                                              @OZ32776
***************************************************************@OZ32776
         SPACE 1                                               @G38ESBB
PPSMFBLD XC    SMFRDW+2(SMF6END1-SMFRDW-2),SMFRDW+2 CLEAR REC  @G38ESBB
         MVC   SMF6JBN,JCTJMRJN    JOBNAME               -JBN  @OZ32776
         MVC   SMF6RST(8),JCTRDRON RDR START TIME/DATE   -RST/D@OZ32776
         MVC   SMF6UIF,JCTUSEID    USER ID               -UIF  @OZ32776
         MVC   SMF6JNM,JCTJOBID+4  JOB NUMBER            -JNM  @OZ32776
         SPACE 1                                               @OZ32776
         L     PW,PWKJOE           CURRENT                     @OZ32776
         USING JOEDSECT,PW          SYSOUT                     @OZ32776
         MVC   SMF6OWC,JOECURCL      CLASS               -OWC  @OZ32776
         SPACE 1                                               @OZ32776
         L     PW,PCEDCT           PROVIDE DCT                 @OZ32566
         USING DCTDSECT,PW          ADDRESSABILITY             @OZ32776
         MVC   SMF6FMN,DCTFORMS    FORMS ID              -FMN  @OZ32776
         MVC   SMF6OUT,DCTDEVN     OUTPUT DEVICE NAME    -OUT  @OZ32776
         MVC   SMF6FCB,DCTFCB      FCB ID                -FCB  @OZ32776
         MVC   SMF6UCS,DCTUCS      UCS ID                -UCS  @OZ32776
         SPACE 1                                               @OZ32776
         MVC   SMF6WST(8),PTIMEON  PRPU START TIME/DATE  -WST/D@OZ32776
         MVC   SMF6NLR,PPLNCDCT    TOTAL RECORD COUNT    -NLR  @OZ32776
         MVC   SMF6PGE,PRPAGECT    TOTAL PAGE COUNT      -PGE  @OZ32776
         MVC   SMF6NDS,PPJNDS      DATA SET COUNT        -NDS  @OZ32776
         MVC   SMF6DCI(1),PSMFDCI  CONTROL INDICATORS    -DCI  @OZ32776
         TM    PPFLAG2,PSMFDSER    IF DATA                     @OZ32776
         BZ    SKIP440              BUFFER ERROR...            @OZ32776
         OI    SMF6IOE,SMFDSER       SET SMF IOE FLAG    -DSER @OZ32776
SKIP440  TM    PPFLAG,PPJCTIOT     IF CONTROL                  @OZ32776
         BNO   SKIP450              BUFFER ERROR...            @OZ32776
         OI    SMF6IOE,SMFCBER       SET SMF IOE FLAG    -CBER @OZ32776
         EJECT                                                 @OZ32776
***************************************************************@OZ32776
*                                                              @OZ32776
*        BUILD JES2-ONLY SECTION                               @OZ32776
*                                                              @OZ32776
***************************************************************@OZ32776
         SPACE 1                                               @OZ32776
SKIP450  MVC   SMF6RTE,DCTNO       DEVICE ROUTE CODE     -RTE  @OZ32776
         SPACE 1                                               @OZ32776
         LA    PC1,SMF6END1-SMF6LN1 LENGTH OF BASE             @OZ32776
         STH   PC1,SMF6LN1           AND JES2-ONLY SECT. -LN1  @OZ32776
         LA    LINK,SMF6END1-SMFRDW TOTAL LENGTH, SO FAR       @OZ32776
         SPACE 1                                               @OZ32776
***************************************************************@OZ32776
*                                                              @OZ32776
*        BUILD NON-IMPACT (3800) PRINTER SECTION               @OZ32776
*                                                              @OZ32776
***************************************************************@OZ32776
         SPACE 1                                               @OZ32776
         CLI   PDEVTYPE+3,UCB3800  NON-IMPACT PRINTER...       @OZ32776
         BNE   PPSMF6A             BR IF NOT                   @OZ32776
         SPACE 1                                               @OZ32776
         LA    PC1,SMF6LN1(PC1)    PROVIDE NON-IMPACT PRINTER  @OZ32776
         USING SMF6NIPX,PC1         SECTION ADDRESSABILITY     @OZ32776
         SPACE 1                                               @OZ32776
         MVC   SMF6CPS,PCOPYGRP    COPY GROUPS           -CPS  @OZ32776
         MVC   SMF6CHR(4*4),DCTCHAR1  MOVE CHARS,        -CHR  @OZ32776
         MVC   SMF6MID,DCTMODF         MODIFY, AND       -MID  @OZ32776
         MVC   SMF6FLI,DCTFLASH         FLASH TO TYPE 6  -FLI  @OZ32776
         LA    R15,SMF6CHR         POINT TO SMF FIELDS         @OZ32776
         LA    R0,4+1+1            CHAR1,2,3,4, MODIFY, FLASH  @OZ32776
         SPACE 1                                               @OZ32776
PPSMFLUP CLC   0(4,R15),=C'****'   TEST FOR NULL               @OZ32776
         BNE   *+10                LEAVE VALUE IF NO           @OZ32776
         MVC   0(4,R15),=C'    '    ELSE MOVE IN BLANKS        @OZ32776
         LA    R15,4(,R15)         INCR SMF FIELD ADDR         @OZ32776
         BCT   R0,PPSMFLUP         CHECK ALL VALUES            @OZ32776
*              THIS LINE DELETED BY APAR NUMBER                @OZ32776
         CLC   SMF6FLI,=C'    '    IF NOT FLASHING             @OZ33895
         BE    PPSMFBST             DON'T SET FLASH COUNT      @OZ33895
         MVC   SMF6FLC,PFLASHC     COPY FLASH COUNT      -FLC  @OZ32776
         CLC   PFLASHC,PPDSCPY     IF FLASH COUNT IS GREATER   @OZ33895
         BNH   PPSMFBST             THAN COPY COUNT,           @OZ33895
         MVC   SMF6FLC,PPDSCPY       SET IT EQUAL TO COPIES    @OZ33895
PPSMFBST DS    0H                                              @OZ33895
         TM    DCTPPSW2,DCTNIBRS   SET                         @OZ32776
         BZ    SKIP460              BURST OR                   @OZ32776
         OI    SMF6BID,SMF6BTS       NOBURST             -BTS  @OZ32776
SKIP460  TM    PPFLAG2,PPOPTJ      SET                         @OZ32776
         BZ    SKIP470             DCB=OPTCD=J                 @OZ32776
         OI    SMF6BID,SMF6OPJ       SPECIFICATION       -OPJ  @OZ32776
         SPACE 1                                               @OZ32776
SKIP470  LA    R0,SMF6END2-SMF6LN2 LENGTH OF NON-              @OZ32776
         STH   R0,SMF6LN2           IMPACT PRINTER SECT. -LN2  @OZ32776
         OI    SMF6IND,SMF6FEXT    SHOW SECTION EXISTS   -IND  @OZ32776
         LA    LINK,SMF6END1-SMFRDW+SMF6END2-SMF6NIPX  NEW LEN @OZ32776
         SPACE 1                                               @OZ32776
         DROP  PC1           SUSPEND EXTENSION ADDRESSABILITY  @OZ32776
         EJECT                                                 @OZ32776
***************************************************************@OZ32776
*                                                              @OZ32776
*        SET HEADER INFORMATION AND QUEUE SMF BUFFER FOR WRITE @OZ32776
*                                                              @OZ32776
***************************************************************@OZ32776
         SPACE 1                                               @OZ32776
PPSMF6A  DS    0H                                              @OZ32776
         STH   LINK,SMFRDW         TOTAL RECORD LENGTH   -RDW  @OZ32776
         MVI   SMF6SBS+1,2         JES2 SUBSYSTEM ID     -SBS  @OZ32776
         MVI   SMFHDRTY,SMFOUTTP   SET RECORD ID TO 6    -HDRTY@OZ32776
         CLI   PDEVTYP3,UCB3800    TEST FOR 3800 PRINTER       @G38ESBB
         BE    PPSMFNIP            YES, BYPASS WRITING RECORD  @G38ESBB
         L     PL,PPLSAVE          SHOULD THIS SUBROUTINE      @OZ34616
         LTR   PL,PL                QUEUE THE RECORD...        @OZ34616
         BM    PPSMFRET            BR IF NO                    @OZ34616
         SPACE 1                                               @G38ESBB
PPSMF6Q  MVC   PPSMFBUF,$ZEROS      ELSE CLEAR BUFFER POINTER  @OZ34616
        $QUESMFB                   QUEUE TYPE 6 FOR WRITE      @OZ32776
         SPACE 1                                               @G38ESBB
PPSMFRET PRETURN                   RESTORE REGS AND RETURN     @G38ESBB
         BR    PL                   AND RETURN TO CALLER       @OZ32776
         SPACE 1                                               @OZ32776
PPSMFNIP ST    R1,PPSMFBUF         SAVE SMF BUFFER ADDRESS     @G38ESBB
         SPACE 1                                               @OZ46856
PGETPQES L     PW,PQHADR           ADDRESS PQH                 @OZ46856
         NI    PQHAFLAG-PQHDSECT(PW),FF-PQHALOC  RESET FLAG    @OZ48003
         L     R15,=A(PADDPQE)     CALL SUBROUTINE TO          @OZ48003
         BALR  PL,R15               ALLOCATE A PQE             @OZ48003
         BNZ   PGOTPQES            BRANCH IF SUCCESSFUL        @G38ESBB
         SPACE 1                                               @OZ48003
         L     R1,PPSMFBUF         ADDRESS SMF BUFFER          @OZ48003
         MVC   PPSMFBUF,$ZEROS     CLEAR BUFFER ADDRESS        @OZ48003
         BAL   LINK,PFRESMFB       FREE SMF BUFFER             @OZ48003
         B     PPSMFRET             AND RETURN                 @OZ48003
         SPACE 1                                               @G38ESBB
         USING PQEDSECT,R1         PROVIDE PQE ADDRESSABILITY  @G38ESBB
         USING PQHDSECT,PW         PROVIDE PQH ADDRESSABILITY  @G38ESBB
         SPACE 1                                               @G38ESBB
PGOTPQES MVC   PQESBUF,PPSMFBUF    SAVE SMF BUF ADR IN PQE     @G38ESBB
         MVC   PPSMFBUF,$ZEROS     CLEAR SMF BUFFER ADDRESS    @G38ESBB
         MVI   PQETYPE,PQES        INDICATE SMF TYPE 6 PQE     @G38ESBB
         L     PC1,PQEPREV         GET PREVIOUS PQEC           @G38ESBB
         MVC   PQECPGID(L'PQECPGID+L'PQERPGID+L'PQEFCBLN),PQECPGID-PQEDC
               SECT(PC1)           SET ID'S FROM LAST PAGE PQE @G38ESBB
         B     PPSMFRET            GO RETURN                   @G38ESBB
         DROP  PW,R1               DROP PQH,PQE ADDRESSABILITY @G38ESBB
         TITLE 'HASP PRINT/PUNCH SERVICE -- PDIR SUBROUTINE'        R41
***********************************************************************
*                                                                     *
*        BUILD AND TPPUT A REMOTE SETUP HEADER (PDIR)                 *
*                                                                     *
***********************************************************************
         SPACE 1                                                    R41
PPDIR    DS    0H                  REMOTE SETUP HEADER              R41
         USING PPDIR,BASE2         ESTABLISH ADDRESSABILITY         R41
         ST    BASE2,PDSVSAVE      SAVE OLD BASE2                   R41
         ST    PL,PPLSAVE          SAVE RETURN                      R41
         LR    BASE2,R15           LOAD BASE REGISTER               R41
         USING FMHDSECT-(BUFSTART-BUFDSECT),PBUF  ADDRESSABILITY    R41
         MVI   FMHLNGTH,FMHPDLPP   LENGTH OF PDIR TO BE SENT        R41
         MVI   FMHBYTE1,FMHTYPE2   SET PDIR TYPE INDICATOR          R41
         MVI   FMHSEL,1            SET SELECTION INDICATOR          R41
         MVC   FMHPDRID,PPDIRID    SET PDIR IDENTIFIER              R41
         SPACE 1                                                    R41
PPDDATE  MVC   FMHPDATE,=C'00/00/00' SET DEFAULT DATE               R41
         LA    LINK,PTIMEON        POINT TO PRINT TIME AND DATE     R41
PPDEXDA  UNPK  FMHPDATE+6(3),5(2,LINK) PUT YEAR IN PDIR             R41
         ZAP   PCCWORK(8),4(4,LINK) CONVERT                         R41
         SRP   PCCWORK,64-3,0        YEAR                           R41
         CVB   R1,PCCWORK             TO BINARY                     R41
         L     PW,=A(PYEARTAB)     COPY DATA CONVERSION TABLE  @OZ32776
         MVC   PMONTHS,0(PW)       FOR POSSIBLE UPDATE         @OZ32776
         SLR   R0,R0               CLEAR REGISTER                   R41
         LA    R0,3                PREPARE TEST FOR LEAP YEAR       R41
         NR    R0,R1               IF NOT LEAP YEAR                 R41
         BNZ   PPDCONV              USE TABLE AS IS                 R41
         MVI   PFEB,29             SET TABLE FOR LEAP YEAR          R41
PPDCONV  ZAP   PCCWORK(8),6(2,LINK)     CONVERT MONTH AND           R41
         CVB   R1,PCCWORK          DAY TO BINARY                    R41
         SLR   R0,R0               CLEAR REGISTER                   R41
         LA    PW,PMONTHS-4        PREPARE TO SCAN CONVERSION TABLE R41
         LA    PC1,0               START WITH MONTH 0               R41
PPDDTLP  LA    PC1,1(,PC1)         INCREMENT MONTH                  R41
         SR    R1,R0               CONVERT                          R41
         LA    PW,4(,PW)            FROM JULIAN                     R41
         IC    R0,0(,PW)             DATE TO                        R41
         CLR   R0,R1                  MONTH                         R41
         BL    PPDDTLP                 AND DAY                      R41
         CVD   PC1,PCCWORK         PREPARE MONTH                    R41
         UNPK  FMHPDATE(2),PCCWORK+6(2) PUT MONTH IN PDIR           R41
         OI    FMHPDATE+1,X'F0'    MAKE LAST CHARACTER PRINTABLE    R41
         CVD   R1,PCCWORK          PREPARE DAY                      R41
         UNPK  FMHPDATE+3(2),PCCWORK+6(2) PUT DAY IN PDIR           R41
         OI    FMHPDATE+4,X'F0'    MAKE LAST CHARACTER PRINTABLE    R41
         SPACE 1                                                    R41
PPDTIME  MVC   FMHPDTIM,=C'00.00.00' SET DEFAULT TIME               R41
         L     PC2,0(,LINK)        PREPARE TIME                     R41
         SLR   PC1,PC1              FOR DIVISION                    R41
         D     PC1,=A(60*60*100)   CALCULATE HOURS                  R41
         CVD   PC2,PCCWORK         PREPARE HOURS                    R41
         UNPK  FMHPDTIM(2),PCCWORK+6(2) PUT HOURS IN PDIR           R41
         OI    FMHPDTIM+1,X'F0'    MAKE UNITS DIGIT PRINTABLE       R41
         SRDL  PC1,32              PREPARE REMAINDER FOR DIVISION   R41
         D     PC1,=A(60*100)      CALCULATE MINUTES                R41
         CVD   PC2,PCCWORK         PREPARE MINUTES                  R41
         UNPK  FMHPDTIM+3(2),PCCWORK+6(2) PUT MINUTES IN PDIR       R41
         OI    FMHPDTIM+4,X'F0'    MAKE UNITS DIGIT PRINTABLE       R41
         CVD   PC1,PCCWORK         PREPARE SECONDS                  R41
         UNPK  FMHPDTIM+6(4),PCCWORK+5(3) PUT SECONDS IN PDIR       R41
         SPACE 1                                                    R41
PPDFROMS MVI   FMHPDFRM,C' '       CLEAR REST OF PDIR               R41
         MVC   FMHPDFRM+1(FMHPDEND-FMHPDFRM-1),FMHPDFRM             R41
         L     R1,PCEDCT           ADDRESS PRINTER DCT         @OZ32566
         USING DCTDSECT,R1                                          R41
         MVC   FMHPDFRM(4),DCTFORMS PUT FORMS IN PDIR               R41
         SPACE 1                                                    R41
PPDFCB   MVC   FMHPDFCB(4),DCTFCB  PUT FCB NAME IN PDIR             R41
         SPACE 1                                                    R41
PPDTRAIN MVC   FMHPDUCS(4),DCTUCS  PUT UCS NAME IN PDIR             R41
         SPACE 1                                                    R41
         DROP  R1                                                   R41
         SPACE 1                                                    R41
         L     R1,PPDSRCT                   CONVERT RECORD COUNT    R41
         CVD   R1,PCCWORK                    TO DECIMAL             R41
         MVC   FMHPDCNT,=X'4020202020202120'  AND EDIT              R41
         ED    FMHPDCNT,PCCWORK+4              INTO PDIR            R41
         SPACE 1                                                    R41
         L     JCT,PJCTBUF         ASSURE JCT ADDRESSABILITY   @OZ56508
         MVC   FMHPDJOB,JCTJNAME   SET JOBNAME IN PDIR              R41
         SPACE 1                                                    R41
         SLR   PW,PW               COMPUTE                          R41
         SLR   R1,R1                NUMBER                          R41
         CLI   PPDIRID,X'01'         OF                             R41
         BE    PPDCOPY                REMAINING                     R41
         IC    PW,PPDSCPY              COPIES                       R41
         IC    R1,PPRCPYCT              MINUS                       R41
         SLR   PW,R1                     ONE                        R41
         BCTR  PW,0                       = ADDTIONAL COPIES        R41
         SPACE 1                                                    R41
PPDCOPY  CVD   PW,PCCWORK          CONVERT COPY COUNT TO DECIMAL    R41
         MVC   FMHPDCPY+2(6),=X'402020202120'  SET EDIT PATTERN     R41
         ED    FMHPDCPY+2(6),PCCWORK+5  EDIT COUNT INTO PDIR        R41
         SPACE 1                                                    R41
PPDPUT   LM    PC1,PC2,PDIRCCW     INITIALIZE                       R41
         ALR   PC1,PBUF             OUTPUT                          R41
         STM   PC1,PC2,POUTCCWA      CCW                            R41
         LA    R0,POUTCCWA         PICK UP ADDRESS OF CCW           R41
         L     R1,PCEDCT           PICK UP DCT ADDRESS         @OZ32566
         $EXTP PUT,(R1),(R0)       PASS CCW TO RTAM                 R41
         BNM   PPDEXIT             BR IF PDIR ACCEPTED              R41
         SPACE 1                                                    R41
         LA    PW,0                ELIMINATE PDIR ADDITIONAL COPIES R41
         B     PPDCOPY              AND RESEND PDIR                 R41
         SPACE 1                                                    R41
         SPACE 1                                                    R41
PPDEXIT  SLR   R1,R1               DECREMENT                        R41
         IC    R1,PPDSCPY           REMAINING                       R41
         SLR   R1,PW                 NUMBER                         R41
         STC   R1,PPDSCPY             OF COPIES                     R41
         L     BASE2,PDSVSAVE      RESTORE BASE2                    R41
         L     PL,PPLSAVE          RESET RETURN REGISTER            R41
         BR    PL                  RETURN                           R41
         SPACE 3                                                    R41
FMHPDLPP EQU   FMHPDSTP-FMHDSECT   LENGTH OF PDIR SENT BY HASPPRPU  R41
         SPACE 3                                                    R41
         USING BUFDSECT,PBUF       RE-ESTABLISH ADDRESSABILITY      R41
         EJECT                                                      R41
         LTORG                     SETUP HEADER LITERAL POOL        R41
         TITLE 'HASP PRINT/PUNCH SERVICE -- DEVICE SETUP VERIFICATION'
***********************************************************************
*                                                                     *
*        DEVICE SETUP/VERIFICATION FOR PUNCHES AND IMPACT PRINTERS    *
*                                                                     *
*        ENTRY:  R1  = ADDRESS OF SETUP PARAMETER LIST         @OZ75372
*        EXIT:   R15 = --- RETURN CODE                         @OZ75372
*                    = 0   NORMAL RETURN                       @OZ75372
*                    = 4   OPERATOR TERMINATED                 @OZ75372
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
PRPUDSV  DS    0H                                                    R4
         PSAVE ALL                 SAVE CALLER'S REGISTERS     @G38ESBB
         LR    BASE2,R15           SETUP LOCAL                       R4
         USING PRPUDSV,BASE2        ADDRESSABILITY                   R4
         CLI   PDEVTYPE+3,UCB3800  TEST FOR NON-IMPACT PRINTER       R4
         BNE   DSVJCT              BRANCH IF NOT               @G38ESBB
         L     R15,=A(P3800DSV)    ELSE, GO TO 3800                  R4
         B     P3800DS              DEVICE SETUP VERIFICATION        R4
         SPACE 1                                                     R4
***********************************************************************
*                                                                     *
*        DETERMINE IF AN OPERATOR MESSAGE IS NECESSARY                *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
DSVJCT   DS    0H                                              @G38ESBB
         SLR   PL,PL               INITIALIZE                  @OZ75372
         L     JCT,PSAVAREA         DEVICE SETUP               @OZ75372
         ST    PL,4+(15*4)(,JCT)     RETURN CODE               @OZ75372
         L     JCT,PJCTBUF         ADDRESS JCT BUFFER                R4
         USING JCTDSECT,JCT        ACTIVATE JCT ADDRESSABILITY       R4
         LR    PL,R1               COPY PARAMETER REGISTER           R4
         L     PC1,PCEDCT          ADDRESS PRINT/PUNCH DCT     @OZ32566
         STCM  PC1,7,BUFDCT+1      ENSURE CORRECT DCT ADDR IN BUFFER R4
         USING DCTDSECT,PC1        ACTIVATE DCT ADDRESSABILITY       R4
         NI    DCTPPSW,255-DCTPPSWO RESET OPERATOR $T ALLOWED        R4
         MVC   PCEFORM(12),DCTFORMS SAVE CURRENT DEVICE SETUP        R4
         MVC   PCCWORK(1),DCTPPSW  COPY PRINT/PUNCH FLAGS      @OZ66318
         NI    PCCWORK,DCTPPSWU+DCTPPSWB ISOLATE UCS/FCB BITS  @OZ66318
         TM    DCTPPSW2,DCTNINIT   IF DEVICE DOES NOT NEED     @OZ32311
         BZ    DSVFORM              INITIALIZATION, BRANCH     @OZ32311
         NI    DCTPPSW2,255-DCTNINIT RESET INITIALIZ. FLAG     @OZ32311
         TM    DCTPPSW,DCTPPSWF    OPERATOR FORM MODE ?        @OZ40515
         BO    DSVOPMD              YES..NO OPERATOR ACTION    @OZ40515
         OI    DCTPPSW,DCTPPSWC+DCTPPSWT+DCTPPSWO SETUP FLAGS  @OZ32311
DSVOPMD  OI    DCTPPSW,DCTPPSWC+DCTPPSWT  SETUP FLAGS          @OZ40515
         EJECT                                                 @OZ75372
***********************************************************************
*                                                                     *
*        CHECK FORMS                                                  *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
DSVFORM  DS    0H                                                    R4
         CLC   DCTFORMS,0(PL)      FORMS CHANGE                      R4
         BE    DSVFCB              BRANCH IF NO                      R4
         OI    DCTPPSW,DCTPPSWO    SET OPERATOR $T ALLOWED           R4
         MVC   DCTFORMS,0(PL)      NEW FORMS ID TO DCT               R4
         SPACE 1                                               @OZ75372
***********************************************************************
*                                                                     *
*        CHECK FCB AND INDEX                                          *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
DSVFCB   DS    0H                                                    R4
         TM    PCEID,PCEPUSID      IS THIS A PUNCH PROCESSOR         R4
         BO    DSVMSG              BRANCH IF YES                     R4
         TM    PRINDEX,X'C0'       WAS AN INDEX VALUE SUPPLIED       R4
         BZ    SKIP580             BRANCH IF NO - AVOID RELOAD       R4
         CLC   PRINDEX,DCTINDEX    DOES NEW INDEX = OLD              R4
SKIP580  BE    *+8                 BRANCH IF YES - AVOID RELOAD      R4
         OI    DCTPPSW,DCTPPSWC    SET FCB LOAD REQUIRED SW          R4
         CLC   DCTFCB,4(PL)        FCB CHANGE                        R4
         BE    DSVUCSB             BRANCH IF NO                      R4
         CLC   4(4,PL),=CL4'****'  IS STANDARD FCB REQUESTED         R4
         BNE   DSVFCB05            BRANCH IF NO                      R4
         TM    DCTPPSW,DCTPPSWB    IS DEVICE FCB STANDARD            R4
         BZ    DSVUCSB             BRANCH IF YES                     R4
         CLC   DCTFCB,$PRTFCB      IF INSTALLATION DEFAULT IS        R4
         BNE   DSVFCB05              CURRENTLY LOADED,               R4
         NI    DCTPPSW,X'FF'-DCTPPSWB RESET 'NON-STD' BIT            R4
         B     DSVUCSB               AND BRANCH TO TEST UCS          R4
         SPACE 1                                                     R4
DSVFCB05 DS    0H                                                    R4
         OI    DCTPPSW,DCTPPSWB+DCTPPSWC NON-STD+CARRIAGE LOAD       R4
         L     R0,4(,PL)           PICK UP REQUESTED FCB             R4
         CL    R0,=CL4'****'       CHECK FOR DEFAULT SPECIFICATION   R4
         BNE   SKIP590               SKIP IF NOT                     R4
         L     R0,$PRTFCB          REPLACE WITH DEFAULT NAME         R4
         NI    DCTPPSW,X'FF'-DCTPPSWB RESET 'NON-STANDARD' BIT       R4
SKIP590  ST    R0,DCTFCB           STORE FCB ID IN DCT               R4
         CLI   PDEVBYT3,UCB1403    IS DEVICE 1403              @OZ40627
         BNE   DSVUCSB             BRANCH IF NOT A 1403        @OZ40627
         OI    DCTPPSW,DCTPPSWO    SET OPERATOR $T ALLOWED           R4
         EJECT                                                 @OZ75372
***********************************************************************
*                                                                     *
*        CHECK UCS                                                    *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
DSVUCSB  DS    0H                                                    R4
         CLC   DCTUCS,8(PL)        UCSB CHANGE                       R4
         BE    DSVMSG              BRANCH IF NO                      R4
         CLC   8(4,PL),=CL4'****'  IS STANDARD UCS REQUESTED         R4
         BNE   DSVUCSB5            BRANCH IF NO                      R4
         TM    DCTPPSW,DCTPPSWU    IS DEVICE UCS STANDARD            R4
         BZ    DSVMSG              BRANCH IF YES                     R4
         CLC   DCTUCS,$PRTUCS      IF INSTALLATION DEFAULT IS        R4
         BNE   DSVUCSB5              CURRENTLY LOADED,               R4
         NI    DCTPPSW,X'FF'-DCTPPSWU RESET 'NON-STD' BIT            R4
         B     DSVMSG                AND BRANCH TO ISSUE MESSAGE     R4
DSVUCSB5 DS    0H                                                    R4
         OI    DCTPPSW,DCTPPSWO+DCTPPSWT+DCTPPSWU MSG+NONSTD+TRAIN   R4
         L     R0,8(,PL)           PICK UP REQUESTED UCS             R4
         CL    R0,=CL4'****'       CHECK FOR DEFAULT SPECIFICATION   R4
         BNE   SKIP600             SKIP IF NOT                       R4
         L     R0,$PRTUCS          REPLACE WITH DEFAULT NAME         R4
SKIP600  ST    R0,DCTUCS           STORE UCS ID IN DCT               R4
         EJECT                                                       R4
***********************************************************************
*                                                                     *
*        ISSUE LOCAL OPERATOR SETUP MESSAGE                           *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
DSVMSG   DS    0H                                                    R4
*              THIS LINE DELETED BY APAR NUMBER                @OZ18398
*              THIS LINE DELETED BY APAR NUMBER                @OZ18398
         TM    PCEID,PCERJEID      TEST PROCESSOR TYPE              R41
         BZ    DSVMSG1             BR IF NOT REMOTE                 R41
         TM    MDCTFEAT,DCTPSHDR   IF A SETUP HEADER WILL BE SENT   R41
         BO    DSVEXIT             EXIT IF YES                 @OZ18398
*                                  THIS LINE DELETED BY APAR   @OZ53538
*                                  THIS LINE DELETED BY APAR   @OZ53538
*                                  THIS LINE DELETED BY APAR   @OZ53538
*                                  THIS LINE DELETED BY APAR   @OZ53538
*                                  THIS LINE DELETED BY APAR   @OZ53538
*                                  THIS LINE DELETED BY APAR   @OZ53538
DSVMSG1  TM    DCTPPSW,DCTPPSWO    SETUP MESSAGE REQUIRED...   @OZ18398
         BZ    DSVLUCSB            BRANCH IF NO                @OZ18398
         SPACE 1                                                     R4
        $MID   190                                                   R4
*                                  THIS LINE DELETED BY APAR   @OZ53538
         SPACE 1                                                    R41
         MVC   BUFSTART+7(2),=X'190E' MOVE MESSAGE ID          @OZ18398
         MVC   BUFSTART+9(8),JCTJOBID   MOVE JOB NUMBER              R4
         MVI   BUFSTART+17,C' '         SPACE BEFORE JOB NAME        R4
         MVC   BUFSTART+18(8),JCTJNAME  MOVE JOB NAME                R4
         MVC   BUFSTART+26(54),=CL54' SETUP -- PRINTER1 -- F = FORM -- C
               C = FCBI -- T = UCSI'    MOVE SETUP MESSAGE           R4
         MVC   BUFSTART+36(8),DCTDEVN   MOVE DEVICE NAME             R4
         MVC   BUFSTART+52(4),DCTFORMS  MOVE FORMS ID                R4
         MVC   BUFSTART+64(4),DCTFCB    MOVE FCB ID                  R4
         MVC   BUFSTART+76(4),DCTUCS    MOVE UCS ID                  R4
         LA    R0,73               LENGTH FOR PRINTER MESSAGE        R4
         TM    PCEID,PCEPRSID      TEST PROCESSOR TYPE               R4
         BO    *+8                 BRANCH IF PRINT                   R4
         LA    R0,49               LENGTH FOR PUNCH MESSAGE          R4
         LA    R15,DSVWTOA         POINT TO REG. $DOMACT $WTO LIST   JN
         TM    PCEID,PCERJEID      IF THIS IS A LOCAL DEVICE,        JN
         BZ    DSVWTO                GO ISSUE $WTO                   JN
         ICM   R1,7,DCTDCB+1       REMOTE STILL CONNECTED....  @OZ47975
         BZ    DSVNODOM            NO, GO CHECK FOR $ COMMAND  @OZ47975
         L     R1,MDCTRAT-DCTDSECT(,R1)  FOR THIS REMOTE             JN
         TM    RATCONF-RATDSECT(R1),RATCONFI IF 'INFO' SETUP         JN
         BZ    DSVWTO                NOT REQUESTED, GO ISSUE $WTO    JN
         LA    R15,DSVWTOI         POINT TO 'INFO' $DOMACT $WTO LIST JN
         SPACE 1                                                     JN
DSVWTO  $WTO   BUFSTART+7,(R0),MF=(E,0(,R15)) ISSUE LOCAL SETUP      JN
         TM    PCEID,PCERJEID      IS THIS A REMOTE TERMINAL         R4
         BZ    DSVSTOP             BR IF NOT                         R4
         TM    MDCTSTAT,DCTABORT   TEST RJE STATUS                   R4
         BO    DSVSTART            BR IF ABORTING                    R4
         EJECT                                                       R4
***********************************************************************
*                                                                     *
*        ISSUE REMOTE TERMINAL OPERATOR SETUP MESSAGE                 *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         ST    R1,PPLC             SAVE $DOM CMB ADDRESS             R4
         LA    R0,73               LENGTH FOR PRINTER MESSAGE        R4
         TM    PCEID,PCEPRSID      TEST PROCESSOR TYPE               R4
         BO    SKIP610             BRANCH IF PRINT                   R4
         LA    R0,49               LENGTH FOR PUNCH MESSAGE          R4
SKIP610  L     R1,DCTDCB           ADDRESS LINE DCT                  R4
         L     R1,MDCTRAT-DCTDSECT(,R1) ADDRESS REMOTE CONSOLE       R4
         ICM   R0,2,RATCONRT+1-RATDSECT(R1) GET CONSOLE ROUTE CODE   R4
        $WTO   BUFSTART+7,(R0),JOB=NO,  ISSUE REMOTE SETUP           R4C
               RMT=YES,CLASS=$ACTION,PRI=$ST                         R4
         TM    PCEID,PCEPUSID      TEST PROCESSOR TYPE               R4
         BO    DSVBFLSH            BRANCH IF REMOTE PUNCH            R4
         LM    PC1,PC2,PRCCWEJ     LOAD EJECT CCW                    R4
         BAL   PL,PPPUT            SEND CCW TO REMOTE                R4
         MVC   BUFSTART(9),=C'$HASP190 '  MOVE MESSAGE ID            R4
         LA    PC1,BUFSTART        SET MESSAGE ADDRESS               R4
         LA    PC2,80               AND LENGTH                       R4
         TM    $RUNOPTS,$MSGID     IF MESSAGE IDS REQUIRED,          R4
         BO    SKIP620              BR TO SET COMMAND TYPE           R4
         MVI   BUFSTART+7,C'$'       ELSE RESET MESSAGE ID,          R4
         LA    PC1,BUFSTART+7         MESSAGE ADDRESS,               R4
         LA    PC2,73                  AND MESSAGE LENGTH            R4
SKIP620  ICM   PC1,8,=X'71'        SET COMMAND TYPE                  R4
         BAL   PL,PPPUT2           SEND CCW TO REMOTE                R4
         SPACE 1                                                     R4
***********************************************************************
*                                                                     *
*        ENSURE THAT REMOTE TERMINAL BUFFER IS FLUSHED                *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
DSVBFLSH DS    0H                                                    R4
         ICM   PC1,8,=X'FF'        BUFFER FLUSH COMMAND              R4
         BAL   PL,PPPUT2           SEND CCW TO REMOTE                R4
         L     R1,PPLC             RESTORE $DOM CMB ADDRESS          R4
         L     PC1,PCEDCT          ADDRESS PRINT/PUNCH DCT     @OZ32566
         EJECT                                                       R4
***********************************************************************
*                                                                     *
*        WAIT FOR OPERATOR ACTION                                     *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
DSVSTOP  OI    DCTFLAGS,DCTSTOP    STOP THE DEVICE                   R4
         SPACE 1                                                     R4
DSVWAIT  TM    DCTFLAGS,DCTSTOP    TEST FOR $S DEVICE                R4
         BZ    DSVSTART            BRANCH IF TRUE START              R4
         NI    DCTFLAGS,255-DCTDELET-DCTRSTRT-DCTBKSP RESET
        $WAIT  IO                  WAIT FOR A POST FROM COMM         R4
         B     DSVWAIT             TEST FOR $S DEVICE                R4
DSVSTART DS    0H                                                    R4
         LTR   R1,R1               BRANCH AROUND IF                  R4
         BZ    DSVNODOM              NOTHING TO $DOM                 R4
        $DOM   CMB=(R1)            DELETE THE MESSAGE                R4
DSVNODOM DS    0H                                                    R4
         TM    DCTFLAGS,DCTDELET+DCTRSTRT $C OR $E OR $I             R4
         BNZ   DSVTERM             TERMINATE DATA SET IF SO    @OZ31651
         TM    PCEID,PCERJEID      TEST FOR REMOTE             @OZ31651
         BZ    DSVLUCSB            NO PROBLEM IF LOCAL         @OZ31651
         TM    MDCTSTAT,DCTABORT   IF REMOTE NOT DISCONNECTED  @OZ31651
         BZ    DSVLUCSB            ...THIS POST WAS VALID      @OZ31651
         MVC   DCTFORMS(12),PCEFORM ELSE REVERT TO OLD FORMS   @OZ31651
         NI    DCTPPSW,FF-DCTPPSWU-DCTPPSWB RESET UCS/FCB      @OZ66318
         OC    DCTPPSW,PCCWORK     BITS TO PREVIOUS SETUP      @OZ66318
         B     DSVEXIT             ALL ELSE HAS BEEN SET UP.   @OZ31651
DSVTERM  DS    0H                                              @OZ31651
         LA    R15,4               SET OPERATOR                @OZ75372
         L     R1,PSAVAREA          TERMINATED                 @OZ75372
         ST    R15,4+(15*4)(,R1)     RETURN CODE               @OZ75372
         OI    PPFLAG,PPDELSW+PRDELSW  CAUSE DATA SET TERMINATION    R4
         OC    PDCTFLAG,DCTFLAGS   PROVIDE REASON FOR TERMINATION    R4
         NI    DCTFLAGS,255-DCTDELET-DCTRSTRT-DCTBKSP  RESET DCT     R4
         MVC   DCTFORMS(12),PCEFORM REVERT TO PREVIOUS SETUP         R4
         NI    DCTPPSW,FF-DCTPPSWU-DCTPPSWB RESET UCS/FCB      @OZ66318
         OC    DCTPPSW,PCCWORK     BITS TO PREVIOUS SETUP      @OZ66318
         TM    PCEID,PCERJEID      TEST FOR REMOTE                   R4
         BZ    DSVLUCSB            BR IF NOT                         R4
         TM    MDCTSTAT,DCTABORT   TEST FOR REMOTE ABORTING          R4
         BO    DSVEXIT             EXIT IF YES                       R4
         EJECT                                                       R4
***********************************************************************
*                                                                     *
*        LOAD UCSB IF SUPPORTED BY DEVICE                             *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
DSVLUCSB DS    0H                                                    R4
         NI    DCTPPSW,255-DCTPPSWO $T NOT ALLOWED                   R4
         TM    PCEID,PCEPUSID      TEST PROCESSOR ID                 R4
         BO    DSVEXIT             BRANCH IF PUNCH                   R4
         LM    PC1,PC2,PRCCWEJ     GET EJECT CCW                     R4
         BAL   PL,PPPUT            ADD TO CHAIN                      R4
         BAL   PL,PPWRITE          STAGE WRITE                       R4
         BAL   PL,PPCHECK          CHECK WRITE                       R4
         L     PC1,PCEDCT          ADDRESS PRINT/PUNCH DCT     @OZ32566
         TM    PDEVTYPE+1,X'80'    TEST UCSB OPTION FIELD            R4
         BZ    DSVLFCB             BRANCH IF NOT SUPPORTED           R4
         TM    DCTPPSW,DCTPPSWT    UCSB LOAD REQUIRED                R4
         BZ    DSVLFCB             BRANCH IF NO                      R4
         NI    DCTPPSW,255-DCTPPSWT RESET TRAIN LOAD BIT             R4
         CLC   DCTUCS,=C'0   '     IS UCS = 0 REQUESTED              R4
         BE    DSVLFCB             BRANCH IF YES - SKIP UCS LOAD     R4
         MVC   BUFSTART(4),=X'0001003A' BLDL PARAMETER LIST          R4
         MVC   BUFSTART+4(4),=C'UCS1' IMAGE PREFIX FOR 1403 UCS      R4
         MVC   BUFSTART+8(4),DCTUCS USER UCS IMAGE ID                R4
         CLI   PDEVBYT3,UCB1403    IS THIS A 1403              @OZ40627
         BE    DSVUCS01            BRANCH IF YES                     R4
         MVI   BUFBYT7,C'3'        SET FOR 3203 IMAGE          @OZ40627
         CLI   PDEVBYT3,UCB3203    IS THIS A 3203              @OZ40627
         BE    DSVUCS01            SET CORRECTLY FOR IMAGE TASK@OZ40627
         MVI   BUFBYT7,C'2'        IMAGE PREFIX FOR 3211 UCS   @OZ40627
*                                                              @OZ40627
*        JES2 ONLY INITS 3 TYPES OF IMPACT PRTS GO ON AS 3211  @OZ40627
*                                                              @OZ40627
DSVUCS01 DS    0H                  *                                 R4
         TM    $IMAGECB,X'40'      IS IMAGE LOADER TASK BUSY         R4
         BNO   DSVUCS02            BRANCH IF NO                      R4
        $WAIT  IMAG                WAIT FOR IMAGE TASK TO $$POST     R4
         B     DSVUCS01            TRY AGAIN                         R4
DSVUCS02 DS    0H                  *                                 R4
         MVI   BUFECBCC,X'80'      SET BUFFER ECB AS WAITING         R4
         POST  $IMAGECB,(R3)       POST WITH BUFFER ADDRESS          R4
DSVUCS03 DS    0H                  *                                 R4
        $WAIT  IMAG                WAIT FOR IMAGE TASK TO $$POST     R4
         TM    BUFECBCC,X'7F'      TEST STATUS OF LOAD REQUEST       R4
         BZ    DSVUCS03            BRANCH IF NOT COMPLETE            R4
         BM    DSVUCSNO            BRANCH IF IMAGE NOT FOUND         R4
         L     R15,PRPUUCB         ADDRESS PRINTER UCB               R4
         USING UCBDSECT,R15        ACTIVATE UCB ADDRESSABILITY       R4
         L     R15,UCBXTADR        ADDRESS UCB EXTENSION             R4
         USING UCBUCS,R15          UCB EXTENSION ADDRESSABILITY      R4
        MODESET EXTKEY=ZERO        GET ZERO KEY TO CHANGE THE UCB    R4
         OI    UCBUCSOP,UCBUCSO1+UCBUCSO2  SET DEFAULT UCS AND FOLD R41
         NI    DCTPPSW,255-DCTPPSWU RESET NON-STANDARD UCS FLAG      R4
         TM    BUFSTART,X'80'      STANDARD UCS IMAGE...            R41
         BO    DSVSETU             BR IF YES                        R41
         OI    DCTPPSW,DCTPPSWU    SET NON-STANDARD UCS FLAG         R4
         NI    UCBUCSOP,255-UCBUCSO1 RESET DEFAULT UCS IMAGE FLAG    R4
DSVSETU  MVC   UCBUCSID,DCTUCS     SET UCS IMAGE ID IN UCB          R41
         TM    BUFSTART,X'40'      FOLD THE UCS IMAGE...            R41
         BO    DSVKEY              BR IF YES                        R41
         NI    UCBUCSOP,255-UCBUCSO2  ELSE, RESET UCS FOLD BIT      R41
DSVKEY   MODESET EXTKEY=HASP       RETURN TO NORMAL HASP KEY        R41
         DROP  R15                 SUSPEND UCB EXTENSION BASE        R4
         LM    PC1,PC2,PUNFOLD     3211 UNFOLD CONTROL CCW           R4
         TM    BUFSTART,X'40'      FOLD THE UCS IMAGE...            R41
         BZ    DSVC3211            BR IF NO                         R41
         ICM   PC1,8,=X'43'         ELSE REPLACE CC WITH 3211 FOLD  R41
DSVC3211 CLI   PDEVBYT3,UCB1403    IS DEVICE A 1403            @OZ40627
         BNE   DSVUCS04            IF 1403 SKIP GATE CCW       @OZ40627
         LM    PC1,PC2,PUCSGATE    1403 UCS GATE CCW                 R4
         SPACE 1                                                    R41
DSVUCS04 BAL   PL,PPPUT2           ADD CCW TO CHAIN                 R41
         SPACE 1                                                    R41
         LM    PC1,PC2,PUCSBDC     BLOCK DATA CHECK CCW              R4
         BAL   PL,PPPUT2           ADD CCW TO CHAIN                  R4
         LM    PC1,PC2,PUCSLOAD    LOAD UCSB CCW                     R4
         ALR   PC1,PBUF            ADJUST DATA ADDRESS               R4
         CLI   PDEVTYPE+3,UCB1403  TEST DEVICE TYPE                  R4
         BE    DSVC1403            BR IF 1403                       R41
         ICM   PC2,3,UCSL3203      UCSB LENGTH FOR 3203        @OZ40627
         CLI   PDEVBYT3,UCB3203    TEST FOR 3203               @OZ40627
         BE    DSVUCPUT            BR IF IT IS A 3203          @OZ40627
         ICM   PC2,3,UCSL3211      SET FOR 3211 UCS LEN        @OZ40627
*                                                              @OZ40627
*        JES2 ONLY INITS 3 TYPES OF IMPACT PRTS GO ON AS 3211  @OZ40627
*                                                              @OZ40627
         B     DSVUCPUT            GO ON AS 3211               @OZ40627
UCSL3203 DC    H'304'              3203 UCS LENGTH             @OZ40627
UCSL3211 DC    H'512'              3211 UCS LENGTH             @OZ40627
         SPACE 1                                                    R41
DSVC1403 TM    BUFSTART,X'40'      FOLD THE UCS IMAGE...            R41
         BZ    DSVUCPUT            BR IF NO                         R41
         ICM   PC1,8,=X'F3'         ELSE REPLACE CC WITH 1403 FOLD  R41
         SPACE 1                                                    R41
DSVUCPUT BAL   PL,PPPUT2           ADD CCW TO CHAIN                 R41
         BAL   PL,PPWRITE           INITIATE WRITE                   R4
         BAL   PL,PPCHECK           AND CHECK                        R4
         L     PC1,PCEDCT          ADDRESS PRINT/PUNCH DCT     @OZ32566
         B     DSVLFCB             GO LOAD FCB IMAGE                 R4
DSVUCSNO DS    0H                  *                                 R4
        $MID   180                                                   R4
         MVC   BUFSTART(2),=X'180F' MOVE MESSAGE NUMBER              R4
         MVC   BUFSTART+2(8),DCTDEVN MOVE DEVICE NAME                R4
         MVC   BUFSTART+10(26),=CL26' FCB  IMAGE XXXX NOT FOUND'     R4
         MVC   BUFSTART+11(4),=C'UCSB' MOVE BUFFER ID                R4
         MVC   BUFSTART+22(4),DCTUCS MOVE UCS ID                     R4
        $WTO   BUFSTART,36,JOB=YES, ISSUE BUFFER LOAD FAIL MSG       R4C
               ROUTE=$LOG+$UR,CLASS=$ACTION,PRI=$ST                  R4
         OI    DCTPPSW,DCTPPSWT+DCTPPSWO LOAD UCS + MSG              R4
         B     DSVMSG              GO PRINT SETUP MESSAGE            R4
         EJECT                                                       R4
***********************************************************************
*                                                                     *
*        LOAD FCB IF SUPPORTED BY DEVICE                              *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
DSVLFCB  DS    0H                                                    R4
         TM    DCTPPSW,DCTPPSWC    FCB LOAD REQUIRED                 R4
         BZ    DSVEXIT             BRANCH IF NO                      R4
         NI    DCTPPSW,255-DCTPPSWC RESET CARRIAGE LOAD BIT          R4
         TM    PCEID,PCERJEID      TEST PROCESSOR TYPE              R41
         BZ    DSVFCB00            BR IF NOT REMOTE                 R41
         ICM   PC1,8,=X'FF'        SEND BUFFER FLUSH           @OZ53538
         BAL   PL,PPPUT2            COMMAND TO RTAM            @OZ53538
         L     PC1,PCEDCT          RESTORE DCT                 @OZ53538
         SPACE 1                                                    R41
DSVFCB00 CLI   PDEVTYPE+3,UCB1403  TEST DEVICE TYPE                 R41
         BE    DSVEXIT             BRANCH IF 1403                    R4
         MVC   BUFSTART(4),=X'0001003A' BLDL PARAMETER LIST          R4
         MVC   BUFSTART+4(4),=C'FCB2' IMAGE PREFIX FOR 3211 FCB      R4
         MVC   BUFSTART+8(4),DCTFCB USER FCB IMAGE ID                R4
DSVFCB01 DS    0H                  *                                 R4
         TM    $IMAGECB,X'40'      IS IMAGE LOADER TASK BUSY         R4
         BNO   DSVFCB02            BRANCH IF NO                      R4
        $WAIT  IMAG                WAIT FOR IMAGE TASK TO $$POST     R4
         B     DSVFCB01            TRY AGAIN                         R4
DSVFCB02 DS    0H                  *                                 R4
         MVI   BUFECBCC,X'80'      SET BUFFER ECB AS WAITING         R4
         POST  $IMAGECB,(R3)       POST WITH BUFFER ADDRESS          R4
DSVFCB03 DS    0H                  *                                 R4
        $WAIT  IMAG                WAIT FOR IMAGE TASK TO $$POST     R4
         TM    BUFECBCC,X'7F'      TEST STATUS OF LOAD REQUEST       R4
         BZ    DSVFCB03            BRANCH IF NOT COMPLETE            R4
         BM    DSVFCBNO            BRANCH IF IMAGE NOT FOUND         R4
         SPACE 1                                               @OZ69106
         XC    PCCWORK+4(4),PCCWORK+4 CLEAR WORKAREA           @OZ69106
         LA    R15,BUFSTART+2      POINT TO FCB IMAGE          @OZ69106
         SLR   PL,PL               GET LENGTH                  @OZ69106
         IC    PL,BUFSTART+1        OF FCB IMAGE               @OZ69106
         TM    0(R15),X'80'        TEST FOR INDEX BYTE         @OZ69106
         BO    DSVFCB07            BRANCH IF YES               @OZ69106
         EJECT                                                 @OZ69106
DSVFCB06 LA    R0,1                INITIALIZE R1               @OZ69106
         ICM   R1,1,0(R15)         GET IMAGE BYTE              @OZ69106
         BZ    DSVFCB07            BRANCH IF NULL              @OZ69106
         N     R1,=F'15'           ISOLATE CHANNEL NUMBER      @OZ69106
         BZ    DSVFCB07            BRANCH IF NONE              @OZ69106
         SLL   R0,0(R1)             CREATE                     @OZ69106
         STH   R0,PCCWORK+6          CHANNEL                   @OZ69106
         OC    PCCWORK+4(2),PCCWORK+6 MASK                     @OZ69106
         SPACE 1                                               @OZ69106
DSVFCB07 LA    R15,1(,R15)         INCREMENT IMAGE POINTER     @OZ69106
         BCT   PL,DSVFCB06         LOOP THRU FCB IMAGE         @OZ69106
         TM    PCCWORK+5,X'02'     TEST FOR CH1 DEFINED        @OZ69106
         BO    DSVFCB08            BRANCH IF YES               @OZ69106
         SPACE 1                                               @OZ69106
        $MID   180                                             @OZ69106
         MVC   BUFSTART(2),=X'180F' MOVE MESSAGE NUMBER        @OZ69106
         MVC   BUFSTART+2(8),DCTDEVN MOVE DEVICE NAME          @OZ69106
         MVC   BUFSTART+10(26),=CL26' FCB IMAGE XXXX INVALID'  @OZ69106
         MVC   BUFSTART+21(4),DCTFCB MOVE FCB IMAGE ID         @OZ69106
        $WTO   BUFSTART,36,JOB=YES, ISSUE IMAGE INVALID MSG    @OZ69106C
               ROUTE=$LOG+$UR,CLASS=$ACTION,PRI=$ST            @OZ69106
         OI    DCTPPSW,DCTPPSWC+DCTPPSWO SET LOAD AND MSG REQ  @OZ69106
         B     DSVMSG              GO ISSUE SETUP MSG          @OZ69106
         EJECT                                                 @OZ69106
DSVFCB08 MVC   PFCBMAP,PCCWORK+4   SAVE FCB MASK               @OZ69106
         TM    PCEID,PCERJEID      TEST PROCESSOR TYPE               R4
         BO    DSVFCBRJ            BRANCH IF REMOTE                  R4
         L     R15,PRPUUCB         ADDRESS PRINTER UCB               R4
         USING UCBDSECT,R15        ACTIVATE UCB ADDRESSABILITY       R4
         L     R15,UCBXTADR        ADDRESS UCB EXTENSION             R4
         USING UCBUCS,R15          UCB EXTENSION ADDRESSABILITY      R4
        MODESET EXTKEY=ZERO        GET ZERO KEY TO CHANGE THE UCB    R4
         OI    UCBFCBOP,UCBFCBO1   SET DEFAULT FCB IMAGE FLAG        R4
         TM    BUFSTART,X'80'      IS THIS A STANDARD FCB IMAGE      R4
         BO    SKIP660             BRANCH IF YES                     R4
         NI    UCBFCBOP,255-UCBFCBO1 RESET DEFAULT FCB IMAGE FLAG    R4
SKIP660  MVC   UCBFCBID,DCTFCB     COPY FCB IMAGE ID TO THE UCB      R4
        MODESET EXTKEY=HASP        RETURN TO NORMAL HASP KEY         R4
         DROP  R15                 SUSPEND UCB EXTENSION BASE        R4
DSVFCBRJ DS    0H                                                    R4
         NI    DCTPPSW,255-DCTPPSWB RESET NON-STANDARD FCB FLAG      R4
         TM    BUFSTART,X'80'      IS THIS A STANDARD FCB IMAGE      R4
         BO    SKIP670             BRANCH IF YES                     R4
         OI    DCTPPSW,DCTPPSWB    SET NON-STANDARD FCB FLAG         R4
SKIP670  TM    PRINDEX,X'C0'       SPECIFIC INDEX REQUESTED          R4
         BZ    DSVFCB04            BRANCH IF NO                      R4
         CLC   PRINDEX,BUFSTART+2  IS IMAGE INDEX BEING CHANGED      R4
         BE    DSVFCB04            BRANCH IF NO                      R4
         OI    DCTPPSW,DCTPPSWC    SET LOAD FCB FLAG                 R4
         MVC   BUFSTART+2(1),PRINDEX SET 3211 INDEX VALUE            R4
DSVFCB04 DS    0H                  *                                 R4
         MVC   DCTINDEX,BUFSTART+2 SAVE NEW INDEX VALUE              R4
         LM    PC1,PC2,PFCBLOAD    LOAD FCB CCW                      R4
         ALR   PC1,PBUF            ADJUST DATA ADDRESS               R4
         IC    PC2,BUFSTART+1      GET LENGTH OF FCB IMAGE           R4
         TM    PCEID,PCERJEID      TEST PROCESSOR TYPE               R4
         BZ    SKIP680             BRANCH IF LOCAL                   R4
         ICM   PC1,8,=X'61'        SET SPECIAL RJE FCB CC            R4
SKIP680  BAL   PL,PPPUT2           ADD CCW TO CHAIN                  R4
         BAL   PL,PPWRITE           INITIATE WRITE                   R4
         BAL   PL,PPCHECK           AND CHECK                        R4
         B     DSVEXIT             RETURN TO CALLER                  R4
         EJECT                                                 @OZ69106
DSVFCBNO DS    0H                  *                                 R4
        $MID   180                                                   R4
         MVC   BUFSTART(2),=X'180F' MOVE MESSAGE NUMBER              R4
         MVC   BUFSTART+2(8),DCTDEVN MOVE DEVICE NAME                R4
         MVC   BUFSTART+10(26),=CL26' FCB  IMAGE XXXX NOT FOUND'     R4
         MVC   BUFSTART+22(4),DCTFCB MOVE FCB ID                     R4
        $WTO   BUFSTART,36,JOB=YES, ISSUE BUFFER LOAD FAIL MSG       R4C
               ROUTE=$LOG+$UR,CLASS=$ACTION,PRI=$ST                  R4
         OI    DCTPPSW,DCTPPSWC+DCTPPSWO SET LOAD AND MSG REQD       R4
         B     DSVMSG              GO PRINT SETUP MESSAGE            R4
         SPACE 2                                                     R4
DSVEXIT  DS    0H                                                    R4
         PRETURN ,                 RESTORE REGS AND RETURN     @G38ESBB
*        DELETED                                               @G38ESBB
         SPACE 1                                               @G38ESBB
         DROP  PC1                 SUSPEND DCT ADDRESSABILITY        R4
         EJECT                                                       JN
*        PARM LISTS FOR LOCAL SETUP $WTO'S                          R41
         SPACE 4                                                    R41
         DS    0F                                                   R41
DSVWTOA $WTO   JOB=NO,             ACTION-TYPE MESSAGE,             R41C
               ROUTE=$LOG+$UR,      DISPLAYABLE VIA '$DO'           R41C
               CLASS=$DOMACT,                                       R41C
               PRI=$ST,                                             R41C
               MF=L                                                 R41
         SPACE 3                                                    R41
DSVWTOI $WTO   JOB=NO,             INFO-TYPE MESSAGE,               R41C
               ROUTE=$LOG+$UR,      DISPLAYABLE VIA '$DO'           R41C
               CLASS=$DOMACT+$NORMAL,                               R41C
               PRI=$ST,                                             R41C
               MF=L                                                 R41
         SPACE 2                                               @OZ69106
         LTORG                                                 @OZ69106
         TITLE ' HASP PRINT/PUNCH SERVICE -- 3800 DEVICE SETUP'      R4
***************************************************************@OZ75372
*                                                              @OZ75372
*        3800 -- DEVICE SETUP VERIFICATION                     @OZ75372
*                                                              @OZ75372
*        ENTRY:  SETUP PARAMETER LIST IN PCE WORK AREA         @OZ75372
*        EXIT:   R15 = --- RETURN CODE                         @OZ75372
*                    = 0   NORMAL RETURN                       @OZ75372
*                    = 4   OPERATOR TERMINATED                 @OZ75372
*                    = 8   3800 JAM OR CANCEL KEY              @OZ75372
*                    = 12  3800 ERROR IN SETUP                 @OZ75372
*                                                              @OZ75372
***************************************************************@OZ75372
         SPACE 1                                               @OZ75372
P3800DSV DS    0H                  ENTRY TO 3800 DEVICE SETUP        R4
         PSAVE ALL                 SAVE CALLER'S REGISTERS     @G38ESBB
         SPACE 1                                                     R4
         USING SPPARM-(BUFSTART-BUFDSECT),PBUF SPPARM ADDRESSABILITY R4
         SPACE 1                                                     R4
P3800DS  DS    0H                                              @G38ESBB
         LR    BASE2,R15           ESTABLISH LOCAL                   R4
         USING P3800DSV,BASE2       ADDRESSABILITY                   R4
         USING DCTDSECT,PC1        ESTABLISH DCT ADDRESSABILITY      R4
         L     PC1,PCEDCT          ADDRESS 3800 DCT            @OZ32566
         STCM  PC1,7,BUFDCT+1-BUFDSECT(PBUF)   PLACE DCT ADDR IN BUF R4
         USING UCB3800X,PC2        ESTABLISH 3800 UCB                R4
         L     PC2,PRPUUCB             EXTENSION                     R4
         L     PC2,UCBXTADR-UCBDSECT(,PC2) ADDRESSABILITY            R4
         L     JCT,PJCTBUF         ESTABLISH JCT                     R4
         LTR   JCT,JCT             JCT READ...                 @G38ESBB
         BNZ   *+8                 YES, BRANCH                 @G38ESBB
         LA    JCT,JCT             SHOW NON-ZERO JCT FOR I/O   @G38ESBB
         USING JCTDSECT,JCT         ADDRESSABILITY                   R4
         EJECT                                                 @OZ75372
*                                                                    R4
*        CLEAR SETPRT PARMLIST AND WORKAREA - SAVE DCT STATUS        R4
*                                                                    R4
         SPACE 1                                               @OZ75372
         SLR   R15,R15             INITIALIZE                  @OZ75372
         L     PL,PSAVAREA          3800 SETUP                 @OZ75372
         ST    R15,4+(15*4)(,PL)     RETURN CODE               @OZ75372
         XC    SPPARM(SPWSAVE1-SPPARM),SPPARM  INITIALIZE AREA       R4
         OI    DCTPPSW,DCTPPSWO    SET OPERATOR $T ALLOWED     @OZ78585
         TM    DCTPPSW2,DCTNINIT   DOES 3800 NEED INITIALIZATION...  R4
         BZ    SPRXMIT             BR IF NO                          R4
         OI    SPWFLAG,SPWINIT+SPWSETP  TELL SETPRT                  R4
         NI    DCTPPSW2,255-DCTNINIT  RESET UNTIL $DRAINED           R4
         MVC   DCTCHAR1,DCTUCS     PRESET                           R41
         MVI   DCTCHAR2,C'*'        DCT FIELDS                      R41
         MVC   DCTCHAR2+1(5*4-1),DCTCHAR2  (CHARS,FLASH,MODIFY)     R41
         XC    PREVCPYS(2),PREVCPYS  AND PREV. START AND # OF COPIES R4
         SPACE 1                                                     R4
SPRXMIT  DS    0H                                                    R4
         TM    SPFLAG,SPREXMIT     TEST FOR RE-TRANSMISSION          R4
         BO    SPRRXMIT            BR IF YES                         R4
         MVC   SPWFORMS(3*4),DCTFORMS SAVE FORMS, FCB, DEFAULT CHAR1 R4
         MVC   SPWCHAR1(4*4),DCTCHAR1 SAVE CHAR1,2,3,4 VALUES        R4
         TM    DCTPPSW2,DCTNIBRS   SAVE DCT                          R4
         BZ    SKIP690              BURSTER                          R4
         OI    SPWFLAG,SPWDCTB       STATUS                          R4
         SPACE 3                                                     R4
SKIP690  CLC   DCTFORMS,SPFORMS    TEST FOR FORMS CHANGE             R4
         BE    SPRFCB              BR IF NO                          R4
         OI    SPWFLAG,SPWWTO      INDICATE SETUP MSG NEEDED         R4
         MVC   DCTFORMS,SPFORMS    MOVE IN NEW FORMS ID              R4
         SPACE 1                                                     R4
SPRFCB   DS    0H                                                    R4
         CLI   SPFCB,C'*'          TEST FOR DEFAULT REQUEST          R4
         BNE   SKIP700             BR IF NO                          R4
         MVC   SPFCB,PRDFCB        ELSE, USE INSTALLATION DEFAULT    R4
SKIP700  CLC   DCTFCB,SPFCB        TEST FOR FCB CHANGE               R4
         BE    SPRCHARS            BR IF NO                          R4
         MVC   DCTFCB,SPFCB        MOVE IN NEW FCB IMAGE ID          R4
         EJECT                                                       R4
*        SETUP CHARACTER SETS (CHARS) REQUEST                       R41
*                                                                   R41
*        THIS SECTION BUILDS THE CHAR SET REQUEST USING THE         R41
*        SETUP PARMLIST (SPCHAR1-4.) IF ROOM PERMITS, THE DE-       R41
*        FAULT CHAR (DCTUCS) IS ADDED TO THE LIST.  FOR EACH        R41
*        CHAR, DCTCHAR'S FIELDS ARE SEARCHED TO DETERMINE IF IT     R41
*        IS ALREADY LOADED. IF NOT, THE NEW REQUESTS REPLACE        R41
*        THE OLD SETUP. IF ALREADY LOADED, THE CCW OP-CODE TO       R41
*        SELECT THE CORRESPONDING TANSLATE TABLE IS RECORDED        R41
*        IN THE 'PXTABCCW' TABLE ENTRY FOR THAT CHAR.               R41
*        THE FINAL TABLE REPRESENTS A MAPPING OF THE REQUESTED      R41
*        CHARACTER SETS TO THEIR ACTUAL POSITIONS IN THE 3800,      R41
*        ELIMINATING UNNECESSARY RE-LOADS DUE TO CHARACTER          R41
*        SET POSITIONS.                                             R41
         SPACE 1                                                    R41
SPRCHARS MVC   PXTABCCW,SPRXCCWS   INITIALIZE CCW OP-CODE TABLE     R41
         TM    SPFLAG,SPSEP        SETUP FOR SEPARATORS...          R41
         BZ    SPRCNSEP            BR IF NOT                        R41
         MVI   PRMAXTRC,0          ONLY USE 1 CHAR SET              R41
         BAL   PL,SPRTDFLT         DEFAULT CHAR SET LOADED...       R41
         BNZ   SPRCNDEF            BR IF NOT                        R41
         MVC   PXTABCCW(1),0(PW)    ELSE SET CCW FOR DEFAULT        R41
         B     SPRFLASH              AND BR TO CONTINUE             R41
         SPACE 1                                                    R41
SPRCNDEF TM    DCTPPSW2,DCTSEPNL   MUST LOAD DEFAULT FOR SEP...     R41
         BZ    SPRCHDEF            BR IF YES                        R41
         B     SPRFLASH             ELSE USE 1ST CHAR SET           R41
         SPACE 1                                                    R41
SPRCNSEP CLI   SPCHAR1,C'*'        WAS CHARS SPECIFIED...           R41
         BNE   SPRCH1              BR IF YES                        R41
         SPACE 1                                                    R41
SPRCHDEF MVC   SPCHAR1,DCTUCS      USE JES2 PRINTER DEFAULT         R41
         MVC   SPCHAR2,=C'****'    CLEAR CHAR2                 @OZ28970
         MVC   SPCHAR3(2*4),SPCHAR2 CHAR3 AND CHAR4            @OZ28970
         SPACE 1                                                    R41
SPRCH1   SLR   R1,R1               ONLY BUILD                       R41
         IC    R1,PRMAXTRC          ENTRIES FOR                     R41
         LA    R1,1(,R1)             REQUESTED CHARS                R41
         LA    PL,PXTABCCW         ADDR OF CCW TABLE                R41
         LA    R14,SPCHAR1         ADDR OF CHARS REQUEST            R41
         SPACE 1                                                    R41
SPRCH2   LA    R15,DCTCHAR1        ADDR OF CURRENT CHARS LOADED     R41
         LA    R0,4                SET LOOP COUNTER                 R41
         SPACE 1                                                    R41
SPRCH3   CLC   0(4,R14),0(R15)     THIS CHAR ALREADY LOADED...      R41
         BE    SPRCH4              BR IF YES                        R41
         LA    R15,4(,R15)          ELSE CHECK WITH NEXT            R41
         BCT   R0,SPRCH3             CHAR ALREADY LOADED            R41
         SPACE 1                                                    R41
         MVC   PXTABCCW,SPRXCCWS   NOT LOADED ALREADY -- SET TO     R41
         MVC   DCTCHAR1(4*4),SPCHAR1  LOAD ALL REQUESTED CHARS      R41
         B     SPRCH5                  AND BR TO CONTINUE           R41
         EJECT                                                      R41
SPRCH4   LNR   R15,R0              LOADED ALREADY -- DETERMINE      R41
         LA    R15,SPRXCCWS+4(R15)  ACTUAL POSITION OF CHAR AND     R41
         MVC   0(1,PL),0(R15)        INSERT CORRECT CCW OP-CODE     R41
         LA    R14,4(,R14)         INCR TO NEXT REQUESTED           R41
         LA    PL,1(,PL)            CHAR AND GO DETERMINE           R41
         BCT   R1,SPRCH2             IF IT IS ALREADY LOADED        R41
         SPACE 1                                                    R41
SPRCH5   IC    R1,PRMAXTRC         GET NUMBER OF CHARACTER          R41
         LA    R1,1(,R1)            SETS REQUESTED                  R41
         CLM   R1,1,UCBCGMNO       POTENTIAL WCGM'S LEFT...         R41
         BNL   SPRFLASH            BR IF NO                         R41
         BAL   PL,SPRTDFLT         DEFAULT CHAR SET LOADED...       R41
         BZ    SPRFLASH            BR IF YES.   ELSE,               R41
         MVC   PXTABCCW,SPRXCCWS   INIT CCW OP-CODE TABLE      @OZ58409
         MVC   DCTCHAR1(4*4),SPCHAR1  ADD CHARS REQ'D AND      @OZ58409
         SLL   R1,2                ADD DEFAULT                      R41
         LA    R1,DCTCHAR1(R1)      TO LIST                         R41
         MVC   0(4,R1),DCTUCS        OF CHARS                       R41
         OI    SPWFLAG,SPWDEFLT    SET INDICATION                   R41
         OI    SPPFLAG1,SPPBOMSG   SETPRT BYPASS WCGM MSG      @G38ESBB
         SPACE 1                                               @G38ESBB
SPRFLASH DS    0H                                                    R4
         LH    PW,PCCWLAST         GET OFFSET TO PCIE          @OZ45078
         AL    PW,POUTCCWA         ADD CCW AREA BASE           @OZ45078
         LA    PW,PCIESIZE(,PW)    ADDRESS BFW                 @OZ45078
         USING BFWDSECT,PW         PROVIDE BFW ADDRESSABILITY  @OZ45078
         SPACE 1                                               @OZ45078
         SLR   R15,R15             CLEAR WORK REGISTER         @OZ45078
         TM    SPWFLAG,SPWWTO      CHANGE IN FORMS...          @OZ45078
         BZ    *+8                 NO, BYPASS SETTING          @OZ45078
         LA    R15,BFWCCM          INDICATE CHECK CONSOLE MSG  @OZ45078
         SPACE 1                                               @OZ45078
         CLI   SPFLASH,C'*'        TEST FOR FLASH SPECIFIED          R4
         BNE   SPRF1               BR IF YES                         R4
         CLI   DCTFLASH,C'*'       TEST FOR FLASH CURRENTLY ACTIVE   R4
         BE    SPRBURST            BR IF NO                          R4
         B     SPRF2               ELSE, GO TO RESET DCT             R4
SPRF1    CLC   DCTFLASH,SPFLASH    TEST FOR CHANGE                   R4
         BE    SPRBURST            BR IF NO                          R4
         OI    SPWFLAG,SPWWTO      INDICATE SETUP MSG NEEDED         R4
         LTR   R15,R15             CHANGE IN FORMS ALSO...     @OZ45078
         BNZ   *+8                 YES, BYPASS FLASH SETTING   @OZ45078
         LA    R15,BFWFLASH        INDICATE FLASH CHANGE       @OZ45078
         SPACE 1                                               @OZ45078
SPRF2    MVC   DCTFLASH,SPFLASH    UPDATE DCT WITH NEW REQUEST       R4
         SPACE 1                                                     R4
SPRBURST DS    0H                                                    R4
         SPACE 1                                               @OZ45078
*        DELETED                                               @OZ45078
*        DELETED                                               @OZ45078
*        DELETED                                               @OZ45078
*        DELETED                                               @OZ45078
*        DELETED                                               @OZ45078
*        DELETED                                               @OZ45078
*        DELETED                                               @OZ45078
*        DELETED                                               @OZ45078
         SPACE 1                                               @G38ESBB
         TM    SPFLAG,SPBURST      TEST FOR BURST=YES SPECIFIED      R4
         BZ    SPRB1               BR IF NOT - BURSTER NOT WANTED    R4
         TM    DCTPPSW2,DCTNIBRS   TEST FOR DEVICE ALREADY BURSTING  R4
         BO    SPRMODFY            BR IF YES                         R4
         OI    DCTPPSW2,DCTNIBRS   ELSE, MOVE IN B=Y STATUS          R4
         LTR   R15,R15             CHANGE IN FORMS OR FLASH... @G38ESBB
         LA    R15,BFWCCM            CHECK CONSOLE MESSAGE     @OZ45078
         BNZ   SPRB2               YES, BYPASS BURSTER SETTING @G38ESBB
         LA    R15,BFWNBB          INDICATE NON-BURST TO BURST @G38ESBB
         TM    UCBOPTNS,UCBBRSTR   BURSTER INSTALLED...        @G38ESBB
         BO    SPRB2               YES, BYPASS RESETTING       @G38ESBB
         LA    R15,BFWCCM          NO, RESET TO CHECK CON MSG  @G38ESBB
         B     SPRB2                AND GO FLAG FOR WTO              R4
SPRB1    TM    DCTPPSW2,DCTNIBRS   TEST BURST STATUS                 R4
         BZ    SPRMODFY            BR IF NOT ACTIVE                  R4
         NI    DCTPPSW2,255-DCTNIBRS  ELSE, MOVE IN B=N STATUS       R4
         LTR   R15,R15             CHANGE IN FORMS OR FLASH... @G38ESBB
         LA    R15,BFWCCM            CHECK CONSOLE MESSAGE     @OZ45078
         BNZ   SPRB2               YES, BYPASS BURSTER SETTING @G38ESBB
         LA    R15,BFWBNB          INDICATE BURST TO NON-BURST @G38ESBB
         TM    UCBOPTNS,UCBBRSTR   BURSTER INSTALLED...        @G38ESBB
         BO    SPRB2               YES, BYPASS RESETTING       @G38ESBB
         LA    R15,BFWCCM          NO, RESET TO CHECK CON MSG  @G38ESBB
SPRB2    OI    SPWFLAG,SPWWTO      INDICATE SETUP MSG NEEDED         R4
         SPACE 1                                                     R4
SPRMODFY DS    0H                                                    R4
         STC   R15,BFWSTCD         SET DISPLAY STATUS CODE     @G38ESBB
         CLI   SPMODF,C'*'         TEST FOR COPY MOD SPECIFIED       R4
         BNE   SPRM1               BR IF YES                         R4
         CLI   DCTMODF,C'*'        TEST FOR COPY MOD ALREADY ACTIVE  R4
         BE    SPRMSG              BR IF NO                          R4
         OI    SPWFLAG,SPWCLRM     INDICATE COPY MOD MUST BE CLEARED R4
SPRM1    MVC   DCTMODF,SPMODF      MOVE IN NEW COPY MODIFICATION ID  R4
         SPACE 1                                               @G38ESBB
         DROP  PW                  DROP BFW ADDRESSABILITY     @G38ESBB
         EJECT                                                       R4
*                                                                    R4
*        ISSUE OPERATOR SETUP MSG FOR FORMS(F) - FLASH(O) - BURST(B) R4
*                                                                    R4
         SPACE 1                                                     R4
SPRMSG   DS    0H                                                    R4
         TM    SPWFLAG,SPWWTO      TEST FOR SETUP MSG NEEDED         R4
         BZ    SPRLFCB             BR IF NO                          R4
         LM    PC1,PC2,PCCWCP      ISSUE                       @G38ESBB
         BAL   PL,PPPUT              CLEARPRINT CCW            @G38ESBB
         BAL   PL,PPWRITE             TO EMPTY PAGE BUFFER     @G38ESBB
         BAL   PL,PPCHECK              BEFORE STATUS CODE      @G38ESBB
         LM    PC1,PC2,PCCWXORD    GET EXECUTE ORDER CCW       @G38ESBB
         IC    PC2,=AL1(L'BFWDSC)  CHANGE LENGTH FROM 2 TO 3   @G38ESBB
         LH    R1,PCCWLAST         RESTORE                     @G38ESBB
         AL    R1,POUTCCWN          BFW                        @G38ESBB
         LA    R1,PCIESIZE(,R1)      ADDRESS                   @G38ESBB
         LA    R1,BFWDSC-BFWDSECT(,R1) INDICATE DISPLAY        @G38ESBB
         ALR   PC1,R1                   STATUS CODE ORDER      @G38ESBB
         BAL   PL,PPPUT            CALL PPPUT TO ADD CCW       @G38ESBB
         BAL   PL,PPWRITE          SCHEDULE I/O                @G38ESBB
         L     PC1,PCEDCT          RESTORE DCT ADDRESS         @G38ESBB
         L     PC2,PRPUUCB         RESTORE UCB                 @G38ESBB
         L     PC2,UCBXTADR-UCBDSECT(,PC2) EXTENSION ADR       @G38ESBB
         SPACE 1                                               @G38ESBB
SPRMSG1  DS    0H                                                    R4
        $MID   190                                                   R4
         MVC   SPWMSG+7(2),=X'190E'     MOVE MESSAGE NUMBER          R4
         MVC   SPWMSG+9(8),JCTJOBID     MOVE JOB NUMBER              R4
         MVI   SPWMSG+17,C' '           MOVE SPACE BEFORE JOBNAME    R4
         MVC   SPWMSG+18(8),JCTJNAME    MOVE JOBNAME                 R4
         MVC   SPWMSG+26(52),=CL52' SETUP -- PRINTERN -- F = FORM -- O C
               = FLSH -- B = N'         MOVE SETUP MESSAGE           R4
         MVC   SPWMSG+36(8),DCTDEVN     MOVE DEVICE NAME             R4
         MVC   SPWMSG+52(4),DCTFORMS    MOVE FORMS ID                R4
         MVC   SPWMSG+64(4),DCTFLASH    MOVE FLASH-FRAME ID          R4
         TM    DCTPPSW2,DCTNIBRS        TEST FOR B=N                 R4
         BZ    *+8                      BR IF YES                    R4
         MVI   SPWMSG+76,C'Y'           MOVE IN B=Y                  R4
         DROP  JCT                 SUSPEND JCT ADDRESSABILITY        R4
         SPACE 1                                                     R4
         $WTO  SPWMSG+7,70,JOB=NO, ISSUE PRINTER SETUP MSG     @OZ58707C
               ROUTE=$LOG+$UR,CLASS=$DOMACT,PRI=$ST                  R4
         NI    SPWFLAG,255-SPWWTO  RESET WTO FLAG                    R4
         SPACE 1                                                     R4
SPRSTOP  OI    DCTFLAGS,DCTSTOP    STOP ($Z) THE PRINTER             R4
         EJECT                                                       R4
*                                                                    R4
*        WAIT FOR $S COMMAND FROM OPERATOR                           R4
*                                                                    R4
SPRWAIT  DS    0H                                                    R4
         TM    DCTFLAGS,DCTSTOP    TEST FOR $S PRINTER               R4
         BZ    SPRSTART            BR IF START COMMAND ISSUED        R4
         NI    DCTFLAGS,FF-DCTDELET-DCTRSTRT-DCTBKSP RESET FLG @G38ESBB
        $WAIT  IO                  WAIT FOR A POST FROM COMM         R4
         B     SPRWAIT             GO CHECK FOR $S                   R4
         SPACE 1                                                     R4
SPRSTART DS    0H                                                    R4
        $DOM   CMB=(R1)            DELETE SETUP MESSAGE              R4
         SPACE 1                                               @OZ75372
         TM    SPWFLAG,SPWPREV     REVERT TO PREVIOUS SETUP..  @OZ75372
         BNO   SPRTCJP             BRANCH IF NOT               @OZ75372
         TM    DCTFLAGS,DCTDELET+DCTRSTRT  TERMINATION CMD...  @OZ75372
         BZ    SPRLFCB             BRANCH IF NOT               @OZ75372
         NI    DCTFLAGS,FF-DCTDELET-DCTRSTRT-DCTBKSP  RESET    @OZ75372
         MVC   DCTCHAR1,=C'****'     RESET                     @OZ75372
         MVC   DCTCHAR2(3*4),DCTCHAR1 CHARS,                   @OZ75372
         MVC   DCTFORMS,DCTCHAR1       FORMS,                  @OZ75372
         MVC   DCTFCB,=C'****'          FCB,                   @OZ75372
         MVC   DCTFLASH,=C'****'         FLASH,                @OZ75372
         MVC   DCTMODF,=C'****'           AND MODIFY           @OZ75372
         NI    DCTPPSW,FF-DCTNIBRS RESET BURST STATUS          @OZ75372
         OI    DCTPPSW2,DCTNINIT   IND INITIALIZATION NEEDED   @OZ75372
         LA    R15,12              INDICATE                    @OZ75372
         L     R1,PSAVAREA          SETUP ERROR                @OZ75372
         ST    R15,4+(15*4)(,R1)     RETURN CODE               @OZ75372
         B     SPREXIT2            RETURN TO CALLER            @OZ75372
         SPACE 1                                               @OZ75372
SPRTCJP  L     R1,PCEJQE           TEST FOR $CJ,P              @OZ75372
         TM    JQEFLAGS-JQEDSECT(R1),QUEOPCAN+QUEPURGE         @OZ75372
         BNO   SPROPTST            BRANCH IF NOT               @OZ75372
         OI    DCTFLAGS,DCTDELET   ELSE, SIMULATE $CPRT        @OZ75372
         SPACE 1                                               @OZ75372
SPROPTST TM    DCTFLAGS,DCTDELET+DCTRSTRT  $C, $E, OR $I...    @OZ75372
         BZ    SPRLFCB             BRANCH IF NOT               @OZ75372
         LA    R15,4               SET OPERATOR                @OZ75372
         L     R1,PSAVAREA          TERMINATED                 @OZ75372
         ST    R15,4+(15*4)(,R1)     RETURN CODE               @OZ75372
         EJECT                                                 @OZ75372
SPRNTCJP OI    PPFLAG,PPDELSW+PRDELSW  CAUSE TERMINATION       @OZ75372
         OC    PDCTFLAG,DCTFLAGS   PROVIDE TERMINATION REASON       R41
         NI    DCTFLAGS,255-DCTDELET-DCTRSTRT-DCTBKSP RESET    @OZ18409
         OI    PPFLAG3,PP3800R     SET REPO IN PROGRESS INDIC  @OZ45081
         TM    SPFLAG,SPCMD        SETUP FOR CMD MSG...        @OZ75372
         BNZ   SPREVERT            BRANCH IF YES               @OZ75372
         L     PW,PQHADR           ADDRESS PQH                 @G38ESBB
         USING PQHDSECT,PW         PROVIDE PQH ADDRESSABILITY  @G38ESBB
         LA    R1,PQHFIRST-(PQENEXT-PQEDSECT) ADDRESS PQE0     @G38ESBB
         USING PQEDSECT,R1         PROVIDE PQE ADDRESSABILITY  @G38ESBB
         LR    R15,R1              ADDRESS PQE0                @G38ESBB
         SPACE 1                                               @G38ESBB
SPRPREV  L     R1,PQEPREV          GET PREVIOUS PQE            @G38ESBB
         CLR   R1,R15              END OF PPQ...               @G38ESBB
         BE    SPREVERT            YES, BRANCH                 @G38ESBB
         CLI   PQETYPE,PQEC        CHECKPOINT PQE...           @G38ESBB
         BNE   SPRPREV             NO, LOOP BACK               @G38ESBB
         L     PL,PQECPQED         ADDRESS DATA SET PQE        @G38ESBB
         CLC   PQEDWJOE-PQEDSECT(,PL),PWKJOE DOES JOB HAVE ANY @G38ESBB
         BNE   SPREVERT                       PQE'S..NO,BRANCH @G38ESBB
         OI    PQECFLAG,PQECLPG    SET LAST PAGE OF DATA SET   @G38ESBB
         NI    PQECFLAG,FF-PQECBSP RESET STACKED INDICATOR     @OZ75372
         OI    PQEDFLAG-PQEDSECT(PL),PQEDLAST LAST DS OF JOE   @G38ESBB
         TM    PDCTFLAG,DCTDELET   IS COMMAND $C...            @G38ESBB
         BZ    SPRCHKIN            NO, GO CHECK $I $E          @G38ESBB
         OI    PQEDFLAG-PQEDSECT(PL),PQEDCAN SET DS CANCELLED  @G38ESBB
         B     SPRESET             GO RESET COMMAND FLAG       @G38ESBB
         SPACE 1                                               @OZ75372
SPRCHKIN TM    PDCTFLAG,DCTBKSP    IS COMMAND $I...            @G38ESBB
         BZ    SPRECMD             NO, GO PROCESS $E           @G38ESBB
         OI    PQEDFLAG-PQEDSECT(PL),PQEDINT SET DS INTERRUPT  @G38ESBB
         IC    R14,PQHCMDCT        INCREMENT                   @G38ESBB
         LA    R14,1(,R14)          COUNT OF DEFERRED          @G38ESBB
         STC   R14,PQHCMDCT          $I COMMANDS               @G38ESBB
         B     SPRESET             GO RESET COMMAND FLAG       @G38ESBB
         SPACE 1                                               @G38ESBB
SPRECMD $#ADD  WORK=PWKJOE,CHAR=PCHJOE REQUEUE JOE FOR $E      @G38ESBB
         BZ    SPRESET             BRANCH IF SUCCESSFUL        @G38ESBB
         OI    PQEDFLAG-PQEDSECT(PL),PQEDRST DEFER $E CMD      @G38ESBB
         IC    R14,PQHCMDCT        INCREMENT                   @G38ESBB
         LA    R14,1(,R14)          DEFERRED                   @G38ESBB
         STC   R14,PQHCMDCT          COMMAND COUNT             @G38ESBB
         SPACE 1                                               @G38ESBB
SPRESET  OI    PQHFLAG,PQHDSVC     INDICATE RESET NEEDED       @OZ45081
         DROP  R1,PW               DROP PQE,PQH ADDRESSABILITY @G38ESBB
         EJECT                                                 @OZ75372
*                                                                    R4
*        IF PRINTER CANCELLED -- REVERT TO PREVIOUS SETUP           R41
*                                                                    R4
         SPACE 1                                               @G38ESBB
SPREVERT MVC   DCTFORMS(3*4),SPWFORMS RESET FORMS-FCB-DEF CHAR @G38ESBB
         MVC   DCTCHAR1(4*4),SPWCHAR1  RESET CHAR1,2,3,4 VALUES      R4
         MVC   DCTFLASH,=C'****'   CLEAR FLASH AND                   R4
         MVC   DCTMODF,=C'****'     COPY MODIFICATION                R4
         OI    SPWFLAG,SPWCLRM     CLEAR COPYMOD               @OZ50544
         OI    SPWFLAG,SPWPREV     INDICATE PREVIOUS SETUP     @OZ75372
         NI    DCTPPSW2,255-DCTNIBRS     RESET                       R4
         TM    SPWFLAG,SPWDCTB            THE                        R4
         BZ    SPRLFCB                     BURSTER                   R4
         OI    DCTPPSW2,DCTNIBRS            STATUS                   R4
         B     SPRLFCB             CONTINUE 3800 SETUP         @OZ69106
         EJECT                                                       R4
***********************************************************************
*                                                                     *
*        LOAD FCB FOR CCW VALIDATION AND COMMAND REPOSITIONING        *
*                                                                     *
***********************************************************************
         SPACE 1                                               @OZ69106
         USING PQHDSECT,PW                                     @OZ69106
SPRFCBM  L     PW,PQHADR           GET PQH ADDRESS             @OZ69106
         TM    PPFLAG3,PP3800R     REPOSITIONING CMD...        @OZ69106
         BZ    SPRTFCB             BRANCH IF NOT               @OZ69106
         CLC   PQHMAPV,$ZEROS      ANY MAPPING NEEDED...       @OZ69106
         BNE   SPRLFCB1            BRANCH IF YES               @OZ69106
         SPACE 1                                               @OZ69106
SPRTFCB  TM    SPWFLAG,SPWSETP     WAS SETPRT CALLED...        @OZ69106
         BZ    SPREXIT1            BRANCH IF NOT               @OZ69106
         TM    SPWFLAG,SPWINIT     PRINTER REINITIALIZED...    @OZ69106
         BO    SPRLFCB1            BRANCH IF YES               @OZ69106
         CLC   SPPFCB,$ZEROS       FCB CHANGE...               @OZ69106
         BE    SPREXIT1            BRANCH IF NOT               @OZ69106
         SPACE 1                                               @OZ69106
SPRLFCB1 $GETBUF WAIT=YES          GET BUFFER FOR FCB LOAD     @OZ69106
         ST    R1,PQHFCB           SAVE BUFFER ADDRESS         @OZ69106
         USING BUFDSECT,R1         FCB BUFFER ADDRESSABILITY   @OZ69106
         ST    R4,BUFDCT           INIT DCT ADDR IN BUFFER     @OZ69106
         CLI   UCBFCBNM,X'40'      HDWR DEFAULT LOADED...      @OZ69106
         BE    PFCBBLD             BRANCH IF YES               @OZ69106
         MVC   BUFSTART(4),=X'0001003A' CREATE BLDL LIST       @OZ69106
         MVC   BUFSTART+4(4),=C'FCB3'    FCB PREFIX            @OZ69106
         MVC   BUFSTART+8(4),UCBFCBNM    FCB NAME              @OZ69106
         SPACE 1                                               @G38ESBB
PIMGBUSY TM    $IMAGECB,X'40'      IS IMAGE LOADER TASK BUSY   @G38ESBB
         BNO   PIMGPOST            BR IF NOT                   @G38ESBB
        $WAIT  IMAG                WAIT FOR IMAGE TASK $$POST  @G38ESBB
         B     PIMGBUSY            TRY AGAIN                   @G38ESBB
         SPACE 1                                               @G38ESBB
PIMGPOST L     R1,PQHFCB           ADDRESS BUFFER FOR FCB      @G38ESBB
         MVI   BUFECBCC,X'80'      SET BUFFER ECB AS WAITING   @G38ESBB
         POST  $IMAGECB,(R1)       POST WITH BUFFER ADDRESS    @G38ESBB
         EJECT                                                 @OZ69106
PIMGWT  $WAIT  IMAG                WAIT FOR IMAGE TASK $$POST  @G38ESBB
         L     R1,PQHFCB           RESTORE FCB BUFFER ADDRESS  @G38ESBB
         TM    BUFECBCC,X'7F'      TEST STATUS OF LOAD REQUEST @G38ESBB
         BZ    PIMGWT              BR IF NOT COMPLETE          @G38ESBB
         BO    SPRLFCB2            BRANCH IF SUCCESSFUL        @OZ69106
         SPACE 1                                               @OZ69106
        $MID   180                                             @OZ69106
         MVC   BUFSTART(2),=X'180F' MOVE MESSAGE NUMBER        @OZ69106
         MVC   BUFSTART+2(8),DCTDEVN MOVE DEVICE NAME          @OZ69106
         MVC   BUFSTART+10(25),=C' FCB IMAGE XXXX NOT FOUND'   @OZ69106
         MVC   BUFSTART+21(4),UCBFCBNM MOVE FCB IMAGE ID       @OZ69106
         SPACE 1                                               @OZ69106
        $WTO   BUFSTART,35,JOB=YES, ISSUE LOAD FAILED MSG      @OZ69106C
               ROUTE=$LOG+$UR,CLASS=$DOMACT,PRI=$ST            @OZ69106
         B     SPRLFCB5            BR TO FREE FCB BUFFER       @OZ69106
         SPACE 1                                               @OZ69106
         USING PFCB,PL             SET FCB ADDRESSABILITY      @OZ69106
SPRLFCB2 LA    PL,BUFSTART         POINT TO 3800 FCB           @OZ69106
         LH    R15,PFCBLENG        GET FCB LENGTH              @OZ69106
         LA    PL,PFCBSTRT         POINT TO IMAGE              @OZ69106
         XC    PCCWORK+4(4),PCCWORK+4 CLEAR WORKAREA           @OZ69106
         SLR   R14,R14             CLEAR WORK REGISTER         @OZ69106
         SPACE 1                                               @OZ69106
SPRLFCB3 LA    R1,1                INITIALIZE R1               @OZ69106
         ICM   R14,1,0(PL)         GET IMAGE BYTE              @OZ69106
         BZ    SPRLFCB4            BRANCH IF NULL              @OZ69106
         SLL   R1,0(R14)            CREATE                     @OZ69106
         STH   R1,PCCWORK+6          CHANNEL                   @OZ69106
         OC    PCCWORK+4(2),PCCWORK+6 MASK                     @OZ69106
         SPACE 1                                               @OZ69106
SPRLFCB4 LA    PL,1(,PL)           INCREMENT IMAGE POINTER     @OZ69106
         BCT   R15,SPRLFCB3        LOOP THRU FCB IMAGE         @OZ69106
         TM    PCCWORK+5,X'02'     TEST IF CH1 DEFINED         @OZ69106
         BO    SPRLFCB6            BRANCH IF YES               @OZ69106
         DROP  PL                  SUSPEND FCB ADDRESSABILITY  @OZ69106
         EJECT                                                 @OZ69106
        $MID   180                                             @OZ69106
         L     R1,PQHFCB           RESTORE BUFFER ADDRESS      @OZ69106
         MVC   BUFSTART(2),=X'180F' MOVE MESSAGE NUMBER        @OZ69106
         MVC   BUFSTART+2(8),DCTDEVN MOVE DEVICE NAME          @OZ69106
         MVC   BUFSTART+10(23),=C' FCB IMAGE XXXX INVALID'     @OZ69106
         MVC   BUFSTART+21(4),UCBFCBNM MOVE FCB IMAGE ID       @OZ69106
        $WTO   BUFSTART,33,JOB=YES, ISSUE IMAGE INVALID MSG    @OZ69106C
               ROUTE=$LOG+$UR,CLASS=$DOMACT,PRI=$ST            @OZ69106
         SPACE 1                                               @OZ69106
SPRLFCB5 ST    R1,SPWSAVE1         SAVE CMB ADDRESS            @OZ69106
         OI    SPWFLAG,SPWSETP+SPWINIT FORCE CALL TO SETPRT    @OZ69106
         XC    SPPARM(SPWFORMS-SPPARM),SPPARM CLEAR PARM LIST  @OZ69106
         L     R1,PQHFCB           GET BUFFER ADDRESS          @OZ69106
        $FREEBUF (R1)              FREE FCB BUFFER             @OZ69106
         XC    PQHFCB,PQHFCB       CLEAR BUFFER ADDRESS        @OZ69106
         L     R1,SPWSAVE1         RESTORE CMB ADDRESS         @OZ69106
         B     SPRSTOP             WAIT FOR OPERATOR           @OZ69106
         SPACE 1                                               @OZ69106
SPRLFCB6 MVC   PFCBMAP,PCCWORK+4   SAVE FCB MAP                @OZ69106
         TM    PPFLAG3,PP3800R     REPOSITIONING CMD...        @OZ69106
         BZ    SPRLFCB7            BRANCH IF NOT               @OZ69106
         CLC   PQHMAPV,$ZEROS      MAPPING NEEDED...           @OZ69106
         BNE   SPREXIT1            BRANCH IF YES               @OZ69106
         SPACE 1                                               @OZ69106
SPRLFCB7 L     R1,PQHFCB           GET BUFFER ADDRESS          @OZ69106
        $FREEBUF (R1)              FREE FCB BUFFER             @OZ69106
         XC    PQHFCB,PQHFCB       CLEAR BUFFER ADDRESS        @OZ69106
         B     SPREXIT1            TERMINATE 3800 SETUP        @OZ69106
         EJECT                                                 @OZ69106
***************************************************************@OZ69106
*                                                              @OZ69106
*        BUILD A DEFAULT FCB WHEN NO IMAGE NAME IS AVAILABLE   @G38ESBB
*                                                              @G38ESBB
***************************************************************@G38ESBB
         SPACE 1                                               @G38ESBB
PFCBBLD  LM    PC1,PC2,PCCWNOP     LOAD CCW TO GET RPI/SIB     @G38ESBB
         ICM   PC2,B'1000',=X'60'  SET CHAINING ON IN AREA     @G38ESBB
         BAL   PL,PPPUT            ADD CCW TO AREA             @G38ESBB
         BAL   PL,PPWRITE          WRITE CCW AREA              @G38ESBB
         BAL   PL,PPCHECK          COMPLETE I/O                @G38ESBB
         L     PW,PQHADR           RESTORE PQH ADDRESS         @G38ESBB
         L     R1,PQHFCB           RESTORE FCB BUFFER ADDRESS  @G38ESBB
         LA    R1,BUFSTART         GET FCB ADDRESS             @G38ESBB
         USING PFCB,R1             FCB BUFFER ADDRESSABILITY   @G38ESBB
         L     PC1,PCEDCT          RESTORE DCT ADDRESS         @G38ESBB
         L     PC2,PRPUUCB         RESTORE UCB                 @G38ESBB
         L     PC2,UCBXTADR-UCBDSECT(,PC2) EXTENSION ADDRESS   @G38ESBB
         DROP  PW                  SUSPEND PQH ADDRESSABILITY  @G38ESBB
         SPACE 1                                               @G38ESBB
***************************************************************@G38ESBB
*                                                              @G38ESBB
*        DETERMINE FCB LENGTH FROM FORMS LENGTH IN HALF INCHES @G38ESBB
*                                                              @G38ESBB
***************************************************************@G38ESBB
         SPACE 1                                               @G38ESBB
         LH    PW,PCCWLAST         GET OFFSET TO PCIE          @G38ESBB
         AL    PW,POUTCCWN         ADD CCW AREA BASE           @G38ESBB
         LA    PW,PCIESIZE(,PW)    GET TO BUFFER WORK AREA     @G38ESBB
         SLR   PL,PL               CLEAR FOR INSERT            @G38ESBB
         IC    PL,BFWFLENG-BFWDSECT(,PW) GET FORMS LENGTH      @G38ESBB
         BCTR  PL,0                SUBTRACT TOP 1/2 INCH       @G38ESBB
         BCTR  PL,0                SUBTRACT BOTTOM 1/2 INCH    @G38ESBB
         MH    PL,PH6LPI           GET # OF LINES IN 6 LPI FCB @G38ESBB
         STH   PL,PFCBLENG         SAVE FCB LENGTH             @G38ESBB
         MVI   PFCBSTRT,X'01'      SET CHAN 1 IN FIRST BYTE    @G38ESBB
         BCTR  PL,0                DECREMENT FOR CHANNEL 1     @G38ESBB
         BCTR  PL,0                DECREMENT FOR EXECUTE       @G38ESBB
         EX    PL,PFCBCLR          SET REMAINING FCB TO ZERO   @G38ESBB
         MVC   PCCWORK+4(2),=X'0002' SET FCB MAP               @OZ69106
         L     PW,PQHADR           RESTORE PQH ADDRESS         @OZ69106
         B     SPRLFCB6            EXIT FCB LOAD               @OZ69106
         SPACE 1                                               @OZ69106
PFCBCLR  XC    PFCBSTRT+1(*-*),PFCBSTRT+1 EXECUTE ONLY         @OZ69106
         SPACE 1                                               @OZ69106
         DROP  R1                  SUSPEND FCB BUFFER          @OZ69106
         EJECT                                                 @OZ69106
***********************************************************************
*                                                                     *
*        DETERMINE IF A CALL TO SETPRT (SVC 81) IS NECESSARY          *
*                                                                     *
***********************************************************************
         SPACE 1                                               @OZ69106
SPRLFCB  CLI   DCTFCB,C'*'         TEST FOR FCB SPECIFIED      @OZ69106
         BNE   SPRLINIT            BRANCH IF YES               @OZ69106
         CLI   UCBFCBNM,X'40'      HDWR DFLT LOADED...         @OZ69106
         BE    SPRLCHRS            BRANCH IF YES               @OZ69106
         OI    SPWFLAG,SPWINIT+SPWSETP SET INIT PRINTER        @OZ69106
         B     SPRLCHRS            BRANCH TO CONTINUE          @OZ69106
         SPACE 1                                               @OZ69106
SPRLINIT TM    SPWFLAG,SPWINIT     INITIALIZE PRINTER...       @G38ESBB
         BO    SPRLFCBN            FORCE FCB UPDATE IF YES     @G38ESBB
         CLC   DCTFCB,UCBFCBNM     FCB MATCH PRINTER FCB...         R41
         BE    SPRLCHRS            BR IF YES                        R41
         SPACE 1                                                    R41
SPRLFCBN MVC   SPPFCB,DCTFCB       SHOW SETPRT CALL NEEDED     @G38ESBB
         OI    SPWFLAG,SPWSETP      TO LOAD FCB                     R41
         SPACE 1                                                     R4
SPRLCHRS DS    0H                                                    R4
         TM    SPWFLAG,SPWINIT     IF INIT BIT IS SET,               R4
         BO    SPRLC2               THEN FORCE UPDATE                R4
         LA    R1,DCTCHAR1         IF ANY                            R4
         LA    R15,UCBCHAR1         CHARS VALUES                     R4
         LA    R0,4                  DIFFER                          R4
SPRLC1   CLI   0(R1),C'*'             FROM                           R4
         BE    SPRLFLSH                UCB,                          R4
         CLC   0(4,R1),0(R15)          THEN                          R4
         BNE   SPRLC2                   BRANCH                       R4
         LA    R1,4(,R1)                 TO                          R4
         LA    R15,4(,R15)                LOAD                       R4
         BCT   R0,SPRLC1                   ALL SPECIFIED             R4
         B     SPRLFLSH            ELSE, DON'T CALL SETPRT           R4
         SPACE 1                                                     R4
SPRLC2   CLI   DCTCHAR1,C'*'       UNTIL                             R4
         BE    SPRLFLSH             END                              R4
         MVC   SPPXLAT1,DCTCHAR1     OF                              R4
         OI    SPWFLAG,SPWSETP        CHARS                          R4
         CLI   DCTCHAR2,C'*'           VALUES                        R4
         BE    SPRLFLSH                 IS                           R4
         MVC   SPPXLAT2,DCTCHAR2         REACHED,                    R4
         CLI   DCTCHAR3,C'*'              UPDATE                     R4
         BE    SPRLFLSH                    PARAMETER                 R4
         MVC   SPPXLAT3,DCTCHAR3            LIST                     R4
         CLI   DCTCHAR4,C'*'                 AND INDICATE            R4
         BE    SPRLFLSH                       SETPRT                 R4
         MVC   SPPXLAT4,DCTCHAR4               CALL NEEDED           R4
         EJECT                                                 @OZ18414
SPRLFLSH CLI   DCTFLASH,C'*'       FLASH SPECIFIED...          @OZ18414
         BNE   SPRLF1              BR IF YES                   @OZ18414
         CLI   UCBIMAGE,X'40'      FLASH ACTIVE...             @OZ18414
         BE    SPRLBRST            BR IF NO                    @OZ18414
         MVC   SPPIMAGE,=X'40000000'  ELSE RESET FLASH ID      @OZ18414
         B     SPRLF2              BR TO CONTINUE              @OZ18414
         SPACE 1                                               @OZ18414
SPRLF1   MVC   SPPIMAGE,DCTFLASH   SET FLASH ID                @OZ18414
         TM    SPFLAG,SPSEP+SPNOFLSH  INHIBIT FLASHING...      @OZ38238
         BNZ   SPRLF2              SET ZERO COUNT IF YES       @OZ38238
         ICM   PW,1,SPFLASHC       GET FLASH-COPY COUNT        @OZ18414
         BNZ   *+8                 USE IT IF NOT ZERO          @OZ18414
         LA    PW,255               ELSE USE MAX COPY COUNT    @OZ18414
         STC   PW,SPPFRMNR         SET FLASH COUNT             @OZ18414
         TM    SPWFLAG,SPWINIT     INITIALIZE PRINTER...       @OZ18414
         BO    SPRLBRST            BR IF YES                   @OZ18414
         CLC   DCTFLASH,UCBIMAGE   THIS FLASH LOADED...        @OZ18414
         BNE   SPRLF2              BR IF NO                    @OZ18414
         CLM   PW,1,PREVFLCT       FLASH COUNT LOADED...       @OZ18414
         BE    SPRLBRST            SKIP SETPRT IF YES          @OZ18414
         SPACE 1                                               @OZ18414
SPRLF2   OI    SPWFLAG,SPWSETP     INDICATE SETPRT NEEDED      @OZ18414
         SPACE 1                                                     R4
SPRLBRST DS    0H                                                    R4
         TM    UCBOPTNS,UCBBRSTR   TEST FOR 3800 BURSTER FEATURE     R4
         BZ    SPRLMODF            BR IF NO                          R4
         TM    DCTPPSW2,DCTNIBRS   TEST FOR BURSTER WANTED           R4
         BZ    SPRLB1              BR IF NO                          R4
         TM    UCBACTIV,UCBBRSTA   TEST FOR BURSTER ACTIVE           R4
         BO    SPRLMODF            BR IF YES                         R4
         OI    SPPFLAG1,SPPBURST   INDICATE BURST=YES                R4
         B     SKIP740             GO TO INDICATE SETPRT NEEDED      R4
SPRLB1   TM    UCBACTIV,UCBBRSTA   TEST FOR BURSTER ACTIVE           R4
         BZ    SPRLMODF            BR IF NO                          R4
SKIP740  OI    SPWFLAG,SPWSETP     INDICATE SETPRT NEEDED            R4
         SPACE 1                                                     R4
SPRLMODF DS    0H                                                    R4
         CLI   DCTMODF,C'*'        TEST FOR MODIFY SPECIFIED         R4
         BNE   SPRLM1              BR IF YES                         R4
         TM    SPWFLAG,SPWCLRM     TEST FOR COPY MOD RESET NEEDED    R4
         BZ    SPRPCOPY            BR IF NO                          R4
         LA    R1,SPRNULLM         ADDR OF NULL COPY MOD             R4
         ST    R1,SPPMDPTA          INTO SETPRT LIST                 R4
         OI    SPPFLAG2,SPPMODI    INDICATE IN-CORE COPY MOD         R4
         B     SPRLM2              GO INDICATE SETPRT NEEDED         R4
SPRLM1   MVC   SPPMODPT,DCTMODF    MOVE COPY MOD ID INTO LIST        R4
         SLR   PW,PW               ASSUME MODIFY-TRC=0         @OZ18410
         CLC   SPMODFT,PRMAXTRC    IS MODIFY-TRC VALID...      @OZ18410
         BH    *+8                 BR IF NOT TO USE TRC=0      @OZ18410
         IC    PW,SPMODFT           ELSE USE SUPPLIED MOD-TRC  @OZ18410
         IC    PW,PXTABCCW(PW)     DETERMINE                   @OZ18410
         SLL   PW,26                ACTUAL                     @OZ18410
         SRL   PW,30                 CHARACTER SET             @OZ18410
         STC   PW,SPPTRC              POSITION                 @OZ18410
SPRLM2   OI    SPWFLAG,SPWSETP     INDICATE SETPRT NEEDED            R4
         SPACE 1                                                     R4
SPRPCOPY DS    0H                                                    R4
         CLC   PREVCPYS(2),SPCOPYS TEST IF COUNTS MATCH PREVIOUS     R4
         BE    SPRSETP             BR IF YES                         R4
         OI    SPWFLAG,SPWSETP     ELSE, FORCE A CALL TO SETPRT      R4
         EJECT                                                       R4
*                                                                    R4
*        CALL SETPRT (SVC 81) VIA JES2 IMAGE LOADER SUB-TASK         R4
*                                                                    R4
SPRSETP  DS    0H                                                    R4
         TM    SPWFLAG,SPWSETP     TEST FOR SETPRT NEEDED            R4
         BZ    SPREXIT             BR IF NO                          R4
         SPACE 1                                                     R4
         TM    SPWFLAG,SPWINIT     TEST FOR INIT NEEDED              R4
         BZ    SKIP750             BR IF NO                          R4
         OI    SPPFLAG1,SPPINIT    ELSE, TELL SETPRT                 R4
SKIP750  B     SPRSETP1            GO CALL SETPRT                    R4
         SPACE 1                                                     R4
SPRRXMIT DS    0H                                                    R4
         MVC   SPPFRMNR,SPFLASHC   MOVE IN FLASH COUNT               R4
         OI    SPPFLAG1,SPPREX     INDICATE RE-XMISSION TO SETPRT    R4
         NI    SPFLAG,255-SPREXMIT RESET PRPU FLAG INDICATION        R4
         SPACE 1                                                     R4
SPRSETP1 DS    0H                                                    R4
         L     R1,DCTDCB           ADDRESS 3800 DCB                  R4
         NI    DCBIFLGS-DCBDSECT(R1),253-DCBIFEC  RESET        @OZ46952C
                                   DCBIFEC AND DCBIFLDT        @OZ46952
         ST    R1,SPPDCBA          SET DCB ADDR IN PARMLIST          R4
         MVI   SPPFDUNF,SPPFBLK+SPPEXTL  BLK DATA CK + EXTENDED LIST R4
         OI    SPPFLAG1,SPPBFREQ+SPPBTREQ BYPASS ALL POSSIBLE WTOR'S R4
         MVC   SPPCPYNR(1),SPCOPYN MOVE IN NUMBER OF COPIES          R4
         MVC   SPPSTCNR(1),SPCOPYS MOVE IN STARTING COPY NUMBER      R4
         MVC   PREVCPYS(2),SPCOPYS SAVE COPY COUNTS                  R4
         MVC   PREVFLCT,SPPFRMNR   SAVE FLASH COUNT            @OZ18414
         XC    SPWMSG,SPWMSG       CLEAR SETPRT MESSAGE AREA   @G38ESBB
         LA    R1,L'SPWMSG-M151L+(SPPTXT-SPPMCOMA) MSG LENGTH  @G38ESBB
         LA    R15,SPWMSG          GET MESSAGE FEEDBACK AREA   @G38ESBB
         STH   R1,M151L-(SPPTXT-SPPMCOMA)(,R15) STORE MSG LNG  @G38ESBB
         LA    R15,M151L-(SPPTXT-SPPMCOMA)(,R15) GET MSG ADDR  @G38ESBB
         ST    R15,SPPEMSGA        PASS ADDRESS FOR SETPRT USE @G38ESBB
         MVI   SPPLEN+1,SPPEND-SPPARM SET SETPRT PARM LENGTH   @G38ESBB
         L     R1,PRIMGDTE         ADDR OF HASPIMAG DTE        @OZ26939
         LA    R1,8(,R1)           ADDR OF HASPIMAG WORK-ECB   @OZ26939
         MVI   BUFECBCC-BUFDSECT(PBUF),X'80'  SET ECB AS WAITING     R4
         POST  (1),(PBUF)          POST (PASSING BUFFER ADDR)  @OZ26939
         SPACE 1                                                     R4
SPRWIM2  DS    0H                                                    R4
        $WAIT  IMAG                WAIT FOR POST FROM SUB-TASK       R4
         TM    BUFECBCC-BUFDSECT(PBUF),X'7F' TEST STATUS OF SETPRT   R4
         BZ    SPRWIM2             BR IF NOT COMPLETE                R4
         BO    SPREXIT             BR IF SUCCESSFUL                  R4
         EJECT                                                       R4
*                                                                    R4
*        SETPRT ERROR DETECTED - ATTEMPT RECOVERY                    R4
*                                                                    R4
         SPACE 1                                                     R4
SPRERR   DS    0H                                                    R4
         CLI   SPWRTCDE,0          TEST RETURN CODE                  R4
         BE    SPREXIT             EXIT IF ALL IS OK                 R4
         CLI   SPWRTCDE,SPPLDATA   PAPER JAM OR CANCEL KEY     @G38ESBB
         BE    SPRCKJAM                 DURING SETPRT          @G38ESBB
*              THIS LINE DELETED BY APAR NUMBER                @OZ45281
         LA    R1,SPWMSG           GET SETPRT MSG AREA         @G38ESBB
         LA    R1,M151L-(SPPTXT-SPPMCOMA)(,R1) OFFSET TO MSG   @G38ESBB
         USING SPPMCOMA,R1         MSG AREA ADDRESSABILITY     @G38ESBB
         TM    SPWFLAG,SPWDEFLT    ATTEMPT TO ADD DEFAULT...   @OZ39479
         BZ    SPERR1              BR IF NO                    @OZ39479
         CLI   SPWRTCDE,X'30'       ELSE BRANCH TO             @OZ39479
         BE    SPREMOVE              REMOVE THE                @OZ39479
         CLI   SPWRSCDE,X'04'         DEFAULT CHARACTER        @OZ39479
         BE    SPREMOVE                SET FROM THE            @OZ39479
         CLI   SPWRSCDE,X'10'           REQUEST IF THE         @OZ39479
         BE    SPREMOVE                  ERROR MIGHT HAVE      @OZ39479
         CLI   SPWRSCDE,X'1C'             BEEN CAUSED BY THE   @OZ39479
         BE    SPREMOVE                    ATTEMPT TO LOAD IT  @OZ39479
         SPACE 1                                               @OZ39479
SPERR1   OI    SPWFLAG,SPWSETP+SPWINIT CAN'T TRUST SETUP       @OZ45281
         CLC   SPPTXTL,$ZEROS      DID SETPRT PROVIDE MSG...   @OZ45281
         BE    SPRNOMSG            BR IF NO                    @G38ESBB
         CLI   SPWRSCDE,SPPCPMOD   COPYMOD ERROR               @G38ESBB
         BNE   SPREFLSH            BR IF NOT                   @G38ESBB
         OI    SPWFLAG,SPWCLRM     INDICATE CLEAR IF OP RESETS @G38ESBB
         B     SPRRMSG             BR TO WRITE MSG             @G38ESBB
         SPACE 1                                               @G38ESBB
SPREFLSH CLI   SPWRSCDE,SPPFOSEQ   FLASH ERROR                 @G38ESBB
         BE    SPRWTON             BR IF YES                   @G38ESBB
         CLI   SPWRSCDE,X'00'      UNKNOWN ERROR               @G38ESBB
         BNE   SPRRMSG             BR IF NOT                   @G38ESBB
         SPACE 1                                               @G38ESBB
SPRWTON  OI    SPWFLAG,SPWWTO      SHOW SETUP MSG NEEDED       @G38ESBB
         B     SPRRMSG                                         @G38ESBB
         SPACE 1                                               @G38ESBB
SPRCKJAM OI    DCTPPSW2,DCTCKJAM+DCTNINIT LOST DATA + INIT PRT @G38ESBB
         MVC   DCTLDPID,UCBPGID    GET ORIGIN PAGE ID          @G38ESBB
         OI    PPFLAG,PPDELSW      INDIC SUSPENSION            @G38ESBB
         MVC   PFSBSCT,$ZEROS      DEFAULT TO PAPER JAM        @G38ESBB
         LA    R15,8               INDICATE                    @OZ75372
         L     R1,PSAVAREA          PJAM/CKEY                  @OZ75372
         ST    R15,4+(15*4)(,R1)     RETURN CODE               @OZ75372
         CLI   SPWRSCDE,SPPCNCLK   CANCEL KEY...               @G38ESBB
         BNE   SPREXIT2            BR IF NOT                   @G38ESBB
         MVC   PFSBSCT,PMAXPAGE    INDICATE $F PRTN,D          @G38ESBB
         B     SPREXIT2            BR TO RETURN                @G38ESBB
         EJECT                                                 @OZ75372
*                                  THIS LINE DELETED BY APAR   @OZ39479
*                                  THIS LINE DELETED BY APAR   @OZ39479
SPREMOVE SLR   R1,R1                REMOVE                     @OZ39479
         IC    R1,PRMAXTRC           DEFAULT                        R41
         SLL   R1,2                   CHAR (DCTUCS)                 R41
         LA    PW,DCTCHAR1+4(R1)       FROM                         R41
         MVC   0(4,PW),=C'****'         REQUEST                     R41
         NI    SPWFLAG,255-SPWDEFLT RESET DEFAULT-ATTEMPT FLAG      R41
         LA    PW,SPPXLAT1+4(R1)   REMOVE REQUEST FROM              R41
         XC    0(4,PW),0(PW)        SETPRT PARMLIST                 R41
         NI    SPPFLAG1,FF-SPPBOMSG TURN OFF BYPASS WCGM MSG   @G38ESBB
         B     SPRSETP             BR TO RETRY SETPRT REQUEST  @G38ESBB
         SPACE 1                                                    R41
SETRTCD  EQU   SPWSAVE1,2*L'SPWRTCDE WORK AREA FOR RETURN CODE @G38ESBB
         SPACE 1                                               @G38ESBB
SPRNOMSG UNPK  SETRTCD(L'SETRTCD+1),SPWRTCDE(L'SPWRTCDE+1)     @G38ESBBC
                                   GET RETURN CODE AND         @G38ESBB
         TR    SETRTCD,$HEXTRAN     TRANSLATE TO EBCDIC        @G38ESBB
        $MID   157                 SETPRT ERROR, NO MESSAGE    @G38ESBB
         PMSG  SPWMSG,M157L,(X'157F',DCTDEVN,C' SETPRT ERROR, RETURN COC
               DE=',SETRTCD)       MOVE MESSAGE TEXT           @G38ESBB
         MVI   SPPRSV08+1,M157L-M151L SET MESSAGE LENGTH       @OZ45079
         B     SPRWTO              GO ISSUE MESSAGE            @G38ESBB
         EJECT                                                 @OZ75372
***************************************************************@G38ESBB
*                                                              @G38ESBB
*        WRITE SETPRT ERROR MESSAGE TO THE CONSOLE             @G38ESBB
*                                                              @G38ESBB
***************************************************************@G38ESBB
         SPACE 1                                               @G38ESBB
SPRRMSG  DS    0H                  ISSUE SETPRT ERROR MESSAGE  @G38ESBB
         IC    R15,SPPTXTL+1       SAVE MESSAGE LENGTH         @OZ45079
        $MID   151                 SETPRT ERROR MESSAGE        @G38ESBB
         PMSG  SPWMSG,M151L,(X'151F',DCTDEVN,C' ')             @G38ESBBC
                                   MOVE MESSAGE TEXT           @G38ESBB
         STC   R15,SPPRSV08+1      SAVE MESSAGE LENGTH         @OZ45079
         SPACE 1                                               @G38ESBB
SPRWTO   LM    PC1,PC2,PCCWCP      LOAD CLEAR PRINT CCW        @G38ESBB
         BAL   PL,PPPUT            ADD CCW TO AREA             @G38ESBB
         BAL   PL,PPWRITE          WRITE CCW AREA              @G38ESBB
         BAL   PL,PPCHECK          WAIT FOR I/O TO FINISH      @G38ESBB
         L     PC1,PCEDCT          ADDRESS 3800 DCT            @G38ESBB
         L     PC2,PRPUUCB         ADDRESS UCB                 @G38ESBB
         L     PC2,UCBXTADR-UCBDSECT(,PC2)  ADR UCB EXTENSION  @G38ESBB
         LA    R1,SPWMSG           RESTORE MESSAGE AREA ADR    @G38ESBB
         LA    R1,M151L-(SPPTXT-SPPMCOMA)(,R1) GET MSG ADDR    @G38ESBB
         SLR   R15,R15             CLEAR WORK REGISTER         @OZ45079
         IC    R15,SPPRSV08+1      GET MESSAGE LENGTH          @OZ45079
         MVI   SPPRSV08+1,C' '     RESTORE BLANK TEXT          @OZ45079
         LA    R15,M151L(,R15)     ACCOUNT FOR MSG PREFIX LGTH @G38ESBB
        $WTO   SPWMSG,(R15),JOB=YES, TELL OPERATOR             @G38ESBBC
               ROUTE=$LOG+$UR,CLASS=$DOMACT,PRI=$ST            @G38ESBB
         ST    R1,SPWSAVE1         SAVE CMB ADDRESS            @G38ESBB
         USING BUFDSECT,R1         RESTORE FCB BUFFER ADR      @G38ESBB
         USING JCTDSECT,JCT        JCT ADDRESSABILITY          @G38ESBB
         EJECT                                                 @G38ESBB
***************************************************************@G38ESBB
*                                                              @G38ESBB
*        WRITE SETPRT ERROR MESSAGE TO THE PROGRAMMER          @G38ESBB
*                                                              @G38ESBB
***************************************************************@G38ESBB
         SPACE 1                                               @G38ESBB
        $MID   154                 SETPRT ERROR MSG TO PRINTER @G38ESBB
         PMSG  SPWMSG,M154L,(C'$HASP154 SETPRT ERROR DEVICE=',DCTDEVN,CC
               ' JOB=',JCTJNAME,C' ID=',JCTJOBID,C' ROOM=',JCTROOMN)   C
                                   MOVE MESSAGE TEXT           @G38ESBB
         LA    R1,SPWMSG           GET ADDR OF MSG AREA        @G38ESBB
         LM    PC1,PC2,PRCCWCOM    LOAD PRINT CCW              @G38ESBB
         ALR   PC1,R1              UPDATE DATA ADDRESS         @G38ESBB
         IC    PC2,=AL1(M154L)     GET LENGTH OF MESSAGE       @G38ESBB
         BAL   PL,PPPUT            ADD CCW TO AREA             @G38ESBB
         BAL   PL,PPWRITE          WRITE MESSAGE TO PRINTER    @G38ESBB
         BAL   PL,PPCHECK          CHECK MSG IO COMPLETION     @G38ESBB
         SPACE 1                                               @G38ESBB
         DROP  JCT                 SUSPEND JCT                 @G38ESBB
         SPACE 1                                               @G38ESBB
*        DELETED                                               @G38ESBB
         SPACE 1                                               @G38ESBB
***************************************************************@G38ESBB
*                                                              @G38ESBB
*        RESTORE 3800 DEVICE SETUP CONTROL BLOCKS              @G38ESBB
*                                                              @G38ESBB
***************************************************************@G38ESBB
         SPACE 1                                               @G38ESBB
         L     PC1,PCEDCT          ADDRESS 3800 DCT            @G38ESBB
         L     PC2,PRPUUCB             EXTENSION               @G38ESBB
         L     PC2,UCBXTADR-UCBDSECT(,PC2)  ADDRESSABILITY     @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
         OI    SPWFLAG,SPWSETP     FORCE CALL TO SETPRT              R4
         XC    SPPARM(SPWFORMS-SPPARM),SPPARM  RESET PARMLIST        R4
         L     R1,SPWSAVE1         RESTORE CMB ADDRESS         @G38ESBB
         TM    SPWFLAG,SPWWTO      TEST FOR SETUP MSG NEEDED         R4
         BZ    SPRSTOP             BR IF NOT -- WAIT FOR OPERATOR    R4
        $DOM   CMB=(R1)            DELETE ERROR MESSAGE        @G38ESBB
         B     SPRMSG1             GO ISSUE SETUP MESSAGE           R41
         SPACE 1                                                    R41
SPREXIT  TM    UCBOPTNS,UCBBRSTR   DON'T VALIDATE STACKER IF        R41
         BZ    SPRFCBM              BTSS NOT INSTALLED         @OZ69106
         TM    DCTPPSW2,DCTNIBRS   TEST WANTED-STACKER               R4
         BZ    SPRXCFS             BR IF CFS                         R4
         TM    UCBACTIV,UCBBRSTA   TEST ACTUAL-STACKER               R4
         BZ    SPRMSG1             ERROR IF NOT BTSS                 R4
         B     SPRFCBM             BR TO BUILD FCB MAP         @OZ69106
         SPACE 1                                               @OZ69106
SPRXCFS  TM    UCBACTIV,UCBBRSTA   TEST ACTUAL-STACKER               R4
         BO    SPRMSG1             ERROR IF BTSS                     R4
         B     SPRFCBM             BR TO BUILD FCB MAP         @OZ69106
         EJECT                                                      R41
SPREXIT1 DS    0H                  ISSUE EJECT CCW             @G38ESBB
         NI    DCTPPFL,FF-DCTEJECT   RESET CHAN 1 SWITCH       @OZ57092
         LM    PC1,PC2,PRCCWEJ     SKIP TO                          R41
         BAL   PL,PPPUT             TOP                             R41
         ICM   PC1,8,PXTABCCW               SELECT             @OZ24675
         BAL   PL,PPPUT2                     CHAR1             @OZ51441
         BAL   PL,PPWRITE            OF                             R41
         BAL   PL,PPCHECK            PAGE                           R41
         SPACE 1                                               @G38ESBB
SPREXIT2 L     R4,PCEDCT           ADDRESS 3800 DCT            @OZ78585
         NI    DCTPPSW,FF-DCTPPSWO RESET OPERATOR $T ALLOWED   @OZ78585
         PRETURN ,                 RESTORE REGS AND RETURN     @OZ78585
         SPACE 4                                                    R41
*                                                                   R41
*        SPRTDFLT -- SUBR TO TEST IF DEFAULT CHAR IS LOADED         R41
*                                                                   R41
* OUTPUT CC    = ZERO     - DEFAULT ALREADY LOADED                  R41
*        CC    = NON-ZERO - DEFAULT NOT LOADED                      R41
*        PW    = ADDR OF CCW TO SELECT DEFAULT (IF CC=ZERO)         R41
*                                                                   R41
         SPACE 1                                                    R41
SPRTDFLT LA    R0,4                COUNT OF CHARS                   R41
         LA    R15,DCTCHAR1        ADDR OF CHARS                    R41
         LA    PW,SPRXCCWS         ADDR OF SELECT CCW TABLE         R41
         SPACE 1                                                    R41
SPRTDLUP CLC   DCTUCS,0(R15)       IS THIS THE DEFAULT...           R41
         BER   PL                  RETURN IF YES  (CC=ZERO)         R41
         LA    R15,4(,R15)         ADDR OF NEXT CHAR VALUE          R41
         LA    PW,1(,PW)            AND ITS SELECT CCW              R41
         BCT   R0,SPRTDLUP         LOOP FOR ALL CHARS               R41
         BR    PL                  RETURN NOT FOUND (CC=N-ZERO)     R41
         EJECT                                                      R41
*                                                                    R4
*        DELETED                                               @G38ESBB
         SPACE 1                                               @G38ESBB
         PRINT OFF                 THIS SECTION DELETED BY     @G38ESBB
*        THIS LINE DELETED BY PSETPRT                          @G38ESBB
*        THIS LINE DELETED BY PSETPRT                          @G38ESBB
*        THIS LINE DELETED BY PSETPRT                          @G38ESBB
*        THIS LINE DELETED BY PSETPRT                          @G38ESBB
*        THIS LINE DELETED BY PSETPRT                          @G38ESBB
*        THIS LINE DELETED BY PSETPRT                          @G38ESBB
*        THIS LINE DELETED BY PSETPRT                          @G38ESBB
*        THIS LINE DELETED BY PSETPRT                          @G38ESBB
*        THIS LINE DELETED BY PSETPRT                          @G38ESBB
*        THIS LINE DELETED BY PSETPRT                          @G38ESBB
         PRINT ON                  THIS SECTION DELETED BY     @G38ESBB
         SPACE 3                                                     R4
*                                                                    R4
*        NULL IN-CORE COPY MODIFICATION RECORD TO RESET 3800 WITHOUT R4
*                     ISSUING AN INITIALIZE-PRT                      R4
*                                                                    R4
         SPACE 2                                                     R4
SPRNULLM DS    0F                                                    R4
         SPACE 1                                                     R4
         DC    CL4'****'                COPY MOD IDENTIFICATION      R4
         DC    XL2'0'                   RESERVED                     R4
         DC    XL2'7'                   LENGTH OF COPY MOD RECORD    R4
         DC    XL1'0'                   STARTING COPY NUMBER         R4
         DC    XL1'1'                   NUMBER OF COPIES             R4
         DC    XL1'1'                   STARTING LINE NUMBER         R4
         DC    XL1'1'                   NUMBER OF LINES              R4
         DC    XL1'1'                   STARTING POSITION            R4
         DC    XL1'1'                   NUMBER OF CHARACTERS         R4
         DC    CL1' '                   MODIFICATION TEXT            R4
         SPACE 4                                                     R4
SPRXCCWS DS    0XL4                3800 TABLE-SELECT CCW OPS        R41
         DC    X'47'               SELECT TRANSLATE TABLE 0         R41
         DC    X'57'               SELECT TRANSLATE TABLE 1         R41
         DC    X'67'               SELECT TRANSLATE TABLE 2         R41
         DC    X'77'               SELECT TRANSLATE TABLE 3         R41
         EJECT                                                      R41
         LTORG                                                       R4
         SPACE 2                                                     R4
         TITLE 'HASP PRINT/PUNCH SERVICE-- FCB/UCS IMAGE READ' @OZ34384
************************************************************** @OZ34384
*                                                            * @OZ34384
* ENTER FROM HASPPRPU INITIALIZATION.  READ UCS/FCB IMAGE.   * @OZ34384
* ENTERED FOR THE FIRST USE OF UNIT ONLY.  MUST READ IMAGE   * @OZ34384
* TO DETERMINE IF UCS OR FCB IS A NON-STANDARD IMAGE.        * @OZ34384
*                                                            * @OZ34384
************************************************************** @OZ34384
         SPACE 1                                               @OZ34384
PINITSU  DS    0H                                              @OZ34384
         ST    BASE2,PDSVSAVE      SAVE BASE REGISTER          @OZ34384
         LR    BASE2,R15           SET UP                      @OZ34384
         USING PINITSU,BASE2         LOCAL ADDRESSABILITY      @OZ34384
         ST    PL,PCERETN          SAVE RETURN REGISTER        @OZ34384
         MVC   PFCBMAP,=X'1FFE'    INITIALIZE FCB MAP          @OZ69106
         L     R1,PRIMGDTE         GET HASPIMAGE DTE ADDRESS   @OZ34384
         OC    0(4,R1),0(R1)       SUBTASK ALREADY ACTIVE...   @OZ34384
         BNZ   PGOTIMAG            BR IF YES                   @OZ34384
         SPACE 1                                               @OZ34384
         L     R15,=A(PIMAGLST)    ELSE MOVE ATTACH PARMS      @OZ34384
         MVC   $CSAVREG(PIMAGLL),0(R15)  TO WORK AREA          @OZ34384
         LR    PW,R1               SAVE DTE ADDRESS            @OZ34384
         ATTACH ECB=4(,R1),MF=(E,(1)),SF=(E,$CSAVREG)          @OZ34384
         STCM  R1,7,1(PW)          STORE TCB ADDRESS IN DTE    @OZ40222
         SPACE 1                                               @OZ34384
PIMGATCH DS    0H                                              @OZ34384
        $WAIT  IMAG                WAIT FOR SUBTASK TO INIT    @OZ34384
         TM    0(PW),X'80'         HASPIMAGE INITIALIZED...    @OZ34384
         BZ    PIMGATCH            BR IF NOT, TRY AGAIN        @OZ34384
         SPACE 1                                               @OZ34384
PGOTIMAG DS    0H                                              @OZ34384
         L     PC1,PCEDCT          ESTABLISH DCT               @OZ34384
         USING DCTDSECT,PC1          ADDRESSABILITY            @OZ34384
         NI    DCTPPSW,255-DCTPPSWU RESET NON-STD SWITCH       @OZ34384
         TM    PDEVTYPE+1,X'80'    UCS ALLOWED...              @OZ34384
         BZ    PFCBLD              BR IF NOT SUPPORTED         @OZ34384
         CLC   DCTUCS,=C'0   '     IS UCS = 0 REQUESTED...     @OZ34384
         BE    PFCBLD              BR IF YES, CHECK FCB        @OZ34384
         SPACE 1                                               @OZ34384
         $GETBUF PNOBUF            GET BUFFER FOR UCS READ     @OZ34384
         LR    WB,R1               LOAD BUFFER INTO R3         @OZ34384
         USING BUFDSECT,WB         SET BUFFER ADDRESSABILITY   @OZ34384
         MVC   BUFSTART(4),=X'0001003A' BLDL PARAMETER LIST    @OZ34384
         MVC   BUFSTART+4(4),=C'UCS1' IMAGE PREFIX-1403 USC    @OZ34384
         MVC   BUFSTART+8(4),DCTUCS USER UCS IMAGE ID          @OZ34384
         STCM  PC1,7,BUFDCT+1      ENSURE CORRECT DCT ADDR     @OZ34384
         CLI   PDEVBYT3,UCB1403    IS DEVICE A 1403...         @OZ40627
         BE    PUCS01              BR IF YES, SKIP NEXT        @OZ34384
         MVI   BUFBYT7,C'3'        SET TO 3203 IMAGE           @OZ40627
         CLI   PDEVBYT3,UCB3203    IS IT A 3203                @OZ40627
         BE    PUCS01              IF YES SKIP TO IMAGE TASK   @OZ40627
         MVI   BUFBYT7,C'2'        IMAGE PREFIX FOR 3211       @OZ40627
*        JES2 ONLY INITS 3 PRINTERS GO ON AS 3211              @OZ40627
PUCS01   DS    0H                                              @OZ34384
         TM    $IMAGECB,X'40'      IS IMAGE LOADER TASK BUSY...@OZ34384
         BZ    PUCS02              BR IF NO, CONTINUE          @OZ34384
         $WAIT IMAG             WAIT FOR IMAGE TASK TO $$POST  @OZ34384
         B     PUCS01              TRY AGAIN                   @OZ34384
         SPACE 1                                               @OZ34384
PUCS02   DS    0H                                              @OZ34384
         MVI   BUFECBCC,X'80'      SET BUFFER ECB AS WAITING   @OZ34384
         POST  $IMAGECB,(R3)       POST WITH BUFFER ADDRESS    @OZ34384
         SPACE 1                                               @OZ34384
PUCS03   DS    0H                                              @OZ34384
         $WAIT IMAG             WAIT FOR IMAGE TASK TO $$POST  @OZ34384
         TM    BUFECBCC,X'7F'      IS LOAD REQUEST COMPLETE... @OZ34384
         BZ    PUCS03              BR IF NOT, TRY AGAIN        @OZ34384
         BM    PUCSMS              BR TO 'NOT FOUND' MSG       @OZ34384
         TM    BUFSTART,X'80'      IS IT A STD UCS IMAGE...    @OZ34384
         BO    PUCSEX              BR IF YES                   @OZ34384
         OI    DCTPPSW,DCTPPSWU    TURN ON NON-STD SWITCH      @OZ34384
         DROP  WB                  DROP BUFFER ADDRESSIBILITY  @OZ34384
         SPACE 1                                               @OZ34384
PUCSEX   $FREEBUF  (WB)            FREE BUFFER                 @OZ34384
         SPACE 1                                               @OZ34384
PFCBLD   DS    0H                                              @OZ34384
         NI    DCTPPSW,255-DCTPPSWB TURN OFF NON-STAND SWITCH  @OZ34384
         CLI   PDEVBYT3,UCB1403    IS DEVICE A 1403...         @OZ40627
         BNE   PGTBUF              BR IF NOT A 1403            @OZ34384
         CLC   $PRTFCB,DCTFCB      IS IT STD FCB...            @OZ34384
         BE    PSUPEX              BR IF YES, RETURN TO INIT   @OZ34384
         OI    DCTPPSW,DCTPPSWB    TURN ON NON-STD SWITCH      @OZ34384
         B     PSUPEX              FINSHED, RETURN TO INIT     @OZ34384
         SPACE 1                                               @OZ34384
PGTBUF   $GETBUF PNOBUF            GET BUFFER FOR FCB READ     @OZ34384
         LR    WB,R1               LOAD R3 WITH BUFF ADDRESS   @OZ34384
         USING BUFDSECT,WB         SET BUFFER ADDRESSABILITY   @OZ34384
         MVC   BUFSTART(4),=X'0001003A' BLDL PARAMETER LIST    @OZ34384
         MVC   BUFSTART+4(4),=C'FCB2' IMAGE PREFIX-3211 FCB    @OZ34384
         MVC   BUFSTART+8(4),DCTFCB   USER FCB IMAGE ID        @OZ34384
         STCM  PC1,7,BUFDCT+1      ENSURE CORRECT DCT ADDR     @OZ34384
         SPACE 1                                               @OZ34384
PFCB01   DS    0H                                              @OZ34384
         TM    $IMAGECB,X'40'      IS IMAGE TASK BUSY...       @OZ34384
         BZ    PFCB02              BR IF NO, CONTINUE          @OZ34384
         $WAIT IMAG             WAIT FOR IMAGE TASK TO $$POST  @OZ34384
         B     PFCB01              TRY AGAIN                   @OZ34384
         SPACE 1                                               @OZ34384
PFCB02   DS    0H                                              @OZ34384
         MVI   BUFECBCC,X'80'      SET BUFFER ECB AS WAITING   @OZ34384
         POST  $IMAGECB,(R3)       POST WITH BUFFER ADDRESS    @OZ34384
         SPACE 1                                               @OZ34384
PFCB03   DS    0H                                              @OZ34384
         $WAIT IMAG             WAIT FOR IMAGE TASK TO $$POST  @OZ34384
         TM    BUFECBCC,X'7F'      LOAD REQUEST FINISHED...    @OZ34384
         BZ    PFCB03              BR IF NOT, TRY AGAIN        @OZ34384
         BM    PFCBMS              BR TO 'NOT FOUND' MSG       @OZ34384
         TM    BUFSTART,X'80'      TEST FOR STD IMAGE...       @OZ34384
         BO    PFCBEX              BR IF YES, SKIP NEXT        @OZ34384
         OI    DCTPPSW,DCTPPSWB    TURN ON NON-STD SWITCH      @OZ34384
         DROP  WB                  DROP BUFFER ADDRESSABILITY  @OZ34384
         SPACE 1                                               @OZ34384
PFCBEX   $FREEBUF (WB)             FREE BUFFER                 @OZ34384
         B     PSUPEX              FINSHED, GET A JOB          @OZ34384
         SPACE 1                                               @OZ34384
PUCSMS   DS    0H                                              @OZ34384
         USING BUFDSECT,WB         GET BUFFER ADDRESSABILITY   @OZ34384
         MVC   BUFSTART(2),=X'000F' MOVE MESSAGE NUMBER        @OZ34384
         MVC   BUFSTART+2(8),DCTDEVN MOVE DEVICE NAME          @OZ34384
         MVC   BUFSTART+10(26),=CL26' UCS  IMAGE XXXX NOT FOUND' Z34384
         MVC   BUFSTART+22(4),DCTUCS MOVE UCS ID               @OZ34384
        $WTO   BUFSTART,36,JOB=NO, ISSUE BUFFER LOAD FAIL MSG  @OZ34384*
               ROUTE=$LOG+$UR,CLASS=$ACTION,PRI=$ST            @OZ34384
         B     PUCSEX              FINISHED UCS, FREE BUFFER   @OZ34384
         SPACE 1                                               @OZ34384
PFCBMS   DS    0H                                              @OZ34384
         USING BUFDSECT,WB         GET BUFFER ADDRESSABILITY   @OZ34384
         MVC   BUFSTART(2),=X'000F' MOVE MSG ID                @OZ34384
         MVC   BUFSTART+2(8),DCTDEVN MOVE DEVICE NAME          @OZ34384
         MVC   BUFSTART+10(26),=CL26' FCB  IMAGE XXXX NOT FOUND' Z34384
         MVC   BUFSTART+22(4),DCTFCB MOVE FCB ID               @OZ34384
        $WTO   BUFSTART,36,JOB=NO, ISSUE BUFFER LOAD FAIL MSG  @OZ34384*
               ROUTE=$LOG+$UR,CLASS=$ACTION,PRI=$ST            @OZ34384
         B     PFCBEX              FINISHED FCB, FREE BUFFER   @OZ34384
         SPACE 1                                               @OZ34384
PNOBUF  $WTO   PNBUFF,PMSGL,JOB=NO,     BUFFER SHORTAGE MSG    @OZ34384*
               ROUTE=$LOG+$UR,CLASS=$NORMAL,PRI=$ST            @OZ34384
         B     PSUPEX              NO BUFFERS, GET A JOB       @OZ34384
         SPACE 1                                               @OZ34384
PNBUFF   $MSG  000,'NO BUFFERS TO DETERMINE IF STD UCS/FCB'    @OZ34384
PMSGL    EQU   *-PNBUFF                                        @OZ34384
         SPACE 1                                               @OZ34384
PSUPEX   DS    0H                                              @OZ34384
         L     R1,PCEDCT           GET DCT ADDRESS FOR RETURN  @OZ34384
         L     PL,PCERETN          RESTORE RETURN REGISTER     @OZ34384
         L     BASE2,PDSVSAVE      RESTORE BASE REGISTER       @OZ34384
         BR    PL                  RETURN TO INITIALIZATION    @OZ34384
         SPACE 1                                               @OZ34384
         LTORG                                                 @OZ34384
         SPACE 1                                               @G38ESBB
 TITLE 'HASP PRINT/PUNCH SERVICE -- 3800 PQE DEALLOCATION'     @OZ48003
***************************************************************@OZ48003
*                                                              @OZ48003
*        PDELPQE -- RETURN A PQE TO THE FREE QUEUE OF ITS      @OZ48003
*                   ASSOCIATED PPQ EXTENT.                     @OZ48003
*                   DECHAIN AND FREEMAIN SECONDARY PPQ         @OZ48003
*                   EXTENTS IF EMPTY.                          @OZ48003
*                                                              @OZ48003
*        R0  - ON ENTRY, COUNT OF PQE'S TO RETURN              @OZ48003
*        R1  - ON ENTRY, ADDRESS OF FIRST PQE TO FREE          @OZ48003
*        PW  - ADDRESS OF THE PQH                              @OZ48003
*                                                              @OZ48003
*        NOTE - ON ENTRY, R0 = ZERO WILL DECHAIN TO THE        @OZ48003
*               END OF THE PENDING PAGE QUEUE.                 @OZ48003
*               ON EXIT, R1 = ADDRESS OF THE PREVIOUS PQE      @OZ48003
*               ON THE ACTIVE QUEUE.                           @OZ48003
*                                                              @OZ48003
***************************************************************@OZ48003
         SPACE 1                                               @OZ48003
         USING PQHDSECT,PW         PROVIDE PQH ADDRESSABILITY  @OZ48003
         USING PQEDSECT,R5         PROVIDE PQE ADDRESSABILITY  @OZ48003
         SPACE 1                                               @OZ48003
PDELPQE  PSAVE ALL                 SAVE CALLER'S REGISTERS     @OZ48003
         LR    BASE2,R15           ESTABLISH LOCAL ADDR        @OZ48003
         USING PDELPQE,BASE2       PROVIDE LOCAL ADDR          @OZ48003
         LR    R4,R0               SAVE PQE COUNT              @OZ48003
         LR    R5,R1               SAVE FIRST PQE ADDR         @OZ48003
         B     PQEFREE             ENTER ROUTINE               @OZ48003
         SPACE 1                                               @OZ48003
PFRECHK  LA    R15,PQHFIRST-(PQENEXT-PQEDSECT) POINT TO PQE0   @OZ48003
         CLR   R15,R5              MORE PQE'S TO FREE...       @OZ48003
         BE    PFRERETN            BRANCH IF NONE              @OZ48003
         SPACE 1                                               @OZ48003
PQEFREE  L     R15,PQEPREV                    DECHAIN PQE      @OZ48003
         MVC   PQENEXT-PQEDSECT(,R15),PQENEXT  FROM THE        @OZ48003
         L     R15,PQENEXT                      ACTIVE         @OZ48003
         MVC   PQEPREV-PQEDSECT(,R15),PQEPREV    QUEUE         @OZ48003
         L     R3,PQEHDR           POINT TO EXTENT HEADER      @OZ48003
         MVC   PQENEXT,PQHFREE-PQHDSECT(R3)  ADD PQE TO THE    @OZ48003
         ST    R5,PQHFREE-PQHDSECT(,R3)       FREE CHAIN       @OZ48003
         LR    R5,R15              POINT TO NEXT PQE           @OZ48003
         IC    R1,PQHPQECT-PQHDSECT(,R3) INCREMENT PQE         @OZ48003
         LA    R1,1(,R1)                  AVAILABLE            @OZ48003
         STC   R1,PQHPQECT-PQHDSECT(,R3)   COUNT               @OZ48003
         BCT   R4,PFRECHK          LOOP THRU CHAIN             @OZ48003
         EJECT                                                 @OZ48003
PFRERETN L     R1,PQEPREV          GET PREVIOUS PQE ADDR       @OZ48003
         L     R15,PSAVAREA        GET SAVE AREA ADDRESS       @OZ48003
         ST    R1,4+(R1*4)(,R15)   SAVE PQE ADDR FOR CALLER    @OZ48003
         SPACE 1                                               @OZ48003
         SLR   R0,R0               CHECK IF NUMBER OF          @OZ48003
         IC    R0,PQHPQELM          AVAILABLE PQE'S IN         @OZ48003
         SRL   R0,1                  PRIMARY EXTENT EXCEED     @OZ48003
         CLM   R0,1,PQHPQECT          ONE HALF OF MAXIMUM      @OZ48003
         BH    PFRERET             BRANCH IF NOT               @OZ48003
         LR    R3,PW               POINT R3 TO PQH             @OZ48003
         SPACE 1                                               @OZ48003
PFRETST  ICM   R3,15,PQHFCHN-PQHDSECT(R3) GET NEXT PPQ EXTENT  @OZ48003
         BZ    PFRERET             BRANCH IF NONE              @OZ48003
         CLI   PQHPQECT-PQHDSECT(R3),PNUMPQE TEST PQE COUNT    @OZ48003
         BL    PFRETST             BRANCH IF NOT EMPTY         @OZ48003
         SPACE 1                                               @OZ48003
***************************************************************@OZ48003
*                                                              @OZ48003
*        DECHAIN AND FREEMAIN PPQ EXTENT                       @OZ48003
*                                                              @OZ48003
***************************************************************@OZ48003
         SPACE 1                                               @OZ48003
PFREPPQX L     R0,PQHFCHN-PQHDSECT(,R3)  GET FWD CHAIN ADDR    @OZ48003
         L     R4,PQHBCHN-PQHDSECT(,R3)  GET BACK CHAIN ADDR   @OZ48003
         ST    R0,PQHFCHN-PQHDSECT(,R4)  DECHAIN PPQX          @OZ48003
         LTR   R15,R0              TEST FWD CHAIN ADDRESS      @OZ48003
         BZ    *+8                 BRANCH IF NONE              @OZ48003
         ST    R4,PQHBCHN-PQHDSECT(,R15) SET BACK CHAIN ADDR   @OZ48003
         IC    R15,PQHPCNT         DECREMENT                   @OZ48003
         BCTR  R15,0                COUNT OF                   @OZ48003
         STC   R15,PQHPCNT           PPQ EXTENTS               @OZ48003
         LH    R0,=Y(PQHLENG+PNUMPQE*PQELENG) SET EXT LENGTH   @OZ48003
         LR    R1,R3               SET EXTENT ADDRESS          @OZ48003
         FREEMAIN R,LV=(R0),A=(R1) FREE STORAGE                @OZ48003
         LR    R3,R4               SET PREVIOUS EXTENT ADDR    @OZ48003
         B     PFRETST             CHECK FOR MORE EXTENTS      @OZ48003
         SPACE 1                                               @OZ48003
PFRERET  PRETURN ,                 RETURN TO CALLER            @OZ48003
         SPACE 1                                               @OZ48003
         DROP  PW,R5               SUSPEND ADDRESSABILITY      @OZ48003
 TITLE 'HASP PRINT/PUNCH SERVICE -- 3800 PQE ALLOCATION'       @OZ48003
***************************************************************@OZ48003
*                                                              @OZ48003
*        PADDPQE -- OBTAIN NEW PAGE QUEUE ENTRY                @OZ48003
*                                                              @OZ48003
*        R1  - PQE ADDRESS ON EXIT                             @OZ48003
*        PW  - ADDRESS OF PQH ON ENTRY                         @OZ48003
*                                                              @OZ48003
*        CC = ZERO      PQE ALLOCATION UNSUCCESSFUL            @OZ48003
*        CC = NON-ZERO  PQE ALLOCATION SUCCESSFUL              @OZ48003
*                                                              @OZ48003
***************************************************************@OZ48003
         SPACE 1                                               @OZ48003
         USING PQHDSECT,PW         PROVIDE PQH ADDRESSABILITY  @OZ48003
         USING PQEDSECT,R1         PROVIDE PQE ADDRESSABILITY  @OZ48003
         SPACE 1                                               @OZ48003
PADDPQE  PSAVE ALL                 SAVE CALLER'S REGISTERS     @OZ48003
         LR    BASE2,R15           ESTABLISH LOCAL ADDR        @OZ48003
         USING PADDPQE,BASE2       PROVIDE LOCAL ADDR          @OZ48003
         NI    PQHAFLAG,FF-PQHCPIO RESET CLEARPRINT FLAG       @OZ48003
         LR    PL,PW               POINT TO PQH                @OZ48003
         SPACE 1                                               @OZ48003
PCKABORT TM    PQHAFLAG,PQHABORT   TEST FOR PPQ ERROR          @OZ49145
         BZ    PCKAVAIL            BRANCH IF NOT               @OZ49145
         LA    R14,=C'12'          POINT TO REASON CODE        @OZ49145
         SLR   PL,PL               CLEAR EXTENT ADDRESS        @OZ49145
         B     PPQWTO              BR TO INFORM OPERATOR       @OZ49145
         SPACE 1                                               @OZ49145
PCKAVAIL L     R1,PQHFREE-PQHDSECT(,PL)  GET NEXT FREE PQE     @OZ48003
         LA    R15,PQHFIRST-PQHDSECT(,PL) CHECK FOR            @OZ48003
         CR    R1,R15                      EMPTY QUEUE         @OZ48003
         BNE   PADDINIT            BRANCH IF NOT               @OZ48003
         ICM   R0,15,PQHFCHN-PQHDSECT(PL) GET NEXT EXTENT      @OZ48003
         BZ    PPQALOCC            BRANCH IF NONE              @OZ48003
         LR    PL,R0               POINT TO NEXT EXTENT        @OZ48003
         B     PCKAVAIL            CHECK NEXT EXTENT           @OZ48003
         SPACE 1                                               @OZ48003
PPQALOCC TM    PQHAFLAG,PQHALOC    TEST ALLOCATION FLAG        @OZ48003
         BO    PPQSETRC            BR IF CLRPRT SUPPRESSED     @OZ48003
         TM    PQHAFLAG,PQHCPIO    CLEARPRINT I/O COMPLETE...  @OZ48003
         BO    PPQSETRC            BRANCH IF YES               @OZ48003
         EJECT                                                 @OZ48003
PPQCPIO  LM    PC1,PC2,PCCWCP      ISSUE CLEARPRINT            @OZ48003
         BAL   PL,PPPUT             CCW TO PRINT               @OZ48003
         BAL   PL,PPWRITE            PAGE BUFFER               @OZ48003
         BAL   PL,PPCHECK             AND FREE PQE'S           @OZ48003
         L     PW,PQHADR           RESTORE PQH ADDRESS         @OZ48003
         TM    PQHAFLAG,PQHPBUF0   IS PAGE BUFFER EMPTY...     @OZ48003
         BZ    PPQCPIO             BRANCH IF NOT               @OZ48003
         OI    PQHAFLAG,PQHCPIO    IND CLEARPRINT COMPLETE     @OZ48003
         LR    PL,PW               POINT TO PQH                @OZ48003
         B     PCKABORT            RETRY PQE ALLOCATION        @OZ49145
         SPACE 1                                               @OZ48003
PADDINIT IC    R15,PQHPQECT-PQHDSECT(,PL) DECREMENT            @OZ48003
         BCTR  R15,0                       AVAILABLE           @OZ48003
         STC   R15,PQHPQECT-PQHDSECT(,PL)   PQE COUNT          @OZ48003
         XC    PQEDATA,PQEDATA     CLEAR THE PQE               @OZ48003
         MVC   PQHFREE-PQHDSECT(,PL),PQENEXT  DECHAIN PQE      @OZ48003
         LA    R15,PQHFIRST-(PQENEXT-PQEDSECT) POINT TO PQH    @OZ48003
         ST    R15,PQENEXT         CHAIN PQE TO PQH            @OZ48003
         CL    R15,PQHPIDE         INITIALIZE PQE              @OZ48003
         BNE   *+8                  PENDING ID                 @OZ48003
         ST    R1,PQHPIDE            IF NECESSARY              @OZ48003
         L     R15,PQHLAST             INSERT NEW              @OZ48003
         ST    R15,PQEPREV              PQE TO THE             @OZ48003
         ST    R1,PQENEXT-PQEDSECT(,R15) END OF THE            @OZ48003
         ST    R1,PQHLAST                 ACTIVE QUEUE         @OZ48003
         SPACE 1                                               @OZ48003
PADDRET  L     R15,PSAVAREA        GET SAVE AREA ADDRESS       @OZ48003
         ST    R1,4+(R1*4)(,R15)   SAVE PQE ADDR FOR CALLER    @OZ48003
         LTR   R1,R1               SET COND CODE FOR CALLER    @OZ48003
         PRETURN ,                 RETURN TO CALLER            @OZ48003
         SPACE 1                                               @OZ48003
         DROP  R1                  SUSPEND PQE ADDRESSABILITY  @OZ48003
         EJECT                                                 @OZ48003
***************************************************************@OZ48003
*                                                              @OZ48003
*        PPQALLOC -- OBTAIN, FORMAT, AND CHAIN A NEW           @OZ48003
*                    PENDING PAGE QUEUE EXTENT                 @OZ48003
*                                                              @OZ48003
***************************************************************@OZ48003
         SPACE 1                                               @OZ48003
PPQALLOC LH    R1,=Y(PQHLENG+PNUMPQE*PQELENG) SET EXTENT SIZE  @OZ48003
         GETMAIN RC,LV=(R1)        ATTEMPT GETMAIN             @OZ48003
         LTR   R15,R15             TEST RETURN CODE            @OZ48003
         BZ    PPQAGET1            BRANCH IF GOTTEN            @OZ48003
         SLR   PL,PL               CLEAR EXTENT ADDRESS        @OZ48003
         LA    R14,=C'8 '          POINT TO REASON CODE        @OZ48003
         B     PPQWTO              INFORM OPERATOR             @OZ48003
         SPACE 1                                               @OZ48003
PPQAGET1 XC    0(PQHLENG,R1),0(R1) CLEAR EXTENT HEADER         @OZ48003
         ST    R1,PQHFCHN-PQHDSECT(,PL)    ADD NEW EXTENT      @OZ48003
         ST    PL,PQHBCHN-PQHDSECT(,R1)     IN THE CHAIN       @OZ48003
         MVI   PQHPQECT-PQHDSECT(R1),PNUMPQE  SET AVAIL CNT    @OZ48003
         MVI   PQHPQELM-PQHDSECT(R1),PNUMPQE   AND LIMIT       @OZ48003
         LA    R15,PQHEND-PQHDSECT(,R1)  POINT TO FIRST PQE    @OZ48003
         ST    R15,PQHFREE-PQHDSECT(,R1) SET FREE CHN POINTER  @OZ48003
         SPACE 1                                               @OZ48003
         USING PQEDSECT,R15        PROVIDE PQE ADDRESSABILITY  @OZ48003
         LA    R0,PNUMPQE          SET PQE COUNT               @OZ48003
         BCTR  R0,0                DECREMENT COUNT OF PQE'S    @OZ48003
PPQINIT  LA    R14,PQEEND          POINT TO NEXT PQE           @OZ48003
         ST    R14,PQENEXT         SET FORWARD CHAIN           @OZ48003
         ST    R1,PQEHDR           SET EXTENT HEADER ADDR      @OZ48003
         LR    R15,R14             BUMP TO NEXT PQE            @OZ48003
         BCT   R0,PPQINIT          LOOP THRU ALL PQE'S         @OZ48003
         ST    R1,PQENEXT          POINT LAST PQE TO HEADER    @OZ48003
         ST    R1,PQEHDR           SET EXTENT HEADER ADDR      @OZ48003
         DROP  R15                 SUSPEND PQE ADDRESSABILITY  @OZ48003
         IC    R15,PQHPCNT         INCREMENT                   @OZ48003
         LA    R15,1(,R15)          COUNT OF                   @OZ48003
         STC   R15,PQHPCNT           PQE EXTENTS               @OZ48003
         LR    PL,R1               POINT TO NEW EXTENT         @OZ48003
         B     PCKAVAIL            RETRY PQE ALLOCATON         @OZ48003
         EJECT                                                 @OZ48003
PPQSETRC LA    R14,=C'4 '          POINT TO REASON CODE        @OZ48003
         TM    PPFLAG3,PPQSPND     ALLOC RCVY SUSPENDED...     @OZ48003
         BO    PPQCKEND            BRANCH IF YES               @OZ48003
         SPACE 1                                               @OZ48003
         USING DCTDSECT,R4         PROVIDE DCT ADDRESSABILITY  @OZ48003
PPQWTO   L     R4,PCEDCT           SET DCT ADDRESS             @OZ48003
         L     JCT,PJCTBUF         SET JCT ADDRESS             @OZ48003
         SPACE 1                                               @OZ48003
         $MID  158                                             @OZ48003
         MVC   PMESSAGE(2),=X'158F'     MOVE MSG ID            @OZ48003
         MVC   PMESSAGE+2(8),DCTDEVN    MOVE DEVICE NAME       @OZ48003
         MVC   PMESSAGE+10(29),PPQAMSG  MOVE MSG TEXT          @OZ48003
         MVC   PMESSAGE+33(2),0(R14)    SET REASON CODE        @OZ48003
         SPACE 1                                               @OZ48003
         LTR   PL,PL               TEST CURRENT EXTENT         @OZ48003
         BZ    PPQWTO1             BRANCH IF NONE              @OZ48003
         OI    DCTFLAGS,DCTSTOP    HALT THE DEVICE             @OZ48003
         $WTO  PMESSAGE,39,JOB=YES,  INFORM OPERATOR           @OZ48003C
               ROUTE=$LOG+$UR,CLASS=$DOMACT,PRI=$ST            @OZ48003
         SPACE 1                                               @OZ48003
PPQAWAIT TM    DCTFLAGS,DCTSTOP    TEST FOR $S DEVICE          @OZ48003
         BZ    PPQADOM             BRANCH IF YES               @OZ48003
         NI    DCTFLAGS,FF-DCTDELET-DCTRSTRT-DCTBKSP  RESET    @OZ48003
         $WAIT IO                  WAIT FOR POST FROM COMM     @OZ48003
         B     PPQAWAIT            BRANCH TO TEST FLAGS        @OZ48003
         SPACE 1                                               @OZ48003
PPQADOM  $DOM  CMB=(R1)            DELETE OPERATOR MESSAGE     @OZ48003
         B     PPQCMDCK            BR TO CHECK FOR CMDS        @OZ48003
         SPACE 1                                               @OZ48003
PPQWTO1  $WTO  PMESSAGE,39,JOB=YES,  INFORM OPERATOR           @OZ48003C
               ROUTE=$LOG+$UR,CLASS=$NORMAL,PRI=$ST            @OZ48003
         SPACE 1                                               @OZ49145
         TM    PQHAFLAG,PQHABORT   TEST FOR PPQ ERROR          @OZ49145
         BO    PPQTERM             BRANCH IF YES               @OZ49145
         NI    DCTFLAGS,FF-DCTDELET RESET DELETE FLAG          @OZ48003
         OI    DCTFLAGS,DCTRSTRT+DCTBKSP IND INTERRUPTED       @OZ48003
         EJECT                                                 @OZ48003
PPQCMDCK TM    DCTFLAGS,DCTDELET+DCTRSTRT TERMINATION CMD...   @OZ48003
         BZ    PPQALLOC            BRANCH IF NOT               @OZ48003
         OI    PPFLAG,PPDELSW+PRDELSW CAUSE TERMINATION        @OZ48003
         OC    PDCTFLAG,DCTFLAGS   PROVIDE TERMINATION REASON  @OZ48003
         NI    DCTFLAGS,FF-DCTDELET-DCTRSTRT-DCTBKSP  RESET    @OZ48003
         OI    PPFLAG3,PP3800R+PPQSPND SET CMD FLAGS           @OZ48003
         L     R1,PQHLAST          POINT TO LAST PQE           @OZ48003
         SPACE 1                                               @OZ48003
         USING PQEDSECT,R1         SET PQE ADDRESSABILITY      @OZ48003
PPQCHK   CR    R1,PW               CHECK FOR END OF PPQ        @OZ48003
         BE    PPQCKEND            BRANCH IF YES               @OZ48003
         CLI   PQETYPE,PQEC        TEST FOR PQEC               @OZ48003
         BNE   PPQCHK1             BRANCH IF NOT               @OZ48003
         L     PL,PQECPQED         GET PQED ADDRESS            @OZ48003
         CLC   PQEDWJOE-PQEDSECT(,PL),PWKJOE  TEST JOE ADDR    @OZ48003
         BNE   PPQCKEND            BRANCH IF NOT CURRENT       @OZ48003
         OI    PQEDFLAG-PQEDSECT(PL),PQEDLAST  IND LAST DS     @OZ48003
         OI    PQECFLAG,PQECLPG    IND LAST PAGE OF DATASET    @OZ48003
         OI    PQHFLAG,PQHDSVC     IND RESET NEEDED            @OZ48003
         TM    PDCTFLAG,DCTDELET   TEST FOR $C CMD             @OZ48003
         BZ    PPQCKINT            BRANCH IF NOT               @OZ48003
         OI    PQEDFLAG-PQEDSECT(PL),PQEDCAN SET DS CANCELLED  @OZ48003
         B     PPQCKEND            BRANCH TO RETURN            @OZ48003
         SPACE 1                                               @OZ48003
PPQCKINT TM    PDCTFLAG,DCTBKSP    TEST FOR $I CMD             @OZ48003
         BZ    PPQRSTRT            BR IF NOT, PROCESS $E CMD   @OZ48003
         OI    PQEDFLAG-PQEDSECT(PL),PQEDINT SET DS INTERRUPT  @OZ48003
         B     PPQSETCT            BRANCH TO SET COUNT         @OZ48003
         SPACE 1                                               @OZ48003
PPQRSTRT $#ADD WORK=PWKJOE,CHAR=PCHJOE  REQUEUE THE JOE        @OZ48003
         BZ    PPQCKEND            BRANCH IF SUCCESSFUL        @OZ48003
         OI    PQEDFLAG-PQEDSECT(PL),PQEDRST  DEFER REQUEUE    @OZ48003
PPQSETCT IC    R15,PQHCMDCT        INCREMENT COUNT             @OZ48003
         LA    R15,1(,R15)          OF DEFERRED                @OZ48003
         STC   R15,PQHCMDCT          COMMANDS                  @OZ48003
         B     PPQCKEND            BRANCH TO RETURN            @OZ48003
         EJECT                                                 @OZ48003
PPQCHK1  CLI   PQETYPE,PQED        TEST FOR PQED               @OZ48003
         BE    PPQCHK2             BRANCH IF YES               @OZ48003
         L     R1,PQEPREV          GET PREVIOUS PQE ADDRESS    @OZ48003
         B     PPQCHK              BR TO CHECK PREVIOUS PQE    @OZ48003
         SPACE 1                                               @OZ48003
PPQCHK2  LA    R0,1                SET PQE COUNT               @OZ48003
         L     R15,=A(PDELPQE)     CALL SUBROUTINE TO          @OZ48003
         BALR  PL,R15               DELETE PQED                @OZ48003
         B     PPQCHK              BR TO CHECK PREVIOUS PQE    @OZ48003
         SPACE 1                                               @OZ48003
PPQCKEND SLR   R1,R1               SHOW NO PQE ALLOCATED       @OZ48003
         B     PADDRET             RETURN                      @OZ48003
         SPACE 1                                               @OZ49145
PPQTERM  OI    PPFLAG3,PP3800R     SET COMMAND FLAG            @OZ49145
         OI    PQHFLAG,PQHIPPQM+PQHRSTRT SET RESTART FLAGS     @OZ49145
         LM    PC1,PC2,PCCWCP      ISSUE CLEARPRINT            @OZ49145
         BAL   PL,PPPUT             TO PRINT PAGE BUFFER       @OZ49145
         BAL   PL,PPWRITE            AND WAIT FOR ALL          @OZ49145
         BAL   PL,PPCHECK             I/O TO COMPLETE          @OZ49145
         L     R4,PCEDCT           RELOAD DCT ADDRESS          @OZ49145
         XC    DCTCSW,DCTCSW       CLEAR RESTART CSW           @OZ49145
         NI    PPFLAG,FF-PPNEWS    RESET NEWS FLAG             @OZ49145
         B     PPDSEND             BR TO RETURN SAVE AREAS     @OZ49145
         SPACE 1                                               @OZ49145
PPQAMSG  DC    CL29' PPQ SHORTAGE - CODE = X '                 @OZ48003
         SPACE 1                                               @OZ48003
PNUMPQE  EQU   150                 NUMBER OF PQE'S IN EXTENT   @OZ48003
         SPACE 1                                               @OZ48003
         DROP  R1,PW,R4            SUSPEND ADDRESSABILITY      @OZ48003
         SPACE 1                                               @OZ48003
         PRINT OFF                 THIS SECTION DELETED BY     @OZ48003
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
         SPACE 1                                               @OZ26939
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
         PRINT ON                  THIS SECTION DELETED BY     @G38ESBB
 TITLE 'HASP PRINT/PUNCH SERVICE -- 3800 PQEC INITIALIZATION'  @G38ESBB
***************************************************************@G38ESBB
*                                                              @G38ESBB
*        ACQUIRE AND INITIALIZE 3800 CHECKPOINT                @G38ESBB
*        PAGE QUEUE ENTRY (PQEC)                               @G38ESBB
*                                                              @G38ESBB
*        R1  - ADDRESS OF PQED (ON EXIT)                       @G38ESBB
*        PW  - ADDRESS OF PENDING PAGE QUEUE HEADER            @G38ESBB
*                                                              @G38ESBB
***************************************************************@G38ESBB
         SPACE 1                                               @G38ESBB
         USING PQECINIT,BASE2      PROVIDE LOCAL ADR           @G38ESBB
         USING PQHDSECT,PW         PROVIDE PQH ADDRESSABILITY  @G38ESBB
         SPACE 1                                               @G38ESBB
PQECINIT PSAVE ALL                 SAVE CALLER'S REGISTERS     @G38ESBB
         LR    BASE2,R15           ESTABLISH LOCAL ADR         @G38ESBB
         SPACE 1                                               @G38ESBB
PGETPQEC L     PW,PQHADR           ADDRESS PQH                 @G38ESBB
         L     R15,=A(PADDPQE)     CALL SUBROUTINE TO          @OZ48003
         BALR  PL,R15               ALLOCATE A PQE             @OZ48003
         BNZ   PGOTPQEC            BRANCH IF SUCCESSFUL        @OZ48003
         SPACE 1                                               @OZ48003
         NI    PQHAFLAG,FF-PQHALOC RESET ALLOCATION FLAG       @OZ48003
         SLR   R1,R1               SHOW NO PQE ALLOCATED       @OZ48003
         B     PQECRETN             AND RETURN                 @OZ48003
         SPACE 1                                               @G38ESBB
         USING PQEDSECT,R1         PROVIDE PQE ADDRESSABILITY  @G38ESBB
PGOTPQEC MVI   PQETYPE,PQEC        INDICATE CKPT PQE           @G38ESBB
         L     R15,PQEPREV         POINT PQEC TO               @G38ESBB
         MVC   PQECPQED,PQECPQED-PQEDSECT(R15) PQED            @G38ESBB
         MVC   PQECJRCB,PCEEJRCB   SET INIT BUFFER OFFSET      @G38ESBB
         MVC   PQECPPCT,PDDBPGCT   SET PDDB LOGICAL PAGE COUNT @G38ESBB
         MVC   PQECTLNC,PPLNCDCT   SET TOTAL JOE LINE COUNT    @G38ESBB
         MVC   PQECTPCT,PRPAGECT   SET TOTAL JOE PAGE COUNT    @G38ESBB
         MVC   PQECMTTR,PCEJMTTR   SET MTTR OF SPOOL DATA      @G38ESBB
         NI    PQHAFLAG,FF-PQHALOC RESET ALLOCATION FLAG       @OZ48003
         SPACE 1                                               @OZ48003
PQECRETN LTR   R1,R1               SET CONDITION CODE          @OZ48003
         PRETURN ,                 RESTORE REGS AND RETURN     @G38ESBB
         SPACE 1                                               @G38ESBB
         DROP  PW,R1               DROP PQH, PQE ADDR          @G38ESBB
 TITLE 'HASP PRINT/PUNCH SERVICE -- 3800 PQED INITIALIZATION'  @G38ESBB
***************************************************************@G38ESBB
*                                                              @G38ESBB
*        ACQUIRE AND INITIALIZE 3800 DATA SET                  @G38ESBB
*        PAGE QUEUE ENTRY (PQED)                               @G38ESBB
*                                                              @G38ESBB
***************************************************************@G38ESBB
         SPACE 1                                               @G38ESBB
         USING PQEDINIT,BASE2      PROVIDE LOCAL ADR           @G38ESBB
         USING PQHDSECT,PW         PROVIDE PQH ADDRESSABILITY  @G38ESBB
         USING PQEDSECT,R1         PROVIDE PQE ADDRESSABILITY  @G38ESBB
         SPACE 1                                               @G38ESBB
PQEDINIT PSAVE ALL                 SAVE CALLER'S REGISTERS     @G38ESBB
         LR    BASE2,R15           ESTABLISH LOCAL ADR         @G38ESBB
         SPACE 1                                               @G38ESBB
PGETPQED L     PW,PQHADR           ADDRESS PQH FOR SUB         @G38ESBB
         NI    PQHAFLAG,FF-PQHALOC RESET ALLOCATION FLAG       @OZ48003
         L     R15,=A(PADDPQE)     CALL SUBROUTINE TO          @OZ48003
         BALR  PL,R15               ALLOCATE A PQE             @OZ48003
         BNZ   PGOTPQED            BRANCH IF SUCCESSFUL        @OZ48003
         SPACE 1                                               @OZ48003
         SR    R1,R1               SHOW NO PQE ALLOCATED       @OZ48003
         B     PQEDRET              AND RETURN                 @OZ48003
         SPACE 1                                               @G38ESBB
PGOTPQED MVI   PQETYPE,PQED        INDICATE DATA SET PQE       @G38ESBB
         MVC   PQEDCOPY,PPRCPYCT   SET COPY NUMBER             @G38ESBB
         SLR   PL,PL               ZERO INDEX REGISTER         @G38ESBB
         IC    PL,PDDBCPYG         GET OFFSET INTO COPY GROUP  @G38ESBB
         IC    PL,PCOPYGRP(PL)     GET COPY GROUP NUMBER       @G38ESBB
         LTR   PL,PL               IF NO COPYGROUPING          @G38ESBB
         BNZ   *+8                  SPECIFIED, SET             @G38ESBB
         LA    PL,1                  COPYGROUP COUNT TO 1      @G38ESBB
         STC   PL,PQEDCGCT         SET COPYGROUP COUNT         @G38ESBB
         MVC   PQEDCPYG,PDDBCPYG   SET OFFSET INTO COPY GROUP  @G38ESBB
         MVC   PQEDTNDS,PPJNDS     SET TOTAL JOE DATA SET CNT  @G38ESBB
         MVC   PQEDWJOE,PWKJOE     SET JOE ADDRESS             @G38ESBB
         MVC   PQEDIOTR,PCEIOTTR   SET CURRENT IOT TRACK ADR   @G38ESBB
         MVC   PQEDJKEY,PPJOBKEY   SET JOB IDENTIFIER KEY      @G38ESBB
         MVC   PQEDPDDB,PDDBDISP   SET DISP OF PDDB INTO IOT   @G38ESBB
         MVC   PQEDCGRP,PCOPYGRP   SET DATASET COPY GROUPS     @OZ49282
         MVC   PQEDSCPY,PPDSCPY    SET DATASET COPY COUNT      @OZ49282
         MVC   PQEDSKEY,PPDSKEY    SET DATASET KEY             @OZ49282
         TM    PPFLAG,PPDALOC      ALLOCATION IOT...           @G38ESBB
         BZ    *+8                 NO, BYPASS ALOC FLAG        @G38ESBB
         OI    PQEDFLAG,PQEDALOC   INDICATE ALLOCATION IOT     @G38ESBB
         EJECT                                                 @OZ49282
***************************************************************@G38ESBB
*                                                              @G38ESBB
*        ACQUIRE PAGE QUEUE ENTRY FOR BEGINNING OF DATA SET    @G38ESBB
*                                                              @G38ESBB
***************************************************************@G38ESBB
         SPACE 1                                               @G38ESBB
         L     R15,=A(PQECINIT)    CALL PQECINIT TO ACQUIRE    @G38ESBB
         BALR  PL,R15               AND INIT FIRST PAGE PQEC   @G38ESBB
         BZ    PQEDRET             BRANCH IF NOT SUCCESSFUL    @OZ48003
         L     R15,=A(PPGIDIO)     CALL PPGIDIO TO SOLICIT     @G38ESBB
         BALR  PL,R15               ID FOR FPG PQEC            @G38ESBB
         L     PW,PQHADR           RESTORE PQH ADDRESS         @G38ESBB
         L     R1,PQHLAST          ADDRESS PQEC                @G38ESBB
         L     R15,PQEPREV         ADDRESS PQED                @G38ESBB
         ST    R15,PQECPQED        POINT PQEC TO PQED          @G38ESBB
         OI    PQECFLAG,PQECFPG    SET BEGIN DATA SET          @G38ESBB
         TM    PCKJOE,$JOECKV      DATA SET WARMSTART          @G38ESBB
         BZ    PQEDRETC            NO, RETURN TO CALLER        @OZ48003
         BAL   PL,PPWRITE          SCHEDULE I/O TO SOLICIT ID  @G38ESBB
         BAL   PL,PPCHECK          CHECK I/O COMPLETION        @G38ESBB
         SPACE 1                                               @G38ESBB
***************************************************************@G38ESBB
*                                                              @G38ESBB
*        ACQUIRE AND INITIALIZE WARMSTART PQE                  @G38ESBB
*                                                              @G38ESBB
***************************************************************@G38ESBB
         SPACE 1                                               @G38ESBB
         L     R0,PDDBPGCT         GET WARMSTART PAGE COUNT    @G38ESBB
         LCR   R0,R0               COMPLEMENT FOR RECOMPUTE    @G38ESBB
         L     R15,=A(PRECOMP)     CALL RECOMPUTE TO SHOW PAGE @G38ESBB
         BALR  PL,R15               DELTA TO WARMSTART POINT   @G38ESBB
         SPACE 1                                               @G38ESBB
PGETPQEW L     PW,PQHADR           ADDRESS THE PQH             @G38ESBB
         NI    PQHAFLAG,FF-PQHALOC RESET ALLOCATION FLAG       @OZ48003
         L     R15,=A(PADDPQE)     CALL SUBROUTINE TO          @OZ48003
         BALR  PL,R15               ALLOCATE A PQE             @OZ48003
         BNZ   PGOTPQEW            BRANCH IF SUCCESSFUL        @OZ48003
         SPACE 1                                               @OZ48003
         SR    R1,R1               SHOW NO PQE ALLOCATED       @OZ48003
         B     PQEDRET              AND RETURN                 @OZ48003
         SPACE 1                                               @G38ESBB
PGOTPQEW MVC   PQHPIDE,PQENEXT     SET PQE PENDING ID TO PQE0  @G38ESBB
         L     R15,PQEPREV         ADDRESS FPG PQEC            @G38ESBB
         MVC   PQETYPE(PQEEND-PQETYPE),PQETYPE-PQEDSECT(R15)   @G38ESBBC
                                   MOVE FPG PQEC TO WRMST PQEC @G38ESBB
         NI    PQECFLAG,FF-PQECFPG RESET FPG IN WARMST PQEC    @G38ESBB
         MVC   PQERPGID,PQECPGID   SHOW PAGE NUMBER OF WARMST  @G38ESBB
         MVC   PQECMTTR-PQEDSECT(,R15),PPLC SET BEGIN DS MTTR  @G38ESBB
         MVC   PQECPPCT-PQEDSECT(,R15),$ZEROS CLEAR PAGE COUNT @G38ESBB
         MVC   PQECJRCB-PQEDSECT(,R15),PCCW+2 SET INIT BUF OFF @G38ESBB
         EJECT                                                 @OZ49282
PQEDRETC LTR   R1,R1               SET CONDITION CODE          @OZ48003
PQEDRET  PRETURN ,                 RESTORE REGS AND RETURN     @G38ESBB
         DROP  PW,R1               DROP PQH, PQE ADDR          @G38ESBB
 TITLE 'HASP PRINT/PUNCH SERVICE -- 3800 PAGE ID I/O ROUTINE'  @G38ESBB
***************************************************************@G38ESBB
*                                                              @G38ESBB
*        ISSUE I/O TO SENSE HARDWARE PAGE ID AND FCB LINE      @G38ESBB
*        POSITION FOR PQE                                      @G38ESBB
*                                                              @G38ESBB
***************************************************************@G38ESBB
         SPACE 1                                               @G38ESBB
PPGIDIO  PSAVE ALL                 SAVE CALLER'S REGISTERS     @G38ESBB
         LR    BASE2,R15           ESTABLISH LOCAL ADR         @G38ESBB
         USING PPGIDIO,BASE2       PROVIDE LOCAL ADR           @G38ESBB
         SPACE 1                                               @G38ESBB
PPGIOCK  LH    PW,PCCWLAST         GET OFFSET TO PCIE          @OZ46856
         AL    PW,POUTCCWA         ADD CCW AREA BASE           @OZ46856
         TM    PCISGNAL-PCIDSECT(PW),PCIACTIV  TEST CCW AREA   @OZ46856
         BZ    PPGCCWCK            BRANCH IF NOT ACTIVE        @OZ46856
         TM    PPFLAG,PPWSW        TEST IF WRITE IN PROGRESS   @OZ46856
         BZ    PPGCCWCK            BRANCH IF NOT               @OZ46856
         BAL   PL,PCIWAIT          ELSE, WAIT FOR I/O          @OZ46856
         B     PPGIOCK             CHECK NEW CCW AREA          @OZ46856
         SPACE 1                                               @OZ46856
PPGCCWCK LA    PW,PCIESIZE(,PW)    ADDRESS BFW                 @OZ46856
         L     PC1,PCCWPT          ADDRESS LAST CCW IN AREA    @OZ46856
         LA    PC1,4*8+RPISIBSZ+PCIESIZE(,PC1) CAN TIC,RPI,    @OZ46856
         CR    PC1,PW               AND SIB FIT IN AREA...     @OZ46856
         BL    PPGBRPI             YES, GO BUILD RPI CCW       @OZ46856
         BAL   PL,PPWRITE          NO, WRITE THIS AREA         @OZ46856
         B     PPGIOCK             CHECK NEW CCW AREA          @OZ46856
         SPACE 1                                               @G38ESBB
PPGBRPI  IC    R1,BFWPQECT-BFWDSECT(,PW) INCREMENT COUNT       @G38ESBB
         LA    R1,1(,R1)                   OF ID'S SOLICITED   @G38ESBB
         STC   R1,BFWPQECT-BFWDSECT(,PW)     IN THIS AREA      @G38ESBB
*                                  THIS LINE DELETED BY APAR   @OZ46142
         LM    PC1,PC2,PCCWXORD    CALL                        @G38ESBB
         LH    PW,PCCWLAST          PPPUT                      @G38ESBB
         AL    PW,POUTCCWA           TO ISSUE                  @G38ESBB
         LA    PW,PCIESIZE(,PW)       THE REQUEST              @G38ESBB
         LA    PW,BFWRPI-BFWDSECT(,PW) PRINTER INFORMATION     @G38ESBB
         ALR   PC1,PW                   ORDER OF THE           @G38ESBB
         BAL   PL,PPPUT2                 EXECUTE ORDER CCW     @OZ51441
         SPACE 1                                               @G38ESBB
         L     PC1,PCCWPT          ADDRESS CURRENT CCW         @G38ESBB
         LA    PW,12(,PC1)         ADDRESS RIGHT HALF OF TIC   @G38ESBB
         L     R1,PQHADR           ADDRESS PQH                 @G38ESBB
         L     R1,PQHLAST-PQHDSECT(,R1) ADDRESS CURRENT PQE    @G38ESBB
         ST    PW,PQECSENS-PQEDSECT(,R1) SET PTR TO SENSE INFO @G38ESBB
         LA    PC1,16(,PC1)        BUILD DUMMY                 @G38ESBB
         AL    PC1,PCNOPTIC+8        TIC CCW                   @G38ESBB
         SLR   PC2,PC2             INITIALIZE SENSE            @G38ESBB
         BCTR  PC2,0                AREA TO FF'S               @G38ESBB
         BAL   PL,PPPUT2           ADD DUMMY TIC CCW           @OZ51441
         SPACE 1                                               @G38ESBB
         LM    PC1,PC2,PCCWSIB     GET SENSE INT BUF CCW       @G38ESBB
         L     PW,PCCWPT           ADDRESS TIC CCW             @G38ESBB
         LA    PW,4(,PW)           ADDRESS RIGHT HALF OF TIC   @G38ESBB
         ALR   PC1,PW              POINT CCW TO TIC + 4        @G38ESBB
         IC    PC2,=X'04'          CHANGE LENGTH TO 4          @G38ESBB
         BAL   PL,PPPUT2           ADD SENSE INT BUF CCW       @OZ51441
         SPACE 1                                               @G38ESBB
         PRETURN ,                 RESTORE REGS AND RETURN     @G38ESBB
 TITLE 'HASP PRINT/PUNCH SERVICE -- PPQ MANAGEMENT ROUTINE'    @G38ESBB
***************************************************************@G38ESBB
*                                                              @G38ESBB
*        3800 PRINTER PENDING PAGE QUEUE MANAGEMENT ROUTINE    @G38ESBB
*                                                              @G38ESBB
*        R0  - STACKED PAGE ID                                 @G38ESBB
*        PW  - PQH ADDRESS                                     @G38ESBB
*        R3  - PQE ADDRESS                                     @G38ESBB
*        R4  - STACKED PAGE ID                                 @G38ESBB
*        R5  - PQE COUNT                                       @G38ESBB
*        R10 - ADDRESS OF PQE TO CHECKPOINT                    @G38ESBB
*                                                              @G38ESBB
***************************************************************@G38ESBB
         SPACE 1                                               @G38ESBB
         USING PPQMGR,BASE2        PROVIDE LOCAL ADR           @G38ESBB
         USING PQHDSECT,PW         PROVIDE PQH ADDRESSABILITY  @G38ESBB
         USING PQEDSECT,R3         PROVIDE PQE ADDRESSABILITY  @G38ESBB
         SPACE 1                                               @G38ESBB
PPQMGR   PSAVE ALL                 SAVE CALLER'S REGISTERS     @G38ESBB
         LR    BASE2,R15           SET UP LOCAL BASE           @G38ESBB
         L     PW,PQHADR           ADDRESS THE PQH             @G38ESBB
         TM    PQHFLAG,PQHIPPQM    TEST IF INHIBITED           @OZ46674
         BO    PPQRET              BRANCH IF YES               @OZ46674
         LR    R4,R0               SAVE STACKER PAGE ID        @G38ESBB
         SLR   R5,R5               INITIALIZE PQE COUNT TO 0   @G38ESBB
         SLR   R10,R10             INIT ADR OF PQE FOR CKPT    @G38ESBB
         LA    R3,PQHFIRST-(PQENEXT-PQEDSECT) ADDRESS PQE0     @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
         SPACE 1                                               @G38ESBB
PNXTPQE  L     R3,PQENEXT          ADDRESS NEXT PQE            @G38ESBB
         LA    R15,PQHFIRST-(PQENEXT-PQEDSECT) GET PQE0        @G38ESBB
         CR    R3,R15              END OF PPQ...               @G38ESBB
         BE    PPQENDQ             YES, DONE, BRANCH           @G38ESBB
         TM    PQHFLAG,PQHDRAIN    TEST IF DRAINING            @OZ49145
         BO    PNXTPQE1            BRANCH IF YES               @OZ49145
         C     R3,PQHPIDE          ID PENDING FOR THIS PQE...  @G38ESBB
         BE    PPQENDQ             YES, DONE, BRANCH           @G38ESBB
PNXTPQE1 LA    R5,1(,R5)           INCREMENT PQE COUNT         @OZ49145
         CLI   PQETYPE,PQED        IS PQE A DATA SET PQE...    @G38ESBB
         BE    PNXTPQE             YES, GO GET NEXT PQE        @G38ESBB
         CLI   PQETYPE,PQEC        IS PQE A CHECKPOINT PQE...  @G38ESBB
         BE    PSTKCHK             YES, GO CHECK IF STACKED    @G38ESBB
         EJECT                                                 @OZ48003
***************************************************************@G38ESBB
*                                                              @G38ESBB
*        QUEUE SMF6 BUFFER FOR PQES AT STACKER                 @G38ESBB
*                                                              @G38ESBB
***************************************************************@G38ESBB
         SPACE 1                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
         CLI   PQETYPE,PQES        IS PQE AN SMF PQE...        @G38ESBB
         BNE   PNXTPQE             NO, PQEJ, GO GET NEXT PQE   @G38ESBB
         L     R1,PQESBUF          ADDRESS SMF BUFFER          @G38ESBB
        $QUESMFB                   QUEUE TYPE 6 FOR WRITE      @G38ESBB
         SPACE 1                                               @G38ESBB
         LR    R1,R3               ADDRESS PQE TO DELETE       @G38ESBB
         LA    R0,1                SET PQE DELETE COUNT TO 1   @G38ESBB
         L     R15,=A(PDELPQE)     CALL PDELPQE                @OZ48003
         BALR  PL,R15               TO DELETE PQE              @OZ48003
         LR    R3,R1               ADR PREVIOUS PQE SO THAT    @G38ESBBC
                                   PNXTPQE WILL ADR NEXT PQE   @G38ESBB
         BCTR  R5,0                DECREMENT PQE COUNT         @G38ESBB
         B     PNXTPQE             GO GET NEXT PQE             @G38ESBB
         SPACE 1                                               @G38ESBB
***************************************************************@G38ESBB
*                                                              @G38ESBB
*        PROCESS STACKED PQEC'S                                @G38ESBB
*                                                              @G38ESBB
***************************************************************@G38ESBB
         SPACE 1                                               @G38ESBB
PSTKCHK  TM    PQECFLAG,PQECBSP    IS PQE ALREADY STACKED...   @G38ESBB
         BO    PNXTPQE             YES, GO GET NEXT PQE        @G38ESBB
         TM    PQHFLAG,PQHDRAIN    TEST IF DRAINING            @OZ49145
         BO    PSTKCHK1            BRANCH IF YES               @OZ49145
         LH    R15,PQECPGID        GET PQE ID                  @G38ESBB
         LR    R0,R4               GET STACKER PAGE ID         @G38ESBB
         SR    R0,R15              COMPARE                     @G38ESBB
         N     R0,PCLRHALF          PQE ID WITH                @G38ESBB
         C     R0,PQELIMIT           STACKER PAGE ID           @G38ESBB
         BH    PCKPGID             BRANCH IF PQE NOT STACKED   @OZ49145
PSTKCHK1 TM    PQECFLAG,PQECLPG    END OF DATASET...           @OZ49145
         BO    PPQEODS             YES, BRANCH                 @G38ESBB
         LR    R10,R3              SAVE PQE ADR FOR CHECKPOINT @G38ESBB
         TM    PQECFLAG,PQECFPG    BEGINNING OF DATA SET...    @G38ESBB
         BZ    PQEDPREV            NO,GO DELETE PREVIOUS PQEC  @G38ESBB
         OI    PQECFLAG,PQECBSP    IND BEGIN DS PQEC STACKED   @G38ESBB
         B     PNXTPQE             GO GET NEXT PQE             @G38ESBB
         EJECT                                                 @OZ48003
***************************************************************@G38ESBB
*                                                              @G38ESBB
*        LAST PAGE OF DATA SET                                 @G38ESBB
*                                                              @G38ESBB
*        IF LAST DATA SET OF JOE, PROCESS ANY DEFERRED CMDS    @G38ESBB
*                                                              @G38ESBB
***************************************************************@G38ESBB
         SPACE 1                                               @G38ESBB
PPQEODS  SLR   R10,R10             INDICATE NO PQE FOR CKPT    @G38ESBB
         L     PL,PQECPQED         ADDRESS DATA SET PQE        @G38ESBB
         TM    PQEDFLAG-PQEDSECT(PL),PQEDLAST LAST DATA SET... @G38ESBB
         BZ    PDELDS              NO, GO DELETE PQE'S FOR DS  @G38ESBB
         CLC   PCEJQE,$ZEROS       TEST IF CURRENT JOB         @OZ48530
         BE    PPQEODS1            BRANCH IF NONE              @OZ48530
         CLC   PQEDWJOE-PQEDSECT(,PL),PWKJOE  TEST JOE ADDR    @OZ48530
         BE    PPQENDQ             BRANCH IF NOT COMPLETED     @OZ48530
PPQEODS1 TM    PQEDFLAG-PQEDSECT(PL),PQEDINT+PQEDRST+PQEDRPT   @OZ48530C
                                   DEFERRED $I, $E, $N...      @G38ESBB
         BZ    PPQSPUR             NO,GO CHECK FOR SPIN DS     @G38ESBB
         IC    R15,PQHCMDCT        DECREMENT                   @G38ESBB
         BCTR  R15,0                COUNT OF DEFERRED          @G38ESBB
         STC   R15,PQHCMDCT          $I COMMANDS               @G38ESBB
         TM    PQEDFLAG-PQEDSECT(PL),PQEDINT IS CMD $I         @G38ESBB
         BZ    PPQENPUT            BR IF NOT                   @G38ESBB
         SPACE 1                                               @G38ESBB
PPQGETQ  BAL   PL,PCKPTNI          CHECKPOINT CKPT JOE FOR $I  @G38ESBB
         BZ    PPQGOTQ             BRANCH IF QUEUES OWNED      @G38ESBB
        $QSUSE                     ACQUIRE ACCESS TO CKPT DATA @G38ESBB
         B     PPQGETQ             GO RETRY SUB CALL           @G38ESBB
         SPACE 1                                               @G38ESBB
PPQGOTQ  L     PL,PQECPQED         RESTORE PQED ADDRESS        @G38ESBB
         L     PW,PQEDWJOE-PQEDSECT(,PL) ADDRESS WORK JOE      @G38ESBB
*        NOTE: CHECKPOINT JOE ADDRESS SET IN R1 FROM PCKPTNI   @G38ESBB
        $#PUT  WORK=(PW),PRC=(R1)  RETURN TO JOT WITH CKPT     @G38ESBB
         L     PW,PQHADR           RESTORE PQH ADDRESS         @G38ESBB
         B     PDELDS              GO DELETE PQE'S FOR DS      @G38ESBB
         SPACE 1                                               @G38ESBB
PPQENPUT L     PW,PQEDWJOE-PQEDSECT(,PL) ADDRESS WORK JOE      @G38ESBB
        $#PUT  WORK=(PW)           RETURN TO JOT, NO CKPT      @G38ESBB
         L     PW,PQHADR           RESTORE PQH ADDRESS         @G38ESBB
         B     PDELDS              GO DELETE PQE'S FOR DS      @G38ESBB
         EJECT                                                 @OZ48003
***************************************************************@G38ESBB
*                                                              @G38ESBB
*        PROCESS SPIN DATA SETS                                @G38ESBB
*                                                              @G38ESBB
***************************************************************@G38ESBB
         SPACE 1                                               @G38ESBB
PPQSPUR  TM    PQEDFLAG-PQEDSECT(PL),PQEDALOC ALLOC IOT...     @G38ESBB
         BZ    PREMJOE             NO, BYPASS SPIN DS PURGE    @G38ESBB
        $QSUSE                     ACQUIRE ACCESS TO CKPT DATA @G38ESBB
         L     R1,PQEDWJOE-PQEDSECT(,PL) ADDRESS WORK JOE      @G38ESBB
         LH    R1,JOEJQE-JOEDSECT(,R1) GET JQE OFFSET          @G38ESBB
         N     R1,PCLRHALF         CLEAR LEFT HALFWORD         @G38ESBB
         SLL   R1,2                EXPAND TO BYTE OFFSET       @G38ESBB
         AL    R1,$JOBQPTR         ADD JOB QUEUE ORIGIN        @G38ESBB
         LH    R1,JQEJOE-JQEDSECT(,R1) ADDRESS FIRST JOE       @G38ESBB
         B     PPQJOCH1            GO CHECK FIRST JOE          @G38ESBB
         SPACE 1                                               @G38ESBB
PPQJOECH LH    R1,JOEJOE-JOEDSECT(,R1) GET NEXT JOE IN CHAIN   @G38ESBB
         LTR   R1,R1               END OF CHAIN...             @G38ESBB
         BZ    PPQGTIOT            YES, BRANCH, PURGE OK       @G38ESBB
         SPACE 1                                               @G38ESBB
PPQJOCH1 N     R1,PCLRHALF         CLEAR LEFT HALFWORD         @G38ESBB
         SLL   R1,2                EXPAND TO BYTE OFFSET       @G38ESBB
         AL    R1,$JOTABLE         ADD JOB OUTPUT TABLE ORIGIN @G38ESBB
         CLC   JOEIOTTR-JOEDSECT(,R1),PQEDIOTR-PQEDSECT(PL)    @G38ESBBC
                                   SAME IOT...                 @G38ESBB
         BNE   PPQJOECH            NO, GO GET NEXT JOE         @G38ESBB
         CL    R1,PQEDWJOE-PQEDSECT(,PL) SAME JOE...           @G38ESBB
         BE    PPQJOECH            YES, GO GET NEXT JOE        @G38ESBB
         B     PREMJOE             BYPASS PURGE                @G38ESBB
         EJECT                                                 @OZ48003
PPQGTIOT DS    0H                  PURGE SPIN DS SPOOL SPACE   @G38ESBB
        $GETBUF WAIT=YES,FIX=YES   GET TEMP BUFFER FOR IOT I/O @G38ESBB
         MVC   PQHSAVE2,PBUFSAVE   SAVE SECONDARY BUFFER ADR   @G38ESBB
         ST    R1,PBUFSAVE         SET BUFFER ADR FOR PRDBUF   @G38ESBB
         L     PW,PINIOB           SAVE INPUT IOB ADDRESS      @G38ESBB
         MVC   PINIOB,PPJOBKEY     MOVE JOB KEY TO TEMP FULLWD @G38ESBB
         L     R14,PINIOB          SAVE CHANNEL JOB JOB KEY    @G38ESBB
         ST    R1,PINIOB           TEMP BUFFER IS IOB FOR READ @G38ESBB
         ICM   R1,3,PPFLAG         SAVE CHANNEL JOB FLAGS      @OZ47265
         ICM   R1,4,PBFAVAIL        AND BUFFER COUNT           @OZ47265
         NI    PPFLAG,FF-PPRDERR   RESET ERROR FOR PURSPDS     @OZ51839
         MVC   PPJOBKEY,PQEDJKEY-PQEDSECT(PL) STACKER JOB KEY  @G38ESBB
         L     R0,PQEDIOTR-PQEDSECT(,PL) ADDRESS THE IOT       @G38ESBB
         L     R15,=A(PURSPDS)     CALL SUBROUTINE TO PURGE    @G38ESBB
         BALR  PL,R15               SPIN DATA SET SPOOL SPACE  @G38ESBB
         ST    R14,PINIOB          MOVE JOB KEY TO TEMP FULLWD @G38ESBB
         MVC   PPJOBKEY,PINIOB     RESTORE CHANNEL JOB JOB KEY @G38ESBB
         ST    PW,PINIOB           RESTORE INPUT IOB ADDRESS   @G38ESBB
         L     PW,PQHADR           RESTORE PQH ADDRESS         @G38ESBB
         STCM  R1,3,PPFLAG         RESTORE CHANNEL JOB FLAGS   @OZ47265
         STCM  R1,4,PBFAVAIL        AND BUFFER COUNT           @OZ47265
        $FREEBUF PBUFADDR          FREE TEMP BUFFER            @G38ESBB
         MVC   PBUFADDR,PBUFSAVE   RESTORE PRIMARY BUFFER ADR  @G38ESBB
         MVC   PBUFSAVE,PQHSAVE2   RESTORE SECONDARY BUF ADR   @G38ESBB
         L     PL,PQECPQED         RESTORE PQED ADDRESS        @G38ESBB
         EJECT                                                 @OZ47265
***************************************************************@G38ESBB
*                                                              @G38ESBB
*        REMOVE STACKED JOE'S FROM JOT                         @G38ESBB
*                                                              @G38ESBB
***************************************************************@G38ESBB
         SPACE 1                                               @G38ESBB
PREMJOE  L     R1,PQEDWJOE-PQEDSECT(,PL) ADDRESS THE WORK JOE  @G38ESBB
        $#REM  WORK=(R1)           REMOVE WORK JOE FROM JOT    @G38ESBB
         SPACE 1                                               @G38ESBB
PJQECHK  L     R3,PQENEXT          SEE IF ANY MORE             @OZ46484
         CR    R3,PW                JOBS ARE IN THE PPQ        @OZ46484
         BE    PCLRJQE             BRANCH IF NONE              @OZ46484
         CLI   PQETYPE,PQES        TEST PQE TYPE               @OZ46484
         BE    PJQECHK             IGNORE SMF PQE              @OZ46484
         B     PDELDS              CONTINUE, MORE JOBS FOUND   @OZ46484
         SPACE 1                                               @OZ46484
PCLRJQE  MVC   PQHXJQE,$ZEROS      CLEAR JQE POINTER           @OZ46484
         EJECT                                                 @OZ46484
***************************************************************@G38ESBB
*                                                              @G38ESBB
*        REMOVE STACKED DATA SET PQE'S FROM PAGE QUEUE         @G38ESBB
*                                                              @G38ESBB
***************************************************************@G38ESBB
         SPACE 1                                               @G38ESBB
PDELDS   LR    R0,R5               SET PQE COUNT FOR DELETE    @G38ESBB
         L     R1,PQHFIRST         ADDRESS PQED                @G38ESBB
         L     R15,=A(PDELPQE)     CALL PDELPQE TO             @OZ48003
         BALR  PL,R15               DELETE DATASET PQE'S       @OZ48003
         LR    R3,R1               ADDRESS PQE0                @G38ESBB
         SLR   R5,R5               CLEAR PQE COUNT             @G38ESBB
         B     PNXTPQE             GO PROCESS NEXT PQE         @G38ESBB
         SPACE 1                                               @OZ46484
***************************************************************@G38ESBB
*                                                              @G38ESBB
*        DELETE PREVIOUS CHECKPOINT PQE IF NOT BACKSPACE PQE   @G38ESBB
*                                                              @G38ESBB
***************************************************************@G38ESBB
         SPACE 1                                               @G38ESBB
PQEDPREV L     R15,PQEPREV         ADDRESS PREVIOUS PQE        @G38ESBB
         TM    PQECFLAG-PQEDSECT(R15),PQECBSP PQEC FOR CKPT... @G38ESBB
         BO    PBSPCHK             NO,GO PROCESS BACKSP PQE'S  @G38ESBB
         LA    R0,1                SET PQE DELETE COUNT TO 1   @G38ESBB
         LR    R1,R15              ADDRESS PQE TO DELETE       @G38ESBB
         L     R15,=A(PDELPQE)     CALL PDELPQE TO DELETE      @OZ48003
         BALR  PL,R15               PREVIOUS CKPT PQEC         @OZ48003
         BCTR  R5,0                DECREMENT PQE COUNT         @G38ESBB
         L     R15,PQEPREV         GET PREVIOUS BACKSPACE PQE  @G38ESBB
         EJECT                                                 @OZ48003
***************************************************************@G38ESBB
*                                                              @G38ESBB
*        DETERMINE IF PQE SHOULD BE KEPT AS BACKSPACE ENTRY    @G38ESBB
*                                                              @G38ESBB
***************************************************************@G38ESBB
         SPACE 1                                               @G38ESBB
PBSPCHK  LH    R14,PQERPGID        DETERMINE IF PQE IS         @G38ESBB
         SH    R14,PQERPGID-PQEDSECT(,R15) GREATER THAN        @G38ESBB
         N     R14,PCLRHALF                 $BSPGCT AWAY       @G38ESBB
         SLR   R15,R15                       FROM PREVIOUS     @G38ESBB
         IC    R15,$BSPGCT                    BACKSPACE        @G38ESBB
         CR    R14,R15                         PQE             @G38ESBB
         BL    PNXTPQE             NO, GO PROCESS NEXT PQE     @G38ESBB
         OI    PQECFLAG,PQECBSP    INDICATE PQE STACKED        @G38ESBB
         IC    R15,$BSPNTE         HAS NUMBER OF               @G38ESBB
         LA    R15,2(,R15)          BACKSPACE PQE'S            @G38ESBB
         CR    R5,R15                REACHED MAXIMUM...        @G38ESBB
         BNH   PNXTPQE             NO,GO PROCESS NEXT PQE      @G38ESBB
         LA    R0,1                SET PQE DELETE COUNT TO 1   @G38ESBB
         L     R1,PQHFIRST         ADDRESS PQED                @G38ESBB
         L     R1,PQENEXT-PQEDSECT(,R1) ADDRESS PQECFPG        @G38ESBB
         L     R1,PQENEXT-PQEDSECT(,R1) ADDRESS OLDEST BS PQEC @G38ESBB
         L     R15,=A(PDELPQE)     CALL PDELPQE TO DELETE      @OZ48003
         BALR  PL,R15               OLDEST BACKSPACE PQE       @OZ48003
         BCTR  R5,0                DECREMENT PQE COUNT         @OZ47048
         B     PNXTPQE             GO PROCESS NEXT PQE         @G38ESBB
         EJECT                                                 @OZ49145
***************************************************************@G38ESBB
*                                                              @G38ESBB
*        VALIDATE 3800 PAGE ID'S                               @OZ49145
*        CHECKPOINT LAST PQE STACKED, IF ANY                   @G38ESBB
*                                                              @G38ESBB
***************************************************************@G38ESBB
         SPACE 1                                               @OZ49145
PCKPGID  LH    R0,PQHCPG           SET CHANNEL PAGE ID         @OZ49145
         LR    R1,R0                IN REG0 AND REG1           @OZ49145
         SR    R0,R4               COMPUTE MAX PAGE DELTA      @OZ49145
         SR    R1,R15              COMPUTE PQE PAGE DELTA      @OZ49145
         N     R0,PCLRHALF         CLEAR LEFT HALF WORD        @OZ49145
         N     R1,PCLRHALF         CLEAR LEFT HALF WORD        @OZ49145
         CR    R1,R0               COMPARE WITH MAXIMUM        @OZ49145
         BNH   PPQENDQ             BRANCH IF VALID             @OZ49145
         OI    PQHAFLAG,PQHABORT   ELSE, SET ABORT FLAG        @OZ49145
         SPACE 1                                               @OZ49145
PPQENDQ  LTR   R10,R10             ANY PQE TO CHECKPOINT...    @G38ESBB
         BZ    PPQRET              NO, GO RETURN               @G38ESBB
         LR    R3,R10              INDICATE PQE TO CHECKPOINT  @G38ESBB
         BAL   PL,PCKPTNI          PERFORM CKPT JOE CHECKPOINT @G38ESBB
         TM    PQECFLAG-PQEDSECT(R10),PQECBSP BACKSPACE PQE... @G38ESBB
         BO    PPQRET              YES, GO RETURN              @G38ESBB
         L     PW,PQHADR           RESTORE PQH ADDRESS         @G38ESBB
         LA    R0,1                SET PQE COUNT               @OZ48003
         LR    R1,R10              SET PQE ADDRESS             @OZ48003
         L     R15,=A(PDELPQE)     CALL PDELPQE TO             @OZ48003
         BALR  PL,R15               DELETE CHECKPOINT PQE      @OZ48003
         SPACE 1                                               @OZ49145
PPQRET   TM    PQHFLAG,PQHDRAIN    TEST IF DRAINING            @OZ49145
         BZ    PPQRETN             BRANCH IF NOT               @OZ49145
         NI    PQHFLAG,FF-PQHDRAIN  RESET DRAIN FLAG           @OZ49145
         NI    PQHAFLAG,FF-PQHABORT RESET ABORT FLAG           @OZ49145
         ST    PW,PQHPIDE          SHOW NO IDS PENDING         @OZ49145
         XC    PQHXJQE,PQHXJQE     SHOW NO JOB ON PRINTER      @OZ49145
         XC    PQHPQEJ,PQHPQEJ     SHOW NO JOBS PENDING        @OZ49145
         SPACE 1                                               @OZ49145
PPQRETN  PRETURN                   RESTORE REGS AND RETURN     @OZ49145
         DROP  PW,R3               DROP PQH,PQE ADR            @G38ESBB
         USING BUFDSECT,PBUF       RESTORE BUFFER ADDRESS      @G38ESBB
         EJECT                                                 @G38ESBB
***********************************************************************
*                                                              @G38ESBB
*        ACQUIRE ACCESS TO CHECKPOINT DATA, THEN UPDATE AND    @G38ESBB
*        CHECKPOINT THE CHECKPOINT JOE. IF NECESSARY, INDICATE @G38ESBB
*        CKECKPOINT JOE IS VALID.                              @G38ESBB
*                                                              @G38ESBB
*        R1  - ADDRESS OF CHECKPOINT JOE                       @G38ESBB
*        R3  - PQE TO CHECKPOINT                               @G38ESBB
*        R15 - ADDRESS OF WORK JOE                             @G38ESBB
*                                                              @G38ESBB
*        ON EXIT CONDITION CODE IS SET                         @G38ESBB
*        CC=ZERO     - CHECKPOINT ACCESS ACQUIRED              @G38ESBB
*        CC=NON-ZERO - CHECKPOINT ACCESS FAILED                @G38ESBB
*                                                              @G38ESBB
***************************************************************@G38ESBB
         SPACE 1                                               @G38ESBB
PCKPTNI  LR    R1,PL               SAVE RETURN REG             @G38ESBB
         L     R15,=A(PGETQS)      CALL CKPT                   @G38ESBB
         BALR  PL,R15               ACCESS ROUTINE             @G38ESBB
         LR    PL,R1               RESTORE RETURN REG          @G38ESBB
         BNZR  PL                  Q'S NOT OWNED, RETURN       @G38ESBB
         SPACE 1                                               @G38ESBB
         PRINT OFF                 THIS SECTION DELETED BY     @G38ESBB
*        DELETED                                               @G38ESBB
         PRINT ON                  THIS SECTION DELETED BY     @G38ESBB
         SPACE 1                                               @G383SBB
***************************************************************@G38ESBB
*                                                              @G38ESBB
*        ACCESS TO CKPT DATA HAS BEEN ACQUIRED.                @G38ESBB
*        UPDATE THE CKPT JOE FROM INFO IN PQEC AND PQED.       @G38ESBB
*                                                              @G38ESBB
***************************************************************@G38ESBB
         SPACE 1                                               @G38ESBB
         USING PQEDSECT,R3         PROVIDE PQE ADDRESSABILITY  @G38ESBB
         SPACE 1                                               @G38ESBB
         PRINT OFF                 THIS SECTION DELETED BY     @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
*        DELETED                                               @G38ESBB
         PRINT ON                  THIS SECTION DELETED BY     @G38ESBB
         SPACE 1                                               @G38ESBB
         L     R15,PQECPQED        ADDRESS THE PQED            @G38ESBB
         L     R1,PQEDWJOE-PQEDSECT(,R15) ADDRESS WORK JOE     @G38ESBB
         USING JOEDSECT,R1         PROVIDE JOE ADDRESSABILITY  @G38ESBB
         LH    R1,JOECKPT          GET CKPT JOE OFFSET/4       @G38ESBB
         N     R1,PCLRHALF         ZERO LEFT HALFWORD          @G38ESBB
         SLL   R1,2                COMPUTE BYTE OFFSET         @G38ESBB
         AL    R1,$JOTABLE         ADD BASE                    @G38ESBB
         MVC   JOEJRCB,PQECJRCB    MOVE DISP INTO EJECT BUFFER @G38ESBB
         MVC   JOEPDDB,PQEDPDDB-PQEDSECT(R15) PDDB OFFSET      @G38ESBB
         MVC   JOEPPCT,PQECPPCT    MOVE PDDB LOGICAL PAGE CT   @G38ESBB
         MVC   JOETLNC,PQECTLNC    MOVE TOTAL JOE LINE COUNT   @G38ESBB
         MVC   JOETPCT,PQECTPCT    MOVE TOTAL JOE PAGE COUNT   @G38ESBB
         MVC   JOEMTTR,PQECMTTR    MOVE MTTR OF SPOOL DATA     @G38ESBB
         MVC   JOEIOTTR,PQEDIOTR-PQEDSECT(R15) IOT TRACK ADR   @G38ESBB
         MVC   JOECOPY,PQEDCOPY-PQEDSECT(R15) COPY IN PROGRESS @G38ESBB
         MVC   JOECPYG,PQEDCPYG-PQEDSECT(R15) COPY GROUP OFFST @G38ESBB
         MVC   JOETNDS,PQEDTNDS-PQEDSECT(R15) NUMBER OF DS'S   @G38ESBB
        $#CKPT JOE=0(,R1),TYPE=A   SCHEDULE CKPT JOE FOR CKPT  @G38ESBB
         SPACE 1                                               @G38ESBB
         DROP  R1                  DROP CKPT JOE ADR           @G38ESBB
         USING JOEDSECT,R15        PROVIDE WORK JOE ADR        @G38ESBB
         SPACE 1                                               @G38ESBB
         L     R15,PQECPQED        ADDRESS PQED                @G38ESBB
         L     R15,PQEDWJOE-PQEDSECT(,R15) ADDRESS WORK JOE    @G38ESBB
*        DELETED                                               @G38ESBB
         TM    JOEFLAG,$JOECKV     CKPT JOE ALREADY VALID...   @G38ESBB
         BO    PCKNIEND            YES, GO EXIT                @G38ESBB
         OI    JOEFLAG,$JOECKV     SET CKPT VALID FLAG         @G38ESBB
        $#CKPT JOE=0(,R15),TYPE=A  CKPT THE WORK JOE           @G38ESBB
*        DELETED                                               @G38ESBB
         SPACE 1                                               @G38ESBB
PCKNIEND SR    R15,R15             SET ZERO CC                 @G38ESBB
         BR    PL                  RETURN                      @G38ESBB
         DROP  R3,R15              DROP PQE,JOE ADR            @G38ESBB
         USING BUFDSECT,PBUF       RESTORE BUFFER ADDRESS      @G38ESBB
         SPACE 3                                               @G38ESBB
         LTORG                                                 @G38ESBB
 TITLE 'HASPPRPU -- LOCATE 3800 TRANSFER STATION'              @G38ESBB
***************************************************************@G38ESBB
*                                                              @G38ESBB
*        THE LOCATE FUNCTION DETERMINES WHICH PHYSICAL PAGE    @G38ESBB
*        IS AT THE TRANSFER STATION OR, IF PAPER JAM, THE      @G38ESBB
*        FUSER STATION.  IT DOES THIS BY USING THE REQUEST     @G38ESBB
*        PRINTER INFORMATION DATA IN THE BUFFER WORK AREA.     @G38ESBB
*        IN ADDITION, PP3800R IS SET IF THE COMMAND ENTERED    @G38ESBB
*        IF PROCESSABLE FOR THE TRANSFER PAGE.                 @G38ESBB
*                                                              @G38ESBB
*        NON-PROCESSABLE COMMANDS OCCUR WHEN                   @G38ESBB
*                                                              @G38ESBB
*        - THE TRANSFER STATION IS AT A JOE BOUNDARY           @G38ESBB
*          AND THE COMMAND IS $N, $C, $E, $I OR $F             @G38ESBB
*        - THE TRANSFER STATION IS AT A DATA SET BOUNDARY      @G38ESBB
*          AND $F WAS SPECIFIED                                @G38ESBB
*        - THE JOE AT THE TRANSFER STATION WAS PREVIOUSLY      @G38ESBB
*          CANCELLED                                           @G38ESBB
*                                                              @G38ESBB
*        R1  - ADDRESS OF THE PQE                              @G38ESBB
*        PW  - ADDRESS OF THE PQH                              @G38ESBB
*                                                              @G38ESBB
***************************************************************@G38ESBB
         SPACE 1                                               @G38ESBB
PLOCATE  PSAVE ALL                 SAVE ALL REGISTERS          @G38ESBB
         LR    BASE2,R15           SETUP LOCAL                 @G38ESBB
         USING PLOCATE,BASE2            ADDRESSABILITY         @G38ESBB
         L     PW,PQHADR           ESTABLISH PQH               @G38ESBB
         USING PQHDSECT,PW              ADDRESSABILITY         @G38ESBB
         L     R15,PCEDCT          TEST FOR PAPER JAM OR       @G38ESBB
         TM    DCTPPSW2-DCTDSECT(R15),DCTCKJAM CANCEL KEY...   @G38ESBB
         BZ    PLOCTPQE            BR IF NOT                   @G38ESBB
         MVC   PQHOPG,DCTLDPID-DCTDSECT(R15) GET ORG PG IN DCT @G38ESBB
         SPACE 1                                               @G38ESBB
PLOCTPQE L     R1,PQHPIDE          GET ID TO BE COMPLETED      @G38ESBB
         LA    R15,PQHFIRST-(PQENEXT-PQEDSECT) GET PQE0        @G38ESBB
         CR    R1,R15              ANY ID'S PENDING...         @G38ESBB
         BNE   PLOCPREV            YES, BRANCH                 @G38ESBB
         CL    R15,PQHLAST         ANY PQE'S...                @G38ESBB
         BE    PTSTCJP             BRANCH IF NOT               @OZ44633
         LR    R1,R15              ADDRESS PQE0                @G38ESBB
         USING PQEDSECT,R1         PQE ADDRESSABILITY          @G38ESBB
         SPACE 1                                               @G38ESBB
PLOCPREV L     R1,PQEPREV          GET LAST COMPLETED PQE      @G38ESBB
         LA    R15,PQHFIRST-(PQENEXT-PQEDSECT) GET PQE0        @G38ESBB
         CR    R1,R15              END OF PQE...               @G38ESBB
         BE    PTSTCJP             BRANCH IF YES               @OZ44633
         CLI   PQETYPE,PQEC        VALID CKPT ENTRY...         @G38ESBB
         BNE   PLOCPREV            GET PREVIOUS PQE IF NOT     @G38ESBB
         LH    R15,PQHOPG          TEST IF THIS PQE IS BEYOND  @G38ESBB
         SH    R15,PQERPGID             THE ORIGIN PAGE        @G38ESBB
         N     R15,PCLRHALF             (ORIGIN PAGE LESS      @G38ESBB
         C     R15,PQELIMIT             THAN RPGID)            @G38ESBB
         BH    PLOCPREV            KEEP SCANNING IF NOT        @G38ESBB
         CLC   PQHOPG,PQERPGID     IS ORIGIN PAGE AT A PQEC    @G38ESBB
         BNE   PSAVOPQE            BR IF NOT, OPQE FOUND       @G38ESBB
         CLC   PQEFCBLN,=H'1'      MUST BE AT TOP OF FCB       @G38ESBB
         BNE   PLOCPREV             TO BE USED AS OPQE         @G38ESBB
         SPACE 1                                               @G38ESBB
PSAVOPQE ST    R1,PQHOPQE          SAVE PQE CLOSEST TO OPG     @G38ESBB
         SPACE 1                                               @G38ESBB
***************************************************************@G38ESBB
*                                                              @G38ESBB
*        DETERMINE WHETHER COMMAND IS PROCESSABLE              @G38ESBB
*                                                              @G38ESBB
***************************************************************@G38ESBB
         SPACE 1                                               @G38ESBB
         L     R15,PCEDCT          TEST FOR PAPERJAM OR        @G38ESBB
         TM    DCTPPSW2-DCTDSECT(R15),DCTCKJAM CANCEL KEY      @G38ESBB
         BO    PLOCEND             BR IF YES, ALWAYS PROCESSED @G38ESBB
         TM    PQHFLAG,PQH2CMD     DOUBLE COMMAND...           @G38ESBB
         BO    PLOCEND             YES,BRANCH,CMD PROCESSABLE  @G38ESBB
         L     R14,PQECPQED        GET DATA SET PQE FOR OPQE   @G38ESBB
         TM    PQEDFLAG-PQEDSECT(R14),PQEDCAN+PQEDINT+PQEDRPT+PQEDRST  X
                                   DEFERRED CMD ISSUED ...     @OZ66187
         BNZ   PTSTCJP             BR IF YES, NOT PROCESSABLE  @OZ66187
         TM    PQECFLAG,PQECLPG    TEST FOR LAST PAGE OF DS    @G38ESBB
         BZ    PTESTCJP            BR IF NOT                   @G38ESBB
         SPACE 1                                               @G38ESBB
***************************************************************@G38ESBB
*                                                              @G38ESBB
*        LAST PAGE OF DATA SET IS AT TRANSFER STATION          @G38ESBB
*                                                              @G38ESBB
***************************************************************@G38ESBB
         SPACE 1                                               @G38ESBB
         TM    PDCTFLAG,DCTDELET+DCTRSTRT+DCTRPT $C,$E,$I,$N.. @G38ESBB
         BNZ   PJOEEND             YES, BR TO TEST END OF JOE  @G38ESBB
         L     R15,PFSBSCT         GET BACKSPACE AMOUNT        @G38ESBB
         LTR   R15,R15             TEST FOR $B OR $F           @G38ESBB
         BNL   PROCNOP             BR IF $F                    @G38ESBB
         LPR   R15,R15             GET POSITIVE BACKSPACE CT   @G38ESBB
         CLC   PFSBSCT,PMAXPAGE         TO TEST $BPRTN,D       @G38ESBB
         BE    PROCNOP             BR IF YES                   @G38ESBB
         TM    PQEDFLAG-PQEDSECT(R14),PQEDLAST LAST DS OF JOE. @G38ESBB
         BZ    PLOCEND             NO,BRANCH,CMD PROCESSABLE   @G38ESBB
         L     PL,PCEDCT           ADDRESS PRPU DCT            @G38ESBB
         TM    DCTPPSW2-DCTDSECT(PL),DCTBFCKP TEST $B,C        @G38ESBB
         BO    PLOCEND             YES,BRANCH,CMD PROCESSABLE  @G38ESBB
         LH    PL,PQHOPG           IF TARGET PAGE              @G38ESBB
         SH    PL,PQERPGID          NOT WITHIN SPOOL           @G38ESBB
         CR    PL,R15                DATA, THEN COMMAND        @G38ESBB
         BNL   PROCNOP                IS NOT PROCESSABLE       @G38ESBB
         B     PLOCEND             CMD IS $B, PROCESSABLE      @G38ESBB
         EJECT                                                 @OZ46351
PJOEEND  TM    PDCTFLAG,DCTRPT     TEST FOR $N COMMAND         @OZ46351
         BZ    PTSTCJP             BRANCH IF NOT               @OZ46351
         TM    PQEDFLAG-PQEDSECT(R14),PQEDLAST CK FOR LAST DS  @OZ46351
         BZ    PTESTCJP            BR IF NOT                   @G38ESBB
         SPACE 1                                               @G38ESBB
PTSTCJP  ICM   R15,15,PCEJQE       ADDRESS CHANNEL JOB'S JQE   @OZ44633
         BZ    PROCNOP             BRANCH IF NONE              @OZ44633
         TM    JQEFLAGS-JQEDSECT(R15),QUEOPCAN+QUEPURGE $CJ,P  @OZ44633
         BNO   PROCNOP             BRANCH IF NOT               @OZ44633
         L     R1,PQHLAST          ADDRESS LAST PQE            @OZ44633
         B     PTSTCJP1            LOCATE PQED                 @OZ44633
         SPACE 1                                               @OZ44633
PTESTCJP ICM   R15,15,PCEJQE       ADDRESS CHANNEL JOB'S JQE   @OZ44633
         BZ    PTESTRPT            BRANCH IF NONE              @OZ44633
         TM    JQEFLAGS-JQEDSECT(R15),QUEOPCAN+QUEPURGE $CJ,P  @OZ44633
         BNO   PTESTRPT            BRANCH IF NOT               @OZ44633
         L     R15,PQEDWJOE-PQEDSECT(,R14) GET OPQE WORK JOE   @OZ44633
         LH    R15,JOEJQE-JOEDSECT(,R15) GET OPQE JQE OFFSET   @OZ44633
         N     R15,PCLRHALF        CLEAR LEFT HALFWORD         @OZ44633
         SLL   R15,2               EXPAND TO BYTE OFFSET       @OZ44633
         AL    R15,$JOBQPTR        ADD JOB QUEUE ORIGIN        @OZ44633
         CL    R15,PCEJQE          CANCELLED JQE EQ OPQE JQE   @OZ44633
         BE    PLOCEND             CANCEL OPQE JOE IF YES      @OZ44633
         L     R1,PQHLAST          ADDRESS LAST PQE            @OZ44633
         SPACE 1                                               @OZ44633
PTSTCJP1 CR    R1,PW               CHECK FOR END OF PPQ        @OZ44633
         BE    PROCNOP1            BRANCH IF YES               @OZ44633
         CLI   PQETYPE,PQEC        TEST FOR PQEC               @OZ44633
         BE    PTSTCJP2            BRANCH IF YES               @OZ44633
         L     R1,PQEPREV          GET PREVIOUS PQE            @OZ44633
         B     PTSTCJP1            TEST PREVIOUS PQE           @OZ44633
         SPACE 1                                               @OZ44633
PTSTCJP2 L     R1,PQECPQED         ADDRESS PQED                @OZ44633
         CLC   PQEDWJOE,PWKJOE     CHECK JOE ADDRESS           @OZ44633
         BNE   PLOCRET             BRANCH IF NOT CURRENT       @OZ44633
         TM    PQEDFLAG,PQEDCAN+PQEDCJP  ALREADY CANCELLED...  @OZ44633
         BM    PROCNOP1            BRANCH IF YES               @OZ44633
         OI    PQEDFLAG,PQEDCAN+PQEDCJP  INDICATE CANCELLED    @OZ44633
         OI    PQHFLAG,PQHDSVC     INDICATE RESET NEEDED       @OZ44633
         B     PLOCRET             CHANNEL ORIENTED            @OZ44633
         SPACE 1                                               @G38ESBB
PTESTRPT TM    PDCTFLAG,DCTRPT     $N COMMAND...               @G38ESBB
         BZ    PLOCEND             COMMAND PROCESSABLE IF NOT  @G38ESBB
         NI    PDCTFLAG,FF-DCTRPT  RESET REPEAT FLAG           @G38ESBB
         L     R15,PQEDWJOE-PQEDSECT(,R14) GET OPQE WORK JOE   @G38ESBB
         LH    R1,JOECHAR-JOEDSECT(,R15) GET CHAR JOE OFFSET   @G38ESBB
         N     R1,PCLRHALF         CLEAR LEFT HALFWORD         @G38ESBB
         SLL   R1,2                EXPAND TO BYTE OFFSET       @G38ESBB
         AL    R1,$JOTABLE         ADD JOB OUTPUT TABLE ORIGIN @G38ESBB
         LR    R0,R15              SAVE WORK JOE ADDR FOR ADD  @G38ESBB
        $#ADD  WORK=(R0),CHAR=(R1) COPY JOE BACK INTO QUEUE    @G38ESBB
         BZ    PNMSG               BR IF ADD SUCCESSFUL        @G38ESBB
         L     R1,PQHOPQE          ADDRESS ORIGIN PQE          @G38ESBB
         L     R1,PQECPQED         GET DATA SET PQE AT ORIGIN  @G38ESBB
         TM    PQEDFLAG,PQEDLAST   LAST DS OF JOE...           @G38ESBB
         BO    PDEFRPT             YES, GO DEFER COMMAND       @G38ESBB
         OI    PDCTFLAG,DCTRPT      ELSE, RESET COMMAND FLAG   @G38ESBB
         B     PNMSG               BRANCH, DEFER AT PPDONE     @G38ESBB
         SPACE 1                                               @G38ESBB
PDEFRPT  OI    PQEDFLAG,PQEDRPT    INDICATE DEFERRED $N        @G38ESBB
         IC    R14,PQHCMDCT        GET DEFERRED CMD COUNT      @G38ESBB
         LA    R14,1(,R14)         INCREMENT DEFERRED COUNT    @G38ESBB
         STC   R14,PQHCMDCT        SAVE NEW COUNT              @G38ESBB
         SPACE 1                                               @G38ESBB
PNMSG    L     R15,PCEDCT          GET DCT ADDRESS             @G38ESBB
         USING DCTDSECT,R15        PROVIDE DCT ADDRESSABILITY  @G38ESBB
        $MID   170                 MESSAGE IDENTIFIER          @G38ESBB
         PMSG  PMESSAGE,M170L,(X'170F',DCTDEVN,C' REPEATED')   @G38ESBBC
                                   MOVE MESSAGE TEXT           @G38ESBB
         DROP  R15                 DROP DCT ADDRESSABILITY     @G38ESBB
        $WTO   PMESSAGE,M170L,     INFORM THE OPERATOR         @G38ESBBC
               ROUTE=$LOG+$UR,CLASS=$NORMAL,PRI=$ST,JOB=NO     @G38ESBB
         B     PROCNOP1            BR TO CLEAR FLAG SETTINGS   @G38ESBB
         SPACE 1                                               @G38ESBB
***************************************************************@G38ESBB
*                                                              @G38ESBB
*        COMMAND NOT PROCESSABLE, WRITE REJECT MESSAGE AND     @G38ESBB
*        RESET FLAGS                                           @G38ESBB
*                                                              @G38ESBB
***************************************************************@G38ESBB
         SPACE 1                                               @G38ESBB
PROCNOP  L     R15,PCEDCT          GET DCT ADDRESS             @G38ESBB
         USING DCTDSECT,R15        PROVIDE DCT ADDRESSABILITY  @G38ESBB
         TM    DCTPPSW2,DCTCKJAM   PAPER JAM...                @G38ESBB
         BZ    PNOPMSG             NO, GO ISSUE MESSAGE        @G38ESBB
         LA    R1,PQHFIRST-(PQENEXT-PQEDSECT) ADDRESS PQE0     @G38ESBB
         SPACE 1                                               @OZ47595
PNOPLOOP LR    R0,R1               SAVE PREVIOUS PQE ADDR      @OZ47595
         L     R1,PQENEXT          GET NEXT PQE                @OZ47595
         CR    R1,PW               CHECK FOR END OF PPQ        @OZ47595
         BE    PNOPRSET            BRANCH IF YES               @OZ47595
         CLI   PQETYPE,PQES        CHECK FOR PQES              @OZ47595
         BE    PNOPLOOP            BRANCH IF YES               @OZ47595
PNOPSET  ST    R0,PQHOPQE          SET PQE ADDRESS             @OZ47595
         OI    PQHFLAG,PQHRSTRT    INDICATE PPQ RESTART        @OZ47595
         B     PLOCEND             CONTINUE                    @OZ47595
         SPACE 1                                               @OZ47595
PNOPRSET CLC   PCEJQE,$ZEROS       TEST FOR CURRENT JOB        @OZ47595
         BNE   PNOPSET             BRANCH IF YES               @OZ47595
         TM    PPFLAG3,PP3800R     REPOSITIONING CMD...        @OZ75434
         BO    PNOPSET             YES, INDICATE RESTART       @OZ75434
         NI    DCTPPSW2,FF-DCTCKJAM  RESET CK/JAM SWITCH       @OZ47595
         B     PROCNOP1            BRANCH TO RESET FLAGS       @OZ47595
         SPACE 1                                               @G38ESBB
PNOPMSG  DS    0H                                              @G38ESBB
        $MID   152                 GET MESSAGE IDENTIFIER      @G38ESBB
         PMSG  PMESSAGE,M152L2,(X'152F',DCTDEVN,C' COMMAND REJECTED')  C
                                   MOVE MESSAGE TEXT           @G38ESBB
        $WTO   PMESSAGE,M152L2,JOB=NO, INFORM OPERATOR         @G38ESBBC
               ROUTE=$LOG+$UR,CLASS=$NORMAL,PRI=$ST            @G38ESBB
         SPACE 1                                               @G38ESBB
PROCNOP1 NI    PDCTFLAG,FF-DCTSPACE-DCTDELET-DCTRSTRT-DCTBKSP  @G38ESBB
         NI    PDCTFLAG,FF-DCTRPT  RESET REPEAT FLAG ($N)      @OZ46352
         NI    PPFLAG,FF-PPDELSW-PRDELSW RESET COMMAND FLAGS   @G38ESBB
         B     PLOCRET             BR TO RETURN                @G38ESBB
         SPACE 1                                               @G38ESBB
PLOCEND  OI    PPFLAG3,PP3800R     INDICATE CMD PROCESSABLE    @G38ESBB
         NI    PPFLAG3,FF-PP3800S-PP38CKPT  RESET FLAGS        @OZ51592
         MVC   PQHLPG,PQHOPG       SET LOCATED XFER STN PGID   @OZ46351
         OI    PQHFLAG,PQHIPPQM    SET INHIBIT FLAG            @OZ46674
         NI    PPFLAG,FF-PPNEWS    RESET NEWS FLAG             @OZ50141
         L     R15,PCEDCT          ADDRESS DCT                 @G38ESBB
         MVC   DCTCSW,$ZEROS       CLEAR RESTART CSW           @G38ESBB
         SPACE 1                                               @G38ESBB
         TM    DCTPPSW2,DCTCKJAM   PJAM/CKEY...                @OZ48003
         BZ    PLOCRET             BRANCH IF NOT               @OZ48003
         NI    PPFLAG3,FF-PPQSPND  RESET PPQ SUSPEND FLAG      @OZ48003
         NI    PDCTFLAG,FF-DCTDELET-DCTRSTRT-DCTBKSP  RESET    @OZ48003
         SPACE 1                                               @OZ48003
PLOCRET  PRETURN                   RESTORE REGS AND RETURN     @G38ESBB
         DROP  PW,R1,R15           SUSPEND PQH, PQE, DCT       @G38ESBB
         LTORG                                                 @OZ51011
 TITLE 'HASP PRINT/PUNCH SERVICE -- 3800 COMMAND PROCESSING'   @OZ51011
***************************************************************@OZ51011
*                                                              @OZ51011
*   PROCESS SYNCHRONOUS OPERATOR COMMANDS AFTER AN I/O ERROR   @OZ51011
*                                                              @OZ51011
***************************************************************@OZ51011
         SPACE 1                                               @OZ51011
         USING PPCMDCK,BASE2       LOCAL ADDRESSABILITY        @OZ51011
         USING DCTDSECT,R15        PROVIDE DCT ADDRESSABILITY  @OZ51011
         USING PQHDSECT,PW         PROVIDE PQH ADDRESSABILITY  @OZ51011
         USING PQEDSECT,R1         PROVIDE PQE ADDRESSABILITY  @OZ51011
         USING BFWDSECT,R4         PROVIDE BFW ADDRESSABILITY  @OZ51011
         SPACE 1                                               @OZ51011
PPCMDCK  PSAVE ALL                 SAVE REGISTERS              @OZ51011
         LR    BASE2,R15           SET LOCAL BASE REGISTER     @OZ51011
         CLI   PDEVTYP3,UCB3800    TEST FOR 3800               @OZ51011
         BNE   PPCMDRTN            BRANCH IF NOT               @OZ51011
         TM    PPFLAG3,PP3800R     3800 REPOSITIONING...       @OZ51011
         BO    PPCMDRTN            BRANCH IF YES               @OZ51011
         L     R15,PCEDCT          GET DCT ADDRESS             @OZ51011
         L     PW,PQHADR           GET PQH ADDRESS             @OZ51011
         SPACE 1                                               @OZ51011
         ICM   R1,15,PCEJQE        GET JQE ADDRESS             @OZ51011
         BZ    PPCMDRTN            BRANCH IF NONE              @OZ51011
         TM    JQEFLAGS-JQEDSECT(R1),QUEOPCAN+QUEPURGE TEST    @OZ51011
         BNO   PPCKTRM             BRANCH IF NOT $CJP          @OZ51011
         OI    DCTFLAGS,DCTDELET   ELSE, SIMULATE $CPRT        @OZ51011
         SPACE 1                                               @OZ51011
PPCKTRM  TM    DCTFLAGS,DCTDELET+DCTRSTRT TERMINATION CMD...   @OZ51011
         BZ    PPCMDRTN            BRANCH IF NOT               @OZ51011
         OC    PDCTFLAG,DCTFLAGS   PROVIDE TERMINATION REASON  @OZ51011
         NI    DCTFLAGS,FF-DCTDELET-DCTRSTRT-DCTBKSP RESET     @OZ51011
         OI    PPFLAG,PRDELSW+PPDELSW SET TERMINATION FLGS     @OZ51011
         OI    PPFLAG3,PP3800R     SET CMD FLAG                @OZ51011
         NI    PPFLAG,FF-PPWSW     INDICATE I/O COMPLETE       @OZ51011
         XC    DCTCSW,DCTCSW       CLEAR INTV REQD ADDR        @OZ51011
         DROP  R15                 SUSPEND DCT ADDRESSABILITY  @OZ51011
         SPACE 1                                               @OZ51011
         OI    PQHFLAG,PQHCHCMD    SET SYNC CMD FLAG           @OZ51011
         SLR   R0,R0               SET COUNT FOR               @OZ51011
         BCTR  R0,0                 PQECOMP ROUTINE            @OZ51011
         L     R15,=A(PQECOMP)     CALL PQECOMP TO             @OZ51011
         BALR  PL,R15               PROCESS COMPLETED PQE'S    @OZ51011
         NI    PQHFLAG,FF-PQHCHCMD RESET SYNC CMD FLAG         @OZ51011
         EJECT                                                 @OZ51011
         LH    R4,PCCWLAST         GET PCIE OFFSET             @OZ51011
         AL    R4,POUTCCWN         ADD CCW AREA BASE           @OZ51011
         LA    R4,PCIESIZE(,R4)    POINT TO BFW                @OZ51011
         MVI   BFWPQECT,0          ZERO PQE COUNT IN AREA      @OZ51011
         LH    R4,PCCWLAST         GET PCIE OFFSET             @OZ51011
         AL    R4,POUTCCWA         ADD CCW AREA BASE           @OZ51011
         LA    R4,PCIESIZE(,R4)    POINT TO BFW                @OZ51011
         MVI   BFWPQECT,0          ZERO PQE COUNT IN AREA      @OZ51011
         L     R1,PQHPIDE          ADDR FIRST INCOMPLETE PQE   @OZ51011
         LR    R5,R1               SAVE PQE ADDRESS            @OZ51011
         DROP  R4                  SUSPEND BFW ADDRESSABILITY  @OZ51011
         SPACE 1                                               @OZ51011
PPCHKPQE L     R1,PQEPREV          GET PREVIOUS PQE            @OZ51011
         CLR   R1,PW               TEST FOR END OF PPQ         @OZ51011
         BE    PPRGPQEJ            BRANCH IF YES               @OZ51011
         CLI   PQETYPE,PQED        TEST FOR PQED               @OZ51011
         BNE   PPCHKCS             BRANCH IF NOT               @OZ51011
         LR    R5,R1               SAVE PQE ADDRESS            @OZ51011
         B     PPCHKPQE            GET NEXT PQE                @OZ51011
         SPACE 1                                               @OZ51011
PPCHKCS  CLI   PQETYPE,PQEC        TEST FOR PQEC               @OZ51011
         BNE   PPCHKPQE            BRANCH IF NOT               @OZ51011
         L     R4,PQECPQED         ADDRESS PQED                @OZ51011
         CLC   PQEDWJOE-PQEDSECT(,R4),PWKJOE  TEST JOE ADDR    @OZ51011
         BNE   PPRGPQEJ            BRANCH IF NOT CURRENT       @OZ51011
         OI    PQEDFLAG-PQEDSECT(R4),PQEDLAST SET LAST D.S.    @OZ51011
         OI    PQECFLAG,PQECLPG    SET LAST PAGE OF DATASET    @OZ51011
         NI    PQECFLAG,FF-PQECBSP RESET STACKED INDICATOR     @OZ51011
         TM    PDCTFLAG,DCTDELET   TEST FOR $C CMD             @OZ51011
         BZ    PPTSTINT            BRANCH IF NOT               @OZ51011
         OI    PQEDFLAG-PQEDSECT(R4),PQEDCAN SET DS CANCELLED  @OZ51011
         OI    PSMFDCI,SMFOPSTP    SET SMF FLAG                @OZ51011
         B     PRSTFLGS            BRANCH TO RESET FLAGS       @OZ51011
         SPACE 1                                               @OZ51011
PPTSTINT TM    PDCTFLAG,DCTBKSP    TEST FOR $I CMD             @OZ51011
         BZ    PPTSTRST            BRANCH IF NOT               @OZ51011
         OI    PQEDFLAG-PQEDSECT(R4),PQEDINT SET DS INTERRUPT  @OZ51011
         OI    PSMFDCI,SMFINTRP    SET SMF FLAG                @OZ51011
         B     PPSETCNT            BRANCH TO SET CMD COUNT     @OZ51011
         EJECT                                                 @OZ51011
PPTSTRST OI    PSMFDCI,SMFRESTR    SET SMF FLAG                @OZ51011
        $#ADD  WORK=PWKJOE,CHAR=PCHJOE REQUEUE JOE FOR $E CMD  @OZ51011
         BZ    PRSTFLGS            BRANCH IF SUCCESSFUL        @OZ51011
         OI    PQEDFLAG-PQEDSECT(R4),PQEDRST DEFER REQUEUE     @OZ51011
         SPACE 1                                               @OZ51011
PPSETCNT IC    R15,PQHCMDCT        INCREMENT COUNT             @OZ51011
         LA    R15,1(,R15)          OF DEFERRED                @OZ51011
         STC   R15,PQHCMDCT          COMMANDS                  @OZ51011
         SPACE 1                                               @OZ51011
PRSTFLGS NI    PDCTFLAG,FF-DCTDELET-DCTRSTRT-DCTBKSP  RESET    @OZ51011
         B     PPREMPQE            BR TO REMOVE INC PQE'S      @OZ51011
         SPACE 1                                               @OZ51011
PPRGPQEJ LA    R4,PQHPQEJ-(PQEJNEXT-PQEDSECT) POINT TO PQEJ0   @OZ51011
         SPACE 1                                               @OZ51011
PPCKJQUE ICM   R1,15,PQEJNEXT-PQEDSECT(R4) GET NEXT PQEJ       @OZ51011
         BZ    PPREMPQE            BRANCH IF NONE              @OZ51011
         CLC   PQEJWJOE,PWKJOE     TEST WORK JOE ADDRESS       @OZ51011
         BE    PDELPQEJ            BRANCH IF CURRENT           @OZ51011
         LR    R4,R1               SAVE PREVIOUS PQEJ ADDR     @OZ51011
         B     PPCKJQUE            BR TO TEST NEXT PQEJ        @OZ51011
         SPACE 1                                               @OZ51011
PDELPQEJ MVC   PQEJNEXT-PQEDSECT(,R4),PQEJNEXT DECHAIN PQEJ    @OZ51011
         LA    R0,1                SET PQE COUNT               @OZ51011
         L     R15,=A(PDELPQE)     CALL PDELPQE                @OZ51011
         BALR  PL,R15               TO DELETE PQEJ             @OZ51011
         SPACE 1                                               @OZ51011
PPREMPQE ST    PW,PQHPIDE          SHOW NO IDS PENDING         @OZ51011
         NI    PDCTFLAG,FF-DCTRPT  RESET $N CMD FLAG           @OZ79804
         LR    R1,R5               POINT TO PQE FOR PURGE      @OZ51011
         CLR   R1,PW               ANY PQES TO PURGE...        @OZ51011
         BE    PPABORT             BRANCH IF NOT               @OZ51011
         SLR   R0,R0               CALL PDELPQE                @OZ51011
         L     R15,=A(PDELPQE)      TO DELETE ALL              @OZ51011
         BALR  PL,R15                INCOMPLETE PQE'S          @OZ51011
         B     PPABORT             BRANCH TO PROC TERMINATION  @OZ51011
         SPACE 1                                               @OZ51011
PPCMDRTN PRETURN ,                 RESTORE REGS AND RETURN     @OZ51011
         DROP  R1,PW               SUSPEND ADDRESSABILITY      @OZ51011
         EJECT                                                 @OZ51011
***************************************************************@G38ESBB
*                                                              @G38ESBB
*        3800 COMMAND PROCESSING CONSISTS OF PURGING THE PAGE  @G38ESBB
*        BUFFER, DETERMINING THE PPQ RESTART PAGE, UPDATING    @G38ESBB
*        THE PPQ, AND SYNCHRONIZING THE DEVICE FOR THE         @G38ESBB
*        RESTART PAGE.                                         @G38ESBB
*                                                              @G38ESBB
*        R1  - ADDRESS OF PENDING PAGE QUEUE ENTRY             @G38ESBB
*        PW  - ADDRESS OF PENDING PAGE QUEUE HEADER            @G38ESBB
*                                                              @G38ESBB
***************************************************************@G38ESBB
         SPACE 1                                               @G38ESBB
P3800CMD PSAVE ,                   SAVE LINKAGE AND BASE       @G38ESBB
         LR    BASE2,R15           SETUP LOCAL                 @G38ESBB
         USING P3800CMD,BASE2           ADDRESSABILITY         @G38ESBB
         L     JCT,PJCTBUF         ADDRESS JCT IF READ         @G38ESBB
         LTR   JCT,JCT             JCT READ...                 @G38ESBB
         BNZ   *+8                 YES, BRANCH                 @G38ESBB
         LA    JCT,JCT             SHOW NON-ZERO JCT FOR I/O   @G38ESBB
         LH    PL,PCCWLAST         GET PCIE OFFSET             @OZ46290
         AL    PL,POUTCCWN         ADD CCW AREA BASE           @OZ46290
         LA    PL,PCIESIZE(,PL)    POINT TO BUFFER WORK AREA   @OZ46290
         MVI   BFWPQECT-BFWDSECT(PL),0  ZERO COUNT             @OZ46290
         LH    PL,PCCWLAST         GET PCIE OFFSET             @OZ46290
         AL    PL,POUTCCWA         ADD CCW AREA BASE           @OZ46290
         LA    PL,PCIESIZE(,PL)    POINT TO BUFFER WORK AREA   @OZ46290
         MVI   BFWPQECT-BFWDSECT(PL),0  ZERO COUNT             @OZ46290
         L     PW,PQHADR           GET PQH ADDRESS             @OZ46290
         TM    PQHFLAG-PQHDSECT(PW),PQH2CMD  TEST DOUBLE CMD   @OZ46290
         BO    PDETCMD             BRANCH IF YES               @OZ46290
         L     PW,PCEDCT           GET DCT ADDRESS             @OZ46290
         TM    DCTPPSW2-DCTDSECT(PW),DCTCKJAM  JAM/CKEY...     @OZ46290
         BO    PCMDSET             YES, BR, ALREADY PURGED     @OZ46290
         LM    PC1,PC2,PCCWXORD    LOAD EXEC ORDER CCW         @OZ46290
         LA    R1,BFWPPB-BFWDSECT(,PL) LOAD ADDR PURGE PG BUF  @OZ46290
         ALR   PC1,R1              PLACE ORDER CODE ADR IN CCW @G38ESBB
         BAL   PL,PPPUT2           ADD CCW TO AREA             @OZ51441
         BAL   PL,PPWRITE          WRITE CCW AREA              @G38ESBB
         BAL   PL,PPCHECK          COMPLETE I/O                @G38ESBB
         SPACE 1                                               @G38ESBB
PCMDSET  TM    PPFLAG2,PPRSW       IF A READ IS                @G38ESBB
         BZ    PCMDDSV              OUTSTANDING, CLEAR         @G38ESBB
         BAL   PL,PRDTCHK            ALL INPUT I/O             @G38ESBB
         EJECT                                                 @OZ46290
PCMDDSV  L     R15,PCHJOE          ADDRESS CHARACTERISTICS JOE @G38ESBB
         MVC   SPFORMS(2*4),JOEFORM-JOEDSECT(R15) FORMS,FCB    @G38ESBB
         MVC   SPFLASH,JOEFLASH-JOEDSECT(R15) MSG WON'T FLASH  @G38ESBB
         MVC   SPMODF,=C'****'     RESET COPY MODIFICATION     @G38ESBB
         MVI   SPCOPYN,1           FORCE ONLY 1 COPY OF MSG    @G38ESBB
         MVI   SPCOPYS,1           INDICATE STARTING COPY NUM  @G38ESBB
         MVI   SPFLAG,SPSEP+SPCMD  INIT FLAGS FOR MSG SETUP    @OZ75372
         TM    JOECFLAG-JOEDSECT(R15),$JOEBRST  B=Y REQUEST... @OZ47910
         BZ    *+8                 BR IF NOT                   @OZ47910
         OI    SPFLAG,SPBURST      SET B=Y (BURSTING REQD)     @OZ47910
         L     R15,=A(PRPUDSV)     CALL DEVICE                 @G38ESBB
         BALR  PL,R15               SETUP VERIFICATION         @G38ESBB
         L     PW,PQHADR           GET PQH ADDRESS             @OZ75372
         B     PCMDDSVR(R15)       BRANCH ON RETURN CODE       @OZ75372
         SPACE 1                                               @OZ75372
PCMDDSVR B     PDETCMD             0  NORMAL RETURN            @OZ75372
         B     PDETCMD             4  OPER TERMINATE           @OZ75372
         B     PCMDJAM             8  3800 JAM OR CANCEL KEY   @OZ75372
         B     PCMDTERM            12 3800 ERROR IN SETUP      @OZ75372
         SPACE 1                                               @OZ75372
PCMDJAM  NI    PDCTFLAG,FF-DCTDELET-DCTRSTRT-DCTBKSP RESET     @OZ75372
         L     R15,=A(PLOCATE)     CALL THE LOCATE ROUTINE     @OZ75372
         BALR  PL,R15               TO GET THE ORIGIN PQE      @OZ75372
         B     PDETCMD             CONTINUE CMD PROCESSING     @OZ75372
         SPACE 1                                               @OZ75372
PCMDTERM OI    PPFLAG3,PPDVNAVL                   INIT FLAGS   @OZ75372
         OI    PQHAFLAG-PQHDSECT(PW),PQHABORT       FOR PPQ    @OZ75372
         OI    PQHFLAG-PQHDSECT(PW),PQHRSTRT+PQH2CMD  RESTART  @OZ75372
         SPACE 1                                               @OZ75372
***************************************************************@G38ESBB
*                                                              @G38ESBB
*        DETERMINE SPECIFIC COMMAND                            @G38ESBB
*                                                              @G38ESBB
***************************************************************@G38ESBB
         SPACE 1                                               @G38ESBB
PDETCMD  DS    0H                                              @OZ75372
         TM    PQHFLAG-PQHDSECT(PW),PQHRSTRT  PPQ RESTART...   @OZ47595
         BO    PQRSTRT             BRANCH IF YES               @OZ47595
         TM    PDCTFLAG,DCTDELET+DCTRSTRT TERMINATION COMMAND  @OZ47595
         BNZ   P3800CEI            BR IF YES                   @G38ESBB
         L     R0,PFSBSCT          TEST FOR $B                 @G38ESBB
         LTR   R0,R0                OR $F                      @G38ESBB
         BH    PFSPACE             GO PROCESS $F OR CANCEL KEY @G38ESBB
         B     PBSPACE             GO PROCESS $B OR PAPER JAM  @G38ESBB
 TITLE 'HASP PRINT/PUNCH SERVICE -- 3800 $C,$E,$I ROUTINE'     @G38ESBB
***************************************************************@G38ESBB
*                                                              @G38ESBB
*        3800 $C,$E,$I ROUTINE                                 @G38ESBB
*                                                              @G38ESBB
***************************************************************@G38ESBB
         SPACE 1                                               @G38ESBB
         USING PQEDSECT,R1         PROVIDE PQE ADDRESSABILITY  @G38ESBB
         USING PQHDSECT,PW         PROVIDE PQH ADDRESSABILITY  @G38ESBB
         SPACE 1                                               @G38ESBB
P3800CEI L     PW,PQHADR           ADDRESS PQH                 @G38ESBB
         MVI   PQHFLAG2,PCMDCEI    SET COMMAND FLAG            @OZ47787
         L     R1,PQHOPQE          SET                         @G38ESBB
         ST    R1,PQHTPQE           PQHTPQE                    @G38ESBB
         MVC   PQHTPG,PQHLPG       SET TARGET PAGE ID          @OZ53047
         NI    PQECFLAG,FF-PQECBSP  RESET STACKED INDICATOR    @OZ46674
         OI    PQECFLAG,PQECLPG    SET LAST PAGE OF DATA SET   @G38ESBB
         L     R1,PQECPQED         ADDRESS PQED                @G38ESBB
         OI    PQEDFLAG,PQEDLAST   SET LAST DATA SET OF JOE    @G38ESBB
         SPACE 1                                               @G38ESBB
         TM    PDCTFLAG,DCTDELET   IS COMMAND $C...            @G38ESBB
         BZ    PCHKIN              NO, GO CHECK FOR $I $E      @G38ESBB
         OI    PQEDFLAG,PQEDCAN    SET DATA SET CANCELLED      @G38ESBB
         LA    PL,=C' DELETED    ' SET UP MESSAGE TEXT         @G38ESBB
         B     PCEIMSG             GO WRITE TERMINATION MSG    @G38ESBB
         SPACE 1                                               @G38ESBB
PCHKIN   TM    PDCTFLAG,DCTBKSP    IS COMMAND $I...            @G38ESBB
         BZ    PECMD               NO, GO PROCESS $E           @G38ESBB
         OI    PQEDFLAG,PQEDINT    SET DATA SET INTERRUPTED    @G38ESBB
         IC    R15,PQHCMDCT        INCREMENT                   @G38ESBB
         LA    R15,1(,R15)          COUNT OF DEFERRED          @G38ESBB
         STC   R15,PQHCMDCT          COMMANDS                  @G38ESBB
         LA    PL,=C' INTERRUPTED' SET UP MESSAGE TEXT         @G38ESBB
         B     PCEIMSG             GO WRITE TERMINATION MSG    @G38ESBB
         SPACE 1                                               @G38ESBB
PECMD    LA    PL,=C' RESTARTED  ' SET UP MESSAGE TEXT         @G38ESBB
         L     R15,PQEDWJOE        GET TPQE WORK JOE           @G38ESBB
         LR    R0,R15              PLACE WORK JOE ADDR IN R0   @G38ESBB
         LH    R1,JOECHAR-JOEDSECT(,R15) GET CHAR JOE OFFSET   @G38ESBB
         N     R1,PCLRHALF         CLEAR LEFT HALFWORD         @G38ESBB
         SLL   R1,2                EXPAND TO BYTE OFFSET       @G38ESBB
         AL    R1,$JOTABLE         ADD JOB OUTPUT TABLE ORIGIN @G38ESBB
        $#ADD  WORK=(R0),CHAR=(R1) COPY JOE BACK INTO QUEUE    @G38ESBB
         BZ    PCEIMSG             ADD SUCCESSFUL, WRITE MSG   @G38ESBB
         L     R1,PQHOPQE          ADDRESS ORIGIN PQE          @G38ESBB
         L     R1,PQECPQED         ADDRESS DATA SET PQE AT ORG @G38ESBB
         OI    PQEDFLAG,PQEDRST    DEFER $E COMMAND            @G38ESBB
         IC    R14,PQHCMDCT        INCREMENT                   @G38ESBB
         LA    R14,1(,R14)             DEFERRED                @G38ESBB
         STC   R14,PQHCMDCT                COMMAND COUNT       @G38ESBB
         SPACE 1                                               @G38ESBB
PCEIMSG  TM    PQHFLAG,PQH2CMD     DOUBLE COMMAND...           @G38ESBB
         BZ    PRMSGCEI            NO, GO WRITE MSG TO PRINTER @G38ESBB
         L     R15,PCEDCT          GET DCT ADDRESS             @G38ESBB
         USING DCTDSECT,R15        PROVIDE DCT ADDRESSABILITY  @G38ESBB
        $MID   170                                             @G38ESBB
         PMSG  PMESSAGE,M170L2,(X'170F',DCTDEVN)               @G38ESBBC
                                   MOVE MESSAGE TEXT           @G38ESBB
         MVC   PMESSAGE+M170L2(12),0(PL) MOVE MESSAGE TEXT     @G38ESBB
         DROP  R15                 DROP DCT ADDRESSABILITY     @G38ESBB
        $WTO   PMESSAGE,M170L2+12,ROUTE=$LOG+$UR, SEND MSG     @G38ESBBC
               CLASS=$NORMAL,PRI=$ST,JOB=NO        TO OPERATOR @G38ESBB
         B     PGETMAPV            GO DETERMINE FCB MAP VALUE  @G38ESBB
         SPACE 1                                               @G38ESBB
PRMSGCEI DS    0H                                              @OZ48259
         LR    R14,PL              GET MESSAGE TEXT            @OZ48259
         LA    R1,=C'$HASP170 '    SET UP MESSAGE ID           @OZ48259
         L     R15,=A(PRMSG)       ISSUE MESSAGE TO OPERATOR   @G38ESBB
         BALR  PL,R15               AND ADD IT TO OUTPUT       @OZ48259
         B     PGETMAPV            GO DETERMINE FCB MAP VALUE  @G38ESBB
 TITLE 'HASP PRINT/PUNCH SERVICE -- 3800 BACKSPACE ROUTINE'    @G38ESBB
***************************************************************@G38ESBB
*                                                              @G38ESBB
*        3800 BACKSPACE ROUTINE                                @G38ESBB
*                                                              @G38ESBB
***************************************************************@G38ESBB
         SPACE 1                                               @G38ESBB
         SPACE 1                                               @G38ESBB
PBSPACE  L     R0,PFSBSCT          GET BACKSPACE NUMBER        @G38ESBB
         LPR   R0,R0               MAKE BACKSPACE CT POSITIVE  @G38ESBB
         ST    R0,PFSBSCT          SAVE POSITIVE BACKSPACE CT  @G38ESBB
         SPACE 1                                               @G38ESBB
PBSTART  L     PW,PQHADR           ADDRESS PQH                 @G38ESBB
         MVI   PQHFLAG2,PCMDBKSP   SET COMMAND FLAG            @OZ47787
         L     R1,PQHOPQE          GET ORIGIN PQE              @G38ESBB
         ST    R1,PQHTPQE          SET TARGET PQE TO ORIG PQE  @G38ESBB
         LH    R14,PQHLPG          GET LOCATED XFER STN PGID   @OZ46351
         L     R15,PCEDCT          GET DCT ADDRESS             @G38ESBB
         TM    DCTPPSW2-DCTDSECT(R15),DCTBFCKP TEST $BPRTN,C   @G38ESBB
         BZ    PTESTBD             BR IF NOT                   @G38ESBB
         NI    DCTPPSW2-DCTDSECT(R15),FF-DCTBFCKP RESET C FLAG @G38ESBB
         LH    R14,PQERPGID        STARTING PG IS PQE PAGE     @G38ESBB
         CLC   PQEFCBLN,=H'1'      PQEC AT TOP OF PAGE...      @G38ESBB
         BE    PTESTBD             YES, BYPASS INCREMENT       @G38ESBB
         LA    R14,1(,R14)         INCR FOR TOP OF NEXT PAGE   @G38ESBB
         SPACE 1                                               @G38ESBB
PTESTBD  CLC   PFSBSCT,PMAXPAGE    $B DATA SET...              @G38ESBB
         BNE   PCOPYG              BR IF NOT                   @G38ESBB
         TM    PQECFLAG,PQECFPG    FIRST PAGE OF DATASET...    @G38ESBB
         BZ    PGETPREV            BR IF NOT                   @G38ESBB
         SPACE 1                                               @G38ESBB
PSETPG   MVC   PQHTPG,PQERPGID     SET TARGET PAGE ID          @OZ53047
         B     PBSPEXIT            BR TO RETURN                @G38ESBB
         SPACE 1                                               @G38ESBB
PCOPYG   LA    R0,1                DEFAULT COPY GROUP TO ONE   @G38ESBB
         TM    PQECFLAG,PQECLPG    TEST FOR DATA SET BOUNDARY  @G38ESBB
         BO    PDECRCT             BR IF YES                   @G38ESBB
         L     R15,PQECPQED        DETERMINE DATA SET          @G38ESBB
         IC    R0,PQEDCGCT-PQEDSECT(,R15) COPYGROUPING         @G38ESBB
***************************************************************@G38ESBB
*                                                              @G38ESBB
*        MODIFY BACKSPACE AMOUNT BY COPY GROUP FACTOR          @G38ESBB
*                                                              @G38ESBB
***************************************************************@G38ESBB
         SPACE 1                                               @G38ESBB
PDECRCT  LR    PC2,R14             GET LARGER PAGE ID          @G38ESBB
         SH    PC2,PQERPGID        GET PAGE DIFFERENCE         @G38ESBB
         N     PC2,PCLRHALF        CLEAR LEFT HALFWORD         @G38ESBB
         MR    PC1,R0              MULTIPLY BY COPY GROUP      @G38ESBB
         L     PC1,PFSBSCT              TO GET PHYSICAL PAGES  @G38ESBB
         SR    PC1,PC2             DECREMENT B'SPACE AMT       @G38ESBB
         ST    PC1,PFSBSCT              BY # OF PHYSICAL PAGES @G38ESBB
         LTR   PC1,PC1             BACKSPACING DONE            @G38ESBB
         BP    PPQETEST            BR IF NO                    @G38ESBB
         LPR   PC1,PC1             MAKE COUNT POSITIVE         @G38ESBB
         L     R15,PQECPQED        ADDRESS PQED                @G38ESBB
         CLM   PC1,1,PQEDCGCT-PQEDSECT(R15) IS TPQE IS ON SAME @G38ESBBC
                                   LOGICAL PAGE AS TARGET...   @G38ESBB
         BNL   PBSDONE             NO,TPQE PRIOR,DONE,BRANCH   @G38ESBB
         CLC   PQEFCBLN,=H'1'      YES, MUST BE AT TOP OF FCB  @G38ESBB
         BE    PBSDONE              TO BE DONE BACKSPACING     @G38ESBB
         EJECT                                                 @G38ESBB
***************************************************************@G38ESBB
*                                                              @G38ESBB
*        TEST FOR END OF PPQ                                   @G38ESBB
*                                                              @G38ESBB
***************************************************************@G38ESBB
         SPACE 1                                               @G38ESBB
PPQETEST TM    PQECFLAG,PQECFPG+PQECBSP DS BOUND IN STACKER    @G38ESBB
         BO    PSETPG              BR IF YES, TPG FOUND        @G38ESBB
         LH    R14,PQERPGID        UPDATE LARGER PAGE ID       @G38ESBB
         SPACE 1                                               @G38ESBB
PGETPREV LR    R0,R1               SAVE PQEC ADDRESS           @G38ESBB
         LA    R15,PQHFIRST-(PQENEXT-PQEDSECT) ADDRESS PQE0    @G38ESBB
         SPACE 1                                               @G38ESBB
PGTPRVLP L     R1,PQEPREV          GET PREVIOUS PQE            @G38ESBB
         CR    R1,R15              END OF PPQ...               @G38ESBB
         BNE   PGTPQEC             NO, GO CHECK FOR PQEC       @G38ESBB
         LR    R1,R0               RESTORE SAVED FPG PQEC      @G38ESBB
         B     PSETPG              DONE, BRANCH                @G38ESBB
         SPACE 1                                               @G38ESBB
PGTPQEC  CLI   PQETYPE,PQEC        IS THIS PQE A CHECKPT PQE   @G38ESBB
         BNE   PGTPRVLP            BR IF NOT                   @G38ESBB
         TM    PQECFLAG,PQECLPG    END OF DATA SET...          @G38ESBB
         BZ    PTESTBD             NO, BRANCH TO CONTINUE SCAN @G38ESBB
         L     R15,PQECPQED        ADDRESS DATA SET PQE        @G38ESBB
         TM    PQEDFLAG-PQEDSECT(R15),PQEDLAST JOE BOUNDARY... @G38ESBB
         BZ    PTESTBD             NO, BRANCH TO CONTINUE SCAN @G38ESBB
         OI    PQHFLAG,PQHHDR      INDICATE HEADER NEEDED      @G38ESBB
         TM    PQEDFLAG-PQEDSECT(R15),PQEDRPT+PQEDINT+PQEDRST+PQEDCAN  C
                                   DEFERRED COMMAND...         @G38ESBB
         BZ    PTESTBD             BRANCH IF NO                @G38ESBB
         B     PLOCPQEC            RESUME AT NEXT PQE          @G38ESBB
         SPACE 1                                               @G38ESBB
PBSDONE  TM    PQECFLAG,PQECLPG    DATA SET BOUNDARY           @G38ESBB
         BZ    PSETTPG             BR IF NOT                   @G38ESBB
         L     R15,PCEDCT          GET DCT ADDRESS             @G38ESBB
         TM    DCTPPSW2-DCTDSECT(R15),DCTCKJAM  PAPER JAM...   @OZ46952
         BZ    PLOCPQEC            BR IF NOT                   @G38ESBB
         L     R15,PQECPQED        GET DATA SET PQE            @G38ESBB
         TM    PQEDFLAG-PQEDSECT(R15),PQEDLAST JOE BOUNDARY    @G38ESBB
         BZ    PLOCPQEC            BR IF NOT                   @G38ESBB
         OI    PQHFLAG,PQHHDR      TURN ON HEADER NEEDED INDIC @G38ESBB
         SPACE 1                                               @G38ESBB
PLOCPQEC L     R1,PQENEXT          GET NEXT PQE AS RESTART     @G38ESBB
         CLI   PQETYPE,PQEC        IS THIS A CHECKPT PQE       @G38ESBB
         BNE   PLOCPQEC            BR IF NOT                   @G38ESBB
         B     PSETPG              GO SET TARGET PAGE          @G38ESBB
         SPACE 1                                               @G38ESBB
PSETTPG  SLR   PC1,PC1             CLEAR WORK REGISTER         @G38ESBB
         L     PC2,PFSBSCT         GET BACKSPACE AMOUNT        @G38ESBB
         LPR   PC2,PC2             GET POSITIVE BKSP AMOUNT    @G38ESBB
         DR    PC1,R0              DIVIDE BY COPY GROUP        @G38ESBB
         LH    PC1,PQERPGID        GET TARGET PAGE ID          @G38ESBB
         AR    PC1,PC2             ADD LOGICAL BACKSPACE AMT   @G38ESBB
         STH   PC1,PQHTPG          SET TARGET PAGE ID          @OZ53047
         EJECT                                                 @G38ESBB
***************************************************************@G38ESBB
*                                                              @G38ESBB
*        WRITE OUT BACKSPACE OR PAPER JAM MESSAGE              @G38ESBB
*                                                              @G38ESBB
***************************************************************@G38ESBB
         SPACE 1                                               @G38ESBB
PBSPEXIT ST    R1,PQHTPQE          SET TARGET PQE ADDRESS      @G38ESBB
         NI    PQECFLAG,FF-PQECLPG RESET LP FLAG ON TARGET PQE @OZ66187
         OI    PPFLAG4,PPREPOBS    IND DUE TO BACKSPACE        @OZ78256
         CLC   PQHTPG,PQERPGID     ANY MAPPING NEEDED...       @OZ53047
         BNE   PBSPXIT             BRANCH IF YES               @OZ51592
         OI    PPFLAG3,PP3800S     IND REPOSITIONING           @OZ51592
         SPACE 1                                               @OZ51592
PBSPXIT  L     R15,PCEDCT          GET DCT ADDRESS             @OZ47734
         L     R1,PQECPQED         GET PQED ADDRESS            @OZ56978
         NI    PQEDFLAG,FF-PQEDLAST RESET LAST DATASET IND     @OZ56978
         USING DCTDSECT,R15        PROVIDE DCT ADDRESSABILITY  @G38ESBB
         TM    DCTPPSW2,DCTCKJAM   PAPERJAM...                 @G38ESBB
         BZ    PBWRITE             BR IF NOT                   @G38ESBB
         SPACE 1                                               @OZ66187
*        IF PAPER JAM, RESET ALL DEFERRED COMMANDS FOR DATASET @OZ66187
         SPACE 1                                               @OZ66187
         TM    PQEDFLAG,PQEDCAN+PQEDINT+PQEDRPT+PQEDRST  ANY   @OZ66187X
                                   DEFERRED COMMANDS ...       @OZ66187
         BZ    PBNDFCMD            NO, ISSUE FUSER RST. MSG    @OZ66187
         NI    PQEDFLAG,FF-PQEDCAN-PQEDINT-PQEDRPT-PQEDRST     @OZ66187X
                                   ELSE, RESET DEF. CMD BITS   @OZ66187
         IC    R1,PQHCMDCT         DECREMENT DEFERRED          @OZ66187
         BCTR  R1,0                 COMMAND                    @OZ66187
         STC   R1,PQHCMDCT           COUNT                     @OZ66187
PBNDFCMD DS    0H                  CONTINUE                    @OZ66187
         SPACE 1                                               @OZ66187
        $MID   153                 PAPER JAM MESSAGE           @G38ESBB
         PMSG  PMESSAGE,,(C'$HASP153 ',DCTDEVN,C' JAMMED, FUSER RESTARTC
               ')                  MOVE MESSAGE TEXT           @G38ESBB
         L     R15,=A(PCOMMENT)    ADD MESSAGE                 @G38ESBB
         BALR  PL,R15               TO OUTPUT                  @G38ESBB
         L     R15,PCEDCT          ADDRESS PRPU DCT            @G38ESBB
         PMSG  PMESSAGE,M153L,(X'153F',DCTDEVN,C' JAMMED, FUSER RESTARTC
               ')                  MOVE MESSAGE TEXT           @G38ESBB
         DROP  R15                 DROP DCT ADDRESSABILITY     @G38ESBB
        $WTO   PMESSAGE,M153L,ROUTE=$LOG+$UR, INFORM OPERATOR  @G38ESBBC
               CLASS=$NORMAL,PRI=$ST,JOB=NO    OF PAPER JAM    @G38ESBB
         B     PGETMAPV            GO DETERMINE MAPPING VALUE  @G38ESBB
         SPACE 1                                               @G38ESBB
PBWRITE  LA    R14,=C' BACKSPACED ' SET FOR $B MESSAGE         @OZ48259
        $MID   170                 MESSAGE IDENTIFIER          @G38ESBB
         LA    R1,=C'$HASP170 '    POINT TO MESSAGE ID         @G38ESBB
         L     R15,=A(PRMSG)       ISSUE MSG TO OPERATOR       @G38ESBB
         BALR  PL,R15              ADD MSG TO OUTPUT           @OZ48259
         B     PGETMAPV            GO DETERMINE FCB MAPPING    @G38ESBB
 TITLE 'HASPPRPU -- 3800 FORWARD SPACE ROUTINE'                @G38ESBB
***************************************************************@G38ESBB
*                                                              @G38ESBB
*        3800 FORWARD SPACE                                    @G38ESBB
*                                                              @G38ESBB
***************************************************************@G38ESBB
         SPACE 1                                               @G38ESBB
PFSPACE  L     PW,PQHADR           SET PQH ADDRESS             @G38ESBB
         MVI   PQHFLAG2,PCMDFWSP   SET COMMAND FLAG            @OZ47787
         L     R1,PQHOPQE          GET ORIGIN PQE              @G38ESBB
         ST    R1,PQHTPQE          SET INITIAL TARGET PQE      @G38ESBB
         L     R15,PQECPQED        GET DATA SET PQE            @G38ESBB
         SLR   R0,R0               CLEAR COPY GROUP REG        @G38ESBB
         IC    R0,PQEDCGCT-PQEDSECT(R15) LOAD COPY GROUP VALUE @G38ESBB
         SPACE 1                                               @G38ESBB
         USING DCTDSECT,R15        PROVIDE DCT ADDRESSABILITY  @OZ51866
PFDETCT  L     R15,PCEDCT          GET DCT ADDRESS             @G38ESBB
         TM    DCTPPSW2,DCTCKJAM   CANCEL KEY...               @OZ51866
         BZ    PFNCKEY             BRANCH IF NOT               @OZ51866
         TM    PQECFLAG,PQECLPG    TEST FOR END OF DATASET     @OZ51866
         BZ    PFNCKEY             BRANCH IF NOT               @OZ51866
        $MID   152                                             @OZ51866
         PMSG  PMESSAGE,M152L3,(X'152F',DCTDEVN,  SET MESSAGE  @OZ51866C
               C' COMMAND REJECTED')               TEXT        @OZ51866
        $WTO   PMESSAGE,M152L3,JOB=NO,                         @OZ51866C
               ROUTE=$LOG+$UR,CLASS=$NORMAL,PRI=$ST            @OZ51866
         LA    R14,PPDSEND2        ALTER                       @OZ56978
         L     R1,PQHTPQE           RETURN                     @OZ56978
         L     R1,PQECPQED           ADDRESS                   @OZ56978
         TM    PQEDFLAG,PQEDLAST      TO BYPASS                @OZ56978
         BZ    *+8                     LAST                    @OZ56978
         LA    R14,PPABORT              PAGE                   @OZ56978
         L     R15,PSAVAREA              PQEC                  @OZ56978
         ST    R14,2*4(,R15)              CREATION             @OZ56978
         XC    PQHMAPV,PQHMAPV     CLEAR MAPPING VALUE         @OZ51866
         LH    R0,PQHOPG           GET ORIGIN PAGE             @OZ51866
         SH    R0,PQERPGID         FIND RECOMPUTE VALUE        @OZ51866
         N     R0,PCLRHALF         CLEAR LEFT HALFWORD         @OZ51866
         L     R15,=A(PRECOMP)     CALL RECOMPUTE TO           @OZ51866
         BALR  PL,R15               UPDATE PPQ RPGIDS          @OZ51866
         B     PFREJCT             BYPASS TARGET SET           @OZ51866
         EJECT                                                 @OZ51866
PFNCKEY  LH    R14,PQHLPG          GET LOCATED XFER STN PGID   @OZ51866
         TM    DCTPPSW2,DCTBFCKP   $FPRTN,C...                 @OZ51866
         BZ    PGETTPG             BR IF NOT                   @G38ESBB
         NI    DCTPPSW2,FF-DCTBFCKP  RESET CKPT FLAG           @OZ51866
         LH    R14,PQERPGID        TPQE HAS SMALLER ID         @G38ESBB
         CLC   PQEFCBLN,PFCBTOP    PQEC AT TOP OF PAGE...      @G38ESBB
         BE    PGETTPG             YES, BYPASS INCREMENT       @G38ESBB
         LA    R14,1(,R14)         INCR TO TOP OF NEXT PAGE    @G38ESBB
         DROP  R15                 SUSPEND DCT ADDRESSABILITY  @OZ51866
         SPACE 1                                               @G38ESBB
PGETTPG  CLC   PFSBSCT,PMAXPAGE    $F TO END OF DATA SET...    @G38ESBB
         BE    PNEXTPQE            BR IF YES                   @G38ESBB
         L     PC2,PFSBSCT         GET FORWARD SPACE AMOUNT    @G38ESBB
         SLR   PC1,PC1             CLEAR DIVIDE REGISTER       @G38ESBB
         DR    PC1,R0              DIVIDE BY COPY GROUPING     @G38ESBB
         AR    R14,PC2             ADD TO ORIGIN PAGE TO       @G38ESBB
         STH   R14,PQHTPG           GET TARGET PAGE ID         @OZ53047
         SPACE 1                                               @G38ESBB
PNEXTPQE L     R1,PQENEXT          GET NEXT PQE                @G38ESBB
         LA    R14,PQHFIRST-(PQENEXT-PQEDSECT) GET PQE0        @G38ESBB
         CR    R1,R14              END OF PQE...               @G38ESBB
         BE    PPQEND              BR IF YES                   @G38ESBB
         CL    R1,PQHPIDE          IS PQE COMPLETE...          @G38ESBB
         BE    PPQEND              NO, BRANCH                  @G38ESBB
         CLI   PQETYPE,PQEC        IS THIS A CHECKPT PQE       @G38ESBB
         BNE   PNEXTPQE            BR IF NOT                   @G38ESBB
         CLC   PFSBSCT,PMAXPAGE    $F PRTN,D...                @G38ESBB
         BE    PUPDTPQE            BR IF YES                   @G38ESBB
         LH    R14,PQHTPG          GET TARGET PAGE ID          @OZ53047
         SH    R14,PQERPGID        DETERMINE IF TARGET         @G38ESBB
         LTR   R14,R14              PAGE AT TPQE               @G38ESBB
         BNZ   PTSTFEND            NO, BRANCH                  @G38ESBB
         CLC   PQEFCBLN,PFCBTOP    YES, TPQE GOOD IF AT TOP    @G38ESBB
         BE    PUPDTPQE             OF PAGE, BRANCH            @G38ESBB
         B     PFSPDONE            NO,DONE,PREVIOUS TPQE GOOD  @G38ESBB
         SPACE 1                                               @G38ESBB
PTSTFEND N     R14,PCLRHALF        ELSE, SEE IF PAGE ID HAS    @G38ESBB
         C     R14,PQELIMIT             REACHED TARGET PAGE    @G38ESBB
         BH    PFSPDONE            BR IF YES                   @G38ESBB
         EJECT                                                 @OZ51866
PUPDTPQE ST    R1,PQHTPQE          SAVE NEW TARGET PQE         @G38ESBB
         TM    PQECFLAG,PQECLPG    TEST FOR END OF DATA SET    @G38ESBB
         BZ    PNEXTPQE            CONTINUE SCAN IF NOT        @G38ESBB
         MVC   PQHMAPV,PFDSET      INDIC END OF DATA SET       @G38ESBB
         LH    R0,PQHOPG           GET ORIGIN PAGE             @G38ESBB
         SH    R0,PQERPGID         FIND RECOMPUTE VALUE        @G38ESBB
         AH    R0,=H'1'            ADD 1 FOR MESSAGE PAGE      @G38ESBB
         N     R0,PCLRHALF         CLEAR LEFT HALFWORD         @G38ESBB
         L     R15,=A(PRECOMP)     CALL RECOMPUTE ROUTINE      @G38ESBB
         BALR  PL,R15                   TO UPDATE PPQ RPGIDS   @G38ESBB
         LA    PL,PFREJCT          SET BR TO BYPASS TGT SET    @OZ48259
         B     PFSPEXIT            BR TO WRITE MSG             @G38ESBB
         SPACE 1                                               @G38ESBB
PPQEND   CLC   PFSBSCT,PMAXPAGE    $F PRTN,D...                @G38ESBB
         BNE   PFSPDONE            BR IF NOT                   @G38ESBB
         MVC   PQHMAPV,PFSPDSET    INDICATE $FPRTN,D           @G38ESBB
         LA    PL,PFREJCT          SET BR TO BYPASS TGT SET    @OZ48259
         B     PFSPEXIT            BR IF YES                   @G38ESBB
         SPACE 1                                               @G38ESBB
PFSPDONE LA    PL,PGETMAPV         SET BR TO SET TGT PAGE      @OZ48259
         L     R1,PQHTPQE          GET TARGET PQE ADDR         @OZ51592
         CLC   PQHTPG,PQERPGID     ANY MAPPING NEEDED          @OZ53047
         BNE   PFSPEXIT            BRANCH IF YES               @OZ51592
         OI    PPFLAG3,PP3800S     IND REPOSITIONING           @OZ51592
         SPACE 1                                               @G38ESBB
PFSPEXIT LA    R14,=C' FWD-SPACED ' SET FOR $F MESSAGE         @OZ48259
        $MID   170                 MESSAGE IDENTIFIER          @G38ESBB
         LA    R1,=C'$HASP170 '    POINT TO MSG ID             @G38ESBB
         L     R15,=A(PRMSG)       SEND MSG TO OPERATOR        @G38ESBB
         BR    R15                 ADD MESSAGE TO OUTPUT       @OZ48259
 TITLE 'HASP PRINT/PUNCH SERVICE -- 3800 RESCHEDULE ROUTINE'   @OZ47595
***************************************************************@OZ47595
*                                                              @OZ47595
*        3800 PENDING PAGE QUEUE RESTART ROUTINE               @OZ47595
*                                                              @OZ47595
***************************************************************@OZ47595
         SPACE 1                                               @OZ47595
         USING DCTDSECT,R15                                    @OZ51930
         SPACE 1                                               @OZ51930
PQRSTRT  DS    0H                                              @OZ47595
        $MID  170                                              @OZ51930
         L     PW,PQHADR           GET PQH ADDRESS             @OZ51930
         L     R15,PCEDCT          GET DCT ADDRESS             @OZ51930
         TM    PQHFLAG,PQH2CMD     TEST FOR INHIBIT I/O        @OZ51930
         BZ    PQRMSG              BRANCH IF NOT               @OZ51930
         NI    PQHFLAG,FF-PQH2CMD  RESET INHIBIT I/O FLAG      @OZ51930
         PMSG  PMESSAGE,M170L3,(X'170F',DCTDEVN,C' ABORTED')   @OZ75372C
                                   MOVE MESSAGE TEXT           @OZ51930
        $WTO   PMESSAGE,M170L3,    INFORM OPERATOR             @OZ51930C
               ROUTE=$LOG+$UR,CLASS=$NORMAL,PRI=$ST,JOB=NO     @OZ51930
         B     PQRSET              CONTINUE                    @OZ51930
         SPACE 1                                               @OZ51930
PQRMSG   LA    R14,=C' ABORTED    ' SETUP MESSAGE TEXT         @OZ75372
         LA    R1,=C'$HASP170 '    SET UP MESSAGE ID           @OZ47595
         L     R15,=A(PRMSG)       ADDRESS MESSAGE ROUTINE     @OZ47595
         BALR  PL,R15              ISSUE MESSAGE               @OZ47595
         L     PW,PQHADR           SET PQH ADDRESS             @OZ47595
         SPACE 1                                               @OZ51930
PQRSET   NI    PDCTFLAG,FF-DCTDELET-DCTRSTRT-DCTBKSP-DCTRPT    @OZ51930
         NI    PPFLAG,FF-PPDELSW-PRDELSW  RESET ALL FLAGS      @OZ47595
         L     R15,PCEDCT          RESTORE DCT ADDRESS         @OZ51930
         NI    DCTPPSW2,FF-DCTCKJAM RESET CKEY/JAM FLAG        @OZ51930
         XC    PQHMAPV,PQHMAPV     CLEAR MAPPING VALUE         @OZ47595
         TM    PQHAFLAG,PQHABORT   TEST FOR PPQ ERROR          @OZ49145
         BZ    PQRSTRT1            BRANCH IF NOT               @OZ49145
         ST    PW,PQHOPQE          SET ORIGIN TO PQE0          @OZ49145
         OI    DCTSTAT,DCTDRAIN    SET DRAIN FLAG              @OZ51930
         SPACE 1                                               @OZ49145
PQRSTRT1 L     R1,PQHOPQE          GET PQE ADDRESS             @OZ49145
         SLR   R0,R0               CLEAR WORK JOE ADDR         @OZ47595
         LA    PL,PRSCHDLP         RESCHEDULE JOE'S            @OZ47595
         BR    PL                   IN 3800 PIPELINE           @OZ47595
         SPACE 1                                               @OZ51930
         DROP R15                  SUSPEND DCT ADDRESSABILITY  @OZ51930
         EJECT                                                 @OZ47595
***************************************************************@G38ESBB
*                                                              @G38ESBB
*        FIRST DETERMINE FCB MAPPING REQUIREMENTS,             @G38ESBB
*        THEN RESCHEDULE JOES IN 3800 PIPELINE                 @G38ESBB
*                                                              @G38ESBB
***************************************************************@G38ESBB
         SPACE 1                                               @G38ESBB
PGETMAPV L     PW,PQHADR           ADDRESS PQH                 @G38ESBB
         LH    R0,PQHOPG           GET ORIGIN PAGE ID          @G38ESBB
         LH    R15,PQHTPG          GET TARGET PAGE ID          @OZ53047
         SR    R0,R15              DISTANCE FROM TARGET PAGE   @G38ESBB
         TM    PQHFLAG,PQH2CMD     DOUBLE COMMAND...           @G38ESBB
         BZ    PSETMAPV            NO, GO DETERMINE MAP VALUE  @G38ESBB
         XC    PQHMAPV,PQHMAPV     NO MAPPING FOR DOUBLE CMD   @G38ESBB
         B     PCALRCMP            BYPASS FCB MAPPING CODE     @G38ESBB
         SPACE 1                                               @G38ESBB
PSETMAPV AH    R0,=H'1'            ADD 1 FOR MSG PAGE          @G38ESBB
         L     R1,PQHTPQE          GET MAPPING ORIGIN          @G38ESBB
         SH    R15,PQERPGID        COMPUTE FCB                 @G38ESBB
         STH   R15,PQHMAPV+2        MAPPING VALUE              @OZ76135
         MVC   PQHFCBLN,PQEFCBLN   SET INITIAL FCB MAP INDEX   @G38ESBB
         SLR   R15,R15             INITIALIZE LINE COUNT TO 0  @G38ESBB
         TM    PQECFLAG,PQECFPG    RESTART AT BEGIN DATA SET.. @G38ESBB
         BO    *+6                 YES, BYPASS COUNT ADJUST    @G38ESBB
         BCTR  R15,0               ADJUST COUNT TO BEFORE 0    @G38ESBB
         ST    R15,PPLC            SET LINE COUNT              @G38ESBB
         SPACE 1                                               @G38ESBB
PCALRCMP L     R15,=A(PRECOMP)     CALL RECOMPUTE ROUTINE TO   @G38ESBB
         BALR  PL,R15               ADJUST PPQ FOR COMMAND     @G38ESBB
         EJECT                                                 @G38ESBB
***************************************************************@G38ESBB
*                                                              @G38ESBB
*        FREE THE JCT FOR THE JOB AT THE CHANNEL               @G38ESBB
*                                                              @G38ESBB
***************************************************************@G38ESBB
         SPACE 1                                               @G38ESBB
PFREJCT  L     JCT,PJCTBUF         ADDRESS JCT IF READ         @G38ESBB
         LTR   JCT,JCT             JCT READ...                 @G38ESBB
         BZ    PRESCHED            NO, GO RESCHEDULE JOES      @G38ESBB
        $#JCT  FREE                RELEASE JCT BUFFER          @G38ESBB
         SPACE 1                                               @G38ESBB
PRESCHED L     PW,PQHADR           RESTORE PQH ADDRESS         @G38ESBB
         L     R1,PQHTPQE          ADDRESS TARGET PQE          @G38ESBB
         L     R15,PQECPQED        ADDRESS PQED                @G38ESBB
         L     R0,PQEDWJOE-PQEDSECT(,R15) ADDRESS WORK JOE     @G38ESBB
         LA    PL,PRSCHDLP         SET RETURN ADDRESS          @OZ48323
         SPACE 1                                               @G38ESBB
***************************************************************@G38ESBB
*                                                              @G38ESBB
*        RESCHEDULE JOE'S IN 3800 PIPELINE                     @G38ESBB
*                                                              @G38ESBB
***************************************************************@G38ESBB
         SPACE 1                                               @G38ESBB
PRSCHDLP L     R1,PQENEXT          GET NEXT PQE                @G38ESBB
         LA    R15,PQHFIRST-(PQENEXT-PQEDSECT) GET PQE0        @G38ESBB
         CR    R1,R15              END OF PPQ...               @G38ESBB
         BE    PRSCHLST            BRANCH IF YES               @OZ48323
         CLI   PQETYPE,PQES        IS PQE FOR SMF TYPE 6...    @G38ESBB
         BNE   PQECHECK            NO, GO CHECK FOR PQEC       @G38ESBB
         CLC   PPSMFBUF,$ZEROS     HAS SMF UPDATE OCCURRED...  @G38ESBB
         BNE   PFREPQES            YES, BYPASS UPDATE          @G38ESBB
         L     R15,PQESBUF         ADDRESS SMF BUFFER          @G38ESBB
         USING SMFDSECT,R15        PROVIDE SMF ADDRESSABILITY  @G38ESBB
         ST    R15,PPSMFBUF        INDICATE UPDATE OCCURRED    @G38ESBB
         MVC   PTIMEON,SMF6WST     UPDATE WRITER START TIME    @G38ESBB
*        DELETED                                               @G38ESBB
         TM    SMF6IOE,PPJCTIOT    RESTORE                     @G38ESBB
         BZ    *+8                  PPJCTIOT                   @G38ESBB
         OI    PPFLAG,PPJCTIOT       FLAG                      @G38ESBB
*        DELETED                                               @G38ESBB
         MVC   PSMFDCI,SMF6DCI     UPDATE DS CNTL INDICATOR    @G38ESBB
         DROP  R15                 DROP SMF ADDRESSABILITY     @G38ESBB
         SPACE 1                                               @G38ESBB
PFREPQES STM   R0,R1,PQHSAVE1      SAVE REGS ACROSS SUB CALL   @G38ESBB
         L     R1,PQESBUF          ADDRESS SMF BUF FOR SUB     @G38ESBB
         BAL   LINK,PFRESMFB       FREE SMF TYPE 6 BUFFER      @G38ESBB
         LM    R0,R1,PQHSAVE1      RESTORE REGS                @G38ESBB
         BR    PL                  LOOP BACK FOR NEXT PQE      @OZ48323
         EJECT                                                 @G38ESBB
***************************************************************@G38ESBB
*                                                              @G38ESBB
*        PROCESS PQEC'S                                        @G38ESBB
*                                                              @G38ESBB
***************************************************************@G38ESBB
         SPACE 1                                               @G38ESBB
PQECHECK CLI   PQETYPE,PQEC        IS PQE A PQEC...            @G38ESBB
         BNER  PL                  NO, LOOP BACK FOR NEXT PQE  @OZ48323
         TM    PQECFLAG,PQECFPG    FIRST PAGE OF DATA SET...   @G38ESBB
         BZR   PL                  NO, LOOP BACK FOR NEXT PQE  @OZ48323
         L     R15,PQECPQED        ADDRESS PQED                @G38ESBB
         TM    PQEDFLAG-PQEDSECT(R15),PQEDINT $I DATA SET...   @G38ESBB
         BZ    PJOECHK             NO, GO CHECK FOR NEW JOE    @G38ESBB
         IC    R14,PQHCMDCT        DECREMENT                   @G38ESBB
         BCTR  R14,0                COUNT OF                   @G38ESBB
         STC   R14,PQHCMDCT          DEFERRED COMMANDS         @G38ESBB
         SPACE 1                                               @G38ESBB
PJOECHK  CL    R0,PQEDWJOE-PQEDSECT(,R15) NEW JOE...           @G38ESBB
         BER   PL                  NO, LOOP BACK FOR NEXT PQE  @OZ48323
         L     R0,PQEDWJOE-PQEDSECT(,R15) UPDATE WORK JOE ADR  @G38ESBB
PJOECHK1 LR    R15,R0              ADDRESS WORK JOE            @OZ48323
         LH    R15,JOEJQE-JOEDSECT(,R15) GET JQE OFFSET        @G38ESBB
         N     R15,PCLRHALF        CLEAR LEFT HALFWORD         @G38ESBB
         SLL   R15,2               EXPAND TO BYTE OFFSET       @G38ESBB
         AL    R15,$JOBQPTR        ADD JOB QUEUE ORIGIN        @G38ESBB
         TM    JQEFLAGS-JQEDSECT(R15),QUEOPCAN+QUEPURGE $CJ... @G38ESBB
         BNO   PMSGCHK             NO, GO CHECK FOR REQ MSG    @G38ESBB
         STM   R0,R1,PQHSAVE1      SAVE REGS ACROSS CALL       @G38ESBB
         LR    R1,R0               ADDRESS WORK JOE            @G38ESBB
        $#REM  WORK=(R1)           REMOVE WORK JOE FROM JOT    @G38ESBB
         LM    R0,R1,PQHSAVE1      RESTORE REGS                @G38ESBB
         BR    PL                  LOOP BACK FOR NEXT PQE      @OZ48323
         SPACE 1                                               @G38ESBB
***************************************************************@G38ESBB
*                                                              @G38ESBB
*        SEND REQUEUED MESSAGE IF JOB ON DEVICE MESSAGE SENT   @G38ESBB
*                                                              @G38ESBB
***************************************************************@G38ESBB
         SPACE 1                                               @G38ESBB
PMSGCHK  CR    R1,PW               CHECK FOR END OF PPQ        @OZ48323
         BE    PRSCHJOE            BRANCH IF YES               @OZ48323
         L     R10,PQECPQED        ADDRESS PQED                @OZ48323
         L     R10,PQEDWJOE-PQEDSECT(,R10) ADDRESS WORK JOE    @G38ESBB
         L     R15,PQHPQEJ         GET JOB MSG QUEUE HEAD      @G38ESBB
         SPACE 1                                               @G38ESBB
PREQCHK  LTR   R15,R15             END OF JOD QUEUE...         @G38ESBB
         BZ    PREQMSG             BR IF YES                   @G38ESBB
         CL    R10,PQEJWJOE-PQEDSECT(,R15) JOB MSG QUEUED...   @G38ESBB
         BE    PRSCHJOE            BR IF YES, NO REQUEUED MSG  @G38ESBB
         L     R15,PQEJNEXT-PQEDSECT(,R15) GET NEXT JOD PQE    @G38ESBB
         B     PREQCHK             BR TO TEST IF QUEUED        @G38ESBB
         SPACE 1                                               @G38ESBB
PREQMSG  LH    R10,JOEJQE-JOEDSECT(,R10) GET JQE OFFSET        @G38ESBB
         N     R10,PCLRHALF        CLEAR LEFT HALFWORD         @G38ESBB
         SLL   R10,2               COMPUTE BYTE OFFSET         @G38ESBB
         AL    R10,$JOBQPTR        ADD JOB QUEUE ORIGIN        @G38ESBB
         L     R15,PCEDCT          ADDRESS DCT                 @G38ESBB
         USING DCTDSECT,R15        PROVIDE DCT ADDRESSABILITY  @G38ESBB
        $MID   156                 MESSAGE IDENTIFIER          @G38ESBB
         PMSG  PMESSAGE,M156L,(X'156F',DCTDEVN,C' JOB REQUEUED')       C
                                   MOVE MESSAGE TEXT           @G38ESBB
         DROP  R15                 DROP DCT ADDRESSABILITY     @G38ESBB
         STM   R0,R1,PQHSAVE1      SAVE REGS ACROSS MACRO CALL @G38ESBB
        $WTO   PMESSAGE,M156L,     INFORM THE OPERATOR         @G38ESBBC
               ROUTE=$LOG+$UR,CLASS=$NORMAL,PRI=$ST,JOB=YES    @G38ESBB
         LM    R0,R1,PQHSAVE1      RESTORE REGS                @G38ESBB
         SPACE 1                                               @G38ESBB
PRSCHJOE STM   R0,R1,PQHSAVE1      SAVE REGS                   @G38ESBB
         LR    R1,R0               ADDRESS WORK JOE            @G38ESBB
         TM    JOEFLAG-JOEDSECT(R1),$JOECKV CKPT JOE VALID...  @G38ESBB
         BZ    PUTNOCK             NO,GO $#PUT WITHOUT CKPT    @G38ESBB
         LH    R0,JOECKPT-JOEDSECT(,R1) GET CKPT JOE OFFSET    @G38ESBB
         N     R0,PCLRHALF         CLEAR LEFT HALFWORD         @G38ESBB
         SLL   R0,2                EXPAND TO BYTE OFFSET       @G38ESBB
         AL    R0,$JOTABLE         ADD JOT ORIGIN              @G38ESBB
        $#PUT  WORK=(R1),PRC=(R0)  RETURN TO JOT WITH CKPT     @G38ESBB
         LM    R0,R1,PQHSAVE1      RESTORE REGS                @G38ESBB
         BR    PL                  LOOP BACK FOR NEXT PQE      @OZ48323
         SPACE 1                                               @G38ESBB
PUTNOCK $#PUT  WORK=(R1)           RETURN TO JOT - NO CKPT     @G38ESBB
         LM    R0,R1,PQHSAVE1      RESTORE REGS                @G38ESBB
         BR    PL                  LOOP BACK FOR NEXT PQE      @OZ48323
         SPACE 1                                               @G38ESBB
PRSCHLST LA    PL,PRSCHEND         SET RETURN ADDRESS          @OZ48323
         CLC   PCEJQE,$ZEROS       TEST CURRENT JOB            @OZ48323
         BER   PL                  BRANCH IF NONE              @OZ48323
         CL    R0,PWKJOE           TEST CURRENT JOE            @OZ48323
         BER   PL                  BRANCH IF PROCESSED         @OZ48323
         L     R0,PWKJOE           GET CURRENT JOE ADDRESS     @OZ48323
         B     PJOECHK1            PROCESS CURRENT JOE         @OZ48323
         EJECT                                                 @OZ47595
PRSCHEND MVC   PPSMFBUF,$ZEROS     CLEAR SMF UPDATE INDICATOR  @G38ESBB
         TM    PQHFLAG,PQHRSTRT    PPQ RESTART...              @OZ47595
         BZ    PRSCHDAL            BRANCH IF NOT               @OZ47595
         NI    PQHFLAG,FF-PQHRSTRT RESET RESTART FLAG          @OZ47595
         NI    PQHAFLAG,FF-PQHABORT RESET ABORT FLAG           @OZ49145
         LA    R14,PJCTFREE        ALTER RETURN                @OZ47595
         L     R15,PSAVAREA         ADDRESS TO ENTER           @OZ47595
         ST    R14,2*4(,R15)         PROCESSOR TERMINATION     @OZ47595
         L     R1,PQHOPQE          GET ORIGIN PQE ADDR         @OZ47595
         B     PPQTRUNC            BR TO TRUNCATE PPQ          @OZ47595
         SPACE 1                                               @OZ47595
PRSCHDAL L     R15,=A(PDEALLOC)    DEALLOCATE JES              @OZ47595
         BALR  PL,R15               JOE'S RESOURCES            @G38ESBB
         SPACE 1                                               @G38ESBB
 TITLE 'HASP PRINT/PUNCH SERVICE -- 3800 JOB SETUP ROUTINE'    @G38ESBB
***************************************************************@G38ESBB
*                                                              @G38ESBB
*        SET UP TO RESUME WITH COMMAND TARGET JOB              @G38ESBB
*                                                              @G38ESBB
***************************************************************@G38ESBB
         SPACE 1                                               @G38ESBB
         USING JOEDSECT,R14        PROVIDE JOE ADDRESSABILITY  @G38ESBB
         USING JCTDSECT,JCT        PROVIDE JCT ADDRESSABILITY  @G38ESBB
         SPACE 1                                               @G38ESBB
PJOSETUP L     R1,PQHTPQE          ADDRESS TARGET PQE          @G38ESBB
         L     R15,PQECPQED        ADDRESS PQED                @G38ESBB
         L     R14,PQEDWJOE-PQEDSECT(,R15) ADDRESS WORK JOE    @G38ESBB
         ST    R14,PWKJOE          SET UP WORK JOE ADDRESS     @G38ESBB
         SPACE 1                                               @G38ESBB
         LH    R0,JOECHAR          GET CHAR JOE OFFSET         @G38ESBB
         N     R0,PCLRHALF         CLEAR LEFT HALFWORD         @G38ESBB
         SLL   R0,2                EXPAND TO BYTE OFFSET       @G38ESBB
         AL    R0,$JOTABLE         ADD JOT ORIGIN              @G38ESBB
         ST    R0,PCHJOE           SET UP CHAR JOE ADDRESS     @G38ESBB
         SPACE 1                                               @G38ESBB
         LH    R0,JOECKPT          GET CKPT JOE OFFSET         @G38ESBB
         N     R0,PCLRHALF         CLEAR LEFT HALFWORD         @G38ESBB
         SLL   R0,2                EXPAND TO BYTE OFFSET       @G38ESBB
         AL    R0,$JOTABLE         ADD JOT ORIGIN              @G38ESBB
         ST    R0,PCKJOE           SET UP CKPT JOE ADDRESS     @G38ESBB
         SPACE 1                                               @G38ESBB
         LH    JCT,JOEJQE          GET JQE OFFSET              @G38ESBB
         N     JCT,PCLRHALF        CLEAR LEFT HALFWORD         @G38ESBB
         SLL   JCT,2               EXPAND TO BYTE OFFSET       @G38ESBB
         AL    JCT,$JOBQPTR        ADD JOB QUEUE ORIGIN        @G38ESBB
         ST    JCT,PCEJQE          SET UP JQE ADDRESS          @G38ESBB
         SPACE 1                                               @G38ESBB
         ST    R14,PQHSAVE1        SAVE WORK JOE ADDRESS       @G38ESBB
         ST    R1,PQHSAVE2         SAVE TARGET PQE ADDRESS     @G38ESBB
        $#JCT  READ                READ JCT (REG JCT=JQE ADR)  @G38ESBB
         L     R14,PQHSAVE1        RESTORE WORK JOE ADDRESS    @G38ESBB
         L     R1,PQHSAVE2         RESTORE TARGET PQE ADDRESS  @G38ESBB
         ST    JCT,PJCTBUF         SET UP JCT BUFFER ADDRESS   @G38ESBB
         L     R15,PCEDCT          ADDRESS DCT                 @G38ESBB
         TM    DCTPPFL-DCTDSECT(R15),DCTTCEL  TRK-CELLING...   @G38ESBB
         BZ    PSETWJOF            NO, BYPASS TRK-CELL SETTING @G38ESBB
         CLI   $TCELSIZ,1          IF TRK-CELL SIZE IS NOT GT  @G38ESBB
         BNH   PSETWJOF             1, FORCE DESPOOL=SINGLE    @G38ESBB
         TM    JOEFLAG2,$JOETCEL   DATA SET TRACK-CELLED...    @G38ESBB
         BZ    PSETWJOF            NO,BYPASS FLAG              @G38ESBB
         OI    PPFLAG2,PPTCEL      INDICATE TRACK-CELL DESPOOL @G38ESBB
         SPACE 1                                               @G38ESBB
PSETWJOF MVC   PCKJOE(L'JOEFLAG),JOEFLAG SET UP WORK JOE FLAG  @G38ESBB
         L     R15,=A(PALLOC)      CALL PALLOC TO ALLOCATE     @G38ESBB
         BALR  PL,R15               JOB'S RESOURCES            @G38ESBB
         L     PBUF,PBUFADDR       ADDRESS BUFFER              @G38ESBB
         DROP  R14                 DROP JOE ADDRESSABILITY     @G38ESBB
         EJECT                                                 @G38ESBB
***************************************************************@G38ESBB
*                                                              @G38ESBB
*        SET UP PCE CHECKPOINT DATA AREA FROM TARGET PQE       @G38ESBB
*                                                              @G38ESBB
***************************************************************@G38ESBB
         SPACE 1                                               @G38ESBB
         L     R15,PQECPQED        ADDRESS PQED                @G38ESBB
         MVC   PCEEJRCB,PQECJRCB   MOVE DISP INTO EJECT BUFFER @G38ESBB
         MVC   PDDBDISP,PQEDPDDB-PQEDSECT(R15) DISP OF PDDB    @G38ESBB
         MVC   PDDBPGCT,PQECPPCT   MOVE PDDB LOGICAL PAGE CNT  @G38ESBB
         MVC   PPLNCDCT,PQECTLNC   MOVE TOTAL JOE LINE COUNT   @G38ESBB
         MVC   PRPAGECT,PQECTPCT   MOVE TOTAL JOE PAGE COUNT   @G38ESBB
         MVC   PCEJMTTR,PQECMTTR   MOVE MTTR OF SPOOL DATA     @G38ESBB
         MVC   PCEIOTTR,PQEDIOTR-PQEDSECT(R15) IOT TRACK ADR   @G38ESBB
         MVC   PPRCPYCT,PQEDCOPY-PQEDSECT(R15) COPY NO IN PROG @G38ESBB
         MVC   PDDBCPYG,PQEDCPYG-PQEDSECT(R15) COPY GRP OFFSET @G38ESBB
         MVC   PCOPYGRP,PQEDCGRP-PQEDSECT(R15) COPY GROUPS     @OZ49282
         MVC   PPDSCPY,PQEDSCPY-PQEDSECT(R15)  COPY COUNT      @OZ49282
         MVC   PPDSKEY,PQEDSKEY-PQEDSECT(R15)  DATASET KEY     @OZ49282
         MVC   PPJNDS,PQEDTNDS-PQEDSECT(R15) JOE DATA SET CNT  @G38ESBB
         MVC   PPJOBKEY,JCTJBKEY   SET JOB KEY                 @G38ESBB
         SLR   R0,R0               SET LOGICAL                 @G38ESBB
         IC    R0,JCTLINCT          PAGE SIZE                  @G38ESBB
         BCTR  R0,0                  FROM JOB'S                @G38ESBB
         ST    R0,PRLINECT            PAGE SIZE                @G38ESBB
         L     PL,PCEDCT           GET DCT ADDRESS             @OZ75372
         NI    DCTPPSW2-DCTDSECT(PL),FF-DCTCKJAM  RESET FLAG   @OZ75372
         SPACE 1                                               @OZ75372
         PUSH  PRINT                                           @OZ75372
         PRINT OFF       SECTION DELETED BY APAR OZ75372       @OZ75372
*              THIS LINE DELETED BY APAR OZ75372               @OZ75372
*              THIS LINE DELETED BY APAR OZ75372               @OZ75372
*              THIS LINE DELETED BY APAR OZ75372               @OZ75372
*              THIS LINE DELETED BY APAR OZ75372               @OZ75372
*              THIS LINE DELETED BY APAR OZ75372               @OZ75372
*              THIS LINE DELETED BY APAR OZ75372               @OZ75372
*              THIS LINE DELETED BY APAR OZ75372               @OZ75372
*              THIS LINE DELETED BY APAR OZ75372               @OZ75372
*              THIS LINE DELETED BY APAR OZ75372               @OZ75372
*              THIS LINE DELETED BY APAR OZ75372               @OZ75372
*              THIS LINE DELETED BY APAR OZ75372               @OZ75372
*              THIS LINE DELETED BY APAR OZ75372               @OZ75372
*              THIS LINE DELETED BY APAR OZ75372               @OZ75372
*              THIS LINE DELETED BY APAR OZ75372               @OZ75372
*              THIS LINE DELETED BY APAR OZ75372               @OZ75372
*              THIS LINE DELETED BY APAR OZ75372               @OZ75372
         POP   PRINT                                           @OZ75372
         SPACE 1                                               @OZ75372
PSETEND  OI    PCKJOE,$JOECKV      SET WARM START INDICATOR    @G38ESBB
         TM    PDCTFLAG,DCTDELET   WAS COMMAND $C...           @G38ESBB
         BZ    *+12                NO, GO CHECK $I $E          @G38ESBB
         OI    PSMFDCI,SMFOPSTP    SET SMF FLAG                @G38ESBB
         B     PRESETAL            GO RESET FLAGS              @G38ESBB
         TM    PDCTFLAG,DCTRSTRT+DCTBKSP WAS COMMAND $I...     @G38ESBB
         BNO   *+12                NO, GO CHECK $E             @G38ESBB
         OI    PSMFDCI,SMFINTRP    SET SMF FLAG                @G38ESBB
         B     PRESETAL            GO RESET FLAGS              @G38ESBB
         TM    PDCTFLAG,DCTRSTRT   WAS COMMAND $E...           @G38ESBB
         BZ    PRESETAL            NO, GO RESET FLAGS          @G38ESBB
         OI    PSMFDCI,SMFRESTR    SET SMF FLAG                @G38ESBB
         SPACE 1                                               @OZ75372
         PUSH  PRINT                                           @OZ75372
         PRINT OFF       SECTION DELETED BY APAR OZ75372       @OZ75372
*              THIS LINE DELETED BY APAR OZ75372               @OZ75372
*              THIS LINE DELETED BY APAR OZ75372               @OZ75372
*              THIS LINE DELETED BY APAR OZ75372               @OZ75372
*              THIS LINE DELETED BY APAR OZ75372               @OZ75372
*              THIS LINE DELETED BY APAR OZ75372               @OZ75372
*              THIS LINE DELETED BY APAR OZ75372               @OZ75372
*              THIS LINE DELETED BY APAR OZ75372               @OZ75372
*              THIS LINE DELETED BY APAR OZ75372               @OZ75372
*              THIS LINE DELETED BY APAR OZ75372               @OZ75372
*              THIS LINE DELETED BY APAR OZ75372               @OZ75372
*              THIS LINE DELETED BY APAR OZ75372               @OZ75372
*              THIS LINE DELETED BY APAR OZ75372               @OZ75372
*              THIS LINE DELETED BY APAR OZ75372               @OZ75372
*              THIS LINE DELETED BY APAR OZ75372               @OZ75372
*              THIS LINE DELETED BY APAR OZ75372               @OZ75372
*              THIS LINE DELETED BY APAR OZ75372               @OZ75372
*              THIS LINE DELETED BY APAR OZ75372               @OZ75372
*              THIS LINE DELETED BY APAR OZ75372               @OZ75372
*              THIS LINE DELETED BY APAR OZ75372               @OZ75372
*              THIS LINE DELETED BY APAR OZ75372               @OZ75372
*              THIS LINE DELETED BY APAR OZ75372               @OZ75372
*              THIS LINE DELETED BY APAR OZ75372               @OZ75372
*              THIS LINE DELETED BY APAR OZ75372               @OZ75372
*              THIS LINE DELETED BY APAR OZ75372               @OZ75372
*              THIS LINE DELETED BY APAR OZ75372               @OZ75372
*              THIS LINE DELETED BY APAR OZ75372               @OZ75372
*              THIS LINE DELETED BY APAR OZ75372               @OZ75372
*              THIS LINE DELETED BY APAR OZ75372               @OZ75372
*              THIS LINE DELETED BY APAR OZ75372               @OZ75372
*              THIS LINE DELETED BY APAR OZ75372               @OZ75372
*              THIS LINE DELETED BY APAR OZ75372               @OZ75372
*              THIS LINE DELETED BY APAR OZ75372               @OZ75372
         POP   PRINT                                           @OZ75372
         SPACE 1                                               @OZ53889
PRESETAL NI    PDCTFLAG,FF-DCTDELET-DCTRSTRT-DCTBKSP RESET ALL @G38ESBB
         NI    PPFLAG,FF-PPDELSW   RESET SUSPENSION SWITCH     @G38ESBB
         MVC   PDDBSKIP,PMAXSKIP   SET SKIP FOR MAPPING        @G38ESBB
 TITLE 'HASP PRINT/PUNCH SERVICE -- 3800 PPQ TRUNCATION'       @G38ESBB
***************************************************************@G38ESBB
*                                                              @G38ESBB
*        TRUNCATE THE PENDING PAGE QUEUE TO REFLECT THE PAGE   @G38ESBB
*        BUFFER PURGE AND RESOLUTION                           @G38ESBB
*                                                              @G38ESBB
***************************************************************@G38ESBB
         SPACE 1                                               @G38ESBB
         L     R1,PQHOPQE          GET ORIGIN PQE ADDRESS      @OZ47787
         TM    PQHFLAG2,PCMDBKSP   TEST FOR BACKSPACE CMD      @OZ47787
         BZ    PPQTRUNC            BRANCH IF NOT               @OZ47787
         L     R1,PQHTPQE          GET TARGET PQE ADDRESS      @OZ47787
*              THIS LINE DELETED BY APAR NUMBER                @OZ51592
*              THIS LINE DELETED BY APAR NUMBER                @OZ51592
*              THIS LINE DELETED BY APAR NUMBER                @OZ51592
*              THIS LINE DELETED BY APAR NUMBER                @OZ51592
         SPACE 1                                               @OZ47734
PPQTRUNC L     R1,PQENEXT          ADDRESS FIRST PQE TO TRUNC  @OZ47787
         SPACE 1                                               @OZ47734
PPQCHECK LA    R0,PQHFIRST-(PQENEXT-PQEDSECT) ARE THERE ANY    @OZ47734
         CR    R1,R0                           PQE'S TO TRUNC. @G38ESBB
         BE    PSETPIDE            NO, BYPASS TRUNCATION       @G38ESBB
         SLR   R0,R0               DELETE TO END OF PPQ        @G38ESBB
         L     R15,=A(PDELPQE)     CALL PDELPQE TO             @OZ48003
         BALR  PL,R15               TRUNCATE THE PPQ           @OZ48003
         LA    R0,PQHFIRST-(PQENEXT-PQEDSECT) ADDRESS PQE0     @G38ESBB
         SPACE 1                                               @G38ESBB
PSETPIDE ST    R0,PQHPIDE          SET PENDING ID PTR TO PQE0  @G38ESBB
         TM    PQHFLAG,PQHHDR      TEST FOR HEADER NEEDED      @OZ75372
         BNO   PCLRFLGS            BRANCH IF NOT               @OZ75372
         NI    PPFLAG,FF-PRDELSW   RESET FLAG FOR PHEADER      @OZ75372
         NI    PQHFLAG,FF-PQHHDR   RESET HEADER NEEDED         @OZ75372
         L     R15,=A(PHEADER)     CALL PHEADER TO             @OZ75372
         BALR  PL,R15               PRINT JOB SEPARATOR        @OZ75372
         B     PHDRTST(R15)        BRANCH ON RETURN CODE       @OZ75372
         SPACE 1                                               @OZ75372
PHDRTST  B     PHDROK              0  NORMAL RETURN            @OZ75372
         B     PHDRTERM            4  OPER TERMINATE           @OZ75372
         B     PDETCMD             8  3800 JAM OR CANCEL KEY   @OZ75372
         B     PCMDTERM            12 3800 ERROR IN SETUP      @OZ75372
         SPACE 1                                               @OZ75372
PHDROK   OI    PPFLAG2,PPFDS       NO OFFSET STACK AFTER HDR   @OZ75372
         B     PCLRFLGS            BR TO RESET FLAGS           @OZ75372
         SPACE 1                                               @OZ75372
PHDRTERM XC    PQHMAPV,PQHMAPV     CLEAR MAPPING VALUE         @OZ75372
         NI    PDCTFLAG,FF-DCTDELET-DCTRSTRT-DCTBKSP  RESET    @OZ75372
         LA    R14,PPABORT         SET RETURN ADDRESS          @OZ75372
         L     R1,PSAVAREA          TO ENTER PROCESSOR         @OZ75372
         ST    R14,2*8(,R1)          TERMINATION               @OZ75372
         EJECT                                                 @OZ75372
PCLRFLGS NI    PQHFLAG,FF-PQHIPPQM RESET INHIBIT FLAG          @OZ75372
         MVI   PQHFLAG2,0          RESET COMMAND FLAGS         @OZ75372
         MVC   PQHPQEJ,$ZEROS      CLEAR PQEJ QUEUE HEAD       @G38ESBB
         CLC   PQHMAPV,$ZEROS      IS MAPPING NEEDED...        @G38ESBB
         BNE   PCMDEXIT            YES, GO RETURN              @G38ESBB
         MVC   PDDBSKIP,$ZEROS     INDICATE NO MAPPING NEEDED  @G38ESBB
         NI    PPFLAG3,FF-PP3800R  INDICATE REPOSITIONING DONE @G38ESBB
         SPACE 1                                               @G38ESBB
PCMDEXIT PRETURN                   RESTORE REGS AND RETURN     @G38ESBB
         SPACE 1                                               @G38ESBB
         DROP  R1,PW,JCT           DROP PQE,PQH,JCT ADR        @G38ESBB
 TITLE 'HASP PRINT/PUNCH SERVICE -- 3800 COMMAND PROCESSING'   @G38ESBB
         LTORG                                                 @G38ESBB
 TITLE 'HASP PRINT/PUNCH SERVICE -- DEVICE ABORT ROUTINE'      @OZ51930
***************************************************************@OZ51930
*                                                              @OZ51930
*        FOR 3800 PRINTERS SETUP FOR PPQ RESTART ROUTINE       @OZ51930
*                                                              @OZ51930
***************************************************************@OZ51930
         SPACE 1                                               @OZ51930
         USING PDEVABRT,BASE2      LOCAL ADDRESSABILITY        @OZ51930
         USING DCTDSECT,R15        PROVIDE DCT ADDRESSABILITY  @OZ51930
         USING PQHDSECT,PW         PROVIDE PQH ADDRESSABILITY  @OZ51930
         USING PQEDSECT,R1         PROVIDE PQE ADDRESSABILITY  @OZ51930
         SPACE 1                                               @OZ51930
PDEVABRT PSAVE ALL                 SAVE CALLERS REGISTERS      @OZ51930
         LR    BASE2,R15           SET BASE ADDRESS            @OZ51930
         L     R15,PCEDCT          GET DCT ADDRESS             @OZ51930
         CLI   PDEVTYP3,UCB3800    TEST FOR 3800 PRINTER       @OZ51930
         BE    PAB3800             BRANCH IF YES               @OZ51930
         OI    DCTSTAT,DCTDRAIN    FORCE DRAIN OF THE DEVICE   @OZ51930
         OI    PDCTFLAG,DCTRSTRT+DCTBKSP FORCE AN INTERRUPT    @OZ51930
         B     PABRETN             BRANCH TO RETURN            @OZ51930
         SPACE 1                                               @OZ51930
PAB3800  L     PW,PQHADR           GET PQH ADDRESS             @OZ51930
         OI    PQHFLAG,PQH2CMD     SET INHIBIT I/O FLAG        @OZ51930
         OI    DCTSTAT,DCTDRAIN    SET DRAIN FLAG              @OZ51930
         OI    PPFLAG3,PPDVNAVL    SET DEVICE NOT AVAILABLE    @OZ51930
         LA    R1,PQHFIRST-(PQENEXT-PQEDSECT) ADDRESS PQE0     @OZ51930
         SPACE 1                                               @OZ51930
PABLOOP  LR    R0,R1               SAVE PREVIOUS PQE ADDR      @OZ51930
         L     R1,PQENEXT          GET NEXT PQE                @OZ51930
         CR    R1,PW               TEST FOR END OF PPQ         @OZ51930
         BE    PABCHK              BRANCH IF YES               @OZ51930
         CLI   PQETYPE,PQES        TEST FOR PQES               @OZ51930
         BE    PABLOOP             BRANCH IF YES               @OZ51930
         SPACE 1                                               @OZ51930
PABSET   ST    R0,PQHOPQE          SET PQE ADDRESS             @OZ51930
         OI    PPFLAG3,PP3800R     SET COMMAND FLAG            @OZ51930
         NI    PPFLAG,FF-PPNEWS    RESET NEWS FLAG             @OZ51930
         OI    PQHFLAG,PQHRSTRT+PQHIPPQM SET RESTART FLAGS     @OZ51930
         XC    DCTCSW,DCTCSW       CLEAR CSW                   @OZ51930
         B     PPDSEND             BR TO RETURN SAVE AREAS     @OZ51930
         SPACE 1                                               @OZ51930
PABCHK   CLC   PCEJQE,$ZEROS       TEST FOR CURRENT JOB        @OZ51930
         BNE   PABSET              BRANCH IF YES               @OZ51930
         NI    PDCTFLAG,FF-DCTDELET-DCTRSTRT-DCTBKSP RESET     @OZ51930
         SPACE 1                                               @OZ51930
PABRETN  PRETURN ,                 RESTORE REGS AND RETURN     @OZ51930
         SPACE 1                                               @OZ51930
         DROP  R1,PW,R15                                       @OZ51930
 TITLE 'HASP PRINT/PUNCH SERVICE -- RESOURCE ALLOCATION'       @G38ESBB
***************************************************************@G38ESBB
*                                                              @G38ESBB
*        GET DATA BUFFERS FROM THE MAIN HASP BUFFER POOL       @G38ESBB
*                                                              @G38ESBB
*        PBUF - PRIMARY DATA BUFFER ADDRESS (ON EXIT)          @G38ESBB
*               MUST BE RE-INITIALIZED IN MAINLINE             @G38ESBB
*        PC1  - WORK REG FOR PCIE                              @G38ESBB
*        PC2  - WORK REG FOR BUFFER WORK AREA                  @G38ESBB
*                                                              @G38ESBB
***************************************************************@G38ESBB
         SPACE 1                                               @G38ESBB
PALLOC   PSAVE ALL                 SAVE ALL REGISTERS          @G38ESBB
         LR    BASE2,R15           SETUP LOCAL                 @G38ESBB
         USING PALLOC,BASE2             ADDRESSABILITY         @G38ESBB
         LA    PC2,1               DETERMINE NUMBER OF         @G38ESBB
         TM    PPFLAG2,PPTCEL       BUFFERS NEEDED             @G38ESBB
         BZ    *+8                   BASED ON                  @G38ESBB
         IC    PC2,$TCELSIZ           TRK-CELL/NON-TRK-CELL    @G38ESBB
        $GETBUF WAIT=YES,NUM=(PC2),FIX=YES  GET FIRST BUF(S)   @G38ESBB
         ST    R1,PBUFSAVE         SAVE ADDR OF 1ST BUFFER     @G38ESBBC
                                   (CHAIN)                     @G38ESBB
         CLI   PBUFOPT,2           TEST BUFFERING OPTION       @G38ESBB
         BNE   PNOSECBF            BRANCH IF NOT DBL BUFFERING @G38ESBB
        $GETBUF WAIT=YES,NUM=(PC2),FIX=YES GET SECOND BUF(S)   @G38ESBB
         SPACE 1                                               @G38ESBB
PNOSECBF DS    0H                  ACTIVATE DATA               @G38ESBB
         LR    PBUF,R1              BUFFER ADDRESSABILITY      @G38ESBB
         ST    PBUF,PBUFADDR       SAVE PRIMARY DATA BUF ADR   @G38ESBB
         EJECT                                                 @G38ESBB
***************************************************************@G38ESBB
*                                                              @G38ESBB
*        GET BUFFER FOR OUTPUT CCW'S                           @G38ESBB
*                                                              @G38ESBB
***************************************************************@G38ESBB
         SPACE 1                                               @G38ESBB
*                                                              @G38ESBB
*        FOR 3800 PRINTERS, GET A PAGE BUFFER                  @G38ESBB
*                                                              @G38ESBB
         CLI   PDEVTYP3,UCB3800    TEST FOR 3800 PRINTER       @G38ESBB
         BNE   PNOT3800            BR IF NOT                   @G38ESBB
        $GETBUF WAIT=YES,TYPE=PAGE,FIX=YES GET OUTPUT CCW BUFF @G38ESBB
         LA    PW,(4096-(BUFSTART-BUFDSECT)-2*((BFWSIZE+7)/8*8)-2*PCIESC
               IZE)/16*16/2        COMPUTE OFFSET TO PCIE      @G38ESBB
         LA    R14,BUFSTART-BUFDSECT+PCIESIZE+(BFWSIZE+7)/8*8(PW,R1)   C
                                   ADDRESS OF SECOND CCW AREA  @G38ESBB
         B     PGBFINIT            GO TO INITIALIZE BUFFER     @G38ESBBC
                                        VARIABLES              @G38ESBB
*                                                              @G38ESBB
*        FOR NON - 3800 DEVICES, GET A PP BUFFER               @G38ESBB
*                                                              @G38ESBB
         SPACE 1                                               @G38ESBB
PNOT3800 DS    0H                                              @G38ESBB
        $GETBUF WAIT=YES,TYPE=PP,FIX=YES  GET OUTPUT CCW BUF   @G38ESBB
         SLR   PW,PW               CLEAR WORK REGISTER FOR IC  @G38ESBB
         IC    PW,$NOPRCCW         PICK UP PRINT CCW COUNT     @G38ESBB
         TM    PCEID,PCEPRSID      TEST PROCESSOR TYPE         @G38ESBB
         BO    PRINIT              BRANCH IF PRINTER           @G38ESBB
         IC    PW,$NOPUCCW          ELSE PICK UP PUNCH CCW CNT @G38ESBB
PRINIT   SLL   PW,3                MAXIMUM OUTPUT CCW CHAIN    @G38ESBBC
                                        LENGTH                 @G38ESBB
         LA    R14,BUFSTART-BUFDSECT+PCIESIZE+(JOESIZE+7)/8*8(PW,R1)   C
                                   ADDRESS OF SECOND CCW AREA  @G38ESBB
PGBFINIT DS    0H                                              @G38ESBB
         ST    R1,POUTIOB          SAVE OUTPUT IOB ADDRESS     @G38ESBB
         L     PL,PCEDCT                 AND SAVE              @G38ESBB
         ST    R1,DCTBUFAD-DCTDSECT(,PL)  IN DCT               @G38ESBB
         LA    R1,BUFSTART-BUFDSECT(,R1) ADDRESS OF FIRST      @G38ESBB
         ST    R1,POUTCCWA                    CCW AREA         @G38ESBB
         ST    R14,POUTCCWN        SECOND CCW AREA ADDRESS     @G38ESBB
         STH   PW,PCCWLAST         OFFSET TO PCIE              @G38ESBBC
                                        (CCW AREA LENGTH)      @G38ESBB
         L     R15,POUTIOB         GET IOB ADDRESS             @G38ESBB
         LR    PL,R14              COPY 2ND CCW ADDRESS        @G38ESBB
         SLR   PL,R15              COMPUTE OFFSET INTO IOB     @G38ESBB
         SRL   PL,3                DIVIDE BY 8                 @G38ESBB
         STH   PL,PPBDISPL-BUFDSECT(R15) SAVE OFFSET/8         @G38ESBB
         EJECT                                                 @G38ESBB
***************************************************************@G38ESBB
*                                                              @G38ESBB
*        INITIALIZE PCIE AT END OF EACH CCW AREA               @G38ESBB
*        FOR 3800, ALSO INITIALIZE RPI/SIB CCW'S AND BFW AT    @G38ESBB
*                  THE END OF EACH CCW AREA                    @G38ESBB
*                                                              @G38ESBB
***************************************************************@G38ESBB
         SPACE 1                                               @G38ESBB
         LR    R0,R1               INITIALIZE                  @G38ESBB
         SH    R0,PCCWLENG          CURRENT                    @G38ESBB
         ST    R0,PCCWPT             CCW POINTER               @G38ESBB
         TM    PCEID,PCERJEID+PCEPRSID TEST PROCESSOR TYPE     @G38ESBB
         BNZ   *+8                 BRANCH IF NOT LOCAL PUNCH   @G38ESBB
         ST    R0,PUERRPT          SET PUNCH ERROR CUTOFF      @G38ESBB
         SPACE 1                                               @G38ESBB
         LRA   R0,0(,R14)          REAL ADR OF 2ND CCW AREA    @G38ESBB
         BAL   PL,PINITCCW         INITIALIZE 1ST CCW AREA     @G38ESBB
         LRA   R0,0(,R1)           REAL ADR OF 1ST CCW AREA    @G38ESBB
         LR    R1,R14              ADDRESS 2ND CCW AREA        @G38ESBB
         BAL   PL,PINITCCW         INITIALIZE 2ND CCW AREA     @G38ESBB
         B     PTDSPLM             BRANCH TO CONTINUE          @G38ESBB
         SPACE 1                                               @G38ESBB
         USING PCIDSECT,PC1        PROVIDE PCIE ADDRESSABILITY @G38ESBB
         USING BFWDSECT,PC2        PROVIDE BFW ADDRESSABILITY  @G38ESBB
         SPACE 1                                               @G38ESBB
PINITCCW LA    PC1,0(PW,R1)        ADDRESS PCIE                @G38ESBB
         MVC   PCI1CCW(PCIESIZE),PCNOPTIC MOVE PCIE SKELETON   @G38ESBB
         STCM  R0,7,PCI2DADR       SET TIC TARGET ADDRESS      @G38ESBB
         CLI   PDEVTYP3,UCB3800    TEST FOR 3800 PRINTER       @G38ESBB
         BNER  PL                  RETURN IF NOT               @G38ESBB
         LA    PC2,PCIESIZE(,PC1)  ADDRESS BFW                 @G38ESBB
         SH    PC1,=Y(RPISIBSZ+L'PCCWNOP) ADR NOP,RPI,SIB CCWS @G38ESBB
         MVC   BFWDSC(L'BFWDSC+L'BFWPPB+L'BFWRPI+L'BFWINV),BFWSKEL     C
                                   INITIALIZE THE BFW          @OZ61672
         MVC   0(L'PCCWNOP,PC1),PCCWNOP MOVE NOP CCW           @G38ESBB
         OI    4(PC1),X'40'        CHAIN NOP CCW TO RPI        @G38ESBB
         MVC   8(L'PCCWXORD,PC1),PCCWXORD MOVE THE REQUEST     @G38ESBB
         LRA   R0,BFWRPI                   PRINTER INFO ORDER  @G38ESBB
         STCM  R0,7,9(PC1)                  OF EXEC ORDER CCW  @G38ESBB
         MVC   16(L'PCCWSIB,PC1),PCCWSIB MOVE IN THE           @G38ESBB
         LRA   R0,BFWSENS                 SENSE INTERMEDIATE   @G38ESBB
         STCM  R0,7,17(PC1)                BUFFER CCW          @G38ESBB
         BR    PL                  RETURN TO CALLER            @G38ESBB
         SPACE 1                                               @G38ESBB
         DROP  PC1,PC2             DROP PCI,BFW ADR            @G38ESBB
         EJECT                                                 @G38ESBB
***************************************************************@G38ESBB
*                                                              @G38ESBB
*        INITIALIZE BUFFER INFORMATION FOR INPUT CCW'S         @G38ESBB
*                                                              @G38ESBB
***************************************************************@G38ESBB
         SPACE 1                                               @G38ESBB
PTDSPLM  TM    PPFLAG2,PPTCEL      TEST DESPOOLING METHOD      @G38ESBB
         BO    PGETPPIN            BR IF TRACK-CELL            @G38ESBB
         ST    PBUF,PINIOB         ELSE USE DATA BUF FOR IOB   @G38ESBB
         B     PFIXDEB             GO FIX DEB                  @G38ESBB
         SPACE 1                                               @G38ESBB
***************************************************************@G38ESBB
*                                                              @G38ESBB
*        ACQUIRE A PP BUFFER FOR TRACK-CELL INPUT CCW'S        @G38ESBB
*                                                              @G38ESBB
***************************************************************@G38ESBB
         SPACE 1                                               @G38ESBB
PGETPPIN DS    0H                                              @G38ESBB
        $GETBUF WAIT=YES,TYPE=PP   GET PP BUFFER               @G38ESBB
         ST    R1,PINIOB           SAVE PP BUFFER'S IOB ADR    @G38ESBB
         SLR   PW,PW               CLEAR PW FOR PINMTTRT       @G38ESBBC
                                        CALCULATION            @G38ESBB
         IC    PW,$TCELSIZ         ADDRESS OF MTTR/BUFFER ADDR @G38ESBBC
                                        TABLE                  @G38ESBB
         LA    R0,1(PW,PW)         =&TCELSIZ*3 (SRCH/TIC/READ) @G38ESBB
         ALR   PW,R0                     +1       (SET SECTOR) @G38ESBB
         SLL   PW,3                      *8                    @G38ESBB
         LA    PW,IOBCCW1-BUFDSECT(PW,R1) PLUS IOBCCW1         @G38ESBB
         ST    PW,PINMTTRT         SAVE TABLE ADDRESS          @G38ESBB
         EJECT                                                 @G38ESBB
***************************************************************@G38ESBB
*                                                              @G38ESBB
*        PAGE FIX PRINT/PUNCH DEB FOR DURATION OF THIS OUTPUT  @G38ESBB
*                                                              @G38ESBB
***************************************************************@G38ESBB
         SPACE 1                                               @G38ESBB
PFIXDEB  L     R1,PCEDCT           ADDRESS LOCAL PRT/PNCH DCT  @G38ESBB
         USING DCTDSECT,R1         PICK-UP DCB ADDRESS         @G38ESBB
         L     R1,DCTDCB            FROM DCT                   @G38ESBB
         USING DCBDSECT,R1         PICK-UP DEB ADDRESS         @G38ESBB
         L     R1,DCBDEBAD          FROM DCB                   @G38ESBB
         LA    R0,DEBBASND-DEBDSECT  SIZE OF JES2 DEB          @G38ESBB
         LA    R2,PPLSAVE          ADDRESS OF A PSEUDO-ECB     @G38ESBB
        $PGSRVC FIX,(R1),(R0),(R2)   FIX PRINTER/PUNCH DEB     @G38ESBB
         SPACE 1                                               @G38ESBB
         PRETURN                     RESTORE REGS AND RETURN   @G38ESBB
         SPACE 1                                               @G38ESBB
         DROP  BASE2,R1              SUSPEND DCB               @G38ESBB
 TITLE 'HASP PRINT/PUNCH SERVICE -- DEALLOCATE RESOURCES'      @G38ESBB
***************************************************************@G38ESBB
*                                                              @G38ESBB
*        FREE ALL PRINT/PUNCH RESOURCES                        @G38ESBB
*                                                              @G38ESBB
***************************************************************@G38ESBB
         SPACE 1                                               @G38ESBB
PDEALLOC PSAVE ALL                 SAVE ALL REGISTERS          @G38ESBB
         LR    BASE2,R15           SETUP LOCAL                 @G38ESBB
         USING PDEALLOC,BASE2           ADDRESSABILITY         @G38ESBB
         TM    PPFLAG2,PPRSW       IF A READ IS                @G38ESBB
         BZ    PIOCLEAR             OUTSTANDING, CLEAR         @G38ESBB
         BAL   PL,PRDTCHK            ALL INPUT I/O             @G38ESBB
         SPACE 1                                               @G38ESBB
PIOCLEAR TM    PCEID,PCERJEID      TEST PROCESSOR TYPE         @G38ESBB
         BO    PFREEBFS            BRANCH IF REMOTE            @G38ESBB
         L     R1,PCEDCT           ADDRESS PRINT/PUNCH DCT     @G38ESBB
         CLI   DCTBUFCT-DCTDSECT(R1),0 ALL OUTPUT COMPLETED... @G38ESBB
         BE    PFREEBFS            BRANCH IF YES               @G38ESBB
        $WAIT  IO                  ELSE, WAIT FOR IO TO FINISH @G38ESBB
         B     PIOCLEAR            GO TRY AGAIN                @G38ESBB
         SPACE 1                                               @G38ESBB
PFREEBFS TM    PPFLAG2,PPTCEL      FREE IOB BUFFER             @G38ESBB
         BZ    PFROUT               USED FOR DE-SPOOLING       @G38ESBB
        $FREEBUF PINIOB              TRACK-CELL'S              @G38ESBB
         SPACE 1                                               @G38ESBB
***************************************************************@G38ESBB
*                                                              @G38ESBB
*        FOR LOCAL DEVICES -- FREE OUTPUT IOB BUFFER           @G38ESBB
*                                                              @G38ESBB
***************************************************************@G38ESBB
         SPACE 1                                               @G38ESBB
PFROUT   TM    PCEID,PCERJEID      TEST PROCESSOR TYPE         @G38ESBB
         BO    PFRBUFS             BR IF REMOTE                @G38ESBB
         SPACE 1                                               @G38ESBB
PFROIOB $FREEBUF POUTIOB           FREE OUTPUT IOB BUFFER      @G38ESBB
         SPACE 1                                               @G38ESBB
***************************************************************@G38ESBB
*                                                              @G38ESBB
*        FREE ALL DATA BUFFERS                                 @G38ESBB
*                                                              @G38ESBB
***************************************************************@G38ESBB
         SPACE 1                                               @G38ESBB
PFRBUFS  L     PC1,PBUFADDR        OBTAIN PRIMARY AND          @G38ESBB
         L     PC2,PBUFSAVE         SECONDARY DATA BUFFER ADR  @G38ESBB
         LTR   PC1,PC1             ANY BUFFERS PRESENT...      @G38ESBB
         BZ    PDEBFREE            BR IF NOT                   @G38ESBB
         TM    PPFLAG2,PPTCEL      TEST DE-SPOOLING METHOD     @G38ESBB
         BO    PFRBUF1             BR IF TRACK-CELL            @G38ESBB
         XC    BUFCHAIN-BUFDSECT(,PC1),BUFCHAIN-BUFDSECT(PC1)  @G38ESBB
         XC    BUFCHAIN-BUFDSECT(,PC2),BUFCHAIN-BUFDSECT(PC2)  @G38ESBB
PFRBUF1  CLI   PBUFOPT,1           TEST FOR SINGLE BUFFERING   @G38ESBB
         BE    PFRBUF2             BR IF YES                   @G38ESBB
        $FREEBUF (PC1),MULTIPLE    FREE PRIMARY DATA BUFFER(S) @G38ESBB
         SPACE 1                                               @G38ESBB
PFRBUF2 $FREEBUF (PC2),MULTIPLE   FREE SECONDARY DATA BUFFERS  @G38ESBB
         SPACE 1                                               @G38ESBB
***************************************************************@G38ESBB
*                                                              @G38ESBB
*        PAGE-FREE LOCAL PRINT/PUNCH DATA-EXTENT-BLOCK (DEB)   @G38ESBB
*                                                              @G38ESBB
***************************************************************@G38ESBB
         SPACE 1                                               @G38ESBB
PDEBFREE TM    PCEID,PCERJEID      TEST PROCESSOR TYPE         @G38ESBB
         BNO   PPGFDEB             BR IF NOT A REMOTE          @OZ43428
         NI    MHASPECF+$EWBJOT,255-$EWFJOT SHOW UNIT TO MLLM  @OZ43428
         B     PDALCEND            BYPASS DEB FREE FOR REMOTE  @OZ43428
         SPACE 1                                               @OZ43428
PPGFDEB  L     R1,PCEDCT           ADDRESS PRINT/PUNCH DCT     @OZ43428
         L     R1,DCTDCB-DCTDSECT(,R1) GET DCB ADDR FROM DCT   @G38ESBB
         USING DCBDSECT,R1         PICK-UP DEB ADDRESS         @G38ESBB
         L     R1,DCBDEBAD          FROM DCB                   @G38ESBB
         LA    R0,DEBBASND-DEBDSECT  SIZE OF JES2 DEB          @G38ESBB
        $PGSRVC FREE,(R1),(R0)     PAGE-FREE THE DEB           @G38ESBB
         SPACE 1                                               @G38ESBB
PDALCEND PRETURN                   RESTORE REGS AND RETURN     @G38ESBB
         SPACE 1                                               @G38ESBB
         DROP  BASE2,R1            SUSPEND LOCAL ADR,DCB       @G38ESBB
         TITLE 'HASP PRINT SERVICE -- RECOMPUTE 3800 PAGE ID'  @G38ESBB
***************************************************************@G38ESBB
*                                                              @G38ESBB
*        THIS ROUTINE ADJUSTS PQERPGID FOR ALL PQE'S BY        @G38ESBB
*        ADDING TO IT THE VALUE SPECIFIED IN REGISTER 0.       @G38ESBB
*        THIS IS DONE TO SHOW GAPS IN SPOOL DATA NOT REFLECTED @G38ESBB
*        BY THE PAPER LINE.                                    @G38ESBB
*                                                              @G38ESBB
*        R0  - RECOMPUTE VALUE (ON ENTRY)                      @G38ESBB
*        R1  - ADDRESS OF PENDING PAGE QUEUE ENTRY             @G38ESBB
*        PW  - ADDRESS OF PENDING PAGE QUEUE HEADER            @G38ESBB
*                                                              @G38ESBB
***************************************************************@G38ESBB
         SPACE 1                                               @G38ESBB
PRECOMP  PSAVE ,                   SAVE LINKAGE AND BASE       @G38ESBB
         LR    BASE2,R15           SETUP LOCAL                 @G38ESBB
         USING PRECOMP,BASE2               ADDRESSABILITY      @G38ESBB
         L     PW,PQHADR           ESTABLISH PQH               @G38ESBB
         USING PQHDSECT,PW                 ADDRESSABILITY      @G38ESBB
         L     R1,PQHFIRST         GET START OF PPQ            @G38ESBB
         USING PQEDSECT,R1         PQE ADDRESSABILITY          @G38ESBB
PRECOM1  LA    R15,PQHFIRST-(PQENEXT-PQEDSECT) GET PQE0        @G38ESBB
         CR    R1,R15              END OF PQE...               @G38ESBB
         BE    PCOMPEND            BR IF YES                   @G38ESBB
         CLI   PQETYPE,PQED        DATA SET PQE...             @G38ESBB
         BE    PSCANPQE            BR IF YES                   @G38ESBB
         LH    R15,PQERPGID        GET REPO PAGE ID            @G38ESBB
         ALR   R15,R0              ADD RECOMPUTE VALUE         @G38ESBB
         STH   R15,PQERPGID        SAVE RECOMPUTED PAGE ID     @G38ESBB
         SPACE 1                                               @G38ESBB
PSCANPQE L     R1,PQENEXT          POINT TO NEXT ENTRY         @G38ESBB
         B     PRECOM1             BR TO CONTINUE UPDATE       @G38ESBB
         SPACE 1                                               @G38ESBB
PCOMPEND PRETURN                   RESTORE REGS AND RETURN     @G38ESBB
         DROP  PW,R1               SUSPEND PQH,PQE             @G38ESBB
         TITLE 'HASP PRINT SERVICE  --  MAP 3800 FCB'          @G38ESBB
***************************************************************@G38ESBB
*                                                              @G38ESBB
*        MAPFCB USES AS INPUT THE PRINTER COMMAND OF THE LINE  @G38ESBB
*        BEING OPERATED ON AND DETERMINES THAT LINE'S PAGE     @G38ESBB
*        LOCATION WITHIN THE FCB.                              @G38ESBB
*                                                              @G38ESBB
*        PW  - ADDRESS OF PENDING PAGE QUEUE HEADER            @G38ESBB
*        PC1 - COMMAND CODE SAVE REGISTER                      @G38ESBB
*        R14 - ADDRESS OF FCB BUFFER FOR MAPPING               @G38ESBB
*                                                              @G38ESBB
*        CC  = ZERO     - NON-TERMINATION, CONTINUE PROCESSING @G38ESBB
*              NON-ZERO - TERMINATION, BRANCH PPDONE           @G38ESBB
*                                                              @G38ESBB
***************************************************************@G38ESBB
         SPACE 1                                               @G38ESBB
PMAPFCB  PSAVE ,                   SAVE LINKAGE AND BASE       @G38ESBB
         LR    BASE2,R15           SETUP LOCAL                 @G38ESBB
         USING PMAPFCB,BASE2            ADDRESSABILITY         @G38ESBB
         SPACE 1                                               @G38ESBB
         USING PQHDSECT,PW         PQH ADDRESSABILITY          @G38ESBB
         SPACE 1                                               @G38ESBB
         L     PW,PQHADR           GET PQH ADDRESS             @G38ESBB
         L     PC1,PCCWORK         GET COMMAND CODE            @G38ESBB
         TR    PCCWORK(1),PCCTABLE TRANSLATE  CONTROL COMMAND  @G38ESBB
         TM    PCCWORK,PVALCMD     TEST FOR VALID FCB MAP      @G38ESBB
         BO    PCOMPMAP            BR IF YES                   @G38ESBB
         ST    PC1,PCCWORK         RESTORE COMMAND CODE        @G38ESBB
         B     PMAPEND             BR TO RETURN                @G38ESBB
         SPACE 1                                               @G38ESBB
PCOMPMAP L     R14,PQHFCB          ADDRESS FCB BUFFER          @G38ESBB
         LA    R14,BUFSTART-BUFDSECT(,R14) ADDRESS FCB         @G38ESBB
         USING PFCB,R14            FCB ADDRESSABILITY          @G38ESBB
         TM    PCCWORK,PSKIPCMD    TEST FOR CHANNEL SKIP       @G38ESBB
         BO    PSKIP               BR IF YES                   @G38ESBB
         SPACE 1                                               @G38ESBB
***************************************************************@G38ESBB
*                                                              @G38ESBB
*        PROCESS FCB MAPPING FOR SPACE IMMEDIATE OR            @G38ESBB
*        WRITE AND SPACE                                       @G38ESBB
*                                                              @G38ESBB
***************************************************************@G38ESBB
         SPACE 1                                               @G38ESBB
PSPACE   NI    PCCWORK,PSAVBITS    RESERVE LOWER FOUR BITS     @G38ESBB
         SLR   R15,R15             ZERO WORK AREA              @G38ESBB
         IC    R15,PCCWORK         SET CONVERTED LINE SPACING  @G38ESBB
         AH    R15,PQHFCBLN        ADD CURRENT FCB LINE POS    @G38ESBB
         STH   R15,PQHFCBLN        SAVE NEW FCB LINE POSITION  @G38ESBB
         CLC   PQHFCBLN,PFCBLENG   PASSED BOTTOM OF PAGE...    @G38ESBB
         BNH   PNOSPACE            BR IF NOT                   @G38ESBB
         L     R15,PQHMAPV         GET MAPPING PAGES           @OZ53047
         BCTR  R15,0               DECREMENT PAGES             @G38ESBB
         ST    R15,PQHMAPV         SAVE NEW MAP PAGE COUNT     @OZ53047
         MVC   PQHFCBLN,PFCBTOP    POINT FCB TO TOP OF PAGE    @G38ESBB
PNOSPACE NI    PFCBFLG1,FF-PFCBNOSP RESET WRT-NO-SPACE IND     @G38ESBB
         CLI   PCCWORK,PWRTNOSP    TEST FOR WRITE-NO-SPACE     @G38ESBB
         BNE   PTSTDONE            BR IF NO                    @G38ESBB
         OI    PFCBFLG1,PFCBNOSP TURN ON WRITE-NO-SPACE IND    @G38ESBB
         B     PTSTDONE            BR TO RETURN                @G38ESBB
         SPACE 1                                               @G38ESBB
***************************************************************@G38ESBB
*                                                              @G38ESBB
*        PROCESS FCB MAPPING FOR SKIP TO CHANNEL               @G38ESBB
*                                                              @G38ESBB
***************************************************************@G38ESBB
         SPACE 1                                               @G38ESBB
PSKIP    NI    PCCWORK,PSAVBITS    RESERVE LOWER 4 BITS        @G38ESBB
         SLR   R15,R15             ZERO WORK REGISTER          @G38ESBB
         IC    R15,PCCWORK         GET CHANNEL CODE            @G38ESBB
         L     R0,PQHMAPV          GET MAPPING PAGES           @OZ53047
         LA    R1,PFCBSTRT         GET ADDRESS OF FCB          @G38ESBB
         LH    PL,PQHFCBLN         GET CURRENT FCB LINE        @G38ESBB
         ALR   R1,PL               ADD FCB LINE TO FCB ADDRESS @G38ESBB
         BCTR  R1,0                MAKE ZERO OFFSET            @G38ESBB
         CLM   R15,1,0(R1)         ARE WE AT CHANNEL           @G38ESBB
         BNE   PFCBSCAN            BR IF NOT                   @G38ESBB
         TM    PFCBFLG1,PFCBNOSP PREVIOUS WRITE-NO-SPACE       @G38ESBB
         BZ    PTSTDONE            BR IF NOT                   @G38ESBB
         SPACE 1                                               @G38ESBB
***************************************************************@G38ESBB
*                                                              @G38ESBB
*        SCAN FCB FOR MATCH ON CHANNEL COMMAND                 @G38ESBB
*                                                              @G38ESBB
***************************************************************@G38ESBB
         SPACE 1                                               @G38ESBB
PFCBSCAN LA    PL,1(PL)            INCREMENT FCB INDEX         @G38ESBB
         LA    R1,1(R1)            POINT TO NEXT BYTE IN FCB   @G38ESBB
         CH    PL,PFCBLENG         PAST END OF FCB...          @G38ESBB
         BNH   PFCB2               BR IF NOT                   @G38ESBB
         LA    R1,PFCBSTRT         RESET TO TOP OF FCB         @G38ESBB
         LA    PL,1                RESET FCB LINE COUNT        @G38ESBB
         BCTR  R0,0                INDIC PAGE DECREMENT        @G38ESBB
         SPACE 1                                               @G38ESBB
PFCB2    CLM   R15,1,0(R1)         CHANNEL REACHED...          @G38ESBB
         BE    PFCBMTCH            BR IF YES                   @G38ESBB
         CH    PL,PQHFCBLN         ENTIRE FCB SCANNED...       @G38ESBB
         BNE   PFCBSCAN            BR IF NOT                   @G38ESBB
         LA    PL,1(,PL)           INCREMENT LINE POSITION     @G38ESBB
         A     R0,=F'1'            NO PAGE DECREMENT           @OZ53047
         CH    PL,PFCBLENG         END OF PAGE REACHED         @G38ESBB
         BNH   PFCBMTCH            BR IF NO                    @G38ESBB
         LA    PL,1                RESET FCB LINE COUNT        @G38ESBB
         BCTR  R0,0                NEED PAGE DECREMENT         @G38ESBB
         SPACE 1                                               @G38ESBB
PFCBMTCH STH   PL,PQHFCBLN         SAVE NEW FCB INDEX          @G38ESBB
         ST    R0,PQHMAPV          SAVE NEW MAP PAGE COUNT     @OZ53047
         EJECT                                                 @G38ESBB
***************************************************************@G38ESBB
*                                                              @G38ESBB
*        IF RESTART POINT REACHED, RESET INDICATORS TO RESUME  @G38ESBB
*        PRINTING.                                             @G38ESBB
*        IF TERMINATION COMMAND, SET CONDITION CODE FOR        @G38ESBB
*        MAINLINE BRANCH TO PROCESSOR TERMINIATION.            @G38ESBB
*                                                              @G38ESBB
***************************************************************@G38ESBB
         SPACE 1                                               @G38ESBB
PTSTDONE ST    PC1,PCCWORK         RESTORE COMMAND CODE        @G38ESBB
         DROP  R14                 SUSPEND BUFFER ADDRESS      @G38ESBB
         CLC   PQHMAPV,$ZEROS      MAPPING DONE                @G38ESBB
         BNE   PMAPEND             BR IF NO                    @G38ESBB
         L     R1,PQHLAST          GET LAST PQE                @G38ESBB
         CR    R1,PW               TEST FOR EMPTY PPQ          @OZ46351
         BE    PMAPRST             BRANCH IF YES               @OZ46351
         USING PQEDSECT,R1         PQE ADDRESSABILITY          @G38ESBB
         TM    PQECFLAG,PQECLPG    LAST PAGE OF DATA SET       @G38ESBB
         BZ    PCHKFCB             BR IF NOT                   @G38ESBB
         L     R1,PQECPQED         GET DATA SET PQE            @G38ESBB
         TM    PQEDFLAG,PQEDLAST   LAST DATA SET OF JOE        @G38ESBB
         BZ    PCHKFCB             BR IF NOT             .     @G38ESBB
PMAPRST  DS    0H                                              @OZ46351
         NI    PPFLAG3,FF-PP3800R  RESET 3800 REPOSITION INDIC @G38ESBB
         NI    PPFLAG4,FF-PPREPOBS RESET B-SPACE FLAG IF SET   @OZ78256
         XC    PDDBSKIP,PDDBSKIP   RESET TO RESUME PRINTING    @G38ESBB
         ICM   R1,15,PQHFCB        GET FCB BUFFER ADDR         @OZ51936
         BZ    PMAPRET             BRANCH IF NONE              @OZ51936
        $FREEBUF (R1)              FREE FCB BUFFER             @OZ51936
         MVC   PQHFCB,$ZEROS       CLEAR BUFFER ADDR           @OZ51936
PMAPRET  LA    R15,8               SET CONDITION CODE          @OZ51936
         LTR   R15,R15             SET NON-ZERO CONDITION CODE @G38ESBB
         PRETURN                   RETURN TO MAINLINE          @G38ESBB
         EJECT                                                 @OZ51936
PCHKFCB  CLC   PQHFCBLN,PFCBTOP    ARE WE AT TOP OF FCB...     @G38ESBB
         BE    PSTARTPR            YES,GO SET FOR START PRINT  @G38ESBB
         OI    PCCWORK,PCONVIMM    ELSE,CONVERT CCW TO         @G38ESBB
         LM    PC1,PC2,PCCWORK      IMMEDIATE AND PUT INTO     @G38ESBB
         BAL   PL,PPPUT              CHANNEL PROGRAM           @G38ESBB
         L     PW,PQHADR           RESTORE PQH ADDRESS         @G38ESBB
         SPACE 1                                               @G38ESBB
PSTARTPR NI    PPFLAG3,FF-PP3800R  RESET 3800 REPO INDIC       @G38ESBB
         NI    PPFLAG4,FF-PPREPOBS RESET B-SPACE FLAG IF SET   @OZ78256
         OI    PPFLAG3,PP38CKPT    CREATE PQEC FOR RESUME      @OZ52713
         XC    PDDBSKIP,PDDBSKIP   RESET PRINTING INDIC        @G38ESBB
         L     R1,PQHFCB           GET FCB ADDRESS             @G38ESBB
         LTR   R1,R1               FCB LOADED....              @G38ESBB
         BZ    PMAPEND             BR IF NOT                   @G38ESBB
        $FREEBUF (R1)              RELEASE THE FCB             @G38ESBB
         MVC   PQHFCB,$ZEROS       RESET FCB ADDRESS           @G38ESBB
         SPACE 1                                               @G38ESBB
PMAPEND  SR    R15,R15             SET ZERO CONDITION CODE     @G38ESBB
         PRETURN                   RESTORE REGS AND RETURN     @G38ESBB
         DROP  BASE2,PW,R1         SUSPEND BASE,PQH,PQE        @G38ESBB
         EJECT                                                 @G38ESBB
***************************************************************@G38ESBB
*                                                              @G38ESBB
*        A TRANSLATE TABLE WILL BE USED TO CONVERT THE CONTROL @G38ESBB
*        CHARACTERS TO A FORMAT MORE EASILY USED.              @G38ESBB
*        THE BITS IN EACH BYTE HAVE THE FOLLOWING FORMAT -     @G38ESBB
*                                                              @G38ESBB
*        0 -     RESERVED                                      @G38ESBB
*                                                              @G38ESBB
*        1 - 0 = DATA TRANSFER                                 @G38ESBB
*            1 = NO DATA TRANSFER (AN IMMEDIATE COMMAND)       @G38ESBB
*                                                              @G38ESBB
*        2 - 0 = LOW ORDER BITS ARE # OF LINES TO SPACE        @G38ESBB
*            1 = LOW ORDER BITS ARE # OF CHANNEL TO SKIP TO    @G38ESBB
*                                                              @G38ESBB
*        3 - 0 = NOT VALID FCB MAPPING COMMAND, IGNORE         @G38ESBB
*            1 = VALID FCB MAPPING COMMAND                     @G38ESBB
*                                                              @G38ESBB
*        4-7   = # OF LINES TO SPACE OR CHANNEL TO SKIP TO     @G38ESBB
*                                                              @G38ESBB
***************************************************************@G38ESBB
         SPACE 1                                               @G38ESBB
PCCTABLE DC    X'00100000000000000011005100000000'             @G38ESBB
         DC    X'00120052000000000013005300000000'             @G38ESBB
         DC    X'00000000000000000000000000000000'             @G38ESBB
         DC    X'00000000000000000000000000000000'             @G38ESBB
         DC    X'00000000000000000000000000000000'             @G38ESBB
         DC    X'00000000000000000000000000000000'             @G38ESBB
         DC    X'00000000000000000000000000000000'             @G38ESBB
         DC    X'00000000000000000000000000000000'             @G38ESBB
         DC    X'00000000000000000031007100000000'             @G38ESBB
         DC    X'00320072000000000033007300000000'             @G38ESBB
         DC    X'00340074000000000035007500000000'             @G38ESBB
         DC    X'00360076000000000037007700000000'             @G38ESBB
         DC    X'00380078000000000039007900000000'             @G38ESBB
         DC    X'003A007A00000000003B007B00000000'             @G38ESBB
         DC    X'003C007C000000000000000000000000'             @G38ESBB
 TITLE 'HASP PRINT/PUNCH SERVICES -- PRODUCE JOB HEADER'       @G38ESBB
***************************************************************@G38ESBB
*                                                              @G38ESBB
*        SETUP PRINT/PUNCH DEVICE BY CHAR-JOE AND PRINT        @G38ESBB
*        SEPARATORS.                                           @G38ESBB
*                                                              @G38ESBB
*        R1  - ADDRESS CHARACTERISTICS JOE                     @G38ESBB
*        JCT - ADDRESS JCT                                     @G38ESBB
*                                                              @G38ESBB
*        RC = 0  - NORMAL RETURN                               @OZ75372
*           = 4  - TERMINATION COMMAND DETECTED DURING SETUP   @OZ75372
*           = 8  - 3800 JAM OR CANCEL KEY DURING SETUP         @OZ75372
*           = 12 - 3800 ERROR DURING DEVICE SETUP              @OZ75372
*                                                              @G38ESBB
***************************************************************@G38ESBB
         SPACE 1                                               @G38ESBB
         USING JOEDSECT,R1         PROVIDE JOE ADDRESSABILITY  @G38ESBB
         USING JCTDSECT,JCT        PROVIDE JCT ADDRESSABILITY  @G38ESBB
         SPACE 1                                               @G38ESBB
PHEADER  PSAVE ALL                 SAVE ALL REGISTERS          @G38ESBB
         LR    BASE2,R15           SETUP LOCAL                 @G38ESBB
         USING PHEADER,BASE2             ADDRESSABILITY        @G38ESBB
         TM    PPFLAG3,PP3800R     3800 RESTART...             @G38ESBB
         BO    PSIGNON             BR IF YES                   @G38ESBB
         CLI   PDEVTYP3,UCB3800    3800 PRINTER....            @G38ESBB
         BE    PCOLDST             BR IF YES                   @G38ESBB
         SPACE 1                                               @G38ESBB
PSIGNON  L     R0,PWKJOE           GET WORK JOE PARAMETER      @G38ESBB
         L     R15,=A(PJODMSG)     CALL JOB ON DEVICE          @G38ESBB
         BALR  PL,R15                   MESSAGE ROUTINE        @G38ESBB
         SPACE 1                                               @G38ESBB
PCOLDST  L     R1,PCHJOE           ADDRESS CHAR-JOE            @G38ESBB
         L     PL,PCEDCT           ADDRESS PRINTER DCT         @G38ESBB
         CLI   PDEVTYP3,UCB3800    TEST FOR 3800 PRINTER       @G38ESBB
         BNE   PSETUPST            BR IF NOT - SETUP IMPACT PR @G38ESBB
         MVC   SPFORMS(2*4),JOEFORM USE JOE FORMS AND FCB ID   @G38ESBB
         MVC   SPFLASH,JOEFLASH    SET FLASH (SEP WON'T FLASH) @G38ESBB
         MVC   SPMODF,=C'****'     RESET COPY MODIFICATION     @G38ESBB
         MVI   SPCOPYN,1           FORCE ONLY 1 COPY OF HEADER @G38ESBB
         MVI   SPCOPYS,1           INDICATE STARTING COPY NUM  @G38ESBB
         MVI   SPFLAG,SPSEP        INIT FLAGS FOR SEP PAGE     @G38ESBB
         TM    JOECFLAG,$JOEBRST   SET FOR                     @G38ESBB
         BZ    PSETUP               NOBURST OR                 @G38ESBB
         OI    SPFLAG,SPBURST        BURST                     @G38ESBB
         B     PSETUP              ENTER COMMON SETUP          @G38ESBB
         SPACE 1                                               @G38ESBB
PSETUPST DS    0H                  SETUP STANDARD PRINT/PUNCH  @G38ESBB
         LA    R1,JOEFORM          ADDRESS SETUP PORTION       @G38ESBB
         MVI   DCTACPTN-DCTDSECT(PL),X'00' DISABLE COMPACT FOR @G38ESBB
         MVI   PRINDEX,X'81'       SET 3211 INDEX TO 1         @G38ESBB
         MVI   PREVCPYN,1          SET NON 3800 COPY COUNT     @G38ESBB
         SPACE 1                                               @G38ESBB
PSETUP   DS    0H                                              @G38ESBB
         L     R15,=A(PRPUDSV)     CALL DEVICE                 @G38ESBB
         BALR  PL,R15               SETUP VERIFICATION         @G38ESBB
         SPACE 1                                               @OZ75372
         L     R1,PSAVAREA         SAVE DEVICE SETUP           @OZ75372
         ST    R15,4+(15*4)(,R1)    RETURN CODE                @OZ75372
         B     PSETUPR(R15)        BRANCH ON RETURN CODE       @OZ75372
         SPACE 1                                               @OZ75372
PSETUPR  B     POFFSTK             0  NORMAL RETURN            @OZ75372
         B     PTESTI              4  OPER TERMINATE           @OZ75372
         B     PSETJAM             8  3800 JAM OR CANCEL KEY   @OZ75372
         B     PTABRTN             12 3800 ERROR IN SETUP      @OZ75372
         SPACE 1                                               @OZ75372
PSETJAM  L     R15,=A(PLOCATE)     CALL THE LOCATE ROUTINE     @OZ75372
         BALR  PL,R15               TO GET THE ORIGIN PQE      @OZ75372
         B     PTABRTN             RETURN ERROR TO CALLER      @OZ75372
         SPACE 1                                               @OZ75372
*              THIS LINE DELETED BY APAR OZ75372               @OZ75372
*              THIS LINE DELETED BY APAR OZ75372               @OZ75372
         SPACE 1                                               @OZ70297
        $MID   170                                             @OZ45081
PTESTI   LA    R1,=C'$HASP170 '    MESSAGE ID                  @OZ70297
         TM    PDCTFLAG,DCTRSTRT+DCTBKSP $I - (INTERRUPT)...   @OZ45081
         BNO   PTESTE              ON, GO TEST $E              @OZ45081
         LA    R14,=C' INTERRUPTED' MESSAGE TEXT               @OZ48259
         OI    PSMFDCI,SMFINTRP    SET SMF FLAG                @OZ45081
         B     PDOMSG              GO ISSUE MESSAGE            @OZ45081
         SPACE 1                                               @OZ45081
PTESTE   TM    PDCTFLAG,DCTRSTRT   $E - (RESTART)...           @OZ45081
         BNO   PTESTC              NO, BRANCH                  @OZ45081
         LA    R14,=C' RESTARTED  ' MESSAGE TEXT               @OZ48259
         OI    PSMFDCI,SMFRESTR    SET SMF FLAG                @OZ45081
         B     PDOMSG              GO ISSUE MESSAGE            @OZ45081
         SPACE 1                                               @OZ45081
PTESTC   TM    PDCTFLAG,DCTDELET   $C - (CANCEL)...            @OZ45081
         BNO   PTRM                NO, BRANCH                  @OZ45081
         LA    R14,=C' DELETED    ' MESSAGE TEXT               @OZ48259
         OI    PSMFDCI,SMFOPSTP    SET SMF FLAG                @OZ45081
         B     PDOMSG              GO ISSUE MESSAGE            @OZ45081
         EJECT                                                 @OZ70297
PTRM     DS    0H                                              @OZ45081
        $MID   185                                             @OZ45081
         LA    R1,=C'$HASP185 '    MESSAGE ID                  @OZ45081
         LA    R14,=C' TERMINATED ' MESSAGE TEXT               @OZ48259
         SPACE 1                                               @OZ45081
PDOMSG   L     R15,=A(PRMSG)       ISSUE MESSAGE TO OPERATOR   @OZ45081
         BALR  PL,R15                AND ADD IT TO OUTPUT      @OZ48259
         CLI   PDEVTYP3,UCB3800    TEST FOR 3800               @OZ70297
         BNE   PTABRTN             BRANCH IF NOT               @OZ70297
         L     PW,PQHADR           ADDRESS 3800 PQH            @OZ45081
         TM    PQHFLAG-PQHDSECT(PW),PQHDSVC RESET NEEDED...    @OZ45081
         BZ    PTABRTN             BRANCH IF NOT               @OZ70297
         NI    PQHFLAG-PQHDSECT(PW),FF-PQHDSVC RESET INDICATOR @OZ45081
         NI    PDCTFLAG,FF-DCTDELET-DCTRSTRT-DCTBKSP RESET CMD @OZ45081
         SPACE 1                                               @OZ70297
         SPACE 1                                               @OZ75372
PTABRTN  DS    0H                                              @OZ75372
         PRETURN ,                 RETURN TO CALLER            @OZ75372
         EJECT                                                 @G38ESBB
***************************************************************@G38ESBB
*                                                              @G38ESBB
*        OFFSET-STACK 3800 BURSTER JOBS                        @G38ESBB
*                                                              @G38ESBB
***************************************************************@G38ESBB
         SPACE 1                                               @G38ESBB
POFFSTK  CLI   PDEVTYP3,UCB3800    TEST DEVICE TYPE            @G38ESBB
         BNE   PTESTSEP            BR IF NOT 3800 PRINTER      @G38ESBB
         LM    PC1,PC2,PCCWOFST    ISSUE AN OFFSET-            @G38ESBB
         BAL   PL,PPPUT2             STACK COMMAND             @OZ51441
         TM    PPFLAG3,PP3800R     3800 RESTART                @G38ESBB
         BO    PTESTSEP            BR IF YES                   @G38ESBB
         SPACE 1                                               @G38ESBB
PGETPQEJ L     PW,PQHADR           ADDRESS PQH                 @G38ESBB
         USING PQHDSECT,PW         SET PQH ADDRESSABILITY      @OZ48003
         NI    PQHAFLAG,FF-PQHALOC RESET ALLOCATION FLAG       @OZ48003
         L     R15,=A(PADDPQE)     CALL SUBROUTINE TO          @OZ48003
         BALR  PL,R15               ALLOCATE A PQE             @OZ48003
         BNZ   PGOTPQEJ            BRANCH IF SUCCESSFUL        @OZ48003
         LA    R15,4               SET OPERATOR                @OZ75372
         L     R1,PSAVAREA          TERMINATED                 @OZ75372
         ST    R15,4+(15*4)(,R1)     RETURN CODE               @OZ75372
         B     PTESTI              BR TO TEST OPER CMD         @OZ75372
         DROP  PW                  SUSPEND PQH ADDRESSABILITY  @OZ48003
         SPACE 1                                               @G38ESBB
         USING PQEDSECT,R1         PQE ADDRESSABILITY          @G38ESBB
PGOTPQEJ MVI   PQETYPE,PQEJ        INDICATE JOB START PQE      @G38ESBB
         MVC   PQEJWJOE,PWKJOE     GET WORK JOE FOR PQEJ       @G38ESBB
         L     R15,=A(PPGIDIO)     ISSUE I/O TO                @G38ESBB
         BALR  PL,R15               COMPLETE PQE               @G38ESBB
         SPACE 2                                               @G38ESBB
***************************************************************@G38ESBB
*                                                              @G38ESBB
*        SEE IF OUTPUT SEPARATORS ARE WANTED                   @G38ESBB
*                                                              @G38ESBB
***************************************************************@G38ESBB
         SPACE 1                                               @G38ESBB
         USING DCTDSECT,R1         DCT ADDRESSABILITY          @G38ESBB
         SPACE 1                                               @G38ESBB
PTESTSEP L     R1,PCEDCT           ADDR OF DCT                 @G38ESBB
         TM    DCTPPSW,DCTPPSWS    SUPPRESS SEPARATORS...      @G38ESBB
         BO    PHDREND             BR IF YES                   @G38ESBB
         TM    PCEID,PCEPRSID      TEST PROCESSOR TYPE         @G38ESBB
         BO    PRINTSEP            BRANCH IF PRINT             @G38ESBB
         SPACE 1                                               @G38ESBB
         DROP  R1                  SUSPEND DCT ADDRESSABILITY  @G38ESBB
         EJECT                                                 @G38ESBB
***************************************************************@G38ESBB
*                                                              @G38ESBB
*        PRODUCE PUNCH SEPARATOR LACE CARD                     @G38ESBB
*                                                              @G38ESBB
***************************************************************@G38ESBB
         SPACE 1                                               @G38ESBB
PUNCHSEP DS    0H                                              @G38ESBB
         SPACE 1                                               @G38ESBB
         TM    PCEID,PCERJEID      TEST PROCESSOR TYPE         @G38ESBB
         BZ    PUNCHRM             BR IF NOT REMOTE            @G38ESBB
         TM    MDCTFEAT-DCTDSECT(R1),DCTPSHDR IF NO SETUP HDR  @G38ESBB
         BNO   PUNCHRM                    FOR PUNCH, SKIP PDIR @G38ESBB
         MVI   PPDIRID,X'01'       INDICATE SEPARATOR PDIR     @G38ESBB
         LA    PL,2                USE A RECORD COUNT OF 2     @G38ESBB
         ST    PL,PPDSRCT           FOR PUNCH                  @G38ESBB
         L     R15,=A(PPDIR)       POINT TO SEPARATOR ROUTINE  @G38ESBB
         BALR  PL,R15              PUT SEPARATOR PDIR          @G38ESBB
         SPACE 1                                               @G38ESBB
PUNCHRM  MVC   PCCWORK(4),JCTROOMN SET UP ROOM NUMBER          @G38ESBB
         MVC   PCCWORK+4(4),JCTJOBID+4 AND JOB NUMBER          @G38ESBB
         LA    R1,BUFSTART         ADDR BUFFER AS A WORK AREA  @G38ESBB
         SLR   PW,PW               PREPARE FOR SCAN            @G38ESBB
PUCHGEN  MVI   0(R1),X'6A'         START FIELD WITH 12-11 PNCH @G38ESBB
         IC    R0,PCCWORK(PW)      MOVE NEXT CHARACTER         @G38ESBB
         STC   R0,1(,R1)            TO IMAGE                   @G38ESBB
         OI    1(R1),X'30'         CONVERT                     @G38ESBB
         CLI   1(R1),X'F0'          EACH                       @G38ESBB
         BH    PUCHARD               CHARACTER                 @G38ESBB
         BE    PUCHAR0                FROM                     @G38ESBB
         MVI   1(R1),X'EA'             EBCDIC                  @G38ESBB
         SPACE 1                                               @G38ESBB
PUCHAR0  XI    1(R1),X'E0'              TO                     @G38ESBB
         SPACE 1                                               @G38ESBB
PUCHARD  XI    1(R1),X'60'               12-11-X PUNCH         @G38ESBB
         MVC   2(7,R1),1(R1)       PROPAGATE CHARACTER         @G38ESBB
         MVI   9(R1),X'6A'         TERM FIELD WITH 12-11 PUNCH @G38ESBB
         LA    R1,10(,R1)          STEP TO NEXT FIELD          @G38ESBB
         LA    PW,1(,PW)           STEP TO NEXT CHARACTER      @G38ESBB
         CL    PW,=F'8'            TEST                        @G38ESBB
         BL    PUCHGEN             BRANCH IF NOT LAST CHAR     @G38ESBB
         LA    PC1,BUFSTART        SETUP PUNCH CCW             @G38ESBB
         AL    PC1,PUCCW           ADD LEFT HALF OF CCW        @G38ESBB
         CLI   PDEVTYPE+3,X'0C'    IS DEV A 3525 PUNCH         @OZ45115
         BNE   PUCHARD2            BR IF NO TO 2540 DEFAULT    @OZ45115
         ICM   PC1,8,=X'01'        SET STACKER 1 DEFAULT       @OZ45115
PUCHARD2 DS    0H                                              @OZ45115
         L     PC2,PUCCW+4         AND RIGHT HALF OF CCW       @G38ESBB
         BAL   PL,PPPUT            ADD CCW TO CHAIN            @G38ESBB
         CLI   PDEVTYPE+3,X'0C'    IS DEVICE A 3525....        @OZ49531
         BE    PUCHARD3            BR IF YES, DON'T PUNCH BLNK @OZ49531
         LM    PC1,PC2,PUCCWBL     LOAD BLANK CARD CCW         @G38ESBB
         BAL   PL,PPPUT            ADD CCW TO CHAIN            @G38ESBB
PUCHARD3 DS    0H                                              @OZ49531
         BAL   PL,PPWRITE           INITIATE WRITE             @G38ESBB
         BAL   PL,PPCHECK            AND CHECK                 @G38ESBB
         B     PHDREND             CONTINUE                    @G38ESBB
         EJECT                                                 @G38ESBB
***************************************************************@G38ESBB
*                                                              @G38ESBB
*        PRODUCE PRINT SEPARATOR PAGE                          @G38ESBB
*                                                              @G38ESBB
***************************************************************@G38ESBB
         SPACE 1                                               @G38ESBB
         SPACE 1                                               @G38ESBB
PRINTSEP L     R1,$NEWSTTR         GET JES2-NEWS MTTR          @G38ESBB
         LTR   R1,R1               NEWS AVAILABLE...           @G38ESBB
         BZ    PHEADER1            BR IF NOT                   @G38ESBB
         TM    PPFLAG3,PP3800R     ARE WE REPOSITIONING...     @G38ESBB
         BO    PHEADER1            YES, NO NEWS AFTER REPO     @G38ESBB
         OI    PPFLAG,PPNEWS        ELSE SET NEWS FLAG         @G38ESBB
         ST    R1,PCEJMTTR         SET NEWS MTTR               @G38ESBB
         MVC   PCEEJRCB,PCCW+2     SET INITIAL RCB OFFSET      @G38ESBB
         MVC   PPKEY,=C'$$NEWS'    SET SPECIAL JOB/DS KEY      @G38ESBB
         MVC   PRLINECT,=F'-1'     SET LARGE PAGE SIZE         @G38ESBB
         SPACE 1                                               @G38ESBB
PHEADER1 LA    R1,=C'START'        ASSUME COLD START           @G38ESBB
         TM    PCKJOE,$JOECKV      'START' OR 'CONT'...        @G38ESBB
         BZ    PHEADER2            BR IF COLD START            @G38ESBB
         LA    R1,=C'CONT '         ELSE SET FOR CONTINUATION  @G38ESBB
         SPACE 1                                               @G38ESBB
PHEADER2 L     R15,=A(PRINTID)     PRODUCE PRINT               @G38ESBB
         BALR  PL,R15                 SEPARATOR PAGE           @G38ESBB
         SPACE 1                                               @G38ESBB
PHDREND  SR    R15,R15             SET ZERO CONDITION CODE     @G38ESBB
         PRETURN ,                 RETURN TO CALLER            @G38ESBB
         SPACE 1                                               @G38ESBB
         DROP  JCT                 SUSPEND JCT                 @G38ESBB
         SPACE 3                                               @G38ESBB
         LTORG                                                 @G38ESBB
 TITLE 'HASP PRINT/PUNCH SERVICE -- CHECKPOINT ACCESS ROUTINE' @G38ESBB
***************************************************************@G38ESBB
*                                                              @G38ESBB
*        CHECKPOINT ACCESS ROUTINE                             @G38ESBB
*                                                              @G38ESBB
*        ON EXIT -                                             @G38ESBB
*        CONDITION CODE SET                                    @G38ESBB
*        ZERO     - CHECKPOINT ACCESS SUCCESSFULLY ACQUIRED    @G38ESBB
*        NON-ZERO - CHECKPOINT ACCESS NOT ACQUIRED             @G38ESBB
*                                                              @G38ESBB
***************************************************************@G38ESBB
         SPACE 1                                               @G38ESBB
PGETQS   PSAVE ALL                 SAVE CALLER'S REGS          @G38ESBB
         LR    BASE2,R15           SETUP LOCAL                 @G38ESBB
         USING PGETQS,BASE2         ADDRESSABILITY             @G38ESBB
         SPACE 1                                               @G38ESBB
         $QSUSE TYPE=TEST          THIS CPU OWN THE CKPT DATA  @G38ESBB
         BZ    PGOTQS              BR IF YES                   @G38ESBB
         SPACE 1                                               @G38ESBB
***************************************************************@G38ESBB
*                                                              @G38ESBB
*        FORCE CKPT IF $CKPTIME INTERVAL HAS EXPIRED           @G38ESBB
*                                                              @G38ESBB
***************************************************************@G38ESBB
         SPACE 1                                               @G38ESBB
         STCK  PCER1               GET CURRENT TIME            @G38ESBB
         L     PW,PCER1            UPPER WORD ALMOST SECONDS   @G38ESBB
         SL    PW,PCECLOCK         IF NOT LONGER               @G38ESBB
         C     PW,$CKPTIME          THAN LIMIT,                @G38ESBB
         BL    PQSEND                RETURN WITH CC SET LOW    @G38ESBB
         SPACE 1                                               @G38ESBB
PGETQS1  DS    0H                  FORCE QUEUE OWNERSHIP       @G38ESBB
         $QSUSE                    REQUEST ACCESS TO CKPT DATA @G38ESBB
         SPACE 1                                               @G38ESBB
PGOTQS   DS    0H                  NOW OWN ACCESS TO CKPT DATA @G38ESBB
         STCK  $DOUBLE             RESET START TIME FOR        @G38ESBB
         MVC   PCECLOCK,$DOUBLE     CHECKPOINT INTERVAL        @G38ESBB
         SR    R15,R15             SET CONDITION CODE          @G38ESBB
         SPACE 1                                               @G38ESBB
PQSEND   PRETURN                   RETURN WITH CC SET          @G38ESBB
         SPACE 1                                               @G38ESBB
         DROP  BASE2               DROP LOCAL ADDRESSAVILITY   @G38ESBB
 TITLE 'HASP PRINT/PUNCH SERVICE -- JOB ON DEVICE MSG ROUTINE' @G38ESBB
***************************************************************@G38ESBB
*                                                              @G38ESBB
*        ISSUE PRINT/PUNCH JOB SIGN ON MESSAGE TO THE OPERATOR @G38ESBB
*                                                              @G38ESBB
*        REGISTERS ON ENTRY                                    @G38ESBB
*        R0  = ADDRESS OF WORK JOE                             @G38ESBB
*        R10 = ADDRESS OF JQE                                  @G38ESBB
*                                                              @G38ESBB
***************************************************************@G38ESBB
         SPACE 1                                               @G38ESBB
PJODMSG  PSAVE ALL                 SAVE ALL REGISTERS          @G38ESBB
         LR    BASE2,R15           SETUP LOCAL                 @G38ESBB
         USING PJODMSG,BASE2        ADDRESSABILITY             @G38ESBB
         L     R1,PCEDCT           GET ADDRESS OF PRT/PU DCT   @G38ESBB
         USING DCTDSECT,R1         ESTABLISH DCT ADR           @G38ESBB
        $MID   150                                             @G38ESBB
         PMSG  PMESSAGE,M150L,(X'150F',C'ON ',DCTDEVN)         @G38ESBB
         LR    R2,R0               ADDRESS WORK-JOE            @G38ESBB
         USING JOEDSECT,R2         PROVIDE JOE ADDRESSABILITY  @G38ESBB
         L     R2,JOERECCT         GET SYSOUT RECORD COUNT     @G38ESBB
         DROP  R2                  DROP JOE ADDRESSABILITY     @G38ESBB
         LA    R0,M150L            SHORT MESSAGE LENGTH        @G38ESBB
         LTR   R2,R2               TEST RECORD COUNT           @G38ESBB
         BZ    PONWTO              BR IF NONE                  @G38ESBB
         CVD   R2,$DOUBLE          MAKE RECORD COUNT DECIMAL   @OZ46142
         MVC   PMESSAGE+M150L(L'PPRTSTAT),PPRTSTAT MAKE REC CT @G38ESBB
         ED    PMESSAGE+M150L(L'PPRTSTAT),$DOUBLE+4  READABLE  @OZ46142
         PMSG  PMESSAGE+M150L+L'PPRTSTAT,M150L2,(C' LINES')    @G38ESBB
         TM    PCEID,PCEPRSID      TEST PROCESSOR TYPE         @G38ESBB
         BO    PONPRTR             BR IF PRINTER               @G38ESBB
         PMSG  PMESSAGE+M150L+L'PPRTSTAT,,(C' CARDS')          @G38ESBB
         SPACE 1                                               @G38ESBB
PONPRTR  LA    R0,M150L+L'PPRTSTAT+M150L2 ASSUME MULTIPLE REC  @G38ESBB
         BCT   R2,PONWTO           BR IF PLURAL                @G38ESBB
         BCTR  R0,0                 ELSE DROP THE 'S'          @G38ESBB
         SPACE 1                                               @G38ESBB
PONWTO  $WTO   PMESSAGE,(R0),      'JOB ON PRINTER/PUNCH' MSG  @G38ESBBC
               ROUTE=$LOG+$UR,JOB=YES,  (NOTE: R10=ADR OF JQE) @G38ESBBC
               CLASS=$TRIVIA,PRI=$ST                           @G38ESBB
         SPACE 1                                               @G38ESBB
         PRETURN                   RESTORE REGS AND RETURN     @G38ESBB
 TITLE 'HASP PRINT/PUNCH SERVICE -- PURGE SPIN DATA SET SPACE' @G38ESBB
***************************************************************@G38ESBB
*                                                              @G38ESBB
*        PURGE SPIN DATA SETS TRACKS FROM THE SPOOL PACKS      @G38ESBB
*                                                              @G38ESBB
*        R0 - IOT TRACK ADDRESS (ON ENTRY)                     @G38ESBB
*                                                              @G38ESBB
***************************************************************@G38ESBB
         SPACE 1                                               @G38ESBB
         USING PURSPDS,BASE2       PROVIDE LOCAL ADR           @G38ESBB
         USING IOTDSECT,JCT        ACTIVATE IOT ADDRESSABILITY @G38ESBB
         SPACE 1                                               @G38ESBB
PURSPDS  PSAVE ALL                 SAVE CALLER'S REGISTERS     @G38ESBB
         LR    BASE2,R15           PROVIDE LOCAL ADR           @G38ESBB
         CLI   PDEVTYP3,UCB3800    3800 PRINTER ...            @OZ58550
         BE    PURSPOK             YES, PURGE TRACKS           @OZ58550
         $QSUSE                    REQUEST CKPT ACCESS         @OZ58550
         L     R1,PCEJQE           GET JQE ADDRESS             @OZ58550
         LH    R1,JQEJOE-JQEDSECT(,R1)  GET 1ST JOE OFFSET     @OZ58550
         N     R1,=XL4'0000FFFF'   CLEAR HIGH BYTES            @OZ58550
         SLL   R1,2                EXPAND TO OFFSET ADR.       @OZ58550
         B     PURSCK2             GO CHECK JOES               @OZ58550
         SPACE ,                                               @OZ58550
PURSCK1  LH    R1,JOEJOE-JOEDSECT(,R1)  GET NXT JOE PTR        @OZ58550
         LTR   R1,R1               END OF CHAIN ...            @OZ58550
         BZ    PURSPOK             YES, GO PURGE TRACKS        @OZ58550
         N     R1,=XL4'0000FFFF'   ELSE, CLR HIGH ZEROES       @OZ58550
         SLL   R1,2                 AND EXPAND OFFSET          @OZ58550
         SPACE ,                                               @OZ58550
PURSCK2  AL    R1,$JOTABLE         COMPUTE JOE ADDRESS         @OZ58550
         CL    R0,JOEIOTTR-JOEDSECT(,R1)  IOT TR'S MATCH ...   @OZ58550
         BNE   PURSCK1             NO, CHECK NEXT JOE          @OZ58550
         CLM   R1,7,PWKJOE+1       ELSE, SAME JOE ...          @OZ58550
         BE    PURSCK1             YES, KEEP CHECKING          @OZ58550
         B     PURSPRET            NO, DON'T PURGE TRKS        @OZ58550
         SPACE ,                                               @OZ58550
PURSPOK  LR    R15,R0              IOT ADDRESS FOR READ        @OZ58550
         BAL   PL,PRDBUF           READ IOT INTO A BUFFER      @OZ58550
         BAL   PL,PRDCHK           CHECK READ                  @G38ESBB
         LR    JCT,PBUF            ADDRESS IOT IN BUFFER       @G38ESBB
         TM    PPFLAG,PPRDERR      TEST FOR I/O ERROR ON READ  @G38ESBB
         BO    PSPNIOTR            BR IF YES                   @G38ESBB
         CLC   IOTJBKEY,PPJOBKEY   IS THE IOT VALID            @G38ESBB
         BE    PSPNIOT1            BRANCH IF YES               @G38ESBB
         SPACE 1                                               @G38ESBB
PSPNIOTR $DISTERR                  INDICATE CONTROL BLOCK ERR  @G38ESBB
         OI    PPFLAG,PPJCTIOT+PRDELSW SET REASON FOR TERM     @G38ESBB
         B     PURSPEND            SKIP REMAINDER OF IOT PURGE @G38ESBB
         SPACE 1                                               @G38ESBB
***************************************************************@G38ESBB
*                                                              @G38ESBB
*        RESET SPIN IOT ALLOC BIT AND PURGE DATA SET TRACKS    @G38ESBB
*                                                              @G38ESBB
***************************************************************@G38ESBB
         SPACE 1                                               @G38ESBB
PSPNIOT1 DS    0H                                              @G38ESBB
         L     PL,$IOTPDDB         POINT TO                    @G38ESBB
         ALR   PL,JCT               1ST (AND ONLY) PDDB IN IOT @G38ESBB
         NI    PDBFLAG1-PDBDSECT(PL),FF-PDB1PSO RESET PSO BIT  @G38ESBB
         TM    IOTFLAG1,IOT1ALOC   ALLOCATION IOT              @G38ESBB
         BZ    PSPNIOT4            NO, SKIP $PURGE             @G38ESBB
         NI    IOTFLAG1,FF-IOT1ALOC RESET ALLOCATION IOT BIT   @G38ESBB
         SPACE 1                                               @G38ESBB
        $PURGE IOTTGMAP            PURGE TRACKS USED FOR DS    @G38ESBB
         SPACE 1                                               @G38ESBB
PSPNIOT4 MVC   PCESEEK,IOTTRACK    SET IOT TRACK INTO DA DCT   @G38ESBB
         MVI   PCEDEVTP,PCEDAWR    SET DA DCT TO WRITE         @G38ESBB
         ST    JCT,PCEBUFAD        SET ADDRESS OF IOT BUFFER   @G38ESBB
         LA    R1,PCEDADCT         ADDRESS DA DCT              @G38ESBB
         L     PL,BUFCHAIN         SAVE BUFCHAIN VALUE         @G38ESBB
        $EXCP  (R1),WAIT=YES       WRITE IOT TO SPOOL          @G38ESBB
         ST    PL,BUFCHAIN         REPLACE BUFCHAIN VALUE      @G38ESBB
         BO    PURSPEND            BR IF I/O GOOD              @G38ESBB
         SPACE 1                                               @G38ESBB
PSPNIOTW $DISTERR                  INDICATE CONTROL BLOCK ERR  @G38ESBB
         SPACE 1                                               @G38ESBB
PURSPEND CLI   PDEVTYP3,UCB3800    TEST FOR 3800 PRINTER       @G38ESBB
         BE    PURSPRET            YES, BYPASS PBUF ALTERATION @G38ESBB
         L     R1,PSAVAREA         ADDRESS OUR SAVE AREA       @G38ESBB
         ST    PBUF,4+(PBUF*4)(,R1) UPDATE PBUF IN SAVE AREA   @G38ESBB
         SPACE 1                                               @G38ESBB
PURSPRET PRETURN                   RESTORE REGS AND RETURN     @G38ESBB
         DROP  JCT                 DROP IOT ADDRESSABILITY     @G38ESBB
 TITLE 'HASP PRINT/PUNCH SERVICE -- PQE COMPLETION ROUTINE'    @G38ESBB
***************************************************************@G38ESBB
*                                                              @G38ESBB
*        FOR 3800 PRINTER,                                     @G38ESBB
*        COMPLETE ANY PQE'S ASSIGNED DURING THIS CCW AREA      @G38ESBB
*                                                              @G38ESBB
*        R0  - CT OF ID'S SOLICITED FOR THIS CCW AREA (ENTRY)  @G38ESBB
*        R1  - ADDRESS PENDING PAGE QUEUE ENTRY                @G38ESBB
*        PW  - ADDRESS PENDING PAGE QUEUE HEADER               @G38ESBB
*                                                              @G38ESBB
***************************************************************@G38ESBB
         SPACE 1                                               @G38ESBB
         USING PQECOMP,BASE2       PROVIDE LOCAL ADR           @G38ESBB
         USING PQHDSECT,PW         PROVIDE PQH ADDRESSABILITY  @G38ESBB
         USING PQEDSECT,R1         PROVIDE PQE ADDRESSABILITY  @G38ESBB
         SPACE 1                                               @G38ESBB
PQECOMP  PSAVE ALL                 SAVE CALLER'S REGISTERS     @G38ESBB
         LR    BASE2,R15           ESTABLISH LOCAL ADR         @G38ESBB
         L     PW,PQHADR           ADDRESS PQH                 @G38ESBB
         L     R1,PQHPIDE          GET OLDEST NON-COMPLETE PQE @G38ESBB
*              THIS LINE DELETED BY APAR OZ76973               @OZ76973
*              THIS LINE DELETED BY APAR OZ76973               @OZ76973
         SPACE 1                                               @G38ESBB
PCMPCE   CLR   R1,PW               TEST FOR END OF PPQ         @OZ51011
         BE    PCMPEND             BRANCH IF YES               @OZ51011
         TM    PQETYPE,PQED+PQES   DATASET OR SMF PQE...       @OZ51011
         BNZ   PCMPCNT1            YES, BYPASS PQE COMPLETION  @G38ESBB
         LTR   R0,R0               ANY MORE ID'S               @G38ESBB
         BZ    PCMPEND             NO, DONE, BRANCH            @G38ESBB
         SPACE 1                                               @G38ESBB
         L     R14,PQECSENS        ADDRESS SENSED INFO         @G38ESBB
         CLI   2(R14),X'FF'        AREA ABORT BEFORE SENSE...  @G38ESBB
         BE    PCMPEND             YES, BRANCH                 @G38ESBB
         MVC   PQEFCBLN,L'PQERPGID(R14) MOVE FCB LINE INDEX    @G38ESBB
         LH    R14,0(,R14)         GET SENSED PAGE ID          @G38ESBB
         N     R14,PCLRHALF        CLEAR LEFT HALFWORD         @G38ESBB
         LA    R14,1(,R14)         COMPENSATE FOR HARDWARE     @G38ESBB
         STH   R14,PQERPGID        SET REPO PAGE ID IN PQE     @G38ESBB
         STH   R14,PQECPGID        CHANNEL ID = REPOSITION ID  @G38ESBB
         SPACE 1                                               @G38ESBB
         CLI   PQETYPE,PQEJ        IS PQE FOR JOB START...     @G38ESBB
         BNE   PCMPCNT             NO, GO DECREMENT ID COUNT   @G38ESBB
         L     R14,PQHPQEJ         GET PQEJ QUEUE HEAD         @G38ESBB
         LTR   R14,R14             IS QUEUE EMPTY...           @G38ESBB
         BNZ   PCMPSCHQ            NO, GO SEARCH FOR QUEUE END @G38ESBB
         ST    R14,PQEJNEXT        SET NEW PQEJ'S PTR TO 0     @G38ESBB
         ST    R1,PQHPQEJ          NEW PQEJ IS ONLY IN QUEUE   @G38ESBB
         B     PCMPCNT             GO DECREMENT ID COUNT       @G38ESBB
         SPACE 1                                               @G38ESBB
PCMPSCHQ L     R15,PQEJNEXT-PQEDSECT(,R14) GET NEXT PQEJ IN Q  @G38ESBB
         LTR   R15,R15             END OF QUEUE REACHED...     @G38ESBB
         BZ    PCMPQEND            YES, BRANCH                 @G38ESBB
         LR    R14,R15             NO, GET NEXT CKPE           @G38ESBB
         B     PCMPSCHQ            LOOP BACK                   @G38ESBB
         SPACE 1                                               @G38ESBB
PCMPQEND ST    R15,PQEJNEXT        SET NEW PQEJ'S PTR TO 0     @G38ESBB
         ST    R1,PQEJNEXT-PQEDSECT(,R14) ADD NEW PQEJ TO Q    @G38ESBB
         SPACE 1                                               @G38ESBB
PCMPCNT  BCTR  R0,0                DECREMENT COUNT OF ID'S     @G38ESBB
         SPACE 1                                               @G38ESBB
PCMPCNT1 L     R1,PQENEXT          GET NEXT PQE                @G38ESBB
         B     PCMPCE              LP BACK TO PROCESS NEXT ID  @G38ESBB
         EJECT                                                 @G38ESBB
***************************************************************@G38ESBB
*                                                              @G38ESBB
*        PQE'S COMPLETED FOR THIS CCW AREA                     @G38ESBB
*                                                              @G38ESBB
***************************************************************@G38ESBB
         SPACE 1                                               @G38ESBB
PCMPEND  L     R15,POUTIOB         RESTORE IOB ADR             @G38ESBB
         L     R14,PPBPCIE-BUFDSECT(,R15) RESTORE PCIE ADDRESS @G38ESBB
         ST    R1,PQHPIDE          SET OLDEST NON-COMP PQE ADR @G38ESBB
         TM    PQHFLAG,PQHCHCMD    TEST FOR SYNC CMD           @OZ51011
         BO    PCMPRTN             BRANCH IF YES               @OZ51011
         USING BFWDSECT,R15        PROVIDE BFW ADDRESSABILITY  @OZ48003
         LA    R15,PCIESIZE(,R14)  ADDRESS BFW                 @OZ48003
         STC   R0,BFWPQECT         UPDATE PQE CNT IN CCW AREA  @OZ48003
         SLR   R1,R1               CLEAR R1                    @OZ48003
         ICM   R1,3,BFWXPGID       SET ORIGIN PAGE TO POINT    @OZ48003
         LA    R1,1(,R1)            TO TOP OF NEW PAGE         @OZ48003
         STCM  R1,3,PQHOPG           AT TRANSFER STATION       @OZ48003
         ICM   R1,3,BFWCPGID       SET CHANNEL                 @OZ49145
         LA    R1,1(,R1)            PAGE ID                    @OZ49145
         STCM  R1,3,PQHCPG           FOR PPQMGR                @OZ49145
         NI    PQHAFLAG,FF-PQHPBUF0 ASSUME BUFFER NOT EMPTY    @OZ48003
         CLC   BFWXPGID,BFWCPGID   CHECK IF                    @OZ48003
         BNE   PCKJQUE              3800 PAGE                  @OZ48003
         CLC   BFWFCBLN,PFCBTOP      BUFFER IS                 @OZ48003
         BNE   PCKJQUE                EMPTY                    @OZ48003
         OI    PQHAFLAG,PQHPBUF0   IND PAGE BUFFER IS EMPTY    @OZ48003
         DROP  R15                 SUSPEND BFW ADDRESSABILITY  @OZ48003
         SPACE 1                                               @OZ48003
***************************************************************@G38ESBB
*                                                              @G38ESBB
*        PROCESS JOB ON DEVICE MESSAGE IF NEEDED               @G38ESBB
*                                                              @G38ESBB
***************************************************************@G38ESBB
         SPACE 1                                               @G38ESBB
PCKJQUE  L     R1,PQHPQEJ          GET FIRST PQEJ IN PQEJ Q    @OZ48003
         SPACE 1                                               @G38ESBB
PJSLOCX  LTR   R1,R1               ARE ANY JOB STARTS PENDING  @G38ESBB
         BZ    PCALLMGR            NO, GO PROCESS STACKER ID'S @G38ESBB
         LH    R0,BFWXPGID-BFWDSECT+PCIESIZE(,R14) DETERMINE   @G38ESBB
         SH    R0,PQECPGID         IF THE JOB                  @G38ESBB
         N     R0,PCLRHALF          HAS CROSSED THE            @G38ESBB
         C     R0,PQELIMIT           TRANSFER STATION          @G38ESBB
         BH    PCALLMGR            NO, GO CALL PPQMGR          @G38ESBB
         ST    JCT,PQHSAVE2        SAVE JCT REGISTER           @G38ESBB
         L     R1,PQEJWJOE         WORK JOE ADR FOR PJODMSG    @G38ESBB
         LH    JCT,JOEJQE-JOEDSECT(,R1) GET JQE OFFSET         @G38ESBB
         N     JCT,PCLRHALF        CLEAR LEFT HALFWORD         @G38ESBB
         SLL   JCT,2               EXPAND TO BYTE OFFSET       @G38ESBB
         AL    JCT,$JOBQPTR        ADD JOB QUEUE ORIGIN        @G38ESBB
         ST    JCT,PQHXJQE         SET JQE OF XFER STATION JOB @G38ESBB
         LR    R0,R1               ADR WORK JOE FOR PJODMSG    @G38ESBB
         L     R15,=A(PJODMSG)     CALL SUBROUTINE TO          @G38ESBB
         BALR  PL,R15               WRITE JOB ON DEVICE MSG    @G38ESBB
         L     JCT,PQHSAVE2        RESTORE JCT REGISTER        @G38ESBB
         L     R1,PQHPQEJ          RESTORE PQEJ ADDRESS        @G38ESBB
         L     R4,PQEJNEXT         GET NEXT JOB                @OZ48003
         ST    R14,PQHSAVE2        SAVE PCIE ADDRESS           @G38ESBB
         LA    R0,1                DELETE ONE PQE              @G38ESBB
         L     R15,=A(PDELPQE)     CALL PDELPQE TO             @OZ48003
         BALR  PL,R15               DELETE PQEJ                @OZ48003
         L     R14,PQHSAVE2        RESTORE PCIE ADDRESS        @G38ESBB
         LR    R1,R4               ADDRESS NEXT PQEJ           @OZ48003
         ST    R1,PQHPQEJ          UPDATE PQEJ QUEUE           @G38ESBB
         B     PJSLOCX             LOOP BACK FOR NEXT JOB      @G38ESBB
         SPACE 1                                               @G38ESBB
PCALLMGR LH    R0,BFWSPGID-BFWDSECT+PCIESIZE(,R14) STACKER ID  @G38ESBB
         L     R15,=A(PPQMGR)      CALL PPQ MANAGEMENT         @G38ESBB
         BALR  PL,R15               ROUTINE, R0 CONTAINS ID    @G38ESBB
         SPACE 1                                               @OZ51011
PCMPRTN  PRETURN ,                 RESTORE REGS AND RETURN     @OZ51011
         DROP  PW,R1               DROP PQH,PQE ADR            @G38ESBB
 TITLE 'HASP PRINT/PUNCH SERVICE -- 3800 MICROCODE PATH SUBROUTINE'
***********************************************************************
*                                                                     *
*        FOR 3800 PRINTER RUNNING UNDER CONTROL OF A 3081D            *
*                                                                     *
*        THIS SUBROUTINE ISSUES AN EXECUTE ORDER CCW THAT INVOKES     *
*        THE INTERRUPTABLE WCS DATA-TRANSFER MICROCODE PATH OR        *
*        THE NON-INTERRUPTABLE ROS DATA-TRANSFER MICROCODE PATH.      *
*        THIS ENSURES THAT THE 3800 WILL NOT DROP READY DURING        *
*        3081D CACHE INTERLOCK RECOVERY.                              *
*                                                                     *
*        REG       ENTRY                  EXIT                        *
*                                                                     *
*        R13       PCE ADDRESS            UNCHANGED                   *
*        R14       RETURN ADDRESS         UNCHANGED                   *
*        R15       ROUTINE ADDRESS        UNCHANGED                   *
*                                                                     *
*        NOTE: INVOKE ORDER STATUS CODES ARE DEFINED IN $BFW.         *
*                                                                     *
***********************************************************************
         SPACE 1                                               @OZ61672
         USING PPSETMC,BASE2       SET LOCAL ADDRESSABILITY    @OZ61672
         SPACE 1                                               @OZ61672
PPSETMC  PSAVE ALL                 SAVE CALLERS REGISTERS      @OZ61672
         LR    BASE2,R15           SET LOCAL BASE REGISTER     @OZ61672
         L     R15,=A(PALLOC)      CALL SUBROUTINE TO          @OZ61672
         BALR  PL,R15               ALLOCATE RESOURCES         @OZ61672
         L     PBUF,PBUFADDR       GET BUFFER ADDRESS          @OZ73371
         L     R1,PSAVAREA         ADDRESS OUR SAVE AREA       @OZ73371
         ST    PBUF,4+(PBUF*4)(,R1) UPDATE PBUF IN SAVE AREA   @OZ73371
         STIDP PCCWORK             GET CPU ID INFORMATION      @OZ61672
         LA    R0,BFWROS           ASSUME ROS PATH             @OZ61672
         CLC   PCCWORK+4(2),=XL2'3081' TEST FOR 3081           @OZ61672
         BNE   PPSETMD             BRANCH IF NOT               @OZ61672
         CLI   PCCWORK,X'03'       TEST FOR MODEL D            @OZ61672
         BNE   PPSETMD             BRANCH IF NOT               @OZ61672
         LA    R0,BFWWCS           INDICATE WCS PATH           @OZ61672
         OI    PPFLAG4,PP3081D     SET 3081D MODE              @OZ61672
         EJECT                                                 @OZ61672
PPSETMD  LM    PC1,PC2,PCCWXORD    PICK-UP EXECUTE ORDER CCW   @OZ61672
         IC    PC2,=AL1(L'BFWINV)  SET ORDER LENGTH            @OZ61672
         LH    R1,PCCWLAST         GET PCIE OFFSET             @OZ61672
         AL    R1,POUTCCWN         ADD CCW AREA BASE           @OZ61672
         LA    R1,PCIESIZE(,R1)    POINT TO BUFFER WORK AREA   @OZ61672
         STC   R0,BFWINVSC-BFWDSECT(,R1)  SET STATUS CODE      @OZ61672
         LA    R1,BFWINV-BFWDSECT(,R1)  PNT TO INVOKE MC ORDER @OZ61672
         ALR   PC1,R1              PUT VIRT ADDR IN CCW        @OZ61672
         MVC   PWKJOE,$ZEROS       SHOW ZERO WORK JOE FOR I/O  @OZ61672
         LA    JCT,1               SET NON-ZERO JCT ADDR       @OZ61672
         BAL   PL,PPPUT2           SETUP CCW                   @OZ61672
         BAL   PL,PPWRITE          ISSUE I/O                   @OZ61672
         BAL   PL,PPCHECK          CHECK FOR I/O COMPLETE      @OZ61672
         L     R15,=A(PDEALLOC)    CALL SUBROUTINE TO          @OZ61672
         BALR  PL,R15               DE-ALLOCATE RESOURCES      @OZ61672
         SPACE 1                                               @OZ61672
         OI    PPFLAG4,PP38PSI     INDICATE PATH SET           @OZ61672
         PRETURN ,                 RETURN TO CALLER            @OZ61672
         SPACE 1                                               @OZ61672
         DROP  BASE2               KILL LOCAL ADDRESSABILITY   @OZ61672
         EJECT                                                 @OZ61672
***********************************************************************
*                                                                     *
*        RESET 3800 DATA TRANSFER PATH MICROCODE                      *
*                                                                     *
***********************************************************************
         SPACE 1                                               @OZ61672
         USING PPRSTMC,BASE2       SET LOCAL ADDRESSABILITY    @OZ61672
         SPACE 1                                               @OZ61672
PPRSTMC  PSAVE ALL                 SAVE CALLERS REGISTERS      @OZ61672
         LR    BASE2,R15           SET LOCAL BASE              @OZ61672
         NI    PPFLAG4,FF-PP38PSI  RESET PATH INDICATOR FLAG   @OZ61672
         TM    PPFLAG4,PP3081D     WCS PATH INVOKED...         @OZ61672
         BZ    PPRSRET             BRANCH IF NOT               @OZ61672
         NI    PPFLAG4,FF-PP3081D  RESET WCS PATH INDICATOR    @OZ61672
         LM    PC1,PC2,PCCWXORD    PICK-UP EXECUTE ORDER CCW   @OZ61672
         IC    PC2,=AL1(L'BFWINV)  SET ORDER LENGTH            @OZ61672
         LH    R1,PCCWLAST         GET PCIE OFFSET             @OZ61672
         AL    R1,POUTCCWN         ADD CCW AREA BASE           @OZ61672
         LA    R1,PCIESIZE(,R1)    POINT TO BUFFER WORK AREA   @OZ61672
         MVI   BFWINVSC-BFWDSECT(R1),BFWROS  SET ROS PATH      @OZ61672
         LA    R1,BFWINV-BFWDSECT(,R1)  PNT TO INVOKE MC ORDER @OZ61672
         ALR   PC1,R1              PUT VIRT ADDR IN CCW        @OZ61672
         MVC   PWKJOE,$ZEROS       SHOW ZERO WORK JOE FOR I/O  @OZ61672
         LA    JCT,1               SET NON-ZERO JCT ADDR       @OZ61672
         BAL   PL,PPPUT2           SETUP CCW                   @OZ61672
         BAL   PL,PPWRITE          ISSUE I/O                   @OZ61672
         BAL   PL,PPCHECK          CHECK FOR I/O COMPLETE      @OZ61672
         SPACE 1                                               @OZ61672
PPRSRET  PRETURN ,                 RETURN TO CALLER            @OZ61672
         SPACE 1                                               @OZ61672
         DROP  BASE2               KILL LOCAL ADDRESSABILITY   @OZ61672
         TITLE 'HASP PRINT PUNCH SERVICE -- IMAGE LOADER TASK' @G38ESBB
***************************************************************@G38ESBB
*                                                              @G38ESBB
*        HASP IMAGE LOADER SUBTASK                             @G38ESBB
*                                                              @G38ESBB
***************************************************************@G38ESBB
         SPACE 1                                               @G38ESBB
HASPIMAG $ENTRY BASE=R15           IMAGE LOADER MAIN ENTRY     @G38ESBB
         DROP  R15                 SUSPEND LOCAL ADR           @G38ESBB
         SAVE  (14,12)             SAVE CALLER'S REGISTERS     @G38ESBB
         LR    BASE2,R15           ESTABLISH BASE REGISTER     @G38ESBB
         USING HASPIMAG,BASE2      PROVIDE LOCAL ADR           @G38ESBB
         L     BASE1,=V(HASP)      SET HCT ADDRESS             @G38ESBB
         USING HCTDSECT,BASE1      PROVIDE HCT ADDRESSABILITY  @G38ESBB
         LR    R3,R1               SAVE DTE ADDR PASSED        @G38ESBB
         GETMAIN R,LV=IMGSWLEN     GETMAIN SAVE/WORK AREA      @G38ESBB
         ST    R13,4(,R1)          STORE BACKWARD POINTER      @G38ESBB
         LR    R4,R13              HOLD CALLER'S SAVE ADDRESS  @G38ESBB
         LR    R13,R1              ADDRESS MY SAVE AREA        @G38ESBB
         USING IMGDSECT,R13        PROVIDE SAVE AREA ADR       @G38ESBB
         ST    R13,8(,R4)          STORE FWD POINTER           @G38ESBB
         ST    R3,IMGDTE           STORE DTE ADDRESS           @G38ESBB
         XC    IMGDCB,IMGDCB       CLEAR SYS1.IMAGLIB DCB ADDR @G38ESBB
         SPACE 3                                               @G38ESBB
***************************************************************@G38ESBB
*                                                              @G38ESBB
*        ESTABLISH ESTAE EXIT FOR ABNORMAL TERMINATIONS        @G38ESBB
*                                                              @G38ESBB
***************************************************************@G38ESBB
         SPACE 1                                               @G38ESBB
         LA    R1,12(,SAVE)        ESTAE PARAMETER LIST AREA   @G38ESBB
         MVC   0(IMGABNDL,R1),IMGABND  MOVE-IN ESTAE PARMLIST  @G38ESBB
         ESTAE PARAM=(13),MF=(E,(1))  ESTABLISH IMGESTAE EXIT  @G38ESBB
         EJECT                                                 @G38ESBB
***************************************************************@G38ESBB
*                                                              @G38ESBB
*        OPEN SYS1.IMAGELIB FOR IMAGE LOADING                  @G38ESBB
*                                                              @G38ESBB
***************************************************************@G38ESBB
         SPACE 1                                               @G38ESBB
         LA    R0,$IMAGTCB         LOAD PRINTER DTE . . .      @G38ESBB
         LA    R3,0(R3)            CLEAR HI-ORDER BYTE         @G38ESBB
         CLR   R0,R3               IMPACT PRINTER DTE. . .     @G38ESBB
         BNE   IMGACTIV            BR IF NO                    @G38ESBB
         IMGLIB OPEN               OPEN IMAGE LIBRARY          @G38ESBB
         ST    R1,IMGDCB           SAVE IMAGELIB DCB ADDRESS   @G38ESBB
         SPACE 1                                               @G38ESBB
***************************************************************@G38ESBB
*                                                              @G38ESBB
*        SIGNAL ATTACHER THAT IMAGE LOADER TASK IS ACTIVE      @G38ESBB
*                                                              @G38ESBB
***************************************************************@G38ESBB
         SPACE 1                                               @G38ESBB
IMGACTIV OI    0(R3),X'80'         SHOW HASPIMAG INITIALIZED   @G38ESBB
         SPACE 1                                               @G38ESBB
IMGNACTV LA    R0,$IMAGTCB         LOAD PRINTER DTE            @G38ESBB
         CLR   R0,R3               IS IT IMPACT PRINTER...     @G38ESBB
         BNE   IMG$$P              IF 3800, $$POST             @G38ESBB
         LTR   R1,R1               DID SYS1.IMAGELIB OPEN...   @OZ53418
         BNZ   IMGOPN              BR IF YES,                  @OZ53418
         OI    0(R3),X'40'           ELSE SHOW ERROR IN DTE    @OZ53418
IMGOPN   DS    0H                                              @OZ53418
         POST  $PIMGECB            POST HASPINIT               @G38ESBB
         B     IMGWAIT             BR TO WAIT                  @G38ESBB
IMG$$P $$POST  TYPE=IMAG,R11=HCT   POST ATTACHOR               @G38ESBB
         EJECT                                                 @G38ESBB
***************************************************************@G38ESBB
*                                                              @G38ESBB
*        WAIT FOR WORK POST OR $PHASP POST                     @G38ESBB
*                                                              @G38ESBB
***************************************************************@G38ESBB
         SPACE 1                                               @G38ESBB
IMGWAIT  L     R3,IMGDTE           ADDR OF HASPIMAG DTE        @G38ESBB
         LA    R1,8(,R3)           ADDR OF HASPIMAG WORK-ECB   @G38ESBB
         WAIT  ECB=(1)             WAIT FOR WORK OR $PJES2     @G38ESBB
         TM    0(R3),X'80'         SUBTASK INITIALIZED...      @G38ESBB
         BZ    IMGDOWN             BR IF NO TO SHUTDOWN        @G38ESBB
         LR    R1,R3               SAVE REG 3                  @G38ESBB
         ICM   R3,7,8+1(R3)        IS THERE NEW WORK...        @G38ESBB
         MVI   8(R1),X'00'         RESET WORK ECB              @G38ESBB
         BNZ   IMGBLDL             BR IF NEW WORK,ELSE SHUTDWN @G38ESBB
         SPACE 1                                               @G38ESBB
***************************************************************@G38ESBB
*                                                              @G38ESBB
*        SHUTDOWN ASSUMED - CLOSE SYS1.IMAGELIB                @G38ESBB
*                                                              @G38ESBB
***************************************************************@G38ESBB
         SPACE 1                                               @G38ESBB
IMGDOWN  L     R1,IMGDCB           GET IMAGELIB DCB ADDR       @G38ESBB
         LTR   R1,R1               WAS IMAGELIB OPENED...      @G38ESBB
         BZ    IMGEXIT             BR IF NO                    @G38ESBB
         IMGLIB CLOSE,(1)          CLOSE IMAGELIB              @G38ESBB
         SPACE 1                                               @G38ESBB
IMGEXIT  L     R13,IMGSAV+4        RESTORE CALLER'S R13        @G38ESBB
         RETURN (14,12),RC=0       RETURN WITH ZERO COMP CODE  @G38ESBB
         EJECT                                                 @G38ESBB
***************************************************************@G38ESBB
*                                                              @G38ESBB
*        BLDL AGAINST SYS1.IMAGELIB TO PROVIDE PARM LIST FOR   @G38ESBB
*        LOAD                                                  @G38ESBB
*                                                              @G38ESBB
***************************************************************@G38ESBB
         SPACE 1                                               @G38ESBB
IMGBLDL  DS    0H                  *                           @G38ESBB
         USING BUFDSECT,R3         ACTIVATE BUFFER ADR         @G38ESBB
         SPACE 1                                               @G38ESBB
         ST    R3,IMGBUFAD         SAVE BUFFER ADR FOR ESTAE   @G38ESBB
         XC    IMGNAME(8),IMGNAME  CLEAR IMAGENAME             @OZ59193
         NI    IMGFLAG1,FF-IMGESDEL-IMGABEND INIT FLAGS OFF    @OZ59193
IMGBLDL2 LA    R0,$IMAGTCB         LOAD PRINTER DTE            @G38ESBB
         CL    R0,IMGDTE           IMAGE LOAD FUNCTION...      @G38ESBB
         BNE   IMGSTPRT            BR IF FOR 3800 SETUP        @G38ESBB
         L     R1,IMGDCB           ADDR IMAGELIB DCB FOR BLDL  @G38ESBB
         BLDL  (R1),BUFSTART       CONSTRUCT LIST FOR LOAD     @G38ESBB
         SPACE 1                                               @G38ESBB
***************************************************************@G38ESBB
*                                                              @G38ESBB
*        POST BUFFER WITH ERROR IF BLDL FAILED                 @G38ESBB
*                                                              @G38ESBB
***************************************************************@G38ESBB
         SPACE 1                                               @G38ESBB
         LTR   R15,R15             WAS BLDL SUCCESSFUL         @G38ESBB
         BZ    IMGLOAD             BRANCH IF YES               @G38ESBB
         MVI   BUFECBCC,X'41'      POST BUFFER WITH PERM ERROR @G38ESBB
         B     IMGRETN             GO TO POST PRPU             @G38ESBB
         SPACE 1                                               @G38ESBB
***************************************************************@G38ESBB
*                                                              @G38ESBB
*        LOAD IMAGE FROM IMAGELIB USING BLDL LIST              @G38ESBB
*                                                              @G38ESBB
***************************************************************@G38ESBB
         SPACE 1                                               @G38ESBB
IMGLOAD  DS    0H                  *                           @G38ESBB
         LM    R6,R7,BUFSTART+4    SAVE NAME OF LOADED IMAGE   @G38ESBB
*              THIS LINE DELETED BY APAR OZ59193               @OZ59193
         LA    R0,BUFSTART+4       ADDRESS DESCRIPTOR LIST     @G38ESBB
         L     R1,IMGDCB           ADDRESS DCB FOR IMAGELIB    @G38ESBB
         LOAD  DE=(0),DCB=(1)      LOAD REQUESTED IMAGE        @G38ESBB
         STM   R6,R7,IMGNAME       MODULE TO BE DELETED        @OZ59193
         LR    R2,R0               COPY IMAGE BASE             @G38ESBB
         CLI   IMGNAME,C'F'        IS THIS AN FCB IMAGE        @G38ESBB
         BNE   IMGUCS              BRANCH IF NO                @G38ESBB
         EJECT                                                 @G38ESBB
***************************************************************@G38ESBB
*                                                              @G38ESBB
*        COPY FCB IMAGE FROM LOAD MODULE TO CALLER'S BUFFER    @G38ESBB
*                                                              @G38ESBB
***************************************************************@G38ESBB
         SPACE 1                                               @G38ESBB
IMGFCB   CLC   IMGNAME(4),=C'FCB3' 3800 FCB IMAGE...           @G38ESBB
         BE    IMGF3800            BR IF YES                   @G38ESBB
         MVC   BUFSTART(2),0(R2)   MOVE FLAG BYTE AND LENGTH   @G38ESBB
         LA    R4,BUFSTART+2       START OF IMAGE IN BUFFER    @G38ESBB
         SLR   R5,R5               CLEAR REGISTER              @G38ESBB
         IC    R5,1(,R2)           GET IMAGE LENGTH            @G38ESBB
         LA    R6,2(,R2)           START OF IMAGE IN LOAD MOD  @G38ESBB
         LR    R7,R5               COPY IMAGE LENGTH           @G38ESBB
         TM    2(R2),X'C0'         FIRST BYTE DEFINE INDEX...  @G38ESBB
         BNZ   IMGFCBI             BRANCH IF YES               @G38ESBB
         SPACE 1                                               @G38ESBB
***************************************************************@G38ESBB
*                                                              @G38ESBB
*        NO INDEX BYTE SUPPLIED - DEFAULT TO X'81'             @G38ESBB
*                                                              @G38ESBB
***************************************************************@G38ESBB
         SPACE 1                                               @G38ESBB
         MVI   BUFSTART+2,X'81'    SET INDEX VALUE OF X'81'    @G38ESBB
         LA    R4,1(,R4)           INCREMENT BUFFER START ADR  @G38ESBB
         LA    R5,1(,R5)           GET NEW IMAGE LENGTH        @G38ESBB
         STC   R5,BUFSTART+1       STORE INTO BUFFER           @G38ESBB
IMGFCBI  DS    0H                  *                           @G38ESBB
         MVCL  R4,R6               MOVE IMAGE TO BUFFER        @G38ESBB
         B     IMGDELET            GO DELETE LOADED MODULE     @G38ESBB
         SPACE 1                                               @G38ESBB
***************************************************************@G38ESBB
*                                                              @G38ESBB
*        COPY 3800 FCB IMAGE TO CALLER'S BUFFER FOR            @G38ESBB
*        MAPPING PURPOSES                                      @G38ESBB
*                                                              @G38ESBB
***************************************************************@G38ESBB
         SPACE 1                                               @G38ESBB
         USING PFCB,R7             FCB BUFFER ADDRESSABILITY   @G38ESBB
         SPACE 1                                               @G38ESBB
IMGF3800 LA    R7,BUFSTART         GET FCB BUFFER ADDRESS      @G38ESBB
         LH    R5,6(,R2)           GET LENGTH OF FCB           @G38ESBB
         LA    R4,7(R5,R2)         POINT TO LAST BYTE OF FCB   @G38ESBB
         LA    R6,1                INDICATE TO SCAN BACKWARDS  @G38ESBB
         BAL   R14,IMGSTRIP        STRIP OFF BOTTOM 1/2 INCH   @G38ESBB
         LA    R4,8(,R2)           POINT TO 1ST USEABLE BYTE   @G38ESBB
         LCR   R6,R6               INDICATE TO SCAN FORWARD    @G38ESBB
         BAL   R14,IMGSTRIP        STRIP OFF TOP 1/2 INCH      @G38ESBB
         STH   R5,PFCBLENG         SAVE USEABLE FCB LENGTH     @G38ESBB
         LA    R1,PFCBSTRT         POINT TO USER BUFFER        @G38ESBB
         SPACE 1                                               @G38ESBB
IMGMOVE  MVC   0(1,R1),0(R4)       MOVE FCB BYTE TO BUFFER     @G38ESBB
         NI    0(R1),PSAVBITS      ISOLATE CHANNEL ID          @G38ESBB
         LA    R1,1(,R1)           POINT TO NEXT BYTE IN BUF   @G38ESBB
         LA    R4,1(,R4)           POINT TO NEXT BYTE IN FCB   @G38ESBB
         BCT   R5,IMGMOVE          CONTINUE TO MOVE FCB        @G38ESBB
         B     IMGDELET            GO DELETE LOAD MODULE       @G38ESBB
         SPACE 1                                               @G38ESBB
***************************************************************@G38ESBB
*                                                              @G38ESBB
*        COPY UCS IMAGE FROM LOAD MODULE TO CALLER'S BUFFER    @G38ESBB
*                                                              @G38ESBB
***************************************************************@G38ESBB
         SPACE 1                                               @G38ESBB
IMGUCS   DS    0H                  *                           @G38ESBB
IMGL3203 EQU   304                 LENGTH OF 3203 IMAGE        @G38ESBB
IMGL3211 EQU   512                 LENGTH OF 3211 IMAGE        @G38ESBB
         MVC   BUFSTART(1),0(R2)   MOVE FLAG BYTE              @G38ESBB
         LA    R4,BUFSTART+1       START OF IMAGE IN BUFFER    @G38ESBB
         LA    R5,IMGL3203         SET TO 3203 IMAGE LEN       @GZ40627
         CLI   IMGBYT3,C'3'        TEST FOR 3203 IMAGE         @G38ESBB
         BE    IMGUCS1             BRANCH IF YES               @G38ESBB
         LA    R5,240              SET 1403 IMAGE LENGTH       @G38ESBB
         CLI   IMGBYT3,C'1'        IS IT SET CORRECTLY         @G38ESBB
         BE    IMGUCS1             YES SET OK                  @G38ESBB
         LA    R5,IMGL3211         SET FOR 3211                @G38ESBB
*                                                              @G38ESBB
*        JES2 ONLY INITS 3 TYPES OF IMPACT PRTS GO ON AS 3211  @G38ESBB
*                                                              @G38ESBB
IMGUCS1  SLR   R6,R6               CLEAR REGISTER              @G38ESBB
         IC    R6,1(,R2)           NUMBER OF VERIFY LINES      @G38ESBB
         LA    R6,2(R6,R2)         START OF IMAGE IN LOAD MOD  @G38ESBB
         LR    R7,R5               COPY MODULE LENGTH          @G38ESBB
         MVCL  R4,R6               COPY IMAGE TO BUFFER        @G38ESBB
         EJECT                                                 @G38ESBB
***************************************************************@G38ESBB
*                                                              @G38ESBB
*        DELETE THE LOADED IMAGE MODULE                        @G38ESBB
*                                                              @G38ESBB
***************************************************************@G38ESBB
         SPACE 1                                               @G38ESBB
IMGDELET DS    0H                  *                           @G38ESBB
         DELETE EPLOC=IMGNAME      REMOVE MOD FORM LOAD LIST   @G38ESBB
         XC    IMGNAME(8),IMGNAME  CLEAR IMAGE NAME            @OZ59193
         MVI   BUFECBCC,X'7F'      POST BUFFER WITH GOOD CC    @G38ESBB
IMGRETN  DS    0H                                              @G38ESBB
       $$POST  TYPE=IMAG,R11=HCT   POST HASP PRINT PROCESSOR   @G38ESBB
         B     IMGWAIT             GO WAIT FOR WORK OR DRAIN   @G38ESBB
         SPACE 1                                               @G38ESBB
***************************************************************@G38ESBB
*                                                              @G38ESBB
*        INTERFACE TO SETPRT (SVC 81) FOR 3800 PRINTERS        @G38ESBB
*                                                              @G38ESBB
***************************************************************@G38ESBB
         SPACE 1                                               @G38ESBB
IMGSTPRT DS    0H                                              @G38ESBB
         USING SPPARM-(BUFSTART-BUFDSECT),PBUF SPPARM ADR      @G38ESBB
         MVI   SPWRSCDE,0          ASSUME UNKNOWN              @OZ46887
         MVI   SPWRTCDE,4           IMAGE NOT FOUND            @OZ46887
         SETPRT MF=(E,SPPARM)      INVOKE SETPRT SVC           @G38ESBB
         SLR   R6,R6               CLEAR REG FOR INSERT        @G38ESBB
*              THIS LINE DELETED BY APAR NUMBER                @OZ46887
         ICM   R6,8,=X'7F'         ASSUME NO ERROR             @G38ESBB
         LTR   R15,R15             NORMAL RETURN               @G38ESBB
         BZ    IMGRETNJ            BRANCH IF YES               @G38ESBB
         ICM   R6,8,=X'41'         SET ERROR POST CODE         @G38ESBB
         SPACE 1                                               @G38ESBB
*        FIND LEFT-MOST NON-ZERO BYTE AS THE RETURN CODE       @G38ESBB
         SPACE 1                                               @G38ESBB
IMGSPTRC STCM  R15,1,SPWRTCDE      SAVE RIGHT-MOST BYTE        @G38ESBB
         SRL   R15,8               SHIFT OUT SAVED BYTE        @G38ESBB
         LTR   R15,R15             ALL ZEROES LEFT             @G38ESBB
         BNZ   IMGSPTRC            IF NOT, REPEAT PROCESS      @G38ESBB
         STC   R0,SPWRSCDE         SAVE REASON CODE            @G38ESBB
         SPACE 1                                               @G38ESBB
IMGRETNJ DS    0H                                              @G38ESBB
         STCM  R6,8,BUFECBCC-BUFDSECT(R3)   SET POST CODE      @G38ESBB
         B     IMGRETN                      GO POST PRINT PROC @G38ESBB
 TITLE 'HASP PRINT/PUNCH SERVICE -- 3800 FCB LOAD'             @G38ESBB
***************************************************************@G38ESBB
*                                                              @G38ESBB
*        ROUTINE TO SCAN OFF A HALF INCH OF THE FCB IMAGE      @G38ESBB
*                                                              @G38ESBB
***************************************************************@G38ESBB
         SPACE 1                                               @G38ESBB
IMGSTRIP LA    R0,12               GET 24THS IN HALF INCH      @G38ESBB
         SPACE 1                                               @G38ESBB
IMGSTRP2 IC    R1,0(,R4)           GET AN FCB CODE BYTE        @G38ESBB
         SLL   R1,26               ELIMINATE ALL BUT           @G38ESBB
         SRL   R1,30                    LPI CODE               @G38ESBB
         SLL   R1,1                DOUBLE THE VALUE FOR INDEX  @G38ESBB
         SR    R4,R6               POINT TO NEXT BYTE TO SCAN  @G38ESBB
         BCTR  R5,0                DECREMENT FCB LENGTH        @G38ESBB
         SH    R0,LPITABLE(R1)     DECR AMT LEFT IN 1/2 INCH   @G38ESBB
         BP    IMGSTRP2            LOOP IN HALF INCH NOT DONE  @G38ESBB
         BR    R14                 RETURN                      @G38ESBB
         SPACE 1                                               @G38ESBB
***************************************************************@G38ESBB
*                                                              @G38ESBB
*        THE FOLLOWING TABL IS TO CONVERT THE TWO-BIT LPI CODE @G38ESBB
*        (LINES-PER-INCH) TO THE NUMBER OF TWENTY-FOURTHS      @G38ESBB
*        OF AN INCH THAT THE LINE WILL TAKE.                   @G38ESBB
*                                                              @G38ESBB
***************************************************************@G38ESBB
         SPACE 1                                               @G38ESBB
LPITABLE DC    Y(4,3,4,2)          00=6, 01=8, 10=6, 11=12     @G38ESBB
 TITLE 'HASP PRINT/PUNCH SERVICE -- ESTAE EXIT ROUTINE'        @G38ESBB
***************************************************************@G38ESBB
*                                                              @G38ESBB
*        ESTAE EXIT ROUTINE -- REINSTATE SUB-TASK              @G38ESBB
*                                                              @G38ESBB
***************************************************************@G38ESBB
         SPACE 1                                               @G38ESBB
         USING IMGESTAE,R15        PROVIDE LOCAL ADR           @G38ESBB
         USING SDWA,R1             PROVIDE SDWA ADDRESSABILITY @G38ESBB
         SPACE 1                                               @G38ESBB
IMGESTAE LR    R3,R1               ASSUME NO SDWA,GET CMP CODE @G38ESBB
         LA    R4,12               TEST FOR                    @G38ESBB
         CLR   R0,R4                SDWA PRESENT               @G38ESBB
         BE    SKIP760             BR IF NONE -- INFO IN REGS  @G38ESBB
         L     R3,SDWAABCC         OBTAIN COMPLETION CODE      @G38ESBB
         L     R2,SDWAPARM         HASPIMAG SAVE AREA ADDRESS  @G38ESBB
SKIP760  ST    R3,IMGABCC-IMGDSECT(,R2)  SAVE CC FOR RETRY     @G38ESBB
         CLR   R0,R4               WAS AN SDWA PROVIDED...     @G38ESBB
         BE    IMGESTA1            BR IF NO                    @G38ESBB
         SETRP RC=4,RETADDR=IMGRETRY,FRESDWA=YES  FOR RETRY    @G38ESBB
         BR    R14                 SCHEDULE RETRY ROUTINE      @G38ESBB
         SPACE 1                                               @G38ESBB
IMGESTA1 LA    R15,4               INDICATE RETRY ROUTINE PROC @G38ESBB
         BALR  R0,R14              RETURN,R0=RETRY ROUTINE ADR @G38ESBB
         EJECT                                                 @G38ESBB
***************************************************************@G38ESBB
*                                                              @G38ESBB
*        ESTAE RETRY ROUTINE -- FOR IMAGE LIBRARY LOADERS,     @G38ESBB
*        CLOSE AND RE-OPEN SYS1.IMAGELIB, AND RETRY FAILING    @G38ESBB
*        LOAD IN CASE OF EXTENDED SYS1.IMAGELIB                @G38ESBB
*        OTHERWISE, INFORM OPERATOR OF ABEND                   @G38ESBB
*        RE-ESTABLISH ENVIRONMENT (SAVE, BASE1, BASE2, R3)     @G38ESBB
*                                                              @G38ESBB
***************************************************************@G38ESBB
         SPACE 1                                               @G38ESBB
         USING IMGRETRY,R15        PROVIDE LOCAL ADR           @G38ESBB
         USING BUFDSECT,R3         PROVIDE BUFFER ADR          @G38ESBB
         SPACE 1                                               @G38ESBB
IMGRETRY LR    SAVE,R1             HASPIMAG SAVE AREA ADDR     @G38ESBB
         L     BASE1,=V(HASP)      RESTORE BASE1               @G38ESBB
         L     BASE2,=A(HASPIMAG)  RESTORE BASE2               @G38ESBB
         DROP  R15                 PICK-UP ADR ON BASE2        @G38ESBB
         L     R15,IMGDTE          GET DTE ADDR                @G38ESBB
         USING DTEDSECT,R15                                    @G38ESBB
         ICM   R15,7,DTEWECB+1     ARE WE POSTED TO END...     @G38ESBB
         BZ    IMGDET              YES, DON'T ISSUE MESSAGE    @G38ESBB
         DROP  R15                                             @G38ESBB
         L     R3,IMGBUFAD         RESTORE BUFFER ADDRESS      @G38ESBB
         LA    R1,$IMAGTCB         ARE WE AN IMAGE             @G38ESBB
         CL    R1,IMGDTE            LIBRARY LOADER...          @G38ESBB
         BNE   IMGWMSG               NO, BRANCH                @G38ESBB
         SPACE 1                                               @OZ59193
         OC    IMGNAME(8),IMGNAME  TEST FOR IMAGE NAME         @OZ59193
         BZ    IMGNONM             BR IF NO NAME TO DELETE     @OZ59193
         TM    IMGFLAG1,IMGESDEL   HAS DELETE FAILED BEFORE    @OZ59193
         BO    IMGNONM              YES,BYPASS DELETE          @OZ59193
         OI    IMGFLAG1,IMGESDEL   INDIC SVC DELETE ENTERED    @OZ59193
         DELETE EPLOC=IMGNAME      DELETE IMAGE                @OZ59193
         NI    IMGFLAG1,FF-IMGESDEL INDIC RTRN FROM DELETE     @OZ59193
         XC    IMGNAME(8),IMGNAME  CLEAR IMAGENAME             @OZ59193
         SPACE 1                                               @OZ59193
IMGNONM  TM    IMGFLAG1,IMGABEND   IS THIS SECOND ABEND ...    @OZ59193
         BO    IMGWMSG             YES, GO WRITE MESSAGE       @G38ESBB
         SPACE 1                                               @G38ESBB
         OI    IMGFLAG1,IMGABEND   INDICATE FIRST ABEND        @G38ESBB
         L     R1,IMGDCB           GET IMGLIB DCB ADDRESS      @G38ESBB
         LTR   R1,R1               WAS IMAGELIB OPENED...      @G38ESBB
         BZ    IMGWMSG             NO, GO WRITE MESSAGE        @G38ESBB
         SPACE 1                                               @G38ESBB
         IMGLIB CLOSE,(1)          CLOSE IMAGELIB              @G38ESBB
         MVC   IMGDCB,$ZEROS       CLEAR DCB ADDRESS           @G38ESBB
         SPACE 1                                               @G38ESBB
         IMGLIB OPEN               RE-OPEN IMAGELIB            @G38ESBB
         SPACE 1                                               @G38ESBB
         ST    R1,IMGDCB           SAVE IMAGELIB DCB ADDRESS   @G38ESBB
         B     IMGBLDL2            GO RE-TRY FAILING LOAD      @G38ESBB
         SPACE 1                                               @G38ESBB
IMGWMSG  LA    R1,IMGMSG           POINT TO MESSAGE AREA       @OZ46887
         MVC   0(IMGAMSGL,R1),IMGAMSG  MOVE IN MESSAGE         @OZ46887
         UNPK  IMGAMCC(,R1),IMGABCC+1(2)         INSERT ABEND  @G38ESBB
         OI    IMGAMCC+2(R1),X'F0'                 COMPLETION  @G38ESBB
         TR    IMGAMCC(,R1),IMGTRTAB-X'F0'          CODE       @G38ESBB
         L     R3,IMGDTE           ADDRESS DTE                 @G38ESBB
         TM    0(R3),X'80'         WAS SUBTASK INITIALIZED...  @G38ESBB
         BO    IMGRDCT             YES, BRANCH                 @G38ESBB
         MVC   IMGAMDN(8,R1),=CL8'INITIAL' SET SPECIAL DEVNAME @G38ESBB
       $$WTO   (R1)                WRITE MESSAGE               @G38ESBB
         B     IMGNACTV            BRANCH TO POST ATTACHER     @G38ESBB
         SPACE 1                                               @G38ESBB
IMGRDCT  L     R3,IMGBUFAD         RESTORE BUFFER ADDRESS      @G38ESBB
         L     R2,BUFDCT           ADDRESS DCT                 @G38ESBB
         MVC   IMGAMDN(8,R1),DCTDEVN-DCTDSECT(R2) MOVE DEVNAME @G38ESBB
       $$WTO   (R1)                INFORM OPER OF REINSTATE    @G38ESBB
IMGDET   MVI   BUFECBCC,X'40'      SET ERROR CODE              @G38ESBB
         B     IMGRETN             GO TO POST PRPU             @G38ESBB
         EJECT                                                 @G38ESBB
         SPACE 5                                               @G38ESBB
PIMAGLST ATTACH EP=HASPIMAG,SM=SUPV,SF=L  HASPIMAG ATTACH LIST @G38ESBB
PIMAGLL  EQU   *-PIMAGLST          LENGTH OF ATTACH LIST       @G38ESBB
         EJECT                                                 @G38ESBB
IMGTRTAB DC    C'0123456789ABCDEF' HEX TO EBCDIC TRAN TABLE    @G38ESBB
         SPACE 2                                               @G38ESBB
        $MID   179                 MESSAGE IDENTIFIER          @G38ESBB
IMGAMSG  WTO   '&MID.******** IMAGE LOADER SUB-TASK REINSTATED AFTER ABC
               END (***)',ROUTCDE=(7),DESC=(4),MF=L            @G38ESBB
IMGAMSGL EQU   *-IMGAMSG           LENGTH OF ABOVE MESSAGE     @G38ESBB
IMGAMDN  EQU   IMGAMSGL-4-59,L'DCTDEVN DEVICE NAME INSERT      @G38ESBB
IMGAMCC  EQU   IMGAMSGL-4-4,3      ABEND COMP CODE INSERT      @G38ESBB
         SPACE 3                                               @G38ESBB
IMGABND  ESTAE IMGESTAE,RECORD=YES,MF=L  ESTAE PARAMETER LIST  @G38ESBB
IMGABNDL EQU   *-IMGABND           LENGTH OF ABOVE             @G38ESBB
         SPACE 2                                               @G38ESBB
IMGDSECT DSECT                     SAVE AREA DSECT             @G38ESBB
IMGSAV   DS    18F                 IMAGE LOADER SAVE AREA      @G38ESBB
IMGNAME  DS    2F                  NAME OF LOADED MODULE       @G38ESBB
IMGBYT3  EQU   IMGNAME+3           IMAGE NAME PREFIX BYTE      @G38ESBB
IMGDCB   DS    F                   ADDRESS OF IMAGELIB DCB     @G38ESBB
IMGBUFAD DS    F                   BUFFER ADDRESS FOR ESTAE    @G38ESBB
IMGABCC  DS    F                   ABEND COMP CODE FOR RETRY   @G38ESBB
IMGDTE   DS    F                   ADDRESS OF OUR DTE          @G38ESBB
IMGMSG   DS    CL80                MESSAGE AREA                @G38ESBB
IMGFLAG1 DS    X                   IMAGE LOADER FLAG BYTE      @G38ESBB
IMGABEND EQU   B'10000000'         IMAGE LOADER ABEND FLAG     @G38ESBB
IMGESDEL EQU   B'01000000'         DELETE RTN FLAG IN ESTAE    @OZ59193
IMGSWLEN EQU   *-IMGDSECT          LENGTH OF SAVE/WORK AREA    @G38ESBB
         SPACE 1                                               @G38ESBB
HASPPRPU CSECT                     END OF SAVE AREA DSECT      @G38ESBB
***************************************************************@G38ESBB
*                                                              @G38ESBB
*        FCB FORMAT                                            @G38ESBB
*                                                              @G38ESBB
***************************************************************@G38ESBB
         SPACE 1                                               @G38ESBB
PFCB     DSECT                     FCB MAPPING DSECT           @G38ESBB
PFCBLENG DS    H                   LENGTH OF FCB               @G38ESBB
PFCBFLG1 DS    X                   FLAG BYTE                   @G38ESBB
         SPACE 1                                               @G38ESBB
PFCBNOSP EQU   B'10000000'         WRITE-NO-SPACE INDICATOR    @G38ESBB
         SPACE 1                                               @G38ESBB
PFCBFLG2 DS    X                   FLAG BYTE                   @G38ESBB
PFCBSTRT DS    0F                  START OF FCB BYTES          @G38ESBB
         SPACE 1                                               @G38ESBB
HASPPRPU CSECT                     END OF FCB MAPPING DSECT    @G38ESBB
         LTORG                     IMAGE LOADER LITERAL POOL   @G38ESBB
         SPACE 5                                               @G38ESBB
$DLENGTH $DLENGTH                  COMPUTE CONTROL SECT LENGTH @G38ESBB
APARNUM  DC    CL5'79804'          APAR NUMBER
         END   ,                                               @G38ESBB
