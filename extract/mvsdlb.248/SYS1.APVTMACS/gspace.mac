             MACRO
&NAME        GSPACE &R,&LV=
.*****************************************************************
.*
.* MACRO NAME - GSPACE
.*
.* DESCRIPTIVE NAME -GET SPACE MACRO (THIS IS GETMAIN LIKE)
.*
.* COPYRIGHT - N/A
.*
.* STATUS - OS/VS2 REL. 3.7 PTF                                @ZA28413
.*
.* FUNCTION -
.*
.*   THE FUNCTION OF THIS MACRO IS TO SETUP THE INPUT REGS TO THE
.*   GSPACE SERVICE ROUTINE LOCATED IN MODULE IEFAB4F6.
.*
.*   OPERATION -
.*
.*      THIS MACRO REQUIRES - GSPACE R,LV=(0),A=(1)
.*      1) IF INVALID PARAMETERS PRINT MNOTE.
.*      2) INSERT CODE
.*      3) EXIT
.*
.* NOTES -
.*   DEPENDENCIES -
.*      THIS MACRO IS CALLED BY USING THE PL/S STACK OPTION.
.*      THEREFORE CHANGES TO PL/S MAY IMPACT THIS MACRO
.*      THE ONLY REGISTERS THE MACRO MAY ALTER ARE 0,1,14,15.
.*      A CHANGE TO SVC99'S CSECT NAME MEANS A CHANGE TO GSPACE@YM05157
.*
.*   RESTRICTIONS
.*      NONE.
.*   REGISTER CONVENTIONS -
.*      MAY ONLY USE 14 THRU 1
.*
.*   PATCH LABEL - N/A
.*
.* MODULE TYPE - MACRO
.*
.*   PROCESSER - ASSEM
.*   MODULE SIZE - N/A
.*   ATTRIBUTES - EXECUTABLE MACRO
.*
.* ENTRY POINT - N/A
.*
.* EXIT -
.*   R0- UNPREDICTABLE
.*   R1- ADDRESS RETURNED FROM GSPACE ROUTINE OR FROM GETMAIN
.*   R2-R13 NOT CHANGED
.*   R14 - UNPREDICTABLE
.*   R15 - UNPREDICTABLE
.*
.* EXTERNAL REFERENCES - NONE
.*
.* TABLES - NONE
.* MACROS - NONE
.*
.* CHANGE ACTIVITY - Y02670
.*                 - OZ28413                                  @ZA28413
.*
.* MESSAGES - SEE MNOTE IN MACRO CODE.
.*
.* ABEND CODES - NONE
.*
.*****************************************************************
.* GSPACE - C - MERGED BACK TO REL 3.7                         @ZA28413
             AIF   ('&R' EQ 'R' AND '&LV' EQ '(0)').OK
             MNOTE 4,'GSPACE PARAMETERS INVALID'
.OK          ANOP
             AIF   ('&SYSECT' NE 'IEFDB400' AND '&SYSECT' NE 'IEFBB401'*
                AND '&SYSECT' NE 'IEFBB410').OK1              @ZM41392
.* CSECTS IEFBB401, IEFBB410 HAVE UNPREDICTABLE REG 13.       @ZM41392
.* CSECT     IEFDB400(SVC99)  HAS AN UNPREDICTABLE REG 13     @YM05157
             SLR   1,1                                        @YM05157
             AGO   .OK2                                       @YM05157
.OK1         ANOP                                             @YM05157
             LR    1,13                SET ADDRESS IN REG 1
.OK2         ANOP                                             @YM05157
&NAME        L     15,GS&SYSNDX.C      SET GSPACE EP IN REG 15
             CNOP  2,4                 *
GS&SYSNDX.L  BALR  14,15               CALL GSPACE
             B     GS&SYSNDX.E         +0 RETURN IS NORMAL RETURN
             B     GS&SYSNDX.D         +4 RETURN REQUIRES GETMAIN
GS&SYSNDX.C  DC    V(GSPACE)           GSPACE PRIMARY ENTRY POINT
GS&SYSNDX.V  DC    V(GSPACE1)          GSPACE SECONDARY ENTRY POINT
GS&SYSNDX.D  SVC   10                  GETMAIN
             LTR   0,14                Q. IS RETURN TO GSPACE NECESSARY
             L     15,GS&SYSNDX.V      GET SECONDARY ENTRY POINT
             BNZ   GS&SYSNDX.L         A. YES. RETURN TO GSPACE
GS&SYSNDX.E  EQU   *                   *
             MEND
