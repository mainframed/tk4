         MACRO
         IECIOLTP
*******************************************************$********  20102
*                                                                 20102
*                     IOS - OLTEP CLEANUP ROUTINES                20102
*                                                                 20102
*   FUNCTION   RESTORE SYSTEM TO NORMAL PROCESSING BY RESETTING   20102
*              THE IOS-OLTEP VECTOR TABLE FUNCTION SWITCH, AND    20102
*              RELEASING MAIN STORAGE WHICH WAS USED FOR CONTROL  20102
*              BLOCKS.  THE FUNCTIONS ARE SET UP IN IGC01059 AND  20102
*              IN IGC03059, AND ARE DEFINED IN FULL THERE.        20102
*                                                                 20102
*   ENTRY POINTS   CLEANCH FOR CLEANING UP CHANNEL FUNCTION       20102
*                  CLEANDE FOR CLEANING UP D.E. POSTING FUNCTION  20102
*                  CLEANAT FOR CLEANING UP ATT'N FUNCTION         20102
*                  CLEANBP FOR CLEANING UP BYPASS CP PREFIX       21051
*                  CLEANEX FOR CLEANING UP SPECIFIC EXPOSURE      21051
*                  CLEANSI FOR CLEANING UP 2ND SIO                21051
*                  FREEQUE IF FROM ABEND                          20102
*                                                                 20102
*   INPUT      ADDRESS OF TWO WORD PARAMETER LIST IN REGISTER 9.  20102
*              FOR DEVICE FUNCTIONS, THE UCB, DEB ADDRESSES       20102
*              FOR CHANNEL FUNCTION, THE DEB AND CHAN ADDRESSES   20102
*              IF REG9=0, OLTEP REQUIRES TOTAL CLEANUP            20067
*                                                                 20102
*   OUTPUT     N/A
*                                                                 20102
*   EXTERNAL ROUTINES  FREEMAIN                                   20102
*                                                                 20102
*   EXITS      RETURN TO SYSTEM ON REGISTER 14                    20102
*                                                                 20102
*   TABLES/WORK AREAS  THE IOS V.T., THE IOS-OLTEP QUEUE          20102
*                                                                 20102
*   NOTES      THIS PART OF IGC0005I WAS WRITTEN AND IS           20102
*              MAINTAINED BY IOS.                                 20102
*                                                                 20102
*******************************************************$********  20102
         EJECT                                                    20102
*   REGISTER DEFINITIONS                                          20102
GMREG    EQU   1                        GETMAIN CORE ADDRESS      20102
VTREG    EQU   2                        VECTOR TABLE IN IOS       20102
MSTREG   EQU   5                        ADDRESS OF MASTER Q ENTRY 20102
SCBREG   EQU   6                        UCB OR DEB ADR            20102
LNKREG1  EQU   7                        LINK REGISTER             20102
SUBREG   EQU   8                        SUB-ENTRY ADDRESS         20102
WKREG1   EQU   10                       WORK REGISTER             20102
WKREG2   EQU   11                       WORK REGISTER             20102
WKREG3   EQU   13                       WORK REGISTER             20102
RETNREG  EQU   14                       RETURN TO THE SYSTEM      20102
         SPACE 1                                                  20102
*   OLTEP QUEUE ADDRESSING                                        20102
LOGDCB   EQU   116                      OFFSET TO LOGREC DCB      20102
OLTSW    EQU   0                        FUNCTION SWITCH           20102
OLTQUE   EQU   4                        ADR OF QUEUE ORIGIN       20102
         SPACE 1                                                  20102
*   OFFSETS IN QUEUE ENTRY                                        20102
LNKFLD   EQU   0                        LINK FIELD                20102
SUBSCB   EQU   4                        UCB OR DEB                20102
MSTRCH   EQU   7                        CHAN ADR IN MASTER ENTRY  20102
SUBOPT   EQU   8                        OPTION CODE FIELD         20102
SUBDTI   EQU   11                       TRUE DEVTAB INDEX         21051
SUBUCB   EQU   12                       UCB ADDRESS               21051
         SPACE 1                                                  20102
*   PARAMETER LIST OFFSETS                                        20102
OPTBYTE  EQU   0                        OPTION CODE BYTE          20102
SCBADR   EQU   0                        DEB OR UCB C.B.           20102
CHADR    EQU   7                        CHANNEL ADDRESS           20102
         SPACE 1                                                  20102
*   OPTION CODES                                                  20102
CHOPT    EQU   X'80'                    CHANNEL OPTION            20102
DEOPT    EQU   X'40'                    D.E. OPTION               20102
ATOPT    EQU   X'20'                    ATTENTION OPTION          20102
BPOPT    EQU   X'10'                    BYPASS OPTION             20102
EXPOPT   EQU   X'08'                    SPECIFIC EXPOSURE         21051
SIOOPT   EQU   X'02'                    2ND SIO                   21051
         SPACE 1                                                  20102
*   TCB FIELDS AND FLAGS                                          20067
TCBFLTRN EQU   20                       TCB FLAG BYTE             20102
TCBOLTFL EQU   X'02'                    TCB OLTEP FLAG            20102
TCBFLAG  EQU   29                       TCB FLAG BYTES            20102
ABND     EQU   X'80'                    TASK BEING ABENDED        20102
         SPACE 1                                                  20102
*   UCB OFFSETS                                                   21051
UCBDTI   EQU   7                        DEVICE TABLE INDEX        21051
UCBDUTYP EQU   18                       DEVICE CLASS/UNIT TYPE    21051
UCBWKADB EQU   60                       UCB EXTENSION             21051
*   MISCELLANEOUS                                                 20102
NEXAVL   EQU   X'7C'                    ADR OF NEXT AVAIL. RQE    20102
CHMSK    EQU   20                       CHANNEL MASK FOR RESTART  20102
ZERO     EQU   0                        ZERO                      20102
ADDONE   EQU   1                        ONE                       20102
HIBYTE   EQU   24                       SLL FACTOR                20067
VTLN     EQU   8                        LENGTH OF VECTOR TABLE    20067
EXPCHN   EQU   92                       CONTROL UCB PTR OFFSET    21051
NXTEXP   EQU   4                        EXP. UCB PTR OFFSET       21051
         SPACE 3                                                  20102
CLEANBP  DS    0H                       BYPASS FUNCTION           20102
         MVI   OPTBYTE(PARMREG),BPOPT   INDICATE BYPASS OPTION    20102
         BC    15,FREEQUE               GO CLEAN IT UP            20102
         SPACE 1                                                  20102
CLEANCH  DS    0H                       CHANNEL CLEANUP           20102
         MVI   OPTBYTE(PARMREG),CHOPT   SET UP CHAN OPTION CODE   20102
         BC    15,FREEQUE               GO CLEAN IT UP            20102
         SPACE 1                                                  20102
CLEANDE  DS    0H                       D.E. OPTION CLEANUP       20102
         MVI   OPTBYTE(PARMREG),DEOPT   INDICATE D.E. OPTION      20102
         BC    15,FREEQUE               GO CLEAN IT UP            20102
         SPACE 1                                                  20102
CLEANAT  DS    0H                                                 20102
         MVI   OPTBYTE(PARMREG),ATOPT   INDICATE ATT'N OPTION     20102
         B     FREEQUE                  CLEAN UP                  21051
         SPACE 1                                                  21051
CLEANEX  MVI   OPTBYTE(PARMREG),EXPOPT  INDICATE SPECIFIC EXP OPTN21051
         B     FREEQUE                  CLEAN UP                  21051
         SPACE 1                                                  21051
CLEANSI  MVI   OPTBYTE(PARMREG),SIOOPT  INDICATE 2ND SIO OPTION   21051
*****    B     FREEQUE                  CLEAN UP                  21051
         SPACE 1                                                  20102
*   SCAN THE IOS-OLTEP QUEUE AND FIND THE NUMBER OF SUB-ENTRIES   20102
*   FOR THE INPUT OPTION AND THE ENTRY FOR THE INPUT UCB OR DEB.  20102
         SPACE 1                                                  20102
FREEQUE  DS    0H                       ENTRY IF FROM ABEND       20102
         L     VTREG,LOGDCB(0,CVTREG)   LOG DCB ADR               20102
         SH    VTREG,TWELVE             BACK UP TWELVE            20102
         L     VTREG,ZERO(0,VTREG)      POINT AT VECTOR TABLE     20102
         L     MSTREG,OLTQUE(0,VTREG)   GET MASTER ENTRY          20102
         LTR   MSTREG,MSTREG            ANYTHING TO CLEAN UP      20102
         BCR   8,RETNREG                NO, RETURN                20102
         TM    TCBFLAG(TCBREG),ABND     IS TASK BEING ABENDED     20102
         BC    1,FREEQ1                 YES, CLEAN UP QUEUE       20102
         LTR   PARMREG,PARMREG          OLTEP WANT TOTAL CLEANUP  20102
         BC    8,FREEQ1                 YES, GO DO IT             20102
         L     SCBREG,SCBADR(0,PARMREG) GET INPUT C.B.            20102
         LA    SCBREG,ZERO(0,SCBREG)    CLEAR HIGH ORDER BYTE     20102
         SR    RCREG,RCREG              INITIALIZE COUNTER        20102
         SR    SUBREG,SUBREG            CLEAR FOR LATER COMPARE   20102
         LR    WKREG1,MSTREG            COPY MASTER ADDRESS       20102
FIND1    L     WKREG2,LNKFLD(0,WKREG1)  GET NEXT ENTRY            20102
         LTR   WKREG2,WKREG2            END OF QUEUE              20102
         BC    8,DEQNTRY                YES, GO DEQUE IT          20102
         LR    LNKREG1,WKREG1           SAVE BACK LINK POINTER    20102
         LR    WKREG1,WKREG2            COPY ENTRY ADDRESS        20102
         CLC   OPTBYTE(1,PARMREG),SUBOPT(WKREG1) FOR THIS OPTION  20102
         BC    7,FIND1                  NO, GET NEXT ENTRY        20102
         LA    RCREG,ADDONE(0,RCREG)    INCREMENT COUNTER         20102
         C     SCBREG,SUBSCB(0,WKREG1)  IS IT FOR INPUT SCB       20102
         BC    7,FIND1                  NO, GET NEXT ENTRY        20102
         LR    SUBREG,WKREG1            SAVE ENTRY ADDRESS        20102
         LR    WKREG3,LNKREG1           SAVE BACK LINK ENTRY      M1008
         BC    15,FIND1                 CONTINUE SCAN             20102
         SPACE 1                                                  20102
*   THE ENTRY ADDRESS IS IN SUBREG, THE ENTRY PRECEDING THIS      20102
*   ENTRY IS IN WKREG3.  THE LAST QUEUE ELEMENT IS IN WKREG1.     20102
*   THE NUMBER OF ENTRIES FOR THIS FUNCTION IS IN RCREG.          20102
*   IF ONLY ONE ENTRY IS FOUND FOR THIS FUNCTION, THE IOS-OLTEP   20102
*   SWITCH BIT FOR THIS FUNCTION IS TURNED OFF. IF NO OTHER       20102
*   FUNCTIONS ARE LEFT, THE ENTIRE QUEUE IS FREED AND THE IOS     20102
*   VECTOR TABLE IS RE-INITIALIZED.                               20102
         SPACE 1                                                  20102
DEQNTRY  LTR   SUBREG,SUBREG            WAS THE ENTRY FOUND       20102
         BCR   8,RETNREG                NO, RETURN                20102
         BCT   RCREG,DEQ2               SKIP AROUND IF ANY LEFT   20102
         XC    OLTSW(1,VTREG),OPTBYTE(PARMREG) TURN OFF SWITCH
         BC    8,DEQ4                   FREE Q IF NO FUNC IS LEFT 20102
         SPACE 1                                                  20102
*   DEQUE THE ENTRY FOR THIS FUNCTION                             20102
DEQ2     EQU   *                                                  21051
         CLI   SUBOPT(SUBREG),EXPOPT .  THIS A SPECIFIC EXP FUNC  21051
         BNE   ALLSET .                 BRANCH NO                 21051
         L     WKREG2,SUBUCB(0,SUBREG) .GET UCB ADDRESS           21051
         LA    LNKREG1,ALLSET                                     21051
         SPACE 1                                                  21051
***  IF DEVICE IS A MULTIPLE EXPOSURE DEVICE, RESET DTI IN ALL    21051
***  EXPOSURE UCB'S. IF NOT,RESET MAIN UCB DTI.                   21051
         SPACE 1                                                  21051
RESETRTN IC    GMREG,SUBDTI(0,SUBREG)   TRUE DEVTAB INDEX         21051
         STC   GMREG,UCBDTI(0,WKREG2)   SET TRUE INDEX IN UCB     21051
         CLC   UCBDUTYP(2,WKREG2),ZEUSCON                         21051
         BE    RESETALL                                           21051
         CLC   UCBDUTYP(2,WKREG2),ZEUSCN1                         21051
         BCR   7,LNKREG1                                          21051
RESETALL L     SCBREG,UCBWKADB(0,WKREG2) BASE UCB ENTENSION       21051
         L     RCREG,EXPCHN+NXTEXP(0,SCBREG) LAST EXP. STARTED    21051
         LR    WKREG2,RCREG             SAVE LAST EXP STARTED     21051
SETDTI   STC   GMREG,UCBDTI(0,WKREG2)   CHANGE EXP DTI            21051
         L     SCBREG,UCBWKADB(0,WKREG2) THIS EXPOSURE EXTENSION  21051
         L     WKREG2,EXPCHN+NXTEXP(0,SCBREG) NEXT EXP IN CHAIN   21051
         CR    WKREG2,RCREG             ALL EXPOSURES CHANGED     21051
         BNE   SETDTI                   BRANCH NO                 21051
         BR    LNKREG1                                            21051
         SPACE 1                                                  21051
ALLSET   L     WKREG1,LNKFLD(0,SUBREG)  RELINK REST OF THE        21051
         ST    WKREG1,LNKFLD(0,WKREG3)  OLTEP QUEUE               20102
         LR    GMREG,SUBREG             SET UP FREEMAIN PARAMETER 20102
         BAL   LNKREG1,FREENTRY         RELEASE CORE              20102
         CLI   OPTBYTE(PARMREG),CHOPT   IS THIS CHAN OPT          20102
         BCR   7,RETNREG                NO, RETURN TO SYSTEM      20102
         SPACE 1                                                  20102
*   IF CHANNEL OPTION, TELL IOS TO RESTART CHANNEL                20102
DEQ3     L     WKREG1,NEXAVL(0,CVTREG)  GET NEXT AVAIL. ADR       20102
         SR    WKREG2,WKREG2            CLEAR WORK REGISTER       20067
         IC    WKREG2,MSTRCH(0,MSTREG) GET CHANNEL ADR            20102
         LA    WKREG3,X'80'             SET UP MASK BIT           20102
         SLL   WKREG3,HIBYTE            SHIFT TO HIGH-ORDER BYTE  20102
         SRL   WKREG3,0(WKREG2)         SHIFT BIT TO CORRECT POS  20102
         O     WKREG3,CHMSK(0,WKREG1)   PICK UP ANY BITS THERE    20102
         ST    WKREG3,CHMSK(0,WKREG1)   ADD THIS CHAN TO MASK     20102
         BCR   15,RETNREG               RETURN TO SYSTEM          20102
         SPACE 1                                                  20102
DEQ4     OC    OLTSW(1,VTREG),OPTBYTE(PARMREG)     RESET SWITCH   20102
FREEQ1   TM    OLTSW(VTREG),CHOPT       NEED CHANNEL CLEANUP      20067
         BC    8,FREEQ1A                NO, CONTINUE              20067
         LR    RCREG,RETNREG            SAVE RETURN ADR           20067
         BAL   RETNREG,DEQ3             GO FORCE RESTART OF CHAN  20067
         LR    RETNREG,RCREG            RESTORE RETURN ADR        20067
FREEQ1A  LR    SUBREG,MSTREG            COPY MASTER ADR           20067
FREEQ2   LR    GMREG,SUBREG             SET UP REG1 FOR FREEMAIN  20102
         L     SUBREG,LNKFLD(0,SUBREG)  SAVE NEXT IN QUEUE        20102
         BAL   LNKREG1,FREENTRY         FREE ENTRY                20102
         LTR   SUBREG,SUBREG            ANY LEFT IN QUEUE         20102
         BZ    ENDALL                   BRANCH NO,ALL THROUGH     21051
         TM    SUBOPT(SUBREG),EXPOPT    EXP FUNCTION              21051
         BZ    FREEQ2                   BRANCH NO                 21051
         L     WKREG2,SUBUCB(0,SUBREG)  GET UCB ADDRESS           21051
         LA    LNKREG1,FREEQ2           RETURN ADDRESS            21051
         B     RESETRTN                 GO RESTORE DEVTAB INDEX   21051
         SPACE 1                                                  21051
ENDALL   XC    OLTSW(VTLN,VTREG),OLTSW(VTREG) ZERO OUT VECTOR TBL 21051
         BCR   15,RETNREG               RETURN TO SYSTEM          20102
         SPACE 1                                                  20102
FREENTRY L     REG0,SPL16               SET UP PARAMETER          20102
         FREEMAIN R,LV=(0),A=(1)
         BCR   15,LNKREG1                                         20102
         SPACE 1                                                  20102
***            CONSTANTS                                          20102
         SPACE 1                                                  20102
TWELVE   DC    H'12'                    TWELVE                    20102
ZEUSCON  DC    X'2006'                  2305-1                    21051
ZEUSCN1  DC    X'2007'                  2305-2                    21051
SPL16    DS    0F                       FREEMAIN PARAMETERS       20102
         DC    X'EA000010'              16 BYTES, SUBPOOL 234    X01906
         EJECT
         MEND
         END
