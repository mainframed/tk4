         MACRO
&LABEL   BRREAD  &REG,&TYPE,&ASID=                               Y02006
.*D026500-035000                                                 Y02006
.*C001000,014500,015500,020000-022000,023500,024500,026000       Y02006
.*C036000                                                        Y02006
.*A002700,003100-003492,027100-028100,035200-035300              Y02006
.*A035600-035920,036600,038600-039330                            Y02006
.***  GLOBAL VARIABLE &PLIST CONTROL GENERATION OF PARMETER LIST  ***
         GBLB  &PARMLST
         GBLC  &PLIST
.***  GLOBAL VARIABLE &BALMOD INDICATES IF BAL MODULE OR PLS MODULE **
         GBLB  &BALMOD                                           Y02006
         LCLC  &TLAB
.***  TEST FOR INIT OR ADJUST FUNCTIONS  ***                     Y02006
         AIF    ('&SYSLIST(1)' EQ 'INIT').CALINIT                Y02006
         AIF    ('&SYSLIST(1)' EQ 'ADJUST').CALFREE              Y02006
.***  TEST IF DATA OR CPU CALL  ***                              Y02006
         AIF   ('&TYPE' EQ 'DATA' OR '&TYPE' EQ 'CPU').TESTNUM   Y02006
         AIF   ('&TYPE' EQ '').TESTNUM                           Y02006
         MNOTE 12,'INVALID READ FUNCTION'                        Y02006
         MEXIT                                                   Y02006
.***  TEST FOR TOO MANY PARAMETERS  ***                          Y02006
.TESTNUM AIF   (N'&SYSLIST GT 2).MANYMSG                         Y02006
.***  TEST FOR P-REG  ***
.TESTREG AIF   ('&REG' NE '').OK1
         MNOTE 12,'MISSING ADDR REG'
         MEXIT
.OK1     ANOP
.***  TEST FOR DEMAND P-LIST  ***
         AIF   (&PARMLST).EXTGEN
.***  TEST FOR FIRST CALL OF BRREAD IN THIS CSECT  ***
         AIF   ('&PLIST' EQ 'RDL'.'&SYSECT'(4,5)).NOLIST
.***  IF FIRST CALL GENERATE PARAMETER LIST  ***
&PLIST   SETC  'RDL'.'&SYSECT'(4,5)
         AGO   .NOEXTGN
.EXTGEN  ANOP
&PLIST   SETC  'TMP'.'&SYSNDX'
.NOEXTGN ANOP
         CNOP  0,4                 ALLIGN ON WORD BOUNDARY
&LABEL   ST    &REG(1),&PLIST      STORE ADDRESS OF STORAGE REQUIRED
.ST11    ANOP
.***  GENERATE STORE OF REQUESTED ADDRESS BEFORE POINT TO P-LIST  ***
&TLAB    SETC  'RDL'.'&SYSNDX'
         BAL   1,&TLAB             POINT TO PARAMETER LIST
&PLIST   DC    F'0'                ADDRESS OF STORAGE REQUIRED   Y01980
         DC    X'00'               DATA TYPE CODE FOR READ RTN   Y02006
         DC    X'00'               RESERVED                      Y01980
         DC    H'0'                ASID OF THIS STORAGE          Y02006
&TLAB    EQU   *
         AGO   .TESTID
.NOLIST  ANOP
&LABEL   ST    &REG(1),&PLIST      STORE ADDRESS OF STORAGE REQUIRED
.ST12    ANOP
         LA    1,&PLIST            POINT TO PARAMETER LIST
.TESTID  ANOP
.**** IF ASID IS NOT SPECIFIED, LEAVE PARAMETER AS IT IS         Y02006
         AIF   ('&ASID' EQ '').TSTTYPE                           Y02006
.**** DISTINGUISH BETWEEN ASID REG AND ASID ABSOLUTE NUMBER      Y02006
         AIF   ('&ASID'(1,1) EQ '(').REG                         Y02006
         AIF   ('&ASID' EQ '0').GENSR                            Y02006
         MVC   &PLIST+6(2),&ASID .GET VALUE OF ASID FROM STORAGE Y02006
         AGO   .TSTTYPE                                          Y01980
.GENSR   ANOP .                                                  Y01980
         SR    15,15 .   SET ASID VALUE EQUAL ZERO               Y02006
.STORID  ANOP .                                                  Y01980
         STH   15,&PLIST+6 .  STORE ASID IN PARAMETER LIST       Y02006
         AGO  .TSTTYPE  .                                        Y01980
.REG     ANOP .                                                  Y01980
         STH   &ASID(1),&PLIST+6 . STORE ASID IN PARAMETER LIST  Y02006
.TSTTYPE AIF   ('&TYPE' EQ 'CPU').SETCPU                         Y02006
         MVI   &PLIST+4,X'00'      ZERO DATA TYPE CODE FOR DATA
*                                  REQUEST                       Y02006
         AGO   .CALL
.SETCPU  ANOP
         MVI   &PLIST+4,X'80'      INDICATE CPU STATUS DATA REQUIRED
*                                                                Y02006
.CALL    ANOP
         AIF   (&BALMOD).BALDATA   CHECK IF BAL MODULE           Y02006
         L     R15,BRRDDATA(,COMBASE) FETCH READ ROUTINE ADDRESS FROM
*                                  COMMON CSECT                  Y02006
         AGO   .CALLD                                            Y02006
.BALDATA ANOP                                                    Y02006
         L     15,BRRDDATA         FETCH READ ROUTINE ADDRESS FROM
*                                  COMMON CSECT
.CALLD   ANOP                                                    Y02006
         BALR  14,15               LINK TO READ
         AIF   (NOT(&PARMLST)).OUT
&PLIST   SETC  'RDL'.'&SYSECT'(4,5)
&PARMLST SETB  0
         AGO   .OUT                                              Y02006
.CALINIT ANOP                                                    Y02006
         AIF   (N'&SYSLIST GT 1).MANYMSG   CHECK # OF PARMS      Y02006
         AIF   ('&ASID' NE '').MANYMSG                           Y02006
         AIF   (&BALMOD).BALINIT   CHECK IF BAL MODULE           Y02006
&LABEL   L     R15,BRRDINIT(,COMBASE)  ADDRESS OF INITIALIZATION
*                                  ROUTINE                       Y02006
         AGO   .CALLI                                            Y02006
.BALINIT ANOP                                                    Y02006
&LABEL   L     R15,BRRDINIT        ADDRESS OF INITIALIZATION ROUTINE
*                                                                Y02006
.CALLI   ANOP                                                    Y02006
         BALR  R14,R15             LINK TO INIT                  Y02006
         AGO   .OUT                                              Y02006
.CALFREE ANOP                                                    Y02006
         AIF   (N'&SYSLIST GT 1).MANYMSG   CHECK # OF PARMS      Y02006
         AIF   ('&ASID' EQ '').OK2CALL                           Y02006
.MANYMSG MNOTE 12,'TOO MANY PARAMETERS SPECIFIED'                Y02006
         MEXIT                                                   Y02006
.OK2CALL ANOP                                                    Y02006
         AIF   (&BALMOD).BALADJ    BRANCH IF BAL MODULE          Y02006
&LABEL   L     R15,BRRDADJ(,COMBASE)  ADDRESS OF ADJUST ROUTINE  Y02006
         AGO   .CALLA                                            Y02006
.BALADJ  ANOP                                                    Y02006
&LABEL   L     R15,BRRDADJ         ADDRESS OF ADJUST ROUTINE     Y02006
.CALLA   ANOP                                                    Y02006
         BALR  R14,R15             LINK TO IT                    Y02006
.OUT     ANOP
         MEND
