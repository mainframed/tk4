         MACRO
&NAME    WINDOW &PARM
         LCLC  &TEMP
         LCLC  &TEMP2
&TEMP    SETC  'IHB1&SYSNDX'           GENERATE UNIQUE LABEL
&TEMP2   SETC  'IHB2&SYSNDX'           GENERATE UNIQUE LABEL
         AIF   ('&PARM' EQ '').ERROR   CHECK IF OPERAND SPECIFIED
         AIF   ('&PARM'(1,1) EQ '(').RXFORM1 CHECK IF RX FORM
         STCTL 0,0,&PARM               SAVE THE CURRENT C.R. 0 VALUE
         AGO   .COMMON                 PERFORM COMMON WINDOW OPERATION
.RXFORM1 ANOP                          RX FORM SPECIFIED
         STCTL 0,0,0&PARM              SAVE THE CURRENT C.R. 0 VALUE
.COMMON  ANOP                          COMMON WINDOW CODE
         STCTL 0,0,PSACR0              STORE C.R. 0 IN WORK AREA
         NC    PSACR0(4),&TEMP         CLEAR ALL EXTERNAL INTERRUPTS
         OC    PSACR0(4),&TEMP2        SET MFA AND EMS BITS
         LCTL  0,0,PSACR0              LOAD C.R. 0
         STOSM PSACR0,X'01'            ENABLE FOR EMS/MFA INTERRUPTS
         STNSM PSACR0,X'FE'            DISABLE FOR EMS/MFA INTERRUPTS
         STCTL 0,0,PSACR0              STORE CR0 IN WORK AREA
         AIF   ('&PARM'(1,1) EQ '(').RXFORM2 CHECK IF RX FORM
         OC    &PARM.(1),PSACR0        INSURE TIMER BIT STAYS SAME
         LCTL  0,0,&PARM               RELOAD ORIGINAL C.R. 0
         AGO   .END                    EXIT
.ERROR   ANOP                          NO C.R. 0 SAVE AREA WAS SPEC
         MNOTE 8,'NO C.R. 0 SAVE AREA ADDRESS WAS SPECIFIED'
         AGO   .END2                   EXIT
.RXFORM2 ANOP                          RX FORM SPECIFIED
         OC    0(1,&PARM(1).),PSACR0   INSURE TIMER BIT STAYS SAME
         LCTL  0,0,0&PARM              RELOAD ORIGINAL C.R. 0
.END     ANOP                          FINISHED
         B     *+12                    BRANCH AROUND CONSTANTS
&TEMP    DC    X'FFFFC31F'             DISABLE C.R. 0 MASK
&TEMP2   DC    X'0000C000'             ENABLE EMS/MFA MASK
.END2    ANOP
         MEND
