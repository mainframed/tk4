         MACRO
         IKJEBEST &CSECT=IBM
         LCLA  &A,&B,&C,&D,&E
         LCLA  &F                                                A42956
         LCLC  &CNAME,&SCNAME,&ABBR,&LDMOD,&LABEL,&LABEL1,&LABEL2,&NMBR
.*A035000,185000,230600-239000,255000                            A42956
         AIF   ('&CSECT' NE 'IBM').CONT0
&CNAME   SETC  'IKJEBMA8'     DEFINE CSECT NAME FOR IBM TABLE.
IKJEBMA8 CSECT
         ENTRY MA8IP002
         ENTRY MA8LI002
         AGO   .CONT1
.CONT0   ANOP
         AIF   ('&CSECT' NE 'USER').ERROR2
&CNAME   SETC  'IKJEBMA9'     DEFINE CSECT NAME FOR USER TABLE.
IKJEBMA9 CSECT
.CONT1   ANOP
&A       SETA  N'&SYSLIST
         AIF   (&A EQ 0).END
&B       SETA  1
&F       SETA  1                                                 A42956
.CONT2   ANOP
&C       SETA  N'&SYSLIST(&B)
         AIF   (&C LT 2 OR &C GT 3).ERROR1
&E       SETA  K'&SYSLIST(&B,&C)
&D       SETA  &E-1
.*  THE FOLLOWING FLAGED INSTRUCTIONS WERE ADDED TO PROVIDE      A42956
.*  UNIQUE LABELS, EVEN IF MODULES HAVE IDENTICAL LAST TWO       A42956
.*  CHARACTERS IN ENTRY POINT NAMES.  THE LABELS FOR MODULES     A42956
.*  IKJEBELI AND IKJEBEIP ARE UNCHANGED, SINCE THEY ARE          A42956
.*  REFERENCED WITHIN IKJEBEMA.                                  A42956
         AIF   ('&CSECT' NE 'IBM').CONT10                        A42956
         AIF   ('&SYSLIST(&B,&C)'(&D,&E) EQ 'LI' OR                    X
                '&SYSLIST(&B,&C)'(&D,&E) EQ 'IP' ).CONT11        A42956
.CONT10  ANOP                                                    A42956
&LABEL1  SETC  '&CNAME'(6,8).'@'.'&F'                            A42956
&F       SETA  &F+1                                              A42956
&LABEL2  SETC  '&CNAME'(6,8).'@'.'&F'                            A42956
&F       SETA  &F+1                                              A42956
         AGO   .CONT12                                           A42956
.CONT11  ANOP                                                    A42956
&LABEL1  SETC  '&CNAME'(6,8).'&SYSLIST(&B,&C)'(&D,&E).'001'
&LABEL2  SETC  '&CNAME'(6,8).'&SYSLIST(&B,&C)'(&D,&E).'002'
.CONT12  ANOP                                                    A42956
&SCNAME  SETC  '&SYSLIST(&B,1)'
         SPACE 2
         DC    AL1(&LABEL1-*-1) LENGTH OF SUBCOMMAND NAME.
         DC    C'&SCNAME' SUBCOMMAND NAME.
&LABEL1  EQU   *
         DC    AL1(&LABEL2-*-1) LENGTH OF ABBREVIATION.
         AIF   (K'&SYSLIST(&B,2) EQ 0).CONT5
&ABBR    SETC '&SYSLIST(&B,2)'
         DC    C'&ABBR' ABBREVIATION FOR SUBCOMMAND.
.CONT5   ANOP
&LABEL2  EQU   *
&LDMOD   SETC  '&SYSLIST(&B,&C)'
         DC    CL8'&LDMOD' LOAD MODULE NAME.
         AIF   (&B EQ &A).END
&B       SETA  &B+1
         AGO   .CONT2
.END     ANOP
         SPACE 2
         DC    AL1(255)       END OF TABLE MARKER.
         MEXIT
.ERROR1  MNOTE 12,'INVALID TABLE ENTRY'
         MEXIT
.ERROR2  MNOTE 12,'INVALID KEYWORD VALUE'
         MEND
