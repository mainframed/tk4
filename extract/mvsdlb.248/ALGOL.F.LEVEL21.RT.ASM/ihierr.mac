ERR      TITLE 'IHIERROR, RUNTIME ERROR ROUTINE, ALGOL F LIB'
*
*        COMPONENT ID - 360S-LM-532 ALGOL F LIBRARY
*
*        STATUS - LEVEL 2.1
*
*        FUNCTION/OPERATION -
*        IF AN ERROR OCCURS DURING EXECUTION OF AN ALGOL PROGRAM
*        THIS ROUTINE IS CALLED BY IHIFSARA
*        A MESSAGE IS GIVEN SPECIFYING THE ERROR. IF DUMP IS
*        SPECIFIED THE ROUTINE EDITS AND PRINTS THE DATA STORAGE
*        AREAS CONTAINING THE VALUES OF THE IDENTIFIERS
*
*        ENTRY POINT - IHIERROR
*
*        OUTPUT-
*        ERROR MESSAGES AND ALGOL DUMPS ARE PRINTED ON THE
*        PRINTER AS 90 CHARACTER RECORDS WITH CONTROL CHARACTER
*        IN SOME CASES A MESSAGE IS ISSUED TO THE CONSOLE
*
*        EXTERNAL ROUTINES -
*        IHIIORCL - FOR CLOSING DATASET NUMBER 1 (PRINTER)
*        IHIIOROP - FOR OPENING DATASET NUMBER 1
*        FRDSA    - ROUTINE IN IHIFSARA WHICH ISSUES FREEMAIN FOR
*                   THE CURRENT DATA STORAGE AREAS
*
*        EXIT - NORMAL - TO TERMINATION ROUTINE IN IHIFSARA
*
*        EXIT - ERROR - N/A
*
*        TABLES/WORK AREAS -
*        THE CONTROL SECTION NAMED IHIGERMSG CONTAINS ALL
*        MESSAGES TO BE PRINTED
*
IHIERROR CSECT
*
         USING DSTABLE,R5
*
***********************************************************************
*
*        REGISTER USAGE
*
***********************************************************************
*
CDSA     EQU   10                      ADDRESS OF CURRENT DSA
PBT      EQU   11
*
*        MESSAGE FORMAT FLAGS
*
FLAGIDS  EQU   X'03'                   INSERT DSNAME INTO MSG
FLAGIPS  EQU   X'01'                   INSERT PSW INTO MSG
FLAGMC   EQU   X'04'                   MSG CONTINUATION
*
         SAVE  (14,12),,'IHIERROR LEVEL 2.1 &SYSDATE &SYSTIME'
*
         LR    R7,R15
         USING IHIERROR,R7
         MVC   VFRDSA(20),0(R1)        STORE EXTERNAL ADDR PARAMETERS
         LR    R12,R13                 R12 -> FSA
         LA    R13,SAVEAREA            R13 -> SAVEAREA
*
***********************************************************************
*
*        TEST IF ERROR MESSAGE NUMBER IS 32 OR 41 FOR DATASET 1
*
***********************************************************************
*
         BCT   R6,B1                   DSNUMBER ^= 1 ? BRANCH
*
*        CURRENT DATASET IS DATASET 1
*        DETERMINE IF DSNUM 1 IS ALGLDD01 OR SYSPRINT
*
         TM    DSF+1,DS11              DS11 = 0 ?  IE ALGLDD01
         BZ    B1                      YES, BRANCH
*
*        PROCESSING FOR SYSPRINT
*
         CLI   FSAERCOD(R12),X'80'     SYSPRINT, I/O ERROR (32) ?
         BE    IOERR                   YES, ISSUE WTO IHI032I
         CLI   FSAERCOD(R12),X'A4'     DD STMT ? (41)
         BNE   B1                      YES, ISSUE IHI041I
*
         WTO   'IHI041I SYSPRINT DD STMT INCORRECT OR MISSING',        X
               ROUTCDE=11,DESC=7
*
         B     SETOPTSW
*
IOERR    WTO   'IHI032I SYSPRINT UNRECOVERABLE I/O ERROR',             X
               ROUTCDE=11,DESC=7
*
SETOPTSW OI    OPTSW(R12),X'02'        SET SWITCH IN FSA
         NI    DTSW(R12),X'7F'         REMOVE DUMP SWITCH
         B     SPDAFREE
*
***********************************************************************
*
*        EDIT AND PRINT ERROR MESSAGE
*
***********************************************************************
*
B1       LA    R8,1(,R6)               RESTORE DSNUMBER AT ERROR
         L     R5,ADSTAB(,R12)         R5 -> ADSTAB IN FSA
         LA    R5,DSTABLEL+4(,R5)      R5 -> DSTABLE ENTRY FOR DS NO 1
         OI    DSF+1,DS15              FLAG CLOSE FROM IHIERR
         L     R15,VIORCP              CLOSE ALL DATASETS
         BALR  R14,R15
         OI    DSF+1,DS11              DS11=1, OPEN SYSPRINT
*
*        SET OUTPUT FLAGS
*        OUTPUT OP, OPEN FOR OUTPUT
*
         OI    DSF,DS2+DS6             DS2, DS6 = 1
         NI    DSF,255-DS0-DS1         DS0, DS1 = 0, ^ OPEN, ^ BLOCKED
         LA    R6,1                    R6 = DATASET 1
         L     R15,VIOROP              R15 -> IHIIOROP
         BALR  R14,R15                 OPEN SYSPRINT
         MVI   BUFFER,C' '             PRINT ONE BLANK LINE TO PREVENT
         MVC   BUFFER+1(L'BUFFER-1),BUFFER  OVERPRINTING
         BAL   R3,OUTPUT
         SR    R2,R2
         IC    R2,FSAERCOD(,R12)       GET ADDR OF ERROR MESSAGE
         L     R6,VERMSG               R6 -> IHIERMSG MODULE
         L     R6,0(R2,R6)             R6 -> MSG TEXT BLOCK
         SRL   R2,2                    CONVERT MESSAGE NUMBER TO
         CVD   R2,WORKD                DECIMAL FOR PRINTING
         L     R3,VERM01               R3 -> 'IHI0XXI SC=   ' STRING
         UNPK  4(2,R3),WORKD           MOVE IN MSG NUMBER
         OI    5(R3),X'F0'             MAKE PRINTABLE
         LH    R2,SCRCS(,R12)          CONVERT SEMICOLON COUNTER TO
         CVD   R2,WORKD                DECIMAL FOR PRINTING
         MVC   12(L'SCPATTN,R3),SCPATTN  MOVE IN EDIT PATTERN
         ED    12(L'SCPATTN,R3),WORKD+5  FORMAT SEMICOLON COUNT
         TM    1(R6),FLAGIDS           DSNAME TO BE INSERTED ?
         BNO   NOT1617A                NO, BRANCH
         STC   R8,DSNUMBER             YES, STORE DATASET NUMBER
         CLI   DSNUMBER,16             DSN = 16 ?
         BNE   DSN17                   NO, BRANCH
         MVC   2(6,R6),=C'SYSUT2'      SYSUT2
         B     PSWDSN
*
DSN17    BL    NOT1617
         MVC   2(6,R6),=C'SYSUT1'      SYSUT1
         B     PSWDSN
*
TAKEDSN  CVD   R8,WORKD                CONVERT DATASET NUMBER TO
         UNPK  6(2,R6),WORKD           DECIMAL FOR PRINTING
         OI    7(R6),X'F0'             MAKE PRINTABLE
         B     PSWDSN
*
TAKEPSW  UNPK  7(9,R6),PGOPSW(5,R12)   UNPACK OLD PSW FOR PRINTING
         TR    7(8,R6),TRTABLE-240
         MVI   15(R6),C' '
         UNPK  16(9,R6),PGOPSW+4(5,R12)
         MVI   24(R6),C' '
         TR    16(8,R6),TRTABLE-240
         B     PSWDSN
*
NOT1617  TM    1(R6),FLAGIDS           INSERT DATASET NUMBER ?
         BO    TAKEDSN                 YES, BRANCH
NOT1617A BM    TAKEPSW
*
PSWDSN   MVC   BUFFER(19),0(R3)        MOVE MESSAGE INTO BUFFER
         SR    R2,R2
LINE2    IC    R2,0(R6)                GET L'MSG TEXT
         BCTR  R2,0                    DECR FOR EXE
         EX    R2,EXMVC                MOVE MSG TEXT
         TM    1(R6),FLAGMC            MSG HAS CONTINUATION ?
         BZ    ENDLINE                 NO, BRANCH
         BAL   R3,OUTPUT
         LA    R6,3(R2,R6)             R6 -> NEXT LINE OF MESSAGE
         B     LINE2                   LOOP FOR CONTINUATION LINE
*
EXMVC    MVC   BUFFER+19(0),2(R6)      EXE MVC
*
ENDLINE  OI    FLAG,X'04'
         BAL   R3,OUTPUT
         MVI   SPACE,C'0'              SET ASA SKIP CHARACTER
         MVI   SCOUNT+3,X'01'          INSERT NEW STEP LENGTH FOR S
*
***********************************************************************
*
*        GO THROUGH THE RETURN ADDR STACK
*        IF A BLOCK OR PROCEDURE ENTRY IS FOUND THE DATA STORAGE
*        AREAS ARE EDITED AND FSA IS CALLED FOR ISSUEING
*        FREEMAIN
*
***********************************************************************
*
SPDAFREE L     R1,VSPDAP               FREEMAIN FOR SPDA AREAS
         L     R2,0(,R1)
MORESPDA LTR   R1,R2
         BZ    MORERAS
         L     R2,0(,R1)
*
         FREEMAIN R,LV=64,A=(R1)
*
         B     MORESPDA                LOOP AROUND
*
MORERAS  L     R14,RASPT(R12)          TEST IF MORE ENTRIES IN RAS
MORERASA C     R14,RASSTART(R12)
         BNH   ENDOFR
         CLI   0(R14),X'00'            PROCEDURE OR BLOCK ENTRY ?
         BE    PRENTRY
         TM    DTSW(R12),X'80'         DUMP SPECIFIED ?
         BZ    RELDSA
         B     EDIT
*
PRENTRY  LA    R2,8                    GET PRECEDING ENTRY IN RAS
         SR    R14,R2
         ST    R14,RASPT(R12)          NEW ADDR TO RASTP IN FSA
         B     MORERASA
*
RELDSA   LR    R13,R12                 RELEASE CURRENT DATA STORAGE
         L     CDSA,0(,R14)            AREA
         L     PBT,16(,CDSA)
         L     R1,VFRDSA
         BALR  R15,R1
         LR    R12,R13
         LA    R13,SAVEAREA
         B     MORERAS
*
ENDOFR   LR    R13,R12
*
         RETURN (14,12)                TO TERMINATION ROUTINE IN FSA
*
***********************************************************************
*
*        PRINT CURRENT DATA STORAGE AREAS IF DUMP IS SPECIFIED
*
***********************************************************************
*
*        BLOCK NUMBER AND TYPE OF BLOCK MOVED INTO TEXT
*
***********************************************************************
*
EDIT     L     CDSA,0(,R14)            POINTER TO CURRENT DSA
         L     PBT,16(,CDSA)           ADDR OF PBTAB
         MVC   ERM02M(4),4(PBT)        NAME OF LOAD MODULE
         AH    PBT,8(,CDSA)            TYPE OF BLOCK
         SR    R2,R2
         IC    R2,6(,PBT)
         SLL   R2,2
         LA    R6,ERM02BK(R2)
         MVC   ERM02TP,0(R6)           MOVE IN BLOCK TYPE TEXT
         LH    R2,8(,CDSA)             CONVERT BLOCK NUMBER TO DECIMAL
         SRL   R2,3                    FOR PRINTING
         CVD   R2,WORKD
         MVC   ERM02BN,BNPATTN
         ED    ERM02BN,WORKD+6
         OI    FLAG,X'04'              SET SKIP FLAG
         MVC   BUFFER(ERM02L),IHIERM02  MOVE MSG INTO BUFFER
         BAL   R3,OUTPUT
*
***********************************************************************
*
*        EDIT THE FORMAL PARAMETERS IF PROCEDURE BLOCK
*
***********************************************************************
*
         SR    R1,R1
         LA    R9,24(,CDSA)            FIRST BYTE TO BE EDITED
         TM    6(PBT),X'FF'            TEST FOR FORMAL PARAMETERS
         BZ    NOFPARAM
         MVC   BUFFER+9(L'KFORMAL),KFORMAL  HANDLING FORMAL PARAMETERS
         SR    R5,R5
         IC    R5,7(,PBT)              PARAMETERS BY EIGHT
         SLL   R5,3                    MULTIPLY NUMBER OF FORMAL
         TM    6(PBT),X'08'
         BZ    NOTYPE
         LA    R5,8(,R5)               ADD EIGHT IF TYPE PROCEDURE
NOTYPE   AR    R5,R9                   END OF FORMAL PARAMETERS IN R5
         BAL   R4,TRANSDAT             PRINT FORMAL PARAMETERS
NFPARM   OI    FLAG,X'04'              SET SKIP FLAG
         BAL   R3,OUTPUT               LAST PARAMETERS
NOFPARAM LH    R2,4(,PBT)              GET END OF OBJECT TIME STACK
         LA    R5,0(R2,CDSA)
         CR    R9,R5
         BNL   ARRAYS
*
***********************************************************************
*
*        EDIT OBJECT TIME STACK
*
***********************************************************************
*
         MVC   BUFFER+9(L'IHIERM05),IHIERM05
         NI    FLAG,X'7D'              BUFFER AND ARRAY FLAG
         LTR   R1,R1                   ANY FORMAL PARAMETER PRINTED
         BNZ   NOTZERO
         B     MULT32A
*
MULT32   SRDL  R0,27
MULT32A  OI    FLAG,X'80'              BUFFER FLAG
         LA    R4,NFPARM
         B     TRANSDAT
*
NOTZERO  SLDL  R0,27                   TEST IF END OF LINE
         LTR   R1,R1
         BZ    MULT32                  YES, BRANCH TO MULT32
         SRDL  R0,27
         CR    R9,R5                   TEST IF ANY DATA TO BE PRINTED
         BNL   NFPARM
         BAL   R3,OUTPUT               YES, PRINT HEADING
         STC   R1,IF16                 TEST IF TIME FOR ONE STEP
         NI    IF16,X'0F'
         BNZ   LABAA
         LA    R8,1(,R8)
LABAA    BAL   R4,INSETDA
         B     NFPARM
*
***********************************************************************
*
*        CONVERSION OF DATA FOR PRINTING AND EDITING OF THE OUTPUT
*
***********************************************************************
*
TRANSDAT CR    R9,R5                   MORE DATA IS TO BE EDITED ?
         BNLR  R4
         SLDL  R0,27
         LTR   R1,R1                   PRINT BUFFER IF END OF LINE
         BZ    SETDISP
         SLDL  R0,1
         LTR   R1,R1                   IF INDEX MULTIPLE OF 16 THEN
         BNZ   SHIFTB                  MAKE ONE EXTRA SPACE
         LA    R8,1(,R8)
SHIFTB   SRDL  R0,28
TRANS    UNPK  0(9,R8),0(5,R9)         UNPACK HEXADECIMAL DATA FOR
         MVI   8(R8),C' '              PRINTING
         TR    0(8,R8),TRTABLE-240
         LA    R1,4(,R1)
         LA    R8,10(,R8)
         LA    R9,4(,R9)
         B     TRANSDAT
*
***********************************************************************
*
*        DISPLACEMENT FOR DATA EDITED
*
***********************************************************************
*
SETDISP  SRDL  R0,27
         BAL   R3,OUTPUT
         TM    FLAG,X'02'              CHECK IF EDITING ARRAYS TO GET
         BO    ADDR                    RIGHT DISPLACEMENT
INSETDA  LA    R3,24(,R1)
INSETD   ST    R3,WORKD                UNPACK HEXADECIMAL ADDR FOR
         UNPK  BUFFER(7),WORKD+1(4)    PRINTING
         OI    BUFFER+6,C' '
         TR    BUFFER(6),TRTABLE-240
         TM    FLAG,X'80'              PRINTING IS TO BE CONTINUED ?
         BZ    INSETDB                 AND NOT START AT THE
         LA    R8,BUFFER+9             BEGINNING OF A NEW LINE
INSETDB  OI    FLAG,X'80'
         B     TRANS
*
ADDR     LR    R3,R1                   DISPLACEMENT FOR ARRAY
         B     INSETD
*
***********************************************************************
*
*        EDITING OF DECLARED ARRAYS
*
***********************************************************************
*
ARRAYS   ICM   R2,B'1111',12(CDSA)     ANY DECLARED ARRAYS ?
         BZ    VALUE
MOREARRY OI    FLAG,X'02'
VARRSMF  L     R9,8(R2,CDSA)           STARTING ADDR OF ARRAY
         ST    R2,WORKD                UNPACK HEXADECIMAL DISPLACEMENT
         UNPK  ERM06SMF,WORKD+1(4)     OF SMF FOR PRINTING
         MVI   ERM06SMF+6,C' '
         TR    ERM06SMF(6),TRTABLE-240
         MVC   BUFFER+9(ERM06L),IHIERM06
         L     R5,12(R2,CDSA)          END ADDR OF ARRAY
         SR    R1,R1
         BAL   R4,TRANSDAT
         L     R2,0(R2,CDSA)           GET CHAINING ADDR
         LA    R2,0(,R2)
         OI    FLAG,X'04'              SET SKIP FLAG
         BAL   R3,OUTPUT               PRINT LAST LINE
         LTR   R2,R2
         BNZ   MOREARRY
*
***********************************************************************
*
*        EDITING OF VALUE ARRAYS
*
***********************************************************************
*
VALUE    LH    R2,10(,CDSA)            TEST FOR VALUE ARRAYS
         LTR   R2,R2
         BZ    ENDVALAR
VALARRAY OI    FLAG,X'02'
         ST    R2,WORKD                UNPACK HEX DISPLACEMENT OF
         UNPK  ERM07SMF,WORKD+1(4)     SMF FOR PRINTING
         MVI   ERM07SMF+6,C' '
         TR    ERM07SMF(6),TRTABLE-240
         MVC   BUFFER+9(ERM07L),IHIERM07   TO BUFFER
         L     R9,0(R2,CDSA)           ADDR OF SMF
         L     R5,12(,R9)              END OF ARRAY
         L     R9,8(,R9)               BEGINNING OF ARRAY
         SR    R1,R1
         BAL   R4,TRANSDAT             PRINT DATA
         OI    FLAG,X'04'
         BAL   R3,OUTPUT               PRINT LAST LINE
         L     R9,0(R2,CDSA)           GET CHAINING DISPLACEMENT
         LH    R2,2(,R9)
         LTR   R2,R2
         BZ    ENDVALAR
         B     VALARRAY
*
ENDVALAR BAL   R3,OUTPUT               PRINT ONE EXTRA BLANK LINE
         OI    FLAG,X'04'
         NI    FLAG,X'FD'              RESET ARRAY FLAG
         B     RELDSA
*
***********************************************************************
*
*        PRINT THE MSG BUFFER BY USING THE IHIIORNX ROUTINE
*
***********************************************************************
*
OUTPUT   STM   R5,R8,40(R12)           SAVE REGS USED
         LA    R6,1                    SET THE DATASET NUMBER TO 1
         L     R5,ADSTAB(,R12)         R5 -> DSTAB
         LA    R5,DSTABLEL+4(,R5)      R5 -> DSTABLE ENTRY FOR DSN 1
         L     R8,R                    R8 = CHARACTER PTR
         OI    DSF,DS3                 SET DS3 = 1, DATA IN BLOCK
         MVC   0(L'BUFFER,R8),BUFFER   MOVE BUFFER TO OUTPUT
         LR    R0,R14                  SAVE ADDR OF RETURN STACK PTR
         L     R15,VIORNX
         BALR  R14,R15                 CALL NEXT RECORD ROUTINE
         TM    FLAG,X'04'
         BZ    NOFLAG
         LH    R6,S                    INCREASE S-COUNTER IN DSTAB
SCOUNT   LA    R6,2(,R6)
         STH   R6,S
         L     R5,R                    R5 = CHARACTER POINTER
         BCTR  R5,0                    DECR ADDR FOR ASA CNTL CHAR
         CLI   0(R5),C'1'              SKIP TO NEW PAGE ?
         BE    RESFLAG                 YES, BRANCH
         MVC   0(1,R5),SPACE           NO
RESFLAG  NI    FLAG,X'FB'              RESET SKIP FLAG
NOFLAG   LR    R14,R0                  RESTORE RETURN STACK PTR
         MVI   BUFFER,C' '             BLANK BUFFER AFTER PRINTING
         MVC   BUFFER+1(L'BUFFER-1),BUFFER
         LM    R5,R8,40(R12)
         BR    R3                      RETURN TO CALLER
*
SAVEAREA DC    18F'0'
*
WORKD    DC    D'0'
TRTABLE  DC    C'0123456789ABCDEF'     TRANSLATION TABLE
FLAG     DC    X'84'
SPACE    DC    C'-'                    ASA CNTL CHAR
IF16     DS    0C
DSNUMBER DC    X'00'
BUFFER   DC    CL90' '                 MESSAGE BUFFER
SCPATTN  DC    X'402020202020'         PATTERN FOR SEMICOLON COUNT
BNPATTN  DC    X'40202020'             PATTERN FOR BLOCK NUMBER
*
*        HEADINGS
*
IHIERM02 DC    C'MODULE = '
ERM02M   DC    CL8' '
         DC    C'  PROGRAM BLOCK NUMBER = '
ERM02BN  DC    C'    '
         DC    C' ('
ERM02TP  DC    CL16' '
ERM02L   EQU   *-IHIERM02              L'IHIERM02
*
ERM02BK  DC    CL16'BLOCK)          '  *
         DC    CL16'PROCEDURE)      '  ×
         DC    CL16'TYPE PROCEDURE) '  V
*
KFORMAL  DC    C'FORMAL PARAMETERS'
*
IHIERM05 DC    C'DECLARED IDENTIFIERS AND OBJECT TIME STACK'
*
IHIERM06 DC    C'SMF DISPLACEMENT IN DSA = '
ERM06SMF DC    CL7' '
         DC    C'DECLARED ARRAY'
ERM06L   EQU   *-IHIERM06              L'IHIERM06
*
IHIERM07 DC    C'SMF DISPLACEMENT IN DSA = '
ERM07SMF DC    CL7' '
         DC    C'ARRAY CALLED BY VALUE'
ERM07L   EQU   *-IHIERM07              L'IHIERM07
*
***********************************************************************
*
*        EXTERNAL ADDRS

***********************************************************************
*
*        PASSED IN PARAMETER LIST FROM CALLER IHIFSA
*
VFRDSA   DC    A(0)      V(FRDSA)
VSPDAP   DC    A(0)      V(SPDAP)
VIOROP   DC    A(0)      V(IHIIOROP)
VIORCP   DC    A(0)      V(IHIIORCP)
VIORNX   DC    A(0)      V(IHIIORNX)
*
*        ADDRS IN IHIERMSG MODULE
*
VERMSG   DC    V(IHIERMSG)
VERM01   DC    V(IHIERM01)
*
FSARE    DSECT
*
         COPY  FSAREA
*
*        DATASET TABLE MAPPING DSECT
*
         DSTABLE DSECT=YES
*
*        REGISTER EQUATES
*
         IEZREGS
*
         END
