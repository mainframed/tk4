QRT      TITLE 'IHISSQRT, SQUARE ROOT, SHORT PRECISION, ALGOL F LIB'
*
*        COMPONENT ID - 360S-LM-532 ALGOL F LIBRARY
*
*        STATUS - LEVEL 2.1
*
*        FUNCTION/OPERATION -
*        WRITE X = M*16**(2P-Q), M MANTISSA, Q=0 OR 1
*        THEN SQRT(X) = SQRT(M*16**-Q)*16**P
*
*        ENTRY POINT -
*        IHISSQ - SQRT FUNCTION, SHORT
*                 LA   R1,PARMLIST
*                 BALR R14,R15
*                 DATA PASSED BY NAME
*        THE MODULE IS ENTERED FROM THE GENERATED OBJECT MODULE
*
*        INPUT - N/A
*
*        OUTPUT - N/A
*
*        EXTERNAL ROUTINES - N/A
*
*        EXIT - NORMAL -
*        RETURN VIA R14, RESULT IN FPR0
*
*        EXIT - ERROR -
*        IF ARGUMENT NEGATIVE GOTO ERROR ROUTINE VIA
*        B   FSAERR+23*4(R13)
*
*        TABLES/WORKAREAS - N/A
*
IHISSQRT CSECT
*
         ENTRY IHISSQ
*
FPR0     EQU   0                       RESULT REGISTER
FPR2     EQU   2                       SCRATCH REGISTERS
FPR4     EQU   4
*
IHISSQ   SAVE  (14,12),,'IHISSQRT LEVEL 2.1 &SYSDATE &SYSTIME'
*
         USING IHISSQRT,R15
         L     R1,0(,R1)
         L     R0,0(,R1)               OBTAIN ARGUMENT
         LE    FPR4,0(,R1)
         LTR   R0,R0
         BZ    ZRANS                   ARG IS 0, RESULT IS 0
         BM    ERROR                   NEGATIVE ARG, ERROR
         AL    R0,BIAS                 ADD X'41' TO CHAR
         SRDL  R0,25
         SLL   R0,24                   CHAR OF RESULT IS READY
         LR    R14,R0                  KEEP IT IN R14
         LTR   R1,R1                   SIGN BIT OF R1 = 1-Q
         BM    LABAA
         A     R14,FOUR                Q=1,ADD 4 (B31) TO R14 FOR INDEX
LABAA    SRL   R1,3                    SCALE 1+M (Q=0) OR M (Q=1) TO B3
         A     R1,C(R14)               OBTAIN FIRST APPROXIMATION BY
*                                      A HYPERBOLIC FIT OF THE
         L     R0,B(R14)               RESPECTIVE INTERVAL
         DR    R0,R1                   Q=1, INTERPRET M AS M/16 (B-1)
         A     R1,A(R14)
         AR    R1,R14                  ADD ON CHAR TO COMPLETE FIRST
         ST    R1,BUFF                 APPROXIMATION
         LER   FPR0,FPR4               GIVE 2 PASSES OF NEWTON-RAPHSON
         DE    FPR0,BUFF               ITERATION
         AE    FPR0,BUFF
         HER   FPR0,FPR0
         DER   FPR4,FPR0
         SER   FPR4,FPR0               Y2 =(Y1+X/Y1)/2 = (X/Y1-Y1)/2+Y1
         HER   FPR4,FPR4               USE THE LATTER TO PROTECT
         AER   FPR0,FPR4               LAST DIGIT
*
FIN      RETURN (14,12)                RESTORE REGS AND RETURN
*
ZRANS    SER   FPR0,FPR0
         B     FIN
*
ERROR    SLL   R0,1
         LTR   R0,R0                   NEGATIVE ZERO ?
         BZ    ZRANS
         B     FSAERR+23*4(R13)        NEGATIVE PARAMETER
*
FSAERR   EQU   X'1CC'
*
BUFF     DC    F'0'
BIAS     DC    X'41000000'
FOUR     DC    F'4'
A        DC    X'01CE9FE0'             1.8071270   A0 (B7)
         DC    X'006DC57C'             0.4287950   A1 (B7) MINUS 4(B31)
B        DC    X'FFE6C37D'            -1.5772732   B0 (B11)
         DC    X'FFFA82EB'            -0.0214398   B1 (B7)
C        DC    X'FF44546F'             0.95418214  C0 (B3) MINUS 1(B3)
         DC    X'0E0A7419'             0.0548470   C1 (B-1)
*
*        REGISTER EQUATES
*
         IEZREGS
*
         END
