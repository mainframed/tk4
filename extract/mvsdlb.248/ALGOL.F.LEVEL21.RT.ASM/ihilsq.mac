QRT      TITLE 'IHILSQRT, SQUARE ROOT, LONG PRECISION, ALGOL F LIB'
*
*        COMPONENT ID - 360S-LM-532 ALGOL F LIBRARY
*
*        STATUS - LEVEL 2.1
*
*        FUNCTION/OPERATION -
*        WRITE X = M*16**(2P-Q), M MANTISSA, Q=0 OR 1
*        THEN SQRT(X) = SQRT(M)*(4**-Q)*(16**P)
*
*        ENTRY POINT -
*        IHILSQ - SQRT FUNCTION, LONG
*                 LA   R1,PARMLIST
*                 BALR R14,R15
*                 DATA PASSED BY NAME
*        THE MODULE IS ENTERED FROM THE GENERATED OBJECT MODULE
*
*        INPUT - N/A
*
*        OUTPUT - N/A
*
*        EXTERNAL ROUTINES - N/A
*
*        EXIT - NORMAL -
*        RETURN VIA R14, RESULT IN FPR0
*
*        EXIT - ERROR -
*        IF ARGUMENT NEGATIVE GOTO ERROR ROUTINE VIA
*        B   FSAERR+23*4(R13)
*
*        TABLES/WORKAREAS - N/A
*
IHILSQRT CSECT
*
         ENTRY IHILSQ
*
FPR0     EQU   0                       RESULT REGISTER
FPR2     EQU   2                       SCRATCH REGISTERS
FPR4     EQU   4
*
IHILSQ   SAVE  (14,12),,'IHILSQRT LEVEL 2.1 &SYSDATE &SYSTIME'
*
         USING IHILSQRT,R15
         L     R1,0(,R1)
         LD    FPR0,0(,R1)             OBTAIN ARGUMENT
         LTDR  FPR4,FPR0               ARG = 0 ?
         BZ    FIN                     YES, ANS = 0, RETURN
         BM    ERROR                   NEGATIVE ARG, ERROR
         STE   FPR0,BUFF               CONSTRUCT Y = M*16**P AS FOLLOWS
         SR    R0,R0
         IC    R0,BUFF                 P+64 = INT PART OF (CHAR+65)/2
         AH    R0,BIAS
         SRDL  R0,1                    P+64 IN R0, R1 IS - IF EVEN CHAR
         STC   R0,BUFF                 Y IS READY IN BUFF
         STC   R0,B                    CONSTRUCT (2/9)*16**P IN B
         LE    FPR0,BUFF               COMPUTE ((2/9+(8/9)*M)*16**P
         ME    FPR0,A
         AE    FPR0,B
         LTR   R1,R1
         BM    OK                      EVEN CHAR, 1ST APPROX IS READY
         HER   FPR0,FPR0               ODD CHAR, DIVIDE BY 4 TO OBTAIN
         HER   FPR0,FPR0               1ST APPROXIMATION
OK       LER   FPR2,FPR4               NEWTON RAPHSON ITERATIONS
         DER   FPR2,FPR0               Y(N+1) = (Y(N)+ARG/Y(N))/2
         AER   FPR0,FPR2               2 PASSES IN SHORT FORM
         HER   FPR0,FPR0
         LER   FPR2,FPR4
         DER   FPR2,FPR0
         AER   FPR0,FPR2
         HER   FPR0,FPR0
         LDR   FPR2,FPR4               2 PASSES IN LONG FORM
         DDR   FPR2,FPR0
         HDR   FPR2,FPR2
         HDR   FPR0,FPR0
         ADR   FPR0,FPR2
         DDR   FPR4,FPR0
         SWR   FPR4,FPR0
         HDR   FPR4,FPR4
         ADR   FPR0,FPR4
*
FIN      RETURN (14,12)                RESTORE REGS AND RETURN
*
ERROR    B     FSAERR+23*4(R13)        NEGATIVE PARAMETER
*
FSAERR   EQU   X'1CC'
*
BUFF     DC    F'0'
A        DC    X'40E38E39'
B        DC    X'4038E38E'
*
BIAS     DC    H'65'
*
*        REGISTER EQUATES
*
         IEZREGS
*
         END
