SYS      TITLE 'IHISYSCT, SYSACT I/O SERVICE ROUTINES, ALGOL F LIB'
*
*        COMPONENT ID - 360S-LM-532 ALGOL F LIBRARY
*
*        STATUS - LEVEL 2.1
*
*        FUNCTION/OPERATION -
*        CONSISTS OF 15 ROUTINES WITH DIFFERENT ACTIONS ON DATASET
*        WHICH DEPENDING ON SECOND PARAMETER IN SYSACT
*
*        ENTRY POINTS -
*        IHISYSCT - FROM GENERATED OBJECT MODULE
*                   LA   R1,PARMLIST
*                   BALR R14,R15
*                   DATA PASSED BY NAME
*
*        INPUT - SYSACT4 READS TWO RECORDS AFTER REPOSITIONING
*
*        OUTPUT - N/A
*
*        EXTERNAL ROUTINES -
*        IHIIOR - EVALUATE DATASET NUMBER
*               - OPEN DATASET
*               - CHANGE TO NEXT RECORD
*               - CLOSE DATASET
*               - CONVERT REAL TO INTEGER
*
*        EXITS - NORMAL - RELOAD REGISTERS RETURN VIA R14
*              - ERROR  -
*                DATASET NUMBER OUT OF RANGE                      NO 0
*                INCOMPATIBLE ACTIONS ON SAME DATASET             NO 2
*                INPUT REQUEST BEYOND END OF DATASET              NO 5
*                UNDEFINED FUNCTION NUMBER                        NO 9
*                DATASET CLOSED                                   NO 10
*                DATASET OPEN                                     NO 11
*                QUANTITY IN SYSACT PROCEDURE MUST BE A VARIABLE  NO 12
*                QUANTITY IN SYSACT PROCEDURE OUT OF RANGE        NO 13
*                BACKWARD REPOSITIONING NOT DEFINED               NO 14
*                BRANCH TO IHIFSA
*                          L  R13,IHIFSA
*                          B  FSAERR+XX*4(R13)       XX  ERROR NO
*
*        TABLES/WORK AREAS - N/A
*
*        ATTRIBUTES - SERIALLY REUSABLE
*
*        NOTES -
*        A FULL DESCRIPTION OF THE PARAMETERS PASSED TO SYSACT MAY
*        BE FOUND IN GC28-6615 ALGOL LANGUAGE P82 - P89
*        'PROCEDURE' SYACT (DATASET NUMBER,FUNCTION,QUANTITY);
*        DATASET NUMBER -
*                0 - 15
*
*
*        REGISTERS
*        R1  -> PARAMETER LIST
*        R5  -> DSTABLE ENTRY FOR DATASET
*        R6   = DATASET NUMBER
*        R8  -> DCB AND DECB
*        R12 -> FSA
*        R13 -> SAVE AREA IN FSA
*        R14 -> RETURN
*        R15 -> ROUTINE SYSACT
*
IHISYSCT CSECT
*
*        FLOATING POINT REGISTER
*
FR0      EQU   0
*
*        DISPLACEMENTS IN ADRLST IN IHIFSA
*
*                                      DISPLACEMENT FOR -
CI       EQU   0                       IHIIORCI
CL       EQU   4                       IHIIORCL
EV       EQU   8                       IHIIOREV
NX       EQU   12                      IHIIORNX
OP       EQU   16                      IHIIOROP
OQ       EQU   20                      IHIIOROQ
*
         SAVE  (14,12),,'IHISYSCT LEVEL 2.1 &SYSDATE &SYSTIME'
*
         LR    R7,R15                  LOAD BASE REGISTER
         USING IHISYSCT,R7
         USING IHADCB,R8
         LR    R12,R13                 R12 -> FIXED STORAGE AREA
         ST    R13,SAVEAR+4
         LA    R13,SAVEAR
*
*        EVALUATE FIRST PARAMETER
*
*        DATASET NUMBER 0 - 15
*
         L     R15,IORLST(,R12)        R15 -> COMMON I/O ROUTINES
         L     R15,EV(,R15)            EVALUATE DATASET NUMBER
         BALR  R14,R15
*
*        ON RETURN -
*        R6  = DATASET NUMBER
*        R5 -> DSTABLE ENTRY
*
         USING DSTABLE,R5
*
*        EVALUATE SECOND PARAMETER
*
*        FUNCTION 1 - 15
*
         ICM   R2,B'1111',4(R1)
         BNM   SYS1
         TM    OPTSW(R12),X'20'        CONVERSION TO INTEGER TEST PRE
         BO    SYS11                   SHORT
         LD    FR0,0(,R2)              LONG
         B     SYS11A
*
SYS11    LE    FR0,0(,R2)
SYS11A   L     R15,IORLST(,R12)
         L     R15,CI(,R15)
         BALR  R14,R15                 CONVERT TO INTEGER
         B     SYS1A
*
SYS1     L     R0,0(,R2)
SYS1A    LTR   R2,R0                   R2 = FUNCTION NUMBER
         BNP   SYSERR9                 ZERO OR NEG, FUNCT OUT OF RANGE
         C     R2,=F'15'               EXCEED FUNCTION RANGE ?
         BH    SYSERR9                 YES, ERROR CONDITION
         SLA   R2,2                    CONVERT TO OFFSET
         L     R3,SYSVECT(R2)          R3 -> REQUESTED SYSACT ROUTINE
*
*        EVALUATE THIRD PARAMETER
*
*        QUANTITY - VALUE VARIES ACCORDING TO FUNCTION
*
         SR    R9,R9                   ZERO FLAG REG
         L     R2,8(,R1)
         SLL   R2,1
         LTR   R2,R2
         BM    SYS11B
         LA    R9,1(,R9)               QUANTITY IS A VARIABLE
SYS11B   SRL   R2,1
         N     R2,=X'00FFFFFF'
         L     R4,0(,R2)
*
*        R3 -> REQUESTED SYSACT RTN
*        R2 -> QUANTITY
*        R4  = QUANTITY
*        R9 EQUAL ONE IF QUANTITY IS A VARIABLE ELSE ZERO
*
         BR    R3                      -----> TO SYSACT ROUTINE
*
RETSYS   L     R13,SAVEAR+4
*
         RETURN (14,12)                RESTORE CALLERS REGS AND RETURN
*
SYSERR9  LR    R13,R12                 UNDEFINED FUNCTION NUMBER ERR9
         B     FSAERR+9*4(,R12)
*
*        BRANCH TO NEXTREC IN IHIIOR
*
NXREC    L     R15,IORLST(,R12)
         L     R15,NX(,R15)
         BR    R15
*
*----------------------------------------------------------------------
*
*        ROUTINE SYSACT1
*
*----------------------------------------------------------------------
*
*        ASSIGN CURRENT VALUE OF CHARACTER POINTER TO R
*
*        QUANTITY = R
*
SYSACT1  TM    DSF,DS0
         BO    SYS1T1
SYSCLOSD LR    R13,R12                 DATASET IS CLOSED ERROR 10
         B     FSAERR+10*4(,R12)
*
SYS1T1   LTR   R9,R9                   QUANTITY A VARIABLE ?
         BP    SYS1T2                  YES, BRANCH
SYSCONST LR    R13,R12                 NO, QUANTITY MUST BE A VARIABLE
         B     FSAERR+12*4(,R12)       ERROR12
*
SYS1T2   L     R4,R
         S     R4,RE
         AH    R4,P
         LA    R4,1(,R4)
         ST    R4,0(,R2)
         B     RETSYS
*
*----------------------------------------------------------------------
*
*        ROUTINE SYSACT2
*
*----------------------------------------------------------------------
*
*        R = QUANTITY
*
SYSACT2  LTR   R4,R4
         BP    SYSACT2A
         B     SYS2T1
*
SYSACT2A CH    R4,P
         BNH   SYS2T1A
SYS2T1   LR    R13,R12
         B     FSAERR+13*4(,R12)       QUANTITY OUT OF RANGE ERROR 13
*
SYS2T1A  TM    DSF,DS0                 DATASET OPEN ?
         BZ    SYSCLOSD                NO, DATASET IS CLOSED
         LR    R3,R4                   YES
         A     R3,RE
         SH    R3,P
         BCTR  R3,0
         S     R3,R
         BP    SYS2T2
*
*        NEW CHARACTER POINTER LESS OR EQUAL R
*
         TM    DSF,DS2                 LAST I/O OUTPUT ?
         BO    SYS2T3                  YES, BRANCH
*                                      LAST I/O PROCEDURE WAS INPUT
         TM    DSF+1,DS10              DATASET OPENED BY SYSACT 12 ?
         BO    SYS2T30                 YES, BRANCH
         TM    DSF,DS7                 EOD BEEN REACHED ?
         BO    SYSEOD                  YES, BRANCH
         BAL   R14,NXREC               LAST I/O PROCEDURE WAS INPUT
         TM    DSF,DS7                 END OF DATA REACHED ?
         BZ    SYS2T1B                 NO, BRANCH
         LR    R3,R4                   QUANTITY = 1 ?
         BCT   R3,SYSEOD               NO, INPUT REQUEST BEYOND EOD
SYS2T1B  A     R4,RE
         SH    R4,P                    ASSIGN A NEW VALUE TO CHARACTER
         BCTR  R4,0
         ST    R4,R
         B     RETSYS
*
SYS2T30  LTR   R6,R6                   DATASET NUMBER = 0 ?
         BZ    SYSINCOM                YES, BRANCH
         OI    DSF,DS2                 SET LAST I/O OUTPUT
*
*        LAST I/O PROCEDURE WAS OUTPUT
*
SYS2T3   L     R3,RE                   CALCULATE NO OF BLANKS REQUIRED
         S     R3,R
         BZ    SYS2T31                 NONE REQUIRED, BRANCH
         LA    R15,SYBLANK             FILL RECORD WITH BLANKS
         BALR  R14,R15
SYS2T31  BAL   R14,NXREC
*
         LR    R3,R4
         BCTR  R3,0
         LTR   R3,R3
         BZ    SYS2T32
         OI    DSF,DS3
         LA    R15,SYBLANK             FILL RECORD WITH BLANKS
         BALR  R14,R15
SYS2T32  L     R11,R
         AR    R4,R11
         BCTR  R4,0                    CHARACTER POINTER
         ST    R4,R                    QUANTITY+R-1 ASSIGN TO R
         B     RETSYS
*
*        NEW CHARACTER POINTER GREATER THAN R
*
SYS2T2   TM    DSF,DS2                 LAST I/O OUTPUT ?
         BO    SYS2T20                 YES, BRANCH
         TM    DSF+1,DS10              DATASET OPENED BY SYSACT 12 ?
         BZ    SYS2T5                  NO, BRANCH
         LTR   R6,R6                   DATASET NUMBER = 0 ?
         BZ    SYSINCOM                YES, BRANCH
         OI    DSF,DS2                 SET LAST I/O OUTPUT
SYS2T20  LR    R3,R4                   LAST I/O WAS OUTPUT
         A     R3,RE
         S     R3,R
         OI    DSF,DS3
         SH    R3,P
         BCTR  R3,0
         LA    R15,SYBLANK             FILL RECORD WITH BLANKS
         BALR  R14,R15
*
SYS2T5   TM    DSF,DS7                 EOD BEEN REACHED ?
         BO    SYSEOD                  YES, BRANCH
SYS2T4   A     R4,RE
         SH    R4,P                    ASSIGN NEW VALUE TO CHAR POINTER
         BCTR  R4,0
         ST    R4,R
         B     RETSYS
*
*----------------------------------------------------------------------
*
*        ROUTINE SYSACT3
*
*----------------------------------------------------------------------
*
*        QUANTITY = S
*
SYSACT3  TM    DSF,DS0                 DATASET OPEN ?
         BZ    SYSCLOSD                NO, DATASET CLOSED, ERROR10
         LTR   R9,R9
         BZ    SYSCONST                QUANTITY NOT A VARIABLE ERR 12
         LH    R4,S
         ST    R4,0(,R2)
         B     RETSYS
*
*----------------------------------------------------------------------
*
*        ROUTINE SYSACT4
*
*----------------------------------------------------------------------
*
*        UNDEFINED IF DATASET SPLIT INTO SECTION BY SYSACT8 OR
*        IF THE DATASET IS 0 OR 1 (SYSIN/ALGLDD01)
*
*        ACTION AFTER INPUT - S = QUANTITY
*                             R = 1
*        ACTION AFTER OUTPUT - S = QUANTITY
*                              R = 1
*        IF FORWARD SKIPPING FILL SKIPPED RECORDS WITH BLANKS
*
SYSACT4  L     R8,ADCB                 R8 -> DCB
         LTR   R4,R4                   TEST QUANTITY
         BP    SYS4T01
         LR    R13,R12
         B     FSAERR+13*4(,R12)       QUANTITY OUT OF RANGE
*
SYS4T01  TM    DSF+1,DS9               DATASET SECTIONED ?
         BO    SYSINCOM                YES, BRANCH
         LTR   R2,R6                   DATASET NO = 1 OR 0 ?
         BZ    SYSINCOM                DATASET ZERO, BRANCH
         BCT   R2,SYS4T0               BATASET ONE, BRANCH
         B     SYSINCOM
*
SYS4T0   TM    DSF,DS0                 DATASET OPEN ?
         BZ    SYSCLOSD                NO, BRANCH
         CH    R4,S                    QUANTITY > S ?
         BL    SYS4T2                  NO, LESS
         BE    SYS4T3                  NO, EQUAL
SYS4T1   TM    DSF,DS2                 LAST I/O OUTPUT ?
         BO    SYS4T15                 YES, BRANCH TO SYS4T15
*
*        LAST I/O INPUT AND QUANTITY > S
*
         TM    DSF+1,DS10              DATASET OPENED BY SYSACT 12 ?
         BZ    SYS4T12                 NO, BRANCH
         OI    DSF,DS2                 SET LAST I/O OUTPUT
         B     SYS4T15
*
SYS4T12  LA    R15,SYSNOT              QUANTITY IN NOTTAB ?
         BALR  R14,R15
         TM    DSF,DS7                 EOD BEEN REACHED ?
         BO    SYSEOD                  YES, BRANCH
         LTR   R3,R3
         BP    SYS4T21                 YES, BRANCH TO SYS4T21
SYS4T13  TM    DSF,DS7                 END OF DATA BEEM REACHED ?
         BO    SYSEOD                  YES, BRANCH
         BAL   R14,NXREC               GET NEXT RECORD
         CH    R4,S                    QUANTITY = S ?
         BNE   SYS4T13
         B     RETSYS
*
*        LAST I/O OUTPUT AND QUANTITY > S
*
SYS4T15  L     R3,RE                   FILL RECORD WITH BLANKS
         S     R3,R
         BZ    SYS4T14
         LA    R15,SYBLANK
         BALR  R14,R15
SYS4T14  BAL   R14,NXREC
         CH    R4,S                    QUANTITY = S ?
         BNE   SYS4T15
         B     RETSYS
*
SYS4T2   LA    R15,SYSNOT              QUANTITY IN NOTTAB ?
         BALR  R14,R15
         LTR   R3,R3                   NOTE TAB ENTRY FOUND ?
         BZ    SYSERR14                NO BACKWARD REP. NOT DEFINED
         TM    DSF,DS2                 LAST I/O OUTPUT ?
         BO    SYS4T24
*
*        LAST I/O INPUT AND QUANTITY < S
*
         OI    DSF,DS5                 SET DS5 BACKWARD POSITIONING
         NI    DSF,255-DS6-DS7         SET OFF OPEN FOR OUTPUT AND EOD
SYS4T21  OI    DSF+1,DS8               SET ON NO MORE DATA
*
         CHECK SYDECB
*
         NI    DSF+1,255-DS8           SET OFF NO MORE DATA
SYS4T22  LR    R0,R10
*
         POINT (R8),(0)
*
         L     R2,NBB                  R2 -> ALTERNATIVE BUFFER
*
         READ  SYDECB,SF,(R8),(R2),MF=E
*
         L     R2,BB
         AH    R2,BL
         ST    R2,RE
         LR    R2,R4
         BCTR  R2,0
         STH   R2,S                    S = QUANTITY - 1
         BAL   R14,NXREC
         TM    DSF,DS1                 DATASET BLOCKED ?
         BO    RETSYS                  YES, BRANCH
         SR    R2,R2                   NO, EVALUATE NUMBER OF FIRST
         L     R3,BL                   RECORD IN THE BLOCK =
         SRL   R3,16                   ENTIER((QUANTITY-1)/(BLKLTH/
         L     R1,S                    RECLNTH))*(BLKLNTH/RECLNTH)+1
         SLL   R1,16
         SRL   R1,16
         DR    R2,R1
         LR    R1,R3
         SR    R2,R2
         LR    R3,R4
         BCTR  R3,0
         DR    R2,R1
         MR    R2,R1
         LA    R3,1(,R3)
         STH   R3,S
SYS4T23  CH    R4,S                    QUANTITY = S ?
         BE    RETSYS
         BAL   R14,NXREC
         B     SYS4T23
*
*        LAST I/O WAS OUTPUT AND QUANTITY < S
*
SYS4T24  TM    DSF,DS1                 DATASET BLOCKED ?
         BO    SYS4T25
         L     R3,BB
         AH    R3,BL
         ST    R3,RE
SYS4T25  L     R3,RE                   FILL BLOCK WITH BLANKS
         S     R3,R
         BZ    SYS4T26
         LA    R15,SYBLANK
         BALR  R14,R15
SYS4T26  BAL   R14,NXREC
*
SYS4T27  CHECK SYDECB
*
         CLOSE ((R8),LEAVE),TYPE=T     SET END OF DATA MARK
*
         OI    DSF,DS5                 SET BACKWARD POSITIONING
         NI    DSF,255-DS2-DS6         SET DS2 AND DS6 = 0
         B     SYS4T22
*
SYS4T3   TM    DSF,DS2
         BZ    SYS4T31
*
*        LAST I/O OUTPUT AND QUANTITY = S
*
         OI    DSF,DS4
         L     R3,BB
         AH    R3,BL
         ST    R3,RE
         S     R3,R                    FILL BLOCK WITH BLANKS
         BZ    SYS4T33
         LA    R15,SYBLANK
         BALR  R14,R15
SYS4T33  BAL   R14,NXREC               WRITE BLOCK
         LA    R15,SYSNOT              MAKE NOTTAB ENTRY
         BALR  R14,R15
         B     SYS4T27
*
*        LAST I/O INPUT AND QUANTITY = S
*
SYS4T31  L     R2,RE
         SH    R2,P
         ST    R2,R                    RESET CHARACTER POINTER
         B     RETSYS
*
SYSEOD   LR    R13,R12
         B     FSAERR+5*4(,R12)
*
*        SYSNOT
*
*        SCAN NOTTAB IN ORDER TO DETERMINE IF AN ENTRY HAS BEEN
*        MADE FOR QUANTITY
*        IF YES R3 IS SET TO ONE, RETURN ADDR OF NOTEADR IN R10
*
SYSNOT   ICM   R3,B'1111',ANOTTAB(R12)  R3 -> NOTE TABLE
         BZ    SYSNOT2                 NO NOTE TABLE ALLOCTED
         LR    R10,R3
SYSNOT1  C     R10,0(,R3)
         BE    SYSNOT2
         LA    R10,8(,R10)
         LR    R2,R6
         CH    R2,0(,R10)
         BNE   SYSNOT1
         CH    R4,2(,R10)
         BNE   SYSNOT1
         LA    R3,1                    ENTRY FOUND SET VALUE OF 1
         LA    R10,4(,R10)
         BR    R14                     RETURN
*
SYSNOT2  SR    R3,R3                   NO ENTRY FOUND, ZERO R3
         BR    R14                     RETURN
*
SYSERR14 LR    R13,R12                 BACKWARD REPOSITIONING NOT
         B     FSAERR+14*4(,R12)       DEFINED
*
*----------------------------------------------------------------------
*
*        ROUTINE SYSACT5
*
*----------------------------------------------------------------------
*
*        QUANTITY = P
*
SYSACT5  TM    DSF,DS0                 DATASET OPEN ?
         BZ    SYSCLOSD                NO, DATASET CLOSED ERROR10
         LTR   R9,R9
         BZ    SYSCONST                QUANTITY NOT A VARIABLE ERR 12
         LH    R4,P
         ST    R4,0(,R2)
         B     RETSYS
*
*----------------------------------------------------------------------
*
*        ROUTINE SYSACT6
*
*----------------------------------------------------------------------
*
*        P = QUANTITY
*        DATASET HAS TO BE CLOSED
*
SYSACT6  TM    DSF,DS0                 DATASET OPEN ?
         BO    SYSOPEN                 YES, BRANCH
         LTR   R4,R4                   TEST QUANTITY
         BNP   SYSQOUTR                MINUS OR ZERO
         C     R4,=F'32760'
         BH    SYSQOUTR                TOO GREAT
         STH   R4,P
         B     RETSYS
*
SYSOPEN  LR    R13,R12                 DATASET IS OPEN ERROR 11
         B     FSAERR+11*4(,R12)
*
*----------------------------------------------------------------------
*
*        ROUTINE SYSACT7
*
*----------------------------------------------------------------------
*
*        QUANTITY = Q
*
SYSACT7  LTR   R9,R9
         BZ    SYSCONST                QUANTITY NOT A VARIABLE ERR 12
         SR    R4,R4
         IC    R4,Q
         ST    R4,0(,R2)
         B     RETSYS
*
*----------------------------------------------------------------------
*
*        ROUTINE SYSACT8
*
*----------------------------------------------------------------------
*
*        Q = QUANTITY
*        DATASET HAS TO BE CLOSED
*        ASSIGNING A VALUE TO Q
*
SYSACT8  TM    DSF,DS0                 DATASET IS OPEN ?
         BO    SYSOPEN                 YES, OPEN ERROR11
         LTR   R6,R6                   SYSIN/DATASET 0 ?
         BZ    SYSINCOM                YES, INCOMPATIBLE ACTION
         LTR   R3,R4                   TEST QUANTITY
         BNP   SYSQOUTR                MINUS OR ZERO
         S     R3,KF256
         BP    SYSQOUTR
         BZ    SYSQOUTR
         STC   R4,Q                    SET RECORDS PER SECTION
         OI    DSF+1,DS9               SET RECORD CONTAIN CNTL CHAR
         B     RETSYS
*
SYSDSN0  LR    R13,R12                 DATASET NUMBER OUT OF RANGE
         B     FSAERR(,R12)
*
SYSQOUTR LR    R13,R12                 QUANTITY OUT OF RANGE ERROR 13
         B     FSAERR+13*4(,R12)
*
KF256    DC    F'256'
*
*----------------------------------------------------------------------
*
*        ROUTINE SYSACT9
*
*----------------------------------------------------------------------
*
*        QUANTITY = K
*
SYSACT9  LTR   R9,R9
         BZ    SYSCONST                QUANTITY NOT A VARIABLE ERR 12
         SR    R4,R4
         IC    R4,K
         ST    R4,0(,R2)
         B     RETSYS
*
*----------------------------------------------------------------------
*
*        ROUTINE SYSACT10
*
*----------------------------------------------------------------------
*
*        K = QUANTITY
*        ASSIGN NUMBER OF BLANK DELIMTERS
*
SYSACT10 LTR   R4,R4                   TEST QUANTITY
         BNP   SYSQOUTR                MINUS OR ZERO
         C     R4,KF256
         BNL   SYSQOUTR                TOO GREAT
         STC   R4,K
         B     RETSYS
*
*----------------------------------------------------------------------
*
*        ROUTINE SYSACT11
*
*----------------------------------------------------------------------
*
*        ASSIGN VALUE TO QUANTITY FOR DEFINING IF DATASET OPEN
*        OR CLOSED
*
SYSACT11 LTR   R9,R9
         BZ    SYSCONST                QUANTITY NOT A VARIABLE ERR 12
         SR    R4,R4
         TM    DSF,DS0                 DATASET OPEN ?
         BZ    SYS11T1                 NO, DATASET IS CLOSED
         LA    R4,1(,R4)
         TM    DSF,DS7                 END OF FILE ?
         BO    SYS11T2                 YES, BRANCH
         TM    DSF,DS2                 LAST I/O OUTPUT ?
         BO    SYS11T1                 YES, OUTPUT
         TM    DSF,DS1                 DATASET BLOCKED ?
         BO    SYS11T1                 YES, BRANCH
         L     R11,R
         L     R1,RE
         SH    R1,P
         CR    R1,R11
         BNE   SYS11T1                 R NOT EQUAL RE MINUS P
         L     R1,BB
         AH    R1,BL
         SR    R1,R11
SYS11T3  CLI   0(R11),C' '             CHARACTER BLANK ?
         BNE   SYS11T1                 NO, BRANCH
         LA    R11,1(,R11)             INCR PTR
         BCT   R1,SYS11T3              LOOP
         L     R8,ADCB
*
         CHECK SYDECB
*
         TM    DSF,DS7                 END OF FILE ?
         BZ    SYS11T1                 NO, BRANCH
SYS11T2  LNR   R4,R4                   YES
SYS11T1  ST    R4,0(,R2)
         B     RETSYS
*
*----------------------------------------------------------------------
*
*        ROUTINE SYSACT12
*
*----------------------------------------------------------------------
*
*        OPEN OR CLOSE DATASET
*
SYSACT12 LTR   R4,R4                   OPEN OR CLOSE REQUEST ?
         BZ    SYS12T1                 CLOSE, BRANCH
         BCT   R4,SYSQOUTR             QUANTITY OUT OF RANGE ^ 0 OR 1
         TM    DSF,DS0                 DATASET ALREADY OPEN ?
         BO    SYS12T2                 YES, RETURN
         OI    DSF+1,DS10              SET DATASET OPENED BY SYSACT 12
         L     R15,IORLST(,R12)
         L     R15,OQ(,R15)            OPEN DATASET
         BALR  R14,R15
         B     RETSYS
*
SYS12T1  TM    DSF,DS0                 DATSET ALREDAY CLOSED ?
         BZ    SYS12T2                 YES, RETURN
         L     R15,IORLST(,R12)        CLOSE DATASET
         L     R15,CL(,R15)
         BALR  R14,R15
SYS12T2  B     RETSYS                  RETURN TO CALLER
*
*----------------------------------------------------------------------
*
*        ROUTINE SYSACT13
*
*----------------------------------------------------------------------
*
*        QUANTITY = S
*        SETTING DS4 FLAG USED BY A LATER SYSACT4 FOR AN ENTRY
*        TO NOTTAB
*
SYSACT13 TM    DSF,DS0                 DATASET OPEN ?
         BZ    SYSCLOSD                NO, DATASET IS CLOSED ERROR10
         TM    DSF+1,DS9               DATASET SECTIONED ?
         BO    SYSINCOM                YES, INCOMPATIBLE ACTION
         LTR   R3,R6                   DATASET NO = 1 OR 0 ?
         BZ    SYSINCOM                DATASET 0, ERROR
         BCT   R3,SYS13T1              DATASET ^= 1, BRANCH
*
SYSINCOM LR    R13,R12                 INCOMPATIBLE ACTION ON THE SAME
         B     FSAERR+2*4(,R12)        DATASET
*
SYS13T1  LTR   R9,R9
         BZ    SYSCONST                QUANTITY NOT A VARIABLE ERR 12
         LH    R4,S
         ST    R4,0(,R2)
         OI    DSF,DS4                 SET DS4, REQUEST NOTE TAB ENTRY
         B     RETSYS
*
*----------------------------------------------------------------------
*
*        ROUTINE SYSACT14
*
*----------------------------------------------------------------------
*
*        SKIPS RECORDS OR FILLS THEM BY BLANKS, DEPENDING ON THE
*        LAST I/O PROCEDURE
*
SYSACT14 LTR   R4,R4
         BNP   SYSQOUTR                QUANTITY OUT OF RANGE ERROR13
         TM    DSF,DS0                 DATASET OPEN ?
         BZ    SYSCLOSD                NO, BRANCH
SYS14T1  AH    R4,S                    YES
         TM    Q,X'FF'
         BZ    SYS14T2
*
*        SECTIONED FORMAT ONLY OUTPUT POSSIBLE
*
         SR    R2,R2
         IC    R2,Q
         CR    R4,R2
         BNH   SYS14T2
         L     R3,RE
         S     R3,R                    NUMBER OF BLANKS IN R3
         BZ    SYS14T11
         LA    R15,SYBLANK             FILL RECORD WITH BLANKS
         BALR  R14,R15
SYS14T11 MVC   S+1(1),Q
         OI    DSF,DS2                 OUTPUT
         BAL   R14,NXREC               SKIP TO BEGIN OF NEXT SECTION
         B     RETSYS
*
*        NOT SECTIONED FORMAT OR QUANTITY <= 0
*
SYS14T2  TM    DSF,DS2                 LAST I/O OUTPUT ?
         BZ    SYS14T4                 NO, BRANCH
SYS14T3  L     R3,RE                   YES
         S     R3,R
         BZ    SYS14T5
         LA    R15,SYBLANK             FILL RECORD WITH BLANKS
         BALR  R14,R15
SYS14T5  BAL   R14,NXREC
         CH    R4,S
         BNE   SYS14T3
         B     RETSYS
*
*        INPUT
*
SYS14T4  TM    DSF+1,DS10              DATASET OPENED BY SYSACT 12 ?
         BO    SYS14T7                 YES, BRANCH
*
*        ONLY INPUT READ THE FOLLWING UNTIL RECORD POINTER S
*        EQUALS QUANTITY
*
SYS14T6  TM    DSF,DS7                 EOD BEEN REACHED ?
         BO    SYSEOD
         BAL   R14,NXREC
         CH    R4,S
         BE    RETSYS
         B     SYS14T6
*
SYS14T7  LTR   R6,R6                   DATASET NUMBER = 0 ?
         BZ    SYSINCOM                YES, BRANCH
         OI    DSF,DS2                 NO, SET LAST I/O OUTPUT
         B     SYS14T3
*
*----------------------------------------------------------------------
*
*        ROUTINE SYSACT15
*
*----------------------------------------------------------------------
*
*        SKIP TO RECORD EQUAL QUANTITY IN NEXT SECTION IF DATA
*        IS NOT SECTIONED. SYSACT14 IS INVOKED
*
SYSACT15 LTR   R4,R4
         BNP   SYSQOUTR                QUANTITY OUT OF RANGE ERROR13
         TM    DSF,DS0                 DATASET OPEN ?
         BZ    SYSCLOSD                NO, BRANCH
         TM    Q,X'FF'
         BZ    SYS14T1                 DATA IS NOT SECTIONED SYSACT14
*                                      IS INVOKED
         SR    R3,R3
         IC    R3,Q
         CR    R4,R3
         BH    SYSQOUTR
         L     R3,RE
         S     R3,R                    FILL RECORD WITH BLANKS
         BZ    SYS15T0
         LA    R15,SYBLANK             FILL RECORD WITH BLANKS
         BALR  R14,R15
SYS15T0  MVC   S+1(1),Q
         OI    DSF,DS2                 OUTPUT
         BAL   R14,NXREC
         LR    R3,R4
         BCT   R3,SYS14T3
*
SYS15T1  B     RETSYS                  QUANTITY EQUALS ONE
*                                      FIRST RECORD IN NEXT SECTION
*
*        SYBLANK
*
*        FILL RECORD WITH BLANKS
*
SYBLANK  L     R11,R                   R11 = CHARACTER POINTER
SYBLANK1 MVI   0(R11),C' '             MOVE IN BLANK
         LA    R11,1(,R11)             INCR PTR
         BCT   R3,SYBLANK1             LOOP
         BR    R14
*
*        CONSTANTS
*
SAVEAR   DC    18F'0'                  SAVEAREA
*
         LTORG
*
*        SYSACT VECTOR TABLE
*
SYSVECT  DS    A(0)                    NO ROUTINE
         DC    A(SYSACT1)
         DC    A(SYSACT2)
         DC    A(SYSACT3)
         DC    A(SYSACT4)
         DC    A(SYSACT5)
         DC    A(SYSACT6)
         DC    A(SYSACT7)
         DC    A(SYSACT8)
         DC    A(SYSACT9)
         DC    A(SYSACT10)
         DC    A(SYSACT11)
         DC    A(SYSACT12)
         DC    A(SYSACT13)
         DC    A(SYSACT14)
         DC    A(SYSACT15)
*
*        DSTABLE
*
         DSTABLE  DSECT=YES
*
*        DSECT FOR ADDRESSING DECB
*
         PRINT NOGEN
*
         DCBD  DSORG=BS,DEVD=DA
*
         PRINT GEN
*
         READ  SYDECB,SF,MF=L
*
FAS      DSECT
*
         COPY  FSAREA
*
         IEZREGS
*
         END
