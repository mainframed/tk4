IDE      TITLE 'IHIIDECM, INREAL, ININTEGER, ALGOL F LIB'
*
*        COMPONENT ID - 360S-LM-532 ALGOL F LIBRARY
*
*        STATUS - LEVEL 2.1
*
*        FUNCTION/OPERATION -
*        SCAN INPUT BUFFER UNTIL A VALID NUMBER IS FOUND TRANSFER
*        TO BINARY AND STORE INTO SECOND PARAMETER
*
*        ENTRY POINTS -
*        IHIIDEIR - FROM - GENERATED OBJECT MODULE - INREAL
*        IHIIDEII - FROM - GENERATED OBJECT MODULE - ININTEGER
*                   LA   R1,PARMLIST
*                   BALR R14,R15
*                   DATA PASSED BY NAME
*        IHIIDEAI - FROM - ARRAY MODULE IHIIAR
*                   L    R7,A(DESTINATION)
*                   L    R10,FLAG
*                   BALR R14,R15
*                   DATA PASSED BY NAME
*
*        INPUT - N/A
*
*        OUTPUT - N/A
*
*        EXTERNAL ROUTINES -
*        IHIIOR - EVALUATE DATASET NUMBER
*               - OPEN DATASET
*               - CHANGE TO NEXT INPUT RECORD
*               - CONVERT REAL TO INTEGER
*        IHIFSA - CNVIRD, INTEGER TO REAL
*        IHIPTT - POWER OF TEN TABLE
*
*        TABLE/WORK AREAS -
*        SCANTAB - TO EVALUATE CHARACTER FROM INPUT BUFFER WHEN
*                  SCANNING AND TO BRANCH TO CORRESPONDING SUBPROGRAM
*        MB    -   MANTISSA BUFFER FOR INTERMEDIATE STORING OF VALID
*                  DIGITS
*
*        EXITS - NORMAL - RELOAD REGISTERS AND RETURN VIA R14
*
*        EXITS - ERROR - INPUT REQUEST BEYOND END OF DATASET NO 5
*                      - EXP PART OF INPUT NUMBER CONSISTS OF
*                        MORE THAN 2 DIGITS NO 6
*                        BRANCH TO FSA
*                        LA     R13,IHIFSA
*                        B      FSAERR+XX*4(R13) XX CORRESPONDING
*                                                   ERROR NUMBER
*
*        NOTES - LINKING TO IHIIDEAI DEVIATES FROM STANDARD SEE
*                ABOVE
*
IHIIDECM CSECT
*
         ENTRY IHIIDEIR
         ENTRY IHIIDEII
         ENTRY IHIIDEAI
*
*        REGISTER CONTENTS ON ENTRY POINT IHGIDEAI
*
*        R7                            DESTINATION ADDR
*        R10                           FLAG BYTE
*
*        FLOATING POINT REGISTERS
*
FPR0     EQU   0
*
*        OTHER GENERAL REGISTERS
*
*        R5                            -> DSTABLE ENTRY
*        R6                            DATASET NUMBER
*        R13                           BASE REGISTER FOR
*        R7                            -> DESTINATION
*        R4                            CHARACTER POINTER
*        R0                            INTEGER NUMBER
*        R8                            MANTISSA BUFFER POINTER
*        R9                            EXPONENT BUFFER POINTER
*        R10                           DECIMAL POINT POINTER
*        R11                           EXPONENT LESS EIGHT
*        R15                           -> POWERTEN TABLE LESS EIGHT
*
         USING DSTABLE,R5
*
*        DISPLACEMENTS IN ADRLST IN IHIFSA
*
CI       EQU   0             DISPLACEMENT FOR - IHIIORCI
CL       EQU   4                                IHIIORCL
EV       EQU   8                                IHIIOREV
NX       EQU   12                               IHIIORNX
OP       EQU   16                               IHIIOROP
OQ       EQU   20                               IHIIOROQ
*
*        ARRAY MODULE IHIIAR ENTRY
*
IHIIDEAI SAVE  (14,12),,'IHIIDEAI LEVEL 2.1 &SYSDATE &SYSTIME'
*
         USING IHIIDEAI,R15
         LR    R3,R13                  CHAIN SAVE AREAS
         LA    R13,SAVEAREA
         ST    R3,4(,R13)
         ST    R13,8(,R3)
         LA    R3,COMMON
         DROP  R15
         USING COMMON,R3
         STC   R10,FKT                 STORE FLAG BYTE
         B     LADDRA
*
         DROP  R3
*
*        ININTEGER ENTRY
*
IHIIDEII SAVE  (14,12),,'IHIIDEII LEVEL 2.1 &SYSDATE &SYSTIME'
*
         USING IHIIDEII,R15
         LR    R12,R13
         LA    R13,SAVEAREA            CHAIN SAVE AREAS
         ST    R12,4(,R13)
         ST    R13,8(,R12)
         LA    R3,COMMON
         DROP  R15
         USING COMMON,R3
         MVI   FKT,X'04'               FLAG BYTE: ININTEGER
         B     COMMON
*
         DROP  R3
*
*        INREAL ENTRY
*
IHIIDEIR SAVE  (14,12),,'IHIIDEIR LEVEL 2.1 &SYSDATE &SYSTIME'
*
         USING IHIIDEIR,R15
         LR    R12,R13
         LA    R13,SAVEAREA            CHAIN SAVE AREAS
         ST    R12,4(,R13)
         ST    R13,8(,R12)
         LA    R3,COMMON
         DROP  R15
         USING COMMON,R3
         MVI   FKT,X'00'               FLAG BYTE: INREAL
         B     COMMON
*
*        COMMON CODE FOR BOTH ENTRY POINTS
*
*        EVALUATE DATASET NUMBER (EVDSN)
*
COMMON   L     R15,IORLST(,R12)
         L     R15,EV(,R15)
         BALR  R14,R15
*
LADDR    L     R7,4(,R1)               R7 -> DESTINATION ADDR
LADDRA   NI    DSF+1,255-DS10          SET DS10 = 0, NOT OPENED BY
*                                      SYSACT 12
         CLI   Q,0                     DATASET SECTIONED ?
         BNE   ERROR2                  YES, INCOMPATIBLE ACTION
         CH    R6,=H'1'                DATASET NO = 1 ?
         BE    ERROR2                  YES, INCOMPATIBLE ACTION
         TM    DSF,DS0                 DATASET OPEN ?
         BO    DSOPEN                  YES, BRANCH
         NI    DSF,255-DS6             NO, SET OFF OPEN FOR OUTPUT
         L     R15,IORLST(,R12)        SETUP FOR OPEN REQUEST
         L     R15,OP(,R15)
         BALR  R14,R15                 OPEN DATASET
         B     SCAN
*
*        DATASET WAS ALREADY OPEN
*
DSOPEN   TM    DSF,DS2                 LAST I/O OUTPUT ?
         BZ    DSOPENA                 NO, BRANCH
         B     ERROR3                  INPUT BEYOND LAST OUTPUT
*
DSOPENA  TM    DSF,DS6                 OPEN FOR OUTPUT ?
         BO    ERROR2                  YES, INCOMPATIBLE ACTION ON DS
*
*        SCAN FOR DATA
*
SCAN     TM    DSF,DS7                 END OF FILE ?
         BO    ERROR5                  YES, ERROR
         L     R4,R                    CHARACTER POINTER
         LA    R8,MB+1                 MANTISSA BUFFER POINTER
         LA    R9,MB+19                EXPONENT BUFFER POINTER
         MVC   MB+1(L'MB-1),MB         CLEAR BUFFER
SCAN1    BCTR  R4,0                    DECREASE CHARACTER POINTER
SCAN2    MVI   SM,C'+'                 MANTISSA SIGN INITIALLY PLUS
SCAN2A   MVI   F,0                     CLEAR FLAG BYTE F
         MVI   SE,C'+'                 EXPONENT SIGN INITIALLY PLUS
SCAN3    LA    R4,1(,R4)               INCR CHARACTER POINTER
         C     R4,RE                   CHAR PTR = RECORD END(RE) ?
         BNE   SCAN5                   NO, BRANCH
*
*        RECORD END IS REACHED (R=RE)
*
SCAN4    TM    F,X'82'                 A VALID NUMBER HAS BEEN READ ?
         BNZ   DELIMIT                 YES, BRANCH
         L     R15,IORLST(,R12)        NO, REQUEST NEXT RECORD
         L     R15,NX(,R15)
         BALR  R14,R15                 RECORD CHANGE IS PERFORMED
         TM    DSF,DS7                 END OF FILE ?
         BO    ERROR5                  YES, END OF DATA REACHED
         L     R4,R                    REFRESH CHARACTER PTR
         B     SCAN1                   NEXT RECORD IS TO BE SCANNED
*
*        EVALUATE NEXT CHARACTER
*
SCAN5    SR    R2,R2
         TRT   0(256,R4),SCANTAB       USE SCANTAB TO SELECT ROUTINE
         SR    R15,R15                 ZERO BLANK COUNTER
         B     *(R2)
         B     BLANK                   +04
         B     OTHERS                  +08
         B     DIGIT                   +12
         B     SIGN                    +16
         B     DECPT                   +20
         B     APOSTR                  +24
*
BLANK    LA    R15,1(,R15)             INCR BLANK COUNTER
         LA    R4,1(,R4)               INCR CHARACTER PTR
         C     R4,RE                   CHARACTER PTR = RECORD END ?
         BE    SCAN4                   YES, RECORD END IS REACHED
         CLI   0(R4),C' '              NO, NEXT CHARACTER ALSO BLANK ?
         BNE   SCAN5                   NO, PROCESS CHARACTER
         CLM   R15,B'0001',K           BLANK COUNT = BLANK DELIM CHARS?
         BL    BLANK                   NO, NOT ENOUGH BLANKS FOUND
         BCTR  R4,0                    YES, K BLANKS READ, DECREASE R
*
OTHERS   TM    F,X'82'
         BZ    SCAN2                   NO VALID NUMBER READ, NEW SCAN
         B     DELIMIT                 A VALID NUMBER AND K BLANKS READ
*
DIGIT    TM    F,X'08'
         BO    DIGIT1                  EXPONENT DIGIT
         TM    F,X'40'                 MANTISSA DIGIT
         BZ    DIGIT2                  FIRST MANTISSA DIGIT
         CR    R8,R9
         BL    DIGIT3                  < 19 MANTISSA DIGITS
         B     DIGIT3A
*
DIGIT3   MVC   0(1,R8),0(R4)           DIGIT INTO MANTISSA BUFFER (MB)
DIGIT3A  LA    R8,1(,R8)               INCREASE MANTISSA POINTER
         TM    F,X'20'
         BZ    SCAN3                   NO DEC POINT IS READ, NEXT CHAR
         OI    F,X'10'                 F3 = 1 DIGIT BEHIND DEC PT READ
         B     SCAN3
*
DIGIT2   CLI   0(R4),C'0'
         BE    DIGIT2A
         OI    F,X'C0'                 F0, F1 = 1 MANTISSA DIGIT ^ 0
         B     DIGIT3
*
DIGIT2A  OI    F,X'80'                 F0 = 1 LEADING ZERO
         TM    F,X'20'
         BZ    SCAN3
         BCTR  R10,0                   DECR DECIMAL POINT POINTER
         OI    F,X'10'                 F3 = 1 DIGIT BEHIND DEC P READ
         B     SCAN3
*
DIGIT1   TM    F,X'01'
         BZ    DIGIT4                  NO EXP DIGIT NOT 0 WAS READ
         LA    R2,MB+21                END OF EXPONENT BUFFER
         CR    R9,R2
         BNL   ERROR6                  MORE THAN 2 EXP DIGITS WERE READ
DIGIT5   MVC   0(1,R9),0(R4)           DIGIT INTO EXPONENT BUFFER
         LA    R9,1(,R9)               INCREASE EXPONENT POINTER
         B     SCAN3                   EVALUATE NEXT CHARACTER
*
DIGIT4   OI    F,X'02'                 F6 = 1 ANY EXPONENT DIGIT READ
         CLI   0(R4),C'0'
         BE    SCAN3                   EVALUATE NEXT CHARACTER
         OI    F,X'01'                 F7 = 1 EXPONENT DIGIT ^ 0 READ
         B     DIGIT5
*
SIGN     TM    F,X'08'
         BO    SIGN1                   SIGN FOLLOWED AN APOSTROPHE
         TM    F,X'80'
         BO    DELIMIT                 SIGN FOLLOWED A MANTISSA DIGIT
         B     SIGN2
*
SIGN1    TM    F,X'06'
         BZ    SIGN2A                  EXPONENT SIGN
         TM    F,X'82'
         BNZ   DELIMIT                 SIGN FOLLOWED A VALID NUMBER
SIGN2    MVC   SM,0(R4)                MANTISSA SIGN
         B     SCAN2A                  NEW SCAN
*
SIGN2A   MVC   SE,0(R4)                EXPONENT SIGN
         OI    F,X'04'                 F5 = 1 EXPONENT SIGN READ
         B     SCAN3                   EVALUATE NEXT CHARACTER
*
DECPT    TM    F,X'28'
         BZ    DECPT1                  NO DEC POINT OR APOST BEFORE
         TM    F,X'82'
         BNZ   DELIMIT                 DEC PT FOLLOWED A VALID NUMBER
         TM    F,X'08'
         BO    DECPTA                  DEC POINT FOLLOWED APOSTROPHE
         MVI   SM,C'+'                 MANTISSA SIGN INITIALLY PLUS
         B     SCAN3                   NEW SCAN
*
DECPTA   MVC   SM,SE                   EXP SIGN IS ASSIGNED TO MANTSIGN
         MVI   SE,C'+'                 EXPONENT SIGN INITIALLY PLUS
         MVI   F,0                     CLEAR FLAG BYTE F
DECPT1   OI    F,X'20'                 F2 = 1 DEC POINT WAS READ
         LR    R10,R8                  STATE OF MANT POINTER INTO R10
         ST    R4,DPI                  STATE OF CHAR POINTER INTO DPI
         B     SCAN3                   NEXT CHARACTER
*
APOSTR   TM    F,X'08'
         BO    APOSTRA                 AN APOSTROPHE WAS ALREADY READ
         TM    F,X'30'
         BNM   APOSTR1                 A VALID NUMBER WAS READ
APOSTRA  TM    F,X'82'                 APOSTROPHE FOLLOWED DEC POINT
         BNZ   DELIMIT                 NUMBER BEFORE DEC POINT IS VALID
         MVC   SM,SE                   EXP SIGN IS ASSNED TO MANT SIGN
         MVI   SE,C'+'                 EXPONENT SIGN INITIALLY PLUS
         MVI   F,0                     CLEAR FLAG BYTE F
APOSTR1  OI    F,X'08'                 APOSTROPHE WAS READ F4.=1
         ST    R4,API                  STATE OF CHAR POINTER INTO API
         B     SCAN3                   EVALUATE NEXT CHARACTER
*
DELIMIT  TM    F,X'30'
         BNM   DELIMIT1                MANTISSA PART IS VALID
         L     R4,DPI                  CHAR POINTER OF DEC POINT INTO R
         NI    F,X'DF'                 F2.=0 NO DEC POINT WAS READ
         B     DELIMIT2
*
DELIMIT1 TM    F,X'0A'
         BNM   DELIMIT2                EXPONENT PART IS VALID
         L     R4,API                  CHAR PTR OF APOSTROPHE INTO R
         NI    F,X'F3'
DELIMIT2 C     R4,RE
         BNE   DELIMITB
DELIMITC L     R15,IORLST(,R12)
         L     R15,NX(,R15)            REQUEST NEXT RECORD
         BALR  R14,R15                 RECORD CHANGE IS PERFORMED
         B     DELIMIT3
*
DELIMITB LA    R4,1(,R4)               INCR CHARACTER POINTER
         C     R4,RE
         BE    DELIMITC
         ST    R4,R                    STORE CHAR POINTER TO DSTABLE
DELIMIT3 TM    F,X'28'                 TEST TYPE OF NUMBER
         BNZ   TRREAL                  REAL TYPE NUMBER
         TM    F,X'40'
         BO    DELIMITD                NUMBER ^= ZERO
         SR    R0,R0
         B     TRINT1                  NUMBER = ZERO
*
DELIMITD LA    R2,MB+11
         CR    R8,R2
         BH    TRREAL                  > 10 DIGITS READ, REAL
         BL    DELIMITH                < 10 DIGITS READ, INTEGER
         CLC   MB+1(10),DMINT          10 DIGITS WERE READ
         BNL   TRREAL                  NUMBER GREATER (2**31)-1, REAL
DELIMITH LA    R2,MB+2
         SR    R8,R2                   NUMBER OF DIGITS DECR BY ONE
         EX    R8,PACK                 PACK NUMBER
         CLI   SM,C'+'                 TEST SIGN OF THE NUMBER
         BNE   DELIMITE
         OI    MB+17,X'0F'
         B     DELIMITF
*
DELIMITE OI    MB+17,X'0D'
         NI    MB+17,X'FD'
DELIMITF CVB   R0,MB+10
TRINT1   LA    R2,4
         CLI   FKT,X'04'
         BE    FIN3                    CONVERSION NOT REQ, FKT=INTEGER
*
*        CALL CONVERSION ROUTINE (LOADED IN FSA)
*
         STM   R14,R13,8(R13)          ALL REGISTERS INTO SAVEAREA
         LR    R2,R13                  R2 -> SAVE AREA
         LA    R7,ACNVIRD(,R12)
         LR    R14,R0                  INTEGER INTO R14
         LR    R13,R12                 R13 -> FSA
         BALR  R8,R7
         LM    R14,R13,8(R2)           RESTORE REGS
         SR    R2,R2
         USING FSAAREA,R12
         TM    OPTSW(R12),X'20'        LONG OR SHORT PRECISION ?
         BZ    FIN3                    LONG PRECISION STATED
         LA    R2,8
         B     FIN3
*
TRREAL   SWR   FPR0,FPR0
         TM    F,X'C0'
         BO    TRREALA
         BM    FIN                     MANTISSA IS ZERO
         SR    R10,R10
         LD    FPR0,KFPD1              MANTISSA IS 1.0
         B     TRREAL1
*
TRREALA  TM    F,X'20'
         BO    TRREALB                 A DECIMAL WAS READ
         LR    R10,R8                  DECPT IS ACCEPTED BEH LAST DIGIT
TRREALB  LA    R2,MB+10
         SR    R10,R2                  POS OF DECPT: BEHIND 9. DIGIT
TRREAL2  PACK  MB+2(8),MB+1(9)         PACK UPPER 9 DIGITS
         OI    MB+9,X'0F'
         CVB   R0,MB+2                 CONVERT UPPER 9 DIGITS TO BINARY
         ST    R0,MASK+4               MASK FLOATING ZERO WITH EXPON 78
         AD    FPR0,MASK               TRANSFORM NUMBER TO FLOAT TYPE
         CR    R8,R2
         BNH   TRREAL1
         MD    FPR0,TPNINE
         S     R10,KF9                 SHIFT DECPT 9 POSITIONS TO RIGHT
         SR    R8,R8
         PACK  MB+2(8),MB+10(9)        PACK LOWER 9 DIGITS
         B     TRREAL2+6               REPEAT ACTION FOR LOWER 9 DIGITS
*
TRREAL1  CLI   SM,C'+'
         BE    TRREAL1A                NUMBER IS POSITIVE
         LCDR  FPR0,FPR0               INSERT MINUS
TRREAL1A SR    R0,R0
         TM    F,X'01'
         BZ    TRREAL3                 SCALE FACTOR
         NI    MB+19,X'0F'             CLEAR ZONE OF ONE EXPONENT DIGIT
         IC    R0,MB+19
         TM    MB+20,X'FF'             2. EXPONENT DIGIT IS TESTED
         BZ    TRREAL3A                NO 2. EXPONENT
         MH    R0,KH10                 FIRST EXPONENT DIGIT MULT BY 10
         LR    R2,R0
         NI    MB+20,X'0F'             CLEAR ZONE OF 2. EXPONENT DIGIT
         IC    R0,MB+20
         AR    R0,R2                   TOTAL PRELIMINARY EXPONENT
TRREAL3A CLI   SE,C'+'
         BE    TRREAL3                 EXPONENT IS POSITIVE
         LCR   R0,R0                   INSERT MINUS
TRREAL3  AR    R10,R0                  CORR EXP BY DEC POINT POSITION
         BZ    FIN                     CORRECTED EXPONENT IS ZERO
         L     R15,VPTTAB              ADDR OF POWERTEN TABLE
         BP    TRREAL3B
         LA    R15,128(R15)            ADDR OF 10**(-1) DIMIN BY EIGHT
TRREAL3B LPR   R10,R10                 ABS (CORRECTED EXPONENT)
         SR    R11,R11                 CLEAR REGISTER
         SRDA  R10,3
         SLA   R10,3
TRREAL5  BZ    TRREAL4A                CORRECTED EXPONENT LESS 8
         C     R10,KF72
         BNH   TRREAL4                 CORRECTED EXPONENT LESS 73
         MD    FPR0,128(,R15)          MULTIPLY OR DIVIDE BY 10**72
         S     R10,KF72                DECREASE CORRECTED EXP BY 72
         B     TRREAL5                 REPEAT MULT OR DIVISION
*
TRREAL4  MD    FPR0,56(R10,R15)
TRREAL4A SRL   R11,26
         LTR   R11,R11
         BZ    FIN
         MD    FPR0,0(R11,R15)       * OR / 10**(EXP-(ENTIER(EXP/8)*8))
FIN      SR    R2,R2                   CLEAR R2, TYPE = REAL LONG
         TM    OPTSW(R12),X'20'        LONG OR SHORT PRECISION ?
         BZ    FIN1                    LONG PRECISION STATED
         STD   FPR0,BUFF               SHORT PRECISION STATED
         MVC   ROUND(1),BUFF           EXPONENT TO ROUND
         AD    FPR0,ROUND              LONG PRECISION ROUNDED TO SHORT
         LA    R2,8                    TYPE = REALSHORT
FIN1     CLI   FKT,0
         BE    FIN3                    CONVERSION NOT NECESSARY
         LA    R2,4                    TYPE = INTEGER
         L     R15,IORLST(,R12)
         L     R15,CI(,R15)
         BALR  R14,R15                 NUMBER CONVERTED TO INTEGER
FIN3     EX    0,STORE(R2)
         L     R13,SAVEAREA+4
*
         RETURN (14,12)
*
PACK     PACK  MB+10(8),MB+1(0)        EXE INSTRUCTIONS
STORE    STD   FPR0,0(,R7)             *
         ST    R0,0(,R7)               ×
         STE   FPR0,0(,R7)             V
*
ERROR2   LR    R13,R12
         B     FSAERR+2*4(,R12)        INCOMP ACTION ON DATASET
*
ERROR3   LR    R13,R12
         B     FSAERR+3*4(,R12)        INPUT BEYOND LAST OUTPUT
*
ERROR5   LR    R13,R12                 INPUT REQ BEYOND END OF DATASET
         B     FSAERR+5*4(,R12)
*
ERROR6   LR    R13,R12                 EXP PART CONSIST OF
         B     FSAERR+6*4(,R12)        MORE TH.2DIGITS
*
*        EXTERNAL ADDRS
*
VPTTAB   DC    V(IHIPTTAB)
*
ACNVIRD  EQU   X'120'
*
*        INTERNAL CONSTANTS AND STORAGE
*
KF9      DC    F'9'                    FOR SHIFT OF DECIMAL POINT
KF72     DC    F'72'                   FOR EXPONENT TREATMENT
API      DC    A(0)                    CHARACTER POINTER OF APOSTROPHE
DPI      DC    A(0)                    CHARACTER POINTER OF DEC POINT
BUFF     DC    D'0'                    FOR CONVERSION OF NUMBER
KFPD1    DC    D'1.0'
TPNINE   DC    DE9'1'                  10**9
MASK     DC    FL8S56'78'              FLOAT 0 WITH EXPONENT 78
ROUND    DC    X'0000000080000000'     FOR CONVERSION OF NUMBER
KH10     DC    H'10'                   FOR EXPONENT TREATMENT
FKT      DC    X'00'                   FLAG BYTE
F        DC    X'00'                   FLAG BYTE
SM       DC    C' '
SE       DC    C' '
MB       DC    XL21'00'                MANTISSA BUFFER
DMINT    DC    C'2147483648'           2**31
*
SCANTAB  DC    64X'08'                 OTHERS
         DC    X'04'                   BLANK
         DC    10X'08'                 OTHERS
         DC    X'14'                   DECIMAL POINT
         DC    2X'08'                  OTHERS
         DC    X'10'                   SIGN +
         DC    17X'08'                 OTHERS
         DC    X'10'                   SIGN -
         DC    28X'08'                 OTHERS
         DC    X'18'                   APOSTROPHE
         DC    114X'08'                OTHERS
         DC    10X'0C'                 DIGITS 0 TO 9
         DC    6X'08'                  OTHERS
*
SAVEAREA DC    18F'0'
*
         LTORG
*
         DSTABLE  DSECT=YES
*
FSAAREA  DSECT
*
         COPY  FSAREA
*
*        REGISTER EQUATES
*
         IEZREGS
*
         END
