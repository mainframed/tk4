         MACRO
&LABEL   IECOBTWA &EP,&F4,&D1=(0),&ADT=NO
.*
.*       MACRO DELETIONS/CHANGES FOR VS2 RELEASE 2
.*
.*       MACRO DELETIONS/ADDITIONS FOR VS2 RELEASE 3.7
.*0000009000-010000,024500                             DELETED @ZA31937
.*0000009000-009600,018600-018700,024500-024600        ADDED   @ZA31937
.*0000119540-119900                                    ADDED   @ZA31937
.*
.**********************************************************************
.*
.* MODULE NAME = IECOBTWA (VS2-2)
.*
.* DESCRIPTIVE NAME = DADSM OBTAIN WORK AREA ADDRESSING MACRO
.*
.* COPYRIGHT = NONE
.*
.* STATUS = CHANGE LEVEL 000
.*
.* FUNCTION = DADSM OBTAIN MACRO WHICH PROVIDES ADDRESSABILITY
.*            TO THE 612 DECIMAL BYTE WORK AREA (NOT INCLUDING @ZA31937
.*            THE 8 WORD PREFIX AND THE 6 WORD RECOVERY ROUTINE@ZA31937
.*            PARAMETER LIST) USED BY OBTAIN.                  @ZA31937
.*
.* NOTES = THIS MACRO IS AN INTERNAL MACRO USED IN THE FOLLOWING
.*         DADSM FUNCTIONS:
.*
.*             OBTAIN
.*
.*         TO REFERENCE THIS WORK AREA WITHIN THE FUNCTIONS LISTED
.*         ABOVE, CODE A USING STATEMENT ON THE LABEL 'OBTNWKA'.
.*
.*      DEPENDENCIES = THIS MACRO IS DESIGNED TO BE USED OPTIONALLY
.*                     WITH THE IECSDSL1 DSCB MAPPING MACRO, THE
.*                     PREFX MACRO (DEFINED IN THE IECDSECS MACRO)
.*                     WHICH DEFINES THE 8 WORD WORK AREA PREFIX,
.*                     THE 20 WORD EXTENDED WORK AREA PREFIX, AND
.*                     THE 4 WORD DADSM WTG TABLE, AND THE DSMADT
.*                     MACRO (ALSO DEFINED IN THE IECDSECS MACRO)
.*                     WHICH DEFINES THE 6 WORD DADSM AUDIT TRAIL.
.*                     THE AUDIT TRAIL MUST APPEAR AS THE      @ZA31937
.*                     FINAL 6 WORDS OF THE OBTAIN WORK AREA.  @ZA31937
.*
.*      RESTRICTIONS = NONE
.*
.*      REGISTER CONVENTIONS = NOT APPLICABLE
.*
.* PATCH LABEL = NOT APPLICABLE
.*
.* MODULE TYPE = MACRO
.*
.*      PROCESSOR = ASSEMBLER LANGUAGE
.*
.*      MODULE SIZE = 636 DECIMAL BYTES (NOT INCLUDING THE     @ZA31937
.*                    PREFIX)                                  @ZA31937
.*
.*      ATTRIBUTES = NOT APPLICABLE
.*
.* ENTRY POINT = OBTPREFX
.*
.*      PURPOSE = THIS MACRO IS CALLED AT ASSEMBLY TIME BY THE
.*                FUNCTIONS LISTED UNDER 'NOTES' TO EXPAND THE
.*                OBTAIN WORK AREA.
.*
.*      LINKAGE =
.*
.*                LABEL IECOBTWA EP,F4,D1=(N),ADT=YES
.*
.*      INPUT =
.*
.*                OPTIONAL LABEL SPECIFIED AS AN ACCEPTABLE ASSEMBLER
.*                LABEL WHICH CAN BE USED BY THE CALLING CSECT TO
.*                ESTABLISH ADDRESSABILITY TO THE ENTIRE WORK AREA.
.*
.*                EP IS THE FIRST POSITIONAL PARAMETER, SPECIFYING
.*                THAT THE LABELS FOR EACH OF THE FIELDS IN THE 8
.*                WORD PREFIX, THE 20 WORD EXTENDED PREFIX, AND THE 4
.*                WORD DADSM WTG TABLE ARE TO BE EXPANDED.  IF THESE
.*                FIELDS ARE NOT TO BE DEFINED, A COMMA MUST BE CODED
.*                TO INDICATE THE ABSENCE OF THIS PARAMETER.
.*
.*                F4 IS THE SECOND POSITIONAL PARAMETER, SPECIFYING
.*                THAT THE FIELDS OF THE FORMAT 4 DSCB ARE TO BE
.*                EXPANDED USING THE IECSDSL1 DSCB MAPPING MACRO.
.*                IF THESE FIELDS ARE NOT TO BE DEFINED AND A
.*                KEYWORD PARAMETER FOLLOWS, A COMMA MUST BE
.*                CODED TO INDICATE THE ABSENCE OF THIS PARAMETER.
.*
.*                D1=(N) IS A KEYWORD PARAMETER THAT SPECIFIES THE
.*                FORMAT(S) OF THE DSCB(S) TO BE EXPANDED IN THE
.*                140 BYTE DSCB AREA, USING THE IECSDSL1 MACRO.
.*                N CAN BE A NUMBER FROM 0 - 6.  IF N IS 0 OR 4 OR
.*                IF THIS KEYWORD IS OMITTED, NO LABELS ARE EXPANDED.
.*                IF N IS 1, 2, 3, 5, OR 6, THE CORRESPONDING FORMAT
.*                OF THE DSCB WILL BE EXPANDED.  MORE THAN ONE DSCB
.*                FORMAT CAN BE MAPPED IN THE 140 BYTE DSCB AREA
.*                BY SEPARATING THE FORMAT NUMBERS BY COMMAS.
.*
.*                ADT=YES IS A KEYWORD PARAMETER WHICH SPECIFIES THAT
.*                THE LABELS FOR EACH OF THE FIELDS WITHIN THE 6 WORD
.*                DADSM AUDIT TRAIL ARE TO BE EXPANDED.
.*
.*      OUTPUT = DADSM OBTAIN WORK AREA EXPANSION
.*
.* EXIT-NORMAL = MEND STATEMENT
.*
.* EXIT-ERROR = ERROR MESSAGES:
.*
.*      MNOTE 12,***  IHB002 INVALID EP OPERAND SPECIFIED-&EP
.*      MNOTE 12,***  IHB002 INVALID F4 OPERAND SPECIFIED-&F4
.*      MNOTE 12,***  IHB002 INVALID ADT OPERAND SPECIFIED-&ADT
.*
.*      THE MACRO EXPANSION CONTINUES AS IF THE INVALID PARAMETER
.*      WERE NOT SPECIFIED.
.*
.* EXTERNAL REFERENCES = NONE
.*
.*      ROUTINES = NONE
.*
.*      DATA AREAS = OBTAIN WORK AREA
.*
.*      CONTROL BLOCKS = DSCB, ECB, IOB, DCB, DEB
.*
.* TABLES = NONE
.*
.* MACROS = IECDSECS DSMADT
.*          IECDSECS PREFX
.*          IECSDSL1
.*          IHBERMAC
.*
.* CHANGE ACTIVITY = SEE DELETIONS/CHANGES PRECEDING THE MACRO
.*
.**********************************************************************
.*
         LCLA  &C
&LABEL   DSECT
***********************************************************************
*
*        OBTAIN WORK AREA
*
***********************************************************************
         SPACE 1
***********************************************************************
*        PREFIX, EXTENDED PREFIX, AND WTG TABLE AREA
***********************************************************************
         SPACE 1
OBTPREFX DS    8F                       WORK AREA PREFIX
OBTNWKA  DS    20F                      EXTENDED PREFIX
OWTGTBL  DS    4F                       DADSM WTG TABLE
         AIF   ('&EP' EQ 'EP').OBT0010
         AIF   ('&EP' EQ '').OBT0020
         IHBERMAC 1001,EP,&EP
         AGO   .OBT0020
.OBT0010 ANOP
         ORG   OBTPREFX
*        IECDSECS PREFX,EXPAND=YES
         IECDSECS PREFX,EXPAND=YES
.OBT0020 ANOP
         SPACE 1
***********************************************************************
*        FORMAT 4 DSCB AREA
***********************************************************************
         SPACE 1
F4DSCB   DS    XL96                     FORMAT 4 DSCB
ENQAREA  EQU   F4DSCB+4                 ENQ PARAMETER LIST
MJELNAME EQU   ENQAREA+12               MAJOR ELEMENT NAME
         AIF   ('&F4' EQ 'F4').OBT0030
         AIF   ('&F4' EQ '').OBT0040
         IHBERMAC 1001,F4,&F4
         AGO   .OBT0040
.OBT0030 ANOP
         ORG   F4DSCB
*        IECSDSL1 (4)
         IECSDSL1 (4)
.OBT0040 ANOP
         SPACE 1
***********************************************************************
*        140-BYTE DSCB AREA
***********************************************************************
         SPACE 1
OBTDSCB  DS    XL140                    140-BYTE DSCB AREA
&C       SETA  1
.OBT0050 AIF   (&C GT N'&D1).OBT0060
         AIF   (&D1(&C) EQ 0 OR &D1(&C) EQ 4).OBT0055
         ORG   OBTDSCB
         MNOTE '         IECSDSL1 (&D1(&C))'
         IECSDSL1 (&D1(&C))
.OBT0055 ANOP
&C       SETA  &C+1
         AGO   .OBT0050
.OBT0060 ANOP
         DS    F                        UNUSED FULLWORD
         SPACE 1
***********************************************************************
*        OBTAIN INTERNAL DEB/DCB AREA
***********************************************************************
         SPACE 1
DEB      DS    F                        DEB
DCB      DS    11F                      DCB
DEBPTR   DS    F                        DCB POINTER TO DEB
         SPACE 1
***********************************************************************
*        OBTAIN INTERNAL ECB/IOB AREA
***********************************************************************
         SPACE 1
ECB      DS    F                        ECB
IOB      DS    8F                       IOB
SEEK     DS    D                        DIRECT ACCESS ADDRESS
         SPACE 1
***********************************************************************
*        OBTAIN CHANNEL CONTROL WORDS AREA
***********************************************************************
         SPACE 1
CCW0     DS    D
CCW1     DS    D
CCW2     DS    D
CCW3     DS    D
CCW4     DS    D
CCW5     DS    D
CCW6     DS    D
CCW7     DS    D
CCW8     DS    D
CCW9     DS    D
CCW10    DS    D
CCW11    DS    D
CCW12    DS    D
         SPACE 1
***********************************************************************
*        OBTAIN WORK AREA FIELDS
***********************************************************************
         SPACE 1
ERCODE   DS    H                        ERROR RETURN CODE
         CNOP  2,4                      ALIGN DSABQDB TO FULLWORD
TIOTMNR  DS    CL6                      ASID/DSAB QDB ADDRESS
DSABQDB  EQU   TIOTMNR+2                DSAB QDB ADDRESS
TCBADDR  DS    F                        POINTER TO THE TCB
         DS    H                        UNUSED HALFWORD
THETA    DS    XL1                      RPS SECTOR VALUE
         SPACE 1
***********************************************************************
*        ABSOLUTE TRACK ADDRESSES
***********************************************************************
         SPACE 1
NEWCCHHR DS    XL5                      CCHHR OF THE FORMAT 1 DSCB
         SPACE 1
***************************************************************@ZA31937
*        PROTECTED COPIES OF PARAMETERS PASSED BY CALLER       @ZA31937
***************************************************************@ZA31937
         SPACE 1                                               @ZA31937
OPTIONS  DS    XL4                      OBTAIN OPTION CODE     @ZA31937
PDSNAME  DS    CL44                     DATA SET NAME FOR      @ZA31937
*                                       SEARCH OPTION          @ZA31937
PCCHHR   EQU   PDSNAME ,CL5             CCHHR FOR SEEK OPTION  @ZA31937
PVOLSER  DS    CL6                      VOLUME SERIAL NUMBER   @ZA31937
         SPACE 1                                               @ZA31937
***********************************************************************
*        OBTAIN RECOVERY ROUTINE AUDIT TRAIL AREA
***********************************************************************
         SPACE 1
OBTADTRL DS    6F                       OBTAIN AUDIT TRAIL WORDS
         AIF   ('&ADT' EQ 'YES').OBT0090
         AIF   ('&ADT' EQ 'NO').OBT0100
         IHBERMAC 1001,ADT,&ADT
         AGO   .OBT0100
.OBT0090 ANOP
         ORG   OBTADTRL
*        IECDSECS DSMADT,EXPAND=YES
         IECDSECS DSMADT,EXPAND=YES
.OBT0100 ANOP
         SPACE 1
***********************************************************************
OBTWAEND EQU   *                        OBTAIN WORK AREA END
OWALNGTH EQU   OBTWAEND-OWTGTBL         WORK AREA LENGTH FOR IECRES GET
***********************************************************************
         MEND
