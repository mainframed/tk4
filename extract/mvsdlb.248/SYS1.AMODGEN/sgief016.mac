         MACRO
         SGIEF016      &ID                                     @G64QPSJ
.* CHANGE LEVEL = 07/12/74                                     @Z30ANSF
.*              = 12/01/76                                     @ZA15758
.*              = 08/01/78 MVS SYSTEM CONTROL PROGRAM 2        @G64QPSJ
.*
.*A000000-999999                                                 Y02670
         COPY  SGIEF020
         LCLA  &I,&J,&K,&L,&D,&GRP,&GLT(1917)                  @ZA15758
         LCLB  &P
         LCLC  &HEX
         ACTR  1000000
.**********************************************************************
.*       EDTTAB  SUPPORT  MACRO
.*
.*  GENERATES HEADER SECTION,DEVICE LIST SECTION,AND GROUP
.*  DESCRIPTOR SECTION OF EDTTAB.
.*
.*   (MACRO HAS NO PARAMETERS)
.*
.*  INPUTS ARE GBL VARS 'GPFXT','DGR','DGE','DUL','DAD'.
.*  OUTPUT IS VARIABLE 'GXT' IN ADDITION TO TABLES.
.*
.*  DEVICE LIST GENERATION CONSISTS OF SELECTING GENERIC TYPES
.*  IN THE ORDER OF THEIR CODES IN GPFXT, SCANNING DGE FOR ALL
.*  OCCURRENCES OF EACH GENERIC SELECTED, AND GENERATING A TABLE ENTRY
.*  CONTAINING THE DUL VALUE ASSOCIATED WITH EACH SELECTED DGE ENTRY.
.*  (ALL DEVICES IN A GROUP ARE CONSECUTIVE IN THE D.. VARIABLES AND
.*  ALL GROUPS IN A GENERIC TYPE ARE ALSO CONSECUTIVE.)
.*  THE ENTRY INDEX OF EACH GROUP START IS SAVED IN LOCAL GLT FOR
.*  PHASE THAT GENS GROUP DESCRIPTORS. THE GROUP IDS ARE SAVED IN GXT
.*  IN THE ORDER PROCESSED.
.*
.*  GROUP DESCRIPTOR ENTRIES ARE PRODUCED DIRECTLY FROM GLT. THE ITH
.*  ENTRY OF GLT CONTAINS THE INDEX OF THE FIRST DEVICE LIST ENTRY
.*  FOR THE ITH GROUP.  THIS IS CONVERTED TO AN ADCON RELATIVE TO
.*  DEVICE LIST START..  COUNTS ARE OBTAINED AS DIFFERENCES BETWEEN
.*  CONSECUTIVE GLT ENTRIES.
.**********************************************************************
         TITLE 'EDTTAB  -  GENERAL HEADER'
EDTTAB   CSECT
         DC    A(NMTAB)
         DC    A(GENTAB)
         DC    A(GDTAB)
         DC    A(DLIST)
         DC    A(MASKTAB)
         DC    A(GRPTAB)
         DC    A(PREFTAB)
         DC    CL8'&ID'               EDT ID                   @G64QPSJ
         DC    CL8'&SYSDATE'          DATE OF STAGE II         @G64QPSJ
         DC    CL5'&SYSTIME'          TIME OF STAGE II         @G64QPSJ
         DC    3XL1'00'               RESERVED                 @G64QPSJ
         AGO   .M1            INCLUDE DEVICE LIST
.MAIN2   AGO   .R1            INCLUDE GROUP DESCRIPTORS
.MAIN3   MEXIT
.*  PROCEDURE -
.*          GRP=1
.*         (GEN TABLE START)
.*          DO I-1 BY 1 WHILE GPFXT(I) NE 0;
.*              P=TRUE;
.*              DO J=1 BY 1 WHILE DGR(J) NE 0 AND ((WHERE CLAUSE TRUE)
.*                                OR P) WHERE GPFXT(I) EQ DGE(J);
.*                  P=FALSE;
.*                  D=D+1;                            COUNT DEVICES
.*                  IF GXT(GRP) NE DGR(J);             TST 1'ST IN GRP
.*                      IF D GT 1;                      TST FIRST EVBR
.*                         GRP=GRP+1
.*                      GXT(GRP)=DGR(J);         SAVE OLD GRP ID
.*                      GLT(GRP)=D;              AND PTR TO 1ST IN GRP.
.*                 (GEN DLIST ENTRY WITH COMMENTARY)
.*              END;
.*          END.
.*         (GEN TABLE ENDING)
.*          RETURN;
.*
.**********************************************************************
.*
.*  IMPLEMENTATION -
.M1      ANOP
&GRP     SETA  1
         TITLE 'EDTTAB - UCB LOOKUP VALUE SECTION'
DLIST    DC    A(DLENTS)  HEADER
         DC    A(DLELEN)  SEGMENT
DLSTRT   DS    0A
&I       SETA  0                        INIT DO INDEX
.M100    ANOP                           TOP OF OUTER DO
&I       SETA  &I+1
         AIF   (&GPFXT(&I) EQ 0).M300       WHILE CLAUSE
         AIF   (&GPFXT(&I) GT 256).M300
&P       SETB  1                        INITIALIZE SWITCH FOR INNER DO
&J       SETA  0                        INIT DO INDEX
.M200    ANOP                           TOP OF INNER DO
&J       SETA  &J+1
         AIF  (&DGR(&J)  EQ   0).M100     WHILE CLAUSE
         AIF   (&GPFXT(&I) NE  &DGE(&J)).M202        WHILE/WHERE TEST.
&P       SETB  0     ** INDICATE GENERIC FOUND IN SYSLIST **
&D       SETA  &D+1                     COUNT DEVICE ENTRIES.
         AIF  (&GXT(&GRP) EQ &DGR(&J)).M201        TST FOR GRP START.
         AIF   (&D EQ 1).M2001
&GRP     SETA  &GRP+1                      (AT GRP STRT SAVE VALUES
.M2001   ANOP
&GXT(&GRP) SETA &DGR(&J)                    FOR LATER TABLE GENS)
&GLT(&GRP) SETA &D
.M201    ANOP
.*   GEN AN ENTRY FOR DLIST
.*
.*  CONVERT ADDRESS TO HEX
.*
&HEX     SETC  ''
&K       SETA  &DAD(&J)
.M201A   ANOP
&HEX     SETC  '0123456789ABCDEF'(1+&K-&K/16*16,1).'&HEX'
&K       SETA  &K/16
         AIF   (&K NE 0).M201A
.*   INSURE  3-BYTE LENGTH OF ADDRESS OF DEVICE
&HEX     SETC  '00'.'&HEX'
&K       SETA  K'&HEX
&HEX     SETC  '&HEX'(&K-2,3)
***********************************************************************
 MNOTE *,' THIS IS UCB LOOKUP FOR      &NAME(&GPFXT(&I)),GROUP=&GRP'
 MNOTE *,'                             ADDR=&HEX'
         DC    H'&DUL(&J)'
         AGO   .M200         (LOOP ON INNER DO)
.M202    AIF   (&P).M200     (WHERE FAILED,BUT WHILE STILL OK)
         AGO   .M100
.M300    ANOP
DLENTS   EQU   &D
DLELEN   EQU   (*-DLSTRT)/DLENTS
&GLT(&GRP+1) SETA &D+1
***********************************************************************
*  END OF TABLE
***********************************************************************
         AGO   .MAIN2
.**********************************************************************
.*  GDTMAKER
.**********************************************************************
.*
.*  COMMENT -
.*
.*        THIS SECTION USES INFORMATION STORED BY DLISTMAKER IN'GLT'
.*        TO PRODUCE THE GROUP DESCRIPTOR TABLE (GDT).
.*
.**********************************************************************
.*
.*  PROCEDURE -
.*
.*          GEN GDT HEADER
.*          DO I=1 BY 1 WHILE GLT(I) NE 0;
.*             (GEN I'TH ENTRY FOR GDT)
.*          END;
.*         (GEN TABLE CLEANUP)
.*          RETURN
.*
.**********************************************************************
.*
.*  IMPLEMENTATION -
.*
.R1      ANOP
         TITLE 'EDTTAB  -  GROUP DESCRIPTOR SECTION'
GDTAB    DC    A(GDTENT)
         DC    A(GDTLEN)
GDTSTRT  DS    0A
******************************  BEGIN TABLE BODY  *********************
*
&I       SETA  1
&K       SETA  0
.R101    ANOP
*
 MNOTE *,'***** GROUP &I DESCRIPTOR *****'
*
 DC  F'&I'     GROUP ID
&K       SETA  &GLT(&I+1)-&GLT(&I)
         DC    F'&K'         COUNT OF DEVICES IN THE GROUP
&L       SETA  &GLT(&I)-1
 DC  A(DLSTRT+DLELEN*&L)  POINTER TO FIRST DEVICE IN GROUP
&I       SETA  &I+1
         AIF   (&GLT(&I+1) NE 0).R101
.*
.*  TABLE CLEANUP
.*
&I       SETA  &I-1
GDTENT   EQU   &I
GDTLEN   EQU  (*-GDTSTRT)/GDTENT
         AGO   .MAIN3
.**********************************************************************
.*
         MEND
