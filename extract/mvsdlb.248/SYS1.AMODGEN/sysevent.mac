* /* MACCOMP Z-4 SC1CX/PKD16                                         */
* /* MACSTAT Z-4 75007/Z40BPSS                                       */
         MACRO
&LABEL   SYSEVENT  &EVENT,&ASID=,&ASIDL=,&PGN=,&ENTRY=SVC,&TYPE=
         ANOP                              ADDED TYPE= KEYWORD @ZA29015
         LCLA      &EVENTO
         LCLA      &FIPDISP                                   @ZA31158
* /* MACDATE Y-2 74040                                               */
&FIPDISP SETA  99                                             @ZA31158
&EVENTO  SETA  1
         AIF   ('&EVENT'EQ'TIMEREXP').EOK
&EVENTO  SETA      2
         AIF   ('&EVENT'EQ'TERMWAIT').EOK
&EVENTO  SETA      3
         AIF   ('&EVENT'EQ'NIOWAIT').EOK
&EVENTO  SETA      4
         AIF   ('&EVENT'EQ'USERRDY').EOK
&EVENTO  SETA      5
.*
&EVENTO  SETA      6
         AIF   ('&EVENT'EQ'MEMCREAT').EOK
&EVENTO  SETA      7
         AIF   ('&EVENT'EQ'MEMDEL').EOK
&EVENTO  SETA      8
         AIF   ('&EVENT'EQ'JOBSELCT').EOK
&EVENTO  SETA      9
         AIF   ('&EVENT'EQ'JOBTERM').EOK
&EVENTO  SETA      10
         AIF   ('&EVENT'EQ'INITATT').EOK
&EVENTO  SETA      11
         AIF   ('&EVENT'EQ'INITDET').EOK
&EVENTO  SETA      12
         AIF   ('&EVENT'EQ'QSCEST').EOK
&EVENTO  SETA      13
         AIF   ('&EVENT'EQ'QSCECMP').EOK
&EVENTO  SETA      14
         AIF   ('&EVENT'EQ'TRANSWAP').EOK                     @ZA16887
&EVENTO  SETA      15
         AIF   ('&EVENT'EQ'SWOUTCMP').EOK
&EVENTO  SETA      16
         AIF   ('&EVENT'EQ'SWINSTAT').EOK                     @Z40BPCH
&EVENTO  SETA      17
         AIF   ('&EVENT'EQ'SWINFL').EOK
&EVENTO  SETA      18
         AIF   ('&EVENT'EQ'QSCEFL').EOK
&EVENTO  SETA      19
         AIF   ('&EVENT'EQ'RSTORCMP').EOK
&EVENTO  SETA      20
         AIF   ('&EVENT'EQ'ENQHOLD').EOK
&EVENTO  SETA      21
         AIF   ('&EVENT'EQ'ENQRLSE').EOK
&EVENTO  SETA      22
         AIF   ('&EVENT'EQ'RSMCNSTS').EOK
&EVENTO  SETA      23
         AIF   ('&EVENT'EQ'AVQLOW').EOK
&EVENTO  SETA      24
         AIF   ('&EVENT'EQ'AVQOK').EOK
&EVENTO  SETA      25
         AIF   ('&EVENT'EQ'SQALOW').EOK
&EVENTO  SETA      26
         AIF   ('&EVENT'EQ'SQAOK').EOK
&EVENTO  SETA      27
.*                                                            @Z40BPCH
&EVENTO  SETA      28
         AIF   ('&EVENT'EQ'DEVALLOC').EOK
&EVENTO  SETA      29
         AIF   ('&EVENT'EQ'CONFIGCH').EOK
&EVENTO  SETA      30
         AIF   ('&EVENT'EQ'VERIFYPG').EOK
&EVENTO  SETA      31
         AIF   ('&EVENT'EQ'RESETPG').EOK
&EVENTO  SETA      32
         AIF   ('&EVENT'EQ'NEWIPS').EOK
&EVENTO  SETA      33
         AIF   ('&EVENT'EQ'ALTCPREC').EOK
&EVENTO  SETA      34
         AIF   ('&EVENT'EQ'TGETTPUT').EOK
&EVENTO  SETA      35
         AIF   ('&EVENT'EQ'SYQSCST').EOK
&EVENTO  SETA      36
         AIF   ('&EVENT'EQ'SYQSCCMP').EOK
&EVENTO  SETA      37
         AIF   ('&EVENT'EQ'SETDMN').EOK                   @Z40BPCH
&EVENTO  SETA      38
         AIF   ('&EVENT'EQ'REQSERVC').EOK
&EVENTO  SETA      39
         AIF   ('&EVENT'EQ'REQPGDAT').EOK
&EVENTO  SETA      40
         AIF   ('&EVENT'EQ'COPYDMDT').EOK
&EVENTO  SETA      41
         AIF   ('&EVENT'EQ'DONTSWAP').EOK
&EVENTO  SETA      42
         AIF   ('&EVENT'EQ'OKSWAP').EOK
&EVENTO  SETA      43
         AIF   ('&EVENT'EQ'REQSWAP').EOK
&EVENTO  SETA      44
         AIF   ('&EVENT'EQ'BRINGIN').EOK
&EVENTO  SETA      45
         AIF   ('&EVENT'EQ'WKLDINIT').EOK
&EVENTO  SETA      46
         AIF   ('&EVENT'EQ'WKLDCOLL').EOK
&EVENTO  SETA      47
         AIF   ('&EVENT'EQ'WKLDTERM').EOK
&EVENTO  SETA      48
.*
&EVENTO  SETA      49
         AIF   ('&EVENT'EQ'REQSVDAT').EOK
&EVENTO  SETA      50
.*
&EVENTO  SETA      51                                        @ZA31158
.*                                                           @ZA31158
&EVENTO  SETA      52                                        @ZA31158
.*                                                           @ZA31158
         AIF ('&EVENT'NE'EVENT53').NEXT54                    @ZA31158
&EVENTO  SETA      53                                        @ZA31158
&FIPDISP SETA      12                                        @ZA31158
         AGO       .EOK                                      @ZA31158
.NEXT54  AIF ('&EVENT'NE'EVENT54').NEXT55                    @ZA31158
&EVENTO  SETA      54                                        @ZA31158
&FIPDISP SETA      16                                        @ZA31158
         AGO       .EOK                                      @ZA31158
.NEXT55  AIF ('&EVENT'NE'EVENT55').NEXT56                    @ZA31158
&EVENTO  SETA      55                                        @ZA31158
&FIPDISP SETA      20                                        @ZA31158
         AGO       .EOK                                      @ZA31158
.NEXT56  AIF ('&EVENT'NE'EVENT56').NEXT57                    @ZA31158
&EVENTO  SETA      56                                        @ZA31158
&FIPDISP SETA      24                                        @ZA31158
         AGO       .EOK                                      @ZA31158
.NEXT57  AIF ('&EVENT'NE'EVENT57').NEXT58                    @ZA31158
&EVENTO  SETA      57                                        @ZA31158
&FIPDISP SETA      28                                        @ZA31158
         AGO       .EOK                                      @ZA31158
.NEXT58  AIF ('&EVENT'NE'EVENT58').NEXT59                    @ZA31158
&EVENTO  SETA      58                                        @ZA31158
&FIPDISP SETA      32                                        @ZA31158
         AGO       .EOK                                      @ZA31158
.NEXT59  AIF ('&EVENT'NE'EVENT59').NEXT60                    @ZA31158
&EVENTO  SETA      59                                        @ZA31158
&FIPDISP SETA      36                                        @ZA31158
         AGO       .EOK                                      @ZA31158
.NEXT60  AIF ('&EVENT'NE'ICSCHK').NEXT61                     @ZA39354
&EVENTO  SETA      60                                        @ZA39354
&FIPDISP SETA      40                                        @ZA39354
         AGO       .EOK                                      @ZA39354
.NEXT61  ANOP                                                @ZA39354
.NEXT62  ANOP                                                @ZA39354
&EVENTO  SETA      63                                        @ZA39354
.NEXT63  AIF ('&EVENT'EQ'CMDSTART').EOK                      @ZA39354
         AIF ('&EVENT'NE'CMDEND').NEXT65                     @ZA39354
&EVENTO  SETA      64                                        @ZA39354
&FIPDISP SETA      44                                        @ZA39354
         AGO       .EOK                                      @ZA39354
.NEXT65  ANOP                                                @ZA39354
&EVENTO  SETA      65                                        @ZA39354
.*                                                           @ZA39354
         AGO       .ERROR1
.EOK     AIF       ('&ASID'NE''AND'&ASIDL'NE'').ERROR4
         AIF       ('&ASID'EQ'').NULREG
         AIF       ('&ASID'(1,1)NE'(').ERROR2
         AIF       ('&ASID'EQ'(0)').SKPREG
&LABEL   LR    0,&ASID(1)              PUT ASID INTO REGISTER 0
         AGO       .SHFTID
.NULREG  AIF       ('&ASIDL'EQ'').OKLAB
         AIF       ('&ASIDL'(1,1)EQ'(').ERROR3
         AIF       ('&ASIDL'(K'&ASIDL,1)NE')').OKLAB
&LABEL   LH    0,&ASIDL                PUT ASID INTO REGISTER 0
         AGO       .SHFTID
.OKLAB   ANOP
&LABEL   LA    0,&EVENTO.(0,0)         SYSEVENT CODE
         AIF       ('&ASIDL'EQ'').IC2                          @ZA29015
         ICM   0,12,&ASIDL             PUT ASID INTO REGISTER 0
         AGO       .IC2                                        @ZA29015
.SKPREG  AIF       ('&LABEL'EQ'').SHFTID
&LABEL   DS    0H
.SHFTID  SLL   0,16(0)                 SHIFT INTO PROPER POSITION
         AIF       ('&ENTRY'NE'BRANCH').IC
         LA    14,&EVENTO.(0,0)        SYSEVENT CODE
         OR    0,14                    PUT CODE INTO REGISTER 0
         AGO       .IC2                                        @ZA29015
.IC      AIF   ('&TYPE'EQ'NOSWIN'AND'&EVENT'EQ'MEMDEL').NOSWIN @ZA29015
         IC    0,IRAC&SYSNDX+1         PUT CODE INTO REGISTER 0
         B     IRAB&SYSNDX             BRANCH AROUND CONSTANT
IRAC&SYSNDX DC AL2(&EVENTO)            SYSEVENT CODE
         AGO   .OKID1                                          @ZA29015
.NOSWIN  ANOP                          FOR MEMDEL TYPE=NOSWIN  @ZA29015
         ICM   0,3,IRAC&SYSNDX         SYSEVENT TO STOP SWAPIN @ZA29015
         B     IRAB&SYSNDX             BRANCH AROUND CONSTANTS @ZA29015
IRAC&SYSNDX DC BL1'10000000'           MASK FOR TYPE=NOSWIN    @ZA29015
            DC AL1(&EVENTO)            SYSEVENT CODE           @ZA29015
         AGO   .OKID1                                          @ZA29015
.IC2     AIF   ('&TYPE'EQ'NOSWIN'AND'&EVENT'EQ'MEMDEL').NOSWI2 @ZA29015
         AGO   .OKID2                                          @ZA29015
.NOSWI2  ANOP                                                  @ZA29015
         ICM   0,2,IRAC&SYSNDX         SYSEVENT TO STOP SWAPIN @ZA29015
         B     IRAB&SYSNDX             BRANCH AROUND CONSTANTS @ZA29015
IRAC&SYSNDX DC BL1'10000000'           MASK FOR TYPE=NOSWIN    @ZA29015
            DC BL1'00000000'           RESERVED                @ZA29015
.OKID1   ANOP
IRAB&SYSNDX EQU *
.OKID2   AIF   ('&PGN'NE''AND'&TYPE'NE'').ERROR6               @ZA29015
         AIF       ('&PGN'EQ'').OKID3  CHECK FOR PERF GROUP NO @ZA29015
         SR    14,14                   CLEAR REG 14 FOR INSERT @Z40BPCH
         IC    14,&PGN                 OBTAIN THE PERF GRP #   @Z40BPCH
         SLL   14,8                    MOVE TO BYTE 2          @Z40BPCH
         OR    0,14                    PGN IN BYTE 2 OF REG 0  @Z40BPCH
.OKID3   AIF       ('&ENTRY'EQ'BRANCH').BR                     @ZA29015
         AIF       ('&ENTRY'NE'SVC').ERROR5
.SVC     SVC   95                      SYSTEM RESOURCES MANAGER SVC
         MEXIT
.BR      ANOP
         AIF   ('&FIPDISP'NE'99').FASTBR                       @ZA31158
         L     15,CVTPTR(,0)           SYSTEM CVT ADDRESS
         L     15,CVTOPTE-CVT(,15)     SYSTEM RESOURCES MANAGER ENTRY
         BALR  14,15                   INVOKE SYSTEM RESOURCES MANAGER
         MEXIT
.FASTBR  ANOP
         L     15,CVTPTR(,0)           SYSTEM CVT ADDRESS      @ZA31158
         L     15,CVTRV609-CVT(,15)    SRM ENTRY TABLE         @ZA31158
         L     14,&FIPDISP.(,15)       SYSEVENT ENTRY ADDRESS  @ZA31158
         L     15,0(,15)               MAIN ENTRY POINT        @ZA31158
         BALR  14,14                   INVOKE SRM              @ZA31158
         MEXIT                                                 @ZA31158
.ERROR1  MNOTE     4,'***  IRA001  INVALID SYSEVENT MNEMONIC NAME OPERAX
               ND -                &EVENT  ***'
         MEXIT
.ERROR2  MNOTE     8,'***  IRA002  INVALID ASID OPERAND SPECIFICATION -X
                                   &ASID  ***'
         MNOTE     *,'             USE ASIDL KEYWORD WHEN THE ASID VALUX
               E                   IS NOT CONTAINED IN A REGISTER'
         MEXIT
.ERROR3  MNOTE     8,'***  IRA003  INVALID ASIDL OPERAND SPECIFICATION X
               -                   &ASIDL  ***'
         MNOTE     *,'             USE ASID KEYWORD IF THE ASID VALUE IX
               S                   PROVIDED IN A REGISTER'
         MEXIT
.ERROR4  MNOTE     8,'***  IRA004  ASID AND ASIDL PARAMETERS ARE       X
                                   MUTUALLY EXCLUSIVE  ***'
         MEXIT
.ERROR5  MNOTE     4,'***  IRA005  INVALID ENTRY TYPE - SVC ASSUMED  **X
               *'
         AGO       .SVC
.ERROR6  MNOTE     8,'***  IRA006  PGN AND TYPE PARAMETERS ARE         X
                                   MUTUALLY EXCLUSIVE  ***'    @ZA29015
         MEXIT                                                 @ZA29015
         MEND
