         MACRO
   IECICS  &CHAN,&SLCHS
         GBLB  &IECCSFT
         LCLA  &COUNT,&LCHNS(60),&LCOUNT,&COUNT2                   I267
         LCLA  &NUM,&NUM2,&NUM3,&X
         LCLC  &LABEL,&LCHADR(30)
.***************************************************************
.*
.*       THIS MACRO GENERATES THE CHANNEL SEARCH MODULE CODE
.*       AND ENTRIES TO BE USED BY CHANNEL RESTART
.*
.*       ITS INPUT CONSISTS OF TWO ARGUMENTS:
.*             &CHAN - IS THE CHANNEL NUMBER FOR WHICH
.*             THIS ENTRY SET IS BEING BUILT
.*             &SLCHS - IS A VECTOR WITH THE FOLLOWING INFORMATION
.*                 1 - THE LOWEST LCH ADDRESS ASSOCIATED WITH
.*                     THIS CHANNEL MINUS ONE. THIS IS REPRESENTED
.*                     BY &SLCHS(1).
.*                 2 - TWO ENTRIES FOR EACH LCH BETWEEN THE
.*                     LOWEST AND HIGHEST LCH ADDRESSES FOR
.*                     THIS CHANNEL. THESE ENTRIES ARE:
.*                        0 - THIS CHAN NOT ON THIS LCH
.*                        1 - THIS CHAN ON THIS LCH
.*                        .
.*                        0 - THIS LCH HAS NO STAND ALONE SEEK
.*                            DEVICES
.*                        1 - THIS LCH HAS STAND ALONE SEEK DEVICES
.*                     THE FIRST INDICATOR IS USED TO TELL HOW MANY
.*                     AND WHICH LCHS SHOULD BE INCLUDED AND THE SECOND
.*                     TO INDICATE WHETHER A STAND ALONE SEEK LCH
.*                     NEEDS TO BE GENERATED FOR THIS LCH.
.*                 3 - AN END OF ENTRY INDICATOR
.*                     0 - IF THIS IS NOT THE LAST CHANNEL
.*                     1 - IF THIS IS THE LAST CHANNEL (AND THUS
.*                         THE LAST ENTRY TO IECICS)
.***************************************************************
         AIF   (&IECCSFT EQ 1).NOHEAD
&IECCSFT SETB 1
         TITLE 'CHANNEL SEARCH MODULES'
*****************************************************************
*                                                               *
*              CHANNEL SEARCH MODULES                           *
*        ONE PER PHYSICAL CHANNEL DEPICTING THE LOGICAL CHANNELS*
*        ASSOCIATED WITH EACH CHANNEL AND THE OPERATIONS TO BE  *
*        PERFORMED FOR THEM. THE OPERATIONS ARE                 *
*              0  - STAND ALONE SEEKS FOR THIS LCH              *
*              4  - SENSE LCH FOR CHANNEL                       *
*              8  - DATA TRANSFER PATH THROUGH CHANNEL SEARCH.  *
*                   IF MORE THAN ONE OF THESE EXIST, THEY ARE   *
*                   ALTERNATED.                                 *
*              12 - END OF ENTRIES                              *
*                                                               *
*****************************************************************
         SPACE 2
.NOHEAD  ANOP
&COUNT2  SETA  2
         AIF   (T'&SLCHS EQ 'O').MPXSRH
.LOOP    AIF   (&SLCHS(&COUNT2) EQ 0).CKEND
         AIF   (&COUNT2 EQ N'&SLCHS).ZZ
&LCOUNT  SETA  &LCOUNT+1
.CKEND   AIF   (&COUNT2+1 GE N'&SLCHS).ZZ
&COUNT2  SETA  &COUNT2+2
         AGO   .LOOP
.ZZ      ANOP
IECCX&CHAN BALR 12,14                   SET POINTER TO C.S.TABLE
&LCOUNT  SETA  0
&COUNT2  SETA  1                        INITIALIZE COUNTER
         AGO   .LCHFND2                 SKIP FIRST UPDATE
.LCHFND  ANOP
&COUNT2  SETA  &COUNT2+2                UPDATE THRU SLCH LIST
.LCHFND2 ANOP
&COUNT   SETA  &COUNT+1                UPDATE COUNTER
         AIF   (&COUNT2+1  EQ  N'&SLCHS).MVCCODE  TEST END OF LIST
         AIF   (&SLCHS(&COUNT2+1)  EQ  0).LCH3   CHECK IF LCH ASSIGNED
&LCOUNT  SETA  &LCOUNT+1               UPDATE NUM OF LCHS THIS CHAN
&LCHNS(&LCOUNT) SETA &COUNT+&SLCHS(1)  SET PROPER LCH NUMBER
.LCH3   AIF   (&SLCHS(&COUNT2+2)  EQ  0).LCHFND   CHECK FO R DA SK DEV
         DC    H'0',AL2(LCH&LCHNS(&LCOUNT).-IECVLCH) SA SEEK ENTRY
         AGO   .LCHFND                 RETURN TO FIND LOOP
.MVCCODE ANOP
&X       SETA  &CHAN+1
         DC    H'4',AL2(SLCH&X.-IECVLCH) SENSE LCH ENTRY
&X       SETA  1
.LCH2    ANOP
         DC    H'8',AL2(LCH&LCHNS(&X).-IECVLCH) DATA XFER ENTRY
&X       SETA  &X+1
         AIF   (&X GT &LCOUNT).EOE
         AGO   .LCH2
.MPXSRH  ANOP
IECCX&CHAN BALR 12,14                   SET POINTER TO C.S.TABEL
&X SETA &CHAN+1
         DC    H'4',AL2(SLCH&X.-IECVLCH) SENSE LCH ENTRY
         DC    H'8',AL2(LCH&X.-IECVLCH) DATA XFER ENTRY
.EOE     DC    H'12',AL2(0)             END OF ENTRIES
         SPACE 1
         MEND
