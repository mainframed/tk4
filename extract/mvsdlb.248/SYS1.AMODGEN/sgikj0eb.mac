         MACRO
         SGIKJ0EB &DSTYPE=,&BLKSIZE=,&RECFM=,&DLRECLF=,          A45714*
               &MLRECLF=,&DLRECLV=,&MLRECLV=,&TRNSLT=,&CHKR=,    A45714*
               &USREXT=,&PRMTR=,&DSNSRC=,&DATEXIT=               Y02676
         LCLA  &TEMPA,&I,&J,&ATTR1(26),&ATTR2(26),&CODE(26)      A45714
         LCLC  &CHKOPT(26)
.*A003500,004500,077500-077960,078460-078960,079460-079992       A45714
.*A106500,107500,137600,182500                                   A45714
.*C002000-003000,004000,005000,075000,077000,154000-182000       A45714
.*C183000-423000,586000,594000,613000,621000,633000,645000       A45714
.*C661000,669000                                                 A45714
.*D009000-059000,078000,079000,080000-086000,107000              A45714
.*D152000-153000,424000-575000                                   A45714
.*A209500                                                        A50465
.*C346000-347000                                                 A50479
.*D359000-362000                                                 A50479
.*A107600,137800,199500,200500,212500-216600,243500              Y02676
.*A245500-245700,324500-324940,352500,378500-378800              Y02676
.*A417500-417940                                                 Y02676
.*C004000,004500-005000,075000,077600-077700,079100,199000       Y02676
.*C244000                                                        Y02676
.*D212000-216000                                                 Y02676
.*A586500,594500,613500,621500,633500,645500,661500,669500     @ZA02998
* C 244000                                                      ZA35948
IKJEBEPD CSECT
***********************************************************************
*                                                                     *
*STATUS  --  VERSION NO. 01, O/S RELEASE NO. 20.                      *
*                                                                     *
*FUNCTION  --  THIS MODULE CONSISTS SOLELY OF A TABLE OF CONSTANTS    *
*   CORRESPONDING TO THE SUNDRY DATA SET TYPES.                       *
*                                                                     *
*   IKJEBEPS & IKJEBEPD ARE THE 1ST & 2ND CSECTS, RESP., OF A TWO-    *
*   CSECT LOAD MODULE.                                                *
*                                                                     *
*ENTRY POINTS, INPUT, OUTPUT, EXTERNAL REFERENCES, EXITS, NORMAL, AND *
*EXITS,ERROR  --  DO NOT APPLY.                                       *
*                                                                     *
*TABLES/WORK AREAS -- IKJEBEPD, THE PROCESSOR DATA TABLE.             *
*   EACH ENTRY IS 82 BYTES LONG & BEGINS WITH AN 8-BYTE DATA     Y02676
*   SET TYPE NAME, FOLLOWED BY A DATA SET NAME QUALIFIER, BOTH LEFT-  *
*   JUSTIFIED AND PADDED TO THE RIGHT WITH BLANKS.  AT SYSTEM    A45714
*   GENERATION TIME UPTO 10 NEW USER DATA SET TYPES MAY BE ADDED A45714
*   AND THE FOLLOWING DEFAULT ATTRIBUTES OF BOTH THE USER AND THE     *
*   IBM SUPPORTED DATA SET TYPES MAY BE MODIFIED: DEFAULT BLOCK       *
*   SIZE, DEFAULT RECORD FORMAT, DEFAULT AND MAXIMUM LOGICAL RECORD   *
*   LENGTH, DEFAULT CHARACTER CONVERSION, SYNTAX CHECKER NAME,        *
*   PROMPTER NAME, USER EXIT NAME(USER DATA SET TYPES ONLY),     Y02676
*   PROMPTER INPUT SOURCE, AND DATA EXIT ROUTINE NAME (SEQUENCE  Y02676
*   DEPENDENT DATA SET TYPES). TYPE CODES LESS THAN X'33'        Y02676
*   (DEC. 51) INDICATE THE DATA SET TYPES WHICH ARE EDITABLE.    A45714
*   CODES LESS THAN X'15'(DEC.21)INDICATE STANDARD EDIT DATA SET A45714
*   TYPES.  CODES GREATER THAN X'14'(DEC.20) AND LESS THAN X'1F' A45714
*   (DEC.31)INDICATE STANDARD EDIT DATA SET TYPES WHOSE PROMPTERSA45714
*   ARE IBM PROGRAM PRODUCTS.  CODES GREATER THAN X'1D'(DEC. 29) Y02676
*   AND LESS THAN X'23' (DEC. 35) INDICATE DATA SET TYPES WHOSE  A45714
*   COMPILERS AND SYNTAX CHECKERS ARE IBM PROGRAM PRODUCTS.      A45714
*   CODES GREATER THAN X'22'(DEC.34) AND LESS THAN X'33 (DEC.51) A45714
*   INDICATE USER DATA SET TYPES SUPPLIED AT SYSGEN TIME.        A45714
*                                                                     *
*              FIELD                        LENGTH(BYTES)             *
*                                                                     *
*              DATA SET TYPE                8                         *
*              DSNAME QUALIFIER             8                         *
*              DEFAULT BLOCK SIZE           2                         *
*              LINE NO. OFFSET              1                         *
*              LINE NO. LENGTH              1                         *
*              TABULATION SETTINGS         12                         *
*              SYNTAX CHECKER NAME          8                         *
*              DATA SET TYPE CODE           1                         *
*              DATA SET ATTRIBUTES          1                         *
*              DATA SET ATTRIBUTES2         1                         *
*              RECORD FORMAT DEFAULT        1                         *
*              F FORMAT LENGTH DEFAULT      2                         *
*              F FORMAT LENGTH MAXIMUM      2                         *
*              V FORMAT LENGTH DEFAULT      2                         *
*              V FORMAT LENGTH MAXIMUM      2                         *
*              U FORMAT LENGTH DEFAULT      2                         *
*              U FORMAT LENGTH MAXIMUM      2                         *
*              SYNTAX CHECKER OPT. BYTES    2                    A45714
*              PROMPTER NAME                8                         *
*              USER EXIT NAME               8                    A45714
*              DATA EXIT ROUTINE NAME       8                    Y02676
*                                                                     *
*ATTRIBUTES  --  NON-EXECUTABLE.                                      *
*                                                                     *
*NOTES  --  THE SYNTAX-CHECKER-NAME FIELD MUST BE ON A DOUBLEWORD     *
*   BOUNDARY IN EACH TABLE ENTRY.                                     *
*   THERE IS CHARACTER-CODE INDEPENDENCE.                             *
*                                                                     *
***********************************************************************
         SPACE
         SPACE
***********************************************************************
*                                                                     *
*                               EQUATES                               *
*                                                                     *
***********************************************************************
         SPACE
* DATA SET ATTRIBUTES
         SPACE
CCMUSTAY EQU   X'08' -   CONTINUATION CHAR MUST REMAIN IN RCD
LINUMREQ EQU   X'04' -   DATA SET MUST BE LINE-NUMBERED
LRLDFREQ EQU   X'02' -   DEFAULT LRECL IS REQUIRED
RUNOK    EQU   X'80' -   DATA SET IS EXECUTABLE UNDER EDIT
SNTXCKOK EQU   X'40' -   SYNTAX CHECKING IS AVAILABLE
CAPSREQ  EQU   X'20' -   "CAPS" ATTRIBUTE IS REQUIRED
DFALTCAP EQU   X'10' -   "CAPS" IS A DEFAULT ATTRIBUTE
         SPACE
* DATA SET ATTRIBUTES2
         SPACE
NUMINTAB EQU   X'80' -   LINE NO. LNG IS INCLUDED IN TAB VALUE
DFALTYPE EQU   X'40' -   DSTYPE IS DEFAULT FOR THIS DSNAME QUALIFIER
RUNOBGEN EQU   X'20' -   OBJECT DATA SET GENERATED AT RUN TIME.
INCOREDS EQU   X'10' -   INCORE INPUT ACCEPTABLE AT RUN TIME.    A45714
INLISTDS EQU   X'08' -   INLIST INPUT ACCEPTABLE AT RUN TIME     Y02676
         SPACE
* RECORD FORMAT DEFAULT
         SPACE
FMTDFALF EQU   X'80' -   FIXED FORMAT IS DEFAULT
FMTDFALV EQU   X'40' -   VARIABLE FORMAT IS DEFAULT
FMTDFALU EQU   X'C0' -   UNDEFINED FORMAT IS DEFAULT
         SPACE
***********************************************************************
*                                                                     *
*                        PROCESSOR DATA TABLE                         *
*                                                                     *
***********************************************************************
         SPACE
TABLE    EQU   *
.* INITIALIZE LOOP CONTROL VALUE 'J' TO NO. OF DSTYPES PASSED    A45714
&J       SETA  N'&DSTYPE                                         A45714
.* CHECK IF DSTYPE ENTRY 'J' IS NULL - YES, ADJUST LOOP 'J'      A45714
.LOOP0   AIF   ('&DSTYPE(&J)' NE '').OUT0                        A45714
&J       SETA  &J-1                                              A45714
         AGO   .LOOP0                                            A45714
.OUT0    ANOP                                                    A45714
.* INITIALIZE INDEX 'I' TO ONE.                                  A45714
&I       SETA  1                                                 A45714
.* CHECK IF ALL DSTYPES PASSED PROCESSED                         A45714
.LOOP2   AIF   (&I GT &J).OUT2                                   A45714
.* INITIALIZE DSTYPE ATTRIBUTES1 TO ZERO.                        A45714
&ATTR1(&I) SETA 0                                                A45714
.* CHECK IF DSTYPE REQUIRES THAT CONTINUATION CHAR. MUST REMAIN. A45714
         AIF   ('&DSTYPE(&I)' EQ 'GOFORT').TAG11                 A45714
         AIF   ('&DSTYPE(&I)' NE 'CLIST').TAG12                  A45714
.TAG11   ANOP                                                    A45714
&ATTR1(&I) SETA &ATTR1(&I)+8                                     A45714
.* CHECK IF DSTYPE REQUIRES LINE NUMBERS.                        A45714
.TAG12   AIF   ('&DSTYPE(&I)' EQ 'CLIST').TAG13                  A45714
         AIF   ('&DSTYPE(&I)' EQ 'GOFORT').TAG13                 A45714
         AIF   ('&DSTYPE(&I)' EQ 'BASIC').TAG13                  A45714
         AIF   ('&DSTYPE(&I)' NE 'IPLI').TAG14                   A45714
.TAG13   ANOP                                                    A45714
&ATTR1(&I) SETA &ATTR1(&I)+4                                     A45714
.* CHECK IF DSTYPE REQUIRES FIXED RECORD FORMAT.                 A45714
.TAG14   AIF   ('&RECFM(&I)' EQ 'VAR').TAG15                     A45714
&TEMPA   SETA  &MLRECLF(&I)                                      A45714
         AIF   (&DLRECLF(&I) NE &TEMPA).TAG15                    A45714
         AIF   (&MLRECLV(&I) NE 0).TAG15                         A45714
&ATTR1(&I) SETA &ATTR1(&I)+2                                     A45714
.* CHECK IF DSTYPE HAS A PROMPTER.                               A45714
.TAG15   AIF   ('&PRMTR(&I)' EQ '').TAG16                        A45714
&ATTR1(&I) SETA &ATTR1(&I)+128                                   A45714
.* CHECK IF DSTYPE HAS A SYNTAX CHECKER.                         A45714
.TAG16   AIF   ('&CHKR(&I)' EQ '').TAG17                         A45714
&ATTR1(&I) SETA &ATTR1(&I)+64                                    A45714
.TAG17   AIF   ('&TRNSLT(&I)' EQ 'ASIS').TAG18                   A45714
.* CHECK IF DSTYPE HAS UPPER CASE CONVERSION AS DEFAULT.         A45714
&ATTR1(&I) SETA &ATTR1(&I)+16                                    A45714
.* CHECK IF UPPER CASE CONVERSION IS REQUIRED.                   A45714
         AIF   ('&TRNSLT(&I)' NE 'CAPSONLY').TAG18               A45714
&ATTR1(&I) SETA &ATTR1(&I)+32                                    A45714
.TAG18   ANOP                                                    A45714
.* INITIALIZE DSTYPE ATTRIBUTES2 TO ZERO.                        A45714
&ATTR2(&I) SETA 0                                                A45714
.* CHECK IF DSTYPE IS COBOL OR VSBASIC - SPECIAL LINE NO. FIELD. Y02676
         AIF   ('&DSTYPE(&I)' EQ 'VSBASIC').TAG18A               Y02676
         AIF   ('&DSTYPE(&I)' NE 'COBOL').TAG19                  A45714
.TAG18A  ANOP                                                    Y02676
&ATTR2(&I) SETA &ATTR2(&I)+128                                   A45714
.TAG19   ANOP                                                    A45714
.* CHECK IF DSTYPE IS DEFAULT DSNAME QUALIFIER TYPE.             A45714
         AIF   ('&DSTYPE(&I)'(1,4) EQ 'FORT').TAG20              A45714
         AIF   ('&DSTYPE(&I)' EQ 'PLIF').TAG20                   A45714
&ATTR2(&I) SETA &ATTR2(&I)+64                                    A45714
.* CHECK IF DSTYPE GENERATES AN OBJECT DATA SET.                 A45714
.TAG20   AIF   ('&DSTYPE(&I)' EQ 'ASM').TAG21                    A45714
         AIF   ('&DSTYPE(&I)' EQ 'COBOL').TAG21                  A45714
         AIF   ('&DSTYPE(&I)' EQ 'PLI').TAG21                    A50465
         AIF   ('&DSTYPE(&I)' NE 'FORTGI').TAG22                 A45714
.TAG21   ANOP                                                    A45714
&ATTR2(&I) SETA &ATTR2(&I)+32                                    A45714
.* ESTABLISH SOURCE DATA ATTRIBUTES FOR RUN SUBCOMMAND.  IF      Y02676
.* SOURCE FOR RUN IS 'DATASET', BYPASS ATTRIBUTE SETTING.        Y02676
.TAG22   AIF   ('&DSNSRC(&I)' EQ 'DATASET').TAG23                Y02676
         AIF   ('&DSNSRC(&I)' EQ 'INLIST').TAG22A                Y02676
&ATTR2(&I) SETA &ATTR2(&I)+16 SET 'INCORE' INDICATION            Y02676
         AGO   .TAG23                                            Y02676
.TAG22A  ANOP                                                    Y02676
&ATTR2(&I) SETA &ATTR2(&I)+8  SET 'INLIST' INDICATION            Y02676
.TAG23   ANOP                                                    Y02676
.* INCREMENT INDEX 'I' BY ONE.                                   A45714
&I       SETA  &I+1                                              A45714
.* REPEAT LOOP.                                                  A45714
         AGO   .LOOP2                                            A45714
.OUT2    ANOP                                                    A45714
.* INITIALIZE INDEX 'I' TO ONE.                                  A45714
&I       SETA  1                                                 A45714
.* CHECK IF ALL DSTYPES PROCESSED - INDEX 'I' GT NO. OF DSTYPES. A45714
.LOOP3   AIF   (&I GT &J).OUT3                                   A45714
.* INITIALIZE SYNTAX CHECKER OPTION BYTES TO ZERO.               A45714
&CHKOPT(&I) SETC '00'                                            A45714
.* ASSIGN DSTYPE CODE AND CHECKER OPTION VALUES TO EACH DSTYPE.  A45714
         AIF   ('&DSTYPE(&I)' EQ 'PLI').TAG29                    A45714
         AIF   ('&DSTYPE(&I)' EQ 'PLIF').TAG30                   A45714
         AIF   ('&DSTYPE(&I)' EQ 'BASIC').TAG31                  A45714
         AIF   ('&DSTYPE(&I)' EQ 'FORTE').TAG32                  A45714
         AIF   ('&DSTYPE(&I)' EQ 'FORTG').TAG33                  A45714
         AIF   ('&DSTYPE(&I)' EQ 'FORTH').TAG34                  A45714
         AIF   ('&DSTYPE(&I)' EQ 'FORTGI').TAG35                 A45714
         AIF   ('&DSTYPE(&I)' EQ 'GOFORT').TAG36                 A45714
         AIF   ('&DSTYPE(&I)' EQ 'IPLI').TAG37                   A45714
         AIF   ('&DSTYPE(&I)' EQ 'TEXT').TAG38                   A45714
         AIF   ('&DSTYPE(&I)' EQ 'DATA').TAG39                   A45714
         AIF   ('&DSTYPE(&I)' EQ 'CLIST').TAG40                  A45714
         AIF   ('&DSTYPE(&I)' EQ 'CNTL').TAG41                   A45714
         AIF   ('&DSTYPE(&I)' EQ 'ASM').TAG42                    A45714
         AIF   ('&DSTYPE(&I)' EQ 'COBOL').TAG43                  A45714
         AIF   ('&DSTYPE(&I)' EQ 'VSBASIC').TAG28                Y02676
&CODE(&I) SETA  &I+20                                           ZA35948
         AGO   .TAG44                                            A45714
.TAG28   ANOP                                                    Y02676
&CODE(&I) SETA 30                                                Y02676
         AGO   .TAG44                                            Y02676
.TAG29   ANOP                                                    A45714
&CODE(&I) SETA 34                                                A45714
&CHKOPT(&I) SETC '0248'                                          A45714
         AGO   .TAG44                                            A45714
.TAG30   ANOP                                                    A45714
&CODE(&I) SETA 1                                                 A45714
&CHKOPT(&I) SETC '0248'                                          A45714
         AGO   .TAG44                                            A45714
.TAG31   ANOP                                                    A45714
&CODE(&I) SETA 32                                                A45714
&CHKOPT(&I) SETC '0100'                                          A45714
         AGO   .TAG44                                            A45714
.TAG32   ANOP                                                    A45714
&CODE(&I) SETA 2                                                 A45714
&CHKOPT(&I) SETC '0101'                                          A45714
         AGO   .TAG44                                            A45714
.TAG33   ANOP                                                    A45714
&CODE(&I) SETA 3                                                 A45714
&CHKOPT(&I) SETC '0202'                                          A45714
         AGO   .TAG44                                            A45714
.TAG34   ANOP                                                    A45714
&CODE(&I) SETA 4                                                 A45714
&CHKOPT(&I) SETC '0002'                                          A45714
         AGO   .TAG44                                            A45714
.TAG35   ANOP                                                    A45714
&CODE(&I) SETA 23                                                A45714
&CHKOPT(&I) SETC '0402'                                          A45714
         AGO   .TAG44                                            A45714
.TAG36   ANOP                                                    A45714
&CODE(&I) SETA 31                                                A45714
&CHKOPT(&I) SETC '0302'                                          A45714
         AGO   .TAG44                                            A45714
.TAG37   ANOP                                                    A45714
&CODE(&I) SETA 33                                                A45714
         AGO   .TAG44                                            A45714
.TAG38   ANOP                                                    A45714
&CODE(&I) SETA 5                                                 A45714
         AGO   .TAG44                                            A45714
.TAG39   ANOP                                                    A45714
&CODE(&I) SETA 6                                                 A45714
         AGO   .TAG44                                            A45714
.TAG40   ANOP                                                    A45714
&CODE(&I) SETA 7                                                 A45714
         AGO   .TAG44                                            A45714
.TAG41   ANOP                                                    A45714
&CODE(&I) SETA 8                                                 A45714
         AGO   .TAG44                                            A45714
.TAG42   ANOP                                                    A45714
&CODE(&I) SETA 21                                                A45714
         AGO   .TAG44                                            A45714
.TAG43   ANOP                                                    A45714
&CODE(&I) SETA 22                                                A45714
.TAG44   ANOP                                                    A45714
.* INCREMENT INDEX 'I' BY ONE.                                   A45714
&I       SETA  &I+1                                              A45714
.* REPEAT LOOP.                                                  A45714
         AGO   .LOOP3                                            A45714
.OUT3    ANOP                                                    A45714
.* INITIALIZE INDEX 'I' TO ONE.                                  A45714
&I       SETA  1                                                 A45714
.* CHECK IF ALL DSTYPES PROCESSED - INDEX 'I' GT NO. OF DSTYPES. A45714
.MAINLP  AIF   (&I GT &J).MAINEXT                                A45714
.* BUILD EACH DATA SET TYPE ENTRY TO GENERATE TABLE.             A45714
         DC    CL8'&DSTYPE(&I)' -            DATA SET TYPE       A45714
.* CHECK IF DSTYPE IS PLI OR PLIF - DSNAME QUALIFIER IS PLI.     A45714
         AIF   ('&DSTYPE(&I)'(1,3) NE 'PLI').TAGM10              A45714
         DC    CL8'PLI' -                    DSNAME QUALIFIER    A45714
         AGO   .TAGM13                                           A45714
.* CHECK IF DSTYPE IS ONE OF FORTRANS - DSNAME QUALIFIER IS FORT.A45714
.TAGM10  AIF   ('&DSTYPE(&I)'(1,4) EQ 'FORT').TAGM11             A45714
         AIF   ('&DSTYPE(&I)' NE 'GOFORT').TAGM12                A45714
.TAGM11  ANOP                                                    A45714
         DC    CL8'FORT' -                   DSNAME QUALIFIER    A45714
         AGO   .TAGM13                                           A45714
.TAGM12  ANOP                                                    A45714
.* DSTYPE IS ALSO DSNAME QUALIFIER.                              A45714
         DC    CL8'&DSTYPE(&I)' -            DSNAME QUALIFIER    A45714
.TAGM13  ANOP                                                    A45714
         DC    HL2'&BLKSIZE(&I)' -           DEFAULT BLOCK SIZE  A45714
.* CHECK FOR TYPE VSBASIC - SPECIAL CASE SEQUENCE NUMBER IS      Y02676
.* 5 CHARACTERS LONG BEGINNING IN THE FIRST POSITION OF THE LINE Y02676
         AIF   ('&DSTYPE(&I)' NE 'VSBASIC').TAG13A               Y02676
         DC    FL1'1' -                      LINE NO. OFFSET     Y02676
         DC    FL1'5' -                      LINE NO. LENGTH     Y02676
         AGO   .TAGM15                                           Y02676
.TAG13A  ANOP                                                    Y02676
.* CHECK IF DEFAULT RECORD FORMAT IS VARIABLE - SET LINE NUMBER  A45714
.*     FIELD DEFAULT TO COLUMNS 1 THRU 8.                        A45714
         AIF   ('&RECFM(&I)' NE 'VAR').TAGM14                    A45714
         DC    FL1'1' -                      LINE NO. OFFSET     A45714
         DC    FL1'8' -                      LINE NO. LENGTH     A45714
         AGO   .TAGM15                                           A45714
.TAGM14  ANOP                                                    A45714
.* COBOL HAS SPECIAL LINE NUMBER FIELD - COLUMN 1 THRU 6.        A45714
         AIF   ('&DSTYPE(&I)' NE 'COBOL').TAGM14A                A45714
         DC    FL1'1' -                      LINE NO. OFFSET     A45714
         DC    FL1'6' -                      LINE NO. LENGTH     A45714
         AGO   .TAGM15                                           A45714
.TAGM14A ANOP                                                    A45714
.* SET LINE NUMBER OFFSET TO LRECL - 7 FOR FIXED FORMAT.         A45714
&TEMPA   SETA  &DLRECLF(&I)-7                                    A45714
         DC    AL1(&TEMPA) -                 LINE NO. OFFSET     A45714
         DC    FL1'8' -                      LINE NO. LENGTH     A45714
.TAGM15  ANOP                                                    A45714
         DC    X'FF'                                             A45714
.* ASSIGN DEFAULT TAB SETTINGS TO EACH DSTYPE.                   A45714
         AIF   ('&DSTYPE(&I)' EQ 'PLI').TAG1A                    A45714
         AIF   ('&DSTYPE(&I)' EQ 'PLIF').TAG1A                   A50479
         AIF   ('&DSTYPE(&I)' EQ 'IPLI').TAG1A                   A50479
         AIF   ('&DSTYPE(&I)'(1,4) EQ 'FORT').TAG2               A45714
         AIF   ('&DSTYPE(&I)' EQ 'GOFORT').TAG2                  A45714
         AIF   ('&DSTYPE(&I)' EQ 'TEXT').TAG3                    A45714
         AIF   ('&DSTYPE(&I)' EQ 'ASM').TAG4                     A45714
         AIF   ('&DSTYPE(&I)' EQ 'COBOL').TAG5                   A45714
         AIF   ('&DSTYPE(&I)' EQ 'VSBASIC').TAG5A                Y02676
         DC    FL1'10,20,30,40,50,60' -      TABULATION SETTINGS A45714
         DC    5X'0' -                                           A45714
         AGO   .TAG6                                             A45714
.TAG1A   ANOP                                                    A45714
         DC    FL1'5,10,15,20,25,30,35,40,45,50'                 A45714
         DC    X'0'                                              A45714
         AGO   .TAG6                                             A45714
.TAG2    ANOP                                                    A45714
         DC    FL1'7,72' -                   TABULATION SETTINGS A45714
         DC    9X'0'                                             A45714
         AGO   .TAG6                                             A45714
.TAG3    ANOP                                                    A45714
         DC    FL1'5,10,15,20,30,40' -       TABULATION SETTINGS A45714
         DC    5X'0'                                             A45714
         AGO   .TAG6                                             A45714
.TAG4    ANOP                                                    A45714
         DC    FL1'10,16,31,72' -            TABULATION SETTINGS A45714
         DC    7X'0'                                             A45714
         AGO   .TAG6                                             A45714
.TAG5    ANOP                                                    A45714
         DC    FL1'8,12,72' -                TABULATION SETTINGS A45714
         DC    8X'0'                                             A45714
         AGO   .TAG6                                             Y02676
.TAG5A   ANOP                                                    Y02676
         DC    FL1'10,15,20,25,30,35,40,45,50,55' TABULATION     Y02676
*                                                 SETTINGS       Y02676
         DC    X'0'                                              Y02676
.TAG6    ANOP                                                    A45714
.* CHECK IF SYNTAX CHECKER SPECIFIED FOR DSTYPE.                 A45714
         AIF   ('&CHKR(&I)' EQ '').TAGM16                        A45714
         DC    CL8'&CHKR(&I)' -              SYNTAX CHECKER NAME A45714
         AGO   .TAGM17                                           A45714
.TAGM16  ANOP                                                    A45714
         DC    8X'0' -                       SYNTAX CHECKER NAME A45714
.TAGM17  ANOP                                                    A45714
         DC    AL1(&CODE(&I)) -              DATA SET TYPE CODE  A45714
         DC    AL1(&ATTR1(&I)) -             DATA SET ATTRIBUTE1 A45714
         DC    AL1(&ATTR2(&I)) -             DATA SET ATTRIBUTE2 A45714
.* ASSIGN DSTYPE RECORD FORMAT DEFAULT.                          A45714
         AIF   ('&RECFM(&I)' NE 'VAR').TAGM18                    A45714
         DC    AL1(FMTDFALV) -               RECORD FORMAT DFLT  A45714
         AGO   .TAGM19                                           A45714
.TAGM18  ANOP                                                    A45714
         DC    AL1(FMTDFALF) -               RECORD FORMAT DFLT  A45714
.TAGM19  ANOP                                                    A45714
         DC    HL2'&DLRECLF(&I)' -           F FORMAT LRECL DFLT A45714
         DC    HL2'&MLRECLF(&I)' -           F FORMAT LRECL MAX  A45714
         DC    HL2'&DLRECLV(&I)' -           V FORMAT LRECL DFLT A45714
         DC    HL2'&MLRECLV(&I)' -           V FORMAT LRECL MAX  A45714
         DC    HL2'0' -                      U FORMAT LRECL DFLT A45714
         DC    HL2'0' -                      U FORMAT LRECL MAX  A45714
         DC    XL2'&CHKOPT(&I)' -            CHECKER OPT. BYTES  A45714
.* CHECK IF PROMPTER SPECIFIED FOR DSTYPE.                       A45714
         AIF   ('&PRMTR(&I)' EQ '').TAGM20                       A45714
         DC    CL8'&PRMTR(&I)' -             PROMPTER NAME       A45714
         AGO   .TAGM21                                           A45714
.TAGM20  ANOP                                                    A45714
         DC    8X'0' -                       PROMPTER NAME       A45714
.TAGM21  ANOP                                                    A45714
.* CHECK IF USER EXIT SPECIFIED FOR DSTYPE.                      A45714
         AIF   ('&USREXT(&I)' EQ '').TAGM22                      A45714
         DC    CL8'&USREXT(&I)' -            USER EXIT NAME      A45714
         AGO   .TAGM23                                           A45714
.TAGM22  ANOP                                                    A45714
         DC    8X'0' -                       USER EXIT NAME      A45714
.TAGM23  ANOP                                                    A45714
.* CHECK IF DATEXIT ROUTINE SPECIFIED FOR DATA SET TYPE          Y02676
         AIF   ('&DATEXIT(&I)' EQ '').TAGM24                     Y02676
         DC    CL8'&DATEXIT(&I)' -      DATA EXIT NAME           Y02676
         AGO   .TAGM25                                           Y02676
.TAGM24  ANOP                                                    Y02676
         DC    8X'0' -                  DATA EXIT NAME           Y02676
.TAGM25  ANOP                                                    Y02676
.* INCREMENT INDEX 'I' BY ONE.                                   A45714
&I       SETA  &I+1                                              A45714
*--------------------------------------------------------------- A45714
.* REPEAT LOOP.                                                  A45714
         AGO   .MAINLP                                           A45714
.MAINEXT ANOP                                                    A45714
*
* THE FOLLOWING TABLE ENTRIES APPLY TO NON-"EDIT"ABLE DATA SET TYPES.
*
*----------------------------------------------------------------------
         DC    CL8'STEX' -                   DATA SET TYPE
         DC    CL8'STEX' -                   DSNAME QUALIFIER
         DC    24X'0'
         DC    X'33' (DECIMAL 51)            DATA SET TYPE CODE
         DC    X'0' -                        DATA SET ATTRIBUTES
         DC    AL1(DFALTYPE) -               DATA SET ATTRIBUTES2
         DC    31X'0' -                                          A45714
         DC    8X'0'                                           @ZA02998
*----------------------------------------------------------------------
         DC    CL8'OBJ' -                    DATA SET TYPE
         DC    CL8'OBJ' -                    DSNAME QUALIFIER
         DC    24X'0'
         DC    X'34' (DECIMAL 52)            DATA SET TYPE CODE
         DC    X'0' -                        DATA SET ATTRIBUTES
         DC    AL1(DFALTYPE) -               DATA SET ATTRIBUTES2
         DC    31X'0' -                                          A45714
         DC    8X'0'                                           @ZA02998
*----------------------------------------------------------------------
         DC    CL8'LIST' -                   DATA SET TYPE
         DC    CL8'LIST' -                   DSNAME QUALIFIER
         DC    H'3556' -      DEFAULT BLKSIZE
         DC    FL1'1'  -      LINE NUMBER OFFSET
         DC    FL1'8'  -      LINE NUMBER LENGTH
         DC    12X'00' -           TABS
         DC    8X'0' -             SYNTAX CHECKER NAME
         DC    X'35' (DECIMAL 53)            DATA SET TYPE CODE
         DC    X'0' -                        DATA SET ATTRIBUTES
         DC    AL1(DFALTYPE) -               DATA SET ATTRIBUTES2
         DC    AL1(FMTDFALV) -     RECORD FORMAT DEFAULT
         DC    HL2'0' -            F FORMAT LRECL DEFAULT
         DC    HL2'0' -            F FORMAT LRECL MAX
         DC    HL2'148' -          V FORMAT LRECL DEFAULT
         DC    HL2'148' -          V FORMAT LRECL MAX
         DC    HL2'0' -            U FORMAT LRECL DEFAULT
         DC    HL2'0' -            U FORMAT LRECL MAX
         DC    18X'0' -                                          A45714
         DC    8X'0'                                           @ZA02998
*----------------------------------------------------------------------
         DC    CL8'LOAD' -                   DATA SET TYPE
         DC    CL8'LOAD' -                   DSNAME QUALIFIER
         DC    24X'0'
         DC    X'36' (DECIMAL 54)            DATA SET TYPE CODE
         DC    X'0' -                        DATA SET ATTRIBUTES
         DC    AL1(DFALTYPE) -               DATA SET ATTRIBUTES2
         DC    31X'0' -                                          A45714
         DC    8X'0'                                           @ZA02998
*----------------------------------------------------------------------
         DC    CL8'LINKLIST' -               DATA SET TYPE
         DC    CL8'LINKLIST' -               DSNAME QUALIFIER
         DC    HL2'81' -           DEFAULT BLKSIZE
         DC    22X'0'
         DC    X'37' (DECIMAL 55)            DATA SET TYPE CODE
         DC    X'0' -                        DATA SET ATTRIBUTES
         DC    AL1(DFALTYPE) -               DATA SET ATTRIBUTES2
         DC    AL1(FMTDFALF) -     RECFM DEFAULT
         DC    HL2'81' -           F FORMAT LRECL DEFAULT
         DC    HL2'81' -           F FORMAT LRECL MAX
         DC    26X'0'                                            A45714
         DC    8X'0'                                           @ZA02998
*----------------------------------------------------------------------
         DC    CL8'LOADLIST' -               DATA SET TYPE
         DC    CL8'LOADLIST' -               DSNAME QUALIFIER
         DC    HL2'81' -           DEFAULT BLKSIZE
         DC    22X'0'
         DC    X'38' (DECIMAL 56)            DATA SET TYPE CODE
         DC    X'0' -                        DATA SET ATTRIBUTES
         DC    AL1(DFALTYPE) -               DATA SET ATTRIBUTES2
         DC    AL1(FMTDFALF) -     RECFM DEFAULT
         DC    HL2'81' -           F FORMAT LRECL DEFAULT
         DC    HL2'81' -           F FORMAT LRECL MAX
         DC    26X'0'                                            A45714
         DC    8X'0'                                           @ZA02998
*----------------------------------------------------------------------
         DC    CL8'TESTLIST' -               DATA SET TYPE
         DC    CL8'TESTLIST' -               DSNAME QUALIFIER
         DC    HL2'1695' -         DEFAULT BLKSIZE
         DC    22X'0'
         DC    X'39' (DECIMAL 57)            DATA SET TYPE CODE
         DC    X'0' -                        DATA SET ATTRIBUTES
         DC    AL1(DFALTYPE) -               DATA SET ATTRIBUTES2
         DC    AL1(FMTDFALV) -     RECFM DEFAULT
         DC    HL2'0' -            F FORMAT LRECL DEFAULT
         DC    HL2'0' -            F FORMAT LRECL MAX
         DC    HL2'1695' -         V FORMAT LRECL DEFAULT
         DC    HL2'1695' -         V FORMAT LRECL MAX
         DC    HL2'0' -            U FORMAT LRECL DEFAULT
         DC    HL2'0' -            U FORMAT LRECL MAX
         DC    18X'0' -                                          A45714
         DC    8X'0'                                           @ZA02998
*----------------------------------------------------------------------
         DC    CL8'OUTLIST' -                DATA SET TYPE
         DC    CL8'OUTLIST' -                DSNAME QUALIFIER
         DC    24X'0'
         DC    X'3A' (DECIMAL 58)            DATA SET TYPE CODE
         DC    X'0' -                        DATA SET ATTRIBUTES
         DC    AL1(DFALTYPE) -               DATA SET ATTRIBUTES2
         DC    31X'0' -                                          A45714
         DC    8X'0'                                           @ZA02998
*----------------------------------------------------------------------
         DC    X'0' -                        END-OF-TABLE MARKER
         MEND
