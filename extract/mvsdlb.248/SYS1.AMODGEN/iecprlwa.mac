         MACRO
&LABEL   IECPRLWA &EP,&F4,&D1=(0),&D2=(0),&ADT=NO
.*
.*       MACRO DELETIONS/CHANGES FOR VS2 RELEASE 2
.*
.*
.**********************************************************************
.*
.* MODULE NAME = IECPRLWA (VS2-2)
.*
.* DESCRIPTIVE NAME = DADSM PARTIAL RELEASE WORK AREA ADDRESSING MACRO
.*
.* COPYRIGHT = NONE
.*
.* STATUS = CHANGE LEVEL 000
.*
.* FUNCTION = DADSM PARTIAL RELEASE MACRO WHICH PROVIDES ADDRESSABILITY
.*            TO THE 620 DECIMAL BYTE WORK AREA (NOT INCLUDING THE
.*            8 WORD PREFIX OR THE 6 WORD RECOVERY ROUTINE PARAMETER
.*            LIST) USED BY PARTIAL RELEASE.
.*
.* NOTES = THIS MACRO IS AN INTERNAL MACRO USED IN THE FOLLOWING
.*         DADSM FUNCTIONS:
.*
.*             PARTIAL RELEASE
.*
.*         TO REFERENCE THIS WORK AREA WITHIN THE FUNCTIONS LISTED
.*         ABOVE, CODE A USING STATEMENT ON THE LABEL 'PRLSEWKA'.
.*
.*      DEPENDENCIES = THIS MACRO IS DESIGNED TO BE USED OPTIONALLY
.*                     WITH THE IECSDSL1 DSCB MAPPING MACRO, THE
.*                     PREFX MACRO (DEFINED IN THE IECDSECS MACRO)
.*                     WHICH DEFINES THE 8 WORD WORK AREA PREFIX,
.*                     THE 20 WORD EXTENDED WORK AREA PREFIX, AND
.*                     THE 4 WORD DADSM WTG TABLE, AND THE DSMADT
.*                     MACRO (ALSO DEFINED IN THE IECDSECS MACRO)
.*                     WHICH DEFINES THE 6 WORD DADSM AUDIT TRAIL.
.*
.*      RESTRICTIONS = NONE
.*
.*      REGISTER CONVENTIONS = NOT APPLICABLE
.*
.* PATCH LABEL = NOT APPLICABLE
.*
.* MODULE TYPE = MACRO
.*
.*      PROCESSOR = ASSEMBLER LANGUAGE
.*
.*      MODULE SIZE = 620 DECIMAL BYTES (NOT INCLUDING THE PREFIX)
.*
.*      ATTRIBUTES = NOT APPLICABLE
.*
.* ENTRY POINT = PRLPREFX
.*
.*      PURPOSE = THIS MACRO IS CALLED AT ASSEMBLY TIME BY THE
.*                FUNCTIONS LISTED UNDER 'NOTES' TO EXPAND THE
.*                PARTIAL RELEASE WORK AREA.
.*
.*      LINKAGE =
.*
.*                LABEL IECPRLWA EP,F4,D1=(N),D2=(N),ADT=YES
.*
.*      INPUT =
.*
.*                OPTIONAL LABEL SPECIFIED AS AN ACCEPTABLE ASSEMBLER
.*                LABEL WHICH CAN BE USED BY THE CALLING CSECT TO
.*                ESTABLISH ADDRESSABILITY TO THE ENTIRE WORK AREA.
.*
.*                EP IS THE FIRST POSITIONAL PARAMETER, SPECIFYING
.*                THAT THE LABELS FOR EACH OF THE FIELDS IN THE 8
.*                WORD PREFIX, 20 WORD EXTENDED PREFIX, AND THE 4
.*                WORD DADSM WTG TABLE ARE TO BE EXPANDED. IF THESE
.*                FIELDS ARE NOT TO BE DEFINED, A COMMA MUST BE
.*                CODED TO INDICATE THE ABSENCE OF THIS PARAMETER.
.*
.*                F4 IS THE SECOND POSITIONAL PARAMETER, SPECIFYING
.*                THAT THE FIELDS OF THE FORMAT 4 DSCB ARE TO BE
.*                EXPANDED USING THE IECSDSL1 DSCB MAPPING MACRO.
.*                IF THESE FIELDS ARE NOT TO BE DEFINED AND A
.*                KEYWORD PARAMETER FOLLOWS, A COMMA MUST BE
.*                CODED TO INDICATE THE ABSENCE OF THIS PARAMETER.
.*
.*                D1=(N) IS A KEYWORD PARAMETER THAT SPECIFIES THE
.*                FORMAT(S) OF THE DSCB(S) TO BE EXPANDED IN THE
.*                FIRST 140 BYTE DSCB AREA, USING THE IECSDSL1 MACRO.
.*                N CAN BE A NUMBER FROM 0 - 6.  IF N IS 0 OR 4 OR
.*                IF THIS KEYWORD IS OMITTED, NO LABELS ARE EXPANDED.
.*                IF N IS 1, 2, 3, 5, OR 6, THE CORRESPONDING FORMAT
.*                OF THE DSCB WILL BE EXPANDED.  MORE THAN ONE DSCB
.*                FORMAT CAN BE MAPPED IN THE FIRST 140 BYTE DSCB
.*                AREA BY SEPARATING THE FORMAT NUMBERS BY COMMAS.
.*
.*                D2=(N) IS A KEYWORD PARAMETER THAT SPECIFIES
.*                THE FORMAT(S) OF THE DSCB(S) TO BE EXPANDED IN THE
.*                SECOND 140 BYTE DSCB AREA, USING THE IECSDSL1 MACRO.
.*                N CAN BE A NUMBER FROM 0 - 6.  IF N IS 0 OR 4 OR
.*                IF THIS KEYWORD IS OMITTED, NO LABELS ARE EXPANDED.
.*                IF N IS 1, 2, 3, 5, OR 6, THE CORRESPONDING FORMAT
.*                OF THE DSCB WILL BE EXPANDED.  MORE THAN ONE DSCB
.*                FORMAT CAN BE MAPPED IN THE SECOND 140 BYTE DSCB
.*                AREA BY SEPARATING THE FORMAT NUMBERS BY COMMAS.
.*
.*                ADT=YES IS A KEYWORD PARAMETER WHICH SPECIFIES THAT
.*                THE LABELS FOR EACH OF THE FIELDS WITHIN THE 6 WORD
.*                DADSM AUDIT TRAIL ARE TO BE EXPANDED.
.*
.*      OUTPUT = DADSM PARTIAL RELEASE WORK AREA EXPANSION
.*
.* EXIT-NORMAL = MEND STATEMENT
.*
.* EXIT-ERROR = ERROR MESSAGES:
.*
.*      MNOTE 12,***  IHB002 INVALID EP OPERAND SPECIFIED-&EP
.*      MNOTE 12,***  IHB002 INVALID F4 OPERAND SPECIFIED-&F4
.*      MNOTE 12,***  IHB002 INVALID ADT OPERAND SPECIFIED-&ADT
.*
.*      THE MACRO EXPANSION CONTINUES AS IF THE INVALID PARAMETER
.*      WERE NOT SPECIFIED.
.*
.* EXTERNAL REFERENCES = NONE
.*
.*      ROUTINES = NONE
.*
.*      DATA AREAS = PARTIAL RELEASE WORK AREA
.*
.*      CONTROL BLOCKS = DSCB
.*
.* TABLES = NONE
.*
.* MACROS = IECDSECS DSMADT
.*          IECDSECS PREFX
.*          IECSDSL1
.*          IHBERMAC
.*
.* CHANGE ACTIVITY = SEE DELETIONS/CHANGES PRECEDING THE MACRO
.*
.**********************************************************************
.*
         LCLA  &C
&LABEL   DSECT
***********************************************************************
*
*        PARTIAL RELEASE WORK AREA
*
***********************************************************************
         SPACE 1
***********************************************************************
*        PREFIX, EXTENDED PREFIX, AND WTG TABLE AREA
***********************************************************************
         SPACE 1
PRLPREFX DS    8F                       WORK AREA PREFIX
PRLSEWKA DS    20F                      EXTENDED PREFIX
FIRST    EQU   PRLSEWKA
PWTGTBL  DS    4F                       DADSM WTG TABLE
         AIF   ('&EP' EQ 'EP').PRL0010
         AIF   ('&EP' EQ '').PRL0020
         IHBERMAC 1001,EP,&EP
         AGO   .PRL0020
.PRL0010 ANOP
         ORG   PRLPREFX
         MNOTE '         IECDSECS PREFX,EXPAND=YES'
         IECDSECS PREFX,EXPAND=YES
.PRL0020 ANOP
         SPACE 1
***********************************************************************
*        FORMAT 4 DSCB AREA
***********************************************************************
         SPACE 1
VTOCDSCB DS    XL96                     FORMAT 4 DSCB
         AIF   ('&F4' EQ 'F4').PRL0030
         AIF   ('&F4' EQ '').PRL0040
         IHBERMAC 1001,F4,&F4
         AGO   .PRL0040
.PRL0030 ANOP
         ORG   VTOCDSCB
         MNOTE '         IECSDSL1 (4)'
         IECSDSL1 (4)
.PRL0040 ANOP
         SPACE 1
***********************************************************************
*        FIRST 140-BYTE DSCB AREA
***********************************************************************
         SPACE 1
INDSCB   DS    XL140                    140-BYTE DSCB AREA
&C       SETA  1
.PRL0050 AIF   (&C GT N'&D1).PRL0060
         AIF   (&D1(&C) EQ 0 OR &D1(&C) EQ 4).PRL0055
         ORG   INDSCB
         MNOTE '         IECSDSL1 (&D1(&C))'
         IECSDSL1 (&D1(&C))
.PRL0055 ANOP
&C       SETA  &C+1
         AGO   .PRL0050
.PRL0060 ANOP
         SPACE 1
***********************************************************************
*        SECOND 140-BYTE DSCB AREA
***********************************************************************
         SPACE 1
OUTDSCB  DS    XL140                    140-BYTE DSCB AREA
&C       SETA  1
.PRL0070 AIF   (&C GT N'&D2).PRL0080
         AIF   (&D2(&C) EQ 0 OR &D2(&C) EQ 4).PRL0075
         ORG   OUTDSCB
         MNOTE '         IECSDSL1 (&D2(&C))'
         IECSDSL1 (&D2(&C))
.PRL0075 ANOP
&C       SETA  &C+1
         AGO   .PRL0070
.PRL0080 ANOP
         SPACE 1
***********************************************************************
*        DADSM EXTENT TABLE
***********************************************************************
         SPACE 1
DADSMTBL DS    0XL68                    DADSM EXTENT TABLE
PTYPEFLG DS    XL1                      PARTIAL RELEASE SWITCHES
*
ERXTENT  EQU   X'80'                    OVERLAPPING EXTENTS FOUND IN F5
*                                       DSCB
FROMCR   EQU   X'40'                    ENTERED FROM CHECKPOINT/RESTART
DCHNSRCH EQU   X'20'                    DEB CHAIN SEARCH ACTIVE
RNDXTENT EQU   X'01'                    ROUND THE EXTENT TO BE RELEASED
*                                       TO A CYLINDER BOUNDARY
*
EXTNUM   DS    XL1                      NUMBER OF ENTRIES IN DADSMTBL
HOLENUM  DS    H                        NUMBER OF CREATED F0 DSCB'S
ENTRIES  DS    16F                      SIXTEEN EXTENT ENTRIES
         SPACE 1
***********************************************************************
*        PARTIAL RELEASE WORK AREA FIELDS
***********************************************************************
         SPACE 1
HOLDEXTS DS    F                        EXTENT SAVE AREA
HOLD     DS    2F                       EXTENT SAVE AREA
MBBCCHHR DS    D                        CONVERSION AREA
SVRBPTR  DS    F                        POINTER TO THE SVRB
DCBSAVE  DS    F                        USER'S DCB ADDRESS
CONVCON  DS    H                        CONVERSION WORK AREA
USEXTNUM DS    XL1                      USER'S EXTENT NUMBER
*                                       (FROM DCBFDAD)
         SPACE 1
***********************************************************************
*        ABSOLUTE TRACK ADDRESSES
***********************************************************************
         SPACE 1
VTOCADR  DS    XL5                      FORMAT 4 CCHHR
DADSMADR DS    XL5                      FORMAT 5 CCHHR
INCCHHR  DS    XL5                      CHANNEL PROGRAM WORK AREA
OUTCCHHR DS    XL5                      CHANNEL PROGRAM WORK AREA
NEXTDADS DS    XL5                      CHANNEL PROGRAM WORK AREA
         SPACE 1
***********************************************************************
*        PARTIAL RELEASE RECOVERY ROUTINE AUDIT TRAIL AREA
***********************************************************************
         SPACE 1
PRLADTRL DS    6F                       RELEASE AUDIT TRAIL WORDS
         AIF   ('&ADT' EQ 'YES').PRL0090
         AIF   ('&ADT' EQ 'NO').PRL0100
         IHBERMAC 1001,ADT,&ADT
         AGO   .PRL0100
.PRL0090 ANOP
         ORG   PRLADTRL
         MNOTE '         IECDSECS DSMADT,EXPAND=YES'
         IECDSECS DSMADT,EXPAND=YES
.PRL0100 ANOP
         SPACE 1
***********************************************************************
PRLWAEND EQU   *                        PARTIAL RELEASE WORK AREA END
PWALNGTH EQU   PRLWAEND-PWTGTBL         WORK AREA LENGTH FOR IECRES GET
***********************************************************************
         MEND
