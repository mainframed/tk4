         MACRO
&NAME    DELCPOOL &CPID=,&BRANCH=YES,&EXTENT=,&FREE=YES,&RELATED=
*        OS/VS2 RELEASE 2    --      9/15/73
         LCLB  &FS,&PL,&AL,&LS     OPTION BITS
         AIF   ('&BRANCH' NE 'YES').ERR1  BRANCH=YES MANDATORY
         AIF   ('&CPID' EQ '').ERR3       CPID PARAMETER REQUIRED
         AIF   ('&CPID'(1,1) NE '(').ERR2 CPID MUST BE REGISTER
.*       SYNTAX OK, CONTINUE WITH EXPANSION.
         AIF   ('&FREE' EQ 'YES' OR '&FREE' EQ 'NO').FROK
         IHBERMAC  1015,&FREE,''FREE='',YES
.FROK    ANOP
&FS      SETB  ('&FREE' EQ 'NO')
&PL      SETB  ('&EXTENT' EQ '')
         AIF   (&PL).COMM
&LS      SETB  ('&EXTENT' EQ 'LAST')
&AL      SETB  ('&EXTENT' EQ 'ALL')
         AIF   (1 NE &AL+&LS).ERR6
.COMM    ANOP
&NAME    STM   2,12,28(13)         SAVE CALLERS REGISTERS 2-12
         AIF   ('&CPID(1)' EQ '0').NZLD
         LR    0,&CPID(1)          LOAD R0 WITH CPID
.NZLD    LA    1,B'0000&PL&AL&LS&FS'(0,0)  SET DELCPOOL OPTIONS YM02296
         L     3,16(0,0)           CVT ADDRESS FOR DELCPOOL     YM02296
         L     15,CVTDELCP-CVTMAP(0,3) LOAD DELCPOOL ENTRY ADDR
         BALR  14,15               GO TO DELCPOOL SERVICE
         LM    2,12,28(13)         RESTORE CALLERS REGISTERS
         MEXIT
.ERR1    IHBERMAC  1006,''BRANCH=YES''
         MEXIT
.ERR2    IHBERMAC  1001,CPID,&CPID
         MEXIT
.ERR6    IHBERMAC  1001,EXTENT,&EXTENT
         MEXIT
.ERR3    IHBERMAC  1006,CPID
         MEND
