         MACRO
         MGCR  &PARAM,&PAR2
.*   MGCR MACRO
.*
.*   THIS MACRO PROVIDES A FUNCTION FOR THE ISSUANCE OF THE
.*   SVC 34. SVC 34 REQUIRES IN REGISTER 1 A POINTER TO A PARAMETER
.*   LIST. THE MACRO-INSTRUCTION IS MGCR AND IS ASSOCIATED WITH SOME
.*   OPERAND ACCORDING TO THE FOLLOWING FORMATS
.*
.*       MGCR  (1)       ,WHERE REGISTER ONE HAS BEEN LOADED WITH
.*                        THE POINTER TO THE PARAMETER LIST. THE
.*                       GENERATED CODE IS
.*
.*                                            SVC 34
.*
.*       MGCR  (X)       WHERE X (EITHER A SYMBOL EQUATED TO
.*                       A REGISTER OR THE ACTUAL REGISTER NUMBER)
.*                       HAS BEEN LOADED WITH THE POINTER TO THE
.*                       PARAMETER LIST. THE GENERATED CODE IS
.*
.*                                            LR 1,X
.*                                            SVC 34
.*
.*       MGCR  LABEL     WHERE LABEL IS THE SYMBOLIC NAME OF THE
.*                       PARAMETER LIST. THE GENERATED CODE IS
.*
.*                                            LA 1,LABEL
.*                                            SVC 34
.*
.*   THE ADDITION OF THE SECOND PARAMETER, 'CHAIN', WILL CAUSE
.*   THE CONTENT OF REGISTER ONE TO BE COMPLEMENTED BEFORE THE
.*   ISSUANCE OF SVC 34. THE GENERATED CODE IS AS FOLLOWS.
.*
.*       MGCR  (1),CHAIN
.*
.*             LNR 1,1
.*             SVC 34
.*
.*       MGCR  (X),CHAIN
.*
.*             LCR 1,X
.*             SVC 34
.*
.*       MGCR  LABEL,CHAIN
.*
.*             LA  1,LABEL
.*             LCR 1,1
.*             SVC 34
.*
         AIF   ('&PAR2' EQ 'CHAIN' OR '&PAR2' EQ '').FIRST
         MNOTE '***INVALID PARAMETER'
         MEXIT
.FIRST   AIF   ('&PARAM' NE '').GO
         MNOTE '***PARAMETER NOT SPECIFIED'
         MEXIT
.GO      AIF   ('&PAR2' EQ 'CHAIN').NEG
         AIF   ('&PARAM'(1,1) EQ '(').REG
         LA    1,&PARAM
         AGO   .ISVC
.REG     AIF   ('&PARAM(1)' EQ '1').ISVC
         LR    1,&PARAM(1)
         AGO   .ISVC
.NEG     ANOP
         SR    0,0
         AIF   ('&PARAM'(1,1) EQ '(').NREG
         LA    1,&PARAM
         LCR   1,1
         AGO   .ISVC
.NREG    AIF   ('&PARAM(1)' EQ '1').ILNR
         LCR   1,&PARAM(1)
         AGO   .ISVC
.ILNR    LNR   1,1
.ISVC    SVC   34                             ISSUE MGCR SVC
         MEND
