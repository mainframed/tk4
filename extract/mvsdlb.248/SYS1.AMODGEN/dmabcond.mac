         MACRO
&NAME    DMABCOND &CODE,&MODID,&SVC=,&DCB=,&RETURN=,&RETCODE=,         X
               &RES=YES,&REGSAVE=
.*
.*  THIS MACRO IS CALLED ONLY BY OPEN/CLOSE/EOV TRANSIENT SVC MODULES
.*  AND A FEW PROBLEM PROGRAM STATE ACCESS METHOD MODULES.
.*  IT IS USED IN PLACE OF AN ABEND MACRO CALL IN ORDER TO XCTL TO AN
.*  OPEN/CLOSE/EOV PROBLEM DETERMINATION MODULE, WHICH WILL ISSUE
.*  A WRITE-TO-PROGRAMMER AND MAY POSSIBLY RECOVER FROM THE ERROR
.*  OR ELSE THEN ISSUE THE ABEND.
.*
         AIF   ('&CODE' EQ '').E1       IF ERROR CODE MISSING
         AIF   ('&CODE' EQ 'PCK').DMA1000 INTERFACE ERROR        Y02080
         AIF   ('&DCB' EQ '').GEN3      IF NO DCB ADDR
         AIF   ('&DCB'(1,1) EQ '(').GEN4  IF DCB ADDR IN REGISTER
.GEN3    ANOP
&NAME    IHBINNRA &DCB,&CODE            LOAD REGS 1 AND 0
         AGO   .GEN0
.GEN4    ANOP                           FORCE LA INSTEAD OF LR 1,DCB
&NAME    IHBINNRA 0&DCB,&CODE           LOAD REGS 1 AND 0
.GEN0    AIF   ('&SVC' EQ 'YES').GENSVC
         MVC   DXCALLID,6(RWTG)         SAVE ID OF CALLING MODULE
         AIF   ('&RETURN' EQ '').CHKWTG
         AIF   ('&RETURN' EQ 'NONE').CHKWTG
         AIF   ('&RETURN'(1,1) EQ '(').REGRET
         MVC   DXRETMOD,&RETURN         SAVE ID & TTR OF RETURN MODULE
         AGO   .CHKWTG
.REGRET  MVC   DXRETMOD,0&RETURN        SAVE ID & TTR OF RETURN MODULE
.*
.CHKWTG  AIF   ('&MODID' EQ '').GEN2
         AIF   ('&MODID(1)' EQ '(').GEN1
         MVC   0(5,RWTGC),&MODID        MOVE ID & TTR INTO WTG TABLE
         AGO   .GEN2
.GEN1    MVC   0(5,RWTGC),0&MODID       MOVE ID & TTR INTO WTG TABLE
.*
.GEN2    AIF   ('&RES' EQ 'NO').SKPRES
         IECRES LOAD,BRCODE=&RETCODE,BRANCH=QUEUED               Y02080
.*                                BR TO RESIDENT ROUTINE TO XCTL Y02080
         MEXIT
.SKPRES  AIF   ('&REGSAVE' NE 'YES').MEXIT
         STM   9,14,DXREG9              SAVE REGISTERS 9-14
.MEXIT   MEXIT
.*
.GENSVC  LNR   1,1                      INDICATE ABEND CONDITION
         SVC   55                       ISSUE EOV SVC
         MEXIT
.*                                                               Y02080
.DMA1000 ANOP                                                    YM2869
&NAME    DC    XL1'00',C'&SYSECT &MODID ERROR'         ABEND 0C1
         DS    0H
         MEXIT                                                   Y02080
.*
.E1      MNOTE 8,'NO CODE SPECIFIED'
         MEND
