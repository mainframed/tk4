         MACRO
&NAME    IOSINTRP &UCB=1,&VAR=1,&BRANCH=YES                    @ZA16163
.*
.*       CSECT AUTHORIZATION
.*
         AIF   ('&SYSECT' EQ 'IEE3103D').SIMAUTH
         AIF   ('&SYSECT' EQ 'IECVDAVV').SIMAUTH
         AIF   ('&SYSECT' EQ 'IECTCATN').SIMAUTH
         AIF   ('&SYSECT' EQ 'IGFDI1').SIMAUTH
         AIF   ('&SYSECT' EQ 'IECVCINT').SIMAUTH               @Y30CQLE
         AIF   ('&SYSECT' EQ 'IGE0001H').SIMAUTH  MSS          @ZUP1085
         AIF   ('&SYSECT' EQ 'ICB2AEA').SIMAUTH  MSS           @ZUP1085
         AIF   ('&SYSECT' EQ 'IGE0003C').SIMAUTH  3838 SUPPORT @G29ANLO
         AIF   ('&SYSECT' EQ 'ICBTMSS0').SIMAUTH
         AIF   ('&SYSECT' EQ 'IGTUJA').SIMAUTH                 @G29ANLO
.*       AGO   .IOSGEN1                                        @ZA12429
.SIMAUTH ANOP
.*
.*       CHECK FOR MISSING PARAMETERS
.*
         AIF   ('&BRANCH' NE 'YES').IOSGEN3
         AIF   (('&VAR' NE '1') AND ('&VAR' NE '2')).IOSGEN3
         SPACE 2
*****************************************************************
*                                                               *
* FUNCTION:                                                     *
*        SIMULATES A DEVICE END OR CHANNEL AVAILABLE INTERRUPT. *
*                                                               *
* MACRO CALL:                                                   *
*    LABEL  IOSINTRP UCB=(REG),VER=1,BRANCH=YES                 *
*                                                               *
*         PLS - GEN REFS(CVTPTR,CVTIXAVL,CVTMAP,                *
*               IOCGENA,IOCOM)                                  *
*               (IOSINTRP KEYWORDS AS IN BAL);                  *
*                                                               *
* INPUT:                                                        *
*    UCB=  MAY SPECIFY A GPR (SYMBOLIC OR ABSOLUTE), WHICH      *
*    IS TO CONTAIN THE ADDRESS OF THE UCB COMMON SEGMENT(VAR=1) *
*    FOR WHICH THE INTERRUPT IS TO BE SIMULATED. THE DEFAULT IS *
*    UCB=(1).                                                   *
*                                                               *
*    VAR=2 GENERATE CHAN AVAILABLE INTERRUPT FOR CRH. FOR THIS  *
*    CASE UCB= SPECIFIES A REGISTER CONTAINING THE DEVICE       *
*    ADDRESS.                                                   *
*                                                               *
*    BRANCH=YES CAUSES A BALR TO MODULE IECVGENA.               *
*                                                               *
*    LABEL - OPTIONAL LABEL.                                    *
*                                                               *
* OUTPUT:                                                       *
*    A CHANNEL AVAILABLE INTERRUPT (ZERO STATUS) IS PRESENTED   *
*    IF THE UCB STATUS FLAGS INDICATE AN OUTSTANDING CHANNEL    *
*    END OR NO INTERRUPT EXPECTED.  IF THE DEVICE IS            *
*    WAITING FOR A DEVICE END (SOLICITED OR UNSOLICITED -       *
*    BUSY OR NOT READY), A DEVICE END INTERRUPT IS PRESENTED.   *
*                                                               *
* ATTRIBUTES:                                                   *
*    THIS FUNCTION IS EXPANDED AS RE-ENTRANT CODE.              *
*                                                               *
* OTHER:                                                        *
*    REGISTER 13 MUST CONTAIN THE ADDRESS OF A 16 WORD          *
*    SAVE AREA. THE REGISTERS ARE STORED 0 THRU 15 FROM         *
*    OFFSET 0.                                                  *
*                                                               *
*    THE EXPANSION OF THIS FUNCTION IS DEPENDENT ON THE         *
*    SPECIFICATION OF THE FOLLOWING MAPPING MACROS              *
*    IN THE CALLING CSECT ASSEMBLY:                             *
*         IECDIOCM                                              *
*         CVT DSECT=YES                                         *
*                                                               *
*    THIS ROUTINE MODIFIES THE I/O OLD PSW AND SERIALIZES       *
*    ITS OPERATION VIA STNSM INSTRUCTION THROUGHOUT             *
*    THE INTERRUPT PROCESS.  THE RESUME ADDRESS FIELD OF THE    *
*    I/O PSW IS SET TO THE LAST INSTRUCTION OF THE EXPANSION.   *
*                                                               *
*    LOCKS ARE NOT REQUIRED BY THIS FUNCTION.                   *
*                                                               *
*    THIS FUNCTION WAS FORMALLY PROVIDED BY IOSGEN SIMINT       *
*    AS AN INLINE EXPANSION.                                    *
*                                                               *
*****************************************************************
         SPACE 2
&NAME    STM   0,15,0(13)       SAVE ALL REGISTERS             @ZA16163
         AIF   ('&VAR' EQ '2').SIMNEG                          @ZA16163
         LA    1,0(&UCB(1).)    BE SURE ADDR IS POSITIVE       @ZA16163
         AGO   .SIMCON                                         @ZA16163
.SIMNEG  LA    &UCB(1).,0(&UCB(1).) BE SURE ADDR IS POSITIVE   @ZA16163
         LNR   1,&UCB(1)  THEN FORCE NEGATIVE FOR DEVICE ADDR  @ZA16163
.SIMCON  ANOP
         L     15,CVTPTR(0,0)   GET CVT ADDRESS                @ZA16163
         L     15,CVTIXAVL-CVTMAP(15) GET IOCOM ADDRESS        @ZA16163
         L     15,IOCGENA-IOCOM(15) GET IECVGENA ADDRESS       @ZA16163
         BAL   14,8(15)         CALL SIMULATE FUNCTION         @ZA16163
         LM    0,15,0(13)       RESTORE REGISTERS              @ZA16163
         SPACE
         MEXIT
.*
.IOSGEN3 MNOTE 8,'*** ERROR IN PARAMETER SET UP.'
         MEND
