* % IEETRACE: MACRO;
*
* /*******************************************************************/
* /*                                                                 */
* /*  MODULE NAME = IEETRACE                                         */
* /*                                                                 */
* /*  DESCRIPTIVE NAME = MASTER TRACE FACILITY INTERFACE             */
* /*                                                                 */
* /*  STATUS = OS/VS2 - SYSTEM CONTROL PROGRAM 2                     */
* /*                                                                 */
* /*   LAST UPDATE = 05/11/78                                        */
* /*                                                                 */
* /*  FUNCTION = PROVIDE INTERFACE TO MASTER TRACE                   */
* /*             SERVICE ROUTINES                                    */
* /*                                                                 */
* /*  OPERATION = THIS MACRO PERFORMS THE FOLLOWING FUNCTIONS:       */
* /*                                                                 */
* /*                DETERMINES IF MASTER TRACE IS ACTIVE BY          */
* /*                CHECKING THE MASTER TRACE TABLE POINTER          */
* /*                (BAMTTBL) IN THE MASTER SCHEDULER RESIDENT       */
* /*                DATA AREA (MAPPED VIA MACRO IEEBASEA)            */
* /*                                                                 */
* /*                IF 'TEST' IS SPECIFIED, A RETURN CODE IS         */
* /*                PROVIDED INDICATING STATUS OF MASTER TRACE       */
* /*                AND CONTROL IS RETURNED TO THE CALLER            */
* /*                                                                 */
* /*                IF 'ENTRY' OR NO OPERAND IS SPECIFIED            */
* /*                AND MASTER TRACE IS ACTIVE, A POINTER            */
* /*                (BAMTRTN) TO THE MASTER TRACE ENTRY ROUTINE      */
* /*                IS LOADED AND CONTROL PASSED TO IT VIA           */
* /*                BRANCH.                                          */
* /*                                                                 */
* /*                IF 'ENTRY' OR NO OPERAND IS SPECIFIED AND        */
* /*                MASTER TRACE IS NOT ACTIVE, A RETURN CODE IS     */
* /*                PROVIDED BEFORE CONTROL IS RETURNED TO THE       */
* /*                CALLER                                           */
* /*                                                                 */
* /*                                                                 */
* /*                IF 'TABLEMOD' IS SPECIFIED, A POINTER(BAMTCDR)   */
* /*                TO THE MASTER TRACE CREATE/DEACTIVATE ROUTINE    */
* /*                IS LOADED AND CONTROL PASSED TO IT VIA BRANCH    */
* /*                                                                 */
* /*  NOTES =                                                        */
* /*                                                                 */
* /*   RESTRICTIONS  = THE CALLER MUST INCLUDE THE MASTER SCHEDULER  */
* /*                   RESIDENT DATA AREA (VIA IEEBASEA MAPPING      */
* /*                   MACRO) AND ASSURE ADDRESSABILITY TO IT.       */
* /*                                                                 */
* /*                                                                 */
* /*                                                                 */
* /*  MODULE TYPE = MACRO                                            */
* /*                                                                 */
* /*   PROCESSOR = BILINGUAL PLS 2 AND ASSEMBLER                     */
* /*                                                                 */
* /*   MODULE SIZE = SEE ASSEMBLER OUTPUT                            */
* /*                                                                 */
* /*   ATTRIBUTES = REENTRANT                                        */
* /*                                                                 */
* /*  ENTRY POINT = IEETRACE                                         */
* /*                                                                 */
* /*   INPUT =                                                       */
* /*                                                                 */
* /*    'TEST' SPECIFIED                                             */
* /*           NONE                                                  */
* /*                                                                 */
* /*    'TEST' NOT SPECIFIED =                                       */
* /*           REGISTER 1 POINTING TO A PARAMETER LIST,              */
* /*           MAPPED BY IEZMTPRM                                    */
* /*                                                                 */
* /*           REGISTER 13 POINTING TO AN 18 WORD WORK AREA          */
* /*                                                                 */
* /*  OUTPUT = SEE EXITS                                             */
* /*                                                                 */
* /*                                                                 */
* /*  EXIT-NORMAL                                                    */
* /*   MASTER TRACE ACTIVE                                           */
* /*                                                                 */
* /*    CONDITION = 'TEST' OR 'TABLEMOD' NOT SPECIFIED               */
* /*    OUTPUT = CONTROL PASSED TO MASTER TRACE ENTRY SERVICE        */
* /*             ROUTINE  (IEEMB808)                                 */
* /*                                                                 */
* /*    CONDITION = 'TEST' SPECIFIED                                 */
* /*    OUTPUT = CONTROL RETURNED TO CALLER WITH A RETURN CODE       */
* /*                                                                 */
* /*    CONDITION = 'TABLEMOD' SPECIFIED                             */
* /*    OUTPUT = CONTROL PASSED TO MASTER TRACE CREATE/DEACTIVATE    */
* /*             ROUTINE (IEEMB809)                                  */
* /*                                                                 */
* /*   MASTER TRACE NOT ACTIVE                                       */
* /*                                                                 */
* /*    CONDITION = 'TABLEMOD' NOT SPECIFIED                         */
* /*    OUTPUT = CONTROL RETURNED TO CALLER WITH A RETURN CODE       */
* /*                                                                 */
* /*    CONDITION = 'TABLEMOD' SPECIFIED                             */
* /*    OUTPUT = CONTROL PASSED TO MASTER TRACE CREATE/DEACTIVATE    */
* /*             ROUTINE (IEEMB809)                                  */
* /*                                                                 */
* /*  EXIT-ERROR                                                     */
* /*    CONDITION = INVALID OPERAND ON INVOCATION                    */
* /*    OUTPUT= COMPILE TIME  OR ASSEMBLY TIME ERROR MESSAGE         */
* /*            ISSUED                                               */
* /*                                                                 */
* /*  EXTERNAL REFERENCES =                                          */
* /*                                                                 */
* /*    ROUTINES = IEEMB808,IEEMB809                                 */
* /*                                                                 */
* /*    DATA AREAS = IEEBASEA                                        */
* /*                                                                 */
* /*     CHANGE ACTIVITY = G64MP2D                           @G64MP2D*/
* /*  MESSAGES = NONE                                                */
* /*                                                                 */
* /*  ABEND CODES =                                                  */
* /*                                                                 */
* /*       0NX                ADDRESSABILITY ERROR IN CALLERS        */
* /*                          PARMLIST                               */
* /*                                                                 */
* /*           0NX IS A SYSTEM COMPLETION CODE SET                   */
* /*           BY THE MASTER TRACE SERVICE ROUTINE.                  */
* /*                                                                 */
* /*  RETURN CODES =                                                 */
* /*                                                                 */
* /*  'TEST' NOT SPECIFIED                                           */
* /*     NORMAL                                                      */
* /*                                                                 */
* /*       RETURN   REASON    DESCRIPTION                            */
* /*       CODE     CODE                                             */
* /*       (REG15)  (REG0)                                           */
* /*                                                                 */
* /*       00       NONE      CALLER DATA SUCCESSFULLY RECORDED      */
* /*                          IN THE MASTER TRACE TABLE OR MASTER    */
* /*                          TRACE TABLE SUCCESSFULLY ACTIVATED     */
* /*                          OR DEACTIVATED                         */
* /*                                                                 */
* /*       04       NONE      MASTER TRACE NOT ACTIVE                */
* /*                                                                 */
* /*     ERROR                                                       */
* /*                                                                 */
* /*       08       01        SPECIFIED DATA LENGTH EXCEEDS SIZE OF  */
* /*                          TABLE                                  */
* /*                                                                 */
* /*                02        UNABLE TO DETERMINE IF                 */
* /*                          REQUEST IS FOR CREATE OR               */
* /*                          DEACTIVATE                             */
* /*                                                                 */
* /*       12       01        UNABLE TO OBTAIN NEW MASTER TRACE      */
* /*                          TABLE, EXISTING TABLE IF ANY STILL     */
* /*                          IN USE                                 */
* /*                                                                 */
* /*                02        MASTER TRACE FAILURE, MASTER           */
* /*                          TRACE RESTARTED                        */
* /*                                                                 */
* /*                03        RECURSIVE MASTER TRACE ERROR           */
* /*                          MASTER TRACE TERMINATED                */
* /*                                                                 */
* /*     NOTE: RETURN CODES ABOVE 4 ARE SET BY                       */
* /*           THE MASTER TRACE SERVICE ROUTINE.                     */
* /*                                                                 */
* /*    'TEST' SPECIFIED                                             */
* /*                                                                 */
* /*       RETURN   REASON    DESCRIPTION                            */
* /*       CODE     CODE                                             */
* /*                                                                 */
* /*       00       NONE      MASTER TRACE IS ACTIVE                 */
* /*                                                                 */
* /*       04       NONE      MASTER TRACE IS NOT ACTIVE             */
* /*                                                                 */
* /*  EXAMPLES =                                                     */
* /*                                                                 */
* /*         TO INVOKE IEETRACE WITH THE DEFAULT OPTION OF           */
* /*         'ENTRY' IN PLS2, CODE THE FOLLOWING:                    */
* /*                                                                 */
* /*         ? IEETRACE;                                             */
* /*                                                                 */
* /*         TO INVOKE IEETRACE WITH THE 'ENTRY' OPTION IN           */
* /*         PLS2, CODE THE FOLLOWING;                               */
* /*                                                                 */
* /*         ? IEETRACE (ENTRY);                                     */
* /*                                                                 */
* /*         TO INVOKE IEETRACE WITH THE 'TABLEMOD' OPTION           */
* /*         IN PLS2, CODE THE FOLLOWING;                            */
* /*                                                                 */
* /*         ? IEETRACE (TABLEMOD);                                  */
* /*                                                                 */
* /*         TO INVOKE IEETRACE WITH THE 'TEST' OPTION IN PLS2,      */
* /*         CODE THE FOLLOWING:                                     */
* /*                                                                 */
* /*         ? IEETRACE (TEST);                                      */
* /*                                                                 */
* /*         TO INVOKE IEETRACE WITH THE DEFAULT OPTION OF           */
* /*         'ENTRY' IN ASSEMBLER, CODE THE FOLLOWING:               */
* /*                                                                 */
* /*                 IEETRACE                                        */
* /*                                                                 */
* /*         TO INVOKE IEETRACE WITH THE 'ENTRY' OPTION IN           */
* /*         ASSEMBLER, CODE THE FOLLOWING;                          */
* /*                                                                 */
* /*                 IEETRACE ENTRY                                  */
* /*                                                                 */
* /*         TO INVOKE IEETRACE WITH THE 'TABLEMOD' OPTION           */
* /*         IN ASSEMBLER, CODE THE FOLLOWING:                       */
* /*                                                                 */
* /*                 IEETRACE TABLEMOD                               */
* /*                                                                 */
* /*         TO INVOKE IEETRACE WITH THE 'TEST' OPTION IN ASSEMBLER, */
* /*         CODE THE FOLLOWING:                                     */
* /*                                                                 */
* /*                  IEETRACE TEST                                  */
* /*                                                                 */
* /*                                                                 */
* /*******************************************************************/
* DCL CMNT FIXED;                    /*START COLUMN FOR COMMENTS     */
* DCL COMMAC CHAR;                   /*COMMENT MACRO INVOCATION      */
* DCL L   FIXED;                     /*START COLUMN FOR CODE         */
* DCL OPER  CHAR;                    /*OPERAND SPECIFIED             */
* DCL EHAREGS CHAR EXT;              /* REGS TO BE DCLD BY EPILOG    */
*
* IF INDEX(EHAREGS,'01P')=0 THEN     /* HAS REG1 BEEN USED?          */
*    EHAREGS=EHAREGS××'01P';         /*NO,INDICATE IT IS TO BE DCLD  */
* IF INDEX(EHAREGS,'15P')=0 THEN     /* HAS REG15 BEEN USED?         */
*    EHAREGS=EHAREGS××'15P';         /*NO,INDICATE IT IS TO BE DCLD  */
* CMNT = 37;                         /*PRIME COMMENT COLUMN CONSTANT */
* L = MACLMAR;                       /*PRIME CODE COLUMN CONSTANT    */
* OPER = MACLIST;
* COMMAC = 'IEETRACE '×× OPER;       /*SET INVOCATION AS COMMENT     */
* COMMAC = COMMAC ×× ' @G64MP2D';    /*ADD FLAGING TO COMMENT        */
* IF OPER ^= '(ENTRY)'THEN
*  DO;
*   IF OPER ^= '' THEN
*    DO;
*     IF OPER ^= '(TEST)' THEN
*      DO;
*       IF OPER ^= '(TABLEMOD)' THEN
*        DO;
*      /* GENERATE INVALID OPERAND CODE                          */
*         ANS MSG(' INVALID OPERAND ON IEETRACE MACRO ')SKIP COL(L);
*         ANS MSG(' LEAVE FIELD BLANK TO OBTAIN DEFAULT');
*         ANS MSG(' OR SPECIFY ENTRY, TABLEMOD, ')SKIP COL(L);
*         ANS MSG(' OR TEST  @G64MP2D ');
*         RETURN CODE(8);
*
*        END;
*       ELSE
*        DO;
*      /* GENERATE 'TABLEMOD' CODE                             */
*         ANS('DO;')SKIP COL(L);
*         ANS(COMMENT(COMMAC))COL(CMNT);
*         ANS('RFY (GPR01P,GPR15P) RSTD;')SKIP;
*         ANS('/*      @G64MP2D*/')SKIP COL(CMNT);
*         ANS('CALL MB809;')SKIP COL(L);
*         ANS('/* TABLE CHANGE REQUESTED CALL*/')SKIP COL(CMNT);
*         ANS('/* TABLE SERVICE RTN @G64MP2D*/')SKIP COL(CMNT);
*         ANS('RFY (GPR01P,GPR15P)UNRSTD;')SKIP;
*         ANS('/*      @G64MP2D*/')SKIP COL(CMNT);
*         ANS('END;')SKIP COL(L);
*         ANS(COMMENT(COMMAC))COL(CMNT);
*       RETURN;
*      END;
*      END;
*     ELSE;
*    END;
*   ELSE;
*  END;
*  ELSE;
*  ANS('DO;')SKIP COL(L);
*  ANS(COMMENT(COMMAC))COL(CMNT);
*  ANS('RFY (GPR01P,GPR15P)RSTD;')SKIP;
*  ANS('/*      @G64MP2D*/')SKIP COL(CMNT);
*  ANS('IF BAMTTBL = 0 THEN ')SKIP COL(L);
*  ANS('/* IS MASTER TRACE ACTIVE @G64MP2D*/')COL(CMNT);
*  ANS(' GPR15P = 4;')SKIP COL(L);
*  ANS('/* NO TABLE,SET MASTER TRACE */')COL(CMNT);
*  ANS('/* NOT ACTIVE RETURN CODE @G64MP2D*/')SKIP COL(CMNT);
* IF OPER = '(TEST)'
*            THEN DO;
*      /* GENERATE 'TEST' CODE                                  */
*                 ANS('ELSE ')SKIP;
*                 ANS('/* TABLE EXISTS,  */')COL(CMNT);
*                 ANS(' GPR15P = 0;')SKIP;
*                 ANS('/* SET MASTER TRACE ACTIVE */')SKIP COL(CMNT);
*                 ANS('/* RETURN CODE @G64MP2D*/')SKIP COL(CMNT);
*                 ANS('RFY (GPR01P,GPR15P)UNRSTD;')SKIP COL(L);
*                 ANS('/*      @G64MP2D*/')SKIP COL(CMNT);
*                 ANS('END;')SKIP COL(L);
*                 ANS(COMMENT(COMMAC))COL(CMNT);
*                 END;
*            ELSE DO;
*       /* GENERATE 'ENTRY' CODE                                 */
*                 ANS('ELSE ')SKIP COL(L);
*                 ANS('/* TABLE EXISTS, */')COL(CMNT);
*                 ANS(' CALL MB808;')SKIP COL(L);
*                 ANS('/* CALL ENTRY SERVICE */')SKIP COL(CMNT);
*                 ANS('/* RTN  @G64MP2D*/')SKIP COL(CMNT);
*                 ANS('RFY (GPR01P,GPR15P)UNRSTD;')SKIP COL(L);
*                 ANS('/*      @G64MP2D*/')SKIP COL(CMNT);
*                 ANS('END;')SKIP COL(L);
*                 ANS(COMMENT(COMMAC))COL(CMNT);
*                 END;
* % END;
*
*
         MACRO
         IEETRACE &OPER
         AIF   ('&OPER' EQ 'TABLEMOD').CALLTAB
.*                                TABLE CHANGE REQUEST
.*                                GENERATE TABLE SERVICE RTN CALL
         ICM   14,15,BAMTTBL      MASTER TRACE TABLE EXIST?    @G64MP2D
         LA    15,4(0,0)          PRIME REG 15 WITH MASTER     @G64MP2D
*                                 TRACE INACTIVE RETURN CODE   @G64MP2D
         BZ    N&SYSNDX           NO TABLE, RETURN CONTROL     @G64MP2D
         AIF   ('&OPER' EQ 'TEST').CALLTST
.*                                TEST REQUEST, GENERATE REGISTER
.*                                15 RESET
         AIF   (('&OPER' EQ 'ENTRY') OR ('&OPER' EQ '')).CALLENT
.*                                ENTRY REQUEST OR DEFAULTED,
.*                                GENERATE ENTRY SERVICE RTN CALL
         MNOTE 8,'INVALID OPERAND-LEAVE FIELD BLANK TO ACCEPT DEFAULT, *
               OR SPECIFY         ENTRY, TABLEMOD OR TEST'
         AGO   .EXIT
.CALLTST  ANOP
         SR    15,15              SET MASTER TRACE ACTIVE CODE @G64MP2D
         AGO   .EXIT
.CALLENT ANOP
         L     15,BAMTRTN         GET ENTRY SERVICE ROUTINE    @G64MP2D
*                                 ENTRY POINT                  @G64MP2D
         BALR  14,15              INVOKE ENTRY SERVICE ROUTINE @G64MP2D
         AGO   .EXIT
.CALLTAB ANOP
         L     15,BAMTCDR         GET TABLE SERVICE ROUTINE    @G64MP2D
*                                 ENTRY POINT                  @G64MP2D
         BALR  14,15              INVOKE TABLE SERVICE ROUTINE @G64MP2D
.EXIT    ANOP
N&SYSNDX EQU   *                                               @G64MP2D
         MEND
