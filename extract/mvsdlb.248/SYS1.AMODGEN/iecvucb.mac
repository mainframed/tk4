         MACRO
         IECVUCB &UCBNO,&DEVAD,&TYPE,&CPRFX,&ERRTAB,&STATAB,&LCHN,     *
               &DEVNDX,&TYPEA,&FL5,&SYSRES,&DDTNM,&OFF,&GR,&TMSK,      *
               &EXT,&NOP2,&DEVADE,&SHR,&SHRUP,&FLP1            @G64HPLP
         GBLA  &IECIZS1,&IECIZCT,&PRFX
         LCLA  &COUNT,&A
         LCLC  &CUHEXAD,&DEVADDR
         LCLA  &BITS          FOR BYTES OF COMMON EXT          @Y40MPLG
         AIF   (&EXT EQ 1).UCBEXT
.**********************************************************************
.*                                                                    *
.*                   THIS SETS UP THE VS2-2 PREFIX.                   *
.*                   UCBIOQ                                           *
.*                   UCBLOCK                                          *
.*                                                                    *
.**********************************************************************
         DC    D'0'                     UCBIOQ AND UCBLOCK
.**********************************************************************
.*                                                                    *
.*    THIS SECTION SETS UP FOR GRAPHICS OR TP IN THE STANDARD PORTION *
.*                                                                    *
.**********************************************************************
&DEVADDR SETC  '&DEVADE'                 SET CAR ADDRESS
         AIF   ('&TYPE' NE 'TP' AND '&TYPE' NE 'GR').ST GRAPHIC OR TP?
&DEVADDR SETC  '&DEVAD'                SET TP CAR ADDRESS
.**********************************************************************
.*                                                                    *
.*        THIS SETS UP THE STANDARD PORTION OF ALL UCBS.              *
.*             UCBJBNR,UCBFL5,UCBID,UCBSTAT                           *
.*             UCBCHAN(UCBCHA/UCBUA),UCBFLA,UCBFLB                    *
.*                                                                    *
.**********************************************************************
.ST      ANOP
UCB&UCBNO  DC  AL1(0),AL1(&FL5),X'FF'   JOBNO,UCBFL5,ID
         AIF   (&OFF EQ 0).ST1          TEST FOR DEVICE OFFLINE
         DC    X'&SYSRES'               UCBSTAT FOR DEVICE ONLINE
         AGO   .ST2
.ST1     DC    X'8&SYSRES'              UCBSTAT
.ST2     DC    X'0&DEVAD',XL4'0'        UCBCHAN,UCBFLA,UCBFLB
.*                                      UCBCHANMASK,UCBCNT            *
         AIF   ('&TYPE' NE 'ZS').REG    IF NOT A 2305, BRANCH
         DC    AL1(&LCHN),XL2'0008'     UCB-(LCI,CPU,WGT)
         AGO   .UCB3
.REG     DC    AL1(&LCHN),XL2'0'        UCB-(LCI,CPU,WGT)
.UCB3    DC    CL3'&DEVADDR',XL4'&TYPEA' UCBNAME,UCBTYPE
         DC    XL1'0'                   UCBFLC
         DC    AL3(UCBX&UCBNO)          POINTER TO UCB EXTENSION
.**********************************************************************
.*   TEST FOR A PRINTER AND THE UCS FEATURE OR A 3505 OR A 3525 DEVICE*
.*   THE 'WR' DEVICE TYPE ACRONYM IS USED FOR THE 3505 CARD PUNCH AND *
.*   THE 3525 CARD PRINT. ONLY THE 3211, A UCS 1403 PRINTER, A 3540   *
.*   DISKET READER AND A 3800 PRINTER GET A UCB EXTENSION.    @Y40OPLG*
.*                                                            @Y40MPLG*
.*   SET A SENSE LENGTH OF TWO BYTES FOR 1403 PRINTER.                *
.*   SET A SENSE LENGTH OF FOUR BYTES FOR A 3505 AND A 3525.          *
.*   SET A SENSE LENGTH OF SIX BYTES FOR A 3211 PRINTER.              *
.*   SET A SENSE LENGTH OF SIX BYTES FOR A 3540 DISKET RDR/WTR@Y30OPLA*
.**********************************************************************
         AIF   ('&TYPEA'(6,3) EQ '842').URDSM DSM DEVICE       @Y30LPLC
         AIF   ('&TYPEA'(6,3) EQ '844').UR3540 3540 DISKET     @Y30OPLA
         AIF   ('&TYPE' EQ 'WR').UC3211 TEST FOR 3211 OR A 3505/3525
         AIF   ('&TYPE' NE 'UR').NOTPRT TEST FOR UR
         AIF   (NOT(('&TYPEA'(6,3) EQ '808') AND ('&TYPEA'(3,1) EQ '8')*
               )).NOTPRT                IT IS NOT A PRINTER WITH UCS
         DC    A(UCBW&UCBNO)
         AGO   .END
.UC3211  AIF   ('&TYPEA'(7,2) NE '09' AND '&TYPEA'(7,2) NE '0E').END
.*                                                             @Y40MPLG
.UR3540  DC    A(UCBW&UCBNO)       3211,3540,3800              @Y30OPLA
         AGO   .END
.URDSM   ANOP                      DSM DEVICE                  @Y30LPLC
         DC    2F'0'               2 WORD DEVICE DEPEND. SECT. @Y30LPLC
         AGO   .END                                            @Y30LPLC
.**********************************************************************
.*                                                                    *
.*       GRAPHICS AND ANR GRAPHICS SEGMENT SECTION                    *
.*                                                                    *
.**********************************************************************
.NOTPRT  AIF   ('&TYPE' NE 'GR').URCHECK  TEST FOR GRAPHICS
  AIF  ('&TYPEA'(7,2) NE '09' AND '&TYPEA'(7,2) NE '0A' AND '&TYPEA'(7,X
               2) NE '0B' AND '&TYPEA'(7,2) NE '0C' AND '&TYPEA'(7,2)  X
               NE '0D').NOTANR                                 @G51APLK
         DC    XL2'&TMSK',XL2'0'        ADDITIONAL OPTIONS BYTE
         DC    3F'0'                    IRB ADD,IOB ADD,D.I.
         AGO   .END
.NOTANR  ANOP
         AIF   ('&TYPEA'(8,1) EQ '7' OR '&TYPEA'(8,1) EQ '8').M3066
         DC    3F'0'                    SENSE,DUC,GCB
         AIF   ('&ERRTAB' EQ '66').SDA0 IS THIS 2260
         DC    XL1'&DEVNDX'             DEVICE INDEX           @ZA40477
         AIF   (&GR EQ 1).GRF           TEST FOR GAM
         DC    XL3'00'                  NO GAM SUPPORT
         AGO   .END
.GRF     AIF   ('&TYPEA'(1,2) EQ '31').MOD1  CHECK FOR 2250-1
&CUHEXAD SETC  '&DEVAD'(1,2)            CONVERT TO CU ADDRESS
         DC    VL3(IFFB&NOP2)           BUFFER TABLE NAME
         AGO   .END
.MOD1    DC    VL3(IFFB&DEVAD)          BUFFER TABLE NAME
         AGO   .END
.M3066   AIF   ('&TYPEA'(8,1) NE '8').END                       @YM8410
         DC    3F'0'
.SDA0    DC    F'0'                     2260 OR 1053
         AGO   .END
.**********************************************************************
.*                                                                    *
.*            UNIT RECORD,TELE-PROCESSING AND DUMMY UCB SEGMENTS      *
.**********************************************************************
.URCHECK AIF   ('&TYPE' NE 'UR' ).DUMMY
         AIF   ('&TYPEA'(6,3) NE '84C').UR3886 IS IT A 3838    @G29ANLO
         DC    2F'0'                 3838 DEVICE DEPEND. SECT. @G29ANLO
         AGO   .END                                            @G29ANLO
.UR3886  ANOP                                                  @G29ANLO
         AIF   ('&TYPEA'(6,3) NE '817').END  BRANCH IF IT IS NOT A 3886
         DC    F'0'                     FOUR BYTES FOR THE 3886
         AGO   .END
.DUMMY   AIF   ('&TYPE' NE 'DUM').TP
         DC    F'0'                     EXTRA WORD FOR DUM UCB USE
         AGO   .END
.TP      AIF   (NOT('&TYPE' EQ 'TP')).TAPE IS IT TP
   AIF   (NOT('&TYPEA'(8,1) EQ '5' OR '&TYPEA'(8,1) EQ '6'  OR         *
               '&TYPEA'(5,4) EQ '4100' OR '&TYPEA'(7,2) EQ 'F1' )).END
         DC    2F'0'                    3704/3705 SEGMENT
*                                  ALSO CTC & 3791             @Z40KPLG
         AGO   .END
.**********************************************************************
.*                                                                    *
.*             THIS SECTION SETS UP THE TAPE SEGMENT                  *
.*             3420 HAS 24 SENSE BYTES                                *
.*             34XX HAS 09 SENSE BYTES                                *
.*             ALL OTHERS HAVE 6 SENSE BYTES                          *
.**********************************************************************
.TAPE    AIF   ('&TYPEA'(5,2) NE '80').DASD BR IF NOT TAPE
         AIF   ('&TYPEA'(5,4) EQ '8001').NOASP  CHECK FOR 2400
         AIF   ('&TYPE' NE 'AT').NOASP  NOT AN ASPEN TAPE
         AIF   (&SHR EQ 1).NOASP        3400 SHARABLE?
         DC    H'0',H'0',XL6'0',XL2'8000',2F'0'   VOLSER
*          FILE SEQ CT, FILE SEQ NO,NOT SHARABLE/STATAB & DMCT,DS SERNO
         AGO   .REST1
.NOASP   DC    H'0',H'0',XL6'0',XL2'0',2F'0'   VOLSER
*               FILE SEQ CT, FILE SEQ NO,STATAB & DMCT,DS SERNO
.REST1   DC    AL1(&TMSK),AL3(UCBW&UCBNO) UCBVOPT AND UCBXTNB
         AGO   .END                     END OF TAPE UCB
.**********************************************************************
.*                                                                    *
.*             DIRECT ACCESS DEVICES SEGMENTS.                        *
.*             THE 2305 AND 3330'S HAVE 24 SENSE BYTES.               *
.*             A 2314 HAS 6 SENSE BYTES.                              *
.**********************************************************************
.*             ML (MERLIN) TEST INCLUDES 3330,3340,3350        @Z30RSTA
.*             REFERENCE MACRO SGIEC202 IN AGENLIB             @Z30RSTA
.DASD    AIF   ('&TYPE' EQ 'ML' OR '&TYPE' EQ 'ZS').SPEED
.DABS    DC    XL6'0',XL2'0',F'0',XL4'0'   VOLSER
*               VTOC, STATB & DMCT, UCBSQC,UCBFL4,UCBUSER,RESERVED
         AGO   .END
.SPEED   DC    F'0',XL6'0',XL2'0',XL4'0'
*               VTOC, STATB & DMCT, UCBSQC,UCBFL4,UCBUSER,RESERVED
         AIF   ('&TYPE' EQ 'ML').END
         AIF   (&IECIZCT  NE 0).ZSET    TEST FOR 1ST ZEUS UCB
&IECIZS1 SETA  &UCBNO                   SET POINTER TO FIRST UCB
.ZSET    ANOP
&IECIZCT   SETA  &IECIZCT+1             UPDATE COUNTER
         AIF   (&IECIZCT  EQ 8).ZSET1   TEST FOR LAST
&A       SETA  &UCBNO+1                 SET PTR TO NEXT UCB
         DC    A(UCB&IECIZS1-512)       POINTER TO FIRST ZEUS UCB
         DC    A(UCB&A-512)             POINTER TO NEXT IN RING
         AGO   .END                     NOT LAST 2305
.ZSET1   ANOP                           LAST DEVICE ON 2305 CHAIN
&A       SETA  &IECIZS1+1               SET LAST TO POINT TO SECOND
         DC    A(UCB&IECIZS1-512)       POINTER TO FIRST ZEUS UCB
         DC    A(UCB&A-512)             POINTER TO NEXT IN RING
&IECIZCT   SETA  0                      CLEAR COUNTER
         AGO   .END                     LAST 2305
.UCBEXT  ANOP
UCBX&UCBNO DC  XL1'&ERRTAB',AL1(&STATAB)
.**********************************************************************
.*   TEST FOR A PRINTER AND THE UCS FEATURE OR A 3505 OR A 3525 DEVICE*
.*   THE 'WR' DEVICE TYPE ACRONYM IS USED FOR THE 3505 CARD PUNCH AND *
.*   THE 3525 CARD PRINT. ONLY THE 3211 AND A UCS 1403 PRINTER GET    *
.*   A UCB EXTENSION.                                                 *
.*   SET A SENSE LENGTH OF TWO BYTES FOR 1403 PRINTER.                *
.*   SET A SENSE LENGTH OF FOUR BYTES FOR A 3505 AND A 3525.          *
.*   SET A SENSE LENGTH OF SIX BYTES FOR A 3211 PRINTER.              *
.*   SET A SENSE LENGTH OF 24 BYTES FOR A 3800 PRINTER         @Y40MPLG
.*   SET A SENSE LENGTH OF TWO BYTES FOR A 3895 RDR-INSCRIBER  @G30HNLO
.*   SET A SENSE LENGTH OF SIX BYTES FOR A 3540 DISKET RDR/WTR @Y30OPLA
.**********************************************************************
         AIF   ('&TYPE' EQ 'WR').UC3211A TEST FOR 3211 OR A 3505/3525
         AIF   ('&TYPE' NE 'UR').NOPRT TEST FOR UR
         AIF   ('&TYPEA'(6,3) EQ '844').UR3540A 3540 DISKET    @Y30OPLA
         AIF   ('&TYPEA'(6,3) EQ '842').URDSMA DSM DEVICE      @Y30LPLC
         AIF   (NOT(('&TYPEA'(6,3) EQ '808') AND ('&TYPEA'(3,1) EQ '8')*
               )).NOPRT                 IT IS NOT A PRINTER WITH UCS
         DC    XL2'0'                   UCBDTI,ATI/UCS PRINTER
         DC    X'02'                    SENSE COUNT/UCS PRINTER
         AGO   .UCB5
.UC3211A AIF   ('&TYPEA'(7,2) NE '09').NEWUR  IT IS NOT A 3211
.UR3540A DC    XL2'0'              UCBDTI,ATI/UCS PRINTER,3540 @Y30OPLA
         DC    X'06'               SENSE COUNT/3211 PRNTR,3540 @Y30OPLA
         AGO   .UCB5
.NEWUR   AIF   ('&TYPEA'(7,2) EQ '0E').UCB3800  3800 PRINTER   @Y40MPLG
         AIF   ('&TYPEA'(7,2) EQ '19').UCB3895  3895           @G30HNLO
         DC    XL2'0'                   UCBDTI,ATI/NEW UR
         DC    X'04'                    SENSE COUNT/NEW UR
         AGO   .UCB5
.UCB3800 DC    XL2'0'               UCBDTI,ATI/3800 PRT        @Y40MPLG
         DC    X'18'                SENSE COUNT/3800 PRT       @Y40MPLG
         AGO   .UCB5
.UCB3895 DC    XL2'0'               UCBDTI,ATI/3895            @G30HNLO
         DC    X'02'                SENSE COUNT/3895           @G30HNLO
         AGO   .UCB5                                           @G30HNLO
.URDSMA  ANOP                                                  @Y30LPLC
         DC    X'0038'                  UCBDTI,ATI/ DSM        @G64HPLP
         DC    X'20'                    SENSE COUNT/ DSM       @Y30LPLC
         AGO   .UCB5                                           @Y30LPLC
.**********************************************************************
.*                                                                    *
.*       GRAPHICS AND ANR GRAPHICS SEGMENT SECTION                    *
.*                                                                    *
.**********************************************************************
.NOPRT  AIF   ('&TYPE' NE 'GR').URDUMM TEST FOR GRAPHICS
  AIF  ('&TYPEA'(7,2) NE '09' AND '&TYPEA'(7,2) NE '0A' AND '&TYPEA'(7,X
               2) NE '0B' AND '&TYPEA'(7,2) NE '0C' AND '&TYPEA'(7,2)  X
               NE '0D').NOANR      ANR?                        @G51APLK
         DC    X'001C'                  UCBDTI,ATI/ANR GRAPHIC @G64HPLP
         DC    X'06'                    SENSE COUNT/ANR GRAPHICS
         AGO   .UCB5
.NOANR   ANOP
         DC    X'0010'                  UCBDTI,ATI/NON-ANR     @G64HPLP
*                                       GRAPHICS               @G64HPLP
         DC    X'06'                    SENSE COUNT/NON-ANR GRAPHICS
         AGO   .UCB5
.**********************************************************************
.*                                                                    *
.*            UNIT RECORD,TELE-PROCESSING AND DUMMY UCB SEGMENTS      *
.**********************************************************************
.URDUMM  AIF   (NOT('&TYPE' EQ 'UR' OR '&TYPE' EQ 'DUM')).TPC
         AIF   ('&TYPEA'(6,3) EQ '817').OCR3886 CHECK FOR 3886 @ZA05374
         AIF   ('&TYPEA'(6,3) NE '84C').URDUM1 IS IT 3838      @G29ANLO
         DC    X'0000'                  UCBDTI,ATI/3838        @G64HPLP
         DC    X'20'                    SENSE COUNT/3838       @G29ANLO
         AGO   .UCB5                                           @G29ANLO
.OCR3886 ANOP                                                  @ZA05374
         DC    XL2'0'                   UCBDTI,ATI             @ZA05374
         DC    X'06'                    SENSE COUNT FOR 3886.  @ZA05374
         AGO   .UCB5                                           @ZA05374
.URDUM1  ANOP                                                  @G29ANLO
         DC    XL2'0'                   UCBDTI,ATI/UR OR DUMMY UCB'S
         DC    X'02'                    SENSE COUNT/UR OR DUMMY UCB'S
         AGO   .UCB5
.TPC     AIF   (NOT('&TYPE' EQ 'TP')).TAPEC IS IT TP
         AIF   ('&TYPEA'(5,4) EQ '4100').TPCTC                 @YM08445
         DC    X'0000'                  UCBDTI,ATI/TP UCB'S    @G64HPLP
         DC    X'02'                    SENSE COUNT/TP UCB'S
         AGO   .UCB5
.TPCTC   ANOP
         DC    X'002C'                  UCBDTI,ATI/TP CTC UCB  @G64HPLP
*                                                              @ZM30469
         DC    X'01'                    SENSE COUNT/TP UCB   DCRR21211
         AGO   .UCB5
.**********************************************************************
.*                                                                    *
.*       THIS SECTION SETS UP THE TAPE UCB AND SEGMENTS TO IT         *
.*             3420 HAS 24 SENSE BYTES                                *
.*             34XX HAS 09 SENSE BYTES                                *
.*             ALL OTHERS HAVE 6 SENSE BYTES                          *
.**********************************************************************
.TAPEC   AIF   ('&TYPEA'(5,2) NE '80').DASDC BR IF NOT TAPE
         AIF   ('&TYPEA'(5,4) EQ '8001').C2400  CHECK FOR 2400
         AIF   ('&TYPE' NE 'AT').C34XX  NOT AN ASPEN TAPE
         DC    X'0000'                  UCBDTI,ATI/TAPE UCB'S  @G64HPLP
         DC    X'18'                    SENSE COUNT/TAPE UCB'S
         AGO   .UCB5
.C34XX   DC    X'0000'                  UCBDTI,ATI/ASPEN-TAPE  @G64HPLP
*                                       UCB,S                  @G64HPLP
         DC    X'09'                    SENSE COUNT/ASPEN-TAPE UCB'S
         AGO   .UCB5
.C2400   DC    X'0000'                  UCBDTI,ATI/TAPE UCB'S  @G64HPLP
         DC    X'06'                    SENSE COUNT/TAPE UCB'S
         AGO   .UCB5
.**********************************************************************
.*                                                                    *
.*       DIRECT ACCESS DEVICES UCB SENSE COUNTS AND SEGMENTS.         *
.*             THE 2305 AND 3330'S HAVE 24 SENSE BYTES.               *
.*             A 2314 HAS 6 SENSE BYTES.                              *
.**********************************************************************
.*             ML (MERLIN) TEST INCLUDES 3330,3340,3350        @Z30RSTA
.*             REFERENCE MACRO SGIEC202 IN AGENLIB             @Z30RSTA
.DASDC   AIF   ('&TYPE' EQ 'ML' OR '&TYPE' EQ 'ZS').MLZSC
         DC    X'0000'                  UCBDTI,ATI/DA UCB'S    @G64HPLP
         DC    X'06'                    SENSE COUNT/DA UCB'S
         AGO   .UCB5
.MLZSC   AIF   (NOT('&TYPE' EQ 'ML')).ZSDTI
.* NOTE: THIS TEST ASSUMES NO OTHER BIT FOR V3330 IN HALF BYTE @ZM30SS1
         AIF   ('&TYPEA'(4,1) NE '8').NVML  IF NOT VIRT 3330   @ZM30SS1
         DC    X'0000'             DTI,ATI V3330               @G64HPLP
         DC    X'1C'               SENSE COUNT 28 FOR V3330    @ZM30SS1
         AGO   .UCB5                                           @ZM30SS1
.NVML    DC    X'0000'                  UCBDTI,ATI/DA MERLIN   @G64HPLP
*                                       UCB'S                  @G64HPLP
         DC    X'18'                    SENSE COUNT/DA MERLIN UCB'S
         AGO   .UCB5
.ZSDTI   DC    X'0000'                  UCBDTI/ATI             @G64HPLP
         DC    X'18'                    SENSE COUNT/DA ZEUS UCB'S
.UCB5    ANOP
&BITS    SETA  0              INIT BYTE5 CNTR                  @Y40MPLG
         AIF   ('&SHRUP' EQ '').UCB6                           @ZM30409
         AIF   (&SHRUP EQ 0).UCB6  IS IT SHARED IN UP, NO BRANCH
&BITS    SETA  &BITS+64            X'40' SHARED UP BIT         @Y40MPLG
.UCB6    AIF   ('&FLP1' EQ '').UCB65                           @Y40MPLG
&BITS    SETA  &BITS+&FLP1         ERROR LOG                   @Y40MPLG
.UCB65   DC    AL1(&BITS),XL2'0'        UCBFLP1,RESVD HALFWORD @Y40MPLG
.UCB7    ANOP
&PRFX    SETA  &CPRFX                   CONVERT DBL WRD OFFSET @ZA16150
*                                       TO CCW PREFIX TO BYTE
         DC    H'&PRFX',XL6'0'          UCBCCWOF OFFSET,UCBPMSK
*                                       UCBMFCNT,UCBASID
         DC    X'00',XL3'00'            UCBMIHBY,UCBWTOID      @G64HPLP
.* IF NO DDT IS SPECIFIED OR THE PARAMETER SPECIFIED IS 0 THEN @ZM48987
.* GENERATE AN ADCON OF 0 RATHER THAN A V-CON.                 @ZM48987
         AIF   ('&DDTNM' EQ '' OR '&DDTNM' EQ '0').NODDT       @ZM48987
         DC    V(&DDTNM)                DDT POINTER            @ZM48987
         AGO   .END                                            @ZM48987
.NODDT   ANOP                                                  @ZM48987
         DC    A(0)                     DDT POINTER            @ZM48987
.END     ANOP                                                  @ZM48987
         MEND
