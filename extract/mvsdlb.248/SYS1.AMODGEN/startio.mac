         MACRO
&SYMBOL  STARTIO    &SRB=,&TCB=
         LCLB  &TCBOSW,&SRBNEG
         AIF   (T'&SRB EQ 'O').E1          REQ. PARAM. MISSING
         AIF   (T'&SYMBOL EQ 'O').NOSYM    ANY NAME FIELD?
&SYMBOL  DS    0H
.NOSYM   AIF   (T'&TCB NE 'O').TCBOK       IS TCB SPECIFIED?
&TCBOSW  SETB  1                           NO, SET INDICATOR
         AGO   .SRBTST                     GO TEST SRB
.TCBOK   AIF   ('&TCB' NE 'SRB').SRBTST    YES, IS IT SRB?
&SRBNEG  SETB  1                           YES, SET INDICATOR
.SRBTST  AIF   ('&SRB'(1,1) EQ '(').SRBREG IS REGISTER SPECIFIED?
         LA    1,&SRB                      SRB PARAMETER REG
         AIF   ('&SRBNEG' NE '1').TCBTST   TCB=SRB?
         LNR   1,1                         TCB PARAMETER REG
         AGO   .CVT
.SRBREG  AIF   ('&SRBNEG' NE '1').POSREG
         LNR   1,&SRB(1)                   SRB PARAMETER REG
         AGO   .CVT
.POSREG  AIF   ('&SRB' EQ '(1)').TCBTST    ALREADY IN REG 1?
         LR    1,&SRB(1)                   SRB PARAMETER REG
.TCBTST  AIF   ('&TCBOSW' NE '1').ZEROTST  TCB OMITTED?
         SR    0,0                         TCB PARAMETER REG
         AGO   .CVT
.ZEROTST AIF   ('&TCB' NE '0').REGTST      TCB=0?
         LNR   0,1                         TCB PARAMETER REG
         AGO   .CVT
.REGTST  AIF   ('&TCB'(1,1) NE '(').NOREG  TCB IN REGISTER?
         AIF   ('&TCB' EQ '(0)').CVT       ALREADY IN REG 0?
         LR    0,&TCB(1)                   TCB PARAMETER REG
         AGO   .CVT
.NOREG   LA    0,&TCB                      TCB PARAMETER REG
.CVT     L     3,CVTPTR(0,0)               CVT ADDRESS          YM2296P
         L     15,CVTIXAVL-CVTMAP(,3)      IOCOM ADDRESS        YM2296P
         L     15,IOCSTIO-IOCOM(,15)       CHAN. SCHED. ADDRESS YM2296P
         BALR  14,15                       INITIATE THE I/O
         MEXIT
.E1      IHBERMAC   01,039                 SRB MISSING
         MEND
