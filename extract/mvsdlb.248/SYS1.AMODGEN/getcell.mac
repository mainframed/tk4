         MACRO
&NAME    GETCELL   &CPID=,&BRANCH=YES,&SAVE=YES,&RELATED=
*        OS/VS2 RELEASE 2   ---   9/15/73
         AIF   ('&BRANCH' NE 'YES').ERR1    BRANCH=YES MANDATORY
         AIF   ('&CPID' EQ '').ERR3         CPID REQUIRED
         AIF   ('&CPID'(1,1) NE '(').ERR2   CPID MUST BE REGISTER
         AIF   ('&SAVE' EQ 'YES' OR '&SAVE' EQ 'NO').SAVOK
         IHBERMAC 1015,&SAVE,''SAVE='',YES
.*       SYNTAX OK, CONTINUE WITH THE EXPANSION.
.SAVOK   AIF   ('&NAME' EQ '').SKPN
&NAME    DS    0H                       GETCELL MACRO
.SKPN    AIF   ('&SAVE' EQ 'NO').NRGS
         STM   2,12,28(13)         SAVE CALLERS REGISTERS 2-12
.NRGS    AIF   ('&CPID(1)' EQ '0').NZLD
         LR    0,&CPID(1)          LOAD R0 WITH CPID
.NZLD    L     3,16(0,0)           CVT ADDRESS FOR GETCELL      YM02296
         L     15,CVTGETCL-CVTMAP(0,3)    LOAD GETCELL RT ENTRY POINT
         BALR  14,15               GO TO GETCELL ROUTINE
         AIF   ('&SAVE' EQ 'NO').NRGR
         LM    2,12,28(13)         RESTORE CALLERS REGISTERS
.NRGR    MEXIT
.ERR1    IHBERMAC 1006,''BRANCH=YES''
         MEXIT
.ERR2    IHBERMAC 1001,CPID,&CPID
         MEXIT
.ERR3    IHBERMAC 1006,CPID
         MEND
