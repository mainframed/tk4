         MACRO
         SGIEF014  &TYP=,&PREF,&CODE
.*C003000,029000                                               @YM08650
.*A000000-999999                                                 Y02670
         COPY  SGIEF020
         GBLA  &PENT
         GBLA  &Z,&E,&GG
         GBLB  &DONE(188)                                      @YM08650
         GBLB  &FTSW
         LCLA  &J
.**********************************************************************
.*       EDTTAB  SUPPORT  MACRO
.*   PARAMETERS ARE :
.*          TYP   (G=GENERIC,E=ESOTERIC,GG=GENERATED GENERIC)
.*          PREF  (UNITNAME)
.*          CODE  (TYP=GG ONLY - - - INTERNAL CODE FOR TABLES)
.*   CALL WITHOUT PARAMETERS SIGNALS END OF PREFTAB.
.*
.*  THIS MACRO GENERATES AN ENTRY IN PREFTAB FOR EACH TYP=G IF NAME
.*  CAN BE FOUND IN GBL VAR 'NAME'.
.*
.*  THE MACRO GENERATES A UNIQUE CODE FOR EACH NAME AND SRORES THE
.*  CODES IN SEQUENCE IN GBL VAR 'GPFXT'.ORDER OF CODES IN GPFXT
.*  (HENCE ORDER OF CALLS) IS PRECEDENCE SEQUENCE FOR UNITNAMES.
.**********************************************************************
         AIF   (&FTSW EQ 0).B100
.A1      ANOP
         AIF   ('&TYP' EQ 'G').C100
         AIF   ('&TYP' EQ 'E').D100
         AIF   ('&TYP' EQ 'GG').E100
         AGO   .N100
.*          IF Z=0;
.*        (GENERATE PREFTAB HEADER
.*          Z=Z+1;
.*          IF TYP=G;
.*             DO J=1 BY 1 WHILE NAME(J) NE NULL AND NAME(J) NE PREF;
.*             END;
.*             IF(NAME(J) = PREF) AND(DONE(J) = FALSE)
.*                GPFXT(Z) = J
.*                DONE(J)  = TRUE
.*                (GENERATE  AN   ENTRY IN PREFTAB)
.*          ELSE;
.*             IF TYP=E;
.*                E=E+1;
.*                ENAME(E) = PREF;
.*                GPFXT(Z) = E+256;
.*             ELSE;
.*                IF TYP=GG;
.*                   GG=GG+1
.*                   GNAME(GG) = PREF
.*                   GPFXT(Z)  = GG+1024
.*          END;
.*          MEXIT;
.*
.*
.C100    ANOP
&J       SETA  0
.C101    ANOP
&J       SETA  &J+1
         AIF   (&J  GT 188).Z                                  @YM08650
         AIF   ('&NAME(&J)' NE '&PREF').C101
         AIF   (&DONE(&J)).Z
         DC CL8'&PREF'
&Z       SETA  &Z+1
&GPFXT(&Z)  SETA  &J
&DONE(&J)   SETB  1
&PENT    SETA  &PENT+1
.Z       MEXIT
.*
.*
.*
.D100    ANOP
&E       SETA  &E+1
&ENAME(&E)  SETC  '&PREF'
&Z       SETA  &Z+1
&GPFXT(&Z)  SETA  &E+256
         MEXIT
.*
.*
.*
.E100    ANOP
&GG      SETA  &GG+1
&GKLIST(&GG)  SETA  &CODE
&GNAME(&GG)  SETC  '&PREF'
&Z       SETA  &Z+1
&GPFXT(&Z)   SETA  &GG+1024
         MEXIT
.*
.*
.*
.B100    ANOP
&FTSW    SETB  1
         TITLE 'PREFTAB  -  INSTALLATION GENERIC PRECEDENCE TABLE'
PREFTAB  CSECT
         DC    A(NRENTS)          HEADER
         DC    A(ENTLEN)            SECTION
PTSTRT   DS    0F
*
****************************  BEGIN TABLE BODY  ***********************
         AGO   .A1
.*
.*
.*
.N100    ANOP
NRENTS   EQU   &PENT
ENTLEN   EQU   (*-PTSTRT)/NRENTS
         MEND
