         MACRO
         SGIEF0SS &PRMNAME,&JESNAME
.**********************************************************************
.*
.*  MACRO NAME = SGIEF0SS
.*
.*  DESCRIPTIVE NAME = GROUP 0 SYSGEN MACRO JOB ENTRY SUBSYSTEM
.*                     NAMES TABLE GENERATOR
.*
.*  FUNCTION =  GENERATES TABLE OF JOB ENTRY SUBSYSTEM NAMES
.*
.*  OPERATION = GENERATES AN 4-CHARACTER TABLE ENTRY FOR:
.*                - THE PRIMARY JOB ENTRY SUBSYSTEM NAME
.*                  SPECIFIED IN THE SCHEDULR MACRO.
.*                - THE MASTER SUBSYSTEM (MSTR)
.*                - EACH JOB ENTRY SUBSYSTEM NAME SPECIFIED
.*                  IN THE SCHEDULR MACRO.
.*                - AN ENTRY OF ZEROS DENOTING END OF TABLE.
.*
.*              ALL SUBSYSTEM NAMES ARE LESS THAN OR EQUAL TO
.*              FOUR CHARACTERS IN LENGTH.  NAMES ARE LEFT-
.*              JUSTIFIED AND PADDED WITH BLANKS.
.*
.*  DEPENDENCIES = SCHEDULR SYSGEN MACRO (GROUP 1):
.*                  -PROVIDE NAME OF PRIMARY JOB ENTRY SUBSYSTEM
.*                   AND NAMES FOR JOB ENTRY SUBSYSTEMS TO BE
.*                   USED IN SYSTEM CREATED BY THIS SYSGEN
.*                  -CALL MACRO SGIEF2SS
.*
.*                 SGIEF2SS SYSGEN MACRO (GROUP 2):
.*                  -CALL SGIEF0SS, PASSING THE PARAMETERS
.*                   SPECIFIED ON THE SCHEDULR MACRO.
.*
.*  INPUT = &PRMNAME - PRIMARY JOB ENTRY SUBSYSTEM NAME
.*          &JESNAME - ARRAY CONTAINING UP TO 15 JOB ENTRY
.*                     SUBSYSTEM NAMES.
.*
.*  OUTPUT = JOB ENTRY SUBSYSTEM NAMES TABLE
.*
.**********************************************************************
.*  SET JESNAMES ARRAY COUNTERS
         LCLA  &I,&K
.*  SET COUNT OF JOB ENTRY SUBSYSTEM NAMES PASSED
&I       SETA  N'&JESNAME
&K       SETA  1
         TITLE '  JOB ENTRY SUBSYSTEM NAMES TABLE'
IEFJESNM CSECT
         ENTRY IEFJTBNM
***********************************************************************
*
*  MODULE NAME = IEFJESNM
*
*  DESCRIPTIVE NAME = JOB ENTRY SUBSYSTEM NAMES TABLE
*
*  COPYRIGHT = NONE
*
*  STATUS = 0S/VS2 RELEASE 2
*
*  FUNCTION = TABLE OF JOB ENTRY SUBSYSTEM NAMES
*
*    OPERATION = THIS TABLE CONTAINS THE NAMES OF THE PRIMARY JOB
*                ENTRY SUBSYSTEM AND THE JOB ENTRY SUBSYSTEMS
*                SPECIFIED AT SYSGEN TIME.  IT ALSO CONTAINS THE
*                NAME 'MSTR' FOR THE MASTER SUBSYSTEM.  THE MSTR
*                ENTRY IS WIRED IN - IT IS NOT SPECIFIED AT
*                SYSGEN TIME.  THESE NAMES ARE USED AT IPL TIME
*                TO INITIALIZE THE SUBSYSTEM CVT'S
*
*                THE FIRST ENTRY CONTAINS THE NAME OF THE PRIMARY
*                JOB ENTRY SUBSYSTEM.  THE SECOND ENTRY CONTAINS THE
*                NAME 'MSTR' FOR THE MASTER SUBSYSTEM.  THE FOLLOWING
*                ENTRIES CONTAIN THE  JOB ENTRY SUBSYSTEM NAMES.
*
*                SUBSYSTEM CVT'S WILL BE GENERATED AND CHAINED
*                IN THE ORDER OF THE ENTRIES IN THE NAMES TABLE.
*
*  NOTES
*
*    DEPENDENCIES = SYSGEN SPECIFICATION OF JOB ENTRY SUBSYSTEM
*                   NAMES
*      CHARACTER-CODE DEPENDENCIES = NONE
*    RESTRICTIONS = NONE
*    REGISTER CONVENTIONS = N/A
*    PATCH LABEL = SSPATCH - RESERVED FOR FUTURE USE.
*                  NAMES MAY BE ADDED TO TABLE AFTER SYSGEN;
*                  HOWEVER, THE LAST ENTRY OF TABLE MUST ALWAYS BE
*                  ZERO TO TERMINATE TABLE SCAN AT IPL TIME.
*
*  MODULE TYPE = TABLE
*
*    MODULE SIZE = 4-CHARACTER ENTRY FOR EACH SUBSYSTEM NAME SPECIFIED
*                  AT SYSGEN TIME, 4-CHARACTER ENTRY FOR MASTER
*                  SUBSYSTEM, PLUS PATCH AREA SIZE
*
*    ATTRIBUTES = NOT EXECUTABLE
*
*  ENTRY POINT = IEFJTBNM
*    PURPOSE = TO ACCESS NAME TABLE AT IPL TIME TO INITIALIZE
*              THE SUBSYSTEM CVT'S
*    LINKAGE = NO LINKAGE TO THIS ROUTINE-
*              TABLE IS ACCESSED BY IEFJSINT - SUBSYSTEM INTERFACES
*              INITIALIZATION ROUTINE
*    INPUT = NONE
*
*  EXIT-NORMAL = NONE
*    CONDITIONS = NONE
*    OUTPUT = NONE
*    RETURN CODES = NONE
*
*  EXIT-ERROR = NONE
*    CONDITIONS = NONE
*    OUTPUT = NONE
*    RETURN CODES = NONE
*
*  EXTERNAL REFERENCES = NONE
*    ROUTINES = NONE
*    DATA AREAS = NONE
*    CONTROL BLOCKS = NONE
*
*  TABLES = NONE
*
*  MACROS = NONE
*    SERIALIZATION = NONE
*
*  CHANGE ACTIVITY = Y02668
*
*  MESSAGES = NONE
*
*  ABEND CODES = NONE
*
***********************************************************************
         EJECT
         DS    0F
IEFJTBNM EQU   *
         DC    CL4'&PRMNAME'      PRIMARY JOB ENTRY SUBSYSTEM NAME
.*
.*  GENERATE SUBSYSTEM NAME ENTRY FOR THE MASTER SUBSYSTEM (MSTR)
.*
         DC    CL4'MSTR'          MASTER SUBSYSTEM NAME
.*
.*  CHECK IF ANY JOB ENTRY SUBSYSTEM NAMES OTHER THAN THE PRIMARY
.*  WERE SPECIFIED.  IF NOT, BYPASS GENERATING ENTRIES -
.*
         AIF   (&I EQ 0).FINI
*
*  ENTRIES FOR SYSGENED JOB ENTRY SUBSYSTEM NAMES
*
.LOOP    ANOP
         DC    CL4'&JESNAME(&K)'
         AIF   (&K EQ &I).FINI
&K       SETA  &K+1
         AGO   .LOOP
.FINI    ANOP
*
*  PATCH AREA - FIRST ENTRY OF ZEROS THAT IS FOUND DURING TABLE
*  SCAN INDICATES END OF TABLE.  THERE MUST ALWAYS BE AN ENTRY
*  OF ZEROS TO TERMINATE THE TABLE SCAN.
*
SSPATCH  EQU   *
         DC    F'0'
         DC    F'0'
         DC    F'0'
         MEND
