         MACRO
         IECTBL &CHANLST,&LCHTOT,&TCAM,&CCWCNT,&GS,&ANR,&CTC,&ACR,&CRH
         GBLA  &IECIOPT(255)                                   @ZA34236
         LCLA  &INSERT,&LSTINDX,&CHMSK,&B,&CNT,&SUM
.*   &CHANLST  LIST OF CHANNELS BY TYPE
.*   &LCHTOT   TOTAL LCH'S IN SYSTEM
.*   &TCAM     TELECOMMUNICATIONS ACCESS METHOD IN SYSTEM
.*   &CCWCNT   NUMBER OF FULL WORDS FOR CCW POOL
.*   &GS       GRAPHICS ATTENTION INDICATOR
.*   &ANR      ANR ATTENTION ROUTINE INDICATOR
.*   &ACR      ACR CODE INDICATOR
.*   &CTC      CHANNEL TO CHANNEL INDICATOR                   DCRR21211
.*   &CRH      CHAN RECONFIG HARDWARE CODE INDICATOR           @Y30CQLF
         TITLE ' CHANNEL TABLE'
*****************************************************************
*                                                               *
*        CHANNEL TABLE - DRIVES IOS CHANNEL RESTART             *
*                                                               *
*****************************************************************
         SPACE 2
IECRCTBL DS    0F                       CHANNEL TABLE
.CONTIN   AIF  (&LSTINDX EQ N'&CHANLST).ENDA
         AIF   (&CHMSK GT 1).CONTINA    TEST FOR INITIALIZATION
&CHMSK   SETA  65536                    INITIALIZE MASK
.CONTINA ANOP
&CHMSK   SETA  &CHMSK/2                 SHIFT RIGHT 1
&LSTINDX SETA  &LSTINDX+1               INCREMENT INDEX
         AIF   (&CHANLST(&LSTINDX) EQ 0).OUT1     TEST FOR CHAN
&INSERT  SETA  &LSTINDX-1               SET CHAN CODE POINTER
         DC    X'00'                    FLAGS
         DC    AL1(&CHANLST(&LSTINDX))  CHANNEL TYPE
         DC    AL2(&CHMSK)              MASK
         DC    A(IECCX&INSERT)          CHAN SEARCH CODE
         SPACE 1
&INSERT  SETA  &LSTINDX                 GET HI-TOT FOR UCBSAVE
&SUM     SETA  &SUM+&CHMSK              USED TO GEN IOCX
         AGO   .CONTIN                  CONTINUE
.OUT1    DC    X'00'                    DUMMY ENTRY
         DC    AL1(0)                   NOT A VALID CHANNEL
         DC    AL2(&CHMSK)              MASK
         DC    A(0)                     FILLER
         AGO   .CONTIN                  CONTINUE
.ENDA    ANOP
         TITLE 'LOGICAL CHANNEL TABLE'
*****************************************************************
*                                                               *
* LOGICAL CHANNEL TABLE. THIS TABLE REPRESENTS QUEUES OF I/O    *
* REQUESTS WAITING TO BE STARTED TO DEVICES. A LOGICAL CHANNEL  *
* CONSISTS OF A SET OF DEVICES ACCESSABLE VIA A COMMON SET OF   *
* CHANNEL PATHS. THE MINIMUM IS A ONE FOR ONE CORRESPONDENCE    *
* BETWEEN PHYSICAL AND LOGICAL CHANNEL. ASSOCIATED WITH EACH    *
* LOGICAL CHANNEL IS A TEST CHANNEL PATH TABLE.                 *
*                                                               *
*****************************************************************
         SPACE 2
IECVLCH  DS    0D
IECILCH  EQU   *
&CNT     SETA  1                        INITIALIZE LCH NUMBER
&IECIOPT(1) SETA 1                      INITIALIZE CHANNEL 0
.LCHST   AIF   (&CNT GT &LCHTOT).LCHEND
LCH&CNT  DC    X'FFFFFFFF',A(0)         IOQ CHAIN POINTERS
         DC    D'0'                     LCH LOCK
         DC    A(TCH&CNT.LST)           TEST CHANNEL PATH TABLE ADDR
         DC    X'0&IECIOPT(&CNT)',X'00',5H'0' NO.CHANNELS,FLAGS
*                                       AND OPTIMIZER COUNTERS
         SPACE 1
&CNT     SETA  &CNT+1
         AGO   .LCHST
.LCHEND  EJECT
*
*        SENSE LOGICAL CHANNELS - ONE PER PHYSICAL CHANNEL
*
         SPACE 2
IECVSLCH DS    0C
&CNT     SETA  1
.SLCHST  AIF   (&CNT GT N'&CHANLST).SLCHEND
SLCH&CNT DC    X'FFFFFFFF',A(0),D'0' SENSE LCH
         DC    A(0),X'01',X'00',5H'0' CHANS,FLAGS AND COUNTS
         SPACE 1
&CNT     SETA  &CNT+1
         AGO   .SLCHST
.SLCHEND ANOP
         TITLE 'ATTENTION TABLE'
*****************************************************************
*                                                               *
*        ATTENTION TABLE                                        *
*                                                               *
*****************************************************************
.**********************************************************************
.*                   ATTENTION TABLE SUMMARY                          *
.*                                                                    *
.*   UPDATED TO SHOW RTM'S OWNERSHIP OF INDEX 32 IN SUPPORT OF        *
.*   APAR OZ29195                                                     *
.*                                                                    *
.**********************************************************************
.* INDEX * USER OR  * ATTN RTN * FLAGS * ASID *       COMMENTS        *
.*DEC/HEX* DEV TYPE *  NAME    *       *      *                       *
.**********************************************************************
.*  0/0  * DUMMY    *     0    * 0000  *  0   *   DUMMY ENTRY         *
.*  4/4  * CONSOLE  * IEEBA1   * C000  *  1   * CONSOLE ATTN HANDLER  *
.*  8/8  * DUMMY    *     0    * C000  *  1   * USED BY PROG PRODS ETC*
.* 12/C  * SCHEDULER* IEFVPOST * C000  *  1   * SCHEDULER POST ROUTINE*
.* 16/10 * GRAPHICS * IFFIOM   * C000  *  1   * DUMMY IF NO GRAPHICS  *
.* 20/14 * TCAM     * IEDQATTN * C000  *  1   * TCAM ATTN HANDLER     *
.* 24/18 * JES/2-3  * DUMATN   * 4000  *  1   * USED BY JES/2 OR 3    *
.* 28/1C * ANR      * IECTATEN * 5000  *  1   * DUMMY IF ANR NOT GENED*
.* 32/20 * RTM      * DUMATN   * C000  *  1   * USED BY RTM           *
.* 36/24 * JES/3    * DUMATN   * 4000  *  1   * USED BY JES/3         *
.* 40/28 * JES/3    * DUMATN   * C000  *  1   * USED BY JES/3         *
.* 44/2C * CTC ADPTR* IECTCATN * E000  *  1   * CHAN TO CHAN ADAPTER  *
.* 48/30 * VTAM-370X* ISTZFMAB * 0000  *  1   * VTAM ATTN HANDLER     *
.* 52/34 * VTAM-3270* ISTZFMAA * 0000  *  1   * VTAM ATTN HANDLER     *
.* 56/38 * SS1-DSM  * DUMATN   * C000  *  1   * SS1/DSM ATTN   @Y30LPLC
.**********************************************************************
.*   NOTE -- FLAGS ARE DEFINED AS FOLLOWS:                            *
.*    1ST BYTE --                                                     *
.*      BIT 0 (MASK=X'80') B'1' DENOTES ATTENTION SEPARATION REQUIRED *
.*      BIT 1 (MASK=X'40') B'1' DENOTES LOCAL LOCK REQUIRED           *
.*      BIT 2 (MASK=X'20') B'1' DENOTES UCB TO BE SET QUIESED         *
.*      BIT 3 (MASK=X'10') B'1' DENOTES UCB TO BE SET INTRCEPT PENDING*
.*      BITS 4-7 RESERVED FOR FUTURE USE                              *
.*    2ND BYTE -- RESERVED FOR FUTURE USE                             *
.**********************************************************************
         SPACE 2
IECVATTN DS    0F
*                                       INDEX 0
         DC    X'0000',H'1'             FLAGS,RESERVED,ASID
         DC    A(0)                     DUMMY ENTRY
         SPACE 1
*                                       INDEX 4
         DC    X'C000',H'1'                                    @ZA24419
         DC    A(IEEBA1)                CONSOLE ATTENTION HANDLER
         SPACE 1
.*
.*   THE FOLLOWING ENTRY CANNOT BE ASSIGNED DUE TO USAGE BY PROGRAM
.*   PRODUCTS AND VARIOUS USER APPLICATIONS.
.*
*                                       INDEX 8
         DC    X'C000',H'1'
         DC    A(0)                     DUMMY ENTRY
         SPACE 1
*                                       INDEX 12 (X'0C')
         DC    X'C000',H'1'
         DC    A(IEFDPOST)              SCHEDULER POST ROUTINE
         SPACE 1
         AIF   (&GS EQ 2).GRYES         TEST FOR GRAPHICS ATTN RTN
*                                       INDEX 16 (X'10')
         DC    X'0000',H'1'             FLAGS,RESERVED,ASID
         DC    A(DUMATN)                DUMMY ENTRY
         AGO   .TCAMTST
.GRYES   ANOP
*                                       INDEX 16 (X'10')
         DC    X'C000',H'1'
         DC    V(IFFIOM)                GRAPHICS ATTENTION HANDLER
.TCAMTST ANOP                                                  @ZA29181
.*       AIF   (&TCAM NE 0).TCAMYES     TEST FOR TCAM          @ZA29181
.*                                      INDEX 20 (X'14')       @ZA29181
.*       DC    X'0000',H'1'                                    @ZA29181
.*       DC    A(DUMATN)                                       @ZA29181
.*       AGO   .DUM001                                         @ZA29181
.TCAMYES ANOP
         SPACE 1
*                                       INDEX 20 (X'14')
         DC    X'C000',H'1'
         WXTRN IEDQATTN                                        @ZA29181
         DC    V(IEDQATTN)              TCAM ATTENTION HANDLER @ZA29181
.DUM001  ANOP
         SPACE 1
*                                       INDEX 24 (X'18')
         DC    X'4000',H'1'
         DC    A(DUMATN)                JES/2 OR 3
         AIF   (&ANR EQ 2).ANRYES       TEST FOR ANR ATTN RTN
         SPACE 1
*                                       INDEX 28 (X'1C')
         DC    X'0000',H'1'
         DC    A(DUMATN)
         AGO   .DUM002
.ANRYES  ANOP
         SPACE 1
*                                       INDEX 28 (X'1C')
         DC    X'5000',H'1'                                    @YM05691
         DC    V(IECTATEN)              ANR ATTENTION HANDLER
.DUM002  ANOP
         SPACE 1
*                                       INDEX 32 (X'20')
         DC    X'C000',H'1'
         DC    A(DUMATN)
         SPACE 1
*                                       INDEX 36 (X'24')
         DC    X'4000',H'1'
         DC    A(DUMATN)                JES/3
         SPACE 1
*                                       INDEX 40 (X'28')
         DC    X'C000',H'1'
         DC    A(DUMATN)                JES/3
         SPACE 1
         AIF   (&CTC NE 1).CTCNO1                             DCRR21211
*                                       INDEX 44 (X'2C')
         DC    X'E000',H'1'
         DC    V(IECTCATN)              CHAN TO CHAN ADAPTER
         AGO   .CTCYES1                                       DCRR21211
.CTCNO1  ANOP                                                 DCRR21211
         DC    X'0000',H'1'                                   DCRR21211
         DC    A(DUMATN)                                      DCRR21211
.CTCYES1 ANOP                                                 DCRR21211
*                                       INDEX 48 (X'30')
         DC    X'0000',H'1'
         DC    V(ISTZFMAB)              VTAM -- 370X ATTENTION HANDLER
         SPACE 1
*                                       INDEX 52 (X'34')
         DC    X'1000',H'1'                                    @ZM33570
         SPACE 2
         DC    V(ISTZFMAA)              VTAM -- 3270 ATTENTION HANDLER
*
         DC    X'C000',H'1'            INDEX 56 (X'38')        @Y30LPLC
         DC    A(DUMATN)               SS1/DSM ATTN HANDLER    @Y30LPLC
         SPACE 2
IECVDATN EQU   *
DUMATN   BR    14                       DUMMY ATTENTION ROUTINE
         TITLE 'I/O COMMUNICATIONS AREA EXTENTION'
*****************************************************************
*                                                               *
*        EXTENTION TO THE I/O COMMUNICATIONS TABLE              *
*                                                               *
*****************************************************************
         SPACE 2
IECVSGT  DS    0F                                              @ZM30411
         DC    AL2(PURGPTR-LIST)        MAX OFFSET IN UCBLOOK TBL
         DC    AL2(&SUM)                MASK OF SYSGENED CHANNELS
         DC    AL1(&INSERT-1)           HIGHEST CHANNEL SYSGENED
         DC    AL1(&LCHTOT)             NUMBER OF LOGICAL CHANNELS
         DC    X'0000'                  RESERVED FOR FUTURE USE@YM04197
         AIF   (&CTC NE 1).CTCNO2                             DCRR21211
         DC    V(IECTCQSC)              E.P.- ATTEN. HANDLG RTN@YM04197
         AGO   .CTCYES2                                       DCRR21211
.CTCNO2  ANOP                                                 DCRR21211
         DC    A(DUMATN)                                      DCRR21211
.CTCYES2 ANOP                                                 DCRR21211
         AIF ('&ACR' EQ '0' OR '&CRH' EQ '0').CRHNO            @YM30437
         DC    V(IECVCRHA)              CRH ACTIVATION RTN     @Y30CQLE
         DC    V(IECVCRHD)              CRH DEACTIVATION RTN   @Y30CQLE
         DC    V(IECVCRHV)              CRH STIDC RTN          @Y30CQLE
         AGO   .PSIF                                           @Y30IPLG
.CRHNO   DC    3F'0'                    DUMMY ZERO ENTRIES     @Y30CQLE
.PSIF    DC    V(IEAVPSIX)              PAGE FIX FAST PATH     @Y30IPLG
         DC    V(IEAVPSIF)              PAGE FREE FAST PATH    @Y30IPLG
.TITLE   TITLE 'CHANNEL PROGRAM PREFIX AREA'
*****************************************************************
*                                                               *
*              IOS CHANNEL PROGRAM PREFIX CCW POOL              *
*        THE FOLLOWING STORAGE IS REQUIRED FOR THE CCWS         *
*        GENERATED BY IOS FOR EACH DEVICE TYPE.                 *
*              DASD - 32 BYTES                                  *
*              TAPE - 24 BYTES                                  *
*              GRAPHICS,UR,TP,DUM - 8 BYTES                     *
*                                                               *
*****************************************************************
         SPACE 2
IECVCCWS DS    0D
         DS    &CCWCNT.F                AREA FOR IOS TO USE FOR CCWS
         MEND
