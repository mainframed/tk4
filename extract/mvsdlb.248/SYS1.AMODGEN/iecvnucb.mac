         MACRO
&LABEL   IECVNUCB &ENTYPE,&DATA
         LCLA  &X
         LCLC  &C
.***** START OF SPECIFICATIONS **************************************
.*
.* MODULE NAME - IECVNUCB
.*
.* DESCRIPTIVE NAME - GENERATE UCBS
.*
.* COPYRIGHT - NONE
.*
.* STATUS - CHANGE LEVEL 00
.*
.* FUNCTION -
.*   THIS MACRO GENERATES THE VARIOUS PARTS OF UCBS (BASE, COMMON
.*   EXTENSION).
.*
.*   THIS MACRO IS PUNCHED BY SGIEC202 DURING STAGE 1 SYSGEN.
.*
.* NOTES - N/A
.*
.* MODULE TYPE - MACRO
.*
.* ENTRY POINT - N/A
.*
.* INPUT -
.*         THE FOLLOWING PARAMETERS ARE PASSED -
.*           &LABEL  - LABEL TO BE ASSIGNED
.*           &ENTYPE - ENTRY TYPE (UCBBASE, UCBCEXT)
.*           &DATA   - SUBLIST OF DATA USED TO GENERATE UCB.
.*                     THE SUBLIST CONSISTS OF THE FOLLOWING DATA-
.*                     UCBBASE-
.*                        &DATA(1) - 3 CHARACTER DEVICE ADDRESS
.*                        &DATA(2) - VALUE TO BE PLACED IN UCBFL5
.*                        &DATA(3) - OFFLINE INDICATOR
.*                                   1 IF OFFLINE=YES WAS SPECIFIED,
.*                                   0 OTHERWISE)
.*                        &DATA(4) - NON-REMOVABLE INDICATOR
.*                                   1 IF DEVICE IS NON-REMOVABLE,
.*                                   0 OTHERWISE)
.*                        &DATA(5) - VALUE TO BE PLACED IN UCBLCI
.*                        &DATA(6) - 8 CHARACTER VALUE FOR UCBTYPE
.*
.*                     UCBCEXT-
.*                        &DATA(1) - 3 CHARACTER DEVICE ADDRESS
.*                        &DATA(2) - VALUE TO BE PLACED IN UCBETI
.*                        &DATA(3) - VALUE TO BE PLACED IN UCBSTI
.*                        &DATA(4) - VALUE TO BE PLACED IN UCBATI
.*                        &DATA(5) - VALUE TO BE PLACED IN UCBSNSCT
.*                        &DATA(6) - VALUE TO BE PLACED IN UCBFLP1
.*                        &DATA(7) - VALUE TO BE PLACED IN UCBCCWOF
.*                        &DATA(8) - THE NAME OF THE UCBDDT    @G64HPLP
.*
.* OUTPUT -
.*       OUTPUT CONSISTS OF THE FOLLOWING -
.*           UCBBASE - BASE UCB WITH A LABEL OF UCBBXXX (FOLLOWING
.*                     THE UCB PREFIX) WHERE XXX IS THE DEVICE
.*                     ADDRESS PASSED. ANY LABEL ON THE MACRO
.*                     CALL IS EQUATED TO UCBBXXX.
.*           UCBCEXT - UCB COMMON EXTENSION WITH A LABEL OF
.*                     UCBCXXX WHERE XXX IS THE DEVICE
.*                     ADDRESS PASSED. ANY LABEL ON THE MACRO
.*                     CALL IS EQUATED TO UCBCXXX.
.*
.*       LOCALS USED ARE -
.*          &X - USED AS WORK VARIABLE.
.*          &C - SET TO DEVICE ADDRESS  (&DATA(1)) FOR LABELS.
.*
.*
.* EXIT, NORMAL - N/A
.*
.* EXIT, ERROR - N/A
.*
.* EXTERNAL REFERENCES - N/A
.*
.* TABLES - N/A
.*
.* MACROS - N/A
.*
.* CHANGE ACTIVITY - N/A
.*
.*A-000000-999999                                              @G64HPLP
.***** END OF SPECIFICATIONS ******************************************
         AIF   (N'&DATA EQ 0).MEND     IGNORE IF NO DATA PASSED
&C       SETC  '&DATA(1)'
         AIF   ('&ENTYPE' EQ 'UCBBASE').UCBBASE  PROCESS BASE UCB
         AIF   ('&ENTYPE' EQ 'UCBCEXT').UCBCEXT  PROCESS COM. EXT.
         MEXIT                         UNKNOWN ENTRY TYPE-IGNORE
.UCBBASE ANOP
         DC    D'0'                    UCBLOCK, UCBIOQ
UCBB&C   DC    AL1(0)                  UCBJBNR
         AIF   (K'&LABEL EQ 0).UCBB1   ANY OTHER LABEL ?
&LABEL   EQU   UCBB&C                  COMPATIBILITY LABEL
.UCBB1   ANOP
         DC    AL1(&DATA(2)),X'FF'     UCBFL5,UCBID
         AIF   (&DATA(3)).UCBB2        OFFLINE SPECIFIED ?
&X       SETA  &X+X'80'                NO - SET UCBONLI
.UCBB2   ANOP
         AIF   (NOT &DATA(4)).UCBB3    NON-REMOVABLE ?
&X       SETA  &X+X'04'                YES-SET UCBPRES
.UCBB3   ANOP
         DC    AL1(&X),XL2'&C'         UCBSTAT,UCBCHAN
         DC    XL4'0'                  UCBFLA,UCBFLB,UCBCHM,UCBCNT
         DC    AL1(&DATA(5))           UCBLCI
         DC    XL2'0',CL3'&C'          UCBCPU,UCBWGT,UCBNAME
         DC    XL4'&DATA(6)',XL1'0'    UCBTYP,UCBFLC
         DC    AL3(UCBC&C)             UCBCMEXT (PTR TO COMMON EXT)
         MEXIT
.UCBCEXT ANOP
UCBC&C   DC    0F'0',AL1(&DATA(2))     ALIGNMENT,UCBETI
         AIF   (K'&LABEL EQ 0).UCBC1   ANY LABEL ?
&LABEL   EQU   UCBC&C                  COMPATIBILITY LABEL
.UCBC1   ANOP
         DC    AL1(&DATA(3)),XL1'0'    UCBSTI,UCBDTI
         DC    AL1(&DATA(4),&DATA(5))  UCBATI,UCBSNSCNT
         DC    AL1(&DATA(6)),XL2'0'    UCBFLP1, UCBRV041
         DC    H'&DATA(7)'             UCBCCWOF
         DC    XL6'0'                  UCBPMSG,UCBMFCNT,UCBASID
         DC    F'0'                    UCBRV043,UCBWTOID
         DC    V(&DATA(8))             UCBDDT                  @G64HPLP
         MEXIT
.MEND    MEND
