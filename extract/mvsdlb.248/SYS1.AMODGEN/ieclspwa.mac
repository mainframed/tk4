         MACRO
&LABEL   IECLSPWA &EP,&F4,&D1=(0),&ADT=NO
.*
.*       MACRO DELETIONS/CHANGES FOR VS2 RELEASE 2
.*
.*
.**********************************************************************
.*
.* MODULE NAME = IECLSPWA (VS2-2)
.*
.* DESCRIPTIVE NAME = DADSM LSPACE WORK AREA ADDRESSING MACRO
.*
.* COPYRIGHT = NONE
.*
.* STATUS = CHANGE LEVEL 000
.*
.* FUNCTION = DADSM LSPACE MACRO WHICH PROVIDES ADDRESSABILITY
.*            TO THE 540 DECIMAL BYTE WORK AREA (NOT INCLUDING THE
.*            8 WORD PREFIX OR THE 6 WORD RECOVERY ROUTINE PARAMETER
.*            LIST) USED BY LSPACE.
.*
.* NOTES = THIS MACRO IS AN INTERNAL MACRO USED IN THE FOLLOWING
.*         DADSM FUNCTIONS:
.*
.*             LSPACE
.*
.*         TO REFERENCE THIS WORK AREA WITHIN THE FUNCTIONS LISTED
.*         ABOVE, CODE A USING STATEMENT ON THE LABEL 'LSPACWKA'.
.*
.*      DEPENDENCIES = THIS MACRO IS DESIGNED TO BE USED OPTIONALLY
.*                     WITH THE IECSDSL1 DSCB MAPPING MACRO, THE
.*                     PREFX MACRO (DEFINED IN THE IECDSECS MACRO)
.*                     WHICH DEFINES THE 8 WORD WORK AREA PREFIX,
.*                     THE 20 WORD EXTENDED WORK AREA PREFIX, AND
.*                     THE 4 WORD DADSM WTG TABLE, AND THE DSMADT
.*                     MACRO (ALSO DEFINED IN THE IECDSECS MACRO)
.*                     WHICH DEFINES THE 6 WORD DADSM AUDIT TRAIL.
.*
.*      RESTRICTIONS = NONE
.*
.*      REGISTER CONVENTIONS = NOT APPLICABLE
.*
.* PATCH LABEL = NOT APPLICABLE
.*
.* MODULE TYPE = MACRO
.*
.*      PROCESSOR = ASSEMBLER LANGUAGE
.*
.*      MODULE SIZE = 540 DECIMAL BYTES (NOT INCLUDING THE PREFIX)
.*
.*      ATTRIBUTES = NOT APPLICABLE
.*
.* ENTRY POINT = LSPPREFX
.*
.*      PURPOSE = THIS MACRO IS CALLED AT ASSEMBLY TIME BY THE
.*                FUNCTIONS LISTED UNDER 'NOTES' TO EXPAND THE
.*                LSPACE WORK AREA.
.*
.*      LINKAGE =
.*
.*                LABEL IECLSPWA EP,F4,D1=(N),ADT=YES
.*
.*      INPUT =
.*
.*                OPTIONAL LABEL SPECIFIED AS AN ACCEPTABLE ASSEMBLER
.*                LABEL WHICH CAN BE USED BY THE CALLING CSECT TO
.*                ESTABLISH ADDRESSABILITY TO THE ENTIRE WORK AREA.
.*
.*                EP IS THE FIRST POSITIONAL PARAMETER, SPECIFYING
.*                THAT THE LABELS FOR EACH OF THE FIELDS IN THE 8
.*                WORD PREFIX, THE 20 WORD EXTENDED PREFIX, AND THE 4
.*                WORD DADSM WTG TABLE ARE TO BE EXPANDED.  IF THESE
.*                FIELDS ARE NOT TO BE DEFINED, A COMMA MUST BE CODED
.*                TO INDICATE THE ABSENCE OF THIS PARAMETER.
.*
.*                F4 IS THE SECOND POSITIONAL PARAMETER, SPECIFYING
.*                THAT THE FIELDS OF THE FORMAT 4 DSCB ARE TO BE
.*                EXPANDED USING THE IECSDSL1 DSCB MAPPING MACRO.
.*                IF THESE FIELDS ARE NOT TO BE DEFINED AND A
.*                KEYWORD PARAMETER FOLLOWS, A COMMA MUST BE
.*                CODED TO INDICATE THE ABSENCE OF THIS PARAMETER.
.*
.*                D1=(N) IS A KEYWORD PARAMETER THAT SPECIFIES THE
.*                FORMAT(S) OF THE DSCB(S) TO BE EXPANDED IN THE
.*                140 BYTE DSCB AREA, USING THE IECSDSL1 MACRO.
.*                N CAN BE A NUMBER FROM 0 - 6.  IF N IS 0 OR 4 OR
.*                IF THIS KEYWORD IS OMITTED, NO LABELS ARE EXPANDED.
.*                IF N IS 1, 2, 3, 5, OR 6, THE CORRESPONDING FORMAT
.*                OF THE DSCB WILL BE EXPANDED.  MORE THAN ONE DSCB
.*                FORMAT CAN BE MAPPED IN THE 140 BYTE DSCB AREA
.*                BY SEPARATING THE FORMAT NUMBERS BY COMMAS.
.*
.*                ADT=YES IS A KEYWORD PARAMETER WHICH SPECIFIES THAT
.*                THE LABELS FOR EACH OF THE FIELDS WITHIN THE 6 WORD
.*                DADSM AUDIT TRAIL ARE TO BE EXPANDED.
.*
.*      OUTPUT = DADSM LSPACE WORK AREA EXPANSION
.*
.* EXIT-NORMAL = MEND STATEMENT
.*
.* EXIT-ERROR = ERROR MESSAGES:
.*
.*      MNOTE 12,***  IHB002 INVALID EP OPERAND SPECIFIED-&EP
.*      MNOTE 12,***  IHB002 INVALID F4 OPERAND SPECIFIED-&F4
.*      MNOTE 12,***  IHB002 INVALID ADT OPERAND SPECIFIED-&ADT
.*
.*      THE MACRO EXPANSION CONTINUES AS IF THE INVALID PARAMETER
.*      WERE NOT SPECIFIED.
.*
.* EXTERNAL REFERENCES = NONE
.*
.*      ROUTINES = NONE
.*
.*      DATA AREAS = LSPACE WORK AREA
.*
.*      CONTROL BLOCKS = DSCB, ECB, IOB, DCB, DEB
.*
.* TABLES = NONE
.*
.* MACROS = IECDSECS DSMADT
.*          IECDSECS PREFX
.*          IECSDSL1
.*          IHBERMAC
.*
.* CHANGE ACTIVITY = SEE DELETIONS/CHANGES PRECEDING THE MACRO
.*
.**********************************************************************
.*
         LCLA  &C
&LABEL   DSECT
***********************************************************************
*
*        LSPACE WORK AREA
*
***********************************************************************
         SPACE 1
***********************************************************************
*        PREFIX, EXTENDED PREFIX, AND WTG TABLE AREA
***********************************************************************
         SPACE 1
LSPPREFX DS    8F                       WORK AREA PREFIX
LSPACWKA DS    20F                      EXTENDED PREFIX
FIRST    EQU   LSPACWKA
LWTGTBL  DS    4F                       DADSM WTG TABLE
         AIF   ('&EP' EQ 'EP').LSP0010
         AIF   ('&EP' EQ '').LSP0020
         IHBERMAC 1001,EP,&EP
         AGO   .LSP0020
.LSP0010 ANOP
         ORG   LSPPREFX
*        IECDSECS PREFX,EXPAND=YES
         IECDSECS PREFX,EXPAND=YES
.LSP0020 ANOP
         SPACE 1
***********************************************************************
*        FORMAT 4 DSCB AREA
***********************************************************************
         SPACE 1
F4IN     DS    XL96                     FORMAT 4 DSCB
ENQAREA  EQU   F4IN+4                   ENQ PARAMETER LIST
MJELNAME EQU   ENQAREA+16               MAJOR ELEMENT NAME
         AIF   ('&F4' EQ 'F4').LSP0030
         AIF   ('&F4' EQ '').LSP0040
         IHBERMAC 1001,F4,&F4
         AGO   .LSP0040
.LSP0030 ANOP
         ORG   F4IN
*        IECSDSL1 (4)
         IECSDSL1 (4)
.LSP0040 ANOP
         SPACE 1
***********************************************************************
*        140-BYTE DSCB AREA
***********************************************************************
         SPACE 1
DSCBF5   DS    XL140                    140-BYTE DSCB AREA
&C       SETA  1
.LSP0050 AIF   (&C GT N'&D1).LSP0060
         AIF   (&D1(&C) EQ 0 OR &D1(&C) EQ 4).LSP0055
         ORG   DSCBF5
         MNOTE '         IECSDSL1 (&D1(&C))'
         IECSDSL1 (&D1(&C))
.LSP0055 ANOP
&C       SETA  &C+1
         AGO   .LSP0050
.LSP0060 ANOP
         DS    F                        UNUSED FULLWORD
         SPACE 1
***********************************************************************
*        LSPACE INTERNAL DEB/DCB AREA
***********************************************************************
         SPACE 1
DEB      DS    12F                      DEB
DEBPTR   DS    F                        DCB POINTER TO DEB
DCB      EQU   DEBPTR-44                DCB
         SPACE 1
***********************************************************************
*        LSPACE INTERNAL ECB/IOB AREA
***********************************************************************
         SPACE 1
ECB      DS    F                        ECB
IOB      DS    8F                       IOB
SEEK     DS    D                        DIRECT ACCESS ADDRESS
         SPACE 1
***********************************************************************
*        LSPACE CHANNEL CONTROL WORDS AREA
***********************************************************************
         SPACE 1
CCW0     DS    D
CCW1     DS    D
CCW2     DS    D
CCW3     DS    D
CCW4     DS    D
         SPACE 1
***********************************************************************
*        LSPACE WORK AREA FIELDS
***********************************************************************
         SPACE 1
CVTAREA  DS    D                        CONVERSION WORK AREA
SAVETOT  DS    3F                       TOTAL SPACE IN FORMAT 5
TCBADDR  DS    F                        POINTER TO THE TCB
MIELNAME DS    CL6                      MINOR ELEMENT ENQ NAME
TEMPCTG  DS    XL3                      LARGEST EXTENT
THETA    DS    XL1                      SECTOR VALUE FOR RPS DEVICE
SMFFLAG  DS    XL1                      SMF INDICATOR
SWITCH   DS    XL1                      LSPACE SWITCHES
*
*        DEFINITION OF LSPACE SWITCH BIT SETTINGS
*
*        BIT 0 = READ ISSUED FOR SECOND FORMAT 5 DSCB INDICATOR
*        BIT 1 = READ ISSUED FOR VOLUME LABEL INDICATOR
*        BITS 4 - 7 = ERROR CODE
*
RD2NDF5  EQU   X'80'                    SECOND FORMAT 5 DSCB READ
RDVOLABL EQU   X'40'                    READ VOLUME LABEL INDICATOR
*
         DS    XL3                      THREE UNUSED BYTES
         SPACE 1
***********************************************************************
*        ABSOLUTE TRACK ADDRESSES
***********************************************************************
         SPACE 1
VTOCADR  DS    XL5                      FORMAT 4 CCHHR
         SPACE 1
***********************************************************************
*        LSPACE RECOVERY ROUTINE AUDIT TRAIL AREA
***********************************************************************
         SPACE 1
LSPADTRL DS    6F                       LSPACE AUDIT TRAIL WORDS
         AIF   ('&ADT' EQ 'YES').LSP0090
         AIF   ('&ADT' EQ 'NO').LSP0100
         IHBERMAC 1001,ADT,&ADT
         AGO   .LSP0100
.LSP0090 ANOP
         ORG   LSPADTRL
*        IECDSECS DSMADT,EXPAND=YES
         IECDSECS DSMADT,EXPAND=YES
.LSP0100 ANOP
         SPACE 1
***********************************************************************
LSPWAEND EQU   *                        LSPACE WORK AREA END
LWALNGTH EQU   LSPWAEND-LWTGTBL         WORK AREA LENGTH FOR IECRES GET
***********************************************************************
         MEND
