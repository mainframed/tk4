         MACRO
&NAME    IEAVBK &HOURS=,&MINUTES=,&FLAG=,&CTLFLGS=,&RPS=,&ACR=,        *
               &WARN=,&STORAGE=
.* CHANGE LEVEL = 11/19/74                                     @ZA02383
.*              = 12/12/74                                     @YM30488
.*              = 12/12/75                                     @Z404PXG
.*              = 12/12/75                                     @Z40FPXG
.*              = 05/08/75                            @Z40WPSM,@Z40WPSF
.*              = 11/05/75                                     @Z40WPXG
.*              = 12/19/77                                     @G64RP2R
.*              = 08/03/78                                     @G64UPXR
.*              = 08/30/78                                     @ZA16888
.*              = 10/04/78                                     @ZM49479
.*              = 06/15/82                                     @ZA63149
.*
.* CHANGE = YM06241,M03590,R21170,M01886,M01513,M01525,R21055,
.*          R20690,YM30488,G64RP2R
.*          OZ63149 - INTEGRITY                                @ZA63149
.*
         LCLA  &CVTTZ,&CARRYHA,&CARRYMA,&CARRY,&HOURS2,&MINUTE2
         LCLA &A,&B,&N
         LCLC  &OURS,&MINS,&CARRYHC,&CARRYMC,&PSW,&SCBPARM
         LCLC  &PGM,&WAITCT,&SQEFLGS,&TASK,&GENREG(16)           Y02952
         LCLC  &ALIAS,&RQUEUE,&SISTER,&ORIGIN,&SEXIT,&SCBNUM
         LCLC  &NEG,&CVTTZC,&TASKID,&TCBBACK
         LCLC  &TCBLTC,&RBLABEL,&RSMASCB,&TCBFLG4                Y02952
         LCLC  &TCBFLG5,&TCBFLG6,&TCBFLG7,&TCBFLG3,&TCBLMP,&TCBDSP
         LCLC  &SIRBLAB,&ASCBNAM,&ASCBFWD,&ASCBBWD,&ASCBLOC      Y02752
         LCLC  &ASCBCPU,&ASCBID,&ASCBSEQ,&ASCBDP,&ASCBRSM        Y02752
         LCLC  &ASCBDS1,&ASCBTCB,&ASXBNAM,&ASXBFTC,&ASXBLTC      Y02752
         LCLC  &ASXBIHS,&ASXBFRW,&ASXBSPS,&ASXBSIR,&ASXBTSK      Y02752
         LCLC  &ASCBSPL,&ASCBASX,&ASXBTCB,&ASCBFL2,&ASCBJNS      Y02752
         LCLC  &ASXBOSB                                          Y02133
         LCLC  &ASCBPSW                                        @G64RP2R
         LCLC  &SPLNAME,&ASCBOUB,&ASCBOXB,&ASCBRSF             @ZA16888
         LCLC  &ASCBFL1,&ASCBAFF,&ASCB,&ASXB
.*  RB MAPPING MACRO                                             R20781
         IHARB                                                   R20781
         EJECT                                                  YM06241
.*  SPL DSECT                                                   YM06241
         IHASPL                                                 YM06241
         EJECT                                                  YM06241
.*  TCB MAPPING MACRO
         IKJTCB SYS=AOS2,LIST=YES
         EJECT
.*  STAE CONTROL BLOCK MAPPING MACRO
         IHASCB
         EJECT
.*  ASXB MAPPING MACRO
         IHAASXB
         EJECT
.*  ASCB MAPPING MACRO
         IHAASCB
         EJECT
.*  RSM MAPPING MACRO
         IHARSMHD
         EJECT
IEAVBK00 CSECT
         DC    CL8'IEAVBK00'                                     Y02952
         DC    CL8'10/04/78'                                   @ZM49479
*        ENTRY POINTS FOR MASTER MEMORY TCBS                     Y02952
         ENTRY IEADSTCB
         ENTRY IEECVTCB
         ENTRY IEAMSTCB
.*
*        ENTRY POINTS FOR WAIT MEMORY TCB                        Y02952
         ENTRY IEAWTCB                                           Y02952
         ENTRY WAITTCB                                           Y02952
.*                                                               Y02952
*        ENTRY POINTS FOR REQUEST BLOCKS                         Y02952
         ENTRY IEAMPRB                                           Y02952
         ENTRY IEADSPRB                                          Y02952
         ENTRY IEACVPRB                                          Y02952
         ENTRY IEAWPRB                                           Y02952
         ENTRY IEAMSIRB                                          R20781
.*                                                               Y02952
*        ENTRY POINTS FOR ASCB                                   Y02752
         ENTRY IEAMASCB                                          Y02752
         ENTRY IEAWASCB                                          Y02752
.*                                                               Y02952
* DOUBLE WORD SIZE OF VARIOUS CONTROL BLOCKS
DWASCB   EQU   (ASCBEND+7-ASCB)/8
DWASXB   EQU   (ASXBEND+7-ASXB)/8
DWTCB    EQU   (TCBLEN+7)/8
.*       LOCAL CONSTANTS
&GENREG(1)     SETC '0'
&GENREG(2)     SETC '0'
&GENREG(3)     SETC '0'
&GENREG(4)     SETC '0'
&GENREG(5)     SETC '0'
&GENREG(6)     SETC '0'
&GENREG(7)     SETC '0'
&GENREG(8)     SETC '0'
&GENREG(9)     SETC '0'
&GENREG(10)    SETC '0'
&GENREG(11)    SETC '0'
&GENREG(12)    SETC '0'
&GENREG(13)    SETC '0'
&GENREG(14)    SETC '0'
&GENREG(15)    SETC '0'
&GENREG(16)    SETC '0'
&SCBPARM SETC  '0'
         EJECT
.*
.*       MASTER MEMORY'S ASCB, ASXB, AND RSM BLOCK VARIABLES     Y02752
&ASCBNAM SETC  'IEAMASCB'                                        Y02752
&ASCBFWD SETC  '0'                                             @Z40WPSF
&ASCBBWD SETC  '0'                                               Y02752
&ASCBLOC SETC  '00000000'                                        Y02752
&ASCBSPL SETC  'IEAMSPL'                                         Y02752
&ASCBCPU SETC  '1'                                               Y02752
&ASCBID  SETC  '0001'                                            Y02752
&ASCBSEQ SETC  '0001'                                            Y02752
&ASCBDP  SETC  'FF'                                              Y02752
&ASCBASX SETC  'IEAMASXB'                                        Y02752
&ASCBDS1 SETC  '00'                                              Y02752
&ASCBFL1 SETC  '81'
&ASCBFL2 SETC  'C0'                                              Y02752
&ASCBTCB SETC  '1'                                               Y02752
&ASCBRSM SETC  'RSMHD1'                                          Y02752
&ASCBAFF SETC  'FFFF'
&ASCBOUB SETC  'OUCBMSTR'                                      @Z40WPXG
&ASCBOXB SETC  'OUXBMSTR'                                      @Z40WPXG
&ASCBJNS SETC  'MSJBNM'                                          Y02752
&ASCBPSW SETC  '07'                                            @G64RP2R
&ASCBRSF SETC  '40'                                            @ZA16888
&ASXBNAM SETC  'IEAMASXB'                                        Y02752
&ASXBFTC SETC  'IEAMSTCB'                                        Y02752
&ASXBLTC SETC  'IEECVTCB'                                        Y02752
&ASXBTCB SETC  '0003'                                            Y02752
&ASXBIHS SETC  'IEAMIHSA'                                        Y02752
&ASXBFRW SETC  'IEAMFRWA'                                        Y02752
&ASXBSPS SETC  'IEAMSPSA'                                        Y02752
&ASXBSIR SETC  'IEAMSIRB'                                        Y02752
&ASXBTSK SETC  'IEAMSTCB'                                        Y02752
&ASXBOSB SETC  'OUSBMSTR'                                        Y02133
&RSMASCB SETC  'IEAMASCB'                                        Y02752
&SPLNAME SETC  'IEAMSPL'                                        YM06241
.*                                                               Y02952
.*       MASTER SCHEDULER'S TASK'S CONTROL BLOCK VARIABLES       Y02952
*        MASTER SCHEDULER'S TASK'S CONTROL BLOCKS                Y02952
&PGM     SETC  '0'                                               Y02952
&PSW     SETC  '040C0000'                                        Y02952
&RQUEUE  SETC  'IEADSTCB'                                        Y02952
&SCBNUM  SETC  '0'                                               Y02702
&SEXIT   SETC  '0'                                               Y02702
&SISTER  SETC  '0'                                               Y02952
&TCBBACK SETC  '0'                                               Y02952
&TASK    SETC  'IEAMSTCB'                                        Y02952
&TASKID  SETC  'FB'                                              Y02952
&ORIGIN  SETC  '0'                                               Y02952
&WAITCT  SETC  '00'                                              Y02952
&ALIAS   SETC  ''                                                Y02952
&TCBLTC  SETC  'IEADSTCB'                                        Y02952
&RBLABEL SETC  'IEAMPRB'                                         Y02952
&TCBFLG6 SETC  '80'
&TCBFLG7 SETC  '04'
&TCBFLG3 SETC  '80'
&TCBFLG4 SETC  '00'
&TCBFLG5 SETC  '00'
&TCBLMP  SETC  'FF'
&TCBDSP  SETC  'FF'
&N       SETA  1
.BUILDA  ANOP                                                    Y02952
*        ASCB AND ASXB FOR MEMORY                                Y02752
.*                                                               Y02752
         DS    0D
ASCB&N   EQU   *
&ASCBNAM DS   0D
         DC    (DWASCB)D'0'
         ORG   ASCB&N
         DC    CL4'ASCB'
         ORG   ASCB&N+(ASCBFWDP-ASCB)
         DC    A(&ASCBFWD)
         ORG   ASCB&N+(ASCBBWDP-ASCB)
         DC    A(&ASCBBWD)
         ORG   ASCB&N+(ASCBLOCK-ASCB)
         DC    X'&ASCBLOC'
         ORG   ASCB&N+(ASCBSPL-ASCB)
         DC    A(&ASCBSPL)
         ORG   ASCB&N+(ASCBCPUS-ASCB)
         DC    F'&ASCBCPU'
         ORG   ASCB&N+(ASCBASID-ASCB)
         DC    X'&ASCBID'
         ORG   ASCB&N+(ASCBSEQN-ASCB)
         DC    X'&ASCBSEQ'
         ORG   ASCB&N+(ASCBDP-ASCB)
         DC    X'&ASCBDP'
         ORG   ASCB&N+(ASCBRSM-ASCB)
         DC    A(&ASCBRSM)
         ORG   ASCB&N+(ASCBAFFN-ASCB)
         DC    X'&ASCBAFF'
         ORG   ASCB&N+(ASCBFLG1-ASCB)
         DC    X'&ASCBFL1'
         ORG   ASCB&N+(ASCBASXB-ASCB)
         DC    A(&ASCBASX)
         ORG   ASCB&N+(ASCBDSP1-ASCB)
         DC    X'&ASCBDS1'
         ORG   ASCB&N+(ASCBFLG2-ASCB)
         DC    X'&ASCBFL2'
         ORG   ASCB&N+(ASCBTCBS-ASCB)
         DC    F'&ASCBTCB'
         ORG   ASCB&N+(ASCBOUCB-ASCB)                          @Z40WPXG
         DC    A(&ASCBOUB)                                     @Z40WPXG
         ORG   ASCB&N+(ASCBOUXB-ASCB)                          @Z40WPXG
         DC    A(&ASCBOXB)                                     @Z40WPXG
         ORG   ASCB&N+(ASCBJBNS-ASCB)                          @Z40WPXG
         DC    A(&ASCBJNS)
         ORG   ASCB&N+(ASCBRSMF-ASCB)                          @ZA16888
         DC    X'&ASCBRSF'                                     @ZA16888
         ORG   ASCB&N+(ASCBSRBM-ASCB)                          @G64RP2R
         DC    X'&ASCBPSW'                                     @G64RP2R
.*                                                              YM06241
         ORG
         SPACE 3                                               @ZA02383
.* SPL FOR MEMORY                                               YM06241
.*   THE SPL MUST IMMEDIATELY FOLLOW THE ASCB DUE TO CODE       YM06241
.*   IN IEAVECBV WHICH VERIFIES THE SPL ADDRESS FOR             YM06241
.*   SUPERVISOR RECOVERY                                        YM06241
.*                                                              YM06241
&SPLNAME DC    XL(LSPLSIZE)'00'                                 YM06241
         EJECT
         AIF   (&N EQ 6).BUILD1   NO ASXB FOR WAIT
         EJECT                                                 @ZA02383
.*                                                               Y02752
         DS    0D
ASXB&N   EQU   *
&ASXBNAM DS    0D
         DC    (DWASXB)D'0'
         ORG   ASXB&N
         DC    CL4'ASXB'
         ORG   ASXB&N+(ASXBFTCB-ASXB)
         DC    A(&ASXBFTC)
         ORG   ASXB&N+(ASXBLTCB-ASXB)
         DC    A(&ASXBLTC)
         ORG   ASXB&N+(ASXBTCBS-ASXB)
         DC    X'&ASXBTCB'
         ORG   ASXB&N+(ASXBIHSA-ASXB)
         DC    A(&ASXBIHS)
         ORG   ASXB&N+(ASXBFRWA-ASXB)
         DC    A(&ASXBFRW)
         ORG   ASXB&N+(ASXBSPSA-ASXB)
         DC    A(&ASXBSPS)
         ORG   ASXB&N+(ASXBOUSB-ASXB)
         DC    A(&ASXBOSB)
         ORG   ASXB&N+(ASXBPRG-ASXB)
         DC    CL16' '
         ORG   ASXB&N+(ASXBPSWD-ASXB)
         DC    CL8' '
         ORG   ASXB&N+(ASXBSIRB-ASXB)
         DC    A(&ASXBSIR)
         ORG   ASXB&N+(ASXBETSK-ASXB)
         DC    A(&ASXBTSK)
         ORG
         EJECT
.BUILD1  ANOP                                                    Y02752
*        TCB FOR TASK                                            Y02752
         ORG
PREFIX&N DS    0D
         DC    (DWTCB)D'0'
         ORG   PREFIX&N+(TCB-TCBFIX)
&ALIAS   DS    0D
&TASK    DC    A(RB&N)
.BUILD2  ANOP
         ORG   &TASK+(TCBFLGS3-TCB)
         DC    X'&TCBFLG3'
         ORG   &TASK+(TCBFLGS4-TCB)
         DC    X'&TCBFLG4'
         ORG   &TASK+(TCBFLGS5-TCB)
         DC    X'&TCBFLG5'
         ORG   &TASK+(TCBLMP-TCB)
         DC    X'&TCBLMP'
         ORG   &TASK+(TCBDSP-TCB)
         DC    X'&TCBDSP'
         ORG   &TASK+(TCBGRS0-TCB)
         DC    A(&GENREG(1))
         ORG   &TASK+(TCBGRS1-TCB)
         DC    A(&GENREG(2))
         ORG   &TASK+(TCBGRS2-TCB)
         DC    A(&GENREG(3))
         ORG   &TASK+(TCBGRS3-TCB)
         DC    A(&GENREG(4))
         ORG   &TASK+(TCBGRS4-TCB)
         DC    A(&GENREG(5))
         ORG   &TASK+(TCBGRS5-TCB)
         DC    A(&GENREG(6))
         ORG   &TASK+(TCBGRS6-TCB)
         DC    A(&GENREG(7))
         ORG   &TASK+(TCBGRS7-TCB)
         DC    A(&GENREG(8))
         ORG   &TASK+(TCBGRS8-TCB)
         DC    A(&GENREG(9))
         ORG   &TASK+(TCBGRS9-TCB)
         DC    A(&GENREG(10))
         ORG   &TASK+(TCBGRS10-TCB)
         DC    A(&GENREG(11))
         ORG   &TASK+(TCBGRS11-TCB)
         DC    A(&GENREG(12))
         ORG   &TASK+(TCBGRS12-TCB)
         DC    A(&GENREG(13))
         ORG   &TASK+(TCBGRS13-TCB)
         DC    A(&GENREG(14))
         ORG   &TASK+(TCBGRS14-TCB)
         DC    A(&GENREG(15))
         ORG   &TASK+(TCBGRS15-TCB)
         DC    A(&GENREG(16))
         ORG   &TASK+(TCBTCB-TCB)
         DC    A(&RQUEUE)
         ORG   &TASK+(TCBJSTCB-TCB)
         DC    A(&TASK)
         ORG   &TASK+(TCBNTC-TCB)
         DC    A(&SISTER)
         ORG   &TASK+(TCBOTC-TCB)
         DC    A(&ORIGIN)
         ORG   &TASK+(TCBLTC-TCB)
         DC    A(&TCBLTC)
         ORG   &TASK+(TCBSTAB-TCB)
         DC    A(&SCBNUM)
         ORG   &TASK+(TCBFLGS6-TCB)
         DC    X'&TCBFLG6'
         ORG   &TASK+(TCBFLGS7-TCB)
         DC    X'&TCBFLG7'
         ORG   &TASK+(TCBEXT2-TCB)
         DC    A(XTEN&N)
         ORG   &TASK+(TCBBACK-TCB)
         DC    A(&TCBBACK)
         ORG   &TASK+(TCBTID-TCB)
         DC    X'&TASKID'
         ORG   &TASK+(TCBTCBID-TCB)
         DC    C'TCB '
         ORG   &TASK+TCBMNLEN
XTEN&N   EQU   *
         EJECT                                                   Y02952
.*       GENERATE PRB
*      PRB FOR THE ABOVE TASK
         ORG
RBFIX&N  DS    0D
         DC    XL(PRBLEN)'00'
         ORG   RBFIX&N+RBPRFLNA
RB&N     EQU   *
&RBLABEL DS    0D
         ORG   RB&N+(RBSIZE-RBSECT)
         DC    X'00110080'
         ORG   RB&N+(RBOPSW-RBSECT)
         AIF   (&N NE 6).NOWAIT
         ENTRY IEAWPSW
IEAWPSW  DC    X'&PSW'
         AGO   .WTLABEL
.NOWAIT  DC    X'&PSW'
.WTLABEL DC    A(&PGM)
         ORG   RB&N+(RBWCF-RBBASIC)
         DC    X'&WAITCT'
         ORG   RB&N+(RBLINKB-RBBASIC)
         DC    AL3(&TASK)
         ORG
         AIF   (&N EQ 1 OR &N GE 4).NOSTAR
         SPACE 3                                               @ZA02383
.*       GENERATE ESTAR SCB                                    @ZA02383
* ESTAR SCB FOR THE ABOVE TASK                                 @ZA02383
SCB&N    DC    XL(SCBLEN)'00'
         ORG   SCB&N+(SCBEXIT-SCB)
         DC    A(&SEXIT)
         ORG   SCB&N+(SCBFLGS1-SCB)
         DC    X'56'                                           @ZA08178
         ORG   SCB&N+(SCBPARMA-SCB)
         DC    AL3(&SCBPARM)
         ORG   SCB&N+(SCBFLGS2-SCB)
         DC    X'03'
         ORG   SCB&N+(SCBID-SCB)
         DC    X'DB'
         ORG   SCB&N+(SCBRSVRE-SCB)
         DC    CL1' '
SCBEND&N DS    0C                                              @ZA02383
         ORG
.NOSTAR  ANOP                                                    Y02702
         SPACE 3                                                 Y02952
         AIF   (&N NE 1).NOSIRB                                @Z40WPSF
&SIRBLAB SETC  'IEAMSIRB'                                        Y02952
.BLDSIRB ANOP                                                    Y02952
*        SIRB FOR MEMORY                                         Y02952
         DS    0D                                                R20781
         DC    XL(RBPRFLNA)'00'             PREFIX               R20781
&SIRBLAB DS    0H                           LABEL                R20781
         DC    XL(SIRBLEN-RBPRFLNA)'00'                          R20781
         ORG
.*                                                               R20781
         SPACE 3                                                 Y02952
*        RSM HEADER FOR MEMORY                                   Y02752
         SPACE 1                                                 Y02752
RSMHD&N  DS    0F
         DC    XL(RSMLEN)'00'
         ORG   RSMHD&N+(RSMASCB-RSMHD)
         DC    A(&RSMASCB)
         ORG
.NOSIRB  ANOP                                                    Y02952
         EJECT                                                   Y02952
.SKIPASM ANOP                                                  @Z40WPSF
&N       SETA  &N+1                                              Y02952
         AIF   (&N EQ 2).DSS                                     Y02952
         AIF   (&N EQ 3).COMTASK                                 Y02952
         AIF   (&N EQ 4).SKIPASM                               @Z40WPSF
         AIF   (&N EQ 5).SKIPASM                               @Z40WPSF
         AIF   (&N EQ 6).WAIT                                    Y02952
         AGO   .ENDTCBS                                          Y02952
.DSS     ANOP
*        DSS TASK'S CONTROL BLOCKS
.*       DSS TASK'S CONTROL BLOCK VARIABLES
&PGM     SETC  '0'
&PSW     SETC  '060C0000'
&RQUEUE  SETC  'IEECVTCB'                                        Y02952
&SCBNUM  SETC  'SCB&N'
&SEXIT   SETC  '0'                                               Y02952
&SISTER  SETC  'IEECVTCB'                                        Y02952
&TCBBACK SETC  'IEAMSTCB'                                        Y02952
&TASK    SETC  'IEADSTCB'
&TASKID  SETC  'F9'
&ORIGIN  SETC  'IEAMSTCB'                                        Y02952
&TCBLTC  SETC  '0'                                               Y02952
&WAITCT  SETC  '01'                                              Y02952
&ALIAS   SETC  ''                                                Y02952
&RBLABEL SETC  'IEADSPRB'                                        Y02952
&TCBFLG6 SETC  '00'
&TCBFLG7 SETC  '04'
&TCBFLG3 SETC  '80'
&TCBFLG4 SETC  '00'
&TCBFLG5 SETC  '00'
&TCBLMP  SETC  'FF'
&TCBDSP  SETC  'FF'
         AGO   .BUILD1
.COMTASK ANOP
*        COMM-TASK TASK'S CONTROL BLOCKS
.*       COMM-TASK TASK'S CONTROL BLOCK VARIABLES
&PGM     SETC  '0'
&PSW     SETC  '040C0000'
&TASK    SETC  'IEECVTCB'                                        Y02952
&SCBNUM  SETC  'SCB&N'
&SEXIT   SETC  '0'
&SISTER  SETC  '0'                                               Y02952
&TCBBACK SETC  'IEADSTCB'                                        Y02952
&RQUEUE  SETC  '0'                                               Y02952
&TASKID  SETC  'FD'
&WAITCT  SETC  '01'                                              Y02952
&ORIGIN  SETC  'IEAMSTCB'                                        Y02952
&TCBLTC  SETC  '0'                                               Y02952
&ALIAS   SETC  ''                                                Y02952
&RBLABEL SETC  'IEACVPRB'                                        Y02952
&TCBFLG6 SETC  '00'
&TCBFLG7 SETC  '04'
&TCBFLG3 SETC  '80'
&TCBFLG4 SETC  '00'
&TCBFLG5 SETC  '00'
&TCBLMP  SETC  'FF'
&TCBDSP  SETC  'FF'
         AGO   .BUILD1
.**************************************************************@Z40WPSM
.** DEFINITIONS FOR ASM MEMORY'S ASCB, ASXB, RSMHD, TCB, AND   @Z40WPSM
.** RB BLOCK VARIABLES WERE DELETED IN SUPPORT OF ASM          @Z40WPSM
.** REDESIGN FOR THE ELIMINATION OF ASID2                      @Z40WPSM
.**************************************************************@Z40WPSM
.WAIT    ANOP                                                    Y02952
.*       WAIT MEMORY'S ASCB BLOCK VARIABLES                      Y02752
&ASCBNAM SETC  'IEAWASCB'                                        Y02752
&ASCBFWD SETC  '0'                                               Y02752
&ASCBBWD SETC  '0'                                               Y02752
&ASCBLOC SETC  '7FFFFFFF'                                        Y02752
&ASCBSPL SETC  'IEAWSPL'                                         Y02752
&ASCBCPU SETC  '0'                                               Y02752
&ASCBID  SETC  '0000'                                            Y02752
&ASCBSEQ SETC  'FFFF'                                            Y02752
&ASCBDP  SETC  '00'                                              Y02752
&ASCBASX SETC  'IEAMASXB'                                        M01525
&ASCBDS1 SETC  '80'                                              Y02752
&ASCBFL2 SETC  'C0'                                            @ZA02383
&ASCBTCB SETC  '1'                                               Y02752
&ASCBRSM SETC  'RSMHD1'                                          M01525
&ASCBPSW SETC  '00'                                            @G64RP2R
&ASCBJNS SETC  '0'                                               Y02752
&SPLNAME SETC  'IEAWSPL'                                        YM06241
&ASCBOUB SETC  '0'                                             @Z40WPXG
&ASCBOXB SETC  '0'                                             @Z40WPXG
&ASCBRSF SETC  '00'                                            @ZA16888
.*                                                               Y02952
*        WAIT TASK'S CONTROL BLOCKS                              Y02952
.*       WAIT TASK'S CONTROL BLOCK VARIABLES                     Y02952
&SCBNUM  SETC  '0'                                               Y02702
&TASK    SETC  'IEAWTCB'                                         Y02952
&SISTER  SETC  '0'                                               Y02952
&TCBBACK SETC  '0'                                               Y02952
&RQUEUE  SETC  '0'                                               Y02952
&TASKID  SETC  '00'                                              Y02952
&ORIGIN  SETC  '0'                                               Y02952
&TCBLTC  SETC  '0'                                               Y02952
&WAITCT  SETC  '00'                                              Y02952
&PSW     SETC  '070E0000'                                        Y02952
&PGM     SETC  '0'                                               Y02952
&ALIAS   SETC  'WAITTCB'                                         Y02952
&RBLABEL SETC  'IEAWPRB'                                         Y02952
&SEXIT   SETC  '0'                                               Y02702
&TCBFLG6 SETC  '00'
&TCBFLG7 SETC  '04'
&TCBFLG3 SETC  '80'
&TCBFLG4 SETC  '00'
&TCBFLG5 SETC  '80'
&TCBLMP  SETC  'FF'
&TCBDSP  SETC  'FF'
         AGO   .BUILDA                                           Y02952
.ENDTCBS ANOP                                                    Y02952
*        JOBNAMES FOR GENERALIZED TRACE FACILITY
*
MSJBNM   DC    CL8'*MASTER*'                                     Y02952
ASMJBNM  DC    CL8'*AUXSTM*'                                     M01886
*
         SPACE 5
*
*     SVC/LINK LIBRARY'S DUMMY DCBS
*
         ENTRY LINKDCB
         ENTRY SVCDCB
         DS    0D
LINKDCB  EQU   *-44
         DC    D'0'
SVCDCB   EQU   *-44
         DC    D'0'
*
         ENTRY IEASVDCB
         ENTRY IEFLINK
IEFLINK  EQU   LINKDCB
IEASVDCB EQU   SVCDCB
         SPACE 5
*
*        POINTER TO LPAQ
*
         ENTRY IEAQLPAQ
IEAQLPAQ DC    F'0'
*
*        TIMER SUPERVISION WORK AREA                             Y02952
         IEAVVTPC DSECT=NO                                       Y02952
*                                                                Y02952
         EJECT
*
*        DSS CONTROL VECTOR TABLE
         IQADSV DSECT=NO                                         Y02952
         ENTRY IQADSV00
*
         EJECT
*  GENERATE THE CVT TABLE FOR AOS/2 SYSTEM
*
         AIF   ('&HOURS' EQ '0').MINS
&A       SETA  K'&HOURS
         AIF   ('&HOURS'(1,1) EQ '-').NEGH
&OURS    SETC  '&HOURS'(1,&A-2)
         AGO   .POSH
.NEGH    ANOP
&OURS    SETC  '&HOURS'(2,&A-3)
&NEG     SETC  '-'
.POSH    ANOP
&CARRYHC SETC  '&HOURS'(&A-1,2)
&HOURS2  SETA  &OURS
&CARRYHA SETA  &CARRYHC
.MINS    ANOP
         AIF   ('&MINUTES' EQ '0').CARRY
&B       SETA  K'&MINUTES
         AIF   ('&MINUTES'(1,1) EQ '-').NEGM
&MINS    SETC  '&MINUTES'(1,&B-2)
         AGO   .POSM
.NEGM     ANOP
&MINS    SETC  '&MINUTES'(2,&B-3)
&NEG     SETC  '-'
.POSM    ANOP
&CARRYMC SETC  '&MINUTES'(&B-1,2)
&MINUTE2 SETA  &MINS
&CARRYMA SETA  &CARRYMC
.CARRY   ANOP
&CARRY   SETA  &CARRYHA+&CARRYMA
         AIF   (&CARRY LT 150).ONEKARY
&MINUTE2 SETA  &MINUTE2+2
         AGO   .NOTKARY
.ONEKARY ANOP
         AIF   (&CARRY LT 50).NOTKARY
&MINUTE2 SETA  &MINUTE2+1
.NOTKARY ANOP
&CVTTZ   SETA  &HOURS2+&MINUTE2
&CVTTZC  SETC  '&NEG&CVTTZ'
IEACVT   CVT   PREFIX=YES,DSECT=NO,FLAG=&FLAG,FLAGB=48,CVTTZ=&CVTTZC,CT*
               LFLGS=&CTLFLGS,RPS=&RPS,LIST=YES,WARN=&WARN,STORAGE=&STO*
               RAGE
         ENTRY IEACVT
*
         EJECT
*
*        GENERATE THE SCVT
*
IEABEND  DS    0D
         IHASCVT DSECT=NO,LIST=YES
         ENTRY IEABEND
*
         EJECT
*        GENERATE BIT MAP FOR APPLIED SU PACKAGES              @Z404PXG
*                                                              @Z404PXG
IHASETSU DS    0F                                              @Z404PXG
         IHASETSU                                              @Z404PXG
         EJECT                                                 @Z404PXG
*        GENERATE  THE RMS VECTOR TABLE
*
IGFRVT   DS    0F                                                M01513
         IHARVT DSECT=NO                                         M01513
         ENTRY IGFRVT
         EJECT
*
*        DSECTS NEEDED FOR SERVICE MANAGER                       Y02715
*                                                                Y02715
         IHAIHSA                                                 Y02715
         EJECT                                                   Y02715
.*                                                               Y02715
         IHASDWA DSECT=YES                                       Y02715
         EJECT
*
*        FE PATCH AREA
*
IEAPATCH CSECT
         DC   200X'00'
         SPACE 5
*        TESTAUTH AUTHORIZATION TABLE
*
IEAVAUTH CSECT
         DC    X'02'                AC SIZE
         DC    X'02'                FC SIZE
         DC    X'10'                BIT TABLE                  @ZA63149
         SPACE 5                                                 Y02751
*        SAVE AREA/WORK AREA FOR BRANCH ENTERED ROUTINES         Y02751
*                                                                Y02751
IEAVWSAG CSECT                                                 @G64UPXR
         IHAWSAVT  CLASS=GLOBAL,DSECT=NO,FUNC=INIT             @G64UPXR
*                                                              @G64UPXR
IEAVGWSA CSECT                                                   Y02751
         IHAWSAVT  CLASS=GLOBAL,DSECT=NO                         Y02751
*                                                                Y02751
IEAVWSAL CSECT                                                   Y02751
         IHAWSAVT  CLASS=LOCAL,FUNC=INIT,DSECT=NO                Y02751
*                                                                Y02751
         AIF   ('&ACR' EQ 'YES').YESMP    MP CODE IN SYSTEM?     R20690
.* NO, GENERATE 8-BYTE RESERVED AREA                             R20690
IEAVWSAC CSECT                                                   R20690
         IHAWSAVT  CLASS=CPU,FUNC=INIT,DSECT=NO,ACR=NO           R20690
*                                                                R20690
         AGO   .NOMP                                             R20690
.YESMP   ANOP          GENERATE ACR GLOBAL SAVE AREA             R20690
IEAVWSAC CSECT                                                   Y02751
         IHAWSAVT  CLASS=CPU,FUNC=INIT,DSECT=NO                  Y02751
*                                                                Y02751
.* IEAICPUS IS BEING MOVED TO SQA SO THAT THE STORAGE MAY BE   @YM30488
.* FREED WHEN VARY CPU OFFLINE                                 @YM30488
*                                                                R20690
.NOMP    ANOP                                                    R20690
*                                                                Y02751
IEAMSPSA CSECT                                                   Y02751
         IHAWSAVT  CLASS=LOCAL,DSECT=NO                          Y02751
*                                                                Y02751
         EJECT                                                   Y02751
*        SETLOCK CSECT                                           Y02751
*                                                                Y02751
IEAVESLA CSECT                                                   Y02751
.* CMS AREAS - CMSSRBL,CMSASBL & CMSASBF - WERE DELETED IN SUPPORT
.* OF LOCK MANAGER PERFORMANCE.                                @Z40FPXG
         ENTRY DISPLOCK                                          Y02751
         ENTRY SALCLOCK                                          Y02751
         ENTRY OPTLOCK                                           Y02751
         ENTRY CMSLOCK                                           Y02751
         ENTRY CMSSQH                                          @Z40FPXG
         DS    0F
DISPLOCK DC    A(0)             THE GLOBAL DISPATCHER LOCK       Y02751
SALCLOCK DC    A(0)             THE GLOBAL STORAGE ALLOCATION    Y02751
*                               LOCK                             Y02751
OPTLOCK  DC    A(0)             THE GLOBAL OPTIMIZER LOCK        Y02751
         DS    0D               ALIGN ON DOUBLE WORD BOUNDARY  @Z40FPXG
CMSLOCK  DC    A(0)             THE CROSS MEMORY SERVICE LOCK    Y02751
CMSSQH   DC    A(0)             ADDRESS OF FIRST SRB SUSPENDED @Z40FPXG
*                               ON THE CMS LOCK. ZERO IF NONE.   Y02751
         EJECT                                                   Y02751
*                                                                Y02715
IEAVBK00 CSECT                                                   Y02715
*                                                                Y02715
*        SERVICE MANAGER VARIABLES                               Y02715
         ENTRY IEAGSMQ                                           Y02715
         ENTRY IEALSMQ                                           Y02715
         ENTRY IEAGSPL                                           Y02715
         ENTRY IEASMSY                                           Y02715
         ENTRY IEASMNQ                                           Y02715
IEAGSMQ  DC    F'0'                     GLOBAL SERVICE MANAGER   Y02715
*                                       QUEUE HEADER             Y02715
IEALSMQ  DC    F'0'                     LOCAL SERVICE MANAGER    Y02715
*                                       QUEUE HEADER             Y02715
IEAGSPL  DS    0D                       GLOBAL SPL               Y02715
         DC    XL(GSPLSIZE)'00'                                  Y02715
         ORG   IEAGSPL+GSPLNQ-GSPL                               Y02715
IEASMNQ  DS    0D                                                Y02715
         ORG   IEAGSPL+GSPLSYS-GSPL                              Y02715
IEASMSY  DS    0D                                                Y02715
         ORG   IEAGSPL+GSPLSIZE                                  Y02715
*                                                                Y02715
*        SAVE AREAS FOR MASTER MEMORY                          @Z40WPSF
         DS    0D                                                Y02715
IEAMIHSA DS    XL(IHSALEN)                                       Y02715
         DS    0D                                                Y02715
IEAMFRWA DS    XL(SDWAFLEN)                                      Y02715
*                                                                Y02715
* SRM USER CONTROL BLOCKS                                      @Z40WPXG
*                                                                Y02133
         IRAOUCB DSECT=NO,LABEL=OUCBMSTR                       @Z40WPXG
         IHAOUXB DSECT=NO,LABEL=OUXBMSTR                       @Z40WPXG
         IHAOUSB DSECT=NO,LABEL=OUSBMSTR                       @Z40WPXG
         MEND
