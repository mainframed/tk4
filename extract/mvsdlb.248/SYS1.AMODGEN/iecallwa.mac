         MACRO
&LABEL   IECALLWA &EP,&F4,&D1=(0),&D2=(0),&ADT=NO
.*
.*       MACRO DELETIONS/CHANGES FOR VS2 RELEASE 038
.*
.* $01=OZ65137,EDM1102,T820306,STLMDB: FIX FOR ENQ LOCKOUT PROB    @01A
.*
.**********************************************************************
.*
.* MODULE NAME = IECALLWA (VS2-2)
.*
.* DESCRIPTIVE NAME = DADSM ALLOCATE WORK AREA ADDRESSING MACRO
.*
.* COPYRIGHT = NONE
.*
.* STATUS = CHANGE LEVEL 000
.*
.* FUNCTION = DADSM ALLOCATE MACRO WHICH PROVIDES ADDRESSABILITY
.*            TO THE 820 DECIMAL BYTE WORK AREA (NOT INCLUDING THE
.*            8 WORD PREFIX OR THE 6 WORD RECOVERY ROUTINE PARAMETER
.*            LIST) USED BY ALLOCATE, ISAM ALLOCATE, AND THE VTOC
.*            CONVERSION ROUTINES.
.*
.* NOTES = THIS MACRO IS AN INTERNAL MACRO USED IN THE FOLLOWING
.*         DADSM FUNCTIONS:
.*
.*             ALLOCATE
.*             EXTEND (INTERFACE TO VTOC CONVERSION)
.*             ISAM ALLOCATE
.*             VTOC CONVERSION
.*
.*         TO REFERENCE THIS WORK AREA WITHIN THE FUNCTIONS LISTED
.*         ABOVE, CODE A USING STATEMENT ON THE LABEL 'ALLOCWKA'.
.*
.*      DEPENDENCIES = THIS MACRO IS DESIGNED TO BE USED OPTIONALLY
.*                     WITH THE IECSDSL1 DSCB MAPPING MACRO, THE
.*                     PREFX MACRO (DEFINED IN THE IECDSECS MACRO)
.*                     WHICH DEFINES THE 8 WORD WORK AREA PREFIX,
.*                     THE 20 WORD EXTENDED WORK AREA PREFIX, AND
.*                     THE 4 WORD DADSM WTG TABLE, AND THE DSMADT
.*                     MACRO (ALSO DEFINED IN THE IECDSECS MACRO)
.*                     WHICH DEFINES THE 6 WORD DADSM AUDIT TRAIL.
.*
.*      RESTRICTIONS = NONE
.*
.*      REGISTER CONVENTIONS = NOT APPLICABLE
.*
.* PATCH LABEL = NOT APPLICABLE
.*
.* MODULE TYPE = MACRO
.*
.*      PROCESSOR = ASSEMBLER LANGUAGE
.*
.*      MODULE SIZE = 820 DECIMAL BYTES (NOT INCLUDING THE PREFIX)
.*
.*      ATTRIBUTES = NOT APPLICABLE
.*
.* ENTRY POINT = ALLPREFX
.*
.*      PURPOSE = THIS MACRO IS CALLED AT ASSEMBLY TIME BY THE
.*                FUNCTIONS LISTED UNDER 'NOTES' TO EXPAND THE
.*                ALLOCATE WORK AREA.
.*
.*      LINKAGE =
.*
.*                LABEL IECALLWA EP,F4,D1=(N),D2=(N),ADT=YES
.*
.*      INPUT =
.*
.*                OPTIONAL LABEL SPECIFIED AS AN ACCEPTABLE ASSEMBLER
.*                LABEL WHICH CAN BE USED BY THE CALLING CSECT TO
.*                ESTABLISH ADDRESSABILITY TO THE ENTIRE WORK AREA.
.*
.*                EP IS THE FIRST POSITIONAL PARAMETER, SPECIFYING
.*                THAT THE LABELS FOR EACH OF THE FIELDS IN THE 8
.*                WORD PREFIX, 20 WORD EXTENDED PREFIX, AND THE 4
.*                WORD DADSM WTG TABLE ARE TO BE EXPANDED. IF THESE
.*                FIELDS ARE NOT TO BE DEFINED, A COMMA MUST BE
.*                CODED TO INDICATE THE ABSENCE OF THIS PARAMETER.
.*
.*                F4 IS THE SECOND POSITIONAL PARAMETER, SPECIFYING
.*                THAT THE FIELDS OF THE FORMAT 4 DSCB ARE TO BE
.*                EXPANDED USING THE IECSDSL1 DSCB MAPPING MACRO.
.*                IF THESE FIELDS ARE NOT TO BE DEFINED AND A
.*                KEYWORD PARAMETER FOLLOWS, A COMMA MUST BE
.*                CODED TO INDICATE THE ABSENCE OF THIS PARAMETER.
.*
.*                D1=(N) IS A KEYWORD PARAMETER THAT SPECIFIES THE
.*                FORMAT(S) OF THE DSCB(S) TO BE EXPANDED IN THE
.*                FIRST 140 BYTE DSCB AREA, USING THE IECSDSL1 MACRO.
.*                N CAN BE A NUMBER FROM 0 - 6.  IF N IS 0 OR 4 OR
.*                IF THIS KEYWORD IS OMITTED, NO LABELS ARE EXPANDED.
.*                IF N IS 1, 2, 3, 5, OR 6, THE CORRESPONDING FORMAT
.*                OF THE DSCB WILL BE EXPANDED.  MORE THAN ONE DSCB
.*                FORMAT CAN BE MAPPED IN THE FIRST 140 BYTE DSCB
.*                AREA BY SEPARATING THE FORMAT NUMBERS BY COMMAS.
.*
.*                D2=(N) IS A KEYWORD PARAMETER THAT SPECIFIES
.*                THE FORMAT(S) OF THE DSCB(S) TO BE EXPANDED IN THE
.*                SECOND 140 BYTE DSCB AREA, USING THE IECSDSL1 MACRO.
.*                N CAN BE A NUMBER FROM 0 - 6.  IF N IS 0 OR 4 OR
.*                IF THIS KEYWORD IS OMITTED, NO LABELS ARE EXPANDED.
.*                IF N IS 1, 2, 3, 5, OR 6, THE CORRESPONDING FORMAT
.*                OF THE DSCB WILL BE EXPANDED.  MORE THAN ONE DSCB
.*                FORMAT CAN BE MAPPED IN THE SECOND 140 BYTE DSCB
.*                AREA BY SEPARATING THE FORMAT NUMBERS BY COMMAS.
.*
.*                ADT=YES IS A KEYWORD PARAMETER WHICH SPECIFIES THAT
.*                THE LABELS FOR EACH OF THE FIELDS WITHIN THE 6 WORD
.*                DADSM AUDIT TRAIL ARE TO BE EXPANDED.
.*
.*      OUTPUT = DADSM ALLOCATE WORK AREA EXPANSION
.*
.* EXIT-NORMAL = MEND STATEMENT
.*
.* EXIT-ERROR = ERROR MESSAGES:
.*
.*      MNOTE 12,***  IHB002 INVALID EP OPERAND SPECIFIED-&EP
.*      MNOTE 12,***  IHB002 INVALID F4 OPERAND SPECIFIED-&F4
.*      MNOTE 12,***  IHB002 INVALID ADT OPERAND SPECIFIED-&ADT
.*
.*      THE MACRO EXPANSION CONTINUES AS IF THE INVALID PARAMETER
.*      WERE NOT SPECIFIED.
.*
.* EXTERNAL REFERENCES = NONE
.*
.*      ROUTINES = NONE
.*
.*      DATA AREAS = ALLOCATE WORK AREA
.*
.*      CONTROL BLOCKS = DSCB, ECB, IOB, DCB, DEB
.*
.* TABLES = NONE
.*
.* MACROS = IECDSECS DSMADT
.*          IECDSECS PREFX
.*          IECSDSL1
.*          IHBERMAC
.*
.* CHANGE ACTIVITY = SEE DELETIONS/CHANGES PRECEDING THE MACRO
.*
.**********************************************************************
.*
         LCLA  &C
&LABEL   DSECT
***********************************************************************
*
*        ALLOCATE WORK AREA
*
***********************************************************************
         SPACE 1
***********************************************************************
*        COMMON ALLOCATE PREFIX, EXTENDED PREFIX, AND WTG TABLE AREA
***********************************************************************
         SPACE 1
ALLPREFX DS    8F                       WORK AREA PREFIX
ALLOCWKA DS    20F                      EXTENDED PREFIX
FIRST    EQU   ALLOCWKA
AWTGTBL  DS    4F                       DADSM WTG TABLE
         AIF   ('&EP' EQ 'EP').ALL0010
         AIF   ('&EP' EQ '').ALL0020
         IHBERMAC 1001,EP,&EP
         AGO   .ALL0020
.ALL0010 ANOP
         ORG   ALLPREFX
         MNOTE '         IECDSECS PREFX,EXPAND=YES'
         IECDSECS PREFX,EXPAND=YES
.ALL0020 ANOP
         SPACE 1
***********************************************************************
*        COMMON ALLOCATE DSCB AREAS
***********************************************************************
         SPACE 1
ADSCBF4  DS    XL96                     COMMON ALLOCATE FORMAT 4 DSCB
*                                       AREA
         AIF   ('&F4' EQ 'F4').ALL0030
         AIF   ('&F4' EQ '').ALL0040
         IHBERMAC 1001,F4,&F4
         AGO   .ALL0040
.ALL0030 ANOP
         ORG   ADSCBF4
*        IECSDSL1 (4)
         IECSDSL1 (4)
.ALL0040 ANOP
DSCBF1   DS    XL140                    COMMON ALLOCATE FIRST 140-BYTE
*                                       DSCB AREA
F5OUT    EQU   DSCBF1+30
&C       SETA  1
.ALL0050 AIF   (&C GT N'&D1).ALL0060
         AIF   (&D1(&C) EQ 0 OR &D1(&C) EQ 4).ALL0055
         ORG   DSCBF1
         MNOTE '         IECSDSL1 (&D1(&C))'
         IECSDSL1 (&D1(&C))
.ALL0055 ANOP
&C       SETA  &C+1
         AGO   .ALL0050
.ALL0060 ANOP
DSCBF5   DS    XL140                    COMMON ALLOCATE SECOND 140-BYTE
*                                       DSCB AREA
F5IN     EQU   DSCBF5
&C       SETA  1
.ALL0070 AIF   (&C GT N'&D2).ALL0080
         AIF   (&D2(&C) EQ 0 OR &D2(&C) EQ 4).ALL0075
         ORG   DSCBF5
         MNOTE '         IECSDSL1 (&D2(&C))'
         IECSDSL1 (&D2(&C))
.ALL0075 ANOP
&C       SETA  &C+1
         AGO   .ALL0070
.ALL0080 ANOP
         SPACE 1
***********************************************************************
*        THE CONTROL BLOCK AREAS THAT FOLLOW (DEB/DCB, ECB/IOB, AND
*        THE CCW'S) ARE USED BY THE ALLOCATE ROUTINES TO READ AND WRITE
*        THE DSCB'S.
***********************************************************************
         SPACE 1
DEB      DS    12F                      DEB
DEBUCBAD EQU   DEB+32                   UCB ADDRESS                @01A
DEBPTR   DS    F                        DCB POINTER TO DEB
DCB      EQU   DEBPTR-44                DCB ADDRESS
ECB      DS    F                        ECB
IOB      DS    8F                       IOB
SEEK     DS    D                        DIRECT ACCESS ADDRESS
         SPACE 1
***********************************************************************
*        COMMON ALLOCATE CHANNEL CONTROL WORDS AREA
***********************************************************************
         SPACE 1
CCW1     DS    D
CCW2     DS    D
CCW3     DS    D
CCW4     DS    D
CCW5     DS    D
CCW6     DS    D
CCW7     DS    D
CCW8     DS    D
CCW9     DS    D
CCW10    DS    D
CCW11    DS    D
CCW12    DS    D
CCW13    DS    D
         SPACE 1
***********************************************************************
*        WORK AREA FIELDS USED BY ALLOCATE
***********************************************************************
         SPACE 1
MBBCCHHR EQU   CCW12                    DOUBLEWORD
FULLWORD DS    F                        FULLWORD
SLOWCCHH DS    F                        CCHH OF THE DIRECTORY EXTENT
         SPACE 1
***********************************************************************
*        WORK AREA FIELDS USED BY THE VTOC CONVERSION ROUTINES
***********************************************************************
         SPACE 1
         ORG   CCW13
F5FNDCT  DS    H                        NUMBER OF F5 DSCB'S FOUND
*                                       WHILE SEARCHING THE VTOC
MAXTRK   DS    H                        MAXIMUM TRACKS/CYLINDER
NEXTEXTN DS    F                        POINTER TO NEXT EXTENT
LASTF5AD DS    F                        ADDRESS OF LAST REBUILT F5
F5BLDCT  DS    H                        NUMBER OF REBUILT F5 DSCB'S
LSTF5CT  DS    H                        NUMBER OF EXTENTS IN LAST F5
         SPACE 1
***********************************************************************
*        WORK AREA FIELDS USED BY ISAM ALLOCATE
***********************************************************************
         SPACE 1
         ORG   SLOWCCHH
DIRQTY   DS    H                        EMBEDDED INDEX QUANTITY
PRIQTY   DS    H                        PRIMARY QUANTITY ON THIS VOLUME
         SPACE 1
***********************************************************************
*        COMMON ALLOCATE POINTERS AND ABSOLUTE TRACK ADDRESSES
***********************************************************************
         SPACE 1
ATIOTPTR DS    F                        POINTER TO DD ENTRY IN TIOT
ADSABLST DS    F                        POINTER TO INPUT DSAB LIST
AJFCBPTR DS    F                        JFCB POINTER
TTRLL    DS    XL5                      TTRLL FOR ALLOCATE'S F1 DSCB,
*                                       F2 DSCB CCHHR FOR ISAM ALLOCATE
CCHHR    EQU   TTRLL                    CCHHR OF F1 DSCB
VTOCADR  DS    XL5                      F4 DSCB CCHHR
DADSMADR DS    XL5                      F5 DSCB CCHHR
COUNT    DS    XL5                      CHANNEL PROGRAM WORK AREA
IDAREA   EQU   COUNT                    CHANNEL PROGRAM WORK AREA
FMTINAD  DS    XL5                      CHANNEL PROGRAM WORK AREA
SAVEADR  EQU   FMTINAD                  F1 DSCB CCHHR FOR ISAM ALLOCATE
FMTOUTAD DS    XL5                      CHANNEL PROGRAM WORK AREA,
*                                       F3 DSCB CCHHR FOR ISAM ALLOCATE
ALLCCHHR DS    XL5                      CCHHR HOLD AREA
***********************************************************************
*        ALLOCATE DASD SWITCH FLAG BYTE                            @01A
***********************************************************************
ADSFLAG  DS    XL1            ALLOCATE DASD SWITCH FLAG            @01A
ADSFLG80 EQU   X'80'          RESERVED                             @01A
ADSFLG40 EQU   X'40'          RESERVED                             @01A
ADSFLG20 EQU   X'20'          RESERVED                             @01A
ADSFLG10 EQU   X'10'          RESERVED                             @01A
ADSFLG08 EQU   X'08'          RESERVED                             @01A
ADSFLG04 EQU   X'04'          RESERVED                             @01A
ADSFLG02 EQU   X'02'          RESERVED                             @01A
ALLRACF  EQU   X'01'          1= RACF DEFINED, F1 DSCB ^ WRITTEN   @01A
*                             0= RACF DEFINED, F1 DSCB WRITTEN     @01A
         DS    XL2                      ALIGNMENT - NOT USED       @01C
MIELNAME DS    CL6                      MINOR ENQ/DEQ RESOURCE NAME
         SPACE 1
***********************************************************************
*        COMMON ALLOCATE DADSM EXTENT TABLE
***********************************************************************
         SPACE 1
DADSMTBL DS    0XL24                    DADSM EXTENT TABLE
TYPEFLG  DS    XL1                      ALLOCATION TYPE FLAG
*
SMCDONE  EQU   X'80'                    STEP-MUST-COMPLETE ENQ ISSUED
USRLBL   EQU   X'40'                    USER LABEL TRACK REQUESTED
DIRREQ   EQU   X'02'                    DIRECTORY EXTENT NEEDED
*
AENTRYNM DS    XL1                      NUMBER OF ENTRIES IN DADSMTBL
UHOLECTR DS    H                        NUMBER OF USED F0 DSCB'S
AENTRIES DS    5F                       FIVE EXTENT ENTRIES
PDLIST1  EQU   AENTRIES+4               PUSHDOWN LIST FOR ALLOCATE
PDLIST5  DS    F                        5TH WORD OF ALLOCATE'S PDLIST
         SPACE 1
***********************************************************************
*        WORK AREA FIELDS USED BY THE VTOC CONVERSION ROUTINES
***********************************************************************
         SPACE 1
PREVF5AD DS    XL5                      VIRTUAL ADDRESS OF PREVIOUS F5
*                                       DSCB
CCHHRSAV DS    XL5                      CCHHR SAVE AREA
SAVEXTNT DS    XL5                      F6 DSCB EXTENT SAVE AREA
         DS    XL15                     UNDEFINED FOR ALLOCATE OR FOR
*                                       THE VTOC CONVERSION ROUTINES
         SPACE 1
***********************************************************************
*        WORK AREA FIELDS USED BY ISAM ALLOCATE
***********************************************************************
         SPACE 1
         ORG   PDLIST5
PDLIST   DS    XL25                     PUSHDOWN LIST FOR ISAM ALLOCATE
TYPE     DS    XL1                      EXTENT TYPE
CTR      DS    XL1                      NUMBER OF VOLUMES
F2       DS    XL1                      BUILD F2 DSCB INDICATOR
HOWMUCH  DS    F                        TOTAL PRIMARY QUANTITY
ABSTR    DS    H                        ABSOLUTE TRACK ADDRESS
         SPACE 1
***********************************************************************
*        COMMON ALLOCATE INTERNAL SWITCHES
***********************************************************************
         SPACE 1
ASWITCH  DS    XL1                      ALLOCATE SWITCH
         SPACE 1
***********************************************************************
*        BIT SETTINGS USED BY ALLOCATE AND ISAM ALLOCATE
***********************************************************************
         SPACE 1
WRZERO   EQU   X'80'                    WRITE F0 DSCB
RCDALT   EQU   X'40'                    F5 DSCB HAS BEEN MODIFIED
PRCNTL   EQU   X'20'                    PROCESS CONTROL SWITCH
WRTFNL   EQU   X'10'                    WRITE FINAL F5 DSCB TO VTOC
FRSTF5   EQU   X'08'                    FIRST F5 IN WORK AREA
NEWF5    EQU   X'04'                    NEW F5 DSCB HAS BEEN BUILT
DADBYP   EQU   X'02'                    BYPASS UPDATING DADSMTBL PTR
OUTBYP   EQU   X'01'                    BYPASS UPDATING OUTPUT F5 PTR
         SPACE 1
***********************************************************************
*        BIT SETTINGS USED BY THE VTOC CONVERSION ROUTINES
***********************************************************************
         SPACE 1
EOVSW    EQU   X'80'                    LAST DSCB IN VTOC HAS BEEN READ
SPCYLSW  EQU   X'40'                    SPLIT CYLINDER EXTENT FOUND
MERGESW  EQU   X'20'                    MORE THAN 1 F5 DSCB HAS BEEN
*                                       CREATED
*
ACNVSW   DS    XL1                      ADDITIONAL SWITCHES
*
EXTENDID EQU   X'80'                    VTOC CONVERSION ENTERED FROM
*                                       EXTEND
ALLOCID  EQU   X'40'                    VTOC CONVERSION ENTERED FROM
*                                       ALLOCATE
DIRFSET  EQU   X'10'                    VTOC CONVERSION ENTERED
*                                       BECAUSE DIRF BIT WAS SET
AI7CNTRL EQU   X'08'                    IGG032I7 ENTRY CONTROL
AF5ZERO  EQU   X'04'                    F5 DSCB WITH NO EXTENT ENTRIES
*                                       EXISTS ON VTOC
         SPACE 1
***********************************************************************
*        COMMON ALLOCATE RECOVERY ROUTINE AUDIT TRAIL AREA
***********************************************************************
         SPACE 1
ALLADTRL DS    6F                       ALLOCATE AUDIT TRAIL WORDS
         AIF   ('&ADT' EQ 'YES').ALL0090
         AIF   ('&ADT' EQ 'NO').ALL0100
         IHBERMAC 1001,ADT,&ADT
         AGO   .ALL0100
.ALL0090 ANOP
         ORG   ALLADTRL
         MNOTE '         IECDSECS DSMADT,EXPAND=YES'
         IECDSECS DSMADT,EXPAND=YES
.ALL0100 ANOP
         SPACE 1
***********************************************************************
*        WORK AREA FIELDS USED FOR RACF PROCESSING BY ALLOCATE
***********************************************************************
         SPACE 1
RACDEF   EQU   IECREGSV                                            @01A
         SPACE 1
***********************************************************************
ALLWAEND EQU   *                        ALLOCATE WORK AREA END
AWALNGTH EQU   ALLWAEND-AWTGTBL         WORK AREA LENGTH FOR IECRES GET
***********************************************************************
         MEND
