         MACRO
&NAME    IECRPS &RDEB=,&WKREG1=,&EXTPR=
.*
.*       MACRO DELETIONS/CHANGES FOR VS1 RELEASE 2
.*       MACRO DELETIONS/CHANGES FOR VS2 RELEASE 1.6
.*       MACRO DELETIONS/CHANGES FOR OS  RELEASE 21.7
.*       MACRO DELETIONS/CHANGES FOR VS2 RELEASE 2
.*0000000100,000350,000850,000950-001050,001700,002100,002600,   Y02080
.*0000002900,003000,003450-003500,004300,055450-055750,055850-   Y02080
.*0000057350,057600-057650,057900,058050,058250,058400,058750-   Y02080
.*0000058850,059300,059600,059750-060100                         Y02080
.*0000003500,057400,057500,058600,058700-059550,059750           YM3048
.*0000000900,001500,002100,002600,002850-003050,003550,004300-   YM3997
.*0000004350,057600-058600,059650-060150                         YM3997
.*0000002100                                                     YM6546
.*
.**********************************************************************
.*
.* MODULE NAME = IECRPS
.*
.* DESCRIPTIVE NAME = DADSM RPS SETUP MACRO
.*
.* COPYRIGHT = NONE
.* STATUS = CHANGE LEVEL 000
.* FUNCTION = THIS MACRO OBTAINS A 128 BYTE RPS WORK AREA AND
.*            LOCATES THE ADDRESS OF IGG019EK, THE RPS SIO APPENDAGE.
.*
.* NOTES = THIS IS AN INTERNAL MACRO USED IN THE FOLLOWING DADSM
.*         FUNCTIONS:
.*
.*             ALLOCATE
.*             SCRATCH
.*             EXTEND
.*             PARTIAL RELEASE
.*
.*    DEPENDENCIES = THE NAME AND EPA OF IGG019EK MUST BE DEFINED
.*                   BY THE XCTLTABL MACRO IN EACH MODULE THAT USES
.*                   THE IECRPS MACRO.
.*
.*    RESTRICTIONS = NONE
.*
.*    REGISTER CONVENTIONS = USES REGISTERS 0, 1, AND 15
.*
.*    PATCH LABEL = NOT APPLICABLE
.*
.* MODULE TYPE = MACRO
.*
.*    PROCESSOR = ASSEMBLER
.*
.*    MODULE SIZE = 124 DECIMAL BYTES
.*
.*    ATTRIBUTES = NOT APPLICABLE
.*
.* ENTRY POINT = NOT APPLICABLE
.*
.*    PURPOSE = SEE FUNCTION
.*
.*    LINKAGE =
.*             NAME IECRPS RDEB=R1,WKREG1=R2,EXTPR=R3
.*
.*    INPUT = NAME SPECIFIED AS AN ACCEPTABLE ASSEMBLER LANGUAGE
.*            SYMBOL.
.*
.*            RDEB= IS A KEYWORD PARAMETER THAT IS A REGISTER NAME OF
.*            A REGISTER THAT CONTAINS THE ADDRESS OF THE DEB THAT IS
.*            TO BE USED FOR RPS.
.*
.*            WKREG1= IS A KEYWORD PARAMETER THAT IS A REGISTER NAME
.*            OF A REGISTER THAT MAY BE USED BY THE MACRO AS A WORK
.*            REGISTER
.*
.*            EXTPR= IS A KEYWORD PARAMETER THAT IS A REGISTER NAME
.*            OF A REGISTER THAT POINTS TO THE EXTENDED PREFIX OF THE
.*            CALLING FUNCTION'S WORK AREA, IN WHICH REGISTERS 2-14
.*            WILL BE SAVED AT OFFSET 12.
.*
.*    OUTPUT = RPS WORK AREA IS OBTAINED, RPS SIO/CE/ABE APPENDAGE
.*             ADDRESS IS LOCATED.
.*
.* EXIT-NORMAL = MEND STATEMENT
.*
.* EXIT-ERROR = NOT APPLICABLE
.*
.* EXTERNAL REFERENCES = NONE
.*
.*    ROUTINES = NONE
.*
.*    DATA AREAS = NONE
.*
.*    CONTROL BLOCKS = DEB
.*                     APPENDAGE VECTOR TABLE
.*
.* MACROS = IECRES GET AND LOAD
.*          MODESET
.*
.* CHANGE ACTIVITY = SEE DELETIONS/CHANGES PRECEDING MACRO
.*
.*
.**********************************************************************
         MNOTE '         IECRES GET,EXTPR=(&EXTPR),ID=RPSA,LV=128,'
         MNOTE '               STM=(2,14,12(&EXTPR))'
         IECRES GET,EXTPR=(&EXTPR),ID=RPSA,LV=128,               Y02080X
               STM=(2,14,12(&EXTPR))    OBTAIN WORK AREA         Y02080
         LR    &WKREG1,1                SAVE PTR TO RPS WKAREA
         L     15,28(,&RDEB)            PTR TO APPENDAGE VECTOR TABLE
         MVC   0(20,&WKREG1),0(15)      COPY AVT INTO RPS WORK AREA
         ST    15,120(,&WKREG1)         SAVE PTR TO OLD AVT IN RPS
*                                       WORK AREA
         MNOTE '         IECRES LOAD,EXTPR=(&EXTPR),MODNM=RPS19EK,'
         MNOTE '               BRANCH=NO                ISSUE LOAD'
         IECRES LOAD,EXTPR=(&EXTPR),MODNM=RPS19EK,BRANCH=NO      YM3997
         MVC   4(4,&WKREG1),WTGMODEP    STORE PTR TO SIO APPENDAGE
*                                       IN NEW AVT
*        MODESET EXTKEY=ZERO            RETURN TO KEY ZERO AFTER
*                                       ISSUING THE IECRES GET FOR
*                                       CORE IN THE DATAMGT KEY
         MODESET EXTKEY=ZERO            RETURN TO KEY ZERO
         ST    &WKREG1,28(,&RDEB)       STORE PTR TO NEW AVT IN DEB
         XC    WTGMODEP,WTGMODEP        CLEAR THE ENTRY POINT ADDRESS
         OI    DSMADTB1,DSMRPSAP        SET THE RPS APPENDAGE SWITCH
*                                       IN THE DADSM AUDIT TRAIL
         MEND
