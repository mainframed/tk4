         MACRO
&NAME    HOOK  &EID=,&TYPE=
         GBLB  &HOOKSW
         GBLB  &TEMPSW
.*A000000-999999                                                 Y01014
.*A021500,024440,C019500,C044500,C045000                         Y02014
.*A028100,044100-044900,041100,C028000,041000                    Y02014
.*C013500,024200,D015500-020500,024500,024400,024500-026000      Y02014
.* DETERMINE THE TYPE OF HOOK
         AIF   ('&EID' NE '').TYPECHK
         MNOTE 8,'AHL052  INSUFFICIENT KEYWORD PARAMETERS.'
         MEXIT
.TYPECHK ANOP
         AIF   ('&TYPE' EQ '').PMPMAP
         AIF   ('&TYPE' NE 'P' AND '&TYPE' NE 'BP' AND '&TYPE' NE      X
               'BPN' AND '&TYPE' NE 'BPN2').TMP                @G64RP2H
.PMPMAP  ANOP
.**********************************************************************
.*  THE FOLLOWING LOGIC DETERMINES WHICH MAPPING MACRO SHOULD BE CALLED
.*  WHEN A NEW MAPPING MACRO IS ADDED, THIS PORTION MUST BE UPDATED TO
.*  CALL THE NEW MACRO.
.**********************************************************************
.IEACHEK ANOP
         AIF   ('&EID'(1,3) EQ 'IEA').IEA
         AGO   .IECCHEK
.IEA     ANOP
         IEAPMP
         AGO   .BRCHK
.IECCHEK ANOP
         AIF   ('&EID'(1,3) EQ 'IEC').IEC
         AGO   .IRACHEK
.IEC     ANOP
         IECPMP
         AGO   .BRCHK
.IRACHEK ANOP
         AIF   ('&EID'(1,3) EQ 'IRA').IRA
         AGO   .ISPCHEK
.IRA     ANOP
         IRAPMP
         AGO   .BRCHK
.ISPCHEK ANOP
         AIF   ('&EID'(1,3) NE 'ISP').INVEID
         ISPPMP
         AGO   .BRCHK
.**********************************************************************
.**********************************************************************
.TMP     ANOP
         AIF   ('&TYPE' EQ 'T' OR '&TYPE' EQ 'BT' OR '&TYPE' EQ        X
               'BTN').HKSWCHK
  MNOTE 8,'AHL053  INVALID TYPE= KEYWORD.'
         MEXIT
.HKSWCHK ANOP
         AIF   (&HOOKSW).TEMPEID
         AIF   ('&NAME' EQ '').NONAME
&NAME    EQU   *
.NONAME  ANOP
         MEXIT
.**********************************************************************
.*  THE FOLLOWING DETERMINES IF THE TEMPORARY MAPPING MACRO MUST
.*  BE CALLED.
.**********************************************************************
.TEMPEID ANOP
         AIF   ('&EID'(1,3) EQ 'TMP').T10
         AGO   .INVEID
.T10     ANOP
         AIF   (&TEMPSW).BRCHK
         TMPEID
&TEMPSW  SETB  1
         AGO   .BRCHK
.INVEID  ANOP
         MNOTE 8,'AHL055  INVALID EID= KEYWORD.'
         MEXIT
.BRCHK   ANOP
.* GENERATE BRANCH OR PERMANENT HOOK
         AIF   ('&TYPE' EQ 'BP' OR '&TYPE' EQ 'BT' OR '&TYPE' EQ 'BTN' X
               OR '&TYPE' EQ 'BPN' OR '&TYPE' EQ 'BPN2').GENBR @G64RP2H
&NAME    DC    XL1'AF',AL3(&EID)   MC INSTRUCTION
         MEXIT
.GENBR   ANOP
*
* SPECIAL BRANCH HOOK GENERATED BY THE HOOK MACRO
*
         AIF   ('&TYPE' EQ 'BPN' OR '&TYPE' EQ 'BTN').CON1
         AIF   ('&TYPE' EQ 'BPN2').CONMCH                      @G64RP2H
&NAME    L     15,IEACON1          RESIDENT RTN E.P.S
         AGO   .BRANCH
.CON1    ANOP
&NAME    L     15,IEACON2          RESIDENT RTN E.P.S
         AGO   .BRANCH                                         @G64RP2H
.CONMCH  ANOP                                                  @G64RP2H
&NAME    L 15,16(0)                GET CVT PTR                 @G64RP2H
         L 15,CVTGTF(15)           GET MCHEAD PTR              @G64RP2H
         L 15,MCHMCIHC(15)         GET MCIHC  PTR              @G64RP2H
.BRANCH  ANOP
         BALR  14,15               BRANCH HOOK
         DC    XL1'47',AL3(&EID)   NOP TO CARRY EID
         MEND
