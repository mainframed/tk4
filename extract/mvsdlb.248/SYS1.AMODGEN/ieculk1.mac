         MACRO
         IECULK1 &INDXVLU,&OCHAN,&NCHAN,&LSTCHAN,&NEG,&IRSW,&PLUS7 I267
         GBLB  &IECICTB(16),&IECIC2B,&FIRST2                      M1791
         GBLA  &IECICTA(16),&CHAN,&COUNT2                         M1791
         LCLB  &ZRONDX                                           A38114
         LCLA  &COUNT,&COUNT1                                      TBYT
         AIF   (&LSTCHAN EQ 1).LSTCHK   IS THIS THE LAST ENTRY
         AIF   (&PLUS7 EQ 0).NOT7       TEST FOR + 7 CHAN          I267
&COUNT1  SETA  2*(&INDXVLU-15)          GET 16 CHAN INDEX       VS02239
         AGO   .NOT7A                   BRANCH AROUND              I267
.NOT7    ANOP                                                      I267
&COUNT1  SETA  &INDXVLU*2-8             ADJUST VALUE FOR 2 BYTE    I267
         AIF   (&IRSW EQ 1).NOTFRST                              A38114
&CHAN    SETA  0                                                 A38114
.NOTFRST ANOP                                                    A38114
&CHAN    SETA  &CHAN+1                                           A38114
         AIF   (&NEG NE 1).NORMNEG                             @ZA07000
&COUNT1  SETA  (0-8)                                           @ZA07000
         AGO   .SETUK2                                         @ZA07000
.NORMNEG ANOP                                                  @ZA07000
         AIF   (&COUNT1 GE 0).NOT7A    INDEX VALUE POSITIVE?     A38114
.SETUK2  ANOP                                                  @ZA07000
         AIF   (&IECIC2B).SECTIM       FIRST NEG?                A38114
.*  IF THE INDEX VALUE IS NEGITIVE IT MUST BE SET TO ZERO AND IECULK2
.* MUST PUT OUT AS MUCH ZERO FILL AS THE INDEX IS NEGITIVE
&IECICTA(&CHAN) SETA  (0-&COUNT1)                                A38114
&IECICTB(&CHAN) SETB  1                                          A38114
&COUNT1  SETA  0                       RESET INDEX VALUE         A38114
&COUNT2  SETA  &IECICTA(&CHAN)                                   A38114
&ZRONDX  SETB  1                                                 A38114
&IECIC2B SETB  1                                                 A38114
         AGO   .NOT7A                                            A38114
.SECTIM  ANOP                                                    A38114
&COUNT1  SETA  (0-&COUNT1)             GET POSITIVE VALUE        A38114
         AIF   (&COUNT1 NE &COUNT2).NOTEQ  ARE VAL SAME          A38114
&COUNT1  SETA  0                       YES ZERO INDEX            A38114
&ZRONDX  SETB  1                                                 A38114
         AGO   .NOT7A                                            A38114
.NOTEQ   ANOP                                                    A38114
         AIF   (&COUNT1 LT &COUNT2).NEWLESS                      A38114
&IECICTA(&CHAN) SETA  (&COUNT1-&COUNT2)                          A38114
&COUNT2  SETA  (&COUNT2+(&COUNT1-&COUNT2)) UPDATE INDX VAL       A38114
&IECICTB(&CHAN) SETB  1                                          A38114
&COUNT1  SETA  0                       USE ZERO INDEX            A38114
&ZRONDX  SETB  1                                                 A38114
         AGO   .NOT7A                                            A38114
.NEWLESS ANOP                                                    A38114
         AIF   (&COUNT1 NE 2).NOT2     IS VAL 2?                 A38114
         AIF   (&FIRST2).NOT7A          FIRST 2 ?                A38114
         AIF   (&COUNT2-&COUNT1 NE 2).NO2DIF                     A38114
.TWODIF  ANOP                                                    A38114
&COUNT1  SETA  2                                                 A38114
&ZRONDX  SETB  1                                                 A38114
         AGO   .NOT7A                                            A38114
.NO2DIF  ANOP                                                    A38114
&FIRST2  SETB  1                                                 A38114
&IECICTA(&CHAN) SETA  4                                          A38114
&IECICTB(&CHAN) SETB  1                                          A38114
         AGO   .NOT7A                                            A38114
.NOT2    ANOP                                                    A38114
         AIF   ((&COUNT2-&COUNT1 EQ 2) AND NOT &FIRST2).TWODIF   A38114
         AIF   (&COUNT2-&COUNT1 NE 2).NDXNRM                     A38114
&ZRONDX  SETB  1                                                 A38114
&COUNT1  SETA  6                                                 A38114
         AGO   .NOT7A                                            A38114
.NDXNRM  ANOP                                                    A38114
&IECICTA(&CHAN) SETA  (&COUNT2-&COUNT1)                          A38114
&IECICTB(&CHAN) SETB  1                                          A38114
.NOT7A   ANOP                                                    A38114
&COUNT   SETA  &OCHAN                   SET UP FCOUNT FOR FILLER CHK
         AIF   (&IRSW EQ 1).FILLOOP     FIRST CARD
&COUNT   SETA  &COUNT-1            FIRST TIME LOGIC
.FILLOOP AIF   (&COUNT+1 GE &NCHAN).INSRTI  CHECK FOR  CHAN GAPS
         DC    AL1(0)                   CHANNEL FILLER
&COUNT   SETA &COUNT+1                  UPDATE OLD CHANNEL COUNTER
         AGO   .FILLOOP                 RETURN TO FILL GAP CHESH
.INSRTI  AIF   (&NEG EQ 1).ZEROINS     IF INDEX IS NEG.-INSERT ZERO
         AIF   (&ZRONDX).NEGCNT                                  A38114
         DC    AL1((&COUNT1+&COUNT2)/2)                 A38114,@ZA08721
         AGO   .EXIT                                             A38114
.NEGCNT  ANOP                                                    A38114
         DC    AL1(&COUNT1/2)   YES, USE INDEX ALONE    A38114,@ZA08721
&ZRONDX  SETB  0                                                 A38114
         AGO   .EXIT                    TO EXIT FROM MACRO
.LSTCHK  ANOP                           CHANNEL FILL ROUTINE
&COUNT   SETA  &NCHAN                   SET LASS CHANNEL COUNT
.FINFIL  AIF   (&NCHAN LT 7).FINFILA    TEST FOR 16 CHANNEL        I267
         AIF   (&COUNT EQ 29).EXIT      TEST FOR LAST FILL         I267
         AGO   .FINFILB                 BRANCH TO FILL             I267
.FINFILA AIF   (&COUNT EQ 6).TBTST      TEST FOR FINAL FILL        I267
.FINFILB DC    AL1(0)                   CHANNEL FILL A             I267
&COUNT   SETA  &COUNT+1                 UPDATE FINAL CHECK COUNT
         AGO   .FINFIL                  RETURN FOR END CHECK
.TBTST   ANOP
&CHAN    SETA  0                       RESET GBL FOR ULK2        A38114
         DC    X'FF'                    TWO BYTE LOOKUP CHANNEL FILTBYT
         AGO   .EXIT                                               TBYT
.ZEROINS ANOP       ZERO FILLER FOR NEGATIVE INDEX
         DC    AL1(0)                   CHANNEL INDEX (SUBSTITUTE)
.*21960000500,002700,004230                                        TBYA
.EXIT    MEND
