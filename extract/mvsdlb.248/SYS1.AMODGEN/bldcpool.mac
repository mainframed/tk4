         MACRO
&NAME    BLDCPOOL  &CPID=,&BRANCH=YES,&SP=,&CSIZE=,&POOLSIZ=,&CPADDR=, *
               &RECOVER=,&RELATED=,&AUTODEL=NO,&CELLCNT=,&BNDRY=FWORD, *
               &SERIAL=NO                                      @ZA28730
*     OS/VS2 RELEASE 3.0 VERSION 5/27/77                       @ZA28730
         LCLA  &REG,&GMFAIL
         LCLB  &R,&G,&C,&DBL,&AUTO,&SER                        @ZA28730
         LCLC  &NMST,&CSTRNG,&BSTRNG,&ASTRNG
&NMST    SETC  'IHA'.'&SYSNDX'
&GMFAIL  SETA  4                   RETURN CODE FOR GETMAIN ERROR
         AIF   ('&BRANCH' NE 'YES').ERR1    BRANCH=YES MANDATORY
&DBL     SETB  ('&BNDRY' EQ 'DWORD')
         AIF   ('&BNDRY' EQ 'FWORD' OR '&BNDRY' EQ 'DWORD').BNDRYOK
         IHBERMAC &BNDRY,BNDRY,FWORD
.BNDRYOK AIF   ('&RECOVER' EQ '').CONT1
         AIF   ('&RECOVER' NE 'YES').ERR5   RECOVER MUST=YES, IF GIVEN
.*       RECOVER=YES SPECIFIED
         AIF   ('&CSIZE'.'&POOLSIZ'.'&CELLCNT'.'&SP' NE '').ERR9
.*       NO EXTRANEOUS PARAMETERS SPECIFIED FOR RECOVER=YES
&R       SETB  1              INDICATE RECOVERY TYPE
         AIF   ('&CPID' EQ '').ERR6         CPID MUST BE GIVEN
         AIF   ('&CPID'(1,1) NE '(').ERR2   CPID MUST BE REGISTER
&C       SETB  1              INDICATE CPID PRESENT
         AGO   .GEN1
.*       RECOVER PARAMETER NOT SPECIFIED
.CONT1   AIF   ('&CSIZE' EQ '').ERR7        CSIZE MUST BE GIVEN
         AIF   ('&POOLSIZ' NE '' AND '&CELLCNT' NE '').ERR11
.*       CELLCNT AND POOLSIZ ARE MUTUALLY EXCLUSIVE PARAMETERS
         AIF   ('&CPID' EQ '').CONT2        CPID OPTION PRESENT?
         AIF   ('&CPID'(1,1) NE '(').ERR2   CPID MUST BE REGISTER
&C       SETB  1              INDICATE CPID PRESENT
.CONT2   AIF   ('&CPADDR' NE '').CONT3
&G       SETB  1              INDICATE GETMAIN TO BE ISSUED
&REG     SETA  4              INDICATE R4 USED INSTEAD OF R0 FOR CPID
         AGO   .GEN1
.CONT3   AIF   ('&CPADDR'(1,1) NE '(').ERR2A CPADDR MUST BE REGISTER
.*       COMMON EXPANSION
.GEN1    ANOP
&NAME    STM   2,12,28(13)         SAVE CALLERS REGISTERS 2-12
         AIF   (&C EQ 0).SKIP1          SKIP IF CPID NOT GIVEN
         AIF   (&G EQ 1).SET            SKIP IF GETMAIN TO BE ISSUED
         AIF   ('&CPID(1)' EQ '0').SKIP2
.SET     LR    &REG,&CPID(1)       PRESERVE CPID VALUE AROUND GETMAIN
         AGO   .SKIP2
.SKIP1   SR    &REG,&REG           INDICATE NO CPID (DYNAMIC POOL)
.SKIP2   AIF   (&R EQ 1).GENR           SKIP IF RECOVER OPTION GIVEN
         AIF   ('&SP' EQ '').ERR8       SUBPOOL MUST BE GIVEN
.*       FILTER OUT INVALID SUBPOOL SPECIFICATIONS
         AIF   ('&SP'(1,1) EQ '(' OR T'&SP NE 'N').SPOK
.*       NO CHECK CAN BE MADE IF SP# EQUATED OR IN A REGISTER
         AIF   (&SP GT 255).ERR3        SUBPOOL MUST BE LE 255
.*       VALID SUBPOOL PARAMETER GIVEN
.SPOK    AIF   ('&POOLSIZ' EQ '').PSKIP
&CSTRNG  SETC  'POOLSIZ'
&BSTRNG  SETC  '1024'
&ASTRNG  SETC  ''
         AIF   ('&POOLSIZ'(1,1) EQ '(' OR T'&POOLSIZ NE 'N').#SKIP
.*       LIMIT TEST CANNOT BE MADE IF EQUATED OR REGISTER
         AIF   (&POOLSIZ LT 1 OR &POOLSIZ GT 255).ERR4A
         AGO   .#SKIP         SKIP OVER CELL COUNT CHECKS
.PSKIP   AIF   ('&CELLCNT' EQ '').ERR10      CELLCNT OR POOLSIZ REQ'D
&CSTRNG  SETC  'CELLCNT'
&BSTRNG  SETC  'CSIZE+4'
&ASTRNG  SETC  '32+'
.#SKIP   AIF   ('&CSIZE'(1,1) EQ '(' OR T'&CSIZE NE 'N').CSKIP
.*       LIMIT TEST CANNOT BE MADE IF EQUATED OR REGISTER
         AIF   (&CSIZE LT 8 OR &CSIZE GT 4092).ERR4
.CSKIP   AIF   ('&CELLCNT' EQ '').POOLS CELL COUNT SPECIFIED?
         AIF   ('&CSIZE'(1,1) NE '(').CNT#EQ CSIZE IN A REGISTER?
         LA    15,4(&CSIZE(1).,0)  CELL SIZE FOR MULTIPLICATION YM02296
         AGO   .MULTP
.CNT#EQ  LA    15,&CSIZE.+4(0,0)   CELL SIZE FOR MULTIPLICATION YM02296
.MULTP   LA    14,3(0,0)           SET UP MASK TO ROUND CSIZE   YM02296
         NR    14,15               REDUCE CSIZE TO REMAINDER OF
*                                  DIVISION BY 3
         IC    14,*+8(14)          SELECT PROPER ROUNDING FACTOR
         B     *+8                 BRANCH AROUND TABLE
         DC    FL1'0,3,2,1'
         AR    15,14               ADD THE ROUNDING FACTOR TO CSIZE
         AIF   (NOT &DBL).NOADJB NO ADJUSTMENT CODE IF BNDRY=FWORD
         LA    14,4(0,0)           CELLSIZE IS NOW A MULTIPLE   YM02296
         NR    14,15               OF 4, IF IT IS ALSO A MULTIPLE
         BZ    &NMST.0             OF 8, THEN ADD 4 TO FORCE
         LA    15,4(15,0)          DOUBLE WORD CELL BOUNDARIES. YM02296
&NMST.0  DS    0H
.NOADJB  SR    14,14               RESET REGISTER FOR MULTIPLY
         AIF   ('&CELLCNT'(1,1) EQ '(').CNTREG CELL COUNT IN REG?
         LA    2,&CELLCNT.(0,0)    CELL COUNT TO MULTIPLY       YM02296
         MR    14,2                COMPUTE REQUIRED POOL SIZE
         AGO   .ADDCPAB
.CNTREG  MR    14,&CELLCNT(1)      COMPUTE REQUIRED POOL SIZE
.ADDCPAB LA    2,32(15,0)          ADD THE CPAB LENGTH          YM02296
.POOLS   AIF   ('&CPADDR(1)' EQ '1' OR &G EQ 1).SKIP3
         AIF   ('&CPADDR(1)' EQ '0').ERR2A    ERROR, CPID=CPADDR=(0)
.*       ALL PARAMETERS VALID
         LR    1,&CPADDR(1)        LOAD R1 WITH CPADDR
         MNOTE *,'IT IS ASSUMED THAT &ASTRNG.(&CSTRNG.*(&BSTRNG.)) BYTES
               S ARE AVAILABLE STARTING AT CPADDR.'
.*       COMMON TO ALL EXPANSIONS EXCEPT RECOVER=YES
.SKIP3   AIF   ('&CELLCNT' NE '').SKIP3C
         AIF   ('&POOLSIZ'(1,1) EQ '(').SKIP3A POOLSIZ = REGISTER?
         LA    2,&POOLSIZ.(0,0)    LOAD POOLSIZE IN 1K BLOCKS   YM02296
         AGO   .SKIP3B
.SKIP3A  AIF   ('&POOLSIZ(1)' EQ '2').SKIP3B STORE DIRECT, POOLSIZ=(2)
         LR    2,&POOLSIZ(1)       LOAD POOL SIZE IN 1K BLOCKS
.SKIP3B  SLL   2,10(0)             TIMES 1024 FOR SIZE IN BYTES YM02296
.SKIP3C  AIF   (&G EQ 0).GENC
.*       COMMON TO ALL EXPANSIONS EXCEPT RECOVER=YES AND CPADDR=(R)
         GETMAIN RC,LV=(2),SP=&SP  GETMAIN THE POOL SEGMENT
         LTR   15,15               TEST GETMAINS RETURN CODE IN R15
         BZ    &NMST.A             GETMAIN WORKED, SKIP ERROR SEQUENCE
         LR    0,15                SAVE GETMAIN'S CODE AS REASON CODE
         LA    15,&GMFAIL.(0,0)    SET UP GETMAIN FAILURE CODE  YM02296
         B     &NMST.B             RETURN TO USER
&NMST.A  LR    0,4                 GETMAIN OK, RESTORE CPID TO REG 0
.*       COMMON TO ALL EXPANSIONS EXCEPT RECOVER=YES
.GENC    XC    0(32,1),0(1)        INITIALIZE CPAB(E)
         ST    0,0(0,1)            SAVE CPID IN CPAB(E)
         ST    2,24(0,1)           SAVE POOL SIZE IN CPAB(E)
         AIF   ('&AUTODEL' EQ 'NO').CKSER                      @ZA28730
&AUTO    SETB  1         AUTO DELETE SPECIFIED                 @ZA28730
         AIF   ('&AUTODEL' EQ 'YES').CKSER                     @ZA28730
&AUTO    SETB  0         AUTODEL PARM INVALID--DEFAULT TO NO   @ZA28730
         IHBERMAC 1015,&AUTODEL,AUTODEL,NO
.CKSER   AIF   ('&SERIAL' EQ 'NO').CKBNDRY                     @ZA28730
         AIF   ('&SERIAL' EQ 'YES').SERON                      @ZA28730
         IHBERMAC 1015,&SERIAL,SERIAL,NO                       @ZA28730
         AGO   .CKBNDRY           SERIAL=NO
.SERON   ANOP                                                  @ZA28730
&SER     SETB  1         SERIALIZATION SPECIFIED               @ZA28730
         AIF   (&AUTO).CKBNDRY    IF SERIAL, MUST HAVE AUTODEL @ZA28730
&SER     SETB  0         SERIAL PARM INVALID--DEFAULT TO NO    @ZA28730
         IHBERMAC 1016,''SERIAL=YES'',''AUTODEL=NO''           @ZA28730
.CKBNDRY AIF   (&SER+&AUTO+&DBL EQ 0).NOAUTO2     ANY OPTIONS? @ZA28730
         OI    17(1),B'0&DBL&AUTO.00&SER.00' SET OPTION FLAGS  @ZA28730
.NOAUTO2 AIF   ('&SP'(1,1) EQ '(').SPREG IS SP A REGISTER?
         MVI   16(1),&SP           SAVE SUBPOOL ID IN CPAB(E)
         AGO   .GENC1             BACK TO MAINLINE
.SPREG   STC   &SP(1),16(0,1)      SAVE SUBPOOL ID IN CPAB(E)
.GENC1   AIF   ('&CSIZE'(1,1) EQ '(').GENM IS CSIZE A REGISTER?
         LA    2,&CSIZE.(0,0)      LOAD CELL SIZE               YM02296
         ST    2,4(0,1)            SAVE CELL SIZE IN CPAB(E)
         AGO   .GENX              RETURN TO MAINLINE
.GENM    ST    &CSIZE(1),4(0,1)    SAVE CELL SIZE IN CPAB(E)
.*       COMMON TO ALL EXPANSIONS
.GENX    L     3,16(0,0)           CVT ADDRESS FOR BLDCPOOL     YM02296
         L     15,CVTBLDCP-CVTMAP(0,3)    LOAD BLDCPOOL BRANCH ENTRY
         BALR  14,15               GO TO BLDCPOOL SERVICE
         AIF   (&G EQ 0).SKIP4
&NMST.B  DS    0H                  BRANCH HERE ON A GETMAIN FAILURE
.SKIP4   LM    2,12,28(13)         RESTORE CALLERS REGISTERS
         MEXIT
.*       RECOVER=YES REQUEST SPECIAL PROCESSING
.GENR    SR    1,1                 INDICATE RECOVERY REQUEST
         AGO   .GENX
.ERR1    IHBERMAC 1006,''BRANCH=YES''
         MEXIT
.ERR2    IHBERMAC 1001,CPID,&CPID
         MEXIT
.ERR2A   IHBERMAC 1001,CPADDR,&CPADDR
         MEXIT
.ERR3    IHBERMAC 1001,SP,&SP
         MEXIT
.ERR4    IHBERMAC 1001,CSIZE,&CSIZE
         MEXIT
.ERR4A   IHBERMAC 1001,POOLSIZ,&POOLSIZ
         MEXIT
.ERR5    IHBERMAC 1001,RECOVER,&RECOVER
         MEXIT
.ERR6    IHBERMAC 1006,CPID
         MEXIT
.ERR7    IHBERMAC 1006,CSIZE
         MEXIT
.ERR8    IHBERMAC 1006,SP
         MEXIT
.ERR9    AIF   ('&POOLSIZ' EQ '').ERR9A
         IHBERMAC 1020,POOLSIZ,''RECOVER=YES''
.ERR9A   AIF   ('&SP' EQ '').ERR9B
         IHBERMAC 1020,SP,''RECOVER=YES''
.ERR9B   AIF   ('&CSIZE' EQ '').ERR9C
         IHBERMAC 1020,CSIZE,''RECOVER=YES''
.ERR9C   AIF   ('&CELLCNT' EQ '').ERR9D
         IHBERMAC 1020,CELLCNT,''RECOVER=YES''
.ERR9D   MEXIT
.ERR10   IHBERMAC 1006,POOLSIZ
         MEXIT
.ERR11   IHBERMAC 1020,CELLCNT,POOLSIZ
         MEND
