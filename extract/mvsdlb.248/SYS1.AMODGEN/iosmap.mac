         MACRO
&NAME    IOSMAP &TABLE=,&UCB=,&VAR=,&LINKR=
         GBLB   &IECMREG         FIRST TIME SWITCH
         AIF   ('&IECMREG' EQ '1').MAP001
&IECMREG SETB 1                  SET FIRST TIME SWITCH
         SPACE 2
*****************************************************************
*                                                               *
*                      IOSMAP MACRO                             *
*                                                               *
*                     OS/VS2 LEVEL=00                           *
*                                                               *
* FUNCTION:                                                     *
*       DETERMINES THE AVAILABILITY OF ALL PATHS TO A           *
*       DEVICE, WHETHER ONLINE OR OFFLINE.  THE STATUS OF       *
*       EACH CHANNEL SET AND THE CHANNEL AVAILABILITY TABLE     *
*       FOR EACH CHANNEL SET WILL BE CHECKED AS WILL THE        *
*       UCB CHANNEL MASK.                                       *
*                                                               *
* MACRO CALL:                                                   *
*    LABEL  IOSMAP  TABLE=LABEL OR (REG),UCB=(REG),             *
*                   LINKR=(REG),VAR=1                           *
*                                                               *
*         PLS - GEN REFS(CVT,CVTPTR,CVTIXAVL,IOCOM,IOCMAP)      *
*               (IOSMAP  KEYWORDS AS IN BAL);                   *
*                                                               *
* INPUT:                                                        *
*    REGISTER 13 MUST CONTAIN THE ADDRESS OF A 16 WORD          *
*    SAVEAREA.                                                  *
*                                                               *
*    TABLE=  MUST SPECIFY THE ADDRESS OF A TWO WORD AREA IN     *
*    WHICH THE MAP WILL BE BUILT. OPTIONALLY, THE ADDRESS OF    *
*    THE TABLE MAY BE PASSED IN ANY REGISTER OTHER THAN 13.     *
*    IF OMITTED, REGISTER 5 IS ASSUMED.                         *
*                                                               *
*    UCB=  MUST SPECIFY A GPR (SYMBOLIC OR ABSOLUTE), CON-      *
*    TAINING THE ADDRESS OF THE UCB COMMON SEGMENT FOR WHICH    *
*    PATH STATUS TO BE DETERMINED.  IF OMITTED, REGISTER 4      *
*    IS ASSUMED.                                                *
*                                                               *
*    LINKR=  MAY SPECIFY A GPR (OTHER THAN 13) CONTAINING       *
*    A RETURN ADDRESS FROM THE MAP FUNCTION.  IF OMITTED,       *
*    RETURN WILL BE TO THE NEXT SEQUENTIAL INSTRUCTION.         *
*                                                               *
*    VAR=  MUST BE SET TO 1.                                    *
*                                                               *
*    LABEL - OPTIONAL LABEL.                                    *
*                                                               *
* OUTPUT:                                                       *
*    THE GENERATED PATH MAP TABLE WILL REFLECT THE ONLINE       *
*    AND OFFLINE STATUS FOR EACH OF THE PATHS TO THE DEVICE     *
*    FROM EACH CHANNEL SET.  OFFLINE PATHS ARE A RESULT OF AN   *
*    EXPLICIT VARY COMMAND OR DUE TO IPL/NIP PATH               *
*    INITIALIZATION.  NON-EXISTENT PATHS (ONLY ONE PATH FROM    *
*    EACH CHANNEL SET OR AN ASYMMETRIC DEVICE) WILL SHOW        *
*    AS OFFLINE.  THE FORMAT OF THE TABLE FOLLOWS:              *
*                                                               *
*                                     CHANNEL  CHANNEL          *
*                                      SET-0    SET-1           *
*                  +0               +2      +3                  *
*                  ************************************         *
* PRIMARY PATHS    * DEVICE ADDRESS *   00   *   01   *         *
*                  +4 ************* +6 ***** +7 *******         *
* SECONDARY PATHS  * DEVICE ADDRESS *   01   *   00   *         *
*                  ************************************         *
*                                                               *
* WHERE  00 = PATH NOT AVAILABLE (OFFLINE)                      *
*        01 = PATH AVAILABLE (ONLINE)                           *
*                                                               *
*    WHEN THERE IS ONLY A PRIMARY PATH (POSSIBLY ONE FROM       *
*    EACH CHANNEL SET), THE PRIMARY AND SECONDARY DEVICE        *
*    ADDRESSES ARE THE SAME AND PATHS FROM EACH CHANNEL SET     *
*    FOR THE SECONDARY ENTRY ARE MARKED AS NOT AVAILABLE.       *
*                                                               *
* OTHER:                                                        *
*    THE EXPANSION OF THIS FUNCTION IS DEPENDENT ON THE         *
*    SPECIFICATION OF THE FOLLOWING MAPPING MACROS              *
*    IN THE CALLING CSECT ASSEMBLY:                             *
*         CVT   DSECT=YES             IECDIOCM                  *
*                                                               *
*    LOCKS ARE NOT REQUIRED BY THIS FUNCTION.                   *
*                                                               *
.* A-000000-999999                                             @G64APLK
*****************************************************************
         SPACE 2
*****************************************************************
*                                                               *
*        REGISTER DEFINITIONS                                   *
*                                                               *
*****************************************************************
         SPACE
IECIMR0  EQU   0                   REGISTER 0
IECIMWK1 EQU   2                   WORK REG
IECIMUCB EQU   4                   UCB PTR
IECIMTBL EQU   5                   PATH TABLE PTR
IECIMSAV EQU   13                  SAVE AREA PTR
IECIMRTN EQU   14                  DEFAULT RETURN REG
IECIMENT EQU   15                  ENTRY REGISTER
.MAP001  ANOP
.*
.*       CHECK FOR MISSING PARAMETERS
.*
         AIF   ('&VAR' NE '1').IOSERR
         SPACE
*****************************************************************
*                                                               *
*        ESTABLISH INTERFACE AND CALL IECVMAP                   *
*                                                               *
*****************************************************************
         SPACE
&NAME    STM   IECIMR0,IECIMENT,0(IECIMSAV) SAVE REGS
         AIF   ('&TABLE' EQ '').MAP005
         AIF   ('&TABLE'(1,1) EQ '(').MAP003
         LA    IECIMTBL,&TABLE      GET PATH TABLE PARM ADDR
         AGO   .MAP005
.MAP003  ANOP
         L     IECIMTBL,&TABLE(1).*4(,IECIMSAV) GET TABLE PTR
*                                      FROM SAVEAREA
.MAP005  AIF   ('&UCB' EQ '').MAP010
         L     IECIMUCB,&UCB(1).*4(,IECIMSAV) GET UCBPTR FROM SAVEAREA
.MAP010  ANOP
         SR    IECIMR0,IECIMR0      CLEAR REGISTER FOR VAR=1
         L     IECIMWK1,CVTPTR      GET CVT PTR
         L     IECIMWK1,CVTIXAVL-CVT(IECIMWK1) GET IOCM PTR
         L     IECIMENT,IOCMAP-IOCOM(IECIMWK1) GET IECVMAP ADDRESS
         BALR  IECIMRTN,IECIMENT    CALL IECVMAP
         LM    IECIMR0,IECIMENT,0(IECIMSAV) RESTORE REGS
         AIF   ('&LINKR' EQ '').MAP090
         BR    &LINKR(1)           RETURN TO CALLER
.MAP090  ANOP
         MEXIT
.*
.IOSERR  MNOTE 8,'*** ERROR IN PARAMETER SET UP'
         MEXIT
         MEND
