*%/*
         MACRO
&NAME    STATUS &TYPE,&CLASS,&SCOPE,&MASK,&XPLICIT,&TCB=,&ASID=,       X
               &RELATED=
.*
.*   THIS IS THE OS/VS2 STATUS MACRO
.*
         LCLB  &A(32)                   MASK VALUES (0 OR 1)
         LCLA  &B                       CODE VALUE (1-12)
         LCLB  &C                       SET/RESET VALUE (0 OR 1)
         LCLB  &D                       TCBLOC STORAGE INDICATOR
         LCLA  &E                       MASK CHARACTER SUBSCRIPT
         LCLA  &F                       MASK SUB-OPERAND SUBSCRIPT
         LCLA  &G                       MASK VALUE SUBSCRIPT
         LCLA  &H                       SCOPE CHARACTER SUBSCRIPT
         LCLB  &I                       TCBLOC REGISTER INDICATOR
         LCLC  &J                       TCBLOC REGISTER NAME
         LCLC  &K                       APOSTROPHE DELIMITER
         LCLC  &L
.*
.* TYPE OPERAND SCAN.
.*
         AIF   (T'&TYPE EQ 'O').ERROR10
         AIF   (T'&ASID EQ 'O').CONT0
         AIF   ('&TYPE' NE 'START' AND '&TYPE' NE 'STOP').CONT
         AIF   ('&TCB' NE '').ERROR8
         AIF   ('&CLASS' EQ '').ERROR8
         AIF   ('&TYPE' EQ 'STOP').ERROR8
.CONT    ANOP
         AIF   ('&CLASS' EQ 'MC').ERROR3
         AIF   ('&SCOPE' EQ 'SYSTEM').ERROR6
         AIF   ('&TYPE' EQ 'SET' AND '&SCOPE' EQ 'STEP').ERROR8
         AIF   ('&TYPE' EQ 'SET' AND '&SCOPE' EQ '').ERROR8
.CONT0   ANOP
         AIF   ('&TYPE' EQ 'START' OR '&TYPE' EQ 'STOP').SSCK   20021
&C       SETB  ('&TYPE' EQ 'RESET')
         AIF   (NOT &C AND '&TYPE' NE 'SET').ERROR1
         AIF   (T'&TCB NE 'O').ERROR8
.CONT1   ANOP
.*
.* CLASS OPERAND SCAN.
.*
         AIF   (T'&CLASS EQ 'O').ERROR11
         AIF   ('&CLASS' EQ 'MC').SCOPEMC
         AIF   ('&CLASS' EQ 'SD').SCOPESD
         AIF   ('&CLASS' EQ 'ND').SCOPEND
         AGO   .ERROR2
.*
.* SCOPE OPERAND SCAN FOR CLASS=MC.
.*
.SCOPEMC AIF   (T'&MASK NE 'O' OR T'&XPLICIT NE 'O').ERROR3
         AIF   (T'&SCOPE EQ 'O').CONT2
         AIF   ('&SCOPE' EQ 'STEP').CONT2
         AGO   .ERROR6
.CONT2   ANOP
&B       SETA  1
         AGO   .CONT9
.*
.* SCOPE OPERAND SCAN FOR CLASS=SD.
.*
.SCOPESD AIF   (T'&SCOPE EQ 'O').CONT35
         AIF   ('&SCOPE' EQ 'STEP').CONT35
         AIF   ('&SCOPE' EQ 'SYSTEM').CONT36
&H       SETA  K'&SCOPE
         AIF   ('&SCOPE'(1,1) EQ '''').CONT3
         AIF   ('&SCOPE'(1,1) EQ '(').CONT31
         AIF   (K'&SCOPE GT 8).ERROR6
&D       SETB  1
         AGO   .CONT32
.CONT3   ANOP
&K       SETC  ''''
         AGO   .ERROR6
.CONT31  ANOP
         AIF   (K'&SCOPE GT 10).ERROR6
         AIF   (K'&SCOPE LT 3).ERROR6
&H       SETA  &H-2
&J       SETC  '&SCOPE'(2,&H)
&I       SETB  1
.CONT32  AIF   (T'&XPLICIT EQ 'O').CONT33
         AIF   ('&XPLICIT' NE 'E').ERROR13
&B       SETA  11
         AGO   .CONT38
.CONT33  ANOP
&B       SETA  10
         AGO   .CONT38
.CONT35  ANOP
&B       SETA  8
         AGO   .CONT37
.CONT36  ANOP
&B       SETA  9
.CONT37  AIF   (T'&XPLICIT NE 'O').ERROR3
.*
.* MASK OPERAND SCAN FOR CLASS=SD.
.*
.CONT38  ANOP
         AIF   (T'&MASK EQ 'O').CONT9
         AIF   ('&MASK'(1,1) NE '(').ERROR9
         AIF   (K'&MASK GT 88).ERROR9
&E       SETA  K'&MASK
         AIF   ('&MASK'(&E,1) NE ')').ERROR9
         AIF   (K'&MASK LT 3).ERROR9
         AIF   (N'&MASK GT 32).ERROR9
.CONT39  ANOP
         AIF   (&F EQ N'&MASK).CONT9
&F       SETA  &F+1
         AIF   ('&MASK(&F)' LT '1').ERROR9
         AIF   ('&MASK(&F)' GT '32').ERROR9
&G       SETA  &MASK(&F)
         AIF   (&A(&G)).ERROR9
&A(&G)   SETB  1
         AGO   .CONT39
.*
.* SCOPE OPERAND SCAN FOR CLASS=ND.
.*
.SCOPEND AIF   (T'&SCOPE EQ 'O').CONT5
         AIF   ('&SCOPE' EQ 'STEP').CONT5
         AIF   ('&SCOPE' EQ 'SYSTEM').CONT6
&H       SETA  K'&SCOPE
         AIF   ('&SCOPE'(1,1) EQ '''').CONT4
         AIF   ('&SCOPE'(1,1) EQ '(').CONT41
         AIF   (K'&SCOPE GT 8).ERROR6
&D       SETB  1
         AGO   .CONT42
.CONT4   ANOP
&K       SETC  ''''
         AGO   .ERROR6
.CONT41  ANOP
         AIF   (K'&SCOPE GT 10).ERROR6
         AIF   (K'&SCOPE LT 3).ERROR6
&H       SETA  &H-2
&J       SETC  '&SCOPE'(2,&H)
&I       SETB  1
.CONT42  ANOP
         AIF   (T'&XPLICIT EQ 'O').CONT43
         AIF   ('&XPLICIT' NE 'E').ERROR13
&B       SETA  12
         AGO   .CONT7
.CONT43  ANOP
&B       SETA  5
         AGO   .CONT7
.CONT5   ANOP
&B       SETA  3
         AGO   .CONT65
.CONT6   ANOP
&B       SETA  4
.CONT65  AIF   (T'&XPLICIT NE 'O').ERROR3
.*
.* MASK OPERAND SCAN FOR CLASS=ND.
.*
.CONT7   ANOP
         AIF   (T'&MASK EQ 'O').CONT9
         AIF   ('&MASK'(1,1) NE '(').ERROR9
         AIF   (K'&MASK GT 40).ERROR9
&E       SETA  K'&MASK
         AIF   ('&MASK'(&E,1) NE ')').ERROR9
         AIF   (K'&MASK LT 3).ERROR9
         AIF   (N'&MASK GT 16).ERROR9
.CONT8   ANOP
         AIF   (&F EQ N'&MASK).CONT9
&F       SETA  &F+1
         AIF   ('&MASK(&F)' LT '1').ERROR9
         AIF   ('&MASK(&F)' GT '16').ERROR9
&G       SETA  &MASK(&F)
         AIF   (&A(&G)).ERROR9
&A(&G)   SETB  1
         AGO   .CONT8
.*
.* CODE GENERATION SEQUENCE.
.*
.CONT9   ANOP
         CNOP  0,4
         AIF   ('&CLASS' NE 'SD').CONT95
&NAME    B     *+16              .BRANCH AROUND CONSTANT
         DC    BL2'&A(1)&A(2)&A(3)&A(4)&A(5)&A(6)&A(7)&A(8)&A(9)&A(10)&X
               A(11)&A(12)&A(13)&A(14)&A(15)&A(16)'     .SECONDARY
         DC    BL2'&A(17)&A(18)&A(19)&A(20)&A(21)&A(22)&A(23)&A(24)&A(2X
               5)&A(26)&A(27)&A(28)&A(29)&A(30)&A(31)&A(32)'   .MASK
         DC    A(&B)              .ACTION CODE
         DC    BL1'&C.0000000'    .SET/RESET INDICATOR
         DC    AL3(0)             .CONSTANT OF ZERO
         AIF   (NOT &D AND NOT &I).CONT92
         LM    15,0,*-12          .LOAD MASK AND CODE
         AIF   (&D).CONT101
         AGO   .CONT111
.CONT92  ANOP
         LM    15,1,*-12          .LOAD MASK, CODE AND SET/RESET
         AGO   .CONT12
.CONT95  ANOP
&NAME    B     *+12               .BRANCH AROUND CONSTANT
         DC    BL2'&A(1)&A(2)&A(3)&A(4)&A(5)&A(6)&A(7)&A(8)&A(9)&A(10)&X
               A(11)&A(12)&A(13)&A(14)&A(15)&A(16)'    .PRIMARY MASK
         DC    AL2(&B)            .ACTION CODE
         DC    BL1'&C.0000000'    .SET/RESET INDICATOR
         DC    AL3(0)             .CONSTANT OF ZERO
         AIF   (&D).CONT10
         AIF   (&I).CONT11
         LM    0,1,*-8            .LOAD MASK, CODE AND SET/RESET
         AGO   .CONT12
.CONT10  ANOP
         L     0,*-8              .LOAD MASK AND CODE
.CONT101 ANOP
         L     1,&SCOPE           .LOAD TCB ADDRESS
         O     1,*-12             .'OR IN' SET/RESET INDICATOR
         AGO   .CONT12
.CONT11  ANOP
         L     0,*-8              .LOAD MASK AND CODE
.CONT111 ANOP
         LR    1,&J               .LOAD TCB ADDRESS
         O     1,*-10             .'OR IN' SET/RESET INDICATOR
         AGO   .CONT12
.*
.*  STOP/START CODE GENERATION
.*
.SSCK     ANOP                                                    20021
         AIF   ('&CLASS' NE '' AND '&TCB' NE '').ERROR12
         AIF   ('&CLASS' EQ 'SRB').SRB
         AIF   ('&CLASS' EQ 'SYNCH').SYNCH
         AIF   ('&CLASS' NE '' OR '&SCOPE' NE '').ERROR12
         AIF   ('&MASK' NE '' OR T'&XPLICIT NE 'O').ERROR12
         AIF   ('&TYPE' EQ 'STOP').STPCK                          20021
.STRTCK  ANOP
&NAME    LA    0,7(0,0)           .LOAD START CODE               YM1995
         AIF   ('&TCB' EQ '').ALTSK                               20021
         AIF   ('&TCB'(1,1) EQ '(').TCBREG                        20021
         L     1,&TCB(1)          .LOAD TCB ADDRESS
         AGO   .CONT12                                            20021
.TCBREG  LR    1,&TCB(1)          .LOAD TCB ADDRESS
         AGO   .CONT12                                            20021
.STPCK   ANOP
&NAME    LA    0,6(0,0)           .LOAD STOP CODE                YM1995
         AIF   ('&TCB' EQ '').ALTSK                               20021
         AIF   ('&TCB'(1,1) EQ '(').TCBREG                        20021
         L     1,&TCB(1)          .LOAD TCB ADDRESS
         AGO   .CONT12                                            20021
.SRB     LA    0,13(0,0)                                         YM1995
.ONOFF   AIF   ('&TYPE' EQ 'STOP').SRB1
&L       SETC  'C'
.SRB1    L&L.R 1,0
         AGO   .CONT12
.SYNCH   ANOP
         AIF   ('&TYPE' NE 'STOP').ERROR12
         LA    0,14(0,0)  .        LOAD STOP STOP SYNCH CODE.    YM1995
         AGO   .CONT124
.ASID    LA    0,14(0,0)                                         YM1995
         AGO   .ONOFF
.ALTSK   SR    1,1                .INDICATE ALL TASK CHANGE
.CONT12  AIF   ('&ASID' EQ '').CONT125
         AIF   ('&CLASS' NE 'ND').CONT122
         AIF   ('&ASID'(1,1) EQ '(').CONT121
         LH    15,&ASID
         AGO   .CONT124
.CONT121 LH    15,0(&ASID(1).,0)                                 YM1995
         AGO   .CONT124
.CONT122 AIF   ('&ASID'(1,1) EQ '(').CONT123
         ICM   0,12,&ASID
         AGO   .CONT124
.CONT123 ICM   0,12,0(&ASID(1))
.CONT124 ANOP
         SVC   79                 .ISSUE STATUS SVC
         MEXIT
.CONT125 AIF   ('&CLASS' NE 'ND').CONT124
         SR    15,15
         AGO   .CONT124
.*
.* ERROR EXITS.
.*
.*             12,***  IHB002 INVALID TYPE OPERAND SPECIFIED-&TYPE
.ERROR1  IHBERMAC  1001,TYPE,&TYPE
         MEXIT
.*             12,***  IHB002 INVALID CLASS OPERAND SPECIFIED-&CLASS
.ERROR2  IHBERMAC  1001,CLASS,&CLASS
         MEXIT
.*             12,***  IHB243 INVALID OPERAND FOR MC/ND/SD OPTION
.ERROR3  IHBERMAC  1007,&CLASS
         MEXIT
.*             12,***  IHB002 INVALID SCOPE OPERAND SPECIFIED-&SCOPE
.ERROR6  IHBERMAC  1001,SCOPE,&K.&SCOPE.&K
         MEXIT
.*             12,***  IHB244 INVALID KEYWORD SET/RESET OPERAND
.ERROR8  IHBERMAC  1008,&TYPE,2
         MEXIT
.*             12,***  IHB002 INVALID MASK OPERAND SPECIFIED-&MASK
.ERROR9  IHBERMAC  1001,MASK,&MASK
         MEXIT
.*             12,***  IHB001 TYPE OPERAND REQUIRED, NOT SPECIFIED.
.ERROR10 IHBERMAC  1006,TYPE
         MEXIT
.*             12,***  IHB001 CLASS OPERAND REQUIRED, NOT SPECIFIED.
.ERROR11 IHBERMAC  1006,CLASS
         MEXIT
.*             12,***  IHB244 INVALID POSITIONAL START/STOP OPERAND.
.ERROR12 IHBERMAC  1008,&TYPE,1
         MEXIT
.*        12,***  IHB002 INVALID EXPLICIT OPERAND SPECIFIED-&XPLICIT
.ERROR13 IHBERMAC  1001,EXPLICIT,&XPLICIT
         MEND
**/
* STATUS:
*/********************************************************************/
*/* PL/S VERSION OF STATUS MACRO.                                    */
*/********************************************************************/
*  MACRO
*    KEYS(TCB,                     /* TCB ADDRESS                    */
*         SRB,                     /* DISPATCHABILITY OF QUIESCEABLE
*                                     SRB                            */
*         SYNCH,                   /* SYNCHRONOUS STOP               */
*         ASID,                    /* REMOVE/RESTORE AN ASCB FROM/TO
*                                     THE READY ASCB QUEUE           */
*         RTCD,                    /* RETURN CODE FROM SVC IS TO BE
*                                     SAVED                          */
*         RELATED);
*        ANS('?'××MACLABEL××'STATUSP '××MACLIST××MACKEYS××';');
*%END;
