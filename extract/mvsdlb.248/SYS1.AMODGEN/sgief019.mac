         MACRO
         SGIEF019   &NM
.*C013000                                                      @YM08650
.*                                                             @YM04048
.*A000000-999999                                                 Y02670
         COPY    SGIEF020
         LCLA  &I,&J,&K
         LCLC  &A
         ACTR 100000                                           @YM04048
.**********************************************************************
.*
.*  (THIS MACRO DOES NO GENERATION, STORES GBL DATA ONLY)
.*
.*  PROCESSES VAM ASSOCIATIONS OF NAMES.  PARAM IS A NAME WHICH USER
.*  HAS SPECIFIED AS VAM-ELIGIBLE.  A SEARCH FOR THE NAME IS MADE IN
.*  'NAME' AND 'ENAME' GBL VARS.  IF NAME IS NOT FOUND IN EITHER
.*  VARIABLE, NO PROCESSING IS DONE.  IF NAME FOUND IN NAME, TYPE IS
.*  GENERIC. IF NAME IS FOUND IN ENAME , NAME IS ESOTERIC.  IN EITHER
.*  CASE, THE VAM INDICATOR IS SET IF THE NAME IS  ASSOCIATED
.*  CORRECTLY WITH A DA DEVICE TYPE.  IF NOT, AN APPROPRIATE MESSAGE
.*  IS GENERATED AND THE VAM SPECIFICATION IS IGNORED.
.**********************************************************************
&A       SETC  '&NM'
.*
.*  SEARCH FOR NM IN NAME, THEN ENAME,  THEN GNAME.
.*
&I       SETA  0
.A101    ANOP
&I       SETA  &I+1
         AIF   (&I  GT 188).B100                               @YM08650
         AIF   ('&NAME(&I)' NE '&A').A101                 NOT THIS ONE
.*   CHECK FOR VALIDITY, ISSUING MSG IF NOT DA TYPE.
&K       SETA  &HLFLIST(&I)/X'2000'
         AIF   (1 EQ &K-&K/2*2).D100
 MNOTE 4,'UNITNAME &A WAS SPECIFIED AS VAM ELIGIBLE. THE SPECIFICATION X
               IS IGNORED BECAUSE &A IS NON-DIRECT ACCESS GENERIC '
   MEXIT
         AGO   .D100                    *  FOUND IN NAME  *
.B100    ANOP
&I       SETA  0
.B101    ANOP
&I       SETA  &I+1
         AIF   ('&ENAME(&I)' EQ '').C100                END OF 'ENAME'
         AIF  ('&ENAME(&I)' NE '&A').B101               NOT THIS ONE
&I       SETA  &I+256                   *  FOUND  IN  ENAME   *
.*  FIND HIGHEST PRECEDENCE GENERIC ELIGIBLE TO NM
&J       SETA  0
.Q100    ANOP
&J       SETA  &J+1
         AIF   (&GPFXT(&J) GT 256).Q900
&K       SETA  0
.Q200    ANOP
&K       SETA  &K+1
         AIF   (&VY(&K) EQ 0).Q100
         AIF   (&VY(&K) NE &I).Q200
         AIF   (&GGXREF(&VX(&K)) NE &GPFXT(&J)).Q200
.*  FIRST GENERIC PERTINENT TO THIS ESOTERIC FOUND, ALLOW VAM ONLY IF
.*  THE TYPE IS DIRECT ACCESS.
&K       SETA  &HLFLIST(&GPFXT(&J))/X'2000'
         AIF   (1 EQ &K-&K/2*2).D100
         AGO   .Q100
.Q900    ANOP
  MNOTE 4,'NAME &A WAS SPECIFIED AS VAM ELIGIBLE. THE SPECIFICATION IS I
               IGNORED BECAUSE &A IS NOT ELIGIBLE TO ANY IBM SUPPORTED X
               DIRECT ACCESS GENERIC TYPE.  '
   MEXIT
         AGO   .D100
.C100    ANOP
   MEXIT
.* GENERATED NAMES NOT CURRENTLY PERMITTED VAM ELEGIBILITY. REMOVE
.*  ABOVE MEXIT IF AND WHEN SUPPORT IS EXTENDED.
&I       SETA  0
.C101    ANOP
&I       SETA  &I+1
         AIF   ('&GNAME(&I)' EQ '').E100                END OF 'GNAME'
         AIF   ('&GNAME(&I)' NE '&A').C101              NOT THIS ONE
&I       SETA  &I+1024                  *  FOUND  IN  GNAME  *
.D100    ANOP
.*
.*   NAME WAS FOUND IN ONE OF ARRAYS AND ITS CODE IS NOW IN I.
.*
.*              FIND  CODE IN GPFXT, SET CORRESPONDING SW IN VAM.
.*
.*
&J       SETA  0
.D101    ANOP
&J       SETA  &J+1
         AIF   (&GPFXT(&J) NE &I).D101
&VAM(&J) SETB  1
.E100    MEXIT
         MEND
