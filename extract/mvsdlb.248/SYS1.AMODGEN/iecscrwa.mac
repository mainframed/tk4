         MACRO
&LABEL   IECSCRWA &EP,&F4,&D1=(0),&D2=(0),&ADT=NO
.*
.*       MACRO DELETIONS/CHANGES FOR VS2 RELEASE 038
.*
.* $01=OZ65137,EDM1102,B820313,STLMDB: FIX FOR ENQ LOCKOUT PROB    @01A
.*
.*       MACRO DELETIONS/CHANGES FOR VS2 RELEASE 2
.*0000                                                           YM1337
.*0000                                                           YM7036
.*       MACRO DELETIONS/CHANGES FOR VS2 RELEASE 3             @ZM30032
.*0000                                                         @Y30LSTG
.*       MACRO DELETIONS/ADDITIONS/CHANGES FOR VS2 RELEASE 3.7 @ZA31933
.*0000021500-022500                                    DELETED @ZA31933
.*0000018560-018620,021500-021700,175944-175996        ADDED   @ZA31933
.*0000009000,028500                                    CHANGED @ZA31933
.*
.*       SU32 ADDITIONS                                        @G32DSPD
.*
.*       SU60 DELETIONS/CHANGES                                @G60ASBJ
.*A 182700                                                     @G60ASBJ
.*C 182700                                                     @ZA43917
.*                                                             @G60ASBJ
.**********************************************************************
.*
.* MODULE NAME = IECSCRWA (VS2-2)
.*
.* DESCRIPTIVE NAME = DADSM SCRATCH WORK AREA ADDRESSING MACRO
.*
.* COPYRIGHT = NONE
.*
.* STATUS = CHANGE LEVEL 000
.*
.* FUNCTION = DADSM SCRATCH MACRO WHICH PROVIDES ADDRESSABILITY
.*            TO THE 896 DECIMAL BYTE WORK AREA (EXCLUDING THE @ZA31933
.*            8 WORD PREFIX OR THE 6 WORD RECOVERY ROUTINE PARAMETER
.*            LIST) USED BY SCRATCH.
.*
.* NOTES = THIS MACRO IS AN INTERNAL MACRO USED IN THE FOLLOWING
.*         DADSM FUNCTIONS:
.*
.*             SCRATCH
.*
.*         TO REFERENCE THIS WORK AREA WITHIN THE FUNCTIONS LISTED
.*         ABOVE, CODE A USING STATEMENT ON THE LABEL 'SCRTHWKA'.
.*
.*      DEPENDENCIES = THIS MACRO IS DESIGNED TO BE USED OPTIONALLY
.*                     WITH THE IECSDSL1 DSCB MAPPING MACRO, THE
.*                     PREFX MACRO (DEFINED IN THE IECDSECS MACRO)
.*                     WHICH DEFINES THE 8 WORD WORK AREA PREFIX,
.*                     THE 20 WORD EXTENDED WORK AREA PREFIX, AND
.*                     THE 4 WORD DADSM WTG TABLE, AND THE DSMADT
.*                     MACRO (ALSO DEFINED IN THE IECDSECS MACRO)
.*                     WHICH DEFINES THE 6 WORD DADSM AUDIT TRAIL.
.*                     THE AUDIT TRAIL MUST APPEAR AS THE      @ZA31933
.*                     FINAL 6 WORDS OF THE SCRATCH WORK AREA. @ZA31933
.*
.*                     THE FIRST 364 BYTES OF THE SCRATCH AND RENAME
.*                     WORK AREAS (ADDRESSED RESPECTIVELY BY MAPPING
.*                     MACROS IECSCRWA AND IECRENWA) ARE MAPPED
.*                     IDENTICALLY - 8 WORD PREFIX, 20 WORD EXTENDED
.*                     PREFIX, 4 WORD DADSM WTG TABLE, 96 BYTE DSCB
.*                     AREA, AND 140 BYTE DSCB AREA.  THE 250  @ZA31933
.*                     BYTES FROM LABELS 'WKADEB' THROUGH      @ZA31933
.*                     'PVOLSER' ARE ALSO MAPPED IDENTICALLY.  @ZA31933
.*
.*      RESTRICTIONS = NONE
.*
.*      REGISTER CONVENTIONS = NOT APPLICABLE
.*
.* PATCH LABEL = NOT APPLICABLE
.*
.* MODULE TYPE = MACRO
.*
.*      PROCESSOR = ASSEMBLER LANGUAGE
.*
.*      MODULE SIZE = 920 DECIMAL BYTES (EXCLUDING THE PREFIX) @ZA31933
.*
.*      ATTRIBUTES = NOT APPLICABLE
.*
.* ENTRY POINT = SCRPREFX
.*
.*      PURPOSE = THIS MACRO IS CALLED AT ASSEMBLY TIME BY THE
.*                FUNCTIONS LISTED UNDER 'NOTES' TO EXPAND THE
.*                SCRATCH WORK AREA.
.*
.*      LINKAGE =
.*
.*                LABEL IECSCRWA EP,F4,D1=(N),D2=(N),ADT=YES
.*
.*      INPUT =
.*
.*                OPTIONAL LABEL SPECIFIED AS AN ACCEPTABLE ASSEMBLER
.*                LABEL WHICH CAN BE USED BY THE CALLING CSECT TO
.*                ESTABLISH ADDRESSABILITY TO THE ENTIRE WORK AREA.
.*
.*                EP IS THE FIRST POSITIONAL PARAMETER, SPECIFYING
.*                THAT THE LABELS FOR EACH OF THE FIELDS IN THE 8
.*                WORD PREFIX, THE 20 WORD EXTENDED PREFIX, AND THE 4
.*                WORD DADSM WTG TABLE ARE TO BE EXPANDED.  IF THESE
.*                FIELDS ARE NOT TO BE DEFINED, A COMMA MUST BE CODED
.*                TO INDICATE THE ABSENCE OF THIS PARAMETER.
.*
.*                F4 IS THE SECOND POSITIONAL PARAMETER, SPECIFYING
.*                THAT THE FIELDS OF THE FORMAT 4 DSCB ARE TO BE
.*                EXPANDED USING THE IECSDSL1 DSCB MAPPING MACRO.
.*                IF THESE FIELDS ARE NOT TO BE DEFINED AND A
.*                KEYWORD PARAMETER FOLLOWS, A COMMA MUST BE
.*                CODED TO INDICATE THE ABSENCE OF THIS PARAMETER.
.*
.*                D1=(N) IS A KEYWORD PARAMETER THAT SPECIFIES THE
.*                FORMAT(S) OF THE DSCB(S) TO BE EXPANDED IN THE
.*                FIRST 140 BYTE DSCB AREA, USING THE IECSDSL1 MACRO.
.*                N CAN BE A NUMBER FROM 0 - 6.  IF N IS 0 OR 4 OR
.*                IF THIS KEYWORD IS OMITTED, NO LABELS ARE EXPANDED.
.*                IF N IS 1, 2, 3, 5, OR 6, THE CORRESPONDING FORMAT
.*                OF THE DSCB WILL BE EXPANDED.  MORE THAN ONE DSCB
.*                FORMAT CAN BE MAPPED IN THE FIRST 140 BYTE DSCB
.*                AREA BY SEPARATING THE FORMAT NUMBERS BY COMMAS.
.*
.*                D2=(N) IS A KEYWORD PARAMETER THAT SPECIFIES
.*                THE FORMAT(S) OF THE DSCB(S) TO BE EXPANDED IN THE
.*                SECOND 140 BYTE DSCB AREA, USING THE IECSDSL1 MACRO.
.*                N CAN BE A NUMBER FROM 0 - 6.  IF N IS 0 OR 4 OR
.*                IF THIS KEYWORD IS OMITTED, NO LABELS ARE EXPANDED.
.*                IF N IS 1, 2, 3, 5, OR 6, THE CORRESPONDING FORMAT
.*                OF THE DSCB WILL BE EXPANDED.  MORE THAN ONE DSCB
.*                FORMAT CAN BE MAPPED IN THE SECOND 140 BYTE DSCB
.*                AREA BY SEPARATING THE FORMAT NUMBERS BY COMMAS.
.*
.*                ADT=YES IS A KEYWORD PARAMETER WHICH SPECIFIES THAT
.*                THE LABELS FOR EACH OF THE FIELDS WITHIN THE 6 WORD
.*                DADSM AUDIT TRAIL ARE TO BE EXPANDED.
.*
.*      OUTPUT = DADSM SCRATCH WORK AREA EXPANSION
.*
.* EXIT-NORMAL = MEND STATEMENT
.*
.* EXIT-ERROR = ERROR MESSAGES:
.*
.*      MNOTE 12,***  IHB002 INVALID EP OPERAND SPECIFIED-&EP
.*      MNOTE 12,***  IHB002 INVALID F4 OPERAND SPECIFIED-&F4
.*      MNOTE 12,***  IHB002 INVALID ADT OPERAND SPECIFIED-&ADT
.*
.*      THE MACRO EXPANSION CONTINUES AS IF THE INVALID PARAMETER
.*      WERE NOT SPECIFIED.
.*
.* EXTERNAL REFERENCES = NONE
.*
.*      ROUTINES = NONE
.*
.*      DATA AREAS = SCRATCH WORK AREA
.*
.*      CONTROL BLOCKS = DSCB, ECB, IOB, DCB, DEB
.*
.* TABLES = NONE
.*
.* MACROS = IECDSECS DSMADT
.*          IECDSECS PREFX
.*          IECSDSL1
.*          IHBERMAC
.*
.* CHANGE ACTIVITY = SEE DELETIONS/CHANGES PRECEDING THE MACRO PROLOGUE
.*       EXTENDED MVS CVOL SUPPORT                             @Z40CSRC
.*
.**********************************************************************
.*
         LCLA  &C
&LABEL   DSECT
***********************************************************************
*
*        SCRATCH WORK AREA
*
***********************************************************************
         SPACE 1
***********************************************************************
*        PREFIX, EXTENDED PREFIX, AND WTG TABLE AREA
***********************************************************************
         SPACE 1
SCRPREFX DS    8F                       WORK AREA PREFIX
SCRTHWKA DS    20F                      EXTENDED PREFIX
FIRST    EQU   SCRTHWKA
RACDEF   EQU   SCRTHWKA                 RACDEF PARM LIST           @01A
SWTGTBL  DS    4F                       DADSM WTG TABLE
         AIF   ('&EP' EQ 'EP').SCR0010
         AIF   ('&EP' EQ '').SCR0020
         IHBERMAC 1001,EP,&EP
         AGO   .SCR0020
.SCR0010 ANOP
         ORG   SCRPREFX
         MNOTE '         IECDSECS PREFX,EXPAND=YES'
         IECDSECS PREFX,EXPAND=YES
.SCR0020 ANOP
         SPACE 1
***********************************************************************
*        FORMAT 4 DSCB AREA
***********************************************************************
         SPACE 1
VTOCDSCB DS    XL96                     FORMAT 4 DSCB
         AIF   ('&F4' EQ 'F4').SCR0030
         AIF   ('&F4' EQ '').SCR0040
         IHBERMAC 1001,F4,&F4
         AGO   .SCR0040
.SCR0030 ANOP
         ORG   VTOCDSCB
         MNOTE '         IECSDSL1 (4)'
         IECSDSL1 (4)
.SCR0040 ANOP
         SPACE 1
***********************************************************************
*        FIRST 140-BYTE DSCB AREA
***********************************************************************
         SPACE 1
INDSCB   DS    XL140                    140-BYTE DSCB AREA
&C       SETA  1
.SCR0050 AIF   (&C GT N'&D1).SCR0060
         AIF   (&D1(&C) EQ 0 OR &D1(&C) EQ 4).SCR0055
         ORG   INDSCB
         MNOTE '         IECSDSL1 (&D1(&C))'
         IECSDSL1 (&D1(&C))
.SCR0055 ANOP
&C       SETA  &C+1
         AGO   .SCR0050
.SCR0060 ANOP
         SPACE 1
***********************************************************************
*        SECOND 140-BYTE DSCB AREA
***********************************************************************
         SPACE 1
OUTDSCB  DS    XL140                    140-BYTE DSCB AREA
ENQAREA  EQU   OUTDSCB+4                ENQ PARAMETER LIST
MJNAME   EQU   ENQAREA+16               MAJOR ELEMENT NAME
MINAME   EQU   ENQAREA+24               MINOR ELEMENT NAME
&C       SETA  1
.SCR0070 AIF   (&C GT N'&D2).SCR0080
         AIF   (&D2(&C) EQ 0 OR &D2(&C) EQ 4).SCR0075
         ORG   OUTDSCB
         MNOTE '         IECSDSL1 (&D2(&C))'
         IECSDSL1 (&D2(&C))
.SCR0075 ANOP
&C       SETA  &C+1
         AGO   .SCR0070
.SCR0080 ANOP
         SPACE 1
***********************************************************************
*        THE CONTROL BLOCK AREAS THAT FOLLOW (DEB/DCB, ECB/IOB, AND
*        THE CCW'S) ARE USED BY THE SCRATCH ROUTINES TO READ AND WRITE
*        THE DSCB'S.
***********************************************************************
         SPACE 1
WKADEB   DS    F                        DEB
DCB      DS    11F                      DCB
DEBPTR   DS    F                        DCB POINTER TO DEB
ECB      DS    F                        ECB
IOB      DS    8F                       IOB
SEEK     DS    D                        DIRECT ACCESS ADDRESS
         SPACE 1
***********************************************************************
*        SCRATCH CHANNEL CONTROL WORDS AREA
***********************************************************************
         SPACE 1
CCW1     DS    D
CCW2     DS    D
CCW3     DS    D
CCW4     DS    D
CCW5     DS    D
CCW6     DS    D
CCW7     DS    D
CCW8     DS    D
CCW9     DS    D
CCW10    DS    D
CCW11    DS    D
CCW12    DS    D
         SPACE 1
***********************************************************************
*        SCRATCH WORK AREA FIELDS
***********************************************************************
         SPACE 1
FIELD1   DS    D                        PASSWORD WORK AREA FIELD
OLDPLPTR DS    F                        PARAMETER LIST ADDRESS
PRUCBPTR DS    F                        PRIMARY (DEMOUNTABLE) UCB ADDR
VOLPTR   DS    F                        NEXT VOLUME LIST ENTRY ADDRESS
TCBADDR  DS    F                        POINTER TO THE TCB
DSABADDR DS    F                        DSAB ADDRESS OF CURRENT VOLUME
PRDSABAD DS    F                        DSAB ADDRESS OF PRIMARY UCB
VOLNUM   DS    H                        NUMBER OF VOLUMES REMAINING
         CNOP  2,4                      ALIGN DSABQDB TO FULLWORD
TIOTMNR  DS    CL6                      ASID/DSAB QDB ADDRESS
DSABQDB  EQU   TIOTMNR+2                DSAB QDB ADDRESS
VOLMINOR DS    CL6                      VOLSER FOR DEQ ON SYSZVOLS
ERCODE   DS    H                        ERROR RETURN CODE
SISW1    DS    XL1                      SECURITY/INTEGRITY SWITCHES
*
TIOTENQ  EQU   X'80'                    ENQ ON SYSTIOT ISSUED
DSNENQ   EQU   X'40'                    ENQ ON SYSDSN ISSUED
VOLENQ   EQU   X'20'                    ENQ ON SYSZVOLS ISSUED
VOLDEQ   EQU   X'10'                    DEQ ON SYSZVOLS TO BE ISSUED
UCBALLOC EQU   X'08'                    UCB DYNAMICALLY ALLOCATED
DEBSRCH  EQU   X'04'                    DEB SEARCH REQUIRED INDICATOR
NOSMFRCD EQU   X'02'                    NO SMF RECORD INDICATOR
VOLRACHK EQU   X'01'                    VOL RACHECKED OK       @G32DSPD
*
SISW2    DS    XL1                      SECURITY/INTEGRITY SWITCHES
*
DEMNTSW  EQU   X'80'                    VOLUME DEMOUNT INDICATOR
MOUNTSW  EQU   X'40'                    VOLUME MOUNT/VERIFY INDICATOR
VERIFYSW EQU   X'20'                    VOLUME VERIFY INDICATOR
UCBFNDSW EQU   X'10'                    ALLOCATED UCB FOUND INDICATOR
VIRTMNT  EQU   X'08'                    VIRTUAL MOUNT ISSUED   @Y30LSTG
DSNENQCV EQU   X'04'                    ENQ ON SYSCTLG.V------ @Z40CSRC
DORACDEF EQU   X'02'                    DO A DACDEF TYPE=DELETE    @01A
*
         DS    H                        UNUSED
PVOLSER  DS    CL6                      VOLUME SERIAL NUMBER IN
*                                       PROTECTED STORAGE
PDSNAME  DS    CL44                     DATA SET NAME IN PROTECTED
*                                       STORAGE
         DS    H                        UNUSED HALFWORD
         SPACE 1
***********************************************************************
*        DADSM EXTENT TABLE
***********************************************************************
         SPACE 1
DADSMTBL DS    0XL68                    DADSM EXTENT TABLE
STYPEFLG DS    XL1                      SCRATCH SWITCHES
*
ERXTENT  EQU   X'80'                    OVERLAPPING EXTENTS FOUND
*                                       IN THE FORMAT 5 DSCB
VTOCENQ  EQU   X'40'                    RESERVE ON SYSVTOC ISSUED
VIOBIT   EQU   X'20'                    VIO DATA SET INDICATOR
NOMNTDEV EQU   X'10'                    NO VOLUME MOUNTED AND
*                                       NO DEVICE ALLOCATED
SHRCYL   EQU   X'08'                    SHARED CYLINDERS (SPLIT
*                                       CYLINDER EXTENT)
PSWDCHKD EQU   X'04'                    PASSWORD CHECKED FOR
*                                       PASSWORD-PROTECTED DATA SET
DOSPACK  EQU   X'02'                    DOS VOLUME INDICATOR
EXITBIT  EQU   X'01'                    EXIT INDICATOR (LAST VOLUME
*                                       LIST ENTRY PROCESSED)
*
EXTNUM   DS    XL1                      NUMBER OF EXTENT ENTRIES
*                                       IN DADSMTBL
HOLENUM  DS    H                        NUMBER OF F0 DSCB'S THAT HAVE
*                                       BEEN USED
ENTRIES  DS    16F                      SIXTEEN EXTENT ENTRIES
         SPACE 1
***********************************************************************
*        ABSOLUTE TRACK ADDRESSES
***********************************************************************
         SPACE 1
VTOCADR  DS    XL5                      FORMAT 4 CCHHR
DADSMADR DS    XL5                      FORMAT 5 CCHHR
INCCHHR  DS    XL5                      CHANNEL PROGRAM WORK AREA
OUTCCHHR DS    XL5                      CHANNEL PROGRAM WORK AREA
         CNOP  0,4                      ALIGN HOLD TO FULLWORD
HOLD     DS    XL5                      CHANNEL PROGRAM WORK AREA
NEXTDADS DS    XL5                      CHANNEL PROGRAM WORK AREA
INLINESK DS    CL6                      SEEK ADDRESS
         SPACE 1
***************************************************************@Y30LSTG
*        VIRTUAL DEVICE POINTER TO RELINQUISH WORK FIELD       @Y30LSTG
***************************************************************@Y30LSTG
         SPACE 1                                               @Y30LSTG
RELQADR  DS   1F                        PTR TO ICBACREL LIST   @Y30LSTG
         SPACE 1                                               @Y30LSTG
***************************************************************@ZA31933
*        PROTECTED COPIES OF PARAMETERS PASSED BY CALLER       @ZA31933
*        AND POINTER TO PROTECTED COPY OF VOLUME LIST          @ZA31933
***************************************************************@ZA31933
         SPACE 1                                               @ZA31933
OPTIONS  DS    XL4                      SCRATCH OPTION CODE    @ZA31933
PDSNADDR DS    F                        DATA SET NAME ADDRESS  @ZA31933
VOLISTAD DS    F                        CALLER'S VOLUME LIST   @ZA31933
*                                       ADDRESS                @ZA31933
PVOLNUM  DS    H                        VOLUME COUNT           @ZA31933
VOL1ADDR DS    F                        ADDRESS OF FIRST       @ZA31933
*                                       VOLUME ID IN VOLUME    @ZA31933
*                                       LIST WORK AREA         @ZA31933
SCRSAVE  DS    F                        REGISTER 14 SAVE AREA  @ZA43917
         SPACE 1                                               @ZA31933
***********************************************************************
*        SCRATCH RECOVERY ROUTINE AUDIT TRAIL AREA
***********************************************************************
         SPACE 1
SCRADTRL DS    6F                       SCRATCH AUDIT TRAIL WORDS
         AIF   ('&ADT' EQ 'YES').SCR0090
         AIF   ('&ADT' EQ 'NO').SCR0100
         IHBERMAC 1001,ADT,&ADT
         AGO   .SCR0100
.SCR0090 ANOP
         ORG   SCRADTRL
         MNOTE '         IECDSECS DSMADT,EXPAND=YES'
         IECDSECS DSMADT,EXPAND=YES
.SCR0100 ANOP
         SPACE 1
***********************************************************************
SCRWAEND EQU   *                        SCRATCH WORK AREA END
SWALNGTH EQU   SCRWAEND-SWTGTBL         WORK AREA LENGTH FOR IECRES GET
***********************************************************************
         MEND
