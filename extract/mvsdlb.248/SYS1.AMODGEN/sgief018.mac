         MACRO
         SGIEF018  &TYP=
.*A000000-999999                                                 Y02670
         COPY  SGIEF020
         GBLA  &CTR(2),&GG
         LCLA  &I
.**********************************************************************
.*
.*     (THIS MACRO DOES NO GENERATION, STORES GBL DATA ONLY)
.*
.*  STORES GROUP/NAME ASSOCIATIONS.  FIRST POSITIONAL PARAMETER
.*  IS A GROUP ID, SECOND IS NAME ID. TYP PARAMETER INDICATES
.*  TYPE OF NAME.
.*
.*  TYP=D IS A SPECIAL CASE WHICH STORES DEVICE INFORMATION.
.*  POSITIONAL PARAMS ARE GROUP ID, GENERIC ID, UCB INDEX, DEVICE
.*  ADDRESS.  THESE PARAMS ARE STORED IN STAGE 2 GBL VARS
.*  DGR,DGE,DUL,DAD.
.*
.*  FOR ALL OTHER TYPES,GROUPID IS STORED IN VX, A CODE IS
.*  GENERATED FROM THE NAME ID AND TYP PARAM AND STORED IN VY. ON TYP=G
.*  A CROSS REFERENCE ENTRY IS STORED IN GGXREF
.**********************************************************************
.X       AIF   ('&TYP' NE 'E').A
&CTR(1)  SETA  &CTR(1)+1                          *                   *
&VX(&CTR(1)) SETA &SYSLIST(1)      GROUP ID       *  STORES           *
&VY(&CTR(1)) SETA &SYSLIST(2)      NAME ID        *    GROUP/ESOTERIC *
&VY(&CTR(1)) SETA &VY(&CTR(1))+256                *      INCIDENCES   *
         MEXIT
.A       AIF   ('&TYP' NE 'D').B
&CTR(2)  SETA  &CTR(2)+1                                *  STORES     *
&I       SETA  &CTR(2)                                   *            *
&DGR(&I) SETA  &SYSLIST(1)    GROUP ID OF THIS DEVICE    *   DEVICE   *
&DGE(&I) SETA  &SYSLIST(2)    GENERIC ID OF THIS DEVICE  *            *
&DUL(&I) SETA  &SYSLIST(3)    UCB-LOOKUP FOR THIS DEVICE * INFORMATION*
&DAD(&I) SETA  &SYSLIST(4)    ADDRESS OF THIS DEVICE     *            *
         MEXIT
.B       AIF   ('&TYP' NE 'G').C
&I       SETA  &CTR(1)+1                     *  STORES GROUP/GENERIC *
&CTR(1)  SETA &I                             *                        *
&VX(&I)  SETA  &SYSLIST(1)                   *     INCIDENCES         *
&VY(&I)  SETA  &SYSLIST(2)                   *                        *
&GGXREF(&VX(&I))  SETA  &VY(&I)              *                        *
         MEXIT
.*
.*
.*
.C       AIF   ('&TYP' NE 'X').D
&CTR(1)  SETA  &CTR(1)+1                     *  STORE GROUP/GENERIC
&VX(&CTR(1))  SETA  &SYSLIST(1)              *  INCIDENCES FOR GENERICS
&VY(&CTR(1))  SETA  512+&SYSLIST(2)          *  THAT ALLOW SUBSTITUTES.
.*
.*
.*
.D       AIF   ('&TYP' NE 'GG').E
&CTR(1)  SETA  &CTR(1)+1
&VX(&CTR(1))  SETA  &SYSLIST(1)
&VY(&CTR(1))  SETA  1024+&GG
.E       MEXIT
         MEND
