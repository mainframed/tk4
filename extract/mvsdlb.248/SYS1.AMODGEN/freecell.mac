         MACRO
&NAME    FREECELL   &CPID=,&CELL=,&BRANCH=YES,&SAVE=YES,&RELATED=
*        OS/VS2 RELEASE 2   ---  9/15/73
         AIF   ('&BRANCH' NE 'YES').ERR1  BRANCH=YES MANDATORY
         AIF   ('&CPID' EQ '' ).ERR3        CPID REQUIRED
         AIF   ('&CELL' EQ '').ERR3A        CELL REQUIRED
         AIF   ('&CPID'(1,1) NE '(').ERR2   CPID MUST BE A REGISTER
         AIF   ('&CELL'(1,1) NE '(').ERR2B  CELL MUST BE A REGISTER
         AIF   ('&CELL(1)' EQ '0').ERR2B   REG. ZERO RESERVED FOR CPID
         AIF   ('&SAVE' EQ 'YES' OR '&SAVE' EQ 'NO').SAVOK
         IHBERMAC 1015,&SAVE,''SAVE='',YES    INCORRECT 'SAVE=' VALUE
.*       SYNTAX OK, CONTINUE WITH EXPANSION OF MACRO
.SAVOK   AIF   ('&NAME' EQ '').SKPN
&NAME    DS    0H                  FREECELL MACRO
.SKPN    AIF   ('&SAVE' EQ 'NO').NRGS
         STM   2,12,28(13)         SAVE CALLERS REGISTERS 2-12
.NRGS    AIF   ('&CPID(1)' EQ '0').NZLD
         LR    0,&CPID(1)          LOAD R0 WITH CPID
.NZLD    AIF   ('&CELL(1)' EQ '1').NOLD
         LR    1,&CELL(1)          LOAD R1 WITH CELL ADDRESS
.NOLD    L     3,16(0,0)           CVT ADDRESS FOR FREECELL     YM02296
         L     15,CVTFRECL-CVTMAP(0,3)    LOAD FREECELL RT ENTRY POINT
         BALR  14,15               GO TO FREECELL ROUTINE
         AIF   ('&SAVE' EQ 'NO').NRGR
         LM    2,12,28(13)         RESTORE CALLERS REGISTERS
.NRGR    MEXIT
.ERR1    IHBERMAC  1006,''BRANCH=YES''
         MEXIT
.ERR2    IHBERMAC  1001,CPID,&CPID
         MEXIT
.ERR2B   IHBERMAC  1001,CELL,&CELL
         MEXIT
.ERR3    IHBERMAC  1006,CPID
         MEXIT
.ERR3A   IHBERMAC  1006,CELL
         MEND
