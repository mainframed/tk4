         MACRO
&LABEL   IECEXTWA &EP,&D1=(0),&D2=(0),&ADT=NO
.*
.*       MACRO DELETIONS/CHANGES FOR VS2 RELEASE 038
.*
.* $01=OZ65137,EDM1102,T820309,STLMDB: FIX FOR ENQ LOCKOUT PROB    @01A
.*
.**********************************************************************
.*
.* MODULE NAME = IECEXTWA (VS2-2)
.*
.* DESCRIPTIVE NAME = DADSM EXTEND WORK AREA ADDRESSING MACRO
.*
.* COPYRIGHT = NONE
.*
.* STATUS = CHANGE LEVEL 000
.*
.* FUNCTION = DADSM EXTEND MACRO WHICH PROVIDES ADDRESSABILITY
.*            TO THE 488 DECIMAL BYTE WORK AREA (NOT INCLUDING THE
.*            8 WORD PREFIX OR THE 6 WORD RECOVERY ROUTINE PARAMETER
.*            LIST) USED BY EXTEND.
.*
.* NOTES = THIS MACRO IS AN INTERNAL MACRO USED IN THE FOLLOWING
.*         DADSM FUNCTIONS:
.*
.*             EXTEND
.*
.*         TO REFERENCE THIS WORK AREA WITHIN THE FUNCTIONS LISTED
.*         ABOVE, CODE A USING STATEMENT ON THE LABEL 'EXTNDWKA'.
.*
.*      DEPENDENCIES = THIS MACRO IS DESIGNED TO BE USED OPTIONALLY
.*                     WITH THE IECSDSL1 DSCB MAPPING MACRO, THE
.*                     PREFX MACRO (DEFINED IN THE IECDSECS MACRO)
.*                     WHICH DEFINES THE 8 WORD WORK AREA PREFIX,
.*                     THE 20 WORD EXTENDED WORK AREA PREFIX, AND
.*                     THE 4 WORD DADSM WTG TABLE, AND THE DSMADT
.*                     MACRO (ALSO DEFINED IN THE IECDSECS MACRO)
.*                     WHICH DEFINES THE 6 WORD DADSM AUDIT TRAIL.
.*
.*      RESTRICTIONS = NONE
.*
.*      REGISTER CONVENTIONS = NOT APPLICABLE
.*
.* PATCH LABEL = NOT APPLICABLE
.*
.* MODULE TYPE = MACRO
.*
.*      PROCESSOR = ASSEMBLER LANGUAGE
.*
.*      MODULE SIZE = 488 DECIMAL BYTES (NOT INCLUDING THE PREFIX)
.*
.*      ATTRIBUTES = NOT APPLICABLE
.*
.* ENTRY POINT = EXTPREFX
.*
.*      PURPOSE = THIS MACRO IS CALLED AT ASSEMBLY TIME BY THE
.*                FUNCTIONS LISTED UNDER 'NOTES' TO EXPAND THE
.*                EXTEND WORK AREA.
.*
.*      LINKAGE =
.*
.*                LABEL IECEXTWA EP,D1=(N),D2=(N),ADT=YES
.*
.*      INPUT =
.*
.*                OPTIONAL LABEL SPECIFIED AS AN ACCEPTABLE ASSEMBLER
.*                LABEL WHICH CAN BE USED BY THE CALLING CSECT TO
.*                ESTABLISH ADDRESSABILITY TO THE ENTIRE WORK AREA.
.*
.*                EP IS THE FIRST POSITIONAL PARAMETER, SPECIFYING
.*                THAT THE LABELS FOR EACH OF THE FIELDS IN THE 8
.*                WORD PREFIX, 20 WORD EXTENDED PREFIX, AND THE 4
.*                WORD DADSM WTG TABLE ARE TO BE EXPANDED. IF THESE
.*                FIELDS ARE NOT TO BE DEFINED, A COMMA MUST BE
.*                CODED TO INDICATE THE ABSENCE OF THIS PARAMETER.
.*
.*                D1=(N) IS A KEYWORD PARAMETER THAT SPECIFIES THE
.*                FORMAT(S) OF THE DSCB(S) TO BE EXPANDED IN THE
.*                FIRST 140 BYTE DSCB AREA, USING THE IECSDSL1 MACRO.
.*                N CAN BE A NUMBER FROM 0 - 6.  IF N IS 0 OR 4 OR
.*                IF THIS KEYWORD IS OMITTED, NO LABELS ARE EXPANDED.
.*                IF N IS 1, 2, 3, 5, OR 6, THE CORRESPONDING FORMAT
.*                OF THE DSCB WILL BE EXPANDED.  MORE THAN ONE DSCB
.*                FORMAT CAN BE MAPPED IN THE FIRST 140 BYTE DSCB
.*                AREA BY SEPARATING THE FORMAT NUMBERS BY COMMAS.
.*
.*                D2=(N) IS A KEYWORD PARAMETER THAT SPECIFIES
.*                THE FORMAT(S) OF THE DSCB(S) TO BE EXPANDED IN THE
.*                SECOND 140 BYTE DSCB AREA, USING THE IECSDSL1 MACRO.
.*                N CAN BE A NUMBER FROM 0 - 6.  IF N IS 0 OR 4 OR
.*                IF THIS KEYWORD IS OMITTED, NO LABELS ARE EXPANDED.
.*                IF N IS 1, 2, 3, 5, OR 6, THE CORRESPONDING FORMAT
.*                OF THE DSCB WILL BE EXPANDED.  MORE THAN ONE DSCB
.*                FORMAT CAN BE MAPPED IN THE SECOND 140 BYTE DSCB
.*                AREA BY SEPARATING THE FORMAT NUMBERS BY COMMAS.
.*
.*                ADT=YES IS A KEYWORD PARAMETER WHICH SPECIFIES THAT
.*                THE LABELS FOR EACH OF THE FIELDS WITHIN THE 6 WORD
.*                DADSM AUDIT TRAIL ARE TO BE EXPANDED.
.*
.*      OUTPUT = DADSM EXTEND WORK AREA EXPANSION
.*
.* EXIT-NORMAL = MEND STATEMENT
.*
.* EXIT-ERROR = ERROR MESSAGES:
.*
.*      MNOTE 12,***  IHB002 INVALID EP OPERAND SPECIFIED-&EP
.*      MNOTE 12,***  IHB002 INVALID ADT OPERAND SPECIFIED-&ADT
.*
.*      THE MACRO EXPANSION CONTINUES AS IF THE INVALID PARAMETER
.*      WERE NOT SPECIFIED.
.*
.* EXTERNAL REFERENCES = NONE
.*
.*      ROUTINES = NONE
.*
.*      DATA AREAS = EXTEND WORK AREA
.*
.*      CONTROL BLOCKS = DSCB
.*
.* TABLES = NONE
.*
.* MACROS = IECDSECS DSMADT
.*          IECDSECS PREFX
.*          IECSDSL1
.*          IHBERMAC
.*
.* CHANGE ACTIVITY = SEE DELETIONS/CHANGES PRECEDING THE MACRO
.*
.**********************************************************************
.*
         LCLA  &C
&LABEL   DSECT
***********************************************************************
*
*        EXTEND WORK AREA
*
***********************************************************************
         SPACE 1
***********************************************************************
*        PREFIX, EXTENDED PREFIX, AND WTG TABLE AREA
***********************************************************************
         SPACE 1
EXTPREFX DS    8F                       WORK AREA PREFIX
EXTNDWKA DS    20F                      EXTENDED PREFIX
EWTGTBL  DS    4F                       DADSM WTG TABLE
         AIF   ('&EP' EQ 'EP').EXT0010
         AIF   ('&EP' EQ '').EXT0020
         IHBERMAC 1001,EP,&EP
         AGO   .EXT0020
.EXT0010 ANOP
         ORG   EXTPREFX
         MNOTE '         IECDSECS PREFX,EXPAND=YES'
         IECDSECS PREFX,EXPAND=YES
.EXT0020 ANOP
         SPACE 1
RACDEF   EQU   IECREGSV                 RACDEF PARM LIST AREA      @01A
***********************************************************************
*        FIRST 140-BYTE DSCB AREA
***********************************************************************
         SPACE 1
F5DSCB   DS    XL140                    140-BYTE DSCB AREA
&C       SETA  1
.EXT0050 AIF   (&C GT N'&D1).EXT0060
         AIF   (&D1(&C) EQ 0 OR &D1(&C) EQ 4).EXT0055
         ORG   F5DSCB
         MNOTE '         IECSDSL1 (&D1(&C))'
         IECSDSL1 (&D1(&C))
.EXT0055 ANOP
&C       SETA  &C+1
         AGO   .EXT0050
.EXT0060 ANOP
         SPACE 1
***********************************************************************
*        SECOND 140-BYTE DSCB AREA
***********************************************************************
         SPACE 1
F3DSCB   DS    XL140                    140-BYTE DSCB AREA
&C       SETA  1
.EXT0070 AIF   (&C GT N'&D2).EXT0080
         AIF   (&D2(&C) EQ 0 OR &D2(&C) EQ 4).EXT0075
         ORG   F3DSCB
         MNOTE '         IECSDSL1 (&D2(&C))'
         IECSDSL1 (&D2(&C))
.EXT0075 ANOP
&C       SETA  &C+1
         AGO   .EXT0070
.EXT0080 ANOP
         SPACE 1
***********************************************************************
*        DADSM EXTENT TABLE
***********************************************************************
         SPACE 1
EDADSMTB DS    0XL24                    DADSM EXTENT TABLE
ETYPEFLG DS    XL1                      SECONDARY ALLOCATION TYPE FLAG
*
RSMC     EQU   X'80'                    STEP-MUST-COMPLETE ISSUED
*
ENTRYNUM DS    XL1                      NUMBER OF ENTRIES IN DADSMTBL
EHOLECTR DS    H                        NUMBER OF USED F0 DSCB'S
ENTRIES  DS    5F                       FIVE EXTENT ENTRIES, EACH
*                                       CONSISTING OF CCHH
         SPACE 1
***********************************************************************
*        PUSHDOWN EXTENT LIST
***********************************************************************
         SPACE 1
EPDLIST  DS    0XL20                    FIVE WORD PUSHDOWN LIST
EPDLIST1 DS    F
EPDLIST2 DS    F
EPDLIST3 DS    F
EPDLIST4 DS    F
EPDLIST5 DS    F
         SPACE 1
***********************************************************************
*        ABSOLUTE TRACK ADDRESSES
***********************************************************************
         SPACE 1
EVTOCADR DS    XL5                      CCHHR OF F4 DSCB ON CURRENT
*                                       VOLUME
EDADSMAD DS    XL5                      CCHHR OF F5 DSCB ON CURRENT
*                                       VOLUME
ECOUNT   DS    XL5                      CHANNEL PROGRAM WORK AREA
COUNT2   DS    XL5                      CHANNEL PROGRAM WORK AREA
ETTRLL   DS    XL5                      TTRLL FOR F1 DSCB OF DATA
*                                       SET BEING EXTENDED
EIDAREA  DS    XL5                      CHANNEL PROGRAM WORK AREA
ENEXTADR DS    XL5                      CHANNEL PROGRAM WORK AREA
EXTCCHHR DS    XL5                      CCHHR HOLD AREA
         SPACE 1
***********************************************************************
*        INTERNAL EXTEND SWITCHS
***********************************************************************
         SPACE 1
ESWITCH  DS    XL1                      EXTEND F5 DSCB MERGING SWITCH
*
ESWBIT0  EQU   X'80'                    WRITE F0 DSCB
ESWBIT1  EQU   X'40'                    F5 DSCB MODIFIED
ESWBIT2  EQU   X'20'                    PROCESS CONTROL SWITCH
ESWBIT3  EQU   X'10'                    WRITE FINAL F5 DSCB
ESWBIT4  EQU   X'08'                    FIRST F5 DSCB IN WORK AREA
ESWBIT5  EQU   X'04'                    NEW F5 DSCB HAS BEEN BUILT
ESWBIT6  EQU   X'02'                    BYPASS UPDATING DADSMTBL PTR
ESWBIT7  EQU   X'01'                    BYPASS UPDATING F5 DSCB PTR
         SPACE 1
ESWITCH2 DS    XL1                      EXTEND SWITCHES CONTINUED
*
EF5ZERO  EQU   X'01'                    F5 DSCB WITH NO EXTENTS
*                                       EXISTS ON VTOC
*
***********************************************************************
*        EXTEND DASD SWITCH FLAG BYTE                              @01A
***********************************************************************
EDSFLAG  DS    XL1            FLAG BYTE FOR BASIC                  @01A
*                             DEVICE SUPPORT                       @01A
EDSFLG80 EQU   X'80'          RESERVED                             @01A
EDSFLG40 EQU   X'40'          RESERVED                             @01A
EDSFLG20 EQU   X'20'          RESERVED                             @01A
EDSFLG10 EQU   X'10'          RESERVED                             @01A
EDSFLG08 EQU   X'08'          RESERVED                             @01A
EDSFLG04 EQU   X'04'          RESERVED                             @01A
EDSFLG02 EQU   X'02'          RESERVED                             @01A
EXTRACF  EQU   X'01'          1= RACF DEFINED, F1 DSCB ^ WRITTEN   @01A
*                             0= RACF DEFINED, F1 DSCB WRITTEN     @01A
         SPACE 1
         DS    XL1                      ALIGNMENT - NOT USED       @01C
         SPACE 1
***********************************************************************
*        EXTEND RECOVERY ROUTINE AUDIT TRAIL AREA
***********************************************************************
         SPACE 1
EXTADTRL DS    6F                       EXTEND AUDIT TRAIL WORDS
         AIF   ('&ADT' EQ 'YES').EXT0090
         AIF   ('&ADT' EQ 'NO').EXT0100
         IHBERMAC 1001,ADT,&ADT
         AGO   .EXT0100
.EXT0090 ANOP
         ORG   EXTADTRL
         MNOTE '         IECDSECS DSMADT,EXPAND=YES'
         IECDSECS DSMADT,EXPAND=YES
.EXT0100 ANOP
         SPACE 1
***********************************************************************
EXTWAEND EQU   *                        EXTEND WORK AREA END
EWALNGTH EQU   EXTWAEND-EWTGTBL         WORK AREA LENGTH FOR IECRES GET
***********************************************************************
         MEND
