         MACRO
         IEECRDCM    &DEV=,&A=,&KEY=,&DSECT=
         SPACE 1                                               @G64UPSS
***********************************************************************
*                                                              @G64UPSS
*          IEECRDCM - RESIDENT DISPLAY CONTROL MODULE          @G64UPSS
*                                                              @G64UPSS
* STATUS     - OS/VS2 SYSTEM CONTROL PROGRAM 2                 @G64UPSS
*              06/15/78   LEVEL=05                             @G64UPSS
*                                                              @G64UPSS
* INVOCATION - SPECIFY: IEECRDCM DEV=,A=,KEY=,DSECT=           @G64UPSS
*              WHERE:                                          @G64UPSS
*                DEV=   A - 3066 DEVICE                        @G64UPSS
*                       B - 2250 DEVICE                        @G64UPSS
*                       C - 2260 DEVICE                        @G64UPSS
*                       D - 3277 MOD 2 TYPE DEVICE (3158 3036) @G64UPSS
*                       E - 3277 MOD 1 DEVICE                  @G64UPSS
*                A=     NUMBER OF DISPLAY AREAS AND THE        @G64UPSS
*                       LENGTHS OF EACH.                       @G64UPSS
*                       EXAMPLE: A=(6,4) WHERE TWO AREAS ARE   @G64UPSS
*                       BEING DEFINED. THE FIRST CONSISTS OF   @G64UPSS
*                       6 LINES; THE SECOND OF 4 LINES.        @G64UPSS
*                KEY=   FULL NAME OF THE PFK AREA FOR THIS     @G64UPSS
*                       DEVICE. EXAMPLE: "IEECVK10"            @G64UPSS
*                DSECT= Y ONLY ONE SACB WILL BE GENERATED.     @G64UPSS
*                       ENTRY STATEMENTS WILL NOT BE           @G64UPSS
*                       GENERATED. DO NOT SPECIFY DSECT= IF    @G64UPSS
*                       DSECT=Y IS NOT WANTED.                 @G64UPSS
*                                                              @G64UPSS
* FUNCTION   - THIS MACRO IS INVOKED AT SYSGEN TO BUILD AN     @G64UPSS
*              RDCM WITH SACBS IN THE NUCLEUS. THE MAPPING     @G64UPSS
*              MACRO FOR THIS CONTROL BLOCK IS IEERDCM.        @G64UPSS
*                                                              @G64UPSS
* CHANGE-ACTIVITY -                                            @G64UPSS
*              G64DPSS                                         @G64DPSS
*              G64UPSS                                         @G64UPSS
*                                                              @G64UPSS
***********************************************************************
         SPACE 2                                               @G64UPSS
         LCLA        &N,&S,&E
         LCLC  &TABLE,&C,&D,&F,&G,&H                           @G64DPSS
         AIF   ('&DSECT' EQ '').NODEC
&S       SETA   1
&E       SETA   1
&D       SETC   '1'
         AGO   .POS
.NODEC   ANOP
         AIF   ('&A'  EQ  '').NEG
&S       SETA    N'&A
&E       SETA  1
&D       SETC  '&A(&E)'
         AGO   .POS
.NEG     ANOP
&S       SETA  0
.POS     ANOP
&N       SETA  1
&F       SETC  '0'
&TABLE   SETC  'ABCDEFGHIJK'
         AIF   (&S  EQ  0).NUN
&C       SETC  '&TABLE'(&N,1)           COMPUTE AREA ID
&F       SETC  '&TABLE'(&S,1)
.NUN     ANOP
DCMADTRN DC    F'0'                POINTER TO PAGEABLE DCM
         DC    X'00'               RESERVED
DCMRFLGS DC    X'00'               FLAGS
DCMDOM   EQU   X'10'               DOM MUST BE TRIED
DCMNIPP  EQU   X'08'               CONSOLE WAS USED BY NIP     @G64UPSS
DCMLEN   DC    H'0'                LENGTH OF TRANS DCM
DCMADKP  DC    A(0)                ADDRESS OF ROUTED K COMMAND
*                                       PARAMETER LIST
         AIF   ('&F' EQ '0').GGG
DCMTOPAR DC    C'&F'               TOP DISPLAY AREA DEFINED
         AGO   .HHH
.GGG     ANOP
DCMTOPAR DC    X'00'
.HHH     ANOP
DCMTOPDS DC    X'00'               TOP DISPLAY ON SCREEN
         DC    X'00'               RESERVED
DCMDEVTY DC    X'00'               DEVICE TYPE FLAGS
DCMTY60  EQU   X'80'               USABLE FOR SD              MC X01958
DCMTY50  EQU   X'40'               UNUSABLE FOR SD            MC X01958
         AIF   (&S EQ 0).NONE      IF NO SDS AREAS REQUESTED
DCMADSDS DC    A(DCMACB)           POINTER TO FIRST SDS SUPPORT AREA
         AGO   .RMS
.NONE    ANOP
DCMADSDS DC    F'0'                POINTER TO FIRST SDS SUPPORT AREA
.RMS     ANOP
         AIF   ('&DEV' EQ '').EEE
&H       SETC  '03'                RMS CCW INDEX IN IEECSUB    @G64DPSS
         AIF   ('&DEV' NE 'A').AAA
&G       SETC  '3066'
&H       SETC  '04'                RMS CCW INDEX FOR 3066      @G64DPSS
         AGO   .XXX
.AAA     ANOP
         AIF   ('&DEV' NE 'B').BBB
&G       SETC  '2250'
         AGO   .XXX
.BBB     ANOP
         AIF   ('&DEV' NE 'C').CCC
&G       SETC  '2260'
         AGO   .XXX
.CCC     ANOP
         AIF   ('&DEV' NE 'D').DDD
&G       SETC  'AN19'
         AGO   .XXX
.DDD     ANOP
         AGO   .EEE                NO RMS CCWS FOR 3277 MOD 1  @G64DPSS
.XXX     ANOP
DCMRMS   DC    X'&H'               INDEX OF CCW FOR USER MSG   @G64DPSS
DCMADRMS DC    VL3(SUB&G)          POINTER TO RMS CCWS
         AGO   .YYY
.EEE     ANOP
DCMRMS   DC    X'00'               INDEX OF CCW FOR USER MSG   @G64DPSS
DCMADRMS DC   XL3'00'
.YYY     ANOP
DCMWLAST DC    F'0'                PT CON Q ENTRY LAST OUT (O-O-L)
DCMRMSAL DC    H'0'                NUMBER LINES IN MSG AREA
DCMDOMKY DC    H'0'                CONSOLE DOM ELEMENT        MC XM5812
DCMSUBAD DC    V(IEECVSUB)         POINTER TO SUB
         AIF   ('&KEY' EQ '').MARA
DCMADPFK DC    V(&KEY)             POINTER TO RESIDENT PFK AREA
         AGO   .MARB
.MARA    ANOP
DCMADPFK DC    F'0'                POINTER TO RESIDENT PFK AREA
.MARB    ANOP
DCMINTVL DC    H'0'                INTERVAL FOR THIS DCM
DCMTMCTR DC    H'0'                TIME COUNTER FOR THIS DCM
DCMR2FLG DC    X'00'               TIMER FLAGS
DCMRXSFL EQU   X'80'               FULL SCREEN FLAG
DCMRXUNV EQU   X'40'               UNVIEWABLE MESSAGE DISPLAYED
DCMRXTMR EQU   X'20'               TIMER FLAG
DCMRXRLL EQU   X'10'               READY TO ROLL
DCMRXDEL EQU   X'08'               PENDING DELETE REQUEST
DCMRXTIM EQU   X'02'               TIMER ELAPSED FOR THIS DISPLAY
DCMRXDCM EQU   X'01'               T DCM IS IN CORE
DCMR3FLG DC    X'00'               MISC FLAGS
DCMSTSWT EQU   X'80'               CHANGING STATUS OF OUTPUT ONLY CON
DCMKVIP  EQU   X'40'               ENTRY FOR K VARY COMMAND
DCMCLPR  EQU   X'20'               CLOSE IN PROCESS
DCMRXSCN EQU   X'10'               ASY ERROR MESSAGE ON SCREEN
DCMR3PKA EQU   X'08'               DA I/O COMPLETE FLAG
DCMRXHMT EQU   X'04'               FULL SCREEN SIMULATED?     MC YM4102
         DC    XL2'0'              RESERVED
DCMRQDEL DC    XL8'40'             BUFFER FOR PENDING DELETE REQUESTS
DCMRSV01 DC    F'0'                RESERVED                    @G64UPSS
         AIF   (&S EQ 0).END       IF NO SDS AREAS, DEPART
         SPACE 3
***********************************************************************
* SCREEN AREA CONTROL BLOCK - SACB
***********************************************************************
         SPACE
DCMPACB  EQU   *                   SACB PREFIX
DCMPLN   DC    FL2'&D'                  SYSGEN LENGTH OF AREA
DCMPLNPR DC    H'04'               LENGTH OF SACB PREFIX-IN BYTES
DCMPRSZ  EQU   *-DCMPACB           SIZE OF SACB PREFIX IN BYTES
         SPACE
DCMACB   EQU   *                   SCREEN AREA CONTROL BLOCK
         AIF   (&N EQ &S).ZERO     BRANCH IF LAST AREA
DCMACBNX DC    A(*+DCMACBSZ+DCMPRSZ) POINTER TO NEXT SACB
         AGO   .AID
.ZERO    ANOP
DCMACBNX DC    A(0)                POINTER TO NEXT SACB
.AID     ANOP
DCMAID   DC    C'&C'               AREA ID
DCMASACB DC    X'80'               SACB FLAGS
DCMAUSE  EQU   X'80'               AREA PRESENTLY DEFINED     MB Y02958
DCMAGM   EQU   X'40'               GETMAINED SACB
DCMALN   DC    FL2'&D'                  LENGTH OF AREA
DCMATOP  DC    X'00'               TOP ROW OF AREA
DCMACLR  EQU   *                   REINITIALIZED PORTION      MB Y02958
DCMAROW  DC    X'00'               ROW TO BE WRITTEN NEXT
DCMAFR   DC    H'00'               FRAME ON SCREEN
DCMAMJWQ DC    A(0)                POINTER TO CON Q ENTRY FOR MAJOR
DCMAMIN  DC    A(0)                POINTER TO MINOR WQE
DCMATIME DC    F'0'                TIME CONTROL LINE WAS WRITTEN
DCMAMT   DC    XL2'0'              RESERVED                   MB Y02958
DCMAFLG1 DC    X'00'               AREA FLAGS1
DCMADISP EQU   X'40'               DISPLAY IN AREA
DCMADEND EQU   X'20'               END OF DISPLAY ON SCREEN
DCMAFRPR EQU   X'10'               FRAMING IN PROGRESS
DCMAFULL EQU   X'08'               FRAME FULL
DCMABL   EQU   X'04'               BLANKING TO BE DONE
DCMAFLG2 DC    X'00'               AREA FLAGS 2
DCMALMIN EQU   X'80'               SAVED POINTER TO LAST MINOR OUTPUT
DCMAWCON EQU   X'40'               WRITE CONTROL LINE
DCMARCON EQU   X'20'               REWRITE CONTROL LINE
DCMAMJFR EQU   X'10'               MAJOR WQE HAS BEEN FOUND
DCMADFLG DC    X'00'               DYNAMIC DISPLAY FLAGS      MB Y02958
DCMADD   EQU   X'80'               TRACK IN AREA              MB Y02958
DCMAHOLD EQU   X'40'               TRACK IN HOLD MODE         MB Y02958
DCMACSIB EQU   X'20'               TRACK WITH CONTINUATION LINE IN     *
                                   SCREEN IMAGE BUFFER        MB Y02958
DCMATRCK DC    X'00'               TRACK REQUEST INDICATORS   MB Y02958
DCMATJOB EQU   X'80'               TRACK JOBS                 MB Y02958
DCMATJBL EQU   X'40'               TRACK JOBS,LIST            MB Y02958
DCMATTS  EQU   X'08'               TRACK TS                   MB Y02958
DCMATTSL EQU   X'04'               TRACK TS,LIST              MB Y02958
DCMATA   EQU   X'88'               TRACK A                    MB Y02958
DCMATAL  EQU   X'44'               TRACK A,LIST               MB Y02958
DCMAUTME DC    H'0'                UTME=NNN VALUE             MB Y02958
DCMATECB DC    F'0'                TRACK CANCEL ECB ADDRESS   MB Y02958
DCMAPAD  DC    F'0'                RESERVED                   MB Y02958
*                                  NOTE: WHEN THE SACB IS
*                                       REINITIALIZED, FIELDS
*                                       DCMAROW THROUGH THE END
*                                       (INCLUDING THE RESERVED
*                                       FIELD) ARE ZERO'D
DCMACBND EQU   *                   END OF SACB
DCMCLRLN EQU   *-DCMACLR           LENGTH FOR REINITIALIZING  MB Y02958
DCMACBSZ EQU   *-DCMACB            SIZE OF SACB IN BYTES
.NEXT    AIF   (&N EQ &S).END      BRANCH IF LAST AREA IS DONE
&N       SETA  &N+1
&E       SETA  &E+1
&C       SETC  '&TABLE'(&N,1)           COMPUTE AREA ID
&D       SETC  '&A(&E)'
         SPACE
***********************************************************************
* SCREEN AREA CONTROL BLOCK - SACB
***********************************************************************
         SPACE 1                                               @G64UPSS
*                                  SACB PREFIX
         DC    FL2'&D'             SYSGEN LENGTH OF AREA
         DC    H'04'               LENGTH OF SACB PREFIX-IN BYTES
*                                  SACB
         AIF   (&N EQ &S).Z        BRANCH IF LAST AREA
         DC    A(*+DCMACBSZ+DCMPRSZ) POINTER TO NEXT SACB
         AGO   .ID
.Z       ANOP
         DC    A(0)                POINTER TO NEXT SACB
.ID      ANOP
         DC    C'&C'               AREA ID
         DC    X'80'               SACB FLAGS
         DC    FL2'&D'             LENGTH OF AREA
         DC    X'00'               TOP ROW OF AREA
         DC    X'00'               ROW TO BE WRITTEN NEXT
         DC    H'00'               FRAME ON SCREEN
         DC    A(0)                POINTER TO CON Q ENTRY FOR MAJOR
         DC    A(0)                POINTER TO MINOR WQE
         DC    F'0'                TIME CONTROL LINE WAS WRITTEN
         DC    XL2'00'             RESERVED                   MB Y02958
         DC    X'00'               AREA FLAGS1
         DC    X'00'               AREA FLAGS2
         DC    X'00'               DISPLAY FLAGS
         DC    X'00'               TRACK REQUEST INDICATORS   MB Y02958
         DC    H'0'                UTME=NNN VALUE             MB Y02958
         DC    F'0'                CANCEL TRACK ECB ADDRESS   MB Y01958
         DC    F'0'                RESERVED                   MB Y02958
         AGO   .NEXT               RETURN TO GENERATE NEXT AREA
.END     MEND
