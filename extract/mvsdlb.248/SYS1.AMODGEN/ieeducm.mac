         MACRO
         IEEDUCM &A1,&B1,&C1,&E1,&G1,&H1,&ROUTCD1=,&AUTH1=,            +
               &ALTCON1=,&COMPCN1=,&ALTCOM1=
         GBLA  &UCMINDX       TOTAL CONSOLE COUNT
         GBLC  &UCMA(99),&UCMB(99),&UCMC(99)
         GBLC  &UCME(99),&UCMG(99),&UCMH(99)
         GBLC  &ROUTCDE(99),&AUTH(99),&ALTCON(99)
         GBLC  &COMPCON(99),&ALTCOMP(99)
         LCLA  &INDEX         PARAMETER INDEX
         LCLA  &N             PARAMETER COUNT
.*
.*       OS/VS2 RELEASE 3, 07/03/74, LEVEL=2
.*
.*       THIS MACRO PUTS THE PARAMETERS PASSED IN ONE OR TWO CALLS TO
.*       IEEDUCM INTO GLOBAL DATA ARRAYS FOR PROCESSING BY IEECUCM.
.*       UP TO 50 ENTRIES MAY BE IN EACH SUBLIST PER CALL.
.*
&N       SETA  N'&A1          GET NUMBER OF ENTRIES IN SUBLIST
.DUCM100 ANOP
&UCMINDX SETA  &UCMINDX+1     STEP TOTAL COUNTER
&INDEX   SETA  &INDEX+1       STEP SUBLIST ENTRY COUNTER
&UCMA(&UCMINDX) SETC '&A1(&INDEX)' SET DEVICE ADDRESS
&UCMB(&UCMINDX) SETC '&B1(&INDEX)' SET 2-BYTE NAME MODIFIER
&UCMC(&UCMINDX) SETC '&C1(&INDEX)' SET INITIAL ATTRIBUTES (WAXA)
&UCME(&UCMINDX) SETC '&E1(&INDEX)' SET DEVICE TYPE INDICATOR
&UCMG(&UCMINDX) SETC '&G1(&INDEX)' SET CONSOLE'S I/O CAPABILITY
.*                                 INDICATOR
&UCMH(&UCMINDX) SETC '&H1(&INDEX)' SET INTEGRATED OPERATOR'S CONSOLE
.*                                 INDICATOR
&ROUTCDE(&UCMINDX) SETC '&ROUTCD1(&INDEX)' SET ROUTING CODE
&AUTH(&UCMINDX) SETC '&AUTH1(&INDEX)' SET AUTHORIZATION CODE
&ALTCON(&UCMINDX) SETC '&ALTCON1(&INDEX)' SET ALTERNATE CONSOLE ADDRESS
&COMPCON(&UCMINDX) SETC '&COMPCN1(&INDEX)' SET COMPOSITE CONSOLE ADDR
&ALTCOMP(&UCMINDX) SETC '&ALTCOM1(&INDEX)' SET ALTERNATE COMPOSITE ADDR
         AIF   (&INDEX NE &N).DUCM100
         MEND
