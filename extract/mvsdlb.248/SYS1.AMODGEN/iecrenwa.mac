         MACRO
&LABEL   IECRENWA &EP,&F4,&D1=(0),&ADT=NO
.*
.*       MACRO DELETIONS/CHANGES FOR VS2 RELEASE 038
.*
.* $01=OZ65137,EDM1102,B820312,STLMDB: FIX FOR ENQ LOCKOUT PROB    @01A
.*
.*       MACRO DELETIONS/CHANGES FOR VS2 RELEASE 2
.*0000                                                           YM1337
.*0000                                                           YM7036
.*       MACRO DELETIONS/CHANGES FOR VS2 RELEASE 3             @ZM30032
.*0000                                                         @Y30LSTG
.*       MACRO DELETIONS/CHANGES FOR VS2 RELEASE 3.7           @ZA29709
.*                                                             @ZA29709
.*       SU 32 ADDTIONS                                        @G32DSPD
.*0000009000-010000,022000-023000,029000               DELETED @ZA31909
.*0000009000-009600,019060-019120,022000-022600        ADDED   @ZA31909
.*0000029000-029100,152040-152400                      ADDED   @ZA31909
.*
.*        SU60 DELETIONS/CHANGES                               @G60ASBJ
.*A 159200                                                     @G60ASBJ
.*C 159200                                                     @ZA43918
.*                                                             @G60ASBJ
.**********************************************************************
.*
.* MODULE NAME = IECRENWA (VS2-2)
.*
.* DESCRIPTIVE NAME = DADSM RENAME WORK AREA ADDRESSING MACRO
.*
.* COPYRIGHT = NONE
.*
.* STATUS = CHANGE LEVEL 000
.*
.* FUNCTION = DADSM RENAME MACRO WHICH PROVIDES ADDRESSABILITY
.*            TO THE 720 DECIMAL BYTE WORK AREA (NOT INCLUDING @ZA31909
.*            THE 8 WORD PREFIX OR THE 6 WORD RECOVERY ROUTINE @ZA31909
.*            PARAMETER LIST) USED BY RENAME.                  @ZA31909
.*
.* NOTES = THIS MACRO IS AN INTERNAL MACRO USED IN THE FOLLOWING
.*         DADSM FUNCTIONS:
.*
.*             RENAME
.*             SCRATCH AND RENAME VOLUME MOUNTING ROUTINE
.*
.*         TO REFERENCE THIS WORK AREA WITHIN THE FUNCTIONS LISTED
.*         ABOVE, CODE A USING STATEMENT ON THE LABEL 'RENAMWKA'.
.*
.*      DEPENDENCIES = THIS MACRO IS DESIGNED TO BE USED OPTIONALLY
.*                     WITH THE IECSDSL1 DSCB MAPPING MACRO, THE
.*                     PREFX MACRO (DEFINED IN THE IECDSECS MACRO)
.*                     WHICH DEFINES THE 8 WORD WORK AREA PREFIX,
.*                     THE 20 WORD EXTENDED WORK AREA PREFIX, AND
.*                     THE 4 WORD DADSM WTG TABLE, AND THE DSMADT
.*                     MACRO (ALSO DEFINED IN THE IECDSECS MACRO)
.*                     WHICH DEFINES THE 6 WORD DADSM AUDIT TRAIL.
.*                     THE AUDIT TRAIL MUST APPEAR AS THE      @ZA31909
.*                     FINAL 6 WORDS OF THE RENAME WORK AREA.  @ZA31909
.*
.*                     THE FIRST 364 BYTES OF THE SCRATCH AND RENAME
.*                     WORK AREAS (ADDRESSED RESPECTIVELY BY MAPPING
.*                     MACROS IECSCRWA AND IECRENWA) ARE MAPPED
.*                     IDENTICALLY - 8 WORD PREFIX, 20 WORD EXTENDED
.*                     PREFIX, 4 WORD DADSM WTG TABLE, 96 BYTE DSCB
.*                     AREA, AND 140 BYTE DSCB AREA.  THE 250  @ZA31909
.*                     BYTES FROM LABELS 'WKADEB' THROUGH      @ZA31909
.*                     'PVOLSER' ARE ALSO MAPPED IDENTICALLY.  @ZA31909
.*
.*      RESTRICTIONS = NONE
.*
.*      REGISTER CONVENTIONS = NOT APPLICABLE
.*
.* PATCH LABEL = NOT APPLICABLE
.*
.* MODULE TYPE = MACRO
.*
.*      PROCESSOR = ASSEMBLER LANGUAGE
.*
.*      MODULE SIZE = 744 DECIMAL BYTES (NOT INCLUDING THE     @ZA31909
.*                    PREFIX)                                  @ZA31909
.*
.*      ATTRIBUTES = NOT APPLICABLE
.*
.* ENTRY POINT = RENPREFX
.*
.*      PURPOSE = THIS MACRO IS CALLED AT ASSEMBLY TIME BY THE
.*                FUNCTIONS LISTED UNDER 'NOTES' TO EXPAND THE
.*                RENAME WORK AREA.
.*
.*      LINKAGE =
.*
.*                LABEL IECRENWA EP,F4,D1=(N),ADT=YES
.*
.*      INPUT =
.*
.*                OPTIONAL LABEL SPECIFIED AS AN ACCEPTABLE ASSEMBLER
.*                LABEL WHICH CAN BE USED BY THE CALLING CSECT TO
.*                ESTABLISH ADDRESSABILITY TO THE ENTIRE WORK AREA.
.*
.*                EP IS THE FIRST POSITIONAL PARAMETER, SPECIFYING
.*                THAT THE LABELS FOR EACH OF THE FIELDS IN THE 8
.*                WORD PREFIX, THE 20 WORD EXTENDED PREFIX, AND THE 4
.*                WORD DADSM WTG TABLE ARE TO BE EXPANDED.  IF THESE
.*                FIELDS ARE NOT TO BE DEFINED, A COMMA MUST BE CODED
.*                TO INDICATE THE ABSENCE OF THIS PARAMETER.
.*
.*                F4 IS THE SECOND POSITIONAL PARAMETER, SPECIFYING
.*                THAT THE FIELDS OF THE FORMAT 4 DSCB ARE TO BE
.*                EXPANDED USING THE IECSDSL1 DSCB MAPPING MACRO.
.*                IF THESE FIELDS ARE NOT TO BE DEFINED AND A
.*                KEYWORD PARAMETER FOLLOWS, A COMMA MUST BE
.*                CODED TO INDICATE THE ABSENCE OF THIS PARAMETER.
.*
.*                D1=(N) IS A KEYWORD PARAMETER THAT SPECIFIES THE
.*                FORMAT(S) OF THE DSCB(S) TO BE EXPANDED IN THE
.*                140 BYTE DSCB AREA, USING THE IECSDSL1 MACRO.
.*                N CAN BE A NUMBER FROM 0 - 6.  IF N IS 0 OR 4 OR
.*                IF THIS KEYWORD IS OMITTED, NO LABELS ARE EXPANDED.
.*                IF N IS 1, 2, 3, 5, OR 6, THE CORRESPONDING FORMAT
.*                OF THE DSCB WILL BE EXPANDED.  MORE THAN ONE DSCB
.*                FORMAT CAN BE MAPPED IN THE 140 BYTE DSCB AREA
.*                BY SEPARATING THE FORMAT NUMBERS BY COMMAS.
.*
.*                ADT=YES IS A KEYWORD PARAMETER WHICH SPECIFIES THAT
.*                THE LABELS FOR EACH OF THE FIELDS WITHIN THE 6 WORD
.*                DADSM AUDIT TRAIL ARE TO BE EXPANDED.
.*
.*      OUTPUT = DADSM RENAME WORK AREA EXPANSION
.*
.* EXIT-NORMAL = MEND STATEMENT
.*
.* EXIT-ERROR = ERROR MESSAGES:
.*
.*      MNOTE 12,***  IHB002 INVALID EP OPERAND SPECIFIED-&EP
.*      MNOTE 12,***  IHB002 INVALID F4 OPERAND SPECIFIED-&F4
.*      MNOTE 12,***  IHB002 INVALID ADT OPERAND SPECIFIED-&ADT
.*
.*      THE MACRO EXPANSION CONTINUES AS IF THE INVALID PARAMETER
.*      WERE NOT SPECIFIED.
.*
.* EXTERNAL REFERENCES = NONE
.*
.*      ROUTINES = NONE
.*
.*      DATA AREAS = RENAME WORK AREA
.*
.*      CONTROL BLOCKS = DSCB, ECB, IOB, DCB, DEB
.*
.* TABLES = NONE
.*
.* MACROS = IECDSECS DSMADT
.*          IECDSECS PREFX
.*          IECSDSL1
.*          IHBERMAC
.*
.* CHANGE ACTIVITY = SEE DELETIONS/CHANGES PRECEDING THE MACRO PROLOGUE
.*
.**********************************************************************
.*
         LCLA  &C
&LABEL   DSECT
***********************************************************************
*
*        RENAME WORK AREA
*
***********************************************************************
         SPACE 1
***********************************************************************
*        PREFIX, EXTENDED PREFIX, AND WTG TABLE AREA
***********************************************************************
         SPACE 1
RENPREFX DS    8F                       WORK AREA PREFIX
RENAMWKA DS    20F                      EXTENDED PREFIX
FIRST    EQU   RENAMWKA
RWTGTBL  DS    4F                       DADSM WTG TABLE
         AIF   ('&EP' EQ 'EP').REN0010
         AIF   ('&EP' EQ '').REN0020
         IHBERMAC 1001,EP,&EP
         AGO   .REN0020
.REN0010 ANOP
         ORG   RENPREFX
*        IECDSECS PREFX,EXPAND=YES
         IECDSECS PREFX,EXPAND=YES
.REN0020 ANOP
         SPACE 1
***********************************************************************
*        FORMAT 4 DSCB AREA
***********************************************************************
         SPACE 1
VTOCDSCB DS    XL96                     FORMAT 4 DSCB
ENQAREA  EQU   VTOCDSCB+4               ENQ PARAMETER LIST
MJNAME   EQU   ENQAREA+16               MAJOR ELEMENT NAME
MINAME   EQU   ENQAREA+24               MINOR ELEMENT NAME
         AIF   ('&F4' EQ 'F4').REN0030
         AIF   ('&F4' EQ '').REN0040
         IHBERMAC 1001,F4,&F4
         AGO   .REN0040
.REN0030 ANOP
         ORG   VTOCDSCB
*        IECSDSL1 (4)
         IECSDSL1 (4)
.REN0040 ANOP
         SPACE 1
***********************************************************************
*        140-BYTE DSCB AREA
***********************************************************************
         SPACE 1
F1DSCB   DS    XL140                    140-BYTE DSCB AREA
&C       SETA  1
.REN0050 AIF   (&C GT N'&D1).REN0060
         AIF   (&D1(&C) EQ 0 OR &D1(&C) EQ 4).REN0055
         ORG   F1DSCB
         MNOTE '         IECSDSL1 (&D1(&C))'
         IECSDSL1 (&D1(&C))
.REN0055 ANOP
&C       SETA  &C+1
         AGO   .REN0050
.REN0060 ANOP
         DS    F                        UNUSED FULLWORD
         SPACE 1
***********************************************************************
*        RENAME INTERNAL DEB/DCB AREA
***********************************************************************
         SPACE 1
WKADEB   DS    F                        DEB
DCB      DS    11F                      DCB
DEBPTR   DS    F                        DCB POINTER TO DEB
         SPACE 1
***********************************************************************
*        RENAME INTERNAL ECB/IOB AREA
***********************************************************************
         SPACE 1
ECB      DS    F                        ECB
IOB      DS    8F                       IOB
SEEK     DS    D                        DIRECT ACCESS ADDRESS
         SPACE 1
***********************************************************************
*        RENAME CHANNEL CONTROL WORDS AREA
***********************************************************************
         SPACE 1
CCW1     DS    D
CCW2     DS    D
CCW3     DS    D
CCW4     DS    D
CCW5     DS    D
CCW6     DS    D
CCW7     DS    D
CCW8     DS    D
CCW9     DS    D
CCW10    DS    D
CCW11    DS    D
CCW12    DS    D
         SPACE 1
***********************************************************************
*        RENAME WORK AREA FIELDS
***********************************************************************
         SPACE 1
FIELD1   DS    D                        PASSWORD WORK AREA FIELD
OLDPLPTR DS    F                        PARAMETER LIST ADDRESS
PRUCBPTR DS    F                        PRIMARY (DEMOUNTABLE) UCB ADDR
VOLPTR   DS    F                        NEXT VOLUME LIST ENTRY ADDRESS
TCBADDR  DS    F                        POINTER TO THE TCB
DSABADDR DS    F                        DSAB ADDRESS OF CURRENT VOLUME
PRDSABAD DS    F                        DSAB ADDRESS OF PRIMARY UCB
VOLNUM   DS    H                        NUMBER OF VOLUMES REMAINING
         CNOP  2,4                      ALIGN DSABQDB TO FULLWORD
TIOTMNR  DS    CL6                      ASID/DSAB QDB ADDRESS
DSABQDB  EQU   TIOTMNR+2                DSAB QDB ADDRESS
VOLMINOR DS    CL6                      VOLSER FOR DEQ ON SYSZVOLS
ERCODE   DS    H                        ERROR RETURN CODE
SISW1    DS    XL1                      SECURITY/INTEGRITY SWITCHES
*
*        DEFINITION OF SECURITY/INTEGRITY SWITCH 1 BIT SETTINGS
*
*        BIT 0 = ENQ ON SYSTIOT ISSUED
*        BIT 1 = ENQ ON SYSDSN ISSUED
*        BIT 2 = ENQ ON SYSZVOLS ISSUED
*        BIT 3 = DEQ ON SYSZVOLS TO BE ISSUED
*        BIT 4 = UCB DYNAMICALLY ALLOCATED
*        BIT 5 = DEB SEARCH REQUIRED
*        BIT 6 = NO SMF RECORD TO BE BUILT
*        BIT 7 = RACHECK SAID USER HAS VOLUME AUTHORIZATION    @G32DSPD
*
TIOTENQ  EQU   X'80'                    TIOT ENQ'ED INDICATOR
DSNENQ   EQU   X'40'                    DSN ENQ'ED INDICATOR
VOLENQ   EQU   X'20'                    VOLUME ENQ'ED INDICATOR
VOLDEQ   EQU   X'10'                    VOLUME TO BE DEQ'ED INDICATOR
UCBALLOC EQU   X'08'                    UCB ALLOCATED INDICATOR
DEBSRCH  EQU   X'04'                    DEB SEARCH REQUIRED INDICATOR
NOSMFRCD EQU   X'02'                    NO SMF RECORD INDICATOR
VOLRACHK EQU   X'01'                    VOL RACHECKED OK       @G32DSPD
*
SISW2    DS    XL1                      SECURITY/INTEGRITY SWITCHES
*
*        DEFINITION OF SECURITY/INTEGRITY SWITCH 2 BIT SETTINGS
*
*        BIT 0 = VOLUME TO BE DEMOUNTED
*        BIT 1 = VOLUME TO BE MOUNTED AND VERIFIED
*        BIT 2 = VOLUME TO BE VERIFIED
*        BIT 3 = ALLOCATED UNIT HAS BEEN FOUND
*        BIT 4 = MOUNT WAS ISSUED FOR VIRTUAL PACK             @Y30LSTG
*
DEMNTSW  EQU   X'80'                    VOLUME DEMOUNT INDICATOR
MOUNTSW  EQU   X'40'                    VOLUME MOUNT/VERIFY INDICATOR
VERIFYSW EQU   X'20'                    VOLUME VERIFY INDICATOR
UCBFNDSW EQU   X'10'                    ALLOCATED UCB FOUND INDICATOR
VIRTMNT  EQU   X'08'                    VIRTUAL MOUNT ISSUED   @Y30LSTG
*
         DS    H                        UNUSED
PVOLSER  DS    CL6                      PROTECTED VOLUME SERIAL
DSRENOPT DS    F                        RENAME OPTION CODE     @ZA29709
DSRENOAD DS    F                        POINT TO RENAME OLD DSN@ZA29709
DSRENNAD DS    F                        POINT TO RENAME NEW DSN@ZA29709
DSRENVAD DS    F                        POINT TO RENAME VOLIST @ZA29709
PDSNAME  DS    CL44                     PROTECTED DS OLD NAME  @ZA29709
PDSNNAME DS    CL44                     PROTECTED DS NEW NAME  @ZA29709
         DS    XL1                      UNUSED BYTE
RTYPEFLG DS    XL1                      RENAME SWITCHES
*
*        DEFINITION OF RENAME SWITCH BIT SETTINGS
*
*        BIT 1 = RACF SAYS OK TO RENAME - REDRIVE FLAG             @01A
*        BIT 3 = NO VOLUME MOUNTED AND NO DEVICE ALLOCATED
*        BIT 5 = PASSWORD CHECKED FOR PASSWORD-PROTECTED DATA SET
*        BIT 7 = EXIT BIT (LAST VOLUME LIST ENTRY PROCESSED)
*
RACRENOK EQU   X'40'                    REDRIVE FLAG               @01A
NOMNTDEV EQU   X'10'                    NO DEVICE INDICATOR
PSWDCHKD EQU   X'04'                    PASSWORD CHECKED INDICATOR
EXITBIT  EQU   X'01'                    EXIT INDICATOR
*
         SPACE 1
***********************************************************************
*        ABSOLUTE TRACK ADDRESSES
***********************************************************************
         SPACE 1
VTOCADR  DS    XL5                      FORMAT 4 CCHHR
INCCHHR  DS    XL5                      CHANNEL PROGRAM WORK AREA
OUTCCHHR DS    XL5                      CHANNEL PROGRAM WORK AREA
NEWCCHHR DS    XL5                      CHANNEL PROGRAM WORK AREA
         SPACE 1
***************************************************************@ZA31909
*        PROTECTED COPY OF VOLUME COUNT AND                    @ZA31909
*        POINTER TO PROTECTED COPY OF VOLUME LIST              @ZA31909
***************************************************************@ZA31909
         SPACE 1                                               @ZA31909
PVOLNUM  DS    H                        VOLUME COUNT           @ZA31909
VOL1ADDR DS    F                        ADDRESS OF FIRST       @ZA31909
*                                       VOLUME ID IN VOLUME    @ZA31909
*                                       LIST WORK AREA         @ZA31909
RENSAVE  DS    F                        REGISTER 14 SAVE AREA  @ZA43918
         SPACE 1                                               @ZA31909
***********************************************************************
*        RENAME RECOVERY ROUTINE AUDIT TRAIL AREA
***********************************************************************
         SPACE 1
RENADTRL DS    6F                       RENAME AUDIT TRAIL WORDS
         AIF   ('&ADT' EQ 'YES').REN0090
         AIF   ('&ADT' EQ 'NO').REN0100
         IHBERMAC 1001,ADT,&ADT
         AGO   .REN0100
.REN0090 ANOP
         ORG   RENADTRL
*        IECDSECS DSMADT,EXPAND=YES
         IECDSECS DSMADT,EXPAND=YES
.REN0100 ANOP
         SPACE 1
***********************************************************************
RENWAEND EQU   *                        RENAME WORK AREA END
RWALNGTH EQU   RENWAEND-RWTGTBL         WORK AREA LENGTH FOR IECRES GET
***********************************************************************
         MEND
