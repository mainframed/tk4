         MACRO
&NAME    FREEPOOL &PAR,&DUMMY=
.*         RELEASE 23 DELETIONS/CHANGES
.*         RELEASE 22 DELETIONS/CHANGES
.*         RELEASE 21 DELETIONS/CHANGES
.*    001800-002800
.*         DATE OF LAST CHANGE = 84/05/18 APAR OZ79785, PTF UZ75753
.*         DATE OF LAST CHANGE = 85/03/12 APAR OZ86199, PTF UZ39662
.*         DATE OF LAST CHANGE = 85/04/12 APAR OZ87508, PTF UZ39662
         AIF   ('&PAR' EQ '').E1
&NAME    IHBINNRA &PAR
         L     15,20(0,1)              LOAD BUFCB ADDRESS
         OI    23(1),1                 INDICATE NO BUFCB ADDR
         SR    14,14                   CLEAR REGISTER
         SR    1,1                     CLEAR REGISTER         @ZA79785
         ICM   1,3,6(15)               LOAD LENGTH OF BUFFERS @ZA86199
         IC    14,5(0,15)              NUMBER OF BUFFERS      @ZA79785
         MR    0,14                    AREA TO BE FREED       @ZA79785
         LA    1,8(0,1)                ACCOUNT FOR BCB        @ZA86199
         TM    4(15),X'40'             IS BUFCB 16 BYTES       @ZA19719
         BNO   *+8                     BRANCH IF BUFCB = 8 BYTES
         LA    1,8(0,1)                ADJUST SIZE PLUS 8      @ZA87508
         LR    0,1                     LOAD LENGTH TO BE FREED @ZA86199
         LA    1,0(0,15)               LOAD AREA ADDRESS
         SVC   10                      ISSUE FREEMAIN SVC
         MEXIT
.E1      IHBERMAC 01
         MEND
