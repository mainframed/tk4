         MACRO
&SL      IKJRLSA &PDL
.* RELEASE 20 SUPPORT CODE -- 20035
         AIF   (N'&SYSLIST LT 2).A0
         MNOTE 8,'NO MORE THAN 1 POSITIONAL PARAMETER ALLOWED'
         MEXIT
.A0      AIF   ('&PDL' NE '').A1
         MNOTE 8,'AN ADDRESS OR A REGISTER MUST BE SPECIFIED'
         MEXIT
.A1      AIF   ('&PDL'(1,1) EQ '(').L1 REGISTER NOTATION SPECIFIED
&SL      L     1,&PDL                  FETCH PDL ADDRESS
         AGO   .L3
.L1      ANOP
&SL      L     1,0&PDL                 LOAD PDL ADDRESS
.L3      ANOP
         LA    1,0(0,1)                CLEAR HIGH ORDER BYTE     YM2296
         LTR   1,1                     WAS A PDL RETURNED
         BZ    IKJ$&SYSNDX             IF NO SKIP FREEMAIN
IKJ@&SYSNDX DS 0H                      FREE PDL LOOP
         L     14,0(0,1)               LOAD NEXT AREA ADDRESS TO FREE
.*                                                               YM2296
         L     0,4(0,1)                LOAD SUBPOOL AND LENGTH   YM2296
         FREEMAIN R,LV=(0),A=(1)       ISSUE FREEMAIN
         LA    14,0(0,14)              CLEAR HIGH ORDER BYTE     YM2296
         LTR   1,14                    ANY MORE CORE TO FREE
         BNZ   IKJ@&SYSNDX             REPEAT LOOP UNTIL DONE
IKJ$&SYSNDX DS 0H                      BRANCHED TO IF FREEMAIN SKIPPED
         MEND
