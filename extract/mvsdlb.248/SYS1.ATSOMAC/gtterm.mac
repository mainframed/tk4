         MACRO
&NAME    GTTERM &PRMSZE=,&ALTSZE=,&ATTRIB=,&MF=I               @G76XRYU
.* A000000-999999                                              @G76XR00
.* A034000                                                     @OZ90350
.*
         LCLC  &NDX                                            @G76XRYU
&NDX     SETC  '&SYSNDX'                                       @G76XRYU
         AIF   ('&MF' EQ 'I' AND '&PRMSZE' EQ '' AND '&ATTRIB' EQ '').E-
               RROR1                                           @G76XRYU
         AIF   ('&MF' EQ 'L').LFORM
         AIF   ('&MF(1)' EQ 'E').EFORM
         AIF   ('&MF' NE 'I').ERROR2
.*********************I -FORM OF MACRO*********************************
&NAME    CNOP  0,4
         BAL   1,*+16                   BRANCH AROUND PARMS    @G76XRYU
GTRM&NDX DC    A(0)                     ADDRESS OF PRIMARY     @G76XRYU
         DC    A(0)                     ADDRESS OF ALTERNATE
         DC    A(0)                     ADDRESS OF ATTRIBUTE   @G76XRYU
         AGO   .STADDR
.EFORM   ANOP
&NAME    CNOP  0,4                                             @OZ90350
         IHBOPLST ,,&NAME,MF=&MF
.STADDR  ANOP
.*********COMMON CODE FOR BOTH I AND E FORM OF MACRO******************
         AIF   ('&PRMSZE' EQ '').LABEL2
         AIF   ('&PRMSZE'(1,1) NE '(').LOADPRM
         ST    &PRMSZE(1),0(,1)         STORE PRIMARY ADDRESS
         AGO   .LABEL2
.LOADPRM ANOP
         AIF   ('&PRMSZE'(K'&PRMSZE,1) EQ ')' OR '&MF' NE 'I').LPARM
         ORG   GTRM&NDX                 PUT ADDR OF PRIMARY    @G76XRYU
         DC    A(&PRMSZE)               IN PARM LIST           @G76XRYU
         ORG
         AGO   .LABEL2                                         @G76XRYU
.LPARM   ANOP                      ..LA ADDR OR EXECUTE FORM   @G76XRYU
         LA    0,&PRMSZE                LOAD ADDRESS OF PRIMARY
         ST    0,0(,1)                  STORE ADDRESS OF PRIMARY
.LABEL2  ANOP
         AIF   ('&ALTSZE' EQ '').IEATRCK                       @G76XRYU
         AIF   ('&ALTSZE'(1,1) NE '(').LOADALT
         ST    &ALTSZE(1),4(,1)         STORE ADDRESS OF ALTERNATE
         AGO   .IEATRCK                                        @G76XRYU
.LOADALT ANOP
         AIF   ('&ALTSZE'(K'&ALTSZE,1) EQ ')' OR '&MF' NE 'I').LAALT
         ORG   GTRM&NDX+4               PUT ALTERNATE SIZE     @G76XRYU
         DC    A(&ALTSZE)               IN PARM LIST           @G76XRYU
         ORG
         AGO   .IEATRCK                                        @G76XRYU
.LAALT   ANOP                     ...LA ADDR OR EXECUTE FORM   @G76XRYU
         LA    0,&ALTSZE                LOAD ADDR OF ALTERNATE @G76XRYU
         ST    0,4(,1)                  STORE ADD OF ALTERNATE @G76XRYU
.*  PROCESS ATTRIBUTE PARM FOR I AND E FORMS WHEN NOT NULL     @G76XRYU
.IEATRCK ANOP                                                  @G76XRYU
         AIF   ('&ATTRIB' EQ '').SVCENTY                       @G76XRYU
         AIF   ('&ATTRIB'(1,1) EQ '(').REGATR                  @G76XRYU
         AIF   ('&ATTRIB'(K'&ATTRIB,1) EQ ')' OR '&MF' NE 'I').LAATRIB
         ORG   GTRM&NDX+8               PUT ATTRIB BYTE ADDR   @G76XRYU
         DC    A(&ATTRIB)               IN PARM LIST           @G76XRYU
         ORG
         AGO   .SVCENTY                                        @G76XRYU
.LAATRIB ANOP                       .. LA ADDR OR EXECUTE FORM @G76XRYU
         LA    0,&ATTRIB                GET ADR OF ATTRIB BYTE @G76XRYU
         ST    0,8(1)                   PUT IN 3RD PARM WORD   @G76XRYU
         AGO   .SVCENTY                                        @G76XRYU
.REGATR  ANOP                                                  @G76XRYU
         ST    &ATTRIB(1),8(1)          REG => 3RD PARM WORD   @G76XRYU
.SVCENTY ANOP
         OI    8(1),128                 END OF LIST INDICATOR  @G76XRYU
         LA    0,17                     ENTRY CODE
         SLL   0,24                     SHIFT TO HIGH ORDER BYTE
         SVC   94                       ISSUE SVC
         MEXIT
.***************  L  - FORM  ***************************
.LFORM   ANOP
&NAME    DS    0F
         AIF   ('&PRMSZE' EQ '').NOPRMAD
         AIF   ('&PRMSZE'(1,1) EQ '(').NOPRMAD
         DC    A(&PRMSZE)               ADDRESS OF PRIMARY PARM ADDR
         AGO   .CHKALT
.NOPRMAD ANOP
         DC    A(0)                     ADDRESS OF PRIMARY PARM ADDR
.CHKALT  AIF   ('&ALTSZE' EQ '').NOALTAD
         AIF   ('&ALTSZE'(1,1) EQ '(').NOALTAD
         DC    A(&ALTSZE)               ADDRESS OF ALTERNATE ADDR
         AGO   .LATTCK                                         @G76XRYU
.NOALTAD ANOP
         DC    A(0)                     ADDR OF ALTERNATE      @G76XRYU
.*  PROCESS ATTRIBUTE PARM FOR LIST FORM                       @G76XRYU
.LATTCK  ANOP                                                  @G76XRYU
         AIF   ('&ATTRIB' NE '').CKATTR                        @G76XRYU
         DC    A(0)                     L-FORM--ATTRIB BYTE    @G76XRYU
         MEXIT                                                 @G76XRYU
.CKATTR  ANOP                                                  @G76XRYU
         AIF   ('&ATTRIB'(1,1) NE '(').ATTROK                  @G76XRYU
         MNOTE 12,'IHB300 INCOMPATIBLE OPERANDS: MF=L AND ATTRIB=&ATTRI*
               B'                                              @G76XRYU
         MEXIT                                                 @G76XRYU
.ATTROK  ANOP                                                  @G76XRYU
         DC    A(&ATTRIB)               L-FORM--A(ATTR BYTE)   @G76XRYU
         MEXIT
.ERROR1  ANOP
         IHBERMAC 1006,PRMSZE
         MEXIT
.ERROR2  IHBERMAC 54,,&MF
         MEXIT
         MEND
