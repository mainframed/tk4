         MACRO
&NAME    GETLINE &PARM=,&UPT=,&ECT=,&ECB=,&INPUT=,&TERMGET=,&ENTRY=,   *
               &MF=
.**********************************************************************
.*                                                                    *
.*                                                                    *
.*                                                                    *
.*                                                                    *
.*STATUS:                                                             *
.*   CHANGE LEVEL 000.                                                *
.*                                                                    *
.*FUNCTION:                                                           *
.*   THE GETLINE MACRO EXAMINES PARAMETERS SPECIFIED IN THE GETLINE   *
.*   MACRO INSTRUCTION AND BUILDS A PARAMETER BLOCK TO BE USED BY THE *
.*   GETLINE SERVICE ROUTINE (REFER TO SECTION 3.48.9.1 FOR THE       *
.*   PARAMETER BLOCK FORMAT).                                         *
.*   IN THE LIST FORM, THE GETLINE MACRO GENERATES THE GETLINE        *
.*   PARAMETER BLOCK CONSISTING OF ONE WORD OF CONTROL (CONTROL AND   *
.*   TINPUT OPTIONS), AND THE ADDRESS OF THE INPUT BUFFER.            *
.*   IN THE EXECUTE FORM OF THE GETLINE MACRO, CODE IS GENERATED      *
.*   WHICH WILL RESET THE PARAMETER BLOCK IF REQUIRED AND LOAD THE    *
.*   ADDRESSES OF THE UPT, ECT, ECB, AND PARAMETER BLOCK INTO THE     *
.*   SERVICE ROUTINE PARAMETER LIST IF SPECIFIED. A LINK MACRO WILL   *
.*   BE GENERATED TO INVOKE THE GETLINE SERVICE ROUTINE UNLESS AN     *
.*   ENTRY POINT IS SPECIFIED. IF AN ENTRY POINT IS SPECIFIED, THE    *
.*   CODE TO ENABLE A BRANCH AND LINK WILL BE GENERATED.              *
.*                                                                    *
.*ENTRY POINTS:                                                       *
.*   GETLINE MACRO CALL - ONLY ENTRY POINT.                           *
.*                                                                    *
.*INPUT:                                                              *
.*   PARAMETERS SPECIFIED IN THE GETLINE MACRO FORMAT (FOR FORMAT     *
.*   SPECIFICATIONS REFER TO SECTION 3.48.9.1).                       *
.*                                                                    *
.*OUTPUT:                                                             *
.*   IN THE L-FORM, A 2-WORD PARAMETER BLOCK (REFER TO SECTION        *
.*   3.48.9.1) CONSISTING OF CONTROL WHICH IS SET BY THE MACRO AND    *
.*   ADDRESS OF THE INPUT BUFFER.                                     *
.*   IN THE E-FORM, CODE WHICH RESETS CONTROL IN THE PARAMETER BLOCK, *
.*   PLACES THE ADDRESSES OF THE SERVICE ROUTINE PARAMETER BLOCK,     *
.*   UPT, ECT, AND ECB IN THE SERVICE ROUTINE PARAMETER LIST AS       *
.*   SPECIFIED, AND INVOKES THE SERVICE ROUTINE.                      *
.*                                                                    *
.*EXTERNAL REFERENCES:                                                *
.*   LINK MACRO.                                                      *
.*                                                                    *
.*EXITS,NORMAL:                                                       *
.*   IN THE L-FORM, RETURN TO CALLER.                                 *
.*   IN THE E-FORM, CODE IS GENERATED TO INVOKE THE GETLINE SERVICE   *
.*   ROUTINE ACCORDING TO THE STANDARD LINKAGE CONVENTIONS (REFER TO  *
.*   SECTION 3.48.1.5).                                               *
.*                                                                    *
.*EXITS,ERROR:                                                        *
.*   INVALID INPUT.                                                   *
.*                                                                    *
.*TABLES/WORK AREAS:                                                  *
.*   SERVICE ROUTINE PARAMETER LIST - A TABLE OF POINTERS TO THE UPT, *
.*   ECT, ECB, AND SERVICE ROUTINE PARAMETER BLOCK.                   *
.*   SERVICE ROUTINE PARAMETER BLOCK - A CONTROL TABLE SET ACCORDING  *
.*   TO THE SPECIFIED MACRO PARAMETERS AND THE ADDRESS OF THE INPUT   *
.*   BUFFER.                                                          *
.*                                                                    *
.*ATTRIBUTES:                                                         *
.*   SERIALLY REUSABLE.                                               *
.*                                                                    *
.*NOTES:                                                              *
.*   DATE OF CURRENT RELEASE :                                        *
.*      01/22/70                                                      *
.*   PREVIOUS RELEASE DATES :                                         *
.*      12/12/69                                                      *
.*      10/22/69                                                      *
.*                                                                    *
.*                                                                    *
.*                                                                    *
.*                                                                    *
.*                                                                    *
.**********************************************************************
         LCLA  &A
         LCLB  &B1,&B2,&B3,&B4,&B5,&B6
         LCLB  &BS1,&BS2
         LCLC  &C
.E1      AIF   (T'&MF EQ 'O').ERR1     NO MF PARAMETER
         AIF   (N'&SYSLIST EQ 0).MF1   POSITIONAL PARAMETERS INVALID
         AIF   (T'&SYSLIST(1) NE 'O' OR N'&SYSLIST NE 1).ERR4
.MF1     AIF   ('&MF' EQ 'L').L1       LIST FORM
.**********************************************************************
.*                                                                    *
.*                               EXECUTE FORM                         *
.*                                                                    *
.**********************************************************************
         AIF   ('&MF(1)' NE 'E').ERR1
         AIF   (T'&NAME EQ 'O').E2
&NAME    DS    0H
.E2      AIF   (T'&MF(2) EQ 'O').ERR1
         AIF   ('&MF(2)' EQ '(1)').E3
         AIF   ('&MF(2)'(1,1) EQ '(').WARN3
.E2A     LA    1,&MF(2)                LOAD PARM LIST ADDR
.E3      AIF   (T'&UPT EQ 'O').E4      UPT OMITTED
         AIF   ('&UPT'(1,1) EQ '(').E3A
         LA    14,&UPT                 LOAD UPT ADDR
         ST    14,0(0,1)               STORE UPT ADDR IN PARM LIST
         AGO   .E4
.E3A     ANOP
&A       SETA  K'&UPT-2
&C       SETC  '&UPT'(2,&A)
         ST    &C,0(0,1)               STORE UPT ADDR IN PARM LIST
.E4      AIF   (T'&ECT EQ 'O').E5      ECT OMITTED
         AIF   ('&ECT'(1,1) EQ '(').E4A
         LA    14,&ECT                 LOAD ECT ADDR
         ST    14,4(0,1)               STORE ECT ADDR IN PARM LIST
         AGO   .E5
.E4A     ANOP
&A       SETA  K'&ECT-2
&C       SETC  '&ECT'(2,&A)
         ST    &C,4(0,1)               STORE ECT ADDR IN PARM LIST
.E5      AIF   (T'&ECB EQ 'O').E6      ECB OMITTED
         AIF   ('&ECB'(1,1) EQ '(').E5A
         LA    14,&ECB                 LOAD ECB ADDR
         ST    14,8(0,1)               STORE ECB ADDR IN PARM LIST
         AGO   .E6
.E5A     ANOP
&A       SETA  K'&ECB-2
&C       SETC  '&ECB'(2,&A)
         ST    &C,8(0,1)               STORE ECB ADDR IN PARM LIST
.E6      AIF   (T'&PARM EQ 'O').E6B    PARM OMITTED
         AIF   ('&PARM'(1,1) EQ '(').E6A
         LA    14,&PARM                LOAD PARM ADDR
         ST    14,12(0,1)              STORE PARM ADDR IN PARM LIST
         AGO   .E7
.E6A     ANOP
&A       SETA  K'&PARM-2
&C       SETC  '&PARM'(2,&A)
         ST    &C,12(0,1)              STORE PARM ADDR IN PARM LIST
         AIF   (N'&INPUT EQ 0 AND N'&TERMGET EQ 0).E11
         LR    14,&C                   LOAD PARM ADDR
         AGO   .E7
.E6B     AIF   (N'&INPUT EQ 0 AND N'&TERMGET EQ 0).E11
         L     14,12(0,1)              LOAD PARM BLOCK ADDR
.E7      AIF   (N'&INPUT EQ 0 AND N'&TERMGET EQ 0).E11
&A       SETA  N'&INPUT
         AIF   (&A EQ 0).E8            NO INPUT OPERANDS
         AIF   (&A GT 2).ERR2          TOO MANY OPERANDS
         AIF   ('&INPUT' EQ '()').E7H  ALL OPERANDS OMITTED
.E7A     AIF   (T'&INPUT(&A) EQ 'O').E7G OMITTED OPERAND
         AIF   ('&INPUT(&A)' NE 'ISTACK').E7B
         AGO   .E7C
.E7B     AIF   ('&INPUT(&A)' NE 'TERM').E7D
&B4      SETB  1
.E7C     AIF   (&BS1).ERR2             MUTUALLY EXCLUSIVE OPERANDS
&BS1     SETB  1
         AGO   .E7G
.E7D     AIF   ('&INPUT(&A)' NE 'LOGICAL').E7E
         AGO   .E7F
.E7E     AIF   ('&INPUT(&A)' NE 'PHYSICAL').ERR2
&B3      SETB  1
.E7F     AIF   (&BS2).ERR2             MUTUALLY EXCLUSIVE OPERANDS
&BS2     SETB  1
.E7G     ANOP
&A       SETA  &A-1
         AIF   (&A NE 0).E7A
.E7H     MVI   0(14),B'00&B3.&B4.0000' SET CONTROL FIELD
&BS1     SETB  0
&BS2     SETB  0
.E8      ANOP
&A       SETA  N'&TERMGET
         AIF   (&A EQ 0).E11           NO TERMGET OPERANDS
         AIF   (&A GT 2).ERR5          TOO MANY OPERANDS
.E8A     AIF   ('&TERMGET' EQ '()').E8H    ALL OPERANDS OMITTED
         AIF   (T'&TERMGET(&A) EQ 'O').E8G OMITTED OPERAND
         AIF   ('&TERMGET(&A)' NE 'EDIT').E8B
         AGO   .E8C
.E8B     AIF   ('&TERMGET(&A)' NE 'ASIS').E8D
&B6      SETB  1
.E8C     AIF   (&BS1).ERR5             MUTUALLY EXCLUSIVE OPERANDS
&BS1     SETB  1
         AGO   .E8G
.E8D     AIF   ('&TERMGET(&A)' NE 'WAIT').E8E
         AGO   .E8F
.E8E     AIF   ('&TERMGET(&A)' NE 'NOWAIT').ERR5
&B5      SETB  1
.E8F     AIF   (&BS2).ERR5             MUTUALLY EXCLUSIVE OPERANDS
&BS2     SETB  1
.E8G     ANOP
&A       SETA  &A-1
         AIF   (&A NE 0).E8A
.E8H     MVI   2(14),B'100&B5.000&B6'  SET TINPUT OPTIONS FIELD
.E11     AIF   (T'&ENTRY EQ 'O').E12
         AIF   ('&ENTRY' EQ '(15)').E11A
         AIF   ('&ENTRY'(1,1) EQ '(').WARN4
.E11A1   LA    15,&ENTRY               LOAD EP ADDR
.E11A    BALR  14,15                   BRANCH TO SERVICE ROUTINE
         MEXIT
.E12     L      15,16(0,0)          LOAD CVT POINTER
         TM     348(15),B'10000000'   IS GETLINE LOADED ?
         BNO    IKJ@&SYSNDX            NO - BRANCH TO LINK
         L      15,348(0,15)          YES - BALR TO GETLINE
         BALR   14,15
         B      IKJ$&SYSNDX
IKJ@&SYSNDX LINK  EP=IKJGETL
IKJ$&SYSNDX DS    0H
         MEXIT
.**********************************************************************
.*                                                                    *
.*                             LIST FORM                              *
.*                                                                    *
.**********************************************************************
.L1      DS    0F
         AIF   (N'&INPUT EQ 0   AND N'&TERMGET EQ 0).L2I
&A       SETA  N'&INPUT
         AIF   (&A EQ 0).L1H           NO INPUT OPERANDS
         AIF   ('&INPUT' EQ '()').L1GG ALL OPERANDS OMITTED
         AIF   (&A GT 2).ERR2          TOO MANY OPERANDS
.L1A     AIF   (T'&INPUT(&A) EQ 'O').L1G OMITTED OPERAND
         AIF   ('&INPUT(&A)' NE 'ISTACK').L1B
         AGO   .L1C
.L1B     AIF   ('&INPUT(&A)' NE 'TERM').L1D
&B3      SETB  1
.L1C     AIF   (&BS1).ERR2             MUTUALLY EXCLUSIVE OPERANDS
&BS1     SETB  1
         AGO   .L1G
.L1D     AIF   ('&INPUT(&A)' NE 'LOGICAL').L1E
         AGO   .L1F
.L1E     AIF   ('&INPUT(&A)' NE 'PHYSICAL').ERR2
&B2      SETB  1
.L1F     AIF   (&BS2).ERR2             MUTUALLY EXCLUSIVE OPERANDS
&BS2     SETB  1
.L1G     ANOP
&A       SETA  &A-1
         AIF   (&A NE 0).L1A
.L1GG    ANOP
&NAME    DC    B'00&B2.&B3.0000'       SET CONTROL FIELD
         DC    X'00'                   SET CONTROL FIELD
&B2      SETB  0
&BS1     SETB  0
&BS2     SETB  0
         AGO   .L2
.L1H     ANOP
&NAME    DC    X'0000'                 SET CONTROL FIELD
.L2      ANOP
&A       SETA  N'&TERMGET
         AIF   (&A EQ 0).L2H           NO TERMGET OPERANDS
         AIF   ('&TERMGET' EQ '()').L2H ALL OPERANDS OMITTED
         AIF   (&A GT 2).ERR5          TOO MANY OPERANDS
.L2A     AIF   (T'&TERMGET(&A) EQ 'O').L2G OMITTED OPERAND
         AIF   ('&TERMGET(&A)' NE 'EDIT').L2B
         AGO   .L2C
.L2B     AIF   ('&TERMGET(&A)' NE 'ASIS').L2D
&B2      SETB  1
.L2C     AIF   (&BS1).ERR5             MUTUALLY EXCLUSIVE OPERANDS
&BS1     SETB  1
         AGO   .L2G
.L2D     AIF   ('&TERMGET(&A)' NE 'WAIT').L2E
         AGO   .L2F
.L2E     AIF   ('&TERMGET(&A)' NE 'NOWAIT').ERR5
&B1      SETB  1
.L2F     AIF   (&BS2).ERR5             MUTUALLY EXCLUSIVE OPERANDS
&BS2     SETB  1
.L2G     ANOP
&A       SETA  &A-1
         AIF   (&A NE 0).L2A
.L2GG    ANOP
         DC    B'100&B1.000&B2'        SET TINPUT OPTIONS FIELD
         DC    X'00'                   SET TINPUT OPTIONS FIELD
         DC    A(0)      INPUT BUFFER ADDR
         AGO   .L6
.L2H     DC    X'8000'                 SET INPUT OPTIONS FIELD
         DC    A(0)                    INPUT BUFFER ADDR
         AGO   .L6
.L2I     ANOP
&NAME    DC    X'00008000'        SET CONTROL AND TINPUT OPTIONS FIELDS
         DC    A(0)                    INPUT BUFFER ADDR
.L6      MEXIT
.WARN3   MNOTE 0,'PARAMETER LIST ADDR IN MF PARAMETER IS NOT IN REG 1'
         AGO   .E2A
.WARN4   MNOTE 0,'ENTRY POINT ADDR IN ENTRY PARAMETER IS NOT IN REG 15'
         AGO   .E11A1
.ERR1    MNOTE 8,'INVALID OR OMITTED MF PARAMETER'
         MEXIT
.ERR2    MNOTE 8,'INVALID OPERAND IN INPUT PARAMETER SUBLIST'
         MEXIT
.ERR3    MNOTE 8,'QUEUE AND GET ARE MUTUALLY EXCLUSIVE OPERANDS'
         MEXIT
.ERR4    MNOTE 8,'POSITIONAL PARAMETERS ARE INVALID'
         MEXIT
.ERR5    MNOTE 8,'INVALID OPERAND IN TERMGET PARAMETER SUBLIST'
         MEXIT
.ERR6    MNOTE 8,'INVALID QUEUE OPERAND'
         MEXIT
         MEND
