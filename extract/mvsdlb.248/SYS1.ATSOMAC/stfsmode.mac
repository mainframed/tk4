         MACRO
&NAME    STFSMODE &A,&INITIAL=NO,&RSHWKEY=64 PROTOTYPE
.**********************************************************************
.*                                                                    *
.* STATUS - OS/VS2 TSO/VTAM DYNAMIC RESHOW LEVEL 2.0                  *
.*                                                                    *
.* NAME - STFSMODE  (SET FULL SCREEN MODE)                            *
.*                                                                    *
.* FUNCTION - THE STFSMODE MACRO IS USED TO SPECIFY WHETHER OR NOT    *
.*            AN APPLICATION IS PRESENTING DISPLAYS TO A TSO/VTAM     *
.*            DISPLAY TERMINAL IN FULL SCREEN MODE.                   *
.*            WHEN INITIALLY SETTING THE MODE ON, INITIAL=YES WILL    *
.*            PREVENT PAGING FOR THE FIRST FULL SCREEN TPUT ISSUED BY *
.*            THE APPLICATION.                                        *
.*            RSHWKEY IS USED TO SPECIFY WHICH PFKEY IS TO BE USED    *
.*            AS THE RESHOW KEY.  E.G., RSHWKEY=3 MEANS THAT PFKEY 3  *
.*            IS TO BE THE RESHOW KEY.  WHEN STFSMODE IS OFF,         *
.*            RSHWKEY IS NOT TO BE SPECIFIED.  WHEN STFSMODE IS ON    *
.*            AND NO RSHWKEY HAS BEEN SPECIFIED, RSHWKEY WILL DEFAULT *
.*            TO ZERO.                                                *
.*                                                                    *
.*       THE MACRO PROTOTYPE IS ABOVE                                 *
.*                                                                    *
.*       THE STANDARD FORM OF THE EXPANSION IS BELOW                  *
.*&NAME  LA    1,128+64                 FLAGS FOR MODE/INITIAL CALL   *
.*       SLL   1,24                     SHIFT TO HIGH ORDER BYTE      *
.*       SR    0,0                      CLEAR REGISTER                *
.*       LA    0,10                     LOAD RSHWKEY                  *
.*       OR    1,0                      RSHWKEY IN RIGHTMOST BYTE     *
.*       LA    0,18                     LOAD ENTRY CODE               *
.*       SLL   0,24                     SHIFT TO HIGH ORDER BYTE      *
.*       SVC   94                       TERMINAL CONTROL MACRO SVC    *
.*                                                                    *
.**********************************************************************
         LCLA  &SYSFS
         LCLA  &SYSINIT
&SYSFS   SETA  128
&SYSINIT SETA  0
         AIF   ('&INITIAL' NE 'YES' AND '&INITIAL' NE 'NO').ERROR1
         AIF   (&RSHWKEY GT 0 AND &RSHWKEY LT 13).TESTFS
         AIF   (&RSHWKEY NE 64).ERROR2
         AGO   .NEXT
.TESTFS  ANOP
         AIF   ('&A' EQ 'OFF').ERROR3
.NEXT    ANOP
         AIF   ('&INITIAL' EQ 'NO').TESTA
&SYSINIT SETA  64
.TESTA   ANOP
         AIF   ('&A' EQ 'ON' OR '&A' EQ '').FSON
         AIF   ('&A' EQ 'OFF').FSOFF
.ERROR   IHBERMAC 36,,&A
         MEXIT
.ERROR1  IHBERMAC 54,,,
         MEXIT
.ERROR2  IHBERMAC 54,,RSHWKEY
         MEXIT
.ERROR3  IHBERMAC 1020,RSHWKEY,&A
         MEXIT
.FSON    ANOP
&NAME    LA    1,&SYSFS+&SYSINIT        FLAGS FOR MODE/INITIAL CALL
         SLL   1,24                     SHIFT TO HIGH ORDER BYTE
         AGO   .PFCD
.FSOFF   ANOP
&NAME    SR    1,1                      CLEAR PARAMETER REGISTER
.PFCD    ANOP
         AIF   ('&A' EQ 'OFF').ENTRYCD
         AIF   (&RSHWKEY EQ 64).ENTRYCD
         SR    0,0                      CLEAR REGISTER
         LA    0,&RSHWKEY               LOAD RSHWKEY
         OR    1,0                      RSHWKEY IN RIGHTMOST BYTE
.ENTRYCD ANOP
         LA    0,18                     LOAD ENTRY CODE
         SLL   0,24                     SHIFT TO HIGH ORDER BYTE
         SVC   94                       TERMINAL CONTROL MACRO SVC
         MEND
