         MACRO
&NAME    STBREAK &A                     PROTOTYPE
.**********************************************************************
.*                                                                    *
.*RELEASE 23 DELETIONS                                                *
.*                                                                    *
.*RELEASE 22 DELETIONS                                                *
.*                                                                    *
.*RELEASE 21 DELETIONS                                                *
.*                                                                    *
.*STATUS - CHANGE LEVEL 000                                           *
.*                                                                    *
.*NAME - STBREAK                                                      *
.*                                                                    *
.*FUNCTION - THE STBREAK MACRO IS USED TO SPECIFY THE PRESENCE OR     *
.*   ABSENCE OF THE BREAK FEATURE (APPLICABLE TO IBM 1050 AND 2741    *
.*   TERMINALS).                                                      *
.*                                                                    *
.*       THE MACRO PROTOTYPE IS ABOVE                                 *
.*                                                                    *
.*       THE STANDARD FORM OF THE EXPANSION IS BELOW                  *
.*                                                                    *
.*&NAME  LA    1,128                    LOAD FLAG BYTE                *
.*       SLL   1,24                     PUT ABOVE IN LEFT MOST OF REG *
.*       LA    0,4                      LOAD ENTRY CODE               *
.*       SLL   0,24                     PUT ENTRY CODE IN LEFT MOST   *
.*                                      BYTE OF REGISTER              *
.*       SVC   94                       ISSUE SVC                     *
.*                                                                    *
.**********************************************************************
         AIF   ('&A' EQ 'YES' OR '&A' EQ '').BREAK
         AIF   ('&A' EQ 'NO').NOBREAK
.ERROR   IHBERMAC 36,,&A
         MEXIT                          EXIT
.BREAK   ANOP
&NAME    LA    1,128                    PREPARE FLAG BYTE
         SLL   1,24                     PREPARE PARAMETER
         AGO   .ENTRYCD                 GO TO ENTRYCD
.NOBREAK ANOP
&NAME    SR    1,1                      PREPARE PARAMETER
.ENTRYCD LA    0,4                      LOAD ENTRY CODE
         SLL   0,24                     PUT ENTRY CODE IN LEFT MOST
.*                                      BYTE OF REGISTER
         SVC   94 .                     ISSUE ASSOCIATED SVC
         MEND
