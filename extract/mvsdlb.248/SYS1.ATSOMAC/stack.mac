         MACRO
&NAME    STACK &PARM=,&UPT=,&ECT=,&ECB=,&TERM=,&STORAGE=,&DELETE=,     *
               &ENTRY=,&MF=,&DATASET=
.**********************************************************************
.*                                                                    *
.*                                                                    *
.*                                                                    *
.*                                                                    *
.*STATUS:                                                             *
.*   CHANGE LEVEL 002.                                                *
.*                                                                    *
.*FUNCTION:                                                           *
.*   THE STACK MACRO EXAMINES PARAMETERS SPECIFIED IN THE STACK MACRO *
.*   INSTRUCTION AND BUILDS A PARAMETER BLOCK TO BE USED BY THE STACK *
.*   SERVICE ROUTINE                                                  *
.*   IN THE LIST FORM, THE STACK MACRO GENERATES THE STACK            *
.*   PARAMETER BLOCK CONSISTING OF ONE WORD OF CONTROL (OPERATION AND *
.*   CODE) AND THE ADDRESSES OF THE STORAGE LIST SOURCE DESCRIPTOR,   *
.*   THE INPUT DD NAME, THE OUTPUT DD NAME, AND A MEMBER NAME.        *
.*   IN THE EXECUTE FORM OF THE STACK MACRO, CODE IS GENERATED        *
.*   WHICH WILL RESET THE PARAMETER BLOCK IF REQUIRED AND LOAD THE    *
.*   ADDRESSES OF THE UPT, ECT, ECB, AND PARAMETER BLOCK INTO THE     *
.*   SERVICE ROUTINE PARAMETER LIST IF SPECIFIED. A LINK MACRO WILL   *
.*   BE GENERATED TO INVOKE THE STACK SERVICE ROUTINE UNLESS AN       *
.*   ENTRY POINT IS SPECIFIED. IF AN ENTRY POINT IS SPECIFIED, OR IF  *
.*   THE ENTRY POINT ADDR IS IN THE CVT, A BALR WILL BE GENERATED.    *
.*                                                                    *
.*ENTRY POINTS:                                                       *
.*   STACK MACRO CALL - ONLY ENTRY POINT.                             *
.*                                                                    *
.*INPUT:                                                              *
.*   PARAMETERS SPECIFIED IN THE STACK MACRO FORMAT.
.*                                                                    *
.*OUTPUT:                                                             *
.*   IN THE L-FORM, A 5-WORD PARAMETER BLOCK CONSISTING OF CONTROL    *
.*   WHICH IS SET BY THE MACRO, THE ADDRESS OF THE STORAGE LIST       *
.*   SOURCE DESCRIPTOR, AND THE ADDR OF AN INPUT AND OUTPUT DD NAME.  *
.*   IN THE E-FORM, CODE WHICH RESETS CONTROL IN THE PARAMETER BLOCK, *
.*   PLACES THE ADDRESSES OF THE SERVICE ROUTINE PARAMETER BLOCK,     *
.*   UPT, ECT, AND ECB IN THE SERVICE ROUTINE PARAMETER LIST AS       *
.*   SPECIFIED, AND INVOKES THE SERVICE ROUTINE.                      *
.*                                                                    *
.*EXTERNAL REFERENCES:                                                *
.*   LINK MACRO.                                                      *
.*                                                                    *
.*EXITS,NORMAL:                                                       *
.*   IN THE L-FORM, RETURN TO CALLER.                                 *
.*   IN THE E-FORM, CODE IS GENERATED TO INVOKE THE STACK SERVICE     *
.*   ROUTINE ACCORDING TO THE STANDARD LINKAGE CONVENTIONS            *
.*                                                                    *
.*EXITS,ERROR:                                                        *
.*   INVALID INPUT.                                                   *
.*                                                                    *
.*TABLES/WORK AREAS:                                                  *
.*   SERVICE ROUTINE PARAMETER LIST - A TABLE OF POINTERS TO THE UPT, *
.*   ECT, ECB, AND SERVICE ROUTINE PARAMETER BLOCK.                   *
.*   SERVICE ROUTINE PARAMETER BLOCK - A CONTROL TABLE SET ACCORDING  *
.*   TO THE SPECIFIED MACRO PARAMETERS AND THE ADDRESS OF THE STORAGE *
.*   LIST SOURCE DESCRIPTOR.                                          *
.*                                                                    *
.*ATTRIBUTES:                                                         *
.*   SERIALLY REUSABLE.                                               *
.*                                                                    *
.*NOTES:                                                              *
.*   DATE OF CURRENT RELEASE :                                        *
.*      02/15/74                                                      *
.*   PREVIOUS RELEASE DATES :                                         *
.*      03/15/73                                                      *
.*      01/22/70                                                      *
.*      12/12/69                                                      *
.*      10/22/69                                                      *
.*                                                                    *
.*                                                                    *
.*CHANGE ACTIVITY                                                     *
.*               C 000000-999999                               Y30PQJM*
.* A069250   CLEAR CODE BYTES                                 @ZA75363*
.*        REPLACE PREVIOUS LEVEL                              @ZA82204*
.*        REMOVE FIX FOR APAR OZ75363                         @ZA82490*
.*                                                                    *
.**********************************************************************
         LCLA  &A,&NUM
         LCLB  &PROC,&CLOSE,&INDD,&OUTDD,&PROMPT,&LIST,&CNTL,&SEQ
         LCLB  &MEMBR
         LCLC  &C,&DDOUT,&DDIN,&MEMAD
         AIF   (T'&MF EQ 'O').ERR1           NO MF PARAMETER
         AIF   (N'&SYSLIST EQ 0).MF1   POSITIONAL PARAMETERS INVALID
         AIF   (T'&SYSLIST(1) NE 'O' OR N'&SYSLIST NE 1).ERR6
.MF1     AIF   ('&MF' EQ 'L').L1             BRANCH ON LIST FORM
.**********************************************************************
.*                                                                    *
.*                               EXECUTE FORM                         *
.*                                                                    *
.**********************************************************************
         AIF   ('&MF(1)' NE 'E').ERR1
         AIF   (T'&NAME EQ 'O').E1
&NAME    DS    0H
.E1      AIF   (T'&MF(2) EQ 'O').ERR1
         AIF   ('&MF(2)' EQ '(1)').E2
         AIF   ('&MF(2)'(1,1) EQ '(').WARN3
.E1A     LA    1,&MF(2)       LOAD PARM LIST ADDR
.E2      AIF   (T'&UPT EQ 'O').E3       UPT OMITTED
         AIF   ('&UPT'(1,1) EQ '(').E2A
         LA    14,&UPT        LOAD UPT ADDR
         ST    14,0(0,1)     STORE UPT ADDR
         AGO   .E3
.E2A     ANOP
&A       SETA  K'&UPT-2
&C       SETC  '&UPT'(2,&A)
         ST    &C,0(0,1)     STORE UPT ADDR IN PARM LIST
.E3      AIF   (T'&ECT EQ 'O').E4     ECT OMITTED
         AIF   ('&ECT'(1,1) EQ '(').E3A
         LA    14,&ECT                  LOAD ECT ADDR
         ST    14,4(0,1)               STORE ECT ADDR IN PARM LIST
         AGO   .E4
.E3A     ANOP
&A       SETA  K'&ECT-2
&C       SETC  '&ECT'(2,&A)
         ST    &C,4(0,1)               STORE ECT ADDR IN PARM LIST
.E4      AIF   (T'&ECB EQ 'O').E5       ECB OMITTED
         AIF   ('&ECB'(1,1) EQ '(').E4A
         LA    14,&ECB        LOAD ECB ADDR
         ST    14,8(0,1)     STORE ECB ADDR IN PARM LIST
         AGO   .E5
.E4A     ANOP
&A       SETA  K'&ECB-2
&C       SETC  '&ECB'(2,&A)
         ST    &C,8(0,1)     STORE ECB ADDR IN PARM LIST
.E5      AIF   (T'&PARM EQ 'O').E5B     PARM OMITTED
         AIF   ('&PARM'(1,1)  EQ '(').E5A
         LA    14,&PARM       LOAD PARM ADDR
         ST    14,12(0,1)    STORE PARM ADDR IN PARM LIST
         AGO   .E6
.E5A     ANOP
&A       SETA  K'&PARM-2
&C       SETC  '&PARM'(2,&A)
         ST    &C,12(0,1)    STORE PARM ADDR IN PARM LIST
         AIF   (T'&TERM EQ 'O' AND N'&STORAGE EQ 0 AND                 *
               T'&DELETE EQ 'O' AND N'&DATASET EQ 0).E11
         LR    14,&C          LOAD PARM ADDR
         AGO   .E6
.E5B     AIF   (T'&TERM EQ 'O' AND N'&STORAGE EQ 0   AND T'&DELETE     *
               EQ 'O' AND N'&DATASET EQ 0).E11
         L     14,12(0,1)     LOAD PARM BLOCK ADDR INTO REGISTER
.E6      AIF   (T'&TERM EQ 'O' AND N'&STORAGE EQ 0   AND T'&DELETE     *
               EQ 'O' AND N'&DATASET EQ 0).E11
.E7      AIF   (T'&TERM EQ 'O').E8      TERM OMITTED
         AIF   (N'&STORAGE NE 0   OR T'&DELETE NE 'O'                  *
               OR N'&DATASET NE 0).ERR3
         AIF   ('&TERM' NE '*').ERR4
         MVI   0(14),X'80'         SET OPERATION FIELD
         MVI   1(14),X'80'         SET CODE FIELD
         AGO   .E11
.E8      AIF   (N'&STORAGE EQ 0).E9          STORAGE OMITTED
         AIF   (T'&TERM NE 'O' OR T'&DELETE NE 'O'                     *
               OR N'&DATASET NE 0).ERR3
         MVI   0(14),X'80'         SET OPERATION FIELD
         AIF   ('&STORAGE' EQ '()').E8E      ALL OPERANDS OMITTED
&A       SETA  N'&STORAGE
         AIF   (&A GT 3).ERR2           TOO MANY OPERANDS
         AIF   (&A EQ 1 OR T'&STORAGE(2) EQ 'O').E8E   LSD ADDR ONLY
.E8B     AIF   ('&STORAGE(2)' NE 'PROCN').E8C
         MVI   1(14),X'42'         SET CODE FIELD
         AGO   .E8D1
.E8C     AIF   ('&STORAGE(2)' NE 'PROCL').E8D
         MVI   1(14),X'43'         SET CODE FIELD
         AGO   .E8D1
.E8D1    AIF   (&A NE 3).E8F
         AIF   ('&STORAGE(3)' NE 'PROMPT').ERR9
         OI    1(14),B'00000100'
         AGO   .E8F
.E8D     AIF   ('&STORAGE(2)' NE 'SOURCE').ERR2
.E8E     MVI   1(14),X'40'         SET CODE FIELD
.E8F     AIF   (T'&STORAGE(1) EQ 'O' OR '&STORAGE' EQ '()').WARN1
         AIF   ('&STORAGE(1)'(1,1) EQ '(').E8G
         LA    0,&STORAGE(1)       LOAD STORAGE SOURCE DESCRIPTOR ADDR
         ST    0,4(0,14)          STORE STORAGE SOURCE DESCRIPTOR ADDR
         AGO   .E11
.E8G     ANOP
&A       SETA  K'&STORAGE(1)-2
&C       SETC  '&STORAGE(1)'(2,&A)
         ST    &C,4(0,14)         STORE STORAGE SOURCE DESCRIPTOR ADDR
         AGO   .E11
.E9      AIF   (T'&DELETE EQ 'O').E10        DELETE OMITTED
         AIF   (T'&TERM NE 'O' OR N'&STORAGE NE 0                      *
               OR N'&DATASET NE 0).ERR3
.E9A     AIF   ('&DELETE' NE 'TOP').E9B
         MVI   0(14),X'40'    SET OPERATION FIELD
         AGO   .E11
.E9B     AIF   ('&DELETE' NE 'PROC').E9C
         MVI   0(14),X'20'    SET OPERATION FIELD
         AGO   .E11
.E9C     AIF   ('&DELETE' NE 'ALL').ERR5
         MVI   0(14),X'10'    SET OPERATION FIELD
         AGO   .E11
.E10     AIF   (T'&DATASET EQ 'O').E11   DATASET OMITTED
         AGO   .DD0          GO TO DATASET PARSE ROUTINE
.RETURNE ANOP                RETURN POINT FROM PARSE ROUTINE
         MVI   0(14),X'80'              SET OPERATION FIELD
         MVI   1(14),B'10&INDD.&OUTDD.0&PROMPT.&PROC.&LIST.' OPTION
         MVI   3(14),B'0000&MEMBR.&SEQ.&CNTL.&CLOSE.'  SET CLOSE OPTION
.E106    AIF   (&MEMBR NE 1).E108B
         AIF   ('&MEMAD'(1,1) EQ '(').E108RR
         LA    0,&MEMAD
&MEMAD   SETC  '0'
         AGO   .E108AA
.E108RR  ANOP
&MEMAD   SETC   '&MEMAD'(2,K'&MEMAD-2)
.E108AA  ST    &MEMAD,16(0,14)              STORE MEMBER NAME ADDR
.E108B   AIF   (&INDD EQ B'0').E10AA1   INDD NOT SPECIFIED
         AIF   ('&DDIN'(1,1) EQ '(').E10RR1  INDD IN A REGISTER
         LA    0,&DDIN         LOAD ADDRESS OF INPUT DDNAME
&DDIN    SETC  '0'
         AGO   .E10RX1        GO TO STORE INSTRUNCTION
.E10RR1  ANOP
&DDIN    SETC  '&DDIN'(2,(K'&DDIN-2))   STRIP PARENS FROM REGISTER
.E10RX1  ST    &DDIN,8(0,14)       STORE ADDRESS OF INPUT DDNAME
.E10AA1  AIF   (&OUTDD EQ B'0').E10AA2   OUTDD NOT SPECIFIED
         AIF   ('&DDOUT'(1,1) EQ '(').E10RR2   OUTDD IN A REGISTER
         LA    0,&DDOUT       LOAD ADDRESS OF OUTPUT DDNAME
&DDOUT   SETC  '0'
         AGO   .E10RX2        GO TO STORE INSTRUCTION
.E10RR2  ANOP
&DDOUT   SETC  '&DDOUT'(2,(K'&DDOUT-2))  STRIP PARENS FROM REGISTER
.E10RX2  ST    &DDOUT,12(0,14) STORE ADDRESS OF OUTPUT DDNAME
.E10AA2  ANOP
.E11     AIF   (T'&ENTRY EQ 'O').E12         ENTRY POINT OMITTED
         AIF   ('&ENTRY' EQ '(15)').E11B
         AIF   ('&ENTRY'(1,1) EQ '(').WARN4
.E11A    LA    15,&ENTRY      LOAD EP ADDR
.E11B    BALR  14,15     BRANCH TO SERVICE ROUTINE
         MEXIT
.E12     L     15,16(0,0)          LOAD CVT POINTER
         TM    472(15),B'10000000'   IS STACK ADDR HERE ?
         BNO   IKJ@&SYSNDX          NO- BRANCH TO LINK
         L     15,472(15)            YES- BALR TO STACK
         BALR  14,15
         B     IKJ$&SYSNDX         BRANCH AROUND LINK
IKJ@&SYSNDX LINK  EP=IKJSTCK          LINK TO STACK
IKJ$&SYSNDX DS    0H
         MEXIT
.**********************************************************************
.*                                                                    *
.*                                 LIST FORM                          *
.*                                                                    *
.**********************************************************************
.L1      DS    0F
.L2      AIF   (T'&TERM EQ 'O').L3      TERM OMITTED
         AIF   ('&TERM' NE '*').ERR4
         AIF   (N'&STORAGE NE 0   OR T'&DELETE NE 'O'                  *
               OR T'&DATASET NE 'O').ERR3
&NAME    DC    X'80800000'         SET OPERATION AND CODE FIELDS
         DC    4A(0)      STORAGE SOURCE DESCRIPTOR ADDR
         MEXIT
.L3      AIF   (T'&DELETE EQ 'O').L4    DELETE OMITTED
         AIF   (T'&TERM NE 'O' OR N'&STORAGE NE 0                      *
               OR N'&DATASET NE 0).ERR3
.L3A     AIF   ('&DELETE' NE 'TOP').L3B
&NAME    DC    X'40000000'         SET OPERATION AND CODE FIELDS
         AGO   .L3D
.L3B     AIF   ('&DELETE' NE 'PROC').L3C
&NAME    DC    X'20000000'         SET OPERATION AND CODE FIELDS
         AGO   .L3D
.L3C     AIF   ('&DELETE' NE 'ALL').ERR5
&NAME    DC    X'10000000'         SET OPERATION AND CODE FIELDS
.L3D     DC    4A(0)      STORAGE SOURCE DESCRIPTOR ADDR
         MEXIT
.L4      AIF   (N'&STORAGE EQ 0).L5     NO STORAGE OPERANDS
         AIF   (T'&TERM NE 'O' OR T'&DELETE NE 'O'                     *
               OR N'&DATASET NE 0).ERR3
         AIF   ('&STORAGE' EQ '()').L4E      ALL OPERANDS OMITTED
&A       SETA  N'&STORAGE
&LIST    SETB  0
&PROMPT  SETB  0
         AIF   (&A EQ 1 OR T'&STORAGE(2) EQ 'O').L4E   LSD ADDR ONLY
.L4B     AIF   ('&STORAGE(2)' NE 'PROCN').L4C
         AGO   .L4D1
.L4C     AIF   ('&STORAGE(2)' NE 'PROCL').L4D1
&LIST    SETB  1
.L4D1    AIF   (&A LT 3).L4D2
         AIF   ('&STORAGE(3)' NE 'PROMPT').ERR9
&PROMPT  SETB  1
.L4D2    ANOP
&NAME    DC    X'80'
         DC    B'01000&PROMPT.1&LIST.'
         DC    X'0000'
         AGO   .L4F
.L4D     AIF   ('&STORAGE(2)' NE 'SOURCE').ERR2
.L4E     ANOP
&NAME    DC    X'80400000'         SET OPERATION AND CODE FIELDS
.L4F     AIF   (T'&STORAGE(1) EQ 'O' OR '&STORAGE' EQ '()').WARN2
         DC    A(&STORAGE(1))      STORAGE SOURCE DESCRIPTOR ADDR
         DC    3A(0)
         MEXIT
.L5      AIF   (T'&DATASET EQ 'O').L6  IF DATASET OPERAND OMITTED
         AGO   .DD0      GO PARSE DATASET OPERANDS
.RETURNL ANOP      RETURN POINT FROM DATASET PARSE
&NAME    DC    X'80'     SET OPERATION FIELD
         DC    B'10&INDD.&OUTDD.0&PROMPT.&PROC.&LIST.' OPTION
         DC    X'00'
         DC    B'0000&MEMBR.&SEQ.&CNTL.&CLOSE.'   SET CLOSE OPTION
         DC    A(0)     ADDRESS OF LSD
         DC    A(&DDIN.)    ADDRESS OF DDNAME
         DC    A(&DDOUT.)   ADDRESS OF DDNAME
         DC    A(&MEMAD.)   ADDRESS OF MEMBERNAME
         MEXIT
.L6      ANOP
&NAME    DC    X'00000000'         SET OPERATION AND CODE FIELDS
         DC    4A(0)      STORAGE SOURCE DESCRIPTOR ADDR
         MEXIT
.DD0     ANOP
&DDIN    SETC  '0'
&DDOUT   SETC  '0'
&MEMAD   SETC  '0'
&MEMBR   SETB  0
&PROC    SETB  0
&PROMPT  SETB  0
&CLOSE   SETB  0
&LIST    SETB  0
&INDD    SETB  0
&OUTDD   SETB  0
&SEQ     SETB  0
&CNTL    SETB  0
         AIF   (T'&TERM NE 'O' OR T'&STORAGE NE 'O' OR                 *
               T'&DELETE NE 'O').ERR3
&NUM     SETA  N'&DATASET
         AIF   (&NUM EQ 0).DD7
.DD1     AIF   (&NUM NE 1 OR '&DATASET(&NUM)' NE 'CLOSE').DD2
&CLOSE   SETB  1
         AGO   .DD7
.DD2     AIF   ('&DATASET(&NUM)' EQ '*').DD7
.DD3     AIF   ('&DATASET(&NUM)' NE 'PROMPT').DD4A
&PROMPT  SETB  1
         AGO   .DD7
.DD4A    AIF   ('&DATASET(&NUM)' NE 'CNTL').DD4B
&CNTL    SETB  1
         AGO   .DD7
.DD4B    AIF   ('&DATASET(&NUM)' NE 'LIST').DD5
&LIST    SETB  1
         AGO   .DD7
.DD5     AIF   ('&DATASET(&NUM)'(1,5) NE 'INDD=').DD5B
&INDD    SETB  1
&PROC    SETB  1
&DDIN    SETC  '&DATASET(&NUM)'(6,(K'&DATASET(&NUM)-5))
         AGO   .DD7
.DD5B    AIF    ('&DATASET(&NUM)'(1,7) NE 'MEMBER=').DD6
&MEMBR   SETB   1
&MEMAD   SETC   '&DATASET(&NUM)'(8,(K'&DATASET(&NUM)-6))
         AGO     .DD7
.DD6     AIF   ('&DATASET(&NUM)'(1,6) NE 'OUTDD=').DD65
&OUTDD   SETB  1
&PROC    SETB  1
&DDOUT   SETC  '&DATASET(&NUM)'(7,(K'&DATASET(&NUM)-6))
         AGO   .DD7
.DD65    AIF   ('&DATASET(&NUM)' NE 'SEQ').ERR7
&SEQ     SETB  1
.DD7     ANOP
&NUM     SETA  &NUM-1
         AIF   (&NUM GT 0).DD2
         AIF   ('&MF' EQ 'L').RETURNL
         AGO   .RETURNE
.WARN1   MNOTE 0,'FIRST STORAGE OPERAND IS ASSUMED TO BE IN PARM BLOCK'
         AGO   .E10
.WARN2   MNOTE 0,'FIRST STORAGE OPERAND IS OMITTED'
         DC    4A(0)           STORAGE SOURCE DESCRIPTOR ADDR
         MEXIT
.WARN3   MNOTE 0,'PARAMETER LIST ADDR IN MF PARAMETER IS NOT IN REG 1'
         AGO   .E1A
.WARN4   MNOTE 0,'ENTRY POINT ADDR IN ENTRY PARAMETER IS NOT IN REG 15'
         AGO   .E11A
.ERR1    MNOTE 8,'INVALID OR OMITTED MF PARAMETER'
         MEXIT
.ERR2    MNOTE 8,'INVALID STORAGE PARAMETER OPERAND'
         MEXIT
.ERR3    MNOTE 8,'TERM, STORAGE, DATASET, AND DELETE ARE MUTUALLY EXCLU*
               SIVE'
         MEXIT
.ERR4    MNOTE 8,'INVALID TERM OPERAND'
         MEXIT
.ERR5    MNOTE 8,'INVALID DELETE OPERAND'
         MEXIT
.ERR6    MNOTE 8,'POSITIONAL PARAMETERS ARE INVALID'
         MEXIT
.ERR7    MNOTE 8,'INVALID DATASET PARAMETER'
         MEXIT
.ERR9    MNOTE 8,'PROCN OR PROCP SPECIFIED AND KEYWORD OTHER THAN PROMP*
               T FOUND'
         MEND
