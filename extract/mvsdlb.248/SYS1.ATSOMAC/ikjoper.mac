         MACRO
&LABEL   IKJOPER &TYPNAM,&OPERND1=,&OPERND2=,&RSVWD=,&HELP=,           *
               &VALIDCK=,&PROMPT=,&DEFAULT=,&CHAIN=
.**********************************************************************
.* TITLE  -  IKJOPER MACRO (SYMBOLIC DEBUG PROJECT)
.*
.* FUNCTION  -  THE IKJOPER MACRO IS USED BY COMMAND PROCESSORS TO
.*              DEFINE TO PARSE THE EXPECTED FORMAT OF AN EXPRESSION
.*              TYPE PARAMETER.
.*
.* INPUT  -  THE PROTOTYPE STATEMENT IS AS DEFINED ABOVE.  IT'S
.*           PARAMETERS REPRESENT THE FOLLOWING:
.*               &LABEL   - STATEMENT LABEL IS REQUIRED
.*               &TYPNAM  - A PARAMETER TYPE IS REQUIRED
.*               &OPERND1 - SPECIFIES THE PCE-LABEL WHICH WILL
.*                          DESCRIBE THE FIRST OPERAND OF THE
.*                          EXPRESSION.
.*               &OPERND2 - SPECIFIES THE PCE-LABEL WHICH WILL
.*                          DESCRIBE THE SECOND OPERAND OF THE
.*                          EXPRESSION.
.*               &RSVWD   - SPECIFIES THE PCE-LABEL WHICH WILL
.*                          DESCRIBE POSSIBLE RESERVE WORDS IN
.*                          THE EXPRESSION.
.*               &HELP    - SUPPLIES SECOND-LEVEL MESSAGES.
.*               &VALIDCK - SPECIFIES THE ADDRESS OF A USER-SUPPLIED
.*                          VALIDITY CHECK EXIT ROUTINE.
.*               &PROMPT  - SUPPLIES DATA TO BE USED IF PROMPTING FOR
.*                          A MISSING PARAMETER.
.*               &DEFAULT - SUPPLIES VALUE TO BE USED IF THE PARAMETER
.*                          IS NOT ENTERED
.*               &CHAIN -   ADDRESS OF A TERM ADDRESS-TYPE PCE
.*
.* OUTPUT - THIS MACRO PRODUCES A PARAMETER CONTROL ENTRY (PCE) TO BE
.*          PASSED TO THE PARSE SERVICE ROUTINE.
.*          IF AN ERROR IS ENCOUNTERED IN PROCESSING THE MACRO
.*          INSTRUCTION ENTERED BY THE CP, AN APPROPRIATE ERROR MESSAGE
.*          IS PRINTED AND THE PCE IS NOT GENERATED.
.*
.**********************************************************************
.*
.* THE FOLLOWING CODE DEFINES VARIABLES TO BE USED IN THE MACRO
.*   EXPANSION.
.*       GLOBALS   -   IKJCSNM - CSECT NAME FROM IKJPARM MACRO LABEL.
.*                     IKJDSNM - DSECT NAME FOR PDL
.*       LOCALS(ARITHMETIC) -  T - USED AS COUNTER FOR HELP MESSAGES.
.*                             END - SECOND COUNTER FOR HELP MESSAGES.
.*                             PDESIZE - AMOUNT OF SPACE FOR PDE
.*             (BINARY)  -  FLAGS FOR VARIOUS OPTIONS AND TO INDICATE
.*                          THAT NO PCE IS TO BE GENERATED.
.*             (CHARACTER)  -  QUOTE- USED WHEN QUOTES ARE MISSING
.*                                    FROM &TYPNAM.
.*
         GBLC  &IKJCSNM,&IKJDSNM
         LCLA  &T,&END,&PDESIZE
         LCLB  &FPRPT,&FDFLT,&FHELP,&FVALID,&FENDBIT,&FCHN
         LCLC  &QUOTE
&FENDBIT SETB  0
.*
.* DETERMINE IF EXTRANEOUS POSITIONAL PARAMETERS HAVE BEEN SPECIFIED.
.*
         AIF   (N'&SYSLIST LT 2).GOOD
         MNOTE 8,'ONLY ONE POSITIONAL PARAMETER ALLOWED'
&FENDBIT SETB  1
.*
.* DETERMINE IF TYPE NAME HAS BEEN ENTERED.
.*
.GOOD    AIF   ('&TYPNAM' EQ '').BAD
         AGO   .GOOD1
.BAD     MNOTE 8,'PARAMETER TYPE NAME MISSING BUT REQUIRED'
&FENDBIT SETB  1
.*
.* DETERMINE IF CSECT NAME IS MISSING.
.*
.GOOD1   AIF   ('&IKJCSNM' NE '').GOOD2
         MNOTE 8,'CSECT NAME (FROM IKJPARM MACRO LABEL) IS MISSING'
&FENDBIT SETB  1
.*
.* DETERMINE IF STATEMENT LABEL WAS ENTERED.
.*
.GOOD2   AIF   ('&LABEL' NE '').GOOD3
         MNOTE 8,'STATEMENT LABEL MISSING BUT REQUIRED'
&FENDBIT SETB  1
.GOOD3   ANOP
.*
.* THE FOLLOWING CODE SETS FLAGS TO INDICATE OPTIONS SPECIFIED ON THE
.*     MACRO INSTRUCTION.
.*
&FPRPT   SETB  ('&PROMPT' NE '')
&FDFLT   SETB  ('&DEFAULT' NE '')
&FHELP   SETB  ('&HELP' NE '')
&FVALID  SETB  ('&VALIDCK' NE '')
&FCHN    SETB  ('&CHAIN' NE '')
.*
.* PROMPT AND DEFAULT CANNOT BOTH BE SPECIFIED.
.*
.OPT6    AIF   (NOT(&FDFLT AND &FPRPT)).OPT2
         MNOTE 8,'PARAMETER CANNOT BOTH DEFAULT AND REQUIRE PROMPTING'
&FENDBIT SETB  1
.*
.* THE FOLLOWING CODE CHECKS THAT OPERND1, OPERND2, AND RSVWD HAVE
.*     BEEN ENTERED.
.*
.OPT2    AIF   ('&OPERND1' NE '').OPT3
         MNOTE 8,'''OPERND1'' REQUIRED BUT MISSING'
&FENDBIT SETB  1
.OPT3    AIF   ('&OPERND2' NE '').OPT4
         MNOTE 8,'''OPERND2'' REQUIRED BUT MISSING'
&FENDBIT SETB  1
.OPT4    AIF   ('&RSVWD' NE '').OPT5
         MNOTE 8,'''RSVWD'' REQUIRED BUT MISSING'
&FENDBIT SETB  1
.*
.* IF AN ERROR HAS BEEN ENCOUNTERED DO NOT GENERATE THE PCE.
.*
.OPT5    AIF   (NOT(&FENDBIT)).GEN
         MEXIT
.GEN     ANOP
&IKJCSNM CSECT ,                     RESUME PCL CSECT
* FLAGS - BIT3=PROMPT,  BIT4=DEFAULT, BIT6=HELP,
*         BIT7=VALIDCK
DENT&SYSNDX EQU *
&LABEL   EQU   *
         DC    BL2'111&FPRPT&FDFLT.0&FHELP&FVALID.00000000' FLAGS
         DC    AL2(DEND&SYSNDX-DENT&SYSNDX)      LENGTH OF PCE
         DC    AL2(LAB&SYSNDX-&IKJDSNM)          OFFSET TO PDE
.*
.* THE FOLLOWING CODE SUPPLIES QUOTES TO &TYPNAM IF THEY ARE MISSING
.*
&QUOTE   SETC  '&TYPNAM'(1,1)
         AIF   ('&QUOTE' EQ '''').QUOT
&QUOTE   SETC  ''''
         DC    AL2(IKJ@&SYSNDX-*),AL2(18),C&QUOTE&TYPNAM&QUOTE   PARAME*
               TER TYPE MESSAGE SEGMENT
         AGO   .EQUGEN1
.QUOT    ANOP
         DC    AL2(IKJ@&SYSNDX-*),AL2(18),C&TYPNAM  PARAMETER TYPE MESS*
               AGE SEGMENT
.EQUGEN1 ANOP
IKJ@&SYSNDX EQU *
         DC    AL2(&RSVWD-&IKJCSNM)     OFFSET TO RESERVE WORD PCE
         DC    AL2(&OPERND1-&IKJCSNM)   OFFSET TO FIRST OPERAND PCE
         DC    AL2(&OPERND2-&IKJCSNM)   OFFSET TO SECOND OPERAND PCE
         AIF   (&FCHN).DCHERE
         DC    AL2(0)                   NO VAR TERM PCE
         AGO   .CONT
.DCHERE  DC    AL2(&CHAIN-&IKJCSNM)       OFFSET TO VAR TERM PCE
.CONT    AIF   (&FPRPT).PRPT
         AIF   (NOT &FDFLT).NPRDF
         DC    AL1(IKJ$&SYSNDX-*-2),C&DEFAULT     DEFAULT DATA
         AGO   .EQUGEN
.PRPT    DC    AL1(IKJ$&SYSNDX-*-2),C&PROMPT      PROMPT DATA
.EQUGEN  ANOP
IKJ$&SYSNDX EQU *
.NPRDF   ANOP
         AIF   (NOT &FHELP).NOHELP
         DC    AL2(IKJA&SYSNDX-*)                 LENGTH OF HELP DATA
&T       SETA  1
.HELPLP  ANOP
&T       SETA  &T+1
         AIF   (&T EQ 256).HELPER
         AIF   (&T LE N'&HELP).HELPLP
         AGO   .HELPCT
.HELPER  MNOTE 4,'ONLY 255 LEVELS OF HELP AVAILABLE. OTHERS IGNORED'
.HELPCT  ANOP
&T       SETA  &T-1
         DC    AL1(&T)                       NUMBER OF HELP MESSAGES
&END     SETA  &T
&T       SETA  1
.HELPLPA ANOP
         DC    AL2(IKJ&SYSNDX.&T-*),AL2(00),C&HELP(&T) HELP MSG SEGMENT
IKJ&SYSNDX.&T EQU *
&T       SETA  &T+1
         AIF   (&T LE &END).HELPLPA
IKJA&SYSNDX EQU *
.NOHELP  AIF   (NOT &FVALID).NOVALID
         DC    AL3(&VALIDCK)         VALIDITY CHECK ROUTINE ADDRESS
.NOVALID ANOP
DEND&SYSNDX EQU *
&IKJDSNM DSECT
&PDESIZE SETA  2
LAB&SYSNDX   DS    &PDESIZE.F             ALLOCATE PDE SPACE
&SYSECT  CSECT ,                      RESUME USERS CSECT
         MEXIT
         MEND
