         MACRO
&NAME    STCC  &ATN,&LD=,&CD=
.**********************************************************************
.*                                                                    *
.*                                                                    *
.*RELEASE 23 DELETIONS                                                *
.*                                                                    *
.*RELEASE 22 DELETIONS                                                *
.*                                                                    *
.*RELEASE 21 DELETIONS                                                *
.*                                                                    *
.*STATUS - CHANGE LEVEL 000                                           *
.*                                                                    *
.*NAME - STCC                                                         *
.*                                                                    *
.*FUNCTION - THE STCC MACRO ALLOWS THE USER TO SPECIFY TERMINAL       *
.*   CONTROL CHARACTERS FOR LINE DELETING AND CHARACTER DELETING.     *
.*                                                                    *
.*                                                                    *
.*       THE MACRO PROTOTYPE IS ABOVE                                 *
.*                                                                    *
.*       THE STANDARD FORM OF EXPANSION IS BELOW                      *
.*                                                                    *
.*       CNOP  0,4                                                    *
.*&NAME  BAL   1,*+8                    BRANCH PAST DC'S              *
.*       DC    AL1(176)                 FLAG BYTE                     *
.*       DC    X'00'                    NOT USED                      *
.*       DC    C'$'                     LINE-DELETER CHAR             *
.*       DC    C'?'                     CHAR-DELETER CHAR             *
.*       L     1,0(1)                   LOAD PARM IN REG              *
.*       LA    0,7                      LOAD ENTRY CODE               *
.*       SLL   0,24                     PUT ENTRY CODE IN HIGH ORDER  *
.*                                      BYTE OF REGISTER              *
.*       SVC   94                       ISSUE SVC                     *
.*                                                                    *
.**********************************************************************
         LCLA  &SW
         AIF   ('&ATN' EQ  '' AND '&LD' EQ '' AND '&CD' EQ  '').ERR
         AIF   ('&ATN' EQ '').NOTHING
         AIF   ('&ATN' EQ 'NATN').NOATT
         AIF   ('&ATN' EQ 'ATTN').ATT
         AGO   .ERROR
.ATT     ANOP
&SW      SETA  X'C0'                    FLAG FOR ATTN SPECIFIED
         AGO   .CKLD                    GO TO CHECK LD
.NOTHING ANOP
&SW      SETA  X'00'                    FLAG FOR 1ST OPERAND NOT THERE
         AGO   .CKLD                    GO TO CHECK LD
.NOATT   ANOP
&SW      SETA  X'80'                    FLAG FOR NATN SPECIFIED
.CKLD    AIF   ('&LD' EQ '').CKCD
&SW      SETA  &SW+X'20'                FLAG FOR LD SPECIFIED
.CKCD    AIF   ('&CD' EQ '').STUPFLG
&SW      SETA  &SW+X'10'                FLAG FOR CD SPECIFIED
.STUPFLG ANOP
         CNOP  0,4
&NAME    BAL   1,*+8                    REG 1 POINTS AT DC LIST
         DC    AL1(&SW)                 FLAG BYTE
         DC    X'00'                    NOT USED
         AIF   ('&LD' EQ '').LD0
         AIF   ('&LD'(1,1) EQ '(').LD0
         DC    &LD                      LD CHAR
         AGO   .CHKCD
.LD0     DC    X'00'                    LD NOT SPECIFIED OR LD IN REG
.CHKCD   AIF   ('&CD' EQ '').CD0
         AIF   ('&CD'(1,1) EQ '(').CD0
         DC    &CD                      CD CHAR
         AGO   .LOAD
.CD0     DC    X'00'                    CD NOT SPECIFIED OR CD IN REG
.LOAD    L     1,0(1)                   LOAD PARM
         AIF   ('&LD' EQ '').CKCDREG
         AIF   ('&LD'(1,1) NE '(').CKCDREG
         LR    0,&LD                    PUT LD CHAR IN REG 0
         SLL   0,8                      SHIFT LD CHAR LEFT ONE BYTE
         OR    1,0                      PUT LD CHAR IN REG 1
.CKCDREG AIF   ('&CD'EQ '').ENTRYCD
         AIF   ('&CD'(1,1) NE '(').ENTRYCD
.CDREG   OR    1,&CD                    PUT CD CHAR IN REG 1
.ENTRYCD LA    0,7                      LOAD ENTRY CODE
         SLL   0,24                     PUT ABOVE IN HIGH ORDER BYTE
         SVC   94                       ISSUE SVC
         MEXIT
.ERROR   IHBERMAC 54,,&ATN
         MEXIT
.ERR     IHBERMAC   24
         MEND
