         MACRO
&NAME    STTMPMD &A,&KEYS=NO
.**********************************************************************
.*                                                                    *
.* STATUS - NEW MACRO                                           ZA24648
.*                                                                    *
.* NAME - STTMPMD   (SET TMP         MODE)                            *
.*                                                                    *
.* FUNCTION - THE STTMPMD MACRO IS USED TO SPECIFY WHETHER OR NOT     *
.*            AN APPLICATION IS PRESENTING DISPLAYS TO A TSO          *
.*            DISPLAY TERMINAL AS THE TERMINAL CONTROLLER.            *
.*                                                                    *
.*       THE MACRO PROTOTYPE IS ABOVE                                 *
.*                                                                    *
.*       THE STANDARD FORM OF THE EXPANSION IS BELOW                  *
.*&NAME  LA    1,128+64                 SET PARM FLAGS                *
.*       SLL   1,24                     SHIFT TO HIGH ORDER BYTE      *
.*       LA    0,20                     LOAD ENTRY CODE               *
.*       SLL   0,24                     SHIFT TO HIGH ORDER BYTE      *
.*       SVC   94                       TERMINAL CONTROL MACRO SVC    *
.*                                                                    *
.**********************************************************************
         LCLA  &SYSTMP
         LCLA  &SYSKEYS
&SYSTMP  SETA  128
&SYSKEYS SETA  0
         AIF   ('&KEYS' NE 'ALL' AND '&KEYS' NE 'NO').ERROR1
         AIF   ('&KEYS' EQ 'NO').TESTA
&SYSKEYS SETA  64
.TESTA   ANOP
         AIF   ('&A' EQ 'ON' OR '&A' EQ '').FSON
         AIF   ('&A' NE 'OFF').ERROR
.FSOFF   ANOP
&SYSTMP  SETA  0
.FSON    ANOP
&NAME    LA    1,&SYSTMP+&SYSKEYS       SET PARM FLAGS
         SLL   1,24                     SHIFT TO HIGH ORDER BYTE
         LA    0,20                     LOAD ENTRY CODE
         SLL   0,24                     SHIFT TO HIGH ORDER BYTE
         SVC   94                       TERMINAL CONTROL MACRO SVC
         MEXIT
.ERROR   IHBERMAC 36,,&A
         MEXIT
.ERROR1  IHBERMAC 54,,,
         MEND
