         MACRO
&NAME    STLINENO &LINE=,&LINELOC=,&MODE=OFF      PROTOTYPE
.**********************************************************************
.*                                                                    *
.* STATUS - CHANGE LEVEL 000                                          *
.*                                                                    *
.* NAME - STLINENO   (SET LINE NUMBER FOR NEXT TPUT EDIT TO 3270)     *
.*                                                                    *
.* FUNCTION - THE STLINENO MACRO IS USED TO SPECIFY ON WHICH LINE OF  *
.*            A TSO/VTAM DISPLAY SCREEN THE NEXT NON-FULL SCREEN TPUT *
.*            IS TO APPEAR.                                           *
.*                                                                    *
.*       THE MACRO PROTOTYPE IS ABOVE                                 *
.*                                                                    *
.*       THE STANDARD FORM OF THE EXPANSION IS BELOW                  *
.*                                                                    *
.*&NAME  LA    1,3                      PUT LINE NUM IN PARM REG      *
.*       LA    0,19                     LOAD ENTRY CODE               *
.*       SLL   0,24                     SHIFT TO HIGH ORDER BYTE      *
.*       SVC   94                       TERMINAL CONTROL MACRO SVC    *
.*                                                                    *
.**********************************************************************
         AIF   ('&LINE' NE '' AND '&LINELOC' NE '').ERROR1
         AIF   ('&LINE' EQ '' AND '&LINELOC' EQ '').ERROR2
         AIF   ('&MODE' NE 'OFF' AND '&MODE' NE 'ON').ERROR1
         AIF   ('&LINELOC' NE '').LNADDR
         AIF   ('&LINE'(1,1) EQ '(').LNREG
&NAME    LA    1,&LINE                  PUT LINE NUM IN PARM REG
         AGO   .STMODE
.LNREG   ANOP
&NAME    LR    1,&LINE(1)               PUT LINE NUM IN PARM REG
         AGO   .STMODE
.LNADDR  ANOP
         AIF   ('&LINELOC'(1,1) EQ '(').LNADREG
&NAME    L     1,&LINELOC               PUT LINE NUM IN PARM REG
         AGO   .STMODE
.LNADREG ANOP
&NAME    L     1,0(,&LINELOC(1))        PUT LINE NUM IN PARM REG
.STMODE  ANOP
         AIF   ('&MODE' EQ 'OFF').STMDOFF
         LA    0,128                    PREPARE FLAY BYTE
         SLL   0,24                     SHIFT TO HIGH ORDER BYTE
         OR    1,0                      SET MODE=ON FLAG IN REG 1
         AGO   .ENTRYCD
.STMDOFF ANOP
         SR    0,0                      PREPARE FLAG BYTE
         OR    1,0                      SET MODE=OFF FLAG IN REG 1
.ENTRYCD ANOP
         LA    0,19                     LOAD ENTRY CODE
         SLL   0,24                     SHIFT TO HIGH ORDER BYTE
         SVC   94                       TERMINAL CONTROL MACRO SVC
         MEXIT
.ERROR1  IHBERMAC 54,,,
         MEXIT
.ERROR2  IHBERMAC 24,,,
         MEND
