//SMFEXITS     JOB      MSGLEVEL=1                                     *
//                       *                                             C
//       THE SAMPLE SMF EXITS ARE - IEFUJV - JOB VALIDATION            C
//                                  IEFUJI - JOB INITIATION            C
//                                  IEFACTRT JOB TERMINATION           C
//                                  IEFUTL - TIME LIMIT                C
//                                  IEFU83 - SVC 83              Y01034*
//                                  IEFU29 - SMF WRITER EXIT   @Z40FPPF
//                       *                                             C
//         THERE IS NO EXAMPLE FOR IEFUSI, IEFUSO OR IEFUJP            *
//                                      *                              C
//       THEY ARE INTENDED TO DEMONSTRATE ONE FORM OF EXIT PROCESSING  C
//            AND ILLUSTRATE THE USE OF THE TEST PROGRAM,TESTEXIT.     C
//                       *                                             C
//       PROCEDURE:                                                    C
//             EACH EXIT ROUTINE WILL ASSEMBLE AND LINKEDIT INTO A     C
//               DATASET,EXITLIB,UNDER THEIR RESPECTIVE MEMBER NAMES.  C
//               THE NEXT JOB(TESTEXIT) WILL COMBINE THE EXIT ROUTINES C
//                WITH THE TEST PROGRAM.                               C
//                       *                                             C
//             IF THE SAMPLE ROUTINES ARE ACCEPTABLE FOR GENERAL SYSTEMC
//              IMPLEMENTATION,THE FOLLOWING ACTION IS REQUIRED TO     C
//              INCORPORATE THEM INTO AN EXISTING SYSTEM.              C
//                 1.REASSEMBLE EACH EXIT WITHOUT THE PRIVATE MACRO,   C
//                    SMFWTM.  THIS PRIVATE MACRO INTERFACES DIRECTLY  C
//                    WITH TESTEXIT.  THE STANDARD SMFWTM MACRO IS     C
//                    AVAILABLE IN SYS1.MACLIB.                        *
//                       *                                             C
//                 2.THE LINKEDIT PROCEDURE TO INCORPORATE THE SAMPLE  *
//                    EXIT MODULES IS IN THE SMF SRL GC35-0004.        *
//*    A006500,046500-046800,833100-840221                       Y01034
//IEFUJV    EXEC  ASMFCL
//ASM.SYSIN  DD  *
         TITLE 'IEFUJV'
         MACRO                GENERATES CALLING SEQUENCE FOR TSMFWTM
&NAME    SMFWTM &MSGAD
         AIF   ('&MSGAD' EQ '').E1                NO OPERAND
         AIF   ('&MSGAD' EQ '(1)').BAL            REG 1 ALREADY LOADED
         AIF   ('&MSGAD'(1,1) EQ '(').REGA        REG(NOT 1) SPECIFIED
         AGO   .LODIT                             ADDRESS SPECIFIED
.E1      MNOTE '* * * NO OPERAND SPECIFIED * * *'
         MEXIT
.BAL     ANOP
         CNOP  0,4
&NAME    BAL   15,*+8
.LIST    DC    V(TSMFWTM)                         ADDRESS OF TSMFWTM
         L     15,0(15)
         BALR  14,15
         MEXIT
.REGA    ANOP
&NAME    LR    1,&MSGAD(1)
         CNOP  0,4
         BAL   15,*+8
         AGO   .LIST
.LODIT   ANOP
&NAME    LA    1,&MSGAD
         CNOP  0,4
         BAL   15,*+8
         AGO   .LIST
         MEND
IEFUJV   CSECT
* STATUS - CHANGE LEVEL 1
* FUNCTION
*    THIS MODULE CHECKS A TWO CARD JOB CARD SEQUENCE TO
*    DETERMINE IF IT MEETS THE FOLLOWING REQUIREMENTS:
*    - FIRST CARD MUST HAVE AN 8 CHARACTER ACCOUNT NUMBER IN
*    COLUMNS 16-23. THIS ACCOUNT NUMBER WILL BE VALIDATED BY
*    SEARCHING A TABLE OF VALID ACCOUNT NUMBERS.
*    - SECOND CARD MUST CONTAIN THE PRTY, REGION AND TIME
*    PARAMETERS IN THAT ORDER. THE VALUES OF THESE PARAMETERS
*    WILL BE COMPARED WITH MAXIMUM VALUES IN A TABLE USING AN
*    INDEX DERIVED FROM THE ACCT #.
*    IF THESE REQUIREMENTS ARE NOT SATISFIED, THE JOB IS CANCELED.
*    THIS MODULE ALSO GETS THE TIME OF DAY AT WHICH THE JOB IS
*    ENQUEUED AND PUTS IT IN THE USER COMMUNICATIONS AREA WHERE
*    IT WILL BE USED BY THE JOB INITIATION ROUTINE (IEFUJI).
* ENTRY POINTS -
*         IEFUJV
* INPUT
*    REGISTER 1 POINTS TO A LIST OF 4 BYTE ADDRESSES FOR THE
*    FOLLOWING THREE PARAMETERS:
*    1 - 36 BYTES OF THE FOLLOWING JOB INFORMATION:
*    JOBNAME (8 BYTES)
*    TIME STAMP (8 BYTES)
*    SYSTEM ID (4 BYTES)
*    USER ID (8 BYTES)
*    USER COMMUNICATIONS AREA (4 BYTES)
*    ZERO FOR INITIAL ENTRY, FIRST BYTE IS USED AS A
*    BRANCH CODE WHEN PROCESSING THE JOB CARD SEQUENCE.
*    TIME OF DAY FOR JOB INITIATION ROUTINE IS PUT
*    HERE WHEN THE JOB ENQUEUE ENTRY IS TAKEN.
*    2 - 80 BYTES CONTAINING THE JCL STATEMENT IMAGE.
*    3 - 1 BYTE ENTRY CODE WITH ONE OF THE FOLLOWING VALUES:
*    0 - NULL STATEMENT
*    1 - JOB STATEMENT
*    2 - EXEC STATEMENT
*    4 - DD STATEMENT
*    8 - PROC STATEMENT
*    16 - JOB ENQUEUE (SECOND PARAMETER IS MEANINGLESS)
* OUTPUT
*    REGISTER 15 MUST CONTAIN ONE OF THE FOLLOWING RETURN CODES:
*    0 - CONTINUE PROCESSING
*    4 - CANCEL
* EXTERNAL REFERENCES - NONE
* EXITS,NORMAL - RETURN TO CALLER
* EXITS,ERROR - NONE
* TABLES/WORK AREAS -
*    TABLE - TAB OF VALID ACCT NOS.
*    RGNTBL - TAB OF VALID REGION SIZES
*    TIMETBL - TAB OF VALID TIME SPECS
* ATTRIBUTES - STANDARD
* CHARACTER CODE DEPENDENCY - NONE
* NOTES - GETMAIN FREEMAIN AND TIME MACROS USED
*        EJECT
REGA     EQU   2                       @ OF 36 BYTES OF JOB INFO
REGB     EQU   3                       @ OF 80 BYTE JCL STMT
REGC     EQU   4                       @ OF 1 BYTE ENTRY CODE TRY CODE
REGD     EQU   5                       WORK REG FOR BR CODE
REGE     EQU   6                       LOOP CONTROL
REGF     EQU   7                       LOOP CNTRL / WORK REG
REGG     EQU   8                       LOOP CONTROL
REGH     EQU   9                       BRANCH REG
REGEVEN  EQU   14
REGODD   EQU   15
D16      EQU   16                      DISP OF 16
L28      EQU   28                      LENGTH OF 28
D23      EQU   23                      DISP OF 23
R1       EQU   1                       REG 1
D1       EQU   1                       DISP OF 1
D2       EQU   2                       DISP OF 2
L2       EQU   2                       LENGTH OF 2
D22      EQU   22                      DISP OF 22
D7       EQU   7                       DISP OF 7
L7       EQU   7                       LENGTH OF 7
D240     EQU   240                     DISP OF 240
D21      EQU   21                      DISP OF 21
D5       EQU   5                       DISP OF 5
L5       EQU   5                       REG 5
D20      EQU   20                      DISP OF 20
D8       EQU   8                       DISP OF 8
L8       EQU   8                       LENGTH OF 8
L4       EQU   4                       LENGTH OF 4
D4       EQU   4                       DISP OF 4
D32      EQU   32                      DISP OF 32
D12      EQU   12                      DISP OF 12
R12      EQU   12                      REG 12
R14      EQU   14                      REG 14
R13      EQU   13                      REG 13
D0       EQU   0                       DISP OF 0
R0       EQU   0                       REG 0
R11      EQU   11                      REG 11
D15      EQU   15                      DISP OF 15
R15      EQU   15                      REG 15
M2       EQU   2                       MASK OF 2
X10      EQU   X'10'                   HEX 10
X01      EQU   X'01'
XF0      EQU   X'F0'
XF9      EQU   X'F9'
X00      EQU   X'00'
M4       EQU   4                       MASK OF 4
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
         USING *,R15
         B     HERE                    BRANCH AROUND MODULE NAME
         DC    CL8'IEFUJV'             MODULE NAME
HERE     STM   R14,R12,D12(R13)        STORE CALLERS REGS
         DROP  R15
         BALR  R11,R0                  SET UP
         USING *,R11                   BASE
         LM    REGA,REGC,D0(R1)         GET PARAMETER ADDRESSES
         TM    D0(REGC),X10            TEST FOR JOB ENQUEUE ENTRY
         BO    JOBENQ                  PROCESS JOB ENQUEUE ENTRY
         TM    D0(REGC),X01            TEST FOR JOB CARD ENTRY
         BZ    OKEXIT                  NORMAL RETURN
         SR    REGC,REGC
         IC    REGC,D32(REGA)           GET CARD BRANCH CODE
         B     *+D4(REGC)              BRANCH TABLE
         B     ACCTCRD                 - TO ACCT CARD
         B     PARMCRD                 - TO PARAMETER CARD
         B     OKEXIT                  - TO NORMAL EXIT
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
*   PROCESS FIRST CARD OF JOB CARD SEQUENCE                           *
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
ACCTCRD  LM    REGE,REGG,LOOPCTL
NXTACCT  CLC   D15(L8,REGB),D0(REGG)      CHECK FOR VALID ACCOUNT NUM
         BE    OKACCT                  PROCESS ACCT NO.
         BXLE  REGG,REGE,NXTACCT       TEST NEXT ACCT NO.
         MVC   D20(L8,REGA),INVALID      INDICATE INVALID ACCT NO
         B     CANCEL                  CANCEL - INVALID ACCT NO.
OKACCT   MVC   D20(L8,REGA),D15(REGB)     PUT ACCT NUMBER IN USER ID
         LA    REGD,D4(R0)                SET BRANCH CODE FOR PARM CARD
         STC   REGD,D32(REGA)            STORE BRANCH CODE
         B     OKEXIT                  NORMAL RETURN
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
*   PROCESS SECOND CARD OF JOB CARD SEQUENCE                          *
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
PARMCRD  CLC   D15(L5,REGB),PRTYLTRL      CHECK FOR 'PRTY='
         BNE   CANCEL                  CANCEL - INVALID PRTY LITERAL
         LA    REGG,D20(REGB)            SET REGG TO 1ST DIGIT OF PRTY
         LA    REGF,D2(REGG)             SET LOOP CONTROL FOR DIGIT CK
         BAL   REGH,CKDGTS               CHECK DIGITS
         CLC   D0(L8,REGG),RGNLTRL        CHECK FOR ',REGION='
         BNE   CANCEL                    CANCEL - INVALID REGION LIT
         IC    REGE,D21(REGA)            GET MAX PRTY CODE FROM ACCT #
         LA    REGE,PRTYTBL-D240(REGE)
         EX    REGEVEN,CLIPRTY           CHECK FOR VALID PRTY CODE
         BL    CANCEL                    CANCEL - INVALID PRTY CODE
         LA    REGG,D8(REGG)             SET REGG TO FIRST DIGIT OF RGN
         LA    REGF,D5(REGG)             SET LOOP CONTROL FOR DIGIT CK
         BAL   REGH,CKDGTS             CHECK DIGITS
         CLC   D0(L7,REGG),TIMELTRL       CHECK FOR 'K,TIME='
         BNE   CANCEL                  CANCEL - INVALID TIME LITERAL
         IC    REGE,D22(REGA)            GET MAX REGION CODE FROM ACCT
         LA    REGF,D15
         NR    REGE,REGF                REMOVE ZONE BITS FROM CODE
         SLL   REGE,L2                   MULTIPLY CODE BY 4
         C     REGEVEN,RGNTBL(REGE)     DETERMINE IF THE SPECIFIED
*                                       REGION IS =< THE MAXIMUM FOR
*                                       THE SPECIFIC ACCT
         BH    CANCEL                  CANCEL - INVALID REGION FOR ACCT
         SR    REGD,REGD                ZERO TIME ACCUMULATION REGISTER
         LA    REGG,D7(REGG)             SET REGG TO 1ST CHAR OF TIME
         CLI   0(REGG),C'('             CHECK FOR TIME PARAM
         BNE   MINUTES                 PROCESS MINUTES
         SR    REGC,REGC                REGC IS USED AS A FLAG TO
*                                       INDICATE PARENTHESIS
         LA    REGG,D1(REGG)
         CLI   0(REGG),C','            CHECK FOR SECONDS PARAM
         BE    SECONDS                 PROCESS SECONDS
MINUTES  LA    REGF,D4(REGG)             SET LOOP CONTROL
         LR    REGB,REGG                SAVE ADDR OF 1ST CHAR OF TIME
         BAL   REGH,CKDGTS             CHECK DIGITS
         CR    REGB,REGG                TEST FOR NO DIGITS
         BE    CANCEL                  CANCEL - TOO MANY DIGITS
         SLL   REGEVEN,L8
         LR    REGD,REGEVEN             PUT MINUTES IN ACCUMULATION REG
         LTR   REGC,REGC                TEST PARENTHESIS FLAG
         BNZ   CKTIME                   BRANCH FOR NO PARENTHESIS
         CLI   0(REGG),C','            CHECK FOR SECONDS PARAM
         BE    SECONDS                 PROCESS SECONDS
         CLI   0(REGG),C')'            CHECK FOR ENDING PAREN
         BNE   CANCEL                  CANCEL - UNBALANCED PARENS
         B     CKTIME                  PROCESS TIME
SECONDS  LA    REGG,D1(REGG)
         LA    REGF,D2(REGG)             SET LOOP CONTROL
         BAL   REGH,CKDGTS             CHECK DIGITS
         CH    REGEVEN,MAXSEC          CHECK AMOUNT OF TIME
         BH    CANCEL                  CANCEL - TOO MUCH TIME SPEC.
         AR    REGD,REGEVEN             ADD SECONDS TO ACCUM. REG
CKTIME   IC    REGE,D23(REGA)            GET MAX TIME CODE FROM ACCT #
         LA    REGF,D15
         NR    REGE,REGF                REMOVE ZONE BITS FROM CODE
         SLL   REGE,L2                   MULTIPLY CODE BY FOUR
         C     REGD,TIMETBL(REGE)       CHECK MAX TIME FOR ACCT
         BH    CANCEL                 CANCEL - TOO MUCH TIME SPEC.
         LA    REGD,D8(R0)                SET BRANCH CODE FOR OKEXIT
         STC   REGD,D32(REGA)            STORE BRANCH CODE
         B     OKEXIT                 NORMAL RETURN
CKDGTS   SR    REGEVEN,REGEVEN
         LA    REGE,D1                   SET LOOP INCREMENT
NXTDGT   CLI   0(REGG),XF0            CHECK FOR NUMERIC CHARACTER
         BCR   M4,REGH                NOT NUMERIC
         CLI   0(REGG),XF9
         BC    M2,CANCEL              NOT NUMERIC
         IC    REGODD,0(REGG)
         SLL   REGODD,L28                ISOLATE AND SAVE DECIMAL PART
         SLDL  REGEVEN,L4                  OF EACH EBCDIC DIGIT
         BXLE  REGG,REGE,NXTDGT       TEST NEXT DIGIT
         B     CANCEL                   CANCEL IF TOO MANY DIGITS
CLIPRTY  CLI   0(REGE),X00
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
*   GET TIME OF DAY THAT JOB WAS ENQUEUED                             *
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
JOBENQ   GETMAIN    R,LV=8              GET AREA FOR TEMP STORAGE
         LR    REGB,R1
         TIME       BIN                 GET TIME OF JOB ENQUEUE
         ST    R0,D0(REGB)                PUT TIME IN INTERMEDIATE AREA
         MVC   D32(L4,REGA),0(REGB)       MOVE TIME TO USER COMM AREA
         FREEMAIN   R,LV=8,A=(REGB)
OKEXIT   SR    REGA,REGA               SET RETURN CODE TO ZERO
         B     EXIT                   RETURN
CANCEL   LA    REGA,D4(R0)               SET RETURN CODE TO FOUR
EXIT     ST    REGA,D16(R13)             PUT RETURN CODE IN SAVE AREA
         LM    R14,R12,D12(R13)
         BR    R14                    RETURN
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
*   DEFINE CONSTANTS AND TABLES FOR VALIDITY CHECKS                   *
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
LOOPCTL  DC    F'8'                   COUNT OF 8 FOR LOOPING
         DC    A(TABLEND)             @ OF END OF TABLE
         DC    A(TABLE)               @ OF BEGINNING OF TABLE
TABLE    DC    C'A2224567'            TAB OF VALID
         DC    C'B3235678'            ACCOUNT
         DC    C'C4444444'            NUMBERS
TABLEND  EQU   *-8
RGNTBL   DS    0F
         DC    X'00000032'            32 K
         DC    X'00000064'            64 K
         DC    X'00000128'            128 K
         DC    X'00000256'            256 K
         DC    X'00000512'            512 K
TIMETBL  DS    0F
         DC    X'00000030'              30 SECONDS
         DC    X'00000330'              3 MINUTES AND 30 SECONDS
         DC    X'00001000'              10 MINUTES
         DC    X'00003000'              30 MINUTES
         DC    X'00144000'              1440 MINUTES (NO TIME LIMIT)
         DS    0H
MAXSEC   DC    X'0059'                MAXIMUM SECONDS
PRTYTBL  DC    X'0102030413'          PRIORTY TABLE
PRTYLTRL DC    C'PRTY='               PRIORTY LITERAL
RGNLTRL  DC    C',REGION='            REGION LITERAL
TIMELTRL DC    C'K,TIME='             TIME LITERAL
INVALID  DC    C'INVALID '            INVALID LITERAL
         END
//LKED.SYSLMOD  DD  DSNAME=EXITLIB,VOLUME=SER=231400,UNIT=2314,        *
//             DISP=(NEW,KEEP),SPACE=(TRK,(5,1,2))
//LKED.SYSIN  DD  *
    NAME  IEFUJV
//IEFUJI  EXEC  ASMFCL
//ASM.SYSIN DD *
         TITLE 'IEFUJI'
         MACRO                GENERATES CALLING SEQUENCE FOR TSMFWTM
&NAME    SMFWTM &MSGAD
         AIF   ('&MSGAD' EQ '').E1                NO OPERAND
         AIF   ('&MSGAD' EQ '(1)').BAL            REG 1 ALREADY LOADED
         AIF   ('&MSGAD'(1,1) EQ '(').REGA        REG(NOT 1) SPECIFIED
         AGO   .LODIT                             ADDRESS SPECIFIED
.E1      MNOTE '* * * NO OPERAND SPECIFIED * * *'
         MEXIT
.BAL     ANOP
         CNOP  0,4
&NAME    BAL   15,*+8
.LIST    DC    V(TSMFWTM)                         ADDRESS OF TSMFWTM
         L     15,0(15)
         BALR  14,15
         MEXIT
.REGA    ANOP
&NAME    LR    1,&MSGAD(1)
         CNOP  0,4
         BAL   15,*+8
         AGO   .LIST
.LODIT   ANOP
&NAME    LA    1,&MSGAD
         CNOP  0,4
         BAL   15,*+8
         AGO   .LIST
         MEND
IEFUJI   CSECT
*C 365000,386000                                                 A48478
*C 234000                                                      @YM04803
* STATUS - CHANGE LEVEL 1
* FUNCTION
*    THIS MODULE CALCULATES THE AMOUNT OF TIME THAT A JOB WAS
*    ENQUEUED AWAITING INITIATION. THIS TIME AND THE EFFECTIVE JOB
*    PRIORITY ARE WRITTEN TO THE SMF DATA SET AS RECORD TYPE 128.
* ENTRY POINTS -
*         IEFUJI
* INPUT
*    REGISTER 1 POINTS TO A LIST OF 4 BYTE ADDRESSES FOR THE
*    FOLLOWING FOUR PARAMETERS:
*    1 - 36 BYTES OF THE FOLLOWING JOB INFORMATION:
*    JOBNAME (8 BYTES)
*    TIME STAMP (8 BYTES)
*    SYSTEM ID (4 BYTES)
*    USER ID (8 BYTES)
*    USER COMMUNICATIONS AREA (4 BYTES)
*    WHEN ENTERED, THIS FIELD CONTAINS THE TIME OF DAY
*    AT WHICH THE JOB WAS ENQUEUED (SUPPLIED BY THE
*    IEFUJV ROUTINE). THIS FIELD IS ZEROED BEFORE THE
*    ROUTINE IS EXITED.
*    2 - 20 BYTES CONTAINING THE PROGRAMMERS NAME
*    3 - 1 BYTE INDICATING THE EFFECTIVE JOB PRIORITY
*    4 - JOB ACCOUNTING FIELDS (NOT USED)
* OUTPUT
*    A 47 BYTE RECORD IS WRITTEN TO THE SMF DATA SET USING THE
*    SMFWTM MACRO. THE FORMAT OF THIS RECORD IS DESCRIBED BY THE
*    DSECT FOR THE SMF RECORD.
*    REGISTER 15 MUST CONTAIN ONE OF THE FOLLOWING RETURN CODES:
*    0 - CONTINUE PROCESSING
*    4 - CANCEL
* EXTERNAL REFERENCES -  NONE
* EXITS,NORMAL -  RETURN TO CALLER     RC=0
* EXITS,ERROR - RETURN TO CALLER    RC=4 - CANCEL REQUEST
* TABLES/WORK AREAS -  DSECT FOR SMF RECORD EMPLOYED
* ATTRIBUTES - STANDARD
* CHARACTER CODE DEPENDENCY - NONE
* NOTES - GETMAIN FREEMAIN SMFWTM AND TIME MACROS WERE USED
         EJECT
REGA     EQU   2                       @ OF 36 BYTES OF JOB INFO
REGC     EQU   4                       @ OF 1 BYTE WITH EFF JOB PRTY
REGD     EQU   5                       BASE REG FOR DSECT
D20      EQU   20                      DISP OF 20
L8       EQU   8                       LENGTH OF 8
L16      EQU   16                      LENGTH OF 16
L6       EQU   6                       LENGTH OF 6
L1       EQU   1                       LENGTH OF 1
M2       EQU   2                       MASK OF 2
D16      EQU   16                      DISP OF 16
D12      EQU   12                      DISP OF 12
D0       EQU   0                       DISP OF 0
D32      EQU   32                      DISP OF 32
R15      EQU   15                      REG 15
R14      EQU   14                      REG 14
R13      EQU   13                      REG 13
R12      EQU   12                      REG 12
R11      EQU   11                      REG 11
R0       EQU   0                       REG 0
R1       EQU   1                       REG 1
L4       EQU   4                       LENGTH OF 4
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
         USING *,R15
         B     HERE                   BRANCH AROUND MODULE NAME
         DC    CL8'IEFUJI'             MODULE NAME
HERE     STM   R14,R12,D12(R13)        SAVE REGS
         DROP  R15
         BALR  R11,R0                  SET UP BASE
         USING *,R11                   REGISTER
         LM    REGA,REGC,D0(R1)        GET PARAMETER ADDRESSES
         GETMAIN   R,LV=56             GET CORE FOR SMF RECORD   A48478
         LR    REGD,R1
         USING SMFDSECT,REGD           BASE FOR SMF DSECT
         TIME      BIN
         ST    R0,TIME                 TIME FOR SMF RECORD
         ST    R1,DATE                 DATE FOR SMF RECORD
         MVC   ENQTIME(L4),D32(REGA)   GET JOB ENQUEUE TIME
         XC    D32(L4,REGA),D32(REGA)  ZERO COMMUNICATIONS WORD
         C     R0,ENQTIME              CHECK FOR A NEW DAY
         BC    M2,SAMEDAY              ENQUEUED SAME DAY
         A     R0,HR24                 ADD 24 HOURS (86400 SECONDS)
SAMEDAY  S     R0,ENQTIME              DETERMINE TIME JOB WAS ON QUEUE
         ST    R0,ENQTIME              PUT TIME IN SMF RECORD
         MVC   PRTY(L1),D0(REGC)       GET PRIORITY CODE
         MVC   SYSID(L4),D16(REGA)     GET SYSTEM ID
         MVC   HEADER(L6),SMFRCD       GET HEADER
         MVC   JOBLOG(L16),D0(REGA)    GET JOB LOG
         MVC   USERID(L8),D20(REGA)    GET USER ID
         LA    R1,SMFOUT               GET SMF BUFFER @
         SMFWTM    (1)                 WRITE SMF RECORD TYPE 128
         LR    R1,REGD
         FREEMAIN  R,LV=56,A=(1)                                 A48478
         LM    R14,R12,D12(R13)        RESTORE REGS
         XR    R15,R15                 SET RETURN CODE TO ZERO
         BR    R14                     RETURN
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
*   DEFINE DATA CONSTANTS                                             *
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
HR24     DC    F'8640000'              24 HRS IN SECS
SMFRCD   DC    X'002F00000280'         SMF RECORD              @YM04803
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
*   DEFINE DSECT FOR JOB INITIATION RECORD (TYPE X'80')               *
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
SMFDSECT DSECT
         DS    1H                      ALIGN TIME/DATE ON FWD
SMFOUT   EQU   *
HEADER   DS    6C                      HEADER
TIME     DS    1F                      TIME
DATE     DS    1F                      DATE
SYSID    DS    1F                      SYSTEM ID
JOBLOG   DS    4F                      JOB LOG
USERID   DS    2F                      USER ID
ENQTIME  DS    1F                      ENQUEUE TIME
PRTY     DS    1C                      PRIORTY
         END
//LKED.SYSLMOD DD DSNAME=EXITLIB,VOLUME=SER=231400,UNIT=2314,DISP=OLD
//LKED.SYSIN  DD *
     NAME IEFUJI
//IEFACTRT EXEC ASMFCL
//ASM.SYSIN DD *
         TITLE 'IEFACTRT'
         MACRO                GENERATES CALLING SEQUENCE FOR TSMFWTM
&NAME    SMFWTM &MSGAD
         AIF   ('&MSGAD' EQ '').E1                NO OPERAND
         AIF   ('&MSGAD' EQ '(1)').BAL            REG 1 ALREADY LOADED
         AIF   ('&MSGAD'(1,1) EQ '(').REGA        REG(NOT 1) SPECIFIED
         AGO   .LODIT                             ADDRESS SPECIFIED
.E1      MNOTE '* * * NO OPERAND SPECIFIED * * *'
         MEXIT
.BAL     ANOP
         CNOP  0,4
&NAME    BAL   15,*+8
.LIST    DC    V(TSMFWTM)                         ADDRESS OF TSMFWTM
         L     15,0(15)
         BALR  14,15
         MEXIT
.REGA    ANOP
&NAME    LR    1,&MSGAD(1)
         CNOP  0,4
         BAL   15,*+8
         AGO   .LIST
.LODIT   ANOP
&NAME    LA    1,&MSGAD
         CNOP  0,4
         BAL   15,*+8
         AGO   .LIST
         MEND
IEFACTRT CSECT
*A 541001                                                         M4736
*C 501500,508000,509500,510500                                   A40917
*D 542000-593000,637000,638000,659000-671000                     A40917
*A 523500,523600,526500-526984,641500-641700                     A40917
* STATUS - CHANGE LEVEL 1
* FUNCTION
*    THIS MODULE IS ENTERED AT JOB/STEP TERMINATION. IF THE
*    MODULE WAS ENTERED FOR STEP TERMINATION, THE CPU TIME IS
*    TIME IS EXAMINED FOR A FLUSH CONDITION.  IF THE STEP WAS
*    FLUSHED, THE SMF RECORD IS NOT PRINTED AND THE MODULE
*    RETURNS TO THE CALLING ROUTINE.  OTHERWISE,
*    THE SMF TERMINATION RECORD TYPE FIELD IS CHANGED AND
*    THE RECORD IS WRITTEN AS A USER RECORD BY THE SMFWTM MACRO.
*    IF THE RETURN CODE FROM THE MACRO INDICATES THAT THE RECORD
*    WAS NOT WRITTEN SUCCESSFULLY, A MESSAGE IS WRITTEN TO THE
*    CONSOLE INDICATING AN ERROR CONDITION.  FOR JOB
*    TERMINATION, A RECORD IS WRITTEN TO THE SYSOUT DATA SET USING
*    THE IEFYS ROUTINE.
* ENTRY POINTS -
*         IEFACTRT
* INPUT
*    REGISTER 0 CONTAINS A BINARY 16 IF THE MODULE WAS ENTERED
*    FOR JOB TERMINATION.  IT CONTAINS A BINARY 12 IF ENTERED
*    FOR STEP TERMINATION.
*    REGISTER 1 POINTS TO A LIST OF 4 BYTE ADDRESSES FOR THE
*    FOLLOWING TEN PARAMETERS:
*    1 - 36 BYTES OF THE FOLLOWING JOB INFORMATION:
*    JOBNAME (8 BYTES)
*    TIME STAMP (8 BYTES)
*    SYSTEM ID (4 BYTES)
*    USER ID (8 BYTES)
*    USER COMMUNICATIONS AREA (4 BYTES)
*    THIS FIELD IS ZEROED AT JOB TERMINATION.
*    2 - STEP NAME (ADDRESS IS ZERO FOR JOB TERM ENTRY)
*    3 - PROGRAMMERS NAME
*    4 - JOB EXECUTION TIME (NOT USED BY THIS ROUTINE)
*    5 - JOB ACCOUNTING FIELDS (NOT USED)
*    6 - STEP EXECUTION TIME
*    7 - STEP ACCOUNTING FIELDS (NOT USED)
*    8 - FLAGS AND STEP NUMBER (NOT USED)
*    9 - TERMINATION STATUS (NOT USED)
*    10 - SMF TERMINATION RECORD
*    REGISTER 0 CONTAINS ONE OF THE FOLLOWING ENTRY CODES:
*    12 - STEP TERMINATION ENTRY
*    16 - JOB TERMINATION ENTRY
* OUTPUT
*    THE JOB/STEP TERMINATION RECORD CREATED BY SMF IS WRITTEN TO
*    THE SMF DATA SET AFTER THE RECORD TYPE FIELD HAS BEEN
*    MODIFIED.
*    AT JOB TERM. THE RECORD DEFINED BY THE JOBMSG DSECT IS
*    WRITTEN TO THE SYSOUT DATA SET USING THE IEFYS ROUTINE.
*    AN ERROR MESSAGE IS WRITTEN TO THE CONSOLE IF THE SMF
*    JOB/STEP TERM. RECORD CANNOT BE WRITTEN SUCCESSFULLY.
*    REGISTER 15 CONTAINS A RETURN CODE OF 0 INDICATING THAT THE
*    JOB SHOULD BE CONTINUED.
*    REGISTER 1 CONTAINS A RETURN CODE OF 4 INDICATING THAT THE
*    SMF TERMINATION RECORD SHOULD NOT BE WRITTEN.
* EXTERNAL REFERENCES
*         IEFYS
* EXITS,NORMAL - RETURN TO CALLER
* EXITS,ERROR - NONE, RETURN CODES INDICATE ACTION
* TABLES/WORK AREAS - DSECTS FOR WTO RCD AND JOB MSG
* ATTRIBUTES - STANDARD
* CHARACTER CODE DEPENDENCY - NONE
* NOTES -
*    GETMAIN,FREEMAIN,EXTRACT,TIME,SMFWTM MACROS WERE USED
         EJECT
REG0     EQU   0                       JOB/STEP TERM INDICATOR   A40917
REGA     EQU   2                       @ OF 36 BYTES OF JOB INFO
REGB     EQU   3                       @ OF STEPNAME
REGC     EQU   4                       @ OF PGMR'S NAME
REGD     EQU   5                       @ OF SMF TERM RECORD
REGE     EQU   6                       WORK REGISTER
REGF     EQU   7                       LENGTH FOR GETMAIN
REGG     EQU   8                       WORK REGISTER             A40917
REGH     EQU   9                       BASE REGISTER FOR DSECTS
REGI     EQU   10                      @ AND VALUE OF CPU TIME   A40917
TYPE     EQU   5
CPUSTM   EQU   20                                                A40917
SMFPTR   EQU   36
GETLNGTH EQU   128
TIOTDDL  EQU   24
L18      EQU   18                      LENGTH OF 18
L31      EQU   31                      LENGTH OF 31
L20      EQU   20                      LENGTH OF 20
L8       EQU   8                       LENGTH OF 8
L73      EQU   73                      LENGTH OF 73
L4       EQU   4                       LENGTH OF 4
L16      EQU   16                      LENGTH OF 16
L2       EQU   2                       LENGTH OF 2
D20      EQU   20                      DISP OF 20
D42      EQU   42                      DISP OF 42
D36      EQU   36                      DISP OF 36
D32      EQU   32                      DISP OF 32
D8       EQU   8                       DISP OF 8
D6       EQU   6                       DISP OF 6
D12      EQU   12                      DISP OF 12
D24      EQU   24                      DISP OF 24
D0       EQU   0                       DISP OF 0
D126     EQU   126                     DISP OF 126
XF0      EQU   X'F0'                   ZONE FOR ERROR CODES
R15      EQU   15                      REG 15
R14      EQU   14                      REG 14
R13      EQU   13                      REG 13
R12      EQU   12                      REG 12
R11      EQU   11                      REG 11
R0       EQU   0                       REG 0
R1       EQU   1                       REG 1
L3       EQU   3                       REG 3
L13      EQU   13                      LENGTH OF 13
L1       EQU   1                       LENGTH OF 1
R2       EQU   2                       REG 2
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
         USING *,R15
         B     HERE                    BRANCH AROUND MODULE NAME
         DC    CL8'IEFACTRT'           MODULE NAME
HERE     STM   R14,R12,D12(R13)        ENTRY LINKAGE
         DROP  R15
         LA    REGA,L4
         ST    REGA,D24(R13)           SET REG 1 RETURN CODE TO 4
         BALR  R11,R0                  SET UP
         USING *,R11                   ADDRESSABILITY
         LM    REGA,REGC,D0(R1)        GET PARAMETER ADDRESSES
         L     REGD,SMFPTR(R1)         GET SMF TERM. RECORD ADDRESS
         L     REGI,CPUSTM(R1)         GET ADDR OF CPU STP TIME  A40917
         LR    REGG,REG0               SAVE 'REASON FOR ENTRY'   A40917
         LA    REGF,GETLNGTH(R0)
         GETMAIN   R,LV=(REGF)
         LR    REGH,R1                 SET BASE REG FOR DSECT
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
*  ELIMINATE SMF RECORD TYPE 4 IF STEP IS BEING  FLUSHED              *
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
         LA    REGE,L16                                          A40917
         CR    REGG,REGE               IS IT JOB TERMINATION     A40917
         BE    CONT                    YES, SKIP CPU TIME CHECK  A40917
         CLC   FLSHFLG(L3),D0(REGI)    ZERO CPU TIME?            A40917
         BE    EXIT                    RETURN W.O. WRITING REC   A40917
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
*   CONVERT SMF RECORD TYPE AND WRITE IT                              *
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
CONT     SR    REGG,REGG
         IC    REGG,TYPE(REGD)         GET RECORD TYPE
         LA    REGG,D126(REGG)         CHANGE RECORD TYPE
         STC   REGG,TYPE(REGD)
         SMFWTM     (REGD)             WRITE SMF RECORD TYPE 130/131
         LA    REGG,L4                 SET COMPARAND REG TO 4
         CR    R15,REGG                TEST SMFWTM RETURN CODE TO
*                                      DETERMINE IF RECORD WAS WRITTEN
         BH    ERRWTO                  IF NOT, WRITE ERROR MSG
         LTR   REGB,REGB               TEST FOR ZERO STEP NAME ADDRESS
*                                      INDICATING ENTRY FOR JOB TERM
         BZ   JOBTERM                  YES, JOB TERM
         B    EXIT                     FINISHED IF STEP TERM      M4736
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
*   WRITE ERROR MESSAGE TO CONSOLE                                    *
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
         USING WTORCD,REGH             BASE FOR WTORCD DSECT
ERRWTO   CVD   R15,MSGAREA             CONVERT RETURN CODE TO DECIMAL
         UNPK  ERRCODE(L2),MSGAREA+D6(R2) CONVERT RETURN CODE TO EBCDIC
         OI    ERRCODE,XF0             SET ZONE ON LOW ORDER DIGITS
         OI    ERRCODE+L1,XF0
         MVC   MSGAREA(L73),WTOMSG     SET UP CONSOLE MESSAGE
         MVC   JOBNAME(L8),D0(REGA)    FILL IN JOBNAME
         WTO       MF=(E,(REGH))
         LTR   REGB,REGB               TEST FOR ZERO STEP NAME ADDRESS
*                                      INDICATING ENTRY FOR JOB TERM.
         BNZ   EXIT                    IF NOT, EXIT
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
*   WRITE JOB TERMINATION RECORD TO SYSOUT                            *
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
JOBTERM  GETMAIN   R,LV=256            GET SAVE AREA FOR IEFYS
         ST    R1,D8(R13)
         LR    REGE,R13                SAVE POINTER TO REG SAVE AREA
         LR    R13,R1                  REG 13 POINTS TO 64 WORD AREA
         XC    D32(L4,REGA),D32(REGA)  ZERO USER COMM AREA
         USING JOBMSG,REGH
         ST    REGH,0(REGH)            PUT ADDRESS IN A TEMP LOCATION
         MVC   D36(L4,R12),D0(REGH)    MSG ADDRESS FOR IEFYS
         MVC   D42(L2,R12),MSGLEN      MSG LENGTH FOR IEFYS
         MVC   MSG1(L31),MSGTXT1       SET UP SYSOUT MSG
         MVC   JOB(L8),D0(REGA)        CONTAINING
         MVC   MSG2(L13),MSGTXT2       JOB NAME
         MVC   ACCTNO(L8),D20(REGA)    ACCOUNTING NO.
         MVC   MSG3(L18),MSGTXT3       AND
         MVC   PGRNAME(L20),D0(REGC)   PGMR NAME
         MVC   MSG4(L18),MSGTXT1
         L     R15,ADDRYS
         BALR  R14,R15                 BRANCH TO IEFYS ROUTINE AND
*                                      WRITE MSG TO SYSOUT
         LR    R13,REGE                RESTORE SAVE AREA POINTER
         FREEMAIN  R,LV=256,A=(1)
EXIT     LR    R1,REGH
         FREEMAIN  R,LV=GETLNGTH,A=(1)
         LM    R14,R12,D12(R13)        RESTORE REGS
         SR    R15,R15                 SET REG 15 RETURN CODE TO 0
         BR    R14                     RETURN
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
*   DEFINE CONSTANTS FOR STEP FLUSH CHECK
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
FLSHFLG  DC  X'000000'                 CPU TIME FOR STEP FLUSH   A40917
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
*   DEFINE CONSTANTS FOR CONSOLE MESSAGE                              *
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
WTOMSG   DS    0F
         DC    AL2(MSGEND-*)           MESSAGE LENGTH
         DC    AL2(0)                  SPACING
         DC    C'SMF JOB/STEP TERMINATION RECORD NOT WRITTEN FOR ' MSG
         DC    C'JJJJJJJJ. ERROR CODE = '                         TEXT
         DS    2C                      PAD FOR ERROR CODE
MSGEND   EQU   *
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
*   DEFINE CONSTANTS FOR SYSOUT JOB TERMINATION MESSAGE               *
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
MSGLEN   DC    AL2(ENDMSG-MSG1)        SYSOUT JOB TERM MSG
MSGTXT1  DC    C'   *** JOB END ***   JOBNAME = '        TEXT
MSGTXT2  DC    C'     ACCT# = '        CONTAINING ACCT NO.
MSGTXT3  DC    C'     PROGRAMMER = '   AND PGMR NAME
ADDRYS   DC    V(IEFYS)                @ OF IEFYS (EXT ROUTINE)
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
*   DEFINE STORAGE FOR CONSOLE MESSAGE                                *
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
WTORCD   DSECT
MSGAREA  DS    52C                     MSG BUFFER
JOBNAME  DS    8C                      JOB NAME
         DS    13C                     SPACING
ERRCODE  DS    2C                      ERROR CODE
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
*   DEFINE STORAGE FOR SYSOUT JOB TERMINATION MESSAGE                 *
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
JOBMSG   DSECT
MSG1     DS    31C                     MSG TEXT
JOB      DS    8C                      JOB NAME
MSG2     DS    13C                     TEXT
ACCTNO   DS    8C                      ACCT NO.
MSG3     DS    18C                     TEXT
PGRNAME  DS    20C                     PGMR NAME
MSG4     DS    18C                     AND TEXT
ENDMSG   EQU   *
         END
//LKED.SYSLMOD DD DSNAME=EXITLIB,VOLUME=SER=231400,UNIT=2314,DISP=OLD
//LKED.SYSIN  DD *
     NAME IEFACTRT
//IEFUTL  EXEC  ASMFCL
//ASM.SYSIN DD *
         TITLE 'IEFUTL'
         MACRO                GENERATES CALLING SEQUENCE FOR TSMFWTM
&NAME    SMFWTM &MSGAD
         AIF   ('&MSGAD' EQ '').E1                NO OPERAND
         AIF   ('&MSGAD' EQ '(1)').BAL            REG 1 ALREADY LOADED
         AIF   ('&MSGAD'(1,1) EQ '(').REGA        REG(NOT 1) SPECIFIED
         AGO   .LODIT                             ADDRESS SPECIFIED
.E1      MNOTE '* * * NO OPERAND SPECIFIED * * *'
         MEXIT
.BAL     ANOP
         CNOP  0,4
&NAME    BAL   15,*+8
.LIST    DC    V(TSMFWTM)                         ADDRESS OF TSMFWTM
         L     15,0(15)
         BALR  14,15
         MEXIT
.REGA    ANOP
&NAME    LR    1,&MSGAD(1)
         CNOP  0,4
         BAL   15,*+8
         AGO   .LIST
.LODIT   ANOP
&NAME    LA    1,&MSGAD
         CNOP  0,4
         BAL   15,*+8
         AGO   .LIST
         MEND
IEFUTL   CSECT
*C 450000                                                      @YM04803
*C TO GIVE RC8 AND TIME EXTENSION IN SECONDS TO TESTEXIT       @ZA17320
* STATUS - CHANGE LEVEL 1
* FUNCTION
*    THIS MODULE IS ENVOKED WHEN THE JOB TIME, STEP TIME OR
*    JOB WAIT TIME LIMIT IS EXCEEDED. THE JOB IS CANCELED IF THE
*    JOB CPU TIME LIMIT EXPIRES. IF THE STEP CPU TIME LIMIT    @ZA17320
*    EXPIRES, THE TIME WILL BE EXTENDED BY A FIXED NUMBER OF   @ZA17320
*    TIMER UNITS. IF THE CONTINUOUS WAIT TIME LIMIT EXPIRES,   @ZA17320
*    THE TIME WILL BE EXTENDED BY A FIXED NUMBER OF SECONDS.   @ZA17320
*    IF MORE THAN THREE EXTENSIONS ARE REQUIRED, THE JOB WILL  @ZA17320
*    BE CANCELLED. EACH TIME A JOB WAIT TIME IS EXCEEDED, A    @ZA17320
*    RECORD IS WRITTEN TO THE SMF DATA SET.                    @ZA17320
* ENTRY POINTS -
*         IEFUTL
* INPUT
*    REGISTER 1 POINTS TO A 4 BYTE ADDRESS FOR THE FOLLOWING
*    PARAMETER:
*    36 BYTES OF THE FOLLOWING JOB INFORMATION:
*    JOBNAME (8 BYTES)
*    TIME STAMP (8 BYTES)
*    SYSTEM ID (4 BYTES)
*    USER ID (8 BYTES)
*    USER COMMUNICATIONS AREA (4 BYTES) - THE FIRST BYTE
*    IS USED AS AN EXTENSION INDICATOR. IT IS ZERO FOR
*    THE INITIAL ENTRY AND CONTAINS AN INCREMENTED
*    VALUE FOR SUBSEQUENT ENTRIES.
*    REGISTER 0 WILL CONTAIN ONE OF THE FOLLOWING ENTRY CODES
*    INDICATING WHICH TIME LIMIT WAS EXCEEDED:
*             0 - JOB CPU TIME
*             4 - STEP CPU TIME
*             8 - JOB WAIT TIME
* OUTPUT
*    EACH TIME THE JOB WAIT TIME LIMIT IS EXCEEDED, A 43 BYTE
*    RECORD IS WRITTEN TO THE SMF DATA SET USING THE SMFWTM MACRO.
*    THE FORMAT OF THIS RECORD IS DESCRIBED BY THE DSECT FOR THE
*    SMF RECORD.
*    IF THE TIME LIMIT IS EXTENDED, THE AMOUNT OF THE EXTENSION
*    IN TIMER UNITS MUST BE RETURNED IN REGISTER 1.
*    REGISTER 15 MUST CONTAIN ONE OF THE FOLLOWING RETURN CODES:
*             0 - CONTINUE PROCESSING (CANCEL THE JOB)
*             4 - CONTINUE THE JOB WITH THE TIME EXTENSION IN REG 1
*                 IN TIMER UNITS.                              @ZA17320
*             8 - CONTINUE THE JOB WITH THE TIME EXTENSION IN  @ZA17320
*                 REG1 IN SECONDS.                             @ZA17320
* EXTERNAL REFERENCES - NONE
* EXITS,NORMAL - RETURN TO CALLER
* EXITS,ERROR - NONE
* TABLES/WORK AREAS - DSECT FOR SMF RECORD
* ATTRIBUTES - STANDARD
* CHARACTER CODE DEPENDENCY - NONE
* NOTES - GETMAIN,FREEMAIN,TIME,SMFWTM MACROS WERE USED
         EJECT
REGA     EQU   2                       @ OF 36 BYTES OF JOB INFO
REGB     EQU   3                       TIME EXTENSION
REGC     EQU   4                       DSECT BASE
REGD     EQU   5                       WORK REG (CONTAINS 4)
D32      EQU   32                      DISP OF 32
D4       EQU   4                       DISP OF 4
D24      EQU   24                      DISP OF 24
D12      EQU   12                      DISP OF 12
D0       EQU   0                       DISP OF 0
D16      EQU   16                      DISP OF 16
D20      EQU   20                      DISP OF 20
D33      EQU   33                      DISP OF 33              @ZA17320
D1       EQU   1                       DISP OF 1               @ZA17320
L4       EQU   4                       LENGTH OF 4
L16      EQU   16                      LENGTH OF 16
L8       EQU   8                       LENGTH OF 8
L6       EQU   6                       LENGTH OF 6
R15      EQU   15                      REG 15
R14      EQU   14                      REG 14
R13      EQU   13                      REG 13
R12      EQU   12                      REG 12
R11      EQU   11                      REG 11
R0       EQU   0                       REG 0
R1       EQU   1                       REG 1
REG6     EQU   6                                               @ZA17320
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
         USING *,R15
         B     HERE                     BRANCH AROUND MOD NAME
         DC    CL8'IEFUTL'              MODULE NAME
HERE     STM   R14,R12,D12(R13)         STANDARD
         DROP  R15
         BALR  R11,R0                   ENTRY LINKAGE
         USING *,R11
         L     REGA,D0(R1)              GET PARAMETER ADDRESS
         LA    REGD,L4                  SET COMPARAND REGISTER TO 4
         CR    R0,REGD                  TEST FOR JOB/STEP TIME EXCEEDED
         BL    CANCEL                   JOB CPU TIME EXCEEDED  @ZA17320
         GETMAIN   R,LV=56              GET CORE FOR SMF RECORD
         LR    REGB,R0                 SET ENTRY CODE          @ZA17320
         LR    REGC,R1
         USING SMFDSECT,REGC
         TIME      BIN
         ST    R0,TIME                  TIME FOR SMF RECORD
         ST    R1,DATE                  DATE FOR SMF RECORD
         MVC   SYSID(L4),D16(REGA)      BUILD SMF RECORD
         MVC   JOBLOG(D16),D0(REGA)     CONTAINING JOB LOG NO.
         MVC   USERID(L8),D20(REGA)     USER ID
         MVC   HEADER(L6),SMFRCD        AND HEADER
         SR    REGB,REGB
         IC    REGB,D32(REGA)           GET TIME EXTENSION INDICATOR
         STC   REGB,EXTCODE             PUT EXTENSION CODE IN RECORD
         LA    R1,SMFOUT
         SMFWTM    (1)                  WRITE SMF RECORD TYPE 132
         LR    R1,REGC
         FREEMAIN  R,LV=56,A=(1)
         LA    REGD,L4                  SET COMPERAND VALUE    @ZA17320
         CR    REGB,REGD                CHECK STEP CPU TIME    @ZA17320
         BH    PROC8                    NO, GO PROC WAIT TIME  @ZA17320
         TM    D33(REGA),X'01'          1ST ENT FOR STEP TIME  @ZA17320
         BO    CANCEL                   NO, CANCEL STEP        @ZA17320
         OI    D33(REGA),X'01'          SET 1ST TIME INDICATOR @ZA17320
         B     TIMUN                    PROCESS TIMER UNITS    @ZA17320
PROC8    SR    REG6,REG6                CLEAR REGISTER         @ZA17320
         IC    REG6,D32(REGA)           GET ENTRY INDICATOR    @ZA17320
         CH    REG6,CONS1               UTL MORE THAN TWICE    @ZA17320
         BH    CANCEL                   YES, NOT ALLOWED       @ZA17320
         LA    REG6,D1(REG6)            INCREMENT ENTRY VALUE  @ZA17320
         STC   REG6,D32(REGA)           SAVE NEW VALUE         @ZA17320
         MVC   D24(L4,R13),SECEXT       PUT SECONDS IN R1      @ZA17320
         B     INCEXT                                          @ZA17320
TIMUN    MVC   D24(L4,R13),TIMEXT       PUT TIMER UNITS IN R1  @ZA17320
INCEXT   LR    REGD,REGB                SAVE CODE              @ZA17320
         B     EXIT                     RETURN
CANCEL   SR    REGD,REGD                SET RETURN CODE VALUE TO 0
EXIT     ST    REGD,D16(R13)            PUT RETURN CODE IN REG 15 AREA
         LM    R14,R12,D12(R13)         RESTORE REGS
         BR    R14                      RETURN
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
*   DEFINE DATA CONSTANTS                                             *
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
TIMEXT   DC    F'2307692'               ONE MINUTE TIME EXTENSION
SECEXT   DC    F'00000060'              SIXTY SEC TIME EXT     @ZA17320
SMFRCD   DC    X'002B00000284'          SMF RECORD             @YM04803
CONS1    DC    H'0001'                                         @ZA17320
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
*   DEFINE DSECT FOR JOB WAIT TIME LIMIT RECORD (TYPE X'84')          *
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
SMFDSECT DSECT
         DS    1H                       ALIGN TIME/DATE ON FWD
SMFOUT   EQU   *
HEADER   DS    6C                       HEADER
TIME     DS    1F                       TIME
DATE     DS    1F                       DATE
SYSID    DS    1F                       SYSTEM ID
JOBLOG   DS    16C                      JOB LOG
USERID   DS    8C                       USER ID
EXTCODE  DS    1C                       EXIT CODE
         END
//LKED.SYSLMOD DD DSNAME=EXITLIB,VOLUME=SER=231400,UNIT=2314,DISP=OLD
//LKED.SYSIN  DD *
    NAME IEFUTL
//IEFU83    EXEC  ASMFCL                                         Y01034
//ASM.SYSIN  DD  *                                               Y01034
         TITLE 'IEFU83'                                          Y01034
   MACRO                                                         Y01034
&LABEL   WTOR   &MSG,&REPLY,&RLENGTH,&ECB                        Y01034
&LABEL   B    IHB0003                                            Y01034
IHB0001  DC   A(IHB0002)                                         Y01034
IHB0002  DC   CL20'KD (5,10,15'   INVALID CALL                   Y01034
   DC CL20'K ALL'                                                Y01034
   DC CL20'D,ALL'                                                Y01034
   DC CL20'K (1,3,34,125)'                                       Y01034
   DC CL20' D (0,3,48,80,240)'                                   Y01034
IHB0003  L    1,IHB0001                                          Y01034
   MVC   0(20,&REPLY),0(1)                                     @ZA10833
   LA 1,20(1)                                                    Y01034
   CLI   0(1),X'58'                                              Y01034
   BNE   IHB0004                                                 Y01034
   LA 1,IHB0002                                                  Y01034
IHB0004  ST   1,IHB0001                                          Y01034
   MEND                                                          Y01034
   MACRO                                                         Y01034
&LABL WAIT    &ECB=                                              Y01034
&LABL NOPR    0                                                  Y01034
   MEND                                                          Y01034
   MACRO                                                         Y01034
&LBL WTO    &MSG                                                 Y01034
&LBL NOPR    0                                                   Y01034
   MEND                                                          Y01034
IEFU83   CSECT                     U83 EXIT                      Y01034
*A 564600                                                      @YM04803
*C 565000                                                      @YM04803
*A495000,497100,511500-511900,515770-51600,516600,             @ZA02385
*A518050-518150,520120-520460,559200,577050-577400             @ZA02385
*C521500                                                       @ZA02385
*C (IHB0003) APPROX. 46550000                                  @ZA10833
* STATUS - CHANGE LEVEL 1                                        Y01034
* FUNCTION                                                       Y01034
*    THIS MODULE CHECKS FOR TYPE 0 SMF RECORD AND IF SO SETS THE Y01034
*    BITMAP FOR RECORD TYPES TO BE WRITTEN TO THE MAN DATA SET.  Y01034
*    IT ACCOMPLISHES THIS BY ASKING THE OPERATOR TO SPECIFY WHICHY01034
*    TYPES ARE TO BE KEPT AND WHICH DELETED AND TRANSLATING THE  Y01034
*    REPLY INTO A BITMAP - 0 FOR DELETE , 1 FOR KEEP.            Y01034
*    FOR ALL RECORD TYPES IT THEN TESTS THE BITMAP TO SEE IF THE Y01034
*    RECORD IS TO BE WRITTEN - IF SO THE RETURN CODE IS 0, IF NOTY01034
*    THE RETURN CODE IS 4.                                       Y01034
* ENTRY POINTS -                                                 Y01034
*         IEFU83                                                 Y01034
* INPUT                                                          Y01034
*    REGISTER 1 POINTS TO A LIST OF 4 BYTE ADDRESSES FOR THE     Y01034
*    FOLLOWING TWO PARAMETERS:                                   Y01034
*    1 - THE SMF RECORD TO BE PROCESSED BY THE EXIT              Y01034
*    2 - THE SECOND ADDRESS IN THE LIST IS 0, NO 2ND PARAMETER   Y01034
* OUTPUT                                                         Y01034
*    REGISTER 15 MUST CONTAIN ONE OF THE FOLLOWING RETURN CODES: Y01034
*    0 - WRITE THE SMF RECORD (KEEP)                             Y01034
*    4 - DON'T WRITE THE SMF RECORD (DELETE)                     Y01034
* EXTERNAL REFERENCES - NONE                                     Y01034
* EXITS,NORMAL - RETURN TO CALLER                                Y01034
* EXITS,ERROR - NONE                                             Y01034
* TABLES/WORK AREAS -                                            Y01034
*    REPLY - 119 CHARACTER AREA FOR REPLY FROM OPERATOR          Y01034
*    BITMAP - 256 BIT SWITCHES, 1 FOR EACH SMF RECORD TYPE       Y01034
*    MASKS - MASKS FOR SETTING AND TESTING BITMAP                Y01034
* ATTRIBUTES - STANDARD                                          Y01034
* CHARACTER CODE DEPENDENCY - NONE                               Y01034
* NOTES - GETMAIN FREEMAIN MACROS USED                           Y01034
         EJECT                                                   Y01034
R0       EQU   0                                                 Y01034
R1       EQU   1                   PARAMETER ADDRESS             Y01034
R2       EQU   2                   MESSAGE ADDRESS               Y01034
R3       EQU   3                   OPERATOR REPLY POINTER        Y01034
R4       EQU   4                   SWITCH   0-DELETE,1-KEEP      Y01034
R5       EQU   5                   DIGIT COUNT OF RECORD NOS.    Y01034
R6       EQU   6                   BITMAP BYTE POINTER           Y01034
R7       EQU   7                   BITMAP BIT POINTER            Y01034
R8       EQU   8                   BITMAP MASK & RECORD NO.      Y01034
R9       EQU   9                   RECORD TYPE BITMAP POINTER    Y01034
R10      EQU   10                  PARM @ SAVE & BITMAP BYTE PTR Y01034
R11      EQU   11                  CVT AND SMCA PTR            @ZA02385
R12      EQU   12                  BASE REG                      Y01034
R13      EQU   13                  SAVEAREA REG                  Y01034
R14      EQU   14                  RETURN ADDRESS                Y01034
R15      EQU   15                  ENTRY ADDRESS & BASE          Y01034
D0       EQU   0                                               @ZA02385
D1       EQU   1                   DI                            Y01034
D2       EQU   2                     SP                          Y01034
D3       EQU   3                       LA                        Y01034
D4       EQU   4                         CE                      Y01034
D5       EQU   5                           ME                    Y01034
D12      EQU   12                            NT                  Y01034
D8       EQU   8                                                 Y01034
D20      EQU   20                  VAL                           Y01034
D29      EQU   29                     UES                        Y01034
TYPE0    EQU   X'00'               0 OFFSET & TYPE 0 RECORD      Y01034
BLANK    EQU   X'40'               BLANK                         Y01034
COMMA    EQU   X'6B'               COMMA                         Y01034
K        EQU   X'D2'               KEEP CODE                     Y01034
D        EQU   X'C4'               DELETE CODE                   Y01034
LP       EQU   X'4D'               LEFT PAREN                    Y01034
RP       EQU   X'5D'               RIGHT PAREN                   Y01034
XF0      EQU   X'F0'               LOWER BOUND NUMERIC           Y01034
XF9      EQU   X'F9'               UPPER BOUND NUMERIC           Y01034
XFF      EQU   X'FF'               BITMAP INITIALIZER            Y01034
C0       EQU   0                   RETURN CODE OF 0              Y01034
C4       EQU   4                   RETURN CODE OF 4              Y01034
L0       EQU   0                   LENGTH TO BE ALTERED BY EX    Y01034
L1       EQU   1                   LENGTH OF BITMASK             Y01034
L3       EQU   3                   LENGTH FOR 'ALL' COMPARE      Y01034
L8       EQU   8                   LENGTH FOR CVB                Y01034
L32      EQU   32                  LENGTH OF BITMAP              Y01034
L31      EQU   31                  LENGTH FOR BITMAP INIT        Y01034
L119     EQU   119                 REPLY FIELD LENGTH            Y01034
SAVE72   EQU   72                  SAVEAREA LENGTH             @ZA02385
MAPLEN   EQU   32                  BITMAP LENGTH               @ZA02385
GETAREA  EQU   204                 GETMAIN SIZE                @ZA02385
CVTPTR   EQU   16                  CVT LOCATION                @ZA02385
CVTSMCA  EQU   X'C4'               SMCAPTR                     @ZA02385
SMCASAVE EQU   X'80'               PTR SAVE FIELD              @ZA02385
         USING *,R15               TEMPORARY BASE FOR            Y01034
         B     HERE                BRANCHING AROUND              Y01034
         DC    CL8'IEFU83  '       MODULE NAME                   Y01034
HERE     STM   R14,R12,D12(R13)    SAVE REGS                     Y01034
         DROP  R15                 DROP TEMP BASE                Y01034
         BALR  R12,R0              SET UP                        Y01034
         USING *,R12               BASE                          Y01034
         LR    R10,R1              SAVE PTR TO PARMS             Y01034
         SR    R0,R0               CLEAR REG 0                 @ZA02385
         LA    R0,GETAREA          SET LENGTH FOR GETMAIN      @ZA02385
         GETMAIN R,LV=(R0)         GET CORE FOR REG SAVE AREA    Y01034
         XC    D0(GETAREA,R1),D0(R1) CLEAR GOTTEN AREA         @ZA02385
         ST    R1,D8(R13)          CHAIN                         Y01034
         ST    R13,D4(R1)          SAVE                          Y01034
         LR    R13,R1              AREAS                         Y01034
         USING SMFDSECT,R13        ADDRESS GOTTEN AREA         @ZA02385
         L     R11,CVTPTR          PTR TO CVT                  @ZA02385
         L     R11,CVTSMCA(R11)    PTR TO SMCA                 @ZA02385
         SR    R8,R8               CLEAR WORK                    Y01034
         L     R2,0(R10)           GET @ OF SMF RECORD           Y01034
         CLI   D5(R2),TYPE0        IS IT TYPE 0                  Y01034
         BNE   TEST                IF NOT SKIP INITIALIZATION    Y01034
         GETMAIN R,LV=MAPLEN,SP=245  GET BITMAP AREA           @ZA02385
         ST    R1,SMCASAVE(R11)    SAVE PTR TO BITMAP AREA     @ZA02385
         LR    R11,R1              PTR TO BITMAP AREA          @ZA02385
         USING BITDSECT,R11        ADDRESS BITMAP AREA         @ZA02385
WRITE    LA    R3,REPLY            REPLY ADDRESS               @ZA02385
         LA    R4,OPECB            ECB ADDRESS                 @ZA02385
         XC    OPECB,OPECB         CLEAR ECB                   @ZA02385
         XC    REPLY,REPLY         CLEAR REPLY AREA            @ZA02385
         WTOR  'REPLY IN FORM:  KD  ALL(R1,R2,..,RN)  TO INDICATE THE*
                KEEPING OR DELETING OF ALL OR PARTICULAR SMF RECORDS', *
               (3),119,(4)                                     @ZA02385
         LA    R3,REPLY-D1         LOCATE REPLY FROM OPERATOR    Y01034
         SR    R4,R4               REGISTERS                     Y01034
         MVI   BITMAP,XFF          BITMAP INITIALIZATION         Y01034
         MVC   BITMAP+D1(L31),BITMAP                             Y01034
         WAIT  ECB=OPECB                                         Y01034
KORD     LA    R3,D1(R3)                                         Y01034
         CLI   0(R3),BLANK         SCAN FOR 1ST NON-BLANK        Y01034
         BE    KORD                TEST FOR K OR D               Y01034
         CLI   0(R3),K             IS IT A K AS IN KEEP          Y01034
         BNE   NOTK                IF NOT K TEST FOR D           Y01034
         LA    R4,D1(R4)           IF SO SET SWITCH TO KEEP      Y01034
         B     SETBITS             AND GO SET THE BITMAP         Y01034
NOTK     CLI   0(R3),D             IS IT A D AS IN DELETE        Y01034
         BNE   WTO01               IF NOT HAVE OPERATOR REWRITE  Y01034
SETBITS  LA    R3,D1(R3)                                         Y01034
         CLI   0(R3),BLANK         SCAN FOR 1ST RECORD NUMBER    Y01034
         BE    SETBITS             TRY AGAIN, STILL BLANK        Y01034
ALLRECS  CLC   0(L3,R3),ALL        IS IT 'ALL'                   Y01034
         BNE   RECPREP             IF NOT PROCESS RECORD NUMBERS Y01034
SETALL   LTR   R4,R4               IF SO IS IT K OR D            Y01034
         BNZ   TEST                IF K BITMAP IS ALREADY SET    Y01034
         XC    BITMAP(L32),BITMAP  FOR D SET ALL BITS TO 0       Y01034
         B     TEST                AND GO TEST RECORD TYPE       Y01034
RECPREP  CLI   0(R3),COMMA         LEADING COMMA?                Y01034
         BNE   LPAREN              IF NOT IS IT L PAREN          Y01034
         LA    R3,D1(R3)           ELSE MOVE TO FIRST NO.        Y01034
         B     ALLRECS             TEST FOR ALL                  Y01034
LPAREN   CLI   0(R3),LP            LEFT PAREN?                   Y01034
         BNE   WTO02               IF NOT REPLY IS INVALID       Y01034
         LA    R3,D1(R3)           IF SO INCREMENT PASSED IT     Y01034
         CLC   0(L3,R3),ALL        IS IT 'ALL'                   Y01034
         BE    SETALL              IF SO SET BITMAP              Y01034
SOME     LTR   R4,R4               IF SO IS IT K OR D            Y01034
         BZ    RECS                IF D BITMAP IS ALREADY SET    Y01034
         XC    BITMAP(L32),BITMAP  FOR K SET ALL BITS TO 0       Y01034
RECS     SR    R5,R5               FOR INITIALIZATION PROCESS    Y01034
         CLI   0(R3),COMMA         IS IT A COMMA                 Y01034
         BE    GETNEXT             IF SO IGNORE IT               Y01034
         LA    R8,L3               SET BCT CTR FOR REC NO TEST   Y01034
NEXT     CLI   0(R3),XF0           TEST FOR NUMERIC              Y01034
         BL    DELIM               IF SO IS IT A DELIMITER       Y01034
         CLI   0(R3),XF9           IF NOT IT IS AN               Y01034
         BH    WTO04               INVALID REPLY                 Y01034
         LA    R5,D1(R5)           BUMP DIGIT COUNT BY 1         Y01034
         LA    R3,D1(R3)           MOVE TO NEXT DIGIT            Y01034
         BCT   R8,NEXT             AND GO PROCESS IT             Y01034
         CLI   0(R3),COMMA         IS RECORD NO. 2 DIGITS LONG   Y01034
         BE    GOTONE              IF SO GO PROCESS IT           Y01034
         CLI   0(R3),RP            COULD BE LAST NUMBER          Y01034
         BE    GOTONE              IF SO GO PROCESS IT           Y01034
         B     WTO03               ELSE REC NO TOO LONG          Y01034
DELIM    CLI   0(R3),RP            COULD BE LAST NUMBER          Y01034
         BE    GOTONE              IF SO GO PROCESS IT           Y01034
         CLI   0(R3),COMMA         IS RECORD NO. 3 DIGITS LONG   Y01034
         BNE   WTO04               IF NOT HAVE OP. REWRITE       Y01034
GOTONE   SR    R3,R5               RESET TO REC NO               Y01034
         BCTR  R5,R0               DECREMENT FOR EXECUTE OF PACK Y01034
         EX    R5,PACK             PACK THE RECORD NUMBER        Y01034
         CVB   R6,WORK             CONVERT IT TO BINARY          Y01034
         C     R6,MAX              VALID RECORD TYPE?            Y01034
         BH    WTO05               IF NOT WTOR                   Y01034
         SRDA  R6,D3               ISOLATE BITMAP BYTE AFFECTED  Y01034
         SRL   R7,D29              ISOLATE THE BITMAP BIT        Y01034
         IC    R8,MASKS(R7)        GET BITMAP MASK               Y01034
         STC   R8,SAVE72(R13)      STORE IN WORK AREA            Y01034
         LA    R6,BITMAP(R6)       SET UP @ FOR BIT SET          Y01034
         LTR   R4,R4               IS IT K OR D                  Y01034
         BZ    DLT                 IF D GO SET BIT TO 0          Y01034
KP       OC    0(L1,R6),SAVE72(R13)  SET BIT TO 1 FOR KEEP       Y01034
         B     GETNEXT             GET NEXT RECORD               Y01034
DLT      XC    0(L1,R6),SAVE72(R13)  SET BIT TO 0 FOR DELETE     Y01034
GETNEXT  LA    R3,D1(R5,R3)        INCREMENT TO NEXT RECORD NO.  Y01034
         CLI   0(R3),RP            ARE THERE ANY MORE RECNOS.    Y01034
         BNE   RECS                IF NOT GET THE NEXT RECORD NO.Y01034
TEST     L     R11,SMCASAVE(R11)   PICK UP BITMAP              @ZA02385
         IC    R8,D5(R2)           GET RECORD NUMBER FROM RECORD Y01034
         SRDA  R8,D3               ISOLATE WHICH BYTE IT'S IN    Y01034
         SRL   R9,D29              ISOLATE THE BIT               Y01034
         IC    R9,MASKS(R9)        GET THE APPROPRIATE MASK      Y01034
         LA    R10,BITMAP(R8)      GET THE BYTE ADDRESS          Y01034
         EX    R9,TM               TEST TO SEE IF BIT IS 1 OR 0  Y01034
         BZ    DELETE              IF 0 SET CODE TO DELETE       Y01034
KEEP     LA    R6,C0               IF 1 SET KEEP RETURN CODE (0) Y01034
         B     THRU                PROCESSING IS COMPLETE        Y01034
DELETE   LA    R6,C4               RETURN CODE FOR DELETE IS 4   Y01034
THRU     LR    R1,R13              ADDRESS FOR FREEMAIN          Y01034
         L     R13,D4(R13)         GET BACK @ CALLERS SAVE AREA@YM04803
         SR    R0,R0               CLEAR REG 0                 @ZA02385
         LA    R0,GETAREA          FREE GOTTEN AREA            @ZA02385
         FREEMAIN R,LV=(R0),A=(R1)                             @YM04803
         L     R14,D12(R13)        RELOAD REGISTER 14            Y01034
         LR    R15,R6              RESTORE RETURN CODE           Y01034
         LM    R0,R12,D20(R13)     RELOAD REST OF REGISTERS      Y01034
         BR    R14                 RETURN                        Y01034
MASKS    DC    X'8040201008040201' BITMAP SETTING AND TESTING    Y01034
PACK     PACK  WORK(L8),0(L0,R3)   TO PACK THE RECORD NOS.       Y01034
TM       TM    0(R10),TYPE0        TO TEST THE BITMAP FOR 0 OR 1 Y01034
ALL      DC    C'ALL'              FOR ALL RECORDS TESTING       Y01034
MAX      DC    F'255'              MAXIMUM RECORD NUMBER ALLOWED Y01034
WTO01         WTO            'OPERATION NEITHER K OR D'          Y01034
              B              WRITE                               Y01034
WTO02         WTO           'INITIAL OPERAND CHARACTERS NOT , ( OR ALL'
              B              WRITE                               Y01034
WTO03         WTO            'RECORD NUMBER MORE THAN 3 DIGITS LONG'
              B              WRITE                               Y01034
WTO04         WTO            'RECORD NUMBER NOT NUMERIC'         Y01034
              B              WRITE                               Y01034
WTO05         WTO            'RECORD NUMBER LARGER THAN 255'     Y01034
              B              WRITE                               Y01034
BITDSECT DSECT                                                 @ZA02385
BITMAP   DS    CL32                BITMAP                      @ZA02385
SMFDSECT DSECT                                                 @ZA02385
SAVEAREA DS    18F                 SAVEAREA                    @ZA02385
FLAG1    DS    CL1                 FLAG BYTE                   @ZA02385
OPECB    DS    F                   ECB FOR WTOR                @ZA02385
WORK     DS    D                   CONVERTING REC NO TO BINARY @ZA02385
REPLY    DS    CL119               OPERATOR REPLY AREA         @ZA02385
         END
//LKED.SYSLMOD DD DSNAME=EXITLIB,VOLUME=SER=231400,UNIT=2314,DISP=OLD
//LKED.SYSIN  DD *                                               Y01034
    NAME IEFU83
//IEFU29 EXEC  ASMFCL
//ASM.SYSIN DD *
         TITLE 'IEFU29'
         MACRO
&L       WTOR  ,&REPLY,&RLEN,&ECB,&MF=
&L       B     IHB0003                                         @ZA15389
IHB0001  DC    A(IHB0002)                                      @ZA15389
IHB0002  DC    CL1'U'                                          @ZA15389
IHB0003  L     1,IHB0001                                       @ZA15389
         MVC   0(1,&REPLY),0(1)                                @ZA15389
         MEND
         MACRO
&LAB     WAIT  &ECB=
&LAB     NOPR  0
         MEND
IEFU29   CSECT
* C - LABEL &L - CHANGED FROM NOPR TO PROVIDE COMPARE VALUE    @ZA15389
* STATUS - NO CHANGE                                                  *
* FUNCTION -                                                          *
*     THIS EXIT ROUTINE RECEIVES CONTROL FROM THE SMF WRITER WHEN AN  *
*     SMF DATASET NEEDS TO BE DUMPED. A WTOR WILL BE ISSUED TO        *
*     INFORM OPERATOR THAT A DUMP SHOULD BE STARTED ON THE            *
*     DATA SET WHOSE DSN WAS PASSED VIA PARAMETER LIST.               *
* ENTRY POINTS -                                                      *
*     IEFU29                                                          *
* INPUT -                                                             *
*     REGISTER 1 POINTS TO FULLWORD CONTAINING ADDRESS OF DSN         *
*     (SYS1.MANX/Y) WHICH REQUIRES DUMPING.                           *
* OUTPUT -                                                            *
*     RETURN CODE OF 0 IN REGISTER 15                                 *
* EXTERNAL REFERENCES - NONE                                          *
* EXITS,NORMAL - RETURN TO CALLER                                     *
* EXITS,ERROR - NONE                                                  *
* TABLES/WORKAREAS -                                                  *
*     WTOAREA - DSECT OVERLAYING WORK AREA OBTAINED BY GETMAIN        *
* ATTRIBUTES - STANDARD                                               *
* CHARACTER CODE DEPENDENCIES - NONE                                  *
* MACROS - GETMAIN,FREEMAIN,WTO                                       *
         EJECT
R0       EQU   0                        INPUT TO SVC 34...SET TO 0
R1       EQU   1                        ADDR PARM LISTS, GETMAINED AREA
RDSN     EQU   2                        ADDR DSN PASSED TO IEFU29
R3       EQU   3                        REPLY ADDRESS
R4       EQU   4                        ECB ADDRESS
R5       EQU   5                        MESSAGE ADDRESS
RBASE    EQU   12                       BASE REGISTER
R12      EQU   12                       LAST REG TO SAVE
RSAVE    EQU   13                       ADDR SAVE AREA
RET      EQU   14                       RETURN ADDRESS
R14      EQU   14                       FIRST REG TO SAVE
RETCODE  EQU   15                       RETURN CODE WILL BE 0
RWTOR    EQU   11                       ADDRESS OF GETMAINED WTOR LIST
R15      EQU   15                       ENTRY POINT ADDRESS
***********************************************************************
         SPACE 3
         USING *,R15                    TEMP ADDRESSABILITY
         B     U29START                 BR AROUND MODULE ID
         DC    AL1(7),CL7'IEFU29'       MODULE ID
U29START STM   R14,R12,12(RSAVE)        SAVE REGISTERS
         DROP  R15                      DROP TEMP ADDRESSABILITY
         BALR  RBASE,0                  ESTABLISH MODULE ADDRESSABILITY
         USING *,RBASE                  ADDRESSABILITY FOR MODULE
         L     RDSN,0(R1)               SAVE ADDRESS OF DSN PASSED
         GETMAIN R,LV=GETSIZE           GET AREA FOR WTOR PARMS
         LR    RWTOR,R1                 SAVE ADDRESS GETMAIN AREA
         USING WTORAREA,RWTOR           ADDRESSABILITY FOR WTOR
*                                       DSECT
         MVC   WTORAREA(PARMLEN),PARMSTRT
*                                       MOVE LIST FORM INTO
*                                       PARM AREA
         MVC   WTORDSN,0(RDSN)          MOVE IN DSN
WRITE    LA    R3,REPLY                REPLY ADDRESS
         LA    R4,OPECB                ECB ADDRESS
         LA    R5,WTORMSG              ADDRESS OPERATOR MESSAGE
         XC    OPECB,OPECB             CLEAR ECB
         XC    REPLY,REPLY             CLEAR REPLY AREA
         WTOR  ,(R3),1,(R4),MF=(E,(R5)) SEND MESSAGE TO OPERATOR
         WAIT  ECB=OPECB               WAIT FOR REPLY
         CLI   REPLY,C'U'              CHECK REPLY
         BNE   WRITE                   REISSUE MSG IF REPLY NOT 'U'
         FREEMAIN  R,LV=GETSIZE,A=(RWTOR)  RELEASE OBTAINED STORAGE
         LM    R14,R12,12(RSAVE)        RESTORE CALLER'S REGISTERS
         SR    RETCODE,RETCODE          SET SUCCESSFUL RETURN CODE
         BR    RET                      RETURN TO CALLER
         SPACE 3
PARMSTRT DC    F'0'
         DC    F'0'
         DC    AL2(MSGEND-MSGSTART)
         DC    X'8000'
MSGSTART DC    CL62'START SMF DUMP PROGRAM TO DUMP XXXXXXXXX - REPLY U X
               TO CONTINUE'
         DC    X'4000'
         DC    X'8000'
MSGEND   EQU   *
PARMLEN  EQU   *-PARMSTRT
WTORAREA DSECT
WTORMSG  DS    CL(MSGSTART-PARMSTRT)
         DS    CL31
WTORDSN  DS    CL9
         DS    CL26
REPLY    DS    CL1
OPECB    DS    F
GETSIZE  EQU   *-WTORMSG
         END
//LKED.SYSLMOD DD DSNAME=EXITLIB,VOLUME=SER=231400,UNIT=2314,DISP=OLD
//LKED.SYSIN   DD *
   NAME IEFU29
