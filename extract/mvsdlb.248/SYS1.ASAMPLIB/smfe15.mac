//SMFE15 JOB   MSGLEVEL=1
//STEPA  EXEC  ASMFCL
//ASM.SYSIN DD  *
         TITLE 'SMFE15-E15SMF'
* STATUS - CHANGE LEVEL 1
* FUNCTION -
*    ROUTINE IS USED DURING THE SORTING OF SETS CREATED BY SYSTEM
*    MANAGEMENT FACILITIES.IT EXTRACTS NON-JOB ORIENTED RECS FROM
*    SORT INPUT SET AND RETAINS THE EXTRACTED RECS IN A TEMP SET.
*    NON-JOB ORIENTED RECORDS ARE RE-INSERTED INTO THE OUTPUT SET
*    BY AN E35 EXIT ROUTINE.
*    THIS ROUTINE IS DEFINED ONLY FOR RECORD TYPES 0-13.       @YM00552
* ENTRY POINTS -
*         E15
* INPUT - REGISTER 1 POINTS TO A ONE WORD PARAMER LIST.THIS LIST
*    CONTAINS THE ADDRESS OF THE NEW RECORD.
* OUTPUT - A TEMPORARY DATA SET CONTAINING THOSE RECORDS DELETED
*    FROM THE SORT BY THIS ROUTINE.
* EXTERNAL REFERENCES - NONE
* EXITS,NORMAL -   TO OS/SORT
*    RETURN CODES USED
*    RC=0   NO ACTION
*    RC=4   DELETE
*    RC=8   DO NOT RETURN
* EXITS,ERROR - NONE
* TABLES/WORK AREAS - DSECT FOR RECORD FORM
* ATTRIBUTES - STANDARD
* CHARACTER CODE DEPENDENCY - NONE
* NOTES - ENTERED FROM OS/SORT
R00      EQU   0                        REGISTERS 0
R01      EQU   1                        1
R02      EQU   2                        2
R09      EQU   9                        9
R10      EQU   10                       10
DCBREG   EQU   11                       DCB REG
R12      EQU   12                       12
R13      EQU   13                       13
R15      EQU   15                       15
HDRID    EQU   X'02'                    HEADER ID
TLRID    EQU   X'03'                    TRAILER ID
STERMID  EQU   X'04'                    STEP TERM ID
JTERMID  EQU   X'05'                    JOB TERM ID
OPFORMID EQU   X'06'                    OP FORM ID
D4       EQU   4                        DISP OF 4
D8       EQU   8                        DISP OF 8
D24      EQU   24                       DISP OF 24
L8       EQU   8                        LENGTH OF 8
X10      EQU   X'10'                    TEST FOR OPEN
E15      CSECT                          EXIT 15
*D 021500,038000,038500                                        @YM00552
*A 005600                                                      @YM00552
         USING *,R12                    DECLARE BASE
         SAVE  (14,12)                  SAVE SORTS REGS
         LR    R12,R15                  SET UP BASE
         USING RECDFORM,R10             BASE FOR REC'D FORM
         LA    DCBREG,E15DCB            SET UP DCB BASE
         USING IHADCB,DCBREG            DECLARE BASE FOR DCBD
         ST    R13,SAVDS+D4             CHAIN SORT &  E15 SAVE AREA
         LR    R02,R13
         LA    R13,SAVDS                PRIME SAVE REGISTER
         ST    R13,D8(R02)              CHAIN E15 & SORT SAVE AREA
         L     R10,0(R01)               LOAD PARAMETER FROM SORT
         TM    DCBOFLGS,X10             IS DATA SET OPEN
         BO    E15NOPEN                 YES,GO AROUND OPED PROCESSING
         OPEN  (E15DCB,(OUTPUT),E15DCBA,(OUTPUT))
E15NOPEN LTR   R10,R10                  TEST FOR END OF DATA
         BZ    E15NORTN                 YES,GO CLOSE OUT
         CLI   RECDID,HDRID             IS THIS DUMP HDR
         BE    E15XCPTA                 YES,COMPARE WITH PREVIOUS
         CLI   RECDID,TLRID             IS THIS DUMP TRAILER
         BE    E15XCPTC                 YES,PLACE ON E15DCBA
         CLI   RECDID,STERMID           IS RECD STEP TERMINATE
         BE    E15NOACT                 YES,GO MAKE NORMAL RETURN
         CLI   RECDID,JTERMID           IS RECD JOB TERMINATE
         BE    E15NOACT                 YES,GO TO NORMAL RETURN
         CLI   RECDID,OPFORMID          IS RECD O/P FORMS
         BE    E15NOACT                 YES,GO TO NORMAL RETURN
         LR    R00,R10
         PUT   E15DCB,(0)               SAVE SYSTEM RECORDS
*                                       TEMPORARY SET
E15DELET L     R13,SAVDS+D4             ADDRESS OF SORT SAVEAREA
         RETURN (14,12),RC=4            DELETE RCD FROM TO-BE-SORTED
E15NOACT L     R13,SAVDS+D4             ADDRESS OF SORT'S SAVEAREA
         ST    R10,D24(R13)             ADDRESS OF CURRENT RECD TO SAVE
         RETURN (14,12),RC=0            RETAIN TASK RCDS FOR SORTING
E15XCPTA L     R09,HDRTIME              LOAD PREV RECDS TIMESTMP
         LTR   R09,R09                  IS IT ZERO (FIRST TIME THRU)
         BZ    E15XCPTB                 YES,BRANCH AROUND COMPARISONS
         CLC   HDRDATE,RSTPDATE         COMPARE PREVIOUS & CURRENT DATE
         BH    E15WARN                  PREVIOUS DATE HI,ISSUE WARNING
         CLC   HDRTIME,RSTPTIME         COMPARE PREVIOUS & CURRENT TIME
         BH    E15WARN                  PREVIOUS TIME HI,ISSUE WARNING
E15XCPTB MVC   HDRTIME(L8),RSTPTIME     RETAIN DATE/TIME FOR COMPARE
E15XCPTC LR    R00,R10                  @ OF RECORD TO REGISTER 0
         PUT   E15DCBA,(0)              EXECUTE I/O, SAVE HDR & TRL
         B     E15DELET                 RETURN TO SORT W/DELETE CODE
E15WARN  WTO   'SMFX001-INPUT NOT IN CHRONOLOGICAL SEQUENCE'
         B     E15XCPTB                 CONTINUE PROCESSING
E15NORTN CLOSE (E15DCB,,E15DCBA)        CLOSE BOTH TEMPORARY DATA SETS
         L     R13,SAVDS+D4
         RETURN (14,12),RC=8            RETURN & DON'T COME BACK
         DS    0D
SAVDS    DS    18F                      SAVE AREA
HDRTIME  DC    F'0'                     HEADER TIME
HDRDATE  DC    F'0'                     HEADER DATE
E15DCB   DCB   DSORG=PS,MACRF=PM,DDNAME=SORDATA
E15DCBA  DCB   DSORG=PS,MACRF=PM,DDNAME=HDRDATA
DCBFORM  DCBD  DSORG=PS
RECDFORM DSECT                          RECORD
RLNGTH   DS    F                        LENGTH
RCONT    DS    C                        CONT
RECDID   DS    C                        ID
RSTPTIME DS    CL4                      STEP TIME
RSTPDATE DS    CL4                      STEP DATE
RCPUID   DS    CL4                      CPU ID
         DS    CL32                     SPACING
RJOBNAME DS    CL8                      JOB NAME
RSTRTIME DS    CL4                      START TIME
RSTRTDAT DS    CL4                      START DATE
RSTPNUM  DS    C                        STEP NUMBER
         END
//LKED.SYSLMOD DD   DSNAME=SMF1.EXITS,DISP=(NEW,KEEP),UNIT=2314,       *
//             VOLUME=SER=231400,SPACE=(TRK,(10,5,1))
//LKED.SYSIN   DD *
 NAME E15(R)
