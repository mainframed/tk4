         MACRO
         IEECODES &ROUTCDE=13,&DESC=
         LCLC  &CD(4)
         LCLA  &I,&N
         LCLB  &B(32)
.*
.* DESCRIPTOR CODES
.*
&I       SETA  1
.DCHK    AIF   (T'&DESC EQ 'O').RCHK
&N       SETA  &DESC(&I)
&I       SETA  &I+1
         AIF   (&N GE 1 AND &N LE 16).ASSIGND
         MNOTE 8,'&DESC(&I) IS INVALID DESCRIPTOR - IGNORED'
         AGO   .NXTD
.ASSIGND ANOP
&B(&N)   SETB  1
.NXTD    AIF  (&I LE N'&DESC).DCHK
&I       SETA  1
.*
.* ROUTE CODES
.*
.RCHK    AIF   (T'&ROUTCDE EQ 'O').ASSIGNC
&N       SETA  &ROUTCDE(&I)
&I       SETA  &I+1
         AIF   (&N GE 1 AND &N LE 16).ASSIGNR
         MNOTE 8,'ROUTCDE(&I) IS INVALID ROUTE - IGNORED'
         AGO   .NXTR
.ASSIGNR ANOP
&B(&N+16) SETB  1
.NXTR    AIF   (&I LE N'&ROUTCDE).RCHK
.ASSIGNC ANOP
&I       SETA  1
&CD(&I)   SETC  '&B(1)&B(2)&B(3)&B(4)&B(5)&B(6)&B(7)&B(8)'
&CD(&I+1) SETC  '&B(9)&B(10)&B(11)&B(12)&B(13)&B(14)&B(15)&B(16)'
&CD(&I+2) SETC  '&B(17)&B(18)&B(19)&B(20)&B(21)&B(22)&B(23)&B(24)'
&CD(&I+3) SETC  '&B(25)&B(26)&B(27)&B(28)&B(29)&B(30)&B(31)&B(32)'
IEEDESCD DC    BL2'&CD(1)&CD(2)'
IEEROUTC DC    BL2'&CD(3)&CD(4)'
         MEND
         EJECT
IEECVXIT CSECT
PARMPTR  EQU   1                       ADDRESS OF SUPPLIED PARAMETERS
CUCMPTR  EQU   2                       ADDRESS OF EXIT PARAMETERS
CVTPTR   EQU   3                       ADDRESS OF CVT
UCBLOKUP EQU   4                       ADDRESS OF UCB LOOKUP TABLE
UCBPTR   EQU   5                       ADDRESS OF UCB
INDEX    EQU   7                       ADDRESS IN OPTIONAL MSG TABLE
INCR     EQU   8                       INCREMENT FOR MSG TABLE
LIMIT    EQU   9                       END ADDRESS OF MSG TABLE
ENTRY    EQU   15                      ENTRY ADDRESS / BASE REG
TWO      EQU   2                       SELF DEFINED
FOUR     EQU   4                       SELF DEFINED
EIGHT    EQU   8                       SELF DEFINED
CVTLOC   EQU   16                      LOCATION OF CVT
BLANK    EQU   C' '                    SELF DEFINED
         USING *,ENTRY                 EST. ADDRESSABILITY
         B     PROLOG                  BYPASS PROLOGUE
         DC    AL1(18)                 PROLOGUE LENGTH
         DC    C'IEECVXIT &SYSDATE'    MODULE NAME AND COMPILE DATE
         DS    0H                      ALLIGNMENT
PROLOG   SAVE  (14,12)                 SAVE CALLERS REGS
         L     CUCMPTR,0(PARMPTR)      SET ADDR TO IEECUCM DSECT
         USING IEECUCM,CUCMPTR         EST. ADDR TO IEECUCM DSECT
         CLC   UCMMSTXT(L'CICB),CICB   ICB MSG
         BE    UPDROUT                 YES, UPDATE ROUTING CODE
         CLC   UCMMSTXT(L'CBBICB),CBBICB MSSC ICB MSG
         BE    UPDROUT                 YES, UPDATE ROUTING CODE
         CLC   UCMMSTXT(L'CIDC),CIDC   IDC MSG
         BE    UPDROUT                 YES, UPDATE ROUTING CODE
         CLC   UCMMSTXT(L'CIEA),CIEA   IEA000I MSG
         BE    CHKSWAP                 YES, MSS IEA000I MSG
         LA    INDEX,MSGTAB            SET INDEX TO OPTIONAL MSG TABLE
         LA    INCR,L'MSGTAB           SET INCR VALUE
         LA    LIMIT,MSGEND-L'MSGTAB   SET END OF TABLE ADDR
CHKMSG   CLI   0(INDEX),BLANK          OPTINAL MSG ID
         BE    RETURN                  NO, RETURN
         CLC   UCMMSTXT(L'MSGTAB),0(INDEX) OPTIONAL MSG
         BE    UPDROUT                 YES, UPDATE ROUTING CODE
         BXLE  INDEX,INCR,CHKMSG       SCAN ENTIRE OPTIONAL MSG TAB
RETURN   RETURN (14,12)                RETURN TO CALLER
UPDROUT  OC    UCMROUTC(L'IEEROUTC),IEEROUTC ADD ROUTING CODE
         OC    UCMDESCD(L'IEEDESCD),IEEDESCD ADD DESCRIPTOR CODE
         B     RETURN                  RETURN TO CALLER
         EJECT
CHKSWAP  CLC   UCMMSTXT(L'MSCSWAP),MSCSWAP MSS ERP MSC SWITCH
         BE    UPDROUT                 YES, UPDATE ROUTING CODE
         CLC   UCMMSTXT(L'SYSSWAP),SYSSWAP MSS ERP SYS SWITCH
         BE    UPDROUT                 YES, UPDATE ROUTING CODE
         LA    CVTPTR,CVTLOC           SET LOCATION OF CVT ADDR
         L     CVTPTR,0(CVTPTR)        GET CVT ADDRESS
         USING CVT,CVTPTR              EST. ADDR TO CVT
         L     UCBLOKUP,CVTIK2         GET ADDRESS OF UCB LOOKUP TABLE
         B     *+EIGHT                 BYPASS FIRST ADDR UPDATE
CHKUCBAD LA    UCBLOKUP,TWO(UCBLOKUP)  SET NEXT UCB LOOKUP TAB ADDRESS
         LH    UCBPTR,0(UCBLOKUP)      GET UCB ADDRESS
         N     UCBPTR,MASKFFFF         ALLOW FOR FULL 16-BIT ADDR
         C     UCBPTR,MASKFFFF         END OF UCB LOOKUP TABLE
         BE    RETURN                  YES, RETURN TO CALLER
         LTR   UCBPTR,UCBPTR           POINTER TO UCB
         BZ    CHKUCBAD                NO, GET NEXT UCB LOOKUP TAB ADDR
         USING UCB,UCBPTR              EST, ADDR OF CVT
         CLC   UCMMSTXT+EIGHT(L'UCBNAME),UCBNAME UNIT NAME AS IS MSG
         BNE   CHKUCBAD                NO, CHECK NEXT UCB
         TM    UCBTYP3,UCB3DACC        DASD UCB
         BNO   CHKMSC                  NO, CHECK MSC
         TM    UCBTYP2,UCBRVDEV        VUA
         BO    UPDROUT                 YES, UPDATE ROUTING CODE
         B     RETURN                  NO, RETURN TO CALLER
CHKMSC   TM    UCBTYP3,UCB3UREC        UNIT RECORD DEVICE
         BNO   RETURN                  NO, RETURN TO CALLER
         CLI   UCBTYP4,UCBMSC          MSC
         BE    UPDROUT                 YES, UPDATE ROUTING CODE
         B     RETURN                  NO, RETURN TO CALLER
         DS    0F                      ALLIGNMENT
IEEPATCH EQU   *                       PATCH AREA
PATCHA   NOP   *                       LET ASSEMBLER CALC DISP
         DC    5F'0'                   SELF DEFINED
PATCHB   NOP   *                       LET ASSEMBLER CALC DISP
         DC    5F'0'                   SELF DEFINED
         EJECT
         IEECODES
         EJECT
CICB     DC    C'ICB'
CBBICB   DC    C'  ICB'
CIDC     DC    C'IDC'
CIEA     DC    C'IEA000I'
MSGTAB   DC    C'       '
         DC    C'       '
         DC    C'       '
         DC    C'       '
         DC    C'       '
         DC    C'       '
         DC    C'       '
         DC    C'       '
         DC    C'       '
         DC    C'       '
MSGEND   EQU   *
SYSSWAP  DC    C'IEA000I SYS SWITCH'
MSCSWAP  DC    C'IEA000I MSC SWITCH'
         DS    0F
MASKFFFF DC    XL4'0000FFFF'
         EJECT
IEECUCM  DSECT
UCMMSTXT DS    CL128
UCMROUTC DS    CL4
UCMDESCD DS    CL4
CVT      DSECT
         DS    CL40
CVTIK2   DS    F
UCB      DSECT
         DS    CL13
UCBNAME  DS    CL3
UCBTYP1  DS    C
UCBTYP2  DS    C
UCBTYP3  DS    C
UCBTYP4  DS    C
UCBRVDEV EQU   X'08'
UCB3DACC EQU   X'20'
UCB3UREC EQU   X'08'
UCBMSC   EQU   X'42'
         END   IEECVXIT
