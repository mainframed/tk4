EXIT     TITLE 'IFG0199I - INSTALLATION EXIT FOR ABEND PROCESSING'
IFG0199I CSECT
***********************************************************************
*                                                                     *
* MODULE NAME = IFG0199I (OS/VS2)                                     *
*                                                                     *
* DESCRIPTIVE NAME = ABEND INSTALLATION EXIT                          *
*                                                                     *
* COPYRIGHT = NONE                                                    *
*                                                                     *
* STATUS = RELEASE 1, LEVEL 0                                         *
*                                                                     *
* FUNCTION = THIS MODULE DOES THE FOLLOWING FUNCTIONS:                *
*                                                                     *
*    1. CHECK TO MAKE SURE COUNTER HAS BEEN RETRIED ONCE              *
*       BEFORE ISSUING THE WTOR.                                      *
*                                                                     *
*    2. ISSUE WTOR.                                                   *
*                                                                     *
*    3. ISSUE DOM.                                                    *
*                                                                     *
*    4. IF THE OPERATOR'S REPLY IS A 'U' THEN THE ABEND IS            *
*       CONTINUED.  IF THE OPERATOR'S REPLY IS A 'R' THEN THE         *
*       RETURN CODE IS SET TO 4 TO INDICATE A RETRY CONDITION         *
*       AND CONTROL IS RETURNED TO THE CALLING MODULE IFG0199E.       *
*                                                                     *
* NOTES = SEE BELOW                                                   *
*                                                                     *
*    DEPENDENCIES =                                                   *
*            CLASS ONE CHARACTER CODE.  THE EBCDIC CHARACTER CODE     *
*            WAS USED FOR ASSEMBLY.  THE MODULE MUST BE REASSEMBLED   *
*            IF A DIFFERENT CHARACTER SET IS USED FOR EXECUTION.      *
*                                                                     *
*    RESTRICTIONS = NONE                                              *
*                                                                     *
*    REGISTER CONVENTIONS =                                           *
*            R1 INPUT PARAMETER REGISTER                              *
*            R2 POINTS TO DCB                                         *
*            R3 WORK REGISTER                                         *
*            R4 POINTS TO OPEN WORK AREA                              *
*            R5 POINTS TO THE RESIDENT ROUTINE                        *
*            R6 POINTS TO THE JFCB                                    *
*            R7 POINTS TO THE TIOT ENTRY                              *
*            R8 POINTS TO THE UCB                                     *
*            R9 WORK REGISTER                                         *
*            R10 POINTS TO THE GETMAINED STORAGE                      *
*            R11 BASE REGISTER                                        *
*            R12 POINTS TO THE OAIXL PARAMETER LIST                   *
*            R13 POINTS TO THE SAVE AREA ADDRESS                      *
*            R14 POINTS TO THE RETURN ADDRESS                         *
*            R15 WORK REGISTER AND RETURN CODE REGISTER               *
*                                                                     *
*    PATCH LABEL = PATCH                                              *
*                                                                     *
* MODULE TYPE = CONTROL (OPEN, CLOSE, EOV DATA MANAGEMENT)            *
*                                                                     *
*    PROCESSOR = ASSEMBLER XF                                         *
*                                                                     *
*    MODULE SIZE = SEE EXTERNAL SYMBOL DICTIONARY                     *
*                                                                     *
*    ATTRIBUTES = REENTRANT, REFRESHABLE, ENABLED, READ ONLY,         *
*                 PRIVILEGED, SUPERVISOR STATE, KEY ZERO,             *
*                 LINK PACK AREA RESIDENT/PAGEABLE                    *
*                                                                     *
* ENTRY POINT = IFG0199I                                              *
*                                                                     *
*    PURPOSE = SEE FUNCTION                                           *
*                                                                     *
*    LINKAGE =                                                        *
*        FROM IFG0199E:                                               *
*           BALR  R14,R15    BRANCH AND LINK                          *
*                                                                     *
* INPUT = STANDARD REGISTERS                                          *
*    REG 1  - INPUT PARAMETER LIST (OAIXL)                            *
*    REG 13 - SET TO CALLER'S SAVE AREA                               *
*                                                                     *
* OUTPUT = STANDARD REGISTERS AND/OR OPERATOR REPLY MESSAGE           *
*                                                                     *
* EXIT-NORMAL = IFG0199E - ABEND PROCESSING ROUTINE                   *
*                                                                     *
* EXIT-ERROR = NONE                                                   *
*                                                                     *
* EXTERNAL REFERENCES = SEE BELOW                                     *
*                                                                     *
*    ROUTINES = IFG0199E                                              *
*                                                                     *
*    DATA AREAS = OAIXL                                               *
*                                                                     *
*    CONTROL BLOCK = JFCB, DCB, UCB, TIOT                             *
*                                                                     *
* TABLES = NONE                                                       *
*                                                                     *
* MACROS = WTOR, DOM                                                  *
*                                                                     *
* CHANGE ACTIVITY = NONE                                              *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*                                                                     *
*        START OF OPEN ABEND INSTALLATION EXIT                        *
*                                                                     *
***********************************************************************
         SPACE 3
START    BALR  R15,0                    ESTABLISH BASE REGISTER
         USING *,R15                    ESTABLISH ADDRESSABILITY
         B     PROLOG                   BRANCH AROUND PROLOGUE
         DC    C'IFG0199I  81.253'      IDENTIFIER
         DROP  R15                      NO LONGER NEED R15
PROLOG   STM   R14,R12,12(R13)          SAVE SYSTEM REGISTERS
         BALR  R11,R0                   LOAD PROGRAM BASE
         USING *,R11                    USING R11 AS BASE REGISTER
         L     R0,SIZDATAD              GET DSECT SIZE
         GETMAIN R,LV=(0)               GET DSECT STORAGE
         LR    R10,R1                   SAVE GETMAINED AREA
         USING DATAD,R10                REGISTER 10 DSECT REGISTER
         ST    R13,SAVEAREA+FOUR        SAVE R13 FOR BACK POINTER
         LM    R15,R1,16(R13)           RELOAD CALLERS REGISTERS
         ST    R10,8(R13)               SAVE R10 FOR FORWARD POINTER
         LR    R13,R10                  POINT TO NEW SAVE AREA
         LR    R12,R1                   LOAD OAIXL REGISTER FROM PARM
*
         SPACE 3
*
         USING IHADCB,R2                DEFINE BASE TO COPIED DCB
         USING JFCB,R6                  DEFINE BASE TO JFCB
         USING TIOENTRY,R7              DEFINE BASE TO TIOT DD ENTRY
         USING UCB,R8                   DEFINE BASE TO UCB
         USING OAIXL,R12                DEFINE BASE TO OAIXL
*
         EJECT
***********************************************************************
*                                                                     *
*        CHECK THE ABEND CODE TO BE SURE THIS IS A 613-08/0C ABEND    *
*                                                                     *
***********************************************************************
         SPACE 3
CHKABEND LH    R8,OAIXCODE              LOAD FIRST TWO BYTES OF CODE
         CH    R8,HEX613                COMPARE CODE TO ABEND 613
         BNE   CONTINUE                 CONTINUE WITH ABEND
         CLI   OAIXCODE+THREE,HEX08     COMPARE CODE TO ABEND 613-08
         BE    CHKEXIT                  YES, CHECK EXIT TAKEN
         CLI   OAIXCODE+THREE,HEX0C     COMPARE CODE TO ABEND 613-0C
         BNE   CONTINUE                 NO, CONTINUE WITH ABEND
*
***********************************************************************
*                                                                     *
*        CHECK IF THE DCB USER ABEND EXIT WAS TAKEN                   *
*                                                                     *
***********************************************************************
         SPACE 3
CHKEXIT  TM    OAIXFLGS,OAIXEXIT        TEST IF DCB USER EXIT TAKEN
         BO    CONTINUE                 DO NOT OVERRIDE THE USER EXIT'S
*                                       DECISION TO ABEND
*
         EJECT
***********************************************************************
*                                                                     *
*        CHECK THE COUNTER TO BE SURE WE HAVE RETRIED ONE TIME        *
*                                                                     *
***********************************************************************
         SPACE 3
CHKAREA  L     R8,OAIXAREA              LOAD AREA COUNTER INTO REG 8
         LA    R8,ONE(R8)               ADD ONE TO AREA COUNTER
         ST    R8,OAIXAREA              STORE NEW SUM INTO COUNTER
         CLI   OAIXAREA+THREE,TWO       COMPARE COUNTER TO TWO
         BL    RETRY                    LOW, CONTINUE TO RETRY
         SLR   R9,R9                    CLEAR REGISTER 9
         ST    R9,OAIXAREA              STORE ZERO INTO COUNTER
*
***********************************************************************
*                                                                     *
*        SETUP TO ISSUE THE WTOR                                      *
*                                                                     *
***********************************************************************
         SPACE 3
TRYAGAIN SLR   R9,R9                    CLEAR REGISTER 9
         ST    R9,REPLYECB              STORE ZERO INTO REPLY ECB
         MVC   WTORAREA(WTORLEN),WTORLIST MOVE IN WTOR LIST
         LA    R2,REPLY                 LOAD ADDRESS OF REPLY
         LA    R3,REPLYECB              LOAD ADDRESS OF REPLY ECB
*
         EJECT
***********************************************************************
*                                                                     *
*        ISSUE MESSAGE TO THE OPERATOR, AND WAIT FOR                  *
*        HIS REPLY TO 'U' CONTINUE OR 'R' RETRY.                      *
*                                                                     *
***********************************************************************
         SPACE 3
         WTOR  ,(R2),,(R3),MF=(E,WTORAREA) ISSUE WTOR SVC
         LR    R9,R1                    SAVE MSG ID FOR DOM
*
***********************************************************************
*                                                                     *
*        ISSUE WAIT                                                   *
*                                                                     *
***********************************************************************
         SPACE 3
         LA    R3,REPLYECB              LOAD ADDRESS OF REPLY ECB
         WAIT  ECB=(R3)                 WAIT ON REPLY
*
***********************************************************************
*                                                                     *
*        ISSUE DOM                                                    *
*                                                                     *
***********************************************************************
         SPACE 3
         DOM   MSG=(R9)                 DOM MESSAGE
*
         EJECT
***********************************************************************
*                                                                     *
*        CHECK REPLY FROM OPERATOR                                    *
*                                                                     *
***********************************************************************
         SPACE 3
         OC    REPLY(FOUR),BLANKS       MAKE REPLY UPPER CASE
         CLI   REPLY,C'U'               WAS REPLY A 'U'
         BE    CONTINUE                 GO AND CONTINUE WITH ABEND
         CLI   REPLY,C'R'               WAS REPLY A 'R'
         BE    RETRY                    GO AND CONTINUE TO RETRY
         B     TRYAGAIN                 INVALID RESPONSE, TRY AGAIN
*
***********************************************************************
*                                                                     *
*        SET THE RETURN CODE FOR RETRY OR CONTINUE WITH ABEND         *
*                                                                     *
***********************************************************************
         SPACE 3
RETRY    LA    R15,FOUR                 SET RETURN CODE TO FOUR
         OI    OAIXFLGS,OAIXREW         SET ON REW BEFORE RETRY FLAG
         B     EOJ                      BRANCH TO END OF JOB
CONTINUE LA    R15,ZERO                 SET RETURN CODE TO ZERO
EOJ      L     R13,SAVEAREA+FOUR        GET CALLERS SAVE AREA ADDRESS
         ST    R15,16(R13)              SAVE RETURN CODE REGISTER
         L     R0,SIZDATAD              GET DSECT STORAGE SIZE
         FREEMAIN R,LV=(0),A=(R10)      ISSUE FREEMAIN
         LM    R14,R12,12(R13)          RESTORE ALL REGISTERS
         BR    R14                      RETURN TO CALLER
*
         EJECT
***********************************************************************
*                                                                     *
*        CONSTANTS                                                    *
*                                                                     *
***********************************************************************
         SPACE 3
*
WTORLIST WTOR  'IEC613A TAPE POSITIONING ERROR - REPLY ''R'' RETRY OR 'X
               'U'' CONTINUE WITH ABEND',,                             X
               4,ROUTCDE=(1,3,5),MF=L   WTOR FOR MESSAGE IEC613A
WTORLEN  EQU   *-WTORLIST               LENGTH OF WTOR
*
         DS    0H
HEX613   DC    X'6130'                  CONSTANT FOR 613 ABEND
*
BLANKS   DC    C'      '                BLANKS FOR REPLY
*
PATCH    DC    ((*-START)/20)X'00'      PROGRAM PATCH AREA
         EJECT
***********************************************************************
*                                                                     *
*        DSECT STORAGE                                                *
*                                                                     *
***********************************************************************
         SPACE 3
*
DATAD    DSECT
         DS    0D
WTORAREA DS    (WTORLEN)C
*
REPLYECB DS    F                        REPLY ECB
REPLY    DS    CL4                      REPLY RETURN AREA
*
SAVEAREA DS    18F                      PROGRAM SAVE AREA
*
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
ZERO     EQU   0
ONE      EQU   1
TWO      EQU   2
THREE    EQU   3
FOUR     EQU   4
FIVE     EQU   5
HEX08    EQU   X'08'
HEX0C    EQU   X'0C'
         EJECT
***********************************************************************
*                                                                     *
*        OPEN ABEND INSTALLATION EXIT PARAMETER LIST                  *
*                                                                     *
***********************************************************************
*
OAIXL    DSECT                    ABEND INSTALLATION EXIT LIST
*
         DS    0F
OAIXUKEY DS    XL1                PROTECT KEY OF USER DCB
OAIXFLGS DS    XL1                OAIXL FLAG BYTE
OAIXEXIT EQU   X'80'              DCB USER EXIT TAKEN
*                                   00 = DCB USER EXIT NOT TAKEN
*                                   80 = DCB USER EXIT TAKEN
OAIXREW  EQU   X'40'              REWIND TAPE BEFORE RETRY
*                                   00 = DO NOT REWIND THE TAPE
*                                   40 = REWIND THE TAPE
OAIXRESV DS    H                  NOT USED
OAIXPDCB DS    A                  PROTECTED COPY OF THE DCB
OAIXUDCB DS    A                  COPY OF THE USER DCB
OAIXUCBA DS    A                  UCB ADDRESS
OAIXJFCB DS    A                  JFCB ADDRESS
OAIXTIOT DS    A                  TIOT ADDRESS
OAIXCODE DS    F                  ABEND CODE- EXAM X'6130000C'
OAIXAREA DS    F                  INSTALLATION WORK AREA
OAIXLEN  EQU   *-OAIXL            LENGTH OF OAIXL
*
         EJECT
***********************************************************************
*                                                                     *
*        DCB - THE IHADCB MACRO IS IN SYS1.MACLIB                     *
*                                                                     *
***********************************************************************
         SPACE 3
         IHADCB DSORG=(PS,IS,DA,TQ),DEVD=(DA,TA) DCB
         EJECT
***********************************************************************
*                                                                     *
*        UCB - THE IEFUCBOB MACRO IS IN SYS1.AMODGEN                  *
*                                                                     *
***********************************************************************
         SPACE 3
UCB      DSECT
         IEFUCBOB LIST=YES
         EJECT
***********************************************************************
*                                                                     *
*        TIOT - THE IEFTIOT1 MACRO IS IN SYS1.AMODGEN                 *
*                                                                     *
***********************************************************************
         SPACE 3
TIOT     DSECT
         IEFTIOT1
         EJECT
***********************************************************************
*                                                                     *
*        JFCB - THE IEFJFCBN MACRO IS IN SYS1.AMODGEN                 *
*                                                                     *
***********************************************************************
         SPACE 3
JFCB     DSECT
         IEFJFCBN LIST=YES
         EJECT
***********************************************************************
*                                                                     *
*        DATA DEFINITIONS FOR DYNAMIC STORAGE AREA                    *
*                                                                     *
***********************************************************************
         SPACE 3
IFG0199I CSECT
         DS    0F
SIZDATAD DC    AL1(0)
         DC    AL3(ENDDATA-DATAD)       SIZE OF DSECT
DATAD    DSECT
         ORG   *+1-(*-DATAD)/(*-DATAD)  INSURE DSECT DATA
ENDDATA  EQU   *
         END
