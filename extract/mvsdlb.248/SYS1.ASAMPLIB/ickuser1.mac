  TITLE '********* ICKUSER1 - DSF SECURITY USER EXIT MODULE *********'
ICKUSER1 CSECT
*/********************************************************************/
*/*                                                                  */
*/*  MODULE NAME - ICKUSER1 (DEVICE SUPPORT FACILITIES)              */
*/*                                                                  */
*/*  DESCRIPTIVE NAME - DSF SECURITY AUTHORIZATION USER EXIT MODULE  */
*/*                                                                  */
*/*  COPYRIGHT - NONE                                                */
*/*                                                                  */
*/*  STATUS - RELEASE-6 VERSION-0                                    */
*/*                                                                  */
*/*  FUNCTION - THIS MODULE RECEIVES REQUESTS TO PERFORM VOLUME AND  */
*/*             DATASET SECURITY AUTHORIZATION BY THE DSF VOLUME     */
*/*             AUTHORIZATION MODULE ICKSA04.                        */
*/*                                                                  */
*/*  NOTES -                                                         */
*/*                                                                  */
*/*     DEPENDENCIES - STANDARD SUBROUTINE LINKAGE                   */
*/*                                                                  */
*/*     RESTRICTIONS - NONE                                          */
*/*                                                                  */
*/*     REGISTER CONVENTIONS - STANDARD LINKAGE CONVENTIONS WITH     */
*/*                            OTHER REGISTERS USED AS NEEDED.       */
*/*                                                                  */
*/*     PATCH LABEL - 'FIXAREA'                                      */
*/*                                                                  */
*/*  MODULE TYPE - PROCEDURE                                         */
*/*                                                                  */
*/*     PROCESSOR - ASSEMBLER                                        */
*/*                                                                  */
*/*     ATTRIBUTES - REENTRANT AND REUSABLE                          */
*/*                                                                  */
*/*  ENTRY POINT - ICKUSER1                                          */
*/*                                                                  */
*/*   PURPOSE - THIS ENTRY POINT IS ENTERED WHENEVER A               */
*/*             MODULE WITHIN DSF REQUESTS THAT A RESOURCE           */
*/*             ACCESS CONTROL FACILITY(RACF) VOLUME LEVEL OR        */
*/*             DATASET AUTHORIZATION CHECK BE PERFORMED.            */
*/*             THIS ROUTINE CONTROLS THE LEVEL OF SECURITY          */
*/*             CHECKING THAT IS TO BE DONE BY DSF.                  */
*/*                                                                  */
*/*             THIS MODULE PROVIDES DEFAULT PROCESSING OPTIONS      */
*/*             FOR DSF SECURITY AUTHORIZATION CHECKING. IT MAY      */
*/*             BE REPLACED BY A USER WRITTEN MODULE.                */
*/*                                                                  */
*/*                                                                  */
*/*   LINKAGE - CALL ICKUSER1 (PARM-LIST)                            */
*/*                                                                  */
*/*   INPUT   - PARAMETER LIST CONTAINING -                          */
*/*                 1. POINTER TO FORMAT1 DSCB                       */
*/*                 2. POINTER A SIX CHARACTER VOLUME SERIAL         */
*/*                 3. POINTER TO A 2 CHARACTER FIELD CONTAINING -   */
*/*                         A. A ONE BYTE DATASET TYPE CODE          */
*/*                         B. A ONE BYTE DSF COMMAND CODE           */
*/*                                                                  */
*/*                 DATASET TYPE CODES ARE :                         */
*/*                      1 = VSAM DATASET                            */
*/*                      2 = PASSWORD PROTECTED DATASET              */
*/*                      3 = UNEXPIRED DATASET                       */
*/*                                                                  */
*/*                 DSF COMMAND CODES ARE :                          */
*/*                      1 = COMMAND IS BUILDX                       */
*/*                      2 = COMMAND IS INIT                         */
*/*                      3 = COMMAND IS INSPECT                      */
*/*                      4 = COMMAND IS REFORMAT                     */
*/*                                                                  */
*/*   OUTPUT  - NONE                                                 */
*/*                                                                  */
*/*   EXIT NORMAL - A RETURN TO THE CALLER WITH ONE OF THE           */
*/*                 FOLLOWING RETURN CODES:                          */
*/*                                                                  */
*/*           DEC HEX                                                */
*/*           --- ---                                                */
*/*            00 (00) BYAPSS CHECKING FOR THIS VOLUME ,             */
*/*                    DO NOT RETURN TO ICKUSER1 FOR THIS VOLUME     */
*/*                                                                  */
*/*            04 (04) BYPASS CHECKING FOR THIS DATASET ,            */
*/*                    BUT RETURN TO ICKUSER1 FOR NEXT DATASET       */
*/*                                                                  */
*/*            08 (08) DO NORMAL CHECKING FOR THIS DATASET ,         */
*/*                    BUT RETURN TO ICKUSER1 FOR NEXT DATASET       */
*/*                                                                  */
*/*            12 (0C) DO NORMAL CHECKING FOR THIS VOLUME ,          */
*/*                    DO NOT RETURN TO ICKUSER1 FOR THIS VOLUME     */
*/*                                                                  */
*/*            16 (10) REJECT THIS DATASET ,                         */
*/*                    BUT RETURN TO ICKUSER1 FOR NEXT DATASET       */
*/*                                                                  */
*/*            20 (14) TERMINATE THIS COMMAND                        */
*/*                                                                  */
*/*  =======>  NOTE 1.   FOR VSAM DATSETS AND UNEXPIRED DATASETS     */
*/*            THE ONLY ALLOWABLE RETURN CODES ARE 0, 12 AND 20.     */
*/*            (I.E. ALLOW DSF USER TO PROCESS VOLUME FOR GIVEN      */
*/*             DATASET TYPE, DO NOT ALLOW DSF USER TO PROCESS       */
*/*             VOLUME FOR GIVEN DATASET TYPE OR TERMINATE THE       */
*/*             COMMAND - RESPECTIVELY)                              */
*/*                                                                  */
*/*  =======>  NOTE 2.   THAT THE DEFAULT RETURN CODES CAN BE        */
*/*            SUPERZAPPED EASILY. THE DEFAULT RETURN CODES ARE      */
*/*            ARRANGED IN A TABLE BY COMMAND AND BY DATASET         */
*/*            TYPES. THIS TABLE STARTS AT OFFSET 4 FROM THE         */
*/*            ENTRY POINT FOR THIS MODULE.                          */
*/*                                                                  */
*/*  =======>  NOTE 3.   THAT THIS MODULE DOES NOT HAVE A SAVE       */
*/*            AREA OF ITS OWN AND THAT NO SAVE AREA CHAINING CODE   */
*/*            IS PRESENT BECAUSE THIS MODULE CALLS NO OTHERS.       */
*/*            IF USER WANTS TO BUILD ON THIS HE MUST PROVIDE        */
*/*            A SAVE AREA AND CHAINING CODE AS NEEDED.              */
*/*                                                                  */
*/*   EXIT ERROR - NONE                                              */
*/*                                                                  */
*/*  EXTERNAL REFERENCES - NONE                                      */
*/*                                                                  */
*/*     DATA AREAS -                                                 */
*/*                                                                  */
*/*                                                                  */
*/*     CONTROL BLOCKS -                                             */
*/*                                                                  */
*/*                                                                  */
*/*  TABLES - NONE                                                   */
*/*                                                                  */
*/*  MACROS -                                                        */
*/*                                                                  */
*/*  CHANGE ACTIVITY -                                               */
*/*                                                                  */
*/********************************************************************/
         EJECT
         USING *,15                TEMPORARY CODE ADDRESSABILITY
         B     USR1MAIN            BRANCH AROUND CONSTANTS
*
*        START OF DEFAULT RETURN CODES TABLE
*        REQUESTS NORMAL DSF PROCESSING FOR ALL CASES
*
RCTABLE  DS    0F                  DEFAULT RETURN CODES TABLE
BLDXVSAM DC    F'12'               BUILDX COMMAND , VSAM DATASET
BLDXPWDS DC    F'12'               BUILDX COMMAND , PW PROT DATASET
BLDXUEXP DC    F'12'               BUILDX COMMAND , UNEXPIRED DATASET
INITVSAM DC    F'12'               INIT COMMAND , VSAM DATASET
INITPWDS DC    F'12'               INIT COMMAND , PW PROT DATASET
INITUEXP DC    F'12'               INIT COMMAND , UNEXPIRED DATASET
INSPVSAM DC    F'12'               INSP COMMAND , VSAM DATASET
INSPPWDS DC    F'12'               INSP COMMAND , PW PROT DATASET
INSPUEXP DC    F'12'               INSP COMMAND , UNEXPIRED DATASET
RFMTVSAM DC    F'12'               RFMT COMMAND , VSAM DATASET
RFMTPWDS DC    F'12'               RFMT COMMAND , PW PROT DATASET
RFMTUEXP DC    F'12'               RFMT COMMAND , UNEXPIRED DATASET
*
*        END OF DEFAULT RETURN CODES TABLE
*
         DC    AL1(16)
         DC    C'ICKUSER1 DSF 6.0'
*
*        MAINLINE PROGRAM
*
         DS    0H
USR1MAIN EQU   *
         STM   RE,RC,12(RD)       SAVE CALLERS REGS
         BALR  RC,0                BASE REG
@PSTART  EQU   *
         USING @PSTART,RC
         LR    RA,R1               PARMLIST ADDRESS
         USING EXITPRMS,RA         ADDRESS PARMLIST
         LA    R9,RCTABLE          RETURN CODE TABLE ADDRESS(BASE=RF)
         L     R8,XITCODSP         ADDRESS EXIT CODES
         SLR   R7,R7
         USING XITCODES,R8
         IC    R7,XITCMD           GET COMMAND CODE
         BCTR  R7,0                REDUCE BY 1
         MH    R7,CRCTLEN          TIMES LENGTH OF RCT FOR ONE COMMAND
         AR    R9,R7               ADDR OF RCT FOR COMMAND
         SLR   R7,R7
         IC    R7,XITDSTYP         GET DS TYPE
         BCTR  R7,0                REDUCE BY 1
         MH    R7,RCTELEN          LENGTH OF EACH RCT ENTRY
         AR    R9,R7               ADDR OF ENTRY BY COMMAND AND DSTYPE
         L     RF,0(,R9)           RETURN CODE FOR COMMAND AND DSTYPE
         L     RE,12(,RD)          RESTORE REGS
         LM    R0,RC,20(RD)
         BR    RE
*/********************************************************************/
*/*                                                                  */
*/*         PATCH AREA FOR PROGRAM MAINTENANCE.                      */
*/*                                                                  */
*/********************************************************************/
PATCH    DC    10C'PATCH'
         EJECT
*/********************************************************************/
*/*      CONSTANTS USED                                              */
*/********************************************************************/
CRCTLEN  DC    AL2(INITVSAM-BLDXVSAM)
RCTELEN  DC    H'4'
*/********************************************************************/
*/*      STRUCTURE FOR PARAMETER LIST                                */
*/********************************************************************/
EXITPRMS DSECT
XITDSCBP DS    F                   POINTER TO DSCB FORMAT 1
XITVSERP DS    F                   POINTER TO VOLUME SERIAL
XITCODSP DS    F                   POINTER TO CODES
*/********************************************************************/
*/*      STRUCTURE FOR INPUT CODES                                   */
*/********************************************************************/
XITCODES DSECT
XITDSTYP DS    C                   DATASET TYPE CODE
XITCMD   DS    C                   COMMAND CODE
*/* **************************************************************** */
*/*                                                                  */
*/*              ASSEMBLY LANGUAGE VERSION OF IECSDSL1               */
*/*                                                                  */
*/*      ******************************************************      */
*/*                                                                  */
*/*                                                                  */
IECSDSF1 DSECT                     FORMAT1 DSCB
DS1DSNAM DS    CL44                DATA SET NAME
DS1FMTID DS    CL1                 FORMAT IDENTIFIER
DS1DSSN  DS    CL6                 DATA SET SERIAL NUMBER
DS1VOLSQ DS    XL2                 VOLUME SEQUENCE NUMBER
DS1CREDT DS    XL3                 CREATION DATE
DS1EXPDT DS    XL3                 EXPIRATION DATE
DS1NOEPV DS    XL1                 NUMBER OF EXTENTS ON VOLUME
DS1NOBDB DS    XL1                 NUMBER OF BYTES USED IN LAST
*                                     DIRECTORY BLOCK
         DS    XL1                 RESERVED
DS1SYSCD DS    CL13                SYSTEM CODE
         DS    XL7                 RESERVED
DS1DSORG DS    XL2                 DATA SET ORGANIZATION
DS1RECFM DS    XL1                 RECORD FORMAT
DS1OPTCD DS    XL1                 OPTION CODE
DS1BLKL  DS    XL2                 BLOCK LENGTH
DS1LRECL DS    XL2                 RECORD LENGTH
DS1KEYL  DS    XL1                 KEY LENGTH
DS1RKP   DS    XL2                 RELATIVE KEY POSITION
DS1DSIND DS    XL1                 DATA SET INDICATORS
DS1SCALO DS    XL4                 SECONDARY ALLOCATION
DS1LSTAR DS    XL3                 LAST USED TRACK AND BLOCK ON TRACK
DS1TRBAL DS    XL2                 BYTES REMAINING ON LAST TRACK USED
         DS    XL2                 RESERVED
DS1EXT1  DS    XL10                FIRST EXTENT DESCRIPTION
*        FIRST BYTE                EXTENT TYPE INDICATOR
*        SECOND BYTE               EXTENT SEQUENCE NUMBER
*        THIRD - SIXTH BYTES       LOWER LIMIT
*        SEVENTH - TENTH BYTES     UPPER LIMIT
DS1EXT2  DS    XL10                SECOND EXTENT DESCRIPTION
DS1EXT3  DS    XL10                THIRD EXTENT DESCRIPTION
DS1PTRDS DS    XL5                 POSSIBLE PTR TO A FORMAT 2 OR 3 DSCB
DS1END   EQU   *
         EJECT
ICKUSER1 CSECT ,
*/********************************************************************/
*/*      REGISTER EQUATES                                            */
*/********************************************************************/
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
RA       EQU   10
RB       EQU   11
RC       EQU   12
RD       EQU   13
RE       EQU   14
RF       EQU   15
         END   ICKUSER1
