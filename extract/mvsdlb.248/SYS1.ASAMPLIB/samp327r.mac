//SAMP327R JOB BTAM,MSGLEVEL=1
// EXEC ASMFCLG
//ASM.SYSIN DD *
***********************************************************************
*          APARS FIXED:                                               *
*         AZ09358  (04/07/76)        AZ09359  (04/07/76)              *
*         AZ10036  (04/14/76)                                         *
***********************************************************************
SAMP327R CSECT
         SPACE 5
*        THIS IS A SAMPLE PROGRAM FOR A REMOTE 3270 INFORMATION DISPLAY
*  SYSTEM.  PLEASE NOTE THAT THIS PROGRAM HAS BEEN WRITTEN FOR TWO
*  3270 DEVICES, BOTH OF WHICH MUST BE 3277 DISPLAYS.
*
*        THIS PROGRAM IS SET UP FOR AN ASSEMBLE, LINKEDIT AND GO .
*    THE DD3270 DD CARD WILL HAVE TO BE CHANGED TO REFLECT THE
*    ADDRESS OF THE BSC LINE IN THE TRANSMISSION CONTROL UNIT .
*
*        IT MAY BE NECESSARY TO ALTER THIS SAMPLE PROBLEM SO THAT IT
*  MAY FUNCTION WITH MORE THAN TWO DEVICES.  TO DO SO, THE FOLLOWING
*  CARDS MUST BE CHANGED IN THE SOURCE DECK:
*                           1. DFTRMLST MACROS  (SELDSPLY)
*                                               (SPECPOL)
*
*        THIS SAMPLE PROGRAM IS RESTRICTED TO A MAXIMIM OF 32 DEVICES,
*  ALL OF WHICH MUST BE ATTACHED TO ONE REMOTE 3271 CONTROL UNIT.
         EJECT
*        REGISTER EQUATES
REGZERO  EQU   0                   REGISTER 0
REG2     EQU   2                   WORK REGISTER
WORKREG  EQU   3                   WORK REGISTER
MSGADDR  EQU   4                   ADDR OF OUTPUT MESSAGE
MSGLEN   EQU   5                   LENGTH OF OUTPUT MESSAGE
SELREG   EQU   6              SPEC POLL ADDR OF 3270 DISPLAY
FMTREG   EQU   8                   FORMAT IDENTIFIER
DSPTABRG EQU   9                   ADDRESS OF DISPLAY TABLE
LNKREG   EQU   10                  LINKAGE REGISTER
BASEREG  EQU   11                  FIRST BASE REG
BASEREG2 EQU   12                  SENOND BASE REG
PTRTAB   EQU   13                  PRINTER RLN TABLE ADDRESS
RTNCDRG  EQU   15                  RETURN CODE REGISTER
         SPACE 5
*        EQUATES
ZERO     EQU   0                   LENGTH OF 0
ONE      EQU   1                   LENGTH OF 1
TWO      EQU   2                   LENGTH OF 2
THREE    EQU   3                   LENGTH OF 3
FOUR     EQU   4                   FORMAT 1 IDENTIFIER
FIVE     EQU   5              LENGTH OF TERMINAL LIST ENTRIES
SIX      EQU   6                   LENGTH OF 6
TP06     EQU   6                   TP CODE OF 6
SEVEN    EQU   7                   LENGTH OF 7
EIGHT    EQU   8                   FORMAT 2 IDENTIFIER
TP11     EQU   X'11'               TP CODE OF 11
SIXTN    EQU   16                  LENGTH IF 16
TP20     EQU   X'20'
TWENTY4  EQU   24             LENGTH OF 24
TWENTY8  EQU   28                  LENGTH OF 28
TIMEOUT  EQU   X'01'               DECB TIME OUT FLAG
SSMSG    EQU   X'10'               SENSE/STATUS RECEIVED FLAG
EOTRSPTX EQU   X'40'               EOT RESPONSE TO TEXT
EOTRCVD  EQU   X'40'               EOT RECEIVED FLAG
FOURTY1  EQU   X'41'               I/O ERROR COMP CODE
PA1      EQU   X'6C'               ATTENTION ID FOR PA1 KEY
CLEAR    EQU   X'6D'               ATTENTION ID FOR CLEAR KEY
PA2      EQU   X'6E'               ATTENTION ID FOR PA2 (CNCL) KEY
ENTER    EQU   X'7D'               ATTENTION ID FOR ENTER KEY
SEVENF   EQU   X'7F'               NORMAL COMPLETION CODE
LAST     EQU   X'80'          SIGNIFIES END OF POLLING LIST
         EJECT
         SAVE  (14,12)
         BALR  BASEREG,0           ESTABLISH
         USING *,BASEREG,BASEREG2            ADDRESSABILITY
         LR    BASEREG2,BASEREG    INITIALIZE
         AH    BASEREG2,H4096                 SECOND BASE
         ST    13,SAVE+4
         LA    13,SAVE
         B     BEGIN          BRANCH AROUND PATCH
         DC    CL8'SAMP327R'       EYE CATCHER
         DC    CL8'&SYSDATE'       DATE LAST ASSEMBLY
         DC    S(*)                BASE/DISP OF PATCH
         DC    32X'00'             PATCH AREA
         DS    0H
BEGIN    EQU   *
*        OPEN THE LINE GROUP
         OPEN  (DCBR)    OPEN THE DCB
         TM    DCBR+48,X'10'     TEST FOR SUCCESSFUL OPEN
         BO    START        YES, GO TO START
         WTO   'OPEN FAILURE'
         B     ABNORMAL
START    LA    DSPTABRG,DSPTAB     ADDRESS THE DISPLAY TABLE
         LA    SELREG,SELDSPLY     GET SELECTION ADDRESS OF
*                                       FIRST 3270 DISPLAY
         SPACE 5
INITIAL   EQU  *
         LA    MSGADDR,FORMAT0     ADDR OF FORMAT0 MESSAGE
         LA    MSGLEN,FMT0SZ       LENGTH OF MESSAGE
         BAL   LNKREG,WRITETI      GO WRITE FORMAT 0
         BAL   LNKREG,RETCODE      CHECK RETURN CODE
         BAL   LNKREG,WAITD        WAIT FOR COMPLETION
         LA    SELREG,FIVE(SELREG) ADDR NEXT SPECIFIC POLL ENTRY
         TM    ZERO(SELREG),LAST   END OF SELECTION LIST
         BO    READ                YES, GO ISSUE A READ
         LA    SELREG,ONE(SELREG)  ADDR OF NEXT ENTRY
         B     INITIAL             NO, WRITE TO REMAINING DISPLAYS
         SPACE 5
READ     EQU   *
         LA    MSGADDR,INAREA      ADDR OF INPUT AREA
         XC    INAREA(255),INAREA  CLEAR INPUT
         XC    INAREA+255(43),INAREA+255      AREA             @ZA09359
         BAL   LNKREG,READTI       GO READ A DISPLAY
         BAL   LNKREG,RETCODE      CHECK RETURN CODE
         BAL   LNKREG,WAITD        WAIT FOR COMPLETION
         MVC   CUDVSAVE(TWO),INAREA+TWO SAVE CU,DV
         TM    DECBD+TWENTY4,X'02'           STATUS MSG RECIEVED
         BO    SSCHECK
         LA    SELREG,SPECPOL      ADDR OD SPEC POLL TABLE
         SR    REG2,REG2           CLEAR REGISTER 2
CHKIT    EQU   *
         CLC    ONE(TWO,SELREG),INAREA+TWO CHECK FOR CU,DV
         BE    FNDSEL              YES, GET SELECTION ADDR
         LA    REG2,ONE(REG2)      ADD ONE TO INDEX
         LA    SELREG,FIVE(SELREG)  POINT TO END OF ENTRY      @ZA10036
         TM    ZERO(SELREG),LAST   END OF LIST
         BO    ABNORMAL
         LA    SELREG,ONE(SELREG)  POINT TO NEXT ENTRY         @ZA10036
         B     CHKIT               NO KEEP CHECKING
FNDSEL   EQU   *
         STC   REG2,INDEX          SAVE INDEX BYTE
         LR    WORKREG,REG2        GET INDEX INTO ODD REGISTER
         M     REG2,SIXL           MULTIPLY INDEX BY 6
         LR    REG2,WORKREG        RE-ESTABLISH INDEX REG
         LA    SELREG,SELDSPLY     GET SELECTION ADDRES
         AR    SELREG,REG2         ADDR OUTPUT ENTRY IN TABLE
         ST    SELREG,SELSAVE      SAVE SELECTION ADDR
         SR    FMTREG,FMTREG       CLEAR FORMAT REG
         LA    DSPTABRG,DSPTAB     ADDR OF DISPLAY TABLE
         IC    REG2,INDEX                                      @ZA09358
         IC    FMTREG,ZERO(DSPTABRG,REG2)  GET FORMAT ID
         B     FORMATBR(FMTREG)
         SPACE 5
FORMATBR EQU   *
         B     FMT0                FORMAT 0 ON SCREEN
         B     FMT1                FORMAT 1 ON SCREEN
         B     FMT2                FORMAT 2 ON SCREEN
         SPACE 5
FMT0     EQU   *
*   VERIFY THE NAME AND SOCIAL SECURITY NUMBER. ASSUMING THAT THEY
* ARE VALID, WE SHALL CONTINUE PROCESSING.
FMT01    EQU   *
         LA    FMTREG,FOUR         GET FORMAT 1 ID
         STC    FMTREG,ZERO(DSPTABRG,REG2) STORE IN DISPLAY TABLE
         LA    MSGADDR,FORMAT1     ADDR OF FORMAT1 MESSAGE
         LA    MSGLEN,FMT1SZ       LENGTH OF MESSAGE
         BAL    LNKREG,WRITETI     GO WRITE FORMAT 1
         BAL   LNKREG,RETCODE      CHECK RETURN CODE
         BAL   LNKREG,WAITD        WAIT FOR COMPLETION
         B     READ                GO READ ANOTHER DISPLAY
         SPACE 5
FMT1     EQU   *
         CLI   INAREA+FOUR,ENTER   ENTER KEY INTERRUPT
         BE    ENTERINT            YES, GO UPDATE RECORDS
         CLI   INAREA+FOUR,PA1     PA1 KEY INTERRUPT
         BE    PA1INT              YES, GO MAKE HARD COPY
         CLI   INAREA+FOUR,PA2     PA2 OR CNCL KEY INTERRUPT
         BE    PA2INT              YES, GO DEACTIVATE TERMINAL
         CLI   INAREA+FOUR,CLEAR   CLEAR KEY INTERRUPT
         BE    CLEARINT            YES, GO WRITE FORMAT 2
         B     READ                IGNORE THE INTERRUPT AND GO READ
         SPACE 5
ENTERINT EQU   *
*   CREATE A NEW OR UPDATE AN EXISTING ENTRY IN YOUR PERMANENT
* DATA SET.
         LA    MSGADDR,ERALUNP     ADDR OF MESSAGE
         LA    MSGLEN,ERALUNPL     LENGTH OF MESSAGE
         BAL   LNKREG,WRITETI      GO ERASE ALL UNPROTECTED DATA
         BAL   LNKREG,RETCODE      CHECK RETURN CODE
         BAL   LNKREG,WAITD        WAIT FOR COMPLETION
         B     READ                GO READ ANOTHER DISPLAY
PA1INT   EQU   *
         B     NOPRINT             NO PRINTER DEFINED
PA2INT   EQU   *
*   DETERMINE IF ANY DATA WAS ENTERED. IF SO, CREATE A NEW OR UPDATE
* AN EXISTING ENTRY IN YOUR PERMANENT DATA SET. NOW DEACTIVATE THE
* TERMINAL.
PA2INT1  EQU   *
         LA    MSGADDR,CLOSEMG     ADDR OF CLOSE MSG
         LA    MSGLEN,CLOSEMGL     LENGTH OF MSG
         LA    SELREG,SELDSPLY     ADDR OF SELECTION TABLE
ONCEMORE EQU   *
         BAL   LNKREG,WRITETI      GO WRITE ENDING MSG
         BAL   LNKREG,RETCODE      CHECK RETURN CODE
         BAL   LNKREG,WAITD        WAIT FOR COMPLETION
         LA    SELREG,FIVE(SELREG) POINT TO INDICATOR BYTE
         TM    ZERO(SELREG),LAST   END OF SELECTION LIST
         BO    CLOSE               YES, TERMINATE PROGRAM
         LA    SELREG,ONE(SELREG)  POINT TO NEXT ADDRESS
         B     ONCEMORE            NO, WRITE ANOTHER MESSAGE
CLEARINT EQU   *
         LA    MSGADDR,FORMAT2     ADDR OF FORMAT 2 MSG
         LA    MSGLEN,FMT2SZ       LENGTH OF MSG
         BAL   LNKREG,WRITETI      GO WRITE FORMAT 2
         BAL   LNKREG,RETCODE      CHECK RETURN CODE
         BAL   LNKREG,WAITD        WAIT FOR COMPLETION
         LA    FMTREG,EIGHT        GET FORMAT 2 ID
         STC   FMTREG,ZERO(DSPTABRG,REG2) STORE IN DISPLAY TABLE
         B     READ                GO READ ANOTHER DISPLAY
FMT2     EQU   *
         CLI   INAREA+FOUR,ENTER   ENTER KEY INTERRUPT
         BE    FMT01               YES, GO WRITE FORMAT 1
         CLI   INAREA+FOUR,PA1     PA1 KEY INTERRUPT
         BE    PA1INT              YES, GO MAKE HARD COPY
         CLI   INAREA+FOUR,PA2     PA2 OR CNCL KEY INTERRUPT
         BE    PA2INT1             YES, GO DEACTIVATE TERMINAL
         CLI   INAREA+FOUR,CLEAR   CLEAR KEY INTERRUPT
         BE    CLEARINT            GO WRITE FORMAT 2
         B     READ                GO READ ANOTHER DISPLAY
NOPRINT  EQU   *
         LA    MSGADDR,NOPTR       ADDR OF NO PRINTER MSG
         LA    MSGLEN,NOPTRL       LENGTH OF MSG
         BAL   LNKREG,WRITETI      GO WRITE MSG
         BAL   LNKREG,RETCODE      CHECK RETURN CODE
         BAL   LNKREG,WAITD        WAIT FOR COMPLETION
         B     READ                GO READ ANOTHER DISPLAY
         EJECT
RETCODE  EQU   *
         B     RTNCDTAB(RTNCDRG)   BRANCH TO CORRESPONDING ENYRY
RTNCDTAB EQU   *
         B     RTNCD0              I/O SUCCESSFULLY INITIATED
         B     RTNCD4              DTFBT BUSY
         B     RTNCD8              INVALID RLN
         B     RTNCDC              INVALID TYPE CODE
         B     RTNCD10             ALL SKIP BITS ON
         B     RTNCD14             LINE ERROR AT OPEN
         B     RTNCD18             NO BUFFERS
         B     RTNCD1C             NO BUFFER POOL
         B     RTNCD20             NO BUFFER MANAGEMENT
         B     RTNCD24             BSC USAGE COUNT EXCEEDED
RTNCD0   EQU   *
         BR    LNKREG              RETURN
RTNCD4   EQU   *
         S     LNKREG,EIGHT8       SUBTRACT 8 FROM RETURN ADDR
         BR    LNKREG                 TO RETRY THE OPERATION
RTNCD8   EQU   *
RTNCDC   EQU   *
         B     ABNORMAL            THIS CONDITION SHOULD NOT OCCUR
RTNCD10  EQU   *
         B     CLOSE               ALL TERMINALS, TERMINATE
RTNCD14  EQU   *
         B     ABNORMAL            THIS CONDITION SHOULD NOT OCCUR
RTNCD18  EQU   *
         B     ABNORMAL            THIS CONDITION SHOULD NOT OCCUR
RTNCD1C  EQU   *
         B     ABNORMAL            THIS CONDITION SHOULD NOT OCCUR
RTNCD20  EQU   *
         B     ABNORMAL            THIS CONDITION SHOULD NOT OCCUR
RTNCD24  EQU   *
         B     ABNORMAL            THIS CONDITION SHOULD NOT OCCUR
         EJECT
WAITD    EQU   *
         WAIT  ECB=DECBD
CHK7FCC  EQU   *
         CLI   DECBD,SEVENF        NORMAL COMPLETION CODE
         BNE   CHK41CC             NO, KEEP CHECKING
         BR    LNKREG              RETURN
CHK41CC  EQU   *
         CLI   DECBD,FOURTY1       I/O ERROR COMP CODE
         BNE   ABNORMAL          INVALID COMP CODE
         CLI   DECBD+TWENTY8,TP06  TP CODE OF 6
         BNE   CKTP20              NO, CHECK OTHER TP CODES
         TM    DECBD+SIXTN,TIMEOUT DID DEVICE TIME OUT
         BNO   ABNORMAL            NO, TERMINATE
         S     LNKREG,TWELVE       SUBTRACR 12 FROM RETURN ADDR
         BR    LNKREG              TO RETRY THE OPERATION
CKTP20   EQU   *
         CLI   DECBD+TWENTY8,TP20  TP CODE OF 20
         BNE   CKTP11              NO, CHECK OTHERS
CHKEOT   EQU   *
         TM    DECBD+TWENTY4,EOTRSPTX EOT RESPONSE TO TEXT
         BNO   ABNORMAL            NO, TERMINATE
         B     READ                YES, GO GET SENSE STATUS MESSAGE
CKTP11   EQU   *
         CLI   DECBD+TWENTY8,TP11  TP CODE OF 11
         BNE   ABNORMAL            NO, TERMINATE
         B     CHKEOT              YES, CHECK EOT RESPONSE TO TEXT
CHKTP20  EQU   *
         CLI   DECBD+TWENTY8,TP20  TP CODE OF 20
         BNE   ABNORMAL            NO, TERMINATE
         WRITE DECBD,TR,DCBR,INAREA,,,1,MF=E
         B     CHK7FCC             CHECK COMPLETION CODE
         EJECT
CLOSE    EQU   *
         CLOSE (DCBR)    CLOSE THE DCB
         L    13,SAVE+4
         RETURN (14,12)
         SPACE 5
SSCHECK  EQU   *
*     INVESTIGATE THE SENSE/STATUS BYTES SENT BY THE REMOTE DEVICE.
*  IF RECOVERY IS POSSIBLE, ATTEMPT TO DO SO.  WE SHALL ASSUME THAT
*  THE ERROR IS UNRECOVERABLE AND TERMINATE.
         SPACE 5
ABNORMAL EQU   *
         ABEND 1,DUMP     TAKE A DUMP
         L    13,SAVE+4
         RETURN (14,12)
         EJECT
FORMAT0  EQU   *
         DC    X'0227F5'           STX,ESC,E/W
         DC    X'C71DC811C150'     WCC, SF = PROT, SBA = 80
         DC    C'GOOD MORNING.'
         DC    X'1D6011C15F'       SF = PROT, SBA = 94
         DC    C'THIS BEGINS THE DEMONSTRATION '
         DC    C'OF THE  OS/BTAM '
         DC    X'1DC8'             SF = PROT
         DC    C'3270 '
         DC    X'1D60'             SF = PROT
         DC    C'REMOTE SAMPLE PROGRAM.'
         DC    X'1D6011C3F0'       SF = PROT, SBA = 240
         DC    C'ENTER THE FOLLOWING:'
         DC    X'1D6011C4D8'       SF = PROT, SBA = 280
         DC    C'NAME:'
         DC    X'1D4013'           SF = UNPROT, IC
         DC    X'11C5401D60'       SBA = 320,SF = PROT
         DC    C'SOC SEC NUM:'
         DC    X'1D40'             SF = UNPROT
         DC    X'11C5E81D60'       SBA = 360,SF = PROT
         DC    X'03'               ETX
FMT0SZ   EQU   *-FORMAT0
         SPACE 5
FORMAT1  EQU   *
         DC    X'0227F5'           STX,ESC,E/W
         DC    X'C71D60114040'     WCC, SF = PROT, SBA = 0
         DC    C'ENTER DATA REQUESTED BELOW:'
         DC    X'11C150'           SBA = 80
         DC    C'NAME:'
         DC    X'1D401311C1F81D60' SF = UNPROT, IC, SBA = 120,
*                                   SF = PROT
         DC    C'ADDR:'
         DC    X'1D4011C2601D60'   SF = UNPROT, SBA = 160, SF = PROT
         DC    C'CITY:'
         DC    X'1D4011C3C81D60'   SF = UNPROT, SBA = 200, SF = PROT
         DC    C'STATE:'
         DC    X'1D4011C3E41D60'   SF = UNPROT, SBA = 228, SF = PROT
         DC    C'ZIP:'
         DC    X'1D5011C3F01D6011C4D8'  SF = UNPROT, SBA = 240,
*                                  SF = PROT, SBA = 280
         DC    C'ENTER KEY: ENTER DATA;'
         DC    X'11C540'           SBA = 320
         DC    C'PA1 KEY: PRINT DATA;'
         DC    X'11C5E8'           SBA = 360
         DC    C'PA2 (CNCL) KEY: DEACTIVATE TERMINAL;'
         DC    X'11C650'           SBA = 400
         DC    C'CLEAR KEY: CONTROL OPTIONS;'
         DC    X'03'               ETX
FMT1SZ   EQU   *-FORMAT1
         EJECT
FORMAT2  EQU   *
         DC    X'0227F5'           STX,ESC,E/W
         DC    X'C711404013'       WCC, SBA = 0, IC
         DC    C'XXYY4CCDD'
         DC    X'1140E8'           SBA = 40
         DC    C'TO REQUEST BTAM OLT -- ENTER REQUEST FOR TEST MESS'
         DC    C'AGE OVER SAMPLE FORMAT ABOVE:  XX=TEST NO. (23-28)'
         DC    C'  YY=REPEATS (01-99) CCDD=ADDRESS OF TARGET DEVICE'
         DC    C'THEN HIT ERASE EOF AND THEN TEST REQ. USE CLEAR KE'
         DC    C'Y TO RESUME AFTER TEST.'
         DC    X'11C3C8'           SBA = 200
         DC    X'03'               ETX
FMT2SZ   EQU   *-FORMAT2
         SPACE 5
NOPTR    EQU   *
         DC    X'0227F1'           STX,ESC,WRITE
         DC    X'C61DC811C6F8'     WCC, SF = PROT, SBA = 440
         DC    C'NO PRINTER DEFINED FOR THIS PROGRAM'
         DC    X'03'               ETX
NOPTRL   EQU   *-NOPTR
         SPACE 5
CLOSEMG  EQU   *
         DC    X'0227F5'           STX,ESC,E/W
         DC    X'C71140401DC8'     WCC, SBA = 0, SF = PROT
         DC    C'     THE REMOTE 3270 SAMPLE PROGRAM HAS CONCLUDED.'
         DC    X'03'               ETX
CLOSEMGL EQU   *-CLOSEMG
         SPACE 5
         SPACE 5
READBUF  EQU   *
         DC    X'0227F203'         STX,ESC,RD BUF,ETX
READBUFL EQU   *-READBUF
         SPACE 5
ERALUNP  EQU   *
         DC    X'02276F03'         STX,ESC,EAU,ETX
ERALUNPL EQU   *-ERALUNP
         EJECT
*        READ AND WRITE MACROS
         DS    0F
WRITETI  EQU   *
         WRITE DECBD,TI,DCBR,(MSGADDR),(MSGLEN),(SELREG),1,MF=E
         BR    LNKREG
         SPACE 5
READTI   EQU   *
         READ  DECBD,TI,DCBR,INAREA,300,POLDSPLY,1,MF=E
         BR    LNKREG
         EJECT
READTRV  EQU   *
         READ  DECBD,TRV,DCBR,INAREA,256,,1,MF=E
         BR    LNKREG              RETURN
         SPACE 5
WRITETIV EQU   *
         WRITE DECBD,TIV,DCBR,(INAREA,(MSGADDR)),(256,(MSGLEN)),       X
               (SELREG),1,MF=E
         BR    LNKREG              RETURN
         EJECT
READTT   EQU   *
         READ  DECBD,TT,DCBR,(MSGADDR),256,,1,MF=E
         BR    LNKREG              RETURN
         EJECT
*        DISPLAY SELECTION ADDRESSES
*   THE CURRENT MACRO OPERANDS ARE FOR TWO REMOTE 3270 DISPLAYS;
*                          1. 0TH CU, 0TH DEV (60604040)
*                          2. 0TH CU, 1ST DEV (6060C1C1)
SELDSPLY DFTRMLST OPENLST,(606040402D,6060C1C12D)
         SPACE 5
*    DISPLAY SPECIFIC POLLING ADDRESSES
*   THE CURRENT MACRO OPERANDS ARE FOR TWO REMOTE 3270 DISPLAYS:
*                          1. 0TH CU, 0TH DEV (40404040)
*                          2. 0TH CU, 1ST DEV (4040C1C1)
SPECPOL  DFTRMLST OPENLST,(404040402D,4040C1C12D)
         EJECT
*        DISPLAY GENERAL POLLING ADDRESS
POLDSPLY DFTRMLST AUTOWLST,(40407F7F2D,3737373737)
         SPACE 5
         SPACE 5
*        CONSTANTS
H4096    DC    H'4096'
EIGHT8   DC    F'8'                CONSTANT OF 8
DSPTAB   DC    XL32'0'             DISPLAY TABLE
TWELVE   DC    F'12'               LENGTH OF 12
INDEX    DC    X'00'               INDEX BYTE SAVE AREA
CUDVSAVE DC    X'0000'             CU,DV SAVEAREA
SELSAVE  DC    F'0'
SIXL     DC    F'6'
INAREA   DC    500F'0'             INPUT AREA
INAREAL  EQU   *-INAREA
SAVE     DC    18F'0'
         DS    0F
DECBADDR DC    X'80'
         DC    AL3(DECBD)
DCBR     DCB   DSORG=CX,MACRF=(R,W),DDNAME=DD3270,EROPT=T,DEVD=BS,     X
               MODE=(,,A,A),CODE=EBCDIC
         READ  DECBD,TI,DCBR,MF=L
         END
//GO.DD3270 DD UNIT=050
//GO.SYSABEND DD SYSOUT=A
