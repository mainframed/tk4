X10      TITLE 'IEX10 - COMPILER INITIALIZATION PHASE, ALGOL F'
*
*        COMPONENT ID - 360S-AL-531 ALGOL F COMPILER
*
*        FUNCTION/OPERATION -
*        THIS MODULE PERFORMS THE FOLLOWING INITIALIZATION
*        ACTIONS -
*        1. SAVE THE REGISTERS AND ESTABLISH THE LOWER
*           PART OF THE COMMON WORKAREA AS THE NEW SAVEAREA
*           TO BE USED THROUGHOUT THE COMPILATION
*        2. EXECUTE A SPIE MACRO
*        3. SCAN THE OPTION PARAMETER FIELD PROVIDED BY THE USER
*           AND SET THE SWITCHES IN COMPFLGS ACCORDINGLY
*        4. IF PROVIDED BY THE USER, INSERT NEW DD NAMES INTO
*           THE DCB'S AND STORE THE INITIAL PAGECOUNT IN THE COMMON
*           COMMON WORKAREA
*        5. OBTAIN STORAGE FOR THE ERROR POOL AND MOVE ANY ERROR
*           PATTERNS ALREADY STORED FROM THE PRELIMINARY ERROR POOL
*           INTO THE NEW POOL
*        6. COMPLETE AND OPEN THE DCB'S FOR ALL DATASETS TO BE USED
*        7. OBTAIN THE TIME AND DATE WITH A TIME MACRO
*           STORE IT IN EDITED FORM IN A HEADLINE, WHICH IS
*           PRINTED AT THE TOP OF THE FIRST PAGE
*        8. TRANSFER CONTROL TO THE SCANI/II PHASE (IEX11)
*
*        IF ERRORS ARE DETECTED IN THE STEPS DESCRIBED ABOVE
*        (SUCH AS INVALID OPTION PARAMETERS OR INCORRECT DD
*        CARDS), SOME OF THE STEPS MAY BE BYPASSED, AND AN ERROR
*        EXIT MAY BE TAKEN
*
*        ENTRY POINTS -
*        IEX10000 - THE ONLY ENTRY POINT TO THIS MODULE
*                   CONTROL IS TRANSFERRED HERE FROM IEX00 BY THE
*                   MACRO LINK EP=IEX10. R0 THEN CONTAINS THE
*                   ADDR OF THE COMMON WORKAREA AND R1 THE ADDR
*                   OF THE PARAMETER LIST PROVIDED BY THE USER
*
*        INPUT - N/A
*
*        OUTPUT -
*        THE TEXT STORED AT FIRSTLIN IS OUTPUT TO SYSPRINT AS A
*        91-CHARACTER RECORD WITH MACHINE CONTROL CHARACTER
*
*        EXTERNAL ROUTINES - N/A
*
*        IF NO TERMINATING ERRORS ARE FOUND, CONTROL IS
*        TRANSFERRED TO THE SCANI/II PHASE BY MEANS OF THE MACRO
*        'XCTL EP=IEX11'
*
*        EXITS - ERRORS -
*        IF A PROGRAM CHECK OR UNRECOVERABLE I/O ERROR OCCURS
*        DURING ONE OF THE INITIALIZATION STEPS, CONTROL IS
*        TRANSFERRED (VIA AN ERROR ROUTINE IN IEX00) TO THE
*        ROUTINE GOTOTERM IN THIS MODULE. FROM THERE AN EXIT IS
*        MADE TO THE TERMINATING PHASE BY MEANS OF THE MACRO
*        XCTL EP=IEX51002
*
*        IF ANY OTHER TERMINATING ERROR IS DETECTED, CONTROL IS
*        TRANSFERRED TO THE ROUTINE GOTOEDIT IN THIS MODULE. FROM
*        THERE AN EXIT IS MADE TO THE ERROR EDITING MODULE BY
*        MEANS OF THE MACRO XCTL EP=IEX21000
*
*        TABLES/WORK AREAS -
*        STARTING AT PTRN200 - ERROR PATTERNS FOR ALL
*                   ERRORS WHICH MAY BE DETECTED IN THIS MODULE
*        FIRSTLIN - THE FIRST OUTPUT LINE TO BE PRINTED
*                   CONTAINS PROGRAM IDENTIFICATION AND
*                   LEVEL AND THE DATE OF THE RUN
*        PARMLIST - A TABLE USED FOR DECODING THE OPTION
*                   PARAMETERS
*
*        ATTRIBUTES - NOT REUSABLE
*
*        NOTES -
*        THIS MODULE IS ONLY INTENDED TO BE USED IN CONNECTION
*        WITH THE OTHER MODULES COMPRISING THE ALGOL COMPILER
*
*        THE OPERATION OF THIS MODULE DEPENDS UPON AN INTERNAL
*        REPRESENTATION OF THE EXTERNAL CHARACTER SET WHICH IS
*        EQUIVALENT TO THE ONE USED AT ASSEMBLY TIME
*
IEX10000 CSECT
*
*        REGISTER ASSIGNMENTS
*
*        R4         RUNNING ADDR OF PARAMETER FIELD MINUS 2
*        R5         ADDR OF BYTE FOLLOWING LAST BYTE OF FIELD MINUS 2
*        R6         RUNNING ADDR OF PARAMETER LIST
*        R7         ADDR OF LAST ENTRY OF PARAMETER LIST
*        R8         LENGTH-1 OF PARAMETER
*        R9         CURRENT FIELD IN ERROR POOL
*        R10        NEXT FIELD IN ERROR POOL
*        R11        BYTE COUNT OF PARAMETER IN ERROR
*
         SAVE  (14,12),,'IEX10000 LEVEL 2.1 &SYSDATE &SYSTIME'
*
         LR    R2,R15                  SET BASE
         USING IEX10000,R2
         ST    R0,8(R13)               ADDR OF LOWER LEVEL SAVAREA
         LR    R12,R13
         LR    R13,R0
         ST    R12,4(R13)              ADDR OF HIGHER LEVEL SAVEAREA
         USING WORKAREA,R13
*
*        EXECUTE THE SPIE MACRO
*
         L     R3,PICAADD              ADDR OF PICA OF COMPILER
*
         SPIE  MF=(E,(3))
*
         ST    R1,PICAADD              STORE ADDR OF OLD PICA IN
         MVC   ERET,=A(GOTOTERM)       ERROR ROUT FOR NEXT PART
*                                      OF PHASE
*
*        INTERROGATE THE CONTROL PARAMETERS AND SET SWITCHES IN
*        THE WORKAREA
*
         L     R14,4(,R13)
         L     R14,24(,R14)            GET CALLERS R1
         LTR   R14,R14                 ZERO ?
         BZ    BYPASSOP                YES, NO OPTIONS ARE SPECIFIED
         L     R4,0(,14)               ADDR OF PARMETER FIELD
         LA    R4,0(,R4)               RESET HIGH ORDER BYTE
         LH    R5,0(,R4)               FIELD LENGTH
         LTR   R5,R5                   NONZERO FIELD LENGTH ?
         BZ    DDNAMES                 NO CONTROL PARAMETERS SPECIFIED
         AR    R5,R4                   END OF FIELD
         LA    R7,LSTENTRY             LAST ENTRY OF LIST
         SR    R8,R8                   ZERO REG
FNDCOMMA CLI   2(R4),C','              COMMA ?
         BNE   FNDPAR                  NO, BRANCH
STEP1    LA    R4,1(,R4)               STEP FIELD
COMP1    CR    R4,R5
         BL    FNDCOMMA                IF NOT END OF FIELD YET
         TM    COMPFLGS+1,NLOAD+NDECK  NOLOAD AND NODECK ?
         BNO   DDNAMES                 NO, BRANCH
         OI    COMPFLGS,COMPMODE       YES, FORCE SYNTAX MODE ONLY
         B     DDNAMES                 END OF FIELD
*
         USING LENGTH,R6
COMPINST CLC   2(0,R4),PARAM
FNDPAR   CLC   2(5,R4),=C'SIZE='
         BE    FNDSIZE
         LA    R6,PARMLIST
NXTPAR   IC    R8,LENGTH
         EX    R8,COMPINST
         BNE   NOTFOUND
         LA    R10,1(R4,R8)            ADDR OF BYTE FOLLOWING
*                                      PARAMETER-2
         CLI   2(R10),C','             COMMA ?
         BE    SETBIT                  YES, BRANCH
         CR    R10,R5                  END OF PARAMETER FIELD ?
         BL    ERROR200                IF NO
SETBIT   LR    R4,R10
         EX    0,INSTR
         DROP  R6
         B     COMP1
*
NOTFOUND CR    R6,R7
         LA    R6,LENGTH-PARMLIST(R6)  STEP LIST ADDR
         BL    NXTPAR                  IF NOT END OF LIST YET
*
ERROR200 L     R9,NEXTERR              ADDR OF NEXT FREE BYTE
*                                      IN ERRORPOOL
         CLI   0(R9),C'X'              TEST IF THE PRELIMINARY ERROR
         BE    FULLPOOL                POOL IS FILLED
         LA    R10,16(R9)
         ST    R10,NEXTERR
         MVC   0(16,R9),PTRN200        PATTERN WITH BLANKS
*                                      TO ERROR POOL
         LA    R11,12(0)
MOVE     MVC   4(1,R9),2(R4)
         LA    R9,1(R9)
MOVENOT  LA    R4,1(R4)
         CLI   2(R4),C','
         BE    STEP1                   IF END OF PARAMETER
         CLR   R4,R5
         BNL   DDNAMES                 IF END OF PARAMETER LIST
         SH    R11,=H'1'
         BP    MOVE
         B     MOVENOT
*
FNDSIZE  LA    R10,5(,R4)              ADDR OF FIRST DIGIT -2
         SR    R11,R11                 SET COUNT OF DIGITS TO ZERO
TSTDIGIT CLI   2(R10),C'0'             TEST IF
         BL    ERROR200                THE CHARACTER
         CLI   2(R10),C'9'             IS A DIGIT
         BH    ERROR200
         LA    R10,1(,R10)
         CR    R10,R5                  END OF PARAMTER FIELD ?
         BE    PCKSIZE
         CLI   2(R10),C','             DIGIT IS FOLLOWED ?
*                                      BY A COMMA
         BE    PCKSIZE
         LA    R11,1(,R11)             ADD 1 TO DIGIT COUNT
         CH    R11,=H'7'               TEST IF TOO MANY DIGITS
         BE    ERROR200
         B     TSTDIGIT
*
PCKINSTR PACK  FIELD1,7(0,R4)
PCKSIZE  EX    R11,PCKINSTR            L2 IS IN TEGH
         CVB   R6,FIELD1
         LR    R4,R10
         C     R6,=F'45056'            SPECIFIED STORAGE SIZE
         BL    ERROR208                IS TOO SMALL ?
         ST    R6,SIZE
         B     COMP1
*
ERROR208 L     R9,NEXTERR
         MVC   0(4,R9),PTRN208         PATTERN FOR MSG 208
*                                      TO ERROR POOL
         LA    R9,4(R9)                UPDATE POINTER TO NEXT
         CLI   0(R9),C'X'              TEST IF THE PRELIMINARY ERROR
         BE    FULLPOOL                POOL IS FILLED
         ST    R9,NEXTERR              FREE ENTRY
         B     COMP1
*
FULLPOOL MVC   0(4,R9),PTRN206         PATTERN FOR MESSAGE 206
         LA    R9,4(,R9)               UPDATE POINTER TO
         ST    R9,NEXTERR              NEXT FREE ENTRY
         B     DDNAMES                 NO PARAMETER TESTING ANY MORE
*
*        FIND THE DD NAMES
*
DDNAMES  TM    0(14),X'80'
         BO    BYPASSOP                IF NO DDNAMES AND HEADING INFO
         L     R4,4(,14)
         LH    R5,0(,R4)
         LTR   R5,R5
         BZ    BYPASSOP                IF NO DDNAMES SPECIFIED
         CL    R5,=F'80'               MORE THAN TEN
         BNH   FNDDCB-4                DD NAMES
         L     R5,=F'80'               NOT ALLOWED
         L     R9,NEXTERR
         MVC   0(4,R9),PTRN207         PATTERN TO ERROR POOL
         LA    R9,4(,R9)               UPDATE POINTER
         ST    R9,NEXTERR              TO ERROR POOL
         LA    R6,DCBTABLE             R6 -> TABLE OF DCB ADDRS
FNDDCB   L     R7,0(,R6)
         LTR   R7,R7                   THIS DATASET EXIST ?
         BZ    NXTDDNAM                NO, BRANCH
         CLC   2(8,R4),=XL8'00'        NULL DDNAME ?
         BE    NXTDDNAM                YES, BRANCH
         USING IHADCB,R7
         MVC   DCBDDNAM,2(R4)          MOVE IN OVERRIDE DDNAME
         DROP  R7
NXTDDNAM LA    R4,8(,R4)               INCR DDNAME PTR
         LA    R6,4(,R6)
         SH    R5,=H'8'                DECR COUNT
         BP    FNDDCB
*
*        ACQUIRE THE ERROR POOL AND THE SOURCE PROG BUFFER 1
*        STORE CONTENT OF PRELIMINARY ERROR POOL IN NEW ERROR POOL
*
BYPASSOP L     R0,POOLS                SIZE OF ERROR POOL
         LR    R4,R0
         A     R0,SRCE1S               SIZE OF SOURCE PROG BUFFER 1
         LR    R6,R0                   SIZE OF TOTAL AREA
*
         GETMAIN R,LV=(0)
*
         XI    COMPFLGS+1,NOBUF        COMMON BUFFER AREA PRESENT
         L     R5,NEXTERR              ADDR OF NEXT FREE BYTE
*                                      IN PREL POOL
         LA    R7,PRELPOOL             ADDR OF BEGIN OF PREL POOL
         SR    R5,R7                   NUMBER OF BYTES IN ERROR POOL
         EX    R5,MVCPOOL
         B     JUMPEXEA                BRANCH AROUND MVC INSTR
*
MVCPOOL  MVC   0(0,R1),PRELPOOL        EXE INSTRUCTION
*
JUMPEXEA ST    R1,ERRPOOL              ADDR OF FIRST BYTE OF POOL
         AR    R5,R1                   ADDR OF NEXT FREE BYTE
         ST    R5,NEXTERR
         AR    R4,R1                   ADDR OF END OF POOL +1
         ST    R4,SRCE1ADD             ADDR OF SOURCE PROG BUFFER 1
         AR    R6,R1                   END ADDR OF SOURCE PROG BUFFER 1
         ST    R6,SRCE1END
         SH    R4,=H'24'               ADDR OF END OF POOL-23
         ST    R4,ENDPOOL
*
*        COMPLETE THE DCBS
*
         LM    R3,R12,DCBTABLE         ADDRS OF THE DCBS
*                                      R4, R5, R6 NOT USED
         USING IHADCB,R3               SYSLIN DCB
         MVC   DCBEXLSA,=AL3(LINEX)    ADDR OF EXLIST
         DROP  R3
         USING IHADCB,R7               SYSIN DCB
         MVC   DCBEXLSA,=AL3(INEX)     ADDR OF EXLIST
         DROP  R7
         USING IHADCB,R8
         MVC   DCBEXLSA,=AL3(PRINTEX)  EXLIST FOR PRINT DCB
         DROP  R8
         USING IHADCB,R9               SYSPUNCH DCB
         MVC   DCBEXLSA,=AL3(PCHEX)    ADDR OF EXLIST
         DROP  R9
         USING IHADCB,R10              SYSUT1 DCB
         LH    R4,SRCE1S+2             SOURCE PROG BUFFER 1 SIZE
         STH   R4,DCBBLKSI             BLOCK SIZE
         DROP  R10
         USING IHADCB,R11              SYSUT2 DCB
         STH   R4,DCBBLKSI             BLOCK SIZE
         DROP  R11
         MVC   ERET,=A(OPEXERR)        ERROR ROUTINE FOR OPEN EXIT
*                                      ROUTINES
*
*        OPEN THE SYSLIN, SYSPRINT, SYSPUNCH, SYSUT2 AND SYSUT3 DCBS
*
         OPEN  ((3),OUTPUT,(8),OUTPUT,(9),OUTPUT,(11),OUTIN,(12),OUTIN)
*
         OPEN  ((10),OUTIN,(R7),INPUT)  OPEN SYSUT1 AND SYSIN DCBS
*
         MVC   ERET,=A(GOTOTERM)
*
*        TEST IF THE SYSPRINT DCB HAS BEEN OPENED
*
         USING IHADCB,R8
TSTDCB   TM    DCBOFLGS,DCBOFOPN       OPEN SUCCESSFULL ?
         DROP  R8
         BO    EDITDATE                YES, BRANCH
         OI    COMPFLGS+2,PRTNO        SYSPRINT DCB FAILED TO OPEN
         LR    R7,R8                   DCB ADDR
         L     R4,NEXTERR
         B     ERROR201
*
*        GET DATE AND TRANSLATE IT TO DD MMM YYYY HH:MM:SS
*
EDITDATE TIME  DEC                     GET SYSTEM DATE
*
         STM   R0,R1,TEMP2             STORE SYSTEM TIME AND DATE
*                                      INTO TEMP2 AND TEMP3
         ED    HEADTIME,TEMP2          FORMAT TIME
         UNPK  TEMP(3),TEMP3+1(2)      UNPACK YY
         MVC   HEADYEAR+2(2),TEMP      MOVE YEAR ACROSS
         ZAP   TEMP1,TEMP3+2(2)        JULIN DAY NO INTO DOUBLE WORD
         CVB   R14,TEMP1               GET JULIAN DAY IN BINARY
         LA    R15,MNTHTBL             R15 -> MNTHTBL
         SH    R15,=H'8'               ADJUST ADDR FOR FIRST ITERATION
         TM    TEMP3+1,X'01'           IF ODD YEAR THEN
         BO    NOTLEAP                 NOT LEAP YEAR
         TM    TEMP3+1,X'12'           TEST FOR LEAP (OK TILL 2099)
         BNM   NEXTMNTH                IF MIXED NOT LEAP YEAR
NOTLEAP  CH    R14,H60                 DDD AFTER 28TH FEBRUARY?
         BL    NEXTMNTH                NO, LEAP YEAR IRRELEVANT
         LA    R14,1(,R14)             YES, FUDGE DDD ACCORDINGLY
NEXTMNTH LA    R15,8(,R15)             INCREMENT THRU MONTH TABLE
         SH    R14,0(,R15)             DECREASE NUMBER OF DAYS
         BP    NEXTMNTH                NOT YET, TRY NEXT MONTH
*                                      FOUND THE CORRECT MONTH
         AH    R14,0(,R15)             ADD BACK THE DAYS IN MONTH
         CVD   R14,TEMP1               GET DAY OF MONTH
         OI    TEMP1+7,X'0F'           MAKE UNPK RESULT PRINTABLE
         UNPK  HEADDAY,TEMP1+6(2)      FORMAT DAY OF MONTH
         MVC   HEADMON,2(R15)          MOVE IN MONTH NAME
*
*        SETUP HEADING LINE
*
         MVC   PAGEHD1,HEADING         FIRST LINE ON PAGE
*
         MVC   ERET,=A(GOTOEDIT)       ERROR ROUTINE FOR REST OF PHASE
*
*        TEST IF THE OTHER DCBS HAVE BEEN OPENED
*
         LM    R3,R12,DCBTABLE         R3 -> SYSLIN DCB
         L     R4,NEXTERR
         USING IHADCB,R3
         TM    COMPFLGS+1,NLOAD        NOLOAD SPECIFIED ?
         BO    TSTPUNCH                YES, BRANCH
         TM    DCBOFLGS,DCBOFOPN       DCB OPENED SUCCESSFULLY ?
         BO    TSTPUNCH                YES, BRANCH
         OI    COMPFLGS+1,NLOAD        NO, SET NOLOAD OPTION
         MVC   0(4,R4),PTRN202         PATTERN FOR MSG 202
         LA    R4,4(R4)                POINTER TO NEXT FREE ENTRY
         DROP  R3
         USING IHADCB,R9               R9 -> SYSPUNCH
TSTPUNCH TM    COMPFLGS+1,NDECK        NODECK SPECIFIED ?
         BO    TSTIN                   YES, BRANCH
         TM    DCBOFLGS,DCBOFOPN       SYSPUNCH DCB OPEN SUCESSFULLY ?
         BO    TSTIN                   YES, BRANCH
         OI    COMPFLGS+1,NDECK        NO, SET NODECK OPTION
         MVC   0(4,R4),PTRN203         PATTERN FOR MSG 203
         LA    R4,4(,R4)               POINTER TO NEXT FREE ENTRY
         DROP  R9
*
TSTIN    BAL   R14,TSTDCBRT            DCB ADDR FOR SYSIN IS IN REG 7
         LR    R7,R10                  TEST SYSUT1
         BAL   R14,TSTDCBRT
         LR    R7,R11                  TEST SYSUT2
         BAL   R14,TSTDCBRT
         LR    R7,R12                  TEST SYSUT3
*
*        ENTRY IF SYSPRINT NOT OPENED
*
ERROR201 LA    R14,STPOINTR            DO NOT RETURN
         USING IHADCB,R7
TSTDCBRT TM    DCBOFLGS,DCBOFOPN       DCB OPENED ?
         BOR   R14                     YES, RETURN
         MVC   0(4,R4),PTRN201         PATTERN FOR MSG 201
         MVC   4(8,R4),DCBDDNAM        MOVE DDNAME INTO MSG
         LA    R4,12(,R4)              UPDATE POINTER
         OI    COMPFLGS+1,NOGO         SET NOGO SWITCH ON
         BR    R14                     RETURN
*
         DROP  R7
STPOINTR ST    R4,NEXTERR
*
*        END OF INITIALIZATION OF THE COMPILER
*        GOTO SCAN 1/2 OR TO PHASE 21 FOR ERROR EDITING IF
*        COMPILATION IS IMPOSSIBLE
*
         TM    COMPFLGS+1,NOGO
         BO    GOTOEDIT                IF COMPILATION IMPOSSIBLE
         L     R2,=A(IEX10001)
         BR    R2                      TO INITIALIZE IEX11
*
*        DCB EXIT ROUTINES
*
         USING IHADCB,R1
*
*        SYSIN OPEN EXIT
*
INEXRT   LH    R6,DCBBLKSI             GET BLKSIZE
         LTR   R6,R6                   VALUE PROVIDED ?
         BNZR  R14                     YES, RETURN
         MVC   DCBBLKSI,DCBLRECL       NO, SET BLKSIZE TO LRECL
         BR    R14                     RETURN
*
*        SYSLIN OPEN EXIT
*
LINEXRT  LH    R6,DCBBLKSI             GET BLKSIZE
         LTR   R6,R6                   VALUE PROVIDED ?
         BNZR  R14                     YES, RETURN
         LA    R6,3200                 NO, SET A DEFAULT OF 3200
         STH   R6,DCBBLKSI             UPDATE BLKSIZE
         BR    R14                     RETURN
*
*        SYSPUNCH OPEN EXIT
*
PCHEXRT  LH    R6,DCBBLKSI             GET BLKSIZE
         LTR   R6,R6                   VALUE PROVIDED ?
         BNZR  R14                     YES, RETURN
         MVC   DCBBLKSI,DCBLRECL       NO, SET BLKSIZE TO LRECL
         BR    R14                     RETURN
*
*        SYSPRINT OPEN EXIT
*
PRTEXRT  LH    R6,DCBBLKSI             GET BLKSIZE
         LTR   R6,R6                   VALUE PROVIDED ?
         BNZR  R14                     YES, RETURN
         MVC   DCBBLKSI,DCBLRECL       NO, SET BLKSIZE TO LRECL
         BR    R14                     RETURN
*
         DROP  R1
*
*        ERROR ROUTINES
*
*        ROUTINE FOR UNEXPECTED ERROR DURING OPEN
*        ENTERED VIA ERET
*
OPEXERR  OI    COMPFLGS+1,NOGO
         BR    R14                     GO BACK TO OPEN ROUTINE
*
         DROP  R2
*
*        EXIT TO TERMINATING PHASE IF ERROR EDITING NOT POSSIBLE
*
GOTOTERM BALR  R2,0
         USING *,R2
         BAL   R4,CLOSE
*
         XCTL  EP=IEX51002             GOTO TERMINATING PHASE
*
         DROP  R2
*
*        TERMINATE VIA ERROR EDITING PHASE
*
GOTOEDIT BALR  R2,0
         USING *,R2
         BAL   R4,CLOSE
         OI    COMPFLGS,TERR           INDICATE TERMINATING ERROR
*
         XCTL  EP=IEX21000             GOTO EDIT PHASE
*
         DROP  R2
*
*        SUBROUTINE FOR CLOSING SYSIN AND SYSUT1
*
CLOSE    BALR  R3,0
         USING *,R3
         L     R6,ASYSDCB              R6 -> SYSIN DCB
         USING IHADCB,R6
         TM    DCBOFLGS,DCBOFOPN       OPEN SUCCESFULLY ?
         DROP  R6
         BO    *+8                     YES, BRANCH
         OI    SWITCH+1,X'F0'          NO, SYSIN NOT OPENED
         L     R7,AUT1DCB              R7 -> SYSUT1 DCB
*
         CLOSE ((6),,(7))              CLOSE SYSIN AND SYSUT1
*
SWITCH   NOPR  R4                      RETURN IF SYSIN NOT OPENED
*
         FREEPOOL (R6)                 FREE SYSIN BUFFER
*
         BR    R4                      RETURN
*
         DROP  R3
*
         LTORG
*
*        DCB EXIT LIST FOR SYSIN, SYSLIN, SYSPUNCH AND SYSPRINT
*
         DC    0F'0'
INEX     DC    X'85',AL3(INEXRT)
LINEX    DC    X'85',AL3(LINEXRT)
PCHEX    DC    X'85',AL3(PCHEXRT)
PRINTEX  DC    X'85',AL3(PRTEXRT)
*
*
*        PATTERNS FOR THE ERROR MESSAGES OF THE INITIALIZATION
*        PHASE
*
*        W OPTION PARAMETER ... IS INVALID. THE PARAMETER IS
*        DISREGARDED
*
PTRN200  DC    AL1(128+16,200),AL2(0),CL12' '
*
*        T DD CARD FOR ... IS INCORRECT OR MISSING
*
PTRN201  DC    AL1(128+12,201),AL2(0)
*
*        W DD CARD FOR SYSLIN IS INCORRECT OR MISSING. OPTION
*        NOLOAD IS ASSUMED
*
PTRN202  DC    AL1(128+4,202),AL2(0)
*
*        W DD CARD FOR SYSPUNCH IS INCORRECT OF MISSING. OPTION
*        NODECK IS ASSUMED
*
PTRN203  DC    AL1(128+4,203),AL2(0)
*
*        T BLOCKSIZE SPECIFIED FOR SYSIN IS INCORRECT
*
PTRN204  DC     AL1(128+4,204),AL2(0)
*
*        W BLOCKSIZE SPECIFIED FOR ... IS INCORRECT. UNBLOCKED
*        OUTPUT IS GENERATED INSTEAD
*
PTRN205A DC    AL1(128+12,205),AL2(0),CL8'SYSLIN'
PTRN205B DC    AL1(128+12,205),AL2(0),CL8'SYSPUNCH'
PTRN205C DC    AL1(128+12,205),AL2(0),CL8'SYSPRINT'
*
*        W INCORRECT PARAMETER FIELD. NO OPTION PARAMETERS ARE
*        HANDLED ANY MORE
*
PTRN206  DC    AL1(128+4,206),AL2(0)
*
*        W POSSIBLE ERROR IN DD NAMES PARAMETER
*
PTRN207  DC    AL1(128+4,207),AL2(0)
*
*        T SIZE PARAMERER IS INCORRECT. MINIMUM STORAGE SIZE FOR
*        ALGOL F COMPILER IS 45056 BYTES
*
PTRN208  DC   AL1(128+4,208),AL2(0)
*
*        FIRST LINE OF PRINTED OUTPUT
*
HEADING  DC    CL121' '                HEADING LINE
         ORG   HEADING
         DC    C'1'                    ASA CNTL
         DC    C'360S-AL-531 LEVEL 2.1'  IDENTIFY UPDATED SOURCE
         DC    C'          OS ALGOL F'
         ORG   HEADING+87
HEADDAY  DC    C'DD'                                            ×
         DC    C' '                                             ×
HEADMON  DC    C'MMM'                  ALPHA 3 CHAR MONTH       ×
         DC    C' '                                             ×
HEADYEAR DC    C'20XX'                                          ×
HEADTIME DC    XL9'4021217A21217A2121'  PATTERN FOR TIME        ×
         DC    C'      '                                        ×
HEADPAGE DC    C'PAGE   1'                                      ×
         ORG   ,                                                V
*
*        TABLE OF OPTION PARAMETERS
*
PARMLIST DC    AL1(6)                  (LENGTH-1) OF PARAM
         DC    CL9'PROGRAM'            PARAMETER
         NI    COMPFLGS,PGR            SET SWITCH IN COMPFLGS
LENGTH   DC    AL1(8)
PARAM    DC    CL9'PROCEDURE'
INSTR    OI    COMPFLGS,PROC
         DC    AL1(4)
         DC    CL9'SHORT'
         NI    COMPFLGS,SHRT
         DC    AL1(3)
         DC    CL9'LONG'
         OI    COMPFLGS,LNG
         DC    AL1(5)
         DC    CL9'SOURCE'
         NI    COMPFLGS+1,SRCE
         DC    AL1(7)
         DC    CL9'NOSOURCE'
         OI    COMPFLGS+1,NSRCE
         DC    AL1(3)
         DC    CL9'LOAD'
         NI    COMPFLGS+1,LOAD
         DC    AL1(5)
         DC    CL9'NOLOAD'
         OI    COMPFLGS+1,NLOAD
         DC    AL1(3)
         DC    CL9'DECK'
         NI    COMPFLGS+1,DECK
         DC    AL1(5)
         DC    CL9'NODECK'
         OI    COMPFLGS+1,NDECK
         DC    AL1(5)
         DC    CL9'EBCDIC'
         NI    COMPFLGS+1,EBCDIC
         DC    AL1(5)
         DC    CL9'NOTEST'
         OI    COMPFLGS+2,NOTEST
         DC    AL1(3)
         DC    CL9'TEST'
         NI    COMPFLGS+2,TEST
         DC    AL1(2)
         DC    CL9'ISO'
         OI    COMPFLGS+1,ISO
         DC    AL1(1)
         DC    CL9'NS'
         OI    COMPFLGS+1,NSRCE
         DC    AL1(1)
         DC    CL9'ND'
         OI    COMPFLGS+1,NDECK
         DC    AL1(1)
         DC    CL9'NL'
         OI    COMPFLGS+1,NLOAD
         DC    AL1(1)
         DC    CL9'EB'
         NI    COMPFLGS+1,EBCDIC
         DC    AL1(1)
         DC    CL9'PG'
         NI    COMPFLGS,PGR
         DC    AL1(1)
         DC    CL9'PC'
         OI    COMPFLGS,PROC
         DC    AL1(1)
         DC    CL9'NT'
         OI    COMPFLGS+2,NOTEST
         DC    AL1(1)
         DC    CL9'SP'
         NI    COMPFLGS,SHRT
         DC    AL1(1)
         DC    CL9'LP'
         OI    COMPFLGS,LNG
         DC    AL1(0)
         DC    CL9'D'
         NI    COMPFLGS+1,DECK
         DC    AL1(0)
         DC    CL9'I'
         OI    COMPFLGS+1,ISO
         DC    AL1(0)
         DC    CL9'L'
         NI    COMPFLGS+1,LOAD
         DC    AL1(0)
         DC    CL9'S'
         NI    COMPFLGS+1,SRCE
LSTENTRY DC    AL1(0)
         DC    CL9'T'
         NI    COMPFLGS+2,TEST
*
*        WORKSPACE FOR TRANSLATING THE SIZE PARAMETER AND THE
*        ACTUAL DATE
*
         DC    0D'0'
FIELD1   DC    PL8'0'
*
TEMP     DC    F'0'                    TEMP WORK SLOT
TEMP1    DC    D'0'                    WORK SPACE FOR DEC TO BIN CONV
TEMP2    DC    F'0'                    TEMP WORK SLOT
TEMP3    DC    F'0'                    TEMPORARY WORK SPACE
*
*        TABLE OF THE MONTH NAMES AND THE NUMBER OF DAYS
*
MNTHTBL  DC    H'31'                   JAN DAYS IN MONTH
         DC    C'JAN '                     3 CHAR MONTH CODE
         DC    C'01'                       MONTH NUMBER
         DC    H'29'                   FEB
         DC    C'FEB '
         DC    C'02'
         DC    H'31'                   MAR
         DC    C'MAR '
         DC    C'03'
         DC    H'30'                   APR
         DC    C'APR '
         DC    C'04'
         DC    H'31'                   MAY
         DC    C'MAY '
         DC    C'05'
         DC    H'30'                   JUN
         DC    C'JUN '
         DC    C'06'
         DC    H'31'                   JUL
         DC    C'JUL '
         DC    C'07'
         DC    H'31'                   AUG
         DC    C'AUG '
         DC    C'08'
         DC    H'30'                   SEP
         DC    C'SEP '
         DC    C'09'
         DC    H'31'                   OCT
         DC    C'OCT '
         DC    C'10'
         DC    H'30'                   NOV
         DC    C'NOV '
         DC    C'11'
         DC    H'255'                  DEC (ALLOW FOR STUPID DDD)
         DC    C'DEC '
         DC    C'12'
*
H60      DC    H'60'
*
IEX10001 CSECT
*
         USING IEX10001,R2
*
*        INSTRUCTIONS FOR INITIALIZING IEX11 CAN BE INSERTED HERE
*
         XCTL  EP=IEX11000             EXIT TO NEXT PHASE
*
*        DUMMY CONTROL SECTION TO PROVIDE ADRESSABILITY OF DCB
*
         PRINT NOGEN
*
         DCBD  DSORG=(PS),DEVD=(DA)
*
         PRINT GEN
*
WORKAREA DSECT
*
         COPY  WORKAREA
*
*        CWA SYMBOL DEFINITIONS FOR IEX11 CAN BE INSERTED HERE
*
*        REGISTER EQUATES
*
         IEZREGS
*
         END   IEX10000
