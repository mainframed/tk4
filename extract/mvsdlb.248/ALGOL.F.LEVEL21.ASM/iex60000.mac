         TITLE 'IEX60000 - ERROR MESSAGE PROCESSING, ALGOL F'
*
*        COMPONENT ID - 360S-AL-531 ALGOL F COMPILER
*
IEX60000 CSECT
*
         USING IEX60000,R2
*
*        REGISTER DEFINITIONS
*
*        R1                            POINTS TO PARAMETER LIST
*        R1                            RETURNS ADDR OF PRINT BUFFER
*        R2                            BASE REGISTER FOR COTEMER
*        R4                            BASE REGISTER FOR MESSAGE POOL
*        R5                            POINTER TO ENTRY IN ERROR POOL
*        R6                            POINTER TO INSERTION CODE
*        R7                            POINTER TO ENTRY IN MSG POOL
*        R14                           PTR TO MESSAGE TEXT WORKAREA
*
*        INITIALIZATION
*
         USING WORKAREA,R13            BASEREG FOR WORKAREA DSECT
         MVC   AWEMPOOL(10),0(R1)      GET ADDRS AND MOD NUMBER
COT00    LA    R9,WDIRET1              STORE RETURN ADDR
         ST    R9,ERET                 IN WORKAREA
         STM   R14,R15,SVAR1           SAVE RETURN ADDR AND BASE
         TM    COMPFLGS+2,PRT+PRTNO    SYSPRINT DOWN/NOT OPENED ?
         BNZ   COT18                   YES, GIVE CONSOLE MESSAGE
         L     R5,ERRPOOL              ADDR ERROR POOL
         CL    R5,NEXTERR              ERROR POOL EMPTY ?
         BE    COT28                   YES
         CLC   PAGEHEAD+39(L'HEADD1),HEADD1  HEADINGS ALREADY SET ?
         BE    COT01                   YES, BYPASS HEADINGS
         MVI   LINCNT+1,255            FORCE HEADINGS TO BE PRINTED
         MVI   PAGEHD1C+1,C' '         BLANK FIRST HEADING LINE
         MVC   PAGEHD1C+2(109),PAGEHD1C+1
         MVC   PAGEHD2D(L'PAGEHD2D),PAGEHD1D  BLANK 2ND HEADING LINE
         MVC   PAGEHD3D(L'PAGEHD3D),PAGEHD1D  BLANK 3RD HEADING LINE
         MVC   PAGEHD1D+39(L'HEADD1),HEADD1   MOVE IN HEADD1
         MVC   PAGEHD2D(L'HEADD2),HEADD2      MOVE IN HEADD2
*
*        HANDLING OF THE ENTRIES IN ERROR POOL
*
COT01    L     R4,AWEMPOOL             ADDR ERROR MESSAGE POOL
         SR    R7,R7
         IC    R7,1(,R5)               GET ERROR MSG NUMBER
         LA    R10,200
         CR    R7,R10                  DIRECTORY MESSAGE ?
         BL    COT36                   NO
         LH    R10,MODNUMB             YES, MODIFY NUMBER
         SR    R7,R10
COT36    SLL   R7,2                    GET IT FOUR TIMES
         L     R9,AWADDTAB             ADDR ADDR TABLE
         AR    R7,R9                   GET CORRECT ENTRY
         L     R7,0(,R7)               LOAD ADDR TO ENTRY IN ERMSG POOL
*
*        GET LENGTH OF INSERT CODE PART
*
COT02    LA    R6,2(,R7)               POINT TO FIRST INSERTION CODE
         SR    R9,R9
         IC    R9,1(,R7)               GET NUMBER OF INSERTION CODES
         LR    R10,R9
         LR    R12,R9
         LA    R12,1(,R12)             SET INSERTION CODE COUNTER
         SLL   R9,1                    DOUBLE IT
         AR    R9,R10                  GET IT 3 TIMES
*
*        HANDLE SEVERITY CODE
*
COT03    LA    R10,2(R9,R7)            GET ADDR TO BYTE BEFORE TEXT
         MVC   WAREA+9(1),0(R10)       INSERT SEVERITY CODE
         CLI   0(R10),C'W'
         BNE   COT03A
         OI    COMPFLGS,WERR           SET WARNING MESSAGE
         B     COT04
*
COT03A   CLI   0(R10),C'S'
         BNE   COT03B
         OI    COMPFLGS,SERR           SET SEVERE ERROR
         OI    COMPFLGS,COMPMODE       SET SYNTAX CHECK MODE
         B     COT04
*
COT03B   OI    COMPFLGS,TERR           SET BIT FOR TERMINATING ERROR
*
*        INSERTION OF MSG NUMBER AND SEMICOLON COUNTER
*
COT04    SR    R10,R10                 CLEAR REG
         IC    R10,1(R5)               GET ERROR MSG NUMBER
         CVD   R10,WDEC+8
         UNPK  WAREA+3(3),WDEC+8(8)    CONVERT TO PRINTABLE DECIMAL
         OI    WAREA+5,X'F0'           MAKE CHAR PRINTABLE
         TM    0(R5),X'80'             BLANKS FOR SC ?
         BZ    COT26                   NO
         MVC   WAREA+12(5),BLANKS      YES, MOVE BLANKS
         NI    0(R5),X'7F'             REMOVE TAG
         B     COT31
*
COT26    LH    R10,2(,R5)              GET SEMICOLON COUNTER
         CVD   R10,WDEC+8
         UNPK  WAREA+12(5),WDEC+8(8)   CONVERT TO READABLE DECIMAL
         OI    WAREA+16,X'F0'          MAKE CHAR PRINTABLE
*
*        INSERTION OF FIXED MESSAGE
*
COT31    LA    R14,WAREA+20            POINT TO FIRST TEXTBYTE IN WAREA
         CLI   1(R7),0                 ANY INSERTION CODE ?
         BNE   COT05                   YES
         LA    R10,3(,R7)              GET ADDR OF MSG TXT
         SR    R11,R11
         IC    R11,0(,R7)              GET L'ENTRY
         AR    R11,R7                  GET NEXT ENTRY
         SR    R11,R10                 GET L'MSG TXT
         EX    R11,WMOVE1              MOVE MSG TXT TO WORKAREA
         LA    R14,1(R11,R14)          UPDATE WAREA POINTER
COT05    BCT   R12,COT06               GOTO INS CODE TREATMENT IF ANY
         B     COT12                   ALL INS CODES TREATED
*
*        INSERTION CODE TREATMENT
*
*        TEST ACTION PART OF INSERT CODE
*
COT06    LA    R9,15                   LOAD REG TO ZERO HIGHORDER BITS
         SR    R10,R10
         SR    R11,R11
         TM    0(R6),X'F0'
         BO    COT37                   GOTO 'INSERT BLANKS'
         BZ    COT30                   GOTO 'INSERT MESSAGE TEXT'
         TM    0(R6),X'80'
         BO    COT07                   GOTO 'UNALTERED TEXT'
         TM    0(R6),X'40'
         BO    COT10                   GOTO 'ALGOL SYMBOL'
         TM    0(R6),X'20'
         BO    COT33                   GOTO 'INTERNAL CHARACTERS'
*
*        INSERTION OF BLANKS AT END OF PRTLIN
*
COT37    LA    R3,WAREA+90             CALC FREE SPACE ON 1ST PRT LINE
         SR    R3,R14
         BNP   COT09                   BYPASS BLANKING IF LINE FULL
COT38    MVI   0(R14),C' '             INSERT BLANK
         LA    R14,1(,R14)             STEP WAREA POINTER
         BCT   R3,COT38                BRANCH IF 1ST PRT LINE NOT FULL
         B     COT09                   GOTO NEXT INS CODE
*
*        INSERTION OF MESSAGE TEXT PART
*
COT30    IC    R10,2(,R6)
         LA    R10,0(R10,R7)           GET ADDR OF TEXT PART
         IC    R11,1(,R6)              GET L'TEXT PART
COT08    EX    R11,WMOVE1              MOVE TO WORKAREA
COT35    LA    R14,1(R11,R14)          UPDATE WORKAREA POINTER
COT09    LA    R6,3(,R6)               UPDATE INS CODE POINTER
         B     COT05                   GOTO NEXT INS CODE
*
*        UNALTERED TEXT
*
COT07    IC    R11,0(,R5)              GET LENGTH OF SOURCE TEXT
         LA    R10,5
         SR    R11,R10
         BM    COT09                   NO MOVE IF NO SOURCE TEXT
         LA    R10,4(,R5)              GET ADDR OF SOURCE TEXT
         B     COT08                   MOVE TEXT, GOTO NEXT INS CODE
*
*        ALGOL SYMBOL
*
COT10    TM    COMPFLGS+2,SET60        60 CHARACTER SET USED ?
         BO    COT32                   YES, BRANCH
         LA    R11,WORDSISO            USE ISO TABLE
         B     COT32A
*
COT32    LA    R11,WORDSEBC            USE EBCDIC TABLE
COT32A   LA    R3,WSYMBSTK             TABLE FOR STACK OPERATOR
         IC    R10,0(,R6)              GET ONE BYTE ALGOL SYMBOL
         NR    R10,R9
         IC    R10,0(R10,R5)
         STC   R10,WDEC
         TM    WDEC,X'80'              SOURCE OPERATOR ?
         BZ    COT32B                  NO
         LA    R3,WSYMBSRC             YES, CHANGE TABLE
         NI    WDEC,X'7F'              REMOVE TAG
         IC    R10,WDEC
COT32B   SLL   R10,1                   MULTIPLY BY 2
         LH    R10,0(R10,R3)           CONVERT TO EBCDIC OR DISPLACEMT
         LA    R9,256                  TEST KIND OF SYMBOL
         CLR   R10,R9
         BL    COT11                   ONE CHARACTER SYMBOL
         LA    R10,0(R10,R11)          ADDR WORDS ENTRY
         SR    R11,R11
         IC    R11,0(,R10)             GET LENGTH OF SYMBOL
         LA    R10,1(,R10)             GET ADDR OF SYMBOL
         B     COT08                   MOVE SYMB, GOTO NEXT INS CODE
*
COT11    SR    R11,R11                 GET LENGTH-1 OF ONE-CHAR SYMBOL
         STC   R10,0(,R14)             MOVE SYMBOL TO MSG AREA
         B     COT35                   GOTO NEXT INS CODE
*
*        INTERNAL CHARACTERS
*
COT33    LA    R9,5
         IC    R11,0(,R5)              GET NUMBER OF INT CHARACTERS-1
         SR    R11,R9
         STC   R11,COT34+1             STORE IT IN TRANSLATE INSTR
         LA    R10,4(,R5)              GET ADDR OF SOURCE TEXT
         EX    R11,WMOVE1
COT34    TR    0(0,R14),TRINTEXT       TRANSLATE
         B     COT35                   UPDATE PTR, GOTO NEXT INS CODE
*
*        TRANSLATION AND OUTPUT OF MESSAGE
*
COT12    TM    SWTO,X'80'              MESSAGE TO CONSOLE ?
         BO    COT25                   YES
*
*        MESSAGE TO SYSPRINT
*
COT16    LR    R11,R14
         LA    R9,WAREA+20             GET L'MESSAGE TEXT
         SR    R11,R9
         LA    R9,70                   GET L'TEXT ON FIRST LINE
         SR    R11,R9                  SUBTRACT IT
         LA    R9,89                   GET L'PRINTLINE
         LA    R10,WAREA               GET ADDR OF ERROR MESSAGE
         BAL   R15,COT27               OBTAIN OUTPUT BUFFER, BLANK IT
         LTR   R11,R11                 L'MSG TXT > 70 ?
         BNL   COT17                   YES
         ALR   R9,R11                  OBTAIN PRTLINE LENGTH IF < 90
COT17    EX    R9,WMOVE3               MOVE FIRST PART OF ERROR MSG
COT20    LA    R10,1(R9,R10)           INCREASE ADDR IN WORKAREA
         LTR   R11,R11                 MORE TEXT TO BE MOVED ?
         BNH   COT21                   NO
         LA    R9,70                   YES, GET L'NEXT PRTLINE
         SR    R11,R9                  SUBTRACT IT FROM REMAIN TXTLGT
         BAL   R15,COT27               OBTAIN OUTPUT BUFFER, BLANK IT
         LTR   R11,R11                 LENGTH > 70 ?
         BNL   COT19                   YES
         ALR   R9,R11                  OBTAIN LENGTH IF LESS THAN 70
COT19    LA    R15,1                   MODIFY LENGTH FOR MOVE INSTR
         SR    R9,R15
         EX    R9,WMOVE4               MOVE TEXT TO OUTPUT BUFFER
         B     COT20                   PUT OUT NEXT TEXTLINE IF ANY
*
*        MESSAGE TO WTP
*
COT25    LA    R11,40(,R11)            GET LENGTH FOR MOVE INSTRUCTION
         EX    R11,EXMVC
*
COT24    WTO   '                                                       X
                      ',ROUTCDE=11,DESC=7   ISSUE WTP
*
EXMVC    MVC   COT24+8(0),WAREA        MOVE MESSAGE
*
         B     WDIRET2                 TERMINATE
*
*        OBTAIN OUTPUT BUFFER
*
COT27    STM   R14,R15,SVAR2           SAVE REGS FOR CALL OF PRINT RTN
         L     R15,PRTRTADD            LOAD ADDR OF PRINT ROUTINE
         BALR  R14,R15                 CALL PRINT ROUTINE
         LM    R14,R15,SVAR2           RESTORE REGS
         BR    R15
*
*        GOTO NEXT MESSAGE OR TERMINATE
*
COT21    SR    R9,R9
         IC    R9,0(,R5)               GET L'TREATED ENTRY
         AR    R5,R9                   POINT TO NEXT ENTRY
         L     R9,NEXTERR              GET ADDR OF FIRST FREE ENTRY
         CR    R5,R9                   NEXT ENTRY = FIRST FREE ?
         BL    COT01                   NO, HANDLE NEXT ENTRY
         LA    R10,WDIRET2             LOAD NEW RETURN ADDR
         ST    R10,ERET
         L     R10,ERRPOOL             YES, POOL EXHAUSTED, ZERO PTR
         ST    R10,NEXTERR
         TM    COMPFLGS,TERR           TERMINATING ERROR ?
COT28    LM    R14,R15,SVAR1           LOAD RETURN ADDR AND BASE
         BO    COT29                   YES
         BR    R14                     RETURN
*
COT29    XCTL  EP=IEX51002             BRANCH TO TERMINATION
*
*        ACTION TO BE PERFORMED AFTER I/O ERROR OR PROGRAM
*        INTERRUPTION
*
WDIRET1  TM    COMPFLGS+2,PRT          SYSPRINT ERROR ?
         BZ    COT18A                  NO
COT18    OI    SWTO,X'80'              YES, SET SWITCH ON
         B     LAST
*
COT18A   TM    COMPFLGS+1,ERR          PROGRAM INTERRUPT ?
         BZ    LAST                    NO
         LA    R7,211                  YES, GET MESSAGE NUMBER
         LH    R10,MODNUMB             MODIFY IT
         SR    R7,R10
         SLL   R7,2                    GET ENTRY IN ADDR TABLE
         L     R9,AWADDTAB
         AR    R7,R9                   GET ADDR OF MESSAGE
         L     R7,0(,R7)
         BAL   R15,COT27               GET PRINT BUFFER
         MVC   0(20,R1),CPI            MOVE MSG CODE ETC
         MVC   20(51,R1),0(R7)         MOVE MSG TEXT
         BAL   R15,COT27               GET PRINT BUFFER
         L     R9,NEXTERR              GET ADDR OF PSW
         LA    R10,16
         SR    R9,R10
         MVC   20(16,R1),0(R9)         MOVE PSW
         MVI   37(R1),C'.'             INSERT PERIOD
*
WDIRET2  OI    COMPFLGS+1,NSRCE        SET BIT FOR TERMINATING ERROR
         B     COT29                   BRANCH TO TERMINATION
*
*        HANDLE LAST ERROR PATTERN
*
LAST     L     R5,NEXTERR              GET ADDR OF FIRST FREE ENTRY
         LA    R10,12                  GET ADDR OF LAST PATTERN
         SR    R5,R10
         B     COT01                   GOTO NORMAL TREATMENT
*
*        WORKAREAS AND MOVE INSTRUCTIONS
*
         CNOP  0,4
WMOVE1   MVC   0(0,R14),0(R10)
WMOVE3   MVC   0(0,R1),0(R10)
WMOVE4   MVC   20(0,R1),0(R10)
*
WDEC     DC    2D'0'
WAREA    DC    C'IEX000I  0  00000   '
         DC    250C' '
*
*        HEADINGS AND MESSAGE 211 (PART OF)
*
HEADD1   DC    C'DIAGNOSTICS'
HEADD2   DC    C'CODE    SEV  SC     MESSAGE'
*
CPI      DC    C'IEX211I  T          ' FIRST PART OF MESSAGE 211
*
*        ADDRESSES AND CONSTANT
*
AWEMPOOL DC    F'0'                    ADDR OF MESSAGE TEXTS
AWADDTAB DC    F'0'                    ADDR OF ADDR TABLE
MODNUMB  DC    H'0'                    MODIFICATION NUMBER
*
*        VARIOUS
*
SWTO     DC    X'00'                   SWITCH FOR OUTPUT ON CONSOLE
SVAR2    DC    2F'0'                   TO SAVE LINKREGS FOR PRINT RTN
SVAR1    DC    2F'0'                   TO SAVE RETURN ADDR AND BASE
BLANKS   DC    CL5' '
*
*        TABLES FOR CONVERSION AND TRANSLATION
*
*        TRANSLATE INTERNAL CHARS TO EBCDIC
*
TRINTEXT DC    C'+-*/##(:'             00 -> 07
         DC    C'[##;####'             08 -> 0F
         DC    C'=<>#####'             10 -> 17
         DC    C'########'             18 -> 1F
         DC    C'^#×&&#,):'            20 -> 27
         DC    C']## #.''#'            28 -> 2F
         DC    C'01234567'             30 -> 37
         DC    C'89####.'''            38 -> 3F
         DC    C'ABCDEFGH'             40 -> 47
         DC    C'IJKLMNOP'             48 -> 4F
         DC    C'QRSTUVWX'             50 -> 57
         DC    C'YZ$_#@'               58 -> 5D
*
*
*        CONVERSION OF SOURCE OPERATORS
*
         DC    0F'0'
WSYMBSRC DC    X'00'                   00        PLUS
         DC    C'+'
         DC    X'00'                   01        MINUS
         DC    C'-'
         DC    X'00'                   02        MULTIPLY
         DC    C'*'
         DC    X'00'                   03        DIVIDE
         DC    C'/'
         DC    H'256'                  04        INTEGER DIVIDE
         DC    H'260'                  05        'POWER'
         DC    X'00'                   06        LEFT PARENTHESIS
         DC    C'('
         DC    H'268'                  07        COLON
         DC    H'271'                  08        LEFT BRACKET
         DC    H'274'                  09        'ARRAY'
         DC    H'282'                  0A        'SWITCH'
         DC    H'291'                  0B        SEMICOLON
         DC    H'294'                  0C        'BEGIN'
         DC    H'294'                  0D        'BEGIN'
         DC    H'469'                  0E        'PROCEDURE'
         DC    H'469'                  0F        'PROCEDURE'
         DC    H'302'                  10        'EQUAL'
         DC    H'310'                  11        'LESS'
         DC    H'317'                  12        'GREATER'
         DC    H'327'                  13        'NOTEQUAL'
         DC    H'338'                  14        'NOTGREATER
         DC    H'351'                  15        'NOTLESS'
         DC    H'361'                  16        COLON EQUAL
         DC    H'364'                  17        'GOTO'
         DC    H'371'                  18        'FOR'
         DC    H'377'                  19        'STEP'
         DC    H'384'                  1A        'UNTIL'
         DC    H'392'                  1B        'WHILE'
         DC    H'400'                  1C        'DO'
         DC    H'405'                  1D        'IF'
         DC    H'410'                  1E        'THEN'
         DC    H'417'                  1F        'ELSE'
         DC    H'424'                  20        'NOT'
         DC    H'430'                  21        'IMPL'
         DC    H'437'                  22        'OR'
         DC    H'442'                  23        'AND'
         DC    H'448'                  24        'EQUIV'
         DC    X'00'                   25        COMMA
         DC    C','
         DC    X'00'                   26        RIGHT PARENTHESIS
         DC    C')'
         DC    H'268'                  27        COLON
         DC    H'456'                  28        RIGHT SUBSCRIPT BRACK
         DC    H'291'                  29        DELTA
         DC    H'459'                  2A        'END'
         DC    H'291'                  2B        ETA
         DC    H'459'                  2C        'END'
         DC    X'00'                   2D        OMEGA
         DC    C' '
         DC    H'481'                  2E        'CODE'
*
*        CONVERSION OF STACK OPERATORS
*
WSYMBSTK DC    X'00'                   00        PLUS
         DC    C'+'
         DC    X'00'                   01        MINUS
         DC    C'-'
         DC    X'00'                   02        MULTIPLY
         DC    C'*'
         DC    X'00'                   03        DIVIDE
         DC    C'/'
         DC    H'256'                  04        INTEGER DIVIDE
         DC    H'260'                  05        'POWER'
         DC    X'00'                   06        LEFT PARENTHESIS
         DC    C'('
         DC    H'268'                  07        COLON
         DC    H'271'                  08        LEFT BRACKET
         DC    H'274'                  09        'ARRAY'
         DC    H'282'                  0A        'SWITCH'
         DC    H'291'                  0B        SEMICOLON
         DC    H'294'                  0C        'BEGIN'
         DC    H'294'                  0D        'BEGIN'
         DC    H'469'                  0E        'PROCEDURE'
         DC    H'469'                  0F        'PROCEDURE'
         DC    H'302'                  10        'EQUAL'
         DC    H'310'                  11        'LESS'
         DC    H'317'                  12        'GREATER'
         DC    H'327'                  13        'NOTEQUAL'
         DC    H'338'                  14        'NOTGREATER
         DC    H'351'                  15        'NOTLESS'
         DC    H'361'                  16        COLON EQUAL
         DC    H'364'                  17        'GOTO'
         DC    H'371'                  18        'FOR'
         DC    H'377'                  19        'STEP'
         DC    H'384'                  1A        'UNTIL'
         DC    H'392'                  1B        'WHILE'
         DC    H'400'                  1C        'DO'
         DC    H'405'                  1D        'IF'
         DC    H'410'                  1E        'THEN'
         DC    H'417'                  1F        'ELSE'
         DC    H'424'                  20        'NOT'
         DC    H'430'                  21        'IMPL'
         DC    H'437'                  22        'OR'
         DC    H'442'                  23        'AND'
         DC    H'448'                  24        'EQUIV'
         DC    X'00'                   25        ALPHA
         DC    C' '
         DC    H'405'                  26        IFS
         DC    H'410'                  27        THENS
         DC    H'417'                  28        ELSES
         DC    X'00'                   29        LEFT PARENTHESIS
         DC    C'('
         DC    X'00'                   2A        LEFT PARENTHESIS
         DC    C'('
         DC    X'00'                   2B        MONADIC MINUS
         DC    C'-'
         DC    H'271'                  2C        LEFT SUBSCRIPT BRACKET
         DC    H'361'                  2D        COLON EQUAL
         DC    H'361'                  2E        COLON EQUAL
*
*        COMPOUND ALGOL SYMBOLS, EBCDIC
*
         DC    0F'0'
WORDSEBC EQU   *-256
         DC    X'02'                   256       INTEGER DIVIDE
         DC    C'''/'''
         DC    X'01'                   260       POWER
         DC    C'**     '
         DC    X'00'                   268       COLON
         DC    C': '
         DC    X'01'                   271       LEFT BRACKET
         DC    C'(/'
         DC    X'06'                   274       'ARRAY'
         DC    C'''ARRAY'''
         DC    X'07'                   282       'SWITCH'
         DC    C'''SWITCH'''
         DC    X'00'                   291       SEMICOLON
         DC    C'; '
         DC    X'06'                   294       'BEGIN'
         DC    C'''BEGIN'''
         DC    X'00'                   302       EQUAL
         DC    C'=      '
         DC    X'00'                   310       LESSTHAN
         DC    C'<     '
         DC    X'00'                   317       GREATER THAN
         DC    C'>        '
         DC    X'01'                   327       NOT EQUAL
         DC    C'^=        '
         DC    X'01'                   338       LESS THAN OR EQU
         DC    C'<=          '
         DC    X'01'                   351       GR THAN OR EQU
         DC    C'>=       '
         DC    X'01'                   361       COLON EQUAL
         DC    C':='
         DC    X'05'                   364       'GOTO'
         DC    C'''GOTO'''
         DC    X'04'                   371       'FOR'
         DC    C'''FOR'''
         DC    X'03'                   377       'STEP'
         DC    C'''STEP'''
         DC    X'06'                   384       'UNTIL'
         DC    C'''UNTIL'''
         DC    X'06'                   392       'WHILE'
         DC    C'''WHILE'''
         DC    X'03'                   400       'DO'
         DC    C'''DO'''
         DC    X'03'                   405       'IF'
         DC    C'''IF'''
         DC    X'05'                   410       'THEN'
         DC    C'''THEN'''
         DC    X'05'                   417       'ELSE'
         DC    C'''ELSE'''
         DC    X'00'                   424       NOT
         DC    C'^    '
         DC    X'05'                   430       'IMPL'
         DC    C'''IMPL'''
         DC    X'00'                   437       OR
         DC    C'×   '
         DC    X'00'                   442       AND
         DC    C'&&    '
         DC    X'06'                   448       'EQUIV'
         DC    C'''EQUIV'''
         DC    X'01'                   456       RIGHT BRACKET
         DC    C'/)'
         DC    X'04'                   459       'END'
         DC    C'''END'''
         DC    X'02'                   465       LEFT STRINGQUOTE
         DC    C'''('''
         DC    X'0A'                   469       'PROCEDURE'
         DC    C'''PROCEDURE'''
         DC    X'05'                   481       'CODE'
         DC    C'''CODE'''
*
         DS    0F
WORDSISO EQU   *-256               LIST OF COMPOUND ALGOL SYMBOLS, ISO
         DC    X'02'                   256       INTEGER DIVIDE
         DC    C'''/'''
         DC    X'06'                   260       'POWER'
         DC    C'''POWER'''
         DC    X'01'                   268       COLON
         DC    C'..'
         DC    X'01'                   271       LEFT BRACKET
         DC    C'(/'
         DC    X'06'                   274       'ARRAY'
         DC    C'''ARRAY'''
         DC    X'07'                   282       'SWITCH'
         DC    C'''SWITCH'''
         DC    X'01'                   291       SEMICOLON
         DC    C'.,'
         DC    X'06'                   294       'BEGIN'
         DC    C'''BEGIN'''
         DC    X'06'                   302       'EQUAL'
         DC    C'''EQUAL'''
         DC    X'05'                   310       'LESS'
         DC    C'''LESS'''
         DC    X'08'                   317       'GREATER'
         DC    C'''GREATER'''
         DC    X'09'                   327       'NOTEQUAL'
         DC    C'''NOTEQUAL'''
         DC    X'0B'                   338       'NOTGREATER'
         DC    C'''NOTGREATER'''
         DC    X'08'                   351       'NOTLESS'
         DC    C'''NOTLESS'''
         DC    X'01'                   361       COLON EQUAL
         DC    C'.='
         DC    X'05'                   364       'GOTO'
         DC    C'''GOTO'''
         DC    X'04'                   371       'FOR'
         DC    C'''FOR'''
         DC    X'03'                   377       'STEP'
         DC    C'''STEP'''
         DC    X'06'                   384       'UNTIL'
         DC    C'''UNTIL'''
         DC    X'06'                   392       'WHILE'
         DC    C'''WHILE'''
         DC    X'03'                   400       'DO'
         DC    C'''DO'''
         DC    X'03'                   405       'IF'
         DC    C'''IF'''
         DC    X'05'                   410       'THEN'
         DC    C'''THEN'''
         DC    X'05'                   417       'ELSE'
         DC    C'''ELSE'''
         DC    X'04'                   424       'NOT'
         DC    C'''NOT'''
         DC    X'05'                   430       'IMPL'
         DC    C'''IMPL'''
         DC    X'03'                   437       'OR'
         DC    C'''OR'''
         DC    X'04'                   442       'AND'
         DC    C'''AND'''
         DC    X'06'                   448       'EQUIV'
         DC    C'''EQUIV'''
         DC    X'01'                   456       RIGHT BRACKET
         DC    C'/)'
         DC    X'04'                   459       'END'
         DC    C'''END'''
         DC    X'02'                   465       LEFT STRINGQUOTE
         DC    C'''('''
         DC    X'0A'                   469       'PROCEDURE'
         DC    C'''PROCEDURE'''
         DC    X'05'                   481       'CODE'
         DC    C'''CODE'''
*
*        END OF IEX60000
*
