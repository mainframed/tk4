         TITLE 'IEX50 - COMPILATION PHASE - CP69'
*
***********************************************************************
*
*        COMPILER PROGRAM - CP69
*
***********************************************************************
*
*        ARITHMETIC AND RELATIONAL OPERATIONS
*        CONTEXT              EXPRESSION
*        SOURCE OPERATOR      SEE EXPRESSION CONTEXT  MATRIX
*        STACK OPERATOR       SEE EXPRESSION CONTEXT  MATRIX
*        OPERANDS             TWO OPERANDS
*
         USING CP69,R12
CP69     TM    COMPFLGS,OPERAND        OPERAND ON ?
         BZ    GNB2                    NO, BRANCH
         BAL   R4,ARRTEST2
         TM    1(R9),X'03'             LO ARITHMETIC ?
         BNM   GND2                    NO, BRANCH
         TM    6(R9),X'03'             BLO ARITHMETIC ?
         BNM   GND22                   NO, BRANCH
         TM    COMPFLGS,COMPMODE+SUBSCOPT  SYNTAX + SUBSCRIPT OPT ?
         BNZ   GOE11                   NO, BRANCH
         BAL   R4,OPDREC               OPERAND RECOGNIZER LO
         LA    R9,5(,R9)               INCREASE R9
         BAL   R4,OPDREC               OPERAND RECOGNIZER BLO
         SH    R9,KH5                  DECREASE R9
         TM    1(R9),X'02'             LO REAL ?
         BZ    GOB3                    INTEGER, BRANCH
         TM    6(R9),X'02'             BLO REAL ?
         BZ    HQB1                    INTEGER, BRANCH
HEB1     CLI   0(R10),XFPOWER          OPT POWER ?
         BE    HOB1                    YES, BRANCH
DHEB2    TM    5(R9),X'20'             BLO A VALUE ?
         BO    HFB1                    NO, BRANCH
         TM    5(R9),X'40'             BLO IN A FPR ?
         BO    HEC3                    NO, BRANCH
HEE31    MVZ   UPLACE(1),8(R9)         U=R(BLO)
         LA    R9,5(,R9)
         BAL   R4,CLFPR
         SH    R9,KH5
         CLI   0(R10),X'10'
         BL    HEF2
         SH    R7,ONEENTRY
HEF2     LA    R4,HLD1
         BAL   R3,ROUTIN15             CALL ROUTINE NUMBER 15
         MVN   UPLACE(1),VPLACE
         BAL   R4,CLFPR
         BAL   R4,ROUTIN12             CALL ROUTINE NUMBER 12
         B     HKD1
*
HEC3     TM    5(R9),X'80'             BLO IN A REGISTER ?
         BO    HFB1
         SH    R7,ONEENTRY             REDUCE P
HFB1     TM    0(R9),X'20'             LO A VALUE ?
         BO    HGD1                    AN ADDR, BRANCH
         TM    0(R9),X'40'             LO IN A FPR ?
         BO    HGB1                    NO, BRANCH
         CLI   0(R10),XFSLASH          OPT / ?
         BNE   HFD2                    NO, BRANCH
         TM    RIR+1,X'0F'
         BO    HFB11
         BAL   R4,ROUTIN11             CALL ROUTINE NUMBER 11
         SH    R7,ONEENTRY
         BAL   R4,CLFPR
         SLL   R3,1
         SLL   R14,4
         OR    R3,R14
         SRL   R14,4
         STC   R3,HFB12+1
         STC   R3,UPLACE
         TM    5(R9),X'20'
         BO    HFB13
         BAL   R4,ROUTINE4             CALL ROUTINE NUMBER 4
HFB14    BAL   R2,GENTXTP2             GENERATE 2 BYTES
HFB12    DDR   0,0                     *** GENERATED CODE ***
         B     GOE2
*
CLFPR    SR    R3,R3
         IC    R3,3(R9)
         SRL   R3,5
         LA    R15,1
         SLL   R15,0(R3)
         STC   R15,HEE3+1
         XI    HEE3+1,X'FF'
HEE3     NI    RIR+1,X'00'
         BR    R4
*
HFB13    BAL   R4,ROUTINE2             CALL ROUTINE NUMBER 2
         B     HFB14
*
HFB11    SR    R14,R14                 STORE LO
         IC    R14,3(R9)
         STC   R14,UPLACE              U=R(BLO)
         SRL   R14,4
         BAL   R4,ROUTIN13             CALL ROUTINE NUMBER 13
         TM    5(R9),X'20'             BLO A VALUE ?
         BO    HFF2                    AN ADDR, BRANCH
         BAL   R4,ROUTINE4             CALL ROUTINE NUMBER 4
HFG2     BAL   R4,ROUTINE1             CALL ROUTINE NUMBER 1
         B     HLD1
*
HFF2     BAL   R4,ROUTINE2             CALL ROUTINE NUMBER 2
         B     HFG2
*
HFD2     MVZ   UPLACE(1),3(R9)         V=R(LO)
         CLI   0(R10),XFASSIGN         OPT .= ?
         BNE   HFD3                    NO, BRANCH
         BAL   R4,ROUTIN12             CALL ROUTINE NUMBER 12
         LA    R9,5(,R9)               INCREASE R9
         BAL   R4,ROUTIN15             CALL ROUTINE NUMBER 15
HFC51    SH    R9,KH5                  DECREASE R9
         B     HLD1
*
HFD3     MVC   KONSUM(5),0(R9)
         MVC   0(5,R9),5(R9)
         MVC   5(5,R9),KONSUM
         TM    0(R10),X'10'            OPT ARITHMETIC ?
         BO    HFE4                    RELATIONAL, BRANCH
HFH3     MVI   SWREL,X'FF'             SWREL=FF
HGG11    TM    0(R9),X'20'             LO A VALUE ?
         LA    R4,HLD1
         BO    HFJ3                    AN ADDR, BRANCH
         B     ROUTINE1
*
HFJ3     B     ROUTINE3
*
HFE4     BAL   R4,ROUTIN12             CALL ROUTINE NUMBER 12
         B     HFH3
*
HGB1     TM    0(R9),X'80'             LO IN A REGISTER ?
         BO    HGD1                    NO, BRANCH
         SH    R7,ONEENTRY
HGD1     CLI   0(R10),XFASSIGN         OPT .= ?
         BE    HGD2                    YES, BRANCH
         BAL   R4,ROUTIN11             CALL ROUTINE NUMBER 11
         TM    5(R9),X'20'             IS BLO A VALUE
         BZ    HGG1                    YES, BRANCH
         BAL   R4,ROUTINE2             CALL ROUTINE NUMBER 2
HGH1     SLL   R14,4                   1=R(BLO)
         STC   R14,UPLACE
         CLI   0(R10),X'10'            OPT ARITHMETIC ?
         BL    HGG11                   YES, BRANCH
         BAL   R4,ROUTIN12             CALL ROUTINE NUMBER 12
         B     HGG11
*
HGG1     BAL   R4,ROUTINE4             ROUTINE NR.4
         B     HGH1
*
HGD2     TM    5(R9),X'20'             BLO A VALUE ?
         BO    HJB1                    AN ADDR, BRANCH
         TM    0(R9),X'20'             LO A VALUE ?
         BO    HGB2                    AN ADDR, BRANCH
         BAL   R4,ROUTINE1             CALL ROUTINE NUMBER 1
         MVC   HJH11+2(2),8(R9)        PREPARE INSTRUCTION
         CLC   SPBNST+1(1),7(R9)       DSA OF BLO CDSA  ?
         BNE   HGB4                    NO, BRANCH
         OI    HJH11+2,X'A0'           PREPARE INSTRUCTION
         B     HJH1
*
HGB4     CLI   GPBN+1,X'FF'            GDSA DESTROYED ?
         BE    HGB41                   YES, BRANCH
         CLC   GPBN(1),7(R9)           DSA OF BLO IN GPBN ?
         BNE   HGB5                    NO, BRANCH
HGC4     OI    HJH11+2,X'90'           PREPARE INSTRUCTION
         B     HJH1
*
HGB41    MVI   GPBN+1,0                SFT TO ZERO
HGB5     SR    R1,R1                   PREPARE INSTRUCTION
         IC    R1,7(R9)
         SLL   R1,3
         STH   R1,WORKPL
         CLI   VPLACE,X'99'            DSA OF LO IN GDSA ?
         BE    HGE5                    YES, BRANCH
         MVC   HGA5+2(2),WORKPL
         BAL   R2,GENTXT4              GENERATE 4 BYTES
HGA5     L     GDSA,0(PBT,0)           *** GENERATED CODE ***
         MVC   GPBN(1),7(R9)           STORE NEW GDSA
         B     HGC4
*
HGE5     MVC   HGF5+2(2),WORKPL
         BAL   R2,GENTXT4              GENERATE 4 BYTES
HGF5     L     R14,0(PBT,0)            *** GENERATED CODE ***
         OI    HJH11+2,X'E0'           PREPARE INSTRUCTION
         B     HJH1
*
HGB2     BAL   R4,ROUTINE3             CALL ROUTINE NUMBER 3
         MVC   HJH11+4(2),WPLACE
         MVZ   HJH11+4(1),VPLACE
         LA    R9,5(0,R9)              INCREASE R9
         BAL   R4,ROUTINE1             CALL ROUTINE NUMBER 1
         SH    R9,KH5                  DECREASE R9
         MVC   HJH11+2(2),WPLACE
         MVZ   HJH11+2(1),VPLACE
         B     HJH111
*
HJB1     LA    R9,5(0,R9)              INCREASE R9
         BAL   R4,ROUTINE3             CALL ROUTINE NUMBER 3
         MVC   HJH11+2(2),WPLACE
         MVZ   HJH11+2(1),VPLACE
         SH    R9,KH5                  DECREASE R9
         TM    0(R9),X'20'             LO A VALUE ?
         BO    HJC1                    AN ADDR, BRANCH
         BAL   R4,ROUTINE1             CALL ROUTINE NUMBER 1
HJH1     MVC   HJH11+4(2),WPLACE
         MVZ   HJH11+4(1),VPLACE
HJH111   MVI   HJH11+1,0
         TM    1(R9),X'03'
         BO    HJH13
         MVI   HJH11+1,X'03'
         TM    1(R9),X'01'
         BO    HJH13
         LH    R14,ONEENTRY
         BCTR  R14,0
         STC   R14,HJH11+1
HJH13    BAL   R2,GENTXT6              GENERATE 6 BYTES
HJH11    MVC   0(8,0),0(0)             *** GENERATED CODE ***
HLE21    L     R1,ABIG2                R1 -> ABIG2 ENTRY IN CP20
         MVI   SWREL,X'00'
         L     R12,SCPTAB+4*20         R12 -> CP20 FOR ADDRESSABILITY
         BR    R1                      BRANCH TO CP20
*
ABIG2    DC    A(BIG2)
*
HJC1     MVC   HJC11+2(2),3(R9)
         BAL   R2,GENTXT4              GENERATE 4 BYTES
HJC11    L     R14,0(CDSA,0)           *** GENERATED CODE ***
         MVI   VPLACE,X'E0'
         B     HJH1
*
HKD1     MVC   HKH4+1(1),UPLACE
         MVI   HKH4,X'2A'
         CLI   0(R10),XFMINUS          OPT MINUS ?
         BL    HKH41                   OPT PLUS, BRANCH
         BE    HKF2                    OPT MINUS, BRANCH
         MVI   HKH4,X'2C'
         CLI   0(R10),XFSLASH          OPT REAL DIVISION ?
         BL    HKH41                   OPT MULTIPLICATION, BRANCH
         BE    HKD2                    OPT DIVISION, BRANCH
         TM    0(R10),X'10'            OPT RELATIONAL ?
         BO    HKA2                    YES, BRANCH
HKG22    CLI   0(R10),X'04'
         BE    GOE112
HKG2     BAL   R4,SERR2
         DC    H'194'                  ERROR 194
*
GOE1     LA    R9,5(,R9)
GOEZ     STH   R7,WORKPL
         MVN   3(1,R9),WORKPL
         MVC   4(1,R9),WORKPL+1
         OI    0(R9),X'08'
GOF1     BCT   R10,COMP                CLOP AND COMPARE
HKA2     MVI   HKH4,X'29'              PREPARE INSTRUCTIONS
HKH41    BAL   R2,GENTXTP2             GENERATE 2 BYTES
HKH4     ADR   0,0                     *** GENERATED CODE ***
         CLI   0(R10),XFEQUAL
         BL    GOE2
IMB1     AH    R7,ONEENTRY
         STH   R7,WORKPL               CHECK MAX
         BAL   R4,MAXCH
         MVC   8(2,R9),WORKPL
         MVC   IMD2+2(2),WORKPL        PREPARE INSTRUCTION
         OI    IMD2+2,X'A0'
         NI    IMH4+1,X'0F'
         TM    SWREL,X'FF'
         BO    INE2
         CLI   0(R10),XFLT             OPT < ?
         BL    IME4                    =, BRANCH
         BE    IMG4                    YES, BRANCH
         CLI   0(R10),X'13'
         BE    IMH41                   BRANCH ON NOT EQUAL
         BL    IMC4                    BRANCH ON GREATER
         CLI   0(R10),X'15'
         BE    IMD4                    BRANCH ON NOT LOW
         BH    HKG2                    BRANCH ON ERROR
IMF4     OI    IMH4+1,X'D0'            PREPARE INSTRUCTION
IMH51    MVC   IMH5+2(2),IMD2+2
         LA    R2,IMD2
         BAL   R14,GENTXTS
         DC    H'14'                   GENERATE 14 BYTES
         OI    6(R9),X'03'             ADJUST R9 ENTRY OF BLO
         OI    5(R9),X'40'
         NI    5(R9),X'5F'
         B     GOE1                    RETURN
*
IMD2     MVI   0(CDSA),X'01'           *** GENERATED CODE ***
         BALR  BRR,0                   *** GENERATED CODE ***
IMH4     NOP   8(0,BRR)                *** GENERATED CODE ***
IMH5     MVI   0(CDSA),X'00'           *** GENERATED CODE ***
HKD2     MVI   HKH4,X'2D'
         B     HKH41
*
HKF2     MVI   HKH4,X'2B'
         B     HKH41
*
IMC4     OI    IMH4+1,X'20'            SET BRANCH CONDITION IN GEN INST
         B     IMH51
*
IMD4     OI    IMH4+1,X'B0'            SET BRANCH CONDITION IN GEN INST
         B     IMH51
*
IME4     OI    IMH4+1,X'80'            SET BRANCH CONDITION IN GEN INST
         B     IMH51
*
IMG4     OI    IMH4+1,X'40'            SET BRANCH CONDITION IN GEN INST
         B     IMH51
*
IMH41    OI    IMH4+1,X'70'            SET BRANCH CONDITION IN GEN INST
         B     IMH51
*
INE2     MVI   SWREL,0                 ZERO SWREL
         CLI   0(R10),XFLT             WHICH OPT
         BL    IME4                    =, BRANCH
         BE    IMC4                    <, BRANCH
         CLI   0(R10),X'13'
         BE    IMH41                   BRANCH ON NOT EQUAL
         BL    IMG4                    BRANCH ON GREATER
         CLI   0(R10),X'15'
         BE    IMF4                    BRANCH ON NOT LOW
         BH    HKG2                    BRANCH ON ERROR
         B     IMD4                    PREPARE INSTRUCTION
*
GNB2     BAL   R4,SERR4
         DC    H'162'                  ERROR 162
*
         OI    COMPFLGS,OPERAND        SET OPERAND ON
GNE3     MVC   0(5,R9),API             INSTRUCTION API
         BCT   R10,COMP                CLOPT AND COMPARE
GND2     TM    0(R9),APIMASK           LO AN API ?
         BZ    GND21                   LO IS NOT AN API, BRANCH
GND3     LA    R9,5(,R9)               INCREASE R9
         B     GNE3
*
GND21    BAL   R4,SERR2
         DC    H'163'                  ERROR 163
*
         B     GND3
*
GND22    TM    5(R9),APIMASK           BLO AN API ?
         BO    GND3                    YES, BRANCH
         BCTR  R10,0                   REDUCE R10
         BAL   R4,SERR2
         DC    H'163'                  ERROR  163
         LA    R10,1(,R10)             INCREASE R10
         B     GND3
*
GOE11    CLI   0(R10),X'04'            OPT IN STACK INT# DIV ?
         BNE   GOE111                  NO, BRANCH
         TM    1(R9),X'02'             LO REAL ?
         BO    GOE112
         TM    6(R9),X'02'             BLO REAL ?
         BO    GOE112                  YES, BRANCH
GOE111   TM    0(R10),X'10'            OPT RELATIONAL ?
         BZ    GOE1                    NO, BRANCH
         OI    6(R9),X'03'             ADJUST R9 ENTRY OF BLO
         B     GOE1
*
GOE2     SR    R14,R14
         NI    UPLACE,X'F0'
         IC    R14,UPLACE
         SRL   R14,3
         LA    R9,5(,R9)
         ST    R9,RUTR(R14)
         SRL   R14,1
         STH   R14,CIR
         SRL   R14,1
         LA    R15,1
         SLL   R15,0(R14)
         STC   R15,HEF21+1
HEF21    OI    RIR+1,X'00'
         B     GOEZ
*
HLD1     MVC   HLJ4+1(1),UPLACE        PREPARE INSTRUCTION
         MVN   HLJ4+1(1),VPLACE
         MVC   HLJ4+2(2),WPLACE
         MVI   HLJ4,X'6A'
         CLI   0(R10),XFMINUS          OPT MINUS ?
         BL    HLJ41                   +, BRANCH
         BE    HLF2                    MINUS, BRANCH
         MVI   HLJ4,X'6C'
         CLI   0(R10),X'03'            DIVISION ?
         BL    HLJ41
         BE    HLB2                    YES, BRANCH
         TM    0(R10),X'10'            A RELATIONAL OPT ?
         BZ    HKG22
         MVI   HLJ4,X'60'
         CLI   0(R10),XFASSIGN         OPT .= ?
         BE    HLJ41                   YES, BRANCH
HLA2     MVI   HLJ4,X'69'              PREPARE INSTRUCTION
HLJ41    BAL   R2,GENTXTP4             GENERATE 4 BYTES
HLJ4     AD    0,0(0,0)                *** GENERATED CODE ***
         CLI   0(R10),XFMINUS          OPT MINUS ?
         BE    HLF3                    YES, BRANCH
         CLI   0(R10),XFASSIGN         OPT ASSIGNMENT?
         BE    HLE21                   YES, BRANCH
         TM    0(R10),X'10'            OPT RELATIONAL ?
         BO    IMB1                    NO, BRANCH
         MVI   SWREL,0
         B     GOE2
*
GOE112   BAL   R4,SERR3
         DC    H'185'
*
         B     GOE1
*
HLF3     TM    SWREL,X'FF'
         BZ    GOE2
         MVI   SWREL,X'00'              SET TO ZERO SWREL
         MVC   HLH3+1(1),UPLACE         PREPARE INSTRUCTION
         SR    R14,R14
         IC    R14,UPLACE
         SRL   R14,4
         STC   R14,WORKPL
         MVN   HLH3+1(1),WORKPL
         BAL   R2,GENTXTP2              GENERATE 2 BYTES
HLH3     LCDR  0,0                      *** GENERATED CODE ***
         B     GOE2
*
HLB2     MVI   HLJ4,X'6D'
         B     HLJ41
*
HLF2     MVI   HLJ4,X'6B'
         B     HLJ41
*
GOB3     TM    6(R9),X'02'             BLO REAL ?
         BO    HTB1                    YES, BRANCH
         CLI   0(R10),XFASTER          OPT * ?
         BL    DHZB1
         BE    IPB1                    YES, BRANCH
         TM    0(R10),X'10'            OPT RELATIONAL ?
         BO    DHZB1
         CLI   0(R10),X'04'            OPT INTEGER DIV ?
         BE    ISB1                    YES, BRANCH
         BH    IUB1                    OPT IS POWER, BRANCH
HWB1     BAL   R4,TRINRE               CALL CONVERT ROUTINE
         MVC   HYG2+2(2),3(R9)
         LA    R9,5(,R9)               INCREASE R9
         BAL   R4,TRINRE
         BAL   R2,GENTXTP4             GENERATE 4 BYTES
HYG2     DD    0,0(CDSA,0)             *** GENERATED CODE ***
         SH    R7,ONEENTRY             REDUCE P
         B     GOEZ
*
DHZB1    TM    5(R9),X'20'             BLO A VALUE ?
         BO    IAB1                    AN ADDR, BRANCH
         TM    5(R9),X'40'             BLO IN A REGISTER ?
         BO    HZC2                    IN STORAGE, BRANCH
         MVZ   UPLACE(1),8(R9)         V=R(BLO)
         TM    0(R10),X'10'            OPT RELATIONAL ?
         BZ    HZF1                    ARITHMETIC, BRANCH
         BAL   R4,ROUTINE8             CALL ROUTINE NUMBER 8
HZF1     LA    R4,IFC3
         BAL   R3,ROUTIN15             CALL ROUTINE NUMBER 15
         MVN   UPLACE(1),VPLACE        PREPARE INSTRUCTION
         BAL   R4,ROUTINE8             RELILO
         B     IEC3
*
HZC2     TM    5(R9),X'80'             BLO IN A REGISTER ?
         BO    IAB1                    NO, BRANCH
         SH    R7,ONEENTRY             REDUCE P
IAB1     TM    0(R9),X'20'             LO A VALUE ?
         BO    IAD3                    AN ADDR, BRANCH
         TM    0(R9),X'40'             LO IN A REGISTER ?
         BO    IAC2                    IN STORAGE, BRANCH
         MVZ   UPLACE(1),3(R9)         U=R(LO)
         CLI   0(R10),XFASSIGN         OPT.= ?
         BNE   IAE1                    NO, BRANCH
         BAL   R4,ROUTINE8             CALL ROUTINE NUMBER 8
         LA    R9,5(,R9)               INCREASE R9
         BAL   R4,ROUTIN15             CALL ROUTINE NUMBER 15
IAE21    SH    R9,KH5                  REDUCE R9
         B     IFC3
*
IAE1     MVC   KONSUM(5),0(R9)         INTERCHANGE PLACES
         MVC   0(5,R9),5(R9)
         MVC   5(5,R9),KONSUM
         TM    0(R10),X'10'            OPT ARITHMETIC ?
         BO    IAG2                    RELATIONAL, BRANCH
         SR    R14,R14                 INTRODUCE R9 ADD IN RUM
         LA    R9,5(,R9)
         IC    R14,3(R9)
         SRL   R14,4
         SLL   R14,2
         ST    R9,RUTI(R14)
         SH    R9,KH5                  DECREASE R9
IAJ1     MVI   SWREL,X'FF'             SWREL= ONE
IAF3     TM    0(R9),X'20'             LO A VALUE ?
         LA    R4,IFC3
         BO    IAK1                    AN ADDR, BRANCH
         B     ROUTINE1                BRANCH TO ROUTINE NUMBER 1
*
IAK1     B     ROUTINE3                BRANCH TO ROUTINE NUMBER 3
*
IAG2     BAL   R4,ROUTINE8             CALL ROUTINE NUMBER 8
         B     IAJ1
*
IAC2     TM    0(R9),X'80'             LO IN A REGISTER ?
         BO    IAD3                    NO, BRANCH
         SH    R7,ONEENTRY             REDUCE P
IAD3     CLI   0(R10),XFASSIGN         OPT.= ?
         BE    HGD2                    YES, BRANCH
         BAL   R4,ROUTINE7             CALL ROUTINE NUMBER 7
         TM    5(R9),X'20'             BLO A VALUE ?
         BO    IAG3                    AN ADDR, BRANCH
         BAL   R4,ROUTINE5             CALL ROUTINE NUMBER 5
IAH3     SLL   R14,4                   U=R(BLO)
         STC   R14,UPLACE
         TM    0(R10),XFEQUAL
         BZ    IAF3
         BAL   R4,ROUTINE8             CALL ROUTINE NUMBER 8
         B     IAF3
*
IAG3     BAL   R4,ROUTINE6             CALL ROUTINE NUMBER 6
         B     IAH3
*
IEC3     MVC   IED3+1(1),UPLACE        PREPARE INSTRUCTION
         MVI   IED3,X'1A'
         CLI   0(R10),XFMINUS          OPT MINUS ?
         BL    IED31                   OPT PLUS, BRANCH
         BH    IED4                    OPT RELATIONAL, BRANCH
         MVI   IED3,X'1B'              PREPARE INSTRUCTION
IED31    BAL   R2,GENTXT2              GENERATE 2 BYTES
IED3     SR    0,0                     *** GENERATED CODE ***
         CLI   0(R10),X'10'
         BL    GOE1
         B     IMB1
*
IED4     MVI   IED3,X'19'
         B     IED31
*
IFC3     MVC   IFD3+1(1),UPLACE        PREPARE INSTRUCTION
         MVN   IFD3+1(1),VPLACE
         MVC   IFD3+2(2),WPLACE
         MVI   IFD3,X'5A'
         CLI   0(R10),XFMINUS          WHICH OPT
         BL    IFD31                   +, BRANCH
         BH    IFD4                    RELATIONAL, BRANCH
         MVI   IFD3,X'5B'              PREPARE  INSTRUCTION
IFD31    BAL   R2,GENTXT4              GENERATE 4 BYTES
IFD3     S     0,0(0,0)                *** GENERATED CODE ***
         CLI   0(R10),XFMINUS          WHICH OPERATOR
         BL    IFG3+2                  +, BRANCH
         BH    IFD41                   RELATIONAL, BRANCH
         CLI   SWREL,X'FF'             SWREL = ONE
         BNE   IFG3+2
         IC    R14,UPLACE              PREPARE INSTRUCTION
         SRL   R14,4
         STC   R14,IFG3+1
         MVZ   IFG3+1(1),UPLACE
         BAL   R2,GENTXT2              GENERATE 2 BYTES
IFG3     LCR   0,0                     *** GENERATED CODE ***
         MVI   SWREL,0
         B     GOE1
*
IFD4     MVI   IFD3,X'59'
         CLI   0(R10),XFASSIGN         OPT.= ?
         BNE   IFD31                   NO, BRANCH
         MVI   IFD3,X'50'              PREPARE INSTRUCTION
         B     IFD31
*
IFD41    CLI   0(R10),XFASSIGN         OPT.= ?
         BE    HLE21                   YES, BRANCH
         B     IMB1                    BRANCH TO RELATIONAL ROUTINE
*
*        INTEGER-INTEGER MULTIPLICATION
*
IPB1     TM    5(R9),X'20'             BLO A VALUE ?
         BO    IRD1                    AN ADDR, BRANCH
         TM    5(R9),X'40'             BLO A IN A REGISTER ?
         BO    IRB1                    IN STORAGE, BRANCH
         SR    R1,R1                   BLO IN AN ODD REG ?
         IC    R1,8(,R9)
         STC   R1,UPLACE
         SRL   R1,4
         SH    R7,ONEENTRY             DECREASE P
         TM    UPLACE,X'10'
         BZ    IPD2                    BLO IN EVEN GPR, BRANCH
         LA    R2,1                    CLEAR RII OF BLO
         SLL   R2,0(R1)
         STC   R2,IPH11+1
         XI    IPH11+1,X'FF'
IPH11    NI    RII+1,0
         BCTR  R1,0                    V=R(BLO-1)
         NI    UPLACE,X'E0'
         SRL   R2,1                    NEXT EVEN GPR FREE ?
         STC   R2,IPH1+1
IPH1     TM    RII+1,0
         BO    IPH31
IPJ1     LA    R4,IQE2
         BAL   R3,ROUTIN15             CALL ROUTINE NUMBER 15
IQC1     BAL   R4,ROUTINE8             CALL ROUTINE NUMBER 8
IQD11    MVZ   IQD1+1(1),UPLACE        PREPARE INSTRUCTION
         SR    R2,R2
         IC    R2,3(R9)
         SRL   R2,4
IQC12    LA    R1,1
         SLL   R1,0(R2)
         STC   R1,IQC13+1
         XI    IQC13+1,X'FF'
IQC13    NI    RII+1,0
         MVN   IQD1+1(1),VPLACE
         BAL   R2,GENTXT2              GENERATE 2 BYTES
IQD1     MR    0,0                     *** GENERATED CODE  ***
IQE11    MVZ   IQE1+1(1),UPLACE        PREPARE INSTRUCTION
         BAL   R2,GENTXT4              GENERATE 4 BYTES
IQE1     SLDA  0,32                    *** GENERATED CODE  ***
IQB1     SR    R1,R1                   SET NEW CII
         IC    R1,UPLACE
         SRL   R1,4
         STC   R1,CII+1
         SLL   R1,4
         STC   R1,8(R9)
         SRL   R1,4
         LA    R2,1                    SET TO ONE RII OF BLO
         SLL   R2,0(R1)
         STC   R2,IQC14+1
IQC14    OI    RII+1,0
         AH    R7,ONEENTRY
         SLL   R1,2
         LA    R9,5(,R9)
         ST    R9,RUTI(R1)
         B     GOEZ
*
IQE2     MVZ   IQE21+1(1),UPLACE       PREPARE INSTRUCTION
         MVN   IQE21+1(1),VPLACE
         MVC   IQE21+2(2),WPLACE
         BAL   R2,GENTXT4              GENERATE 4 BYTES
IQE21    M     0,0(0,0)                *** GENERATED CODE  ***
         B     IQE11
*
IPH31    TM    0(R9),X'20'             LO AN ADDR ?
         BO    IPH4                    YES, BRANCH
         TM    0(R9),X'40'
         BO    IPH4
IPH3     MVZ   WORKPL(1),3(R9)         LO IN THAT GPR ?
         NI    WORKPL,X'F0'
         NI    UPLACE,X'F0'
         CLC   UPLACE(1),WORKPL
         BNE   IPH4                    BRANCH IF NOT IN THAT GPR
         SH    R7,ONEENTRY             INCREASE P
         B     IQD11
*
IPH4     LA    R1,2(,R1)               NEXT EVEN FREE
         CH    R1,KH8                  NEXT EVEN GPR8 ?
         BE    IPH41                   YES, BRANCH
         SLL   R2,2
IPH43    STC   R2,IPH42+1
IPH42    TM    RII+1,0
         BZ    IPD3                    BRANCH IN FREE
         TM    0(R9),X'20'             LO A VALUE ?
         BO    IPH44
         TM    0(R9),X'40'
         BO    IPH44
         SR    R3,R3                   LO IN THAT GPR ?
         IC    R3,3(0,R9)
         SRL   R3,4
         CR    R3,R1
         BE    IPD3                    YES, BRANCH
IPH44    LR    R14,R1
         ST    R2,RETADR+56            SAVE CONTENTS OF R2 ACROSS RTN9
         BAL   R4,ROUTINE9             CALL ROUTINE NUMBER 9
         L     R2,RETADR+56
         LR    R1,R14
IPD3     LA    R1,1(,R1)               NEXT ODD FREE ?
         SLL   R2,1
IPD33    STC   R2,IPD31+1
IPD31    TM    RII+1,0
         BZ    IPE3
         LR    R14,R1
         BAL   R4,ROUTINE9             CALL ROUTINE NUMBER 9
         LR    R1,R14
         XI    IPD31+1,X'FF'           CLEAR RII OF LAST CLEARED
         NC    RII+1(1),IPD31+1
IPE3     SLL   R1,4                    PREPARE INSTRUCTION
         STC   R1,UPLACE
         MVC   IPE31+1(1),UPLACE
         NI    UPLACE,X'E0'
         IC    R2,8(,R9)
         SRL   R2,4
         STC   R2,WORKPL
         MVN   IPE31+1(1),WORKPL
         BAL   R2,GENTXT2              GENERATE 2 BYTES
IPE31    LR    0,0                     *** GENERATED CODE ***
         B     IPJ1
*
IPH41    SR    R1,R1
         LA    R2,1
         B     IPH43
*
IPD2     SR    R2,R2                   LO IN NEXT GPR ?
         LA    R1,1(0,R1)
         TM    0(R9),X'20'
         BO    IPD21
         TM    0(R9),X'40'
         BO    IPD21
         IC    R2,3(,R9)
         SRL   R2,4
         CR    R1,R2
         BE    IQC11                   YES,  BRANCH
IPD21    LA    R2,1
         SLL   R2,0(R1)
         B     IPD33
*
IRB1     TM    5(R9),X'80'             BLO IN A GPR ?
         BO    IRD1
         SH    R7,ONEENTRY
IRD1     TM    CII+1,X'01'             LAST OCCUPIED REG ODD ?
         BO    IRD11                   YES, BRANCH
         TM    CII+1,X'06'             LAST OCCUPIED R6 ?
         BO    IRD12                   YES, BRANCH
         SR    R1,R1
         IC    R1,CII+1                NEXT EVEN REG FREE ?
         LA    R1,2(,R1)
IRD14    LA    R2,1
         STC   R1,CII+1
         SLL   R2,0(R1)
         LR    R14,R1
         STC   R2,IRD13+1
IRD13    TM    RII+1,0
         BZ    IRF1                    FREE, BRANCH
         ST    R2,RETADR+52
         BAL   R4,ROUTINE9             CALL ROUTINE NUMBER 9
         L     R2,RETADR+52
         LR    R1,R14
IRF1     SLL   R2,1                    NEXT ODD FREE ?
         OC    RII+1(0),IRD13+1
         SLL   R14,4
         STC   R14,UPLACE
         STC   R2,IRF11+1
         LA    R1,1(,R1)
         LR    R14,R1
IRF11    TM    RII+1,0
         BZ    IRG1                    BRANCH IF FREE
         XI    IRF11+1,X'FF'
         NC    RII+1(1),IRF11+1
         BAL   R4,ROUTINE9             CALL ROUTINE NUMBER 9
IRG1     TM    5(R9),X'20'             BLO A VALUE ?
         BO    IRA1                    AN ADDR, BRANCH
         BAL   R4,ROUTINE5             CALL ROUTINE NUMBER 5
         B     IPJ1
*
IRA1     BAL   R4,ROUTINE6             CALL ROUTINE NUMBER 6
         B     IPJ1
*
IRD12    SR    R1,R1
         B     IRD14
*
IRD11    TM    CII+1,X'07'             LAST OCCUPIED R7 ?
         BO    IRD12
         SR    R1,R1
         IC    R1,CII+1
         LA    R1,1(,R1)               INCR
         B     IRD14
*
IQC11    BCTR  R2,0
         STC   R2,VPLACE
         SH    R7,ONEENTRY
         B     IQD11
*
*        INTEGER-INTEGER DIVISION
*
ISB1     TM    5(R9),X'20'             BLO A VALUE ?
         BO    ISB4                    AN ADDR, BRANCH
         TM    5(R9),X'40'             BLO IN A GPR ?
         BO    ISC2                    IN STORAGE, BRANCH
         SR    R14,R14                 CLEAR RII OF BLO
         IC    R14,8(R9)
         SRL   R14,4
         LA    R2,1
         SLL   R2,0(R14)
         STC   R2,ISE3+1
         XI    ISE3+1,X'FF'
ISE3     NI    RII+1,X'00'
         SH    R7,ONEENTRY
         TM    8(R9),X'10'             BLO IN AN EVEN GPR ?
         BO    ISG4                    ODD, BRANCH
ISH11    SLL   R2,1                    NEXT ODD FREE ?
         LA    R14,1(,R14)
         STC   R2,ISH1+1
ISH1     TM    RII+1,0
         BO    ISH2                    OCCUPIED, BRANCH
ISJ11    MVZ   ISJ1+1(1),8(R9)         PREPARE INSTRUCTION
         BAL   R2,GENTXT4              GENERATE 4 BYTES
ISJ1     SRDA  0,32                    *** GENERATED CODE ***
         LA    R4,ITF21
         BAL   R3,ROUTIN15             CALL ROUTINE NUMBER 5
         BAL   R4,ROUTINE8             CALL ROUTINE NUMBER 8
         MVZ   ITE1+1(1),8(R9)         PREPARE INSTRUCTION
         MVN   ITE1+1(1),VPLACE
         BAL   R2,GENTXT2              GENERATE 2 BYTES
ITE1     DR    0,0                     *** GENERATED CODE ***
ITZZ     MVZ   UPLACE(1),8(R9)
         OI    UPLACE,X'10'
         B     IQB1
*
ITF21    MVZ   ITF2+1(1),8(R9)         PREPARE INSTRUCTION
         MVN   ITF2+1(1),VPLACE
         MVC   ITF2+2(2),WPLACE
         BAL   R2,GENTXT4              GENERATE 4 BYTES
ITF2     D     0,0(0,0)                *** GENERATED CODE ***
         B     ITZZ
*
ISH2     TM    0(R9),X'20'             LO A VALUE ?
         BO    ISH3                    AN ADDR, BRANCH
         TM    0(R9),X'40'             LO IN A GPR ?
         BO    ISH3
         SR    R3,R3                   LO IN THAT GPR ?
         IC    R3,3(R9)
         SRL   R3,4
         STC   R3,VPLACE
         CR    R3,R14
         BNE   ISH3                    NO THERE, BRANCH
         MVN   ISJ2+1(1),VPLACE        PREPARE INSTRUCTION
         BAL   R2,GENTXT2              GENERATE 2 BYTES
ISJ2     LR    R14,0                   *** GENERATED CODE ***
         NI    3(R9),X'0F'             ADJUST R9 ENTRY
         OI    3(R9),X'E0'
         B     ISJ11
*
ISH3     BAL   R4,ROUTINE9             CALL ROUTINE NUMBER 8
         B     ISJ11
*
ISG4     LA    R14,1(,R14)             NEXT EVEN FREE ?
         TM    CII+1,X'07'             NEXT EVEN GPR7
         BO    ISG401                  YES
         SLL   R2,1
         STC   R2,ISG41+1
ISG41    TM    RII+1,0
         BZ    ISH4                    BRANCH IF FREE
         ST    R2,RETADR
         BAL   R4,ROUTINE9             STOPINT
         XI    ISG41+1,X'FF'
         NC    RII+1(1),ISG41+1
ISH4     SLL   R14,4                   PREPARE INSTRUCTION
         STC   R14,ISH41+1
         IC    R1,8(R9)
         SRL   R1,4
         STC   R1,WORKPL
         MVN   ISH41+1(1),WORKPL
         BAL   R2,GENTXT2              GENERATE 2 BYTES
ISH41    LR    0,0                     *** GENERATED CODE ***
         L     R2,RETADR
         MVZ   8(1,R9),ISH41+1         ADJUST R9 ENTRY
         SRL   R14,4
         B     ISH11
*
ISC2     TM    5(R9),X'80'             BLO IN A REG ?
         BO    ISB4                    NO, BRANCH
         SH    R7,ONEENTRY             REDUCE P
ISB4     TM    CII+1,X'01'             LAST OCCUPIED EVEN ?
         BO    ISD5                    ODD, BRANCH
         TM    CII+1,X'06'             LAST OCCUPIEN R6 ?
         BO    ISD51                   YES, BRANCH
         SR    R1,R1
         IC    R1,CII+1
         LA    R1,2(,R1)
ISD52    LA    R2,1
         SLL   R2,0(R1)
         STC   R2,ISE4+1
         LR    R14,R1
ISE4     TM    RII+1,0
         BZ    ISF4                    BRANCH IF FREE
         BAL   R4,ROUTINE9             CALL ROUTINE NUMBER 9
ISF4     TM    5(R9),X'20'             BLO A VALUE ?
         BO    ISF41                   AN ADDR, BRANCH
         BAL   R4,ROUTINE5             CALL ROUTINE NUMBER 5
ISF42    SR    R2,R2
         IC    R2,ISE4+1
         B     ISH11
*
ISF41    BAL   R4,ROUTINE6             LOAD BLO
         B     ISF42
*
ISD51    SR    R1,R1                   CLEAR R1
         B     ISD52
*
ISD5     TM    CII+1,X'07'             LAST OCCUPIED R7 ?
         BO    ISD51                   YES, BRANCH
         LH    R1,CII
         LA    R1,1(,R1)
         B     ISD52
*
ISG401   SR    R14,R14                 CLEAR R14
         LA    R2,1
         STC   R2,ISG41+1
         B     ISG41
*
HQB1     BAL   R4,I1B13
         B     HEB1
*
HTB1     CLI   0(R10),XFPOWER          OPT POWER ?
         BE    I1B1
HTB11    BAL   R4,TRINRE               CONVERTION ROUTINE
         B     DHEB2
*
*        INTEGER-INTEGER POWER
*
IUB1     TM    0(R9),X'C0'
         BNO   IUB2
         TM    1(R9),X'30'
         BNZ   IUB2                    NOT CONSTANT, BRANCH
         TM    SWVAL,X'FF'
         BO    IUB2
         TM    RII+1,X'01'             R0 FREE ?
         BZ    IUC1                    YES, BRANCH
         SR    R14,R14                 R14=0
         BAL   R4,ROUTINE9             CALL ROUTINE NUMBER 9
IUC1     TM    RII+1,X'02'             R1 FREE ?
         BZ    IUC11                   YES, BRANCH
         LA    R14,1
         BAL   R4,ROUTINE9             CALL ROUTINE NUMBER 9
         NI    RII+1,X'FD'
IUC11    AH    R7,ONEENTRY             INCREASE P
         STH   R7,IUG2+2               PREPARE INSTRUCTION
         AH    R7,ONEENTRY             INCREASE P
         STH   R7,WORKPL               CHECK MAX
         BAL   R4,MAXCH
         SH    R7,ONEENTRY
         LA    R9,5(,R9)
         LA    R4,IUH21
         BAL   R3,ROUTIN15             CALL ROUTINE NUMBER 15
         SR    R14,R14                 STORE BLO
         SH    R7,ONEENTRY
         IC    R14,3(,R9)
         SRL   R14,4
         BAL   R4,ROUTINE9             CALL ROUTINE NUMBER 9
         BAL   R4,ROUTINE8             CALL ROUTINE NUMBER 8
         MVC   WPLACE(2),3(R9)
         MVI   VPLACE,X'AA'            V=CDSA
IUH21    MVC   IUH2+2(2),WPLACE        PREPARE INSTRUCTION
         MVN   IUH2+1(1),VPLACE
         SH    R9,KH5
         LA    R2,IUG2
         BAL   R14,GENTXTS
         DC    H'8'                    GENERATE 8 BYTES
*
         BAL   R4,ROUTINE1             CALL ROUTINE NUMBER 1
         MVC   IUH2+6(2),WPLACE
         MVN   IUH2+5(1),VPLACE
         LA    R2,IUH2+4
         BAL   R14,GENTXTS
         DC    H'14'                   GENERATE 14 BYTES
*
         OI    RII+1,X'01'             SET TO ONE RII OF GPR0
         MVI   CII+1,0
         STH   R7,WORKPL               ADJUST R9 ENTRY
         MVC   8(2,R9),WORKPL
         LA    R9,5(0,R9)
         ST    R9,RUTI                 INTRODUCE R9 ADD IN RUT
         MVC   2(1,R9),SPBNST+1        ADJUST R9 ENTRY
         OI    1(R9),X'30'
         NI    0(R9),X'90'
         OI    0(R9),X'88'
         L     R14,LATAB
         NI    96(R14),X'7F'
         BCT   R10,COMP
IUG2     LA    R1,R7(CDSA,0)           *** GENERATED CODE ***
IUH2     LA    R14,0(0,0)              *** GENERATED CODE ***
         LA    BRR,0(0,0)              *** GENERATED CODE ***
         STM   R14,BRR,0(R1)           *** GENERATED CODE ***
         L     ENTRY,96(LAT,0)         *** GENERATED CODE ***
         BALR  R14,ENTRY               *** GENERATED CODE ***
*
IUB2     LA    R4,I1B1
         TM    0(R9),X'20'
         BO    I1B13
         TM    0(R9),X'40'
         BO    I1B13
         TM    5(R9),X'20'
         BO    I1B13
         TM    5(R9),X'40'
         BO    I1B13
         SR    R14,R14
         IC    R14,8(R9)
         SRL   R14,4
         LA    R15,1
         SLL   R15,0(R14)
         STC   R15,IUB3+1
         XI    IUB3+1,X'FF'
IUB3     NI    RII+1,0
         SR    R14,R14
         TM    CII+1,X'07'
         BO    IUB13
         LH    R14,CII
         LA    R14,1(,R14)
IUB13    STH   R14,CII
         BAL   R4,I1B13
*
*        REAL-INTEGER POWER
*
I1B1     BAL   R3,HOB11
         LA    R4,I1C11
         BAL   R3,ROUTIN15             CALL ROUTINE NUMBER 15
         SR    R14,R14
         IC    R14,3(,R9)
         SRL   R14,4
         BAL   R4,ROUTINE9             CALL ROUTINE NUMBER 9
         BAL   R4,ROUTINE8             CALL ROUTINE NUMBER 8
         MVC   WPLACE(2),3(R9)         PREPARE INSTRUCTION
         MVI   VPLACE,X'AA'
I1C11    BAL   R3,HOD1
         L     R14,LATAB
         NI    100(R14),X'7F'
         BAL   R2,GENTXT6              GENERATE 6 BYTES
         L     ENTRY,100(LAT,0)        *** GENERATED CODE ***
         BALR  R14,ENTRY               *** GENERATED CODE ***
         BCT   R10,COMP
*
*        REAL-REAL POWER
*
HOB1     BAL   R3,HOB11
         LA    R4,HOC31
         BAL   R3,ROUTIN15             CALL ROUTINE NUMBER 15
         SR    R14,R14
         IC    R14,3(,R9)
         SRL   R14,4
         STC   R14,CIR+1
         BAL   R4,ROUTIN13             CALL ROUTINE NUMBER 13
         BAL   R4,ROUTIN12             CALL ROUTINE NUMBER 12
         MVC   WPLACE(2),3(R9)         PREPARE INSTRUCTION
         MVI   VPLACE,X'AA'
HOC31    BAL   R3,HOD1
         L     R14,LATAB
         NI    104(R14),X'7F'
         BAL   R2,GENTXT6              GENERATE 6 BYTES
         L     ENTRY,104(LAT,0)        *** GENERATED CODE ***
         BALR  R14,ENTRY               *** GENERATED CODE ***
         BCT   R10,COMP
HOB11    TM    RIR+1,X'01'             FPR0 FREE ?
         ST    R3,RETADR+52
         BZ    HOB21                   YES, BRANCH
         BAL   R4,ROUTIN14             CALL ROUTINE NUMBER 14
HOB21    TM    RIR+1,X'02'             FPR2 FREE ?
         BZ    HOB2
         LA    R14,2
         BAL   R4,ROUTIN13             CALL ROUTINE NUMBER 13
         NI    RIR+1,X'FD'
HOB2     TM    RII+1,X'02'             GPR1 FREE ?
         BZ    HOB3                    YES, BRANCH
         LA    R14,1
         BAL   R4,ROUTINE9             CALL ROUTINE NUMBER 9
         NI    RII+1,X'FD'
HOB3     AH    R7,ONEENTRY             INCREASE P
         STH   R7,HOB4+2               PREPARE INSTRUCTION
         AH    R7,ONEENTRY             INCREASE P
         STH   R7,WORKPL               CHECK MAX
         BAL   R4,MAXCH
         SH    R7,ONEENTRY             REDUCE P
         LA    R9,5(,R9)
         LA    R4,HOC11
         BAL   R3,ROUTIN15
         SH    R7,ONEENTRY
         SR    R14,R14
         IC    R14,3(,R9)
         SRL   R14,4
         BAL   R4,ROUTIN13             CALL ROUTINE NUMBER 13
         SRL   R14,1
         LA    R15,1
         SLL   R15,0(R14)
         STC   R15,WORKPL
         XI    WORKPL,X'FF'
         NC    RIR+1(1),WORKPL
         MVC   WPLACE(2),3(R9)         W = DISPL
         MVI   VPLACE,X'AA'            V = CDSA
HOC11    MVC   HOB5+2(2),WPLACE        PREPARE INSTRUCTION
         MVN   HOB5+1(1),VPLACE
         SH    R9,KH5
         LA    R2,HOB4
         BAL   R14,GENTXTS
         DC    H'8'                    GENERATE 8 BYTES
*
         L     R3,RETADR+52
         BR    R3
*
HOD1     MVC   HOB5+6(2),WPLACE
         ST    R3,RETADR+52
         MVN   HOB5+5(1),VPLACE
         LA    R2,HOB4+8
         BAL   R14,GENTXTS
         DC    H'8'                    GENERATE 8 BYTES
         OI    RIR+1,X'01'             SET TO ONE RIR OF FPR0
         MVI   CIR+1,0
         STH   R7,WORKPL               ADJUST R9 ENTRY
         MVC   8(2,R9),WORKPL
         LA    R9,5(,R9)
         ST    R9,RUTR                 INTRODUCE R9 ADD IN RUTR
         MVC   2(1,R9),SPBNST+1        USE R9 ENTRY
         OI    1(R9),X'30'
         NI    0(R9),X'90'
         OI    0(R9),X'88'
         L     R3,RETADR+52
         BR    R3
*
HOB4     LA    R1,7(CDSA,0)            *** GENERATED CODE ***
HOB5     LA    R14,0(0,0)              *** GENERATED CODE ***
         LA    BRR,0(0,0)              *** GENERATED CODE ***
         STM   R14,BRR,0(R1)           *** GENERATED CODE ***
I1B13    ST    R4,RET69
         TM    0(R9),X'20'
         BO    I1B14
         TM    0(R9),X'C0'
         BO    I1B15
I1B14    TM    5(R9),X'20'
         BO    I1B16
         TM    5(R9),X'C0'
         BO    I1B15
I1B16    STH   R7,RET691
         MVC   WORKPL(2),8(R9)
         NI    WORKPL,X'0F'
         LH    R7,WORKPL
         LA    R9,5(,R9)
         BAL   R4,TRINRE
         LH    R7,RET691
         B     I1B17
*
I1B15    LA    R9,5(,R9)
         BAL   R4,TRINRE
I1B17    SH    R9,KH5
         L     R4,RET69
         BR    R4
*
RET69    DC    F'0'
RET691   DC    H'0'
*
***********************************************************************
*
*        END OF IEX50006
*
***********************************************************************
*
