         MACRO
         SGIEF2SS
.*A000000-999999                                                 Y02668
.**********************************************************************
.*
.*  MACRO NAME = SGIEF2SS
.*
.*  DESCRIPTIVE NAME = GROUP 2 SYSGEN MACRO PRIMARY AND SECONDARY
.*                     MASTER SUBSYSTEM NAME CONTROL ROUTINE
.*
.*  FUNCTION = GENERATES THE ASSEMBLY JCL AND THE GROUP O MACRO SOURCE
.*                STATEMENTS TO ASSEMBLE THE
.*                   1) MASTER SUBSYSTEM START JCL       MACRO SGIEE0MS
.*                   2) JOB ENTRY SUBSYSTEM NAMES TABLE  MACRO SGIEF0SS
.*
.*  DEPENDENCIES = SCHEDULR SYSGEN MACRO:
.*                  -PROVIDE NAME OF PRIMARY JOB ENTRY SUBSYSTEM
.*                   IN GLOBAL &SGSPRIC(1)
.*                  -PROVIDE NAME(S) OF SECONDARY JOB ENTRY SUBSYSTEM
.*                   IN GLOBAL ARRAY &SGSUBSC(15)
.*
.*                 SGGBLPAK:
.*                  -CONTAIN THE ABOVE GLOBALS
.*
.*  INPUT = SGGBLPAK
.*             1) &SGSPRIC(1)  -  PRIMARY SUBSYSTEM NAME
.*             2) &SGSUBSC(15) -  SECONDARY SUBSYSTEM NAME(S)
.*
.*  EXIT-NORMAL = MEND TO GENERATE
.*
.*  EXIT-ERROR = NONE
.*
.*  EXTERNAL REFERENCES = SGGBLPAK
.*                        SGASMPAK
.*
.*  MESSAGES =NONE
.*
.**********************************************************************
         COPY  SGGBLPAK
.**********************************************************************
.*
.*  THESE ARE THE LOCAL VARIABLES USED IN THIS MACRO
.*
.**********************************************************************
         LCLA  &I,&K,&COUNT
         LCLC  &BLANKS,&MASK,&COMMALP,&RTPAREN,&COMMA
.**********************************************************************
.*
.*  THIS SECTION OF CODE COPIES THE ASSEMBLY JCL AND ISSUES
.*       THE MACRO CALL FOR SGIEE0MS SO THAT THE MASTER
.*       SUBSYSTEM JCL STARTS THE PRIMARY SUBSYSTEM.
.*
.**********************************************************************
&SGCTRLC(6)    SETC   'MSTRJCL'        SET THE SYSPUNCH DSNAME
         COPY  SGASMPAK                COPY THE ASSEMBLY JCL
         PUNCH  '         SGIEE0MS   &SGSPRIC(1)'
.*                                     PUNCH THE MACRO CALL WITH THE
.*                                     PRIMARY SUBSYSTEM NAME AS PARM
         PUNCH  '         END'         PUNCH THE END CARD
         PUNCH  '/*'                   PUNCH THE /* CARD
.**********************************************************************
.*
.*  THIS SECTION OF CODE SEARCHES THE SECONDARY SUBSYSTEM NAME LIST
.*       AND DETERMINES HOW MANY WERE SPECIFIED.
.*
.**********************************************************************
&I       SETA  1                       INITIALIZE TO NO SECONDARY NAMES
.LP1     ANOP                          LOOP LABEL TO FIND NUMBER OF
.*                                     SECONDARY NAMES AND PRIMAR NAME
         AIF (&I GT 15).ENDLP1         TEST FOR END OF POSSIBLE SEC NMS
.*                                     EXIT LOOP IF AT END OF LIST
         AIF ('&SGSUBSC(&I)' EQ '').ENDLP1
.*                                     IF A NULL NAME IS ENCOUNTERED,
.*                                     THERE ARE NO MORE AND EXIT
&I       SETA  &I+1                    INCREMENT &I TO EXAMINE NEXT ONE
         AGO   .LP1                    GO EXAMINE THIS NEXT ENTRY
.ENDLP1  ANOP       EXIT LABEL FOR .LP1 LOOP
&I       SETA  &I-1                    DECREMENT &I TO GET THE INDEX
.*                                     VALUE OF THE LAST NAME
.**********************************************************************
.*
.*  THIS SECTION OF CODE COPIES THE ASSEMBLY JCL AND ISSUES
.*       THE MACRO CALL FOR SGIEF0SS. THE MACRO CALL CAN TAKE
.*       ONE OF THE FOLLOWING FORMATS:
.*          1)  SGIEF0SS   PRINAME
.*          2)  SGIEF0SS   PRINAME,SECNAME
.*          3)  SGIEF0SS   PRINAME,(SECNAME1,                         X
.*                                  SECNAME2,                         X
.*                                  .....                             X
.*                                  SECNAMEN)
.*
.**********************************************************************
&SGCTRLC(6)    SETC   'IEFJESNM'       SET THE SYSPUNCH DSNAME
         COPY  SGASMPAK                COPY THE ASSEMBLY JCL
.*
.*   INITIALIZE THE WORK FIELDS
.*
&COMMA   SETC  ','
&COMMALP SETC  ',('
&RTPAREN SETC  ')'
&MASK    SETC  '      X'               SET MASK FOR CONT MARK
         AIF (&I NE 0).SSCNT1          IF &I IS NOT 0, A FORMAT
.*                                     2) OR 3) CALL IS NECESSARY ELSE
.*                                     A 0 INDICATES FORMAT 1)
.*
.*       FORMAT  1
.*
         PUNCH  '         SGIEF0SS   &SGSPRIC(1)'
.*                                     PUNCH A FORMAT 1) CARD SINCE
.*                                     THERE ARE NO SECONDARY NAMES
         AGO   .TERM                   EXIT TO FINISH JCL
.*
.*       FORMAT  2
.*
.SSCNT1  ANOP                          LABEL FOR FORMAT 2) OR 3)
         AIF (&I GT 1).SSCNT2          IF &I IS GT 1 THEN A FORMAT 3)
.*                                     CALL IS NECESSARY
.*                                     A 1 INDICATES FORMAT 2)
         PUNCH  '         SGIEF0SS   &SGSPRIC(1)&COMMA&SGSUBSC(1)'
.*                                     PUNCH A FORMAT 2) CARD SINCE
.*                                     THERE IS ONLY ONE SECONDARY NAME
         AGO   .TERM                   EXIT TO FINISH JCL
.*
.*       FORMAT  3
.*
.SSCNT2  ANOP                          FORMAT 3) LABEL
&COUNT   SETA  K'&SGSPRIC(1)+K'&SGSUBSC(1)
.*                                     CALCULATE DISPLACEMENT FOR
.*                                     CONTINUATION MARK
&BLANKS  SETC   '&MASK'(&COUNT-1,9-&COUNT)       SET CONTINUATION MARK
         PUNCH  '         SGIEF0SS   &SGSPRIC(1)&COMMALP&SGSUBSC(1)&COMX
               MA                                        &BLANKS'
.*                                     PUNCH BEGINNING OF FORMAT 3)
.*                                     MORE CARDS WILL FOLLOW FOR OTHER
.*                                     SECONDARY NAMES
&K       SETA  2                       PREPARE TO PUNCH REMAINING NAMES
.LP3     ANOP                          LOOP LABEL TO PUNCH OTHER NAMES
         AIF (&K EQ &I).LASTNM         IF &K INDEX EQUAL &I INDEX THEN
.*                                     THE LAST CARD IS TO BE PUNCHED
&COUNT   SETA  K'&SGSUBSC(&K)          CALCULATE DISPLACEMENT FOR
.*                                     CONTINUETION MARK
&BLANKS  SETC   '&MASK'(&COUNT+3,5-&COUNT)       SET CONTINUATION MARK
         PUNCH  '               &SGSUBSC(&K)&COMMA                     X
                                             &BLANKS'
.*                                     PUNCH SUBLIST CARD FOR FORMAT 3)
&K       SETA  &K+1                    INCREMENT &K INDEX TO PUNCH
.*                                     NEXT SUBLIST CARD
         AGO   .LP3                    GO PROCESS NEXT SUBLIST ENTRY
.LASTNM  ANOP                          LAST NAME EXIT LABEL
         PUNCH  '               &SGSUBSC(&I)&RTPAREN'
.*                                     PUNCH LAST NAME OF SUBLIST FOR
.*                                     FORMAT 3) CALL
.TERM    ANOP                          JCL TERMINATION LABEL
         PUNCH  '         END'         PUNCH END CARD
         PUNCH  '/*'                   PUNCH /* CARD
.MACEND  ANOP
         MEND
