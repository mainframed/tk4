         MACRO                                                   MVS38J
         SGFDSP04 &ID=,&UNIT=                                    MVS38J
.*                                                               MVS38J
.* SGFDSP04 - DSP macro exit; builds 3390 DFT table entry        MVS38J
.*                                                               MVS38J
.* Function: set Device Features Table (DFT) for 3390            MVS38J
.* Written:  2001 January 7                                      MVS38J
.* Author:   James M. Morrison                                   MVS38J
.*           Based on Bertus Bekker's SGFDSP03 macro             MVS38J
.* Replaces: Dummy SGFDSP04 macro                                MVS38J
.* Level:    MVS 3.8j                                            MVS38J
.* Target:   SYS1.AGENLIB                                        MVS38J
.* Process:  MVS System Generation                               MVS38J
.* Dependencies: SGGBLPAK - GLOBAL SYSGEN VARIABLES              MVS38J
.*               SGDITGBL - OLD SYSGEN DIT GEN                   MVS38J
.* Parameters:   UNIT   = DEVICE TYPE                            MVS38J
.*               ID     = MAC ID FOR MNOTES                      MVS38J
.* Caller:   SGDSPDFT macro                                      MVS38J
.* Calls:    SGOPCHAN, SGAP, SGDFTGBL, SGADDR macros             MVS38J
.*                                                               MVS38J
         COPY  SGGBLPAK                     GLOBAL VARIABLES     MVS38J
         LCLC  &UCBFLG5                     UCB FLAG 5 BITS      MVS38J
         LCLC  &UCBFLP1                     UCB        BITS      MVS38J
         LCLC  &UCBTB2                      UCB TYPE BYTE 2      MVS38J
         LCLB  &SHR,&SHRU                   SHARED,SHAREDUP      MVS38J
         LCLB  &TCS                         2-CHAN SWITCH        MVS38J
         LCLB  &OFF                         OFFLINE              MVS38J
         LCLA  &N                           INDEX COUNTER        MVS38J
         LCLA  &PCCW                        # OF PREFIX CCWS     MVS38J
.*                                                               MVS38J
         AIF   ('&UNIT' EQ '3390').G3390    GEN 3390 CALL??      MVS38J
         AGO   .ERROR01                     INVALID DEV TYPE     MVS38J
.G3390   ANOP                                                    MVS38J
         MNOTE *,'SGFDSP04 - &UNIT DEVICE TYPES'                 MVS38J
.* See SGDFTGBL for UCBFLG5 values                               MVS38J
&UCBFLG5 SETC  '(DISCONCC,ENVRTNS'          STD. DASD FEATURES   MVS38J
         MNOTE *,'SGFDSP04 - DISCONNECT FEATURE'                 MVS38J
         MNOTE *,'SGFDSP04 - ENVIRONMENTAL DATA'                 MVS38J
.* See IODEVICE macro for &SGDCLSB definitions                   MVS38J
&SGDCLSB(88) SETB 1                         SET RPS (3340!)      MVS38J
         MNOTE *,'SGFDSP04 - ROTATIONAL POSITION SENSING'        MVS38J
&PCCW    SETA  5                            # OF PREFIX CCWS     MVS38J
&UCBTB2  SETC  '10'            UCB TYPE BYTE 2:  data chaining   MVS38J
.*                                               byte mode       MVS38J
&OFF     SETB  ('&SGOFLPC' EQ 'YES')        SET OFFLINE INDIC    MVS38J
.*                                                               MVS38J
&N       SETA  0                            INIT INDEX           MVS38J
.FTLOOP  ANOP                               FEATURE CHECK LOOP   MVS38J
&N       SETA  &N+1                         UP INDEX             MVS38J
         AIF   (&SGFCNTA LT &N).FTFIN       ALL DONE??           MVS38J
         AIF   ('&SGFETPC(&N)' EQ 'SHARED').SHARED               MVS38J
         AIF   ('&SGFETPC(&N)' EQ 'SHAREDUP').SHAREDU            MVS38J
         AIF   ('&SGFETPC(&N)' EQ 'ALTCTRL').ALTCTRL             MVS38J
         AIF   ('&SGFETPC(&N)' EQ '2-CHANSW').TCHANSW            MVS38J
         MNOTE 5,'SGFDSP04 - 003 - INVALID FEATURE &SGFETPC(&N)' MVS38J
&SGQUITB SETB  1                            SET QUIT SWITCH      MVS38J
         AGO   .FTLOOP                      DO NEXT              MVS38J
.SHARED  ANOP                                                    MVS38J
         MNOTE *,'SGFDSP04 - SHARED BY CPUS'                     MVS38J
&PCCW    SETA  &PCCW+1                      ADD RESERVE CCW SPC  MVS38J
&UCBTB2  SETC  '30'            UCB TYPE BYTE 2:  data chain      MVS38J
.*                                               burst mode      MVS38J
&SHR     SETB  1                            INDICATE SHARED      MVS38J
         AGO   .FTLOOP                      DO NEXT              MVS38J
.SHAREDU ANOP                                                    MVS38J
         MNOTE *,'SGFDSP04 - SHARED WHEN IN UP MODE'             MVS38J
&PCCW    SETA  &PCCW+1                      ADD RESERVE CCW SPC  MVS38J
&UCBFLP1 SETC  'UPSHARBL'                   INDICATE SHRDUP      MVS38J
&SHRU    SETB  1                            INDICATE SHRDUP      MVS38J
         AGO   .FTLOOP                      NEXT                 MVS38J
.ALTCTRL ANOP                                                    MVS38J
         MNOTE *,'SGFDSP04 - HAS ALTERNATE CONTROLLER'           MVS38J
&UCBFLG5 SETC  '&UCBFLG5,ALTCTRL'           ADD FEATURE TO LIST  MVS38J
         AGO   .FTLOOP                      NEXT                 MVS38J
.TCHANSW ANOP                                                    MVS38J
&TCS     SETB  1                            SET FOR 2CS          MVS38J
         AGO   .FTLOOP                      NEXT                 MVS38J
.FTFIN   ANOP                                                    MVS38J
         AIF   (&SHR AND &SHRU).ERROR02     MUTEX FEATURES USD?  MVS38J
         AIF   (&SGOPCTA EQ 0).SINGPTH      OPTCHAN??            MVS38J
         MNOTE *,'SGFDSP04 - OPTIONAL CHANNEL &SGOPCTA'          MVS38J
&UCBFLG5 SETC  '&UCBFLG5,ALTPATH'           ADD FEATURE TO LIST  MVS38J
.SINGPTH ANOP                                                    MVS38J
&UCBFLG5 SETC  '&UCBFLG5.)'                 END OF FEATURE LIST  MVS38J
.*                                                               MVS38J
&N       SETA  1                            INIT INDEX           MVS38J
.DEVLOOP ANOP                               LOOP FOR DEVICES     MVS38J
&SGDOFFB(&SGCNTRA(3)) SETB (&OFF EQ 1)      OFFLINE OPTION       MVS38J
&SGDF01B(&SGCNTRA(3)) SETB (&TCS EQ 1)      TWO CHANNEL SWITCH   MVS38J
&SGUFP6B(&SGCNTRA(3)) SETB 1                DYNAMIC PATHING      MVS38J
         SGOPCHAN PERMIT=YES,UNIT=&UNIT,ID=&ID   OPTCHAN SPEC    MVS38J
         SGAP  PERMIT=YES,UNIT=&UNIT,ID=&ID      ALT. POWER      MVS38J
         SGDFTGBL UCBFLP1=(&UCBFLP1),CCWPFIX=&PCCW,              MVS38J+
               UCBFL5=&UCBFLG5,                                  MVS38J+
               ATNTABX=0,DSEGLEN=16,DPNDSEG=4A(0),               MVS38J+
               ERRTAB=0,UNMOVBL=YES,DEVTYPE=30&UCBTB2.200F       MVS38J
&N       SETA  &N+1                         INCR INDEX           MVS38J
         AIF   (&N GT &SGADRPC(2)).DEVFIN   ALL DONE??           MVS38J
         SGADDR ,                             NO - UP ADDRESS    MVS38J
         AGO   .DEVLOOP                            AND REPEAT    MVS38J
.DEVFIN  ANOP                                                    MVS38J
         AGO   .EXIT001                     GET OUT              MVS38J
.ERROR01 ANOP                                                    MVS38J
    MNOTE 5,'SGFDSP04 - 001 - INVALID DEVICE TYPE (LOGIC ERROR)' MVS38J
&SGQUITB SETB  1                            SET QUIT SWITCH      MVS38J
         AGO   .EXIT001                     GET OUT              MVS38J
.ERROR02 ANOP                                                    MVS38J
         MNOTE 5,'SGFDSP04 - 002 - MUTUALLY EXCLUSIVE FEATURES'  MVS38J
&SGQUITB SETB  1                            SET QUIT SWITCH      MVS38J
         AGO   .EXIT001                     GET OUT              MVS38J
.EXIT001 ANOP                                                    MVS38J
         MEND                                                    MVS38J
