         MACRO
         SGIEC500
         COPY  SGGBLPAK            COPY THE GLOBAL PACK
         LCLA  &I,&W
         LCLC  &C,&T
.***** START OF SPECIFICATIONS **************************************
.*
.* MODULE NAME - SGIEC500
.*
.* DESCRIPTIVE NAME - INCLUDE ERP/LPA MODULES FOR SYSGENED DEVICES
.*
.* COPYRIGHT - NONE
.*
.* STATUS - CHANGE LEVEL 00
.*
.* FUNCTION -
.*   THIS MACRO SCANS THE DIT. FOR EVERY ENTRY WHICH HAS BEEN
.*   GENERATED, THE ERP AND LPA MODULES SPECIFIED IN THE ENTRY ARE
.*   INCLUDED INTO THE SYSTEM BY PUNCHING IEBCOPY CONTROL STATEMENTS.
.*
.*   THIS MACRO IS INVOKED BY GENERATE.
.*
.* NOTES - N/A
.*
.* MODULE TYPE - MACRO
.*
.* ENTRY POINT - N/A
.*
.* INPUT -
.*         NO PARAMETERS ARE PASSED DIRECTLY.
.*
.*         THE FOLLOWING GLOBALS ARE USED-
.*           &SGCPRGB(8)    - VS2 SYSTEM BEING GENERATED
.*           &SGDITDA       - NUMBER OF DIT ENTRIES TO BE SEARCHED
.*           &SGGENDB(1-X)  - DEVICE OF THIS GENERIC HAS BEEN GENNED
.*           &SGERPDC(1-X)  - DLIB NAME FOR ERP MODULES
.*           &SGERPMC(1-X)  - ERP MODULE NAMES TO BE INCLUDED
.*           &SGLPADC(1-X)  - DLIB NAME FOR LPA MODULES
.*           &SGLPAMC(1-X)  - LPA MODULE NAMES TO BE INCLUDED
.*                 (WHERE X IS THE VALUE OF &SGDITDA)
.*
.* OUTPUT -
.*       THE FOLLOWING GLOBALS ARE SET -
.*           &SGQUITB         - QUIT SWITCH IF AN INVALID NAME
.*                              LIST IS FOUND
.*
.*
.*       LOCALS USED ARE -
.*          &I - USED AS LOOP VARIABLE THROUGH DIT. POINTS TO
.*               ENTRY BEING PROCESSED.
.*          &W - WORK VARIABLE. USED TO VALIDITY CHECK LENGTH
.*               OF LPA/ERP NAMES. ALSO USED TO LOOP THROUGH
.*               LPA/ERP CHARACTER STRINGS.
.*          &C - CHARACTER WORK VARIABLE. USED TO CONTAIN CURRENT
.*               LPA/ERP NAME TO BE PUNCHED.
.*          &T - CHARACTER WORK VARIABLE. USED TO CONTAIN CURRENT
.*               ITEM (LPA OR ERP) BEING PROCESSED. PLACED IN
.*               MNOTE IF AN ERROR OCCURS.
.*
.*
.*
.* EXIT, NORMAL - N/A
.*
.* EXIT, ERROR - N/A
.*
.* EXTERNAL REFERENCES - N/A
.*
.* TABLES - DEVICE INFORMATION TABLE (DIT)
.*
.* MACROS - N/A
.*
.* CHANGE ACTIVITY - N/A
.*
.*A-000000-999999                                              @G64HPLP
.***** END OF SPECIFICATIONS ******************************************
.LP      ANOP
&I       SETA  &I+1                INCREMENT LOOP VARIABLE
         AIF   (&I GT &SGDITDA).MEND EXIT IF DONE WITH DIT
.* IF THE DEVICE HAS NOT BEEN SYSGENED GO TO THE NEXT ENTRY.
.* IF THE DEVICE IS DUMMY (&I=76) THEN &SGGENDB WILL NOT
.* BE ON AND THE ENTRY SHOULD BE PROCESSED TO GET DUMMY SUPPORT
.* INTO THE SYSTEM. THE &SGGENDB CANNOT BE SET FOR DUMMY
.* BECAUSE ALLOCATION REQUIRES IT. DUMMY DEVICES ARE PROCESSED
.* FOR ALLOCATION IN A DIFFERENT WAY. SEE ALLOCATION MACROS FOR
.* SUPPORT.
         AIF   (&I EQ 76).LP1           DUMMY ENTRY IS 76.     @ZM49050
         AIF   (NOT &SGGENDB(&I)).LP                           @ZM49050
.LP1     ANOP                                                  @ZM49050
.*             SKIP ERPS IF MODULE NAME LIST OR DLIB IS NULL
         AIF   (K'&SGERPMC(&I) EQ 0 OR K'&SGERPDC(&I) EQ 0).TLPA
&T       SETC  'ERP'               INDICATE ERP LIST BEING PROCESSED
&W       SETA  1+K'&SGERPMC(&I)    1+NUMBER OF CHARS SPECIFIED
         AIF   (&W-&W/9*9 NE 0).ERROR ERROR IF LENGTH NOT MULT OF 9
&W       SETA  1
         PUNCH '      COPY OUTDD=&SGCTRLC(36),INDD=&SGERPDC(&I)        *
               DEVICE=&SGGNRIC(&I)'                            @ZM49120
.ERPLP   ANOP
&C       SETC  '&SGERPMC(&I)'(&W,8)
         PUNCH '         SELECT MEMBER=&C'
&W       SETA  &W+9                INCREMENT TO NEXT NAME IN LIST
         AIF   (&W LT K'&SGERPMC(&I)).ERPLP CONTINUE IF ANY MORE
.TLPA    ANOP
.*             SKIP TEST FOR LPA IF NOT A VS2 SYSTEM
         AIF   (NOT &SGCPRGB(8)).LP
.*             SKIP LPA MODULES IF MODULE NAME LIST OR DLIB IS NULL
         AIF   (K'&SGLPAMC(&I) EQ 0 OR K'&SGLPADC(&I) EQ 0).LP
&T       SETC  'LPA'               INDICATE LPA LIST BEING PROCESSED
&W       SETA  1+K'&SGLPAMC(&I)    1+NUMBER OF CHARS SPECIFIED
         AIF   (&W-&W/9*9 NE 0).ERROR ERROR IF LENGTH NOT MULT OF 9
&W       SETA  1
         PUNCH '  COPY OUTDD=&SGCTRLC(36),INDD=&SGLPADC(&I)            *
               DEVICE=&SGGNRIC(&I)'                            @ZM49120
.LPALP   ANOP
&C       SETC  '&SGLPAMC(&I)'(&W,8)
         PUNCH '         SELECT MEMBER=&C'
&W       SETA  &W+9                INCREMENT TO NEXT NAME IN LIST
         AIF   (&W LT K'&SGLPAMC(&I)).LPALP CONTINUE IF ANY MORE
         AGO   .LP                 PROCESS NEXT DIT ENTRY
.*      ERROR IN NAME LIST--INCORRECT FORMAT
.ERROR   ANOP
         MNOTE 5,'SGIEC500--&T NAME LIST INVALID FOR DIT ENTRY &I, GENEX
               RIC=&SGGNRIC(&I)'
&SGQUITB SETB  1
         AGO   .LP                 PROCESS NEXT DIT ENTRY
.MEND    MEND
