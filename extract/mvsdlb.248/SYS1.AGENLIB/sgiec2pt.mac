         MACRO
    SGIEC2PT   &UCBNO,&DEVAD,&TYPC,&DEVTAB,&STATAB,&LCHN,&DEVNDX,&ALOCC*
               NT,&SYSRES,&SQOPT,&OFF                            Y02146
.*
.* CHANGE ACTIVITY
.*            APARS                     PTMS
.*        YA02442  (10/22/73)       YM0160  (8/16/73)
.*        YA03245  (1/30/74)        YM5809  (2/1/74) = YA03252
.*        YA03252  (1/30/74)
.*        OZ00522  (8/16/74)
.*        ZA02837  (2/19/75)                                LG @ZA02837
.*        AZ04864  (02/23/76)                                  @ZA04864
.*        AZ15410  (11/15/76)
.*        AZ20108  (05/25/77)
.*        AZ25208  (07/29/77)
         COPY  SGGBLPAK
         LCLA  &SCR,&CHNL,&TUCB                               LF YM0160
         LCLA  &LCHCNT,&COUNTB,&COUNTCU
         LCLC  &TYPEA,&ERRTAB,&CHNLAD
&CHNLAD  SETC  '&DEVAD'(1,1)
&LCHCNT  SETA  0
&COUNTB  SETA  &SGDASPA(&UCBNO)                                @ZA15410
&COUNTCU SETA  &COUNTB
&TUCB    SETA  &UCBNO+1    CALC TAG UCB FOR PUNCHED AGO STM   LF YM0160
         AIF   (NOT (&SGDT12B(&COUNTB) AND &SGDU10B(&COUNTB))).SSERR0
&ERRTAB  SETC  '00'                                            @AZ25208
&TYPEA   SETC  '500040F1'                                      @AZ25208
         AGO   .UCB0P2                                         @AZ25208
.SSERR0  ANOP                                                  @AZ25208
         AIF   (NOT (&SGDT12B(&COUNTB) AND &SGDU05B(&COUNTB))).SSERR1
&ERRTAB  SETC  'C7'                                                000R
&TYPEA   SETC  '50004014'                                          000R
         AGO   .UCB0P2                                             000R
.SSERR1  AIF   (NOT &SGDAD7B(&COUNTB)).SSERR                       000R
&ERRTAB  SETC  '2B'
         AGO   .SETCU                                              000T
.SSERR   ANOP
         AIF   (NOT (&SGDT12B(&COUNTB) AND                             *
               (&SGDU06B(&COUNTB) OR &SGDU07B(&COUNTB)))).SSERR2  Z2837
         AIF   (NOT &SGDAD1B(&COUNTB)).RNCHMX                     21101
&TYPEA   SETC  '50004015'                                         21101
&ERRTAB  SETC  '31'                                         LD @ZA00522
         AGO   .UCB0P2                                          YA03245
.RNCHMX  AIF   (NOT &SGDAD2B(&COUNTB)).SSERR2                     21101
&TYPEA   SETC  '50004025'                                         21101
.*           ONE LINE DELETED BY THE FOLLOWING APAR:            YA03245
&ERRTAB  SETC  '31'                                               21101
         AGO   .UCB0P2                                            M1257
.SSERR2  ANOP
&ERRTAB  SETC  '29'
.SETCU   ANOP                                                      000T
         AIF   (&SGDT12B(&COUNTB) AND &SGDU04B(&COUNTB) AND &SGDCLSB(33*
               ) AND &SGDCLSB(67)).IBM7770                        M3967
.CU2702  AIF   (NOT (&SGUNT2B(&COUNTCU) AND &SGUNU2B(&COUNTCU))).CU2701
&TYPEA   SETC  '50004001'               SET 2702 BASIC
         AGO   .ACALANS
.CU2701  AIF   (NOT (&SGUNT2B(&COUNTCU) AND &SGUNU1B(&COUNTCU))).CU2703
&TYPEA   SETC  '50004002'               SET 2701 BASIC
         AGO   .ACALANS
.CU2703  ANOP                                                      000T
&TYPEA   SETC  '50004003'
         AGO   .ACALANS                                            000T
.IBM7770 ANOP                                                      000T
&TYPEA   SETC  '51904000'                                        A42394
.*       THE FOLLOWING LINE CORRECTS APAR YA02442               YA03252
         AGO   .UCB0P1                                          YA03252
.ACALANS ANOP
&CHNL    SETA  1                                                  M0923
.CHNLP   AIF   ('&CHNLAD' EQ '0123456789ABCDEF'(&CHNL,1)).CHNOUT  M0923
&CHNL    SETA  &CHNL+1                                            M0923
         AGO   .CHNLP                                             M0923
.CHNOUT  ANOP                                                     M0923
&CHNL    SETA  &CHNL-1                                            M0923
         AIF   (NOT &SGCHANB(&CHNL*15+3)).MULTPLX
&TYPEA   SETC  '7'.'&TYPEA'(2,7)
.MULTPLX ANOP
         AIF   (NOT &SGDAD7B(&COUNTB)).SETFEAT    IS THIS SDA2?
         AIF   (NOT &SGDU08B(&COUNTB)).SWITCH     BSC3 HAS POLLING
&SGDF01B(&COUNTB) SETB 1
         AGO   .SETFEAT
.SWITCH  AIF   (NOT &SGDU07B(&COUNTB)).SETFEAT    BSC2 HAS SWITCHED
&SGDF02B(&COUNTB) SETB 1
&SGDF03B(&COUNTB) SETB 1
.*
.SETFEAT ANOP
         AIF   (NOT &SGDF01B(&COUNTB)).AUTOCAL    IS IT AUTOPOLL
&LCHCNT  SETA  &LCHCNT+64
.AUTOCAL AIF   (NOT &SGDF02B(&COUNTB)).AUTOANS    AUTOCALL
&LCHCNT  SETA  &LCHCNT+128
.AUTOANS AIF   (NOT &SGDF03B(&COUNTB)).CHECKNG    AUTOANSWER
&LCHCNT  SETA  &LCHCNT+16
.CHECKNG AIF   (NOT &SGDF06B(&COUNTB)).SCONTRL    CHECKING
&LCHCNT  SETA  &LCHCNT+32
.SCONTRL AIF   (NOT &SGDF04B(&COUNTB)).XCONTRL    SCONTROL
&LCHCNT  SETA  &LCHCNT+8
.XCONTRL  AIF  (NOT &SGDF05B(&COUNTB)).OIU        XCONTROL         000K
&LCHCNT  SETA  &LCHCNT+4
.OIU      AIF  (NOT &SGDF07B(&COUNTB)).CHKSAD     OIU              000K
&LCHCNT   SETA &LCHCNT+4+8                                         000K
.CHKSAD  AIF   (&SGDM01B(&COUNTB)).TPADPCK
         AIF   (NOT &SGDM02B(&COUNTB)).SAD2
&LCHCNT  SETA  &LCHCNT+1                          SET SAD 1
.SAD2    AIF   (NOT &SGDM03B(&COUNTB)).SAD3
&LCHCNT  SETA  &LCHCNT+2                          SET SAD 2
.SAD3    AIF   (NOT &SGDM04B(&COUNTB)).TPADPCK
&LCHCNT  SETA  &LCHCNT+3
.TPADPCK AIF   (NOT &SGDAD1B(&COUNTB)).IBMT2      TYPE 1 - IBM1
&TYPEA   SETC  '&TYPEA'(1,6).'1'.'&TYPEA'(8,1)
         AGO   .UNITCHK
.IBMT2   AIF   (NOT &SGDAD2B(&COUNTB)).IBMT3      TYPE 2 - IBM2
&TYPEA   SETC  '&TYPEA'(1,6).'2'.'&TYPEA'(8,1)
         AGO   .UNITCHK
.IBMT3   AIF   (NOT &SGDAD3B(&COUNTB)).IBMT4      TYPE 3 - IBMT
&TYPEA   SETC  '&TYPEA'(1,6).'3'.'&TYPEA'(8,1)
         AGO   .UNITCHK
.IBMT4   AIF   (NOT &SGDAD4B(&COUNTB)).IBMT5      TYPE 4 - TELE1
&TYPEA   SETC  '&TYPEA'(1,6).'4'.'&TYPEA'(8,1)
         AGO   .UNITCHK
.IBMT5 AIF (NOT &SGDAD5B(&COUNTB)).IBMT8     TYPE 5 - TELE2
&TYPEA   SETC  '&TYPEA'(1,6).'5'.'&TYPEA'(8,1)
         AGO   .UNITCHK
.IBMT8   AIF   (NOT &SGDAD6B(&COUNTB)).IBMT9      TYPE 8 - IBM3
&TYPEA SETC '&TYPEA'(1,6).'8'.'&TYPEA'(8,1)
         AGO   .UNITCHK
.IBMT9   AIF   (NOT &SGDAD7B(&COUNTB)).IBMT6  TYPE 9 - BSCA
&TYPEA   SETC  '&TYPEA'(1,6).'9'.'&TYPEA'(8,1)
         AGO   .UNITCHK
.IBMT6   AIF   (NOT &SGDAD8B(&COUNTB)).UNITCHK  TYPE 6 - WTTA
&TYPEA SETC '&TYPEA'(1,6).'6'.'&TYPEA'(8,1)
         AGO   .UNITCHK
.*   NOT CURRENTLY SUPPORTED
.IBMT7  ANOP                           TYPE 7 - SAA1
&TYPEA SETC '&TYPEA'(1,6).'7'.'&TYPEA'(8,1)
.UNITCHK AIF   (NOT &SGDT02B(&COUNTB)).T08B
         AIF   (&SGDU01B(&COUNTB)).M1        WTTA
         AIF   (&SGDU02B(&COUNTB)).M6        2741P
         AIF   (&SGDU03B(&COUNTB)).M5        2741C
         AIF   (&SGDU04B(&COUNTB)).X1050     1050X
         AIF   (&SGDU06B(&COUNTB)).M5        BSC1
         AIF   (&SGDU07B(&COUNTB)).M6        BSC2
         AIF   (&SGDU08B(&COUNTB)).M7        BSC3
         AGO   .TNCSUP
.*
.T08B    AIF   (NOT &SGDT08B(&COUNTB)).T12B
         AIF   (&SGDU01B(&COUNTB)).M1        1030
         AIF   (&SGDU02B(&COUNTB)).M1        1050
         AIF   (&SGDU03B(&COUNTB)).M2        1060
         AIF   (&SGDU04B(&COUNTB)).M1        2260
         AIF   (&SGDU05B(&COUNTB)).M2        115A
         AIF   (&SGDU06B(&COUNTB)).M1        83B3
         AIF   (&SGDU07B(&COUNTB)).M1        TWX
         AIF   (&SGDU08B(&COUNTB)).INTCHK    2740                  00MC
         AGO   .TNCSUP
.*
.INTCHK  AIF   (NOT &SGDF08B(&COUNTB)).M4                          00MC
         AGO   .M9                                                 00MC
.*                                                                 00MC
.T12B    AIF   (NOT &SGDT12B(&COUNTB)).TNCSUP
         AIF   (&SGDU01B(&COUNTB)).M8        2740X
         AIF   (&SGDU02B(&COUNTB)).M3        2740C
         AIF   (&SGDU03B(&COUNTB)).M1        2265                  000T
         AIF   (&SGDU04B(&COUNTB) AND &SGDM03B(&COUNTB)).M1 7770 MOD 3
         AGO   .TNCSUP
.*
.X1050   AIF   (&SGDAD3B(&COUNTB)).M2
         AGO   .M7
.*
.TNCSUP  ANOP                                 NOT CURRENTLY SUPPORTED
&TYPEA   SETC  '30004003'               DUMMY
         AGO   .UCBOPUT
.*
.M9      ANOP                                                      00MC
&LCHCNT  SETA  &LCHCNT+256                                         00MC
.M8      ANOP
&LCHCNT  SETA  &LCHCNT+256
.M7      ANOP
&LCHCNT  SETA  &LCHCNT+256
.M6      ANOP
&LCHCNT  SETA  &LCHCNT+256
.M5      ANOP
&LCHCNT  SETA  &LCHCNT+256
.M4      ANOP
&LCHCNT  SETA  &LCHCNT+256
.M3      ANOP
&LCHCNT  SETA  &LCHCNT+256
.M2      ANOP
&LCHCNT  SETA  &LCHCNT+256
.M1      ANOP
&LCHCNT  SETA  &LCHCNT+256
         AGO   .SETYPA
.SETYPA  ANOP                 CONVERT BEFORE SETTING FINAL TYPE
         CONVERT TO=EBCDIC,VALUE=&LCHCNT,DIGITS=3
&TYPEA   SETC  '&TYPEA'(1,1).'&EBCDIC'(1,3).'&TYPEA'(5,4)
.UCBOPUT ANOP
         AIF   (NOT &SGDEVB(1)).UCB0P2  SKIP INSPECTION
&SCR     SETA  &SGETABA(&COUNTB)-&SGETABA(&COUNTB)/256*256
.*                                  LAST TWO CHAR DEVTAB
         AIF   (&SGDEVTP(&COUNTB) EQ 0 AND &SCR EQ 0).UCB0P1
         CONVERT  TO=EBCDIC,VALUE=&SGDEVTP(&COUNTB),DIGITS=6
&TYPEA   SETC  '&EBCDIC'
         CONVERT  TO=EBCDIC,VALUE=&SCR,DIGITS=2
&TYPEA   SETC  '&TYPEA'.'&EBCDIC'
&ERRTAB  SETC  '0'            ERRTAB UNKNOWN
.UCB0P1  ANOP
&SCR     SETA  (&SGETABA(&COUNTB)-&SGETABA(&COUNTB)/65536*65536)/256
         AIF   (&SCR EQ 0).UCB0P2
&SCR     SETA  &SCR-1
         CONVERT TO=EBCDIC,VALUE=&SCR,DIGITS=2
&ERRTAB  SETC  '&EBCDIC'
.UCB0P2  ANOP
         PUNCH '.UCB&UCBNO   ANOP       UCB ENTRY'
         PUNCH ' IECVUCB &UCBNO,&DEVAD,&TYPC,&DEVTAB,&ERRTAB,&STATAB,&L*
               CHN,&DEVNDX,&TYPEA,&ALOCCNT,&SYSRES,&SQOPT,&OFF,,,0'
         PUNCH ' AGO .UCB&TUCB'                               LF YM0160
         PUNCH '.UCX&UCBNO   ANOP       UCB ENTRY'           NE Z30ANNE
         PUNCH ' IECVUCB &UCBNO,&DEVAD,&TYPC,&DEVTAB,&ERRTAB,&STATAB,&L*
               CHN,&DEVNDX,&TYPEA,&ALOCCNT,&SYSRES,&SQOPT,&OFF,,,1'
         MEND
