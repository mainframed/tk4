         MACRO
         EDTGEN &DSN=SYS1.MLPALIB,&OBJDSN=SYS1.OBJDEV,                 X
               &DSNVOL=(SYSRES,3330),&ID=EDTGEN,&CHECK=YES,            X
               &DIAGNOS=YES,&OCLASS=A,&JCLASS=A
.**********************************************************************
.*
.* MODULE NAME - EDTGEN
.*
.* DESCRIPTIVE NAME - ALLOCATION TABLE MACRO
.*
.* COPYRIGHT - NONE
.*
.* STATUS - OS/VS2 MVS SYSTEM CONTROL PROGRAM 2                @G64QPSJ
.*
.* FUNCTION -
.*       THE EDTGEN MACRO IS USED TO GENERATE THE STAGE II JOBSTREAM
.*       TO BUILD THE ALLOCATION DEVICE TABLES.  THIS INCLUDES
.*       THE INPUT AND OUTPUT CLASSES FOR THE STAGE II JOB STREAM,
.*       AND THE DESTINATION DATA SET FOR THE NEW TABLES.
.*
.* OPERATION -
.*       . VERIFY THAT NEITHER THE EDTGEN NOR GENERATE MACROS
.*         HAVE BEEN INVOKED PREVIOUSLY.
.*
.*       . VERIFY THE INPUT PARAMETERS SPECIFIED BY THE USER.
.*
.*       . INITIALIZE THE INTERNAL PARAMETERS REQUIRED FOR
.*         SUCCESSFUL OPERATION OF THE INTERNAL MACROS.
.*
.*       . INVOKE THE INTERNAL MACROS TO BUILD THE ASSEMBLY AND
.*         LINKEDIT STEPS FOR THE ALLOCATION TABLES.
.*
.* NOTES - N/A
.*
.* DEPENDENCIES -
.*            . THE SGIEI1IO INTERNAL MACRO DOES NOT REQUIRE ANY
.*              INPUT EXCEPT:
.*                    CHANNEL
.*                    IODEVICE
.*                    UNITNAME
.*
.* MODULE TYPE - MACRO
.*
.* ENTRY POINT - N/A
.*
.* INPUT - THE FOLLOWING MAY BE SPECIFIED AS PARAMETERS TO EDTGEN
.*           . &DSN      - OUTPUT DATA SET FOR NEW ALLOCATION TABLES
.*           . &DSNVOL   - SERIAL NUMBER & UNIT TYPE OF &DSN DATA SET
.*           . &OBJDSN   - PARTITIONED DATA SET TO BE USED FOR THE
.*                         STORAGE OF OBJECT MODULES ASSEMBLED DURING
.*                         STAGE II.
.*           . &ID       - USER ID FOR EDT
.*           . &CHECK    - VERIFICATION STEP REQUESTED
.*           . &DIAGNOS  - DIAGNOSTIC OVERRIDE REQUESTED
.*           . &JCLASS   - JOB CLASS FOR STAGE II
.*           . &OCLASS   - OUTPUT CLASS FOR STAGE II
.*
.* OUTPUT -
.*         THE OUTPUT FROM EDTGEN CONSISTS OF A JOB CALLED A STAGE II
.*         INPUT DECK. WHEN THIS JOB IS RUN, A NEW SET OF ALLOCATION
.*         TABLES IS CREATED.
.*
.* EXIT, NORMAL -
.*   CONDITION - NO ERRORS DETECTED OR DIAGNOSTICE OVERRIDE SPECIFIED
.*   OUTPUT    - INFORMATIONAL MNOTES AND THE STAGE II JOB TO ASSEMBLE
.*               AND LINKEDIT THE ALLOCATION TABLES.
.*               MNOTE IEIEDT013 ISSUED IF DIAGNOSTIC OVERRIDE
.*               REQUESTED AND ERRORS ENCOUNTERED.
.*
.* EXIT, ERROR -
.*   CONDITION - EDTGEN MACRO PREVIOUSLY INVOKED
.*   OUTPUT    - MNOTE IEIEDT000 ISSUED AND MACRO TERMINATED
.*               UNLESS DIAGNOSTIC OVERRIDE SPECIFIED
.*
.* EXIT, ERROR -
.*   CONDITION - GENERATE MACRO PREVIOUSLY INVOKED
.*   OUTPUT    - MNOTE IEIEDT001 ISSUED AND MACRO TERMINATED
.*               UNLESS DIAGNOSTIC OVERRIDE SPECIFIED
.*
.* EXIT, ERROR -
.*   CONDITION - &DSN PARAMETER LENGTH GREATER THAN 17
.*               OR NOT INDEXED.
.*   OUTPUT    - MNOTE IEIEDT002 ISSUED AND MACRO TERMINATED
.*               UNLESS DIAGNOSTIC OVERRIDE SPECIFIED
.*
.* EXIT, ERROR -
.*   CONDITION - &DSNVOL VOLUME PARAMETER LENGTH IS GREATER THAN 6
.*   OUTPUT    - MNOTE IEIEDT003 ISSUED AND MACRO TERMINATED
.*               UNLESS DIAGNOSTIC OVERRIDE SPECIFIED
.*
.* EXIT, ERROR -
.*   CONDITION - &DSNVOL UNIT PARAMETER IS NOT DIRECT ACCESS DEVICE
.*   OUTPUT    - MNOTE IEIEDT004 ISSUED AND MACRO TERMINATED
.*               UNLESS DIAGNOSTIC OVERRIDE SPECIFIED
.*
.* EXIT, ERROR -
.*   CONDITION - &OBJDSN PARAMETER LENGTH IS LESS THAN 6 OR GREATER
.*                   THAN 13 CHARACTERS
.*               OR DOES NOT BEGIN WITH "SYS1."
.*   OUTPUT    - MNOTE IEIEDT005 ISSUED AND MACRO TERMINATED
.*               UNLESS DIAGNOSTIC OVERRIDE SPECIFIED
.*
.* EXIT, ERROR -
.*   CONDITION - &CHECK PARAMETER NOT "YES" OR "NO"
.*   OUTPUT    - MNOTE IEIEDT006 ISSUED.  DEFAULT OF YES
.*               ASSUMED AND PROCESSING CONTINUES
.*
.* EXIT, ERROR -
.*   CONDITION - &OCLASS PARAMETER IS NOT A-Z OR 0-9
.*   OUTPUT    - MNOTE IEIEDT008 ISSUED AND MACRO TERMINATED
.*               UNLESS DIAGNOSTIC OVERRIDE SPECIFIED
.*
.* EXIT, ERROR -
.*   CONDITION - &JCLASS PARAMETER IS NOT A-Z OR 0-9
.*   OUTPUT    - MNOTE IEIEDT009 ISSUED AND MACRO TERMINATED
.*               UNLESS DIAGNOSTIC OVERRIDE SPECIFIED
.*
.* EXIT, ERROR -
.*   CONDITION - &DIAGNOS PARAMETER NOT "YES" OR "NO"
.*   OUTPUT    - MNOTE IEIEDT010 ISSUED.  DEFAULT OF YES
.*               ASSUMED AND PROCESSING CONTINUES
.*
.* EXIT, ERROR -
.*   CONDITION - NO DIAGNOSTIC OVERRIDE SPECIFIED AND QUIT
.*               SWITCH SET BEFORE EDTGEN INVOKED
.*   OUTPUT    - MNOTE IEIEDT011 ISSUED AND MACRO TERMINATED
.*
.* EXIT, ERROR -
.*   CONDITION - NO DIAGNOSTIC OVERRIDE SPECIFIED AND QUIT
.*               SWITCH SET WITHIN EDTGEN
.*   OUTPUT    - MNOTE IEIEDT012 ISSUED AND MACRO TERMINATED
.*
.* EXTERNAL REFERENCES - SEE MACROS
.*
.* TABLES - SYSGEN GLOBAL DATA AREA (SGGBLPAK)
.*
.* MACROS -
.*         SGGBLPAK - MACRO GLOBAL DATA AREA
.*         SGIEI1IO - VALIDATES IO DEVICES
.*         SGSETGPT - INITIALIZES PREFERENCE TABLE
.*         JOBCARD  - GENERATES INSTALLATION JOB CARD
.*         SGIEF213 - BUILDS ASSEMBLY STEPS FOR ALLOCATION TABLES
.*
.* CHANGE ACTIVITY - G64QPSJ
.*
.* MESSAGES (MNOTES) -
.*       IEIEDT000 EDTGEN MACRO PREVIOUSLY INVOKED
.*       IEIEDT001 GENERATE MACRO PREVIOUSLY INVOKED
.*       IEIEDT002 DATA SET NAME XXXXXXXX ON DSN PARAMETER INVALID
.*                                          - REASON CODE
.*       IEIEDT003 VOLUME SERIAL XXXXXX ON DSNVOL PARAMETER INVALID
.*       IEIEDT004 DEVICE TYPE XXXXXXXX ON DSNVOL PARAMETER INVALID
.*       IEIEDT005 DATA SET NAME XXXXXXXX ON OBJDSN PARAMETER INVALID
.*                                          - REASON CODE
.*       IEIEDT006 CHECK PARAMETER XXX INVALID
.*       IEIEDT008 OCLASS PARAMETER X INVALID
.*       IEIEDT009 JCLASS PARAMETER X INVALID
.*       IEIEDT010 DIAGNOS PARAMETER XXX INVALID
.*       IEIEDT011 QUIT SWITCH SET PRIOR TO EDTGEN MACRO
.*       IEIEDT012 QUIT SWITCH SET IN EDTGEN MACRO
.*       IEIEDT013 DIAGNOSTIC OVERRIDE SPECIFIED
.*                 ERROR MESSAGES ARE INTERPRETED
.*                 AS WARNINGS
.*
.* SYSGEN - THIS MACRO RESIDES IN SYS1.AGENLIB
.*
.**********************************************************************
.*A 000000-999999                                              @G64QPSJ
.**********************************************************************
.*                         ***** MACRO VARIABLES *****                *
.**********************************************************************
         COPY  SGGBLPAK                   SYSGEN GLOBAL DATA AREA
         LCLA  &I                         LOCAL INDEX COUNTER
         LCLB  &QUITONB                   SAVE FOR GLOBAL QUIT SWITCH
         LCLC  &UCBTYPE                   WORK AREA FOR DEVICE CLASS
         LCLC  &RC                        RETURN CODE VALUE
         LCLC  &IDTEMP                    WORK AREA FOR USER ID
.**********************************************************************
.*                         ***** ENTRY *****                          *
.**********************************************************************
         AIF   (&SGMENTB(84)).A0          EDTGEN PREVIOUSLY ENTERED?
&SGMENTB(84)   SETB 1                     SET EDTGEN ENTRY SWITCH
&QUITONB       SETB (&SGQUITB)            SAVE GLOBAL QUIT SWITCH VALUE
&SGQUITB SETB  0                          TURN OFF GLOBAL QUIT SWITCH
         AGO   .A1                        CHECK GENERATE IF ENTERED
.A0      MNOTE 5,'* * * IEIEDT000 EDTGEN MACRO PREVIOUSLY INVOKED'
&SGQUITB SETB  1                          SET QUIT SWITCH
         AGO   .EXIT                      EXIT MACRO
.A1      AIF   (&SGMENTB(88)).A2          GENERATE PREVIOUSLY ENTERED?
&SGMENTB(88)   SETB 1                     SET GENERATE ENTRY SWITCH
         AGO   .A3                        CHECK DSN PARAMETER
.A2      MNOTE 5,'* * * IEIEDT001 GENERATE MACRO PREVIOUSLY INVOKED'
&SGQUITB SETB  1                          SET QUIT SWITCH
         AGO   .EXIT                      EXIT MACRO
.A3      ANOP
         MNOTE *,'  ALLOCATION TABLE MACRO'
         MNOTE *,'     REPLACES GENERATE MACRO'
.**********************************************************************
.*                         ***** DSN *****                            *
.**********************************************************************
.B0      ANOP
&RC      SETC  '1'                        INIT WRONG LENGTH RC
         AIF   (K'&DSN GT 17 OR K'&DSN LT 3).B2   LENGTH INVALID?
&RC      SETC  '2'                        INIT NOT INDEXED RC
&I       SETA  1                          INIT INDEX FOR SCAN
.B1      ANOP
&I       SETA  &I+1                       INCREMENT INDEX
         AIF   (&I GT 9).B2               IF END, THEN ERROR
         AIF   ('&DSN'(&I,1) NE '.').B1   IF NOT INDEXED, CONT SCAN
&SGCTRLC(40)  SETC  '&DSN'(1,&I-1)        SET PREFIX VALUE
&SGCTRLC(9)   SETC  '&DSN'(&I+1,17)       SET BASE NAME VALUE
         AGO   .C0                        CHECK DSNVOL PARAMETER
.B2      MNOTE 5,'* * * IEIEDT002 DATA SET NAME &DSN ON DSN PARAMETER IX
               NVALID - &RC'
&SGQUITB SETB  1                          SET QUIT SWITCH
.**********************************************************************
.*                         ***** DSNVOL *****                         *
.**********************************************************************
.C0      ANOP
.*   CHECK FOR VALID VOLUME SERIAL
         AIF   (T'&DSNVOL(1) NE 'O').C1   IF NO VOL SER SPECIFIED THEN
&SGCTRLC(7)  SETC 'SYSRES'                SET DEFAULT VOL SER
         AGO   .C2                        PROCESS UNIT TYPE
.C1      AIF   (K'&DSNVOL(1) LE 6).C2     LENGTH VALID?
         MNOTE 5,'* * * IEIEDT003 VOLUME SERIAL &DSNVOL(1) ON DSNVOL PAX
               RAMETER INVALID'
&SGQUITB SETB  1                          SET QUIT SWITCH
.C2      ANOP
&SGCTRLC(7)  SETC '&DSNVOL(1)'            COPY SPECIFIED VOL SER
.*   CHECK FOR VALID DIRECT ACCESS DEVICE TYPE
.*   BY SCANNING THE DEVICE INFORMATION TABLE(DIT)
.*   FOR A MATCHING GENERIC THAT IS DEFINED TO BE
.*   DIRECT ACCESS.
         AIF   (T'&DSNVOL(2) NE 'O').C3   IF NO UNIT SPECIFIED,
&SGCTRLC(8)  SETC '3330'                  SET DEFAULT UNIT TYPE
         AGO   .C6                        ISSUE INFO MNOTE
.C3      ANOP
&SGCTRLC(8)  SETC '&DSNVOL(2)'            SAVE UNIT VALUE
&I       SETA  1                          SET INDEX TO 1
.C31     AIF   (&I GT &SGDITDA).C5        IF NO MORE ENTRIES, ERROR
         AIF   ('&SGGNRIC(&I)' NE '&DSNVOL(2)').C32
&UCBTYPE SETC  '&SGDTYPC(&I)'(5,2)        OBTAIN DEVICE CLASS
         AIF   ('&UCBTYPE' EQ '20').C6    IF DA UNIT, CONTINUE
         AGO   .C5                        ISSUE ERROR MNOTE
.C32     ANOP
&I       SETA  &I+1                       INCREMENT INDEX
         AGO   .C31                       TRY NEXT DEVICE ENTRY
.C5      MNOTE 5,'* * * IEIEDT004 DEVICE TYPE &DSNVOL(2) ON DSNVOL PARAX
               METER INVALID'
&SGQUITB SETB  1                          SET QUIT SWITCH
.C6      ANOP
         MNOTE *,'  ALLOCATION TABLES WILL RESIDE IN &DSN ON &SGCTRLC(7X
               ) UNIT &SGCTRLC(8)'
.**********************************************************************
.*                         ***** OBJDSN *****                         *
.**********************************************************************
.D0      ANOP
&RC      SETC  '1'                        INIT WRONG LENGTH RC
         AIF   (K'&OBJDSN LT 6 OR K'&OBJDSN GT 13).D1 LENGTH INVALID?
&RC      SETC  '2'                        INIT BAD PREFIX RC
         AIF   ('&OBJDSN'(1,5) NE 'SYS1.').D1  PREFIX INVALID?
&SGCTRLC(5)    SETC '&OBJDSN'(6,8)        SET UP OBJDSN
         AGO   .E0                        CHECK OCLASS PARAMETER
.D1      MNOTE 5,'* * * IEIEDT005 DATA SET NAME &OBJDSN ON OBJDSN PARAMX
               ETER INVALID - &RC'
&SGQUITB SETB  1                          SET QUIT SWITCH
.**********************************************************************
.*                         ***** OCLASS *****                         *
.**********************************************************************
.E0  AIF (('&OCLASS' LT 'A') OR ('&OCLASS' GT 'I' AND '&OCLASS' LT 'J')X
               OR ('&OCLASS' GT 'R' AND '&OCLASS' LT 'S') OR ('&OCLASS'X
               GT 'Z' AND '&OCLASS' LT '0') OR ('&OCLASS' GT '9')).E1
&SGCTRLC(41)   SETC  '&OCLASS'            SET OCLASS PARAMETER
         AGO   .F0                        CHECK JCLASS PARM
.E1      MNOTE 5,'* * * IEIEDT008 OCLASS PARAMETER &OCLASS INVALID'
&SGQUITB SETB  1                          SET QUIT SWITCH
.**********************************************************************
.*                         ***** JCLASS *****                         *
.**********************************************************************
.F0  AIF (('&JCLASS' LT 'A') OR ('&JCLASS' GT 'I' AND '&JCLASS' LT 'J')X
               OR ('&JCLASS' GT 'R' AND '&JCLASS' LT 'S') OR ('&JCLASS'X
               GT 'Z' AND '&JCLASS' LT '0') OR ('&JCLASS' GT '9')).F1
&SGCTRLC(42)   SETC  '&JCLASS'            SET JCLASS PARAMETER
         AGO   .G0                        CHECK DIAGNOSE PARAMETER
.F1      MNOTE 5,'* * * IEIEDT009 JCLASS PARAMETER &JCLASS INVALID'
&SGQUITB SETB  1                          SET QUIT SWITCH
.**********************************************************************
.*                         ***** DIAGNOS *****                        *
.**********************************************************************
.G0      AIF   ('&DIAGNOS' EQ 'NO').H0    IF NO SPECIFIED, CONTINUE
         AIF   ('&DIAGNOS' EQ 'YES').G1   IF YES SPECIFIED, SET SWITCH
         MNOTE 5,'* * * IEIEDT010 DIAGNOS PARAMETER &DIAGNOS INVALID'
.G1      ANOP
&SGMENTB(16) SETB 1                       SET DIAGNOSTIC OVERRIDE ON
.**********************************************************************
.*                         ***** INTERNAL MACROS *****                *
.**********************************************************************
.H0      ANOP
&SGACTR  SETA  600000                     SET ACTR COUNTER
*** SGIEI1IO *** SYSGEN                                   *** SC1S1 ***
         SGIEI1IO
*** SGSETGPT *** SYSGEN                                   *** SC1S1 ***
         SGSETGPT
         AIF   (&SGMENTB(16)).H2          OVERRIDE SWITCH?
         AIF   (&QUITONB).I1              QUIT SWITCH BEFORE EDTGEN?
         AIF   (&SGQUITB).I2              QUIT SWITCH SET IN EDTGEN?
         AGO   .H4
.H2      AIF   (NOT(&QUITONB OR &SGQUITB)).H4
         MNOTE 7,'* * * IEIEDT013 DIAGNOSTIC OVERRIDE SPECIFIED'
         MNOTE 7,'* * *           ERROR MESSAGES ARE INTERPRETED'
         MNOTE 7,'* * *           AS WARNINGS'
.H4      ANOP
&SGQUITB SETB  0                    TURN OFF QUIT SWITCH TO FORCE
.*                                  COMPLETE TABLE GENERATION
&SGCPRGB(8)    SETB  1              INDICATE VS2-2
&IDTEMP  SETC  '&ID'(1,8)           TRUNCATE USER ID TO 8 CHARACTERS
.**********************************************************************
.*
.*          CALL JOBCARD MACRO FOR ALLOCATION JOB
.*
.**********************************************************************
         JOBCARD
*** SGIEF213 *** SCHEDULER ALLOCATION TABLE ASSEMBLIES    *** SC1S5 ***
         SGIEF213  ID=&IDTEMP
*** SCHEDULER ALLOCATION TABLE LINKEDITS
&SGCTRLC(1)    SETC  ',LIST'        DEFAULT LIST LKED OPTION
&SGCTRLC(2)    SETC  ',XREF'        DEFAULT XREF LKED OPTION
&SGCTRLC(6)    SETC  ''             SET NULL PROGRAM NAME
&SGCTRLC(10)   SETC  ',LET'         DEFAULT LET LKED OPTION
&SGCTRLC(11)   SETC  ',RENT'        DEFAULT RENT LKED OPTION
         COPY  SGLEDPK1
         COPY  SGLEDPK2
         PUNCH '     INCLUDE SYSPUNCH(IEFWMAS1)'
         PUNCH '  NAME DEVNAMET(R)'
         PUNCH '     INCLUDE SYSPUNCH(IEFDEVPT)'
         PUNCH '  NAME IEFDEVPT(R)'
         PUNCH '     INCLUDE SYSPUNCH(IEFWMSKA)'
         PUNCH '  NAME DEVMASKT(R)'
         PUNCH '     INCLUDE SYSPUNCH(IEFEDTTB)'
         PUNCH '  ENTRY EDTTAB'
         PUNCH '  NAME IEFEDTTB(R)'
         PUNCH '/*'
         AIF   ('&CHECK' EQ 'NO').EXIT    VERIFICATION REQUESTED?
&SGCTRLA(1)  SETA  &SGCTRLA(1)+1          INCREMENT STEP INDEX
 PUNCH '//SG&SGCTRLA(1)      EXEC  PGM=IEFEB400'
 PUNCH '//SYSPRINT DD  SYSOUT=&SGCTRLC(41)'
 PUNCH '//EDT      DD  VOL=SER=&SGCTRLC(7),UNIT=&SGCTRLC(8),DISP=SHR,'
 PUNCH '//             DSN=&DSN'
 PUNCH '//SYSABEND DD  SYSOUT=&SGCTRLC(41)'
         AGO   .EXIT
.**********************************************************************
.*        QUIT SWITCH MESSAGE PROCESSING
.**********************************************************************
.I1      ANOP
        MNOTE 7,'* * * IEIEDT011 QUIT SWITCH SET PRIOR TO EDTGEN MACRO'
         AGO   .EXIT
.I2      ANOP
         MNOTE 7,'* * * IEIEDT012 QUIT SWITCH SET IN EDTGEN MACRO'
.EXIT    ANOP
         MEND
