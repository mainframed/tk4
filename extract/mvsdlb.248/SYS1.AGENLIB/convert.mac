         MACRO
         CONVERT &VALUE=,&DIGITS=,&TO=
         COPY  SGSYSPAK
       LCLA  &A,&B,&C(8),&D
         LCLC  &CA
.***** START OF SPECIFICATIONS **************************************
.*
.* MODULE NAME - CONVERT
.*
.* DESCRIPTIVE NAME - CONVERT
.*
.* COPYRIGHT - NONE
.*
.* STATUS - CHANGE LEVEL 02
.*
.* FUNCTION -
.*       THE CONVERT MACRO IS USED TO CHANGE A VALUE FROM HEX OR EBCDIC
.*       TO ITS OPPOSITE VALUE.
.*
.* NOTES - N/A
.*
.* MODULE TYPE - MACRO
.*
.* ENTRY POINT - N/A
.*
.* INPUT - THE FOLLOWING PARAMETERS ARE ALLOWED ON CONVERT
.*             &VALUE   - SPECIFIES A VALUE TO BE CONVERTED TO HEX OR
.*                        EBCDIC.
.*             &DIGITS  - NUMBER OF EBCDIC CHARACTERS
.*             &TO      - SPECIFIES THE TYPE OF CONVERSION TO BE DONE
.*
.* OUTPUT -
.*       GLOBALS USED ARE -
.*             &EBCDIC      - THE VALUE IN EBCDIC
.*             &HEX         - THE VALUE IN HEX
.*             &SGQUITB     - QUIT SWITCH
.*
.*       LOCALS USED ARE -
.*             &A      - SET TO &VALUE AND MINIPULATED ACCORDINGLY
.*             &B      - SET TO NUMBER OF DIGITS OR USED TO COUNT VALUE
.*             &C(8)   - GLOBAL USED TO CONVERT &VALUE TO EBCDIC
.*             &CA     - SET TO &VALUE WHEN &TO=HEX
.*             &D      - GLOBAL USED TO CONVERT &VALUE TO EBCDIC
.*
.* EXIT, NORMAL - N/A
.*
.* EXIT, ERROR - N/A
.*
.* EXTERNAL REFERENCES - N/A
.*
.* TABLES - N/A
.*
.* MACROS - NONE
.*
.* CHANGE ACTIVITY -
.*
.****** END OF SPECIFICATIONS ***************************************
&EBCDIC  SETC   ''
&HEX     SETA  0
         AIF   ('&TO' EQ 'HEX').TOHEX
         AIF   ('&TO' NE 'EBCDIC').E1   'TO' VALUE INVALID
&A       SETA  &VALUE
&B   SETA  &DIGITS+1
&D       SETA  &A/65536            SHIFT RIGHT 16 POSITIONS.
&C(8)    SETA  &D/4096             SHIFT RIGHT 12 POSITIONS.
&C(7)    SETA  &D/256-16*&C(8)  SHIFT D RIGHT 8 & C LEFT 4-GET 4 ZEROS.
&C(6)    SETA  &D/16-&D/256*16  SHIFT D RIGHT 8 & 4MORE TO GET 4 0'S.
&C(5)    SETA  &D-&D/16*16
&C(4)    SETA  &A/4096-16*&D
&C(3)    SETA  &A/256-&A/4096*16
&C(2)    SETA  &A/16-&A/256*16
&C(1)    SETA  &A-&A/16*16
.BACK    AIF   (&B EQ 1).MEND
&B       SETA  &B-1
&EBCDIC  SETC  '&EBCDIC'.'0123456789ABCDEF'(&C(&B)+1,1)
         AGO   .BACK
.TOHEX   AIF   (&B EQ K'&VALUE).MEND
&B       SETA  &B+1
&CA      SETC  '&VALUE'(&B,1)
         AIF   ('&CA' GE '0').ZERTO9X
         AIF   ('&CA' EQ 'A').AX
         AIF   ('&CA' EQ 'B').BX
         AIF   ('&CA' EQ 'C').CX
         AIF   ('&CA' EQ 'D').DX
         AIF   ('&CA' EQ 'E').EX
         AIF   ('&CA' EQ 'F').FX
         AGO   .E2                      NOT CONVERTIBLE
.ZERTO9X ANOP
&HEX     SETA  &HEX*16+&CA
         AGO   .TOHEX
.AX      ANOP
&HEX     SETA  &HEX*16+X'A'
         AGO   .TOHEX
.BX      ANOP
&HEX     SETA  &HEX*16+X'B'
         AGO   .TOHEX
.CX      ANOP
&HEX     SETA  &HEX*16+X'C'
         AGO   .TOHEX
.DX      ANOP
&HEX     SETA  &HEX*16+X'D'
         AGO   .TOHEX
.EX      ANOP
&HEX     SETA  &HEX*16+X'E'
         AGO   .TOHEX
.FX      ANOP
&HEX     SETA  &HEX*16+X'F'
         AGO   .TOHEX
.E1 MNOTE 5,'* * * IEICON001 ''TO=&TO'' VALUE INVALID'
         AGO   .BEND
.E2 MNOTE 5,'* * * IEICON002 ''VALUE=&VALUE'' NOT CONVERTIBLE'
.BEND    ANOP
&SGQUITB SETB  1
.MEND  MEND
