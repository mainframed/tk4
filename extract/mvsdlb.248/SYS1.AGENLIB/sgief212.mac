         MACRO
         SGIEF212
.*************************************************************
.*
.*  MACRO    -  SGIEF212
.*
.*  STATUS   -  VS2 SYSTEM CONTROL PROGRAM 2                   @G64IPSJ
.*
.*  FUNCTION - THIS MACRO REFORMATS THE ADDR BOXES(&SGDADRA)
.*             FOR USE BY OTHER SCHEDULER SYSGEN MACROS.  IT
.*             ALSO PUNCHES THE SGIEF010 MACRO CALL SO THAT
.*             STAGE II CAN ASSOCIATE DEVICE ID'S WITH THE
.*             GENERIC NAME AND DEVTYPE.
.*
.*  OPERATION - DATA CONCERNING DEVICE SPECIFIED IS STORED
.*              IN SYSGEN GOBAL VARIABLES.  THIS MACRO
.*              OBTAINS THAT DATA AND PUTS IN INTO THE ADDR
.*              BOXES FOR DEVICE TABLE PROCESSING.  THIS
.*              ROUTINE THEN SCANS THE DEVICE INFORMATION
.*              TABLES(DIT) AND PUNCHES THE CALL FOR MACRO
.*              SGIEF010.  THIS MACRO IS ENTERED THREE TIMES
.*              FOR DIFFERENT STAGE II ASSEMBLY STEPS BEING
.*              BUILT.  THE ADDR BOXES ARE REFORMATED ONLY
.*              ONCE.
.*
.*  ENTRY POINT - SGIEF212 FROM SGIEF211 AND SGIEF213
.*
.*  EXIT - TO CALLER
.*
.*  EXTERNAL REFERENCES - SGGBLPAK - GLOBAL DATA AREA
.*
.*  OUTPUT - &SGDADRA - REFORMATED
.*           &SGCNTRA(8) - COUNT OF UNIQUE DEVICE TYPES SYSGENED
.*           &SGNCTRA(9) - COUNT OF SUPPORTED DEVICES SYSGENED
.*           &SGCNTRA(10) - COUNT OF UNIQUE DEVICE TYPES POSSIBLE
.*
.*  CHANGE LEVEL - REWRITTEN - DEVICE INDEPENDENT RESTRUCTURE  @G64IPSJ
.*                 VS49151                                     @VS49151
.*
.*  MNOTES - NONE
.*
.*************************************************************
.*  ACCESS GLOBAL DATA AREA
         COPY  SGGBLPAK
.*  DEFINE LOCAL VARIABLES
         LCLA  &A,&SWCHPTR
         LCLB  &SPECDEV,&DEV2305
         LCLC  &HLFLIST,&TYPLIST,&WORKSTR
.*  SET ACTR VALUE = 1 + 7*#DEVICES + 3*#DIT ENTRIES
         ACTR  14020
.*************************************************************
.*
.*   THIS SECTION OF CODE REFORMATS THE ADDR BOXES(&SGDADRA)
.*   FOR USE IN PROCESSING THE DEVICE ALLOCATION TABLES.   IT
.*   IS DONE ONLY ON THE FIRST CALL - &SGMENTB(83) = '0'B.
.*   THE ADDR BOX ORIGINALLY CONTAINS THE DEVICE ADDRESS IN
.*   HEX(RIGHT-HAND 3 DIGITS).  AFTER PROCESSING, THE ADDR BOX
.*   HAS THE FOLLOWING FORMAT:
.*             HEX ADDRESS  -  BITS  1-12
.*             ZERO'S       -  BITS 12-22
.*             DEVICE ID    -  BITS 13-30
.*             VALID DEVTYP -  BIT  31
.*      NOTE:
.*           ADDR BOX BITS ARE NUMBERED RIGHT TO LEFT
.*           STARTING WITH NUMBER 1.
.*
.*************************************************************
         AIF   (&SGMENTB(83)).NOFRMT   IF ADDR BOXES FORMATED
.*                                       ALREADY, CONTINUE
&SGMENTB(83) SETB 1                    INDICATE ADDR BOXES
.*                                       FORMATED
&A       SETA  0                       INITIALIZE ADDR BOX
.*                                       INDEX
&SGCNTRA(10) SETA 0                    INITIALIZE COUNT OF UNIQUE
.*                                       DEVICE TYPES
.NXTADDR ANOP                          LOOP TO PROCESS A BOX
&A       SETA  &A+1                    INCREMENT DEVICE INDEX
         AIF   (&A GT &SGCNTRA(3) OR &SGCNTRA(9) GT &SGCNTRA(3)).NOFRMT
.*                                     IF NO MORE, EXIT
&SWCHPTR SETA  &SGDASPA(&A)            OBTAIN SWITCH INDEX     @VS49151
         AIF   (&SGCNTRA(10) GT &SGDITXA(&SWCHPTR)).CONT       @VS49151
.*                                     IF STILL GREATEST, CONTINUE
&SGCNTRA(10)  SETA  &SGDITXA(&SWCHPTR) SAVE LARGER VALUE       @VS49151
.CONT    ANOP
         AIF   (&SGDUMMY(&SWCHPTR)).NXTADDR  IF DUMMY, DON'T PROCESS
.*                                                             @VS49151
&SGDADRA(&A) SETA &SGDADRA(&A)-&SGDADRA(&A)/X'1000'*X'1000'+&SGDITXA(&SX
               WCHPTR)*X'400000'+X'40000000' SET ADDR BOX TO   @VS49151
.*                                       ADDRESS - BITS 1-12
.*                                       0'S     - BITS 13-22
.*                                       ID      - BITS 23-30
.*                                       VALID   - BIT 31
&SGCNTRA(9) SETA &SGCNTRA(9)+1         INCREMENT NUMBER OF
.*                                     SUPPORTED DEVICES
&DEV2305 SETB  (&SGDT03B(&SWCHPTR) AND &SGDU08B(&SWCHPTR))
.*                                     SET 2305 INDICATOR
         AIF   (NOT &DEV2305).NOT2305  IF 2305 THEN
&A       SETA  &A+7                    INCREMENT AROUND REST
.*                                     OF 2305 ADDRESSES
         AGO   .NOTSPEC                BYPASS OTHER CHECKS
.NOT2305 ANOP                          ELSE
         AIF   (NOT &SGDT06B(&SWCHPTR)).NOTSPEC     IF NOT A
.*                                     SPECIAL DEVICE CLASS,
.*                                     BYPASS CHECKS
&SPECDEV SETB  (&SGDU04B(&SWCHPTR) OR &SGDU05B(&SWCHPTR))
.*                                     SET ON IF 1419 OR 1275
         AIF   (NOT &SPECDEV).NOTSPEC  IF SPECIAL DEVICE THEN
&A       SETA  &A+1                    INCREMENT COUNTER
.NOTSPEC ANOP
         AGO   .NXTADDR                PROCESS NEXT ADDR BOX
.NOFRMT  ANOP                          EXIT FOR ADDR BOX
.*                                       FORMATING
.*************************************************************
.*
.*   THIS SECTION OF CODE PUNCHES THE SGIEF010 MACRO CALL.
.*   THIS MACRO ASSOCIATES THE DEVICE ID(&SGDITXA) WITH
.*   THE GENERIC NAME(&SGGNRIC) AND DEVICE TYPE(&SGDTYPC).
.*   AS A BY PRODUCT OF THIS PROCESSING, THE NUBMER OF
.*   UNIQUE DEVICE TYPES SYSGENED IS ACCUMULATED.
.*
.*************************************************************
&A       SETA  0                       INITIALIZE DIT INDEX
&SGCNTRA(8) SETA 0                     INITIALIZE UNIQUE
.*                                       DEVTYPE COUNTER
         PUNCH '         SGIEF010                                      X
                               X'
.*                                      PUNCH MACRO CALL
.NEXTYPE ANOP                           LOOP TO PROCESS DIT
&A       SETA  &A+1                     INCREMENT DIT INDEX
         AIF   (&A GT &SGDITDA).NOMORE IF NO MORE ENTRIES,
.*                                       EXIT LOOP
         AIF   (NOT &SGGENDB(&A)).NEXTYPE   IF ENTRY NOT
.*                                          SYSGENED, CONTUNUE
.*                                          WITH NEXT ENTRY
&SGCNTRA(8) SETA &SGCNTRA(8)+1          INCREMENT UNIQUE
.*                                        DEVTYPE COUNTER
&WORKSTR SETC  '&SGDTYPC(&A)'          MOVE TO WORK AREA
&TYPLIST SETC  '&WORKSTR'(1,4)         FILL FIRST HALF OF
.*                                       DEVTYPE
&HLFLIST SETC  '&WORKSTR'(5,4)         FILL SECOND HALF OF
.*                                       DEVTYPE
         PUNCH '               &A,&SGGNRIC(&A),X''&TYPLIST'',X''&HLFLISX
               T'',                           XXXXXXXX'
.*                                     PUNCH OPERANDS FOR
.*                                       THIS DEVICE TYPE
         AGO   .NEXTYPE                PROCESS NEXT ENTRY
.NOMORE ANOP                           EXIT FOR SGIEF010 PROCESSING
.*
.*  END OF SGIEF010 MACRO PROCESSING
.*
        MEND
