         MACRO
         SGIFB600 &RESTYPE                                       S21021
         COPY  SGGBLPAK                                          S21021
         LCLA  &A,&B,&C,&D,&E,&F                                 S21021
         LCLA  &G
.*
.*          AOS EXECUTION OF DIP
.*
.* THIS MACRO ALLOCATES SPACE FOR THE SYS1.LOGREC DEPENDENT UPON S21021
.* THE DEVICE WHERE IT IS TO RESIDE.                             S21021
.*    -COMPLETE REPLACEMENT                                      S21021
&A       SETA  &SGCNTRA(3)+&SG2314A     .TOTAL STAT CTRS IN SYS  S21021
.START   AIF   ('&RESTYPE' EQ '2311').D2311    .SYSRES 2311      S21021
         AIF   ('&RESTYPE' EQ '2301').D2301    .SYSRES 2301      S21021
         AIF   ('&RESTYPE' EQ '2303').D2303    .SYSRES 2303      S21021
         AIF   ('&RESTYPE' EQ '2302').D2302    .SYSRES 2302      S21021
         AIF   ('&RESTYPE' EQ '2305-1').D23051 .SYSRES 2305 MOD 1S21021
         AIF   ('&RESTYPE' EQ '2305-2').D23052 .SYSRES 2305 MOD 2S21021
         AIF   ('&RESTYPE' EQ '2314').D2314    .SYSRES 2314      S21021
         AIF   ('&RESTYPE' EQ '3330').D3330    .SYSRES 3330      S21021
         AIF   ('&RESTYPE' EQ '3330-1').D33301 .SYSRES 3330-1   XL03145
         AIF   ('&RESTYPE' EQ '3340').D3340    .SYSRES 3340     XL03130
         AIF   ('&RESTYPE' EQ '3350').D3350    .SYSRES 3350    @X50RIVS
         AIF   ('&RESTYPE' EQ '3375').D3375 +3375 ??             MVS38J
         AIF   ('&RESTYPE' EQ '3380').D3380 +3380 ??             MVS38J
         AIF   ('&RESTYPE' EQ '3390').D3390 +3390 ??             MVS38J
.*
 MNOTE 5,'* * * IEIFFB600 DEVICE NOT FOUND'
&SGQUITB SETB  1                        .ABEND SYSGEN            S21021
         MEXIT                                                   S21021
.D3375   ANOP                             +TRACK ALLOC PRMS      MVS38J
         MNOTE *,'SGIFB600 - D3375 DIP probably wrong - MVS38J'  MVS38J
&B       SETA  80                         + DEVICES/TRACK        MVS38J
&C       SETA  03                         + 125K                 MVS38J
&D       SETA  02                         + RMS/SER1             MVS38J
&G       SETA  03                         + FRAMES               MVS38J
         AGO   .LOOP                      +                      MVS38J
.D3390   ANOP                             +TRACK ALLOC PRMS      MVS38J
         MNOTE *,'SGIFB600 - D3390 probably wrong - MVS38J'      MVS38J
&B       SETA  80                         + DEVICES/TRACK        MVS38J
&C       SETA  03                         + 125K                 MVS38J
&D       SETA  02                         + RMS/SER1             MVS38J
&G       SETA  03                         + FRAMES               MVS38J
         AGO   .LOOP                      +                      MVS38J
.D3380   ANOP                             +TRACK ALLOC PRMS      MVS38J
&B       SETA  80                         + DEVICES/TRACK        MVS38J
&C       SETA  03                         + 125K                 MVS38J
&D       SETA  02                         + RMS/SER1             MVS38J
&G       SETA  03                         + FRAMES               MVS38J
         AGO   .LOOP                      +                      MVS38J
.D2311   ANOP                      .2311                         S21021
&B       SETA  25                       .DEVICES/TRACK           S21021
&C       SETA  35                       .TRACK ALLOC 125K        S21021
&D       SETA  17                       .TRACK ALLOC-RMS/SER1    S21021
&G       SETA  52                  .TRACK ALLOC-FRAMES
         AGO   .LOOP                             .CONTINUE       S21021
.D2301   ANOP                      .2301                         S21021
&B       SETA  85                       .DEVICES/TRACK           S21021
&C       SETA  6                        .TRACK ALLOC 125K        S21021
&D       SETA  4                        .TRACK ALLOC-RMS/SER1    S21021
&G       SETA  5                   .TRACK ALLOC-FRAMES
         AGO   .LOOP                             .CONTINUE       S21021
.D2303   ANOP                      .2303                         S21021
&B       SETA  25                       .DEVICES/TRACK           S21021
&C       SETA  26                       .TRACK ALLOC 125K        S21021
&D       SETA  17                       .TRACK ALLOC-RMS/SER1    S21021
&G       SETA  26                  .TRACK ALLOC-FRAMES
         AGO   .LOOP                             .CONTINUE       S21021
.D2302   ANOP                      .2302                         S21021
&B       SETA  35                       .DEVICES/TRACK           S21021
&C       SETA  25                       .TRACK ALLOC 125K        S21021
&D       SETA  17                       .TRACK ALLOC-RMS/SER1    S21021
&G       SETA  26                  .TRACK ALLOC-FRAMES
         AGO   .LOOP                             .CONTINUE       S21021
.D23051  ANOP                     .2305 MOD 1                    S21021
&B       SETA  20                       .DEVICE/TRACK
&C       SETA  10                       .TRACK ALLOC 125K        S21021
&D       SETA  5                        .TRACK ALLOC-RMS/SER1    S21021
&G       SETA  8                   .TRACK ALLOC-FRAMES
         AGO   .LOOP                             .CONTINUE       S21021
.D23052  ANOP                      .2305 MOD 2                   S21021
&B       SETA  40                       .DEVICES/TRACK           S21021
&C       SETA  10                       .TRACK ALLOC 125K        S21021
&D       SETA  5                        .TRACK ALLOC-RMS/SER1    S21021
&G       SETA  8                   .TRACK ALLOC-FRAMES
         AGO   .LOOP                             .CONTINUE       S21021
.D2314   ANOP                      .2314                         S21021
&B       SETA  30                       .DEVICES/TRACK           S21021
&C       SETA  17                       .TRACK ALLOC 125K        S21021
&D       SETA  11                       .TRACK ALLOC-RMS/SER1    S21021
&G       SETA  18                  .TRACK ALLOC-FRAMES
         AGO   .LOOP                             .CONTINUE       S21021
.D3330   ANOP                      .3330                         S21021
&B       SETA  50                       .DEVICES/TRACK           S21021
&C       SETA  10                       .TRACK ALLOC 125K        S21021
&D       SETA  5                        .TRACK ALLOC-RMS/SER1    S21021
&G       SETA  9                   .TRACK ALLOC-FRAMES
         AGO   .LOOP           .CONTINUE                         S21021
.D33301  ANOP                      .3330 MOD 1                  XL03145
&B       SETA  50                       .DEVICE/TRACK           XL03145
&C       SETA  10                       .TRACK ALLOC 125K       XL03145
&D       SETA  5                        .TRACK ALLOC-RMS/SER1   XL03145
&G       SETA  9                   .TRACK ALLOC-FRAMES
         AGO   .LOOP           .CONTINUE                        XL03145
.D3340   ANOP                      .3340                        XL03130
&B       SETA  30                       .DEVICE/TRACK           XL03130
&C       SETA  16                       .TRACK ALLOC 125K       XL03130
&D       SETA  9                        .TRACK ALLOC-RMS/SER1   XL03130
&G       SETA  13                  .TRACK ALLOC-FRAMES
         AGO   .LOOP           .CONTINUE                        XL03130
.D3350   ANOP                      .3350                       @X50RIVS
&B       SETA  60                       .DEV/TRK               @X50RIVS
&C       SETA  6                        .TRK ALLOC 125K        @X50RIVS
&D       SETA  4                        .TRK ALLOC-RMS/SER1    @X50RIVS
&G       SETA  6                   .TRACK ALLOC-FRAMES
         AGO   .LOOP           .CONTINUE                       @X50RIVS
.LOOP    ANOP                                                    S21021
&A       SETA  (&A+&B-1)/&B             .NUMBER TRKS FOR OVERFLOWS21021
&E       SETA  &SGBSCA/100                   .ONE TRACK/100 TERMSS21021
&A       SETA  &A+&C+&D+&E                   .TOTAL TRACKS       S21021
         AIF   (&SGCPRGB(4) EQ 1).NOTMVM .BRANCH IF NOT MVM     YL02021
&A       SETA  &A+&A                    .DOUBLE TRACK ALLOCATIONYL02021
.NOTMVM  ANOP                                                   YL02021
         AIF   (&SGDCLSB(44) EQ 0).NOSRF
&A       SETA  &A+&G
.NOSRF   ANOP
         AIF   (&SGDCLSB(28) EQ 0).NOTSS1    .NO SS/1 IN SYSTEM Y30LPDW
&A       SETA  &A+&D                         .ADD TRACKS - SS/1 Y30LPDW
.NOTSS1  ANOP                                                   Y30LPDW
&SGCTRLA(1) SETA &SGCTRLA(1)+1     .ADD ONE TO STEP NAME         S21021
 PUNCH '//SG&SGCTRLA(1) EXEC PGM=IFCDIP00,COND=(8,LT)'
 PUNCH '//SERERDS DD   DSN=&SGCTRLC(40)..LOGREC,DISP=(NEW,KEEP),'
 PUNCH '//             VOLUME=(,RETAIN,SER=&SGCTRLC(30)),'
 PUNCH '//             SPACE=(TRK,(&A),,CONTIG),UNIT=&SGCTRLC(29)'
 PUNCH '//*THE ABOVE STEP WILL INITIALIZE THE SYS1.LOGREC  '   @OZ58826
 PUNCH '//*DATA SET ON THE SYSTEM RESIDENCE VOLUME THAT IS '   @OZ58826
 PUNCH '//*THE TARGET OF THIS SYSGEN. AFTER IPL-ING THE    '   @OZ58826
 PUNCH '//*NEWLY GENERATED SYSTEM IT WILL BE NECESSARY TO  '   @OZ58826
 PUNCH '//*EXECUTE IFCDIP00 AND RE-INITIALIZE SYS1.LOGREC  '   @OZ58826
 PUNCH '//*TO ENSURE THAT THE LATEST MAINTENANCE LEVEL     '   @OZ58826
 PUNCH '//*SOFTWARE IS USED. ADDITIONAL INFORMATION AS TO  '   @OZ58826
 PUNCH '//*THE PARM=FRAMES AND FRAMES DD STATEMENTS        '   @OZ58826
 PUNCH '//*REQUIRED WHEN RUNNING ON A 303X TYPE CPU IS     '   @OZ58826
 PUNCH '//*CONTAINED IN THE OS/VS2 MVS SYSTEM PROGRAMMING  '   @OZ58826
 PUNCH '//*LIBRARY SYS1.LOGREC RECORDING MANUAL.           '   @OZ58826
&SGCTRLA(1) SETA &SGCTRLA(1)+1     .ADD ONE TO STEP NAME         S21021
         AIF   (&SGCPRGB(4) EQ 0).VS2   .BRANCH IF MVM           YM2830
 PUNCH '//SG&SGCTRLA(1) EXEC PGM=IEHPROGM,COND=(8,LT)'
 PUNCH '//TARG DD VOLUME=SER=&SGCTRLC(30),DISP=OLD,UNIT=&SGCTRLC(29)'
 PUNCH '//SYSPRINT DD SYSOUT=&SGCTRLC(41)'
 PUNCH '//SYSIN DD *'                                            S21021
 PUNCH ' CATLG CVOL=&SGCTRLC(29)=&SGCTRLC(30),VOL=&SGCTRLC(29)=&SGCTRLCX
               (30),DSNAME=SYS1.LOGREC'                          S21021
         AGO   .VS1                                              YM2830
.VS2     AIF   (&GETB(3)).VS1                                   YM6505P
 PUNCH '//SG&SGCTRLA(1) EXEC PGM=IDCAMS,COND=(8,LT),REGION=512K' YM5838
 PUNCH '//SYSPRINT DD  SYSOUT=&SGCTRLC(41)'                      YM2830
 PUNCH '//CATLG DD DSN=&SGVSNMC,'                              @ZM40007
 PUNCH '//  VOL=(,RETAIN,SER=&SGDSVLC(15)),'                   @ZM40169
 PUNCH '//             UNIT=&SGDSUNC(15),DISP=SHR'              YM2830P
 PUNCH '//TARG     DD  DSN=&SGCTRLC(40)..LOGREC,DISP=OLD,'       YM2830
 PUNCH '//             VOL=SER=&SGCTRLC(30),UNIT=&SGCTRLC(29)'   YM2830
 PUNCH '//SYSIN    DD  *'                                        YM2830
 PUNCH ' DEFINE NONVSAM (NAME(&SGCTRLC(40)..LOGREC) VOL(&SGCTRLC(30))-'
 PUNCH '        DEVT(&SGCTRLC(29))) CAT(&SGVSNMC)'       @ZA41682YM2830
.VS1     ANOP                                                    YM2830
 PUNCH '/* '                                                     YM2830
.END     MEND
