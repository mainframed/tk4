         MACRO
         SGADDR
         COPY  SGGBLPAK
         LCLC  &ADDR
         LCLA  &DITSAVE,&I                                    @ZA42596
.***START OF SPECIFICATIONS********************************************
.* MODULE NAME - SGADDR                                               *
.*                                                                    *
.* DESCRIPTIVE NAME - UPDATE DEVICE POINTER AND DEVICE ADDRESS ARRAYS *
.*                                                                    *
.* COPYRIGHT - NONE                                                   *
.*                                                                    *
.* STATUS - CHANGE LEVEL 00                                           *
.*                                                                    *
.* FUNCTION -                                                         *
.*   THE FUNCTION OF THIS MACRO IS TO INCREMENT THE DADRA AND DASPA   *
.* ARRAYS AND TO UPDATE THE DEVICE POINTER &SGCNTRA(3). THIS MACRO    *
.* SHOULD ONLY BE USED WHEN MULTIPLE UNITS ARE BEING GENERATED AND    *
.* A NEW ARRAY ENTRY IS REQUIRED.  IF ONLY ONE UNIT IS TO BE          *
.* GENERATED FOR THIS IODEVICE SPECIFICATION, THE SGADDR MACRO        *
.* SHOULD NOT BE USED.                                                *
.*                                                                    *
.* NOTES -  N/A                                                       *
.* MODULE TYPE - MACRO                                                *
.*                                                                    *
.* ENTRY POINT - N/A                                                  *
.*                                                                    *
.* INPUT - THE FOLLOWING GLOBALS ARE REFERENCED                       *
.*                                                                    *
.*       &SGCNTRA(3) - DEVICE POINTER                                 *
.*                                                                    *
.*       &SGADRPC(1) - DEVICE ADDRESS SPECIFICATION                   *
.*                                                                    *
.*       &SGDTIXA(&I) - DIT INDEX ARRAY                               *
.*                                                                    *
.* OUTPUT - THE FOLLOWING GLOBALS ARE SET                             *
.*                                                                    *
.*       &SGCNTRA(3) - DEVICE POINTER                                 *
.*                                                                    *
.*       &SGADRPC(1) - DEVICE ADDRESS SPECIFICATION                   *
.*                                                                    *
.*       &SGDADRA(&I) - DEVICE ADDRESS TABLE                          *
.*                                                                    *
.*       &SGDASPA(&I) - DEVICE POINTER TABLE                          *
.*                                                                    *
.*       &HEX         - DEVICE ADDRESS IN HEX                         *
.*                                                                    *
.*       &SGDTIXA(&I) - DIT INDEX ARRAY
.*                                                                    *
.* LOCALS USED:                                                       *
.*       &I         DEVICE POINTER INDEX                              *
.*                                                                    *
.* EXIT,NORMAL - N/A                                                  *
.*                                                                    *
.* EXIT,ERROR  - N/A                                                  *
.*                                                                    *
.* EXTERNAL REFERENCES - N/A                                          *
.*                                                                    *
.* MACROS - CONVERT                                                   *
.*                                                                    *
.* CHANGE ACTIVITY - C0000400                                @ZA42596 *
.* @ZA72065    ADD TEST FOR ADDRESS 'FFF' AND ISSUE MSG               *
.**********************************************************************
&I          SETA &SGCNTRA(3)            GET DEVICE POINTER
&DITSAVE    SETA &SGDITXA(&I)           SAVE DIT INDEX
&SGCNTRA(3) SETA &SGCNTRA(3)+1          UPDATE DEVICE COUNTER
&I          SETA &SGCNTRA(3)            GET UPDATED POINTER
&SGDITXA(&I) SETA &DITSAVE              UPDATE DIT INDEX
&ADDR       SETC '&SGADRPC(1)'          GET SPECIFIED ADDR
         CONVERT TO=HEX,VALUE=&ADDR,DIGITS=3
&HEX     SETA  &HEX+1                   UP DEVICE ADDRESS
&SGDADRA(&I)   SETA &HEX                SET ADDR ARRAY
&SGDASPA(&I)   SETA &I                  SET PTR. ARRAY
         CONVERT TO=EBCDIC,DIGITS=3,VALUE=&HEX
&SGADRPC(1)  SETC  '&EBCDIC'            NEW ADDR SPECIFICATION
         AIF   ('&SGADRPC(1)' EQ 'FFF').ERR1                   @ZA72065
    MNOTE    *,'     ADDRESS &SGADRPC(1) SPECIFIED '
         AGO   .MEND                                           @ZA72065
.ERR1    ANOP                                                  @ZA72065
    MNOTE 5,'* * * IEISGA101 ADDRESS FFF INVALID FOR IODEVICE          X
               MACRO'                                          @ZA72065
&SGQUITB SETB 1                                                @ZA72065
.MEND    MEND
