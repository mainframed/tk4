         MACRO
         SVCTABLE
         COPY  SGGBLPAK
         LCLA  &B,&I,&J,&K,&L,&N,&P,&R,&S,&T,&CT,&U             Z40FSVN
         LCLB  &X
         LCLC  &A,&C,&D,&H
.***** BEGINNING OF SPECIFICATIONS **********************************
.*
.* MODULE NAME - SVCTABLE
.*
.* DESCRIPTIVE NAME - SVCTABLE BUILDER
.*
.* COPYRIGHT - NONE
.*
.* STATUS - CHANGE LEVEL 04  9/10/75                            Z40FSVN
.*
.* FUNCTION -
.*       THE SVCTABLE MACRO IS USED TO SPECIFY THE NUMBER, TYPE,
.*       AND ENTRY STATUS OF USER WRITTEN SVC ROUTINES THAT ARE
.*       ADDED TO THE SYSTEM.
.*
.* NOTES - N/A
.*
.* MODULE TYPE - MACRO
.*
.* ENTRY POINT - N/A
.*
.* INPUT - THE PARAMETER DESCRIBED HERE IS USED ON THE CALL TO
.*         SVCTABLE
.*             SVC-NNN-TN-(LN,LN...)-FCNN-NP
.*                 NNN - SVC NUMBER
.*                     TN - TYPE
.*                         LN - LOCKS ETC., 1 THRU 5
.*                                   FCNN - FUNCTION CODE OF 00 OR 01
.*                                        NP - NON PREEMPTIBLE
.*
.* OUTPUT -
.*       THE GLOBALS SET ARE -
.*             &SGSVCFA(1-255) - SVC FUNCTION CODE
.*             &SGQUITB - QUIT SWITCH
.*             &SGSVCAB - SVC TYPE DESIGNATOR -
.*             &SGSVCBB - SVC TYPE DESIGNATOR -
.*             &SGSVCCB - SVC TYPE DESIGNATOR -
.*             &SGSVC1B - LOCK DESIGNATOR -
.*             &SGSVC2B - LOCK DESIGNATOR -
.*             &SGSVC3B - LOCK DESIGNATOR -
.*             &SGSVC4B - LOCK DESIGNATOR -
.*             &SGSVC5B - LOCK DESIGNATOR -
.*             &SGSVNPB - NON PREEMPIBLE DESIGNATOR -
.*
.*       THE LOCALS USED ARE -
.*             &I  - INDEX TO OPERAND
.*             &J  - INDEX TO CHARACTER OF OPERAND
.*             &K  - BOX FOR SVC NUMBER
.*             &L  - NUMERIC FOR TYPE
.*             &N  - POINTER TO FIRST SVC CHARACTER
.*             &P  - POINTER TO FIRST TYPE CHARACTER
.*             &T  - BOX FOR TYPE NUMBER
.*             &CT - COUNT OF CHARACTERS
.*             &U  - &J+1
.*             &A  - CHARACTER FOR TYPE
.*             &C  - SVC NUMBER
.*             &D  - VALUE FOR ERROR MESSAGE
.*
.* EXIT, NORMAL - N/A
.*
.* EXIT, ERROR - N/A
.*
.* EXTERNAL REFERENCES - N/A
.*
.* TABLES - N/A
.*
.* MACROS - NONE
.*
.* CHANGE ACTIVITY -
.*    SD CODES Y02036,Z40FSVN
.*
.**** END OF SPECIFICATIONS *****************************************
.L1      AIF   (N'&SYSLIST EQ 0).M1     ERROR EXIT FOR NO OPERANDS.
&I       SETA  &I+1                     SET INDEX TO OPERAND.
&J       SETA  0                        INDEX TO CHAR OF OPERAND.
&CT      SETA  K'&SYSLIST(&I)           COUNT OF CHARACTERS
.L2      AIF   (&J+1 GT &CT).M2         ERROR END OF OPERAND.
&J       SETA  &J+1                     BUMP CHARACTER INDEX.
         AIF   ('&SYSLIST(&I)'(&J,1) NE '-').L2  LOOK FOR MINUS.
&N       SETA  &J+1                     SET PTR TO 1ST SVC CHAR.
.L3      AIF   (&J+1 GT &CT).M2         ERROR END OF OPERAND.
&J       SETA  &J+1                     BUMP CHARACTER INDEX.
         AIF   ('&SYSLIST(&I)'(&J,1) GE '0' AND '&SYSLIST(&I)'(&J,1) LE1
                '9').L3                 GO BACK IF CHAR IN RANGE.
         AIF   ('&SYSLIST(&I)'(&J,1) NE '-').M2  ERROR INVALID CHAR.
         AIF   (&J-&N GT 3).M2
&C       SETC  '&SYSLIST(&I)'(&N,&J-&N) SET SVC NO. IN "C" BOX.
&K       SETA  &C                       PUT SVC NUMBER IN "A" BOX.
         AIF   (&K LT 1 OR &K GT 255).M3  SVC OUT OF RANGE
.R4      ANOP
&P       SETA  &J+2                     POINT TO 1ST TYPE CHARACTER.
&J       SETA  &J+1                     BUMP CHARACTER INDEX.
.L4      AIF   (&J+1 GT &P).L5
&J       SETA  &J+1                     BUMP CHARACTER INDEX.
         AIF   ('&SYSLIST(&I)'(&J,1) GE '0' AND '&SYSLIST(&I)'(&J,1) LE1
                '6').L4                 GO BACK IF CHAR OK      Z40FSVN
.L5      ANOP
&U       SETA  &J+1
         AIF   (&P NE &U).N4
&D       SETC  '&SYSLIST(&I)'(&P,1)     SET VALUE FOR ERROR MSG
         AGO   .M4
.N4      ANOP
         AIF   (&U-&P GT 1).M2
&D       SETC  '&SYSLIST(&I)'(&P,&U-&P)   SET TYPE NO. IN "C" BOX.
&T       SETA  &D                       PUT TYPE IN "A" BOX.
         AIF   (&CT EQ &J).R6           CHECK FOR LAST CHARACTER
&J       SETA  &J+1                     BUMP COUNT
.R5      ANOP
         AIF   ('&SYSLIST(&I)'(&J,2) EQ '-L').LC
         AIF   ('&SYSLIST(&I)'(&J,3) NE '-(L').R6
&J       SETA  &J+1                     UPDATE J FOR THE '('
.LC      AIF   (&T EQ 6).ME             NO LOCKS ON TYPE 6      Z40FSVN
.R51     ANOP                                                   Z40FSVN
&J       SETA  &J+2
&A       SETC  '&SYSLIST(&I)'(&J,1)     EXTRACT LOCK NUMBER
&L       SETA  &A                       PUT INTO "A" BOX
         AIF   ((&L NE 1) AND (&L NE 2) AND (&L NE 3) AND (&L NE 4)    *
               AND (&L NE 5)).M9
.R55     AIF   (((&T EQ 3) OR (&T EQ 4)) AND (&L EQ 4)).MA
&SGSVC1B(&K) SETB (&L EQ 1 OR &SGSVC1B(&K))        SET LOCK ONE BIT.
&SGSVC2B(&K) SETB (&L EQ 2 OR &SGSVC2B(&K))        SET LOCK TWO BIT.
&SGSVC3B(&K) SETB (&L EQ 3 OR &SGSVC3B(&K))        SET LOCK THREE BIT.
&SGSVC4B(&K) SETB (&L EQ 4 OR &SGSVC4B(&K))        SET LOCK FOUR BIT.
&SGSVC5B(&K) SETB (&L EQ 5 OR &SGSVC5B(&K))        SET LOCK FIVE BIT.
.R555    AIF   (&CT EQ &J).R7           CHECK FOR LAST CHARACTER
&J       SETA  &J+1
         AIF   ('&SYSLIST(&I)'(&J,2) EQ ',L').LC
         AIF   ('&SYSLIST(&I)'(&J,1) NE ')').R7
&J       SETA  &J+1                     UPDATE J FOR THE ')'
         AGO   .R7
.R6      AIF   (&T NE 1).R7
&SGSVC1B(&K) SETB   1                   DEFAULT LOCAL LOCK FOR T1
.R7      AIF   (&J+1 GT &CT).MB         CHECK FOR END OF STRING
         AIF   (&CT-&J LT 4).MB         CHECK FOR 5 CHAR. MIN.  Z40FSVN
         AIF   ('&SYSLIST(&I)'(&J,3) NE '-FC').MB               Z40FSVN
         AIF   ('&SYSLIST(&I)'(&J+3,2) EQ '01').R8
         AIF   ('&SYSLIST(&I)'(&J+3,2) NE '00').M8
.R8      ANOP
&C       SETC  '&SYSLIST(&I)'(&J+3,2)   SET FC IN "C" BOX
&SGSVCFA(&K) SETA &C                    PUT FC IN GLOBAL        Z40FSVN
&J       SETA  &J+5                     STEP OVER FC OPERAND    Z40FSVN
.R82     AIF   (&J+1 GT &CT).R9         CHECK FOR END OF STRING Z40FSVN
         AIF   (&CT-&J NE 2).MC         ENOUGH LEFT FOR NP?     Z40FSVN
         AIF   ('&SYSLIST(&I)'(&J,3) NE '-NP').MC   NP?         Z40FSVN
&SGSVNPB(&K) SETB 1                     TURN ON NP GLOBAL       Z40FSVN
.R9      ANOP
         AIF   (&N NE 5).M2             NOT ENOUGH CHAR. BEFORE SVC NO.
         AIF   ('&SYSLIST(&I)'(1,3) NE 'SVC').M6  NOT "SVC"
         AIF   (&K LT 1 OR &K GT 255).L8  DO NEXT               Z40FSVN
         AIF   (&SGSVCAB(&K) OR &SGSVCBB(&K) OR &SGSVCCB(&K)).MD MULT
&SGSVCAB(&K) SETB ((&T EQ 6) OR (&T EQ 5) OR (&T EQ 4))  4-BIT  Z40FSVN
&SGSVCBB(&K) SETB ((&T EQ 6) OR (&T EQ 3) OR (&T EQ 2))  2-BIT  Z40FSVN
&SGSVCCB(&K) SETB ((&T EQ 5) OR (&T EQ 3) OR (&T EQ 1))  1-BIT
.L8      AIF   (&I+1 LE N'&SYSLIST).L1  GO BACK FOR NEXT OPERAND.
         MEXIT
.M1 MNOTE 5,'* * * IEISVC101 NO SVC SPECIFIED'
&SGQUITB SETB  1
         MEXIT
.M2 MNOTE 5,'* * * IEISVC102 OPERAND &I-SYNTAX &SYSLIST(&I) INVALID'
&SGQUITB SETB  1
         AGO  .L8
.M3 MNOTE 5,'* * * IEISVC103 OPERAND &I-SVC VALUE &C INVALID'
&SGQUITB SETB  1
         AGO  .R4
.M4 MNOTE 5,'* * * IEISVC104 OPERAND &I-TYPE VALUE &D INVALID'
&SGQUITB SETB  1
         AGO  .R5
.M6 MNOTE 5,'* * * IEISVC106 OPERAND &I-SVC NOT SPECIFIED'
&SGQUITB SETB  1
         AGO  .L8
.M8 MNOTE 5,'* * * IEISVC108 OPERAND &I-SVC &K FUNCTION CODE NOT 0 OR 1*
               '
&SGQUITB SETB  1
         AGO   .R8                      GO TRY IT               Z40FSVN
.M9 MNOTE 5,'* * * IEISVC109 OPERAND &I-SVC &K LOCK INDICATOR CODE NOT *
               VALID '
&SGQUITB SETB  1
         AGO   .R55
.MA MNOTE 5,'* * * IEISVC110 OPERAND &I-SVC &K A TYPE 3 OR TYPE 4 SVC  *
               MAY NOT HOLD THE SALLOC'
&SGQUITB SETB  1
         AGO   .R555
.MB MNOTE 0,'* * * IEISVC111 FOR SVC &K A FUNCTION CODE OF 00 WAS DEFAU*
               LTED'
         AGO   .R82                     GO DO NP                Z40FSVN
.MC MNOTE 5,'* * * IEISVC112 OPERAND &I - FUNCTION CODE, LOCK, OR NP HA*
               S BEEN IMPROPERLY SPECIFIED'                     Z40FSVN
         AGO  .R9
.MD MNOTE 5,'* * * IEISVC113 OPERAND &I-SVC &K REPEATED'
&SGQUITB  SETB  1
         AGO  .L8
.ME MNOTE 5,'* * * IEISVC114 OPERAND &I-SVC &K-TYPE 6 MAY NOT SPECIFY L*
               OCKS'                                            Z40FSVN
&SGQUITB  SETB  1                                               Z40FSVN
         AGO   .R51                                             Z40FSVN
         MEND                                                   Z40FSVN
