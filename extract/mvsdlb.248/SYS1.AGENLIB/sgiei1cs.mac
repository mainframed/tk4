         MACRO
         SGIEI1CS
.**********************************************************************
.*       SGIEI1CS MACRO VALIDATES THE CONSOLE CONFIGURATION OF THE
.*       SYSTEM BEING GENERATED. DEPENDENCIES ARE RESOLVED.
.*
.*       THE GLOBALS USED ARE -
.*             &SGMENTB(6)     - SCHEDULER MACRO
.*             &SGSCHDC(1)     - I/O ADDR OF CONSOLE
.*             &SGDADRA(1-1917)- DEVICE ADDRESSES
.*             &SGDASPA(1-1917)- SWITCH POINTER
.*             &SGDTXXB(1-1917)- DEVICE CLASSES
.*             &SGDUXXB(1-1917)- DEVICE NAMES
.*             &SGDMXXB(1-1917)- MODEL NUMBERS
.*             &SGDFXXB(1-1917)- FEATURES
.*             &SGQUITB        - QUIT SWITCH
.*             &SGSCHDA(16)    - NUMBER OF PFKS FOR MASTER CONSOLE
.*             &SGSCHDC(16)    - IOC ADDRESS OF MASTER CONSOLE
.*             &SGSCHAA(12)    - TOTAL DISPLAY LN - MASTER CONSOLE
.*             &SGSCHDC(2)     - I/O ADDRESS OF ALTERNATE CONSOLE
.*             &SGSCNYA(2)     - NUMBER OF SECONDARY CONSOLE CALLS
.*             &SGSCNYA(3)     - NUMBER OF CONSOLE DEVICES
.*             &SGSCHDC(14)    - I/O ADDRESS OF HARDCOPY LOG
.*             &SGSCNCC(1-31)  - SECONDARY CONSOLE ADDRESS
.*             &SGUNF1B(1-1917)- UNIT FEATURE SETTINGS
.*             &SGSCHDA(11)    - 2260 COUNT
.*             &SGGRPHB(1-1917)- GRAPHICS
.*             &SGDNBFA(1-1917)- NUMSECT
.*             &SGSCHDA(10)    - 2250 COUNT
.*             &SGSCHDA(17)    - 3277-1 COUNT
.*             &SGSCHDA(18)    - 3036,3158, 3277-2 COUNT        G51DSPD
.*             &SGSCHDA(19)    - 3284/86-1 COUNT
.*             &SGSCHDA(20)    - 3284/86-2 COUNT
.*             &SGSCNAC(1-31)  - ALTERNATE CONSOLE ADDRESSES
.*             &SGSCNPA(1-31)  - NO PGMED FUNCTION KEYS
.*             &SGSCSDB(1-31)  - LIM CAP CONSOLE - SDM
.*             &SGSCMSB(1-31)  - LIM CAP CONSOLE - CMS
.*             &SGAR12A(1-31)  - TOTAL LENGTH OF DISPLAY AREA
.*             &SGARXXA(1-31)  - LENGTH OF XX CRT - XX=1-11
.*
.*         THE LOCALS USED ARE -
.*             &Z              - FIRST HALF OF ALT CONSOLES
.*             &C5             - FFF
.*             &ADDRESI        - I/P PORTION OF COMPOSITE CONSOLE
.*             &ADDRESO        - O/P PORTION OF COMPOSITE CONSOLE
.*             &AC             - HARDCOPY CONSOLE
.*             &GR             - GRAPHICS
.*             &II             - SWITCH
.*             &SS             - SWITCH
.*             &C              - COUNTER FOR SECONDARY CONSOLES
.*             &X              - DEVICE POINTER
.*             &TT             - COUNT OF SECONDARY CONSOLES
.*             &XX             - SEC CONSOLE COUNT
.* CHANGE ACTIVITY OZ10499,@ZA30506,@ZA44996,@ZA53128,@ZA60599
.*                 Z40ASVN,G51DSPD,@ZA71025
.*
.**********************************************************************
         COPY  SGGBLPAK
         LCLC  &Z,&C5,&ADDRESI,&ADDRESO,&IOC
         LCLB  &AC,&GR,&II,&SS
         LCLA  &C,&X,&TT,&XX
         ACTR  &SGACTR
.**********************************************************************
.*             **** CHECK MASTER CONSOLE INPUT ADDRESS ****     G51DSPD
.**********************************************************************
         AIF   ('&SGSCHDC(1)' NE '').NM     IS MCONS SPECIFIED @ZA71025
     MNOTE 5,'* * * IEIGEN307  CONSOLE MACRO MCONS REQUIRED'   @ZA71025
&SGQUITB SETB 1                                                @ZA71025
.NM      AIF   ('&SGSCHDC(1)' EQ '' AND &SGSCNYA(2) EQ 0).ER1  @ZA71025
         AIF   (&SGSCNYA(3) LE 99).TEST    LIMIT OF 99 CONSOLE DEVICES
     MNOTE 5,'* * * IEIGEN334  &SGSCNYA(3) CONSOLE DEVICES SPECIFIED - X
               EXCEEDS LIMIT OF 99'
&SGQUITB SETB 1
.TEST   ANOP
&ADDRESI SETC  '&SGSCHDC(1)'(1,3)
&ADDRESO SETC  '&SGSCHDC(1)'(4,3)
         CONVERT TO=HEX,DIGITS=3,VALUE=&ADDRESI
         IOCHECK LOOKFOR=&HEX
         AIF   (&HEX EQ 0).A1
&X       SETA  &SGDASPA(&HEX)           GET DEVICE POINTER      Z40ASVN
         AIF   (&SGDT04B(&X) AND &SGDU01B(&X) AND &SGDM01B(&X)).SETCONI
.*                                                          1052-7
         AIF   (&SGDT04B(&X) AND &SGDU03B(&X)).SETCONI      3210
         AIF   (&SGDT04B(&X) AND &SGDU04B(&X)).SETCONI      3215
         AIF   (&SGDT07B(&X) AND &SGDU02B(&X) AND (&SGDM01B(&X) OR     *
               &SGDM02B(&X))).SETRDR                        2501-B1,B2
         AIF   (&SGDT07B(&X) AND &SGDU03B(&X) AND &SGDM01B(&X)).SETRDR
.*                                                          2520-B1
         AIF   (&SGDT07B(&X) AND &SGDU04B(&X) AND &SGDM01B(&X)).SETRDR
.*                                                          2540R
         AIF   (&SGDT07B(&X) AND &SGDU08B(&X)).SETRDR       3505
         AIF   (&SGDT07B(&X) AND &SGDU09B(&X)).SETRDR       3525
         AIF   (&SGDT08B(&X) AND &SGDU08B(&X)).SETTPI       2740
.**********************************************************************
.*            CHECK GRAPHICS TO INSURE IT IS NOT THE INPUT HALF OF A  *
.*            COMPOSITE CONSOLE.                                      *
.**********************************************************************
         AIF   ('&ADDRESI' NE '&ADDRESO').A15
         AIF   (&SGDT05B(&X) AND &SGDU01B(&X) AND (&SGDM01B(&X) OR     *
               &SGDM03B(&X))).SETGRI                        2250-1,3
         AIF   (&SGDT05B(&X) AND &SGDU02B(&X) AND &SGDM01B(&X)).SETGRI
.*                                                          2260-1
         AIF   (&SGDT05B(&X) AND &SGDU06B(&X) AND &SGDF01B(&X)).SETGRI
.*                                                          3066
         AIF        (&SGDT05B(&X)  AND &SGDU10B(&X)).SETGRI     G51DSPD
.*                                                          3158
         AIF        (&SGDT05B(&X)  AND &SGDU13B(&X)).SETGRI     G51DSPD
.*                                                          3036
.A1A     AIF   (NOT (&SGDT05B(&X) AND &SGDU08B(&X) AND &SGDM02B(&X))).A*
               1                                            3277-2
         AIF   (&SGDF10B(&X) OR &SGDF11B(&X) OR &SGDF12B(&X) OR        *
               &SGDF13B(&X)).SETGRI                         KEYBOARD
    MNOTE 5,'* * * IEIGEN301 CONSOLE ADDRESS &ADDRESI,REQUIRES KEYBOARD*
                TO HAVE INPUT CAPABILITY'
&SGQUITB SETB  1
         AGO   .A2
.A1 MNOTE 5,'* * * IEIGEN302 CONSOLE MACRO, ADDRESS &ADDRESI INVALID FO*
               R CONSOLE INPUT'
&SGQUITB SETB  1
         AGO   .A3
.A15     AIF   (NOT(&SGDT04B(&X))).A1
     MNOTE 5,'* * * IEIGEN303 CONSOLE MACRO,GRAPHICS ADDRESS &ADDRESI I*
               NVALID FOR INPUT IN A COMPOSITE CONSOLE'
&SGQUITB SETB  1
         AGO   .B1
.**********************************************************************
.*                           PFK CHECK (MASTER)                 G51DSPD
.**********************************************************************
.A2      AIF   (&SGSCHDA(16) EQ 0).A3
         AIF   (NOT (&SGDT05B(&X) AND &SGDU01B(&X))).A2B    2250
         AIF   (&SGDF02B(&X) OR &SGDF03B(&X)).A3  LIGHTPEN / PRGMKYBD
    MNOTE 5,'* * * IEIGEN331 PFK SPECIFICATION FOR 2250, CONSOLE ADDRES*
               S &ADDRESI, REQUIRES PRGMKYBD OR LIGHTPEN FEATURE'
&SGQUITB SETB  1
         AGO   .A3
.*                                 CHECK IF 3277, MODEL 2, OR 3158
.A2B     AIF   (&SGDT05B(&X) AND &SGDU08B(&X) AND &SGDM02B(&X)).A3
         AIF   (&SGDT05B(&X) AND &SGDU10B(&X)).A3          3158
         AIF   (&SGDT05B(&X) AND &SGDU13B(&X)).A3          3036 G51DSPD
     MNOTE 5,'* * * IEIGEN304 CONSOLE MACRO, CONSOLE ADDRESS &ADDRESI  *
               - PFK SPECIFICATION INVALID'
&SGQUITB SETB  1
.**********************************************************************
.*             **** CHECK MASTER CONSOLE OUTPUT ADDRESS ****    G51DSPD
.A3      ANOP  ********************************************************
&ADDRESO SETC  '&SGSCHDC(1)'(4,3)
         AIF   ('&ADDRESI' EQ '&ADDRESO' AND &HEX EQ 0).C1
         AIF   ('&ADDRESI' EQ '&ADDRESO').A5
         CONVERT TO=HEX,DIGITS=3,VALUE=&ADDRESO
         IOCHECK LOOKFOR=&HEX
         AIF   (&HEX EQ 0).A4
&X       SETA  &SGDASPA(&HEX)           GET DEVICE POINTER      Z40ASVN
         AIF   (&SGDT04B(&X) AND &SGDU01B(&X) AND &SGDM01B(&X)).SETCONO
.*                                                          1052-7
         AIF   (&SGDT04B(&X) AND &SGDU03B(&X)).SETCONO      3210
         AIF   (&SGDT04B(&X) AND &SGDU04B(&X)).SETCONO      3215
         AIF   (&SGDT06B(&X) AND &SGDU01B(&X) AND (&SGDM01B(&X) OR     *
               &SGDM02B(&X) OR &SGDM04B(&X))).SETPTR        1403-N1,2,7
         AIF   (&SGDT06B(&X) AND &SGDU03B(&X) AND &SGDM01B(&X)).SETPTR
.*                                                          1443-N1
         AIF   (&SGDT06B(&X) AND &SGDU06B(&X)).SETPTR       3211
         AIF   (&SGDT06B(&X) AND &SGDU08B(&X)).SETCONO      3213
         AIF   (&SGDT08B(&X) AND &SGDU08B(&X)).SETTPO       2740
         AIF   (&SGDT05B(&X) AND &SGDU01B(&X) AND (&SGDM01B(&X) OR     *
               &SGDM03B(&X))).SETGRO                        2250-1,3
         AIF   (&SGDT05B(&X) AND &SGDU02B(&X) AND &SGDM01B(&X)).SETGRO
.*                                                          2260-1
         AIF   (&SGDT05B(&X) AND &SGDU06B(&X) AND &SGDF01B(&X)).SETGRO
.*                                                          3066
         AIF   (&SGDT05B(&X) AND &SGDU10B(&X)).SETGRO       3158
         AIF   (&SGDT05B(&X) AND &SGDU13B(&X)).SETGRO      3036 G51DSPD
         AIF   (&SGDT05B(&X) AND &SGDU08B(&X) AND &SGDM02B(&X)).SETGRO
.*                                                          3277-2
         AIF   (&SGDT05B(&X) AND &SGDU09B(&X) AND (&SGDM01B(&X) OR     *
               &SGDM02B(&X))).SETOPT                        3284-1,2
         AIF   (&SGDT05B(&X) AND &SGDU07B(&X) AND (&SGDM01B(&X) OR     *
               &SGDM02B(&X))).SETOPT                        3286-1,2
.A4 MNOTE 5,'* * * IEIGEN305 CONSOLE MACRO, ADDRESS &ADDRESO INVALID FO*
               R CONSOLE OUTPUT'
&SGQUITB SETB  1
         AGO   .C1
.**********************************************************************
.**     INDICATE TYPE OF DEVICE  USED AS A CONSOLE FOR SGIEA401      **
.**********************************************************************
.SETCONI ANOP                    SET CONSOLE INDICATOR
&SGSCHDB(1)    SETB 1            SET FOR A 1052/3210/3215
         AGO   .A2               GO TO PFK CHECK
.SETCONO ANOP                    SET CONSOLE INDICATOR
&SGSCHDB(1)    SETB 1            SET FOR A 1052/3210/3215/3213
         AGO   .A6               GO SET VALID HARDCOPY SWITCH
.SETRDR  ANOP                    SET READER INDICATOR
&SGSCHDB(2)    SETB 1            SET FOR A 2501/2520/2540R/3505/3525
         AIF   ('&ADDRESI' EQ '&ADDRESO').A4   INVALID OUTPUT DEVICE
         AGO   .A2               GO TO PFK CHECK
.SETPTR  ANOP                    SET PRINTER INDICATOR
&SGSCHDB(3)    SETB 1            SET FOR A 1403/1443/3211
         AGO   .A6               GO SET VALID HARDCOPY SWITCH
.SETGRI  ANOP                    SET INPUT GRAPHICS INDICATOR
&SGSCHDB(5)    SETB 1            SET FOR 2250/2260/3066/3158/3277/3036
.*                                                              G51DSPD
         AGO   .A2               GO TO PFK CHECK
.SETGRO  ANOP                    SET OUTPUT GRAPHICS INDICATOR
&SGSCHDB(5)    SETB 1            SET FOR 2250/2260/3066/3158/3277/3036
.*                                                              G51DSPD
         AGO   .B1               GO TO IOC CHECK
.SETOPT  ANOP                    SET OCR/MICR INDICATOR
&SGSCHDB(5)    SETB 1            SET FOR A 2250/2260/3066/3158/3277
         AGO   .A6               GO SET VALID HARDCOPY SWITCH
.SETTPI  ANOP                    SET TP INPUT INDICATOR
&SGSCHDB(13)   SETB 1            SET FOR A 2740
         AGO   .A2               GO TO PFK CHECK
.SETTPO  ANOP                    SET TP OUTPUT INDICATOR
&SGSCHDB(13)   SETB 1            SET FOR A 2740
         AGO   .A6               GO SET VALID HARDCOPY SWITCH
.**********************************************************************
.*                 VERIFY AND SET HARDCOPY INDICATOR                  *
.**********************************************************************
.A5      AIF   (NOT(&SGDT04B(&X) OR &SGDT08B(&X))).B1  1052-7/3210/3215
.A6      ANOP                    * SWITCH FOR VALID *  2740/1403-N1,2,7
&AC      SETB  1                 * HARDCOPY CONSOLE *  1443-N1/3211
.*                               * CAPABILITY       *  3284/3286
.********************************************************************
.*****  INTEGRATED OPERATOR CONSOLE (MASTER)    ***********  G51DSPD*
.********************************************************************
.B1      ANOP
         AIF   ('&SGSCHDC(16)' EQ '').B1B
     MNOTE 0,'* * * IEIGEN333 INTEGRATED OPERATOR CONSOLE (IOC) NO LONG*
               ER SUPPORTED'                                   @ZA60599
.**********************************************************************
.*             **** AREA CHECK AND DEFAULT (MASTER) ****        G51DSPD
.**********************************************************************
.B1B     CONVERT TO=HEX,DIGITS=3,VALUE=&ADDRESO
         IOCHECK LOOKFOR=&HEX
&X       SETA  &SGDASPA(&HEX)                                   Z40ASVN
.*      THE FOLLOWING AIF EXCLUDE DEVICES WITHOUT DISPLAY AREAS.
         AIF   (&SGSCHAA(12) NE 0 AND NOT (&SGDT05B(&X) AND (&SGDU01B(&*
               X) OR &SGDU02B(&X) OR &SGDU06B(&X) OR (&SGDU08B(&X) AND *
               &SGDM02B(&X)) OR &SGDU10B(&X) OR &SGDU13B(&X)))).B2
.*                                                              G51DSPD
.*  AREAS /2250/2260/3066/3277-2/3158/3036/                     G51DSPD
.*
.*   THE FOLOWING AIF EXCLUDE DEVICES NOT DEFAULTING TO 14 FOR AREA.
         AIF   (NOT (&SGDT05B(&X) AND (&SGDU01B(&X) OR &SGDU06B(&X) OR *
               &SGDU08B(&X) OR &SGDU10B(&X) OR &SGDU13B(&X)))).B2
.*                                                              G51DSPD
.*  14 IS AREA DEFAULT.  2250/3066/3277/3158/3036               G51DSPD
         AIF   (&SGDU01B(&X) AND &SGSCHAA(12) GT 47).B4     2250 ERR
         AIF   (&SGDU06B(&X) AND &SGSCHAA(12) GT 30).B4     3066 ERR
         AIF   (&SGDU08B(&X) AND &SGSCHAA(12) GT 19).B4     3277 ERR
    AIF  ((&SGDU08B(&X) OR &SGDU10B(&X) OR &SGDU13B(&X))               *
               AND &SGSCHAA(12) GT 19).B4                       G51DSPD
.*                                   3277 OR 3158 OR 3036 ERROR G51DSPD
         AIF   (&SGSCHAA(12) NE 0).C1
&SGSCHAA(1)    SETA 14                                      DEFAULT 14
&SGSCHAA(12)   SETA 14
         AGO   .B3
.B2      AIF   (NOT (&SGDT05B(&X) AND &SGDU02B(&X))).C1     2260
         AIF   (&SGSCHAA(12) GT 8).B4                       2260 ERR
         AIF   (&SGSCHAA(12) NE 0).C1
&SGSCHAA(1)    SETA 8                                       DEFAULT 8
&SGSCHAA(12)   SETA 8
.B3 MNOTE 0,'* * * IEIGEN931 CONSOLE ADDRESS &ADDRESO, STATUS DISPLAY A*
               REA DEFAULTED TO LENGTH OF &SGSCHAA(1)'
         AGO   .C1
.B4 MNOTE 5,'* * * IEIGEN306 CONSOLE MACRO, DISPLAY AREA DEFINED INVALI*
               D FOR CONSOLE ADDRESS &ADDRESO'
&SGQUITB SETB  1
.**********************************************************************
.*             **** MULTIPLE CONSOLE SUPPORT ****                     *
.**********************************************************************
.C1      AIF   ('&SGSCHDC(2)' EQ '&SGSCHDC(1)'                         *
               OR '&SGSCHDC(2)' EQ '').D1         ALTCONS   **SCS**
         AIF   (&SGSCNYA(2) NE 0).D1                        **SCS**
    MNOTE 5,'* * * IEIGEN307 CONSOLE MACRO SECONS REQUIRED FOR MCS'
&SGQUITB SETB  1
.**********************************************************************
.*             **** CK HARDCPY ADDR SPEC'D AS CONSOLE ADDR ****       *
.**********************************************************************
.D1      AIF   ('&SGSCHDC(14)' EQ '').F1
&ADDRESO SETC  '&SGSCHDC(1)'(4,3)
         AIF   ('&SGSCHDC(14)' EQ '&ADDRESO').E1
&C       SETA  &SGSCNYA(1)
.D2      AIF   (&C EQ 0).D3
&ADDRESO SETC  '&SGSCNCC(&C)'(4,3)
         AIF   ('&SGSCHDC(14)' EQ '&ADDRESO').E1
&C       SETA  &C-1
         AGO   .D2
.D3 MNOTE 5,'* * * IEIGEN308 HARDCPY ADDRESS &SGSCHDC(14) NOT SPECIFIED*
                IN CONSOLE MACRO AS A VALID OUTPUT CONSOLE'
&SGQUITB SETB  1
.**********************************************************************
.*             **** CK HARDCPY ADDR AS VALID HARDCPY DEVICE ****      *
.E1      ANOP  ********************************************************
&Z       SETC  '&SGSCHDC(14)'(1,3)
         CONVERT TO=HEX,DIGITS=3,VALUE=&Z
         IOCHECK LOOKFOR=&HEX
         AIF   (&HEX EQ 0).E2
&X       SETA  &SGDASPA(&HEX)                                   Z40ASVN
         AIF   (&SGDT04B(&X) AND &SGDU01B(&X) AND &SGDM01B(&X)).F1
.*                                                          1052-7
         AIF   (&SGDT04B(&X) AND &SGDU03B(&X)).F1           3210
         AIF   (&SGDT04B(&X) AND &SGDU04B(&X)).F1           3215
         AIF   (&SGDT06B(&X) AND &SGDU01B(&X) AND (&SGDM01B(&X) OR     *
               &SGDM02B(&X) OR &SGDM04B(&X))).F1            1403-N1,2,7
         AIF   (&SGDT06B(&X) AND &SGDU03B(&X) AND &SGDM01B(&X)).F1
.*                                                          1443-N1
         AIF   (&SGDT06B(&X) AND &SGDU06B(&X)).F1           3211
         AIF   (&SGDT06B(&X) AND &SGDU08B(&X)).F1           3213
         AIF   (&SGDT08B(&X) AND &SGDU08B(&X)).F1           2740
         AIF   (&SGDT05B(&X) AND &SGDU09B(&X)).F1           3284-1,2
         AIF   (&SGDT05B(&X) AND &SGDU07B(&X)).F1           3286-1,2
.E2 MNOTE 5,'* * * IEIGEN309 CONSOLE MACRO, ADDRESS &Z INVALID FOR HARD*
               COPY'
&SGQUITB SETB  1
.**********************************************************************
.*             **** CK SCH ALTCONS SPEC'D AS SEC CONSOLE ****         *
.**********************************************************************
.F1      AIF   (&SGSCNYA(1) EQ 0).H1             NO SECONSLE
         AIF   ('&SGSCHDC(2)' EQ '&SGSCHDC(1)' OR '&SGSCHDC(2)' EQ 'FFF*
               FFF' OR '&SGSCHDC(2)' EQ '').G1   ALTCONS OMITTED
&C       SETA  &SGSCNYA(1)
.F2      AIF   (&C EQ 0).F3
         AIF   ('&SGSCHDC(2)' EQ '&SGSCNCC(&C)').G1 ALT EQ SEC CONSOLE
&C       SETA  &C-1
         AGO   .F2
.F3      ANOP
&Z       SETC  '&SGSCHDC(2)'
         AIF   ('&SGSCHDC(2)'(1,3) NE '&SGSCHDC(2)'(4,3)).F4
&Z       SETC  '&SGSCHDC(2)'(1,3)
.F4 MNOTE 5,'* * * IEIGEN310 CONSOLE ALTCONS  &Z NOT SPECIFIED AS A SEC*
               CONDARY OR MASTER CONSOLE'
&SGQUITB SETB  1
.**********************************************************************
.*             **** CK SCH CONSOLE DUPLICATED IN SEC CONSOLE ****     *
.**********************************************************************
.G1      AIF   ('&SGSCHDC(1)' EQ '').H1          SCH CONSOLE OMITTED
&C       SETA  &SGSCNYA(1)
.G2      AIF   (&C EQ 0).H1
         AIF   ('&SGSCHDC(1)' EQ '&SGSCNCC(&C)').G3   DUPLICATED
&C       SETA  &C-1
         AGO   .G2
.G3      ANOP
&Z       SETC  '&SGSCHDC(1)'
         AIF   ('&SGSCHDC(1)'(1,3) NE '&SGSCHDC(1)'(4,3)).G4
&Z       SETC  '&SGSCHDC(1)'(1,3)
.G4 MNOTE 5,'* * * IEIGEN311 SCHEDULR CONSOLE &Z DUPLICATED IN SECONSLE*
                CONSOLE'
&SGQUITB SETB  1
.**********************************************************************
.*             **** CK CRT CONSOLE REQUIREMENTS      ****             *
.*             **** CRT AND 3270 CONSOLE COUNT       ****             *
.*             **** CK 2740 CONSOLE REQUIREMENTS     ****             *
.*             **** CK SEC ALT SPEC'D AS PRIMARY CON ****             *
.H1      ANOP  ********************************************************
&SS      SETB  1
         AIF   ('&SGSCHDC(1)' EQ '').HE
&Z       SETC  '&SGSCHDC(1)'(1,3)
         AGO   .H3
.H2      AIF   (&XX EQ &SGSCNYA(1)).J1   ALL SECONDARY CONSOLES CHECKED
&XX      SETA  &XX+1                     INCREMENT SEC CONSOLE COUNT
         AIF   ('&SGSCNCC(&XX)' EQ '').HH
&Z       SETC  '&SGSCNCC(&XX)'(1,3)
         AIF   ('&Z' EQ 'JES').H2        NO CHECKS ON JES CONSOLE
         AIF   ('&Z' EQ 'FFF').HG
&II      SETB  1
.H3      CONVERT TO=HEX,DIGITS=3,VALUE=&Z
         IOCHECK LOOKFOR=&HEX
         AIF   (&HEX EQ 0).HD
&X       SETA  &SGDASPA(&HEX)                                   Z40ASVN
         AIF   (NOT (&SGDT05B(&X) AND &SGDU02B(&X))).H5     2260
         AIF   (NOT (&SGDM01B(&X))).H4
         AIF   (&SGDF01B(&X) AND &SGUNF1B(&X)).H4
    MNOTE 5,'* * * IEIGEN312 2260 CONSOLE, ADDRESS &Z, REQUIRES ALPHAME*
               RIC KEYBOARD AND LINE ADDRESSING FEATURES'
&SGQUITB SETB  1
.H4      ANOP
&SGSCHDA(11)   SETA &SGSCHDA(11)+1                          2260 COUNT
         AGO   .HD
.H5      AIF   (NOT (&SGDT05B(&X) AND &SGDU01B(&X))).HA     2250
         AIF   (NOT (&SGDM01B(&X))).H6
         AIF   (&SGDF01B(&X) AND &SGDF07B(&X) AND (&SGDF05B(&X) OR     *
               &SGDF06B(&X))).H9
    MNOTE 5,'* * * IEIGEN313 2250 CONSOLE, ADDRESS &Z, MODEL 1 REQUIRES*
                ALPHAMERIC KEYBOARD, CHARACTER GENERATOR, AND 4K OR 8K *
               BUFFER FEATURES'
&SGQUITB SETB  1
.H6      AIF   (NOT (&SGDM03B(&X))).H9
         AIF   (&SGDF01B(&X)).H7
    MNOTE 5,'* * * IEIGEN314 2250 CONSOLE, ADDRESS &Z, MODEL 3 REQUIRES*
                ALPHAMERIC KEYBOARD FEATURE'
&SGQUITB SETB  1
.H7      AIF   (&SGMENTB(12)).H8      GRAPHICS
    MNOTE 5,'* * * IEIGEN317 ADDRESS &Z, 2250 MODEL 3 CONSOLE, INVALID *
               WITHOUT GAM'
&SGQUITB SETB  1
.H8      AIF   (&SGDNBFA(&X) GE 16).H9             NUMSECT, 16 MINIMUM
&SGDNBFA(&X)   SETA 16                                      16 DEFAULT
    MNOTE 0,'* * * IEIGEN930 NUMSECT, IODEVICE, ADDRESS &Z, HAS BEEN DE*
               FAULTED TO MINIMAL ACCEPTABLE VALUE OF 16'
.H9      ANOP
&SGSCHDA(10)   SETA &SGSCHDA(10)+1                          2250 COUNT
         AGO   .HD
.HA      AIF   (NOT(&SGDT05B(&X) AND &SGDU06B(&X) AND &SGDF01B(&X))).HB
.*                                                          3066
&SG5450A SETA  &SG5450A+1                                   3066 COUNT
.HB      AIF   (NOT (&SGDT08B(&X) AND &SGDU08B(&X))).HC     2740
         AIF   (&SGDF06B(&X)).HD
    MNOTE 5,'* * * IEIGEN315 2740 AS CONSOLE REQUIRES RECORD CHECKING F*
               EATURE'
&SGQUITB SETB  1
         AGO   .HD
.HC      AIF   (NOT (&SGDT05B(&X) AND &SGDU10B(&X))).HC1
&SGSCHDA(18)   SETA &SGSCHDA(18)+1                          3158 COUNT
         AGO   .HD
.HC1     AIF   (NOT (&SGDT05B(&X) AND &SGDU08B(&X) AND &SGDM01B(&X))).H*
               C2                                           3277-1
&SGSCHDA(17)   SETA &SGSCHDA(17)+1                     3277-1 COUNT
         AGO   .HD
.HC2     AIF   (NOT (&SGDT05B(&X) AND &SGDU08B(&X) AND &SGDM02B(&X))).H*
               C3                                           3277-2
&SGSCHDA(18)   SETA &SGSCHDA(18)+1                     3277-2 COUNT
         AGO   .HD
.HC3     AIF   (NOT (&SGDT05B(&X) AND (&SGDU09B(&X) OR &SGDU07B(&X))   *
               AND &SGDM01B(&X))).HC4                  3284-1 3286-1
&SGSCHDA(19)   SETA &SGSCHDA(19)+1                     3284/86-1 COUNT
         AGO   .HD
.HC4     AIF   (NOT (&SGDT05B(&X) AND (&SGDU09B(&X) OR &SGDU07B(&X))   *
               AND &SGDM02B(&X))).HC5            XOX   3284-2 3286-2
&SGSCHDA(20)   SETA &SGSCHDA(20)+1                     3284/86-2 COUNT
         AGO   .HD                                              G51DSPD
.HC5     AIF   (NOT (&SGDT05B(&X) AND &SGDU13B(&X))).HD         G51DSPD
&SGSCHDA(18)   SETA &SGSCHDA(18)+1                              G51DSPD
.HD      AIF   (NOT (&SS)).HF
         AIF   ('&Z' EQ '&SGSCHDC(1)'(4,3)).HE
&Z       SETC  '&SGSCHDC(1)'(4,3)
         AGO   .H3
.HE      ANOP
&SS      SETB  0
         AGO   .H2
.HF      AIF   (NOT (&II)).HH
         AIF   ('&SGSCNCC(&XX)'(1,3) EQ '&SGSCNCC(&XX)'(4,3)).HH
.HG      ANOP
&II      SETB  0
&Z       SETC  '&SGSCNCC(&XX)'(4,3)
         AGO   .H3
.***     CHECK IF ALTERNATE CONSOLE HAS BEEN SPECIFIED FOR A       ***
.***                    SECONDARY CONSOLE                          ***
.HH      AIF   ('&SGSCNAC(&XX)' EQ '').HM   ALTCONS OMITTED
&TT      SETA  &SGSCNYA(1)
.HJ      AIF   (&TT EQ 0).HK
         AIF   (('&SGSCNAC(&XX)' EQ '&SGSCNCC(&TT)') OR ('&SGSCNAC(&XX)*
               ' EQ '&SGSCHDC(1)')).H2
&TT      SETA  &TT-1
         AGO   .HJ
.HK MNOTE 5,'* * * IEIGEN316 CONSOLE ALTCONS, ADDRESS &SGSCNAC(&XX),  H*
               AS NOT BEEN SPECIFIED AS A MASTER OR SECONDARY CONSOLE, *
               OR IS SPECIFIED FOR OUTPUT CAPACITY SECONS ONLY'
.*        MSGIEIGEN316 UPDATED FOR OZ53128                     @ZA53128
&SGQUITB SETB  1
         AGO   .H2
.HM     ANOP             DEFAULT ALTCONS FOR SECONS TO MASTER CONSOLE
&SGSCNAC(&XX) SETC   '&SGSCHDC(1)'      MCONS IS ALTERNATE
  MNOTE 0,'* * * IEIGEN319 ALTCONS DEFAULTED TO MASTER CONSOLE FOR SECO*
               NDARY CONSOLE AT &SGSCNCC(&XX)'
         AGO   .H2
.**********************************************************************
.*             **** VALIDATE O-ONLY CONSOLES ****                     *
.J1      ANOP  ********************************************************
&C5      SETC  'FFF'
&C       SETA  &SGSCNYA(1)
.J2      AIF   (&C EQ 0).K1
&Z       SETC  '&SGSCNCC(&C)'(1,3)
         AIF   ('&Z' NE '&C5').J4
&Z       SETC  '&SGSCNCC(&C)'(4,3)
         CONVERT TO=HEX,DIGITS=3,VALUE=&Z
         IOCHECK LOOKFOR=&HEX
         AIF   (&HEX EQ 0).J3
&X       SETA  &SGDASPA(&HEX)                                   Z40ASVN
         AIF   (&SGDT06B(&X) AND &SGDU01B(&X)).J4           1403
         AIF   (&SGDT06B(&X) AND &SGDU03B(&X)).J4           1443
         AIF   (&SGDT06B(&X) AND &SGDU06B(&X)).J4           3211
         AIF   (&SGDT06B(&X) AND &SGDU08B(&X)).J4           3213
         AIF   (&SGDT05B(&X) AND &SGDU02B(&X)).JA3        2260 @ZA44996
         AIF   (&SGDT05B(&X) AND &SGDU08B(&X)).JA3        3277 @ZA44996
         AIF   (&SGDT05B(&X) AND &SGDU09B(&X)).J4           3284
         AIF   (&SGDT05B(&X) AND &SGDU07B(&X)).J4           3286
.J3 MNOTE 5,'* * * IEIGEN318 O-&Z CONSOLE MUST BE A 1403, 1443, 3211, 3*
               213, 2260, 3277, 3284, OR 3286'
&SGQUITB SETB  1
.J4      ANOP
&C       SETA  &C-1
         AGO   .J2
.JA3 AIF ((&SGDU02B(&X) OR (&SGDU08B(&X) AND &SGDM02B(&X))) AND (NOT (&*
               SGSCSDB(&C) OR &SGSCMSB(&C)))).J3B              @ZA44996
         AGO   .J4                                             @ZA44996
.J3B MNOTE 5,'* * * IEIGEN324 O-&Z CONSOLE REQUIRES USE PARM'  @ZA44996
&SGQUITB SETB  1                                               @ZA44996
         AGO   .J4                                             @ZA44996
.**********************************************************************
.*             **** CHECK SECONSLE CONSOLE INPUT ADDRESS ****         *
.K1      ANOP  ********************************************************
&C       SETA  &SGSCNYA(1)
&C5      SETC  'FFF'
.K2      AIF   (&C EQ 0).KA
         AIF   ('&SGSCNCC(&C)' EQ 'JESJES').K9K  BYPASS CHECKS FOR JES
&ADDRESI SETC  '&SGSCNCC(&C)'(1,3)
&ADDRESO SETC  '&SGSCNCC(&C)'(4,3)
         CONVERT TO=HEX,DIGITS=3,VALUE=&ADDRESI
         IOCHECK LOOKFOR=&HEX
         AIF   ('&ADDRESI' EQ '&C5').K5
         AIF   (&HEX EQ 0).K3
&X       SETA  &SGDASPA(&HEX)           GET DEVICE POINTER      Z40ASVN
         AIF   (&SGDT04B(&X) AND &SGDU01B(&X) AND &SGDM01B(&X)).SETCONA
.*                                                          1052-7
         AIF   (&SGDT04B(&X) AND &SGDU03B(&X)).SETCONA      3210
         AIF   (&SGDT04B(&X) AND &SGDU04B(&X)).SETCONA      3215
         AIF   (&SGDT07B(&X) AND &SGDU02B(&X) AND (&SGDM01B(&X) OR     *
               &SGDM02B(&X))).SETRDRA                       2501-B1,B2
         AIF   (&SGDT07B(&X) AND &SGDU03B(&X) AND &SGDM01B(&X)).SETRDRA
.*                                                          2520-B1
         AIF   (&SGDT07B(&X) AND &SGDU04B(&X) AND &SGDM01B(&X)).SETRDRA
.*                                                          2540R
         AIF   (&SGDT07B(&X) AND &SGDU08B(&X)).SETRDRA      3505
         AIF   (&SGDT07B(&X) AND &SGDU09B(&X)).SETRDRA      3525
         AIF   (&SGDT08B(&X) AND &SGDU08B(&X)).SETTPIA      2740
         AIF   (NOT(&SGDT05B(&X))).K3  ONLY GRAPHICS LEFT TO CHECK
.**********************************************************************
.*            CHECK GRAPHICS TO INSURE IT IS NOT THE INPUT HALF OF A  *
.*            COMPOSITE CONSOLE.                                      *
.**********************************************************************
         AIF   ('&ADDRESI' NE '&ADDRESO').K35
         AIF   (&SGDT05B(&X) AND &SGDU01B(&X) AND (&SGDM01B(&X) OR     *
               &SGDM03B(&X))).SETGRIA                       2250-1,3
         AIF   (&SGDT05B(&X) AND &SGDU02B(&X) AND &SGDM01B(&X)).SETGRIA
.*                                                          2260-1
         AIF   (&SGDT05B(&X) AND &SGDU06B(&X) AND &SGDF01B(&X)).SETGRIA
.*                                                          3066
         AIF   (&SGDT05B(&X) AND &SGDU10B(&X)).SETGRIA          G51DSPD
.*                                                          3158
         AIF   (&SGDT05B(&X) AND &SGDU13B(&X)).SETGRIA          G51DSPD
.*                                                          3036
         AIF   (NOT (&SGDT05B(&X) AND &SGDU08B(&X) AND &SGDM02B(&X))).K*
               3                                            3277-2
         AIF   (&SGDF10B(&X) OR &SGDF11B(&X) OR &SGDF12B(&X) OR        *
               &SGDF13B(&X)).SETGRIA                        KEYBOARD
    MNOTE 5,'* * * IEIGEN301   CONSOLE ADDRESS &ADDRESI,REQUIRES KEYBOA*
               RD TO HAVE INPUT CAPABILITY'
&SGQUITB SETB  1
         AGO   .K4
.K3 MNOTE 5,'* * * IEIGEN320  CONSOLE MACRO, ADDRESS &ADDRESI INVALID F*
               OR SECONDARY CONSOLE INPUT'
&SGQUITB SETB  1
         AGO   .K5
.K35 MNOTE 5,'* * * IEIGEN303 CONSOLE MACRO,GRAPHICS ADDRESS &ADDRESI I*
               NVALID FOR INPUT IN A COMPOSITE CONSOLE'
&SGQUITB SETB  1
         AGO   .K9
.**********************************************************************
.*             **** PFK CHECK ****                                    *
.**********************************************************************
.K4      AIF   (&SGSCNPA(&C) EQ 0).K5
         AIF   (NOT (&SGDT05B(&X) AND &SGDU01B(&X))).K4B    2250
         AIF   (&SGDF02B(&X) OR &SGDF03B(&X)).K5  LIGHTPEN / PRGMKYBD
    MNOTE 5,'* * * IEIGEN322  CONSOLE MACRO, PFK SPECIFICATION FOR 2250*
               , CONSOLE ADDRESS &ADDRESI, REQUIRES PRGMKYBD OR LIGHTPE*
               N FEATURE'
&SGQUITB SETB  1
         AGO   .K5
.*                               CHECK IF 3277, MODEL 2, OR 3158
.K4B     AIF   (&SGDT05B(&X) AND &SGDU08B(&X) AND &SGDM02B(&X)).K5
         AIF   (&SGDT05B(&X) AND &SGDU10B(&X)).K5           3158
         AIF   (&SGDT05B(&X) AND &SGDU13B(&X)).K5          3036 G51DSPD
     MNOTE 5,'* * * IEIGEN304  CONSOLE MACRO, CONSOLE ADDRESS &ADDRESI *
               - PFK SPECIFICATION INVALID'
&SGQUITB SETB  1
.**********************************************************************
.*             **** CHECK SECONSLE ALT CONSOLE OUTPUT ADDR ****       *
.K5      ANOP  ********************************************************
&ADDRESO SETC  '&SGSCNCC(&C)'(4,3)
         AIF   ('&ADDRESI' EQ '&ADDRESO' AND &HEX EQ 0).K9J
         AIF   ('&ADDRESI' EQ '&ADDRESO').K7
         CONVERT TO=HEX,DIGITS=3,VALUE=&ADDRESO
         IOCHECK LOOKFOR=&HEX
         AIF   (&HEX EQ 0).K6
&X       SETA  &SGDASPA(&HEX)           GET DEVICE POINTER      Z40ASVN
         AIF   (&SGDT04B(&X) AND &SGDU01B(&X) AND &SGDM01B(&X)).SETCONB
.*                                                          1052-7
         AIF   (&SGDT04B(&X) AND &SGDU03B(&X)).SETCONB      3210
         AIF   (&SGDT04B(&X) AND &SGDU04B(&X)).SETCONB      3215
         AIF   (&SGDT06B(&X) AND &SGDU01B(&X) AND (&SGDM01B(&X) OR     *
               &SGDM02B(&X) OR &SGDM04B(&X))).SETPTRA       1403-N1,2,7
         AIF   (&SGDT06B(&X) AND &SGDU03B(&X) AND &SGDM01B(&X)).SETPTRA
.*                                                          1443-N1
         AIF   (&SGDT06B(&X) AND &SGDU06B(&X)).SETPTRA      3211
         AIF   (&SGDT06B(&X) AND &SGDU08B(&X)).SETPTRA      3213
         AIF   (&SGDT08B(&X) AND &SGDU08B(&X)).SETTPOA      2740
         AIF   (&SGDT05B(&X) AND &SGDU01B(&X) AND (&SGDM01B(&X) OR     *
               &SGDM03B(&X))).SETGROA                       2250-1,3
         AIF   (&SGDT05B(&X) AND &SGDU02B(&X) AND &SGDM01B(&X)).SETGROA
.*                                                          2260-1
         AIF   (&SGDT05B(&X) AND &SGDU06B(&X) AND &SGDF01B(&X)).SETGROA
.*                                                          3066
         AIF   (NOT (&SGDT05B(&X) AND &SGDU08B(&X) AND &SGDM01B(&X))).K*
               5A                                           3277-1
         AIF   ('&ADDRESI' EQ 'FFF').K9
    MNOTE 5,'* * * IEIGEN324  CONSOLE MACRO, ADDRESS &ADDRESO - 3277 MO*
               DEL 1 IS ONLY VALID AS AN O-ONLY CONSOLE'
&SGQUITB SETB  1
         AGO   .K9
.K5A     AIF   (&SGDT05B(&X) AND &SGDU08B(&X) AND &SGDM02B(&X)).SETGROA
.*                                                          3277-2
         AIF   (&SGDT05B(&X) AND &SGDU09B(&X) AND (&SGDM01B(&X) OR     *
               &SGDM02B(&X))).SETOPTA                       3284-1,2
         AIF   (&SGDT05B(&X) AND &SGDU07B(&X) AND (&SGDM01B(&X) OR     *
               &SGDM02B(&X))).SETOPTA                       3286-1,2
.K6 MNOTE 5,'* * * IEIGEN325  CONSOLE MACRO, ADDRESS &ADDRESO INVALID F*
               OR SECONDARY CONSOLE OUTPUT'
&SGQUITB SETB  1
         AGO   .K9J
.**********************************************************************
.**     INDICATE TYPE OF DEVICE  USED AS A CONSOLE FOR SGIEA401      **
.**********************************************************************
.SETCONA ANOP                    SET INPUT CONSOLE INDICATOR
&SGSCHDB(1)    SETB 1            SET FOR A 1052/3210/3215
         AGO   .K4               GO TO PFK CHECK
.SETCONB ANOP                    SET OUTPUT CONSOLE INDICATOR
&SGSCHDB(1)    SETB 1            SET FOR A 1052/3210/3215
         AGO   .K8               GO SET VALID HARDCOPY SWITCH
.SETRDRA ANOP                    SET READER INDICATOR
&SGSCHDB(2)    SETB 1            SET FOR A 2501/2520/2540R/3505/3525
         AIF   ('&ADDRESI' EQ '&ADDRESO').K6   INVALID OUTPUT DEVICE
         AGO   .K4               GO TO PFK CHECK
.SETPTRA ANOP                    SET PRINTER INDICATOR
&SGSCHDB(3)    SETB 1            SET FOR A 1403/1443/3211/3213
         AGO   .K8               GO SET VALID HARDCOPY SWITCH
.SETGRIA ANOP                    SET INPUT GRAPHICS INDICATOR
&SGSCHDB(5)    SETB 1            SET FOR A 2250/2260/3066/3158/3277
         AGO   .K4               GO TO PFK CHECK
.SETGROA ANOP                    SET OUTPUT GRAPHICS INDICATOR
&SGSCHDB(5)    SETB 1            SET FOR A 2250/2260/3066/3158/3277
         AGO   .K9               GO TO LIMITED CAPACITY CHECK
.SETOPTA ANOP                    SET OCR/MICR INDICATOR
&SGSCHDB(5)    SETB 1            SET FOR A 2250/2260/3066/3158/3277
         AGO   .K8               GO SET VALID HARDCOPY SWITCH
.SETTPIA ANOP                    SET TP INPUT INDICATOR
&SGSCHDB(13)   SETB 1            SET FOR A 2740
         AGO   .K4               GO TO PFK CHECK
.SETTPOA ANOP                    SET TP OUTPUT INDICATOR
&SGSCHDB(13)   SETB 1            SET FOR A 2740
         AGO   .K8               GO SET VALID HARDCOPY SWITCH
.**********************************************************************
.*                 VERIFY AND SET HARDCOPY INDICATOR                  *
.**********************************************************************
.K7      AIF   (NOT(&SGDT04B(&X) OR &SGDT08B(&X))).K9  1052-7/3210/3215
.K8      ANOP                    * SWITCH FOR VALID *  2740/1403-N1,2,7
&AC      SETB  1                 * HARDCOPY CONSOLE *  1443-N1/3211
.*                               * CAPABILITY       *  3284/3286
.**********************************************************************
.*             **** CK LIMITED CAPACITY ****                          *
.**********************************************************************
.K9      AIF   (NOT (&SGSCSDB(&C) OR &SGSCMSB(&C))).K9A     SD / MS
         AIF   (&SGDT05B(&X) AND (&SGDU02B(&X) OR  &SGDU08B(&X) OR     *
               &SGDU10B(&X) OR &SGDU13B(&X))).K9A
.*                                2260/3277/3158/3036          @ZA34348
    MNOTE 5,'* * * IEIGEN326  CONSOLE MACRO, ADDRESS &ADDRESO INVALID A*
               S A LIMITED CAPACITY CONSOLE'
&SGQUITB SETB  1
.**********************************************************************
.*             **** AREA CHECK AND DEFAULT ****                       *
.**********************************************************************
.K9A     AIF   (&SGAR12A(&C) NE 0 AND NOT (&SGDT05B(&X) AND (&SGDU01B(&*
               X) OR &SGDU02B(&X) OR &SGDU06B(&X) OR (&SGDU08B(&X) AND *
               &SGDM02B(&X)) OR &SGDU10B(&X) OR &SGDU13B(&X)))).K9H
.*                       AREA 2250 2260 3066 3158 3277-2 3036
         AIF   (NOT (&SGDT05B(&X) AND (&SGDU01B(&X) OR &SGDU06B(&X)    *
               OR &SGDU13B(&X)))).K9B          2250 3066 3036
         AIF   (&SGDU01B(&X) AND &SGAR12A(&C) GT 47).K9H    2250 ERR
         AIF   (&SGDU06B(&X) AND &SGAR12A(&C) GT 30).K9H    3066 ERR
         AIF   (&SGAR12A(&C) NE 0).K9J
&SGAR01A(&C)   SETA 14                                      DEFAULT 14
&SGAR12A(&C)   SETA 14
         AGO   .K9D
.K9B     AIF   (NOT (&SGDT05B(&X) AND &SGDU02B(&X))).K9E    2260
         AIF   (&SGSCMSB(&C) AND &SGAR12A(&C) NE 0).K9G     MS/AREA ERR
         AIF   (&SGSCMSB(&C)).K9J
         AIF   (NOT &SGSCSDB(&C)).K9C                       STATUS DIS
         AIF   (&SGAR12A(&C) GT 11).K9H                     2260/SD ERR
         AIF   (&SGAR12A(&C) NE 0).K9J
&SGAR01A(&C)   SETA 11                                      DEFAULT 11
&SGAR12A(&C)   SETA 11
         AGO   .K9D
.K9C     AIF   (&SGAR12A(&C) GT 8).K9H                      2260 ERR
         AIF   (&SGAR12A(&C) NE 0).K9J
&SGAR01A(&C)   SETA 8                                       DEFAULT 8
&SGAR12A(&C)   SETA 8
.K9D MNOTE 0,'* * * IEIGEN931  CONSOLE MACRO, CONSOLE ADDRESS &ADDRESO,*
               DISPLAY AREA DEFAULTED TO LENGTH OF &SGAR01A(&C)'
         AGO   .K9J
.K9E     AIF   (NOT (&SGDT05B(&X) AND (&SGDU08B(&X) AND &SGDM02B(&X))  *
               OR &SGDU10B(&X) OR &SGDU13B(&X))).K9J            G51DSPD
.*                                         3066 3158 3277-2     G51DSPD
         AIF   (&SGSCMSB(&C) AND &SGAR12A(&C) NE 0).K9G     MS/AREA ERR
         AIF   (&SGSCMSB(&C)).K9J
         AIF   (NOT &SGSCSDB(&C)).K9F                       STATUS DIS
         AIF   (&SGAR12A(&C) GT 23).K9H              3158/3277-2/SD ERR
         AIF   (&SGAR12A(&C) NE 0).K9J
&SGAR01A(&C)   SETA 13                                      DEFAULT 13
&SGAR02A(&C)   SETA 10                                      DEFAULT 10
&SGAR12A(&C)   SETA 23
    MNOTE 0,'* * * IEIGEN933 CONSOLE MACRO, CONSOLE ADDRESS &ADDRESO,  *
               DISPLAY AREAS DEFAULTED TO LENGTHS OF 13,10'
         AGO   .K9J
.K9F     AIF   (&SGAR12A(&C) GT 19).K9H                 3158/3277-2 ERR
         AIF   (&SGAR12A(&C) NE 0).K9J
&SGAR01A(&C)   SETA 14                                      DEFAULT 14
&SGAR12A(&C)   SETA 14
         AGO   .K9D
.K9G MNOTE 5,'* * * IEIGEN327 CONSOLE MACRO, CONSOLE ADDRESS &ADDRESO, *
               DISPLAY AREA INVALID FOR CONSOLE IN MESSAGE STREAM MODE'
&SGQUITB SETB  1
         AGO   .K9J
.K9H MNOTE 5,'* * * IEIGEN328  CONSOLE MACRO, DISPLAY AREA DEFINED INVA*
               LID FOR CONSOLE ADDRESS &ADDRESO'
&SGQUITB SETB  1
         AGO   .K9K
.**********************************************************************
.*          **** INTEGRATED OPERATOR CONSOLE -- SECONDARY ****        *
.**********************************************************************
.K9J     AIF   ('&SGSIOCC(&C)' EQ '').K9K    SECONSLE IOC NULL
     MNOTE 0,'* * * IEIGEN679 INTEGRATED OPERATOR CONSOLE (IOC) NO LONG*
               ER SUPPORTED'                                   @ZA60599
.**********************************************************************
.*                  **** CHECK HARDCOPY INDICATOR ****                *
.**********************************************************************
.K9K     ANOP
&C       SETA  &C-1
         AGO   .K2
.KA      AIF (&SGSCHDB(36)).MEND   HDCPY=SYSLOG                @AZ10499
         AIF   (&AC).MEND                                      @AZ10499
    MNOTE 5,'* * * IEIGEN329 AT LEAST ONE CONSOLE WITH HARDCOPY CAPABIL*
               ITY IS REQUIRED'
&SGQUITB SETB  1
         AGO   .MEND
.ER1 MNOTE 5,'* * * IEIGEN330 AT LEAST TWO CONSOLE MACROS ARE REQUIRED'
&SGQUITB SETB 1
.MEND    MEND
