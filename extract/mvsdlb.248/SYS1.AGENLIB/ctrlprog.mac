         MACRO
         CTRLPROG   &OPTIONS=,&SQA=,&APFLIB=,&VRREGN=,&REAL=,          *
               &TZ=,&ASCII=EXCLUDE,&CSA=,&STORAGE=0,&WARN=0,           *
               &DEBTINC=80,&DEBTSZE=80,&ACRCODE=YES,                   *
               &PAGNUM=(1,1)                                    Z40WSVN
         COPY  SGSYSPAK
         LCLA  &A,&I,&X,&K,&N
         LCLB  &T
         LCLC  &M,&PM
.***** START OF SPECIFICATIONS **************************************
.*
.* MODULE NAME - CTRLPROG
.*
.* DESCRIPTIVE NAME - CONTROL PROGRAM GENERATION
.*
.* COPYRIGHT - NONE
.*
.* STATUS - CHANGE LEVEL 03
.*
.* FUNCTION -
.*       THE CTRLPROG MACRO IS USED TO SPECIFY THE CONTROL PROGRAM
.*       SUPERVISOR OPTIONS. DEFAULTS FOR REAL, CSA, VRREGN AND SQA
.*       ARE NOT TO BE SET HERE - RE DCRR 20867
.*       WARN=N IS DEFAULTED HERE - ICR CODE
.*
.* NOTES - N/A
.*
.* MODULE TYPE - MACRO
.*
.* ENTRY POINT - N/A
.*
.* INPUT -
.*         THE FOLLOWING PARAMETERS MAY BE CODED ON THE CALL TO
.*         CTRLPROG
.*             &OPTIONS - SPECIFIES WHICH OF THE FOLLOWING CONTROL
.*                        PROGRAM OPTIONS ARE TO BE INCLUDED: DEVSTAT,
.*                        RER, BLDL, RDE, CRH, AND VM
.*             &SQA     - SPECIFIES THE NUMBER OF 64K SEGMENTS
.*                        RESERVED FOR SYSTEM QUEUE SPACE.
.*             &APFLIB  - SPECIFIES THE AUTHORIZED LIBRARIES ALONG
.*                        WITH THEIR VOLIDS (LIMIT OF 16).
.*             &VRREGN  - SPECIFIES THE MINIMUM AMOUNT OF REAL
.*                        STORAGE WHICH MAY BE ALLOCATED AT ANY
.*                        GIVEN TIME TO A JOB REQUIRING V=R SPACE.
.*             &REAL    - NUMBER OF 1K BLOCKS TO BE RESERVED FOR
.*                        THE V=R AREA.
.*             &TZ      - ZONE DEVIATION FROM GREENWICH MEAN TIME.
.*             &ASCII   - ASCII CHARACTER SET INCLUDED
.*             &CSA     - SIZE OF THE COMMON SERVICE AREA
.*             &STORAGE - ADDRESS OF HIGHEST BLOCK OF STORAGE
.*             &DEBTINC - INCREMENT BY WHICH THE DEB TABLE IS
.*                        EXPANDED.
.*             &DEBTSZE - INITIAL SIZE OF THE DEB TABLE.
.*             &ACRCODE - SYSTEM REQUIRES ACR AND VARY CPU CODE
.*             &WARN    - ICR CODE - OPTIONAL VALUE
.*             &PAGNUM  - MAX. NUMBER OF PAGE AND SWAP DATA     Z40WSVN
.*                        SETS THAT MAY BE ADDED BY OPERATOR    Z40WSVN
.*
.* OUTPUT -
.*       THE GLOBALS USED ARE -
.*             &SGCPRGA(2)   - WARN=0 OR (1 THRU MAX OF 16777215)
.*             &SGCPRGA(3)   - DEB TABLE SIZE
.*             &SGCPRGA(4)   - DEB TABLE INCREMENT
.*             &SGCPRGA(6)   - SQA SIZE IN 64K SEGMENTS
.*             &SGCPRGB(8)   - VS2 FLAG
.*             &SGCPRGB(9)   - ACR CODE AND VARY CPU CODE INCLUDED
.*             &SGCPRGB(12)  - OPTIONS=CRH FOR 168MP            Z30CUOS
.*             &SGCPRGB(13)  - WARN VALUE GT ZER0
.*             &SGCPRGC(1-16)- DSNAMES OF APF LIBRARIES
.*             &SGCPRAC(1-16)- VOLIDS OF APF LIBRARIES
.*             &SGCSTOA(1)   - ADDR OF HIGHEST BLOCK OF STORAGE
.*             &SGMENTB(5)   - ENTRY SWITCH
.*             &SGQUITB      - QUIT SWITCH
.*             &SGMENTB(87)  - OPTIONS=DEVSTAT - SMARTNIP
.*             &SGTIMEA      - MODIFICATION TO TIME ZONE
.*             &SGSUPRA(1)   - TRACE TABLE ENTRIES
.*             &SGSUPRA(3)   - REAL STORAGE REQUIRED FOR 1:1 REGION
.*             &SGSUPRA(5)   - NO. OF ADDABLE PAGE DATA SETS    Z40WSVN
.*             &SGSUPRA(6)   - NO. OF ADDABLE SWAP DATA SETS    Z40WSVN
.*             &SGSUPRA(11)  - VIRTUAL REGION SIZE
.*             &SGSUPRA(12)  - COMMON SERVICE AREA SIZE
.*             &SGSUPRB(9)   - OPTIONS=VM VM/370
.*             &SGSUPRB(20)  - FIXED BLDL TABLE
.*             &SGSUPRB(23)  - STORAGE PROTECTION FEATURE
.*             &SGSUPRB(26)  - OLTEP
.*             &SGSUPRB(28)  - MCH
.*             &SGSUPRB(29)  - CCH
.*             &SGSUPRB(32)  - REDUCED ERROR RECOVERY
.*             &SGSUPRB(33)  - DDR
.*             &SGSUPRB(41)  - ASCII IN SYSTEM
.*             &SGSUPRB(43)  - RELIABILITY DATA EXTRACTOR
.*             &SGSUPRB(45)  - FETCH PROTECT
.*
.*       THE LOCALS USED ARE -
.*             &A    - INDEX TO SUBLIST FOR &OPTIONS AND &TZ
.*             &I    - NUMBER OF LIBRARIES IN &APFLIB, USED AS INDEX
.*             &X    - NUMBER OF LIBRARIES AND VOLIDS IN &APFLIB
.*             &K    - NUMBER OF CHARACTERS IN AN &APFLIB PARAMETER
.*             &N    - NUMBER OF PARAMETERS IN &APFLIB
.*             &T    - ON FOR W, OFF FOR E
.*             &M    - SPEC WHETHER &DEBSIZE EXACT OR ROUNDED
.*             &PM   - EAST OR WEST OF GREENWICH
.*
.* EXIT, NORMAL - N/A
.*
.* EXIT, ERROR - N/A
.*
.* EXTERNAL REFERENCES - N/A
.*
.* TABLES - N/A
.*
.* MACROS - NONE
.*
.* CHANGE ACTIVITY - MODIFIED FOR Y02036,Z03CUOS,Z40WSVN
.****** END OF SPECIFICATIONS ***************************************
         AIF   (&SGMENTB(5)).ERR
&SGMENTB(5)    SETB  1
&SGCPRGB(8)    SETB  1        SET AOS/2 FLAG
&SGSUPRB(23)   SETB  1        PROTECT STANDARD
&SGSUPRB(26)   SETB  1        OLTEP STANDARD
&SGSUPRB(28)   SETB  1        RMS STANDARD
&SGSUPRB(29)   SETB  1        CCH STANDARD
&SGSUPRB(33)   SETB  1        DDR STANDARD
         AIF   (T'&OPTIONS EQ 'O').VRREGN
    MNOTE *,'     THE FOLLOWING OPTION(S) HAVE BEEN SELECTED'
.OPT     AIF   (&A EQ N'&OPTIONS).VRREGN
&A       SETA  &A+1
         AIF   ('&OPTIONS(&A)' EQ 'BLDL').OPT1
         AIF   ('&OPTIONS(&A)' EQ 'DEVSTAT').OPT2
         AIF   ('&OPTIONS(&A)' EQ 'RER').OPT3
         AIF   ('&OPTIONS(&A)' EQ 'RDE').OPT4
         AIF   ('&OPTIONS(&A)' EQ 'CRH').OPT5                   Z30CUOS
         AIF   ('&OPTIONS(&A)' EQ 'VM').OPT6
    MNOTE 5,'* * * IEICTR101 OPTIONS VALUE &OPTIONS(&A) INVALID'
&SGQUITB SETB  1
         AGO   .OPT
.OPT1 MNOTE *,'        FIXED BLDL TABLE'
&SGSUPRB(20)   SETB  1
         AGO   .OPT
.OPT2 MNOTE *,'        NIP SUPPORT'
&SGMENTB(87)   SETB  1
         AGO   .OPT
.OPT3 MNOTE *,'        REDUCED TAPE ERROR RECOVERY'
&SGSUPRB(32)   SETB  1
         AGO   .OPT
.OPT4 MNOTE *,'        RELIABILITY DATA EXTRACTOR'
&SGSUPRB(43)   SETB  1
         AGO   .OPT
.OPT5 MNOTE *,'        CHANNEL RECONFIGURATION HARDWARE'        Z30CUOS
&SGCPRGB(12)   SETB  1                                          Z30CUOS
         AGO   .OPT                                             Z30CUOS
.OPT6 MNOTE *,'        VM/370 OPTION'
&SGSUPRB(9)  SETB  1
         AGO   .OPT
.VRREGN         AIF  (T'&VRREGN EQ 'O').VRREGN2
                AIF (T'&VRREGN EQ 'N').VRREGN1
     MNOTE 5,'* * * IEICTR104 VRREGN VALUE &VRREGN INVALID'
&SGQUITB SETB   1
     AGO  .QSP
.VRREGN1 ANOP
&SGSUPRA(11)  SETA  &VRREGN
 MNOTE *,'* * * &SGSUPRA(11) BLOCKS OF V=R ALLOCATED TO VRREGN'
     AGO  .QSP
.VRREGN2  ANOP
     MNOTE 0,'* * * IEICTR901 64 BLOCKS OF V=R ALLOCATION ARE THE DEFAU*
               LT TAKEN'
.QSP     AIF   (T'&SQA EQ 'N').QSP1
         AIF   (T'&SQA NE 'O').QSP0
     MNOTE 0,'     1 64K SEGMENT RESERVED SYSTEM QUEUE SPACE DEFAULT'
         AGO   .REAL
.QSP0    ANOP
    MNOTE 5,'* * * IEICTR102 SQA VALUE &SQA INVALID'
&SGQUITB SETB  1
         AGO   .REAL
.QSP1    ANOP
     MNOTE *,'     &SQA 64K SEGMENTS RESERVED SYSTEM QUEUE SPACE'
&SGCPRGA(6)    SETA  &SQA
.REAL    ANOP
         AIF   (T'&REAL EQ 'N').REAL1     NUMERIC?
         AIF   (T'&REAL EQ 'O').REAL2
        MNOTE 5,'* * * IEICTR109 REAL VALUE &REAL INVALID'
&SGQUITB SETB  1
         AGO   .STORAGE
.REAL1   ANOP
.*
.*     CHECK SPECIFIED VALUE AND ROUND UP TO MULTIPLE OF 4
.*
&SGSUPRA(3) SETA &REAL
     AIF  (&SGSUPRA(3)/4*4 NE &SGSUPRA(3)).RNDUP
     MNOTE *,'    &SGSUPRA(3) 1K BLOCKS OF VIRTUAL=REAL SPACE'
         AGO   .STORAGE
.RNDUP    ANOP
&SGSUPRA(3) SETA   (&REAL/4*4+4)
     MNOTE 0,' &SGSUPRA(3) 1K BLOCKS OF VIRTUAL=REAL SPACE '
         AGO   .STORAGE
.REAL2   ANOP
     MNOTE 0,' 76 1K BLOCKS OF VIRTUAL=REAL SPACE DEFAULT'
.*******************************************************************
.*        ADDRESS OF HIGHEST BLOCK OF STORAGE
.*******************************************************************
.STORAGE        AIF  (T'&STORAGE EQ 'O').STG2
                AIF  (T'&STORAGE EQ 'N').STG1
.ERR6   MNOTE 5,'* * * IEICTR106 STORAGE VALUE &STORAGE INVALID'
&SGQUITB SETB   1
     AGO  .CSA
.STG1 ANOP
       AIF   (&STORAGE LT 0 OR &STORAGE GT 16777216).ERR6
&SGCSTOA(1)  SETA  &STORAGE
 MNOTE *,'* * * &STORAGE IS ADDRESS OF HIGHEST BLOCK OF STORAGE'
     AGO  .CSA
.STG2  ANOP
   MNOTE 0,'* * * IEICTR902 ACTUAL AMOUNT OF STORAGE ONLINE AT IPL IS H*
               IGHEST ADDRESS'
.CSA ANOP
     AIF  (T'&CSA EQ 'O').CSA1
&SGSUPRA(12)  SETA  &CSA
    MNOTE *,'  &SGSUPRA(12) 1K BLOCKS RESERVED FOR COMMON SERVICE AREA'
         AGO   .APFLIB
.CSA1    ANOP
  MNOTE 0,'  100 1K BLOCKS DEFAULT FOR COMMON SERVICE AREA'
.**********************************************************************
.**************           APFLIB VALUES SET           *****************
.**********************************************************************
.APFLIB  ANOP
         AIF   (T'&APFLIB EQ 'O').TZONE   NO APFLIB VALUES
&N       SETA  N'&APFLIB            NUMBER IN SUBLIST
         AIF   (&N GT 32).NOAPF     LIMIT IS 16 LIBRARIES AND VOLIDS
&I       SETA  1                    COUNT OF LIBRARIES
&X       SETA  1                    COUNT OF LIBRARIES AND VOLIDS
.APFLUP  ANOP
&K       SETA  K'&APFLIB(&X)        NUMBER OF CHARS IN PARM
         AIF   (&K LE 44).SETGLOB   SIZE IS VALID
         MNOTE 5,'* * * IEICTR103 &APFLIB(&X) IS INVALID FOR APFLIB'
&SGQUITB SETB  1
&I       SETA  &I+1                 INCREMENT LIBRARY COUNT
&X       SETA  &X+2                 INCREMENT PARAMETER COUNT
         AIF   (&X LT &N).APFLUP    CHECK FOR NEXT LIBRARY
         AGO   .TZONE
.SETGLOB ANOP                       SET GLOBALS FOR VALID LIBRARY
&SGCPRGC(&I)   SETC  '&APFLIB(&X)'  PDS NAME
&X       SETA  &X+1                 INCREMENT PARAMETER COUNT
&K       SETA  K'&APFLIB(&X)        NO. OF CHARS IN VOLID
         AIF   (&K LT 1 OR &K GT 6).A1ERR   INVALID VOLID
&SGCPRAC(&I)   SETC  '&APFLIB(&X)'  VOLID
  MNOTE *,'     APF LIBRARY &SGCPRGC(&I) ON VOLUME &SGCPRAC(&I)  '
         AGO   .NXTAPF
.A1ERR   ANOP
  MNOTE 5,'* * * IEICTR107 &APFLIB(&X) IS INVALID FOR VOLID'
&SGQUITB SETB  1                    ERROR CONDITION
.NXTAPF  ANOP
&X       SETA  &X+1                 INCREMENT PARAMETER COUNT
&I       SETA  &I+1                 INCREMENT LIBRARY COUNT
         AIF   (&X LT &N).APFLUP    ANY MORE LIBRARIES
         AGO   .TZONE
.NOAPF    ANOP
    MNOTE 5,'* * * IEICTR105 INVALID NUMBER OF PARAMETERS IN APFLIB'
&SGQUITB  SETB 1
.TZONE   AIF   (T'&TZ NE 'O').TZ1
.TZ0 MNOTE *,'     GREENWICH MEAN TIME ASSUMED'
         AGO   .ASCII
.TZ1     ANOP
&A       SETA  1
         AIF   (N'&TZ LT 2).TZERR
         AIF   ('&TZ(1)' NE 'E' AND '&TZ(1)' NE 'W').TZERR
&T       SETB  ('&TZ(1)' EQ 'W')
.TZ2     AIF   (&A EQ N'&TZ).TD
&A       SETA  &A+1
         AIF   (T'&TZ(&A) NE 'N').TZERR      TIME MUST BE NUMERIC
         AIF   (&TZ(&A) LT 0).TZERR
         AIF   (&A EQ 3).TZ3
         AIF   (&TZ(2) GT 12).TZERR     MAX OF 12 HOURS
         AGO   .TZ4
.TZ3     AIF   (&TZ(3) GT 59).TZERR
         AIF   (&TZ(2) EQ 12 AND &TZ(3) NE 0).TZERR
.TZ4     AIF   (&T).TZ5
&SGTIMEA(&A-1) SETA &TZ(&A)
&PM      SETC  'EAST'
         AGO   .TZ2
.TZ5     ANOP
&SGTIMEA(&A-1) SETA &SGTIMEA(&A-1)-&TZ(&A)
&PM      SETC  'WEST'
         AGO   .TZ2
.TZERR MNOTE 5,'* * * IEICTR113 TZ VALUE &TZ(&A) INVALID'
&SGQUITB SETB  1
         AGO   .ASCII
.TD      AIF   (&SGTIMEA(1) EQ 0 AND &SGTIMEA(2) EQ 0).TZ0
  MNOTE *,'     DEVIATION FROM GMT -- &SGTIMEA(1) HRS, &SGTIMEA(2) MIN *
               &PM'
.ASCII   AIF   ('&ASCII' EQ 'EXCLUDE').ASEX
         AIF   ('&ASCII' EQ 'INCLUDE').ASIN
    MNOTE 5,'* * * IEICTR124 ASCII VALUE &ASCII INVALID'
&SGQUITB SETB  1
         AGO   .DEBTS
.ASEX MNOTE *,'     THE ASCII TRANSLATE SVC IS EXCLUDED'
         AGO   .DEBTS
.ASIN MNOTE *,'     THE ASCII TRANSLATE SVC IS INCLUDED'
&SGSUPRB(41)   SETB  1
.DEBTS   AIF   (T'&DEBTSZE NE 'N').DE1            MUST BE NUMERIC
         AIF   (&DEBTSZE LT 8 OR &DEBTSZE GT 34760).DE1
         AIF   (&DEBTSZE NE &DEBTSZE/8*8).DEB1    MUST BE MULT. OF 8
&M          SETC ''
&SGCPRGA(3) SETA &DEBTSZE
         AGO   .DM1
.DEB1    ANOP
&M          SETC 'ROUNDED'
&SGCPRGA(3) SETA (&DEBTSZE/8+1)*8                 ROUND UPWARD
.DM1 MNOTE *,'     INITIAL DEB TABLE SIZE IS &SGCPRGA(3) BYTES &M'
         AGO   .DEBTI
.DE1 MNOTE 5,'* * * IEICTR125 DEBTSZE VALUE &DEBTSZE INVALID'
&SGQUITB SETB  1
.DEBTI   AIF   (T'&DEBTINC NE 'N').DE2            MUST BE NUMERIC
         AIF   (&DEBTINC LT 8 OR &DEBTINC GT 32752).DE2
         AIF   (&DEBTINC NE &DEBTINC/8*8).DEB2    MUST BE MULT. OF 8
&M          SETC ''
&SGCPRGA(4) SETA &DEBTINC
         AGO   .DM2
.DEB2    ANOP
&M          SETC 'ROUNDED'
&SGCPRGA(4) SETA (&DEBTINC/8+1)*8                 ROUND UPWARD
.DM2 MNOTE *,'     DEB TABLE INCREMENT IS &SGCPRGA(4) BYTES &M'
         AGO   .SYSSEC
.DE2 MNOTE 5,'* * * IEICTR126 DEBTINC VALUE &DEBTINC INVALID'
&SGQUITB SETB  1
         AGO   .SYSSEC
.ERR MNOTE 5,'* * * IEICTR100 CTRLPROG MACRO USED PREVIOUSLY'
&SGQUITB SETB  1
.SYSSEC  ANOP  LEAVE IN AOS2 ONLY IF &SGSUPRB(45) NEEDED FOR FETCH
.*                                      PROTECTION FOR AN INNER MACRO
&SGSUPRB(45)  SETB 1
         AIF   ('&ACRCODE' EQ 'NO').WARN
         AIF   ('&ACRCODE' NE 'YES').ERR9
     MNOTE *,'     ACRCODE INCLUDED'
&SGCPRGB(9) SETB 1
         AGO   .WARN
.ERR9    MNOTE 5,'* * * IEICTR127 ACRCODE VALUE NOT YES OR NO'
&SGQUITB SETB  1
.WARN    AIF   (T'&WARN NE 'N').ERR10   WARN VALUE A DECIMAL NUMBER
         AIF   (&WARN EQ 0).WARN1       WARN VALUE EQ ZERO
         AIF   (&WARN LT 1 OR &WARN GT 16777215).ERR10
&SGCPRGB(13)   SETB 1                       INDICATE WARN IN SYSTEM
.WARN1   ANOP
&SGCPRGA(2)    SETA &WARN                   SET WARN VALUE
         AGO   .PAGNUM                      NEXT OPERAND        Z40WSVN
.ERR10   MNOTE 5,'* * * IEICTR128 WARN VALUE &WARN INVALID'
&SGQUITB SETB  1                            SET QUIT BIT
.PAGNUM  ANOP                                                   Z40WSVN
&SGSUPRA(5) SETA 1                      DEFAULT PAGE DS'S       Z40WSVN
&SGSUPRA(6) SETA 1                      DEFAULT SWAP DS'S       Z40WSVN
         AIF (N'&PAGNUM EQ 2).PAG1      MUST HAVE 2 OPERANDS    Z40WSVN
 MNOTE 5,'* * * IEICTR130 PAGNUM MUST HAVE 2 PARAMETERS'        Z40WSVN
&SGQUITB SETB  1                                                Z40WSVN
         AGO   .MEND                    QUIT                    Z40WSVN
.PAG1    AIF (&PAGNUM(1) GE 0 AND &PAGNUM(1) LE 64).PAG2        Z40WSVN
 MNOTE 5,'* * * IEICTR131 PAGNUM VALUE -&PAGNUM(1)- INVALID'    Z40WSVN
&SGQUITB SETB  1                                                Z40WSVN
         AGO   .PAG3                    TRY NEXT                Z40WSVN
.PAG2 MNOTE *,'     MAX ADDABLE PAGE DATASETS &PAGNUM(1)'       Z40WSVN
&SGSUPRA(5) SETA &PAGNUM(1)             SET PP                  Z40WSVN
.PAG3    AIF (&PAGNUM(2) GE 0 AND &PAGNUM(2) LE 9).PAG4         Z40WSVN
 MNOTE 5,'* * * IEICTR132 PAGNUM VALUE -&PAGNUM(2)- INVALID'    Z40WSVN
&SGQUITB SETB  1                                                Z40WSVN
         AGO   .MEND                    QUIT                    Z40WSVN
.PAG4 MNOTE *,'     MAX ADDABLE SWAP DATASETS &PAGNUM(2)'       Z40WSVN
&SGSUPRA(6) SETA &PAGNUM(2)             SET PP                  Z40WSVN
.MEND    MEND                                                   Z40WSVN
