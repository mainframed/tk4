         MACRO
         SGIEC301
         COPY  SGGBLPAK            COPY THE GLOBAL PACK
         LCLA  &I,&W
         LCLC  &C
.******* START OF SPECIFICATIONS **************************************
.*
.* MODULE NAME - SGIEC301
.*
.* DESCRIPTIVE NAME - INCLUDE NUCLEUS MODULES FOR SYSGENED DEVICES
.*
.* COPYRIGHT - NONE
.*
.* STATUS - CHANGE LEVEL 00
.*
.* FUNCTION -
.*   THIS MACRO SCANS THE DIT. FOR EVERY ENTRY WHICH HAS BEEN
.*   GENERATED, THE NUCLEUS MODULES SPECIFED IN THE ENTRY ARE
.*   INCLUDED INTO THE SYSTEM BY PUNCHING LINKAGE EDITOR
.*   CONTROL STATEMENTS.
.*
.*   THIS MACRO IS INVOKED BY GENERATE.
.*
.* NOTES - N/A
.*
.* MODULE TYPE - MACRO
.*
.* ENTRY POINT - N/A
.*
.* INPUT -
.*         NO PARAMETERS ARE PASSED DIRECTLY.
.*
.*         THE FOLLOWING GLOBALS ARE USED-
.*           &SGCPRGB(8)    - VS2 SYSTEM BEING GENERATED
.*           &SGDITDA       - NUMBER OF DIT ENTRIES TO BE SEARCHED
.*           &SGGENDB(1-X)  - DEVICE OF THIS GENERIC HAS BEEN GENNED
.*           &SGNUCDC(1-X)  - DLIB NAME FOR NUC MODULES
.*           &SGNUCMC(1-X)  - NUC MODULE NAMES TO BE INCLUDED
.*              (WHERE X IS THE VALUE IN &SGDITDA)
.*
.* OUTPUT -
.*       THE FOLLOWING GLOBALS ARE SET -
.*           &SGQUITB         - QUIT SWITCH IF AN INVALID NAME
.*                              LIST IS FOUND
.*
.*
.*       LOCALS USED ARE -
.*          &I - USED AS LOOP VARIABLE THROUGH DIT. POINTS TO
.*               ENTRY BEING PROCESSED.
.*          &W - WORK VARIABLE. USED TO VALIDITY CHECK LENGTH
.*               OF NUCLEUS NAMES. ALSO USED TO LOOP THROUGH
.*               NUCLEUS CHARACTER STRINGS.
.*          &C - CHARACTER WORK VARIABLE. USED TO CONTAIN CURRENT
.*               NUCLEUS NAME TO BE PUNCHED.
.*
.*
.* EXIT, NORMAL - N/A
.*
.* EXIT, ERROR - N/A
.*
.* EXTERNAL REFERENCES - N/A
.*
.* TABLES - DEVICE INFORMATION TABLE (DIT)
.*
.* MACROS - N/A
.*
.* CHANGE ACTIVITY - N/A
.*
.*A-000000-999999                                              @G64HPLP
.******* END OF SPECIFICATIONS ****************************************
.LP      ANOP                      PROCESS NAMES IN DIT
&I       SETA  &I+1                INCREMENT LOOP VARIABLE
         AIF   (&I GT &SGDITDA).MEND EXIT IF DONE WITH DIT
.* IF THE DEVICE HAS NOT BEEN SYSGENED GO TO NEXT ENTRY. IF THE
.* DEVICE IS DUMMY (I=76) THEN &SGGENDB WILL NOT BE ON AND THE
.* ENTRY SHOULD BE PROCESSED TO GET DUMMY SUPPORT INTO THE
.* SYSTEM. THE &SGGENDB CANNOT BE SET FOR DUMMY BECAUSE ALLOCATION
.* REQUIRES IT. DUMMY DEVICES ARE PROCESSED FOR ALLOCATION IN A
.* DIFFERENT WAY. SEE ALLOCATION MACROS FOR SUPPORT.
         AIF   (&I EQ 76).LP1           DUMMY ENTRY IS 76.     @ZM49049
         AIF   (NOT &SGGENDB(&I)).LP                           @ZM49049
.LP1     ANOP                                                  @ZM49049
.*  IGNORE ENTRY IF NUCLEUS DLIB OR NUCLEUS NAME LIST IS NUL
         AIF   (K'&SGNUCDC(&I) EQ 0 OR K'&SGNUCMC(&I) EQ 0).LP
&W       SETA  1+K'&SGNUCMC(&I)    1+NUMBER OF CHARS SPECIFIED
         AIF   (&W-&W/9*9 NE 0).ERROR ERROR IF LENGTH NOT MULT OF 9
&W       SETA  1
.NUCLP   ANOP
&C       SETC  '&SGNUCDC(&I)'.'('.'&SGNUCMC(&I)'(&W,8).')'
         PUNCH ' INCLUDE &C DEVICE=&SGGNRIC(&I)'               @ZM49120
&W       SETA  &W+9                INCREMENT TO NEXT NAME IN LIST
         AIF   (&W LT K'&SGNUCMC(&I)).NUCLP CONTINUE IF ANY MORE
         AGO   .LP                 PROCESS NEXT DIT ENTRY
.*  ERROR IN NAME LIST--INCORRECT FORMAT
.ERROR   ANOP
         MNOTE 5,'SGIEC301--NUCLEUS NAME LIST INVALID FOR DIT ENTRY &I,X
                GENERIC &SGGNRIC(&I)'
&SGQUITB SETB  1
         AGO   .LP                 PROCESS NEXT DIT ENTRY
.MEND    MEND
