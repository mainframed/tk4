         MACRO
         SGFDSP01 &UNIT=,&ID=
         LCLA     &N
         LCLC     &PNDSEG,&PNDEXT,&FL5B                        @ZA47883
.* FUNCTION - SET DEVICE FEATURES TABLE (DFT) FOR 3203 MODEL 4.
.*
.*      CHANGE ACTIVITIES
.*                                                             @ZA44676
.*                                                             @ZA45245
.*                                                             @ZA46953
.*                                                             @ZA47883
.*                                                             @ZA50079
.*                                                             @ZA48676
.*                                                             @ZA53155
.*                                                             @ZA59932
         COPY  SGGBLPAK
         AIF   ('&UNIT' NE '3203').EXIT01
&N       SETA  &SGADRPC(2)  SET ADDRESS MULTIPLICATION FACTOR
         AIF   ('&SGMDLPC' NE '4').ERR01    ERROR IF NOT 3203 MODEL 4
         MNOTE *,' DEVICE TYPE 3203-4 SPECIFIED          '     @ZA59932
         AIF   (&SGFCNTA NE 0).ERR02   ERROR IF FEATURE(S) SPECIFIED
         AIF   (&SGOPCTA EQ 0).CONT01     OPTIONAL CHANNELS ?  @ZA47883
     MNOTE  *,'   OPTIONAL CHANNEL '                           @ZA47883
&FL5B    SETC   'ALTPATH'                 INDICATE FOR UCB     @ZA47883
         AGO   .CONT01     GO BUILD DFT FOR 3203-4
.ERR01   MNOTE 5,'* * * IEIDFT001 &ID -MODEL VALUE &SGMDLPC INVALID'
&SGQUITB SETB  1           MODEL=INVALID/OMIT -TURN ON ERROR SWITCH
         AGO   .EXIT01
.ERR02   MNOTE 5,'* * * IEIDFT002 FEATURE(S) SPECIFIED -  INVALID; THERX
               E ARE NO OPTIONAL FEATURES FOR 3203-4'
&SGQUITB SETB  1           FEATURE(S) SPECIFIED -TURN ON ERROR SWITCH
         AGO   .EXIT01
.CONT01  ANOP
&SGDT06B(&SGCNTRA(3))   SETB   1   INDICATE DEVICE IS 3211 TO  @32034AP
&SGDU06B(&SGCNTRA(3))   SETB   1   ALLOW 3203-4 DEVICE TO BE   @32034AP
.*                                 SUPPORTED AS A OUTPUT ONLY  @32034AP
.*                                 CONSOLE
&PNDSEG  SETC  'A(UCBE&SGADRPC(1))'
&PNDEXT  SETC  '16AL1(0)'
.*
         AIF   (&SGOPCTA EQ 0).CONT02     OPTIONAL CHANNELS ?  @ZA53155
.*
.* SET OPTIONAL CHANNEL FLAGS IF SPECIFIED                     @ZA47883
.*
         SGOPCHAN  PERMIT=YES,UNIT=&UNIT,ID=&ID
.*
.* BUILD THE DFT FOR THE DEVICE
.*                     @ZA48676  @ZA47883  @ZA46953  @ZA44676  @ZA45245
         SGDFTGBL  ATNTABX=0,DEVTYPE=1080080B,ERRTAB=7,DSEGLEN=4,      X
               DPNDSEG=&PNDSEG,DPNDEXT=&PNDEXT,CCWPFIX=1,              X
               UCBFL5=(&FL5B),UCBFLP1=DDRSWAP
         AGO   .CONT03       BYPASS DUP CALL TO SGDFTGBL
.CONT02  ANOP
.*
.* BUILD THE DFT FOR THE DEVICE WITHOUT OPTIONAL CHAN SPECIFIED
.*           @ZA53155  @ZA48676  @ZA47883  @ZA46953  @ZA44676  @ZA45245
         SGDFTGBL  ATNTABX=0,DEVTYPE=1080080B,ERRTAB=7,DSEGLEN=4,      X
               DPNDSEG=&PNDSEG,DPNDEXT=&PNDEXT,CCWPFIX=1,              X
               UCBFLP1=DDRSWAP
.CONT03  ANOP
.* PROCESS OFFLINE OPTIONS.                                    @ZA50079
         AIF   ('&SGOFLPC' NE 'YES').NOTOFFL DID USER SPECIFY OFFLINE
.*                                                             @ZA50079
&SGDOFFB(&SGCNTRA(3))   SETB   1            INDICATE OFFLINE   @ZA50079
         MNOTE *,'      OFFLINE SPECIFIED FOR THIS DEVICE'     @ZA50079
.NOTOFFL ANOP                                                  @ZA50079
.*
.* CHECK ADDRESS MULTIPLACTION FACTOR AND GENERATE DFT ENTRIES
.* AND ADDRESSES FOR THE SPECIFIED NUMBER OF EXITS
.*
         AIF   ('&SGADRPC(1)' EQ 'FFF').EXIT01   INVALID ADDRESS
         AIF   (&N LE 1).EXIT01   GENERATION REITERATION COMPLETE
.*
.* UPDATE ADDRESS TABLE AND DEVICE COUNT
.*
&N       SETA  &N-1
         SGADDR
.*
.* RETURN TO SET GLOBLES FOR NEXT ADDRESS
.*
         AGO   .CONT01
.EXIT01  MEND
