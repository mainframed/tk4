         MACRO
         SGIEA4D2
         COPY SGGBLPAK
         LCLA &N
.*
.*  THIS MACRO CREATED FOR AOS/2 RELEASE 2 DIDOCS (SD CODE Y02958).
.*  IT CREATES LINK EDIT JCL TO INCLUDE RESIDENT DCM'S, IEECVSUB AND
.*  IEEPFKEY CONTROL BLOCKS INTO THE NUCLEUS.
.*
         AIF   (&SG5450A+&SGSCHDA(10)+&SGSCHDA(11)+&SGSCHDA(17)+&SGSCHDX
               A(18) LT 1).OUT NO GRAPHIC CONSOLES            MB YM4302
.*
.*  THE FOLLOWING COMMENT CARD WILL BE GENERATED INTO THE STAGE 1
.*  OUTPUT TO IDENTIFY THE LEVEL AND CHANGE DATE ASSOCIATED WITH
.*  THE COPY OF SGIEA4D2 BEING USED IN ANY GIVEN SYSGEN.
.*  THE COMMENT SHOULD BE UPDATED WITH A NEW LEVEL NUMBER
.*  AND DATE WHENEVER ANY CHANGES ARE MADE TO THIS MACRO
.*
*** SGIEA4D2 *** DIDOCS *** 03/22/74 *** CHANGE LEVEL 01 ***  MB YM4302
.*
&N       SETA  &SG5450A
.KCHECK  ANOP
         AIF   (&N EQ 0).TST2250
         PUNCH ' INCLUDE SYSPUNCH(IEECVD&N)'
&N       SETA  &N-1
         AGO   .KCHECK
.TST2250 ANOP
&N       SETA  &SGSCHDA(10)
.RCHECK  ANOP
         AIF   (&N EQ 0).TST2260
         PUNCH ' INCLUDE SYSPUNCH(IEECVE&N)'
&N       SETA  &N-1
         AGO   .RCHECK
.TST2260 ANOP
&N       SETA  &SGSCHDA(11)
.CCHECK  ANOP
         AIF   (&N EQ 0).TSTAN48
         PUNCH ' INCLUDE SYSPUNCH(IEECVF&N)'
&N       SETA  &N-1
         AGO   .CCHECK
.TSTAN48 ANOP
&N       SETA  &SGSCHDA(17)
.AN4CHK  ANOP
         AIF   (&N EQ 0).TSTAN19
         PUNCH ' INCLUDE SYSPUNCH(IEECVG&N)'
&N       SETA  &N-1
         AGO   .AN4CHK
.TSTAN19 ANOP
&N       SETA  &SGSCHDA(18)
.AN9CHK  ANOP
         AIF   (&N EQ 0).SUBCHK
         PUNCH ' INCLUDE SYSPUNCH(IEECVH&N)'
&N       SETA  &N-1
         AGO   .AN9CHK
.SUBCHK  ANOP
         PUNCH ' INCLUDE SYSPUNCH(IEECVSUB)'
         AIF   (&SGSCHDA(16) NE 0).PFKIN
&N       SETA  0
.NXTCONS ANOP
         AIF   (&N GE &SGSCNYA(1)).OUT
&N       SETA  &N+1
         AIF   (&SGSCNPA(&N) EQ 0).NXTCONS
.PFKIN   ANOP
         PUNCH ' INCLUDE SYSPUNCH(IEEPFKEY)'
.OUT     MEND
