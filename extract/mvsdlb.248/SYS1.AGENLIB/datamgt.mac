         MACRO
         DATAMGT  &ACSMETH=,&GRAPHCS=,&UCSDFLT=,&IND=,&TABLE=  @Z40MSTG
         COPY  SGGBLPAK
         LCLA  &A,&B,&P                                        @Z40MSTG
         LCLC  &ID(5)                                          @Z40MSTG
.***** START OF SPECIFICATIONS **************************************
.*
.* MODULE NAME - DATAMGT
.*
.* DESCRIPTIVE NAME - DATA MANAGEMENT GENERATION
.*
.*
.* COPYRIGHT - NONE
.*
.* STATUS - CHANGE LEVEL 04
.*
.* FUNCTION -
.*       THE DATAMGT MACRO IS USED TO SPECIFY OPTIONAL ACCESS METHODS
.*       AND GRAPHICS PROGRAM SERVICES.
.*       TRANSLATE TABLES INTO IMAGELIB.
.*
.* NOTES - N/A
.*
.* MODULE TYPE - MACRO
.*
.* ENTRY POINT - N/A
.*
.* INPUT - THE LIST OF PARAMETERS ARE INCLUDED ON THE CALL TO DATAMGT
.*             &ACSMETH - SPECIFIES ONE OR MORE OF BTAM, TCAM, ISAM,
.*                        VTAM AND GAM.
.*             &GRAPHCS - PORRTNS SPECIFIES PROBLEM ORIENTED ROUTINES
.*                        ARE TO BE INCLUDED IN SYS1.LINKLIB
.*                        GPS SPECIFIES THE GRAPHICS SUBROUTINE
.*                        PACKAGE IS TO BE INCLUDED.
.*             &UCSDFLT - DEFAULT UCS IMAGES
.*             &IND     - INDUSTRIES WILL BE IN SYSTEM.         VS32758
.*             &TABLE   - SPCIFIES OPT XLATE TABLES ADDED TO IMAGELIB
.*                                                             @Z40MSTG
.*
.* OUTPUT -
.*       THE GLOBALS USED ARE -
.*             &SGMENTB(34) - ENTRY SWITCH
.*             &SGSUPRB(16) - ALLOWS ASYNCHRONUS EXITS WITHOUT RQES
.*             &SGUCSUC(17) - DEFAULT UCS IMAGES
.*             &SGSUPRB(17) - SAME AS 16
.*             &SGGRPHB(1)  - PROBLEM ORIENTED ROUTINES - INCLUDED
.*             &SGGRPHB(3)  - GRAPHIC SUBROUTINE PACKAGE INCLUDED
.*             &SGDMGTB(2)  - ISAM
.*             &SGDMGTB(4)  - BTAM
.*             &SGDMGTB(5)  - TCAM
.*             &SGDMGTB(6)  - VTAM
.*             &SGSSS2B     - SET IF IND=YES SPECIFIED          VS32758
.*             &SGMENTB(12) - GAM
.*             &SGQUITB     - QUIT SWITCH
.*             &SGAPCSB(1)  - T3211
.*             &SGAPCSB(2)  - T1403
.*             &SGAPCSB(3)  - TOCR
.*             &SGAPCSB(4)  - TKAT
.*             &SGAPCSB(5)  - TFMT
.*
.*       THE LOCALS USED ARE -
.*             &A - INDEX TO PARAMETER SUBLISTS
.*             &B - INDEX TO PARAMETER SUBLISTS
.*             &P - INDEX TO PARAMETER SUBLISTS
.*             &ID(&P) - INDEX TO PARAMETER SUBLISTS
.*
.*
.* EXIT, NORMAL - N/A
.*
.* EXIT, ERROR - N/A
.*
.* EXTERNAL REFERENCES -
.*
.* TABLES - N/A
.*
.* MACROS - NONE
.*
.* CHANGE ACTIVITY - MODIFIED FOR Y02036,VS03045,@Z40MSTG
.*
.****** END OF SPECIFICATIONS ***************************************
         AIF   (NOT &SGMENTB(34)).E2    PREVIOUSLY ENTERED     @Z40MSAP
    MNOTE 5,'* * * IEIDAT103 DATAMGT MACRO PREVIOUSLY USED'    @Z40MSAP
&SGQUITB SETB  1                                               @Z40MSAP
.E2      ANOP                                                  @Z40MSAP
&SGMENTB(34)   SETB 1                   ENTRY SWITCH ON
&SGSUPRB(16) SETB 1 ALLOWS ASYNCHRONOUS EXITS W/O 12*S
&SGSUPRB(17)  SETB  1
         AIF   (T'&UCSDFLT NE 'O').UCS5
         MNOTE 0,'IEIDAT105 ALL IS DEFAULT TAKEN FOR UCS DEFAULT'
         AGO    .UCS4
.UCS5    AIF   ('&UCSDFLT' EQ 'ALL').UCS3
         MNOTE *,'      THE FOLLOWING UCS IMAGES ARE DEFAULTS'
.UCS1    ANOP  BEGINNING OF LOOP FOR UCS IMAGES
&A       SETA  &A+1 INCREMENT INDEX
         AIF   (&A GT 17).UCS2
         AIF   (N'&UCSDFLT LT &A).UCSEND
         AIF   ('&UCSDFLT(&A)' EQ '').UCS1
&SGUCSDC(&A) SETC '&UCSDFLT(&A)'         SET GLOBAL TO IMAGE SPECIFIED
         MNOTE *,'          &UCSDFLT(&A)'
         AGO   .UCS1
.UCS2    AIF   (N'&UCSDFLT LE 17).UCSEND
         MNOTE 0,'          MORE THAN 17 IMAGES HAVE BEEN SPECIFIED, TH*
               E REST ARE IGNORED'
         AGO   .UCSEND
.UCS3    MNOTE *,'      ALL UCS IMAGES ARE DEFAULTS'
.UCS4    ANOP
&SGUCSDC(1) SETC 'ALL'             DEFAULT ALL IMAGES
.UCSEND  ANOP
&A       SETA  0    RESET INDEX TO ZERO
.*********************************************************************
.*              GRAPHICS SUPPORT
.*********************************************************************
         AIF   ('&GRAPHCS' EQ '').DM1
&SGMENTB(12) SETB 1                INDICATE GRAPHICS IN SYSTEM
    MNOTE *,'      GRAPHICS SUPPORT TO BE INCLUDED'
         AIF   (N'&GRAPHCS NE 2).DMG
&SGGRPHB(3) SETB 1
&SGGRPHB(1) SETB 1
    MNOTE *,'      GRAPHICS SUPPORT GSP AND PORRTNS INCLUDED'
         AGO   .DM1
.DMG     ANOP
         AIF   ('&GRAPHCS' EQ 'GSP').DMG1
         AIF   ('&GRAPHCS' NE 'PORRTNS').DMGERR
&SGGRPHB(1) SETB 1
    MNOTE *,'      GRAPHICS SUPPORT TO BE PORRTNS'
         AGO   .DM1
.DMG1    ANOP
&SGGRPHB(3) SETB 1
    MNOTE *,'      GRAPHICS SUPPORT TO BE GSP'
         AGO   .DM1
.DMGERR  ANOP
    MNOTE 5,'* * * IEIDAT104 GRAPHICS VALUE &GRAPHCS INVALID'
&SGQUITB SETB  1
.*********************************************************************
.*              ACCESS METHODS REQUESTED
.*********************************************************************
.DM1     AIF   (T'&ACSMETH EQ 'O').INDUS   PARAMETER OMITTED
    MNOTE *,'     OPTIONAL ACCESS METHOD'
.EXITEST AIF   (N'&ACSMETH EQ &A).INDUS    EXIT FROM SCAN ROUTINE
&A       SETA  &A+1                        INCREMENT COUNTER
         AIF   ('&ACSMETH(&A)' EQ 'BTAM').S1      BTAM OPTION
         AIF   ('&ACSMETH(&A)' EQ 'TCAM').S2      TCAM OPTION
         AIF   ('&ACSMETH(&A)' EQ 'ISAM').S3      ISAM OPTION
         AIF   ('&ACSMETH(&A)' EQ 'GAM').S5       GAM OPTION
         AIF   ('&ACSMETH(&A)' EQ 'VTAM').S6      VTAM OPTION
&SGQUITB SETB  1                        INVALID SPECIFICATION
    MNOTE 5,'* * * IEIDAT101 ACSMETH VALUE &ACSMETH(&A) INVALID'
         AGO   .EXITEST                 GO TEST OFR END OF SCAN
.S1 MNOTE *,'        BTAM'
&SGDMGTB(4)    SETB 1                   BTAM SPECIFIED IN ACSMETH OPND
         AGO   .EXITEST                 GO TEST FOR END OF SCAN
.S2 MNOTE *,'        TCAM'
&SGDMGTB(5)    SETB 1                   TCAM SPECIFIED IN ACSMETH OPND
         AGO   .EXITEST                 GO TEST FOR END OF SCAN
.S3 MNOTE *,'         ISAM'
&SGDMGTB(2)     SETB 1                ISAM SPECIFIED IN ACSMETH OPND
        AGO   .EXITEST
.S5      MNOTE *,'    GAM'
&SGMENTB(12) SETB 1                   INDICATE PRESENCE OF GAM
         AGO   .EXITEST
.S6 MNOTE *,'         VTAM'
&SGDMGTB(6)     SETB 1                VTAM SPECIFIED IN ACSMETH OPND
        AGO   .EXITEST
.*********************************************************************
.*              INDUSTRIES REQUESTED
.*********************************************************************
.INDUS   AIF   ('&IND' EQ 'NO').ARGON NO INDUSTRY REQUESTED    @Z40MSAP
         AIF   ('&IND' EQ '').ARGON   NO INDUSTRY REQUESTED    @Z40MSTG
         AIF   ('&IND' NE 'YES').INDERR IMPROPER PARAMETER     @Z40MSAP
&SGSSS2B SETB  1                      INDUSTRY REQUESTED        VS32758
         MNOTE *,'    IND=YES SPECIFIED '                       VS32758
         AGO   .ARGON                                          @Z40MSAP
.INDERR  ANOP                                                   VS32758
   MNOTE  5,'* * * IEIDAT102 VALUE &IND INVALID FOR IND PARAMETER'
&SGQUITB SETB  1                     ERROR BIT ON
.ARGON   ANOP                                                  @Z40MSTG
         AIF   (T'&TABLE EQ 'O').FINIS                         @Z40MSTG
&P       SETA   1                                              @Z40MSTG
         AIF    ('&TABLE(1)' EQ 'ALL').SETALL1                 @Z40MSTG
&ID(1)   SETC   'T3211'                3211 IMAGES             @Z40MSTG
&ID(2)   SETC   'T1403'                1403 IMAGES             @Z40MSTG
&ID(3)   SETC   'TOCR'                 IMAGE SET               @Z40MSTG
&ID(4)   SETC   'TKAT'                 IMAGE SET               @Z40MSTG
&ID(5)   SETC   'TFMT'                 IMAGE SET               @Z40MSTG
&A       SETA   (N'&TABLE)             INIT COUNTER            @Z40MSTG
         AIF    (&A GT 5).ERRMSG1      TOO MANY PARMS          @Z40MSTG
&B       SETA   0                      CLEAR OUT               @Z40MSTG
.TBL1    AIF    (&A EQ &B).FINIS       GO THRU ALL             @Z40MSTG
&B       SETA   &B+1                   BUMP COUNTER            @Z40MSTG
.TBL2    AIF    ('&TABLE(&B)' EQ '&ID(&P)').SETON1             @Z40MSTG
&P       SETA   &P+1                   BUMP COUNTER            @Z40MSTG
         AIF    (&P GT 5).ERRMSG2      NOT FOUND               @Z40MSTG
         AGO    .TBL2                  BACK TO LOOP            @Z40MSTG
.SETALL1 AIF    (&P GT 5).ALLMSG1      SET ALL                 @Z40MSTG
&SGAPCSB(&P) SETB 1                    SET GLOBAL              @Z40MSTG
&P       SETA   &P+1                   BUMP COUNTER            @Z40MSTG
         AGO    .SETALL1               LOOP BACK               @Z40MSTG
.ALLMSG1  MNOTE 0,' * * * ALL TABLES INCLUDED'                 @Z40MSTG
         AGO    .FINIS                 DONE                    @Z40MSTG
.SETON1  AIF    (&SGAPCSB(&P)).ERRMSG3 PRE SPECIFIED           @Z40MSTG
&SGAPCSB(&P) SETB 1                    SET GLOBAL              @Z40MSTG
         MNOTE 0,' * * * TABLE SET &ID(&P) INCLUDED'           @Z40MSTG
&P       SETA   1                      RESET                   @Z40MSTG
         AGO    .TBL1                  RETURN                  @Z40MSTG
.ERRMSG1 MNOTE 5,' * * * IEIDAT110 INVALID NUMBER OF PARAMETERS'
&SGQUITB SETB 1 QUIT                                           @Z40MSTG
         AGO    .FINIS                 GET OUT                 @Z40MSTG
.ERRMSG2 MNOTE 5,' * * * IEIDAT111 &TABLE(&B) INVALID SPECIFICATION'
&SGQUITB SETB   1                      QUIT                    @Z40MSTG
&P       SETA   1                      RESET                   @Z40MSTG
         AGO    .TBL1                  CHECK REST              @Z40MSTG
.ERRMSG3 MNOTE 5,' * * * IEIDAT112 &TABLE(&B) SPECIFICATION REDUNDANT'
&SGQUITB SETB   1                      QUIT                    @Z40MSTG
&P       SETA   1                      RESET                   @Z40MSTG
         AGO    .TBL1                  CHECK REST              @Z40MSTG
.FINIS   MEND                          DONE                    @X40MSTG
