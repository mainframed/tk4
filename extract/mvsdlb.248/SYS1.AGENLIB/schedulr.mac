         MACRO
    SCHEDULR &TAVR=800,&HARDCPY=SYSLOG,                                *
               &BCLMT=100,&DEVPREF=,&PRISUB=JES2,&SUBSYS=
         COPY  SGSYSPAK
         LCLA  &A,&E,&F,&G,&H,&N,&MAT,&V,&VD
         LCLC  &C,&D
.***** START OF SPECIFICATIONS *************************************
.*
.* MODULE NAME - SCHEDULR
.*
.* DESCRIPTIVE NAME - SCHEDULER GENERATION
.*
.* COPYRIGHT - NONE
.*
.* STATUS - CHANGE LEVEL 02
.*
.*
.* FUNCTION -
.*       THE SCHEDULR MACRO IS USED TO SPECIFY THE JOB SCHEDULER
.*       AND MASTER SCHEDULER OPTIONS.
.*
.* NOTES - N/A
.*
.* MODULE TYPE - MACRO
.*
.* ENTRY POINT - N/A
.*
.* INPUT - THE FOLLOWING PARAMETERS ARE VALID ON SCHEDULR
.*             &BCLMT   - SPECIFIES THE NUMBER OF 130 BYTE RECORDS
.*                        THAT WILL BE SET AS MESSAGES IN
.*                        SYS1.BROADCAST DATA SET.
.*             &TAVR    - SPECIFIES THE STANDARD DENSITY FOR 7-TRACK
.*                        TAPE VOLUMES FOR AUTOMATIC VOLUME
.*                        RECOGNITION.
.*             &HARDCPY - SPECIFIES THE HARDCOPY LOG USED TO RECORD
.*                        OPERATOR COMMANDS, SYSTEM COMMANDS AND
.*                        RESPONSES, AND WRITE TO OPERATOR MESSAGES.
.*             &PRISUB  - SPECIFIES THE NAME OF THE PRIMARY JOB
.*                        ENTRY SUBSYSTEM. DEFAULT IS JES2.
.*             &SUBSYS  - SPECIFIES THE JOB ENTRY SUBSYSTEM NAMES TO
.*                        BE USED IN THE SYSTEM BEING BUILT. DEFAULTS
.*                        TO PRISUB NAME IN SCHEDULR INNER MACRO.
.*             &DEVPREF - SPECIFIES THE LIST OF GENERIC NAMES THAT
.*                        WILL BE USED TO GENERATE THE INSTALLATION
.*                        DEVICE PREFERENCE TABLE.
.*
.* OUTPUT -
.*       GLOBALS USED ARE -
.*             &SGSCHDA(13) - BROADCAST LIMIT FOR RES OR TSO
.*             &SGSCHDB(7)  - FULL SYSTEM MANAGEMENT FACILITY
.*             &SGSCHDB(8)  - CONVERSATIONAL REMOTE JOB ENTRY
.*             &SGSCHDB(13) - 2740 SPECIFIED AS CONSOLE
.*             &SGSCHDB(14) - AUTOMATIC VOLUME RECOGNITION
.*             &SGSCHDB(15) - ESV
.*             &SGSCHDB(23) - 7-TRACK TAPE DENSITY AT 556 BPI
.*             &SGSCHDB(24) - 7-TRACK TAPE DENSITY AT 800 BPI
.*             &SGSCHDB(26) - REMOTE ENTRY SUBSYSTEM
.*             &SGSCHDB(31) - ESV
.*             &SGSCHDB(34) - MULTIPLE CONSOLE SUPPORT
.*             &SGSCHDB(35) - EXIT OPTION OF MCS
.*             &SGSCHDB(36) - SYSLOG USED AS HARD COPY LOG
.*             &SGSCHDB(37) - ALL SYSTEM COMMANDS TO HARD COPY LOG
.*             &SGSCHDB(42) - HARD COPY COMMANDS
.*             &SGSCHDB(43) - COMMANDS AND STATIC DISPLAYS
.*             &SGSCHDB(44) - TSO MACRO SPEC FOR SCHEDULER INTERFACE
.*
.*             &SGSCHDC(4)  - START READER DATA SET NAME
.*             &SGSCHDC(5)  - START WRITER DATA SET NAME
.*             &SGSCHDC(7)  - START READER VOLUME SERIAL
.*             &SGSCHDC(8)  - START WRITER VOLUME SERIAL
.*             &SGSCHDC(14) - OUTPUT DEVICE ADDRESS FOR HARD COPY LOG
.*             &SGSCHDC(15) - EVA
.*             &SGSCHHB(1-16) - WTO/WTORS WITH SPECIFIED ROUTCODES
.*                              TO BE RECEIVED BY HARD COPY LOG
.*             &SGSCHRB(16)  - ROUTE CODE 1 AND 2 ALWAYS RECEIVED
.*             &SGSCHVC(1-30) - GENERIC NAME PREFERENCE LIST
.*             &SGSUBSC(1-15) - JOB ENTRY SUBSYSTEM NAMES
.*             &SGSPRIC(1)  - PRIMARY JOB ENTRY SUBSYSTEM
.*
.*       LOCALS USED ARE -
.*             &V  - INDEX FOR &SUBSYS
.*             &VD - USED TO LOOK AT NEXT IN LIST
.*             &A  - INDEX USED FOR DEVPREF
.*             &E  - START CHAR FOR DEVPREF INDEXED
.*             &N  - NUMBER OF HARD COPY ENTRIES
.*             &D  - SINGLE ROUTE CODE
.*             &G  - COUNT OF DIGITS
.*             &F  - FIRST DIGIT OF NUMBER
.*             &H  - START CHAR FOR HARDCPY INDEXED
.*             &C  - CHARACTER USED TO SCAN FOR '(' OR ','
.*             &D  - ROUTE CODE SPECIFIED
.*
.* EXIT, NORMAL - N/A
.*
.* EXIT, ERROR - N/A
.*
.* EXTERNAL REFERENCES - N/A
.*
.* TABLES - N/A
.*
.* MACROS - NONE
.*
.* CHANGE ACTIVITY - MODIFIED FOR Y02036,OZ04242,OZ09190
.* $01=OZ87535,UZ41399,CT/GLJ       POSITIONAL PARAMETERS ARE OMITTED
.*                                  AND NO ERROR MESSAGES ARE GENERATED
.*                                                       @01D,@01C,@01A
.*
.***** END OF SPECIFICATIONS  *****************************************
         AIF   (&SGMENTB(6)).V1              SCHEDULR ENTRY SWITCH
&SGMENTB(6)  SETB 1
&SGSCHDB(7)  SETB 1                          SMF-STANDARD OPTION
&SGSCHDB(34) SETB 1                          MCS-STANDARD OPTION
&SGSCHDB(14)   SETB 1                  SET VLMOUNT
.****************SUBSYS***********************
&V  SETA 1
&MAT     SETA  1
    AIF  (T'&SUBSYS NE 'O').CKLNG
.*  AIF  (K'&PRISUB GT 4).E6            REMOVED                @AZ09190
    AGO  .PRI
.CKLNG   AIF   (K'&SUBSYS(&V) GT 4).E1
&VD      SETA  &V+1
.CKNXT   AIF   (&VD GT N'&SUBSYS).CKMT        CHECK FOR END OF LIST
         AIF   ('&SUBSYS(&V)' NE '&SUBSYS(&VD)').BUMP
   MNOTE 0,'* * * IEISCH910 &SUBSYS(&VD) DUPLICATED IN SUBSYS LIST'
.BUMP    ANOP
&VD      SETA  &VD+1
         AGO   .CKNXT
.CKMT    AIF   ('&PRISUB' EQ '&SUBSYS(&V)').NOSET
&SGSUBSC(&V)   SETC  '&SUBSYS(&V)'
.NOSET   ANOP
&V       SETA  &V+1
         AIF   ('&SUBSYS(&V)' EQ '').PRI
         AIF   (&V GT 15).E2
         AGO   .CKLNG
.******************PRISUB*************************
.PRI     AIF   (K'&PRISUB GT 4).E6                             @AZ09190
&SGSPRIC(1) SETC  '&PRISUB'
.******************DEVPREF************************
.DEV     AIF   (T'&DEVPREF EQ 'O').TA                          @AZ09190
&A       SETA  1
.LEL     AIF   (N'&DEVPREF LT &A).TA
         AIF   (&A GT 30).E5
&E       SETA  1
.SCAN    AIF   ('&DEVPREF(&A)'(&E,1) GT '9').E4
         AIF   ('&DEVPREF(&A)'(&E,1) LT '0').E4
&E       SETA  &E+1
         AIF   (&E GT 4).OUT
         AGO   .SCAN
.OUT     ANOP
&SGSCHVC(&A) SETC   '&DEVPREF(&A)'
&A           SETA  &A+1
         AGO   .LEL
.* * * TAVR * * *                       * * * TAVR * * *          * * *
.TA      AIF   (&TAVR EQ 200).TA8            TAVR=200
         AIF   (&TAVR NE 556).TA6
&SGSCHDB(23) SETB 1                          TAVR=556
         AGO   .TA8
.TA6     AIF   (&TAVR NE 800).W9             TAVR=INVALID
&SGSCHDB(24) SETB 1                          TAVR=800 OR DEFAULT
.TA8     ANOP
    MNOTE *,'     7-TRACK TAPE DENSITY IS &TAVR-BPI'
.***  BCLMT  ***             ***  BCLMT  ***            ***  BCLMT  ***
.BCLMT   AIF   (T'&BCLMT EQ 'O').HC
         AIF   (T'&BCLMT NE 'N').ZE
         AIF   (&BCLMT LT 1 OR &BCLMT GT 1000).ZE
&SGSCHDA(13) SETA &BCLMT
 MNOTE *,'    SYS1.BRODCAST TO HAVE ROOM FOR &BCLMT NOTICE MESSAGES'
.* * * HARDCPY * * *     HARDCPY        * * * HARDCPY * * *       * * *
.HC      ANOP  PROCESS HARDCPY PARAMETERS
    MNOTE *,'     HARD COPY PARAMETERS--'
&N       SETA  N'&HARDCPY               NUMBER OF VALUES
         AIF   (&N GT 3 OR &N LT 0).Z0       N'HARDCPY VALIDITY  V2
    MNOTE *,'          HARD COPY LOG WILL BE IN THE SYSTEM-'
         AIF   (&N NE 0).HC2A           SKIP DEFAULT
    MNOTE 0,'* * * IEISCH909 HARDCPY=(SYSLOG,ALL) DEFAULTED'
.HC1A    ANOP                           HARDCPY=SYSLOG
    MNOTE *,'          SYSTEM LOG WILL BE THE HARD COPY LOG'
&SGSCHDB(36) SETB 1                     HARDCPY=SYSLOG
         AIF   (&N NE 0).HC4            CHECK WTO'S
         AGO   .HCC                     ALL WTO'S IF DEFAULT
.HC2A    AIF   ('&HARDCPY(1)' EQ 'SYSLOG' OR T'&HARDCPY(1) EQ 'O').HC1A
         AIF   ('&HARDCPY(1)' EQ 'ALL' OR                              *
               '&HARDCPY(1)'(1,1) EQ '(').Z1                       @01A
         AIF   (K'&HARDCPY(1) NE 3).Z1  ADDRESS 3 CHARACTERS
&SGSCHDC(14) SETC '&HARDCPY(1)'         HARD COPY ADDRESS
    MNOTE *,'          HARD COPY LOG ADDRESS=&SGSCHDC(14)'
.HC4     AIF   (&N EQ 1).HCC            DEFAULT ALL IF ONLY ONE PARM
         AIF   ('&HARDCPY(2)' EQ 'ALL' OR T'&HARDCPY(2) EQ 'O').HCC
         AIF   ('&HARDCPY(2)' EQ 'CMDS' OR '&HARDCPY(2)' EQ 'INCMDS'   *
               OR '&HARDCPY(2)' EQ 'STCMDS' OR                         *
               '&HARDCPY(2)' EQ 'NOCMDS').Z2                       @01C
    MNOTE *,'          WTO MESSAGES WITH THE FOLLOWING ROUTE-CODES'
    MNOTE *,'           WILL BE OUTPUT TO THE HARD COPY LOG--'
         AIF   ('&HARDCPY(2)'(1,1) EQ '(' AND                          *
                '&HARDCPY(2)'(K'&HARDCPY(2),1) EQ ')').HC6
         AIF   (T'&HARDCPY(2) NE 'N' OR                                *
               (&HARDCPY(2) LT 1 OR &HARDCPY(2) GT 16)).Z4
&D       SETC  '&HARDCPY(2)'                 SINGLE ROUTE-CODE
&G       SETA  99                            INDICATOR
         AGO   .HCA                          SET IN GLOBAL
.HC6     ANOP  PROCESS MULTIPLE HARD COPY ROUTE-CODES
&F       SETA  2                             FIRST DIGIT OF NUMBER
&G       SETA  0                             COUNT OF DIGITS
&H       SETA  2                             SEARCH FOR ',' OR ')'
.HC8     ANOP  PICK OUT EACH ROUTE-CODE IN SUBLIST
&C       SETC  '&HARDCPY(2)'(&H,1)           LOOK FOR ')' OR ','
         AIF   ('&C' EQ '(').Z2         ANOTHER '(' INVALID
         AIF   ('&C' EQ ',' OR '&C' EQ ')').HC9   GET THE ROUTE-CODE
&H       SETA  &H+1                          SCAN AGAIN
&G       SETA  &G+1                          COUNT OF CHAR
         AGO   .HC8                          ANOTHER TIME THROUGH
.HC9     AIF   (&G EQ 0 AND &F EQ 2).HCC     ALL ANYWAY, ,() ...
         AIF   (&G EQ 0).HCB                 TWO COMMAS IN SUCCESSION
&D       SETC  '&HARDCPY(2)'(&F,&G)          ROUTE-CODE SPECIFIED
         AIF   (&D LT 1 OR &D GT 16).Z4 VALIDITY
.HCA     AIF   (&SGSCHHB(&D)).Z5        ROUTE-CODE REDUNDANT
&SGSCHHB(&D) SETB 1                          NTH ROUTE-CODE
    MNOTE *,'               &D'
.HCB     AIF   (&G EQ 99 OR '&C' EQ ')').HCE END OF THE ROAD
&F       SETA  &F+&G+1
&G       SETA  0
&H       SETA  &H+1
         AGO   .HC8                          TRY IT AGAIN
.HCC     ANOP
    MNOTE *,'          ALL WTO MESSAGES WILL BE OUTPUT TO THE '
    MNOTE *,'          HARD COPY LOG'
&G       SETA  1                        INITIALIZE GLOBAL POINTER
.HCD     AIF   (&G GT 16).HCE                LIMIT
&SGSCHHB(&G) SETB 1                     SET
&G       SETA  &G+1                          INCREMENT POINTER
         AGO   .HCD                     SET NEXT OR FINISH
.HCE     AIF   (&N NE 3).HCE1                                      @01C
&C       SETC  ''                       FOR .M4 MESSAGE
.*       AIF   (&N EQ 2 AND ('&HARDCPY(2)' NE 'CMDS' OR '&HARDCPY(2)'
.*             NE 'INCMDS' OR '&HARDCPY(2)' NE 'STCMDS')).HCE1     @01D
         AIF   (T'&HARDCPY(&N) EQ 'O').HCE1  LAST OMITTED
         AIF   ('&HARDCPY(&N)' NE 'CMDS').HCEA    VALIDITY
&SGSCHDB(37) SETB 1                          CMDS SPECIFIED
    MNOTE *,'          STATUS DISPLAYS WILL BE OUTPUT TO'       OZ04242
    MNOTE *,'           THE HARD COPY LOG '                     OZ04242
         AGO   .HF                      TO MNOTE
.HCEA    AIF   ('&HARDCPY(&N)' NE 'INCMDS').HCEB  VALIDITY
&SGSCHDB(42) SETB 1                     INCMDS SPECIFIED
    MNOTE *,'          STATUS DISPLAYS WILL NOT BE OUTPUT TO'
    MNOTE *,'           THE HARD COPY LOG '
         AGO   .HF                      TO MNOTE
.HCEB    AIF   ('&HARDCPY(&N)' NE 'STCMDS').HCEC    VALIDITY
&SGSCHDB(43) SETB 1                     STCMDS SPECIFIED
    MNOTE *,'          STATIC STATUS DISPLAYS WILL BE OUTPUT TO'
    MNOTE *,'           THE HARD COPY LOG '
         AGO    .HF                                             OZ04242
.HCEC    AIF ('&HARDCPY(&N)' NE 'NOCMDS').Z3      VALIDITY
.HCE1    ANOP
&C       SETC  ' NOT'                         FOR MESSAGE       OZ04242
.HF MNOTE *,'          OPERATOR COMMANDS AND RESPONSES WILL&C BE'  "
    MNOTE *,'           OUTPUT TO THE HARD COPY LOG'
         MEXIT
.**********************************************************************
.*                       ERROR MESSAGES                               *
.**********************************************************************
.V1 MNOTE 5,'* * * IEISCH001 SCHEDULR PREVIOUSLY SPECIFIED'          **
&SGQUITB SETB  1
         AGO   .MEND                         NO PROCESSING
.E1      AIF   ('&SUBSYS' EQ 'PRISUB').PRI
    MNOTE 5,'* * * IEISCH002 SUBSYS=&SUBSYS(&V) MORE THAN 4 CHARACTERS'
&SGQUITB SETB 1
         AGO   .CKMT
.E2 MNOTE 5,'* * * IEISCH003 SUBSYS HAS MORE THAN 15 PARAMETRRS'
&SGQUITB SETB 1
         AGO   .PRI
.E4 MNOTE 5,'* * * IEISCH005 DEVPREF=&DEVPREF(&A) INVALID'
&SGQUITB SETB 1
         AGO   .OUT
.E5 MNOTE 5,'* * * IEISCH006 DEVPREF PARAMETER LIST EXCEEDS 30 DEVICES'
&SGQUITB SETB 1
         AGO   .TA
.E6 MNOTE 5,'* * * IEISCH007 PRISUB=&PRISUB EXCEEDS 4 CHARACTERS'
&SGQUITB SETB 1
         AGO   .DEV                                            @AZ09190
.W9 MNOTE 5,'* * * IEISCH019 TAVR=&TAVR VALUE INVALID'
&SGQUITB SETB 1
         AGO   .BCLMT                   * BCLMT   *
.Z0 MNOTE 5,'* * * IEISCH040 HARDCPY=&HARDCPY HAS INVALID NUMBER OF PAR*
               AMETERS'                                              **
&SGQUITB SETB  1
         AGO   .MEND                      * RETURN *
.Z1 MNOTE 5,'* * * IEISCH041 HARDCPY=-&HARDCPY(1)- VALUE INVALID'    **
&SGQUITB SETB  1
         AGO   .HC4                     * * RETURN * *
.Z2 MNOTE 5,'* * * IEISCH042 HARDCPY=-&HARDCPY(2)- VALUE INVALID'    **
&SGQUITB SETB  1
         AGO   .HCE                     * RETURN **
.Z3 MNOTE 5,'* * * IEISCH043 HARDCPY=-&HARDCPY(&N)- VALUE INVALID'   **
&SGQUITB SETB 1
         AGO   .MEND                      * RETURN *
.Z4 MNOTE 5,'* * * IEISCH044 HARDCPY ROUTE-CODE VALUE &D INVALID'    **
&SGQUITB SETB  1
         AGO   .HCB                     * RETURN **
.Z5 MNOTE 5,'* * * IEISCH045 HARDCPY ROUTE-CODE VALUE &D DUPLICATED' **
&SGQUITB SETB  1
         AGO   .HCB                     * RETURN **
.ZE MNOTE 5,'* * * IEISCH053 BCLMT VALUE &BCLMT INVALID'
&SGQUITB SETB  1
         AGO   .HC                     * HARDCPY *
.MEND    MEND
