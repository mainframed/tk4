         MACRO
         CONSOLE &MCONS=,&SECONS=,&ALTCONS=,&ROUTCDE=,&VALDCMD=,&USE=, *
               &AREA=,&PFK=,&IOC=NO,&OLDWTOR=,&TYPE=
         COPY  SGSYSPAK
         LCLA  &A1,&A2,&A3,&A,&E
         LCLB  &B1,&B2,&B3,&B4,&SW1,&SW2,&SW3,&SCR,&B(10)
         LCLC  &C1,&C2,&LI,&LO,&PI,&PO,&C
.**** BEGINNING OF SPECIFICATIONS ***********************************
.*
.* MODULE NAME - CONSOLE
.*
.* DESCRIPTIVE NAME - CONSOLE DEFINITION MACRO
.*
.* COPYRIGHT - NONE
.*
.* STATUS - CHANGE LEVEL 02
.*
.* FUNCTION -
.*       THE CONSOLE MACRO IS USED TO SPECIFY THE CONSOLES FOR THE
.*       STANDARD MCS FUNCTION OR THE MASTER CONSOLE ONLY IF THERE
.*       ARE NO SECONDARY CONSOLES.
.*
.* NOTES - N/A
.*
.* MODULE TYPE - MACRO
.*
.* ENTRY POINT - N/A
.*
.* INPUT - THE PARAMETERS DESCRIBED HERE ARE USED ON THE CALLS TO
.*         THE CONSOLE MACRO
.*             &MCONS   - THE ADDRESS OR ADDRESSES OF THE SYSTEM
.*                        MASTER CONSOLE.
.*             &SECONS  - THE ADDRESS OR ADDRESSES OF A SECONDARY
.*                        CONSOLE.
.*             &ALTCONS - THE ADDRESS OR ADDRESSES OF THE ALTERNATE
.*                        CONSOLE OF A MASTER OR SECONDARY CONSOLE.
.*             &ROUTCDE - ROUTING CODES THAT THE CONSOLE WILL
.*                        RECEIVE.
.*             &VALDCMD - WHICH COMMANDS CAN BE ENTERED FROM THE
.*                        CONSOLE.
.*             &IOC     - INTEGRATED OPERATORS CONSOLE
.*             &OLDWTOR - ROUTING CODES TO BE ASSIGNED TO ALL WTO
.*                        MESSAGES THAT DO NOT ALREADY HAVE CODES.
.*             &AREA    - LENGTH OF DISPLAY AREAS
.*             &USE     - TYPE OF LIMITED USAGE
.*             &PFK     - NUMBER OF PAROGRAMMED FUNCTION KEYS.
.*
.* OUTPUT -
.*       THE GLOBALS SET ARE -
.*             &SGSCHDB(25)  - PROGRAM FUNCTION KEYS INCLUDED
.*             &SGSCHDC(1)   - I/O ADDRESS OF CONSOLE
.*             &SGSCHDC(2)   - I/O ADDRESS OF ALTERNATE CONSOLE
.*             &SGSCHDC(16)  - MASTER - INTEGRATED OPERATORS CONSOLE
.*             &SGSCHRB(1- ) - ROUTE CODES FOR MASTER CONSOLE
.*             &SGSCHOB(1-16)- OLD WTOR
.*             &SGSCHAA(1-11)- DISPLAY AREAS - MAXTER CONSOLE
.*             &SGSCHAA(12)  - TOTAL FOR MASTER
.*             &SGSCHDA(16)  - NUMBER OF PFKS
.*             &SGSCNYA(1)   - NUMBER OF SECONDARY CONSOLES
.*             &SGSCNYA(2)   - NUMBER OF SECONDARY CONSOLE CALLS
.*             &SGSCNYA(3)   - NUMBER OF CONSOLE DEVICES
.*             &SGSCNCC(1-98)- MCS SECONDARY CONSOLE ADDRESSES
.*             &SGSCNAC(1-98)- MCS ALTERNATE CONSOLE ADDRESSES
.*             &SGQUITB      - QUIT SWITCH
.*             &SGSIOCC(1-98)- IOC SETTING FOR SECONDARY CONSOLES
.*             &SGSCNPA(1-98)- NUMBER OF PFKS
.*             &SGSCMSB(1-98)- LIM CAP CONSOLE - MESSAGE STREAM MODE
.*             &SGSCSDB(1-98)- LIM CAP CONSOLE - STATUS DISPLAY MODE
.*             &SGAR01A(1-98)- LENGTH OF NTH CRT DISPLAY AREA
.*                 ...              N = 1 TO 11
.*             &SGAR11A(1-98)-
.*             &SGAR12A(1-98)- TOTAL LENGTH
.*             &SGRT01B(1-98)- NTH ROUTE CODE  N = 1 TO 16
.*                 ...
.*             &SGRT16B(1-98)-
.*             &SGCMD1B(1-98)- COMMAND GROUP 1
.*             &SGCMD2B(1-98)- COMMAND GROUP 2
.*             &SGCMD3B(1-98)- COMMAND GROUP 3
.*
.*       THE LOCALS USED ARE -
.*             &A1  - NUMBER OF OPERANDS SPEC IN SECONS
.*             &A2  - DUPLICATE GLOBAL INDEX
.*             &A3  - INCR FOR COMPOSITE ALTERNATE
.*             &A   - SUBLIST INDEX
.*             &E   - ROUTE CODE GLOBAL INDEX
.*             &B1  - I SPECIFIED
.*             &B2  - O SPECIFIED
.*             &B3  - COMPOSITE ALTERNATE I ADDRESS
.*             &B4  - COMPOSITE ALTERNATE O ADDRESS
.*             &SW1 - OUTPUT ONLY CONSOLE
.*             &SW2 - COMPOSITE CONSOLE
.*             &SW3 -
.*             &SCR - ERROR IN SECONSOLE SPECIFICATION
.*             &B   - I AND O ADDRESS SPECIFIED SWITCHES
.*             &C1  - SECONDARY CONSOLE I-
.*             &C2  - SECONDARY CONSOLE O-
.*             &LI  - INPUT HALF
.*             &LO  - OUTPUT HALF
.*             &PI  - INPUT FROM GLOBAL FOR DUPLICATE CHECK
.*             &PO  - OUTPUT FROM GLOBAL FOR DUPLICATE CHECK
.*             &C   - HALF OF CONSOLE ADDRESS
.*
.* EXIT, NORMAL - N/A
.*
.* EXIT, ERROR - N/A
.*
.* EXTERNAL REFERENCES - N/A
.*
.* TABLES - N/A
.*
.* MACROS - NONE
.*
.* CHANGE ACTIVITY - VS01199,OZ03843,OZ04246,OZ11147
.*
.*
.***** END OF SPECIFICATIONS ***************************************
.*
.*   START OF MASTER CONSOLE PARAMETER CHECKS
.*      NOTE: SINCE DIFFERENT GLOBALS ARE SET FOR SECONDARY CONSOLE
.*           PARAMETERS, COMMON KEYWORDS ARE CHECKED IN BOTH SECTIONS
.*           OF THIS MACRO.
.* * * JES * * *                    * * * JES * * *       * * *
         AIF   (T'&TYPE EQ 'O').NJ
         AIF   ('&TYPE' NE 'JES').MJ1      INVALID TYPE VALUE
.***     CHECK IF OTHER PARAMTERS USED - ONLY VALDCMD ACCEPTABLE  ***
.***           FOR TYPE=JES                                       ***
.CKJES   AIF   (T'&MCONS NE 'O' OR T'&SECONS NE 'O' OR T'&USE NE 'O'   X
                OR T'&ROUTCDE NE 'O' OR T'&ALTCONS NE 'O').MJ2
         AIF   (T'&AREA NE 'O' OR T'&OLDWTOR NE 'O' OR T'&PFK NE 'O'   X
                OR '&IOC' NE 'NO').MJ2     INVALID PARMS WITH JES
&SGSCNYA(3)  SETA  &SGSCNYA(3)+1           INCREMENT DEVICE COUNT
&SGSCNYA(2)  SETA  &SGSCNYA(2)+1           INCREMENT COUNTER
         AIF (&SGSCNYA(2) GT 98).M0        EXCEEDS SEC CONSOLE LIMIT
&SGSCNYA(1)  SETA  &SGSCNYA(1)+1           INCREMENT CONSOLE INDEX
&SGSCNCC(&SGSCNYA(1))  SETC  'JESJES'      JES INDICATOR IN ADDRESS
&SGSCJSB(&SGSCNYA(1))  SETB  1             JES CONSOLE BIT
         AGO   .C8                         CHECK VALDCMD PARAMETER
.*********************************************************************
.*
.*   START OF MASTER CONSOLE PARAMETER CHECKS
.*      NOTE: SINCE DIFFERENT GLOBALS ARE SET FOR SECONDARY CONSOLE
.*           PARAMETERS, COMMON KEYWORDS ARE CHECKED IN BOTH SECTIONS
.*           OF THIS MACRO.
.*
.*********************************************************************
.* * * MCONS * * *                    * * * MCONS * * *       * * *
.*
.NJ      AIF   (T'&MCONS EQ 'O').R0        MCONS OMITTED
         AIF  (T'&SECONS NE 'O').M3        PARMS MUTUALLY EXCLUSIVE
         AIF ('&SGSCHDC(1)' NE '').MJ      PREVIOSLY SPECIFIED
         AIF   (N'&MCONS GT 2 OR (N'&MCONS EQ 1 AND                    *
                                  K'&MCONS NE 3)).MK VALIDITY
         AIF   (N'&MCONS NE 1).CO1      COMPOSITE CONSOLE
         AIF   ('&IOC' EQ 'NO').CO      NOT INTEGRATED OPERATOR
         AIF   ('&IOC' NE 'YES').MT     IF NOT YES, INVALID
&SGSCHDC(16)   SETC  '&MCONS'
&SGSCNYA(3)  SETA  &SGSCNYA(3)+1        INCREMENT DEVICE COUNT
.CO      ANOP
&SGSCHDC(1) SETC '&MCONS.&MCONS'        ADDR - ADDR
    MNOTE *,'     CONSOLE ADDRESS &MCONS SPECIFIED'
         AGO   .ALT                      ALTCONS NEXT
.*
.***                  COMPOSITE MASTER CONSOLE
.*
.CO1     ANOP
         AIF   ('&IOC' EQ 'NO').NIOC     NOT INTEGRATED OPERATOR CONS
         AIF   ('&IOC' NE 'YES').MT      IF NOT YES, INVALID
&SGSCHDC(16)   SETC '&MCONS'(4,3)        IOC ADDRESS - OUTPUT CONSOLE
.NIOC  MNOTE *,'     COMPOSITE CONSOLE SPECIFIED--'
&A       SETA  0                         CLEAR
.CO2     ANOP                            LOOP
&A       SETA  &A+1                      INCREMENT SUBLIST
         AIF   (&A GT N'&MCONS).CO8      FINISHED
&C       SETC  '&MCONS(&A)'(3,3)         ADDRESS ***
         AIF   ('&MCONS(&A)'(1,2) NE 'I-').CO3  I-*** OF COMPOSITE
         AIF   (&B(1)).MK0               I-ADDR REDUNDANT
&B(1)    SETB  1                         I-ADDR SPECIFIED
&SGSCHDC(1) SETC '&C&SGSCHDC(1)'         I-ADDR
&SGSCNYA(3)  SETA  &SGSCNYA(3)+1         INCREMENT DEVICE COUNT
    MNOTE *,'          INPUT ADDRESS=&C'
         AGO   .CO2                      GET OUTPUT
.CO3     AIF   ('&MCONS(&A)'(1,2) NE 'O-').MK0  O-*** OF COMPOSITE
         AIF   (&B(2)).MK0               O-ADDR REDUNDANT
&B(2)    SETB  1                         O-ADDR SPECIFIED
&SGSCHDC(1) SETC '&SGSCHDC(1)&C'         O-ADDR
&SGSCNYA(3)  SETA  &SGSCNYA(3)+1         INCREMENT DEVICE COUNT
    MNOTE *,'          OUTPUT ADDRESS=&C'
         AGO   .CO2                      END OF GET INPUT
.CO8     AIF   (NOT(&B(1) AND &B(2))).MK0    I-ADDR AND O-ADDR REQ'D
.*********************************************************************
.* * * ALTCONS * * *     FOR MASTER CONSOLE        * * * ALTCONS * * *
.*********************************************************************
.ALT     AIF   (T'&ALTCONS EQ 'O').AL9  ALTCONS SPEC'D     **SCS**
         AIF   (N'&ALTCONS GT 2 OR (N'&ALTCONS EQ 1 AND                *
                                    K'&ALTCONS NE 3)).ML VALIDITY
         AIF   (N'&ALTCONS NE 1).AL1    COMPOSITE CONSOLE
&SGSCHDC(2) SETC '&ALTCONS.&ALTCONS'    ADDR - ADDR
    MNOTE *,'     ALTERNATE CONSOLE ADDRESS &ALTCONS SPECIFIED'
         AGO   .RC                      LOOK AT ROUTCDE
.AL1     ANOP
    MNOTE *,'     COMPOSITE ALTERNATE CONSOLE SPECIFIED--'
&A       SETA  0                        CLEAR SUBLIST INDEX
.AL2     ANOP                                LOOP
&A       SETA  &A+1                          INCREMENT SUBLIST
         AIF   (&A GT N'&ALTCONS).AL8        FINISHED
&C       SETC  '&ALTCONS(&A)'(3,3)           ADDR ***
         AIF   ('&ALTCONS(&A)'(1,2) NE 'I-').AL3  I-ADDR OF COMPOSITE
         AIF   (&B(3)).ML0                   I-ADDR REDUNDANT
&B(3)    SETB  1                             I-ADDR SPECIFIED
&SGSCHDC(2) SETC '&C&SGSCHDC(2)'             I-ADDR
    MNOTE *,'          INPUT ADDRESS=&C'
         AGO   .AL2                          GET OUTPUT
.AL3     AIF   ('&ALTCONS(&A)'(1,2) NE 'O-').ML0  O-ADDR OF COMPOSITE
         AIF   (&B(4)).ML0                   O-ADDR REDUNDANT
&B(4)    SETB  1                             O-ADDR SPECIFIED
&SGSCHDC(2) SETC '&SGSCHDC(2)&C'             O-ADDR
    MNOTE *,'          OUTPUT ADDRESS=&C'
         AGO   .AL2                          END OR GET INPUT
.AL8     AIF   (NOT(&B(3) AND &B(4))).ML0    WTOBFRS NEXT
.AL9     AIF   (T'&ALTCONS NE 'O').RC  ALTCONS SPEC'D     **SCS**
&SGSCHDC(2) SETC '&SGSCHDC(1)'          INDICATE NO ALTCONS**SCS**
.*********************************************************************
.* * * ROUTCDE * * *     FOR MASTER CONSOLE     * * * ROUTCDE * * *
.*********************************************************************
.RC MNOTE *,'     MCS ROUTE-CODES ASSIGNED TO THE MASTER CONSOLE--'
    MNOTE *,'               1 ALWAYS'
    MNOTE *,'               2 ALWAYS'
&SGSCHRB(1) SETB 1                 SET ROUTE CODE 1 VALID
&SGSCHRB(2) SETB 1                 SET ROUTE CODE 2 VALID
         AIF   (T'&ROUTCDE EQ 'O').OW        OMITTED, ASSIGN 1 ONLY
         AIF   ('&ROUTCDE' EQ 'ALL').RC3     IF ALL SPEC. SET GLOBALS
&E       SETA  1
.RC1     AIF   (N'&ROUTCDE LT &E).OW         FINISHED
         AIF   ('&ROUTCDE(&E)' LT '0').MM        NOT NUMERIC
         AIF   (&ROUTCDE(&E) LT 1 OR &ROUTCDE(&E) GT 16).MM VALIDITY
         AIF   (NOT(&SGSCHRB(&ROUTCDE(&E)))).RC6 IF ROUT CODE SPECIFIED
         AIF   (&ROUTCDE(&E) GT 2).MN  AND IT IS NOT 1 OR 2 IT IS
.*                                     INVALID
.RC6     ANOP
&SGSCHRB(&ROUTCDE(&E)) SETB 1                SET ROUTE-CODE GLOBAL
         AIF   (&ROUTCDE(&E) EQ 1 OR &ROUTCDE(&E) EQ 2).RC2
    MNOTE *,'               &ROUTCDE(&E)'
.RC2     ANOP
&E       SETA  &E+1
         AGO   .RC1
.RC3     ANOP
&E       SETA  3                   SET INDEX INTO ROUTE CODE GLBLS
.RC4     ANOP
&SGSCHRB(&E) SETB 1                SET ROUTE CODE VALUE
         MNOTE *,'               &E'
         AIF   (&E EQ 16).OW       HAVE ALL BEEN SET
&E       SETA  &E+1                INCR TO NEXT GLOBAL
         AGO   .RC4                CONTINUE UNTIL ALL ARE SET
.*********************************************************************
.* * * OLDWTOR * * *     FOR MASTER CONSOLE       * * * OLDWTOR * * *
.*********************************************************************
.OW      AIF   (T'&OLDWTOR NE 'O').OW0       NOT OMITTED
    MNOTE *,'     ALL CONSOLES WILL RECEIVE WTOR MESSAGES'      OZ03843
    MNOTE *,'     THAT ARE NOT ASSIGNED A MCS ROUTE-CODE'
&E       SETA  1                   INITIALIZE COUNT
.OW3     AIF   (&E GT 16).AR       IF FINISHED, GO ON
&SGSCHOB(&E) SETB 1                SET OLDWTOR GLOBAL
&E       SETA  &E+1                INCREMENT COUNT
         AGO   .OW3                LOOP TO DO THE NEXT
.OW0     ANOP
    MNOTE *,'     WTOR MESSAGES THAT ARE NOT ASSIGNED A MCS ROUTE-'
    MNOTE *,'     CODE WILL BE ROUTED WITH THE FOLLOWING CODE(S)--'
&E       SETA  1
.OW1A    AIF   (N'&OLDWTOR LT &E).AR    FINISHED, NEXT
         AIF   ('&OLDWTOR(&E)' LT '0').MO    NOT NUMERIC
         AIF   (&OLDWTOR(&E) LT 1 OR &OLDWTOR(&E) GT 16).MO VALIDITY
         AIF   (&SGSCHOB(&OLDWTOR(&E))).MP   N REDUNDANT
&SGSCHOB(&OLDWTOR(&E)) SETB 1
    MNOTE *,'               &OLDWTOR(&E)' MESSAGE
.OW2     ANOP
&E       SETA  &E+1
         AGO   .OW1A
.*********************************************************************
.* * * AREA * * *        FOR MASTER CONSOLE       * * * AREA  * * *
.*********************************************************************
.AR      AIF   (T'&AREA EQ 'O').PF      OMITTED
         AIF   (N'&AREA GT 11).MQ       TOO MANY PARAMETERS
.AX MNOTE *,'     STATUS DISPLAY AREAS FOR MASTER CONSOLE HAVE'
    MNOTE *,'     LENGTHS OF --'
&A       SETA  0                        INITIALIZE
.AR2     AIF   (&A EQ N'&AREA).PF       PARAMETERS EXHAUSTED
&A       SETA  &A+1                     INCREMENT
         AIF   ('&AREA(&A)' LT '0').MR  NUMERIC VALIDITY
         AIF   (&AREA(&A) LT 4 OR &AREA(&A) GT 47).MR  RANGE VALIDITY
&SGSCHAA(12) SETA &SGSCHAA(12)+&AREA(&A)     ACCUMULATED VALUE
&SGSCHAA(&A) SETA &AREA(&A)                  NTH VALUE
     MNOTE *,'               &AREA(&A)'
         AGO   .AR2
.*********************************************************************
.* * * PFK * * *         FOR MASTER CONSOLE        * * * PFK * * *
.*********************************************************************
.PF      AIF   (T'&PFK EQ 'O').CK1      OMITTED
         AIF   ('&PFK' LT '0').MS       NUMERIC VALIDITY
         AIF   (&PFK GT 24).MS          24 maximum              ZJW0011
&SGSCHDA(16) SETA &PFK                  GLOBAL VALUE
&SGSCHDB(25)  SETB   1                  PFK INCLUDED IN SYSTEM
    MNOTE *,'     MASTER CONSOLE WILL HAVE &PFK PROGRAM FUNCTION KEYS'
.CK1     AIF   (T'&USE NE 'O').MH           INVALID WITH MCONS
.CK2     AIF   (T'&VALDCMD NE 'O').MI       INVALID WITH MCONS
         AGO   .MEND
.*********************************************************************
.*           END OF MASTER CONSOLE CHECKS
.*********************************************************************
.*
.*        BEGIN CHECKS FOR SECONDARY CONSOLES
.*
.*********************************************************************
.* * * SECONS * * *         * * *    * * *        * * * SECONS * * *
.R0      ANOP
&SGSCNYA(2) SETA &SGSCNYA(2)+1          NUMBER OF SECONS PARMS
         AIF   (&SGSCNYA(2) GT 98).M0   MAX 98 SECONDARY CONSOLES
         AIF   (T'&SECONS EQ 'O').M1    SECONS VALUE OMITTED
         AIF   (N'&SECONS NE 1).R2      MORE THAN ONE SECONDARY CONSOLE
&C1      SETC  '&SECONS'(1,2)
.*               *************************************
.***                 OUTPUT ONLY SECONDARY CONSOLE                 ***
.*               *************************************
         AIF   ('&C1' NE 'O-').R1       O-ONLY BEGIN
         AIF   (K'&SECONS NE 5).M2      O-XXX FORMAT REQUIRED
&C1      SETC  '&SECONS'(3,3)           UNIT ADDRESS
&C2      SETC  'FFF'                    NULL ADDRESS
&SGSCNYA(1)    SETA &SGSCNYA(1)+1       INCREMENT SECONS COUNT
&SGSCNYA(3)  SETA  &SGSCNYA(3)+1        INCREMENT DEVICE COUNT
&SGSCNCC(&SGSCNYA(1))  SETC  '&C2&C1'   STORE UNIT ADDR-NULL ADDR
    MNOTE *,'     OUTPUT ONLY CONSOLE ADDRESS &C1'
&SW1     SETB  1                        OUTPUT-ONLY-CONSOLE SWITCH
         AGO   .DUPCK                   O-ONLY END
.*               *************************************
.***                 INPUT-OUTPUT CONSOLE                          ***
.*               *************************************
.R1      AIF   (K'&SECONS NE 3).M2      I/O CAPABILITY BEGIN
&SGSCNYA(1)    SETA &SGSCNYA(1)+1       INCREMENT SECONS COUNT
&SGSCNYA(3)  SETA  &SGSCNYA(3)+1        INCREMENT DEVICE COUNT
&SGSCNCC(&SGSCNYA(1))  SETC  '&SECONS&SECONS' IN-OUT ADDRESSES SAME
    MNOTE *,'     CONSOLE ADDRESS &SECONS'
         AGO   .DUPCK                   I/O CAPABILITY END
.*               *************************************
.***                   COMPOSITE CONSOLE                          ***
.*               *************************************
.R2      AIF   (N'&SECONS NE 2).M2      COMPOSITE BEGIN
         AIF   (K'&SECONS(1) NE 5 OR K'&SECONS(2) NE 5).M2
    MNOTE *,'     COMPOSITE CONSOLE WITH FOLLOWING ADDRESSES---'
&SGSCNYA(1)    SETA &SGSCNYA(1)+1       INCREMENT SECONS COUNT
.R3      AIF   (&A1 EQ 2).R5            FINISHED
&A1      SETA  &A1+1                    INCREMENT SUBLIST INDEX
&C1      SETC  '&SECONS(&A1)'(3,3)      FIRST ADDRESS
&C2      SETC  '&SECONS(&A1)'(1,2)      FIRST 2 CHARACTERS I- OR O-
         AIF   ('&C2' NE 'I-').R4       NOT I-, CHECK FOR O-
         AIF   (&B1).M20                I- REPEATED
&B1      SETB  1                        INPUT CONSOLE SWITCH
&SGSCNCC(&SGSCNYA(1)) SETC '&C1&SGSCNCC(&SGSCNYA(1))' I-ADDR(O-ADDR)
&SGSCNYA(3)  SETA  &SGSCNYA(3)+1        INCREMENT DEVICE COUNT
    MNOTE *,'        &C1 - INPUT'       INPUT ADDRESS
         AGO   .R3                      REPEAT FOR OUTPUT CONSOLE
.R4      AIF   ('&C2' NE 'O-').M20      CHECK FOR OUTPUT
         AIF   (&B2).M20                OUTPUT REPEATED?
&B2      SETB  1                        OUTPUT CONSOLE SWITCH ON
&SGSCNCC(&SGSCNYA(1)) SETC '&SGSCNCC(&SGSCNYA(1))&C1' (I-ADDR)O-ADDR
&SGSCNYA(3)  SETA  &SGSCNYA(3)+1        INCREMENT DEVICE COUNT
    MNOTE *,'        &C1 - OUTPUT'
         AGO   .R3                      CHECK NEXT COMPOSITE ADDRESS
.R5      ANOP                           COMPOSITE END
&SW2     SETB  1                        COMPOSITE CONSOLE SWITCH
.*           **********************************************
.****          CHECK FOR DUPLICATION OF CONSOLE ADDRESSES
.*           **********************************************
.DUPCK   AIF   (&SGSCNYA(1) EQ 1).S1B NO PREVIOUS CONSOLES      OZ04246
&LI      SETC  '&SGSCNCC(&SGSCNYA(1))'(1,3)   ADDRESS OF INPUT CONSOLE
&LO      SETC  '&SGSCNCC(&SGSCNYA(1))'(4,3)   ADDRESS OF OUTPUT CONSOLE
&A2      SETA  &SGSCNYA(1)              SECONDARY CONSOLE COUNT
.D1      ANOP
&A2      SETA  &A2-1                    DECREMENT SECONS COUNT
&PO      SETC  '&SGSCNCC(&A2)'(4,3)     PREVIOUS OUTPUT CONSOLE
         AIF   ('&LO' NE '&PO').D2      COMPARE OUTPUT CONSOLES
.XX MNOTE 5,'* * * IEICON000 SECONS VALUE &LO PREVIOUSLY SPECIFIED'
&SGQUITB SETB  1
&SCR     SETB  1
.D2      AIF   (NOT &SW2).D3            NOT COMPOSITE CONSOLE
&PI      SETC  '&SGSCNCC(&A2)'(1,3)     PREVIOUS INPUT CONSOLE
         AIF   ('&LI' NE '&PI').D3      COMPARE INPUT CONSOLES
    MNOTE 5,'* * * IEICON000 SECONS VALUE &LI PREVIOUSLY SPECIFIED'
&SGQUITB SETB  1
&SCR     SETB  1                        ERROR IN SECONS SPECIFICATION
.D3      AIF   (&A2 GT 1).D1            REPEAT FOR ALL PREV CONSOLES
         AIF   (NOT &SCR).S1            NO ERROR IN SECONS SPECS
&SGSCNCC(&SGSCNYA(1)) SETC ''           NULLIFY SECONS ADDRESS
&SGSCNYA(1)    SETA &SGSCNYA(1)-1       DECREMENT SECONS COUNT
         MEXIT                          ERROR EXIT
.*                  ******************************
.*                    IOC FOR SECONDARY CONSOLES
.*                  ******************************
.S1      AIF   ('&SGSCHDC(16)' EQ '').S1B NO MASTER YET        @AZ11147
         AIF   ('&LO' EQ '&SGSCHDC(1)'(1,3)).XX SAME AS MASTER CONSOLE
.S1B     AIF   ('&IOC' EQ 'NO').S1A NOT INTEGRATED OPER CONSOLE OZ04246
         AIF   ('&SGSCNCC(&SGSCNYA(1))'(1,3) EQ 'FFF').IOCE  OUTPUT
&SGSIOCC(&SGSCNYA(1)) SETC '&SGSCNCC(&SGSCNYA(1))'(1,3)
         AGO   .S1A
.IOCE    ANOP
         MNOTE 5,'* *IEICON032 IOC CANNOT BE SPECIFIED FOR OUTPUT ONLY'
         MEXIT
.***           **********************************
.* * *           ALTCONS  FOR SECONDARY CONSOLE                  * * *
.***           **********************************
.S1A     AIF   (T'&ALTCONS EQ 'O').BC   ALTERNATE CONSOLE SPECIFIED
         AIF   (N'&ALTCONS NE 1).S3     OUTPUT ONLY ALTERNATE CONSOLE
&C1      SETC  '&ALTCONS'(1,2)          CHECK 1ST TWO CHAR OF ADDRESS
         AIF   ('&C1' NE 'O-').S2       O- INDICATES OUTPUT ONLY
         AIF   (NOT &SW1).M9            OUTPUT ONLY SECONS SWITCH
         AIF   (K'&ALTCONS NE 5).M4     O-XXX ADDRESS FORMAT REQUIRED
&C1      SETC  '&ALTCONS'(3,3)          UNIT ADDRESS
&C2      SETC  'FFF'                    NULL ADDRESS
&SGSCNAC(&SGSCNYA(1))  SETC  '&C2&C1'   UNIT AND NULL ADDRESS STORED
    MNOTE *,'     OUTPUT ONLY ALTCONS ADDRESS &C1'
         AGO   .BC                      CHECK NEXT PARAMETER
.S2      AIF   (K'&ALTCONS NE 3).M4     INPUT-OUTPUT ALTERNATE CONSOLE
&SGSCNAC(&SGSCNYA(1))  SETC  '&ALTCONS&ALTCONS'     STORE ADDRESS
    MNOTE *,'     ALTCONS ADDRESS &ALTCONS'
         AGO   .BC                       CHECK NEXT PARAMETER
.S3      AIF   (N'&ALTCONS NE 2).M4      COMPOSITE ALTERNATE CONSOLE
         AIF   (K'&ALTCONS(1) NE 5 OR K'&ALTCONS(2) NE 5).M4
    MNOTE *,'     COMPOSITE ALTERNATE CONSOLE WITH FOLLOWING'
    MNOTE *,'      ADDRESSES---'
.S4      AIF   (&A3 EQ 2).BC             CHECK IF BOTH ADDRESSES STORED
&A3      SETA  &A3+1                     INCREMENT SUBLIST INDEX
&C1      SETC  '&ALTCONS(&A3)'(3,3)      ADDRESS
&C2      SETC  '&ALTCONS(&A3)'(1,2)      I-  OR  O-
         AIF   ('&C2' NE 'I-').S5        CHECK IF INPUT ADDRESS
         AIF   (&B3).M40                 CONPOSITE ALTCONS INPUT SWITCH
&B3      SETB  1                         SET SWITCH
&SGSCNAC(&SGSCNYA(1)) SETC '&C1&SGSCNAC(&SGSCNYA(1))'  I-ADDR(O-ADDR)
    MNOTE *,'        &C1 - INPUT'
         AGO   .S4                       REPEAT FOR OUTPUT ALTCONS
.S5      AIF   ('&C2' NE 'O-').M40       INVALID CHARACTERS
         AIF   (&B4).M40                 COMPOSITE ALTCONS OUTPUT SW
&B4      SETB  1                         SET SWITCH
&SGSCNAC(&SGSCNYA(1)) SETC '&SGSCNAC(&SGSCNYA(1))&C1'  (I-ADDR)O-ADDR
    MNOTE *,'        &C1 - OUTPUT'
         AGO   .S4                       REPEAT FOR INPUT ALTCONS
.***           **********************************
.* * *           ROUTCDE FOR SECONDARY CONSOLES
.***           **********************************
.BC      AIF   (T'&ROUTCDE EQ 'O').C7    OMITTED
         AIF   ('&ROUTCDE' EQ 'ALL').C4  ALL ROUTE CODES
&A1      SETA  1
    MNOTE *,'     ROUTING CODES ASSIGNED TO THIS CONSOLE ARE---'
.BE      AIF   (N'&ROUTCDE LT &A1).C8       EXHAUSTED
         AIF   (T'&ROUTCDE(&A1) NE 'N').M5  NOT NUMERIC
         AIF   ('&ROUTCDE(&A1)' LT '1' OR '&ROUTCDE(&A1)' GT '16').M5
.* THE FOLLOWING CHECKS EACH ROUTING CODE FOR ALREADY SPECIFIED -
.* IF IT HAS NOT BEEN SPECIFIED, IT WILL COME THROUGH ALL CHECKS
.* WITHOUT BRANCHING.
  AIF  (&SGRT01B(&SGSCNYA(1)) AND '&ROUTCDE(&A1)' EQ  '1').M55 DUPLIC'D
  AIF  (&SGRT02B(&SGSCNYA(1)) AND '&ROUTCDE(&A1)' EQ  '2').M55 DUPLIC'D
  AIF  (&SGRT03B(&SGSCNYA(1)) AND '&ROUTCDE(&A1)' EQ  '3').M55 DUPLIC'D
  AIF  (&SGRT04B(&SGSCNYA(1)) AND '&ROUTCDE(&A1)' EQ  '4').M55 DUPLIC'D
  AIF  (&SGRT05B(&SGSCNYA(1)) AND '&ROUTCDE(&A1)' EQ  '5').M55 DUPLIC'D
  AIF  (&SGRT06B(&SGSCNYA(1)) AND '&ROUTCDE(&A1)' EQ  '6').M55 DUPLIC'D
  AIF  (&SGRT07B(&SGSCNYA(1)) AND '&ROUTCDE(&A1)' EQ  '7').M55 DUPLIC'D
  AIF  (&SGRT08B(&SGSCNYA(1)) AND '&ROUTCDE(&A1)' EQ  '8').M55 DUPLIC'D
  AIF  (&SGRT09B(&SGSCNYA(1)) AND '&ROUTCDE(&A1)' EQ  '9').M55 DUPLIC'D
  AIF  (&SGRT10B(&SGSCNYA(1)) AND '&ROUTCDE(&A1)' EQ '10').M55 DUPLIC'D
  AIF  (&SGRT11B(&SGSCNYA(1)) AND '&ROUTCDE(&A1)' EQ '11').M55 DUPLIC'D
  AIF  (&SGRT12B(&SGSCNYA(1)) AND '&ROUTCDE(&A1)' EQ '12').M55 DUPLIC'D
  AIF  (&SGRT13B(&SGSCNYA(1)) AND '&ROUTCDE(&A1)' EQ '13').M55 DUPLIC'D
  AIF  (&SGRT14B(&SGSCNYA(1)) AND '&ROUTCDE(&A1)' EQ '14').M55 DUPLIC'D
  AIF  (&SGRT15B(&SGSCNYA(1)) AND '&ROUTCDE(&A1)' EQ '15').M55 DUPLIC'D
  AIF  (&SGRT16B(&SGSCNYA(1)) AND '&ROUTCDE(&A1)' EQ '16').M55 DUPLIC'D
    MNOTE *,'        &ROUTCDE(&A1)'
.***           **********************************
.***              SET ROUTCDE BIT AS SPECIFIED
.***           **********************************
&SGRT01B(&SGSCNYA(1)) SETB ('&ROUTCDE(&A1)' EQ  '1' OR &SGRT01B(&SGSCNY*
               A(1)))
&SGRT02B(&SGSCNYA(1)) SETB ('&ROUTCDE(&A1)' EQ  '2' OR &SGRT02B(&SGSCNY*
               A(1)))
&SGRT03B(&SGSCNYA(1)) SETB ('&ROUTCDE(&A1)' EQ  '3' OR &SGRT03B(&SGSCNY*
               A(1)))
&SGRT04B(&SGSCNYA(1)) SETB ('&ROUTCDE(&A1)' EQ  '4' OR &SGRT04B(&SGSCNY*
               A(1)))
&SGRT05B(&SGSCNYA(1)) SETB ('&ROUTCDE(&A1)' EQ  '5' OR &SGRT05B(&SGSCNY*
               A(1)))
&SGRT06B(&SGSCNYA(1)) SETB ('&ROUTCDE(&A1)' EQ  '6' OR &SGRT06B(&SGSCNY*
               A(1)))
&SGRT07B(&SGSCNYA(1)) SETB ('&ROUTCDE(&A1)' EQ  '7' OR &SGRT07B(&SGSCNY*
               A(1)))
&SGRT08B(&SGSCNYA(1)) SETB ('&ROUTCDE(&A1)' EQ  '8' OR &SGRT08B(&SGSCNY*
               A(1)))
&SGRT09B(&SGSCNYA(1)) SETB ('&ROUTCDE(&A1)' EQ  '9' OR &SGRT09B(&SGSCNY*
               A(1)))
&SGRT10B(&SGSCNYA(1)) SETB ('&ROUTCDE(&A1)' EQ '10' OR &SGRT10B(&SGSCNY*
               A(1)))
&SGRT11B(&SGSCNYA(1)) SETB ('&ROUTCDE(&A1)' EQ '11' OR &SGRT11B(&SGSCNY*
               A(1)))
&SGRT12B(&SGSCNYA(1)) SETB ('&ROUTCDE(&A1)' EQ '12' OR &SGRT12B(&SGSCNY*
               A(1)))
&SGRT13B(&SGSCNYA(1)) SETB ('&ROUTCDE(&A1)' EQ '13' OR &SGRT13B(&SGSCNY*
               A(1)))
&SGRT14B(&SGSCNYA(1)) SETB ('&ROUTCDE(&A1)' EQ '14' OR &SGRT14B(&SGSCNY*
               A(1)))
&SGRT15B(&SGSCNYA(1)) SETB ('&ROUTCDE(&A1)' EQ '15' OR &SGRT15B(&SGSCNY*
               A(1)))
&SGRT16B(&SGSCNYA(1)) SETB ('&ROUTCDE(&A1)' EQ '16' OR &SGRT16B(&SGSCNY*
               A(1)))
.C0      ANOP
&A1      SETA  &A1+1                    INCREMENT COUNT FOR ROUTCDE
         AGO   .BE                      REPEAT FOR NEXT ROUTCDE PARM
.C4 MNOTE *,'     ALL ROUTING CODES ARE ASSIGNED TO THIS CONSOLE'
&SGRT01B(&SGSCNYA(1)) SETB 1            SET ALL ROUTCDE BITS
&SGRT02B(&SGSCNYA(1)) SETB 1
&SGRT03B(&SGSCNYA(1)) SETB 1
&SGRT04B(&SGSCNYA(1)) SETB 1
&SGRT05B(&SGSCNYA(1)) SETB 1
&SGRT06B(&SGSCNYA(1)) SETB 1
&SGRT07B(&SGSCNYA(1)) SETB 1
&SGRT08B(&SGSCNYA(1)) SETB 1
&SGRT09B(&SGSCNYA(1)) SETB 1
&SGRT10B(&SGSCNYA(1)) SETB 1
&SGRT12B(&SGSCNYA(1)) SETB 1
&SGRT11B(&SGSCNYA(1)) SETB 1
&SGRT13B(&SGSCNYA(1)) SETB 1
&SGRT14B(&SGSCNYA(1)) SETB 1
&SGRT15B(&SGSCNYA(1)) SETB 1
&SGRT16B(&SGSCNYA(1)) SETB 1
         AGO   .C8
.C7 MNOTE *,'     NO ROUTING CODES ARE ASSIGNED TO THIS CONSOLE'
.***           **********************************
.***             VALDCMD FOR SECONDARY CONSOLES
.***           **********************************
.C8      AIF   (T'&VALDCMD EQ 'O').CE     OMITTED
 AIF (&SW1).M8                            OUTPUT ONLY CONSOLE
    MNOTE *,'     COMMAND CODES ASSIGNED TO THIS CONSOLE ARE---'
&A1      SETA  1                          RESET SUBLIST INDEX
.CA      AIF   (N'&VALDCMD LT &A1).T1     EXHAUSTED
         AIF   ('&VALDCMD(&A1)' LT '1' OR '&VALDCMD(&A1)' GT '3').M6
  AIF  (&SGCMD1B(&SGSCNYA(1)) AND '&VALDCMD(&A1)' EQ '1').M7 DUPLIC'D
  AIF  (&SGCMD2B(&SGSCNYA(1)) AND '&VALDCMD(&A1)' EQ '2').M7 DUPLIC'D
  AIF  (&SGCMD3B(&SGSCNYA(1)) AND '&VALDCMD(&A1)' EQ '3').M7 DUPLIC'D
    MNOTE *,'        &VALDCMD(&A1)'
&SGCMD1B(&SGSCNYA(1)) SETB ('&VALDCMD(&A1)' EQ '1' OR &SGCMD1B(&SGSCNYA*
               (1)))
&SGCMD2B(&SGSCNYA(1)) SETB ('&VALDCMD(&A1)' EQ '2' OR &SGCMD2B(&SGSCNYA*
               (1)))
&SGCMD3B(&SGSCNYA(1)) SETB ('&VALDCMD(&A1)' EQ '3' OR &SGCMD3B(&SGSCNYA*
               (1)))
.CC      ANOP
&A1      SETA  &A1+1                    INCREMENT SUBLIST INDEX
         AGO   .CA                      REPEAT FOR NEXT VALDCMD PARM
.CE MNOTE *,'     COMMANDS ''DISPLAY,LOG'' ARE ASSIGNED TO THIS CONSOLE*
               '
.***           **********************************
.***               USE FOR SECONDARY CONSOLES
.***           **********************************
.T1      AIF   (T'&USE EQ 'O').E1       OMITTED
         AIF   ('&USE' NE 'MS').T2      GO CHECK FOR SD
    MNOTE *,'     LIMITED CAPACITY CONSOLE-MESSAGE STREAM MODE'
&SGSCMSB(&SGSCNYA(1)) SETB 1            MSG STREAM MODE USE BIT
         AGO   .E1                      CHECK NEXT PARAMETER
.T2      AIF   ('&USE' NE 'SD').MA      ANY OTHER INVALID
    MNOTE *,'     LIMITED CAPACITY CONSOLE-STATUS DISPLAY MODE'
&SGSCSDB(&SGSCNYA(1)) SETB 1            STATUS DISPLAY MODE USE BIT
.***           **********************************
.***               AREA FOR SECONDARY CONSOLES
.***           **********************************
.E1      AIF   (T'&AREA EQ 'O').G1           OMITTED
         AIF   (&SGSCMSB(&SGSCNYA(1))).MB    ERROR IF MS CONSOLE
         AIF   (N'&AREA GT 11).MC            TOO MANY AREAS DEFINED
    MNOTE *,'     STATUS DISPLAY AREAS FOR THIS CONSOLE HAVE LENGTHS OF*
               --'
&A1      SETA  1                             RESET SUBLIST INDEX
.E2      AIF   (&A1 GT N'&AREA).E4           PARAMETERS EXHAUSTED
         AIF   ('&AREA(&A1)' LT '4' OR '&AREA(&A1)' GT '47').MD
    MNOTE *,'        &AREA(&A1)'
&SGAR12A(&SGSCNYA(1)) SETA &SGAR12A(&SGSCNYA(1))+&AREA(&A1)  TOTAL LNG
.E3      ANOP
&A1      SETA  &A1+1                         INCREMENT SUBLIST INDEX
         AGO   .E2                           REPEAT FOR ALL PARAMETERS
.E4      AIF   (&SW3).G1                     ERROR IN PARAMETER
         AIF   (N'&AREA EQ 1).F1
         AIF   (N'&AREA EQ 2).F2
         AIF   (N'&AREA EQ 3).F3
         AIF   (N'&AREA EQ 4).F4
         AIF   (N'&AREA EQ 5).F5
         AIF   (N'&AREA EQ 6).F6
         AIF   (N'&AREA EQ 7).F7
         AIF   (N'&AREA EQ 8).F8
         AIF   (N'&AREA EQ 9).F9
         AIF   (N'&AREA EQ 10).F10
&SGAR11A(&SGSCNYA(1)) SETA &AREA(11)    STORE LNG FOR EACH DISPLAY AREA
.F10     ANOP
&SGAR10A(&SGSCNYA(1)) SETA &AREA(10)
.F9      ANOP
&SGAR09A(&SGSCNYA(1)) SETA &AREA(9)
.F8      ANOP
&SGAR08A(&SGSCNYA(1)) SETA &AREA(8)
.F7      ANOP
&SGAR07A(&SGSCNYA(1)) SETA &AREA(7)
.F6      ANOP
&SGAR06A(&SGSCNYA(1)) SETA &AREA(6)
.F5      ANOP
&SGAR05A(&SGSCNYA(1)) SETA &AREA(5)
.F4      ANOP
&SGAR04A(&SGSCNYA(1)) SETA &AREA(4)
.F3      ANOP
&SGAR03A(&SGSCNYA(1)) SETA &AREA(3)
.F2      ANOP
&SGAR02A(&SGSCNYA(1)) SETA &AREA(2)
.F1      ANOP
&SGAR01A(&SGSCNYA(1)) SETA &AREA(1)
.***           **********************************
.***               PFK FOR SECONDARY CONSOLES
.***           **********************************
.G1      AIF   (T'&PFK EQ 'O').H2            OMITTED
         AIF   ('&PFK' LT '0').ME            NOT NUMERIC
         AIF   ('&PFK' GT '24').ME           24 maximum         ZJW0011
&SGSCNPA(&SGSCNYA(1)) SETA &PFK              STORE NUMBER OF PFKS
&SGSCHDB(25)  SETB   1                       PFK INCLUDED IN SYSTEM
    MNOTE *,'     CONSOLE WILL HAVE &PFK PROGRAM FUNCTION KEYS'
.H2      AIF   (T'&OLDWTOR NE 'O').MG       INVALID WITH SECONS
         MEXIT                              END OF SECONS PARAMETERS
.*********************************************************************
.* * * * * *          * * *  ERROR MESSAGES  * * *         * * * * * *
.*********************************************************************
.*
.M0 MNOTE 5,'* * * IEICON001 MAXIMUM 98 SECONDARY CONSOLES ARE ALLOWED'
&SGQUITB SETB 1
         MEXIT
.M1 MNOTE 5,'* * * IEICON002 MCONS OR SECONS VALUE NOT SPECIFIED'
&SGQUITB SETB 1
         MEXIT
.M20     ANOP
&SGSCNCC(&SGSCNYA(1)) SETC ''
&SGSCNYA(1) SETA &SGSCNYA(1)-1
.M2 MNOTE 5,'* * * IEICON003 SECONS VALUE &SECONS INVALID'
&SGQUITB SETB 1
         MEXIT
.M3 MNOTE 5,'* * * IEICON004 SECONS AND MCONS PARAMETERS MUTUALLY EXLUS*
               IVE'
&SGQUITB SETB 1
         MEXIT
.M40     ANOP
&SGSCNAC(&SGSCNYA(1)) SETC ''
.M4 MNOTE 5,'* * * IEICON005 ALTCONS VALUE &ALTCONS INVALID'
&SGQUITB SETB 1
         AGO   .BC
.M5 MNOTE 5,'* * * IEICON006 ROUTCDE VALUE &ROUTCDE(&A1) INVALID'
&SGQUITB SETB 1
         AGO   .C0
.M55 MNOTE 5,'* * * IEICON007 ROUTCDE VALUE &ROUTCDE(&A1) DUPLICATED'
&SGQUITB SETB 1
         AGO  .C0
.M6  MNOTE  0,'* * * IEICON034 WARNING- VALDCMD VALUE &VALDCMD(&A1)    *
               INVALID AND IGNORED'
         AGO   .CC
.M7 MNOTE 5,'* * * IEICON008 VALDCMD VALUE &VALDCMD(&A1) DUPLICATED'
&SGQUITB SETB 1
         AGO  .CC
.M8 MNOTE 5,'* * * IEICON009 VALDCMD SPECIFICATION FOR OUTPUT ONLY DEVI*
               CE INVALID'
&SGQUITB SETB  1
         AGO   .CC
.M9 MNOTE 5,'* * * IEICON010 OUTPUT ONLY ALTCONS DEVICE INVALID WITH CO*
               NSOLE DEVICE HAVING INPUT CAPABILITY'
&SGQUITB SETB  1
         AGO  .BC
.MA MNOTE 5,'* * * IEICON011 USE VALUE &USE INVALID'
&SGQUITB SETB  1
         AGO   .E1
.MB MNOTE 5,'* * * IEICON012 AREA SPECIFICATION INVALID WITH CONSOLE IN*
                MESSAGE STREAM MODE'
&SGQUITB SETB  1
         AGO   .G1
.MC MNOTE 5,'* * * IEICON013 MAXIMUM OF 11 AREAS MAY BE DEFINED'
&SGQUITB SETB  1
         AGO   .G1
.MD MNOTE 5,'* * * IEICON014 AREA VALUE &AREA(&A1) INVALID'
&SGQUITB SETB  1
&SW3     SETB  1
         AGO   .E3
.ME MNOTE 5,'* * * IEICON015 PFK VALUE &PFK INVALID'
&SGQUITB SETB  1
         AGO   .H2
.MG MNOTE 5,'* * * IEICON017 OLDWTOR PARAMETER ONLY VALID WITH MCONS'
&SGQUITB SETB  1
         AGO   .MEND
.MH MNOTE 5,'* * * IEICON018 USE PARAMETER INVALID FOR MASTER CONSOLE'
&SGQUITB SETB  1
         AGO   .CK2
.MI MNOTE 5,'* * * IEICON019 VALDCMD PARAMETER INVALID FOR MASTER CONSO*
               LE'
&SGQUITB SETB  1
         AGO   .MEND
.MJ MNOTE 5,'* * * IEICON020 MCONS PARAMETER PREVIOUSLY SPECIFIED'
&SGQUITB SETB  1
         AGO   .MEND
.MJ1 MNOTE 5,'* * * IEICON023 TYPE=&TYPE VALUE INVALID'
&SGQUITB SETB 1
         AGO   .CKJES
.MJ2 MNOTE 5,'* * * IEICON024 ONLY VALDCMD VALID WITH TYPE=JES'
&SGQUITB SETB 1
         AGO   .NJ
.MK0     ANOP
&SGSCHDC(1) SETC   ''
.MK MNOTE 5,'* * * IEICON021 MCONS=&MCONS VALUE INVALID'
&SGQUITB SETB  1
         AGO   .ALT
.ML0     ANOP
&SGSCHDC(2) SETC   ''
.ML MNOTE 5,'* * * IEICON022 ALTCONS=&ALTCONS VALUE INVALID'
&SGQUITB SETB  1
         AGO   .RC
.MM MNOTE 5,'* * * IEICON006 ROUTCDE=-&ROUTCDE(&E)- VALUE INVALID'
&SGQUITB SETB  1
         AGO   .RC2
.MN MNOTE 5,'* * * IEICON007 ROUTCDE=-&ROUTCDE(&E)- VALUE DUPLICATED'
&SGQUITB SETB  1
         AGO   .RC2
.MO MNOTE 5,'* * * IEICON013 OLDWTOR=-&OLDWTOR(&E)- VALUE INVALID'
&SGQUITB SETB  1
         AGO   .OW2
.MP MNOTE 5,'* * * IEICON026 OLDWTOR=-&OLDWTOR(&E)- VALUE DUPLICATED'
&SGQUITB SETB  1
         AGO   .OW2
.MQ MNOTE 5,'* * * IEICON027 MAXIMUM OF 11 AREAS MAY BE DEFINED'
&SGQUITB SETB  1
         AGO   .AX
.MR MNOTE 5,'* * * IEICON014 AREA=-&AREA(&A)- VALUE INVALID'
&SGQUITB SETB  1
         AGO   .AR2
.MS MNOTE 5,'* * * IEICON015 PFK=&PFK VALUE INVALID'
&SGQUITB SETB  1
         AGO   .CK1
.MT      MNOTE 5,'* * *  IEICON030  IOC VALUE &IOC IS INVALID'
&SGQUITB SETB  1
         AGO   .CO
       AGO    .RC
.MEND    MEND
