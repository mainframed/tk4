         MACRO
         SGIEA2D1
.*
.*  GLOBAL DEFINITION
         COPY SGGBLPAK
.*  LOCAL A DEFINITION
         LCLA &I,&N,&X,&DEV,&NUM(5),&LIM(5),&J,&K              @ZA14064
.*  LOCAL C DEFINITION
         LCLC &H(2),&TYPC,&ADDR(495)                           @ZA14064
.*
.*  THIS MACRO WAS CREATED FOR VS/2 RELEASE 2 DIDOCS (SD CODE Y02958)
.*  TO ASSEMBLE A PAGEABLE DCM FOR EACH CRT CONSOLE TYPE GENERATED
.*  TO RESIDE ON SYS1.LPALIB.
.*  THE NAMING CONVENTION IS DCMXXX WHERE XXX IS THE DEVICE ADDRESS.
.*  A CSECT NAME AND UP TO 15 ALIAS NAMES WILL BE GENERATED FOR EACH
.*  PAGEABLE DCM.
&H(1)    SETC  '&SGSCHDC(1)'(1,3)
&H(2)    SETC  '&SGSCHDC(1)'(4,3)
&LIM(1)  SETA  0                                               @ZA14064
&NUM(2)  SETA  99                                              @ZA14064
&LIM(2)  SETA  99                                              @ZA14064
&NUM(3)  SETA  198                                             @ZA14064
&LIM(3)  SETA  198                                             @ZA14064
&NUM(4)  SETA  297                                             @ZA14064
&LIM(4)  SETA  297                                             @ZA14064
&NUM(5)  SETA  396                                             @ZA14064
&LIM(5)  SETA  396                                             @ZA14064
.STARTI  ANOP
&I       SETA  1
         AIF   ('&H(1)' NE '&H(2)').LEAVEI
.NEXTI   ANOP
&I       SETA  2
.LEAVEI  CONVERT TO=HEX,VALUE=&H(&I),DIGITS=3
&X       SETA  &HEX
         IOCHECK LOOKFOR=&X
&X       SETA  &SGDASPA(&HEX)                                  @ZA15421
         AIF   (NOT &SGDT05B(&X)).HALFDUN
&DEV     SETA  4                                              MB YM4087
         AIF   (&SGDU10B(&X) OR &SGDU08B(&X) AND &SGDM02B(&X) OR &SGDU1X
               3B(&X)).GR                                      @G51AK02
&DEV     SETA  5                                              MB YM4087
         AIF   (&SGDU08B(&X) AND &SGDM01B(&X)).GR
&DEV     SETA  3                                              MB YM4087
         AIF   (&SGDU02B(&X)).GR
&DEV     SETA  2                                              MB YM4087
         AIF   (&SGDU01B(&X)).GR
&DEV     SETA  1                                              MB YM4087
         AIF   (NOT &SGDU06B(&X)).HALFDUN
.GR      ANOP
&NUM(&DEV) SETA  &NUM(&DEV)+1                                 MB YM4087
&ADDR(&NUM(&DEV)) SETC 'DCM&H(&I)'      SET PAGEABLE DCM NAME MB YM4087
.HALFDUN AIF   ('&H(2)' NE 'FFF' AND &I EQ 1).NEXTI
.JESCON  AIF   (&N GE &SGSCNYA(1)).OUT                      JE @ZM32389
&N       SETA  &N+1
         AIF   ('&SGSCNCC(&N)' EQ 'JESJES').JESCON          JE @ZM32389
&H(1)    SETC  '&SGSCNCC(&N)'(1,3)
&H(2)    SETC  '&SGSCNCC(&N)'(4,3)
.FMSET   AIF   ('&H(1)' EQ 'FFF').NEXTI
         AGO   .STARTI
.OUT     ANOP                                                 MB YM4087
         AIF   (&NUM(1)+&NUM(2)+&NUM(3)+&NUM(4)+&NUM(5) LE 990).NOPCH
.*                                                             @ZA14064
.*  THE FOLLOWING COMMENT CARD WILL BE GENERATED INTO THE STAGE 1
.*  OUTPUT TO IDENTIFY THE LEVEL AND CHANGE DATE ASSOCIATED WITH
.*  THE COPY OF SGIEA2D1 BEING USED IN ANY GIVEN SYSGEN.
.*  THE COMMENT SHOULD BE UPDATED WITH A NEW LEVEL NUMBER AND DATE
.*  WHENEVER ANY CHANGES ARE MADE TO THIS MACRO
.*
*** SGIEA2D1 *** DIDOCS *** 05/23/77 *** CHANGE LEVEL 03 ***   @G51AK02
&J       SETA  0                                              MB YM4087
&DEV     SETA  1                                              MB YM4087
.GENLOOP ANOP                                                 MB YM4087
&K       SETA  0                                               @ZA14064
&J       SETA  &J+1                                           MB YM4087
         AIF   (&J GT &NUM(&DEV)).NXTYPE                      MB YM4087
&TYPC    SETC  'ABCDE'(&DEV,1)                                MB YM4087
&SGCTRLC(6) SETC '&ADDR(&J)'        SET PAGEABLE DCM NAME     MB YM4087
         COPY  SGASMPAK
         PUNCH '&ADDR(&J)    CSECT '                          MB YM4087
.ENTRY   ANOP                                                 MB YM4087
         AIF   (&K EQ 15).ENDDCM                               @ZA14064
&K       SETA  &K+1                                            @ZA14064
&J       SETA  &J+1                                           MB YM4087
         AIF   (&J GT &NUM(&DEV)).ENDDCM                      MB YM4087
         PUNCH '         ENTRY &ADDR(&J)'                     MB YM4087
         PUNCH '&ADDR(&J) EQU   *'                            MB YM4087
         AGO   .ENTRY                                          @ZA14064
.ENDDCM  ANOP                                                 MB YM4087
         PUNCH '         IEECDCM DEVICE=&TYPC'                MB YM4087
         PUNCH '         END '
         PUNCH '/*'
         AGO   .GENLOOP                                        @ZA14064
.NXTYPE  ANOP                                                 MB YM4087
&DEV     SETA  &DEV+1                                          @ZA14064
         AIF   (&DEV GE 6).NOPCH                               @ZA14064
&J       SETA  &LIM(&DEV)                                      @ZA14064
         AGO   .GENLOOP                                        @ZA14064
.NOPCH   MEND                                                 MB YM4302
