         MACRO
      SGIEI1SU
         COPY  SGGBLPAK
.*
.*       SGIEI1SU IS AN INTERNAL MACRO TO GENERATE. IT IS USED TO
.*       VALIDATE SUPERVISOR AND SCHEDULER FUNCTIONS AND TO SET
.*       INTER-MACRO DEFAULTS.
.*
.*       THE GLOBALS USED ARE -
.*             &GETB(1)    - UNITNAME REQUIRED
.*             &GETB(3)    - I/O SYSGEN
.*             &SGMENTB(5) - CTRLPROG DEFAULTED
.*             &SGMENTB(6) - SCHEDULR DEFAULTED
.*             &SGMENTB(36)- UNITNAME REQUIRED
.*             &SGMENTB(61)- EDIT MACRO
.*             &SGMENTB(63)- TSO DEFAULTED
.*             &SGCTRLC(29)- RESVOL DEVICE TYPE
.*             &SGDCLSB(41)- 2314
.*             &SGDCLSB(69)- 3330
.*             &SGDCLSB(75)- 2305-1
.*             &SGDCLSB(76)- 2305-2
.*             &SGDCLSB(84)- 3340
.*             &SGDCLSB(22)- 3350                              @ZM41663
.*             &SGDMGTB(4) - BTAM
.*             &SGDMGTB(5) - TCAM
.*             &SGDMGTB(6) - VTAM; DEFAULTED                    ZM33355
.*             &SGSSS2B    - INDUSTRY SUPPORT                   ZM33355
.*             &SGDSDFB(13)- VTAMLIB DEFINED                    ZM33355
.*             &SGSCHDA(13)- BLCMT
.*             &SGSCHDB(8) - CRJE
.*             &SGQUITB    - QUIT SWITCH
.*
.*
.*
.*
.* CHANGE ACTIVITIES -- Z30RSTA,OZ03665,ZM33355
.*                      ZM41663
.*                      Z40DSVN
.*
.***********************************************.
.***     CHECK FOR MINIMUM MACRO SPECS       ***.
.***********************************************.
*
          AIF   (&SGMENTB(36) OR &GETB(1)).D00  UNITNAME CHECK
    MNOTE 5,'* * * IEIGEN400 UNITNAME MACRO REQUIRED'
&SGQUITB SETB  1
.***********************************************.
.***     DEFAULT IF SOME MACROS NOT SPEC'D   ***.
.***********************************************.
.D00     AIF   (&SGMENTB(5)).A36          CTRLPROG SPECIFIED
*
    MNOTE 0,'* * * IEIGEN940 CTRLPROG MACRO DEFAULTED  '
    MNOTE *,'       CTRLPROG    '
         CTRLPROG
.A36     AIF   (&SGMENTB(6)).GEN          SCHEDULR SPECIFIED
*
    MNOTE 0,'* * * IEIGEN944 SCHEDULR MACRO DEFAULTED'          Z40DSVN
    MNOTE *,'       SCHEDULR    '
         SCHEDULR
*
.GEN     AIF   (&SGMENTB(63)).ALLDEF       TSO SPECIFIED
*
    MNOTE 0,'* * * IEIGEN941 TSO MACRO DEFAULTED'               Z40DSVN
    MNOTE *,'       TSO    '
         TSO
*
.ALLDEF     ANOP
.***********************************************.
.***     CHECK GENERATE COMPATIBILITY        ***.
.***********************************************.
         AIF   ('&SGCTRLC(29)'EQ'2314'   AND &SGDCLSB(41)).SCH
         AIF   ('&SGCTRLC(29)'EQ'2305-1' AND &SGDCLSB(75)).SCH
         AIF   ('&SGCTRLC(29)'EQ'2305-2' AND &SGDCLSB(76)).SCH
         AIF   ('&SGCTRLC(29)'EQ'3330'   AND &SGDCLSB(69)).SCH
         AIF   ('&SGCTRLC(29)'EQ'3340'   AND &SGDCLSB(84)).SCH
         AIF   ('&SGCTRLC(29)'EQ'3330-1' AND &SGDCLSB(85)).SCH
         AIF   ('&SGCTRLC(29)'EQ'3350'   AND &SGDCLSB(22)).SCH @ZM41663
         AIF   ('&SGCTRLC(29)' EQ '3380' AND &SGGENDB(103)).SCH  MVS38J
         AIF   ('&SGCTRLC(29)' EQ '3390' AND &SGGENDB(104)).SCH  MVS38J
         AIF   ('&SGCTRLC(29)' EQ '3375' AND &SGGENDB(105)).SCH  MVS38J
    MNOTE 5,'* * * IEIGEN401 INVALID DEVICE TYPE FOR RESVOL SPECIFIED I*
               N GENERATE MACRO - DEVICE SPECIFIED NOT IN SYSTEM'
&SGQUITB SETB  1
*
.SCH     ANOP
.***********************************************.
.***     CHECK SCHEDULER COMPATIBILITY       ***.
.***********************************************.
         AIF   (&SGDMGTB(4) OR NOT &SGSCHDB(8)).SCEND  CRJE OR BTAM
&SGDMGTB(4) SETB 1                      ACSMETH=BTAM DEFAULTED
    MNOTE 0,'* * * IEIGEN942 ACSMETH=BTAM DEFAULTED TO SUPPORT CRJE' **
.SCEND   ANOP                                                   OZ03665
.***********************************************.               ZM33355
.***     INDUSTRY COMPATIBILITY              ***.               ZM33355
.***********************************************.               ZM33355
         AIF (&SGDMGTB(6) OR NOT &SGSSS2B).INO  VTAM; IND=NO    Z40DSVN
&SGDMGTB(6) SETB 1                      SET VTAM ON             ZM33355
 MNOTE 0,'* * * ACSMETH=VTAM DEFAULTED TO SUPPORT IND=YES'      ZM33355
         AIF   (&SGDSDFB(13)).INO       VTAMLIB DEFINED         ZM33355
 MNOTE 0,'VTAMLIB NOT SPECIFIED.  ASSUMED PREALLOCATED ON RESVOL'      *
                                                                ZM33355
.INO     ANOP                                                   ZM33355
.***********************************************.
.***     TSO COMPATIBILITY                   ***.
.***********************************************.
         AIF   ('&SGCMDSC(1)' NE 'YES').TSN TSO NOT SPECIFIED?  Z40DSVN
         AIF   (&SGDMGTB(5) OR &SGDMGTB(6)).TSA    TCAM OR VTAM Z40DSVN
&SGQUITB SETB 1                NEITHER, QUIT                    Z40DSVN
 MNOTE 5,'* * * IEIGEN952 NO ACCRESS METHOD SPECIFIED FOR TSO'  Z40DSVN
.TSA     AIF   (&SGSCHDA(13) NE 0).TSC                          Z40DSVN
&SGSCHDA(13) SETA 100        DEFAULT BCLMT                      Z40DSVN
 MNOTE *,'    SYS1.BRODCAST WILL DEFAULT ROOM FOR &SGSCHDA(13)  NOTICE X
               MESSAGES'                                        Z40DSVN
.TSC     AIF   (&SGMENTB(61)).TSY       BIF EDIT SPECIFIED      Z40DSVN
    MNOTE *,'* * * IEIGEN943 EDIT DEFAULTED FOR TSO'            Z40DSVN
         EDIT
         AGO   .TSY                     CONTINUE                Z40DSVN
.TSN     ANOP                                                   Z40DSVN
         AIF   (NOT &SGMENTB(61)).TSY   EDIT SPECIFIED?         Z40DSVN
    MNOTE 5,'* * * IEIGEN951 EDIT SPECIFIED WITHOUT TSO'        Z40DSVN
&SGQUITB SETB 1                         SET QUIT BIT            Z40DSVN
.TSY     ANOP                                                   Z40DSVN
         MEND                                                   Z40DSVN
