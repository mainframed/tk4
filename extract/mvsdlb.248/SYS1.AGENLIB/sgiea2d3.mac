         MACRO
         SGIEA2D3
.*
.*  GLOBAL DEFINITION
         COPY SGGBLPAK
.*  LOCAL A DEFINITION
.* THE FOLLOWING CHANGE TO 99 FOR PFKLIM,NDX, AND PFKORG SUPPORT
.* THE CHANGE FROM 32 TO 99 CONSOLES                         @ZA36190
         LCLA &TOTPFK,&PFKLIM(99),&NDX(99),&P,&N,&I,&J,&X
.*  LOCAL B DEFINITION
         LCLB &PFKORG(99)
.*  LOCAL C DEFINITION
         LCLC &H
.*
.*  THIS MACRO WAS CREATED FOR VS/2 RELEASE 2 DIDOCS (SD CODE Y02958)
.*  TO ASSEMBLE IEEPFKEY TO RESIDE IN SYS1.DCMLIB
.*
         AIF   (&SGSCHDA(16) EQ 0).HALFDUN
&H       SETC  '&SGSCHDC(1)'(1,3)
&P       SETA  1
&PFKLIM(1) SETA &SGSCHDA(16)
&TOTPFK  SETA  &SGSCHDA(16)
         AGO   .ENDMAST
.HALFDUN ANOP
&N       SETA  &N+1
         AIF   (&N GT &SGSCNYA(1)).PFKDUN                     MB YM3307
         AIF   (&SGSCNPA(&N) EQ 0).HALFDUN
&H       SETC  '&SGSCNCC(&N)'(1,3)
&P       SETA  &P+1
&PFKLIM(&P) SETA &SGSCNPA(&N)
&TOTPFK  SETA  &TOTPFK+&SGSCNPA(&N)
.ENDMAST ANOP
&NDX(&P) SETA  &N
         CONVERT TO=HEX,VALUE=&H,DIGITS=3
&X       SETA  &HEX
         IOCHECK LOOKFOR=&X
&X       SETA  &SGDASPA(&HEX)                                  @ZA15421
&PFKORG(&P) SETB (NOT &SGDU01B(&X))
&PFKLIM(&P) SETA &PFKLIM(&P)+&PFKORG(&P)
         AGO   .HALFDUN                                       MB YM3307
.PFKDUN  AIF   (&P EQ 0).OUT
.*  THE FOLLOWING COMMENT CARD WILL BE GENERATED INTO THE STAGE 1
.*  OUTPUT TO IDENTIFY THE LEVEL AND CHANGE DATE ASSOCIATED WITH
.*  THE COPY OF SGIEA2D3 BEING USED IN ANY GIVEN SYSGEN.
.*  THE COMMENT SHOULD BE UPDATED WITH A NEW LEVEL NUMBER AND DATE
.*  WHENEVER ANY CHANGES ARE MADE TO THIS MACRO
.*
*** SGIEA2D3 *** DIDOCS *** 05/09/79 *** CHANGE LEVEL 3.7***   @ZA36190
.*
&SGCTRLC(6) SETC 'IEEPFKEY'
         COPY  SGASMPAK
         PUNCH 'IEEPFKEY CSECT'
         PUNCH '         DC    A(KEY)'
&X       SETA  (((&TOTPFK*110+99)+3000)/3000)  X=TOTAL # PFKS
         PUNCH '         DC    &X.XL10''0''' LLMBBCCHHR
         PUNCH 'KEY   DC   AL2(ENDPFK-IEEPFKEY)  '
         PUNCH '    DC   H''0''   '
&I       SETA  1
.CONLOOP ANOP
         PUNCH '         ENTRY IEECVK&NDX(&I)'
         PUNCH 'IEECVK&NDX(&I)  EQU   *'
&J       SETA  &PFKORG(&I)
.KEYLOOP ANOP
         PUNCH '         DC    FL1''&J'''
         PUNCH '         DC    X''0'''
         PUNCH '         DC    CL108'' '''
&J       SETA  &J+1
         AIF   (&J LT &PFKLIM(&I)).KEYLOOP
         PUNCH '         DC    FL1''100'''
&I       SETA  &I+1
         AIF   (&I LE &P).CONLOOP
         PUNCH 'ENDPFK   EQU   *'
         PUNCH '         END'
         PUNCH '/*'
.OUT     MEND
