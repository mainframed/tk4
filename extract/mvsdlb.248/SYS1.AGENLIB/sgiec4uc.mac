         MACRO
         SGIEC4UC
         COPY  SGGBLPAK
         LCLA  &X,&Y,&LIMIT                                      Y02072
         LCLB  &FLG                                              Y02072
         LCLC  &ID(17),&ACC(17),&ALIAS(17),&REP(17),&C1,&C2,&C3  Y02072
.*       CHANGE ACTIVITY
.*
.*       VS2 RELEASE 2
.*D80000                                                         YM1445
.*       CHANGE ACTIVITY
.*
.*       VS2 RELEASE 3 ADDITIONS
.*A069700             04/13/78                                 @ZA31642
.*       3203-4 PRINTER SUPPORT         7/18/78                @32034JK
.*
         AIF   (NOT (&SGDCLSB(55) OR &SGDCLSB(71) OR                   *
               &SGGENDB(101))).MEND                            @32034JK
.*       IF NEITHER 1403 UCS OR 3211 OR 3203-4 IS SPECIFIED    @32034JK
.*       THERE ARE NO UCS IMAGES NEEDED.                       @32034JK
.*
.*       GLOBAL SGUCSDC WILL BE SET UP ON ENTRY TO CONTAIN THE IDS
.*       OF THE UCS IMAGES THAT ARE TO HAVE THE DEFAULT FLAG SET.
.*       THIS GLOBAL IS SET UP BY THE UCSDFLT PARAMETER ON THE
.*       DATAMGT MACRO.  EACH OF THE TRAIN IMAGES ARE IN A SEPERATE
.*       MODULE IN AOSD0.  EACH MODULES CONTAINS TWO COPIES OF THE
.*       IMAGE AS SEPERATE CSECTS.  THE CSECT NAMES ARE THE SAME
.*       EXCEPT THE FOURTH CHARACTER OF THE NAME IS D FOR THOSE
.*       WITH THE DEFAULT FLAG ON AND N FOR THOSE WITH IT OFF.
.*       THERE ARE THREE CHARACTER TYPE ARRAYS IN THIS MACRO -
.*       ID (WHICH CONTAINS THE VALID IDS). ACC (WHICH CONTAINS EITHER
.*       AN N OR D DEPENDING ON WHETHER THE FLAG IS SUPPOSE TO BE
.*       OFF OR ON FOR THE MODULE IN IMAGELIB). REP (WHICH IS
.*       EITHER AN N OR D TO SAY WHICH CSECT IS TO BE DELETED).
.*       FOR EXAMPLE THE FIRST IMAGE IS AN AND IF IT IS SUPPOSE TO
.*       HAVE ITS DEFAULT FLAG ON THEN- ID(1) WILL BE AN, ACC(1)
.*       WILL BE D, AND REP(1) WILL BE N.
.*       ALIAS CONTAINS ANY ALIASES FOR THE IMAGES REQUIRED BY HASP
.*
.*       FIRST THE VALID ENTRIES WILL BE SET INTO ID.
.*       THE FIRST 12 IMAGES ARE FOR 1403/3203-4 UCS.          @32034JK
.*       ALIASES ARE NOT APPLICABLE TO 3203-4 PRINTER.         @32034JK
&ID(1)   SETC  'AN'                                              Y02072
&ALIAS(1) SETC 'A11'                                             Y02072
&ID(2)   SETC  'HN'                                              Y02072
&ALIAS(2) SETC 'H11'                                             Y02072
&ID(3)   SETC  'PCAN'                                            Y02072
&ID(4)   SETC  'PCHN'                                            Y02072
&ID(5)   SETC  'PN'                                              Y02072
&ALIAS(5) SETC 'P11'                                             Y02072
&ID(6)   SETC  'QN'                                              Y02072
&ID(7)   SETC  'RN'                                              Y02072
&ID(8)   SETC  'SN'                                              Y02072
&ID(9)   SETC  'TN'                                              Y02072
&ALIAS(9) SETC 'T11'                                             Y02072
&ID(10)  SETC  'XN'                                              Y02072
&ID(11)  SETC  'YN'                                              Y02072
&ID(12)  SETC  'QNC'                                             Y02072
.*       THE NEXT 5 IMAGES ARE FOR 3211
&ID(13)  SETC  'A11'                                             Y02072
&ALIAS(13) SETC 'AN'                                             Y02072
&ID(14)  SETC  'H11'                                             Y02072
&ALIAS(14) SETC 'HN'                                             Y02072
&ID(15)  SETC  'G11'                                             Y02072
&ID(16)  SETC  'P11'                                             Y02072
&ALIAS(16) SETC 'PNRNQN'                THREE ALIASES FOR P11    Y02072
&ID(17)  SETC  'T11'                                             Y02072
&ALIAS(17) SETC 'TN'                                             Y02072
&X       SETA  1                       INITIALIZE LOOP CONTROL   Y02072
.*       FIRST THE ACC AND REP PARAMETERS WILL BE INITIALIZED.  IF
.*       ALL HAS BEEN SPECIFIED ALL THE ACC FIELDS WILL BE SET TO D
.*       OTHERWISE THEY WILL ALL BE SET TO N.  THE REP WILL BE SET TO
.*       THE OPPOSITE OF THE ACC.
         AIF   ('&SGUCSDC(1)' EQ 'ALL').ALLD  ALL IMAGES ARE DEF Y02072
&C1      SETC  'N'                     SET SO NONE ARE           Y02072
&C2      SETC  'D'                      DEFAULT                  Y02072
         AGO   .SETALL                 GO SET UP ACC AND REP FLDSY02072
.ALLD    ANOP  ,                                                 Y02072
&C1      SETC  'D'                     SET UP SO ALL ARE         Y02072
&C2      SETC  'N'                      DEFAULT                  Y02072
.SETALL  ANOP  ,                                                 Y02072
.*       NOW ALL THE ACC AND REP FIELDS WILL BE SET.  THE ACC ARE
.*       SET TO C1 AND THE REPS ARE SET TO C2
&ACC(&X) SETC  '&C1'                                             Y02072
&REP(&X) SETC  '&C2'                                             Y02072
&X       SETA  &X+1                    INCREMENT CTR             Y02072
         AIF   (&X NE 18).SETALL       SET UP ALL FIELDS         Y02072
.*       NOW ALL THE ID ACC AND REP FIELDS ARE INITIALIZED       Y02072
         AIF   ('&C1' EQ 'D').LINKED    IF ALL WAS SPECIFIED     Y02072
.*                                      THEN SKIP THE NEXT STEP  Y02072
.*
.*       ALL WAS NOT SPECIFIED SO THE LIST PASSED IN THE SGUCSDC
.*       GLOBAL WILL NOW BE SET TO AN ACC OF D AND A REP OF N
&X       SETA  1                       INDEX FO SCAN ID ARRAY    Y02072
&Y       SETA  1                       INDEX TO SCAN SGUCSDC ARRAY02072
.LOOPID  ANOP  ,                                                 Y02072
         AIF   ('&SGUCSDC(&Y)' EQ '&ID(&X)').FND  SET THIS IMAGE Y02072
&X       SETA  &X+1                    INCREMENT ID INDEX        Y02072
         AIF   (&X NE 18).LOOPID       CHECK NEXT ID             Y02072
.*       DROPPING THROUGH THE ABOVE LOOP MEANS AN INVALID IMAGE  Y02072
.*       WAS PASSED.
   MNOTE 0,'* * * IEIUCS105 DEFAULT ID &SGUCSDC(&Y) IS INVALID'  Y02072
.SETUP   ANOP  ,                                                 Y02072
&X       SETA  1                       RESET ID INDEX            Y02072
&Y       SETA  &Y+1                    INCREMENT SGUCSDC INDEX   Y02072
         AIF   (&Y EQ 18).LINKED       NO MORE INPUT TO CHECK  @ZA31642
         AIF   ('&SGUCSDC(&Y)' NE '').LOOPID MORE INPUT TO CHECK Y02072
         AIF   (&FLG).LINKED           AT LEAST ONE VALID INPUT  Y02072
   MNOTE 0,'* * * IEIUCS105 NO VALID DEFAULT ID''S SPECIFIED'    Y02072
   MNOTE 0,'* * * ALL WILL BE ASSUMED'                           Y02072
         AGO   .LINKED                 GO TO LINK-EDIT           Y02072
.FND     ANOP  ,                                                 Y02072
&ACC(&X) SETC  'D'                     ACCEPT DEFAULT            Y02072
&REP(&X) SETC  'N'                     DELETE NON DEFAULT        Y02072
&FLG     SETB  1                       TURN ON FLAG              Y02072
         AGO   .SETUP                  GO CHECK NEXT INPUT       Y02072
.*       THIS SECTION WILL GENERATE THE LINKAGE EDITOR COMMANDS
.*       TO MOVE THE IMAGE FROM DLIB 'AOSD0' TO 'IMAGELIB'.    @32034JK
.*       IT WILL ALSO CAUSE ONE OF THE CSECTS TO BE DELETED.   @32034JK
.*       THE IMAGE WILL BE RENAMED FROM IGGXXXXX TO UCSXXXXX   @32034JK
.*       AND THE CSECT WILL BE RENAMED FROM IGGDXXXX OR IGGN-  @32034JK
.*       XXXX TO UCS1XXXX OR UCS2XXXX OR UCS3XXXX DEPENDING    @32034JK
.*       ON THE CHARACTER IN ACC AND REP.                      @32034JK
.*       UCS1XXXX         UCS IMAGE FOR 1403                   @32034JK
.*       UCS2XXXX         UCS IMAGE FOR 3211                   @32034JK
.*       UCS3XXXX         UCS IMAGE FOR 3203-4                 @32034JK
.LINKED  ANOP  ,                                                 Y02072
&SGCTRLC(7) SETC '&SGCTRLC(62)'        VOLUME SERIAL OF IMAGELIB Y02072
&SGCTRLC(8) SETC '&SGCTRLC(61)'        UNIT NO. OF IMAGELIB      Y02072
&SGCTRLC(9) SETC 'IMAGELIB'            OUTPUT LIBRARY            Y02072
&SGCTRLC(10) SETC ',OL'                ONLY LOADABLE             YM1445
&SGCTRLC(11) SETC ',RENT'              REENTRANT                 YM1445
         COPY  SGLEDPK1                                          Y02072
  PUNCH '//AOSD0 DD DISP=SHR,VOLUME=(,RETAIN),DSNAME=SYS1.AOSD0' Y02072
         COPY  SGLEDPK2                                          Y02072
.D1403   ANOP  ,                       1403-U SECTION          @32034AP
.*    IF WE DO NOT HAVE ANY 1403-U DEVICES, CHECK FOR 3203     @32034AP
         AIF   (NOT &SGDCLSB(55)).D3203                        @32034AP
&X       SETA  1                       INIT. INDEX               Y02072
.LOP1403 ANOP  ,                                               @32034AP
         AIF   (K'&ALIAS(&X) EQ 0).NOAL14  NO ALIAS FOR THIS IMG Y02072
         PUNCH '  ALIAS   UCS1&ALIAS(&X)   '  ALIAS NEEDED       Y02072
.NOAL14  ANOP  ,                                                 Y02072
         PUNCH '  REPLACE IGG&REP(&X)&ID(&X)  ' DELETE CSECT     Y02072
         PUNCH '  CHANGE  IGG&ACC(&X)&ID(&X)(UCS1&ID(&X))  '     Y02072
.*                                   RENAME  ACC CSECT TO  UCS1  Y02072
         PUNCH '  INCLUDE  AOSD0(IGG1&ID(&X))  ' MODULE NAME     Y02072
         PUNCH '  NAME   UCS1&ID(&X).(R)  '  NAME MODULE         Y02072
&X       SETA  &X+1                          INCREMENT INDEX   @32034AP
         AIF   (&X NE 13).LOP1403     ALL 1403 UCS DONE        @32034AP
.D3203   ANOP  ,                       3203-4 SECTION          @32034JK
.*    IF WE DO NOT HAVE ANY 3203-4 DEVICES, CHECK FOR 3211     @32034AP
         AIF   (NOT &SGGENDB(101)).D3211   NO 3203-4           @32034AP
&X       SETA  1                       INIT. INDEX             @32034AP
.LOP3203 ANOP  ,                                               @32034AP
         PUNCH '  REPLACE IGG&REP(&X)&ID(&X)  ' DELETE CSECT   @32034JK
         PUNCH '  CHANGE  IGG&ACC(&X)&ID(&X)(UCS3&ID(&X))  '   @32034JK
.*                                   RENAME  ACC CSECT TO UCS3 @32034JK
         PUNCH '  INCLUDE  AOSD0(IGG3&ID(&X))  ' GET MODULE    @32034JK
         PUNCH '  NAME   UCS3&ID(&X).(R)  '  NAME MODULE       @32034JK
&X       SETA  &X+1                    INCREMENT INDEX           Y02072
         AIF   (&X NE 13).LOP3203      ALL 3203-4 UCS DONE     @32034JK
.D3211   ANOP  ,                       3211 SECTION              Y02072
.*    IF WE DO NOT HAVE ANY 3211 DEVICES EXIT MACRO            @32034AP
         AIF   (NOT &SGDCLSB(71)).END  NO 3211                 @32034AP
&X       SETA  13                      INIT INDEX FOR 3211       Y02072
.LOP3211 ANOP  ,                                                 Y02072
         AIF   (K'&ALIAS(&X) EQ 0).NOAL32  NO ALIAS FOR THIS IMG Y02072
&C3      SETC  '&ALIAS(&X)'(1,2)        SET ID FIELD             Y02072
         PUNCH '  ALIAS   UCS2&C3  '    ALIAS NEEDED             Y02072
         AIF   (K'&ALIAS(&X) EQ 2).NOAL32 ALL ALIASES ASIGNED    Y02072
&ALIAS(&X) SETC '&ALIAS(&X)'(3,K'&ALIAS(&X)) DELETE FIRST ALIAS  Y02072
         AGO   .LOP3211                 CHECK FOR MORE ALIASES   Y02072
.NOAL32  ANOP  ,                                                 Y02072
         PUNCH '  REPLACE IGG&REP(&X)&ID(&X)  ' DELETE CSECT     Y02072
         PUNCH '  CHANGE  IGG&ACC(&X)&ID(&X)(UCS2&ID(&X))  '     Y02072
.*                                   RENAME  ACC CSECT TO  UCS2  Y02072
         PUNCH '  INCLUDE  AOSD0(IGG2&ID(&X))  ' MODULE NAME     Y02072
         PUNCH '  NAME   UCS2&ID(&X).(R)  '  NAME MODULE         Y02072
&X       SETA  &X+1                    INCREMENT INDEX           Y02072
         AIF   (&X NE 18).LOP3211      DO ALL 3211               Y02072
.END     ANOP  ,                                                 Y02072
         PUNCH '/*'                    EOD MARKER                Y02072
.MEND    MEND
