          MACRO
          SGSETGPT
          COPY    SGGBLPAK
          LCLA      &I,&I1,&I2
          LCLA      &PREF1,&PREF2
          LCLA      &P
          LCLA      &SWAPPER,&SORTER(200)
.**** START OF SPECIFICATIONS *****************************************
.*                                                                    *
.* MODULE NAME - SGSETGPT                                             *
.*                                                                    *
.* DESCRIPTIVE NAME - GENERIC PREFERENCE TABLE SUPPORT                *
.*                                                                    *
.* COPYRIGHT - NONE                                                   *
.*                                                                    *
.* STATUS - CHANGE LEVEL 00                                   @G64HSPD*
.*                                                                    *
.* FUNCTION -                                                         *
.*   THIS MACRO SETS UP THE GENERIC PREFERENCE TABLE (GPT), WHICH     *
.*   REPRESENTS THE DEFAULT ORDER IN WHICH GENERICS ARE TO BE         *
.*   CONSIDERED FOR DEVICE ALLOCATION. THE VALUE PLACED IN  AN  ENTRY *
.*   OF THE TABLE IS THE INDEX INTO &SGGNRIC OF THE GENERIC NAME      *
.*   WHICH OCCUPIES THE CORRESPONDING POSITION IN THE GPT. A          *
.*   VALUE ASSIGNED TO A GENERIC (&SGPREFA(1-200)) IS USED TO SORT    *
.*   THE DIT INDEXES INTO GENERIC PREFERENCE ORDER.                   *
.*   THIS MACRO IS CALLED BY GENERATE  FOR ALL AND IO SYSGENS.        *
.*                                                                    *
.* NOTES - N/A                                                        *
.*                                                                    *
.* MODULE TYPE - MACRO                                                *
.*                                                                    *
.* ENTRY POINT - N/A                                                  *
.*                                                                    *
.* INPUT - NONE                                                       *
.*                                                                    *
.* OUTPUT -                                                           *
.*        THE FOLLOWING GLOBALS ARE SET:                              *
.*            &SGGPTXA(1-200) - INDEX INTO &SGGNRIC (IN DEVICE        *
.*                              INFORMATION TABLE) OF GENERIC         *
.*                              OCCUPYING THIS POSITION IN            *
.*                              THE PREFERENCE TABLE.                 *
.*                                                                    *
.*            &SGPREFA(1-200) - PREFERENCE TABLE ORDER NUMBER         *
.*                                                                    *
.*        LOCALS USED ARE -                                           *
.*             &I - CURRENT ENTRY IN THE GENERIC PREFERENCE TABLE     *
.*                                                                    *
.*             &I1,&I2,&P - VARIABLES USED TO INDEX THE SORTING       *
.*                  ARRAY (&SORTER)                                   *
.*                                                                    *
.*             &PREF1,&PREF2 - UTILITY VARIABLES USED TO HOLD AND     *
.*                  COMPARE PREFERENCE ORDER NUMBERS                  *
.*                                                                    *
.*             &SORTER -  ARRAY USED FOR SORTING                      *
.*                                                                    *
.*             &SWAPPER -  VARIABLE USED TO SWAP ADJCENT ENTRIES      *
.*                                                                    *
.*  EXIT, NORMAL - N/A                                                *
.*                                                                    *
.*  EXIT, ERROR  - N/A                                                *
.*                                                                    *
.*  EXTERNAL REFERENCES - N/A                                         *
.*                                                                    *
.*  TABLES - GENERIC PREFERENCE TABLE (GPT)                           *
.*                                                                    *
.*  MACROS - N/A                                                      *
.*                                                                    *
.*  CHANGE ACTIVITY - N/A                                             *
.* A                                      CTC GENERIC        @ZA58289 *
.******* END OF SPECIFICATIONS ****************************************
.*
.*              SET PREFERENCE ORDER NUMBER FOR PRE SU64 DEVICES
.*
.*           DIT  1         2305-1
&SGPREFA(1)  SETA 100       SET PREFERENCE ORDER NUMBER
.*           DIT  2         2305-2
&SGPREFA(2)  SETA 200
.*           DIT  3         3350
&SGPREFA(3)  SETA 300
.*           DIT  4         3330-1
&SGPREFA(4)  SETA 400
.*           DIT  5         3330
&SGPREFA(5)  SETA 500
.*           DIT  6         3340
&SGPREFA(6)  SETA 600
.*           DIT  7         2314
&SGPREFA(7)  SETA 700
.*           DIT  8         2400
&SGPREFA(8)  SETA 800
.*           DIT  9         2400-3
&SGPREFA(9)  SETA 900
.*           DIT  10        2400-4
&SGPREFA(10) SETA 1000
.*           DIT  11        3400-3
&SGPREFA(11) SETA 1100
.*           DIT  12        3400-4
&SGPREFA(12) SETA 1200
.*           DIT  13        2400-1
&SGPREFA(13) SETA 1300
.*           DIT  14        2400-2
&SGPREFA(14) SETA 1400
.*           DIT  15        3400-2
&SGPREFA(15) SETA 1500
.*           DIT  16        3400-5
&SGPREFA(16) SETA 1600
.*           DIT  17        3400-6
&SGPREFA(17) SETA 1700
.*           DIT  18        3800
&SGPREFA(18) SETA 1800
.*           DIT  19        3211
&SGPREFA(19) SETA 1900
.*           DIT  101       3203-4
&SGPREFA(101) SETA 2000
.*           DIT  20        1403
&SGPREFA(20) SETA 2100
.*           DIT  21        1443
&SGPREFA(21) SETA 2200
.*           DIT  22        2501
&SGPREFA(22) SETA 2300
.*           DIT  23        3505
&SGPREFA(23) SETA 2400
.*           DIT  24        3525
&SGPREFA(24) SETA 2500
.*           DIT  25        2671
&SGPREFA(25) SETA 2600
.*           DIT  26        2520
&SGPREFA(26) SETA 2700
.*           DIT  27        2540
&SGPREFA(27) SETA 2800
.*           DIT  28        2540-2
&SGPREFA(28) SETA 2900
.*           DIT  29        1053
&SGPREFA(29) SETA 3000
.*           DIT  30        3210
&SGPREFA(30) SETA 3100
.*           DIT  31        3215
&SGPREFA(31) SETA 3200
.*           DIT  32        3213
&SGPREFA(32) SETA 3300
.*           DIT  33        2250-1
&SGPREFA(33) SETA 3400
.*           DIT  34        2250-3
&SGPREFA(34) SETA 3500
.*           DIT  35        1052
&SGPREFA(35) SETA 3600
.*           DIT  36        3277-1
&SGPREFA(36) SETA 3700
.*           DIT  37        3277-2
&SGPREFA(37) SETA 3800
.*           DIT  38        3158
&SGPREFA(38) SETA 3900
.*           DIT  77        3036
&SGPREFA(77) SETA 4000
.*           DIT  39        3284-1
&SGPREFA(39) SETA 4100
.*           DIT  40        3284-2
&SGPREFA(40) SETA 4200
.*           DIT  41        3286-1
&SGPREFA(41) SETA 4300
.*           DIT  42        3286-2
&SGPREFA(42) SETA 4400
.*           DIT  43        3066
&SGPREFA(43) SETA 4500
.*           DIT  44        2260-1
&SGPREFA(44) SETA 4600
.*           DIT  45        2260-2
&SGPREFA(45) SETA 4700
.*           DIT  46        3890
&SGPREFA(46) SETA 4800
.*           DIT  47        3886
&SGPREFA(47) SETA 4900
.*           DIT  48        1287
&SGPREFA(48) SETA 5000
.*           DIT  49        1288
&SGPREFA(49) SETA 5100
.*           DIT  78        7443
&SGPREFA(78) SETA 5200
.*           DIT  50        1419
&SGPREFA(50) SETA 5300
.*           DIT  51        3895
&SGPREFA(51) SETA 5400
.*           DIT  52        2495
&SGPREFA(52) SETA 5500
.*           DIT  53        3540
&SGPREFA(53) SETA 5600
.*           DIT  54        3851
&SGPREFA(54) SETA 5700
.*           SETA 55        3330V
&SGPREFA(55) SETA 5800
.*           DIT  56        3838
&SGPREFA(56) SETA 5900
.*           DIT  57        1275
&SGPREFA(57) SETA 6000
.*           DIT  58        AAA1 (1030)
&SGPREFA(58) SETA 6100
.*           DIT  59        AAA2 (1050)
&SGPREFA(59) SETA 6200
.*           DIT  60        AAA3 (1060)
&SGPREFA(60) SETA 6300
.*           DIT  61        AAA4 (2260)
&SGPREFA(61) SETA 6400
.*           DIT  62        AAA5 (115A)
&SGPREFA(62) SETA 6500
.*           DIT  63        AAA6 (83B3)
&SGPREFA(63) SETA 6600
.*           DIT  64        AAA7 (TWX)
&SGPREFA(64) SETA 6700
.*           DIT  65        AAA8 (2740)
&SGPREFA(65) SETA 6800
.*           DIT  66        AAA9 (BSC1)
&SGPREFA(66) SETA 6900
.*           DITA 67        AAAA (BSC2)
&SGPREFA(67) SETA 7000
.*           DIT  68        AAAB (BSC3)
&SGPREFA(68) SETA 7100
.*           DIT  69        AAAC (7770)
&SGPREFA(69) SETA 7200
.*           DIT  70        AAAD (WTTA)
&SGPREFA(70) SETA 7300
.*           DIT  71        AAAE (2741P)
&SGPREFA(71) SETA 7400
.*           DIT  72        AAAF (2741C)
&SGPREFA(72) SETA 7500
.*           DIT  73        3705
&SGPREFA(73) SETA 7600
.*           DIT  74        3791L
&SGPREFA(74) SETA 7700
.*           DIT  75        3704
&SGPREFA(75) SETA 7800
.*           DIT  84        CTC                                @ZA58289
&SGPREFA(84) SETA 8400                                         @ZA58289
.*
.* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * **
.*                                                                    *
.* PREPARE TO SORT THE GENERIC PREFERENCE TABLE. THE GENERIC INDEXES  *
.* ARE SORTED ACCORDING TO THEIR GENERIC PREFERENCE ORDER NUMBER.     *
.*                                                                    *
.* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * **
.*
.GET1        ANOP
             AIF   (&I GE 200).MEND
&I           SETA  &I+1
             AIF   (NOT (&SGGENDB(&I))).GET1   IS DEVICE GENED ?
.* MAKE FIRST ENTRY IN THE SORT ARRAY
&P           SETA  1           SET FIRST SORT ENTRY
&SGGPTXA(&P) SETA  &I          SET DIT INDEX IN PREF TABLE
&SORTER(&P)  SETA  &SGPREFA(&I)      SET PREF NUMBER IN SORT TABLE
.START       ANOP
             AIF   (&I GE 200).ALLSEQN
.*  GET NEXT DIT INDEX AND PLACE IN SORT TABLE
&I           SETA  &I+1       INCR DIT INDEX
             AIF   (NOT (&SGGENDB(&I))).START  IS DEVICE GENED ?
&PREF1       SETA  &SORTER(&P)      SET FIRST SORT TEST FIELD
&P           SETA  &P+1                    YES-INCR SORT ARRAY INDEX
&SORTER(&P)  SETA  &SGPREFA(&I)      SET NEXT PREF NUMBER IN SORT TABLE
&PREF2       SETA  &SGPREFA(&I)      SET SECOND SORT TESTS FIELD
&SGGPTXA(&P) SETA  &I                SET DIT INDEX IN PREF TABLE
.*
.*   PERFORM SORT BY COMPARING ADJACENT ENTRIES AND SWAPPING LOW
.*   NUMBERS TO THE BOTTOM OF THE LIST.
.*
.*                  INIT SORT OPERATION
&I2          SETA  &P                   START AT THE TOP
&I1          SETA  &P-1                 SELECT NEXT LOWER ENTRY
.*  MAIN SORT SEQUENCE COMPARE
.UPSWAP      ANOP
             AIF   (&PREF1 GT &PREF2).SWAP1
.TSTBOTM     ANOP
             AIF   (&I1 EQ 1).START     BOTTOM OF TABLE REACHED ?
.*                                      NO. CHECK NEXT LOWER ENTRY
&I2          SETA  &I1                  GET NEXT LOWER SORT TABLE INDEX
&I1          SETA  &I1-1                GET NEXT NEXT LOWER SORT INDEX
.* GET NEXT PREF NUMBER FROM SORT TABLE
&PREF1       SETA  &SORTER(&I1)         GET NEXT LOWER PREF NUMBER
&PREF2       SETA  &SORTER(&I2)         GET NEXT NEXT LOWER NUMBER
             AGO   .UPSWAP
.*
.SWAP1       ANOP
&SWAPPER     SETA  &SORTER(&I1)
&SORTER(&I1) SETA  &SORTER(&I2)
&SORTER(&I2) SETA  &SWAPPER
.*  SWAP  DIT INDEXES
&SWAPPER     SETA  &SGGPTXA(&I1)
&SGGPTXA(&I1) SETA &SGGPTXA(&I2)
&SGGPTXA(&I2) SETA &SWAPPER
             AGO  .TSTBOTM             GO TEST FOR BOTTOM OF TABLE
.ALLSEQN     ANOP
 MNOTE *,'              DEFAULT GENERIC PREFERENCE ORDER        '
&P           SETA  0
.NXENTRY     ANOP
&P           SETA &P+1
             AIF  ((&SGGPTXA(&P) EQ 0) OR (&P GE 200)).MEND
 MNOTE *,' ENTRY &P  &SGGNRIC(&SGGPTXA(&P))    DIT INDEX &SGGPTXA(&P)'
             AGO    .NXENTRY
.MEND        MEND
