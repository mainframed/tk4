         MACRO
&NAME    CHANNEL &ADDRESS=,&TYPE=
         COPY  SGGBLPAK
         LCLA  &A,&B
         LCLB  &E
         LCLC  &ID
.***** START OF SPECIFICATIONS **************************************
.*
.* MODULE NAME - CHANNEL
.*
.* DESCRIPTIVE NAME - CHANNEL
.*
.* COPYRIGHT - NONE
.*
.* STATUS - CHANGE LEVEL 02
.*
.* FUNCTION -
.*       THE CHANNEL MACRO IS USED TO DESCRIBE THE CHANNELS THAT ARE
.*       GOING TO BE IN THE SYSTEM.
.*
.* NOTES - N/A
.*
.* MODULE TYPE - MACRO
.*
.* ENTRY POINT - N/A
.*
.* INPUT - THE FOLLOWING PARAMETERS ARE ALLOWED ON CHANNEL
.*             &ADDRESS - SPECIFIES AN ADDRESS OR GROUP OF ADDRESSES
.*                        TO BE ASSOCIATED WITH THIS &TYPE CHANNEL
.*             &TYPE    - SPECIFIES THE TYPE OF CHANNEL TO BE
.*                        ASSOCIATED WITH THE ADDRESSES SPECIFIED.
.*
.* OUTPUT -
.*       GLOBALS USED ARE -
.*             &SGMENTB(2)  - CHANNEL MACRO SPECIFIED
.*             &SGMENTB(68) - BLKMPXR IN SYSTEM
.*             &SGCNTRA(1)  - COUNT OF ADDRESSES SPECIFIED
.*             &SGCHANA     - CHANNEL MACRO COUNT
.*             &SGCHANB     - THIS CHANNEL IN SYSTEM
.*             &HEX         - ADDRESS SPECIFIED IN HEXADECIMAL
.*             &SGQUITB     - QUIT SWITCH
.*
.*       LOCALS USED ARE -
.*             &A      - USED AS COUNT OF ADDRESSES SPECIFIED
.*             &B      - SET TO HEX VALUE OF THE ADDRESS SPECIFIED *15
.*                       AND USED AS SUBSCRIPT TO SGSCHANB GLOBAL
.*             &E      - SET IF ERROR IN TYPE PARAMETER
.*             &ID     - USED AS CODING ERROR IDENTIFIER
.*
.* EXIT, NORMAL - N/A
.*
.* EXIT, ERROR - N/A
.*
.* EXTERNAL REFERENCES - N/A
.*
.* TABLES - N/A
.*
.* MACROS - NONE
.*
.* CHANGE ACTIVITY -
.*
.****** END OF SPECIFICATIONS ***************************************
&SGMENTB(2)    SETB 1                        CHANNEL ENTRY SWITCH
&SGCHANA SETA  &SGCHANA+1                    CHANNEL MACRO COUNT
         AIF   ('&NAME' EQ '').NONE          BR IF NAME NOT SPECIFIED
&ID      SETC  '&NAME'
         AGO   .NULL
.NONE    ANOP
&ID      SETC  'CHAN.#&SGCHANA'              BUILD CHANNEL ID
.NULL    AIF   (T'&ADDRESS EQ 'O').E2        MUST BE SPECIFIED
.TEST    AIF   (N'&ADDRESS EQ &A).MEND       BR AT END OF SCAN
         AIF   (&SGCNTRA(1) GE 16).E1        EXCEED 16 CHANNELS
&A       SETA  &A+1                          &A-INDEX TO ADDRESS
&SGCNTRA(1)    SETA &SGCNTRA(1)+1            CHANNEL ADDRESS COUNT
         AIF   (K'&ADDRESS(&A) NE 1).E3      ERROR ON NUMBER OF DIGITS
         AIF   (NOT(('&ADDRESS(&A)' GE '0' AND '&ADDRESS(&A)' LE '9')ORX
               ('ADDRESS(&A)' GE 'A' AND '&ADDRESS(&A)' LE 'F'))).E3
         CONVERT TO=HEX,DIGITS=1,VALUE=&ADDRESS(&A)
&B       SETA  &HEX*15                       POINTER TO CHANNEL BITLIST
         AIF   (&SGCHANB(&B+1)).E4           CHANNEL PREVIOUSLY SPEC'D
    MNOTE *,'     CHANNEL &ADDRESS(&A) SPECIFIED'
&SGCHANB(&B+1) SETB 1                        CHANNEL PRESENT BIT
.TYPE    AIF   (&E).TEST                     PREVIOUS TYPE ERROR
         AIF   (T'&TYPE EQ 'O').E5           MUST BE SPECIFIED
         AIF   (N'&TYPE NE 1).E6             MUST BE ONLY ONE
         AIF   ('&TYPE' NE 'MULTIPLEXOR').T1
&SGCHANB(&B+2) SETB 1                        MULTIPLEXOR BIT
         AGO   .M1
.T1      AIF   ('&TYPE' NE 'SELECTOR').T2
&SGCHANB(&B+3) SETB 1                        SELECTOR BIT
         AGO   .M1
.T2      AIF   ('&TYPE' NE 'HISPEEDMPXR').T3
&SGCHANB(&B+4) SETB 1                        HISPEEDMPXR BIT
         AGO   .M1
.T3      AIF   ('&TYPE' NE 'BLKMPXR').E7
&SGMENTB(68)   SETB 1
&SGCHANB(&B+6) SETB 1                        BLKMPXR BIT
.M1 MNOTE *,'        IT IS A &TYPE CHANNEL'
         AGO   .TEST
.E1 MNOTE 5,'* * * IEICHA101 &ID-CHANNEL ADDRESS &ADDRESS(&A+1) EXCEEDSX
                MAXIMUM OF 16 CHANNELS'
&SGQUITB SETB  1
         AGO   .MEND                         CHECK AS MUCH AS POSSIBLE
.E2 MNOTE 5,'* * * IEICHA102 &ID-ADDRESS VALUE NOT SPECIFIED'
&SGQUITB SETB  1
         AGO   .TYPE
.E3 MNOTE 5,'* * * IEICHA103 &ID-ADDRESS VALUE &ADDRESS(&A) INVALID'
&SGQUITB SETB  1
         AGO   .TYPE
.E4 MNOTE 5,'* * * IEICHA104 &ID-CHANNEL &ADDRESS(&A) PREVIOUSLY SPECIFX
               IED'
&SGQUITB SETB  1
         AGO   .TYPE
.E5 MNOTE 5,'* * * IEICHA105 &ID-TYPE VALUE NOT SPECIFIED'
&SGQUITB SETB  1
&E       SETB  1
         AGO   .TEST
.E6 MNOTE 5,'* * * IEICHA106 &ID-TOO MANY TYPE VALUES'
&SGQUITB SETB  1
&E       SETB  1
         AGO   .TEST
.E7 MNOTE 5,'* * * IEICHA107 &ID-TYPE VALUE &TYPE INVALID'
&SGQUITB SETB  1
&E       SETB  1
         AGO   .TEST
.MEND    MEND
