         MACRO
         SGIEA2D2
.*
.*** START OF SPECIFICATIONS ******************************************
.*                                                            @ZA36190*
.* MACRO NAME = SGIEA2D2                                      @ZA36190*
.*                                                            @ZA36190*
.* DESCRIPTIVE NAME = IEECRDCM, IEECVSUB, AND IEEPFKEY BUILD  @ZA36190*
.*                    MACRO                                   @ZA36190*
.*                                                            @ZA36190*
.* COPYRIGHT = NONE                                           @ZA36190*
.*                                                            @ZA36190*
.* STATUS = OS/VS2 037                                        @ZA36190*
.*                                                            @ZA36190*
.* FUNCTION = BUILDS JOBSTEPS FOR STAGE 2 TO BUILD RESIDENT   @ZA36190*
.*            DISPLAY CONTROL MODULES (RDCM'S) FOR DISPLAY    @ZA36190*
.*            CONSOLES, AND NUCLEUS CSECTS IEECVSUB AND       @ZA36190*
.*            IEEPFKEY.                                       @ZA36190*
.*                                                            @ZA36190*
.* OPERATION = THIS MACRO BUILDS A STAGE 2 JOBSTEP FOR EACH   @ZA36190*
.*             DISPLAY CONSOLE TO BUILD ITS RDCM.  THE AREAS  @ZA36190*
.*             DEFINED ON THE 'CONSOLE' MACRO ARE USED HERE.  @ZA36190*
.*                                                            @ZA36190*
.*             A STEP IS BUILT TO ASSEMBLE IEECVSUB,          @ZA36190*
.*             INCLUDING CHANNEL PROGRAMS FOR EACH DEVICE     @ZA36190*
.*             TYPE DEFINED AS A CONSOLE.                     @ZA36190*
.*                                                            @ZA36190*
.*             ANOTHER STAGE 2 JOBSTEP IS BUILT TO ASSEMBLE   @ZA36190*
.*             IEEPFKEY.   THE IEEPFKEY CSECT WILL INCLUDE    @ZA36190*
.*             SPACE FOR EACH PFK DEFINED FOR DISPLAY         @ZA36190*
.*             CONSOLES.                                      @ZA36190*
.*                                                            @ZA36190*
.* MODULE TYPE = MACRO                                        @ZA36190*
.*                                                            @ZA36190*
.*   PROCESSOR = OS/VS2 ASSEMBLER                             @ZA36190*
.*                                                            @ZA36190*
.*   MODULE SIZE = N/A                                        @ZA36190*
.*                                                            @ZA36190*
.*   ATTRIBUTES = N/A                                         @ZA36190*
.*                                                            @ZA36190*
.* ENTRY POINT = N/A                                          @ZA36190*
.*                                                            @ZA36190*
.* INPUT = THE FOLLOWING GLOBAL VARIABLES ARE USED:           @ZA36190*
.*                                                            @ZA36190*
.*       NAME         FUNCTION                                @ZA36190*
.* ----------------   --------                                @ZA36190*
.*                                                            @ZA36190*
.* &SG5450A           NUMBER OF 3066 CONSOLES                 @ZA36190*
.*                                                            @ZA36190*
.* &SGSCHDA(10,11,    NUMBER OF 2250, 2260, 3277-1, 3277-2,   @ZA36190*
.*    17,18,)         CONSOLES RESPECTIVELY                   @ZA36190*
.*                                                            @ZA36190*
.* &SGSCHDA(16)       NUMBER OF PFK'S FOR MASTER CONSOLE      @ZA36190*
.*                                                            @ZA36190*
.* &SGSCHDC(1)        MASTER CONSOLE INPUT/OUTPUT ADDRESSES   @ZA36190*
.*                                                            @ZA36190*
.* &SGSCHAA(1-12)     DISPLAY AREA DEFINITION OF MASTER       @ZA36190*
.*                    CONSOLE                                 @ZA36190*
.*                                                            @ZA36190*
.* &SGSCNYA(1)        NUMBER OF SECONDARY CONSOLES DEFINED    @ZA36190*
.*                                                            @ZA36190*
.* &SGSCNCC(1-98)     SECONDARY CONS INPUT/OUTPUT ADDRESSES   @ZA36190*
.*                                                            @ZA36190*
.* &SGARXXA(1-98)     WHERE XX=01-12, DISPLAY AREA            @ZA36190*
.*                    DEFINITIONS OF SECONDARY CONSOLES       @ZA36190*
.*                                                            @ZA36190*
.* &SGSCNPA(1-98)     NUMBER OF PFK'S FOR SECONDARY CONSOLES  @ZA36190*
.*                                                            @ZA36190*
.* &SGDT05B(1-1917)   IF ON, DEVICE IS GRAPHICS CLASS         @ZA36190*
.*                                                            @ZA36190*
.* &SGDUXXB(1-1917)   WHERE XX=01-13, DENOTES DEVICE TYPE     @ZA36190*
.*                                                            @ZA36190*
.* &SGDMXXB(1-1917)   WHERE XX=MODEL NUMBER OF DEVICE         @ZA36190*
.*                                                            @ZA36190*
.* &HEX               DEVICE ADDRESS RETURNED BY CONVERT      @ZA36190*
.*                                                            @ZA36190*
.* OUTPUT = FOR EACH DISPLAY CONSOLE DEFINED, A STAGE 2       @ZA36190*
.*          JOBSTEP IS BUILT.   &SGCTRLC(6) IS SET EQUAL TO   @ZA36190*
.*          THE RDCM NAME, IN THE FORM 'IEECVXYY', WHERE X IS @ZA36190*
.*          A LETTER THAT INDICATES THE DEVICE TYPE, AND YY   @ZA36190*
.*          IS A ONE- OR TWO-DIGIT NUMBER THAT IS INCREMENTED @ZA36190*
.*          FOR EACH DEVICE TO MAKE THE RDCM NAME UNIQUE.     @ZA36190*
.*          THESE STEPS ARE ASSEMBLIES OF IEECVXYY MODULES,   @ZA36190*
.*          USING THE IEECRDCM MACRO.                         @ZA36190*
.*                                                            @ZA36190*
.*          ANOTHER STAGE 2 JOBSTEP IS BUILT, WITH            @ZA36190*
.*          &SGCTRLC(6) SET EQUAL TO 'IEECVSUB'. THIS IS AN   @ZA36190*
.*          ASSEMBLY OF IEECVSUB.                             @ZA36190*
.*                                                            @ZA36190*
.*          THE LAST STAGE 2 JOBSTEP BUILT BY THIS MACRO IS   @ZA36190*
.*          AN ASSEMBLY OF CSECT 'IEEPFKEY'.  IT CONTAINS     @ZA36190*
.*          SPACE FOR EACH PFK DEFINED FOR DISPLAY CONSOLES.  @ZA36190*
.*          &SGCTRLC(6) IS SET EQUAL TO 'IEEPFKEY'.           @ZA36190*
.*                                                            @ZA36190*
.* EXIT NORMAL = N/A                                          @ZA36190*
.*                                                            @ZA36190*
.* EXIT ERROR = N/A                                           @ZA36190*
.*                                                            @ZA36190*
.* EXTERNAL REFERENCES = N/A                                  @ZA36190*
.*                                                            @ZA36190*
.* TABLES = &PFKLIM - CONTAINS THE NUMBER OF PFK'S OF EACH    @ZA36190*
.*                    CONSOLE                                 @ZA36190*
.*                                                            @ZA36190*
.*          &NDX    - USED TO GENERATE THE LABEL FOR THE      @ZA36190*
.*                    PFK AREA OF EACH DISPLAY CONSOLE        @ZA36190*
.*                                                            @ZA36190*
.*          &PFKORG - CONTAINS A '1'B IF CORRESPONDING        @ZA36190*
.*                    CONSOLE IS A 3277 OR 3270-LIKE DEVICE.  @ZA36190*
.*                    CONTAINS A '0'B OTHERWISE.  INDICATES   @ZA36190*
.*                    THE 'NUMBER' OF THE FIRST PFK FOR THE   @ZA36190*
.*                    DEVICE.                                 @ZA36190*
.*                                                            @ZA36190*
.* VARIABLES = THE FOLLOWING LOCAL VARIABLES ARE USED:        @ZA36190*
.*                                                            @ZA36190*
.*       NAME         FUNCTION                                @ZA36190*
.* ----------------   --------                                @ZA36190*
.*                                                            @ZA36190*
.* &TOTPFK            TOTAL NUMBER OF PFK'S FOR ALL           @ZA36190*
.*                    CONSOLES                                @ZA36190*
.*                                                            @ZA36190*
.* &T2(1-5)           NUMBER OF CONSOLES OF EACH DEVICE TYPE  @ZA36190*
.*                                                            @ZA36190*
.* &I                 INDEX INTO COMPOSITE CONSOLE ADDRESS.   @ZA36190*
.*                    ALSO, INDEX INTO IEEPFKEY NAME TABLE    @ZA36190*
.*                    (&NDX)                                  @ZA36190*
.*                                                            @ZA36190*
.* &J                 PFK NUMBER FOR BUILDING IEEPFKEY        @ZA36190*
.*                                                            @ZA36190*
.* &N                 INDEX INTO TABLE OF SECONDARY CONSOLE   @ZA36190*
.*                    ADDRESSES (&SGSCNCC) AND CORRESPONDING  @ZA36190*
.*                    PFK TABLES                              @ZA36190*
.*                                                            @ZA36190*
.* &P                 NUMBER OF CONSOLES WITH PFK'S DEFINED   @ZA36190*
.*                                                            @ZA36190*
.* &T                 INDEX INTO TABLES INDICATING DEVICE     @ZA36190*
.*                    TYPE                                    @ZA36190*
.*                                                            @ZA36190*
.* &X                 DEVICE ADDRESS IN ARITHMETIC FORM (FOR  @ZA36190*
.*                    USE WITH IOCHECK MACRO)                 @ZA36190*
.*                    ALSO, MULTIPLIER FOR CALCULATING LENGTH @ZA36190*
.*                    OF IEEPFKEY HEADER                      @ZA36190*
.*                                                            @ZA36190*
.* &T1                LETTER INDICATING DEVICE TYPE.  USED    @ZA36190*
.*                    TO REPLACE 'X' IN IEECVXYY              @ZA36190*
.*                                                            @ZA36190*
.* &TYP               LETTER INDICATING DEVICE TYPE AS USED   @ZA36190*
.*                    BY IEECRDCM                             @ZA36190*
.*                                                            @ZA36190*
.* &H(1-2)            ADDRESSES OF INPUT AND OUTPUT HALVES    @ZA36190*
.*                    OF CONSOLES                             @ZA36190*
.*                                                            @ZA36190*
.* &Y(1-5)            SET TO 'Y' IF CORRESPONDING DEVICE      @ZA36190*
.*                    TYPE IS DEFINED, TO 'N' OTHERWISE       @ZA36190*
.*                                                            @ZA36190*
.* &Y(6)              SET TO 'Y' IF PFK'S WERE DEFINED FOR    @ZA36190*
.*                    ONE OR MORE CONSOLES, TO 'N' OTHERWISE  @ZA36190*
.*                                                            @ZA36190*
.* &K1,&K2,&A1,&A2,   USED TO BUILD PARAMETERS FOR GENERATED  @ZA36190*
.* &A3,&A4,&A5        IEECRDCM MACROS                         @ZA36190*
.*                                                            @ZA36190*
.* MACROS = SGGBLPAK, SGASMPAK, CONVERT, IOCHECK              @ZA36190*
.*                                                            @ZA36190*
.* MESSAGES = NONE                                            @ZA36190*
.*                                                            @ZA36190*
.* COMPLETION CODES = N/A                                     @ZA36190*
.*                                                            @ZA36190*
.* CHANGE ACTIVITY = G911P2G,ZA38565                          @ZA36190*
.*                                                            @ZA36190*
.*** END OF SPECIFICATIONS ********************************************
.*  GLOBAL DEFINITION
         COPY SGGBLPAK
.*  LOCAL A DEFINITION
.*  THE FOLLOWING CHANGE TO 99 FOR PFKLIM, NDX, AND PFKORG SUPPORTS
.*  THE CHANGE FROM 32 TO 99 CONSOLES                        @ZA36190
         LCLA &TOTPFK,&PFKLIM(99),&NDX(99),&T2(5),&I,&J,&N,&P,&T,&X
.*  LOCAL B DEFINITION
         LCLB &PFKORG(99)
.*  LOCAL C DEFINITION
         LCLC &T1,&TYP,&H(2),&Y(6),&K1,&K2,&A1,&A2,&A3,&A4,&A5
.*
.*  THIS MACRO WAS CREATED FOR VS/2 RELEASE 2 DIDOCS (SD CODE Y02958)
.*  TO ASSEMBLE A RESIDENT DCM FOR EACH CRT CONSOLE GENERATED,
.*  ASSEMBLE THE IEECVSUB AND ASSEMBLE THE IEEPFKEY TO RESIDE IN
.*  THE SYSTEM NUCLEUS.
.*  THE NAMING CONVENTION FOR THE RESIDENT DCM IS AS FOLLOWS:
.*     A) 2250 IEECVEX
.*     B) 2260 IEECVFX
.*     C) 3066 IEECVDX
.*     D) 3272 MOD 1 IEECVGX
.*     E) 3272 MOD 2 IEECVHX
.*  WHERE X RUNS FROM 1 TO THE TOTAL NUMBER DEVICES OF A
.*  PARTICULAR TYPE.
.*
         AIF   (&SG5450A+&SGSCHDA(10)+&SGSCHDA(11)+&SGSCHDA(17)+&SGSCHDX
               A(18) LT 1).OUT  NO GRAPHIC CONSOLES           MB YM4302
.*
.*  THE FOLLOWING COMMENT CARD WILL BE GENERATED INTO THE STAGE 1
.*  OUTPUT TO IDENTIFY THE LEVEL AND CHANGE DATE ASSOCIATED WITH
.*  THE COPY OF SGIEA2D2 BEING USED IN ANY GIVEN SYSGEN.
.*  THE COMMENT SHOULD BE UPDATED WITH A NEW LEVEL NUMBER AND DATE
.*  WHENEVER ANY CHANGES ARE MADE TO THIS MACRO
.*
*** SGIEA2D2 *** DIDOCS *** 04/24/80 *** CHANGE LEVEL 3.7***   @ZA38565
.*
&Y(1)    SETC  'N'
&Y(2)    SETC  'N'
&Y(3)    SETC  'N'
&Y(4)    SETC  'N'
&Y(5)    SETC  'N'
&Y(6)    SETC  'N'
&I       SETA  2
&H(1)    SETC  '&SGSCHDC(1)'(1,3)
&H(2)    SETC  '&SGSCHDC(1)'(4,3)
         AIF   (&SGSCHAA(12) EQ 0).AREADUN
&A1      SETC  ',A=('.'&SGSCHAA(1)'
         AIF   (&SGSCHAA(2) EQ 0).STOPA
&A2      SETC  ','.'&SGSCHAA(2)'
         AIF   (&SGSCHAA(3) EQ 0).STOPA
&A2      SETC  '&A2'.','.'&SGSCHAA(3)'
         AIF   (&SGSCHAA(4) EQ 0).STOPA
&A3      SETC  ','.'&SGSCHAA(4)'
         AIF   (&SGSCHAA(5) EQ 0).STOPA
&A3      SETC  '&A3'.','.'&SGSCHAA(5)'
         AIF   (&SGSCHAA(6) EQ 0).STOPA
&A4      SETC  ','.'&SGSCHAA(6)'
         AIF   (&SGSCHAA(7) EQ 0).STOPA
&A4      SETC  '&A4'.','.'&SGSCHAA(7)'
         AIF   (&SGSCHAA(8) EQ 0).STOPA
&A4      SETC  '&A4'.','.'&SGSCHAA(8)'
         AIF   (&SGSCHAA(9) EQ 0).STOPA
&A5      SETC  ','.'&SGSCHAA(9)'
         AIF   (&SGSCHAA(10) EQ 0).STOPA
&A5      SETC  '&A5'.','.'&SGSCHAA(10)'
         AIF   (&SGSCHAA(11) EQ 0).STOPA
&A5      SETC  '&A5'.','.'&SGSCHAA(11)'
.STOPA   ANOP
&A5      SETC  '&A5'.')'
.AREADUN AIF   (&SGSCHDA(16) EQ 0).LEAVEP
&P       SETA  1
&PFKLIM(1) SETA &SGSCHDA(16)
&TOTPFK  SETA  &SGSCHDA(16)
         AGO   .ENDMAST
.HALFDUN AIF   ('&H(2)' NE 'FFF' AND &I EQ 1).NEXTI
.JESCON  AIF   (&N GE &SGSCNYA(1)).RDCMDUN                  JE @ZM32389
&N       SETA  &N+1
         AIF   ('&SGSCNCC(&N)' EQ 'JESJES').JESCON          JE @ZM32389
&H(1)    SETC  '&SGSCNCC(&N)'(1,3)
&H(2)    SETC  '&SGSCNCC(&N)'(4,3)
         AIF   ('&H(1)' EQ 'FFF').NEXTI
.STARTI  ANOP
&I       SETA  1
         AIF   ('&H(1)' NE '&H(2)').LEAVEI
.NEXTI   ANOP
&I       SETA  2
.LEAVEI  ANOP
&K1      SETC  ''
&K2      SETC  ''
&A1      SETC  ''
&A2      SETC  ''
&A3      SETC  ''
&A4      SETC  ''
&A5      SETC  ''
         AIF   (&SGAR12A(&N) EQ 0).ARDUN
&A1      SETC  ',A=('.'&SGAR01A(&N)'
         AIF   (&SGAR02A(&N) EQ 0).STOPAR
&A2      SETC  ','.'&SGAR02A(&N)'
         AIF   (&SGAR03A(&N) EQ 0).STOPAR
&A2      SETC  '&A2'.','.'&SGAR03A(&N)'
         AIF   (&SGAR04A(&N) EQ 0).STOPAR
&A3      SETC  ','.'&SGAR04A(&N)'
         AIF   (&SGAR05A(&N) EQ 0).STOPAR
&A3      SETC  '&A3'.','.'&SGAR05A(&N)'
         AIF   (&SGAR06A(&N) EQ 0).STOPAR
&A4      SETC  ','.'&SGAR06A(&N)'
         AIF   (&SGAR07A(&N) EQ 0).STOPAR
&A4      SETC  '&A4'.','.'&SGAR07A(&N)'
         AIF   (&SGAR08A(&N) EQ 0).STOPAR
&A4      SETC  '&A4'.','.'&SGAR08A(&N)'
         AIF   (&SGAR09A(&N) EQ 0).STOPAR
&A5      SETC  ','.'&SGAR09A(&N)'
         AIF   (&SGAR10A(&N) EQ 0).STOPAR
&A5      SETC  '&A5'.','.'&SGAR10A(&N)'
         AIF   (&SGAR11A(&N) EQ 0).STOPAR
&A5      SETC  '&A5'.','.'&SGAR11A(&N)'
.STOPAR  ANOP
&A5      SETC  '&A5'.')'
.ARDUN   AIF   (&SGSCNPA(&N) EQ 0).LEAVEP
&P       SETA  &P+1
&PFKLIM(&P) SETA &SGSCNPA(&N)
&TOTPFK  SETA  &TOTPFK+&SGSCNPA(&N)
.ENDMAST ANOP
&NDX(&P) SETA  &N
&K1      SETC  ',KEY='
&K2      SETC  'IEECVK&N'
.LEAVEP  CONVERT TO=HEX,VALUE=&H(&I),DIGITS=3
&X       SETA  &HEX
         IOCHECK LOOKFOR=&X
&X       SETA  &SGDASPA(&HEX)                                  @ZA15421
         AIF   (NOT &SGDT05B(&X)).HALFDUN
&T       SETA  4
         AIF   (&SGDU10B(&X) OR &SGDU08B(&X) AND &SGDM02B(&X) OR &SGDU1X
               3B(&X)).GR                                      @G51AK02
&T       SETA  5
         AIF   (&SGDU08B(&X) AND &SGDM01B(&X)).GR
&T       SETA  3
         AIF   (&SGDU02B(&X)).GR
&T       SETA  2
         AIF   (&SGDU01B(&X)).GR
&T       SETA  1
         AIF   (NOT &SGDU06B(&X)).HALFDUN
.GR      AIF   (&P EQ 0).NOPFK
         AIF   (&NDX(&P) NE &N).NOPFK
&PFKORG(&P) SETB (&T EQ 4)
&PFKLIM(&P) SETA &PFKLIM(&P)+&PFKORG(&P)
&Y(6)    SETC  'Y'
.NOPFK   ANOP
&Y(&T)   SETC  'Y'
&TYP     SETC  'ABCDE'(&T,1)
&T1      SETC  'DEFHG'(&T,1)
&T2(&T)  SETA  &T2(&T)+1
&SGCTRLC(6) SETC 'IEECV&T1&T2(&T)'
         COPY  SGASMPAK
         PUNCH 'IEECV&T1&T2(&T)  CSECT'
         PUNCH '         IEECRDCM DEV=&TYP&K1&K2&A1&A2&A3&A4&A5'
         PUNCH '         END'
         PUNCH '/*'
         AGO   .HALFDUN
.RDCMDUN ANOP
         AIF   ('&Y(5)' NE 'Y').LEAVE4
&Y(4)    SETC  'Y'
.LEAVE4  ANOP
&SGCTRLC(6) SETC 'IEECVSUB'
         COPY  SGASMPAK
         PUNCH 'IEECVSUB CSECT '
         PUNCH '         IEECSUB &Y(6),&Y(3),&Y(4),&Y(1),&Y(2)'
         PUNCH '         CVT   DSECT=YES'                      @ZA00513
         PUNCH '         IEECUCM  FORMAT=NEW'                  @ZA00513
         PUNCH '         IHAPSA'                               @ZA00513
         PUNCH 'DCMTSRT  DSECT'                                @ZA00513
         PUNCH '         IEECRDCM'                             @ZA00513
         PUNCH '         END'
         PUNCH '/*'
         AIF   (&P EQ 0).OUT
&SGCTRLC(6) SETC 'IEEPFKEY'
         COPY  SGASMPAK
         PUNCH 'IEEPFKEY CSECT'
         PUNCH '         DC    A(KEY)'
&X       SETA  (((&TOTPFK*110+99)+3000)/3000) TOTPFK=TOT #PFK @ZA36190
         PUNCH '         DC    &X.XL10''0''' LLMBBCCHHR
         PUNCH 'KEY      DC    AL4(ENDPFK-IEEPFKEY)  '        @ZA38565
&I       SETA  1
.CONLOOP ANOP
         PUNCH '         ENTRY IEECVK&NDX(&I)'
         PUNCH 'IEECVK&NDX(&I)  EQU   *'
&J       SETA  &PFKORG(&I)
.KEYLOOP ANOP
         PUNCH '         DC    FL1''&J'''
         PUNCH '         DC    X''0'''
         PUNCH '         DC    CL108'' '''
&J       SETA  &J+1
         AIF   (&J LT &PFKLIM(&I)).KEYLOOP
         PUNCH '         DC    FL1''100'''
&I       SETA  &I+1
         AIF   (&I LE &P).CONLOOP
         PUNCH 'ENDPFK   EQU   *'
         PUNCH '         END'
         PUNCH '/*'
.OUT     MEND
