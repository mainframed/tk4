         MACRO
         SGDFTGBL &UCBFL5=,&UCBFLP1=,&CCWPFIX=0,&ATNTABX=0,&ERPFXWA=0, X
               &DSEGLEN=0,&DPNDSEG=,&DPNDEXT=,&DEVTYPE=,&ERRTAB=,      X
               &UNMOVBL=YES
         COPY  SGGBLPAK
         LCLA  &I,&A
         LCLC  &O
.***START OF SPECIFICATIONS********************************************
.* MODULE NAME - SGDFTGBL                                             *
.* DESCRIPTIVE NAME - SET THE SPECIFIED DFT GLOBALS                   *
.*                                                                    *
.* COPYRIGHT - NONE                                                   *
.*                                                                    *
.* STATUS - CHANGE LEVEL 00                                           *
.*                                                                    *
.* FUNCTION -                                                         *
.*    THE FUNCTION OF THIS MACRO SET THE DFT GLOBALS THAT ARE         *
.* SPECIFIED. THIS MACRO PRESENTS A UNIFORM METHOD TO BE USED TO      *
.* TO SET GLOBALS FOR A DFT ENTRY.                                    *
.*                                                                    *
.* NOTES - N/A                                                        *
.*                                                                    *
.* MODULE TYPE - MACRO                                                *
.* ENTRY POINT - N/A                                                  *
.*                                                                    *
.* INPUT _                                                            *
.*    MACRO SPECIFICATION PARAMETERS:                                 *
.*                                                                    *
.*    &UCBFL5  -   UCB UCBFL5 SPECIFICATIONS -THE FOLLOWING FLAGS     *
.*                 ARE DEFINED.                                       *
.*       :                   :                                        *
.*    DISCONCC (0)..DISCONNECT COMMAND CHAIN DEVICE                   *
.*    ...      (1)..DEFINED FOR OTHER USES - MUST BE ZERO             *
.*    LONESEEK (2)..DEVICE REQUIRES STAND ALONE SEEK                  *
.*    VARSDR   (3)..DEVICE HAS VARIABLE LENGTH SDRS                   *
.*    ENVRTNS  (4)..DEVICE RETURNS ENVIRONMENTAL DATA                 *
.*    LASTPATH (5)..LAST PATH/CHANL/CPU TO DEVICE MUST NOT BE         *
.*                  VARIED OFFLINE                                    *
.*    ACTLUNIT (6)..DEVICE HAS ALTERNATE CONTROL UNIT                 *
.*    ALTPATH  (7)..DEVICE HAS ALTERNATE CONTROL PATH(OPTCHAN)        *
.*                                                                    *
.*    &UCBFLP1 -   UCB UCBFLP1 SPECIFICATION                          *
.*    THIS ONE BYTE FIELD CONTAINS THE FOLLOWING FLAGS:               *
.*    UPSHARBL (1)..DEVICE IS SHARABLE WHEN IN UP MODE                *
.*    DDRSWAP  (4)..DEVICE IS ABLE TO BE SWAPPED BY DDR               *
.*    ERRLOG   (5)..ERRLOG PRESENT IN THIS DEVICE                     *
.*                              ***                                   *
.*                                                                    *
.*    &CCWPFIX-    NO. OF CCWS REQUIRED FOR CHANL.PROG. PREFIX        *
.*                                                                    *
.*    &ATNTABX-    ATTENTION TABLE INDEX TO BE PLACED IN UCB          *
.*                                                                    *
.*    &ERPFXWA-    NO. OF BYTES OF ERP/PREFIX WORK AREA(VS1.ONLY)     *
.*                                                                    *
.*    &DSEGLEN-    NO. OF BYTES FOR THE DEVICE DEPENDENT SEGMENT      *
.*                                                                    *
.*    &DPNDSEG-    ENTIRE DEVICE DEPENDENT SEGMENT APPENDED TO UCB    *
.*                                                                    *
.*    &DPNDEXT-    ENTIRE DEVICE DEPENDENT EXTENTION OT UCB           *
.*                                                                    *
.*    &DEVTYPE-    DEVICE TYPE                                        *
.*                                                                    *
.*    &ERRTAB -    ERROR TAB INDEX                                    *
.*                                                                    *
.*    &UNMOVBL-    DEVICE IS PHYSICALLY NON REMOVABLE                 *
.*                                                                    *
.* OTHER INPUT:                                                       *
.*                                                                    *
.*    &SGCNTRA(3) DEVICE NUMBER/POINTER                               *
.*                                                                    *
.* OUTPUT -                                                           *
.*         THE FOLLOWING GLOBALS ARE SET, DEPENDING UPON THE          *
.*         MACRO SPECIFICATIONS:                                      *
.*                                                                    *
.*   &SGUF50B - DISCONNECT COMMAND CHAIN DEVICE                       *
.*                                                                    *
.*   &SGUF51B - UNUSED CURRENTLY SET TO ZERO                          *
.*                                                                    *
.*   &SGUF52B - STAND ALONE SEEK REQUIRED                             *
.*                                                                    *
.*   &SGUF53B - DEVICE HAS VARIABLE LENGTH SDR'S                      *
.*                                                                    *
.*   &SGUF54B - ENVIRONMENTAL DATA RETURNED                           *
.*                                                                    *
.*   &SGUF55B - LAST PATH TO DEVICE MUST NOT BE VARIED OFFLINE        *
.*                                                                    *
.*   &SGUF56B - DEVICE HAS ALTERNATE CONTROL UNIT                     *
.*                                                                    *
.*   &SGUF57B - DEVICE HAS ALTERNATE CONTROL PATH(OPTCHAN)            *
.*                                                                    *
.*   &SGUFP1B - DEVICE IS SHARABLE WHEN IN UP MODE                    *
.*                                                                    *
.*   &SGUFP4B - DEVICE IS ABLE TO BE SWAPPED BY DDR                   *
.*                                                                    *
.*   &SGUFP5B - ERRLOG IS PRESENT IN THIS DEVICE                      *
.*                                                                    *
.*   &SGUCCWA - NO. OF CCWS REQUIRED FOR CHANL.PROG. PREFIX           *
.*                                                                    *
.*   &SGUATIA - ATTENTION TABLE INDEX                                 *
.*                                                                    *
.*   &SGUDSGA - DEVICE DEPENDENT SEGMENT LENGTH                       *
.*                                                                    *
.*   &SGUDSGC - ENTIRE DEVICE DEPENDENT SEGMENT                       *
.*                                                                    *
.*   &SGUDXTC - ENTIRE DEVICE DEPENDENT EXTENTION                     *
.*                                                                    *
.* LOCAL SYMBOLS USED:                                                *
.*                                                                    *
.*       &I -           DEVICE POINTER                                *
.*                                                                    *
.*       &A -           LOOP COUNTER                                  *
.*                                                                    *
.* EXIT, NORMAL - N/A                                                 *
.*                                                                    *
.* EXIT, ERROR - N/A                                                  *
.*                                                                    *
.* EXTERNAL REFERENCES - NONE                                         *
.*                                                                    *
.* MACROS - DSP MACROS(SGIDSP01 - SGIDSP10)                           *
.*                                                                    *
.* CHANGE ACTIVITY -@ZA40915,OZ59937                                  *
.*                                                                    *
.**********************************************************************
.*     GET DEVICE POINTER
&I       SETA &SGCNTRA(3)        GET DEVICE POINTER
         AIF   (T'&UCBFL5 EQ 'O').FINFL5
.MORE1   AIF   (N'&UCBFL5 EQ &A).FINFL5
&A       SETA  &A+1
         AIF   ('&UCBFL5(&A)' EQ 'DISCONCC').UBIT0
         AIF   ('&UCBFL5(&A)' EQ 'LONESEEK').UBIT2
         AIF   ('&UCBFL5(&A)' EQ 'VARSDR').UBIT3
         AIF   ('&UCBFL5(&A)' EQ 'ENVRTNS').UBIT4
         AIF   ('&UCBFL5(&A)' EQ 'LASTPATH').UBIT5
         AIF   ('&UCBFL5(&A)' EQ 'ACTLUNIT').UBIT6
         AIF   ('&UCBFL5(&A)' EQ 'ALTPATH').UBIT7
         AGO  .MORE1
.UBIT0   ANOP
&SGUF50B(&I)   SETB 1
         AGO  .MORE1
.UBIT2   ANOP
&SGUF52B(&I)   SETB 1
         AGO  .MORE1
.UBIT3   ANOP
&SGUF53B(&I)   SETB 1
         AGO  .MORE1
.UBIT4   ANOP
&SGUF54B(&I)   SETB 1
         AGO  .MORE1
.UBIT5   ANOP
&SGUF55B(&I)   SETB 1
         AGO  .MORE1
.UBIT6   ANOP
&SGUF56B(&I)   SETB 1
         AGO  .MORE1
.UBIT7   ANOP
&SGUF57B(&I)   SETB 1
         AGO  .MORE1
.FINFL5  ANOP
         AIF   (T'&UCBFLP1 EQ 'O').FINFLP1
&A       SETA  0
.MORE2   ANOP
         AIF   (N'&UCBFLP1 EQ &A).FINFLP1
&A       SETA  &A+1
         AIF   ('&UCBFLP1(&A)' EQ 'UPSHARBL').FBIT1
         AIF   ('&UCBFLP1(&A)' EQ 'DDRSWAP').FBIT4
         AIF   ('&UCBFLP1(&A)' EQ 'ERRLOG').FBIT5
         AIF   ('&UCBFLP1(&A)' EQ '').MORE2                    @ZA59937
         AGO  .MORE2
.FBIT1   ANOP
&SGUFP1B(&I)   SETB 1
         AGO  .MORE2
.FBIT4   ANOP
&SGUFP4B(&I)   SETB 1
         AGO  .MORE2
.FBIT5   ANOP
&SGUFP5B(&I)   SETB 1
         AGO  .MORE2
.FINFLP1 ANOP
&SGUPRMB(&I)   SETB 1                  THIS DEVICE IS NON-REMOVEABLE
         AIF   ('&UNMOVBL' EQ 'NO').NOTMOVE
&SGUPRMB(&I)   SETB 0                  THIS DEVICE IS REMOVEABLE
.NOTMOVE ANOP
&SGUCCWA(&I)   SETA &CCWPFIX
&SGUATIA(&I)   SETA &ATNTABX
&SGERPWA(&I)   SETA &ERPFXWA
&SGUDSGA(&I)   SETA &DSEGLEN
&SGUDSGC(&I)   SETC '&DPNDSEG'
&SGUDXTC(&I)   SETC '&DPNDEXT'
.*                           SET DEVICE TYPE IF NOT OVERIDDEN BY USER
                AIF  ('&SGDVTPC' NE '').NOSET1  DEVTYPE OVERIDE?
&O             SETC '&DEVTYPE'(1,6)       1ST 6 CHARS OF DEVICE TYPE
               CONVERT TO=HEX,VALUE=&O,DIGITS=6
&SGDEVTP(&I)   SETA  &HEX
&O             SETC  '&DEVTYPE'(7,2)            LAST TWO CHARS
               CONVERT TO=HEX,VALUE=&O,DIGITS=2
&SGETABA(&I)   SETA  &HEX
               AGO   .NOSET1A                                  @ZA40915
.NOSET1        ANOP          SET USER SPECIFIED DEVICE TYPE
&O             SETC '&SGDVTPC'(1,6) 1ST 6 CHARS OF DEVICE TYPE @ZA40915
               CONVERT TO=HEX,VALUE=&O,DIGITS=6
&SGDEVTP(&I)   SETA  &HEX                                      @ZA40915
&O             SETC  '&SGDVTPC'(7,2)            LAST TWO CHARS @ZA40915
               CONVERT TO=HEX,VALUE=&O,DIGITS=2                @ZA40915
&SGETABA(&I)   SETA  &HEX
.NOSET1A       ANOP                                            @ZA40915
.*                    SET ERROR TAB IF NOT OVERIDDEN BY USER
               AIF   ('&SGERTPC' NE '').NOSET2   ERRTAB OVERIDE?
&SGETABA(&I)   SETA  (&ERRTAB+1)*256+&SGETABA(&I) DEFAULT TAB  @ZA40915
               AGO   .NOSET2A                                  @ZA40915
.NOSET2        ANOP                                            @ZA40915
&SGETABA(&I)   SETA  (&SGERTPC+1)*256+&SGETABA(&I) USER ERR TAB@ZA40915
.NOSET2A       MEND
