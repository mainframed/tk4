         MACRO
         SGIEI1IO
         COPY SGGBLPAK
         LCLA  &A,&B,&D,&DD,&E,&I,&J,&K,&HIGH,&LOW,&MESSAGE,&OLDCH
         LCLA  &C,&CH,&CHDEVAD,&CNTPTR,&COUNT,&COUNT2,&CT,&CTLPTR
         LCLA  &CU,&EXPA,&M,&N
         LCLB  &CHAN(16),&FH,&OPTCHB,&S,&S1,&S2,&XB
         LCLC  &ADDRESI,&ADDRESO,&ADDR,&NEWADDR,&ZA,&ZB        @ZA74045
         ACTR  750000                                          @ZA42948
.*
.*       THE I/O CONFIGURATION PARAMETERS ARE VALIDATED IN THE MACRO
.*       SGIEI1IO. CERTAIN DEVICE DEPENDENCIES ARE ALSO CHECKED HERE.
.*
.*       THE GLOBALS USED ARE -
.*             &SGMENTB(2)     - CHANNEL MACRO
.*             &SGMENTB(34)    - DATAMGT ENTRY SWITCH
.*             &SGQUITB        - QUIT SWITCH
.*             &SGCNTRA(5)     - COUNT OF ADDRESSES FROM UNITNAME
.*             &SGCNTRA(6)     - CONTROL IN UNITNAME
.*             &SGUNNMA(1-1028)- ADDRESSES ASSOC WITH NAMES
.*             &SGCNTRA(4)     - NUMBER OF ESOTERIC UNITNAMES
.*             &SGUNSGA        - TOTAL NUMBER OF UNITNAMES
.*             &SGUNNMC(1-100) - ESOTERIC NAMES IN UNITNAME
.*             &SGDT03B(1-1917)- DIRECT ACCESS
.*             &SGUNVB(1-100)  - VIO BIT FOR ESOTERIC NAMES
.*             &SGUNVA         - TOTAL NUMBER OF VIO NAMES
.*             &SGUNNVC(1-100) - VIRTUAL IO DEVICES
.*             &SGCHANB(1-240) - ADDRESS AND TYPE OF CHANNEL
.*             &SGBRSTB(1-1917)- BURST DEVICE ON CHANNEL
.*             &SGDADRA(1-1917)- DEVICE ADDRESS
.*             &SGDASPA(1-1917)- DEVICE POINTER
.*             &SGOPSUB(1-1917)- OPT CHANNEL ON SEL-SUBCHANNEL
.*             &SGDAPXB(1-1917)- X=1 TO F OPTIONAL CHANNEL X
.*             &SGDTXXB(1-1917)- X=01 TO 12 DEVICE TYPE
.*             &SGDUMMY(1-1917)- DUMMY DEVICE
.*             &SGPSUDO(1-1917)- HASP DEVICE
.*             &SGCTRLB(1)     - DUPLICATE ADR CHECK FOR OPCHAN
.*             &SGCTRLA(2)     - INDEX FOR I/O CHECK
.*             &SGDU02B(1-1917)- 2260
.*             &SGDU06B(1-1917)- 3066
.*             &SGDM01B(1-1917)- MODEL NO 1
.*             &SGDM03B(1-1917)- MODEL NO 3
.*             &SGCUN1A(1-1917)- ???
.*             &SGDU08B(1-1917)- 3505
.*             &SGDU09B(1-1917)- 3525
.*             &SGD3505A       - NO OF 3505'S
.*             &SG3525A        - NO OF 3525'S
.*             &SGSUPRB(35)    - APR
.*
.*       THE LOCALS USED ARE -
.*             &A       - UNIT NAME COUNTER START HIGH
.*             &B       - ADDRESS BOX INDEX
.*             &D       - UNIT NAME COUNTER START LOW
.*             &DD      - CHANNEL PORTION OF ADDRESS
.*             &E       - HIGEST PCU NUMBER                    @ZA29084
.*             &I       - INDEX TO ADDRESS BOX
.*             &J       - INDEX
.*             &K       - REVERSE LIMIT FOR SIFT
.*             &HIGH    - HIGH ADDRESS
.*             &LOW     - LOW ADDRESS
.*             &MESSAGE - USED IN CONVERTING BETW HEX AND EBCDIC
.*             &OLDCH   - CHANNEL ADDRESS
.*             &C       - HALF OF ADDRESS
.*             &CH      - CHANNEL ADDRESS
.*             &CHDEVAD - DEVICE PORTION OF ADDRESS
.*             &CNTPTR  - ???
.*             &COUNT   - COUNT OF DEVICES
.*             &COUNT2  - DEVICE COUNT
.*             &CT      - UNIT NAME COUNTER
.*             &CTLPTR  - CONTROL UNIT PTR
.*             &CU      - CONTROL UNIT COUNTER
.*             &EXPA    - EXBFR
.*             &CHAN(16)- CHANNEL AVAILABLE SWITCH
.*             &FH      - FLIP FLOP SWITCH
.*             &OPTCHB  - OPTIONAL CHANNEL SWITCH
.*             &S       - FLIP FLOP SWITCH
.*             &S1      - FLIP FLOP SWITCH STATUS
.*             &S2      - SWITCH FOR DA
.*             &XB      - ???
.*             &ADDRESI - ???
.*             &ADDRESO - ???
.*
.*  MODULE ACTIVITY: VS03045,Z40ASVN
.*                   OZ11149,@ZA20770,@ZA29084,@ZA33305,@ZA36178,
.*                   @ZA42948,@ZA61007,@ZA71976,@ZA74045
         CUPOINT
         AIF   (&SGMENTB(2)).A02
    MNOTE 5,'* * * IEIGEN100 CHANNEL MACRO REQUIRED'
&SGQUITB SETB  1
.A02     AIF   (&SGMENTB(4)).A03
    MNOTE 5,'* * * IEIGEN101 IODEVICE MACRO REQUIRED'
&SGQUITB SETB  1
.**********************************************************************
.*  THIS ROUTINE SORTS ADDRESSES ASSIGNED TO UNITNAMES    SIFT METHOD
.**********************************************************************
.A03     AIF   (&SGCNTRA(5) GT 0).A04
&SGCNTRA(5)  SETA  1
.A04     AIF   (&SGCNTRA(6) EQ 2).FILLIN
&SGUNNMA(&SGCNTRA(5))  SETA  X'FFF'
.FILLIN  ANOP    FFF MEANS END OF ONE NAMES ADDRESSES
&SGUNNMA(&SGCNTRA(5))  SETA  4096*&SGUNNMA(&SGCNTRA(5))+X'FFF'
         AIF   (&SGCNTRA(4) EQ 0).T21       BYPASS ROUTINES IF NO NAMES
&CT      SETA  &SGCNTRA(4)       GET NUMBER OF NAMES
&K       SETA  1                 SET REVERSE LIMIT FOR SIFT
.SRT     ANOP
&I       SETA  &I+1              STEP INDEX
.SRT2    ANOP
&S       SETB  (NOT &S)          COMPLEMENT FLIP-FLOP SWITCH
         AIF   (&S).SRT3         GO COMPARE X(I) LEFT AND X(I) RIGHT
&LOW   SETA  &SGUNNMA(&I)-&SGUNNMA(&I)/4096*4096  COMPARE X(I) RIGHT
&HIGH  SETA  &SGUNNMA(&I+1)/4096        WITH X(I+1) LEFT
         AGO  .SRT4              GO MAKE ACTUAL COMPARE
.SRT3    ANOP
&LOW     SETA  &SGUNNMA(&I)/4096  COMPARE X(2) LEFT AND
&HIGH    SETA  &SGUNNMA(&I)-&SGUNNMA(&I)/4096*4096   X(I) RIGHT
.SRT4    AIF   (&LOW EQ X'FFF').SRT12
         AIF   (&HIGH EQ X'FFF').SRU1
         AIF   (&LOW GT &HIGH).SRT7   THIS IS EXCHANGE CONDITION
         AIF   (&LOW NE &HIGH).SRT5   NO EXCHANGE NEEDED
   CONVERT  TO=EBCDIC,VALUE=&LOW,DIGITS=3  CONVERT ADDRESS TO EBCDIC
    MNOTE 5,'* * * IEIGEN110 UNITNAME &SGUNNMC(&SGCNTRA(4)-&CT+1) HAS D*
               UPLICATE ADDRESS &EBCDIC'
&SGQUITB  SETB 1
.SRT5    AIF   (&J EQ 0).SRT6    BYPASS RESTORE INDEX ROUTINE
.SRT5A   ANOP
&I       SETA  &J                RESTORE INDEX TO RESUME FORWARD SORT
&S       SETB  (&S1)             RESTORE FLIP-FLOP SWITCH
&J       SETA  0                 CLEAR INDEX STORAGE LOCATION
.SRT6    AIF   (&S).SRT2         IF ZERO,BUMP INDEX
         AGO   .SRT
.SRT7    AIF   (NOT &S).SRT8     EXCHANGE ROUTINE
&SGUNNMA(&I)  SETA &SGUNNMA(&I)-&SGUNNMA(&I)/4096*4096+&HIGH*4096  EXCH
&SGUNNMA(&I)  SETA &SGUNNMA(&I)/4096*4096+&LOW  X(I) LEFT AND X(I) RT.
    AGO .SRT9
.SRT8    ANOP                    EXCHANGE ROUTINE
&SGUNNMA(&I+1)  SETA &SGUNNMA(&I+1)-&SGUNNMA(&I+1)/4096*4096+&LOW*4096
&SGUNNMA(&I)  SETA &SGUNNMA(&I)/4096*4096+&HIGH  EXCH X(I+1) L+T,XCI) R
.SRT9    AIF   (&J NE 0).SRT10   IS THIS A FORWARD SORT
&J       SETA  &I                YES,SAVE INDEX FOR FORWARD RESUMPTION
&S1      SETB  (&S)              SAVE FLIP-FLOP SWITCH STATUS
.SRT10   AIF   (&I GT &K).SRT11  IS I EQUAL TO REVERSE LIMIT
         AIF   (&S).SRT5A        IF ZERO,BUMP INDEX DOWN
    AGO .SRT2
.SRT11   AIF   (NOT &S).SRT2     IF ONE BUMP INDEX DOWN
&I       SETA  &I-1              BACK SORT IN PROGRESS
    AGO .SRT2
.SRT12   AIF   (&J NE 0).SRT5A
&CT      SETA  &CT-1
         AIF   (&CT EQ 0).SRU2
         AIF   (&I GE 1028).T21  MAX NUMBER OF UNITS           @ZA20770
&S1      SETB  (NOT &S)
         AIF   (NOT &S).SRT
         AGO   .SRT2
.SRU1    ANOP
&CT      SETA  &CT-1             DECREMENT NAME COUNTER
         AIF   (&CT EQ 0).SRU2   IF ZERO ALL DONE
         AIF   (&I GE 1028).T21  MAX NUMBER OF UNITS           @ZA20770
&K       SETA  &I+1              SET NEW REVERSE LIMIT
&S       SETB  (NOT &S)          COMPLEMENT FLIP-FLOP SWITCH
    AGO  .SRT
.SRU2    ANOP                    END OF UNITNAME ADDRESS SORT ROUTINE
.**********************************************************************
.* THE FOLLOWING ROUTINE CHECKS ADDRESSES OF UNITNAMES FOR DEVICES
.**********************************************************************
&A       SETA  &SGCNTRA(4)       GET TOTAL NUMBER OF UNITNAMES
&D       SETA 1                  COUNT NAMES
.T11     AIF  (&FH).T13           CHECK RIGHT/ LEFT HALF OF ADDRESS BOX
&B       SETA   &B+1              STEP ADDRESS BOX INDEX TO NEW BOX
&C       SETA   &SGUNNMA(&B)/4096  SET LEFT ADDRESS IN BOX INTO C
         AGO .T15
.T13     ANOP
&C  SETA  &SGUNNMA(&B)-&SGUNNMA(&B)/4096*4096  SET RIGHT HALF INTO C
.T15     AIF  (&C EQ X'FFF').T19   EXIT ON ADDRESS DELIMITER
         IOCHECK  LOOKFOR=&C       CALL FOR BINARY SEARCH
         AIF  (&HEX NE 0).T17      IF NOT ZERO ITS FOUND
         CONVERT  TO=EBCDIC,VALUE=&C,DIGITS=3  CONVERT C TO EBCDIC
    MNOTE 5,'* * * IEIGEN120 UNITNAME ADDRESS &EBCDIC NOT A DEVICE'
&SGQUITB SETB  1
         AGO  .T18
.T17     ANOP
&J       SETA  &SGDASPA(&HEX)             DEVICE NUMBER         Z40ASVN
         AIF   (NOT(&SGDT03B(&J))).T18    NOT DIRECT ACCESS DEVICE
&S2      SETB  1                          DASD REQUIRED FOR VIO
.T18     ANOP
&FH      SETB  (NOT(&FH))        COMPLEMENT FLIP-FLOP SWITCH
         AGO .T11                RETURN TO START OF ROUTINE
.T19   ANOP
         AIF   (&SGUNVB(&D) AND (NOT(&S2))).T20   NEED DIRECT ACCESS
&S2      SETB  0                  RESET SWITCH FOR D.A.
&D       SETA  &D+1               CHECK NEXT UNITNAME
&A       SETA  &A-1               DECREMENT UNITNAME COUNT
&FH      SETB  (NOT(&FH))         COMPLEMENT FLIP-FLOP SWITCH
         AIF   (&A NE 0).T11      RETURN TO START OF ROUTINE
         AGO   .T21               END OF CHECK
.T20  MNOTE 5,'* * * IEIGEN121 AT LEAST ONE DEVICE NAMED &SGUNNMC(&D) MX
               UST BE A DIRECT ACCESS DEVICE IF VIO=YES IS SPECIFIED'
&SGQUITB SETB 1
&D       SETA  &D+1               CHECK NEXT UNITNAME
&A       SETA  &A-1               DECREMENT UNITNAME COUNT
&FH      SETB  (NOT(&FH))         COMPLEMENT FLIP-FLOP SWITCH
         AIF   (&A NE 0).T11      RETURN TO START OF ROUTINE
.T21     ANOP                     END OF UNITNAME ADDRESS CHECK
.**********************************************************************
.*  THIS ROUTINE CHECKS HARDWARE COMPATIBILITY
.**********************************************************************
.* THIS ROUTINE SETS UP FOR TEST THAT ALL DEVICES HAVE A CHANNEL AND
.* THAT ALL CHANNELS HAVE A DEVICE ON THEM
.**********************************************************************
&D       SETA 0
&I       SETA   0
.M1      AIF   (&I EQ 16).MX1
&CHAN(&I+1)  SETB  (&SGCHANB(&I*15+1))
&I       SETA  &I+1
         AGO  .M1
.MX1     ANOP
&I       SETA 0
.MX2     AIF   (&I EQ 16).MX6
         AIF   (&SGBRSTB(&I+1) AND &SGCHANB(&I*15+2)).MX3
         AIF   (NOT(&SGBRSTB(&I+1) AND &SGCHANB(&I*15+4))).MX5
         AIF (NOT &SGDISKB(&I+1)).NOTDISK
 MNOTE 5,'* * * DIRECT ACCESS DEVICES INVALID ON HSMPXR CHANNEL &I)'
&SGQUITB SETB 1
.NOTDISK  AIF  (NOT &SGTAPEB(&I+1)).NOTAPE
 MNOTE 0,' TAPES VALID ONLY ON SELECTOR SUBCHANNELS C,D,E,F OF CHANNEL *
               &I'
.NOTAPE  AIF  (NOT &SG2250B(&I+1)).MX5
 MNOTE 0,' 2250 VALID ONLY ON SELECTOR SUBCHANNELS C,D,E,F OF CHANNEL &*
               I'
         AGO  .MX5
 MNOTE 5,'* * * IEIGEN130 BURST DEVICES INVALID ON SUB- CHANNEL OF     *
               CHANNEL &I'
&SGQUITB SETB 1
  MNOTE *,'    BURST DEVICES SUPPORTED ON HISPEEDMPXR INCLUDE          *
               2250-24XX-3410-3420'
         AGO .MX5
.MX3 MNOTE 5,'* * * IEIGEN131 BURST DEVICES INVALID ON MULTIPLEXOR CHAN*
               NEL &I'
&SGQUITB SETB 1
         AGO  .MX5
.MX4     ANOP        TAPES ALLOWED ONLY ON SUB-SELECTOR CHANNELS
         AIF  (&SGDCLSB(9)).MX5
  MNOTE *,' TAPES ALLOWED ONLY ON SUB-SELECTOR CHANNELS C, D, E, AND F'
.MX5     ANOP
&I       SETA &I+1
         AGO .MX2
.MX6     ANOP
&I       SETA  0
&E       SETA  0                                               @ZA29084
.D01     ANOP                    TOP OF  CHECK  LOOP
&I       SETA  &I+1
         AIF   (&I GT &SGCNTRA(3)).D99   LAST DEVICE HAS BEEN CHECKED
&CT      SETA  &SGDADRA(&I)-&SGDADRA(&I)/4096*4096  GET ADDRESS INTO CT
&J       SETA &SGDASPA(&I)              GET DEVICE POINTER      Z40ASVN
&K       SETA &SGDADRA(&I)/4096-&SGDADRA(&I)/1048576*256   GET CU PTR
         AIF   (&J EQ 0).D01     J USED AS DIMENSION
.*           IS THE CHANNEL PRESENT
.*    IF YES INDICATE USED
         AIF   (NOT (&SGCHANB((&CT/256)*15+1))).M2
&CHAN(&CT/256+1)  SETB  0
         CONVERT TO=EBCDIC,DIGITS=3,VALUE=&CT
         AIF   (&SGOPSUB(&J) AND NOT &SGCHANB((&CT/256)*15+4)).M10
         AIF   (NOT &SGCHANB((&CT/256)*15+4)).D02   H.S.MPXR
         AIF   (&SGDAP1B(&J) OR &SGDAP5B(&J) OR &SGDAP6B(&J) OR &SGDAP7*
               B(&J) OR &SGDAP8B(&J) OR &SGDAP9B(&J)).M11
         AIF   (&SGDAPAB(&J) OR &SGDAPBB(&J) OR &SGDAPCB(&J) OR &SGDAPD*
               B(&J) OR &SGDAPEB(&J) OR &SGDAPFB(&J)).M11
         AIF   ((&SGDAP2B(&J) OR &SGDAP3B(&J) OR &SGDAP4B(&J)) AND NOT *
               &SGOPSUB(&J)).M11
.D02     AIF   (&SGDT01B(&J)).TSTCHN  DEVICE IS TAPE
         AIF   (&SGDT05B(&I) AND &SGDU01B(&I)).TSTCHN 2250     @ZA61007
         AIF   (&SGDT05B(&J)).DG1   DEVICE IS  GRAPHIC
         AIF   (&SGDT07B(&I) AND &SGDU13B(&I)).TSTCHN 3851     @ZA61007
         AIF   (&SGDT07B(&I)).DU1   DEVICE IS RDR-PCH
.********PTM 4300 REMOVED THE AGO .D01 STATEMENT PREVIOUSLY HERE*******
.**********************************************************************
.*             THIS ROUTINE CHECKS CHANNEL SWITCHING                  *
.**********************************************************************
.TSTCHN  ANOP
&DD      SETA &CT/X'100'            CHANNEL PORTION OF ADDRESS
&CHDEVAD SETA &CT-&CT/X'10'*X'10'   DEVICE PORTION OF ADDRESS
         AIF (&SGCHANB(&DD*15+4)).HSPX     H.S. MULTIPLEXOR
&DD      SETA &CT-&CT/X'100'*X'100'    C.U. AND DEVICE PORTION
         AGO .RET0
.HSPX    ANOP
&DD      SETA &CT/X'100'*X'100'   CHANNEL PORTION OF ADDR W/00 C.U.+DEV
.RET0    AIF (&SGDAP1B(&J)).CHAN1
.RET1    AIF  (&SGDAP2B(&J)).CHAN2    OPTIONAL CHANNEL 2
.RET2    AIF  (&SGDAP3B(&J)).CHAN3    OPTIONAL CHANNEL 3
.RET3    AIF  (&SGDAP4B(&J)).CHAN4    OPTIONAL CHANNEL 4
.RET4    AIF  (&SGDAP5B(&J)).CHAN5    OPTIONAL CHANNEL 5
.RET5    AIF  (&SGDAP6B(&J)).CHAN6    OPTIONAL CHANNEL 6
.RET6    AIF   (&SGDAP7B(&J)).CHAN7    OPTIONAL CHANNEL 7
.RET7    AIF   (&SGDAP8B(&J)).CHAN8    OPTIONAL CHANNEL 8
.RET8    AIF   (&SGDAP9B(&J)).CHAN9    OPTIONAL CHANNEL 9
.RET9    AIF   (&SGDAPAB(&J)).CHANA    OPTIONAL CHANNEL A
.RETA    AIF   (&SGDAPBB(&J)).CHANB    OPTIONAL CHANNEL B
.RETB    AIF   (&SGDAPCB(&J)).CHANC    OPTIONAL CHANNEL C
.RETC    AIF   (&SGDAPDB(&J)).CHAND    OPTIONAL CHANNEL D
.RETD    AIF   (&SGDAPEB(&J)).CHANE    OPTIONAL CHANNEL E
.RETE    AIF   (&SGDAPFB(&J)).CHANF    OPTIONAL CHANNEL F
&OPTCHB  SETB  0     RESET OPTIONAL CHANNEL SWITCH FOR CHKNBDV RTN
         AIF   (&SGDT05B(&J) AND &SGDU01B(&J)).DG1    2250     @ZA71976
         AIF   (&SGDT07B(&J) AND &SGDU13B(&J)).DU1    3851     @ZA71976
         AGO  .D01
.CHAN1   AIF   (&SGCHANB(1*15+4)).M12   SEL. W/H.S.MPX. 1 FOR OPTCHAN
&CHDEVAD SETA  X'100'+&DD        OPTIONAL DEVICE ADDR
         AGO  .LOOK
.CHAN2   AIF (NOT &SGOPSUB(&J)).CHAN2A   H.S. MULTIPLEXOR SUB
&CHDEVAD SETA &DD+X'D0'+&CHDEVAD       SEL. SUB-CHANNEL OPTCHAN
         AGO .LOOK
.CHAN2A  AIF   (&SGCHANB(2*15+4)).M12   SEL. W/H.S.MPX.  I FOR OPTCHAN
&CHDEVAD SETA  X'200'+&DD        OPTIONAL DEVICE ADDR
         AGO  .LOOK
.CHAN3   AIF (NOT &SGOPSUB(&J)).CHAN3A   H.S. MULTIPLEXOR SUB
&CHDEVAD SETA &DD+X'E0'+&CHDEVAD       SEL. SUB-CHANNEL OPTCHAN
         AGO .LOOK
.CHAN3A  AIF   (&SGCHANB(3*15+4)).M12    SEL. W/H.S.MPX.  3 FOR OPTCHAN
&CHDEVAD SETA  X'300'+&DD  OPTIONAL DEVICE ADDR
         AGO  .LOOK
.CHAN4   AIF (NOT &SGOPSUB(&J)).CHAN4A   H.S. MULTIPLEXOR SUB
&CHDEVAD SETA &DD+X'F0'+&CHDEVAD       SEL. SUB-CHANNEL OPTCHAN
         AGO  .LOOK
.CHAN4A  AIF   (&SGCHANB(4*15+4)).M12   SEL. W/H.S.MPX. 4 FOR OPTCHAN
&CHDEVAD SETA  X'400'+&DD        OPTIONAL DEVICE ADDR
         AGO  .LOOK
.CHAN5   AIF   (&SGCHANB(5*15+4)).M12   SEL. W/H.S.MPX. 5 FOR OPTCHAN
&CHDEVAD SETA  X'500'+&DD        OPTIONAL DEVICE ADDR
         AGO  .LOOK
.CHAN6   AIF   (&SGCHANB(6*15+4)).M12   SEL.  W/H.S.MPX.  6 FOR OPTCHAN
&CHDEVAD SETA  X'600'+&DD        OPTIONAL DEVICE ADDR
         AGO  .LOOK
.CHAN7   AIF   (&SGCHANB(7*15+4)).M12   SEL.  W/H.S.MPX.  7 FOR OPTCHAN
&CHDEVAD SETA  X'700'+&DD        OPTIONAL DEVICE ADDRESS
         AGO   .LOOK
.CHAN8   AIF   (&SGCHANB(8*15+4)).M12   SEL.  W/H.S.MPX.  8 FOR OPTCHAN
&CHDEVAD SETA  X'800'+&DD        OPTIONAL DEVICE ADDRESS
         AGO   .LOOK
.CHAN9   AIF   (&SGCHANB(9*15+4)).M12   SEL.  W/H.S.MPX.  9 FOR OPTCHAN
&CHDEVAD SETA  X'900'+&DD        OPTIONAL DEVICE ADDRESS
         AGO   .LOOK
.CHANA   AIF   (&SGCHANB(10*15+4)).M12  SEL.  W/H.S.MPX. 10 FOR OPTCHAN
&CHDEVAD SETA  X'A00'+&DD        OPTIONAL DEVICE ADDRESS
         AGO   .LOOK
.CHANB   AIF   (&SGCHANB(11*15+4)).M12  SEL.  W/H.S.MPX. 11 FOR OPTCHAN
&CHDEVAD SETA  X'B00'+&DD        OPTIONAL DEVICE ADDRESS
         AGO   .LOOK
.CHANC   AIF   (&SGCHANB(12*15+4)).M12  SEL.  W/H.S.MPX. 12 FOR OPTCHAN
&CHDEVAD SETA  X'C00'+&DD        OPTIONAL DEVICE ADDRESS
         AGO   .LOOK
.CHAND   AIF   (&SGCHANB(13*15+4)).M12  SEL.  W/H.S.MPX. 13 FOR OPTCHAN
&CHDEVAD SETA  X'D00'+&DD        OPTIONAL DEVICE ADDRESS
         AGO   .LOOK
.CHANE   AIF   (&SGCHANB(14*15+4)).M12  SEL.  W/H.S.MPX. 14 FOR OPTCHAN
&CHDEVAD SETA  X'E00'+&DD        OPTIONAL DEVICE ADDRESS
         AGO   .LOOK
.CHANF   AIF   (&SGCHANB(15*15+4)).M12  SEL.  W/H.S.MPX. 15 FOR OPTCHAN
&CHDEVAD SETA  X'F00'+&DD        OPTIONAL DEVICE ADDRESS
.LOOK    AIF   (NOT (&SGCHANB(&CHDEVAD/X'100'*15+1))).CHER  IS CH EXIST
.* REMOVED CODE TO RESET &CHAN BY OPTCHAN BY OZ30343          @ZA30343
&SGCTRLB(1) SETB 1
&SGCTRLA(2) SETA &I              PASS INDEX TO IOCHECK
         IOCHECK LOOKFOR=&CHDEVAD  IS THERE A DEVICE WITH THIS ADDR
&SGCTRLB(1) SETB 0
         AIF  (&HEX EQ 0).GOOD1  NO
         CONVERT TO=EBCDIC,DIGITS=3,VALUE=&HEX
&ADDRESI SETC '&EBCDIC'
&MESSAGE SETA &CHDEVAD/X'100'
         CONVERT TO=EBCDIC,DIGITS=1,VALUE=&MESSAGE
         AIF (NOT &SGOPSUB(&J)).MSGTYP1        NOT SUB-CHANNELS
&MESSAGE SETA &CHDEVAD/X'10'           OPTCHAN AS SPECIFIED
         CONVERT TO=EBCDIC,DIGITS=2,VALUE=&MESSAGE
.MSGTYP1 ANOP
&ADDRESO SETC '&EBCDIC'
         CONVERT TO=EBCDIC,DIGITS=3,VALUE=&CT
    MNOTE 5,'* * * IEIGEN131 ADDRESS &EBCDIC OPTCHAN &ADDRESO CONFLICT *
               AT DEVICE WITH ADDRESS &ADDRESI'
&SGQUITB SETB  1                 TEST TWO CHANNEL SWITCH FOR 2820-2841
.GOOD1   AIF   (&SGOPSUB(&J)).GOOD2     H.S.MPX SUB-CHANNEL OPTCHAN
         AIF  (&CHDEVAD/X'100' EQ 1).RET1  GET OTHER OPTCHANS
         AIF  (&CHDEVAD/X'100' EQ 2).RET2
         AIF  (&CHDEVAD/X'100' EQ 3).RET3
         AIF  (&CHDEVAD/X'100' EQ 4).RET4
         AIF  (&CHDEVAD/X'100' EQ 5).RET5
         AIF  (&CHDEVAD/X'100' EQ 6).RET6
         AIF  (&CHDEVAD/X'100' EQ 7).RET7
         AIF  (&CHDEVAD/X'100' EQ 8).RET8
         AIF  (&CHDEVAD/X'100' EQ 9).RET9
         AIF  (&CHDEVAD/X'100' EQ 10).RETA
         AIF  (&CHDEVAD/X'100' EQ 11).RETB
         AIF  (&CHDEVAD/X'100' EQ 12).RETC
         AIF  (&CHDEVAD/X'100' EQ 13).RETD
         AIF  (&CHDEVAD/X'100' EQ 14).RETE
&OPTCHB  SETB  0        RESET OPTIONAL CHANNEL SWITCH FOR CHKNBDV RTN
         AIF   (&SGDT05B(&J) AND &SGDU01B(&J)).DG1    2250     @ZA71976
         AIF   (&SGDT07B(&J) AND &SGDU13B(&J)).DU1    3851     @ZA71976
         AGO  .D01
.GOOD2   ANOP
&MESSAGE SETA &CHDEVAD/X'10'           SAVE CHDEVAD
&MESSAGE SETA &MESSAGE-&MESSAGE/X'10'*X'10' C.U. PORTION
&CHDEVAD SETA &CT-&CT/X'10'*X'10'      DEVICE PORTION
&HEX     SETA X'D'
         AIF (&MESSAGE EQ &HEX).RET2
&HEX     SETA X'E'
         AIF (&MESSAGE EQ &HEX).RET3
&OPTCHB  SETB 0         RESET OPTIONAL CHANNEL SWITCH FOR CHKNBDV RTN
         AIF   (&SGDT05B(&J) AND &SGDU01B(&J)).DG1    2250     @ZA71976
         AIF   (&SGDT07B(&J) AND &SGDU13B(&J)).DU1    3851     @ZA71976
         AGO .D01
.CHER    CONVERT TO=EBCDIC,DIGITS=3,VALUE=&CHDEVAD
&ADDRESI SETC  '&EBCDIC'(1,1)
         CONVERT TO=EBCDIC,DIGITS=3,VALUE=&CT
     MNOTE 5,'* * * IEIGEN132 DEVICE ADDRESS &EBCDIC OPTCHAN &ADDRESI N*
               OT A DEFINED CHANNEL'
&SGQUITB SETB 1
         AGO .GOOD1                CONTINUE OTHER CHECKS
.M10 MNOTE 5,'* * * IEIGEN133 DEVICE ADDRESS &EBCDIC SPECIFIES OPTCHAN(*
               S) VALID FOR H.S.MPX. ONLY'
&SGQUITB SETB 1
         AGO .D01
.M11 MNOTE 5,'* * * IEIGEN134 DEVICE ADDRESS &EBCDIC SPECIFIES OPTCHAN(*
               S) INVALID FOR H.S.MPX.'
&SGQUITB SETB 1
         AGO .D01
.M12 MNOTE 5,'* * * IEIGEN135 H.S.MPX. OPTCHAN AT DEVICE ADDRESS &EBCDI*
               C INVALID WITH SELECTOR OR MULTIPLEXOR'
&SGQUITB SETB 1
         AGO .D01
.*                               CHECK DISPLAY DEVICES
.DG1     AIF   (NOT &SGDU01B(&J)).D01   NOT A 2250
         AIF   (&SGDM01B(&J)).CHKCNT1   2250 MODEL 1
         AIF   (NOT &SGDM03B(&J)).D01   2250 MODEL 3
         AIF   (&CTLPTR EQ &K).TSTCNT   IS IT THE SAME CTRL UNIT
&CTLPTR  SETA  &K                       NO,CHANGE CTRL UNIT PTR
&CU      SETA  &CU+1                    COUNT 2840 CTRL UNITS
&COUNT   SETA  0                        ERASE COUNT OF DEVICES
&EXPA    SETA  &SGCUN1A(&J)
&ADDR     SETC   '&CT'         ADDRESS OF CURRENT DEVICE
&M       SETA   0
.SRPCU   AIF    (&M GT &SGCNTRA(3)).INCCNT  END OF DADR BOX
&M       SETA   &M+1                       SET DADR POINTER
&N SETA &SGDADRA(&M)/4096-&SGDADRA(&M)/1048576*256  GET CU POINTER
         AIF    (&N GT &E).RESLCL          HIGHEST PCU NUMBER? @ZA29084
.CHPCU   AIF    (&CTLPTR NE &N).SRPCU      SAME PCU NUMBER     @ZA29084
&A  SETA &SGDADRA(&M)-&SGDADRA(&M)/4096*4096 GET ADDRESS
&NEWADDR SETC   '&A'
         CONVERT TO=EBCDIC,DIGITS=3,VALUE=&NEWADDR             @ZA74045
&ZA      SETC   '&EBCDIC'                                      @ZA74045
         CONVERT TO=EBCDIC,DIGITS=3,VALUE=&ADDR                @ZA74045
&ZB      SETC   '&EBCDIC'                                      @ZA74045
         AIF    ('&ZB'(1,1) EQ '&ZA'(1,1)).SRPCU               @ZA74045
     MNOTE 5,'IEIGEN166 DEVICE TYPE 2250 MODEL 3 WITH SAME PCU MUST BE *
                ON THE SAME CHANNEL'
&SGQUITB SETB   1
         AGO   .INCCNT
.RESLCL  ANOP                                                  @ZA29084
&E       SETA  &N                                              @ZA29084
         AGO   .CHPCU                                          @ZA29084
.TSTCNT  AIF   (&COUNT LT 4).INCCNT   CHECK COUNT LT MAX
         CONVERT TO=EBCDIC,DIGITS=3,VALUE=&CT
    MNOTE 5,'* * * IEIGEN150 DEVICE ADDRESS &EBCDIC EXCEEDS MAXIMUM OF *
               4 DEVICES ON CONTROL UNIT'
&SGQUITB SETB  1
         AGO   .D01
.INCCNT  ANOP                    INCREASE COUNT OF DEVICES  BY ONE
&COUNT   SETA  &COUNT+1
         AIF   (&EXPA EQ &SGCUN1A(&J)).D01
    MNOTE 5,'* * * IEIGEN149 DEVICE ADDRESS &EBCDIC, EXPBFR VALUE MUST *
               BE SAME FOR ALL 2250 MODEL 3''S ON SAME CONTROL UNIT'
&SGQUITB SETB  1
         AGO   .D01
.CHKCNT1 ANOP                    MAX OF 8 2250 MODEL 1 ON ONE CHANNEL
&CH      SETA  &CT/X'100'        TAKE CHANNEL NO
         AIF  (&CH EQ &OLDCH).TSTCNT2   IS THIS CHANNEL ALREADY USED
&OLDCH   SETA  &CH               CHANNEL ADDR
&COUNT2  SETA  0                 ERASE DEVICE COUNT
         AGO  .INCPTR2
.TSTCNT2 AIF  (&COUNT2 LT 8).INCPTR2
    MNOTE 5,'* * * IEIGEN151 MAX OF EIGHT 2250 ON THE SAME CHANNEL EXCE*
               EDED'
&SGQUITB SETB  1
         AGO   .D01
.INCPTR2 ANOP                    INCREASE  DEVICE COUNT
&COUNT2  SETA  &COUNT2+1
         AGO  .D01
.*      CHECK READER/PUNCH DEVICES
.DU1     AIF   (NOT (&SGDU08B(&J) OR &SGDU09B(&J))).D01  3505/3525
         AIF   (&XB).D01
         AIF   (&SG3505A GE &SG3525A).D01
    MNOTE 0,'* * * IEIGEN145 GREATER NUMBER OF 3525 PUNCHES THAN 3505 C*
               ONTROL UNITS'                                   @ZA36178
&XB      SETB  1
         AGO   .D01
.M2      ANOP
&J       SETA (&CT/256)
         CONVERT TO=EBCDIC,DIGITS=3,VALUE=&CT
    MNOTE 5,'* * * IEIGEN164 ADDRESS &EBCDIC REQUIRES CHANNEL &J'
&SGQUITB  SETB  1
         AGO  .D01
.*             CHECK THAT ALL CHANNELS HAVE A DEVICE
.D99     ANOP
&I       SETA  0
.M4      AIF   (&I EQ 16).M6                                   @AZ11149
&I       SETA  &I+1
         AIF (NOT (&CHAN(&I))).M4
&CT      SETA  &I-1
.*  NEXT MSG UPDATED BY OZ30343                                @ZA30343
    MNOTE 5,'* * * IEIGEN165 CHANNEL &CT CONTAINS NO DEVICES, OR       X
                             CHANNEL &CT SPECIFIED AS OPTCHAN,         X
                             MINIMUM OF A DUMMY DEVICE REQUIRED'
&SGQUITB  SETB  1
         AGO  .M4
.M6      ANOP         THESE 9 LINES CHECK FOR 3851 IF          @AZ11149
&I       SETA  0      3330V IS SPECIFIED.                      @AZ11149
.M7      AIF (&I GT &SGCNTRA(3)).M5                            @AZ11149
&I       SETA &I+1                                             @AZ11149
         AIF (NOT (&SGDT03B(&I) AND &SGDU07B(&I) AND &SGDF03B(&I))).M7
         AIF (&SGDCLSB(28)).M5                                 @AZ11149
   MNOTE 5,'* * * IEIGEN163 MUST SPECIFY 3851 WITH 3330V'      @AZ11149
&SGQUITB SETB 1                                                @AZ11149
         AGO .M5                                               @AZ11149
.*       END OF HARDWARE COMPATIBILITY CHECK
.M5      AIF   (&E NE &CU).PCUMSG                              @ZA29084
.M5A     AIF   (&SGCNTRA(2) EQ &CU).M51                        @ZA29084
&SGCNTRA(2)  SETA  &CU           SET 2840 CTRL UNIT COUNT
.M51     AIF   (&SGOPCHA LE 0).UCS
&SGSUPRB(35)  SETB  1         ALTERNATE PATH RETRY
    MNOTE 0,'     ALTERNATE PATH RETRY PROVIDED'
.UCS     AIF   (&SGMENTB(34)).MEND
&SGUCSDC(1)   SETC 'ALL'
    MNOTE 0,'     ALL UCS IMAGES ARE DEFAULTS'
         AGO  .MEND                                            @ZA29084
.PCUMSG  ANOP                                                  @ZA29084
  MNOTE 5,'* * * IEIGEN167 2250 MODEL 3 HIGHEST PCU NUMBER NOT EQUAL TO*
                             TOTAL NUMBER OF CONTROL UNITS.            *
                             IFFBDA WILL NOT ASSEMBLE CORRECTLY'
&SGQUITB  SETB 1                                               @ZA33305
         AGO  .M5A                                             @ZA29084
.MEND    MEND
