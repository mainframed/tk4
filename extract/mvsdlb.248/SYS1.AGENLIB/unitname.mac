         MACRO
         UNITNAME   &UNIT=,&NAME=,&VIO=NO
         COPY  SGSYSPAK
         LCLA  &A,&N,&LENGTH
         LCLB  &RTNSW,&MULTAD,&S
         LCLC  &C,&D,&E,&ADDR
         ACTR  150000                                          @ZA75914
.***** START OF SPECIFICATIONS **************************************
.*
.* MODULE NAME - UNITNAME
.*
.* DESCRIPTIVE NAME - UNITNAME
.*
.* COPYRIGHT - NONE
.*
.* STATUS - CHANGE LEVEL 04
.*
.* FUNCTION -
.*       THE UNITNAME MACRO IS USED TO NAME A GROUP OF I/O DEVICES.
.*       ONE IS REQUIRED FOR EACH GROUP OF DEVICES IN THE SYSTEM.
.*
.* NOTES - N/A
.*
.* MODULE TYPE - MACRO
.*
.* ENTRY POINT - N/A
.*
.* INPUT - THE FOLLOWING PARAMETERS ARE ALLOWED ON UNITNAME
.*             &UNIT    - SPECIFIES A GROUP OF ADDRESSES TO BE
.*                        RECOGNIZED BY THE ASSOCIATED NAME.
.*             &NAME    - SPECIFIES THE NAME TO BE ASSIGNED TO THE
.*                        ABOVE DEVICES. 1 TO 8 CHARACTERS, GENERIC
.*                        OR ESOTERIC.
.*             &VIO     - SPECIFIES THE INCLUSION OR EXCLUSION OF
.*                        VIRTUAL I/O FOR THE SPECIFIED NAME.
.*
.* OUTPUT -
.*       GLOBALS USED ARE -
.*             &SGMENTB(36) - UNIT NAME MACRO CALLED
.*             &SGCNTRA(4)  - COUNT OF ESOTERIC NAMES FROM UNITNAME
.*             &SGCNTRA(5)  - COUNT OF ADDRESSES FROM UNITNAME
.*             &SGCNTRA(6)  - CONTROL IN UNITNAME
.*             &SGUNSGA     - TOTAL COUNT OF ESOTERIC AND GENERIC NAMES
.*             &SGUNVA      - COUNT OF VIO NAMES
.*             &SGQUITB     - QUIT SWITCH
.*             &SGUNVB(1-100)  - VIO BIT FOR ESOTERIC LIST
.*             &SGUNNMC(1-100) - NAMES GIVEN IN MACRO CALL
.*             &SGUNNVC(1-100) - VIO DEVICE NAMES
.*             &SGUNNMA(1-1028)- ADDRESSES ASSOCIATED WITH NAMES
.*
.*       LOCALS USED ARE -
.*             &A      - SUBLIST INDEX
.*             &N      - ARITHMETIC CALCULATION FOR NO OF CHARACTERS
.*                       AND MULTIPLE ADDRESS OPERAND
.*             &LENGTH - LENGTH OF MULTIPLE ADDRESS OPERAND
.*             &RTNSW  - SWITCH DEPICTING END OF UNIT PARAMETER
.*             &MULTAD - MULTIPLE ADDRESS SWITCH
.*             &C      - USED TO CHECK VALIDITY OF THE NAME FIELD
.*             &D      - SECOND CHARACTER OF UNIT ADDRESS
.*             &E      - THIRD CHARACTER OF UNIT ADDRESS
.*             &ADDR   - UNIT ADDRESS
.*
.* EXIT, NORMAL - N/A
.*
.* EXIT, ERROR - N/A
.*
.* EXTERNAL REFERENCES - N/A
.*
.* TABLES - N/A
.*
.* MACROS - NONE
.*
.* CHANGE ACTIVITY - MODIFIED FOR Y02036,OZ03914,OZ11135
.*                   Z40ASVN
.*                       OZ03914,OZ11135,OZ34962,OZ36647
.* @ZA75914,UZXXXXX,C/TKNH: ADD ACTR TO MACRO                  @ZA75914
.*
.****** END OF SPECIFICATIONS ***************************************
         AIF   (T'&NAME EQ 'O').E8      NAME ALWAYS REQUIRED
         AIF   (&SGMENTB(36)).VIO       FIRST ENTRY?
&SGMENTB(36)   SETB   1                 UNITNAME ENTRY SWITCH.
    MNOTE *,'     MAXIMUM OF 100 UNIT NAMES ALLOWED -- MAXIMUM UNITS'
    MNOTE *,'     ALLOWED IS 2056 MINUS THE NUMBER OF NAMES'    Z40ASVN
.**********************************************************************
.****                  GENERIC NAME WITH VIO                       ****
.**********************************************************************
.VIO     AIF (NOT ('&VIO' EQ 'YES')).ESOTERC     4 LINE CHANGE @AZ11135
         AIF (NOT ('&NAME' EQ '3350' OR '&NAME' EQ '3330-1' OR '&NAME' X
               EQ '3330' OR '&NAME' EQ '3340' OR '&NAME' EQ '2314' OR  X
               '&NAME' EQ '2305-1' OR '&NAME' EQ '2305-2')).ESOTERC
         AIF   (T'&UNIT EQ 'O').VIO1      UNIT OMITTED
    MNOTE 0,'  UNIT ADDRESS &UNIT IGNORED FOR GENERIC NAME WHEN VIO SPEX
               CIFIED'
.VIO1    AIF   (NOT (K'&NAME GT 8 OR K'&NAME LE 0)).VIO2
    MNOTE 5,'* * * IEIUNI103 NAME VALUE &NAME INVALID'
&SGQUITB SETB  1
.VIO2    AIF   (&SGUNSGA LT 100).VIO3      NAMES LIMITED TO 100
    MNOTE 5,'* * * IEIUNI101 &NAME - MAXIMUM NUMBER OF NAMES EXCEEDED'
&SGQUITB SETB  1
        AGO    .MEND                        ERROR EXIT
.VIO3   ANOP
&SGUNVA          SETA   &SGUNVA+1           INCREMENT VIO NAME COUNT
&SGUNSGA         SETA   &SGUNSGA+1          INCREMENT TOTAL NAME COUNT
&SGUNNVC(&SGUNVA)  SETC   '&NAME'           VIO UNITNAME LIST
    MNOTE *,'       &NAME IS A GENERIC NAME FOR VIO ONLY'
.VIO7    AGO   .MEND
.**********************************************************************
.****                      ESOTERIC NAME                           ****
.**********************************************************************
.ESOTERC ANOP                                                  @ZA36647
   AIF ('&NAME' EQ '3350' OR '&NAME' EQ '3330-1' OR '&NAME' EQ '3330' OX
               R '&NAME' EQ '3340' OR '&NAME' EQ '2314' OR '&NAME' EQ  X
               '2305-1' OR '&NAME' EQ '2305-2').E6             @ZA36647
  AIF ('&NAME' EQ '2400' OR '&NAME' EQ '2400-1' OR '&NAME' EQ '2400-2' X
               OR '&NAME' EQ '2400-3' OR '&NAME' EQ '2495' OR '&NAME' EX
               Q '2400-4' OR '&NAME' EQ '3400-2').E6           @ZA36647
  AIF ('&NAME' EQ '3400-3' OR '&NAME' EQ '3400-4' OR '&NAME' EQ        X
               '3400-5' OR '&NAME' EQ '3400-6' OR '&NAME' EQ '1052'    X
               OR '&NAME' EQ '1053' OR '&NAME' EQ '1403').E6   @ZA36647
  AIF ('&NAME' EQ '1419' OR '&NAME' EQ '3890' OR '&NAME' EQ '7770' OR  X
               '&NAME' EQ '3540' OR '&NAME' EQ '3800').E6      @ZA36647
  AIF ('&NAME' EQ '1443' OR '&NAME' EQ '1443' OR '&NAME' EQ '2501' OR  X
               '&NAME' EQ '2520' OR '&NAME' EQ '2540-1' OR '&NAME' EQ  X
               '2540-2' OR '&NAME' EQ '2671').E6               @ZA36647
  AIF ('&NAME' EQ '3066' OR '&NAME' EQ '3158' OR '&NAME' EQ '3210' OR  X
               '&NAME' EQ '3211' OR '&NAME' EQ '3213' OR '&NAME' EQ '32X
               15' OR '&NAME' EQ '3505' OR '&NAME' EQ '3525').E6
  AIF ('&NAME' EQ '3838' OR '&NAME' EQ '7443' OR '&NAME' EQ '3851' OR  X
               '&NAME' EQ '2250-1' OR '&NAME' EQ '2250-3' OR '&NAME'   X
               EQ '2260-1' OR '&NAME' EQ '2260-2').E6          @ZA36647
  AIF ('&NAME' EQ '2265' OR '&NAME' EQ '3036' OR '&NAME' EQ '3277-1'   X
               OR '&NAME' EQ '3284-2' OR '&NAME' EQ '1287' OR '&NAME' EX
               Q '3286-1' OR '&NAME' EQ '3286-2').E6           @ZA36647
  AIF ('&NAME' EQ '1275' OR '&NAME' EQ '3277-2' OR '&NAME' EQ '3284-1' X
               OR '&NAME' EQ '1288' OR '&NAME' EQ '2955' OR '&NAME' EQ X
               '3886' OR '&NAME' EQ '3895').E6                 @ZA36647
.ESOTERD ANOP                                                  @ZA34962
         AIF   (T'&UNIT EQ 'O').E7      UNIT REQUIRED FOR ESOTERIC NAME
         AIF   (K'&NAME GT 8 OR K'&NAME LE 0).EA  VALIDITY OF NAME
         AIF   ('&VIO' EQ 'NO').VALIDNM           NO VIO SPECIFIED
         AIF   ('&VIO' EQ 'YES').VIOYES           VIO FOR THIS NAME
    MNOTE 5,'* * * IEIUNI109 VIO VALUE &VIO INVALID MUST BE YES OR NO'
&SGQUITB SETB 1
         AGO   .VALIDNM
.VIOYES  ANOP                           VIO SPECIFIED
         AIF   (&SGCNTRA(4) EQ 0).VIOCNT    INITIAL UNITNAME CALL
         AIF   ('&SGUNNMC(&SGCNTRA(4))' EQ '&NAME').TESTVIO  REPEATED
.VIOCNT  ANOP
&SGUNVA  SETA  &SGUNVA+1                INCREMENT VIO NAME COUNT
  MNOTE   *,'       VIO SPECIFIED FOR &NAME'
         AGO   .TESTNM
.TESTVIO AIF   (&SGUNVA EQ 0).NOPVIO    NO VIO TESTED YET       OZ03914
         AIF  ('&SGUNNVC(&SGUNVA)' EQ '&NAME').ON3 BYP NAME TST OZ03914
.NOPVIO  ANOP                                                   OZ03914
  MNOTE  0,'* * * VIO SPECIFIED ON SECOND UNITNAME CALL FOR &NAME AND NX
               OT ON PREVIOUS ONE'
&SGUNVA  SETA  &SGUNVA+1                INCREMENT COUNT
&SGUNNVC(&SGUNVA)      SETC  '&NAME'    UNITNAME IN VIO NAME LIST
&SGUNVB(&SGCNTRA(4))   SETB  1          ESOTERIC NAME HAS VIO
         AGO   .ON3                     BYPASS NAME TESTING IF REPEATED
.**********************************************************************
.***    CHECK EACH CHARACTER OF NAME TO VERIFY VALIDITY   ***
.**********************************************************************
.VALIDNM AIF   (&SGCNTRA(4) EQ 0).TESTNM    INITIAL ALWAYS
         AIF   ('&SGUNNMC(&SGCNTRA(4))' NE '&NAME').TESTNM  DIFF NAME
         AIF   (&SGUNVA EQ 0).ON3      NO VIO SPECIFIED YET
.*              SAME NAME  -  CHECK IF VIO SPECIFIED ON LAST CALL
         AIF   ('&SGUNNVC(&SGUNVA)' NE '&NAME').ON3
   MNOTE  0,'* * * VIO SPECIFIED ON PREVIOUS UNITNAME CALL FOR &NAME ANX
               D NOT ON THIS ONE'
         AGO   .ON3                     BYPASS NAME CHECK IF SAME NAME
.TESTNM  AIF (&A EQ K'&NAME).DONENM     ALL CHARACTERS OF NAME CHECKED
&A SETA &A+1                            INCREMENT SUBLIST INDEX
&C SETC '&NAME'(&A,1)                   CHECK NEXT CHARACTER OF SUBLIST
 AIF (('&C' GE 'A' AND '&C' LE 'I') OR ('&C' GE 'J' AND '&C' LE 'R') OR*
               ('&C' GE 'S' AND '&C' LE 'Z') OR ('&C' GE '0' AND '&C' L*
               E '9')).TESTNM
 AIF ('&C' EQ '@' OR '&C' EQ '$' OR '&C' EQ '#' OR '&C' EQ '/' OR '&C' *
               EQ '-').TESTNM
         AGO   .EA                      NOT A VALID CHARACTER
.*
.***       STORE NAME IN NAME LIST AND UNIT ADDRESSES IN TABLE      ***
.*
.DONENM  ANOP                           VALID CHARACTERS IN NAME
&A SETA 0                               RESET SUBLIST INDEX
.ON MNOTE *,'     &NAME IS THE SYMBOLIC NAME ASSOCIATED WITH     '
    MNOTE *,'     THE DEVICES AT THE FOLLOWING ADDRESSES -      '
         AIF   (&SGCNTRA(4) EQ 0).INITIAL    FIRST TIME ONLY
         AIF   (&SGUNSGA GE 100).E1     MAXIMUM NUMBER OF NAMES
&SGCNTRA(4)    SETA &SGCNTRA(4)+1       INCREMENT NAME COUNT
&SGUNSGA    SETA    &SGUNSGA+1          INCREMENT TOTAL NAME COUNT
.***         UNIT NAME INTO LIST              ***
&SGUNNMC(&SGCNTRA(4))  SETC  '&NAME'    UNITNAME IN LIST
         AIF   ('&VIO' NE 'YES').ONG
&SGUNNVC(&SGUNVA)      SETC  '&NAME'    UNITNAME IN VIO NAME LIST
&SGUNVB(&SGCNTRA(4))   SETB  1          ESOTERIC NAME HAS VIO
.ONG     AIF   (&SGCNTRA(5) GE 1028).E2  MAX. NUMBER OF UNITS   Z40ASVN
         AIF   (&SGCNTRA(6) LT 3).FILLIN
&SGCNTRA(5)    SETA &SGCNTRA(5)+1  COUNT OF SETC'S IN USE FOR ADDRESSES
&SGCNTRA(6)    SETA 1              SET FLIP SWITCH TO ONE.
.FILLIN  ANOP              FFF MEANS END OF ONE NAME'S ADDRESSES.
&SGUNNMA(&SGCNTRA(5))  SETA 4096*&SGUNNMA(&SGCNTRA(5))+X'FFF'
&SGCNTRA(6)    SETA &SGCNTRA(6)+1
         AGO   .ON2
.INITIAL ANOP            INITIALIZATION ROUTINE.
&SGUNSGA       SETA &SGUNSGA+1     INCREMENT TOTAL NAME COUNTER
&SGCNTRA(4)    SETA 1         SET NAME COUNTER
&SGCNTRA(5)    SETA 1         SET ADDRESS COUNTER TO ONE.
&SGCNTRA(6)    SETA 1         SET FLIP MECHANISM TO ONE.
.***         UNIT NAME INTO LIST              ***
&SGUNNMC(&SGCNTRA(4))  SETC  '&NAME'   UNITNAME IN LIST
         AIF   ('&VIO' NE 'YES').ON3
&SGUNNVC(&SGUNVA)      SETC  '&NAME'   UNITNAME IN VIO NAME LIST
&SGUNVB(&SGCNTRA(4))   SETB  1         ESOTERIC NAME HAS VIO
.*
.***              UNIT ADDRESSES INTO TABLE              ***
.*
.ON3     AIF   (&RTNSW).LOOP           IF ON, GENERATE MULTIPLE ADDRESS
         AIF   (N'&UNIT EQ 0).E7       UNIT REQUIRED FOR ESOTERIC NAME
         AIF   (&A EQ N'&UNIT).MEND    ALL UNITS IN ADDRESS TABLE
&A       SETA  &A+1                    INCREMENT SUBLIST INDEX
         AIF   (K'&UNIT(&A) LT 3).E3   3-CHARACTER ADDRESS REQUIRED
         AIF   (K'&UNIT(&A) GT 3).MULT IF GT, CHECK FOR MULT ADDRESS
&C       SETC  '&UNIT(&A)'(1,1)        SINGLE UNIT ADDRESS-1ST CHAR
&D       SETC  '&UNIT(&A)'(2,1)        2ND CHARACTER
&E       SETC  '&UNIT(&A)'(3,1)        3RD CHARACTER
         AGO   .AIF                    BYPASS MULTIPLE ADDRESS SETUP
.MULT    ANOP                          MULTIPLE ADDRESS
&MULTAD  SETB  1                       MULTIPLE ADDRESS SWITCH
&LENGTH  SETA  K'&UNIT(&A)             LENGTH OF MULT ADDRESS OPERAND
         AIF   (&LENGTH LE 6).E4       MINIMUM OF 7 FOR MULTIPLE ADDR
         AIF   ('&UNIT(&A)'(5,1) NE ',').E4  IMPROPER UNIT ADDRESS
&LENGTH  SETA  &LENGTH-6               NO. CHAR - MULTIPLE ADDR OPERAND
         AIF   (&LENGTH GT 3).E5       LIMIT OF 3
&N       SETA  &LENGTH                 USE TO CHECK VALIDITY
.TEST    AIF   (&N EQ 0).VALID         ALL INCREMENTAL DIGITS CHECKED
&C       SETC  '&UNIT(&A)'(&N+5,1)     CHECK VALIDITY OF INCREMENTAL
         AIF   (NOT('&C' GE '0' AND '&C' LE '9')).E5  NOT A DIGIT
&N       SETA  &N-1                    DECREMENT DIGIT COUNT
         AGO   .TEST                   CHECK NEXT DIGIT
.VALID   ANOP                          ALL INCREMENTAL DIGITS CHECKED
&RTNSW   SETB  1                       ON WHILE UNITS BEING STORED
&C       SETC  '&UNIT(&A)'(6,&LENGTH)  CHARACTERS IN OPERAND
&N       SETA  &C                      ARITMETIC VALUE OF OPERAND
&ADDR    SETC  '&UNIT(&A)'(2,3)        ADDRESS TO BE INCREMENTED
.LOOP    ANOP
&C       SETC  '&ADDR'(1,1)            1ST CHAR OF MULTIPLE ADDRESS
&D       SETC  '&ADDR'(2,1)            2ND CHARACTER
&E       SETC  '&ADDR'(3,1)            3RD CHARACTER
.AIF     ANOP
 AIF (NOT(('&C' GE '0' AND '&C' LE '9')OR('&C' GE 'A' AND '&C' LE 'F'))*
               ).E3                    CHECK 1ST CHARACTER FOR VALIDITY
 AIF (NOT(('&D' GE '0' AND '&D' LE '9')OR('&D' GE 'A' AND '&D' LE 'F'))*
               ).E3                    VALIDITY OF 2ND CHARACTER
 AIF (NOT(('&E' GE '0' AND '&E' LE '9')OR('&E' GE 'A' AND '&E' LE 'F'))*
               ).E3                    VALIDITY OF 3RD CHARACTER
         AIF   (&MULTAD).CONV          MULTIPLE ADDRESS
         CONVERT TO=HEX,DIGITS=3,VALUE=&UNIT(&A)  DECIMAL VALUE OF UNIT
 AIF     (&HEX EQ X'FFF').E3           NO VALID FFF ADDRESS
         AGO   .MAX
.CONV    CONVERT    TO=HEX,DIGITS=3,VALUE=&ADDR   DECIMAL VALUE OF UNIT
         AIF   (&HEX EQ X'FFF').E4     NO VALID FFF ADDRESS
.MAX     AIF   (&SGCNTRA(5) EQ 1028 AND &SGCNTRA(6) NE 1).E2 MAX UNITS *
                                                                Z40ASVN
.*
.***           CALCULATE VALUE FOR UNIT ADDRESS IN TABLE          ***
.*
&SGUNNMA(&SGCNTRA(5)) SETA 4096*&SGUNNMA(&SGCNTRA(5))+&HEX    DECIMAL
         AIF   (&MULTAD).AD
    MNOTE *,'        &UNIT(&A)'        MESSAGE FOR SINGLE ADDRESS
         AGO   .INC
.AD MNOTE *,'        &ADDR'            MULTIPLE ADDRESS MESSAGE
&N       SETA  &N-1                    DECREMENT MULTIPLE ADDR OPERAND
         AIF   (&N NE 0).SET           ALL UNITS NOT IN TABLE
&RTNSW   SETB  0                       ALL UNITS IN THE TABLE
&MULTAD  SETB  0                       RESET MULTIPLE ADDRESS SWITCH
         AGO   .INC
.SET     ANOP                          CONTINUE UNIT CHECK
&HEX     SETA  &HEX+1                  INCREMENT ADDRESS TO NEXT UNIT
         CONVERT   TO=EBCDIC,VALUE=&HEX,DIGITS=3    CHANGE TO HEX
&ADDR    SETC  '&EBCDIC'                HEX VALUE IN GLOBAL
.INC     ANOP
&SGCNTRA(6)    SETA &SGCNTRA(6)+1       INCREMENT CONTROL COUNT
.***        RESET CONTROL AND INCREMENT INDEX OF ADDRESS TABLE      ***
.***              IF TWO ADDRESSES HAVE BEEN STORED                 ***
.ON2     AIF   (&SGCNTRA(6) LT 3).ON3   ONLY ONE ADDRESS STORED
&SGCNTRA(5)    SETA &SGCNTRA(5)+1       TWO ADDRESSES STORED
&SGCNTRA(6)    SETA 1                   RESET CONTROL
         AGO   .ON3                    CONTINUE STORE OF UNIT ADDRESSES
.*
.**********************************************************************
.*                 ERROR MESSAGES
.**********************************************************************
.EA MNOTE 5,'* * * IEIUNI100 NAME VALUE &NAME INVALID   '
&SGQUITB SETB  1
         AGO   .DONENM                   BYPASS NAME CHECK
.E1 MNOTE 5,'* * * IEIUNI101 &NAME-MAXIMUM NUMBER OF NAMES EXCEEDED'
&SGQUITB SETB  1
         AGO   .ON3
.E2 MNOTE 5,'* * * IEIUNI102 &NAME-EXCESSIVE ADDRESS VALUES'
         AGO   .BEND
.E3      AIF   (&MULTAD).ER3             MULTIPLE ADDRESS
&ADDR    SETC  '&UNIT(&A)'               SINGLE ADDRESS
.ER3  MNOTE 5,'* * * IEIUNI103 &NAME-UNIT VALUE &ADDR INVALID'
.ER3A    ANOP
&SGQUITB SETB  1
&RTNSW   SETB  0                        RESET AND PREVENT LOOP
&MULTAD  SETB  0                        RESET
         AGO   .ON3
.E4 MNOTE 5,'* * * IEIUNI104 &NAME-MULTIPLE ADDRESS VALUE INVALID'
         AGO   .ER3A
.E5 MNOTE 5,'* * * IEIUNI105 &NAME-MULTIPLE ADDRESS INCREMENT INVALID'
         AGO   .ER3A
.E6 MNOTE 5,'* * * IEIUNI106 &NAME-DEVICE TYPE ESOTERIC NAME INVALID'
&SGQUITB SETB  1                                               @ZA34962
         AGO   .ESOTERD                                        @ZA34962
.E7 MNOTE 5,'* * * IEIUNI107 &NAME HAS NO ASSIGNED UNITS.'
         AGO   .BEND
.E8 MNOTE 5,'* * * IEIUNI108 UNIT AND/OR NAME PARAMETERS ARE REQUIRED'
.BEND    ANOP
&SGQUITB SETB  1
.MEND  MEND
