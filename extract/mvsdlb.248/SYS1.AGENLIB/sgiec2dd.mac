         MACRO
&LABEL   SGIEC2DD &DATA,&LB2
         COPY  SGGBLPAK
         LCLC  &C,&L                   WORK VARIABLE,LABEL
         LCLA  &X,&Y,&Z                WORK VARIABLES
.***** START OF SPECIFICATIONS **************************************
.*
.* MODULE NAME - SGIEC2DD
.*
.* DESCRIPTIVE NAME - SCAN AND PUNCH FOR ASSEMBLER DATA
.*
.* COPYRIGHT - NONE
.*
.* STATUS - CHANGE LEVEL 00
.*
.* FUNCTION -
.*   THIS MACRO PUNCHES DC'S FOR ASSEMBLER DATA PASSED TO IT. THE
.*   DATA IT PUNCHES IS DETERMINED FROM THE DATA WHICH IS INPUT TO
.*   TO THIS MACRO. THE INPUT DATA IS SCANNED AND BROKEN UP AT
.*   COMMAS SO AS TO ALLOW VALID ASSEMBLER DC'S TO BE GENERATED.
.*   THE MACRO LABEL ON THE INVOCATION OF THIS MACRO IS PUNCHED AT THE
.*   START. IN ADDITION, ANOTHER LABEL MAY BE SPECIFIED WHICH ALSO
.*   WILL BE EQUATED TO THE START. IF A SECOND LABEL IS GIVEN
.*   BUT NO FIRST LABEL, THE SECOND LABEL WILL BE USED.
.*
.*   THIS MACRO IS INVOKED BY SGIEC202.
.*
.* NOTES - N/A
.*
.* MODULE TYPE - MACRO
.*
.* ENTRY POINT - N/A
.*
.* INPUT -
.*         THE FOLLOWING POSITIONAL PARAMETERS ARE PASSED -
.*           &DATA - CHARACTER STRING TO BE SCANNED AND PUNCHED.
.*                   NO SINGLE ITEM MAY BE LONGER THAN 56 CHARACTERS,
.*                   I.E. A COMMA MUST APPEAR AT LEAST AFTER EACH
.*                   56 CHARACTERS; IF NOT, THE REMAINDER OF THE
.*                   DATA IS IGNORED, AN MNOTE ISSUED, AND THE
.*                   QUIT SWITCH IS SET.
.*           &LB2  - SECOND LABEL TO BE GENERATED
.*
.*
.* OUTPUT -
.*       OUTPUT CONSISTS OF PUNCH STATEMENTS FOR THE DATA AND LABELS.
.*       IF NO DATA IS GIVEN (I.E. 1ST POSITIONAL PARAMETER IS NULL),
.*       THERE IS NO OUTPUT.
.*
.*
.*       GLOBALS USED ARE -
.*          &SGQUITB - TERMINATE SYSGEN IF ERROR IN DATA STRING
.*
.*       LOCALS USED ARE -
.*          &C - WORK VARIABLE. USED TO OBTAIN CHARACTER STRING
.*               DATA FOR CURRENT PUNCH.
.*          &L - VALUE OF LABEL FOR FIRST PUNCH STATEMENT.
.*          &X - CURRENT POSITION WITHIN INPUT DATA.
.*          &Y - LENGTH FOR CURRENT PUNCH. THIS VALUE IS
.*               DECREMENTED AS PART OF SCAN BACKWARDS FOR A COMMA.
.*
.*
.*
.* EXIT, NORMAL - N/A
.*
.* EXIT, ERROR - N/A
.*
.* EXTERNAL REFERENCES - N/A
.*
.* TABLES - N/A
.*
.* MACROS - N/A
.*
.* CHANGE ACTIVITY - N/A
.*A-000000-999999                                              @G64HPLP
.***** END OF SPECIFICATIONS ****************************************
         AIF   (K'&DATA EQ 0).MEND     IGNORE IF NO DATA
&X       SETA  1                       STARTING COLUMN
&L       SETC  '&LABEL'.'        '     GET MACRO LABEL
         AIF   (K'&LABEL NE 0).LABT    ANY MACRO LABEL ?
&L       SETC  '&LB2'.'        '       NO--USE SECOND LABEL
.LABT    ANOP
&L       SETC  '&L'(1,8)               GET 8 CHARACTERS FOR LABEL
.DDLP    ANOP
.* COMPUTE DEFAULT LENGTH AS FOLLOWS :
.*   71-(LENGTH OF LABEL+2 FOR DC+4 BLANKS)=65-LENGTH OF LABEL
&Y       SETA  65-K'&L                 DEFAULT DATA LENGTH
         AIF   (&X GT K'&DATA).LB2     START COL. WITHIN DATA ?
         AIF   (&X+&Y-1 LT K'&DATA).FCOMMA YES-COMMA SCAN NEEDED ?
&Y       SETA  K'&DATA-&X+2            NO--GET REMAINING LENGTH+1
         AGO   .PUNCH                  PUNCH REMAINING DATA
.FCOMMA  ANOP                          SCAN BACKWARDS FOR COMMA
         AIF   ('&DATA'(&X+&Y-1,1) EQ  ',').PUNCH FOUND COMMA ?
&Y       SETA  &Y-1                    NO-MOVE BACKWARDS
         AIF   (&Y GT 1).FCOMMA        OK TO CONTINUE
         MNOTE 5,'SGIEC2DD--INVALID DATA FORMAT. LABEL=&LABEL, LB2=&LB2X
               , DATA='
         MNOTE 0,'&DATA'
&SGQUITB SETB  1                       SET QUIT SWITCH
         MEXIT                         ERROR-NO COMMA WITHIN RANGE
.PUNCH   ANOP
&C       SETC  '&DATA'(&X,&Y-1)        GET DATA
         PUNCH '&L DC   &C'            PUNCH DATA
&X       SETA  &X+&Y                   POINT TO NEW START COL.
&L       SETC  '        '              SET LABEL=8 BLANKS
         AGO   .DDLP                   CONTINUE SCAN OF CARD
.LB2     AIF   (K'&LB2 EQ 0 OR K'&LABEL EQ 0).MEND NEED 2ND LABEL?
         PUNCH '&LB2     EQU   &LABEL'
.MEND    MEND
