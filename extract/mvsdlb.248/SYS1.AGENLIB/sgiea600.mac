          MACRO
          SGIEA600
.*  LAST UPDATE = 04/05/74                                       Y02952
.*              = 05/08/75                                     @Z40WPSF
.*              =09/21/76    FIX FOR ONE SWAP DATSET REQUEST   @ZA14211
.* CHANGE = YM08616,YM04655,M02635
.*
          COPY  SGGBLPAK
          LCLA  &I,&N,&M,&J,&X,&Y,&A                           @Z40WPSM
          LCLC  &D(2),&C(10),&H(18),&F,&P(25)                    Y02952
          LCLC  &B,&Z(10),&W,&V,&SGRES,&BLANK                    Y02952
          LCLC  &MASKCNT,&CONT,&PAREN                            Y02952
          LCLC  &S(10),&MORE,&MASK                             @Z40WPSF
&SGCTRLC(6) SETC 'PARMLIB'
&SGCTRLC(7) SETC '&SGDSUNC(18)'
&SGCTRLC(9) SETC '&SGDSVLC(18)'
          COPY  SGUPDPAK
&BLANK    SETC  ''                                               Y02952
          PUNCH './   ADD  NAME=IEASYS00,LIST=ALL'
          PUNCH './   NUMBER  NEW1=01,INCR=05'
          AIF   (&SGSUPRA(3) EQ 0).NOREAL                        Y02952
          PUNCH ' REAL=&SGSUPRA(3),'                             Y02952
.NOREAL   ANOP                                                   Y02952
          AIF   (&SGSUPRA(12) EQ 0).NOCSA                        Y02952
          PUNCH ' CSA=&SGSUPRA(12),'                             Y02952
.NOCSA    ANOP                                                   Y02952
          AIF   (&SGSUPRA(11) EQ 0).NOVRR                        Y02952
          PUNCH ' VRREGN=&SGSUPRA(11),'                          Y02952
.NOVRR    ANOP                                                   Y02952
          AIF   (&SGCPRGA(6) EQ 0).NOSQA                         Y02952
          PUNCH ' SQA=&SGCPRGA(6),'                              Y02952
.NOSQA    ANOP                                                   Y02952
          AIF   ('&SGCPRGC(1)' EQ '&BLANK').NOAPF                Y02952
          PUNCH ' APF=00,'                                       Y02952
.NOAPF    ANOP                                                   Y02952
          PUNCH ' PAGNUM=(&SGSUPRA(5),&SGSUPRA(6)),'           @Z40WPSF
          AIF   ('&SGDUPXC' EQ '&BLANK').NODUPLX               @Z40WPSF
          PUNCH ' DUPLEX=&SGDUPXC,'                            @Z40WPSM
.NODUPLX ANOP                                                  @Z40WPSF
.**************************************************************@Z40WPSF
.*                                                             @Z40WPSF
.*    SET UP SWAP PARAMETERS                                   @Z40WPSF
.*                                                             @Z40WPSF
.**************************************************************@Z40WPSF
          AIF   ('&SGSWPDC(1)' EQ '&BLANK').NOSWAP             @Z40WPSF
&N        SETA  1                                              @Z40WPSF
.MORE     ANOP                                                 @Z40WPSF
&S(&N)    SETC  '&SGSWPDC(&N)'                                 @Z40WPSM
&N        SETA  &N+1                                           @Z40WPSF
          AIF   (&N GT 10).NOMORE                              @Z40WPSF
          AIF   ('&SGSWPDC(&N)' NE '&BLANK').MORE              @Z40WPSF
.NOMORE   ANOP                                                 @Z40WPSF
.* ALL DATASET NAME HAS A MINIMUM OF 1 AND A MAXIMUM OF 44     @Z40WPSF
.* CHARACTERS.  &MORE AND &MASK WILL HAVE TO BE RECALCULATED   @Z40WPSF
.* IF THE ABOVE LIMITS WERE CHANGED.                           @Z40WPSF
&MASK     SETC  '                                           X' @Z40WPSF
&A        SETA  1                                              @Z40WPSF
          AIF   (&A EQ &N-1).ONESWAP  IS THIS ONE SWAP REQUEST @ZA14211
&MORE     SETC  '&MASK'(K'&S(1),45-K'&S(1))                    @Z40WPSF
          PUNCH ' SWAP=(&S(1),                   &MORE'        @Z40WPSF
.MOREPCH  ANOP                                                 @Z40WPSF
&A        SETA  &A+1                                           @Z40WPSF
          AIF   (&A EQ &N-1).LASTSWP                           @Z40WPSF
&MORE     SETC  '&MASK'(K'&S(&A),45-K'&S(&A))                  @Z40WPSF
          PUNCH '               &S(&A),           &MORE'       @Z40WPSF
          AGO   .MOREPCH                                       @Z40WPSF
.ONESWAP  PUNCH ' SWAP=&S(1),'      ONE SWAP DATASET REQUEST   @ZA14211
          AGO   .NOSWAP             SWAP DATASET PROC COMPLETE @ZA14211
.LASTSWP   ANOP                                                @Z40WPSF
          PUNCH '               &S(&A)),'                      @Z40WPSF
.NOSWAP    ANOP                                                @Z40WPSF
          AIF   (NOT &SGSUPRB(20)).NOF
&F        SETC  'F'
.NOF      ANOP
          PUNCH ' BLDL&F.=00,'                                   Y02952
.*********
          AIF   ('&SGRES1C(6)' NE '&BLANK').YESFIX   LINKLIB     Y02952
.*                                  MODULES TO BE RESIDENT?      Y02952
          AIF   ('&SGRES1C(9)' NE '&BLANK').YESFIX   SVCLIB      Y02952
.*                                  MODLUES TO BE RESIDENT?      Y02952
          AIF   ('&SGRES1C(12)' NE '&BLANK').YESFIX  LPALIB      Y02952
.*                                  MODULES TO BE RESIDENT?      Y02952
          AGO   .HARDCPY            NO USER MODULES TO BE FIXED  Y02952
.YESFIX   PUNCH ' FIX=01,'                                       Y02952
.*********
.*  SET UP HARDCOPY PARAMETER                                    Y02952
.**********                                                      Y02952
.HARDCPY  ANOP
          AIF   (&SGSCHDB(36)).SYSLOG
&H(1)     SETC  '&SGSCHDC(14)'
          AGO   .HARDCP2
.SYSLOG   ANOP
&H(1)     SETC  'SYSLOG'
.HARDCP2  ANOP
&SGSCHHB(1) SETB 1
&SGSCHHB(2) SETB 1
          AIF   (&SGSCHHB(1) AND &SGSCHHB(2) AND &SGSCHHB(3) AND &SGSCH*
               HB(4) AND &SGSCHHB(5) AND &SGSCHHB(6) AND &SGSCHHB(7) AN*
               D &SGSCHHB(8)).ALL1
          AGO   .D1
.ALL1     AIF   (&SGSCHHB(9) AND &SGSCHHB(10) AND &SGSCHHB(11) AND &SGS*
               CHHB(12) AND &SGSCHHB(13) AND &SGSCHHB(14) AND &SGSCHHB(*
               15) AND &SGSCHHB(16)).ALL
.D1       ANOP
&D(1)     SETC  '('
&D(2)     SETC  ')'
.*********
.NEXTI    ANOP
&I        SETA  &I+1
          AIF   (&I GT 16).ROUTDUN
          AIF   (NOT &SGSCHHB(&I)).NEXTI
&H(&I+1)  SETC  '&I.,'
&M        SETA  &I
          AGO   .NEXTI
.*********
.ROUTDUN  ANOP
&H(&M+1)  SETC  '&M'
&I        SETA  1
          AGO   .HARDCP3
.ALL      ANOP
&H(2)     SETC  'ALL'
.HARDCP3  ANOP
          AIF   (&SGSCHDB(37) OR &SGSCHDB(42) OR &SGSCHDB(43)).INDIVID
&H(18)    SETC  'NOCMDS'
          AGO   .PUNCHRD
.INDIVID  AIF   (NOT &SGSCHDB(37)).INCMDS
&H(18)    SETC  'CMDS'
          AGO   .PUNCHRD
.INCMDS   AIF   (NOT &SGSCHDB(42)).STCMDS
&H(18)    SETC  'INCMDS'
          AGO   .PUNCHRD
.STCMDS   ANOP
&H(18)    SETC  'STCMDS'
.PUNCHRD PUNCH ' HARDCPY=(&H(1).,&D(1)&H(2)&H(3)&H(4)&H(5)&H(6)&H(7)&H(*
               8)&H(9)&H(10)&H(11)&H(12)&H(13)&H(14)&H(15)&H(16)&H(17)&*
               D(2).,&H(18).),'
.********************************************************************
.*       SET UP PAGE PARAMETERS
.********************************************************************
.PAGE     ANOP
&P(1)     SETC  '&SGPAGDC(1)'      1ST PAGING DATA SET          YM08616
&P(2)     SETC  '&SGPAGDC(2)'      2ND PAGING DATA SET          YM08616
&P(3)     SETC  '&SGPAGDC(3)'      3RD PAGING DATA SET         @Z40WPSM
&N        SETA  4                                              @Z40WPSF
.CHECKPG  ANOP                                                   Y02952
          AIF   ('&SGPAGDC(&N)' EQ '&BLANK').LASTPGE   ANOTHER   Y02952
.*                                   PAGING DATA SET SPECIFIED?  Y02952
&P(&N)    SETC  '&SGPAGDC(&N)'                                  YM08616
&N        SETA  &N+1                                             Y02952
          AIF   (&N LE 25).CHECKPG   UP TO 25 PAGING DATA SETS   Y02952
.*                                   MAY BE SPECIFIED            Y02952
.LASTPGE  ANOP                                                   Y02952
&MASKCNT  SETC  '                                           X'  YM08616
&J        SETA  1                                                Y02952
&CONT     SETC  '&MASKCNT'(K'&P(1),45-K'&P(1))     SET UP       YM08616
.*                                   CONTINUTATION CHARACTER     Y02952
.*                                   BASED UPON LENGTH OF FIRST  Y02952
.*                                   DATA SET NAME               Y02952
          PUNCH ' PAGE=(&P(1),                   &CONT'   PUNCH  Y02952
.*                                   FIRST DATA SET NAME         Y02952
.NEXTPNC  ANOP                                                   Y02952
&J        SETA  &J+1                                             Y02952
          AIF   (&J EQ &N-1).OUTPGE  BRANCH IF ON LAST NAME      Y02952
&CONT     SETC  '&MASKCNT'(K'&P(&J),45-K'&P(&J))  SET UP        YM08616
.*                                   CONTINUATION CHARACTER      Y02952
.*                                   BASED UPON LENGTH OF        Y02952
.*                                   DATA SET NAME               Y02952
          PUNCH '               &P(&J),           &CONT'  PUNCH  Y02952
.*                                   PAGING DATA SET NAME        Y02952
          AGO   .NEXTPNC                                         Y02952
.OUTPGE   ANOP                                                   Y02952
&PAREN    SETC  ')'                                              Y02952
          PUNCH '               &P(&J)&PAREN'  PUNCH LAST NAME   Y02952
          PUNCH './   ENDUP'
          PUNCH '/*'
.*************************************************************** Y02952
.*          SET UP FIX LIST                                      Y02952
.*************************************************************** Y02952
          AIF   (('&SGRES1C(6)' EQ '&BLANK') AND ('&SGRES1C(9)' EQ '&BL*
               ANK') AND ('&SGRES1C(12)' EQ '&BLANK')).NIXFIX    Y02952
&SGCTRLC(6) SETC 'PARMLIB'                                       Y02952
&SGCTRLC(7) SETC '&SGDSUNC(18)'                                  Y02952
&SGCTRLC(9) SETC '&SGDSVLC(18)'                                  Y02952
          COPY  SGUPDPAK                                         Y02952
          PUNCH './   ADD NAME=IEAFIX01,LIST=ALL'                Y02952
          PUNCH './   NUMBER NEW1=01,INCR=05'                    Y02952
          AIF   ('&SGRES1C(6)' EQ '&BLANK').NOLINK   BRANCH IF   Y02952
.*                                   NO LINKLIB MODULES TO FIX   Y02952
&X        SETA  6                    SET INDEX FOR LINKLIB       Y02952
.VALUES   ANOP                       PICK UP NAMES OF MODULES    Y02952
.*                                   TO BE MADE RESIDENT         Y02952
&Z(1)     SETC  '&SGRES1C(&X)'                                   Y02952
&Z(2)     SETC  '&SGRES2C(&X)'                                   Y02952
&Z(3)     SETC  '&SGRES3C(&X)'                                   Y02952
&Z(4)     SETC  '&SGRES4C(&X)'                                   Y02952
&Z(5)     SETC  '&SGRES5C(&X)'                                   Y02952
&Z(6)     SETC  '&SGRES6C(&X)'                                   Y02952
&Z(7)     SETC  '&SGRES7C(&X)'                                   Y02952
&Z(8)     SETC  '&SGRES8C(&X)'                                   Y02952
&Z(9)     SETC  '&SGRES9C(&X)'                                   Y02952
&Z(10)    SETC  '&SGRESAC(&X)'                                   Y02952
&I        SETA  1                                                Y02952
.COMMA    ANOP                       SET UP COMMAS               Y02952
&C(&I)    SETC  ','                                              Y02952
&I        SETA  &I+1                                             Y02952
          AIF   (&I LE 10).COMMA                                 Y02952
&I        SETA  1                                                Y02952
.TEST     AIF   ('&Z(&I)' EQ '&BLANK').BLANKIT   CHECK FOR LAST  Y02952
.*                                   MODULE NAME IN LIST         Y02952
&I        SETA  &I+1                                             Y02952
          AIF   (&I GT 10).BLANKIT   MAXIMUM OF 10 NAMES         Y02952
          AGO   .TEST                                            Y02952
.BLANKIT  ANOP                                                   Y02952
&Y        SETA  &I                   'Y' HOLDS NIMBER OF MODULE  Y02952
.*                                   NAMES PLUS 1                Y02952
          AIF   (&X EQ 9).LASTONE    WORKING ON SVCLIB?          Y02952
          AIF   (&X EQ 6).LPANEXT    WORKING ON LINKLIB?         Y02952
&N        SETA  9                    SET UP TO CHECK FOR ANY     Y02952
.*                                   SVCLIB VALUES               Y02952
          AGO   .NOCOMMA                                         Y02952
.LPANEXT  ANOP                                                   Y02952
&N        SETA  12                   SET UP TO CHECK FOR ANY     Y02952
.*                                   LPALIB VALUES               Y02952
.NOCOMMA  ANOP                                                   Y02952
          AIF   ('&SGRES1C(&N)' NE '&BLANK').LEAVEIT    LEAVE    Y02952
.*                                   COMMA AFTER LAST MODULE     Y02952
.*                                   NAME IF THERE ARE MODULES   Y02952
.*                                   FROM OTHER LIBRAIRES TO     Y02952
.*                                   FOLLOW                      Y02952
.LASTONE  ANOP                                                   Y02952
&I        SETA  &I-1                                             Y02952
.LEAVEIT  ANOP                                                   Y02952
&C(&I)    SETC  ''                   BLANK OUT EXCESS COMMAS     Y02952
&I        SETA  &I+1                                             Y02952
          AIF   (&I LE 10).LEAVEIT   MAXIMUM OF TEN COMMAS       Y02952
          AIF   (&X EQ 12).PNCHLPA   WORKING ON LPA MODULES?     Y02952
          AIF   (&X EQ 9).PNCHSVC    WORKING ON SVCLIB MODULES?  Y02952
 PUNCH ' SYS1.LINKLIB &Z(1)&C(1)&Z(2)&C(2)&Z(3)&C(3)&Z(4)&C(4)&Z(5)&C(5*
               )'                                                Y02952
          AIF   (&Y LE 6).NOLINK                                 Y02952
 PUNCH ' &Z(6)&C(6)&Z(7)&C(7)&Z(8)&C(8)&Z(9)&C(9)&Z(10)&C(10)'   Y02952
.NOLINK   ANOP                                                   Y02952
          AIF   ('&SGRES1C(12)' EQ '&BLANK').SVCLB               Y02952
&X        SETA  12                   SET INDEX FOR LPALIB        Y02952
          AGO   .VALUES              GO PICK UP LPALIB MODULES   Y02952
.PNCHLPA  ANOP                                                   Y02952
 PUNCH ' SYS1.LPALIB &Z(1)&C(1)&Z(2)&C(2)&Z(3)&C(3)&Z(4)&C(4)&Z(5)&C(5)*
               '                                                 Y02952
          AIF   (&Y LE 6).SVCLB                                  Y02952
 PUNCH ' &Z(6)&C(6)&Z(7)&C(7)&Z(8)&C(8)&Z(9)&C(9)&Z(10)&C(10)'   Y02952
.SVCLB    ANOP                                                   Y02952
          AIF   ('&SGRES1C(9)' EQ '&BLANK').FINITO               Y02952
&X        SETA  9                    SET INDEX FOR SVCLIB        Y02952
          AGO   .VALUES              GO PICK UP SVCLIB MODULES   Y02952
.PNCHSVC  ANOP                                                   Y02952
 PUNCH ' SYS1.SVCLIB &Z(1)&C(1)&Z(2)&C(2)&Z(3)&C(3)&Z(4)&C(4)&Z(5)&C(5)*
               '                                                 Y02952
          AIF   (&Y LE 6).FINITO                                 Y02952
 PUNCH ' &Z(6)&C(6)&Z(7)&C(7)&Z(8)&C(8)&Z(9)&C(9)&Z(10)&C(10)'   Y02952
.FINITO   PUNCH './    ENDUP'                                    Y02952
          PUNCH '/*'                                             Y02952
.NIXFIX   ANOP                                                   Y02952
.******************************************************          Y02952
.*  SET UP APF LIST                                              Y02952
.******************************************************          Y02952
          AIF  ('&SGCPRGC(1)' EQ '&BLANK').NIXAPF                Y02952
&SGCTRLC(6) SETC  'PARMLIB'                                      Y02952
&SGCTRLC(7) SETC  '&SGDSUNC(18)'                                 Y02952
&SGCTRLC(9) SETC  '&SGDSVLC(18)'                                 Y02952
          COPY  SGUPDPAK                                         Y02952
          PUNCH './   ADD NAME=IEAAPF00,LIST=ALL'                Y02952
          PUNCH './   NUMBER NEW1=01,INCR=05'                    Y02952
&B        SETC  ' '                                              Y02952
&N        SETA  1                                                Y02952
&C(1)     SETC  ','                                              Y02952
.NEXTAPF  AIF   ('&SGCPRGC(&N)' EQ '&BLANK').FINAPF  ANOTHER     Y02952
.*                                   LIBRARY NAME?               Y02952
&W        SETC  '&SGCPRGC(&N)'       PICK UP LIBRARY NAME        Y02952
&V        SETC  '&SGCPRAC(&N)'       PICK UP VOLID               Y02952
&N        SETA  &N+1                 INDEX TO NEXT ENTRY         Y02952
          AIF   (&N GT 16).OUTAPF    FOUND 16 ENTRIES            M02635
          AIF   ('&SGCPRGC(&N)' NE '&BLANK').PUNAPF   IF ANOTHER Y02952
.*                                   NAME TO FOLLOW,LEAVE COMMA  Y02952
.*                                   AFTER THIS ENTRY            Y02952
.OUTAPF   ANOP                                                   Y02952
&C(1)     SETC  ''                   BLANK OUT COMMA             Y02952
.PUNAPF   PUNCH ' &W&B&V&C(1)'       PUNCH ENTRY                 Y02952
          AIF   (&N LE 16).NEXTAPF   MAXIMUN OF 16 ENTRIES       M02635
.FINAPF   PUNCH './   ENDUP'                                     Y02952
          PUNCH '/*'                                             Y02952
.NIXAPF   ANOP                                                   Y02952
          MEND
