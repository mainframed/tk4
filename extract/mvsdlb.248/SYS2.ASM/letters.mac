*          DATA SET CBT1350    AT LEVEL 018 AS OF 10/13/82
***********************************************************************
***********************************************************************
**                                                                   **
**    MODULE NAME:     CBT1350                                       **
**    ALIAS NAMES:     LETTERS, LTR                                  **
**    LINK ATTRIBUTES: RENT, REUS, TEST                              **
**    INITIAL RELEASE: 10-8-82                                       **
**    DESCRIPTION:     CHANGES CRT SCREEN SIZES WITHOUT REQUIRING    **
**                     ENTERING THE RESET + CLEAR KEYS               **
**                                                                   **
***********************************************************************
***********************************************************************
**         *******KEYWORD PARAMETERS*******                          **
** AT LEASE 1 KEYWORD PARAMETER IS REQUIRED, MORE THAN 1 ALLOWED:    **
**  M2, BIG, OR B - REQUESTING MODEL 2 SCREEN (24 * 80) - BIG LETTERS**
**  M3            - REQUESTING MODEL 3 SCREEN (32 * 80)              **
**  M4            - REQUESTING MODEL 4 SCREEN (43 * 80)              **
**  M5,SMALL,OR S - REQUESTING MODEL 5 SCREEN (27 *132)-SMALL LETTERS**
**  LIST OR L     - DISPLAY THE CURRENT SCREEN SIZE                  **
**  RETC OR RC    - GIVE A RETURN CODE (R15) BASED ON MODEL TYPE:    **
**                  RC = 0002 FOR MODEL 2                            **
**                  RC = 0003 FOR MODEL 3                            **
**                  RC = 0004 FOR MODEL 4                            **
**                  RC = 0005 FOR MODEL 5                            **
**                  RC = 0000 EXECUTION SUCCESSFUL + NOT ABOVE MODELS**
**                                                                   **
***NOTE - IF RC PARM IS NOT ENTERED, THE RETURN CODE WILL BE         **
**           RC = 0000 FOR SUCCESSFUL EXECUTION OF COMMAND           **
**           RC = 0012 FOR UNSUCCESSFUL EXECUTION OF COMMAND         **
***NOTE - ANY SCREEN CHANGE REQUESTED IS EXECUTED, EVEN IF THE CRT   **
**        YOU ARE LOGGED ON TO DOESN'T SUPPORT THAT SIZE SCREEN. YOU **
**        WOULD THEN HAVE TO TRY TO RESET THE SCREEN OR GET CANCELED **
***********************************************************************
***********************************************************************
**         *******COMMAND HISTORY*********                           **
**                                                                   **
** 10/08/82 - INITIAL RELEASE              (LAUREEN BEAUCHAINE) CBT  **
**                                                                   **
***********************************************************************
***********************************************************************
         EJECT
CBT1350  CSECT
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3                  PDL - TO CHECK PCE'S
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11                 BASE REG
R12      EQU   12                 2ND BASE REG IF REQUIRED
R13      EQU   13                 GETMAIN AREA
R14      EQU   14                 USED FOR BRANCHING
R15      EQU   15
***********************************************************************
* INITIALIZATION SECTION
***********************************************************************
         USING *,R15
         BC    15,PROLOG
         DC    CL8'CBT1350'       MODULE NAME
         DC    CL8'&SYSDATE'      DATE MODULE COMPILED
         DC    CL8'&SYSTIME'      TIME MODULE COMPILED
         DROP  15
PROLOG   EQU   *
         STM   R14,R12,12(R13)    SAVE REGS
         LR    R11,R15            SET UP BASE REG
         USING CBT1350,R11
         GETMAIN R,LV=LDYNAMIC    LEN OF DYNAMIC AREA
         LR    R10,R1
         ST    R13,4(R10)         SAVE HSA IN LSA
         ST    R10,8(R13)         SAVE LSA IN HSA
         L     R1,24(R13)         RESTORE REG ONE
         LR    R13,R10            SET UP NEW SAVE AREA
         USING DYNMAREA,R13
         MVI   MSGWORK,X'40'      CLEAR MSG BUFFER
         MVC   MSGWORK+1(120),MSGWORK
         MVI   PARM,X'00'         ZERO OUT PARM SWITCH
         MVC   RTNCD,=F'0000'     ZERO OUT RETURN CODE
         EJECT
***********************************************************************
*  GET CMD BUFFER AND SET UP PARSE PARMATER LIST
***********************************************************************
         ST    R1,CPPLADDR        SAVE ADDR OF CPPL
         USING CPPL,R1
         LA    R2,PARSLIST        ADDR OF PARSE PLIST
         USING PPL,R2
         MVC   PPLUPT,CPPLUPT     MOVE ADDR INTO PARSE PARM LIST
         MVC   PPLECT,CPPLECT     MOVE ADDR INTO PARSE PARM LIST
         MVC   PPLCBUF,CPPLCBUF   MOVE ADDR INTO PARSE PARM LIST
         XC    PECB,PECB          CLEAR ECB TO ZEROS
         LA    R0,PECB            ADDR OF PARSE ECB
         ST    R0,PPLECB          STORE INTO PPL
         L     R0,=A(PCLIST)      ADDR OF PCL
         ST    R0,PPLPCL          ADDR OF PCL
         LA    R0,ANSWER          PPL REPLY
         ST    R0,PPLANS          STORE INTO PPL
         XC    PPLUWA,PPLUWA      CLEAR TO ZEROS
         LA    R1,PPL             CPPL DSECT NO LONGER NEEDED
         CALLTSSR EP=IKJPARS,MF=(E,(1))     CALL PARSE
         CLC   ANSWER,=X'FF000000' IF NOT EQUAL
         BC    7,CHECKPDE         NE - PARSE WAS SUCCESSFUL
         MVC   MSGWORK,MSG01      MOVE MSG TO BUFFER
         MVC   RTNCD,=F'0012'     RETURN CODE SETTING
         BAL   R14,PUTLINE        WRITE MSG.
         BC    15,RETURN          TERMINATE CP
         EJECT
***********************************************************************
*  CHECK THE PDE'S FOR RETURN SWITCHES
***********************************************************************
CHECKPDE L     R3,ANSWER          ADDR OF PDL BUILT BY PARSE
         USING PDLDSECT,R3
         CLC   PCEM5,=H'1'        INDICATES MODEL 5 REQUESTED
         BC    8,MODEL5           GO SET SCREEN (27*132)
         CLC   PCEM2,=H'1'        MODEL 2 SCREEN REQUESTED
         BC    8,MODEL2           GO SET SCREEN (24*80)
         CLC   PCEM3,=H'1'        INDICATES MODEL 3 REQUESTED
         BC    8,MODEL3           GO SET SCREEN (32*80)
         CLC   PCEM4,=H'1'        INDICATES MODEL 4 REQUESTED
         BC    8,MODEL4           GO SET SCREEN (43 * 80)
CHECKL   CLC   PCEL,=H'1'         TELL SCRN SIZE
         BC    8,DISPLAY          GO DISPLAY THEM
CHECKRC  CLC   PCERC,=H'1'        GIVE MODEL NO IN RETURN CODE
         BC    8,RCSIZE           GO SET RETURN CODE
         TM    PARM,X'01'         WAS A PARM PASSED?
         BC    1,RETURN           YES, GO TO EOJ
         MVC   MSGWORK,MSG05      MUST GIVE A KEYWORD PARM
         MVC   RTNCD,=F'0012'     SET RETURN CODE
         BAL   R14,PUTLINE        SEND OUT MESSAGE
         BC    15,RETURN          AND EOJ
         EJECT
***********************************************************************
*  NO PARAMETER ENTERED, SO TAKE DEFAULT OF 'LIST'
***********************************************************************
DISPLAY  OI    PARM,X'01'         TURN PARM SWITCH ON
         GTSIZE
         LTR   R15,R15            WAS IT SUCCESSFUL?
         BC    8,SHOWSIZE         WRITE OUT THE SIZE
         MVC   MSGWORK,MSG03      MOVE MESSAGE TO BUFFER
         MVC   RTNCD,=F'0012'     SET RETURN CODE
         BAL   R14,PUTLINE        WRITE MESSAGE
         BC    15,RETURN
SHOWSIZE MVC   MSGWORK,MSG04           MOVE MESSAGE TO BUFFER
         CVD   R0,REGPACK              BINARY TO PACKED DECIMAL
         MVC   MSGWORK+68(4),PATTERN   MOVE IN PATTERN AREA
         ED    MSGWORK+68(4),REGPACK+6 EDIT THE FIELD
         CVD   R1,REGPACK              BINARY TO PACKED DECIMAL
         MVC   MSGWORK+45(4),PATTERN   MOVE IN PATTERN AREA
         ED    MSGWORK+45(4),REGPACK+6 EDIT THE FIELD
         BAL   R14,PUTLINE             WRITE MESSAGE
         BC    15,CHECKRC              SEE IF RC PARM REQUESTED
         EJECT
***********************************************************************
* RETURN THE SCREEN SIZE VIA R15 RETURN CODE AS FOLLOWS:
*      RTNCD = '0002'     FOR  MODEL 2 (24 X  80)
*      RTNCD = '0003'     FOR  MODEL 3 (32 X  80)
*      RTNCD = '0004'     FOR  MODEL 4 (43 X  80)
*      RTNCD = '0005'     FOR  MODEL 5 (27 X 132)
***********************************************************************
RCSIZE   OI    PARM,X'01'         TURN PARM SWITCH ON
         GTSIZE
NOCHARS  C     R1,=X'00000050'    80 CHARS PER LINE
         BC    8,CHARS80          YES GO CK R0
         C     R1,=X'00000084'    132 CHARS PER LINE
         BC    8,CHARS132         YES GO CK R0
         MVC   RTNCD,=F'0000'     MODEL NOT FOUND
         BC    15,RETURN          EOJ
CHARS80  C     R0,=X'00000018'    24 LINES PER SCREEN
         BC    7,M3CHECK          YES, GO CK R1
         MVC   RTNCD,=F'0002'     RC = 2 (MODEL 2)
         BC    15,RETURN          EOJ
M3CHECK  C     R0,=X'00000020'    32 LINES PER SCREEN
         BC    7,M4CHECK          NO, GO CK R1
         MVC   RTNCD,=F'0003'     RC = 3 (MODEL 3)
         BC    15,RETURN          EOJ
M4CHECK  C     R0,=X'0000002B'    43 LINES PER SCREEN
         BC    7,NOMATCH          NO, GO CK R1
         MVC   RTNCD,=F'0004'     RC = 4 (MODEL 4)
         BC    15,RETURN          EOJ
CHARS132 C     R0,=X'0000001B'    27 LINES PER SCREEN
         BC    7,NOMATCH          NO, GO CK R1
         MVC   RTNCD,=F'0005'     RC = 5 (MODEL 5)
         BC    15,RETURN          EOJ
NOMATCH  MVC   RTNCD,=F'0000'     RC = 0 - MODEL NOT FOUND
         BC    15,RETURN          EOJ
***********************************************************************
*  MODEL2 PROCESSING (LARGE LETTERS)  SCREEN SIZE:24 * 80
***********************************************************************
MODEL2   OI    PARM,X'01'         SET PARM SWITCH
         STSIZE SIZE=80,LINE=24   SCREEN SIZE:24*80
         LTR   R15,R15            CHECK THE RETURN CODE
         BC    7,CHGNG            NG GO SEND MSG + RETURN
         BC    15,WRITE           WRITE IT OUT
         EJECT
***********************************************************************
*  MODEL3 PROCESSING (MEDIUM LETTERS) SCREEN SIZE: 32 * 80
***********************************************************************
MODEL3   OI    PARM,X'01'         TURN PARM SWITCH ON
         STSIZE SIZE=80,LINE=32   SCREEN SIZE:32*80
         LTR   R15,R15            CHECK THE RETURN CODE
         BC    7,CHGNG            NG GO SEND MSG + RETURN
         BC    15,WRITEALT        WRITE IT OUT
         EJECT
***********************************************************************
*  MODEL4 PROCESSING              SCREEN SIZE: 43 * 80
***********************************************************************
MODEL4   OI    PARM,X'01'         TURN PARM SWITCH ON
         STSIZE SIZE=80,LINE=43   SCREEN SIZE:43*80
         LTR   R15,R15            CHECK THE RETURN CODE
         BC    7,CHGNG            NG GO SEND MSG + RETURN
         BC    15,WRITEALT        WRITE IT OUT
         EJECT
***********************************************************************
*  MODEL5 PROCESSING (SMALL LETTERS) SCREEN SIZE:27 * 132
***********************************************************************
MODEL5   OI    PARM,X'01'          TURN PARM SWITCH ON
         STSIZE SIZE=132,LINE=27   SCREEN SIZE:27*132
         LTR   R15,R15            CHECK THE RETURN CODE
         BC    7,CHGNG            NG GO SEND MSG + RETURN
         BC    15,WRITEALT        WRITE IT OUT
         EJECT
***********************************************************************
*   SCREEN SIZE CHANGE DID NOT GO - SEND MESSAGE AND RETURN
***********************************************************************
CHGNG    MVC   MSGWORK,MSG02      MOVE MESSAGE TO BUFFER
         MVC   RTNCD,=F'0012'     SET RETURN CODE
         BAL   R14,PUTLINE        WRITE MESSAGE
         BC    15,CHECKL          SEE IF LIST PARM REQUESTED
***********************************************************************
*     CLEAR SCREEN PUTLINE
***********************************************************************
WRITE    MVC   PUTWORK,PTLN2      MOVE IN SCREEN CONTROL CHARS
         OI    CHGSCREN,X'01'     TURN SWITCH ON
         BAL   R14,PUTLINE        GO RESET THE SCREEN
         BC    15,CHECKL          SEE IF LIST PARM REQUESTED
WRITEALT MVC   PUTWORK,PTLN5      MOVE IN SCREEN CONTROL CHARS
         OI    CHGSCREN,X'01'     TURN SWITCH ON
         BAL   R14,PUTLINE        GO RESET THE SCREEN
         BC    15,CHECKL          SEE IF LIST PARM REQUESTED
         EJECT
***********************************************************************
*  ALL MSGS WRITTEN TO THE TERMINAL IS DONE HERE VIA PUTLINE
***********************************************************************
PUTLINE  STM   R0,R15,SAVEREGS    SAVE CALLER'S REGS
         L     R2,CPPLADDR        ADDR OF CPPL
         USING CPPL,R2
         MVC   PUTLPARM,=XL40'00' ZERO OUT PARM AREA
         LA    R1,PUTLPARM        ADDR OF PLIST
         USING IOPL,R1
         MVC   IOPLECT,CPPLECT    MOVE ADDR OF ECT TO PLIST
         MVC   IOPLUPT,CPPLUPT    MOVE ADDR OF UPT TO PLIST
         LA    R0,ECB             ADDR OF ECB
         ST    R0,IOPLECB         STORE INTO PLIST
         XC    PTPB,PTPB          ZERO IT OUT
         XC    ECB,ECB            CLEAR ECB TO ZEROS
         TM    CHGSCREN,X'01'     IS A SCREEN SIZE CHN REQUESTED?
         BC    1,SCRNCHG          YES, DO PUTLINE ASIS
         LA    R5,MSGWORK         ADDR OF MSG BUFFER
         PUTLINE PARM=PTPB,ECB=ECB,                                    X
               TERMPUT=(EDIT,WAIT,NOHOLD,NOBREAK),                     X
               OUTPUT=((R5),TERM,SINGLE,DATA),                         X
               MF=(E,(1))
         MVI   MSGWORK,X'40'      CLEAR BUFFER TO SPACES
         MVC   MSGWORK+1(120),MSGWORK
         BC    15,ENDPUTLN
SCRNCHG  LA    R5,PUTWORK         ADDR OF MSG BUFFER
         PUTLINE PARM=PTPB,ECB=ECB,                                    X
               TERMPUT=(ASIS,WAIT,NOHOLD,NOBREAK),                     X
               OUTPUT=((R5),TERM,SINGLE,DATA),                         X
               MF=(E,(1))
         MVI   CHGSCREN,X'00'     CLEAR SCRN SW TO ZEROS
         MVI   PUTWORK,X'40'      CLEAR BUFFER TO SPACES
         MVC   PUTWORK+1(16),PUTWORK
ENDPUTLN LM    R0,R15,SAVEREGS    RESTORE CALLER'S REGS
         BR    R14                RETURN TO CALLER
         EJECT
***********************************************************************
*  CLEAN UP AND RETURN TO THE TMP
***********************************************************************
RETURN   CLC   ANSWER,=X'FF000000' IF EQUAL
         BE    RETURN02           BYPASS FREEMAIN-PARSE HADN'T WORKED
         IKJRLSA                  ANSWER    FREE PARSE AREA
RETURN02 L     R2,RTNCD           LOAD THE RETURN CODE
         LR    R1,R13             DYNAMIC AREA ADDR
         LA    R0,LDYNAMIC        SIZE OF AREA
         L     R13,4(,R13)        PREV SAVE AREA
         LA    R1,0(,R1)          CLEAR HI ORDER      FREEMAIN
         LM    R3,R12,32(R13)
         SVC   10                 FREEMAIN
         L     R14,12(R13)
         LR    R15,R2             RESTORE THE RETURN CODE
         MVI   12(R13),X'FF'      INDICATE RETURN
         BR    R14                GO BACK TO TMP
         EJECT
***********************************************************************
* CONSTANTS NOT REQUIRING A GETMAINED AREA
***********************************************************************
         LTORG
PATTERN  DC    X'40202020'        EDIT PATTERN
MSG01    DC    H'121'
         DC    H'0'
         DC    CL117'UNABLE TO PARSE INPUT BUFFER. CP TERMINATED'
MSG02    DC    H'121'
         DC    H'0'
         DC    CL117'INVALID SCREEN SIZE - UNABLE TO CHANGE'
MSG03    DC    H'121'
         DC    H'0'
         DC    CL117'TERMINAL SCREEN SIZE COULD NOT BE DETERMINED'
MSG04    DC    H'121'
         DC    H'0'
         DC CL117'CURRENT SCREEN SETTING - CHARS PER LINE:      LINES PX
               ER SCREEN:      '
MSG05    DC    H'121'
         DC    H'0'
         DC    CL117'KEYWORD PARAMETER MISSING, RE-ENTER INPUT'
*SCREEN CONTROL TO SWITCH TO A MODEL 2 (24X80):
PTLN2    DC    H'014'        MESSAGE LENGTH
         DC    H'0'          OFFSET TO MESSAGE
         DC    X'27F5C1'     ESC ERASE WRITE + RESET MDT
         DC    X'114040'     SBA 1,1
         DC    X'3C404000'   RTA 1,1 WITH NULLS
         DC    X'1DC8'       SF, INTENSIFIED
         DC    X'13'         INSERT CURSOR
*SCREEN CONTROL TO SWITCH TO A MODEL 5 (27X132):
PTLN5    DC    H'014'        LENGTH OF MESSAGE
         DC    H'0'          OFFSET TO MESSAGE
         DC    X'277EC1'     ESC ERASE WRITE-ALTERNATE + RESET MDT
         DC    X'114040'     SBA 1,1
         DC    X'3C404000'   RTA 1,1 WITH NULLS
         DC    X'1DC8'       SF, INTENSIFIED
         DC    X'13'         INSERT CURSOR
         EJECT
***********************************************************************
*  PARSE DEFINITIONS
***********************************************************************
         PRINT NOGEN
PCLIST   IKJPARM DSECT=PDLDSECT
PCEM5    IKJKEYWD
         IKJNAME 'SMALL',ALIAS=('S','M5')
PCEM4    IKJKEYWD
         IKJNAME 'M4'
PCEM3    IKJKEYWD
         IKJNAME 'M3'
PCEM2    IKJKEYWD
         IKJNAME 'BIG',ALIAS=('B','M2')
PCEL     IKJKEYWD
         IKJNAME 'LIST',ALIAS=('L')
PCERC    IKJKEYWD
         IKJNAME 'RETC',ALIAS=('RC')
         IKJENDP
         EJECT
***********************************************************************
* WORKAREA FOR GETMAIN'D AREA FOR LITERALS BEING CHANGED
***********************************************************************
DYNMAREA DSECT
SAVEAREA DS    18F                SAVE AREA
SAVEREGS DS    16F
CPPLADDR DS    F                  CPPL ADDR FOR DSECT
*****THE FOLLOWING ARE FOR PARSE:
PARSLIST DS    7A                 FOR PPL DSECT
PECB     DS    F                  ECB FOR PPL
ANSWER   DS    F                  REPLY FROM PARSE IN PPL
*****THE FOLLOWING ARE FOR PUTLINE:
MSGWORK  DS    CL121              FOR PUTLINE
PUTWORK  DS    CL17
PUTLPARM DS    10A
ECB      DS    F
PTPB     PUTLINE MF=L
CHGSCREN DS    X                  SWITCH TO SIGNIFY SCRN SIZE CHG
PARM     DS    X                  SWITCH-WAS A PARM PASSED?
RTNCD    DS    F                  R15 RETURN CODE
*****THE FOLLOWING ARE MISCELLANEOUS:
REGPACK  DS    0D
SAVESIZE DS    CL3
         DS    0D
LDYNAMIC EQU   *-DYNMAREA         SIZE OF DYNAM AREA
         EJECT
***********************************************************************
*  MACROS - DSECTS - ETC.
***********************************************************************
         IKJCPPL
         EJECT
         CVT
         EJECT
         IKJIOPL
         EJECT
         IKJPPL
         END   CBT1350
