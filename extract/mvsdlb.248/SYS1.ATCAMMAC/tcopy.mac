         MACRO
&NAME    TCOPY &TERMNME,&WRKAREA
.*C030000-110000,480000-490000                                  SA51790
.*A300000                                                      @ZA01104
.*             VERSION DATED MARCH 23,1972                      SA51790
         AIF   ('&TERMNME'(1,1) EQ '(' OR K'&TERMNME LT 9).OK
         MNOTE 12,'*** IHB002 INVALID TERMNAME OPERAND SPECIFIED'
         MEXIT
.OK      ANOP
         AIF   ('&WRKAREA'(1,1) EQ '(' OR K'&WRKAREA LT 9).OK1
         MNOTE 12,'*** IHB002 INVALID WORKAREA OPERAND SPECIFIED'
         MEXIT
.ERR1    ANOP
         MNOTE 12,'*** IHK078 TERMNAME OPERAND REGISTER NOTATION       *
               INVALID - (1)'
         MEXIT
.ERR2    ANOP
         MNOTE 12,'*** IHB078 WORKAREA OPERAND REGISTER NOTATION       *
               INVALID - (0)'
         MEXIT
.ERR0    ANOP
         MNOTE 12,'*** IHB004 REQUIRED OPERAND(S) NOT SPECIFIED'
         MEXIT
.OK1     ANOP
&NAME    DS    0H                                              @ZA01104
         AIF   ('&TERMNME'(1,1) NE '(').LA
         AIF   ('&TERMNME' EQ '(0)').WRKA
         AIF   ('&TERMNME' EQ '(1)').ERR1
         LR    0,&TERMNME(1) .          ADDR OF TERMINAL NAME
         AGO   .WRKA
.LA      ANOP
         BAL   0,*+12 .                 ADDR OF TERMINAL NAME
         DC    CL8'&TERMNME' .          TERMINAL NAME
.WRKA    ANOP
         AIF   ('&WRKAREA'(1,1) NE '(').LA1
         AIF   ('&WRKAREA' EQ '(1)').CALL
         AIF   ('&WRKAREA' EQ '(0)').ERR2
         LR    1,&WRKAREA(1) .          ADDRESS OF WORKAREA
         AGO   .CALL
.LA1     ANOP
         LA    1,&WRKAREA .             ADDRESS OF WORKAREA
.CALL    ANOP
         LINK  EP=IEDQE1                                        SA51790
         MEND
