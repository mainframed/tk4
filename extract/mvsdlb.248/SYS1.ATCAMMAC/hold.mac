         MACRO
&NAME    HOLD  &MS,&RL,&INTVL=,&CONNECT=OR,&STATION=**,&LEVEL=MSG,     *
               &BLANK=YES
         GBLA  &IEDQZE(64),&IEDQZD,&IEDQZK
         GBLB  &IEDQZA(64)
         LCLA  &FLG,&LEN,&LN2,&RCL
         LCLB  &NOGO,&BIT
         LCLC  &SC,&SV
.******************* MICROFICHE FLAGS *********************** SUPT CODE
.*C130000-999999                                               @OY12688
.*A670010                                                      @OY13629
.*A505010,510010                                               @OY14507
.*A145010                                                      @OS77935
.*C515010-520010,710010                                        @OY15029
.*D665010-675010,690010-705010                                 @OY20645
         SPACE 1
         AIF   (&IEDQZE(4) NE 0).VCON
         ORG   IEDAS
         DC    V(IEDQAS01)
         ORG
.VCON    IEDQVCON 4,IEDQAS
&LEN     SETA  8                        BASIC PARM LIST LENGTH
         AIF   (&IEDQZA(5) OR &IEDQZA(4) OR &IEDQZA(3)).HOK
         MNOTE 12,'***  IHB070  SEQUENCE ERROR - MUST FOLLOW INHDR,INMSX
               G,OUTHDR OR OUTMSG MACRO'
&NOGO    SETB  1
.HOK     AIF   (NOT &IEDQZA(9)).TSO
         MNOTE 12,'*** IHB312 MUST FOLLOW STARTMH MACRO WITH TSOMH=NO'
&NOGO    SETB  1
.TSO     AIF   ('&INTVL' EQ '').CKRL
         AIF   (NOT &IEDQZA(1)).RTGRP                          @OS77935
         MNOTE 4,'***  IHB300  INTVL OPERAND INVALID IN INHDR OR INMSG *
               SUBGROUPS - IGNORED'
         AGO   .CKRL                                           @OS77935
.RTGRP   ANOP                                                  @OS77935
         AIF   ('&RL' NE '').RLER
&FLG     SETA  &FLG+64                  SET INTVL SPECIFIED
         AIF   ('&INTVL'(1,1) GE '0' AND '&INTVL'(1,1) LE '9').INTMS
         AIF   ('&INTVL'(1,1) EQ 'X' AND '&INTVL'(K'&INTVL,1) EQ '''').X
               INTMS
&LEN     SETA  &LEN+4
&LN2     SETA  4                        INDICATE VCON REQUIRED
&FLG     SETA  &FLG+32                  SET INTVL IN OPFIELD
         AGO   .CKSUB
.INTMS   IEDQMASK &INTVL,2
         AIF   (NOT &IEDQZA(24)).CKSUB
         MNOTE 12,'*** IHB300 INTERVAL OPERAND INVALID AS SPECIFIED'
&NOGO    SETB  1
         AGO   .CKSUB                   CHECK SUBGROUP
.RLER    MNOTE 4,'***  IHB076 INTVL AND RELEASE MUTUALLY EXCLUSIVE, RELX
               EASE ASSUMED'
.CKRL    AIF   ('&RL' EQ '' OR '&RL' EQ 'RELEASE').CKSUB
         MNOTE 4,'***  IHB086 &RL OPERAND INVALID - RELEASE ASSUMED'
.CKSUB   AIF   (&IEDQZA(5)).CMSK        IN/OUTMSG
         AGO   .NST                     CONCHARS NOT SPECIFIED @OY12688
         IEDQCHAR 8,&MS
         AIF   (NOT &IEDQZA(24) AND &IEDQZD LE 8).CST
         MNOTE 12,'***  IHB300  CHARACTER OPERAND INVALID AS SPECIFIED'
&NOGO    SETB  1
.CST     AIF   ('&STATION' EQ '**').NST
&LEN     SETA  &LEN+8                   ADJUST LENGTH
&FLG     SETA &FLG+128                  INDICATE STATION SPECIFIED
         AIF   ('&STATION'(1,1) EQ '''').NST
&FLG     SETA  &FLG+16                  SET OPFIELD OR REG
         AIF   ('&STATION'(1,1) EQ '(').NST  IF REGISTER NOTATION
&FLG     SETA  &FLG+8                   STATION IN OPFIELD
         AIF   (&LN2 NE 0).NST          VCON FLAG SET
&LN2     SETA  4                        SET TO VCON IEDQAE
&LEN     SETA  &LEN+4                   ADJUST LENGTH FOR VCON
.NST     AIF   ('&CONNECT' EQ 'OR').CNOK
         MNOTE 4,'***  IHB300  CONNECT OPERAND INVALID IN HEADER SUBGROX
               UPS - IGNORED'
.CNOK    AIF   ('&LEVEL' EQ 'MSG').LVOK
         MNOTE 4,'***  IHB300  LEVEL OPERAND INVALID IN HEADER SUBGROUPX
               S - IGNORED'
.LVOK    AIF   (&NOGO).MEND
&NAME    DS    0H
         AGO   .NOSC                    IF NO CONCHARS         @OY12688
         IEDQSCAN &IEDQZD,&MS,&BLANK
         AIF   (&IEDQZA(24)).MEND
         LTR   15,15
&SC      SETC  'IEDM'.'&SYSNDX'
         BNP   &SC
         L     1,IEDADBUF
         STH   15,24(,1)
.NOSC    CNOP  0,4                                             @OY12688
         AIF   ('&STATION'(1,1) NE '(').GNBL
         ANOP  &STATION(1),*+16                                @OY12688
.GNBL    BAL   1,*+&LEN+4
         DC    AL1(&IEDQZE(4),&LEN,&FLG,0)
         AIF   (&FLG LE 127).INTEST
&FLG     SETA  &FLG-128
         DC    AL1(255,4)
         AIF   ('&STATION'(1,1) EQ '''').GNMSC
         DC    AL1(&STATION(1),5),A(0)
         AGO   .INTEST
.GNMSC   DC    AL1(0,0),A(IEDQMISC+&IEDQZK)
&SV      SETC  '&SYSECT'
IEDQMISC CSECT
         DC    C&STATION
&IEDQZK  SETA  &IEDQZK+K'&STATION-2
&SV      CSECT
         AGO   .INTEST
.CMSK    AIF   ('&STATION' EQ '**').STAK
         MNOTE 4,'***  IHB300  STATION OPERAND INVALID IN INMSG/OUTMSG X
               SUBGROUPS- IGNORED'
.STAK    ANOP
         AIF   (NOT &IEDQZA(2)).NORCL                          @OY14507
&RCL     SETA 2
.NORCL   ANOP                                                  @OY14507
         AIF   ('&MS' NE '' AND '&MS' NE '0' AND '&MS' NE 'X''000000000*
               0''' AND '&MS' NE 'XL5''0000000000''').CNDTL    @OY15029
&BIT     SETB  1                                               @OY15029
         AIF   ('&MS' EQ '' OR '&LEVEL' NE 'BLK').CKCNT        @OY15029
&LEN     SETA  &LEN+4                                          @OY15029
         AGO   .CKCNT                                          @OY15029
.CNDTL   ANOP                                                  @OY15029
&LEN     SETA  &LEN+4                   ADJUST FOR MASK
         IEDQMASK &MS,5
         AIF   (NOT &IEDQZA(24)).MSOK
.ER1     MNOTE 12,'***  IHB300  MASK OPERAND INVALID AS SPECIFIED'
&NOGO    SETB  1
         AGO   .CKCNT
.MSOK    AIF   ('&MS'(1,1) NE 'X').CKCNT
         AIF   ('&MS'(2,1) EQ 'L' AND '&MS'(3,1) NE '5').ER1
         AIF   ('&MS'(2,1) NE 'L' AND K'&MS NE 13).ER1
.CKCNT   AIF   ('&CONNECT' EQ 'OR').CCN
         AIF   ('&CONNECT' NE 'AND').CNMT
&LEN     SETA  &LEN+1
         AGO   .CCN
.CNMT    MNOTE 4,'***  IHB086  CONNECT= INVALID KEYWORD, OR ASSUMED'
.CCN     AIF   ('&LEVEL' EQ '' OR '&LEVEL' EQ 'MSG').CKNO
         AIF   ('&LEVEL' EQ 'BLK').CKCON
         MNOTE 4,'*** IHB086 LEVEL OPERAND INVALID, LEVEL=MSG ASSUMED'
         AGO   .CKNO
.CKCON   ANOP
&FLG     SETA  &FLG+2
         AIF   ('&RL' EQ '' AND '&INTVL' NE '').CKNO
         MNOTE 12,'*** IHB300 RELEASE OPERAND INVALID WITH LVL=BLK'
&NOGO    SETB  1
         AGO   .CKNO
.CKSBG   AIF   (&IEDQZA(2)).CKNO
         MNOTE 12,'***  IHB300 LEVEL=BLK VALID ONLY IN OUTMSG SUBGROUP'
&NOGO    SETB  1
.CKNO    AIF   (&NOGO).MEND
.NODEF   ANOP
&NAME    DC    AL1(&IEDQZE(4)+&BIT+&RCL,&LEN,&FLG)
         AIF   ('&MS' NE '' AND '&MS' NE '0' AND '&MS' NE 'X''000000000*
               0''' AND '&MS' NE 'XL5''0000000000''').MSCT     @OY15029
         DC    AL1(0)
         AGO   .INTEST
.MSCT    AIF   ('&MS'(1,1) EQ 'X').XM
         DC    FL5'&MS'
         AGO   .INTEST
.XM      DC    &MS
.INTEST  AIF   (&LN2 EQ 0).VCOK
         DC    V(IEDQAE)
.VCOK    AIF   (&FLG GT 63).INTV
         DC    F'0'
         AGO   .CKUI
.INTV    AIF   (&FLG LT 96).CKXI
         DC    AL2(0),AL1(&INTVL,24)
         AGO   .CKUI
.CKXI    AIF   ('&INTVL'(1,1) EQ 'X').XI
         DC    AL2(&INTVL,0)
         AGO   .CKUI
.XI      AIF   ('&INTVL'(2,1) EQ 'L').XL
         AIF   ((K'&INTVL-2)/2 EQ 2).GO
         AGO   .AL1
.XL      AIF   ('&INTVL'(3,1) EQ '2').GO
.AL1     DC    AL1(0)
.GO      DC    &INTVL,H'0'
.CKUI    AIF   (&IEDQZA(5)).MEND        IF IN/OUTMSG
.GNUI    L     15,IEDUI
         BALR  14,15
         AIF   ('&SC' EQ '').MEND
&SC      EQU   *
.MEND    SPACE 2
         MEND
