         MACRO
         IEDQGCH  &A,&I=(0,1,2)                                @OZ41718
         GBLA  &IEDQAR(64),&B
         GBLA  &IEDQCH(128)                                      S22024
         GBLB  &IEDQFL(16),&IEDQZA(64),&IEDQZZ(16) .             S99228
         GBLB  &IEDQTTF(64)                                    @Y17XAFF
         GBLB  &IEDQTAC,&IEDQMHF(64)                           @OZ41718
         GBLC  &IEDQAD(5),&IEDQSV(8)  .                         SA57685
         GBLC  &IEDQSV1(50)                                    @Y17XAFF
         LCLA  &SCRN,&RETCNT,&L .                                S99228
         LCLA  &V                                                S22024
         LCLB  &J                                                S22024
         LCLB  &K .                                              Y06327
         LCLB  &CONC .                                           S22025
         LCLC  &D(20),&SCRSZ(2)                                @OZ41718
.*
.******************* MICROFICHE FLAGS *********************** SUPT CODE
.*C040000,052000,292000,466000 .                                 S99228
.*A294000-306000,451000,461000-463000,474000-478000 .            S99228
.*D055000,490000                                                 S22025
.*C020000,436000,460000                                          S22025
.*A052000-054000,433000,472300-475900                            S22025
.*C050000,060000,060300-066000,180000,210000-220000             SA57685
.*C250000,270000                                                SA57685
.*A480111-480118,053200                                          Y06327
.*C480180                                                        Y06327
.*A480110                                                      @YA06859
.*C480111-500000                                               @YA06859
.*A050000,820000                                               @Y17XAFF
.*C110000                                                      @Y17XAFF
.*A042000                                                      @OZ41718
.*C015000,055000,440000,463000,472400                          @OZ41718
.*C475600-475600,481000                                        @OZ41718
.*PACKAGE ERROR                                                @OZ49699
.**********************************************************************
         AIF   (&IEDQAR(5) EQ 0).GEN
         MNOTE 4,'***IHB321 &IEDQSV(1) CONTINUATION IGNORED'
&IEDQAR(10) SETA  0
&IEDQAR(5)  SETA  0
.*       ONLY TERMINAL AND TPROCESS ARE VALID                  @Y17XAFF
         AIF   (&IEDQAR(10) NE 3 AND &IEDQAR(10) NE 2).ND      @Y17XAFF
.GEN     AIF   (NOT &IEDQFL(1)).DL
         DC    AL1(2),AL2(&IEDQAR(17))
&IEDQFL(1) SETB 0
.DL      AIF   (NOT &IEDQFL(2)).AD
         DC    AL1(&IEDQAR(18))
&B       SETA  &IEDQAR(18)
.TRD     AIF   (&B LE 16).SND  .        DIAL DIGITS 17 TO 20    SA57685
&D(&B)   SETC  '0'.'&IEDQAD(5)'(&B-16,1)  .                     SA57685
&B       SETA  &B-1  .                                          SA57685
         AGO   .TRD  .                  DO NEXT DIGIT           SA57685
.SND     AIF   (&B LE 8).FST  .         DIAL DIGITS 9 TO 16     SA57685
&D(&B)   SETC  '0'.'&IEDQAD(3)'(&B-8,1)
&B       SETA  &B-1
         AGO   .SND  .                  DO NEXT DIGIT           SA57685
.FST     ANOP                           DIAL DIGITS 1 TO 8      SA57685
&D(&B)   SETC  '0'.'&IEDQAD(2)'(&B,1)
&B       SETA  &B-1
         AIF   (&B GE 1).FST  .         DO NEXT DIGIT           SA57685
         DC    X'&D(1)&D(2)&D(3)&D(4)&D(5)&D(6)&D(7)&D(8)&D(9)&D(10)&D(X
               11)&D(12)&D(13)&D(14)&D(15)&D(16)&D(17)&D(18)&D(19)&D(20*
               )'  .                                            SA57685
&IEDQFL(2) SETB 0
.AD      AIF   (NOT &IEDQFL(3)).BL
         AIF   (&IEDQAR(40) EQ 0).AD1 .                          S99228
&L       SETA  (&IEDQAR(19)/2) .                                 S99228
         DC    AL1(&IEDQAR(19)/2),XL&L'&IEDQAD(1).&IEDQAD(4)' .  S99228
         AGO   .AD2 .                                            S99228
.AD1     DC    AL1(&IEDQAR(19)/2),X'&IEDQAD(1)' .              @PI06895
.AD2     ANOP .                                                  S99228
&IEDQFL(3) SETB 0
.BL      AIF   (NOT &IEDQFL(4)).SB
         DC    AL1(2),AL2(&IEDQAR(20))
&IEDQFL(4) SETB 0
.SB      AIF   (NOT &IEDQFL(5)).TR
         DC    AL1(1),AL1(&IEDQAR(21))
&IEDQFL(5) SETB 0
.TR      AIF   (NOT &IEDQFL(6)).BD
         DC    AL1(2),AL2(&IEDQAR(22))
&IEDQFL(6) SETB 0
.BD      AIF   (NOT &IEDQFL(7)).TS
         DC    AL1(2),AL2(&IEDQAR(23))
&IEDQFL(7) SETB 0
.TS      AIF   (NOT &IEDQFL(8)).ND1 .                            S22025
&IEDQFL(8) SETB 0 .                                              S22025
         AIF   (NOT &IEDQMHF(5)).TS2                           @OZ41718
&IEDQMHF(5) SETB 0                                             @OZ41718
&SCRSZ(1) SETC '&IEDQSV1(21)'                                  @OZ41718
&SCRSZ(2) SETC '&IEDQSV1(22)'                                  @OZ41718
&SCRN    SETA  &SCRSZ(1)*256+&SCRSZ(2)                              TSO
         AIF   (NOT &IEDQZZ(2)).TS1 .                            S99228
         DC    AL1(2),AL2(&SCRN)                                    TSO
         AGO   .ND1 .                                            S22025
.TS1     DC    AL1(5),AL2(&SCRN),AL3(0) .                      @PI06895
         AGO   .ND1 .                                            S99228
.TS2     AIF   (NOT &IEDQZZ(2)).TS3 .                            S99228
         DC    AL1(2),AL2(0)                                        TSO
         AGO   .ND1 .                                            S99228
.TS3     DC    AL1(5),AL2(0),AL3(0) .                          @PI06895
.ND1     AIF   ( NOT &IEDQFL(9)).NCT .                           S22025
&CONC    SETB  (&IEDQFL(9) AND NOT &IEDQTAC)                   @OZ41718
         DC    AL1(4),BL1'&CONC.0000000',AL3(IED&IEDQAR(45).&I(&IEDQAR(X
               44)))                                             S22025
.NCT     AIF   ( NOT &IEDQFL(10)).NLMD .                         S22025
         DC    AL1(2),BL1'0000000&IEDQFL(10)',AL1(&IEDQAR(13)) . S22025
&IEDQAR(13) SETA &IEDQAR(13)+1 .                                 S22025
&IEDQFL(10) SETB 0 .                                             S22025
.NLMD    AIF   ( NOT &IEDQFL(11)).ND                           @PI06895
&IEDQFL(11) SETB 0                                               S22025
         DC    AL1(2),AL1(&IEDQAR(27)),AL1(0)                  @OZ41718
&IEDQAR(27) SETA 0                                             @OZ41718
.ND      AIF   (NOT &IEDQFL(12)).ND2                             S22024
&J       SETB  0                                                 S22024
&V       SETA  &IEDQCH(&IEDQAR(12))                              S22024
         AIF   (NOT &IEDQZZ(8)).TSCK                           @YA06859
         AIF ('&IEDQSV1(17)' NE '3335').TSCK                   @OZ41718
&K       SETB 1                                                  Y06327
&J       SETB 1                                                  Y06327
         AGO   .NATTN .                                          Y06327
.TSCK    AIF   (&V GE 8388608).TOSUP                             Y06327
         AGO   .CHKV .                                           Y06327
.TOSUP   ANOP                                                    Y06327
&K       SETB  1                                                 Y06327
.CHKV    AIF   (&V LT 4194304).SM                                S22024
&V       SETA  &V-4194304                                        S22024
         AGO   .CHKV                                             S22024
.SM      AIF   (&V LT 2097152).NATTN                             S22024
&J       SETB  1                                                 S22024
.NATTN   DC    AL1(4),BL2'0',BL2'00000000&K.0&J.00' .          @PI06895
&IEDQFL(12) SETB 0                                               S22024
.ND2     AIF   (NOT &IEDQFL(13)).ND13  NCP DEV DEP INFO        @PI06895
         DC    AL1(29) .                NCPLEN                 @Y17XAFF
         DC    B'&IEDQZA(56)&IEDQZA(57)&IEDQZA(58).00&IEDQZA(59).00'
*                                       NCPFLAG1               @Y17XAFF
         AIF   ('&IEDQSV1(1)' EQ '').NOBKUP                    @Y17XAFF
         DC    CL8'&IEDQSV1(1)' .       NCPBKUPN               @Y17XAFF
         AGO   .NOBKUP1                                        @Y17XAFF
.NOBKUP  DC    XL8'00' .                NCPBKUPN               @PI06895
.NOBKUP1 AIF   ('&IEDQSV1(2)' EQ '').NOIPLN                    @Y17XAFF
         DC    CL8'&IEDQSV1(2)' .       NCPTXID                @Y17XAFF
         AGO   .NOIPLN1                                        @Y17XAFF
.NOIPLN  DC    XL8'00' .                NCPTXID                @PI06895
.NOIPLN1 AIF   ('&IEDQSV1(3)' EQ '').NODDCB                    @Y17XAFF
         DC    AL4(&IEDQSV1(3)) .       NCPDPDCB               @Y17XAFF
         AGO   .NODDCB1                                        @Y17XAFF
.NODDCB  DC    AL4(0) .                 NCPDPDCB               @PI06895
.NODDCB1 AIF   ('&IEDQSV1(4)' EQ '').NOCKPT                    @Y17XAFF
         DC    AL4(&IEDQSV1(4)) .       NCPCKPT                @Y17XAFF
         AGO   .NOCKPT1                                        @Y17XAFF
.NOCKPT  DC    AL4(0) .                 NCPCKPT                @PI06895
.NOCKPT1 DC    AL4(0) .                 NCPLTRACE              @PI06895
&IEDQFL(13) SETB  0           RESET --- DEV DEP FLAG           @PI06895
&IEDQSV1(1) SETC  ''                    BACKUP NAME            @Y17XAFF
&IEDQSV1(2) SETC  ''                    IPL TEXT ID            @Y17XAFF
&IEDQSV1(3) SETC  ''                    DUMP DCB NAME          @Y17XAFF
&IEDQSV1(4) SETC  ''                    CHKPT DCB NAME         @Y17XAFF
&IEDQZA(56) SETB 0                      IPLAUTO=YES IND        @Y17XAFF
&IEDQZA(57) SETB 0                      DMPAUTO=YES IND        @Y17XAFF
&IEDQZA(58) SETB 0                      BRINGUP=YES IND        @Y17XAFF
&IEDQZA(59) SETB 0                      BACKUP=YES IND         @Y17XAFF
.*       BUILD TPROCESS DEV DEP INFO                           @Y17XAFF
.ND13    AIF   (NOT &IEDQFL(14)).ND14                          @Y17XAFF
         DC    AL1(1) .                 PROLEN                 @Y17XAFF
         DC    B'&IEDQZA(43)&IEDQZA(49)&IEDQTTF(1)00000' .  PROFLG
&IEDQZA(43) SETB 0           RESET ---  SEC= OPERAND IND       @PI06895
&IEDQTTF(1) SETB 0                      DATE=YES SPECIFIED     @Y17XAFF
&IEDQZA(49) SETB 0                      LU=YES OPERAND IND     @Y17XAFF
&IEDQFL(14) SETB 0                      DEVICE DEP FLD IND     @Y17XAFF
.ND14    MEND                                                  @PI06895
