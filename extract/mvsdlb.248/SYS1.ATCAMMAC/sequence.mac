         MACRO
&NAME    SEQUENCE &BLANK=YES
         GBLA  &IEDQZE(64)
         GBLB  &IEDQZA(64)
.*             VERSION DATED AUGUST 21, 1970
         SPACE
         AIF   (NOT &IEDQZA(9)).TSOCHK                              TSO
         MNOTE 12,'***  IHB312  MUST FOLLOW STARTMH MACRO WITH TSOMH=NO*
               '
         AGO   .MEND                                                TSO
.TSOCHK  AIF   (&IEDQZA(3) OR &IEDQZA(4)).OK
         MNOTE 12,'***  IHB070  SEQUENCE ERROR-MUST FOLLOW INHDR OR OUT*
               HDR MACRO'
         AGO   .MEND
.OK      AIF   ('&NAME' EQ '').NON
&NAME    DS    0H
.NON     AIF   (&IEDQZA(4)).OUTSEQ
         IEDQVCON 26,IEDQAH
&IEDQZA(8) SETB 1
         CNOP  0,4
         IEDQSCAN 255,,&BLANK
&IEDQZA(8) SETB 0
         AIF   (&IEDQZA(24)).MEND
         LTR   15,15
         BM    *+16
         BAL   1,*+6 .                  SKIP AROUND PARMLIST
         DC    AL1(&IEDQZE(26)),AL1(2)
         AGO   .LOAD
.OUTSEQ  IEDQVCON (38,27),(IEDQAF,IEDQAD)
         BAL   1,*+6
         DC    AL1(&IEDQZE(27)),AL1(&IEDQZE(38))
.LOAD    L     15,IEDUI .               TO SEQUENCE ROUTINE
         BALR  14,15
.MEND    SPACE 2
         MEND
