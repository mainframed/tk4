         MACRO
&NAME    MHGET &WORK=,&REG=,&RESERVE=NO
         GBLB  &IEDQZA(64) .                                    SA62998
         LCLA  &LEN,&KWORK,&A
         LCLB  &NOGO .                                         @SA75452
         LCLC  &WORKARE,&WORKREG,&ANSREG
.*  CHANGE ACTIVITY AS FOLLOWS
.******************* MICROFICHE FLAGS *********************** SUPT CODE
.*A020000,060000                                                SA62998
.*D060350                                                       SA66910
.*A030000,102000,122000,245000,269000,331000                   @SA75452
.*C060100-060300,C340000                                       @SA75452
.*C060100                                                      @OS76999
&LEN     SETA  4
         AIF   (&IEDQZA(5) OR &IEDQZA(14)).BADGRP              @OS76999
         AGO   .CKRESV                                         @OS76999
.BADGRP  ANOP                                                  @OS76999
         MNOTE 12,'*** IHB070 SEQUENCE ERROR - MUST FOLLOW INHDR OR INBX
               UF OR OUTHDR OR OUTBUF MACRO' .                 @SA75452
&NOGO    SETB  1 .                                             @SA75452
.CKRESV  ANOP                                                   SA62998
         AIF   ('&RESERVE'  NE 'YES').NEXT
&A       SETA  2
         AGO   .NEXTA
.NEXT    AIF   ('&RESERVE' EQ '' OR '&RESERVE' EQ 'NO').NEXTA
         MNOTE 12,'***  IHB300  RESERVE OPERAND INVALID AS SPECIFIED'
&NOGO    SETB  1 .                                             @SA75452
.NEXTA   AIF   ('&WORK' EQ '').REGSTR
         AIF   ('&REG' EQ '').CKWK
         MNOTE 12,'***  IEDM05  REG OPERAND INCONSISTENT WITH WORK OPER*
               AND'
&NOGO    SETB  1 .                                             @SA75452
.CKWK    ANOP
         AIF   ('&WORK'(1,1) EQ '(').WKREG
&WORKARE SETC  '&WORK'
&LEN     SETA  &LEN+4
         AIF   ('&WORK'(1,1) GT 'Z').BADWORK
         AGO   .TESTREG
.WKREG   ANOP
         AIF   ('&WORK'(K'&WORK,1) NE ')').BADWORK
&WORKREG SETC  '&WORK'(2,K'&WORK-2)
         AIF   ('&WORKREG'(1,1) LT '0').TESTREG
         AIF   ('&WORKREG' GE '2' AND '&WORKREG' LE '12').TESTREG
.BADWORK ANOP
         MNOTE 12,'***  IHB300  WORK OPERAND INVALID AS SPECIFIED'
&NOGO    SETB  1 .                                             @SA75452
.TESTREG ANOP
         AIF   ('&REG' EQ '').BAL
         AGO   .REGSTR1
.REGSTR  ANOP
         AIF   ('&REG' NE '').REGSTR1
         MNOTE 12,'***  IHB004  REQUIRED OPERAND NOT SPECIFIED'
&NOGO    SETB  1 .                                             @SA75452
         AGO   .BAL
.REGSTR1 ANOP
         AIF   ('&REG'(1,1) NE '(').BADREG
         AIF   ('&REG'(K'&REG,1) NE ')').BADREG
&ANSREG  SETC  '&REG'(2,K'&REG-2)
         AIF   ('&ANSREG'(1,1) LT '0').BAL
         AIF   ('&ANSREG' GE '2' AND '&ANSREG' LE '11').BAL
.BADREG  ANOP
         MNOTE 12,'***  IHB300  REG OPERAND INVALID AS SPECIFIED'
&NOGO    SETB  1 .                                             @SA75452
.BAL     AIF   (&NOGO EQ 1).MEND .                             @SA75452
         CNOP  0,4                                             @SA75452
&NAME    DS    0H
         BAL   1,*+8+&LEN                                      @PI06030
         DC    AL1(&A) .                MHGET ID AND RESERV FLAG
         DC    AL1(&LEN) .              PARM LENGTH
         AIF   ('&WORKREG' NE '').WKREGDC
&WORKREG SETC  '0'
.WKREGDC ANOP
         DC    AL1(&WORKREG) .          WORK REG IF SPECIFIED
         AIF   ('&ANSREG' NE '').WKARDC
&ANSREG  SETC  '0'
.WKARDC  ANOP
         DC    AL1(&ANSREG) .           REPLY REG IF SPECIFIED
         AIF   ('&WORKARE' EQ '').LOAD
         DC    A(&WORK) .               ADR OF WORK AREA IF SPECIF
         AGO   .LOAD
.LOAD    ANOP                                                  @YM06905
         DC    V(IEDQGP)                VCON - MHGET/MHPUT     @YM06905
         L     15,*-4                   MHGET/MHPUT RTNE       @YM06905
         BALR  14,15
.MEND    SPACE 2
         MEND
