         MACRO
         IEDQCHI  &T,&FEATURE,&TTE=(1030,1050,1060,226L,226R,2265,5041,X
               274A,274B,274C,274D,274E,274F,274G,274H,274I,274J,2760, X
               BSC3,277B,278B,BSC1,2741,7770,83B3,115A,3335,WTTY,BSC2, X
               327L,327R,226C,327C,327S,367C),                   S99228X
               &A=(4112,4244,4112,512,4118,4118,1048576,1024,1024,4096,X
               4116,2068,1044,1044,2048,4116,4096,1045,36884,33844,    X
               33844,33812,525312,256,4096,4096,8192,1088,33812,262656,X
               299028,69654,364564,430100,53268),&FMPROF=,&TSPROF=,    *
               &USS=,&FMVAL=(0,0,5),&TSVAL=(1,1,5),&TERM=(PUNT,LUNT,LNC*
               P)                                              @Y17XAFF
.*A-000000-999999                                              @X31X8F0
         GBLA  &IEDQCH1(128)                                   @Y17XAFF
         GBLA  &IEDQAR(64),&IEDQCH(128),&IEDQFFL
         GBLB  &IEDQNO,&IEDQFL(16),&IEDQZA(64),&IEDQZZ(16) .     S99228
         LCLA  &L,&L1,&FM,&TS,&I                               @Y17XAFF
.*C255000,240000,180000,330000,404000,420000,540000            @Y17XAFF
.*A196000,300700,406500,480000,680000                          @Y17XAFF
.*C080000,090000,160000,170000,260000,388000,404000,412000 .     S99228
.*A300200-302600,404500-411000 .                                 S99228
.*D100000,180000 .                                               S99228
.*C100000,180000,412000-420000 .                                 S22025
.*C090000,170000                                                 S21101
.*C120000 .                                                      Y06327
.*A330000,350000 .                                               Y06327
.*A300700                                                        X03039
.*C330000,350000,605000                                        @Z30X8FG
         AIF   ('&T' EQ '' AND &IEDQZA(63)).NOTSO              @Y17XAFF
.C1      AIF   (&L GE N'&TTE).BSC0                               S99228
&L       SETA  &L+1
         AIF   ('&T' NE '&TTE(&L)').C1
         AGO   .GO .                                             S99228
.BSC0    AIF   ('&T' EQ '113B' OR '&T' EQ '202B' OR '&T' EQ 'S36B' OR  X
               '&T' EQ 'S03B' OR '&T' EQ '180B' OR '&T' EQ '373B').BSC1
         AIF   ('&T' EQ 'LUNT').BSC1                             X03039
&L       SETA  0                        INIT FIRST PART OF DCT @Y17XAFF
.*                                      IF PUNT,LNCP,LINE,SSCP @Y17XAFF
         AIF   ('&T' EQ 'PUNT' OR '&T' EQ 'LNCP' OR '&T' EQ 'LINE' OR  X
                '&T' EQ 'SSCP').NOTSO                          @Y17XAFF
         AIF   ('&T' EQ '277A' OR '&T' EQ '278A' OR '&T' EQ '113A' OR  X
               '&T' EQ '202A' OR '&T' EQ 'S36A' OR '&T' EQ 'S03A').BSC3
         AIF   ('&T' EQ '180A' OR '&T' EQ '2972' OR '&T' EQ '373A' OR  X
               '&T' EQ '367A').BSC3 .                            S99228
         AGO   .ER .                                             S99228
.BSC1    ANOP .                                                  S99228
&L       SETA  22  .                                             S99228
         AGO   .GO .                                             S99228
.BSC3    ANOP .                                                  S99228
&L       SETA  19  .                                             S99228
.GO      ANOP .                                                  S99228
&L       SETA  &A(&L)
         AIF   (&IEDQZA(63)).VTAM       BY PASS IF NCP RES     @Y17XAFF
         AIF   ('&T' EQ '5041' AND &IEDQZA(12) EQ 0).ER            TSO
.VTAM    ANOP                                                  @Z30X8FG
         AIF   (NOT &IEDQFL(7)).NOBUF
&L       SETA  &L+8
.NOBUF   AIF   ((&IEDQZZ(8) AND NOT('&T' EQ '1050' OR '&T' EQ '226L' ORX
               '&T' EQ '226R' OR '&T' EQ '5041' OR '&T' EQ '2741' OR   X
               '&T' EQ '3335' OR '&T' EQ '2265'))).ER2 .         S99228
         AGO   .CHK3270 .                                        S99228
.ER2     AIF   ((&IEDQZZ(8) AND NOT('&T' EQ '226C' OR '&T' EQ '327C' ORX
               '&T' EQ '327L' OR '&T' EQ '327R' OR '&T' EQ '327S' OR   *
               '&T' EQ 'LUNT'))).ER1                           @Y17XAFF
.CHK3270 AIF   ('&T'(1,3) EQ '327').SB1 .                        S99228
         AIF   ('&T' EQ 'LUNT').SB1                            @Y17XAFF
         AIF   ('&T'(1,3) EQ '226').SB2 .                        S99228
         AGO   .TST .                                            S99228
.SB1     ANOP .                                                  S99228
&IEDQZZ(1) SETB 1 .                                              S99228
         AGO   .SB3 .                                            S99228
.SB2     ANOP .                                                  S99228
&IEDQZZ(2) SETB 1 .                                              S99228
.SB3     ANOP .                                                  S99228
&IEDQFL(8) SETB 1 .                                              S99228
.TST     AIF   (NOT &IEDQZZ(8) AND '&T' NE '2741' AND '&T' NE '5041'   X
               AND '&T' NE '1050').NOTSO                       @PI06012
         IEDQFEA &T,&FEATURE,USS=&USS                          @PI05738
&L       SETA  &L+&IEDQFFL
.NOTSO   ANOP                                                       TSO
.*                                                             @Y17XAFF
.*       CHECKOUT FMPROF AND TSPROF OPERANDS                   @Y17XAFF
.*                                                             @Y17XAFF
         AIF   (T'&T EQ 'O').PF100                             @Y17XAFF
         AIF   (T'&FMPROF EQ 'O' AND T'&TSPROF EQ 'O').PF1     @Y17XAFF
         AIF   ('&T' EQ 'LNCP' OR '&T' EQ 'PUNT' OR '&T' EQ 'LUNT').PF1
         MNOTE 4,'*** IED920 FMPROF AND TSPROF INVALID WITH TERM=&T,   *
               OPERANDS IGNORED'                               @Y17XAFF
         AGO   .PF100                                          @Y17XAFF
.*       FIND  DEFAULTS                                        @Y17XAFF
.PF1     AIF   (&I GT N'&TERM).PF100    FIND INDEX INTO TERM=  @Y17XAFF
&I       SETA  &I+1                                            @Y17XAFF
         AIF   ('&T' EQ '&TERM(&I)').PF2                       @Y17XAFF
         AGO   .PF1                                            @Y17XAFF
.PF2     ANOP                                                  @Y17XAFF
&FM      SETA  &FMVAL(&I)               ASSIGN DEFAULT VALUES  @Y17XAFF
&TS      SETA  &TSVAL(&I)                    TO FM AND TS      @Y17XAFF
.*       CHECK OUT FMPROF IF CODED                             @Y17XAFF
         AIF   (T'&FMPROF EQ 'O').PF20  IS IT OMMITTED         @Y17XAFF
         AIF   (T'&FMPROF EQ 'N').PF10  IS IT NUMERIC          @Y17XAFF
.PF05    ANOP
         MNOTE 8,'*** IED921 INVALID FMPROF OPRAND,DEFAULT IS USED.'
.*                                                             @Y17XAFF
         AGO   .PF20                                           @Y17XAFF
.PF10    AIF   (&FMPROF GE 7).PF05      LESS THAN 7 IS VALID   @Y17XAFF
&FM      SETA  &FMPROF                                         @Y17XAFF
.PF20    ANOP                                                  @Y17XAFF
.*       CHECK OUT TSPROF IS CODED                             @Y17XAFF
         AIF   (T'&TSPROF EQ 'O').PF40  IS IT OMMITTED         @Y17XAFF
         AIF   (T'&TSPROF EQ 'N').PF30  IS IT NUMERIC          @Y17XAFF
.PF25    ANOP
         MNOTE 8,'*** IED922 INVALID TSPROF OPERAND, DEFAULT IS USED.'
.*                                                             @Y17XAFF
         AGO   .PF40                                           @Y17XAFF
.PF30    AIF   (&TSPROF GE 7).PF25      LESS THAN 7 IS VALID   @Y17XAFF
&TS      SETA  &TSPROF                                         @Y17XAFF
.PF40    ANOP                                                  @Y17XAFF
&L1      SETA  (&TS+(&FM*16))*256       CREATE DCTBYTE4&5      @Y17XAFF
.*                                       B'FMFMTSTS00000000'   @Y17XAFF
.PF100   ANOP                                                  @Y17XAFF
.*                                                             @Y17XAFF
.*       CHECKOUT USS OPRAND IF T=LUNT                         @Y17XAFF
.*                                                             @Y17XAFF
         AIF   ('&T' NE 'LUNT').US100                          @Y17XAFF
         AIF   (&IEDQZZ(8) AND '&USS' EQ 'NONE').US05          @Y17XAFF
         AGO   .US10                                           @Y17XAFF
.US05    ANOP
         MNOTE 8,'*** IED994 USS=NONE AND QUEUES EQ TS ARE MUTUALLY EXC*
               LUSIVE, USS=SCS ASSUMED'                        @YM08573
         AGO   .US21                                           @YM08573
.US10    AIF   ('&USS' NE '3270').US20                         @Y17XAFF
&L1      SETA  &L1+(4*16)               SET DCTL3270           @Y17XAFF
         AGO   .US100                                          @Y17XAFF
.US20    AIF   ('&USS' NE 'SCS').US30                          @Y17XAFF
.US21    ANOP                                                  @YM08573
&L1      SETA  &L1+(8*16)               SET DCTSCS             @Y17XAFF
         AGO   .US100                                          @Y17XAFF
.US30    AIF   ('&USS' EQ 'NONE').US100                        @Y17XAFF
         MNOTE 8,'***IED923 INVALID USS OPERAND, WILL USE DEFAULT'
.*                                                             @Y17XAFF
.US100   ANOP                                                  @Y17XAFF
&IEDQAR(12) SETA 1
.LOP     AIF   (&IEDQAR(12) GT &IEDQAR(11)).NEW
         AIF   ((&IEDQCH(&IEDQAR(12)) EQ &L) AND (&IEDQCH1(&IEDQAR(12))*
                EQ &L1)).MTC            CHECK FOR DUPLICATES   @Y17XAFF
&IEDQAR(12) SETA &IEDQAR(12)+1
         AGO   .LOP
.ER1     MNOTE 12,'***  IHB314  TERMINAL TYPE NOT SUPPORTED BY TSO- &T'
         AGO   .ERR
.ER      AIF   ('&T' EQ '').ERR                                  S22024
         MNOTE 12,'IHB002 INVALID TERM OPERAND SPECIFIED - &T' @Z30X8FG
.ERR     ANOP
&IEDQNO SETB 1
         MEXIT
.NEW     ANOP
&IEDQCH(&IEDQAR(12)) SETA &L
&IEDQCH1(&IEDQAR(12)) SETA &L1          SET DCTBYTE4 + DCTBYTES@Y17XAFF
.MTC     MEND
