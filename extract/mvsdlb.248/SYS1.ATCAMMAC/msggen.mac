         MACRO
&NAME    MSGGEN &MASK,&MSG,&CODE=,&IN=(1030,1050,105F,1060,2260,2265,27*
               40,274F,ITA2,ZSC3,TTYA,TTYB,TTYC,6BIT,ASCI,EBCD,BC41,EB4*
               1,CR41),&CONNECT=OR,&RH=                        @Y17XANQ
         GBLA  &IEDQZD,&IEDQZE(64),&IEDQZK
         GBLB  &IEDQZA(64),&IEDQMHF(64)                        @YM07267
         GBLB  &IEDQRN(8)                                      @YM07669
         LCLA  &A,&IX,&LEN,&T,&FLG                             @Y17XANQ
         LCLB  &NOGO,&ALG,&BIT
         LCLC  &C
.*C540000                                                      @SA72488
.*A126000,909000                                               @Y17XANQ
.*C036000,063000,369000,432000-459000,558000-666000,819000     @Y17XANQ
.*C900000                                                      @Y17XANQ
.*C249500,263500                                               @PI06853
.*C200500,690500,693500                                        @PI06852
.*A116500                                                      @PI06852
.*A627500                                                      @YM07267
.*C004400,634500                                               @YM07267
.*C629500                                                      @YM07694
.*C307000                                                      @OY16457
         SPACE
&C       SETC  '&SYSECT'
&IX      SETA  1
&T       SETA  10
&LEN     SETA  12                      SETUP NORMAL LENGTH     @Y17XANQ
         AIF   (&IEDQZA(5)).OK
         MNOTE 12,'***  IHB070  SEQUENCE ERROR-MUST FOLLOW INMSG OR OUT*
               MSG MACRO'
&NOGO    SETB  1
.OK      AIF   ('&MSG' NE '').CHK
&LEN     SETA  &LEN-4                   REDUCE PARM LIST LNGTH @PI06852
&FLG     SETA  &FLG+32                  INDICATE  NO MSG DATA  @YM06037
         AIF   ('&RH' NE '').CHK        IF RH AND NO MSG = OK  @YM06037
         MNOTE 12,'***  IHB004  REQUIRED OPERANDS NOT SPECIFIED'
&NOGO    SETB  1
.CHK     AIF   ('&MASK' EQ '').NXT
         IEDQMASK &MASK,5
         AIF   (&IEDQZA(24)).ERR
         AIF   ('&MASK'(1,1) NE 'X').NXT
         AIF   ('&MASK'(2,1) EQ 'L' AND '&MASK'(3,1) NE '5').ERR
         AIF   ('&MASK'(2,1) NE 'L' AND K'&MASK NE 13).ERR
         AGO   .NXT
.ERR     MNOTE 12,'***  IHB300  MASK OPERAND INVALID AS SPECIFIED'
&NOGO    SETB  1
.NXT     AIF   ('&MSG' EQ '').CODECHK                          @PI06852
         AIF   ('&MSG'(K'&MSG,1) EQ '''' AND K'&MSG LT 4).MSGERR
         AIF   ('&MSG'(K'&MSG,1) NE '''' AND K'&MSG GT 8).MSGERR
         AIF   ('&MSG'(K'&MSG,1) NE '''').CODECHK
         AIF   (NOT &IEDQZA(9)).TCAMMSG                             TSO
         IEDQCHAR 52,&MSG                                           TSO
         AGO   .ERRCHK                                              TSO
.TCAMMSG IEDQCHAR 255,&MSG                                     @PI06853
.ERRCHK  AIF   (&IEDQZA(24)).MSGERR
         AIF   (&IEDQZD LE 255).CODECHK                        @PI06853
         AIF   (NOT &IEDQZA(9)).MSGERR
         AIF   (&IEDQZD LE 52).CODECHK                              TSO
.MSGERR  MNOTE 12,'***  IHB300  MESSAGE OPERAND INVALID AS SPECIFIED'
&NOGO    SETB  1
.CODECHK AIF   ('&CODE' EQ 'NO' OR '&CODE' EQ 'NONE' OR '&CODE' EQ '').*
               AND
         AIF   ('&MSG' NE '').CDEOK        CODE= + MSG= IS OK  @YM06037
         MNOTE 4,'*** IED930 *** CODE OPERAND INVALID, IGNORED'
         AGO   .AND                                            @YM06037
.CDEOK   ANOP                                                  @YM06037
&LEN     SETA  &LEN+4                   ADJUST FOR TABLE ADDR  @Y17XANQ
         AIF   (K'&CODE GT 8).CMN
.USER    AIF   (&IX GT 19).USERTAB
         AIF   ('&CODE' EQ '&IN(&IX)').AND
&IX      SETA  &IX+1
&T       SETA  &T+1
         AGO   .USER
.USERTAB AIF   (('&CODE'(1,1) GE 'A' AND '&CODE'(1,1) LE 'Z') OR '&CODE*
               '(1,1) EQ '$' OR '&CODE'(1,1) EQ '#' OR '&CODE'(1,1) EQ *
               '@').AND
.CMN     MNOTE 12,'***  IHB300  CODE OPERAND INVALID AS SPECIFIED'
&NOGO    SETB  1
.AND     AIF   ('&CONNECT' EQ 'AND' OR '&CONNECT' EQ 'OR').OP
         MNOTE 12,'***  IHB300  CONNECT OPERAND INVALID AS SPECIFIED'
&NOGO    SETB  1
.OP      AIF   ('&RH' EQ '').RHOK       RH= NOT SPECIFIED      @Y17XANQ
&LEN     SETA  &LEN+4                   ADJUST FR RH POINTER   @Y17XANQ
&FLG     SETA  &FLG+64                  INDICATE RH= (X'40')   @Y17XANQ
         AIF   ('&RH'(K'&RH,1) NE '''').RHOK   RH=LABEL        @Y17XANQ
         AIF   ((K'&RH GE 6) AND (('&RH'(1,2) EQ 'X''' AND K'&RH EQ 9) *
               OR ('&RH'(1,4) EQ 'XL6''' AND K'&RH EQ 11) OR ('&RH'(1,2*
               ) EQ 'C''' AND K'&RH EQ 6))).RHOK               @Y17XANQ
         AIF   ('&RH'(1,4) EQ 'CL3''' AND  K'&RH EQ 8).RHOK    @Y17XANQ
         MNOTE 12,'***  IHB300  RH OPERAND INVALID AS SPECIFIED'
&NOGO    SETB  1                                               @Y17XANQ
.RHOK    AIF   (&NOGO).MEND                                    @Y17XANQ
&ALG     SETB  ('&CONNECT' NE 'OR')     SET FLAG IF 'AND'      @Y17XANQ
         AIF   (&IEDQZA(9)).TSO                                @Y17XANQ
         IEDQVCON 6,IEDQBL
&A       SETA  6                                                    TSO
         AGO   .NOTSO                                               TSO
.TSO     IEDQVCON 44,IEDAYM                                         TSO
&A       SETA  44                                                   TSO
.NOTSO   AIF   (NOT &IEDQZA(1)).PARM
&IEDQZA(18) SETB 1
.PARM    AIF   ('&MASK' NE '0' AND '&MASK' NE 'X''0000000000''' AND '&M*
               ASK' NE 'XL5''0000000000''' AND '&MASK' NE 'XL5''0''' AN*
               D '&MASK' NE 'XL5''00''' AND '&MASK' NE '').SKP @SA72488
&BIT     SETB  1
&LEN     SETA  &LEN-4                   ADJUST FOR NO MASK     @Y17XANQ
.SKP     AIF   ('&CODE' NE '').X00                             @Y17XANQ
&FLG     SETA  &FLG+128                 CODE OMITTED - (X'80') @Y17XANQ
         AGO   .SET                                            @Y17XANQ
.X00     AIF   ('&CODE' NE 'NO' AND '&CODE' NE 'NONE').SET     @Y17XANQ
&FLG     SETA  &FLG+16                  CODE=NO OR NONE (X'10')@Y17XANQ
.SET     ANOP                                                  @Y17XANQ
&NAME    DS    0H                                              @YM07267
      AIF   (&IEDQRN(5) OR (NOT &IEDQZA(1)) OR (NOT &IEDQRN(4))).OLDGEN
&IEDQRN(5) SETB 1                       INDICATE FIRST TIME    @YM07267
         IEDRESP
.OLDGEN  DC    AL1((&IEDQZE(&A)+&BIT),(&LEN+&ALG),(&FLG))      @YM07267
         AIF   (NOT &BIT).S                                    @Y17XANQ
         DC    AL1(0)
         AGO   .MSG
.S       AIF   ('&MASK'(1,1) EQ 'X').DC
         DC    FL5'&MASK' .             ERROR MASK
         AGO   .MSG
.DC      DC    &MASK .                  ERROR MASK
.MSG     AIF   ('&MSG' EQ '').RHG                              @PI06852
         AIF   ('&MSG'(K'&MSG,1) EQ '''').SPLIT                @PI06852
         DC    AL4(&MSG) .              ADDRESS OF MESSAGE
         AGO   .CODE1
.SPLIT   ANOP
IEDQMISC CSECT
         DC    AL1(&IEDQZD),&MSG
&C       CSECT
         DC    AL4(IEDQMISC+&IEDQZK)
&IEDQZK  SETA  &IEDQZK+1+&IEDQZD
.CODE1   AIF   ('&CODE' EQ 'NO' OR '&CODE' EQ 'NONE' OR '&CODE' EQ '').*
               RHG                                             @Y17XANQ
         AIF   (&IX GT 19).USERTBL
&C       SETC  'IEDQ'.'&T'
         DC    V(&C) .                  STANDARD TABLE
         AGO   .RHG
.USERTBL DC    A(&CODE) .               ADDRESS OF SPECIAL TABLE
.RHG     AIF   ('&RH' EQ '').MEND                              @Y17XANQ
         AIF   ('&RH'(K'&RH,1) EQ '''').RHM                    @Y17XANQ
         DC    A(&RH)                   RH DATA ADDRESS        @Y17XANQ
         AGO   .MEND                                           @Y17XANQ
.RHM     ANOP                                                  @Y17XANQ
IEDQMISC CSECT                                                 @Y17XANQ
         DC    &RH                      RH DATA                @Y17XANQ
&C       CSECT                                                 @Y17XANQ
         DC    A(IEDQMISC+&IEDQZK)      RH DATA ADDRESS        @Y17XANQ
&IEDQZK  SETA  &IEDQZK+3                                       @Y17XANQ
.MEND    SPACE 2
         MEND
