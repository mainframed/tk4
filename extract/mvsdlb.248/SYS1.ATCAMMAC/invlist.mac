         MACRO
&NAME    INVLIST &PARM,&ORDER=,&EOT=,&CPUID=0,&MASTER=NO,&AUTO=YES
.*                                                             @OY22597
         GBLA  &IEDQAR(64)
         GBLB  &IEDQZA(64)
         GBLC  &IEDQSV(8)
         LCLA  &L(200),&A,&B,&W,&H,&C
         LCLC  &V,&S(10)
         LCLB  &NO,&MT                                           S99244
.*  CHANGE ACTIVITY AS FOLLOWS
.******************* MICROFICHE FLAGS *********************** SUPT CODE
.*A549000                                                        S99244
.*C018000,063000,168000-174000,189000,207000,270000,594000       S99244
.*A 057000,072000,252000                                        SA55936
.*C243000                                                       SA55936
.*C180000                                                      @OY11500
.*C018000,189000                                               @OY14062
.*A585000,591000                                               @OY15062
.*C018000,174000                                               @OY22597
&IEDQSV(4) SETC '&SYSECT'
         SPACE 2
         AIF   (&IEDQZA(33)).Z
         AIF   ('&NAME' NE '').C0
         MNOTE 12,'*** IHB076 MACRO NAME FIELD BLANK- NAME REQUIRED'
&NO      SETB  1
.C0      AIF   (&IEDQAR(1) NE 0).C0A
         MNOTE 12,'*** IHB305 TTABLE MACRO NOT PREVIOUSLY SPECIFIED'
&NO      SETB  1
.C0A     AIF   (K'&EOT EQ 2 OR '&EOT' EQ '').C0B
         MNOTE 12,'*** IHB002 INVALID EOT OPERAND SPECIFIED- &EOT'
&NO      SETB  1
.C0B     AIF   ('&MASTER' EQ 'NO' OR '&MASTER' EQ 'YES').C0C     S99244
         MNOTE 12,'*** IHB348 INVALID MASTER OPERAND - &MASTER'  S99244
&NO      SETB  1                                                 S99244
.C0C     ANOP                                                  @OY22597
         AIF   ('&PARM' EQ '').VAL                             @OY22597
         MNOTE 12,'*** IHB002 INVALID OPERAND SPECIFIED - &PARM'
&NO      SETB  1                                               @OY22597
.VAL     ANOP                                                  @OY22597
         AIF   ('&ORDER' EQ '').C0D                            @OY22597
         AIF   ('&ORDER'(K'&ORDER-1,1) NE ',').C0D               S99244
.Z       AIF   (&NO).NDE                                       @OY11500
IEDQSCBC CSECT
&NAME    INVLIST1 ORDER=&ORDER,EOT=&EOT,CPUID=&CPUID,MASTER=&MASTER,AUTX
               O=&AUTO                                         @XM05817
         AGO   .ND
.C0D     ANOP                                                    S99244
&A       SETA  N'&ORDER
&H       SETA  &A
         AIF   (&A NE 0).C1
         AIF   (&NO).NDE
IEDQSCBC CSECT
&NAME    DC    A(0)                     OUTPUT ONLY LINE        SA55936
         DC    A(&CPUID)
.ND      ANOP                                                    S99244
&IEDQSV(4) CSECT
.NDE     ANOP
         MEXIT
.C1      AIF   (&W GE K'&ORDER(1)).E1
&W       SETA  &W+1
         AIF   ('&ORDER(1)'(&W,1) NE '+' AND '&ORDER(1)'(&W,1) NE '-').*
               C1
&W       SETA  K'&ORDER(1)-&W
.C2      AIF   ('&ORDER(&A)' EQ '').E2
.C3      AIF   (&L(&A) GE K'&ORDER(&A) OR &L(&A) GT 8).E3
&L(&A)   SETA  &L(&A)+1
         AIF   ('&ORDER(&A)'(&L(&A),1) EQ '+').C4
         AIF   ('&ORDER(&A)'(&L(&A),1) NE '-').C3
&B       SETA  &B+1
.C4      AIF   (&L(&A) EQ 1).E3
         AIF   (K'&ORDER(&A)-&L(&A) EQ &W).C6
&NO      SETB  1
         MNOTE 12,'*** IHB307 INCONSISTENT LENGTH FOR &ORDER(&A)'
.C6      ANOP
&A       SETA  &A-1
         AIF   (&A GT 0).C2
         AIF   (&NO).ND
IEDQSCBC CSECT
         AIF   ((N'&ORDER/2)*2 EQ N'&ORDER).CNOP
&A       SETA  2
.CNOP    CNOP  &A,4
&A       SETA  &H
&C       SETA  82-&IEDQAR(1)
.L1      ANOP
&V       SETC  '&ORDER(&A)'(1,&L(&A)-1)
         DC    AL2((&V-IEDQTNT-&C)/&IEDQAR(1))
&A       SETA  &A-1
         AIF   (&A GT 0).L1
&NO      SETB  ('&EOT' NE '')
&MT      SETB  ('&MASTER' EQ 'YES')                              S99244
&A       SETA  (&W+3)/2
         AIF   (&W NE 0).AOK
&A       SETA  2
.AOK     ANOP
         AIF   ('&AUTO' EQ 'YES').AUTO                         @OY14062
&NAME    DC    AL1(&H),AL1(&H-&B),AL1(&A),B'&NO.0&MT.00000'      S99244
         AGO   .NOAUTO                                         @OY14062
.AUTO    ANOP                                                  @OY14062
&NAME    DC    AL1(&H),AL1(&H-&B),AL1(&A),B'&NO.0&MT.00001'    @OY14062
.NOAUTO  ANOP                                                  @OY14062
         DC    A(&CPUID)                                         S99244
&NO      SETB  0
&V       SETC  '-'
.LX      ANOP
&A       SETA  1
.L2      AIF   (&L(&A) EQ 0).L3
         AIF   ('&ORDER(&A)'(&L(&A),1) EQ '&V'(1,1)).L3
&C       SETA  0
.LOOP    AIF   (K'&ORDER(&A) EQ &L(&A)).LPEND
&S((&C/8)+1)  SETC  '&ORDER(&A)'(&L(&A)+&C+1,8)
&C       SETA  &C+8
         AIF   (&W GT &C).LOOP
.LPEND   AIF   (K'&ORDER(&A)-&L(&A) NE 0).KOK
         DC    X'DF'
         AGO   .IND
.KOK     ANOP
&C       SETA  (&W+1)/2
        DC   XL&C'&S(1)&S(2)&S(3)&S(4)&S(5)&S(6)&S(7)&S(8)&S(9)&S(10)'
.IND     DC    AL1(&A) .                INDEX
.L3      ANOP
&A       SETA  &A+1
         AIF   (&A LE &H).L2
         AIF   (&NO).L6
         AIF   ('&EOT' EQ '').FE
         DC    XL1'&EOT'
.FE      DC    X'FE'
         AIF   (&B EQ 0).ND
&NO      SETB  1
&V       SETC  '+'
         AGO   .LX
.L6      AIF   (&B LE 0 OR &A GT &H).ND
&B       SETA  &B-1
         AGO   .L2
.E3      MNOTE 12,'*** IHB002 INVALID ORDER OPERAND &A SPECIFIED -     *
                        &ORDER(&A)'
&NO      SETB  1
         AGO   .C6
.E2      MNOTE 12,'*** IHB001 ORDER OPERAND &A REQ''D- NOT SPECIFIED'
&NO      SETB  1
         AGO   .C6
.E1      MNOTE 12,'*** IHB002 INVALID ORDER OPERAND 1 SPECIFIED -      *
                       &ORDER(1)'
         MEND
