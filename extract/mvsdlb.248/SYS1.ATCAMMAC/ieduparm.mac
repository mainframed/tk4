         MACRO
&KEY     IEDUPARM &PARM
         GBLA  &IEDMAX,&IEDBTLN,&IEDLULN,&IEDPWLN
         GBLA  &IEDUDLN,&IEDRQLN,&IEDLEV
         GBLB  &IEDVBIN,&IEDKYIN,&IEDPSIN
         GBLC  &IEDDVAL,&IEDLKEY,&IEDVERB(64)
         GBLC  &IEDREQ(64),&IEDLVRB
         LCLA  &CTR                     COUNTER FOR LOOP CONTROL
         LCLA  &KEYLEN                  KEYWORD CHAR STRING LEN
         LCLB  &NOGO                    NO GENERATION SWITCH
         LCLC  &PFLAG(3)                POSITIONAL FLAG BYTE VALUE
         LCLC  &REQIN                   REQUIRED OPERAND INDICATOR
         GBLC  &IEDDISP(7)               DISPLACEMENT TO FIELD VALUE
         LCLC  &BTDISP                  BIT DISPLACEMENT WITHIN BYTE
.*                                       TO FIELD VALUE
         LCLC  &LABEL                   LABEL SAVE AREA
.*A000000-999999                                               @Y17XAFF
         AIF   ('&KEY' EQ '' AND '&PARM' EQ '').A0010
         AIF   ('&KEY' EQ '' OR '&PARM' EQ '').A0020           @YM06169
.A0010   ANOP
         MNOTE 12,'*** IEDU40 KEY AND PARM OPERANDS ARE MUTUALLY EXCLUS*
               IVE.'
&NOGO    SETB  1
.A0020   ANOP
         AIF   ('&IEDDVAL' EQ '').A0030
         IEDUVAL ,&IEDDVAL
.A0030   ANOP
         AIF   (&IEDVBIN).A0040
         MNOTE 12,'*** IEDU41 SEQUENCE ERROR, IEDUVERB MUST BE CODED.'
&NOGO    SETB  1
.A0040   ANOP
         AIF   (&NOGO).A0990
         AIF   ('&KEY' EQ '').A0062
&LABEL   SETC  'USS&SYSNDX'
         AIF   (&IEDKYIN).A0050
         ORG   &IEDLVRB+5
         DC    AL4(&LABEL) .            ADDR TO FIRST KEYWORD ENTRY
         ORG
         AGO   .A0060
.A0050   ANOP
         SPACE
         ORG   &IEDLKEY+1
         DC    AL4(&LABEL) .            ADDR TO NEXT KEYWORD ENTRY
         ORG
.A0060   ANOP
&LABEL   EQU   *
         DC    AL1(USSFKEY) .           KEYWORD ENTRY FLAG BYTE
         DC    AL4(0) .                 NEXT KEYWORD ADDR
&KEYLEN  SETA  K'&KEY
         DC    AL1(&KEYLEN) .           KEYWORD CHAR STRING COUNT
         DC    C'&KEY' .                KEYWORD CHAR STRING
         SPACE
&IEDKYIN   SETB  1                        KEYWORD HAS BEEN CODED
&IEDLKEY  SETC  '&LABEL'                 UPDATE LAST KEYWORD ENTRY NAME
         AGO   .A0990
.A0062   ANOP
&CTR     SETA  1                   CHECK FOR &PARM VALIDITY
.A0065   AIF   (&CTR GT 64).A0070
         AIF   ('&IEDVERB(&CTR)' NE '').A0080
.A0070   MNOTE 12,'*** IEDU42 INVALID PARM FOR DEFINED VERB.'
&NOGO    SETB  1
         AGO   .A0100
.A0080   AIF   ('&IEDVERB(&CTR)' EQ '&PARM').A0090
&CTR     SETA  &CTR+1
         AGO   .A0065
.A0090   ANOP                      INSURE THAT IF CODED AGAIN, MNOTE
&IEDVERB(&CTR) SETC '0'                     WILL BE GENERATED
.A0100   ANOP
         AIF   (&NOGO).A0990       IF ANY ERROR, NO GENERATION
&CTR     SETA  1                   CHECK FOR &PARM REQUIRED
&REQIN   SETC  '0'
.A0110   AIF   ('&IEDREQ(&CTR)' EQ '').A0130
         AIF   (&CTR GT 64).A0130
         AIF   ('&IEDREQ(&CTR)' NE '&PARM').A0120
&IEDREQ(&CTR) SETC '0'                CHANGE VALUE FOR CHECK MADE IN
&REQIN   SETC  'USSPREQ'            IEDUVERB TO INSURE ALL REQUIRED
         AGO   .A0130               PARMS ARE CODED
.A0120   ANOP
&CTR     SETA  &CTR+1
         AGO   .A0110
.A0130   ANOP
&IEDDISP(2) SETC '0'                INITIALIZE FOR ZERO DISPLACEMENT
&IEDDISP(3) SETC '0'
&IEDDISP(4) SETC '0'
         AIF   ('&PARM' NE 'INMODE').A0140
&IEDDISP(1) SETC 'XINSMODE'         SET DISPLACEMENT VALUE
&BTDISP  SETC  '0'                 NO BIT DISPL
&IEDBTLN   SETA  8*8                 CALCULATE BIT LEN
&PFLAG(1) SETC 'USSPCHAR'          SET  CHAR FIELD
&PFLAG(2) SETC '0'
&PFLAG(3) SETC '0'
&IEDMAX     SETA  &IEDMAX-8              DECR MAX
&IEDDVAL   SETC  'C'' '''            SET DEFAULT VALUE
         AGO   .A0900
.A0140   ANOP
         AIF   ('&PARM' NE 'INLU').A0150
         AIF   (&IEDLULN NE 0).A0145
.A0142   MNOTE 12,'*** IEDU42 &PARM IS INVALID BECAUSE ITS LENGTH IS ZE*
               RO.'
         AGO   .A0990
.A0145   ANOP
&IEDDISP(1) SETC 'XINSNLEN'         SET DISPLACEMENT VALUE
&BTDISP  SETC  '0'                 NO BIT DISPL
&IEDBTLN   SETA  &IEDLULN*8            CALCULATE BITLEN
&PFLAG(1) SETC 'USSPCHAR'          SET CHAR FIELD
&PFLAG(2) SETC 'USSPINS'               LEN FIELD MUST BE INSERTED
&PFLAG(3) SETC '0'
&IEDMAX     SETA  &IEDMAX-&IEDLULN-1       DECR MAX
&IEDDVAL   SETC  ''                  SET DEFAULT VALUE
         AGO   .A0900
.A0150   ANOP
         AIF   ('&PARM' NE 'INFORM').A0160
&IEDDISP(1) SETC 'XINSFMTP'         SET DISPLACEMENT VALUE
&BTDISP  SETC  '0'                 NO BIT DISPL
&IEDBTLN   SETA  4                   CALCULATE BIT LEN
&PFLAG(1) SETC 'USSPNOIN'          SET NOINPUT REQUIRED
&PFLAG(2) SETC 'USSPBIN'               BINARY DATA FIELD
&PFLAG(3) SETC '0'
&IEDMAX     SETA  &IEDMAX-0              DECR MAX
&IEDDVAL   SETC  'B''0000'''         SET DEFAULT VALUE
         AGO   .A0900
.A0160   ANOP
         AIF   ('&PARM' NE 'INTYPE').A0170
&IEDDISP(1) SETC 'XINSFMTP'         SET DISPLACEMENT VALUE
&BTDISP  SETC  '4'                 SET BIT DISPLACEMENT
&IEDBTLN   SETA  4                   SET BIT LEN
&PFLAG(1) SETC 'USSPNOIN'          SET NOINPUT REQUIRED
&PFLAG(2) SETC 'USSPBIN'               BINARY DATA FIELD
&PFLAG(3) SETC '0'
&IEDMAX     SETA  &IEDMAX-1              DECR MAX
&IEDDVAL   SETC  'B''0000'''         SET DEFAULT VALUE
         AGO   .A0900
.A0170   ANOP
         AIF   ('&PARM' NE 'INREQID').A0180
         AIF   (&IEDRQLN EQ 0).A0142
&IEDDISP(1) SETC 'XINSRQID'         SET DISPLACEMENT
&IEDDISP(2) SETC '&IEDLULN'            VALUE
&BTDISP  SETC  '0'                 SET BIT DISPL
&IEDBTLN   SETA  &IEDRQLN*8            SET BIT LEN
&PFLAG(1) SETC 'USSPCHAR'          SET CHAR FIELD
&PFLAG(2) SETC 'USSPINS'               INSERT LENGTH
&PFLAG(3) SETC '0'
&IEDMAX     SETA  &IEDMAX-&IEDRQLN-1       DECR MAX
&IEDDVAL   SETC  'C'' '''            SET DEFAULT VALUE
         AGO   .A0900
.A0180   ANOP
         AIF   ('&PARM' NE 'INPWRD').A0190
         AIF   (&IEDPWLN EQ 0).A0142
&IEDDISP(1) SETC 'XINSPWRD'         SET DISPLACEMENT
&IEDDISP(2) SETC '&IEDLULN'            VALUE
&IEDDISP(3) SETC '&IEDRQLN'
&BTDISP  SETC  '0'                 SET BIT DISPL
&IEDBTLN   SETA  &IEDPWLN*8            SET BIT LEN
&PFLAG(1) SETC 'USSPCHAR'          SET CHAR FIELD
&PFLAG(2) SETC 'USSPINS'               INSERT LENGTH
&PFLAG(3) SETC '0'
&IEDMAX     SETA  &IEDMAX-&IEDPWLN-1       DECR MAX
&IEDDVAL   SETC  'C'' '''            SET DEFAULT VALUE
         AGO   .A0900
.A0190   ANOP
         AIF   ('&PARM' NE 'INUDATA').A0200
         AIF   (&IEDUDLN EQ 0).A0142
&IEDDISP(1) SETC 'XINSUFLD'         SET DISPLACEMENT
&IEDDISP(2) SETC '&IEDLULN'            VALUE
&IEDDISP(3) SETC '&IEDRQLN'
&IEDDISP(4) SETC '&IEDPWLN'
&BTDISP  SETC  '0'                 SET BIT DISPL
&IEDBTLN   SETA  &IEDUDLN*8            SET BIT LEN
&PFLAG(1) SETC 'USSPCHAR'          SET CHAR FIELD
&PFLAG(2) SETC 'USSPINS'               INSERT LENGTH
&PFLAG(3) SETC '0'
&IEDMAX     SETA  &IEDMAX-&IEDUDLN-1       DECR MAX
&IEDDVAL   SETC  'C'' '''            SET DEFAULT VALUE
         AGO   .A0900
.A0200   ANOP
         AIF   ('&PARM' NE 'TMTYPE').A0210
&IEDDISP(1) SETC 'XTMSTYPE'         SET DISPLACEMENT VALUE
&BTDISP  SETC  '0'                 SET BIT DISPL
&IEDBTLN   SETA  1*8                 SET BIT LENGTH
&PFLAG(1) SETC 'USSPBIN'           SET BINARY FIELD
&PFLAG(2) SETC 'USSPNOIN'              NO INPUT REQUIRED
&PFLAG(3) SETC '0'
&IEDMAX     SETA  &IEDMAX-1              DECR MAX
&IEDDVAL   SETC  'X''10'''           SET DEFAULT VALUE
         AGO   .A0900
.A0210   ANOP
         AIF   ('&PARM' NE 'TMLU').A0220
         AIF   (&IEDLULN EQ 0).A0142
&IEDDISP(1) SETC 'XTMSPLUN'         SET DISPLACEMENT VALUE
&BTDISP  SETC  '0'                 SET BIT DISPL
&IEDBTLN   SETA  &IEDLULN*8            SET BIT LENGTH
&PFLAG(1) SETC 'USSPCHAR'          SET CHAR FIELD
&PFLAG(2) SETC 'USSPINS'               INSERT LENGTH FIELD
&PFLAG(3) SETC '0'
&IEDMAX     SETA  &IEDMAX-&IEDLULN-1       DECR MAX
&IEDDVAL   SETC  ''                  SET DEFAULT VALUE
         AGO   .A0900
.A0220   ANOP
         AIF   ('&PARM' NE 'INRES').A0230
&IEDDISP(1) SETC 'XINSNTYP'         SET DISPLACEMENT VALUE
&BTDISP  SETC  '0'                 SET BIT DISPL
&IEDBTLN   SETA  1*8                 SET BIT LENGTH
&PFLAG(1) SETC 'USSPCHAR'          SET CHAR FIELD
&PFLAG(2) SETC 'USSPNOIN'              NO INPUT REQUIRED
&PFLAG(3) SETC '0'
&IEDMAX     SETA  &IEDMAX-1              DECR MAX
&IEDDVAL   SETC  'C''3'''            SET DEFAULT VALUE
         AGO   .A0900
.A0230   ANOP
&IEDDISP(1) SETC 'XTMSPTYP'         SET DISPLACEMENT VALUE
&BTDISP  SETC  '0'                 SET BIT DIPLACEMENT
&IEDBTLN   SETA  1*8                 SET BIT LENGTH
&PFLAG(1) SETC 'USSPBIN'           SET BINARY FIELD
&PFLAG(2) SETC 'USSPNOIN'              NO INPUT REQUIRED
&PFLAG(3) SETC '0'
&IEDMAX     SETA  &IEDMAX-1              DECR MAX VALUE
&IEDDVAL   SETC  'C''3'''            SET DEFAULT
.A0900   AIF   (&IEDMAX GE 0).A0910
         MNOTE 12,'*** IEDU42 DEFINED AREA LARGER THAN 256.'
.A0910   ANOP
&IEDPSIN   SETB  1
&IEDLEV     SETA  0
         SPACE
         DC    AL1(USSFPOS) .           POSITIONAL ENTRY IND
         DC    AL1(&PFLAG(1)+&PFLAG(2)+&PFLAG(3)+&REQIN)
*                                       POSITIONAL FLAG
         DC    AL1(&IEDDISP(1)+&IEDDISP(2)+&IEDDISP(3)+&IEDDISP(4)-&IED*
               DISP(7)) .               FIELD DISPLACEMENT (BYTE)
         DC    AL1(&BTDISP) .           BIT DISPLACEMENT
         DC    AL2(&IEDBTLN) .            BIT LENGTH           @PI0615
.A0990   ANOP
         MEND
