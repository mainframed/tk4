         MACRO
&NAME    MSGLIMIT &LIM
         GBLA  &IEDQZE(64)
         GBLB  &IEDQZA(64)
         LCLB  &INBLK                                            S22025
.*       VERSION DATED JANUARY 10,1973                          SA59976
.*C052000,079000                                                 S22029
.*D063000-072000                                                 S22029
.*A274000,282000,360000                                         SA59976
.*D278000,286000,292000,296000-298000,366000                    SA59976
.*C052000                                                       SA59976
         SPACE
         AIF   (&IEDQZA(3) OR &IEDQZA(4) OR &IEDQZA(14)).CHECK   S22029
         MNOTE 12,'*** IHB070 SEQUENCE ERROR-MUST FOLLOW INHDR,OUTHDR  C
               OR INBLOCK MACRO'                                 S22025
         AGO   .MEND
.CHECK   AIF   ('&LIM' NE '').LIMCHK
         MNOTE 12,'***  IHB001  LIMIT OPERAND REQ''D-NOT SPECIFIED'
         AGO   .MEND
.LIMCHK  AIF   (K'&LIM GT 8).LIMERR
         AIF   ('&LIM'(K'&LIM,1) EQ ')').LIMERR
         AIF   ('&LIM'(K'&LIM,1) EQ '''' AND '&LIM'(1,1) NE 'X').LIMERR
         AIF   ('&LIM' EQ '' OR '&LIM' EQ '0').LIMERR
         AIF   ('&LIM'(K'&LIM,1) NE '''' AND (('&LIM'(1,1) GE 'A' AND '*
               &LIM'(1,1) LE 'Z') OR '&LIM'(1,1) EQ '@' OR '&LIM'(1,1) *
               EQ '#' OR '&LIM'(1,1) EQ '$')).ORG
         AIF   ('&LIM' EQ 'X''00''' OR '&LIM' EQ 'X''0''' OR '&LIM' EQ *
               'XL1''00''' OR '&LIM' EQ 'XL1''0''').LIMERR
         IEDQMASK &LIM,1
         AIF   (NOT &IEDQZA(24)).ORG
.LIMERR  MNOTE 12,'***  IHB300  LIMIT OPERAND INVALID AS SPECIFIED'
         AGO   .MEND
.ORG     AIF   ('&NAME' EQ '').NON
&NAME    DS    0H
.NON     ANOP                                                    S22025
&INBLK   SETB  (&IEDQZA(14))                                     S22025
         L     1,IEDADBUF .             BUFFER ADDRESS          SA59976
         AIF   (NOT &INBLK).NOINBLK                              S22025
         TM    8(1),X'40' .             IS THIS A HDR CHGD TO TXTS22025
         BO    *+12 .                   YES, EXECUTE MSGLIMIT    S22025
.NOINBLK ANOP                                                   SA59976
         TM    20(1),X'81' .            HEADER BUFFER            S22025
         AIF   ('&LIM'(1,1) EQ 'X' AND '&LIM'(K'&LIM,1) EQ '''').XLIM
         AIF   ('&LIM'(1,1) GE '0' AND '&LIM'(1,1) LE '9').XLIM
         BNZ   *+50 .                   NO                       S22025
         IEDQVCON (31,20),(IEDQAE,IEDQAG)
.LOC     LOCOPT &LIM
         AIF   (&IEDQZA(61)).MEND
         LTR   1,15 .                   OPTION FIELD FOUND
         LA    15,4 .                   SET RETURN
         BZ    *+26 .                   NO - DO NOT EXECUTE
         MVC   *+11(1),0(1) .           MOVE LIMIT TO LIST
         AGO   .NOTINB                                          SA59976
.XLIM    ANOP                                                    S22025
         BNZ   *+20 .                   NO                       S22025
.NOTINB  ANOP                                                    S22025
         BAL   1,*+6                                             S22025
         DC    AL1(0)
.SET     AIF   ('&LIM'(K'&LIM,1) EQ '''').X
         DC    AL1(&LIM) .              LIMIT VALUE
         AGO   .LOAD
.X       DC    &LIM .                   LIMIT VALUE
.LOAD    L     15,IEDMSGS-1 .           TO LIMIT ROUTINE
         IEDQVCON 20,IEDQAG
         L     15,&IEDQZE(20)(,15)
         BALR  14,15
.MEND    SPACE 2
         MEND
