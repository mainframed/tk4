         MACRO
&NAME    MSGTYPE &PAR,&BLANK=YES,&TABLE=,&EXIT=                  S22027
.*A031000,198200-206600                                          S22027
.*C018000,164000                                                 S22027
.*C198880-198920                                                SA56318
.*D045000,063000,199960,200080,495000-882000                   @OZ38203
.*C154000,203000-203400,203880-203960,204120                   @OZ38203
.*C202800                                                      @OZ41384
         GBLA  &IEDQZD
         GBLA  &IEDQZE(64)                                       S22027
         GBLB  &IEDQZA(64)
         GBLC  &IEDQZB,&IEDQZC,&IEDQZJ
         LCLA  &CNT
         LCLB  &NOGO
         LCLC  &TEMP
.*             VERSION DATED JUNE 7, 1972                        S22027
         SPACE
         AIF   (NOT &IEDQZA(9)).TSOCHK                              TSO
         MNOTE 12,'***  IHB312  MUST FOLLOW STARTMH MACRO WITH TSOMH=NO*
               '
         AGO   .END                                                 TSO
.TSOCHK  ANOP
         AIF   (&IEDQZA(3) OR &IEDQZA(4)).CKTABLE                S22027
         MNOTE 12,'***  IHB070  SEQUENCE ERROR-MUST FOLLOW INHDR OR OUT*
               HDR MACRO'
         AGO   .MEND
.CKTABLE ANOP                                                    S22027
         AIF   ('&TABLE' EQ '' AND '&EXIT' EQ '').CHECK          S22027
         AIF   ('&TABLE' NE '').TABCHAR                          S22027
         MNOTE 12,'***  IHB001  TABLE OPERAND REQUIRED - NOT SPECIFIED'
.*                                                               S22027
         AGO   .CKEXIT                                           S22027
.TABCHAR ANOP                                                    S22027
         AIF   ('&TABLE'(1,1) LE 'Z').CKEXIT                     S22027
         MNOTE 12,'***  IHB300  TABLE OPERAND INVALID AS SPECIFIED'
.CKEXIT  ANOP                                                    S22027
         AIF   ('&EXIT' NE '').EXCHAR                            S22027
         MNOTE 12,'***  IHB001  EXIT OPERAND REQUIRED - NOT SPECIFIED'
.*                                                               S22027
         AGO   .CKPAR                                            S22027
.EXCHAR  ANOP                                                    S22027
         AIF   ('&EXIT'(1,1) LE 'Z').CKPAR                       S22027
         MNOTE 12,'***  IHB300  EXIT OPERAND INVALID AS SPECIFIED'
.CKPAR   ANOP                                                    S22027
         AIF   ('&PAR' EQ '').CKBLANK                            S22027
         MNOTE 12,'*** IED905 MSGTYPE OPERAND INCONSISTENT WITH TABLE O*
               R EXIT OPREAND'                                  SA56318
.CKBLANK ANOP                                                    S22027
         AIF   ('&BLANK' NE 'YES' AND '&BLANK' NE '').CKNO       S22027
&IEDQZE(40) SETA 0                                               S22027
         AGO   .CKNOGO                                           S22027
.CKNO    ANOP                                                    S22027
         AIF   ('&BLANK' EQ 'NO').SET2                           S22027
         MNOTE 12,'***  IHB300  BLANK OPERAND INVALID AS SPECIFIED'
.*                                                               S22027
         AGO   .CKNOGO                                           S22027
.SET2    ANOP                                                    S22027
&IEDQZE(40) SETA 2                                               S22027
.CKNOGO  ANOP                                                    S22027
         AIF   (NOT &IEDQZA(20)).EXPAND                          S22027
         AIF   (&IEDQZA(4)).SEND                                 S22027
         B     &IEDQZB                                           S22027
         AGO   .DS                                               S22027
.SEND    B     &IEDQZC                                           S22027
.DS      ANOP                                                    S22027
&IEDQZJ  DS    0H                                                S22027
&IEDQZA(20) SETB 0                                               S22027
&IEDQZJ  SETC  ''                                                S22027
.EXPAND  ANOP                                                    S22027
&NAME    DS    0H                                                S22027
         CNOP  0,4                                               S22027
         BAL   1,*+16 .                 BRANCH AROUND DCS        S22027
         DC    A(&TABLE) .              ADDR OF TYPETABLE        S22027
         DC    A(&EXIT) .               ADDR OF NO MATCH EXIT    S22027
         DC    AL1(&IEDQZE(30)+&IEDQZE(40)) . INDEX TO IEDQAI    S22027
         DC    AL1(4) .                 LENGTH OF PARM LIST      S22027
         DC    AL1(16)                  REG 15 OFFSET            S22027
         DC    AL1(1) .                 LNTH OF SCAN             S22027
         LA    1,8(,1) .                POINT R1 TO AI PARM      S22027
         L     15,IEDUI .               LOAD UI ADDR             S22027
         BALR  14,15 .                  GO TO IEDQAI             S22027
         LA    0,8 .                    LOAD OFFSET TO AI PARM   S22027
         SR    1,0 .                    POINT R1 TO BEGIN PARM   S22027
         LTR   15,15 .                  COMPLETED OKAY           S22027
         BNP   *+42                     BRANCH IF NOT          @OZ41384
         L     14,IEDADBUF  .           GET BUFR ADR           @OZ39203
         STH   15,24(,14)  .            PUT SCAN IN BUFR       @OZ39203
         LA    15,11(15,14)             GET SCAN PNTR ADR      @OZ39203
         L     14,0(,1) .               GET TABLE ADDR           S22027
         TM    3(14),1 .                LAST ENTRY               S22027
         BNO   *+26 .                   BRANCH IF NOT          @OZ39203
         L     14,IEDADBUF              GET BUFR ADR           @OZ39203
         LH    15,24(,14)  .            GET SCAN PNTR          @OZ39203
         BCTR  15,0 .                   RESET PNTR BACK 1        S22027
         STH   15,24(,14)  .            PUT SCAN IN BUFR       @OZ30203
         L     14,4(,1) .               GET NO MATCH EXIT        S22027
         B     *+26 .                   DEPART                   S22027
         CLC   0(1,14),0(15) .          COMP SCANNED TO TABLE    S22027
         BE    *+12 .                   MATCHED                  S22027
         LA    14,4(14) .               BUMP PNTR TO NEXT ENTRY  S22027
         B     50(,1) .                 GO TO COMPARE            S22027
         L     14,0(,14) .              GET USER RTNE            S22027
         AIF   (&IEDQZA(4)).OUT                                  S22027
         LA    2,&IEDQZB .              ADR OF NEXT SUBGROUP     S22027
         AGO   .USER                                             S22027
.OUT     LA    2,&IEDQZC .              ADR OF NEXT SUBGROUP     S22027
.USER    BR    14 .                     GO TO USER RTNE          S22027
         MEXIT                                                   S22027
.CHECK   AIF   ('&PAR' EQ '').SET
         IEDQCHAR 8,&PAR
         AIF   (&IEDQZA(24)).ERR
         AIF   (&IEDQZD GT 8).ERR
         AGO   .SET
.ERR     MNOTE 12,'***  IHB300  TYPE OPERAND INVALID AS SPECIFIED'
         AGO   .MEND
.SET     AIF   (NOT &IEDQZA(20)).PARM
         AIF   (&IEDQZA(4)).SND
         B     &IEDQZB
         AGO   .EX
.SND     B     &IEDQZC
.EX      AIF   ('&IEDQZJ' EQ '').PARM
&IEDQZJ  DS    0H
.PARM    ANOP
&IEDQZJ  SETC  'IEDX'.'&SYSNDX'
         AIF   ('&NAME' EQ '').FT
&NAME    DS    0H
.FT      AIF   ('&PAR' EQ '').MEND
.SCN     IEDQSCAN &IEDQZD,&PAR,&BLANK
         AIF   (&IEDQZA(24)).MEND
         LTR   15,15
         BNP   &IEDQZJ
         L     1,IEDADBUF .             GET BUFFER ADDRESS
         STH   15,24(,1) .              PUT SCAN IN BUFFER
.MEND    SPACE 2
&IEDQZA(20) SETB 1
.END     MEND
