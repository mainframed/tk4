         MACRO
         IEDUVAL &CHAR,&VAL
         GBLA  &IEDLEV,&IEDBTLN
         GBLC  &IEDDVAL
         LCLA  &FAC                     FACTOR FOR BIT CALCULATION
         LCLA  &CHARLEN                 CHARACTER STRING LENGTH
         LCLA  &CTR                     COUNTER FOR LOOP CONTROL
         LCLA  &MAXVAL                  MAXIMUM VALUE FOR FIELD
         LCLB  &NOGO                    NO GENERATION SWITCH
         LCLC  &VGEN(3)                 VALUE OF GENERATION VALUE
         LCLA  &VLEN                    LEN OF VALUE GENERATION (BYTE)
.*A000000-999999                                               @Y17XAFF
         AIF   ('&VAL' EQ '').A0170
         AIF   ('&CHAR' EQ '').A0030
         AIF   (&IEDLEV LE 1).A0010
         MNOTE 12,'*** IEDU60 INVALID SEQUENCE OF IEDUVAL MACRO.'
&NOGO    SETB  1
         AGO   .A0040
.A0010   AIF   (K'&CHAR LE 8).A0020
         MNOTE 12,'*** IEDU61 INVALID CHAR OPERAND.'
&NOGO    SETB  1
         AGO   .A0040
.A0020   ANOP
&IEDLEV     SETA  1
         AGO   .A0040
.A0030   AIF   (&IEDLEV LT 3).A0035   CAN ONLY BE ONE DEFAULT ENTRY
         MNOTE 12,'*** IEDU60 INVALID SEQUENCE OF IEDUVAL MACRO.'
&NOGO    SETB  1
         AGO   .A0040
.A0035   ANOP
&IEDLEV     SETA  3
.A0040   ANOP
         AIF   ('&VAL'(1,2) NE 'B''').A0050
&FAC     SETA  1
         AGO   .A0070
.A0050   AIF   ('&VAL'(1,2) NE 'X''').A0060
&FAC     SETA  4
         AGO   .A0070
.A0060   AIF   ('&VAL'(1,2) NE 'C''').A0100
&FAC     SETA  8
.A0070   ANOP                           LAST CHAR SHOULD BE '
         AIF   ('&VAL'(K'&VAL,1) EQ '''').A0080
         MNOTE 12,'*** IEDU62 INVALID VAL OPERAND, NEED ENDING QUOTE.'
&NOGO    SETB  1
         AGO   .A0200
.A0080   ANOP                           LENGTH OF VALUE OPERAND MUST
&VLEN    SETA  ((((K'&VAL-3)*&FAC)+7)/8)   FIT IN THE REPL FIELD
         AIF   ((&IEDBTLN+7)/8 GE &VLEN).A0090
         MNOTE 12,'*** IEDU63 VAL SPECIFICATION INVALID FOR TARGET FIEL*
               D.'
&NOGO    SETB  1
         AGO   .A0200
.A0090   ANOP
&VGEN(1) SETC  '&VAL'
         AGO   .A0200
.A0100   ANOP
&CTR     SETA  1
.A0110   AIF   (&CTR GT K'&VAL).A0130
         AIF   ('&VAL(&CTR)' LT '0').A0120
&CTR     SETA  &CTR+1
         AGO   .A0110
.A0120   ANOP
         MNOTE 12,'*** IEDU64 IF VAL OPERAND IS NOT BINARY, HEX, OR CHA*
               R, IT MUST BE NUMERIC.'
&NOGO    SETB  1
         AGO   .A0200
.A0130   ANOP
&VLEN    SETA  (&IEDBTLN+7)/8             CALCULATE LENGTH OF FIELD
         AIF   (&VLEN GT 4).A0155       ONLY ALLOW NUM FOR 4 BYTE FLDS
&MAXVAL  SETA  1                        CALCULATE MAXIMUM VALUE
&CTR     SETA  1                         FROM FIELD'S BIT LENGTH
.A0140   AIF   (&CTR GT &IEDBTLN).A0150
&MAXVAL  SETA  &MAXVAL*2
&CTR     SETA  &CTR+1
         AGO   .A0140
.A0150   ANOP
         AIF   (&VAL LT &MAXVAL).A0160
.A0155   MNOTE 12,'*** IEDU63 VAL SPECIFICATION INVALID FOR TARGET FIEL*
               D.'
&NOGO    SETB  1
         AGO   .A0200
.A0160   ANOP
&VGEN(1) SETC  'AL&VLEN.('
&VGEN(2) SETC  '&VAL'
&VGEN(3) SETC  ')'
         AGO   .A0200
.A0170   ANOP
         AIF   ('&CHAR' EQ '').A0180
         MNOTE 12,'*** IEDU65 IF VAL IS NOT CODED, CHAR SHOULD NOT BE C*
               ODED.'
&NOGO    SETB  1
         AGO   .A0200
.A0180   ANOP                      INSURE THAT ONLY ONE USER INPUT
         AIF   (&IEDLEV LT 2).A0190    VALUE ENTRY/PARM IS REQUESTED
         MNOTE 12,'*** IEDU60 INVALID SEQUENCE OF IEDUVAL MACRO.'
&NOGO    SETB  1
         AGO   .A0200
.A0190   ANOP
&IEDLEV     SETA  2
.A0200   AIF   (&NOGO).A0990
         DC    AL1(USSFVAL) .           VALUE ENTRY INDICATOR
         AIF   ('&CHAR' EQ '').A0210
&CHARLEN SETA  K'&CHAR
         DC    AL1(&CHARLEN) .          CHAR STRING LENGTH
         AGO   .A0220
.A0210   ANOP
         DC    AL1(0) .                 NO CHAR STRING
         AIF   ('&VAL' EQ '').A0220     IF DEFAULT ENTRY,
&IEDDVAL   SETC  ''                        INITIALIZE DEFAULT VALUE
.A0220   ANOP
         AIF   ('&VAL' EQ '').A0230
         DC    AL1(&VLEN) .             VALUE STRING LENGTH
         AGO   .A0240
.A0230   ANOP
         DC    AL1(0) .                 NO VALUE STRING
.A0240   ANOP
         AIF   ('&CHAR' EQ '').A0250
         DC    C'&CHAR' .               CHAR STRING
.A0250   ANOP
         AIF   ('&VAL' EQ '').A0260
         DC    &VGEN(1)&VGEN(2)&VGEN(3) . VALUE STRING
.A0260   ANOP
.A0990   ANOP
         MEND
