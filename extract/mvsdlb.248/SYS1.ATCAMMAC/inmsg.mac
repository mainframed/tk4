         MACRO
&NAME    INMSG &PATH=
         GBLA  &IEDQZE(64)
         GBLB  &IEDQZA(64),&IEDQRN(8)                         @YM07267
         GBLC  &IEDQZB,&IEDQZJ
         LCLB  &NOGO
.*             VERSION DATED OCTOBER 22,1976
         SPACE
&IEDQZA(6) SETB 1
&IEDQRN(5) SETB 0                 RESET MSGGEN/IEDRESP FLAG    @YM07267
        AIF    (&IEDQZA(1)).OK
         MNOTE 12,'***  IHB070  SEQUENCE ERROR - INCOMING GROUP MUST PR*
               ECEDE OUTGOING GROUP AND FOLLOW STARTMH AND INHDR MACRO'
&NOGO    SETB  1
.OK      AIF   ('&PATH' EQ '').CHK
         AIF   ('&PATH(1)' EQ '' OR '&PATH(2)' EQ '').ERR
         AIF   ('&PATH(2)' EQ '0' OR '&PATH(2)' EQ 'X''0''' OR '&PATH(2*
               )' EQ 'X''00''' OR '&PATH(2)'(1,2) EQ 'XL').ERR
         IEDQMASK &PATH(2),1
         AIF   (NOT &IEDQZA(24)).CHK
.ERR     MNOTE 12,'***  IHB300  PATH OPERAND INVALID AS SPECIFIED'
&NOGO    SETB  1
.CHK     AIF   (&NOGO).MEND
         AIF   (NOT &IEDQZA(27)).BITS
         DC    X'0100'
.BITS    AIF   ('&NAME' EQ '').ZJ
&NAME    DS    0H
.ZJ      AIF   (NOT &IEDQZA(20) OR '&IEDQZJ' EQ '').NON
&IEDQZJ  DS    0H
.NON     ANOP
&IEDQZA(1) SETB 1
&IEDQZA(2) SETB 0
&IEDQZA(3) SETB 0
&IEDQZA(4) SETB 0
&IEDQZB  DS    0H
&IEDQZB  SETC  'IEDR'.'&SYSNDX'
         AIF   ('&PATH' EQ '').TRANS
&IEDQZA(5) SETB 0                                              @YM06984
         LOCOPT &PATH(1)
         AIF   (&IEDQZA(61)).MEND
         LTR   15,15 .                  OPFIELD FOUND
         BZ    &IEDQZB .                NO - GO TO NEXT DELIM
         TM    0(15),&PATH(2) .         PATH SWITCHES ON
         BZ    &IEDQZB .                NO - GO TO NEXT DELIM
.TRANS   CNOP  0,4
&IEDQZA(5) SETB 1                                              @YM06984
         AIF   ('&PATH' NE '').BAL
&IEDQZA(10) SETB 1
.BAL     L     15,IEDUI .               INMSG/OUTMSG ROUTINE
         BALR  1,15
         DC    AL1(&IEDQZE(33)),AL1(2)
.MEND    SPACE 2
&IEDQZJ  SETC  ''
&IEDQZA(6) SETB 0
&IEDQZA(18) SETB 0
&IEDQZA(20) SETB 0
&IEDQZA(27) SETB 1
         MEND
