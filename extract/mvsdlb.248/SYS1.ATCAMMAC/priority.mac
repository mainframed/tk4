         MACRO
&NAME    PRIORITY &LEV,&CHARS,&BLANK=YES
         GBLA  &IEDQZD
         GBLB  &IEDQZA(64)
         GBLC  &IEDQZB
         LCLB  &NOGO
         LCLC  &PARM
.*C280000                                                      @OZ09935
.*             VERSION DATED JULY 14,1976                      @OZ09935
         SPACE
         AIF   (NOT &IEDQZA(9)).TSOCHK                              TSO
         MNOTE 12,'***  IHB312  MUST FOLLOW STARTMH MACRO WITH TSOMH=NO*
               '
&NOGO    SETB  1
.TSOCHK  ANOP                                                       TSO
&PARM    SETC  'IEDT'.'&SYSNDX'
         AIF   (&IEDQZA(3)).CHECK
         MNOTE 12,'***  IHB070  SEQUENCE ERROR-MUST FOLLOW INHDR MACRO'
&NOGO    SETB  1
.CHECK   AIF   ('&LEV' EQ '').CHARCHK
         AIF   ('&LEV'(K'&LEV,1) EQ '''').LENERR
         IEDQMASK &LEV,1
         AIF   (NOT &IEDQZA(24)).CHARCHK
.LENERR  MNOTE 12,'***  IHB300  LEVEL OPERAND INVALID AS SPECIFIED'
&NOGO    SETB  1
.CHARCHK ANOP                                                  @OZ09935
         CNOP  0,4                                             @OZ09935
&NAME    DS    0H                                              @OZ09935
.NON     AIF   ('&CHARS' EQ '').DOIT
         IEDQCHAR 8,&CHARS
         AIF   (&IEDQZA(24)).ERR
         AIF   (&IEDQZD LE 8).ORG
.ERR     MNOTE 12,'***  IHB300  CHARACTER OPERAND INVALID AS SPECIFIED'
&NOGO    SETB  1
.ORG     AIF   (&NOGO).MEND
         IEDQSCAN &IEDQZD,&CHARS,&BLANK
         AIF   (&IEDQZA(24)).MEND
         LTR   15,15
         BNP   &PARM
         L     1,IEDADBUF .             GET BUFFER ADDRESS
         STH   15,24(,1) .              PUT SCAN IN BUFFER
.DOIT    AIF   (&NOGO).MEND
         AIF   ('&LEV' NE '').PLUSP
.GO      ANOP
&IEDQZA(8) SETB 1
         IEDQSCAN 1,,&BLANK
&IEDQZA(8) SETB 0
         AIF   (&IEDQZA(24)).MEND
         LTR   15,15
         BM    &PARM
         AGO   .ICSTC
.PLUSP   MVI   IEDDOUBL,&LEV
.ICSTC   L     15,IEDADBUF .            GET BUFFER ADDRESS FROM AVT
         TM    20(15),X'01' .           BYPASS IF NOT FIRST HEADER BFR
         BO    &PARM
         IC    1,IEDDOUBL .             GET PRIORITY
         L     15,12(15) .              GET LCB FROM BUFFER
         L     15,92(,15) .             GET SCB FROM LCB
         STC   1,8(15) .                SET SCB PRIORITY
&PARM    DS    0H
.MEND    SPACE 2
         MEND
