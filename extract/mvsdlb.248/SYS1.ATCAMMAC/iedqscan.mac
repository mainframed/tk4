         MACRO
         IEDQSCAN &LEN,&CHARS,&BLANK
         GBLA  &IEDQZD,&IEDQZE(64),&IEDQZK
         GBLB  &IEDQZA(64)
         LCLA  &LNG,&D
         LCLC  &C
.*             VERSION DATED JULY 7, 1970
&IEDQZA(24) SETB 0
&IEDQZE(40) SETA 0
         AIF   ('&BLANK' NE 'NO').R
&IEDQZE(40) SETA 2
         AGO   .SET
.R       AIF   ('&BLANK' EQ '' OR '&BLANK' EQ 'YES').SET
&D       SETA  &IEDQZD
         IEDQCHAR 1,&BLANK
         AIF   (NOT &IEDQZA(24)).SET
         MNOTE 12,'***  IHB300  BLANK OPERAND INVALID AS SPECIFIED'
         AGO   .MEND
.SET     AIF   ('&CHARS' EQ '').SETA
&LNG     SETA  8
         CNOP  0,4
         AGO   .BAL
.SETA    AIF   ('&BLANK' NE 'NO').SETB
&LNG     SETA  4
         AGO   .BAL
.SETB    AIF   ('&BLANK' EQ '' OR '&BLANK' EQ 'YES').BAL1
&LNG     SETA  6
         AGO   .BAL
.BAL1    ANOP
&LNG     SETA  4
.BAL     BAL   1,*+4+&LNG
         AIF   (&IEDQZA(8)).SZ
         DC    AL1(&IEDQZE(30)+&IEDQZE(40)),AL1(&LNG),AL1(16),AL1(&LEN)
         AGO   .CHK
.SZ      DC    AL1(&IEDQZE(30)+&IEDQZE(40)),AL1(&LNG),AL1(0),AL1(&LEN)
.CHK     AIF   ('&CHARS' EQ '').S
&C       SETC  '&SYSECT'
IEDQMISC CSECT
         AIF   ('&CHARS'(K'&CHARS,1) EQ '''').DC
         DC    C'&CHARS'
         AGO   .C
.DC      DC    &CHARS
.C       ANOP
&C       CSECT
         AIF   ('&BLANK' EQ 'YES' OR '&BLANK' EQ '').SETX
         AIF   ('&BLANK' EQ 'NO').SKP
         AIF   ('&BLANK'(K'&BLANK,1) NE '''').SETL
         DC    &BLANK,AL3(IEDQMISC+&IEDQZK)
         AGO   .SETK
.SETX    DC    X'40',AL3(IEDQMISC+&IEDQZK)
         AGO   .SETK
.SKP     DC    AL1(0),AL3(IEDQMISC+&IEDQZK)
         AGO   .SETK
.SETL    DC    C'&BLANK',AL3(IEDQMISC+&IEDQZK)
.SETK    AIF   (&D EQ 0).SETZ
&IEDQZK  SETA  &IEDQZK+&D
         AGO   .L
.SETZ    ANOP
&IEDQZK  SETA  &IEDQZK+&IEDQZD
         AGO   .L
.S       AIF   (&LNG NE 6).L
         AIF   ('&BLANK'(K'&BLANK,1) EQ '''').DCB
         DC    C'&BLANK',AL1(0)
         AGO   .L
.DCB     DC    &BLANK,AL1(0)
.L       L     15,IEDUI
         BALR  14,15
.MEND    MEND
