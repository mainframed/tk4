         MACRO
&NAME    ICHNG &DDN,&RLN,&WORK,&PASSWRD=
.*                      DATE  72169                             SA54933
.*A016000,912000                                                 A41023
.*D024000,288000-328000,920000-936000                            A41023
.*C504000                                                       SA54933
         AIF   ('&DDN' EQ '(15)').ERR1
         AIF   ('&RLN' EQ '(15)').ERR2
         AIF   ('&WORK' EQ '(15)').ERR3
         AIF   ('&DDN' NE '' AND '&RLN' NE '' AND '&WORK' NE '').OK
         MNOTE 12,'*** IHB004 REQUIRED OPERAND(S) NOT SPECIFIED'
         MEXIT
.ERR1    ANOP
         MNOTE 12,'*** IHB078 DDNAME OPERAND REGISTER NOTATION         *
               INVALID - (15)'
         MEXIT
.ERR2    ANOP
         MNOTE 12,'*** IHB078 RELATIVE LINE NUMBER OPERAND             *
               REGISTER NOTATION INVALID - (15)'
         MEXIT
.ERR3    ANOP
         MNOTE 12,'*** IHB078 WORKAREA OPERAND REGISTER NOTATION       *
               INVALID - (15)'
         MEXIT
.ERR4    ANOP
         MNOTE 12,'*** IHB002 INVALID RLN OPERAND SPECIFIED'
         MEXIT
.OK      ANOP
         AIF   (K'&DDN LT 9 OR '&DDN'(1,1) EQ '(').OK1
.DDERR   ANOP
         MNOTE 12,'*** IHB002 INVALID DDNAME OPERAND SPECIFIED'
         MEXIT
.OK1     ANOP
         AIF   ('&WORK'(1,1) EQ '(' OR K'&WORK LT 9).OK2
         MNOTE 12,'*** IHB002 INVALID WORKAREA OPERAND SPECIFIED'
         MEXIT
.OK2     ANOP
&NAME    DS    0H
         AIF   ('&DDN'(1,1) EQ '(').REG
         AIF   (('&DDN'(1,1) GE 'A' AND '&DDN'(1,1) LE 'Z') OR         X
               '&DDN'(1,1) EQ '$' OR '&DDN'(1,1) EQ '@' OR             X
               '&DDN'(1,1) EQ '#').OKDD
         AGO   .DDERR
.OKDD    ANOP
         AIF   (K'&DDN GT 8).DDERR
         AGO   .ACTION
.REG     ANOP
         MVC   IEDQ&SYSNDX+16(8),0&DDN     MOVE DDNAME INTO CIB
.ACTION ANOP
         AIF   ('&WORK' EQ 'ACT').ACT
         AIF   ('&WORK' EQ 'DEACT').DEACT
         AIF   ('&WORK'(1,1) EQ '(').REG1
         AGO   .PRERLN
.ACT     ANOP
         AGO   .RLNO
.DEACT   ANOP
         MVI   IEDQ&SYSNDX+27,X'01' .   DEACTIVATION CODE
         AGO   .RLNO
.REG1    ANOP
         ST    &WORK(1),IEDQ&SYSNDX+24  STORE WORKAREA ADDRESS  SA54933
*                                       IN PARAMETER LIST
.PRERLN  ANOP
.RLNO    ANOP
         AIF   ('&RLN'(1,1) NE '(').LA
         STC   &RLN(1),IEDQ&SYSNDX+10 . STORE REL LINE NO IN CIB
         AGO   .CALL
.LA      ANOP
         AIF   ('&RLN' LE '0' OR '&RLN' GE '256').ERR4
.CALL    ANOP
         AIF   ('&PASSWRD' EQ '').NOADDR
         AIF   (K'&PASSWRD LT 9).OK3
         MNOTE 12,'*** IHB002 INVALID PASSWORD OPERAND SPECIFIED'
         MEXIT
.OK3     ANOP
         BAL   0,*+12 .                 PASSWORD ADDRESS IN REG 0
         DC    CL8'&PASSWRD'            UNSCRAMBLED PASSWORD
.NOADDR  ANOP
         CNOP  0,4
         BAL   1,*+44 .                 SET CIB ADDR IN PARM REG
IEDQ&SYSNDX DS 0F .                     COMMAND INPUT BUFFER
         DC    F'0' .                   OPERATOR CONTROL QCB ADDRESS
         DC    XL1'E0' .                CIB QUEUING PRIORITY
         DC    XL3'00' .                LINK FIELD
         DC    XL1'49' .                ICHNG VERB CODE
         DC    XL1'1C' .                CIB LENGTH
         AIF   ('&RLN'(1,1) NE '(').PDQ
         DC    XL1'00' .                RELATIVE LINE NUMBER
         AGO   .XYZ
.PDQ     ANOP
         DC    XL1'&RLN' .              RELATIVE LINE NUMBER
.XYZ     ANOP
         DC    XL1'00' .                COMPLETION CODE
         DC    A(IEDQ&SYSNDX+32) .      ECB ADDRESS
         AIF   ('&DDN'(1,1) EQ '(').REG2
         DC    CL8'&DDN' .              DDNAME
         AGO   .REG3
.REG2    ANOP
         DC    XL8'00' .                DDNAME FOR LINE GROUP
.REG3    ANOP
         AIF   ('&WORK' EQ 'ACT').ZOP
         AIF   ('&WORK' EQ 'DEACT').ZOP
         AIF   ('&WORK'(1,1) NE '(').ZIP
.ZOP     ANOP
         DC    XL4'00' .                WORKAREA ADDRESS
         AGO   .ZAP
.ZIP     ANOP
         DC    A(&WORK) .               WORKAREA ADDRESS
.ZAP     ANOP
         DC    A(IEDQ&SYSNDX) .         CIB ADDRESS
         DC    F'0' .                   PCB WORKAREA ADDR/ECB
         DC    F'0' .                   CIB LENGTH ADDRESS
         LINK EP=IEDQET                                          A41023
         MEND
