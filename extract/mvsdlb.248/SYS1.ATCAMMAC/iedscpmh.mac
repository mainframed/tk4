         MACRO
         IEDSCPMH
.*A000000-999999                                              @Y17XAWD
.* A004600,020400,020700                                       @OZ29553
*    ... STARTMH  MH=SSCP ...
IEDSCPMH STARTMH MH=SSCP                                      @YM04650
IEDPREFG EQU   X'03'          OFFSET FOR PRF1FLG1
IEDCNVTD EQU   X'08'          EQU FOR PRF1USS
IEDFSTCH EQU   X'100'         DISPLACEMENT FOR AVTCSTCS
IEDFDDCT EQU   X'5'           DISPLACEMENT FOR DCTBYTE5
IEDF3270 EQU   X'40'          EQUATE FOR DCTL3270
IEDMKLEN EQU   X'3E8'         DISPLACEMENT FOR AVTDCTLN
IEDCINDX EQU   X'10'          DISPLACEMENT FOR TRMCHIN
IEDSRCE  EQU   X'10'          DISPLACEMENT FOR PRFSRCE
IEDSTAT2 EQU   X'47'          DISPLACEMENT FOR WORD CONTAINING LCBRSP
IEDLRSP  EQU   X'01'          EQUATE FOR LCBRESP
IEDPFLCB EQU   X'0C'          EQUATE FOR PRFLCB               @YM07385
IEDDEST  EQU   X'28'                    PRFDEST DISPLACEMENT   @OZ29553
IEDRHFMT EQU   X'08'                    FORMATTED INDICATOR EQU@OZ29553
*    ... INHDR ...
         INHDR
         L     6,IEDADBUF          GET BUFFER ADDRESS
         SR    1,1                 CLEAR REGISTER
         ICM   1,3,IEDSRCE(6)      GET SOURCE TERMINAL'S TNT
         L     15,IEDRNMPT         GET ADDRESS OF TNT ROUNTINE
         BALR  14,15               CONVERT TNT TO TTE
         LR    8,1                 SAVE SOURCE TERMINAL'S TTE ADDR
         LH    11,IEDMKLEN(13)     GET LENGTH FOR DCT MASK
         SR    2,2                 CLEAR REGISTER
         IC    2,IEDCINDX(8)       GET DEVICE CHAR TABLE INDEX
         BCTR  2,0                 SUBTRACT 1 FROM DEVICE CHAR INDEX
         MR    10,2                MUTIPLY DCT MASK LENGTH BY INDEX
         AL    11,IEDFSTCH(13)     ADDR ADDRESS FOR FIRST ENTRY
         TM    IEDFDDCT(11),IEDF3270 IS THIS 3270 DATA STREAM ?
         BZ    IEDN3270            NO... BRANCH
*        REMOVE (SOH % / STX), (SBA ADDR ADDR), (AID CUR CUR)  @YM07265
*        FROM 3270 DATA STREAM                                 @YM07265
*    ... MSGEDIT ((RA,,X'01',(3)),(RA,,X'11',(2)),(RA,,X'7D',(2))),BLANX
               K=NO                                            @YM07265
         MSGEDIT ((RA,,X'01',(3)),(RA,,X'11',(2)),(RA,,X'7D',(2))),BLANX
               K=NO                                            @YM07265
IEDN3270 EQU *
*    ... IEDRH RHIND=(+FMH) ...    TEST FOR FORMATTED RU
         IEDRH RHIND=(+FMH)
         LTR   15,15               RU FORMATTED ?
         BZ    IEDSCPFD            YES... BRANCH
*    ... IEDOPCTL ...         PICK OFF OP CONTROL COMMANDS
         IEDOPCTL
*    ... SETSCAN C'IEDTOTE/',BLANK=NO... UNFORMATTED TOTE COMMAND ?
         SETSCAN C'IEDTOTE/',BLANK=NO                          @Y17XAWU
         LTR   15,15               IS THIS A TOTE COMMAND
         BNZ   IEDTRYFT            NO... TRY TO FORMAT THE COMMAND
*    ... FORWARD TASK=TOTE ...     FORWARD TO TOTE
         FORWARD TASK=TOTE
         L     6,IEDADBUF          GET BUFFER ADDRESS
         MVI   0(6),X'1C'          SET PRFKEY FOR TOTE
         B     IEDNDPRC            BRANCH TO END PROCESSING
IEDTRYFT EQU   *
*        TRY TO FORMAT THE COMMAND
*    ... IEDFMT IN=IEDAREA1,OUT=IEDAREA2,TABLE=IEDUSSTB ...
         IEDFMT IN=IEDAREA1,OUT=IEDAREA2,TABLE=IEDUSSTB
IEDSCPFD EQU   *
*  DO PROCESSING TO FORWARD TO THE SSCP
* TURN OFF LCBRESP TO KEEP IEDRESP FROM SENDING RESPONSE
         L     6,IEDADBUF          GET BUFFER ADDRESS
         L     4,IEDPFLCB(6)       GET PLCB ADDRESS
         NI    IEDSTAT2(4),X'FF'-IEDLRSP
*    ... FORWARD TASK=SSCP ...
         FORWARD TASK=SSCP
IEDNDPRC EQU   *
*    ... INEND ...
         INEND
*    ... OUTHDR ...
         OUTHDR
         L     2,IEDADBUF     GET BUFFER ADDRESS
         S     2,IEDNGPRF     BACK UP TO NEGATIVE PREFIX
         TM    IEDPREFG(2),IEDCNVTD   NEED TO BE CONVERTED
         BZ    IEDNFMT        BRANCH NO
* UNFORMAT THE BUFFER
*    ... IEDFMT IN=IEDAREA1,OUT=IEDAREA2,TABLE=IEDFSSTB ...
         IEDFMT IN=IEDAREA1,OUT=IEDAREA2,TABLE=IEDFSSTB
         B     IEDOUTPC                 GO TO OUTEND PROCESSING@OZ29553
IEDNFMT  EQU   *
         L     6,IEDADBUF               GET BUFFER ADDRESS     @OZ29553
         SLR   1,1                      CLEAR REG              @OZ29553
         ICM   1,3,IEDDEST(6)           GET DEST'S TERM TNT    @OZ29553
         L     15,IEDRNMPT              GET TNT RTN ADDRESS    @OZ29553
         BALR  14,15                    CONVERT TNT TO TTE     @OZ29553
         LR    8,1                      SAVE DEST TTE ADDRESS  @OZ29553
         LH    11,IEDMKLEN(13)          GET LENGTH FOR DCT MASK@OZ29553
         SLR   2,2                      CLEAR REG              @OZ29553
         IC    2,IEDCINDX(8)            DEV CHAR TABLE INDEX   @OZ29553
         BCTR  2,0                      REDUCE BY ONE          @OZ29553
         MR    10,2                     MULT DCT MASK LEN*INDEX@OZ29553
         AL    11,IEDFSTCH(13)          ADDRESS FOR FIRST ENTRY@OZ29553
         TM    IEDFDDCT(11),IEDF3270    3270 DATA STREAM?      @OZ29553
         BZ    IEDOUTPC                 NO... BRANCH           @OZ29553
         S     6,IEDNGPRF               GET NEGATIVE PREFIX    @OZ29553
         TM    0(6),IEDRHFMT            RU FORMATTED?          @OZ29553
         BO    IEDOUTPC                 YES... BRANCH          @OZ29553
*    ... MSGEDIT ((I,XL2'F1C3',,))      INSERT 3270 CNTRL CHARS@OZ29553
         MSGEDIT ((I,XL2'F1C3',,))                             @OZ29553
IEDOUTPC EQU   *                                               @OZ29553
*    ... OUTEND ...
         OUTEND
         DS     0F                                            @YM04656
IEDNGPRF DC     XL4'08'         LENGTH OF BUFFER NEGATIVE PREFIX
IEDAREA1 DC     XL6'010000000000' AREA FOR IEDFMT MACR0
         DC     XL256'00'
IEDAREA2 DC     XL6'010000000000' AREA FOR IEDFMT MACRO
         DC     XL256'00'
         MEND
