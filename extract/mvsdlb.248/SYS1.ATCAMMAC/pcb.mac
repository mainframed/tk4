         MACRO
&NAME    PCB   &MH=,&BUFSIZE=,&BUFIN=2,&BUFOUT=2,&RESERVE=(0,0),       C
               &DATE=NO,&SFLAG=YES, .                          @YM08522C
               &TIMEDLY=0,&PUTCNT=0 .                          @OX11307
         GBLC  &IEDQE13
         LCLA  &TDELAY                                         @OX11307
         LCLA  &PCOUNT                                         @OX11307
.*A632000-638000,645000,025000                                   S22025
.*CHANGE-ACTIVITY = AS FOLLOWS:                                 SA21903
.*************************** MICROFICHE FLAGS *************** SUPT CODE
.*D433000-436000                                                 X01004
.*C020000,780000                                                SA59162
.*C020000                                                      @OX11307
.*A030000,395000,800000                                        @OX11307
.*D790000,800000                                               @OZ29168
.*             11/14/72      MODIFICATION DATE                  SA59162
         AIF   ('&MH' NE '' AND '&BUFSIZE' NE '').OK
         MNOTE 12,'*** IHB004 REQUIRED OPERAND(S) NOT SPECIFIED'
         MEXIT
.OK      ANOP
         AIF   ('&NAME' NE '').OK1
         MNOTE 12,'*** IHB076 MACRO NAME FIELD BLANK-NAME REQ''D'
         MEXIT
.OK1     ANOP
         AIF   (K'&MH LT 9).OK2
.MHERR   ANOP
         MNOTE 12,'*** IHB002 INVALID MH OPERAND SPECIFIED'
         MEXIT
.OK2     ANOP
         AIF   (('&MH'(1,1) GE 'A' AND '&MH'(1,1) LE 'Z') OR           X
               '&MH'(1,1) EQ '$' OR '&MH'(1,1) EQ '@' OR               X
               '&MH'(1,1) EQ '#').OK2A
         AGO   .MHERR
.OK2A    ANOP
         AIF   ('&BUFIN' GT '1' AND '&BUFIN' LT '16').OK5
         MNOTE 12,'*** IHB002 INVALID BUFIN OPERAND SPECIFIED-&BUFIN'
         MEXIT
.OK5     ANOP
         AIF   ('&BUFOUT' GT '1' AND '&BUFOUT' LT '16').OK6
         MNOTE 12,'*** IHB002 INVALID BUFOUT OPERAND SPECIFIED-&BUFOUT'
         MEXIT
.OK6     ANOP
         AIF   ('&BUFSIZE' GT '35' AND '&BUFSIZE' LT '65536').OK7
         MNOTE 12,'*** IHB002 INVALID BUFSIZE OPERAND SPECIFIED-       X
               &BUFSIZE'
         MEXIT
.OK7     ANOP
         AIF   ('&RESERVE(1)' LE '255' AND '&RESERVE(2)' LE '255').OK8
.OK8A    ANOP
         MNOTE 12,'*** IHB002 INVALID RESERVE OPERAND SPECIFIED'
         MEXIT
.OK8     ANOP
         AIF   ('&DATE' EQ 'NO').OK9                             S22025
         AIF   ('&DATE' EQ 'YES').OK9                            S22025
         MNOTE 12,'*** IHB002 INVALID DATE OPERAND SPECIFIED'
         MEXIT                                                   S22025
.OK9     ANOP .                                                  S22025
         AIF ('&TIMEDLY' GE '0' AND '&TIMEDLY' LE '3600').OKA  @OX11307
         MNOTE 0,'*** IHB086 TIMEDLY INVALID KEY20R, 000 ASSUMED'
&TDELAY  SETA  0                                               @OX11307
&PCOUNT  SETA  0                                               @OX11307
         AGO   .OKC                                            @OX11307
.OKA     ANOP                                                  @OX11307
         AIF   ('&PUTCNT' GE '0' AND '&PUTCNT' LE '1000').OKB  @OX11307
         MNOTE 0,'*** IHB086 PUTCNT INVALID KEYWORD, 000 ASSUMED'
&TDELAY  SETA  0                                               @OX11307
&PCOUNT  SETA  0                                               @OX11307
         AGO   .OKC                                            @OX11307
.OKB     ANOP                                                  @OX11307
         AIF   (('&TIMEDLY' EQ '0' AND '&PUTCNT' NE '0') OR            X
               ('&TIMEDLY' NE '0' AND '&PUTCNT' EQ '0')).NOTOK @OX11307
&TDELAY  SETA  &TIMEDLY                                        @OX11307
&PCOUNT  SETA  &PUTCNT                                         @OX11307
         AGO   .OKC                                            @OX11307
.NOTOK   ANOP                                                  @OX11307
         MNOTE 0,'*** IHB086 TIMEDLY INVALID KEYWORD, 000 ASSUMED'
         MNOTE 0,'*** IHB086 PUTCNT INVALID KEYWORD, 000 ASSUMED'
&TDELAY  SETA  0                                               @OX11307
&PCOUNT  SETA  0                                               @OX11307
.OKC     ANOP                                                  @OX11307
         AIF   (N'&RESERVE GT 2).OK8A
         AIF   ('&IEDQE13' NE '').OK3
         ORG   IEDPCBPT
         DC    A(&NAME) .               AVT POINTS TO FIRST PCB
         ORG
         AGO   .OK4
.OK3     ANOP
         ORG   &IEDQE13+25
         DC    AL3(&NAME)               PCB POINTS TO NEXT PCB
         ORG
.OK4     ANOP
&IEDQE13 SETC  '&NAME'
         SPACE 2
*                       PROCESS CONTROL BLOCK
*
&NAME    DS    0F                       ORIGIN ON WORD BOUNDARY
         SPACE
         DC    2F'0' .                  RETRIEVE WORKAREA
         DC    3F'0'     RETRIEVE SCHEDULER QCB
         DC    AL1((&BUFIN*16)+&BUFOUT) BUFIN/BUFOUT
         DC    AL3(&MH)                 MESSAGE HANDLER ADDRESS
         DC    AL1(0)                   USE COUNT
         DC    AL3(0)             LINK FIELD
         DC    XL1'&BUFOUT'             READ-AHEAD Q BUFFER LIMIT
         AIF   ('&DATE' EQ 'NO').NDT                             S22025
         DC    VL3(IEDQB3)              DATE/TIME ROUTINE        S22025
         AGO   .LCB                                              S22025
.NDT     ANOP                                                    S22025
         DC    AL3(0)
.LCB     ANOP                                                    S22025
         DC    A(0)                     LCB ADDRESS
         DC    AL2(0)                   TSO JOB IDENTIFIER
         DC    AL2(0)
         DC    A(0)                     APPL PROG TCB ADDRESS
         DC    AL1(0)                   FLAG BYTE
         DC    AL3(0)
         DC    A(0)
         DC    AL1(0)
         DC    AL1(0)                   UNIT COUNT
         DC    AL2(&BUFSIZE)            BUFFER SIZE
         DC    AL1(0&RESERVE(1)) .      HEADER BUFFER RESERVE
         DC    AL1(0&RESERVE(2)) .      TEXT BUFFER RESERVE
         DC    AL1(0)                   OPEN RETURN CODE
         AIF   ('&SFLAG' EQ 'NO').LCB1                          SA59162
         DC    AL1(0) .                 FLAG BYTE               SA59162
         AGO   .LCB2                                            SA59162
.LCB1    DC    X'80' .                  FLAG BYTE               SA59162
.LCB2    ANOP                                                   SA59162
         DC    H'&TDELAY'               TIME DELAY             @OX11307
         DC    H'&PCOUNT'               MAX CNT BETWEEN DELAYS @OX11307
         DC    H'0'                     COUNT SINCE LAST DELAY @OX11307
         DC    H'0'                     RESERVED               @OX11307
         MEND
