         MACRO
         IEDUVERB &CHAR,&CMD=,&DEL=,&PWRDLEN=,&UDATLEN=,&RQIDLEN=,     *
               &LULEN=
         GBLA  &IEDMAX,&IEDUDLN,&IEDLULN,&IEDPWLN,&IEDRQLN
         GBLA  &IEDRSAV,&IEDPSAV,&IEDUSAV,&IEDLSAV
         GBLB  &IEDRBIT,&IEDPBIT,&IEDUBIT,&IEDLBIT
         GBLB  &IEDTBIN,&IEDVBIN,&IEDPSIN,&IEDKYIN,&IEDEJEC
         GBLC  &IEDREQ(64),&IEDVERB(64),&IEDLKEY
         GBLC  &IEDLVRB,&IEDDVAL,&IEDDISP(7)
         LCLA  &CTR                     COUNTER
         LCLA  &ADD                     NUMBER OF ZEROS TO SEND WITH
.*                                       COMMAND               @PI06031
         LCLA  &VBLEN                   LENGTH OF VERB DATA
         LCLA  &SUM                     VARIBLE LENGTH COUNTER @OZ50295
         LCLB  &NOGO                    NO GENERATION SWITCH
         LCLC  &ID(3)                   SSCP COMMAND ID
         LCLC  &VBVAL                   VERB DATA
         LCLC  &DELVAL                  DELIMITER VALUE
         LCLC  &LABEL                   USED TO GAIN VERB ENTRY NAME
.*A000000-999999                                               @Y17XAFF
.*C726000                                                      @OZ50295
.*A042000                                                      @OZ50295
         AIF   ('&IEDDVAL' EQ '').A0010
         IEDUVAL ,&IEDDVAL
.A0010   ANOP
         AIF   (&IEDTBIN).A0020
         MNOTE 12,'*** IEDU20 INVALID SEQUENCE MUST CODE IEDUTAB FIRST.*
               '
&NOGO    SETB  1
.A0020   ANOP
         AIF   (NOT &IEDRBIT).A0021        WAS LOCAL PREVIOUSLY DEFINED
&IEDRQLN   SETA  &IEDRSAV                   RESTORE GLOBAL LENGTH
&IEDRBIT    SETB  0                        RESET LOCAL VALUE INDICATOR
.A0021   AIF   (NOT &IEDPBIT).A0022
&IEDPWLN   SETA  &IEDPSAV
&IEDPBIT    SETB  0
.A0022   AIF   (NOT &IEDUBIT).A0023
&IEDUDLN   SETA  &IEDUSAV
&IEDUBIT    SETB  0
.A0023   AIF   (NOT &IEDLBIT).A0024
&IEDLULN   SETA  &IEDLSAV
&IEDLBIT    SETB  0
.A0024   ANOP
&CTR     SETA  1                        INIT COUNTER FOR CLEARING
.A0030   AIF   (&CTR GT 64).A0050       IF USER FAILED TO CODE A REQ
         AIF   ('&IEDREQ(&CTR)' EQ '').A0050  PARM,GEN MNOTE & MAKE RDY
         AIF   ('&IEDREQ(&CTR)' EQ '0').A0040 FOR NEXT VERB ASSIGNMENT
         MNOTE 12,'*** IEDU22 &IEDREQ(&CTR) WAS NOT CODED FOR PREVIOUS *
               B.'
.A0040   ANOP
&IEDREQ(&CTR) SETC ''
&CTR     SETA  &CTR+1
         AGO   .A0030
.A0050   ANOP
&CTR     SETA  1                        INIT COUNTER FOR CLEARING
.A0060   AIF   (&CTR GT 64).A0070       MAKE ARRAY READY FOR NEXT VERB
         AIF   ('&IEDVERB(&CTR)' EQ '').A0070  ASSIGNMENT
&IEDVERB(&CTR) SETC ''
&CTR     SETA  &CTR+1
         AGO   .A0060
.A0070   ANOP
         AIF   ('&CHAR' NE '').A0090    &CHAR MUST NOT BE LARGER THAN
.A0080   MNOTE 12,'*** IEDU24 INVALID CHAR OPERAND.' 8 BYTES, AND IF
&NOGO    SETB  1                        HEXADECIMAL,DATA MUST BE X'  '
         AGO   .A0120
.A0090   AIF   ('&CHAR'(1,2) NE 'X''').A0110
         AIF   ('&CHAR'(K'&CHAR,1) NE '''').A0080
         AIF   (K'&CHAR-3 LE 16).A0100                         @YM08574
.A0095   MNOTE 12,'*** IEDU25 CHAR OPERAND MUST BE LESS THAN OR EQUAL T*
               O 8 BYTES.'
&NOGO    SETB  1
         AGO   .A0120
.A0100   ANOP
&VBVAL   SETC  '&CHAR'
&VBLEN   SETA  (K'&CHAR-3+1)/2
         AGO   .A0120
.A0110   ANOP
         AIF   (K'&CHAR GT 8).A0095
&VBVAL   SETC  'C''&CHAR'''
&VBLEN   SETA  K'&CHAR
.A0120   ANOP
&DELVAL  SETC  'X''00'''                GIVE DEFAULT VALUE
         AIF   ('&CMD' NE '').A0170
.A0165   MNOTE 12,'*** IEDU23 INVALID CMD OPERAND, MUST BE CODED.'
&NOGO    SETB  1                        THE COMMANDS SUPPORTED ARE
         AGO   .A0180                        INITS: INIT-SELF
.A0170   ANOP                                TERMS: TERMINATE-SELF
         AIF   ('&CMD' EQ 'INITS').A0180
         AIF   ('&CMD' NE 'TERMS').A0165
.A0180   ANOP
         AIF   (&NOGO).A0220
&ID(1)   SETC  '01'
&ID(2)   SETC  '06'
         AIF   ('&LULEN' EQ '').A0183   IF LULEN IS CODED MUST SAVE
         AIF   (T'&LULEN NE 'N').A0181   GLOBAL VALUE AND SET INDICATOR
         AIF   (&LULEN GT 0 AND &LULEN LE 8).A0182
.A0181   MNOTE 4,'*** IEDU21 INVALID LULEN OPERAND, GLOBAL VALUE USED.'
         AGO   .A0183
.A0182   ANOP
&IEDLBIT    SETB  1
&IEDLSAV   SETA  &IEDLULN
&IEDLULN   SETA  &LULEN
.A0183   ANOP
         AIF   ('&CMD' NE 'INITS').A0198
         AIF   ('&RQIDLEN' EQ '').A0186 IF RQIDLEN IS CODED MUST SAVE
         AIF   (T'&RQIDLEN NE 'N').A0184 GLOBAL VALUE AND SET INDICATOR
         AIF   (&RQIDLEN LE 255 AND &RQIDLEN GE 0).A0185
.A0184   MNOTE 4,'*** IEDU28 INVALIED RQIDLEN OPERAND, GLOBAL VALUE USE*
               D.'
         AGO   .A0186
.A0185   ANOP
&IEDRBIT    SETB  1
&IEDRSAV   SETA  &IEDRQLN
&IEDRQLN   SETA  &RQIDLEN
.A0186   AIF   ('&PWRDLEN' EQ '').A0189 IF PWRDLEN IS CODED MUST SAVE
         AIF   (T'&PWRDLEN NE 'N').A0187 GLOBAL VALUE AND SET INDICATOR
         AIF   (&PWRDLEN LE 255 AND &PWRDLEN GE 0).A0188
.A0187   MNOTE 4,'*** IEDU31  INVALID PWRDLEN OPERAND, GLOBAL VALUE USE*
               D.'
         AGO   .A0189
.A0188   ANOP
&IEDPBIT    SETB  1
&IEDPSAV   SETA  &IEDPWLN
&IEDPWLN   SETA  &PWRDLEN
.A0189   AIF   ('&UDATLEN' EQ '').A0192
         AIF   (T'&UDATLEN NE 'N').A0190
         AIF   (&UDATLEN LE 255 AND &UDATLEN GE 0).A0191
.A0190   MNOTE 4,'*** IEDU29  INVALID UDATLEN OPERAND, GLOBAL VALUE USE*
               D.'
         AGO   .A0192
.A0191   ANOP
&IEDUBIT    SETB  1
&IEDUSAV   SETA  &IEDUDLN
&IEDUDLN   SETA  &UDATLEN
.A0192   ANOP
&IEDDISP(7) SETC 'IEDXINSD'              SET DSECT NAME
&ID(3)   SETC  '81'
&IEDREQ(1)  SETC  'INLU'                   SETUP
&IEDREQ(2)  SETC  'INTYPE'                  REQUIRED
&IEDREQ(3)  SETC  'INFORM'                  POSITIONAL
&IEDREQ(4)  SETC  'INRES'                  PARAMETERS
&IEDREQ(5)  SETC  'INMODE'
         AIF   ('&DEL' EQ '').A0160
         AIF   (&IEDUDLN NE 0).A0140                           @XM05844
         MNOTE 4,'*** IEDU26 DEL OPERAND ONLY VALID IF USER DATA FIELD *
               LENGTH IS NON-ZERO, NO DELIMITER ASSUMED.'      @XM05844
         AGO   .A0145                                          @ZM46674
.A0140   AIF   (K'&DEL EQ 1).A0155
         AIF   ('&DEL'(1,2) EQ 'X''' AND '&DEL'(K'&DEL,1) EQ '''' AND  *
               K'&DEL LE 5).A0150
         MNOTE 4,'*** IEDU27 INVALID DEL OPERAND, NO DELIMITER ASSUMED.*
               '
.A0145   ANOP                                                  @ZM46674
&DELVAL  SETC  'X''00'''
         AGO   .A0160
&NOGO    SETB  1
         AGO   .A0160
.A0150   ANOP
&DELVAL  SETC  '&DEL'
         AGO   .A0160
.A0155   ANOP
&DELVAL  SETC  'C''&DEL'''
.A0160   ANOP
&IEDREQ(6)  SETC  '0'
         AIF   (&IEDUDLN EQ 0 OR '&DELVAL' NE 'X''00''').A0195 @PI06031
&IEDREQ(6)  SETC  'INUDATA'                IF A FIELD HAD A LENGTH
.A0195   ANOP                            SPECIFIED, THE PARMATER IS
&IEDREQ(7)  SETC  '0'                       REQUIRED.
         AIF   (&IEDPWLN EQ 0).A0196
&IEDREQ(7)  SETC  'INPWRD'
.A0196   ANOP
&IEDREQ(8)  SETC  '0'
         AIF   (&IEDRQLN EQ 0).A0197
&IEDREQ(8)  SETC  'INREQID'
.A0197   ANOP
&IEDVERB(1)  SETC  'INLU'                   SETUP
&IEDVERB(2)  SETC  'INTYPE'                  VALID
&IEDVERB(3)  SETC  'INFORM'                  POSITIONAL
&IEDVERB(4)  SETC  'INMODE'                  PARAMETERS
&IEDVERB(5)  SETC  'INREQID'
&IEDVERB(6)  SETC  'INPWRD'
&IEDVERB(7)  SETC  'INUDATA'
&IEDVERB(8)  SETC  'INRES'
         AIF   (&IEDUDLN NE 0).A0200                           @ZM46674
&ADD     SETA  &ADD+1                   MUST DECIDE IF ANY ZERO BYTES
         AIF   (&IEDPWLN NE 0).A0200       MUST TO SENT AS PART OF THE
&ADD     SETA  &ADD+1                    COMMAND               @PI06031
         AIF   (&IEDRQLN NE 0).A0200                           @PI06031
&ADD     SETA  &ADD+1                                          @PI06031
         AGO   .A0200
.A0198   AIF   ('&DEL' EQ '' AND '&PWRDLEN' EQ '' AND                  *
                '&RQIDLEN' EQ '' AND '&UDATLEN' EQ '').A0199
         MNOTE 4,'*** IEDU30 DEL, PWRDLEN, RQIDLEN, AND UDATLEN ONLY   *
               VALID WHEN CMD=INITS.'
.A0199   ANOP
&IEDDISP(7) SETC 'IEDXTMSD'              SET DSECT NAME
&ID(3)   SETC  '83'
&IEDREQ(1)  SETC  'TMLU'                   SETUP
&IEDREQ(2)  SETC  'TMTYPE'                  REQUIRED  PARMS
&IEDREQ(3)  SETC  'TMRES'
&IEDVERB(1)  SETC  'TMLU'                   SETUP
&IEDVERB(2)  SETC  'TMTYPE'                  VALID PARMS
&IEDVERB(3)  SETC  'TMRES'
.A0200   ANOP
&IEDVBIN   SETB  1                        IEDUVERB CODED
         AIF   ('&CMD' NE 'INITS').A0200A                      @OZ50295
&IEDMAX  SETA  256-21                   SET INIT SELF VALUE    @OZ50295
         AGO   .A0200C                                         @OZ50295
.A0200A  ANOP                                                  @OZ50295
         AIF   ('&CMD' NE 'TERMS').A0200B                      @OZ50295
&IEDMAX  SETA  256-13                   SET TERM SELF VALUE    @OZ50295
         AGO   .A0200C                                         @OZ50295
.A0200B  ANOP                                                  @OZ50295
         MNOTE 12,'*** IEDU23 INVALID CMD OPERAND, MUST BE CODE@OZ50295X
               D.'                                             @OZ50295
&IEDMAX  SETA  256-3                    DEFAULT                @OZ50295
.A0200C  ANOP                                                  @OZ50295
         AIF   ('&DEL' EQ '').A0200D                           @OZ50295
&SUM     SETA  (&IEDLULN+&IEDPWLN+&IEDRQLN+&IEDUDLN)           @OZ50295
&IEDMAX  SETA  &IEDMAX-&SUM                                    @OZ50295
         AIF   (&IEDMAX GE 0).A0200D                           @OZ50295
         MNOTE 12,'*** IEDU42 DEFINED AREA LARGER THAN 256.'   @OZ50295
.A0200D   ANOP                                                 @OZ50295
&IEDPSIN   SETB  0                        POS PARM NOT CODED
&IEDKYIN   SETB  0                        KEYWORD PARM NOT CODED
         AIF   (NOT &IEDEJEC).A0201
         EJECT
         AGO   .A0202
.A0201   ANOP
         SPACE 2
&IEDEJEC   SETB  1
.A0202   ANOP
&LABEL   SETC  'USS&SYSNDX'
         AIF   ('&IEDLVRB' EQ '').A0205 ORG TO PREV VERB ENTRY
         ORG   &IEDLVRB+1
         DC    AL4(&LABEL)              ADDR TO NEXT VERB ENTRY
         ORG
.A0205   ANOP
&IEDLVRB SETC  '&LABEL'                 CURRENT VERB ENTRY NAME
&IEDLKEY  SETC  ''                       NO KEYS
&IEDLVRB EQU   *
         DC    AL1(USSFVERB) .          VERB ENTRY IND
         DC    AL4(0) .                 ADDR TO NEXT VERB ENTRY
         DC    AL4(0) .                 ADDR TO NEXT KEYWORD ENTRY
         DC    X'&ID(1)&ID(2)&ID(3)' .  SSCP COMMAND ID
         DC    AL1(&ADD) .              NUM OF ZEROS AT END OF COMMAND
         DC    &DELVAL .                DELIMITER VALUE
         DC    AL1(&IEDUDLN) .            LENGTH OF USER DATA
         DC    AL1(XINSUFLD+&IEDLULN+&IEDRQLN+&IEDPWLN-IEDXINSD)
*                                       DISPLACEMENT TO USER DATA
         DC    AL1(&VBLEN)              LEN OF VERB CHAR STRING
         DC    &VBVAL                   VERB CHAR STRING
         SPACE
         AIF   ('&CMD' NE 'INITS').A0210
         IEDUPARM INFORM
         IEDUPARM INTYPE
         IEDUPARM INRES
         AGO   .A0220
.A0210   ANOP
         IEDUPARM TMTYPE
         IEDUPARM TMRES
.A0220   ANOP
         MEND
