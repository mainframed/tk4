         MACRO
&NAME    LOCOPT &OP,&RG,&STATION=**
         GBLA  &IEDQZE(64),&IEDQZK
         GBLB  &IEDQZA(64)
         LCLA  &LEN
         LCLC  &SAV
         LCLB  &NOGO                                           @Y17XANQ
.*              REWRITTEN OCTOBER 1,1975
.***************************** CHANGE FLAGS ***************************
.*C000074-000076         MACRO RESEQUENCED                     @OZ37911
.**********************************************************************
         SPACE 1
&SAV     SETC  '0'
&LEN     SETA 4                         MINIMUM PARM LIST LENGTH
         AIF   ('&OP' NE '').OPOK       OPTION FIELD IS REQUIRED
         MNOTE 12,'***  IHB300  OPTION FIELD OPERAND INVALID AS SPECIFIX
               ED'
&NOGO    SETB  1                                               @Y17XANQ
.OPOK    AIF   ('&RG' EQ '').RGOK
         AIF   ('&RG'(1,1) EQ '(').RGOK
         MNOTE 12,'*** IHB002 INVALID REGISTER OPERAND SPECIFIED-&RG'
&NOGO    SETB  1                                               @Y17XANQ
.RGOK    AIF   (&IEDQZA(46)).CKMH       IF SPECIFIED IN THE MH
         AIF   (&IEDQZA(7)).NOMH        IF NOT MH MUST BE TCAM
         MNOTE 12,'***  IHB070  SEQUENCE ERROR - MUST FOLLOW INTRO'
&NOGO    SETB  1                                               @Y17XANQ
.NOMH    AIF   ('&STATION' NE '**').STOK
         MNOTE 12,'***  IHB300  STATION OPERAND INVALID AS SPECIFIED'
&NOGO    SETB  1                                               @Y17XANQ
.STOK    AIF   (&NOGO).MEND                                    @Y17XANQ
&NAME    CNOP  0,4                                             @Y17XANQ
         AIF   ('&STATION'(1,1) NE '(').GNBL   NOT SPECIFIED TARGET
         ST    &STATION(1),*+12         REGISTER TO PARM LIST
.GNBL    BAL   1,*+20                   BRANCH AROUND PARM LIST
         DC    AL1(255,12,&OP,0)
         AIF   ('&STATION'(1,1) EQ '''').MISC   STATION NAME
         AIF   ('&STATION'(1,1) EQ '(').GN0
&SAV     SETC  '&STATION'
.GN0     DC    A(&SAV)
.GNVC    DC    V(IEDQAE)
         DC    X'00000000'        REG14 SAVE AREA              @OZ37911
         L     15,*-8                                          @OZ37911
         ST    14,*-8             SAVE REG14 FOR RETURN        @OZ37911
         BAL   14,4(,15)
         L     14,*-16            RESTORE REG14                @OZ37911
         AIF   ('&RG' EQ '').MEND
         LR    &RG(1),1
         AGO   .MEND
.CKMH    AIF   (NOT &IEDQZA(5)).SBOK    NOT INMSG/OUTMSG
         MNOTE 12,'***  IHB070  SEQUENCE ERROR - MUST FOLLOW INHDR,INBUX
               F, INBLOCK, OUTHDR OR OUTBUF MACRO'
&NOGO    SETB  1                                               @Y17XANQ
.SBOK    IEDQVCON 31,IEDQAE
         AIF   (&NOGO).MEND                                    @Y17XANQ
&NAME    CNOP  0,4
         AIF   ('&STATION' EQ '**').GNBA  TARGET= SOURCE/DESTINATION
&LEN     SETA  &LEN+4
         AIF   ('&STATION'(1,1) EQ '(').STRG   REGISTER NOTATION
         AIF   ('&STATION'(1,1) EQ '''').GNBA  NAME SPECIFIED
&SAV     SETC  '&STATION'                                      @Y17XANQ
         AGO   .GNBA
.STRG    ST    &STATION(1),*+12         REGISTER TO PARM LIST
.GNBA    BAL   1,*+&LEN+4
         DC    AL1(&IEDQZE(31),&LEN,&OP)
         AIF   ('&RG' EQ '').NOGR
         DC    AL1(&RG(1))
         AGO   .OUT
.NOGR    DC    AL1(15)                                         @YM07010
.OUT     AIF   (&LEN EQ 4).GNUI
         AIF   ('&STATION'(1,1) NE '''').GNA0
.MISC    DC    A(IEDQMISC+&IEDQZK)
&SAV     SETC  '&SYSECT'                                       @XM05804
IEDQMISC CSECT
         DC    CL8&STATION                                     @XM05831
&SAV     CSECT
&IEDQZK  SETA  &IEDQZK+8                                       @XM05836
         AIF   (NOT &IEDQZA(46)).GNVC    NOT IN MH
         AGO   .GNUI
.GNA0    DC    A(&SAV)                                         @Y17XANQ
.GNUI    L     15,IEDUI
         BALR  14,15
.MEND    SPACE 2
         MEND
