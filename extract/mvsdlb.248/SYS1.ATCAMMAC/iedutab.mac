         MACRO
&NAME    IEDUTAB &LULEN=8,&RQIDLEN=0,&PWRDLEN=0,&UDATLEN=0,&GEN=YES
         GBLA  &IEDRQLN,&IEDPWLN,&IEDUDLN,&IEDLULN
         GBLB  &IEDTBIN
         LCLC  &LABEL                   NAME PLACE ON CSECT CARD
.*A000000-999999                                               @Y17XAFF
&LABEL   SETC  'IEDUSSTB'               GIVE DEFAULT LABEL
         AIF   ('&NAME' EQ '').A0010     UNLESS USER WANTS
&LABEL   SETC  '&NAME'                   ANOTHER
.A0010   ANOP
         AIF   (T'&LULEN NE 'N').A0020  &LULEN MUST BE GT 0 AND LE 8
         AIF   (&LULEN LE 8 AND &LULEN GT 0).A0030
.A0020   MNOTE 4,'*** IEDU02 INVALID LULEN OPERAND, DEFAULT ASSUMED.'
&IEDLULN   SETA  8
         AGO   .A0040
.A0030   ANOP
&IEDLULN   SETA  &LULEN
.A0040   ANOP
         AIF   (T'&RQIDLEN NE 'N').A0050 &RQIDLEN MUST BE GE 0 AND LE
         AIF   (&RQIDLEN LE 255 AND &RQIDLEN GE 0).A0060            255
.A0050   MNOTE 4,'*** IEDU03 INVALID RQIDLEN OPERAND, DEFAULT ASSUMED.'
&IEDRQLN   SETA  0
         AGO   .A0070
.A0060   ANOP
&IEDRQLN   SETA  &RQIDLEN
.A0070   ANOP
         AIF   (T'&PWRDLEN NE 'N').A0080 &PWRDLEN MUST BE GE 3 AND0LE
         AIF   (&PWRDLEN LE 255 AND &PWRDLEN GE 0).A0090           255
.A0080   MNOTE 4,'*** IEDU04 INVALID PWRDLEN OPERAND, DEFAULT ASSUMED.'
&IEDPWLN   SETA  0
         AGO   .A0100
.A0090   ANOP
&IEDPWLN   SETA  &PWRDLEN
.A0100   ANOP
         AIF   (T'&UDATLEN NE 'N').A0110 &UDATLEN MUST BE GE 0 AND LE
         AIF   (&UDATLEN LE 255 AND &UDATLEN GE 0).A0120            255
.A0110   MNOTE 4,'*** IEDU05 INVALID UDATLEN OPERAND, DEFAULT ASSUMED.'
&IEDUDLN   SETA  0
         AGO   .A0130
.A0120   ANOP
&IEDUDLN   SETA  &UDATLEN
.A0130   ANOP
&LABEL   TITLE 'USS-FSS CONVERSION TABLE'
&LABEL   CSECT
&IEDTBIN   SETB  1
         DC    AL1(USSTYPE) .           TABLE TYPE IS USS-FSS
         SPACE 1
         AIF   ('&GEN' EQ 'YES').A0140
         AIF   ('&GEN' EQ 'NO').A0150
         MNOTE 4,'*** IEDU06 INVALID GEN OPERAND, NO ASSUMED.'
         AGO   .A0150
.A0140   ANOP
*        BEGINNING OF DEFAULT USS-FSS TABLE
*'INITS' EQUAL INIT-SELF CMD
         IEDUVERB INITS,CMD=INITS,RQIDLEN=0,PWRDLEN=0,LULEN=8,UDATLEN=0
         IEDUPARM INLU
         IEDUVAL
         IEDUPARM INMODE
         IEDUVAL
*'TERMS' EQUAL TERMINATE-SELF CMD
         IEDUVERB TERMS,CMD=TERMS,LULEN=8
         IEDUPARM TMLU
         IEDUVAL
*'LOGON ' EQUAL INIT-SELF CMD                                  @YM07701
         IEDUVERB LOGON,CMD=INITS,RQIDLEN=0,PWRDLEN=0,LULEN=8,UDATLEN=0
         IEDUPARM INLU                  1ST POSITIONAL PARM IS LU NAME
         IEDUVAL
         IEDUPARM INMODE                2ND POSITIONAL PARM IS MODE NME
         IEDUVAL
.A0150   MEND
