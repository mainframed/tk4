         MACRO
&NAME    QRESET  &DCB,&MAX=
         LCLA  &N
         LCLB  &NOGO
         LCLC  &R
         AIF   ('&MAX' NE '').QR1
         MNOTE 12,'*** IHB001 MAX OPERAND REQUIRED - NOT SPECIFIED'
&NOGO    SETB  1
         AGO   .QR1A
.QR1     AIF   ('&MAX' LT '1' OR '&MAX' GT '9999').ERR2
         AGO   .QR1A
.ERR2    MNOTE 12,'***  IHB300 MAX OPERAND INVALID AS SPECIFIED  '
&NOGO    SETB  1
.QR1A    AIF   ('&DCB' NE '').QR2
         MNOTE 12,'*** IHB001 DCBNAME OPERAND REQUIRED - NOT SPECIFIED'
&NOGO    SETB  1
.QR2     AIF   ('&DCB'(1,1) LE 'Z').QR3
         MNOTE 12,'*** IHB300 DCBNAME OPERAND INVALID AS SPECIFIED '
&NOGO    SETB  1
.QR3     AIF   (&NOGO).EXIT
         AIF   ('&DCB'(1,1) EQ '(').REG
         CNOP  0,4
&NAME    BAL   1,*+12
         DC    AL4(&DCB)
         DC    AL4(&MAX)
         AGO   .BAL
.REG     ANOP
&N       SETA  K'&DCB
&N       SETA  &N-2
&R       SETC  '&DCB'(2,&N)
         CNOP  0,4
&NAME    BAL   1,*+12
         DC    AL4(0)
         DC    AL4(&MAX)
         ST    &R,*-8
.BAL     ANOP
         L     15,=V(IEDQGR)
         BALR  14,15
.EXIT    MEND
