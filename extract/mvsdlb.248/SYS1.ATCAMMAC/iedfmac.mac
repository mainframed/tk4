         MACRO
         IEDFMAC &ID,&MSG=
         GBLB  &IEDVAL(255),&IEDFBIN,&IEDFMIN
         GBLC  &IEDLENT
         LCLC  &LABEL                   BUILD LABEL
         LCLC  &MSGCH                   USED FOR PARTITIONING THE MSG
         LCLB  &NOGO                    NO GENERATION SWITCH
         LCLA  &CTR                     COUNTER
         LCLA  &DISP                    DISPLACEMENT INTO MSG
.*A000000-999999                                               @Y17XAFF
         AIF   (&IEDFBIN).A0010
         MNOTE 12,'*** IEDF21 SEQUENCE ERROR, MUST CODE AN IEDFTAB MACR*
               O.'
&NOGO    SETB  1
.A0010   ANOP
         AIF   ('&MSG' EQ '').A0015  MSG MUST BE CODED WITH QUOTES
         AIF   ('&MSG'(1,1) EQ '''' AND '&MSG'(K'&MSG,1) EQ '''').A0020
.A0015   MNOTE 12,'*** IEDF23 INVALID MSG OPERAND.'
&NOGO    SETB  1
.A0020   AIF   ('&ID' NE '').A0030   ID MUST BE CODED
         MNOTE 12,'*** IEDF26 ID OPERAND MUST BE CODED.'
&NOGO    SETB  1
         AGO   .A0100
.A0030   ANOP
         AIF   (K'&ID LE 3).A0040
         MNOTE 12,'*** IEDF27 ID OPERAND MUST BE LESS THAN OR EQUAL TO *
               256.'
&NOGO    SETB  1
         AGO   .A0100
.A0040   ANOP
         AIF   (T'&ID EQ 'N').A0070 INSURE THAT ID IS NUMERIC
         MNOTE 12,'*** IEDF24 ID OPERAND MUST BE NUMERIC.'
&NOGO    SETB  1
.A0070   AIF   (&ID EQ 0 OR &ID GT 255).A0080
         AIF   (&IEDVAL(&ID)).A0090
.A0080   MNOTE 12,'*** IEDF25 ID VALUE NOT SUPPORTED.'
&NOGO    SETB  1                   INSURE ID VALUE IS
         AGO   .A0100                VALID AND SUPPORTED
.A0090   ANOP
&IEDVAL(&ID) SETB 0                 SAME ID ON NEXT MACRO NOT SUPPORTED
.A0100   ANOP
         AIF   (&NOGO).A0990
&LABEL   SETC  'FSS&SYSNDX'
         AIF   ('&IEDLENT' EQ '').A0110
         ORG   &IEDLENT+1
         DC    AL4(&LABEL)              NEXT ENTRY ADDR
         ORG
.A0110   ANOP
&IEDLENT  SETC  '&LABEL'
         SPACE
&IEDLENT  DC    AL1(&ID) .               MESSAGE ID
         DC    AL4(0) .                 NEXT ENTRY ADDR
&CTR     SETA  K'&MSG-2
         DC    AL1(&CTR)                LENGTH OF MESSAGE
&DISP    SETA  2
.A0120   AIF   (&CTR LE 30).A0130       CREATE MSG IN 30 BYTE INCR
&MSGCH   SETC  '&MSG'(&DISP,30)
         DC    C'&MSGCH'
&DISP    SETA  &DISP+30
&CTR     SETA  &CTR-30
         AGO   .A0120
.A0130   ANOP
&MSGCH   SETC  '&MSG'(&DISP,&CTR)
         DC    C'&MSGCH'
&IEDFMIN   SETB  1
.A0990   MEND
