         MACRO
&NAME    READY &GMMSG=0,&RSMSG=0,&PURGEXT=0,&PRIEXIT=0,         X30X5MTC
               &OPENCHK=CHKPT                                   X30X5MT
         GBLB  &IEDQZA(64)
.********************** MICROFICHE FLAGS ******************************
.*C060000                                                       X30X5MT
.*A210000,330000,930000                                         X30X5MT
.**********************************************************************
         AIF   (&IEDQZA(7) EQ 0).ERR3
         AIF   (&IEDQZA(50) EQ 1).ERR4
         AIF   ('&GMMSG' EQ '').ERR1
         AIF   ('&RSMSG' EQ '').ERR2
         AIF   ('&PURGEXT' EQ '').ERR5                          X30X5MT
         AIF   ('&PRIEXIT' EQ '').ERR6                          X30X5MT
         AIF   ('&OPENCHK' NE 'IDLE' AND '&OPENCHK' NE 'OPEN' AND '&OPE*
               NCHK' NE 'CHKPT').ERR7                           X30X5MT
&IEDQZA(50) SETB 1
&NAME    B     IEDQRDY .                BRANCH AROUND ADCONS
IEDQRDYA DC    A(&GMMSG) .              GOOD MORNING MESSAGE ROUTINE
         DC    A(&RSMSG) .              RESTART-IN-PROGRESS MSG RTN
         AIF   ('&PURGEXT' EQ '0').ADCN                         X30X5MT
         DC    V(&PURGEXT) .            PURGE EXIT ROUTINE      X30X5MT
         AGO   .SETPRI                                          X30X5MT
.ADCN    DC    A(0)                                             X30X5MT
.SETPRI  ANOP                                                   X30X5MT
         AIF   ('&PRIEXIT' EQ '0').PADCN                        X30X5MT
         DC    V(&PRIEXIT) .            PIRORITY EXIT ROUTINE   X30X5MT
         AGO   .SETFG                                           X30X5MT
.PADCN   DC    A(0)                                             X30X5MT
.SETFG   ANOP                                                   X30X5MT
         AIF   ('&OPENCHK' EQ 'IDLE' OR '&OPENCHK' EQ 'CHKPT').TEST2
         DC    XL1'80',XL3'00' .       FLAG FOR OPENCHK=OPEN    X30X5MT
         AGO   .ENDFG                                           X30X5MT
.TEST2   ANOP                                                   X30X5MT
         AIF   ('&OPENCHK' EQ 'CHKPT').IDLFG                    X30X5MT
         DC    XL1'40',XL3'00' .        FLAG FOR OPENCHK=IDLE   X30X5MT
         AGO   .ENDFG                                           X30X5MT
.IDLFG   DC    XL4'00' .               NO FLAG FOR OPENCHK=CHKPTX30X5MT
.ENDFG   ANOP                                                   X30X5MT
IEDQRDY  EQU   *
         AIF   (NOT &IEDQZA(51)).NOTSO                              TSO
         L     2,IEDTSOPT .             ADDRESS OF TSO WORK AREA
         L     2,12(2) .                GET ADDRESS OF STAE EXIT
*        STAE  (2),PURGE=HALT
         STAE  (2),PURGE=HALT
.NOTSO   ANOP                                                       TSO
*        LINK  EP=IEDQND                INVOKE CKPT ATTACHER
         LINK  EP=IEDQND .              INVOKE CKPT ATTACHER
         LA    1,IEDSAVE2 .             ADDRESS OF DISP SAVEAREA
         L     15,IEDEA .               TAM DISPATCHER ENTRY POINT
         OI    IEDBIT1,IEDREADN .       SET 'READY DONE' FLAG IN AVT
         BALR  14,15 .                  CALL TAM DISPATCHER
IEDREADN EQU   X'08' .                  READY MACRO HAS BEEN EXECUTED
         MEXIT
.ERR1    MNOTE 12,'***IHB002 INVALID GMMSG OPERAND SPECIFIED-   '
         MEXIT
.ERR2    MNOTE 12,'***IHB002 INVALID RSMSG OPERAND SPECIFIED-   '
         MEXIT
.ERR3    MNOTE 12,'***IHB305 INTRO MACRO NOT SPECIFIED'
         MEXIT
.ERR4    MNOTE 12,'***IHB067 READY MACRO PREVIOUSLY USED '
         MEXIT                                                  X30X5MT
.ERR5    MNOTE 12,'*** IHB002 INVALID PURGEXT OPERAND SPECIFIED-   '
         MEXIT                                                  X30X5MT
.ERR6    MNOTE 12,'*** IHB002 INVALID PRIEXIT OPERAND SPECIFIED-   '
         MEXIT                                                  X30X5MT
.ERR7    MNOTE 12,'***IHB002 INVALID OPENCHK OPERAND SPECIFIED-&OPENCHKC
               '                                                X30X5MT
         MEND
