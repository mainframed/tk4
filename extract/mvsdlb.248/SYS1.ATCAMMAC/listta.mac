         MACRO
&NM     LISTTA &RLN=,&ADDR=,&SCREEN=,&FULLSCR=,&SESSION=       @Y17XAYP
.*
.*       TCAM LEVEL 10.0                                       @Y17XAYP
.*
.*
.*FUNCTION -- THIS MACRO GIVES THE USER THE CAPABILITY TO SPECIFY
.*   ATTRIBUTES OF HIS CONFIGURATION WHICH VARY AMONG LINES OR
.*   TERMINALS WITHIN A LINE GROUP.
.*
.*ENTRY POINTS -- NOT APPLICABLE
.*
.*INPUT -- MACRO CALL PARAMETERS
.*
.*OUTPUT -- VALUES IN GLOBAL VARIABLES TO BE USED LATER BY THE
.*   'TSOMCP' MACRO.  MNOTE MESSAGES IF INVALID PARAMETERS ARE
.*   DETECTED.
.*
.*EXTERNAL REFERENCES - NOT APPLICABLE
.*
.*EXITS,NORMAL -- NORMAL EXIT OCCURS IF NO SERIOUS ERRORS IN THE
.*   CALLER'S PARAMETERS ARE DETECTED.
.*
.*EXITS, ERROR -- UPON DETECTION OF A SERIOUS ERROR, THE 'QUITSW'
.*   IS SET TO 1 AND FURTHER PROCESSING IS BYPASSED.
.*
.*TABLES/WORK AREAS -- SEE DESCRIPTIONS ACCOMPANYING VARIABLE
.*   DECLARATIONS.
.*
.*ATTRIBUTES -- NOT APPLICABLE
.*
.*CHARACTER CODE DEPENDENCY - NONE
.*
.*NOTES -- NONE
.*
.*
.*       GLOBAL VARIABLES
.*
         GBLA  &NLG                NUMBER OF LINEGRP MACROS ISSUED
         GBLA  &LNO(63)            NUMBER OF LINES/LINE GROUP
         GBLA  &ADRPT              POINTER TO LAST ADDR PARM GROUP
         GBLA  &SCRPT              POINTER TO LAST SCREEN PARM GROUP
         GBLA  &SCRH(2500)         CONTAINS SCREEN HEIGHT
         GBLA  &SCRW(2500)         CONTAINS SCREEN WIDTH
         GBLA  &TRMNO(2500)        NUMBER OF TERMINALS/LINE
.*
         GBLB  &LGRPSW             INDICATES LINEGRP ISSUED
         GBLB  &MCPSW              INDICATES TSOMCP ISSUED
         GBLB  &QUITSW             GENERATION QUIT SWITCH
         GBLB  &DIALSW(63)         ON MEANS SWITCHED LINE
         GBLB  &FSCR               FULL SCREEN REQUEST FLAG      Y06327
         GBLB  &SCRALL(2500)       FULL SCREEN READ SWITCHES     Y06327
         GBLB  &LUSB(2500)         LU/LU SESSION SWITCHES      @Y17XAYP
.*
.*
         GBLC  &GRPNAME(63)        CONTAINS GROUP= PARAMETER     Y06327
         GBLC  &T(63)              CONTAINS TERM= PARAMETER VALUES
         GBLC  &ADCHAR(2500)       CONTAINS ADDR= PARAMETER VALUES
.*
.*       LOCAL VARIABLES
.*
         LCLA  &B                  WORK VARIABLE               @Y17XAYP
         LCLA  &C                  WORK VARIABLE               @Y17XAYP
         LCLA  &D                  WORK VARIABLE               @Y17XAYP
         LCLA  &E                  WORK VARIABLE               @Y17XAYP
         LCLA  &G                  WORK VARIABLE               @Y17XAYP
         LCLA  &K                  WORK VARIABLE               @Y17XAYP
         LCLA  &P                  POINTER TO SCREEN/TERMNO ARRAYS
         LCLA  &I                  WORK VARIABLE
         LCLA  &J                  WORK VARIABLE
         LCLA  &M                  WORK VARIABLE               @Y17XAYP
         LCLC  &H
         LCLC  &W
         LCLA  &SH                 TEMPORARY STORAGE             Y06327
         LCLA  &SW                 TEMPORARY STORAGE             Y06327
         LCLB  &OLD                OLD SCREEN FORMAT FLAG        Y06327
.*
.*       TEST FOR MACRO CALL OUT OF ORDER
.*
         AIF   (&LGRPSW AND NOT &MCPSW).INORDER
         MNOTE 16,'IKJ54120I LISTTA MACRO OUT OF ORDER -- IGNORED'
         AGO   .GIVEUP             GO SET QUIT SW & EXIT
.*
.*       TEST IF CALL IS CONSISTANT WITH LAST LINEGRP CALL
.INORDER AIF   (N'&RLN EQ 1 AND T'&RLN EQ 'N').RLNOK           @Y17XAYP
         MNOTE 16,'IKJ54121I RLN OPERAND MISSING OR INVALID'
         AGO   .GIVEUP                                           Y06327
.RLNOK   AIF   (&RLN LE &LNO(&NLG) AND &RLN NE 0).REFOK        @Y17XAYP
         MNOTE 16,'IKJ54122I RLN OPERAND REFERS TO NON-EXISTENT LINE'
         AGO   .GIVEUP             GO SET QUIT SWITCH & EXIT
.REFOK   AIF   ('&T(&NLG)'(1,3) EQ '226').TERMOK               @Y17XAYP
         AIF   ('&T(&NLG)'(1,3) EQ '327').TERMOK
         AIF   ('&T(&NLG)' EQ 'PUNT').TERMOK
         AIF   ('&T(&NLG)' EQ '1050').TERMOK
         AIF   ('&T(&NLG)' EQ '5041').TERMOK
         MNOTE 16,'IKJ54123I LISTTA MACRO NOT ALLOWED WITH TERMINAL TYP-
               E OF PREVIOUS LINEGRP'
         AGO   .GIVEUP             GO SET QUIT SW & EXIT
.TERMOK  ANOP
.CKRLN   AIF   (&B EQ &RLN-1).SETPT IS THIS THE CORRECT RLN    @Y17XAYP
&C       SETA  &TRMNO(&SCRPT+&B+&D) GET TERMNO VALUE           @Y17XAYP
&E       SETA  &E+&C               ACCUMULATE NO. LU ENTRIES   @Y17XAYP
.LULOP   ANOP                                                  @Y17XAYP
&D       SETA &D+1                 INCREMENT LUNO SELECTOR     @Y17XAYP
&K       SETA &K+&TRMNO(&SCRPT+&B+&D) ACCUMLATE NO. LU         @Y17XAYP
&G       SETA  &G+1                INCREMENT LUNO COUNT/TERMNO @Y17XAYP
         AIF   (&G LT &C).LULOP    MORE LUNO ENTRIES/TERMNO    @Y17XAYP
&B       SETA  &B+1                SELECT NEXT TERMNO ENTRY    @Y17XAYP
&G       SETA  0                   RESET LUNO/TERMNO COUNT     @Y17XAYP
         AGO   .CKRLN                                          @Y17XAYP
.SETPT   ANOP                                                  @Y17XAYP
&P       SETA  &SCRPT+&B+&E        SET TRMNO PTR FOR THIS RLN  @Y17XAYP
&J       SETA  &ADRPT+&K           SET ADDCHAR PTR FOR THIS RLN@Y17XAYP
&D       SETA  0                                               @Y17XAYP
&E       SETA  0                                               @Y17XAYP
&I       SETA  &TRMNO(&P)          GET TERMNO VALUE THSI RLN   @Y17XAYP
.LUCNT   ANOP                                                  @Y17XAYP
&D       SETA  &D+1                INCREMENT LUNO SELECTOR     @Y17XAYP
&E       SETA  &E+&TRMNO(&P+&D)    ACCUMULATE LU COUNT         @Y17XAYP
         AIF   (&D LT &I).LUCNT    MORE LUNO TO ADD            @Y17XAYP
&I       SETA  0                                               @Y17XAYP
         AIF   (N'&SESSION EQ 0).ADDR BR IF OPERAND OMITTED    @Y17XAYP
         AIF   ('&GRPNAME(&NLG)' EQ '').SESERR  NOT ALLOWED    @Y17XAYP
         AIF   ('&T(&NLG)' EQ 'PUNT').MOVSES  VALID OPERAND    @Y17XAYP
.SESERR  MNOTE 4,'IKJ54114I SESSION OPERAND NOT ALLOWED FOR THIS TERMINX
               AL TYPE'                                        @Y17XAYP
         AGO   .ADDR                                           @Y17XAYP
.MOVSES  AIF   (N'&SESSION LE &E).SETSES VALID NO. OPERANDS    @Y17XAYP
         MNOTE 4,'IKJ54124I SESSION OPERAND REFERS TO NON-EXISTENT TERMX
               INAL(S)'                                        @Y17XAYP
         AGO   .ADDR                                           @Y17XAYP
.SETSES  AIF   ('&SESSION(&I+1)' EQ '').LUSES SKP NULL OPERAND @Y17XAYP
         AIF   ('&SESSION(&I+1)' EQ 'YES').LUSES SET ARRAY BIT @Y17XAYP
         AIF   ('&SESSION(&I+1)' EQ 'NO').NOSES                @Y17XAYP
         MNOTE 4,'IKJ60004I SESSION SUBLIST OPERAND &SESSION(&I+1) INCOX
               RRECT -- IGNORED'                               @Y17XAYP
         AGO   .LUSES                                          @Y17XAYP
.NOSES   ANOP                                                  @Y17XAYP
&LUSB(&J+&I)   SETB 0                                          @Y17XAYP
.LUSES   ANOP                                                  @Y17XAYP
&I       SETA  &I+1                CHECK NEXT OPERAND          @Y17XAYP
         AIF   (&I LT N'&SESSION).SETSES CHECK NEXT OPERAND    @Y17XAYP
.ADDR    AIF   (N'&ADDR EQ 0).SCREEN  BR IF OPERAND OMITTED    @Y17XAYP
         AIF   ('&GRPNAME(&NLG)' EQ '').ADDR0  BYPASS IF NOT 3705Y06327
         AIF   ('&T(&NLG)' EQ '226L').ADDER1  BR IF 226L
         MNOTE 4,'IKJ60002I ADDR OPERAND NOT USED FOR 3705. IGNORED'
.*                                                               Y06327
         AGO   .SCREEN                                           Y06327
.ADDR0   AIF   ('&T(&NLG)' NE '327L').NOTLOCL BR IF NOT 327L   @Y17XAYP
.ADDER1  MNOTE 4,'IKJ54114I ADDR OPERAND NOT ALLOWED FOR THIS TERMINAL -
                TYPE'
         AGO   .SCREEN             SKIP ADDR PROCESSING
.NOTLOCL AIF   (N'&ADDR LE &E).PUTADDR VALID NO.ADDR OPERANDS  @Y17XAYP
         MNOTE 4,'IKJ54124I ADDR OPERAND REFERS TO NON-EXISTENT TERMINA-
               L(S)'
         AGO   .SCREEN
.PUTADDR ANOP
.MVADDR  ANOP                      LOOP EXECUTED N'&ADDR TIMES
&I       SETA  0                   SET LOOP CONTROL VARIABLE
.ADDR2   AIF   ('&ADDR(&I+1)' EQ '').ADDRLT  SKIP NULL SUBOPERANDS
         AIF   (K'&ADDR(&I+1) EQ 2 AND '&T(&NLG)' EQ '1050').ADSZOK
         AIF   (K'&ADDR(&I+1) EQ 2 AND '&T(&NLG)' EQ '5041').ADSZOK
         AIF   (K'&ADDR(&I+1) EQ 4 AND '&T(&NLG)' EQ '226R').ADSZOK
         AIF   (K'&ADDR(&I+1) EQ 4 AND '&T(&NLG)' EQ '2265').ADSZOK
         AIF   (K'&ADDR(&I+1) EQ 4 AND '&T(&NLG)' EQ '327R').ADSZOK
         AIF   (K'&ADDR(&I+1) EQ 4 AND '&T(&NLG)' EQ '327S').ADSZOK
         MNOTE 4,'IKJ54118I ADDR=&ADDR(&I+1) INCORRECT LENGTH -- IGNORE-
               D'
         AGO   .ADDRLT
.ADSZOK  ANOP
&ADCHAR(&J+&I) SETC '&ADDR(&I+1)'  PUT IN ARRAY
.ADDRLT  ANOP
&I       SETA  &I+1                BUMP LOOP CONTROL VARIABLE
         AIF   (&I LT N'&ADDR).ADDR2   BR UNTIL ALL OPERANDS HANDLED
.SCREEN  AIF   ('&SCREEN' EQ '' AND '&FULLSCR' EQ '').EXIT     @Y17XAYP
         AIF   ('&SCREEN' EQ '').CHKFSCR                         Y06327
         AIF   ('&T(&NLG)'(1,3) EQ '226').CHKDSPY 2260 VALID     Y06327
         AIF   ('&T(&NLG)'(1,3) EQ '327').CHKDSPY 3270 VALID     Y06327
         AIF   ('&T(&NLG)' EQ 'PUNT').CHKDSPY PUNT VALID       @Y17XAYP
         MNOTE 4,'IKJ54114I SCREEN OPERAND NOT ALLOWED FOR THIS TERMINAL
               L TYPE'
         MEXIT                                                   Y06327
.**********************************************************************
.*       FOR COMPATIBILITY PURPOSES THE OLD FORMAT OF SCREEN
.*       SCREEN=(HEIGHT,WIDTH) IS STILL VALID.
.**********************************************************************
.CHKDSPY AIF   (N'&SCREEN EQ 2).OLDSCR                         @Y17XAYP
         AIF   (N'&SCREEN LE &E).CHKFSCR                       @Y17XAYP
         MNOTE 4,'IKJ54124I SCREEN OPERAND REFERS TO NON-EXISTENT TERMIX
               NAL(S)'                                           Y06327
         MEXIT                                                   Y06327
.OLDSCR  AIF   (K'&SCREEN(1) EQ 2 AND K'&SCREEN(2) EQ 2).OLDSCR1 Y06327
         AIF   (K'&SCREEN(1) EQ 4 AND K'&SCREEN(2) EQ 4).CHKFSCR Y06327
         AIF   (K'&SCREEN(1) EQ 0 AND K'&SCREEN(2) EQ 4).CHKFSCR Y06327
         AIF   (K'&SCREEN(1) EQ 4 AND K'&SCREEN(2) EQ 0).CHKFSCR Y06327
         MNOTE 4,'IKJ54116I SCREEN OPERAND MUST BE A TWO-OPERAND SUBLISX
               T'
         MEXIT
.OLDSCR1 ANOP                                                    Y06327
&OLD     SETB  1
.CHKFSCR AIF   ('&FULLSCR' EQ '').SCR1                         @Y17XAYP
         AIF   ('&T(&NLG)'(1,3) EQ '327').CHKNO                  Y06327
         AIF   ('&T(&NLG)' EQ 'PUNT').CHKNO FULLSCR VALID PUNT @Y17XAYP
         MNOTE 4,'IKJ54114I FULLSCR OPERAND NOT ALLOWED FOR THIS TERMINX
               AL TYPE'                                          Y06327
         MEXIT                                                   Y06327
.CHKNO   AIF   (N'&FULLSCR LE &E).SCR1                         @Y17XAYP
         MNOTE 4,'IKJ54124I FULLSCR OPERAND REFERS TO NON-EXISTENT TERMX
               INAL(S)'                                          Y06327
         MEXIT                                                   Y06327
.SCR1    AIF   ('&SCREEN' EQ '').MVFSCR                        @Y17XAYP
&I       SETA  0                   INITIALIZE SUBLIST POINTER    Y06327
.SCREEN2 AIF   (&OLD).SCR2                                     @Y17XAYP
         AIF   ('&SCREEN(&I+1)' EQ '').SCRNULL DEFAULT           Y06327
         AIF   (K'&SCREEN(&I+1) EQ 4).SCR2                       Y06327
         MNOTE 4,'IKJ60004I SCREEN SUBLIST OPERAND &SCREEN(&I+1) INCORRX
               ECT -- IGNORED'                                   Y06327
         AGO   .MVFSCR                                           Y06327
.SCR2    AIF   (NOT &OLD).SCR3                                   Y06327
&SH      SETA  &SCREEN(1)                                        Y06327
&SW      SETA  &SCREEN(2)                                        Y06327
         AGO   .SCR4                                             Y06327
.SCR3    ANOP                                                    Y06327
&H       SETC  '&SCREEN(&I+1)'(1,2)
&W       SETC  '&SCREEN(&I+1)'(3,2)
&SH      SETA  &H
&SW      SETA  &W
.SCR4    AIF   (&SH EQ 12 AND &SW EQ 40).PUTSCR                @Y17XAYP
         AIF   ('&T(&NLG)'(1,3) EQ '327').LGSCRN CK 24*80 SCR  @Y17XAYP
         AIF   ('&T(&NLG)' EQ 'PUNT').LGSCRN     CK 24*80 SCR  @Y17XAYP
         AIF   (&SH EQ 12 AND &SW EQ 80).PUTSCR                  Y06327
         AIF   (&SH EQ 6 AND &SW EQ 40).PUTSCR                   Y06327
         AIF   (&SH EQ 15 AND &SW EQ 64).PUTSCR                  Y06327
         AGO   .SCRER                                            Y06327
.LGSCRN  AIF   (&SH EQ 24 AND &SW EQ 80).PUTSCR                @Y17XAYP
.SCRER   ANOP                                                    Y06327
         MNOTE 4,'IKJ54117I NON-STANDARD SCREEN DIMENSIONS -- ACCEPTED'
.PUTSCR  ANOP                                                    Y06327
&SCRH(&J+&I) SETA &SH              PUT INTO ARRAY                Y06327
&SCRW(&J+&I) SETA &SW              PUT INTO ARRAY                Y06327
.SCRNULL ANOP                                                    Y06327
&I       SETA  &I+1                UPDATE SUBLIST POINTER        Y06327
         AIF   (NOT &OLD).SCR5                                   Y06327
         AIF   (&I LT &TRMNO(&P)).SCREEN2                        Y06327
         AGO   .MVFSCR                                           Y06327
.SCR5    AIF   (&I LT N'&SCREEN).SCREEN2 GET THE NEXT ENTRY    @Y17XAYP
.MVFSCR  AIF   ('&FULLSCR' EQ '').EXIT                         @Y17XAYP
&FSCR    SETB  1                   REQUEST FULL SCREEN           Y06327
&I       SETA  0                   INITIALIZE SUBLIST POINTER    Y06327
.FULLS2  AIF   ('&FULLSCR(&I+1)' EQ '').FNULL DEFAULT          @Y17XAYP
         AIF   ('&FULLSCR(&I+1)' EQ 'YES').FSCRY                 Y06327
         AIF   ('&FULLSCR(&I+1)' EQ 'NO').FSCRN                  Y06327
         MNOTE 4,'IKJ60004I FULLSCR SUBLIST OPERAND &FULLSCR(&I+1) INCOX
               RRECT -- IGNORED'                                 Y06327
         MEXIT                                                   Y06327
.FSCRY   ANOP                                                    Y06327
&SCRALL(&J+&I) SETB 1              SET FULLSCR FLAG IN ARRAY     Y06327
         AGO   .FNULL                                            Y06327
.FSCRN   ANOP                                                    Y06327
&SCRALL(&J+&I) SETB 0              CLEAR FULLSCR FLAG IN ARRAY   Y06327
.FNULL   ANOP                                                    Y06327
&I       SETA  &I+1                UPDATE SUBLIST POINTER        Y06327
         AIF   (&I LT N'&FULLSCR).FULLS2                         Y06327
         MEXIT                                                   Y06327
.*
.GIVEUP  ANOP
&QUITSW  SETB  1                        SERIOUS ERROR - DON'T GENERATE
.EXIT    ANOP                                                    Y06327
         MEXIT
         MEND
