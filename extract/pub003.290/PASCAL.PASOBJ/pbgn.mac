         MACRO ,
         PBGN
.***************************************************************
.*
.*       THIS MACRO DEFINES A SET OF MACROES USED
.*       TO TRANSLATE THE OUTPUT OF P_CODE ASSEMBLER
.*       (WHEN USED WITH ITS ASSEMBLY OPTION) TO 370
.*       INSTRUCTIONS. IT ALSO DEEINES THE VALUE OF
.*       THE GLOBAL ASSEMBLY TIME PARAMETERS USED
.*       BY OTHER MACROES.
.*
.***************************************************************
         GBLB  &PUTLC
         GBLA  &PGCTR
         LCLA  &I
.*
         PRINT NOGEN
.*
         MACRO
&L       LSA   &R,&Q
.*       ++++  LOAD ADDRESS OF STRING 'Q' INTO REG 'R'
&L       LA    &R,=C&Q
         MEND
         MACRO
         LOC    &I
.*       P_CODE PROGRAM COUNTER
         GBLB  &PUTLC
         GBLA  &PGCTR
&PGCTR   SETA  &I
         AIF (&PUTLC).PUTLC
         MEXIT
.PUTLC   B     *+10     SKIP AROUND P-COUNTER
         DC    CL6'C&I'
         MEND
         MACRO
         PERROR &I
.*       P_ASSEMBLER ERROR MESSAGE .
         MNOTE  8,'** ERROR  &I'
         MEND
         MACRO
&L       PEQ   &I
.*       SYMBOL DEFINING PSEUDO OP.
&L       EQU   &I
         MEND
         MACRO
&L       DEF   &I
.*       SAME AS "PEQ"
&L       EQU   &I
         MEND
         MACRO
         PEND
.*       CONSTANT POOL
         LTORG
         END
         MEND
.*
         PRINT GEN
.*
.*
.*        INITIALIZE GLOBAL VALUES
.*
.*
.*
&PUTLC   SETB  1
&PGCTR   SETA  3   INITIALIZE P_PROGRAM COUNTER
&PUTLC   SETB  1    PUT P_COUNTER IN LINE !
         MEND  ,
