         MACRO
&NAME    ITERATE &LABEL                                             SLB
.*#-------------------------------------------------------------------*
.*# ITERATE: REPEAT A DO GROUP IN STRUCTURED PROGRAMMING              *
.*#-------------------------------------------------------------------*
.*#                                                                   *
.*# FUNCTION: ITERATES A DO-LOOP   (= DO GROUP)                       *
.*#                                                                   *
.*#                                                                   *
.*# CODING:   ITERATE         (OPTIONAL DO GROUP LABEL)               *
.*#                                                                   *
.*#                                                                   *
.*#-------------------------------------------------------------------*
         COPY  IFGLO                                                SLB
         LCLC  &OPND                                                SLB
         LCLA  &I              INDEX DONAME SCANNING                SLB
.*                                                                  SLB
         AIF   (&DOLEVEL GT 0).OBR00                                SLB
    MNOTE 12,' ??????  ADIOS LOGIC !!!,     EMERGENCY MESSAGE FROM STRU*
               CTURED PROGRAMMING SYSTEM.'                          SLB
         MEXIT                                                      SLB
.*                                                                  SLB
.OBR00   ANOP                                                       SLB
         AIF   ('&LABEL' EQ '').NONAME    FIND LEVEL IF DO LABEL    SLB
.*-----------------------------------------------------------------SLB*
.*       LOOK UP NAME-TABLE FOR SPECIFIED NAME                     SLB*
.*-----------------------------------------------------------------SLB*
&I       SETA  1                                                    SLB
.LOOP    ANOP                                                       SLB
         AIF   (&I GT &DOLEVEL).FEHL19                              SLB
         AIF   ('&LABEL' EQ '&DONAME(&I)').ENDLOOP NAME IS IN TABLE SLB
&I       SETA  &I+1                                NEXT ELEMENT     SLB
         AGO  .LOOP                                                 SLB
.*-----------------------------------------------------------------SLB*
.*       EXIT CURRENT LOOP                                         SLB*
.*-----------------------------------------------------------------SLB*
.NONAME  ANOP                                                       SLB
&I       SETA  &DOLEVEL                                             SLB
.*-----------------------------------------------------------------SLB*
.*       BRANCH BACK TO START IF FROM-REGISTER IS NOT ZERO         SLB*
.*-----------------------------------------------------------------SLB*
.ENDLOOP ANOP                                                       SLB
         AIF   ('&DOFROM(&I)' EQ '').WHILE                          SLB
&OPND    SETC  '&IFPRAEF&DOENDLB(&I)-4'                             SLB
         AIF   ('&ASMMREL' NE 'ON').BASE01 CHECK MACRO LEVEL        SLB
         J     &OPND                    BRANCH TO END OF LOOP (BCT) SLB
         MEXIT                                                      SLB
.BASE01  ANOP                                                       SLB
         B     &OPND                    BRANCH TO END OF LOOP (BCT) SLB
         MEXIT                                                      SLB
.*-----------------------------------------------------------------SLB*
.*       BRANCH BACK TO START OF LOOP IN CASE OF WHILE CONTROL     SLB*
.*-----------------------------------------------------------------SLB*
.WHILE   ANOP                                                       SLB
&OPND    SETC  '&IFPRAEF&DOSTART(&I)'                               SLB
         AIF   ('&ASMMREL' NE 'ON').BASE02 CHECK MACRO LEVEL        SLB
         J     &OPND                   BRANCH BACK TO START OF LOOP SLB
         MEXIT                                                      SLB
.BASE02  ANOP                                                       SLB
         B     &OPND                   BRANCH BACK TO START OF LOOP SLB
         MEXIT                                                      SLB
.*                                                                  SLB
         COPY IFERR                                                 SLB
.*                                                                  SLB
         MEXIT                                                      SLB
         MEND                                                       SLB
