         MACRO
&LABEL   E3OP  &R,&BXD,&SUBOP
.*
.*       Generate E3 Opcodes in IFOX00
.*
         DS    0H
&LABEL   DC    0XL6'00'                Set Instruction Length of 6
         AIF   ('&SUBOP' EQ '3E').GENST     STRV Opcode ?
         AIF   ('&SUBOP' EQ '3F').GENST     STRVH Opcode ?
.*
.*       Use IC opcode to generate Base and Index Registers and
.*       DL Displacement for E3 Load instructions
.*
         IC    &R,&BXD                 OpCode overwritten to E3
         AGO   .GENE3
.*
.*       Use STC opcode to generate Base and Index Registers and
.*       DL Displacement for E3 Store instructions
.*
.GENST   ANOP
         STC   &R,&BXD                 Opcode overwritten to E3
.GENE3   ANOP
.*
.*       Org back to reset the Opcode to E3
.*
         ORG   *-4
         DC    X'E3'                   Opcode set to E3
.*
.*       Zero DH and set Sub Opcode
.*
         ORG   *+3
         DC    X'00',X'&SUBOP'
         MEND
