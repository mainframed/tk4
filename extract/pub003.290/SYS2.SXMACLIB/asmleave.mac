         MACRO
&NAME    ASMLEAVE &DO                                               SLB
.*#-------------------------------------------------------------------*
.*#   ASMLEAVE     MACRO FOR STRUCTURED PROGRAMMING                   *
.*#-------------------------------------------------------------------*
.*#
.*#   FUNCTION:    UNCONDITIONAL EXIT OF ONE OR MORE DO GROUPS.
.*#
.*#   SYNTAX       ASMLEAVE  : EXITS CURRENT DO GROUP
.*#
.*#                ASMLEAVE  : DOGROUP
.*#                            EXITS THE DO GROUP WITH THIS LABEL
.*#                            EXAMPLE: ASMLEAVE OUTER
.*#
.*#--------------------------------------------------------------------
         LCLA  &I              INDEX DONAME SCANNING                SLB
         LCLC  &OPND                                                SLB
         COPY  IFGLO                                                SLB
.*                                                                  SLB
&MACNA   SETC  'ASMLEAVE'                                           SLB
.*                                                                  SLB
         AIF   (&DOLEVEL GT 0).OBR00                                SLB
    MNOTE 12,' ??????  ADIOS LOGIC !!!,     EMERGENCY MESSAGE FROM STRU*
               CTURED PROGRAMMING SYSTEM.'                          SLB
         MEXIT                                                      SLB
.OBR00   ANOP                                                       SLB
         AIF   ('&DO' EQ '').NONAME      NAME OF DO GROUP SPECIFIED SLB
.*-----------------------------------------------------------------SLB*
.*       LOOK UP NAME-TABLE FOR CURRENT NAME                       SLB*
.*-----------------------------------------------------------------SLB*
&I       SETA  1                                                    SLB
.LOOP    ANOP                                                       SLB
         AIF   (&I GT &DOLEVEL).FEHL19                              SLB
         AIF   ('&DO' EQ '&DONAME(&I)').ENDLOOP NAME IS IN TABLE    SLB
&I       SETA  &I+1                             NEXT ELEMENT        SLB
         AGO  .LOOP                                                 SLB
.ENDLOOP ANOP                                                       SLB
         AGO  .GENER                                                SLB
.*-----------------------------------------------------------------SLB*
.*       EXIT CURRENT LOOP                                         SLB*
.*-----------------------------------------------------------------SLB*
.NONAME  ANOP                                                       SLB
&I       SETA  &DOLEVEL                                             SLB
.GENER   ANOP                                                       SLB
.*                                                                  SLB
.*-----------------------------------------------------------------SLB*
.*  GENERATE CODE TO EXIT THE SPECIFIED LOOP                       SLB*
.*-----------------------------------------------------------------SLB*
&OPND    SETC  '&IFPRAEF&DOENDLB(&I)'                               SLB
         AIF   ('&ASMMREL' NE 'ON').BASE01 CHECK MACRO LEVEL        SLB
         J     &OPND                     LEAVE THE LOOP             SLB
         AGO   .MACEND                                              SLB
.BASE01  ANOP                                                       SLB
         B     &OPND                     LEAVE THE LOOP             SLB
.*                                                                  SLB
         AGO   .MACEND                                              SLB
.*                                                                  SLB
         COPY IFERR                                                 SLB
.*                                                                  SLB
.MACEND  ANOP                                                       SLB
         MEXIT                                                      SLB
         MEND                                                       SLB
