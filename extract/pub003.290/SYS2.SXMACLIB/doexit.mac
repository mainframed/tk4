         MACRO
&NAME    DOEXIT &EXIT,&P1,&P2,&P3,&P4,&P5,&P6,&P7,&P8,&P9,&P10,     SLB*
               &P11,&P12,&P13,&P14,&P15,&P16,&P17,&P18,&P19,&P20    SLB
.*#-------------------------------------------------------------------*
.*#   DOEXIT     MACRO FOR STRUCTURED PROGRAMMING                     *
.*#-------------------------------------------------------------------*
.*#
.*#   FUNCTION:    CONDITIONALLY EXIT FROM CURRENT DO GROUP
.*#
.*#   SYNTAX   DOEXIT       : UNCONDITIONALLY EXITS CURRENT DO GROUP
.*#
.*#            DOEXIT CC    : CONDITIONALLY EXITS CURRENT DO GROUP
.*#                   COND1
.*#                   COND1,OP1,COND2
.*#                   COND1,OP1,COND2,OP2,COND3,...,CONDN
.*#
.*#                -   CC    : SIMPLY TESTS THE CURRENT CONDITION CODE
.*#                            VALUE, AND MAY BE SPECIFIED AS A
.*#                            MNEMONIC CONDITION CODE OR A NUMERIC
.*#                            VALUE BETWEEN 1 AND 14. THE CC VALUE
.*#                            CAN BE COMBINED WITH CONDI EXPRESSIONS
.*#                            AS DESCRIBED BELOW.
.*#
.*#                -   CONDI : A VALID ASSEMBLER INSTRUCTION WITH
.*#                            MNEMONIC CONDITION CODE (IN PARENTHESES)
.*#                            EXAMPLE: (TM,SWITCH,X'04',O)
.*#                            FOR COMPARE OPERATIONS THE CONDITION-
.*#                            CODE WILL BE PUT BETWEEN THE OPERANDS
.*#                            EXAMPLE: (CLC,FIELD1,EQ,FIELD2)
.*#
.*#--------------------------------------------------------------------
         COPY  IFGLO                                                SLB
&MACNA   SETC  'DOEXIT'                                             SLB
         AIF   (&DOLEVEL GT 0).OBR00                                SLB
    MNOTE 12,' ??????  ADIOS LOGIC !!!,     EMERGENCY MESSAGE FROM STRU*
               CTURED PROGRAMMING SYSTEM.'                          SLB
         MEXIT                                                      SLB
.OBR00   ANOP                                                       SLB
.*-----------------------------------------------------------------SLB*
.*       TREAT DOEXIT SANS CONDITION AS ASMLEAVE                   SLB*
.*-----------------------------------------------------------------SLB*
         AIF   ('&EXIT' NE '').DOEXIT                               SLB
         ASMLEAVE ,                                                 SLB
         MEXIT                                                      SLB
.DOEXIT  ANOP                                                       SLB
.*-----------------------------------------------------------------SLB*
.*       GENERATE LABEL FOR BRANCH ON TRUE                         SLB*
.*-----------------------------------------------------------------SLB*
.*                                                                  SLB
&DOTRUE  SETC  '&DOENDLB(&DOLEVEL)'                                 SLB
.*                                                                  SLB
.*-----------------------------------------------------------------SLB*
.*       CALL IF-PROCESSOR TO ANALYZE CONDITION                    SLB*
.*-----------------------------------------------------------------SLB*
         IFPRO &DOTRUE,&DOTRUE,DOEXIT,&EXIT,                        SLB*
               &P1,&P2,&P3,&P4,&P5,&P6,&P7,                         SLB*
               &P8,&P9,&P10,&P11,&P12,&P13,&P14,&P15,&P16,&P17,     SLB*
               &P18,&P19,&P20                                       SLB
         MEND                                                       SLB
