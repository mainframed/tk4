./  CHANGE NAME=DSSDUMP,NEW=PS
*                                                               JW12114
*  Updated 23APR12 - Authorization through RAC added.           JW12114
*    On systems with resource access control active DSSDUMP     JW12114
*    executes only if the caller has read access to profile     JW12114
*    DSSAUTH in the FACILITY class.                             JW12114
*                                                               JW12114
*************************************************************** JW12114
./ DELETE  SEQ1=01900000,SEQ2=01910000
         BXLE  R15,R15,CHECKRAC YES; check user authorization   JW12114
ABEND    PRTL  '*** DSSDUMP must run authorized ***'            JW12114
BASE2    EQU   4096                                             JW12114
CHECKRAC LA    R12,INCRBASE(,R12) increment 2nd base            JW12114
INCRBASE EQU   CHECKRAC-BASE2 offset to original 2nd base       JW12114
         USING CHECKRAC,R12   establish addressability          JW12114
         L     R15,CVTPTR     get CVT address                   JW12114
         ICM   R15,B'1111',CVTSAF(R15) SAFV defined?            JW12114
         BZ    GOAHEAD        no RAC, go execute                JW12114
         USING SAFV,R15       addressability of SAFV            JW12114
         CLC   SAFVIDEN(4),SAFVID SAFV initialized?             JW12114
         BNE   GOAHEAD        no RAC, go execute                JW12114
         DROP  R15            SAFV no longer needed             JW12114
         RACHECK ENTITY=DSSAUTH,CLASS='FACILITY',ATTR=READ      JW12114
         LTR   R15,R15        RAC authorization granted?        JW12114
         BZ    GOAHEAD        yes, go execute                   JW12114
         S     R12,BASE2INC   no, restore 2nd base ..           JW12114
         B     ABEND           .. and abend                     JW12114
GOAHEAD  S     R12,BASE2INC   restore 2nd base                  JW12114
         B     HAVEAUTH       WHOOPPEE                          JW12114
BASE2INC DC    A(INCRBASE)    offset to original 2nd base       JW12114
DSSAUTH  DC    CL39'DSSAUTH'  facility name to authorize        JW12114
SAFVID   DC    CL4'SAFV'      SAFV eye catcher                  JW12114
         IHAPSA ,                                               JW12114
CVTSAF   EQU   248 CVTSAF doesn't exist but is a reserved field in 3.8J
         ICHSAFV  DSECT=YES  map SAFV                           JW12114
