/*                                                                 */
/* EXERCISE IN 3-DIMENSIONAL BINARY SEARCH.                        */
/*                                                                 */
/* NOTE: REQUIRES "RANDOM" TO BE INSTALLED IN SYS2.CMDPROC         */
/*       OR EQUIVALENT                                             */
/*                                                                 */
SET &N EQ 7 /* OPTIMUM VALUE */
WRITE YOU ARE THE CAPTAIN OF THE DESTROYER USS KLUDGE.
WRITE AN ENEMY SUB HAS BEEN REPORTED IN YOUR AREA.
WRITE YOUR MISSION: DESTROY IT BEFORE IT DESTROYS YOU.
WRITE SPECIFY DEPTH CHARGE EXPLOSION POINT WITH A TRIO OF NUMBERS
WRITE SEPARATED BY BLANKS OR COMMAS. E.G. 51,0,99
WRITE THE FIRST NUMBER REPRESENTS COORDINATES SOUTH-NORTH 0-99.
WRITE THE SECOND NUMBER REPRESENTS COORDINATES WEST-EAST 0-99.
WRITE THE THIRD NUMBER REPRESENTS THE DEPTH. 0 = SURFACE, 99 = BOTTOM.
WRITE YOU ARE SEARCHING A 100 X 100 X 100 CUBE.
GOODLUCK: WRITE GOOD LUCK.
WRITE
RANDOM 2
SET &A EQ &LASTCC
RANDOM 2
SET &B EQ &LASTCC
RANDOM 2
SET &C EQ &LASTCC
SET &I EQ 1
DO WHILE &I LE &N
WRITENR SHOT #&I.. =>&STR( )
AGAIN: +
READ &X,&Y,&Z
IF &X-&A EQ 0 AND &Y-&B EQ 0 AND &Z-&C EQ 0 THEN +
DO
WRITE BOOM! YOU FOUND IT IN &I TRIES.
REPLAY: WRITE ANOTHER GAME? Y/N
READ &YN
IF &YN EQ Y THEN GOTO GOODLUCK
EXIT
END
SET &P EQ &STR()
SET &Q EQ &STR()
SET &R EQ &STR( AND )
SET &S EQ &STR()
IF &X GT &A THEN SET &P EQ NORTH
IF &X LT &A THEN SET &P EQ SOUTH
IF &Y GT &B THEN SET &Q EQ EAST
IF &Y LT &B THEN SET &Q EQ WEST
IF (&P EQ &STR() AND &Q EQ &STR()) THEN SET &R EQ &STR()
IF &Z GT &C THEN SET &S EQ TOO LOW.
IF &Z LT &C THEN SET &S EQ TOO HIGH.
IF &Z EQ &C THEN SET &S EQ DEPTH OK.
WRITE SONAR REPORTS SHOT WAS &P&Q&R&S
SET &I EQ &I+1
END
WRITE YOU HAVE BEEN TORPEDOED! ABANDON SHIP!
WRITE THE SUBMARINE WAS AT &A, &B, &C..
GOTO REPLAY
