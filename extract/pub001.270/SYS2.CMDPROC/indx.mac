PROC 0 SR() TX() ST(1)
/*-------------------------------------------------------------------*/
/* INDX                                                              */
/* A SUBSTITUTE FOR THE SYSINDX FUNCTION OF TSO/E, WHICH IS NOT      */
/* AVAILABLE UNDER MVS 3.8                                           */
/* ARGUMENTS:                                                        */
/*   SR(NEEDLE) - WHAT TO SEARCH - REQUIRED                          */
/*   TX(HAYSTACK) - WHERE TO SEARCH - REQUIRED                       */
/*   ST(STARTPOS) - WHERE TO START (1 IF NOT SPECIFIED)              */
/* RETURN (LASTCC):                                                  */
/*   0 - NOT FOUND OR                                                */
/*   NN - POSITION OF SR WITHIN TX STARTING AT ST                    */
/* REMARKS:                                                          */
/*   FOR PERFORMANCE REASONS, THIS PROCEDURE DOES ALMOST NO ERROR    */
/*   CHECKING. SO IT MAY FAIL IF THE PARAMETERS ARE NOT ENTERED      */
/*   CORRECTLY.                                                      */
/*-------------------------------------------------------------------*/
/* VERSION AUTHOR/REMARKS                                            */
/* ------- -----------------------------------------------------     */
/* 1.0     WOLFGANG SCHAEFER - WOLFGANG@SCHAEFERNET.DE               */
/*         NEW PROCEDURE.                                            */
/*         TO DO: REWRITE AS ASSEMBLER CP FOR BETTER PERFORMANCE     */
/*-------------------------------------------------------------------*/
INDX00: -
  CONTROL NOLIST NOCONLIST NOSYMLIST
  SET REMAIN = &SUBSTR(&ST:&LENGTH(&STR(&TX)),&STR(&TX)) /* TO SCAN  */
  SET INDVAL = 1
INDX01: -
  IF &LENGTH(&STR(&REMAIN)) < &LENGTH(&STR(&SR)) THEN DO
     SET INDVAL = 0
     GOTO INDX90                              /* NO MATCH            */
     END
  IF &SUBSTR(1:&LENGTH(&STR(&SR)),&STR(&REMAIN)) = -
     &STR(&SR) THEN DO
     SET INDVAL = &INDVAL + &ST - 1
     GOTO INDX90                              /* FOUND IT            */
     END
  SET INDVAL = &INDVAL + 1                    /* TRY NEXT ...        */
  SET REMAIN = &SUBSTR(2:&LENGTH(&STR(&REMAIN)),&STR(&REMAIN))
  GOTO INDX01
INDX90: -
  EXIT CODE(&INDVAL)
