PROC 0
/*-------------------------------------------------------------------*/
/* DEMO 8 - SHOW FULL SCREEN WITH SOME (PARSED) INPUT FIELDS (EDS)   */
/*          EXPLOIT WIDE SCREEN SUPPORT AND ROW/COLUMN ADDRESSING    */
/*-------------------------------------------------------------------*/
/* -------------------------------------------------------------- */
/* INITIALIZE VARIABLES FOR USE WITH C3270                        */
/* -------------------------------------------------------------- */
SET INP2 = &STR(                                )
SET INP2 = &STR(&INP2&INP2&INP2&INP2&INP2&INP2&INP2&INP2)
SET C3270ROW = &STR(   )
SET C3270COL = &STR(   )
/* -------------------------------------------------------------- */
/* COMPUTE FIELD COORDINATES                                      */
/* -------------------------------------------------------------- */
C3270 GS WCC(B) CLEAR             /* CLEAR SCREEN AND GET SIZE    */
SET CTRROW = &C3270ROW/2          /* CENTER THE PANEL FROM DEMO 6 */
SET CTRCOL = &C3270COL/2          /* DEPENDING ON THE SCREEN SIZE */
SET CTRCOL = &CTRCOL-10
SET MR1 = &CTRROW-2
SET MR2 = &CTRROW-1
SET MR3 = &CTRROW+2
SET MR4 = &CTRROW+3
SET MC1 = &CTRCOL-7
SET MC2 = &CTRCOL-6
SET CSC = &CTRCOL+14
/* -------------------------------------------------------------- */
/* COMPUTE COORDINATES OF SCREEN CORNER FIELDS                    */
/* -------------------------------------------------------------- */
SET ULR = 1
SET ULC = 1
SET URR = 1
SET URC = &C3270COL-25
SET LLR = &C3270ROW
SET LLC = 1
SET LRR = &C3270ROW
SET LRC = &URC
/* -------------------------------------------------------------- */
/* COMPUTE COORDINATES OF A FRAME AROUND THE DEMO 6 PANEL         */
/* -------------------------------------------------------------- */
SET FLEFT  = &CTRCOL-10
SET FRIGHT = &CTRCOL+30
SET FR1    = &CTRCOL+32
SET FLINE  = &CTRROW-4
IF &FLEFT  LT 100 THEN SET FLEFT  = &STR(0&FLEFT)
IF &FRIGHT LT 100 THEN SET FRIGHT = &STR(0&FRIGHT)
IF &FR1    LT 100 THEN SET FR1    = &STR(0&FR1)
IF &FLINE  LT 100 THEN SET FLINE  = &STR(0&FLINE)
IF &FLEFT  LT 10  THEN SET FLEFT  = &STR(0&FLEFT)
IF &FRIGHT LT 10  THEN SET FRIGHT = &STR(0&FRIGHT)
IF &FR1    LT 10  THEN SET FR1    = &STR(0&FR1)
IF &FLINE  LT 10  THEN SET FLINE  = &STR(0&FLINE)
/* -------------------------------------------------------------- */
/* CREATE FRAME DATASTREAM, TWO SECTIONS DUE TO 256 BYTES LIMIT   */
/* -------------------------------------------------------------- */
SET FRR1 = &FLINE
SET FRAM2 = &STR(Ö$Ö3{4ÖC2ÖH2Ö#&FLINE&FR1 Ö×4)
DO WHILE &FLINE LT &CTRROW
  SET FLINE  = &FLINE+1
  IF &FLINE  LT 100 THEN SET FLINE  = &STR(0&FLINE)
  IF &FLINE  LT  10 THEN SET FLINE  = &STR(0&FLINE)
  SET FRAM2 = &STR(&FRAM2Ö-&FLINE&FLEFTÖ$Ö3{4ÖC2ÖH2 Ö×4)
  SET FRAM2 = &STR(&FRAM2Ö-&FLINE&FRIGHTÖ$Ö3{4ÖC2ÖH2 Ö×4)
END
SET FRAM1 = &STR(&FRAM2)
SET FRR2 = &FLINE+1
SET FRAM2 =
DO WHILE &FLINE LT &CTRROW+4
  SET FLINE  = &FLINE+1
  IF &FLINE  LT 100 THEN SET FLINE  = &STR(0&FLINE)
  IF &FLINE  LT  10 THEN SET FLINE  = &STR(0&FLINE)
  IF &FLINE  EQ &CTRROW+1 +
  THEN SET FRAM2 = &STR(&FRAM2Ö$Ö3{4ÖC2ÖH2 Ö×4)
  ELSE SET FRAM2 = &STR(&FRAM2Ö-&FLINE&FLEFTÖ$Ö3{4ÖC2ÖH2 Ö×4)
  SET FRAM2 = &STR(&FRAM2Ö-&FLINE&FRIGHTÖ$Ö3{4ÖC2ÖH2 Ö×4)
END
SET FLINE  = &FLINE+1
IF &FLINE  LT 100 THEN SET FLINE  = &STR(0&FLINE)
IF &FLINE  LT  10 THEN SET FLINE  = &STR(0&FLINE)
SET FRAM2 = &STR(&FRAM2Ö-&FLINE&FLEFTÖ$Ö3{4ÖC2ÖH2Ö#&FLINE&FR1 Ö×4)
/* -------------------------------------------------------------- */
/* DISPLAY SCREEN CORNER MARKERS                                  */
/* -------------------------------------------------------------- */
C3270 WCC(C) NE +
MESSAGE(&ULR,&ULC,'Ö$Ö3{AÖC4ÖH2 Ö$Ö2{4ÖC6<=== UPPER LEFT CORNERÖ×4')
C3270 WCC(B) NE +
MESSAGE(&URR,&URC,'Ö$Ö2{4ÖC6UPPER RIGHT CORNER ===>Ö$Ö3{AÖC4ÖH2 Ö×4')
C3270 WCC(B) NE +
MESSAGE(&LLR,&LLC,'Ö$Ö3{AÖC4ÖH2 Ö$Ö2{4ÖC6<=== LOWER LEFT CORNERÖ×4')
C3270 WCC(B) NE +
MESSAGE(&LRR,&LRC,'Ö$Ö2{4ÖC6LOWER RIGHT CORNER ===>Ö$Ö3{AÖC4ÖH2 ')
/* -------------------------------------------------------------- */
/* DISPLAY THREE INPUT FIELDS AND USER PROMPT (FROM DEMOS 3/6)    */
/* -------------------------------------------------------------- */
C3270 WCC(B) NE +
MESSAGE(&MR1,&CTRCOL,'Ö$Ö2{4ÖC1ENTER INPUT:Ö$Ö3{AÖC7ÖH4      Ö×4<')
C3270 WCC(B) NE +
MESSAGE(&MR2,&CTRCOL,'Ö$Ö2{4ÖC3 MORE INPUT:Ö$Ö3{AÖC4ÖH0      Ö×4<')
C3270 WCC(B) NE +
MESSAGE(&CTRROW,&CTRCOL,'Ö$Ö2{4ÖC5THIRD INPUT:Ö$Ö3{AÖC2ÖH2      Ö×4<')
C3270 WCC(B) NE +
MESSAGE(&MR3,&MC1,'FILL THE FIELDS AND PRESS <ENTER> ...')
C3270 WCC(B) NE +
MESSAGE(&MR4,&MC2,'PRESS <PF8> TO DISPLAY A FRAME ...')
READIT: -
  C3270 WCC(B) READTO(INP2) CURSOR(&MR1,&CSC) AI TA
  IF LASTCC NE 248 THEN GOTO WRITEIT                  /* PF8?       */
  /* -------------------------------------------------------------- */
  /* DISPLAY FRAME, TWO C3270 CALLS NEEDED DUE TO 256 BYTES LIMIT   */
  /* -------------------------------------------------------------- */
  C3270 WCC(B) NE TA MESSAGE(&FRR1,&FLEFT,'&FRAM1')   /* UPPER HALF */
  C3270 WCC(B) NE TA MESSAGE(&FRR2,&FLEFT,'&FRAM2')   /* LOWER HALF */
  GOTO READIT
  /* -------------------------------------------------------------- */
  /* PARSE INPUT FIELDS DELIMITED BY ×X (COULD BE MADE INTO A LOOP) */
  /* -------------------------------------------------------------- */
WRITEIT: -
  SET INP2 = &STR(&INP2×)
  %INDX SR(×) TX('&STR(&INP2)') ST(4)         /* END OF FIRST FIELD */
  SET FLD1END = &LASTCC
  SET FLD1 = &SUBSTR(8:&FLD1END-1,&STR(&INP2))       /* GET FIELD 1 */
  SET FR1 = &SUBSTR(2:4,&STR(&INP2))                 /* GET ROW 1   */
  SET FC1 = &SUBSTR(5:7,&STR(&INP2))                 /* GET COL 1   */
  %INDX SR(×) TX('&STR(&INP2)') ST(&FLD1END+3)   /* END OF 2. FIELD */
  SET FLD2END = &LASTCC
  SET FLD2 = &SUBSTR(&FLD1END+7:&FLD2END-1,&STR(&INP2))
  SET FR2 = &SUBSTR(&FLD1END+1:&FLD1END+3,&STR(&INP2)) /* GET ROW 2 */
  SET FC2 = &SUBSTR(&FLD1END+4:&FLD1END+6,&STR(&INP2)) /* GET COL 2 */
  %INDX SR(×) TX('&STR(&INP2)') ST(&FLD2END+3)   /* END OF 3. FIELD */
  SET FLD3END = &LASTCC
  SET FLD3 = &SUBSTR(&FLD2END+7:&FLD3END-1,&STR(&INP2))
  SET FR3 = &SUBSTR(&FLD2END+1:&FLD2END+3,&STR(&INP2)) /* GET ROW 3 */
  SET FC3 = &SUBSTR(&FLD2END+4:&FLD2END+6,&STR(&INP2)) /* GET COL 3 */
  %INDX SR(×) TX('&STR(&INP2)') ST(&FLD3END+3)   /* END OF 4. FIELD */
  SET FLD4END = &LASTCC
  SET FLD4 = &SUBSTR(&FLD3END+7:&FLD4END-1,&STR(&INP2))
  SET FR4 = &SUBSTR(&FLD3END+1:&FLD3END+3,&STR(&INP2)) /* GET ROW 4 */
  SET FC4 = &SUBSTR(&FLD3END+4:&FLD3END+6,&STR(&INP2)) /* GET COL 4 */
  %INDX SR(×) TX('&STR(&INP2)') ST(&FLD4END+3)   /* END OF 5. FIELD */
  SET FLD5END = &LASTCC
  SET FLD5 = &SUBSTR(&FLD4END+7:&FLD5END-1,&STR(&INP2))
  SET FR5 = &SUBSTR(&FLD4END+1:&FLD4END+3,&STR(&INP2)) /* GET ROW 5 */
  SET FC5 = &SUBSTR(&FLD4END+4:&FLD4END+6,&STR(&INP2)) /* GET COL 5 */
  %INDX SR(×) TX('&STR(&INP2)') ST(&FLD5END+3)   /* END OF 6. FIELD */
  SET FLD6END = &LASTCC
  SET FLD6 = &SUBSTR(&FLD5END+7:&FLD6END-1,&STR(&INP2))
  SET FR6 = &SUBSTR(&FLD5END+1:&FLD5END+3,&STR(&INP2)) /* GET ROW 6 */
  SET FC6 = &SUBSTR(&FLD5END+4:&FLD5END+6,&STR(&INP2)) /* GET COL 6 */
  %INDX SR(×) TX('&STR(&INP2)') ST(&FLD6END+3)   /* END OF 7. FIELD */
  SET FLD7END = &LASTCC
  SET FLD7 = &SUBSTR(&FLD6END+7:&FLD7END-1,&STR(&INP2))
  SET FR7 = &SUBSTR(&FLD6END+1:&FLD6END+3,&STR(&INP2)) /* GET ROW 7 */
  SET FC7 = &SUBSTR(&FLD6END+4:&FLD6END+6,&STR(&INP2)) /* GET COL 7 */
  /* -------------------------------------------------------------- */
  /* PRINT INPUT FIELDS                                             */
  /* -------------------------------------------------------------- */
  C3270 WCC(B) CLEAR
  WRITE &STR(INPUT INTO FIELD &FR1,&FC1:) &STR(&FLD1)
  WRITE &STR(INPUT INTO FIELD &FR2,&FC2:) &STR(&FLD2)
  WRITE &STR(INPUT INTO FIELD &FR3,&FC3:) &STR(&FLD3)
  WRITE &STR(INPUT INTO FIELD &FR4,&FC4:) &STR(&FLD4)
  WRITE &STR(INPUT INTO FIELD &FR5,&FC5:) &STR(&FLD5)
  WRITE &STR(INPUT INTO FIELD &FR6,&FC6:) &STR(&FLD6)
  WRITE &STR(INPUT INTO FIELD &FR7,&FC7:) &STR(&FLD7)
  WRITE C3270 (AKA KOMM) DEMO ENDED
EXIT
