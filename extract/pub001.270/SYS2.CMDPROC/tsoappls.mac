PROC 0
CONTROL MAIN PROMPT
/*-------------------------------------------------------------------*/
/*                                                                   */
/* Dialog Manager for the TSO Application Menu                       */
/*                                                                   */
/* This CLIST displays a static "primary option menu" type panel     */
/* compiled using MAP3270 on a 3270 device and uses C3270 to manage  */
/* the option selection dialog. If the user enters a valid option    */
/* the corresponding tool (RFE, RPF, IMON/370, QUEUE, HELP/FSH) is   */
/* invoked. In all other cases an appopriate error message is        */
/* issued.                                                           */
/*                                                                   */
/* Initial implementation - 2012/08/16 Juergen Winkelmann ETH Zurich */
/* Prefer FSH over HELP   - 2012/09/23 Juergen Winkelmann ETH Zurich */
/* Fix datastream issues  - 2013/08/12 Juergen Winkelmann ETH Zurich */
/* Add TERMTEST option 7  - 2013/08/28 Juergen Winkelmann ETH Zurich */
/* Enable CP prompting    - 2013/12/06 Juergen Winkelmann ETH Zurich */
/* Add option X to exit   - 2013/12/27 Juergen Winkelmann ETH Zurich */
/*                                                                   */
/*-------------------------------------------------------------------*/
INIT: -
  CHECKMOD FSH Q                             /* Select FSH for HELP  */
  IF &LASTCC EQ 0 THEN SET HELP = &STR(FSH)  /* if it is accessible  */
                  ELSE SET HELP = &STR(HELP) /* else just use HELP   */
  SET OPT = &STR(                     )
  SET OPTION = &STR(1)
  SET FOREVER = 1
  SET MSG = &STR(Ö×8Ö#D" )
  TSOMENU
DO WHILE &FOREVER EQ 1
  C3270 WCC(C) CURSOR(5,14) MESSAGE(4,1,'&MSG')
  C3270 WCC(B) CURSOR(5,14) MESSAGE(5,13,'Ö×AÖSÖC5ÖSÖHÖ0&OPTION')
  C3270 WCC(B) READTO(OPT)
  SET AID = &LASTCC
  SET MSG = &STR(Ö×8Ö#D" )
  SET OPT = &STR(&OPT×)
  %INDX SR(×) TX('&STR(&OPT)') ST(1)
  SET OPTEND = &LASTCC
  SET OPTION = &SUBSTR(&OPTEND:&OPTEND,&STR(&OPTION&OPT))
  IF &AID EQ 125 THEN IF &OPTION EQ X THEN SET AID = 243
  IF &AID EQ 243 THEN SET FOREVER = 0
  ELSE DO
    IF &AID EQ 110 THEN GOTO INIT
    IF &AID EQ 125 THEN -
      DO
        SET COMMAND = &STR(ERROR)
        IF &OPTION EQ 1 THEN SET COMMAND = &STR(RFE)
        IF &OPTION EQ 2 THEN SET COMMAND = &STR(RPF)
        IF &OPTION EQ 3 THEN SET COMMAND = &STR(IM)
        IF &OPTION EQ 4 THEN SET COMMAND = &STR(Q)
        IF &OPTION EQ 5 THEN SET COMMAND = &STR(&HELP)
        IF &OPTION EQ 6 THEN SET COMMAND = &STR(&HELP UTILS)
        IF &OPTION EQ 7 THEN SET COMMAND = &STR(TERMTEST)
        IF &COMMAND NE ERROR THEN -
          DO
            &COMMAND
            SET CCC = &LASTCC
            WRITENR
            TSOMENU
            SET MSG = &STR(Ö×8&COMMAND ENDED WITH RC=&CCCÖ#D" )
          END
        ELSE SET MSG = &STR(Ö×8INVALID OPTIONÖ#D" )
      END
    ELSE SET MSG = &STR(Ö×8KEY NOT ASSIGNEDÖ#D" )
  END
END
EWA3270
ALLOC FI(STDIN)   DA('SYS2.CMDPROC(TSOAPTRM)') SHR
ALLOC FI(MAWKPRT) DA('SYS2.CMDPROC(MAWKPRT)') SHR
ALLOC FI(STDOUT)  DA(*)
MAWK -W EXEC MAWKPRT
FREE FI(STDIN,STDOUT,MAWKPRT)
