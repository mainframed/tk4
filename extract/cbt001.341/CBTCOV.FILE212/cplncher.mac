         TITLE     'LAUNCHER - DATA CONTROL BLOCK(DCB)'
         DCBD      DEVD=(DA),DSORG=(PS)
         TITLE     'LAUNCHER - MAIN PROGRAM'
CPLNCHER CSECT
*********************************************************************
*
*        PROGRAM: LAUNCHER.
*        INSTALLATION: U.S. SENATE COMPUTER CENTER.
*        AUTHOR: D. HALBIG.
*        DATE WRITTEN: MARCH 1980.
*        FUNCTION: TO SUBMIT JOBS TO THE JES INTERNAL READER
*
*        REGISTER                 USAGE
*              R15                ENTRY POINT/RETURN CODE
*              R14                RETURN ADDRESS/ERROR INDICATOR
*              R13                CALLER SAVE AREA/PROGRAM WORK AREA
*              R12                PROGRAM BASE REGISTER
*              R11                SUBROUTINE BASE REGISTER
*              R10                SUBROUTINE RETURN ADDRESS
*              R9-R0              WORK REGISTERS
*
*********************************************************************
         REGEQU
         SAVE      (14,12),,CPLNCHER-&SYSDATE-&SYSTIME
         LR        R12,R15        ESTAB. BASE REGISTER
         USING     CPLNCHER,R12   TELL ASSEMBLER
         L         R0,GETPARMS    OBTAIN GETMAIN SUBPOOL & SIZE
         GETMAIN   R,LV=(0)
         LR        R2,R1          SET UP TO CLEAR G/M AREA
         L         R3,GETPARMS
         SR        R4,R4
         SR        R5,R5
         MVCL      R2,R4          SET AREA TO BINARY ZEROS
         ST        R1,8(,R13)     TELL CALLER ABOUT OUR S/A
         ST        R13,4(,R1)     PUT CALLER'S S/A PTR IN OUR S/A
         LR        R13,R1
         USING     WORKAREA,R13 ESTAB. ADDRESSABILITY FOR WORK AREA
         L         R11,=A(INITRTN)  GET ADDR OF INITIALIZATION RTN
         BALR      R10,R11        PERFORM INITIALIZATION
         TM        ERRORSW,ERRINIT ERROR IN INITIALIZATION?
         BZ        MAININOK       BIF INITIALIZATION OK
         EX        R0,*           OTHERWISE, FORCE AN 0C3
MAININOK EQU       *
         L         R11,=A(READRTN) GET ADDR OF READ/PUT ROUTINE
         BALR      R10,R11        PERFORM IT
         TM        ERRORSW,ERRREAD ERROR IN READ/PUT ROUTINE?
         BZ        MAINRPOK       BIF READ/PUT OK
         EX        R0,*           OTHERWISE, FORCE AN 0C3
MAINRPOK EQU       *
         L         R11,=A(TERMRTN)
         BALR      R10,R11        PERFORM TERMINATION
         TM        ERRORSW,ERRTERM ERROR IN TERMINATION ROUTINE?
         BZ        MAINTMOK       BIF TERMINATION OK
         EX        R0,*           OTHERWISE, SET UP AN 0C3
MAINTMOK EQU       *
         L         R0,GETPARMS    SET UP FOR FREEMAIN
         LR        R1,R13
         L         R2,SAVEAREA+4
         FREEMAIN  R,LV=(0),A=(1)
         LR        R13,R2
         RETURN    (14,12),RC=0
GETPARMS DC        A(WORKLGTH)    PARMS FOR GETMAIN/FREEMAIN
         LTORG
         TITLE     'LAUNCHER - INITIALIZATION ROUTINE'
INITRTN  DS        0H
********************************************************************
*
*        INITIALIZATION ROUTINE
*
**********************************************************************
         USING     INITRTN,R11    MAKE ROUTINE ADDRESSABLE
         MVC       INTRDR(INITDCBL),INITDCB MOVE IN PATTERN OUTPUT DCB
         MVC       SYSIN(INITSYSL),INITSYS MOVE IN PATTERN INPUT DCB
         OI        OPENLIST,X'80' SET END-OF-LIST
         OPEN      (INTRDR,(OUTPUT)),MF=(E,OPENLIST)
         TM        INTRDR+(DCBOFLGS-IHADCB),DCBOFOPN OPEN OK?
         BO        INITOPOK       BIF OUTPUT FILE OPENED OK
         BAL       R14,INITERR    OTHERWISE, SIGNAL ERROR
INITOPOK EQU       *
         OPEN      (SYSIN,(INPUT)),MF=(E,OPENLIST)
         TM        SYSIN+(DCBOFLGS-IHADCB),DCBOFOPN OPEN OK?
         BO        INITINOK       BIF INPUT FILE OPENED OK
         BAL       R14,INITERR    OTHERWISE, SIGNAL ERROR
INITINOK EQU       *
         B         INITEXIT       NOW LEAVE NORMALLY
INITERR  EQU       *
         OI        ERRORSW,ERRINIT SET ERROR INDICATOR
INITEXIT EQU       *
         BR        R10            NOW RETURN TO CALLER
         TITLE     'LAUNCHER - INITIALIZATION CONSTANTS'
INITDCB  DCB       MACRF=(PM),RECFM=F,LRECL=80,BLKSIZE=80,             X
               DDNAME=INTRDR,DSORG=PS
INITDCBL EQU       *-INITDCB
INITSYS  DCB       MACRF=(GM),RECFM=FB,LRECL=80,BLKSIZE=6160,          X
               DDNAME=SYSIN,DSORG=PS,EODAD=READEOF
INITSYSL EQU       *-INITSYS
         LTORG
         DROP      R11
         TITLE     'LAUNCHER - READ && PUT ROUTINE'
READRTN  DS        0H
*********************************************************************
*
*        READ & PUT ROUTINE
*
*********************************************************************
         USING     READRTN,R11
         LA        R1,READEOF     ESTAB. EOF RTN ADDR
         STCM      R1,7,SYSIN+(DCBEODA-IHADCB)
READNEXT EQU       *
         GET       SYSIN,CARDAREA
         PUT       INTRDR,CARDAREA
         B         READNEXT       LOOP UNTIL DATA TRANSFERRED
READEOF  EQU       *
         PUT       INTRDR,SLASHEOF PUT OUT /*EOF  CARD FOR READER
READEXIT EQU       *
         BR        R10            RETURN TO CALLER
         TITLE     'LAUNCHER - READ/PUT ROUTINE CONSTANTS'
SLASHEOF DC        CL80'/*EOF'
         LTORG
         DROP      R11
         TITLE     'LAUNCHER - TERMINATION ROUTINE'
TERMRTN  DS        0H
*********************************************************************
*
*        TERMINATION ROUTINE
*
*********************************************************************
         USING     TERMRTN,R11
         OI        CLOSLIST,X'80' SET END-OF-LIST
         CLOSE     (SYSIN),MF=(E,CLOSLIST)
         CLOSE     (INTRDR),MF=(E,CLOSLIST)
TERMEXIT EQU       *
         BR        R10            RETURN TO CALLER
         LTORG
         DROP      R11
         TITLE     'LAUNCHER - WORK AREA'
WORKAREA DSECT
**********************************************************************
*
*        WORK AREA
*
**********************************************************************
SAVEAREA DS        18F
ERRORSW  DS        XL1            ERROR INDICATORS
ERRINIT  EQU   X'01'              ERROR IN INITIALIZATION ROUTINE
ERRREAD  EQU   X'02'              ERROR IN READ/PUT ROUTINE
ERRTERM  EQU   X'04'              ERROR IN TERMINATION ROUTINE
OPENLIST DS        A
CLOSLIST DS        A
INTRDR   DCB       MACRF=(PM),RECFM=F,BLKSIZE=80,LRECL=80,             X
               DDNAME=INTRDR,DSORG=PS
SYSIN    DCB       MACRF=(GM),RECFM=FB,BLKSIZE=6160,LRECL=80,          X
               DDNAME=SYSIN,DSORG=PS
CARDAREA DS        CL80
WORKLGTH EQU       (((*-WORKAREA+7)/8)*8)
         END
