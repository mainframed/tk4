DRUMWALL CSECT
***********************************************************************
*                                                                     *
*    THIS IS SOURCE FOR MODIFICATION TO FENCE                         *
*    DRUMS FOR TSO AND ONLINE SYSTEM PAGES ONLY.                      *
*                                                                     *
*    PAGE OUTS FOR TSO SESSIONS ARE PASSED THRU TO DRUM UNCONDITIONALL*
*    PAGE OUTS FOR OTHER WORKLOADS ARE ACCOMODATED BY CHECKING DOMAIN *
*    NUMBERS:                                                         *
*                DOMAIN NUMBER        WORKLOAD                        *
*                     3                TEST CICS/M204                 *
*                     4                PROD CICS/M204                 *
*                     7                VTAM                           *
*   THE IPS CONTROLS WHEN THESE WORKLOADS APPEAR IN THEIR RESPECTIVE  *
*   DOMAINS BY SPECIFYING MULTI-PERIOD PERFORMANCE GROUPS.  THE FIRST *
*   PERIOD SPECIFIES A DOMAIN DIFFERENT FROM THE FINAL, DESIRED DOMAIN*
*   THE SECOND PERIOD SPECIFIES THE DOMAIN (ABOVE) WHICH WILL GIVE    *
*   ACCESS TO THE DRUM.  THIS TECHNIQUE PERMITS THE SPECIFIC ONLINE   *
*   WORKLOAD TO COMPLETE SOME PORTION OF ITS INITIALIZATION PROCESS,  *
*   DURING WHICH PAGE OUTS OF UNDESIRED PAGES GO   TO THE             *
*   LOW-PERFORMANCE PAGING DEVICES.  CARE MUST BE TAKEN NOT TO BE TOO *
*   FREE WITH THIS: IF INITIALIZATION CAUSES PAGE-OUTS OF REENTRANT   *
*   CODE WHICH WILL BE SUBSEQUENTLY FREQUENTLY ACCESSED, PERFORMANCE  *
*   COULD SUFFER.  IN THE CASE OF CICS, THE IPS SHOULD FORCE THE      *
*   SWITCH TO THE SECOND PERIOD (AND THUS THE DESIRED DOMAIN) JUST AS *
*   CICS NUCLEUS LOADING (AND PAGE-OUT) IS COMPLETE.  IN THIS CASE,   *
*   THE CICS REENTRANT MODULES WILL THEN BE PAGED-OUT TO THE DRUM,    *
*   A DESIREABLE EFFECT.                                              *
*                                                                     *
*   IF THE ADDRESS SPACE(S) BEING CONTROLLED THIS WAY IS(ARE) MARKED  *
*   NON-SWAPPABLE IN THE MVS PPT, SRM MULTI-PERIOD CONTROL OF THE     *
*   ADDRESS SPACE(S) IS IMPOSSIBLE; THE SRM NEVER MOVES THE ADDRESS   *
*   SPACE INTO PERIOD 2.  IN THIS CASE, IT IS USEFUL TO SET UP TWO    *
*   IPS'S, ONE CONTAINING ONLY NON-PREFERRED DOMAINS, THE SECOND      *
*   CONTAINING PREFERRED DOMAINS FOR PERFORMANCE GROUPS CONTROLLING   *
*   ADDRESS SPACES WHICH WILL HAVE ACCESS TO THE DRUM(S).  WHEN THE   *
*   ADDRESS SPACES ARE BROUGHT UP, THE IPS SHOULD CONTAIN ONLY NON-   *
*   PREFERRED DOMAINS.  AFTER STARTUP FOR ALL THE TARGET ADDRESS      *
*   SPACES IS COMPLETE, THE IPS SHOULD BE SWITCHED TO GIVE THE        *
*   TARGET ADDRESS SPACES ACCESS TO THE DRUM VIA THE PREFERRED        *
*   DOMAIN NUMBERS.                                                   *
*                                                                     *
***********************************************************************
PAREPTR  EQU   2      SAFE
R6       EQU   6      SAFE
R7       EQU   7      BASE 1
R8       EQU   8      BASE 2
R9       EQU   9      POINTER TO ASM TABLES
RB       EQU   11     SAFE
         USING *,R7,R8
         ORG   *+X'139C'    VERSION UZ55891 MVS SP 1.3
*
DRUMFENC L     PAREPTR,16(,R6)              ** ORIGINAL INSTRUCTIONS **
         LTR   PAREPTR,PAREPTR              ** ORIGINAL INSTRUCTIONS **
*
         ORG   DRUMFENC     BACK UP OVER AREA TO BE ZAPPED
*
GOTOZAP  BAL   RB,PATCH     BR TO PATCH     ** NEW INSTRUCTIONS      **
         NOPR  0            DON'T CHANGE CC ** NEW INSTRUCTIONS      **
*
*
         ORG   DRUMWALL+X'1E88'    PATCH AREA
PATCH    DS    0H
         L     PAREPTR,16(,R6)    RESTORE OVERLAYED INSTRUCTION
         LTR   PAREPTR,PAREPTR  Q. ANY DRUMS??
         BZR   RB               A. NO - FORGET ALL THE FANCY FOOTWORK
*   SKIP THE PART ABOUT VIO FROM SHARE MOD--CONTROL IN PAGEADDS.
         L     R6,X'1D0'(,R9)    -> PCELAIA
         SH    R6,=H'36'        BACK UP TO CURRENT PCB
         USING PCB,R6
         L     R6,PCBASCB       -> ASCB
         USING ASCB,R6
         L     R6,ASCBOUCB      -> OUCB
         USING OUCB,R6
         TM    OUCBYFL,OUCBLOG  Q. TSO USER?
         BO    USEDRUM          A. YES - USE DRUM
         CLI   OUCBDMN,3        Q. IS TEST CICS/M204 DOMAIN?
         BE    USEDRUM          A. YES - USE DRUM
         CLI   OUCBDMN,4        Q. IS PROD CICS/M204 DOMAIN?
         BE    USEDRUM          A. YES - USE DRUM
         CLI   OUCBDMN,7        Q. IS VTAM DOMAIN?
         BE    USEDRUM          A. YES - USE DRUM
*                               A. FALL THRU - NO - SKIP DRUM
SKIPDRUM DS    0H
         CLI   =X'00',X'00'     SET CC EQUAL
         BR    RB
*
USEDRUM  DS    0H
         CLI   =X'00',X'FF'     SET CC NOT EQUAL
         BR    RB
*
         LTORG
         PRINT ON,NOGEN
AIA      DSECT
         DS    9X
AIAFLG1  DS    X
         IHAASCB
         IRAOUCB
         IHAPCB
         IHAOUXB
         END
