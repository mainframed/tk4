         TITLE     'GACCTPGM - MAIN PROGRAM'
GACCTPGM CSECT
**********************************************************************
*
*        PROGRAM: ACCOUNT INFORMATION EXTRACTION PROGRAM.
*        AUTHOR: D. HALBIG.
*        DATE WRITTEN: DECEMBER 1979.
*        INSTALLATION: U.S. SENATE COMPUTER CENTER.
*        FUNCTION: TO EXTRACT THE FOLLOWING INFORMATION ON A
*        CURRENTLY-EXECUTING JOB & MAKE THE INFORMATION
*        AVAILABLE TO A USER'S PROGRAM.
*              .SYSTEM ID (TAKEN FROM SID PARAMETER)
*              .JOB NAME
*              .READER START TIME FOR THIS JOB
*              .READER START DATE FOR THIS JOB
*              .JES JOB NUMBER INFORMATION
*              .ACCOUNT # INFORMATION FROM JOB CARD OF THIS JOB
*        THE CALLING PROGRAM MUST PASS THE ADDRESS OF A WORK
*        AREA TO THIS SUBROUTINE.  THE SUBROUTINE FILLS IN
*        THE WORK AREA USING THE FORMAT GIVEN IN MACRO 'GACCTMAC'.
*
*        REGISTER                 USAGE
*              R15                ENTRY POINT/RETURN CODE
*              R14                RETURN ADDRESS/WORK REG
*              R13                SAVE AREA/WORK AREA POINTER
*              R12                BASE REGISTER FOR PROGRAM
*              R11-R0             WORK REGISTERS
*
*        CAUTION: SINCE THIS SUBROUTINE USES 'MODESET' TO PUT
*        ITSELF INTO PROTECT KEY ZERO, THE CALLING PROGRAM
*        & LIBRARY FROM WHICH IT EXECUTES MUST BE AUTHORIZED.
*
*        NOTES: IF SMF IS NOT IN SYSTEM (TCBTCT = 0), THEN RC=4
*
*********************************************************************
         REGEQU
         SAVE      (14,12),,GACCTPGM-&SYSDATE
         LR        R12,R15
         USING     GACCTPGM,R12   TELL ASSEMBLER OF BASE REG.
         L         R11,0(R1)      GET ADDR OF WORK AREA PASSED TO SUBR.
         USING     GACCTMAC,R11   GIVE IT  ADDRESSABILITY
         L         R0,GAGETPRM    OBTAIN GETMAIN PARMS
         GETMAIN   R,LV=(0)       GETMAIN A WORK AREA
         LR        R2,R1          ZERO OUT WORK AREA
         L         R3,GAGETPRM    GET SIZE OF G/M AREA
         SR        R4,R4
         SR        R5,R5
         MVCL      R2,R4
         ST        R1,8(,R13)     COMPLETE S/A CHAINING
         ST        R13,4(,R1)
         LR        R13,R1
         USING     WORKAREA,R13   MAKE AREA ADDRESSABLE
         LA        R10,16         GET ADDR OF AREA WHICH HAS CVT PTR
         L         R10,0(,R10)    GET ADDR OF CVT
         L         R10,0(,R10)    GET ADDR OF NEXT-CURR TCBS
         L         R10,4(,R10)    GET ADDR OF CURR (OUR) TCB.
         USING     TCB,R10        GIVE IT ADDRESSABILITY
         L         R9,TCBTCT      GET TIMING CONTROL TABLE ADDR
         LA        R15,4          SET RC=4 INITIALLY
         ST        R15,RETCODE
         XC        GAMCPUID(GAMEND-GAMCPUID),GAMCPUID ZERO AREA
         LTR       R9,R9          IS SMF IN THE SYSTEM?
         BZ        GACTNSMF       BIF NO SMF IN SYSTEM
         USING     SMFTCT,R9      GIVE IT ADDRESSABILITY
         L         R9,TCTJMR      GET ADDR OF JOB MGMT RECORD
         USING     JMR,R9         GIVE IT ADDRESSABILITY
         MVC       GAMCPUID(4),JMRCPUID SID & MDL FROM SMCA
         MVC       GAMJOB(GAMJES#-GAMJOB),JMRJOB JOBNAME,RDR TIME&DATE
         LA        R15,0          SET RC=0 IF SMF IN SYSTEM
         ST        R15,RETCODE    STOW RETURN CODE
         DROP      R9             DON'T NEED JMR OR  TCT ANY MORE
GACTNSMF EQU       *
         L         R9,TCBJSCBB-1  GET ADDR OF JSCB
         USING     IEZJSCB,R9     GIVE IT ADDRESSABILITY
         L         R8,JSCBSSIB    GET ADDR OF SSIB FOR JOB
         USING     SSIB,R8        GIVE IT ADDRESSABILITY
         MVC       GAMJES#,SSIBJBID   MOVE JOB IDENTIFIER
         L         R8,JSCBJCT     GET ADDR OF JCT PREFIX
         LA        R8,16(,R8)     SKIP PAST PREFIX TO JCT PROPER
         USING     JCT,R8         GIVE IT ADDRESSABILITY
         MODESET   KEY=ZERO
         ICM       R8,7,JCTACTAD  GET ADDR OF ACT PREFIX
         LA        R8,16(,R8)     SKIP PAST PREFIX TO ACT PROPER
         USING     ACTDSKAD,R8    GIVE IT ADDRESSABILITY
         LA        R4,0           ZERO OUT SOME REGS
         LA        R7,0
         IC        R7,ACTJNFLD    GET # OF ACCT FIELDS
         STH       R7,GAM#ACTF    STOW # OF ACCTING FIELDS
         LTR       R7,R7          ARE THERE ANY?
         BZ        GACTNACT       BIF NONE
         C         R7,=F'2'       WE CAN'T HANDLE > 2 FIELDS
         BNH       GACNTOK        BIF 2 FIELDS OR LESS
         LA        R7,2           OTHERWISE, RESET COUNTER TO 2
GACNTOK  EQU       *
         LA        R6,ACTJNFLD+1  GET ADDR OF 1ST FLD W/LGTH
         LA        R5,GAMACT1L    GET ADDR OF 1ST OUTPUT AREA
GACTLOOP EQU       *
         SR        R4,R4          ZERO OUT R4
         IC        R4,0(,R6)      GET LENGTH OF ACCT FIELD
         C         R4,=F'30'      IS ACCOUNTING FIELD > 30 BYTES?
         BH        GACTNMVC       BIF YES; SKIP MOVE
         LR        R3,R4
         S         R3,=F'1'       LENGTH -> LENGTH CODE
         BM        GACTNMVC       BIF DON'T WANT MVC
         STH       R4,0(,R5)      STOW LENGTH IN OUTPUT AREA
         EX        R3,GACTMVCI    MOVE ACCT INFO
GACTNMVC EQU       *
         LA        R6,1(R4,R6)    SKIP TO NEXT INPUT FIELD
         LA        R5,32(,R5)     SKIP TO NEXT OUTPUT FIELD
         BCT       R7,GACTLOOP    LOOP UNTIL ALL FIELDS GOTTEN
         MODESET   KEY=NZERO
GACTNACT EQU       *
         L         R2,SAVEAREA+4  GET PTR TO CALLER'S S/A
         L         R3,RETCODE     GET RETURN CODE
         L         R0,GAGETPRM    GET LENGTH OF G/M AREA
         LR        R1,R13         ADDR OF GETMAINED AREA
         FREEMAIN  R,LV=(0),A=(1)
         LR        R13,R2         PUT CALLER'S S/A PTR INTO R13
         LR        R15,R3         PUT RET. CODE INTO R15
         RETURN    (14,12),RC=(15)
GACTMVCI MVC       2(0,R5),1(R6)
GAGETPRM DC        0F'0',AL1(0),AL3(WORKLGTH)
         LTORG
         TITLE     'GACCTPGM - GETMAINED WORK AREA'
WORKAREA DSECT
*********************************************************************
*
*        PROGRAM WORK AREA
*
*********************************************************************
SAVEAREA DS        18F
RETCODE  DS        F
WORKLGTH EQU       (((*-WORKAREA+7)/8)*8)
         TITLE     'GACCTPGM - LAYOUT OF WORK AREA PASSED BY CALLER'
GACCTMAC DSECT
**********************************************************************
*
*        LAYOUT OF WORK AREA PASSED BY CALLER.  NOTE THAT THE AREA IS
*        UNCONDITIONALLY ZEROED OUT BEFORE ANY VALUES ARE PLACED IN
*        THE WORK AREAS.  NOTE ALSO THAT THE JOBNAME, RDR START TIME
*        &DATE, AND CPUID (SID) ARE NOT PLACED IN THE AREA IF SMF
*        IS NOT ACTIVE IN THE SYSTEM.
*
**********************************************************************
GAMCPUID DS        CL4            CPUID (SID) FROM JMR
GAMJOB   DS        CL8            JOBNAME FROM JMR
GAMRST   DS        BL4            READER START TIME, IN 100THS OF SECS
GAMRSD   DS        PL4            READER START DATE, GIVEN AS 00YYDDDF
GAMJES#  DS        CL8            JES JOB ID ('JOBXXXXX')
GAM#ACTF DS        H              # OF ACCOUNTING FIELDS
GAMACT1L DS        H              LENGTH OF 1ST ACCT FIELD
GAMACT1  DS        CL30           TEXT OF 1ST ACCT FIELD
GAMACT2L DS        H              LENGTH OF 2ND ACCT FIELD
GAMACT2  DS        CL30           TEXT OF 2ND ACCT FIELD
GAMEND   EQU       *
         TITLE     'GACCTPGM - LAYOUT OF TASK CONTROL BLOCK'
         IKJTCB
         TITLE     'GACCTPGM - LAYOUT OF TIMING CONTROL TABLE'
         IEFTCT
         TITLE     'GACCTPGM - LAYOUT OF JOB MANAGEMENT RECORD'
         IEFJMR
         TITLE     'GACCTPGM - LAYOUT OF JOB STEP CONTROL BLOCK'
         IEZJSCB
         TITLE     'GACCTPGM - LAYOUT OF SUBSYSTEM INTERFACE BLOCK'
         IEFJSSIB
         TITLE     'GACCTPGM - LAYOUT OF JOB CONTROL TABLE'
JCT      DSECT
         IEFAJCTB
         END
