CPWTOCMD TITLE 'TASK CONTROL BLOCK(TCB)'
         IKJTCB
         TITLE 'COMMUNICATION VECTOR TABLE(CVT)'
         CVT   DSECT=YES
         TITLE 'TASK I/O TABLE(TIOT)'
TIOT     DSECT
         IEFTIOT1
         TITLE 'DATA CONTROL BLOCK(DCB)'
         DCBD  DEVD=(DA),DSORG=(PS)
         TITLE 'MAIN PROGRAM'
CPWTOCMD CSECT
*********************************************************************
*
*        PROGRAM: WTO & COMMAND UTILITY.
*        INSTALLATION: U.S. SENATE COMPUTER CENTER.
*        AUTHOR: D. HALBIG.
*        DATE WRITTEN: MARCH 1980.
*        FUNCTION: TO SIGNAL OPERATOR WITH A CANNED MESSAGE.
*
*        REGISTER                 USAGE
*              R15                ENTRY POINT/RETURN CODE
*              R14                RETURN ADDRESS/ERROR INDICATOR
*              R13                CALLER SAVE AREA/PROGRAM WORK AREA
*              R12                PROGRAM BASE REGISTER
*              R11                SUBROUTINE BASE REGISTER
*              R10                SUBROUTINE RETURN ADDRESS
*              R9-R0              WORK REGISTERS
*
*        MAINT:
*        07/15/83 (ABS) - MODIFIED TO ADDITIONALLY ALLOW OPERATOR
*        COMMANDS.  IF THE FIRST 5 POSITIONS ARE '-OCMD' THEN THE
*        FOLLOWING TEXT WILL BE TREATED AS A COMMAND.
*
*
*********************************************************************
         REGEQU
         SAVE      (14,12),,CPWTOCMD-&SYSDATE-&SYSTIME
         LR        R12,R15        ESTAB. BASE REGISTER
         USING     CPWTOCMD,R12   TELL ASSEMBLER
         L         R0,GETPARMS    OBTAIN GETMAIN SUBPOOL & SIZE
         GETMAIN   R,LV=(0)
         LR        R2,R1          SET UP TO CLEAR G/M AREA
         L         R3,GETPARMS
         SR        R4,R4
         SR        R5,R5
         MVCL      R2,R4          SET AREA TO BINARY ZEROS
         ST        R1,8(,R13)     TELL CALLER ABOUT OUR S/A
         ST        R13,4(,R1)     PUT CALLER'S S/A PTR IN OUR S/A
         LR        R13,R1
         USING     WORKAREA,R13 ESTAB. ADDRESSABILITY FOR WORK AREA
         L         R11,=A(INITRTN)  GET ADDR OF INITIALIZATION RTN
         BALR      R10,R11        PERFORM INITIALIZATION
         TM        ERRORSW,ERRINIT ERROR IN INITIALIZATION?
         BZ        MAININOK       BIF INITIALIZATION OK
         EX        R0,*           OTHERWISE, FORCE AN 0C3
MAININOK EQU       *
         L         R11,=A(READRTN) GET ADDR OF READ/WTO ROUTINE
         BALR      R10,R11        PERFORM IT
         TM        ERRORSW,ERRREAD ERROR IN READ/WTO ROUTINE?
         BZ        MAINRPOK       BIF READ/PUT OK
         EX        R0,*           OTHERWISE, FORCE AN 0C3
MAINRPOK EQU       *
         L         R11,=A(TERMRTN)
         BALR      R10,R11        PERFORM TERMINATION
         TM        ERRORSW,ERRTERM ERROR IN TERMINATION ROUTINE?
         BZ        MAINTMOK       BIF TERMINATION OK
         EX        R0,*           OTHERWISE, SET UP AN 0C3
MAINTMOK EQU       *
         L         R0,GETPARMS    SET UP FOR FREEMAIN
         LR        R1,R13
         L         R2,SAVEAREA+4
         FREEMAIN  R,LV=(0),A=(1)
         LR        R13,R2
         RETURN    (14,12),RC=0
GETPARMS DC        A(WORKLGTH)    PARMS FOR GETMAIN/FREEMAIN
         LTORG
         TITLE     'WTO AND COMMAND UTILITY - INITIALIZATION ROUTINE'
INITRTN  DS        0H
********************************************************************
*
*        INITIALIZATION ROUTINE
*
**********************************************************************
         USING     INITRTN,R11    MAKE ROUTINE ADDRESSABLE
         MVC       WTOMSG(PATWTOL),PATWTO MOVE PATTERN WTO
         MVC       COMMAND(CMDLEN),PATCMND MOVE PATTERN SVC 34 ARG.
         MVC       SYSIN(INITSYSL),INITSYS MOVE IN PATTERN INPUT DCB
         OI        OPENLIST,X'80' SET END-OF-LIST
         OPEN      (SYSIN,(INPUT)),MF=(E,OPENLIST)
         TM        SYSIN+(DCBOFLGS-IHADCB),DCBOFOPN OPEN OK?
         BO        INITINOK       BIF INPUT FILE OPENED OK
         BAL       R14,INITERR    OTHERWISE, SIGNAL ERROR
INITINOK EQU       *
         B         INITEXIT       NOW LEAVE NORMALLY
INITERR  EQU       *
         OI        ERRORSW,ERRINIT SET ERROR INDICATOR
INITEXIT EQU       *
         BR        R10            NOW RETURN TO CALLER
         TITLE     'WTO AND COMMAND UTILITY - INITIALIZATION CONSTANTS'
PATWTO   WTO       '                                                   X
                                                                       X
               ',ROUTCDE=1,DESC=2,MF=L
PATWTOL  EQU       *-PATWTO
         SPACE 3
PATCMND  DS    0H            COMMAND PATTERN
         DC    AL2(PATCMNDL),AL2(0) L(COMMAND)
         DC    76C' '        COMMAND (MUST INCL. TRAILING BLANK)
PATCMNDL EQU   *-PATCMND
         ORG
         SPACE 3
INITSYS  DCB       MACRF=(GM),RECFM=FB,LRECL=80,BLKSIZE=6160,          X
               DDNAME=SYSIN,DSORG=PS,EODAD=READEOF
INITSYSL EQU       *-INITSYS
         LTORG
         DROP      R11
         TITLE     'READ AND WTO OR COMMAND'
READRTN  DS        0H
*********************************************************************
*
*        READ AND WTO ROUTINE
*
*********************************************************************
         USING     READRTN,R11
         LA        R1,READEOF     ESTAB. EOF RTN ADDR
         STCM      R1,7,SYSIN+(DCBEODA-IHADCB)
         L         R1,16(0,0)     GET CVT ADDR
         USING     CVTMAP,R1      MAKE IT ADDRESSABLE
         L         R1,CVTTCBP     GET NEXT/CURR TCB PTR ADDR
         L         R1,4(,R1)      GET CURR TCB ADDR
         USING     TCB,R1         MAKE IT ADDRESSABLE
         L         R1,TCBTIO      GET ADDR OF TIOT
         USING     TIOT,R1        MAKE IT ADDRESSABLE
         MVC       WTOMSG+08(8),TIOCNJOB GET JOBNAME
         MVC       WTOMSG+17(8),TIOCSTEP+8 STEPNAME
         CLI       TIOCSTEP+8,C' ' IS STEPNAME BLANK?
         BNE       READCARD       IF NOT, USE CURRENT STEPNAME
         MVC       WTOMSG+17(8),TIOCSTEP OTHERWISE, USE OTHER STEPNAME
READCARD EQU       *
         MVI       COMMTEXT,X'40' CLEAR COMMAND AREA
         MVC       COMMTEXT+1(75),COMMTEXT
         GET       SYSIN,CARDAREA
         SPACE 1
         CLC       CARDAREA(5),=C'-OCMD'  SAY THE MAGIC WORD AND A
         BE        DOCMD                  DUCK COMES DOWN; ELSE WTO
         MVC       WTOMSG+26(80),CARDAREA
         WTO       MF=(E,WTOMSG)
         B         READEXIT      NOW LEAVE (ONE NON-ROLLABLE WTO QUOTA)
READEOF  EQU       *
READEXIT EQU       *
         BR        R10            RETURN TO CALLER
         SPACE 1
DOCMD    EQU       *              DO A COMMAND
         MVC       COMMTEXT(75),CARDAREA+5 PUT THAT COMMAND THERE
         MODESET   KEY=ZERO,MODE=SUP
         SR        R0,R0
         LA        R1,COMMAND     R1 = A(COMMAND)
         SVC       CMDSVC
         MODESET   KEY=NZERO,MODE=PROB
         B         READCARD       DO IT AGAIN
         TITLE   'WTO AND COMMAND UTILITY - READ/WTO ROUTINE CONSTANTS'
         LTORG
         DROP      R11
         TITLE     'WTO AND COMMAND UTILITY - TERMINATION ROUTINE'
TERMRTN  DS        0H
*********************************************************************
*
*        TERMINATION ROUTINE
*
*********************************************************************
         USING     TERMRTN,R11
         OI        CLOSLIST,X'80' SET END-OF-LIST
         CLOSE     (SYSIN),MF=(E,CLOSLIST)
TERMEXIT EQU       *
         BR        R10            RETURN TO CALLER
         LTORG
         DROP      R11
         TITLE     'WTO AND COMMAND UTILITY - WORK AREA'
WORKAREA DSECT
**********************************************************************
*
*        WORK AREA
*
**********************************************************************
SAVEAREA DS        18F
ERRORSW  DS        XL1            ERROR INDICATORS
ERRINIT  EQU   X'01'              ERROR IN INITIALIZATION ROUTINE
ERRREAD  EQU   X'02'              ERROR IN READ/PUT ROUTINE
ERRTERM  EQU   X'04'              ERROR IN TERMINATION ROUTINE
OPENLIST DS        A
CLOSLIST DS        A
SYSIN    DCB       MACRF=(GM),RECFM=FB,BLKSIZE=6160,LRECL=80,          X
               DDNAME=SYSIN,DSORG=PS
         SPACE 3
COMMAND  DS        1F             COMMAND LENGTH
COMMTEXT DS        CL76           COMMAND TEXT
CMDLEN   EQU       *-COMMAND
CMDSVC   EQU       34
         SPACE 3
CARDAREA DS        CL80
WTOMSG   WTO       '                                                   X
                                                                       X
               ',ROUTCDE=1,DESC=2,MF=L
WORKLGTH EQU       (((*-WORKAREA+7)/8)*8)
         END
