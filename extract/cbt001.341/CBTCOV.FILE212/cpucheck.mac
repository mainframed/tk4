         TITLE     'CPUCHECK - MAIN PROGRAM'
         PRINT     ON,GEN,DATA
CPUCHECK CSECT
***********************************************************************
*
*        PROGRAM. CPUCHECK
*        AUTHOR. DAVE HALBIG.
*        INSTALLATION. U.S. SENATE
*        DATE WRITTEN. OCTOBER, 1981.
*        FUNCTION. USING PARM FIELD VALUE, CHECK FOR PROCESSOR THIS
*           JOB WAS INTENDED TO RUN ON; IF PROCESSOR ID MATCHES PARM,
*           END QUIETLY; IF PROCESSOR ID DOESN'T MATCH, ISSUE MSG TO
*           OPERATOR TELLING HIM OF HIS SCREW-UP, ISSUE '$TJXXX,S=BBBB'
*           COMMAND TO RESET JOB'S JES PROCESSOR AFFINITY, THEN
*           RESET & CANCEL JOB TO PERMIT IT TO RUN ON CORRECT
*           PROCESSOR.  THIS PROGRAM IS INTENDED TO BE FIRST PGM
*           EXECUTED IN A JOB IF A JOB NEEDS THIS KIND OF PROTECTION.
*           *********THIS PROGRAM MUST RUN AUTHORIZED****************
*
***********************************************************************
         REGISTER
         SAVE      (14,12),,CPUCHECK-&SYSDATE-&SYSTIME
         LR        R12,R15          ESTAB BASE REGISTER
         USING     CPUCHECK,R12
         LA        R14,SAVEAREA
         ST        R14,8(R13)
         ST        R13,SAVEAREA+4
         LR        R13,R14
         L        R11,0(,R1)          GET ADDR OF PARM DATA
         LH       R1,0(,R11)          GET PARM DATA LENGTH INFO
         CH       R1,=H'4'            IS IT CORRECT LENGTH?
         BE       CHKSMFID            BIF YES; CHECK PROCESSOR ID
         WTO      'CPUCHECK-01-I - PARM FIELD INVALID OR MISSING',     X
               ROUTCDE=11,DESC=6
         LA       R15,16              SET RC=16
         B        RETURN
         SPACE    1
CHKSMFID EQU      *
         CALL     GACCTPGM,(GACCTMAC),VL
         LTR      R15,R15             GOT INFO OK?
         BZ       ACCTOK              BIF YES
         WTO      'CPUCHECK-02-I - ''GACCTPGM'' SUBROUTINE FAILED',    X
               ROUTCDE=11,DESC=6
         LA       R15,16              SET RC=16
         B        RETURN
         SPACE    1
ACCTOK   EQU      *
         CLC      2(4,R11),GAMCPUID   DOES SMFID MATCH PARM VALUE?
         BNE      WRONGCPU            BIF NOT; MUST CHANGE CPU'S
         LA       R15,0             SET RC=0
         B        RETURN
         SPACE    1
WRONGCPU EQU      *
         MVC      WTOMSG3+44(4),2(R11)  REQUESTED PROCESSOR
         MVC      WTOMSG3+69(4),GAMCPUID DISCOVERED PROCESSOR
         MVC      WTOMSG3+19(8),GAMJOB   JOBNAME
         WTO      MF=(E,WTOMSG3)
         MVC      JESCOMMD+07(4),GAMJES#+4 BUILD '$TJXXXX,S=CCCC;'
         MVC      JESCOMMD+14(4),2(R11)
         MVC      JESCOMMD+21(4),GAMJES#+4 BUILD 'EJXXXX;'
         MVC      JESCOMMD+28(4),GAMJES#+4 BUILD 'CJXXXX'
         MODESET  KEY=ZERO,MODE=SUP
         SR       R0,R0
         LA       R1,JESCOMMD
         SVC      34
         MODESET  KEY=NZERO,MODE=PROB
         STIMER   REAL,BINTVL=WAIT10SC  WAIT TEN SECONDS FOR CANCEL
         LA       R15,16          SET RC=16 (IF CANCEL DOESN'T GET US)
         B        RETURN
         SPACE    1
RETURN   EQU       *
         L         R13,SAVEAREA+4
         RETURN    (14,12),RC=(15)
         TITLE     'CPUCHECK - PROGRAM WORK/SAVE AREAS'
SAVEAREA DC        18F'0'              SAVE AREA
JESCOMMD DS        0D                  AREA TO CONSTRUCT JES COMMANDS
         DC        AL2(JESCMDL)        LENGTH OF COMMANDS
         DC        AL2(0)              (RESERVED)
         DC        C'$TJXXXX,S=CCCC;'
         DC        C'EJXXXX;CJXXXX'
JESCMDL  EQU       *-JESCOMMD          CALC LENGTH OF COMMANDS
         SPACE     1
WAIT10SC DC        A(10*100)           TIME, IN 100THS OF SECONDS
         SPACE     1
WTOMSG3  DS        0D
         WTO       'CPUCHECK-3-I - XXXXXXXX MUST RUN ON CPU YYYY; WAS SX
               TARTED ON CPU ZZZZ',MF=L,ROUTCDE=2,DESC=6
         TITLE     'CPUCHECK - AREA PASSED TO ''GACCTPGM'''
         GACCTMAC
         LTORG
         END
