       SPACE 3
*************************************************************  SS SEP84
*                                                              SS SEP84
*        ADDED SUPPORT OF DIALOG MEMLIST MODE USING            SS SEP84
*        A DIALOG TABLE TO DISPLAY PDS MEMBER LIST             SS SEP84
*                                                              SS SEP84
*************************************************************  SS SEP84
*
*      MEMLNAME     MEMBER NAME (*MUST* BE ONLY 'KEY' VARIABLE IN TBL)
*      MEMLACT      USER'S ACTION REQUEST
*      MEMLALIS     MAIN NAME IF ALIAS
*      MEMLTTR      MEMBER TTR (REQUIRED TO DETERMINE ALIAS MAIN NAME)
*   THE FOLLOWING ARE UPDATED FOR SPF FILES                    SS MAY84
*      MEMLVMOD     SPF STATS VERSION MOD
*      MEMLCDAT     SPF STATS CREATION DATE
*      MEMLMDAT     SPF STATS LAST MOD DATE
*      MEMLMTIM     SPF STATS LAST MOD TIME
*      MEMLMID      SPF STATS LAST MOD ID
*      MEMLISIZ     SPF STATS INITIAL SIZE                     SS NOV84
*      MEMLCSIZ     SPF STATS CURRENT SIZE
*                                                              SS NOV84
*   THE FOLLOWING ARE UPDATED FOR LOAD LIBRARIES
*      MEMLMNAM     ALIAS MAIN NAME
*      MEMLXATT     LOAD ATTRIBUTE BYTES IN HEX
*      MEMLLENG     LOAD PROGRAM LENGTH      (PRINTABLE)
*      MEMLENTY     LOAD PROGRAM ENTRY POINT (PRINTABLE)
*      MEMLATT1     LOAD ATTRIBUTE - FIRST
*      MEMLATT2     LOAD ATTRIBUTE - SECONDARY
*      MEMLDC       LOAD ATTRIBUTE OF DC
*      MEMLTEST     LOAD ATTRIBUTE OF TEST
*      MEMLAPF      LOAD APF ATTRIBUTE
*      MEMLMODE     LOAD AMODE/RMODE ATTRIBUTE
*      MEMLSSI      SSI INFORMATION                            SS NOV84
*
***********************************************************************
         SPACE 2
MEMLIST  DS    0H                                              SS SEP84
         LA    R1,L530             ASSUME A NON-PARTITIONED DATA SET
         TM    DSORG,DS1DSGPO      CORRECT?
         BZ    MSGNEW              YES, BRANCH
         TM    SPFLAG0,SPFDON      ISPMODE ALREADY ACTIVE?    ABL OCT84
         BO    MEMLIST2            YES, BRANCH                ABL OCT84
         OI    SPFLAG0,SPFDON      ISPMODE IS ACTIVE NOW      ABL OCT84
         NI    FLAGSBB,FF-FONESHOT ALWAYS CONTINUE            ABL OCT84
         TM    SPFLAG0,SPFDSUSP    ISPMODE SUSPENDED?         ABL OCT84
         BO    MEMLIST2            YES, BRANCH                ABL OCT84
         NI    SPFLAG0,FF-SPFDSUSP NO LONGER SUSPENDED        ABL OCT84
         OI    SPFLAG3,SPFCHN      FORCE MSG TO BE LOGGED     ABL OCT84
         MVC   INSERT#1(8),PDSNAME                            ABL OCT84
         TSMSG L100$1                                         ABL OCT84
         MESSAGE MSGBLANK               ONE BLANK LINE        ABL OCT84
         L     R15,$DSN+8               DO A "DSN" SUBCOMMAND ABL OCT84
         BALR  R2,R15                                         ABL OCT84
MEMLIST2 DS    0H                                             ABL OCT84
         ST    R6,SPFSAV6          SAVE                        SS SEP84
         LA    R6,FIRST4K                                      SS SEP84
         USING FIRST4K,R6                                      SS SEP84
         SPACE 1
         MVI   MEM1NAMA,C' '       BLANK                       SS SEP84
         MVC   MEM1NAMA+1(MEMLVLEN-1),MEM1NAMA  RIPPLE         SS SEP84
         SPACE 1
*        CLEAR VARS HERE
         MVC   MEMLNAME,DIRNAME    MEMBER NAME FROM DIR TO VAR
         UNPK  MEMLWK(7),DIRTTR(4) UNPACK HEX
         TR    MEMLWK(6),TRHEX     TRANSLATE TO HEX
         MVC   MEMLTTR(6),MEMLWK
         MVI   MEMLXATT,C'S'       ASSUME SOURCE
         MVC   MEMLXATT+2(4),DIRTTR SAVE TTR                   SS NOV84
         TM    DIRFLAG,DIRALIAS    IS THIS AN ALIAS NAME
         BNO   MMLSTATS            NO: ALIAS LOGIC
         MVC   MEMLALIS,MMLALI          NAME AS AN ALIAS
* DETERMINE IF WE HAVE SPF STATISTICS
MMLSTATS DS    0H                                              SS NOV84
         LA    R0,DIRCUDHW         CLEAR IC REG
         LA    R15,0               CLEAR IC REG
         IC    R15,DIRFLAG         FETCH COUNT FIELD MASK
         NR    R15,R0              ZERO OUT NON-COUNT BITS
         SLL   R15,1               TIMES 2                     SS SEP84
         TM    FLAGSCC,RECFMU      LOAD MODULE LIBRARY?        SS NOV84
         BO    MEMLLOAD            YES, BRANCH                 SS NOV84
         STC   R15,MEMLMNAM        SAVE LENGTH                 SS SEP84
         MVC   MEMLMNAM+1(0),DIRUSER  SAVE DIR                 SS SEP84
         EX    R15,*-6                                         SS SEP84
         CLC   MEMLALIS,MMLALI     ALIAS
         BE    MMLSALI             YES, NO STATS
         TM    DIRFLAG,DIRSPFOF    CK FOR BITS THAT SHOULD BE OFF
         BNZ   MEMLADD             THEY'RE NOT: NO SPF STATS
         TM    DIRFLAG,DIRSPFON    CK FOR BITS THAT SHOULD BE ON
         BNO   MEMLADD             THEY'RE NOT: NO SPF STATS
         OC    DIRSPFZ(3),DIRSPFZ  RESERVED AND 00 OF 00YYDDDF ZEROS?
         BNZ   MEMLADD             NO, BRANCH
         CLI   DIRSPFCD,0          00 OF OTHER 00YYDDDF ZERO?
         BNZ   MEMLADD             NO, BRANCH
         SPACE 1
* ALL TESTS PASSED: ASSUME WE HAVE SPF STATS
         SPACE
         SR    R1,R1
         IC    R1,DIRSPFR          REVISION NUMBER FIRST
         CVD   R1,DOUBLE
         MVC   MEMLVMOD+3(4),=X'40212020'
         ED    MEMLVMOD+3(4),DOUBLE+6
         MVI   MEMLVMOD+4,C'.'
         IC    R1,DIRSPFV          VERSION NUMBER
         CVD   R1,DOUBLE
         MVC   MEMLVMOD(4),=X'40212020'
         ED    MEMLVMOD(4),DOUBLE+6
         LA    R1,DIRSPFCR+1           POINT TO YYDDDF
         LA    R15,FULLWORD+1
         BAL   R14,CONVDATE            CONVERT TO MMDDYY FORMAT
         MVC   FULLWORD(1),FULLWORD+3  CONVERT TO YYMMDD
         MVC   MEMLCDAT(L'DATEMASK),DATEMASK
         ED    MEMLCDAT(L'DATEMASK),FULLWORD
         MVC   MEMLCDAT(8),MEMLCDAT+1                          SS SEP84
         LA    R1,DIRSPFCD+1           POINT TO YYDDDF
         LA    R15,FULLWORD+1
         BAL   R14,CONVDATE            CONVERT TO MMDDYY FORMAT
         MVC   FULLWORD(1),FULLWORD+3  CONVERT TO YYMMDD
         MVC   MEMLMDAT(L'DATEMASK),DATEMASK
         ED    MEMLMDAT(L'DATEMASK),FULLWORD
         MVC   MEMLMDAT(8),MEMLMDAT+1                          SS SEP84
         MVC   MEMLMTIM(6),=X'0021207A2020'                   ABL OCT84
         ED    MEMLMTIM(6),DIRSPFCT    TIME OF LAST CHANGE
         MVC   MEMLMTIM(5),MEMLMTIM+1                          SS SEP84
         MVI   MEMLMTIM+5,X'40'        FOLLOWING BLANK        ABL OCT84
         LH    R1,DIRSPFIN                                     SS NOV84
         CVD   R1,DOUBLE
         MVC   MEMLISIZ(6),=X'002020202120'                    SS NOV84
         ED    MEMLISIZ(6),DOUBLE+5                            SS NOV84
         MVC   MEMLISIZ(5),MEMLISIZ+1                          SS NOV84
         MVI   MEMLISIZ+5,X'40'        FOLLOWING BLANK         SS NOV84
         LH    R1,DIRSPFSI
         CVD   R1,DOUBLE
         MVC   MEMLCSIZ(6),=X'002020202120'                   ABL OCT84
         ED    MEMLCSIZ(6),DOUBLE+5
         MVC   MEMLCSIZ(5),MEMLCSIZ+1                          SS SEP84
         MVI   MEMLCSIZ+5,X'40'        FOLLOWING BLANK        ABL OCT84
         SPACE 1
         MVC   MEMLMID,DIRSPFID
         B     MEMLADD
* PROCESS SOURCE ALIAS                                         SS NOV84
MMLSALI  DS    0H                                              SS NOV84
         MVC   MEMLVMOD(5),MMLALI+1                            SS NOV84
         B     MEMLADD                                         SS NOV84
* PROCESS DIR AS LOAD MODULE
MEMLLOAD DS    0H
* SAVE ATTR BYTES
         MVI   MEMLXATT,C'L'       LOAD TYPE MEMBER
         STC   R15,MEMLVMOD        SAVE LENGTH                 SS SEP84
         MVC   MEMLVMOD+1(0),DIRUSER  SAVE DIR                 SS SEP84
         EX    R15,*-6                                         SS SEP84
         CLI   MEMLVMOD,0          CHECK FOR SOURCE IN LOAD    SS NOV84
         BE    MMLNONL             SOURCE                      SS NOV84
         CLI   MEMLVMOD,30         COMPARE FOR SPF STATS       SS NOV84
         BE    MMLNONL             SOURCE                      SS NOV84
         CLC   MEMLALIS,MMLALI     IS IT ALIAS
         BNE   MMLALIAS
         CLI   MEMLVMOD,34         COMPARE FOR ALIAS AREA      SS NOV84
         BNE   MMLALIAS            GOOD
         MVC   MEMLMNAM,DIRREAL    SAVE CLAIMED MAIN NAME
MMLALIAS DS    0H
* PROCESS PROGRAM LENGTH
         UNPK  MEMLWK(7),DIRSTORE(4)
         TR    MEMLWK(6),TRHEX
         MVC   MEMLLENG(6),MEMLWK
* PROCESS PROGRAM ENTRY
         UNPK  MEMLWK(7),DIREPA(4)
         TR    MEMLWK(6),TRHEX
         MVC   MEMLENTY(6),MEMLWK
* PROCESS APF CODE
         TM    DIRATTR2,DIRVS      IS IT VS
         BO    MMLAPF
         MVC   MEMLAPF(4),MMLCNOVS
         B     MMLDC
MMLAPF   TM    DIRATTR2,DIRAPFF    IS APF THERE
         BZ    MMLDC
         LA    R0,DIRCUDHW         CLEAR IC REG
         LA    R15,0               CLEAR IC REG
         IC    R15,DIRFLAG         FETCH COUNT FIELD MASK
         NR    R15,R0              ZERO OUT NON-COUNT BITS
         SLL   R15,1               MULT BY 2=NUMBER OF BYTES IN ENTRY
         TM    DIRATTR2,DIRSSIF    IS THERE SSI
         BO    MMLM1               LENG COMES OUT EVEN
         TM    DIRATTR,DIRSCTR     IS IT SCATTER LOAD
         BO    MMLM2               LENG COMES OUT EXTRA BYTE
         CH    R15,MMLH34          LENG FOR ALIAS MAIN NAME    SS NOV84
         BE    MMLM1               LENG COMES OUT EVEN
*        MAIN ENTRY DIR ENTRY --   LENG COMES OUT EXTRA BYTE
MMLM2    LA    R15,DIRUSER-2(R15)        GET TO END OF DIR-2
         B     MMLAPF2
MMLM1    LA    R15,DIRUSER-1(R15)        GET TO END OF DIR-1
MMLAPF2  CLI   0(R15),0            TEST AC(0)
         BE    MMLDC
         MVC   MEMLAPF+2(1),0(R15)
         OI    MEMLAPF+2,X'F0'     MAKE PRINTABLE
         MVC   MEMLAPF(2),MMLCAC   FILL IN
MMLDC    TM    DIRATTR+1,DIRNODC   IS IT DC
         BO    MMLSSI              NO                          SS NOV84
         MVC   MEMLDC(2),MMLCDC    SET DC
MMLSSI   TM    DIRATTR2,DIRSSIF    DO WE HAVE SSI INFO         SS NOV84
         BZ    MMLTEST             NO                          SS NOV84
         SH    R15,MMLH5           BACK UP TO SSI INFO         SS NOV84
         UNPK  MEMLWK(9),0(5,R15)  UNPACK HEX                  SS NOV84
         TR    MEMLWK(8),TRHEX     TRANSLATE TO HEX            SS NOV84
         MVC   MEMLSSI(8),MEMLWK                               SS NOV84
MMLTEST  TM    DIRATTR,DIRTEST     IS IT TEST
         BZ    MMLATTR1
         TM    DIRATTR+1,DIRNOED   IS IT EDITABLE
         BZ    MEMLOK              NO, INCOMPATIBLE WITH TEST
         MVC   MEMLATT1(4),MMLCNOED
         MVC   MEMLTEST(3),MMLCERR
         B     MMLLEV2
MEMLOK   MVC   MEMLTEST(4),MMLCTEST
MMLATTR1 TM    DIRATTR+1,DIRNOED   IS IT EDITABLE
         BZ    MMLEXEC             NO, INCOMPATIBLE WITH TEST
         MVC   MEMLATT1(4),MMLCNOED
         B     MMLLEV2
MMLEXEC  TM    DIRATTR,DIREXEC     IS IT EXECUTABLE
         BO    MMLLOAD             YES CONTINUE
         MVC   MEMLATT1(4),MMLCNOEX
         B     MMLLEV2A
MMLLOAD  TM    DIRATTR,DIRLOAD     IS IT LOAD ONLY
         BZ    MMLOVLY             NO  CONTINUE
         MVC   MEMLATT1(4),MMLCLOAD
         B     MMLLEV2B
MMLOVLY  TM    DIRATTR,DIROVLY     IS IT OVERLAY
         BZ    MMLSCTR             NO  CONTINUE
         MVC   MEMLATT1(4),MMLCOVLY
         B     MMLLEV2C
MMLSCTR  TM    DIRATTR,DIRSCTR     IS IT SCATTER LOAD
         BZ    MMLREFR             NO  CONTINUE
         MVC   MEMLATT1(4),MMLCSCTR
         B     MMLLEV2D
MMLREFR  TM    DIRATTR+1,DIRREFR   IS IT REFRESHABLE
         BZ    MMLRENT             NO  CONTINUE
         MVC   MEMLATT1(4),MMLCREFR
         B     MMLLEV2E
MMLRENT  TM    DIRATTR,DIRRENT     IS IT REENTRANT
         BZ    MMLREUS             NO  CONTINUE
         MVC   MEMLATT1(4),MMLCRENT
         TM    DIRATTR,DIRREUS     MUST ALSO BE REUS
         BZ    REUSERR
         MVC   MEMLATT2(4),MMLCREUS
         B     MMLMODE
REUSERR  MVC   MEMLATT2(4),MMLCERR1
         B     MMLMODE
MMLREUS  TM    DIRATTR,DIRREUS     IS IT REENTRANT
         BZ    MMLMODE             NO  CONTINUE
         MVC   MEMLATT1(4),MMLCREUS
         B     MMLMODE
MMLLEV2  DS    0H
         TM    DIRATTR,DIREXEC     IS IT EXECUTABLE
         BO    MMLLOAD2            YES CONTINUE
         MVC   MEMLATT2(4),MMLCNOEX
         B     MMLMODE
MMLLEV2A DS    0H
MMLLOAD2 TM    DIRATTR,DIRLOAD     IS IT LOAD ONLY
         BZ    MMLSCTR2            NO  CONTINUE
         MVC   MEMLATT2(4),MMLCLOAD
         B     MMLLEV2B
MMLOVLY2 TM    DIRATTR,DIROVLY     IS IT OVERLAY
         BZ    MMLSCTR2            NO  CONTINUE
         MVC   MEMLATT2(4),MMLCOVLY
         B     MMLLEV2C
MMLSCTR2 TM    DIRATTR,DIRSCTR     IS IT SCATTER LOAD
         BZ    MMLREFR2            NO  CONTINUE
         MVC   MEMLATT2(4),MMLCSCTR
         B     MMLLEV2D
MMLREFR2 TM    DIRATTR+1,DIRREFR   IS IT REFRESHABLE
         BZ    MMLRENT2            NO  CONTINUE
         MVC   MEMLATT2(4),MMLCREFR
         B     MMLMODE
MMLLEV2E DS    0H
MMLRENT2 TM    DIRATTR,DIRRENT     IS IT REENTRANT
         BZ    MMLREUS2            NO  CONTINUE
         MVC   MEMLATT2(4),MMLCRENT
         TM    DIRATTR,DIRREUS     MUST ALSO BE REUS
         BO    MMLMODE
         MVC   MEMLATT2(4),MMLCERR1
         B     MMLMODE
MMLREUS2 TM    DIRATTR,DIRREUS     IS IT REUSABLE
         BZ    MMLMODE             NO  CONTINUE
         MVC   MEMLATT2(4),MMLCREUS
         B     MMLMODE
MMLLEV2B TM    DIRATTR,DIRBADL
         BZ    MMLREFR2
         MVC   MEMLATT2(4),MMLCERRL
         B     MMLMODE
MMLLEV2C TM    DIRATTR,DIRBADO
         BZ    MMLMODE
         MVC   MEMLATT2(4),MMLCERRO
         B     MMLMODE
MMLLEV2D TM    DIRATTR,DIRBADS
         BZ    MMLREUS2
         MVC   MEMLATT2(4),MMLCERRS
         B     MMLMODE
MMLMODE  DS    0H
         CLI   DIRATTR3,0
         BE    MEMLEMOD
         CLC   MEMLALIS,MMLALI    IS IT ALIAS
         BE    MMLMODEA
         TM    DIRATTR3,DIRRANY    RMODE ANY
         BZ    MMLMODE2
         TM    DIRATTR3,DIRAMOD2   CHK AMODE 31
         BM    MMLR31
         B     MMLERRR
MMLMODE2 TM    DIRATTR3,DIRAMOD2
         BO    MMLAANY
         TM    DIRATTR3,DIRAERR2                               SS NOV84
         BO    MMLERRR                                         SS NOV84
         B     MMLA31
MMLMODEA TM    DIRATTR3,DIRRANY    RMODE ANY
         BZ    MMLMODE3
         TM    DIRATTR3,DIRAMOD1   CHK AMODE 31
         BM    MMLR31
         B     MMLERRR
MMLMODE3 TM    DIRATTR3,DIRAMOD1
         BO    MMLAANY
         TM    DIRATTR3,DIRAERR1                               SS NOV84
         BO    MMLERRR                                         SS NOV84
         B     MMLA31
MMLERRR  MVC   MEMLMODE(4),MMLCERRR
         B     MEMLEMOD
MMLR31   MVC   MEMLMODE(3),MMLCR31
         B     MEMLEMOD
MMLAANY  MVC   MEMLMODE(4),MMLCAANY
         B     MEMLEMOD
MMLA31   MVC   MEMLMODE(3),MMLCA31
         B     MEMLEMOD
MMLNONL  DS    0H                                              SS NOV84
         MVC   MEMLATT1,MMLSORCE                               SS NOV84
         B     MEMLADD                                         SS NOV84
MEMLEMOD DS    0H
MEMLADD  DS    0H
         L     R6,SPFSAV6                                      SS SEP84
         DROP  R6                                              SS SEP84
         MVI   SPFLAG1,SPFADD      REQUEST ADD TO MEMLIST      SS SEP84
         L     R15,=V(ISPDSPY)                                 SS SEP84
         BALR  R14,R15                                         SS SEP84
         NOP   NEWCMD              IGNORE INTERRUPT RETURN     SS SEP84
         B     NEWCMD                                          SS SEP84
*
*  MEML CONSTANT AREA
*
*
MMLH5    DC    H'5'                                            SS NOV84
MMLH34   DC    H'34'                                           SS NOV84
MMLALI   DC    CL8'*ALIAS'
MMLCNOVS DC    CL4'NOVS'
MMLCAC   DC    CL4'AC  '
MMLCDC   DC    CL4'DC  '
MMLCNOED DC    CL4'NOED'
MMLCERR  DC    CL4'ERR '
MMLCTEST DC    CL4'TEST'
MMLCNOEX DC    CL4'NOEX'
MMLCLOAD DC    CL4'LOAD'
MMLCOVLY DC    CL4'OVLY'
MMLCSCTR DC    CL4'SCTR'
MMLCREFR DC    CL4'REFR'
MMLCRENT DC    CL4'RENT'
MMLCREUS DC    CL4'REUS'
MMLCERR1 DC    CL4'ERR1'
MMLCERRL DC    CL4'ERRL'
MMLCERRO DC    CL4'ERRO'
MMLCERRS DC    CL4'ERRS'
MMLCERRR DC    CL4'ERRR'
MMLCR31  DC    CL4'R31 '
MMLCA31  DC    CL4'A31 '
MMLCAANY DC    CL4'AANY'
MMLSORCE DC    CL4'SORC'                                       SS NOV84
         SPACE 2
TRHEX1   DC    C'0123456789ABCDEF'   TRANSLATE TO HEX
TRHEX    EQU   *-256
         EJECT
DIRCALIS EQU   B'10000000'         ALIAS FLAG
DIRCTTRN EQU   B'01100000'         TTR COUNT
DIRCUDHW EQU   B'00011111'         HALFWD COUNT
DIRSPFOF EQU   B'01110000'         BITS ARE OFF IN SPF STATS
* SOME BITS ON FOR LOAD MODULES
DIRSPFON EQU   B'00001111'         BITS ARE ON IN SPF STATS
DIRRENT  EQU   B'10000000'           RENT
DIRREUS  EQU   B'01000000'           REUS
DIROVLY  EQU   B'00100000'           OVLY
DIRBADO  EQU   B'11001100'           OVLY      CONFLICTS
DIRTEST  EQU   B'00010000'           TEST
DIRLOAD  EQU   B'00001000'           LOAD ONLY
DIRBADL  EQU   B'00000100'           LOAD ONLY CONFLICTS
DIRSCTR  EQU   B'00000100'           SCTR LOAD
DIRBADS  EQU   B'10101000'           SCTR LOAD CONFLICTS
DIREXEC  EQU   B'00000010'           EXECUTABLE
DIRF1    EQU   B'00000001'
* ATTR 1 BYTE 1
DIRNODC  EQU   B'10000000'           NOT DC
DIRF2    EQU   B'01000000'
DIRF3    EQU   B'00100000'
DIRF4    EQU   B'00010000'
DIRNOED  EQU   B'00001000'           NOT EDITABLE
DIRF5    EQU   B'00000100'
DIRLKEDF EQU   B'00000010'           F LEVEL LINK EDITER
DIRREFR  EQU   B'00000001'           REFR
* ATTR 2
DIRVS    EQU   B'10000000'           VS LEVEL LINK EDITER
DIRRES1  EQU   B'01000000'
DIRPG    EQU   B'00100000'           PAGE ALIGNMEMT
DIRSSIF  EQU   B'00010000'           SSI PRESENT
DIRAPFF  EQU   B'00001000'           APF PRESENT
* AMODE/RMODE FLAGS
DIRRANY  EQU   B'00010000'           RMODE ANY FLAG
DIRAMOD1 EQU   B'00001100'           AMODE FOR ALIAS
DIRAERR1 EQU   B'00000100'           AMODE INVALID BIT COMB    SS NOV84
DIRAMOD2 EQU   B'00000011'           AMODE FOR MAIN ENTRY
DIRAERR2 EQU   B'00000001'           AMODE INVALID BIT COMB    SS NOV84
