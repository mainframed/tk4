URACF    TITLE 'RACF VALIDATION CHECK FOR OMEGAMON'
*
*****************************************************************
*  URACF -  OMEGAMON/RACF INTERFACE FOR EXTERNAL SECURITY       *
*           CHECKING AND VTAM LOGON                             *
*                                                               *
*                                                               *
*  ON ENTRY: R15 - ADDRESS OF OM$URACF (BASE REG)               *
*            R13 - SAVE AREA                                    *
*            R14 - RETURN ADDRESS                               *
*            R1  - PARM                                         *
*                  +00 - $UCHECK WORKAREA                       *
*                                                               *
*  UACF2 LOGIC:                                                 *
*                                                               *
*           PHASE 1) - IF OMVTAM, VALIDATE USERID AND PASSWORD  *
*           PHASE 2) - ALL MODES, SET INTERNAL SECURITY LEVEL   *
*           PHASE 3) - ALL MODES, VALIDATE OMEGAMON COMMANDS    *
*                                                               *
*           OMEGAMON MUST BE APF AUTHORIZED TO PROCESS          *
*           THE FOLLOWING:                                      *
*                                                               *
*           1) VALIDATION OF USERID AND PASSWORD FOR VTAM MODE  *
*                                                               *
*  NOTE:                                                        *
*           THE CODE FOR THIS EXIT IS NOT SUPPORTED BY          *
*           CANDLE.  IT IS AN EXAMPLE OF A WORKING EXIT         *
*           THAT SHOULD BE USED AS A STARTING POINT.            *
*           REFER TO YOUR RACF PROGRAMMERS GUIDE FOR            *
*           ADDITIONAL INFORMATION.                             *
*                                                               *
*****************************************************************
OM$URACF START 0
         SAVE  (14,12),,OM$URACF-&SYSDATE-&SYSTIME
         LR    RC,RF              SET BASE REGISTER
         USING OM$URACF,RC
         L     R2,0(R1)            GET PASSED PARAMS
         ST    RD,4(R2)            CHAIN THEM
         ST    R2,8(RD)            MORE CHAIN
         LR    RD,R2               A NEW SAVE AREA
         USING $UCHECK,RD
         MVI   M#SW1,0             RESET MY SWITCH
         OI    U#CHAUT1,U@CH1RAC   SET SECURITY SYSTEM TO RACF
*** MCI *** FOLLOWING TWO (2) LINES ADDED TO MOVE IN CLASS DESCRIPTOR
         MVI   U#CHCLS,L'M$OMCLAS  MOVE IN CLASS DESCRIPTOR LENGTH
         MVC   U#CHCLSD,M$OMCLAS   MOVE IN CLASS DESCRIPTOR
*
*        ARE WE AUTHORIZED ?
*
         TESTAUTH FCTN=1           ARE WE AUTHORIZED?
         LTR   RF,RF               CHECK IT OUT
         BNZ   *+8                 NOT AUTH'D
         OI    M#SW1,M@SW1AUT      INDICATE AUTHORIZATION
*
         CLI   U#CHTYP,U@CHCOM    IS IT COMMAND VALIDATION
         BE    CKCMDVER            YES, DO IT NOW
         CLI   U#CHTYP,U@CHTERM   IS IT TERMINATION?
         BE    CKTERM              YES, DO IT NOW
         CLI   U#CHTYP,U@CHINIT   IS IT INITIALIZATION?
         BNE   EXIT0               NOT MY CALL
*
*        SEE IF CLASS OMCANDLE IS ACTIVE AND RACF IS ACTIVE
*
         MVC   OMRACSL(RACSMDLL),RACSMDL SET MODEL FOR RACSTAT
         RACSTAT CLASS=U#CHCLSD,MF=(E,OMRACSL)
         ST    RF,M#SRET            RACSTAT RETURN VALUE
         CH    RF,=H'4'             IS RACF ACTIVE? WITH CLASS
         BL    ISAUTHD              ACTIVE
         BH    EXIT0                NO, RACF/CLASS NOT THERE
*
*        CLASS INACTIVE, INDICATE TO USER
*
         MVC   U#CHMSG+(L'U#CHMSG-L'MSG00)/2(L'MSG00),MSG00
         B     EXIT4
*
*        IF USERID, THEN MUST BE OMVTAM.
*
ISAUTHD  EQU   *
         CLI   U#CHUID,0           ANY USER ID?
         BE    CHOMEGA             NO, VTAM LOGON CODE
*
*****************************************************************
* PHASE 1 -                                                     *
*                                                               *
*     VALIDATE VTAM USERID AND PASSWORD AND CHANGE PASSWORD     *
*     IF REQUESTED.                                             *
*                                                               *
*****************************************************************
*
*        ARE WE AUTHORIZED, IF NOT, FAIL, CANNOT VERIFY PASSWORD
*
         TM    M#SW1,M@SW1AUT      ARE WE AUTH'D?
         BO    VALIDAT             YES, CONTINUE
         MVC   U#CHMSG+(L'U#CHMSG-L'MSG01)/2(L'MSG01),MSG01
         B     EXIT4                   EXIT
*
*        FREE ANY PRIOR ACEE, THIS MAY BE A RELOGON AND WE DO NOT
*        WANT ACEES HANGING AROUND.
*
VALIDAT  EQU   *
         BAL   R9,FREEACEE         FREE IT
*
*        CHECK THE USERID / PASSWORD
*
CHKUID   DS    0H
         SLR   R2,R2                   NO GROUP
         CLI   U#CHGRP,0               ANY GROUP?
         BE    *+8                     NO
         LA    R2,U#CHGRP              POINT TO IT
         SLR   R3,R3                   NO PASSWORD
         CLI   U#CHNPW,0               ANY NEW PASSWORD?
         BE    *+8                     NO
         LA    R3,U#CHNPW              POINT TO IT
         MVC   M#RACIL(RACIMDLL),RACIMDL SET MODEL CODE IN PLACE
         RACINIT USERID=U#CHUID,        USER ID FIELD                  X
               PASSWRD=U#CHCMD,         STORED IN COMMAND FIELD        X
               GROUP=(R2),                                             X
               NEWPASS=(R3),                                           X
               ENVIR=CREATE,                                           X
               ACEE=U#CHACEE,                                          X
               SMC=NO,                                                 X
               PASSCHK=YES,                                            X
               MF=(E,M#RACIL)           USE THIS STORAGE
         ST    RF,M#IRET                SAVE IT
*
*        DETERMINE IF RECOVERABLE ERROR
*
         CH    RF,=H'20'                AN ERROR?
         BH    EXIT4                    YES, TOO BAD
         B     *+4(RF)                  INTO THE TABLE
         B     CHOMEGA              0   GOOD
         B     INUID                4 INVALID USER ID
         B     INPWD                8 INVALID PASSWORD
         B     EXPWD               12 EXPIRED PASSWORD
         B     INNPW               16 INVALID NEW PASSWORD
         B     INGRP               20 INVALID GROUP ID
*
*        INVALID USER ID
*
INUID    EQU   *
         MVC   U#CHMSG+(L'U#CHMSG-L'MSG04)/2(L'MSG04),MSG04
         B     EXIT4                  DO NOT ALLOW
*
*        INVALID PASSWORD
*
INPWD    EQU   *
         MVI   U#CHCMD,0             NO LONGER VALID
         LH    R1,M#PWTIME           GET COUNT
         LA    R1,1(R1)              A HIGHER COUNT
         CH    R1,=H'3'              MUST NOT BE MORE
         BH    INPWDA                TOO BAD, EXIT ERROR
         STH   R1,M#PWTIME           SAVE FOR NEXT TRY
         MVC   U#CHMSG+(L'U#CHMSG-L'MSG08A)/2(L'MSG08A),MSG08A
         B     EXIT4A                 RETRY IT
INPWDA   EQU   *
         MVC   U#CHMSG+(L'U#CHMSG-L'MSG08B)/2(L'MSG08B),MSG08B
         B     EXIT4                  DO NOT ALLOW
*
*        EXPIRED PASSWORD
*
EXPWD    EQU   *
         MVC   U#CHMSG+(L'U#CHMSG-L'MSG12)/2(L'MSG12),MSG12
         B     EXIT4A                 SECOND TRY
*
*        NEW PASSWORD ERRROR
*
INNPW    EQU   *
         MVC   U#CHMSG+(L'U#CHMSG-L'MSG16)/2(L'MSG16),MSG16
         MVI   U#CHNPW,0               RESET PASSWORD
         B     EXIT4A                 SECOND TRY
*
*        INVALID GROUP ID
*
INGRP    EQU   *
         MVC   U#CHMSG+(L'U#CHMSG-L'MSG20)/2(L'MSG20),MSG20
         MVI   U#CHGRP,0               SET LENGTH OFF
         B     EXIT4A                 SECOND TRY
*
*****************************************************************
* PHASE 2 -                                                     *
*                                                               *
*   CHECK RESOURCE INITIAL3,2,1,0 AND IF DEFINED LOCK /PWD TO   *
*   THE RESPECTIVE LEVEL.                                       *
*                                                               *
*   A RESOURCE OF INITIAL (WITHOUT A NUMBER) ALLOWS THE USER    *
*   TO SET HIS OWN SECURITY LEVEL VIA THE /PWD COMMAND          *
*                                                               *
*   IF NO RESOURCE IS DEFINED THEN NO ONE WILL BE ALLOWED TO    *
*   USE OMEGAMON....SO DEFINE AT LEAST ONE RESOURCE.            *
*                                                               *
*****************************************************************
*        HE IS WHO HE SAID HE IS, IS OMEGAMON ALLOWED?
CHOMEGA  EQU   *
         LA    R3,OMENTS           POINT TO TABLE OF RESOURCES
RCHECK   DS    0H
         L     R2,U#CHACEE          POINT TO ACEE IF PRESENT
         MVC   M#RACCL(RACCMDLL),RACCMDL SET MODEL FOR RACHECK
         RACHECK ACEE=(R2),ENTITY=((R3)),CLASS=U#CHCLS,                X
               MF=(E,M#RACCL)
         ST    RF,M#CRET            RACSTAT RETURN VALUE
         B     *+4(RF)              DO IT
         B     CKOK             0   USER IS AUTHORIZED
         B     NOTDEF           4   NOT DEFINED, SAME AS A REJECT
         B     CKLEVL           8   NOT AT THIS LEVEL
         B     SETLOW          12   SHOULD NOT OCCUR
NOTDEF   EQU   *
CKLEVL   EQU   *
         LA    R3,L'OMENTS(R3)     NEXT ONE
         CLI   0(R3),255           END OF LIST?
         BNE   RCHECK              NO, DO MORE
         B     NOTALLOW            TOO BAD, DON'T ACCEPT IT
SETLOW   EQU   *
         LA    R3,OMENT0            PASS WITH LOW AUTH
CKOK     EQU   *
         MVC   U#CHAUT4,OMENTOFF(R3) GET NUMBER 0 - 3 DECIMAL
         NI    U#CHAUT4,X'0F'    CLEAR TOP OF BYTE
         CLI   OMENTOFF(R3),C' '   IS IT BLANK? (XXXXXXX_)
         BE    EXIT0               YES, NOT LOCKED
         OI    U#CHAUT1,U@CH1LOK   INDICATE WE WERE THERE
         B     EXIT0
NOTALLOW EQU   *
         MVC   U#CHMSG+(L'U#CHMSG-L'MSG25)/2(L'MSG25),MSG25 TEXT
*
*        RELEASE THE ACEE
*
         BAL   R9,FREEACEE          FREE IT
         B     EXIT4                FAIL REQUEST
*
*        TERMINATION ROUTINE HERE
*
CKTERM   DS    0H
*
*        IF AUTHORIZED, THEN RELEASE ACEE
*
         TM    M#SW1,M@SW1AUT      INDICATE AUTHORIZATION
         BNO   EXIT0               NOTHING TO DO
*
*        RELEASE THE ACEE
*
         BAL   R9,FREEACEE          FREE IT
         B     EXIT0                 CONTINUE
*
*****************************************************************
* PHASE 2 -                                                     *
*                                                               *
*  EXTERNAL COMMANDS ARE VALIDATED HERE.  THE CLASS CANDLE      *
*  SUPPLIES IS 'OMCANDLE' AND THE RESOURCE IS THE OMEGAMON      *
*  COMMAND.                                                     *
*                                                               *
*****************************************************************
*
*        COMMAND VERIFICATION HERE
*
CKCMDVER DS    0H
         MVI   M#SCR32,C' '                     INSERT A BLANK
         MVC   M#SCR32+1(L'M#SCR32-1),M#SCR32   BLANK ENTIRE BUFFER
*
*        CHANGE COMMANDS THAT HAVE NON STANDARD SYNTAX TO SOMETHING
*        RACF CALL DEAL WITH
*
         CLI   U#CHCMDD,C'/'   /GIVE => $GIVE   IS THIS AN INFO-LINE
         BNE   CKCMD00                          NO, LOOK FOR A DOT
         MVI   U#CHCMDD,U@CHCINF                REPLACE WITH NEW CHAR
         B     CKCMD10
CKCMD00  DS    0H
*
         CLI   U#CHCMDD,C'.'  .DIR => @DIR      IS THIS A DOT CMD
         BNE   CKCMD10                          NO, NORMAL COMMAND
         MVI   U#CHCMDD,U@CHCDOT                REPLACE WITH NEW CHAR
CKCMD10  DS    0H
*
         MVC   M#SCR32(8),U#CHCMDD              MOVE IN COMMAND
         L     R3,U#CHACEE                      GET ADDRESS OF ACEE
         LA    R4,M#SCR32                       LOAD RESOURCE ADDR
         LA    R5,U#CHCLS                       LOAD CLASS ADDR
*
         MVC   M#RACCL(RACCMDLL),RACCMDL SET MODEL FOR RACHECK
         RACHECK ENTITY=((R4)),CLASS=(R5),ATTR=READ,                   X
               ACEE=(R3),MF=(E,M#RACCL)
*
         B     *+4(15)             TEST RACHECK RETURN CODES
         B     EXIT0           0   ACCESS IS ALLOWED
         B     EXIT4           4   RESOURCE NOT DEFINED TO RACF
         B     EXIT8           8   RESOURCE NOT ALLOWED BY RACF
         B     EXIT4           C   SHOULD NEVER HAPPEN
*
*        RETURN TO CALLER WITH PROPER RETURN CODE
*
EXIT0    DS    0H
         LA    RF,0                SET RETURN CODE
         B     INPW990
*
EXIT4A   DS    0H
         OI    U#CHRESP,U@CHRSHO   SET RESHOW BEFORE EXIT
EXIT4    DS    0H
         LA    RF,4                SET RETURN CODE
         B     INPW990
*
EXIT8    DS    0H
         MVC   U#CHMSG(L'MSG30+L'MSG30A),MSG30  MOVE IN MESSAGE
         OI    U#CHRESP,U@CHMSHO   SET MSG PENDING FLAG
         LA    RF,8                SET RETURN CODE
         B     INPW990
*
INPW990  DS    0H
         L     RD,4(RD)              PRIOR SAVE
         RETURN (14,12),RC=(15)      RETURN TO THE CALLER
*
* RELEASE ACEE SUBROUTINE
*
FREEACEE DS    0H
         CLC   U#CHACEE,=A(0)       IS THERE AN ACEE?
         BER   R9                   NO, GET OUT OF HERE FAST!
         XC    M#PWTIME,M#PWTIME    ALLOW FULL MULTIPLE TRY
         MVC   M#RACFL(RACFMDLL),RACFMDL   MOVE IN DELETE MODEL
         RACINIT ACEE=U#CHACEE,MF=(E,M#RACFL)
         XC    U#CHACEE,U#CHACEE    RESET IT
         BR    R9                   RETURN TO CALLER
*
RACIMDL  RACINIT USERID=*-*,            USER ID FIELD                  X
               PASSWRD=*-*,             STORED IN COMMAND FIELD        X
               GROUP=*-*,                                              X
               NEWPASS=*-*,                                            X
               ENVIR=CREATE,                                           X
               ACEE=*-*,                                               X
               SMC=NO,                                                 X
               PASSCHK=YES,                                            X
               APPL=M$APPL,                                            X
               MF=L                     MODEL CODE
RACIMDLL EQU   *-RACIMDL
RACFMDL  RACINIT ACEE=*-*,                                             X
               ENVIR=DELETE,                                           X
               SMC=NO,                                                 X
               MF=L                     MODEL CODE
RACFMDLL EQU   *-RACFMDL
RACSMDL  RACSTAT CLASS=*-*,MF=L
RACSMDLL EQU   *-RACSMDL
RACCMDL  RACHECK ENTITY=(*-*),                                         X
               CLASS=*-*,                                              X
               ATTR=READ,                                              X
               APPL=M$APPL,                                            X
               MF=L
RACCMDLL EQU   *-RACCMDL
*
OMENTS   DS    0CL32
         DC    CL32'INITIAL3'
OMENTOFF EQU   7              OFFSET TO NUMERIC FLAG VALUE
         DC    CL32'INITIAL2'
         DC    CL32'INITIAL1'
OMENT0   DC    CL32'INITIAL0'
         DC    CL32'INITIAL '
         DC    AL1(255)
*
M$APPL   DC    CL8'CANDLE'
M$OMCLAS DC    CL8'$OMEGAMN'
*
MSG00    DC    C'REQUIRED RACF CLASS INACTIVE'
MSG01    DC    C'SESSION NOT APF AUTHORIZED'
MSG04    DC    C'USER NOT DEFINED TO RACF'
MSG08A   DC    C'INVALID PASSWORD, REENTER RACF PASSWORD'
MSG08B   DC    C'INVALID PASSWORD, SESSION REJECTED BY RACF'
MSG12    DC    C'PASSWORD HAS EXPIRED, ENTER NEW PASSWORD'
MSG16    DC    C'NEW PASSWORD IS INVALID, REENTER'
MSG20    DC    C'USER NOT DEFINED TO THIS GROUP, REENTER GROUP'
MSG25    DC    C'RESOURCE CHECKING DONE BY RACF'
MSG30    DC    CL20' '
MSG30A   DC    C'>> SECURITY CHECK FAILED (RACF) <<'
*
         $UCHECK ,
         ORG   U#CHUSER              GO TO USER AREA
M#SW1    DS    X
M@SW1AUT EQU   B'10000000'            AUTHORIZED
M#PWTIME DS    H                     NUMBER OF PASSWORD ATTEMPTS
M#SRET   DS    F                     RACSTAT RETURN CODE
M#IRET   DS    F                     RACINIT RETURN CODE
M#CRET   DS    F                     RACHECK RETURN CODE
M#SCR32  DS    CL32                  32 SCRATCH BYTES
         DS    0F
M#RACIL  DS    XL(RACIMDLL)          RACINIT WORK AREA
         ORG   M#RACIL               USE SAME AREA
OMRACSL  DS    XL(RACSMDLL)
         ORG   M#RACIL               USE SAME AREA
M#RACCL  DS    XL(RACCMDLL)
         ORG   M#RACIL               USE SAME AREA
M#RACFL  DS    XL(RACFMDLL)
         ORG   ,
M@VERFY  EQU   $UCHECK-(*-$UCHECK)+U@CHLEN   OVERLAY
*
*        REGISTER EQUATES
*
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
RA       EQU   10
RB       EQU   11
RC       EQU   12
RD       EQU   13
RE       EQU   14
RF       EQU   15
         END   OM$URACF
