//insert your job card here
//*
//RACFGRP2 EXEC PGM=RACFGRP2
//*********************************************************************
//* THE OUTPUT FROM RACFGRP2 IS FORMATTED AS FOLLOWS:                 *
//*                                                                   *
//*           COLUMNS   1-8: GROUP NAME                               *
//*           COLUMNS  9-16: SUPERIOR GROUP                           *
//*           COLUMNS 17-24: OWNER                                    *
//*           COLUMN     25: UACC                                     *
//*           COLUMN     26: NOTERMUACC                               *
//*           COLUMNS 27-28: NUMBER OF CONNECTED USERS                *
//*           COLUMNS 29-36: CONNECTED USER-ID                        *
//*           COLUMN     37: GROUP AUTHORITY OF CONNECTED USER        *
//*********************************************************************
//STEPLIB      DD DSN=insert your authorized library name here,DISP=SHR
//GRP2FILE     DD SYSOUT=*,DCB=(LRECL=37,BLKSIZE=23458,RECFM=FB)
//SYSUDUMP     DD SYSOUT=*
//SYSABEND     DD SYSOUT=*
//
RACFGRP2 START 0
***********************************************************************
* THE PURPOSE OF THIS PROGRAM IS TO EXTRACT INFORMATION FROM ALL      *
* GROUP PROFILES IN A RACF 1.7 DATASET. THE EXTRACTED DATA            *
* IS PUT INTO A FILE WITH RECORDS FORMATTED AS FOLLOWS:               *
*                                                                     *
*           COLUMNS   1-8: GROUP NAME                                 *
*           COLUMNS  9-16: SUPERIOR GROUP                             *
*           COLUMNS 17-24: OWNER                                      *
*           COLUMN     25: UACC                                       *
*           COLUMN     26: NOTERMUACC                                 *
*           COLUMNS 27-28: NUMBER OF CONNECTED USERS                  *
*           COLUMNS 29-36: CONNECTED USER-ID                          *
*           COLUMN     37: GROUP AUTHORITY OF CONNECTED USER          *
*                                                                     *
* NOTE: THIS PROGRAM MUST BE LINKEDITED INTO AND RUN FROM AN APF      *
*       AUTHORIZED LIBRARY.                                           *
*                                                                     *
* CODED: 10/07/87 BY SCOTT WILSON                                     *
***********************************************************************
R0       EQU   0                       USED BY SYS. MACROS
R1       EQU   1                       USED BY SYS. MACROS
R2       EQU   2                       1ST BASE REGISTER FOR WORKSPACE
R3       EQU   3                       USER COUNT
R4       EQU   4                       1ST MULTIPLIER REGISTER
R5       EQU   5                       2ND MULTIPLIER REGISTER
R6       EQU   6                       DUMMY REGISTER FOR MVCL;
*                                      3RD BASE REGISTER FOR WORKSPACE
R7       EQU   7                       ZERO INDICATOR FOR MVCL
R8       EQU   8                       ADDRESS OF WORKAREA
R9       EQU   9                       LENGTH OF WORKAREA
R10      EQU   10                      ADDRESS OF END OF WORKAREA
R11      EQU   11                      LENGTH OF GROUP NAME
R12      EQU   12                      PROGRAM BASE REGISTER
R13      EQU   13                      ADDRESS OF MVS SAVEAREA
R14      EQU   14                      RETURN ADDRESS
R15      EQU   15                      COMPLETION CODE
*
***********************************************************************
* START OF JOB HOUSEKEEPING                                           *
***********************************************************************
         STM   R14,R12,12(R13)         STORE MVS REGISTERS
         BALR  R12,0                   ESTABLISH R12 AS BASE REGISTER
         USING *,R12                   IDENTIFY BASE REGISTER
         ST    R13,SAVEAREA+4          STORE ADD. OF MVS REG. SAVEAREA
         LA    R13,SAVEAREA            LOAD ADDRESS OF PGM. SAVEAREA
*
***********************************************************************
* SET ADDRESSABILITY TO A 32K WORKSPACE (MAXIMUM RACF PROFILE SIZE)   *
***********************************************************************
WORKADDR EQU   *
         LA    R2,WORKSPCE             LOAD ADDRESS OF WORKSPACE
         USING WORKSPCE,R2             SET ADDRESSABILITY TO WORKSPACE
*
***********************************************************************
* OPEN OUTPUT FILES                                                   *
***********************************************************************
         OPEN  (GRP2FILE,(OUTPUT))     OPEN OUTPUT FILE
*
***********************************************************************
* SET THE LENGTH OF THE WORKAREA AND PROFILE ENTRY                    *
***********************************************************************
INITPROF MVI   PROFILE,X'01'           INITIAL PROFILE LENGTH = 1 BYTE
         MVC   WRKLNGTH(4),=XL4'00008000' PUT WORKAREA LENGTH WHERE...
*                                         ...RACF CAN FIND IT
         MVI   ENTRYNAM,X'00'             CLEAR GROUP PROFILE NAME...
         MVC   ENTRYNAM+1(7),ENTRYNAM     ...IN TWO STEPS
***********************************************************************
* GET A RACF PROFILE                                                  *
***********************************************************************
CLERWORK EQU   *
         L     R7,=XL4'00000000'       INDICATE CLEAR FUNCTION TO MVCL
         LA    R8,WORKDATA             LOAD ADDRESS OF WORK AREA
         L     R9,=XL4'00007FE4'       LOAD LENGTH OF DATA IN WORKAREA
         MVCL  R8,R6                   ZERO OUT THE WORK AREA
         MODESET MODE=SUP,KEY=ZERO          GO SUPERVISOR STATE/KEY 0
         ICHEINTY NEXTC,ENTRY=PROFILE,WKAREA=WORKAREA,TYPE='GRP',      +
               ACTIONS=(GETSGRP,GETOWNR,GETUACC,GETTUACC,GETACL,       +
               GETUSER,GETACCES)
CHECKRC  LTR   R15,R15                 CHECK RETURN CODE FROM ICHEINTY
         BNZ   WRAPUP                  IF NON-ZERO, EXIT THIS PROGRAM
         MODESET MODE=PROB,KEY=NZERO   RETURN TO PROBLEM STATE/NON-ZERO
*        ABEND 1,DUMP
*
***********************************************************************
* WRITE THE RETURNED DATA                                             *
***********************************************************************
         MVI   OUTRECRD,C' '           PROPOGATE BLANK SPACE TO...
         MVC   OUTRECRD+1(36),OUTRECRD ...OUTPUT RECORD IN TWO STEPS
         LA    R10,WORKDATA            LOAD ADDRESS OF RETURNED DATA
         A     R10,DATALNTH            ADD LENGTH OF RETURNED DATA
         L     R11,GRP2LNTH            LOAD LENGTH OF GROUP-ID
         S     R11,=XL4'00000001'      SUBTRACT 1 FROM GROUP-ID LENGTH
         EX    R11,MOVEGRP2            ALTER LENGTH OF GROUP-ID FOR MVC
         B     MOVEOWNR                DON'T EXECUTE MVC TWICE
MOVEGRP2 MVC   OUTGROUP(0),ENTRYNAM    MOVE GROUP INTO OUTPUT FIELD
MOVEOWNR MVC   OUTOWNER(8),WORKSPCE+40 MOVE OWNER INTO OUTPUT FIELD
         MVC   OUTSGRP(8),WORKSPCE+30  MOVE SUPGROUP TO OUTPUT FIELD
         MVC   OUTUACC(1),WORKSPCE+50  MOVE UACC TO OUTPUT FIELD
         MVC   OUTTUACC(1),WORKSPCE+53 MOVE TERMUACC TO OUTPUT FIELD
         MVC   OUTUSERS(2),WORKSPCE+56 MOVE # OF USERS TO OUTPUT FIELD
         LH    R3,WORKSPCE+56          LOAD NUMBER OF USERS
         SLL   R3,16                   TRUNCATE SIGN FIELD
         SRL   R3,16                   RIGHT JUSTIFY NUMBER OF USER'S
         LA    R6,WORKSPCE+60          LOAD ADDRESS OF FIRST USER
         USING USER$ID,R6              SET ADDRESSABILITY TO USER
         SR    R4,R4                   CLEAR OUT REGISTER 4
         C     R3,=XL4'00000000'       ARE THERE USERS?
         BNZ   FINDACES                IF SO, FIND ACCESS LEVEL
         L     R3,=XL4'00000001'       IF NOT, WRITE INDICATIVE...
         MVC   OUTUSER(8),=CL8'* NONE *' ...RECORD CONTAINING THIS INFO
         B     WRITEIT                 WRITE THE OUTPUT RECORD
FINDACES LR    R5,R3                   LOAD # OF USERIDS
         M     R4,=XL4'00000008'       MULTIPLY BY USER-ID LENGTH (8)
         A     R5,=XL4'00000002'       ADD OFFSET FOR ACCESS PREFIX
         AR    R5,R6                   ADD ADDRESS OF FIRST USER-ID
         USING ACCESS,R5               SET ADDRESSABILITY TO ACCESS
NEXTUSER EQU   *
MOVEUSER EQU   *
         MVC   OUTUSER(8),0(R6)        MOVE USER TO OUTPUT FIELD
         MVC   OUTACESS(1),0(R5)       MOVE USER'S GROUP AUTHORITY
WRITEIT  EQU   *
         PUT   GRP2FILE,OUTRECRD       WRITE THE OUTPUT RECORD
         A     R6,=XL4'00000008'       POINT TO NEXT USER
         USING USER$ID,R6              SET ADDRESSABILITY TO USER
         S     R3,=XL4'00000001'       DECREMENT NUMBER OF USERS
         C     R3,=XL4'00000000'       ANY MORE REMAINING USERS?
         BZ    CLERWORK                IF NOT, GET NEXT PROFILE
         B     NEXTUSER                GET THE NEXT USER
*
***********************************************************************
* END OF JOB HOUSEKEEPING                                             *
***********************************************************************
WRAPUP   EQU   *
         MODESET MODE=PROB,KEY=NZERO   RETURN TO PROBLEM STATE/NON-ZERO
CLOSING  CLOSE (GRP2FILE)              CLOSE THE OUTPUT FILE
         DROP  R2,R5,R6                RELEASE BASE REGISTERS
         L     R13,SAVEAREA+4          LOAD RETURN ADDRESS (MVS R13)
         L     R14,12(R13)             LOAD MVS R14 VALUE
         LM    R0,R12,20(R13)          RESTORE MVS REGISTERS 0-12
         SR    R15,R15                 GUARANTEE ZERO RETURN CODE
         BR    R14                     RETURN TO MVS
***********************************************************************
* STORAGE USED BY PROGRAM                                             *
***********************************************************************
SAVEAREA DS    18F                     SAVE AREA FOR SYSTEM REGISTERS
*
GRP2LNTH DC    XL3'000000'             PADDING FOR REGISTER LOADS
PROFILE  DS    AL1                     ENTRY LENGTH FIELD
ENTRYNAM DS    XL8                     GROUP NAME
*
GETSGRP  ICHEACTN FIELD=SUPGROUP,MF=L  RETREIVE NUMBER OF USERS
GETOWNR  ICHEACTN FIELD=AUTHOR,MF=L    RETREIVE PROFILE OWNER
GETUACC  ICHEACTN FIELD=UACC,MF=L      RETREIVE UACC
GETTUACC ICHEACTN FIELD=NOTRMUAC,MF=L  RETREIVE NOTERMUACC
GETACL   ICHEACTN FIELD=ACLCNT,MF=L    RETREIVE NUMBER OF USER-IDS
GETUSER  ICHEACTN FIELD=USERID,MF=L    RETREIVE USER NAME
GETACCES ICHEACTN FIELD=USERACS,MF=L   RETREIVE USER'S GROUP AUTHORITY
*
SUPGROUP DC    CL8'SUPGROUP'           NUMBER OF USERS
OWNER    DC    CL8'AUTHOR  '           OWNER
UACC     DC    CL8'UACC    '           UACC
NOTRMUAC DC    CL8'NOTRMUAC'           NOTERMUACC
ACLCNT   DC    CL8'ACLCNT  '           NUMBER OF USER-IDS
USERID   DC    CL8'USERID  '           CONNECTED USER-ID
USERACS  DC    CL8'USERACS '           GROUP AUTHORITY OF USER
*                  *
OUTRECRD DS    0XL37                   OUTPUT RECORDS
OUTGROUP DS    XL8                     GROUP NAME
OUTSGRP  DS    XL8                     SUPERIOR GROUP
OUTOWNER DS    XL8                     OWNER
OUTUACC  DS    XL1                     UACC
OUTTUACC DS    XL1                     NOTERMUACC
OUTUSERS DS    XL2                     NUMBER OF USERS
OUTUSER  DS    XL8                     USER-ID
OUTACESS DS    XL1                     GROUP AUTHORITY
*
***********************************************************************
* OUTPUT FILE DEFINITION                                              *
***********************************************************************
GRP2FILE DCB   DDNAME=GRP2FILE,MACRF=PM,DSORG=PS
*
         LTORG
*
WORKSPCE EQU   *
WORKAREA DS    0F                      WORKAREA ALIGNMENT ON FULLWORD
WRKLNGTH DS    F                       WORK AREA LENGTH
RBA      DS    CL6                     RBA OF RETURNED ENTRY
         DS    XL2                     RESERVED (BY IBM)
DUPCOUNT DS    F                       DUPLICATE DATASET NAME COUNT
         DS    2F                      RESERVED (BY IBM)
DATALNTH DS    F                       LENGTH OF RETURNED DATA
WORKDATA DS    CL32740                 32K MAXIMUM RETURNED DATA
ENDWORK  EQU   *                       END OF WORKAREA
*
ACCESS   DSECT
USRACESS DS    XL1
*
USER$ID  DSECT
USER     DS    CL8
*
         END
