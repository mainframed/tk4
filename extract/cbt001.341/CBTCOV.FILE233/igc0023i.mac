SVC239   TITLE 'SVC 239 - AUTHORIZATION ON/OFF'
***********************************************************************
* THIS SVC ROUTINE WILL TURN THE JSCB AUTHORIZATION BIT ON OR OFF.    *
* TO UTILIZE THIS SVC, ONE OF THE FOLLOWING TWO CONDITIONS MUST BE    *
* MET:                                                                *
*       (1) THE CALLING ROUTINE IS LOCATED IN THE PLPA                *
*        OR                                                           *
*       (2) THE SVC MUST BE ISSUED BY A RACF-DEFINED USER WHO HAS     *
*           READ ACCESS (OR HIGHER) TO THE RACF APPLICATION NAMED     *
*           SVC239.                                                   *
* ENTRY ENVIRONMENT:                                                  *
*       THIS ROUTINE ASSUMES IT IS A TYPE-3 SVC. ON ENTRY,            *
*       IF R0 = 'AON ' THEN AUTHORIZATION WILL BE TURNED ON           *
*             = 'AOFF' THEN AUTHORIZATION WILL BE TURNED OFF.         *
* EXIT:                                                               *
*       R15 = 0    AUTHORIZATION HAS BEEN SET AS REQUESTED.           *
*           = 4    CALLER REQUESTED AUTHORIZATION ON, BUT IT WAS      *
*                  ALREADY ON.                                        *
*           = 8    REQUEST IGNORED BECAUSE CALLER IS UNDER 'TEST'.    *
*           = 12   R0 VALUE WAS BAD ON ENTRY. AUTHORIZATION HAS BEEN  *
*                  TURNED OFF.                                        *
*           = 16   USE OF SVC 239 NOT AUTHORIZED. AUTHORIZATION HAS   *
*                  BEEN TURNED OFF.                                   *
***********************************************************************
         EJECT
IGC0023I CSECT
         USING *,R6                #####
         USING CVT,R3              #####
         USING TCB,R4              #####
         USING RBBASIC,R5          #####
         LR    R8,R0               SAVE INPUT PARM
         L     R2,RBLINKB-1        POINT TO CALLER'S RB
         LA    R2,0(,R2)           CLEAR HIGH BYTE
         DROP  R5                  #####
         USING RBBASIC,R2          #####
         CLC   CVTLPDIR,RBOPSW+5   RESUME PSW -> PLPA?
         BL    CHKTEST             YES - ENSURE NOT UNDER TEST
*                                  NO - RACHECK FOR AUTHORIZATION
         DROP  R2                  #####
         GETMAIN R,LV=WORKL        FIRST GET SOME STORAGE
         LTR   R15,R15             STORAGE AVAILABLE?
         BNZ   RET16               ERROR IF NOT
         LR    R2,R1               SAVE ADDR OF STORAGE
         USING WORK,R2             #####
         MVC   WRACHECK(RACHECKL),RACHECK  MOVE LIST FORM TO WORK AREA
         RACHECK ENTITY=(SVCENT),  CHECK FOR AUTHORIZATION             X
               CLASS=SVCCLS,                                           X
               ATTR=READ,                                              X
               MF=(E,WRACHECK)
         LR    R9,R15              SAVE RACHECK RETURN CODE
         FREEMAIN R,LV=WORKL,      FREE WORKING STORAGE                X
               A=(R2)
         DROP  R2                  #####
         LTR   R9,R9               AUTHORIZED TO SVC 239?
         BNZ   RET16
CHKTEST  TM    TCBABF,TCBTCP       TEST MODE?
         BO    RET8                YES - IGNORE SVC CALL
         L     R2,TCBJSCB          GET JSCB
         USING IEZJSCB,R2          #####
         CL    R8,=CL4'AON'        REQUEST TO TURN AUTH ON?
         BNE   SETOFF              NO - GO TURN AUTH OFF
         TM    JSCBOPTS,JSCBAUTH   ALREADY AUTHORIZED?
         BO    RET4                YES - LEAVE NON-ZERO RETURN CODE
         OI    JSCBOPTS,JSCBAUTH   NO - AUTHORIZE CALLER
         B     RET0
SETOFF   NI    JSCBOPTS,X'FF'-JSCBAUTH  TURN AUTHORIZATION OFF
         DROP  R2                  #####
         CL    R8,=CL4'AOFF'       TURN AUTHORIZATION OFF?
         BNE   RET12               NO - BAD ENTRY
RET0     SR    R15,R15             SET GOOD RETURN CODE
         BR    R14                 RETURN TO CALLER
RET4     LA    R15,4               SET RC=4
         BR    R14
RET8     LA    R15,8               SET RC=8
         BR    R14
RET12    LA    R15,12              SET RC=12
         BR    R14
RET16    L     R2,TCBJSCB          GET JSCB
         USING IEZJSCB,R2          #####
         EX    0,SETOFF            TURN OFF AUTHORIZATION
         LA    R15,16              SET RC=16
         BR    R14
         EJECT
RACHECK  RACHECK MF=L              LIST FORM OF RACHECK
RACHECKL EQU   *-RACHECK
SVCENT   DC    CL8'SVC239'         ENTITY NAME FOR RACHECK
SVCCLS   DC    AL1(4),C'APPL'      CLASS NAME FOR RACHECK
         LTORG
         EJECT
         REGEQU
         SPACE 5
WORK     DSECT                     WORK AREA
WRACHECK DS    XL(RACHECKL)        SPACE FOR LIST FORM OF RACHECK
WORKL    EQU   *-WORK
         SPACE 5
         PRINT NOGEN
         CVT   DSECT=YES
         IEZJSCB
         IHARB
         IKJTCB
         PRINT GEN
         END   IGC0023I
