SASSXXFF TITLE 'SASSXXFF -- UCC7/RACF LOGOFF EXIT'
*--------------------------------------------------------------------*
*  MODULE NAME -                                                     *
*                                                                    *
*     SASSXXFF -- UCC7/RACF LOGOFF EXIT                              *
*                                                                    *
*  FUNCTION -                                                        *
*                                                                    *
*     LOGOFF OF UCC7/RACF BY PERFORMING CLEANUP, FREE THE ACEE       *
*     AT LOGOFF AS IT WAS ACQUIRED BY THE LOGON EXIT.                *
*                                                                    *
*  ATTRIBUTES -                                                      *
*                                                                    *
*                                                                    *
*     REENTRANT, REUSABLE                                            *
*  OPERATION -                                                       *
*                                                                    *
*     THE FIRST FULLWORD OF THE SCTUSRXX FIELD IS USED AS A          *
*     POINTER TO A USER SCT.  THIS DATA AREA WILL PROBABLY           *
*     EXPAND AS THE NEED ARISES.  HOWEVER, FOR NOW IT KEEPS          *
*     A RECORD OF THE USERID, GROUP, PASSWORD AND POINTS TO          *
*     THE ACEE FOR THE CURRENT LOGGED ON USER.                       *
*                                                                    *
*  INPUT -                                                           *
*                                                                    *
*     R11 = ADDR OF SCT                                              *
*     R12 = ADDR OF SVT                                              *
*                                                                    *
*                                                                    *
*  CHANGE HISTORY -                                                  *
*                                                                    *
*                                                                    *
**********************************************************************
         EJECT
         SPACE  3
*
         PRINT NOGEN
         UCC7SVT
         SASSEQU
         SCTENTRY
         USRSCT
         PRINT GEN
         USING SASSXXFF,R10
         USING SCTSTART,R11
         USING UCC7SVT,R12
         USING USRSCT,R4
SASSXXFF CSECT
         LR    R10,R15
         B     SXFF_FREE_ACEE         CHECK IF ACEE EXISTS
         DC    C'SASSXXFF &SYSDATE'
*
*---------------------------------------------------------------------*
*  CHECK TO SEE IF USER SCT WAS ACQUIRED.                             *
*  CHECK TO SEE IF ACEE WAS ACQUIRED.                                 *
*  IF ACEE WAS  ACQUIRED, THEN FREE IT.                               *
*  NOTE:  THE RACF ROUTINE RESIDES IN SOURCE MODULE SASSXXL1.         *
*---------------------------------------------------------------------*
*
SXFF_FREE_ACEE             DS 0H
         L     R4,SCTUSRXX              USER SCT
         LTR   R4,R4                    WAS IT ACQUIRED
         BZ    SXFF_EXIT                N - RETURN
         TM    USR#FLG1,USR#RACF        ACEE TO FREE
         BNO   SXFF_EXIT                N - RETURN
         NI    USR#FLG1,X'FF'-USR#RACF  TURN OFF FLAG
         OI    USR#FLG1,USR#XACE        INDICATE CLEAR ACEE
         LA    R1,USR#XADR              PARM FOR SASSCHDX
         L     R15,SCHDXADR             EP FOR SUB TASK MANAGER
         BALR  R14,R15                  DO THE RACF ROUTINE
         NI    USR#FLG1,X'FF'-USR#TACT  TURN OFF ACTIVE FLAG
*---------------------------------------------------------------------*
*        EXIT  ROUTINES
*---------------------------------------------------------------------*
SXFF_EXIT                     DS 0H
         LA    R15,0                    SET RC EQUAL TO 0
         SEXIT                          RETURN
*
*---------------------------------------------------------------------*
*        DATA  DEFINITIONS
*---------------------------------------------------------------------*
*
         LTORG
         END
