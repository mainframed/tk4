//insert your job card here
//*********************************************************************
//* TWO OUTPUT RECORDS ARE PRODUCED FOR EACH USER-ID EXTRACTED.       *
//* THESE RECORDS ARE FORMATTED AS FOLLOWS:                           *
//*                                                                   *
//* RECORD 1: COLUMNS   1-8: USER-ID                                  *
//*           COLUMNS  9-28: USER NAME                                *
//*           COLUMNS 29-36: OWNER                                    *
//*           COLUMNS 37-44: DEFAULT GROUP                            *
//*           COLUMN     45: REVOKED INDICATOR                        *
//*           COLUMN     46: SPECIAL INDICATOR                        *
//*           COLUMN     47: OPERATIONS INDICATOR                     *
//*           COLUMN     48: AUDITOR INDICATOR                        *
//*           COLUMN     49: USER AUDIT INDICATOR                     *
//*           COLUMNS 50-52: LAST SIGNON DATE                         *
//*           COLUMNS 53-56: LAST SIGNON TIME                         *
//*           COLUMNS 57-59: LAST PASSWORD CHANGE DATE                *
//*           COLUMN     60: PASSWORD CHANGE INTERVAL                 *
//*                                                                   *
//* RECORD 2: COLUMNS 1-255: USER DATA AREA                           *
//*********************************************************************
//RACFUSER EXEC PGM=RACFUSER
//STEPLIB    DD DSN=insert your authorized library name here,DISP=SHR
//USERFILE   DD SYSOUT=*,DCB=(LRECL=255,BLKSIZE=18870,RECFM=FB)
//SYSUDUMP   DD SYSOUT=*
//SYSABEND   DD SYSOUT=*
//
RACFUSER START 0
***********************************************************************
* THE PURPOSE OF THIS PROGRAM IS TO EXTRACT USER INFORMATION FROM ALL *
* USER PROFILES IN A RACF 1.7 DATASET. THE EXTRACTED DATA IS PUT INTO *
* A FILE WITH LRECL=255 SO THAT IT MAY BE BROWSED BY ISPF. FOR THIS   *
* REASON, IT IS NECESSARY TO PUT THE USER DATA AREA (WHICH MAY        *
* CONTAIN AS MANY AS 255 CHARACTERS BY ITSELF) ON A RECORD SEPARATE   *
* FROM (AND IMMEDIATELY FOLLOWING) THE RECORD CONTAINING THE REST OF  *
* THE EXTRACTED DATA; I.E., TWO OUTPUT RECORDS ARE PRODUCED FOR EACH  *
* USER-ID EXTRACTED. THESE RECORDS ARE FORMATTED AS FOLLOWS:          *
*                                                                     *
* RECORD 1: COLUMNS   1-8: USER-ID                                    *
*           COLUMNS  9-28: USER NAME                                  *
*           COLUMNS 29-36: OWNER                                      *
*           COLUMNS 37-44: DEFAULT GROUP                              *
*           COLUMN     45: REVOKED INDICATOR                          *
*           COLUMN     46: SPECIAL INDICATOR                          *
*           COLUMN     47: OPERATIONS INDICATOR                       *
*           COLUMN     48: AUDITOR INDICATOR                          *
*           COLUMN     49: USER AUDIT INDICATOR                       *
*           COLUMNS 50-52: LAST SIGNON DATE                           *
*           COLUMNS 53-56: LAST SIGNON TIME                           *
*           COLUMNS 57-59: LAST PASSWORD CHANGE DATE                  *
*           COLUMN     60: PASSWORD CHANGE INTERVAL                   *
*                                                                     *
* RECORD 2: COLUMNS 1-255: USER DATA AREA                             *
*                                                                     *
* NOTE: THIS PROGRAM MUST BE LINKEDITED INTO AND RUN FROM AN APF      *
*       AUTHORIZED LIBRARY.                                           *
*                                                                     *
* CODED:  8/11/87 BY SCOTT WILSON                                     *
***********************************************************************
R0       EQU   0                       USED BY SYSTEM MACROS
R1       EQU   1                       USED BY SYSTEM MACROS
R2       EQU   2                       UNUSED
R3       EQU   3                       UNUSED
R4       EQU   4                       UNUSED
R5       EQU   5                       UNUSED
R6       EQU   6                       UNUSED
R7       EQU   7                       UNUSED
R8       EQU   8                       UNUSED
R9       EQU   9                       UNUSED
R10      EQU   10                      LENGTH OF USER-ID
R11      EQU   11                      LENGTH OF WORKAREA
R12      EQU   12                      PROGRAM BASE REGISTER
R13      EQU   13                      ADDRESS OF MVS SAVEAREA
R14      EQU   14                      RETURN ADDRESS
R15      EQU   15                      COMPLETION CODE
*
***********************************************************************
* START OF JOB HOUSEKEEPING                                           *
***********************************************************************
         STM   R14,R12,12(R13)         STORE MVS REGISTERS
         BALR  R12,0                   ESTABLISH R12 AS BASE REGISTER
         USING *,R12                   IDENTIFY BASE REGISTER
         ST    R13,SAVEAREA+4          STORE ADD. OF MVS REG. SAVEAREA
         LA    R13,SAVEAREA            LOAD ADDRESS OF PGM. SAVEAREA
         OPEN  (USERFILE,(OUTPUT))     OPEN OUTPUT FILE
*
***********************************************************************
* CLEAR OUT THE WORKAREA BY FILLING IT WITH BINARY ZEROES             *
***********************************************************************
         XC    WORKAREA,WORKAREA       ZERO OUT WORKAREA
*
***********************************************************************
* SET THE LENGTH OF THE WORKAREA AND PROFILE ENTRY                    *
***********************************************************************
         MVI   PROFILE,X'01'           INITIAL PROFILE LENGTH = 1 BYTE
         LA    R11,ENDWORK-WORKAREA    CALCULATE WORKAREA LENGTH
         ST    R11,WRKLNGTH            PLACE WORKAREA LENGTH WHERE...
*                                      ...RACF CAN FIND IT
***********************************************************************
* GET A RACF PROFILE                                                  *
***********************************************************************
FINDUSER EQU   *
         MVI   WORKDATA,X'00'          PROPOGATE HEX ZEROES TO...
         MVC   WORKDATA+1(254),WORKDATA   ...CLEAR WORK SPACE FIELDS...
         MVC   WORKDATA+256(86),WORKDATA  ...IN TWO STEPS
         MODESET MODE=SUP,KEY=ZERO     GO SUPERVISOR STATE/KEY ZERO
         ICHEINTY NEXT,ENTRY=PROFILE,WKAREA=WORKAREA,MF=I,TYPE='USR',  +
               ACTIONS=(GETNAME,GETOWNR,GETDFLT,GETRVOK,GETSPEC,       +
               GETOPER,GETAUDT,GETUAUD,GETDATE,GETTIME,GETPCDT,        +
               GETPINT,GETDATA)
         LTR   R15,R15                 CHECK RETRUN CODE FROM ICHEINTY
         BNZ   WRAPUP                  IF NON-ZERO, EXIT THIS PROGRAM
         MODESET MODE=PROB,KEY=NZERO   RETURN TO PROBLEM STATE/NON-ZERO
*
***********************************************************************
* WRITE THE RETURNED DATA                                             *
***********************************************************************
         MVI   OUTRECRD,C' '               PROPOGATE BLANK SPACE TO...
         MVC   OUTRECRD+1(254),OUTRECRD   ...OUTPUT RECORD IN TWO STEPS
         L     R10,USRLNGTH            LOAD LENGTH OF USER-ID
         S     R10,=XL4'00000001'      SUBTRACT 1 FROM USER-ID LENGTH
         EX    R10,MOVEID              ALTER LENGTH OF USER-ID FOR MVC
         B     MOVENAME                DON'T EXECUTE MVC TWICE
MOVEID   MVC   OUTUSER(0),ENTRYNAM     MOVE USERID INTO OUTPUT FIELD
MOVENAME MVC   OUTNAME(20),WORKDATA+4  MOVE NAME INTO OUTPUT FIELD
         MVC   OUTOWNER(8),WORKDATA+26 MOVE OWNER INTO OUTPUT FIELD
         MVC   OUTDFLT(8),WORKDATA+36  MOVE DEFAULT GRP TO OUTPUT FIELD
         MVC   OUTREVOK(1),WORKDATA+46 MOVE REVOKE FLAG TO OUTPUT FIELD
         MVC   OUTSPEC(1),WORKDATA+49  MOVE SPECIAL FLAG TO OUT. FIELD
         MVC   OUTOPER(1),WORKDATA+52  MOVE OPERATION FLAG TO OUT. FLD.
         MVC   OUTAUDIT(1),WORKDATA+55 MOVE AUDITOR FLAG TO OUTPUT FLD.
         MVC   OUTUAUDT(1),WORKDATA+58 MOVE UAUDIT FLAG TO OUTPUT FIELD
         MVC   OUTLDATE(3),WORKDATA+61 MOVE LAST USE DATE TO OUT. FLD.
         MVC   OUTLTIME(4),WORKDATA+66 MOVE LAST USE TIME TO OUT. FLD.
         MVC   OUTPDATE(3),WORKDATA+72 MOVE PASSWORD CHANGE DATE
         MVC   OUTPINT(1),WORKDATA+77  MOVE PASSWORD CHANGE INTERVAL
WRITEIT  EQU   *
         PUT   USERFILE,OUTRECRD       WRITE THE OUTPUT RECORD
         MVI   OUTRECRD,C' '           PROPOGATE BLANK SPACE TO CLEAR..
         MVC   OUTRECRD+1(254),OUTRECRD   ...OUTPUT RECORD IN TWO STEPS
         MVC   OUTRECRD(255),WORKDATA+81  MOVE USER DATA AREA TO OUTPUT
         PUT   USERFILE,OUTRECRD       WRITE THE OUTPUT RECORD
         B     FINDUSER                FIND THE NEXT PROFILE
*
***********************************************************************
* END OF JOB HOUSEKEEPING                                             *
***********************************************************************
WRAPUP   EQU   *
         MODESET MODE=PROB,KEY=NZERO   RETURN TO PROBLEM STATE/NON-ZERO
CLOSING  CLOSE (USERFILE)              CLOSE THE OUTPUT FILE
         L     R13,SAVEAREA+4          LOAD RETURN ADDRESS (MVS R13)
         L     R14,12(R13)             LOAD MVS R14 VALUE
         LM    R0,R12,20(R13)          RESTORE MVS REGISTERS 0-12
         SR    R15,R15                 GUARANTEE ZERO RETURN CODE
         BR    R14                     RETURN TO MVS
***********************************************************************
* STORAGE USED BY PROGRAM                                             *
***********************************************************************
SAVEAREA DS    18F                     SAVE AREA FOR SYSTEM REGISTERS
*
USRLNGTH DS    XL3'000000'             PADDING FOR REGISTER LOADS
PROFILE  DS    AL1                     ENTRY LENGTH FIELD
ENTRYNAM DS    XL8'0000000000000000'   USER-ID
*
GETNAME  ICHEACTN FIELD=PGMRNAME,MF=L  RETREIVE USER NAME
GETOWNR  ICHEACTN FIELD=AUTHOR,MF=L    RETREIVE PROFILE OWNER
GETDFLT  ICHEACTN FIELD=DFLTGRP,MF=L   RETREIVE DEFAULT GROUP
GETRVOK  ICHEACTN FIELD=FLAG4,MF=L     RETREIVE FLAG 4 (REVOKED)
GETSPEC  ICHEACTN FIELD=FLAG2,MF=L     RETREIVE FLAG 2 (SPECIAL)
GETOPER  ICHEACTN FIELD=FLAG3,MF=L     RETREIVE FLAG 3 (OPERATIONS)
GETAUDT  ICHEACTN FIELD=FLAG6,MF=L     RETREIVE FLAG 6 (AUDITOR)
GETUAUD  ICHEACTN FIELD=UAUDIT,MF=L    RETREIVE USER AUDIT
GETDATE  ICHEACTN FIELD=LJDATE,MF=L    RETREIVE LAST SIGNON DATE
GETTIME  ICHEACTN FIELD=LJTIME,MF=L    RETREIVE LAST SIGNON TIME
GETPCDT  ICHEACTN FIELD=PASSDATE,MF=L  RETREIVE PASSWORD CHANGE DATE
GETPINT  ICHEACTN FIELD=PASSINT,MF=L   RETREIVE PASSWORD INTERVAL
GETDATA  ICHEACTN FIELD=INSTDATA,MF=L  RETREIVE USER DATA
*
PGMRNAME DS    CL8'PGMRNAME'           PROGRAMMER NAME FIELD DATA
AUTHOR   DS    CL8'AUTHOR  '           OWNER
DFLTGRP  DS    CL8'DFLTGRP '           DEFAULT GROUP
FLAG4    DS    CL8'FLAG4   '           REVOKED USER-ID
FLAG2    DS    CL8'FLAG2   '           SPECIAL USER
FLAG3    DS    CL8'FLAG3   '           OPERATIONS USER
FLAG6    DS    CL8'FLAG6   '           AUDITOR
UAUDIT   DS    CL8'UAUDIT  '           USER AUDIT
LJDATE   DS    CL8'LJDATE  '           LAST SIGNON DATE
LJTIME   DS    CL8'LJTIME  '           LIST SIGNON TIME
PASSDATE DS    CL8'PASSDATE'           PASSWORD CHANGE DATE
PASSINT  DS    CL8'PASSINT '           PASSWORD INTERVAL
INSTDATA DS    CL8'INSTDATA'           INSTALLATION DATA FIELD DATA
*                  *
WORKAREA DS    0F                      WORKAREA ALIGNMENT ON FULLWORD
WRKLNGTH DS    F                       WORK AREA LENGTH
RBA      DS    CL6                     RBA OF RETURNED ENTRY
         DS    CL2                     RESERVED (BY IBM)
         DS    F                       DUPLICATE DATASET NAME COUNT
         DS    2F                      RESERVED (BY IBM)
         DS    CL2                     LENGTH OF RETURNED DATA
WORKDATA DS    CL341                   RETURNED DATA
ENDWORK  EQU   *                       END OF WORKAREA
*
OUTRECRD DS    0XL255                  OUTPUT RECORDS
OUTUSER  DS    XL8                     USER-ID
OUTNAME  DS    XL20                    USER NAME
OUTOWNER DS    XL8                     OWNER
OUTDFLT  DS    XL8                     DEFAULT GROUP
OUTREVOK DS    XL1                     REVOKED
OUTSPEC  DS    XL1                     SPECIAL
OUTOPER  DS    XL1                     OPERATIONS
OUTAUDIT DS    XL1                     AUDITOR
OUTUAUDT DS    XL1                     USER AUDIT
OUTLDATE DS    XL3                     LAST SIGNON DATE
OUTLTIME DS    XL4                     LAST SIGNON TIME
OUTPDATE DS    XL3                     LAST PASSWORD CHANGE DATE
OUTPINT  DS    XL1                     PASSWORD CHANGE INTERVAL
OUTFILLR DS    XL195                   FILLER
*
***********************************************************************
* OUTPUT FILE DEFINITION                                              *
***********************************************************************
USERFILE DCB   DDNAME=USERFILE,MACRF=PM,DSORG=PS
*
         LTORG
*
         END
