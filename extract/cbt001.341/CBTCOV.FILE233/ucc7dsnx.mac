SASSXX07 TITLE 'SASSXX07 -- UCC7/RACF LOGOFF EXIT'
*--------------------------------------------------------------------*
*  MODULE NAME -                                                     *
*                                                                    *
*     SASSXX07 -- UCC7/RACF EXTERNAL DATASET SECURITY EXIT           *
*                                                                    *
*  FUNCTION -                                                        *
*                                                                    *
*     THE PURPOSE OF THIS EXIT IS TO ISSUE RACHECKS FOR ALL          *
*     ACCESSES TO EXTERNAL DATASETS BASED ON THE USER'S ACEE.        *
*                                                                    *
*  ATTRIBUTES -                                                      *
*                                                                    *
*                                                                    *
*  OPERATION -                                                       *
*                                                                    *
*     THE ACEE IS CREATED AT LOGON AND DELETED AT LOGOFF.            *
*                                                                    *
*     NO CHECKING IS DONE FOR THE SECRET PASSWORD.  JCL UPDATES      *
*     ARE ALWAYS REJECTED IF FROM THE MASTER TERMINAL WHICH          *
*     IN THIS INSTALLATION IS THE MVS WTOR INTERFACE.                *
*                                                                    *
*  INPUT -                                                           *
*                                                                    *
*     R2  = PARMLIST                                                 *
*                                                                    *
*              +0 USERID OF TERMINAL OPERATION                       *
*              +1 READ/WRITE INDICATOR  ('R' OR 'W')                 *
*              +2 RETURN CODE FROM UCC7  SECURITY CHECK              *
*                   (0=ACCEPT, 4=REJECT)                             *
*              +4 ADDRESS OF THE 44 BYTE DSN AREA                    *
*              +8 AL1(184) CONSTANT                                  *
*              +9 ADDRESS OF MEMBER NAME OR ZEROS                    *
*                                                                    *
*     R11 = ADDR OF SCT                                              *
*     R12 = ADDR OF SVT                                              *
*                                                                    *
*     NOTE:  THE RACHECK ROUTINE IS LOCATED IN SOURCE                *
*            SASSXXL1.                                               *
*                                                                    *
*  CHANGE HISTORY -                                                  *
*                                                                    *
*                                                                    *
**********************************************************************
         EJECT
         SPACE  3
*
         PRINT NOGEN
         UCC7SVT
         SASSEQU
         SCTENTRY
         USRSCT
         PRINT GEN
         USING SASSXX07,R10
         USING SCTSTART,R11
         USING UCC7SVT,R12
         USING USRSCT,R4
SASSXX07 CSECT
         LR    R10,R15
         B     SX07_FIND_ACEE         CHECK IF USER SCT EXISTS
         DC    C'SASSXX07 &SYSDATE'
*
*---------------------------------------------------------------------*
*  CHECK TO SEE IF USER SCT WAS ACQUIRED.                             *
*  CHECK TO SEE IF ACEE WAS ACQUIRED.                                 *
*---------------------------------------------------------------------*
*
SX07_FIND_ACEE             DS 0H
         L     R4,SCTUSRXX              USER SCT
         LTR   R4,R4                    WAS IT ACQUIRED
         BZ    SX07_EXIT                'N' - RETURN
         TM    USR#FLG1,USR#RACF        ACEE ACQUIRED
         BNO   SX07_EXIT                'N' - RETURN
*
         XC    USR#WORK(256),USR#WORK   CLEAR WORKAREA
         MVI   USR#WORK+C'R',X'02'      SET READ INDICATOR FOR TR
         MVI   USR#WORK+C'W',X'04'      SET WRITE INDICATOR FOR TR
         MVC   USR#RPRM+24,1(R2)        COPY R/W FROM PARMLIST
         TR    USR#RPRM+24(1),USR#WORK  TRANSLATE FOR RACHECK
         L     R5,4(R2)                 ADDRESS OF DSNAME
*
*  IF THE FOLLOWING LOCATE FAILS THEN NO NEED FOR FURTHER
*  SECURITY CHECKING SINCE DSNAME DOES NOT EXIST ANYWAY.
*
         SLOC  DSN=(R5),AREA=USR#WORK,PLIST=USR#PLST,                  X
               VOL=USR#VOL,ERR=SX07_EXIT
*
         ST    R5,USR#RPRM+20           SAVE DSN
         OI    USR#FLG1,USR#RCHK        INDICATE RACHECK
         LA    R1,USR#XADR              PARM FOR SASSCHDX
         L     R15,SCHDXADR             EP FOR SUB TASK MANAGER
         BALR  R14,R15                  DO THE RACF ROUTINE
         LTR   R15,R15                  USER AUTHORIZED
         BZ    SX07_EXIT                'Y' - SET RC=0
         CH    R15,=H'4'                'N' - SEE IF NOT DEF TO RACF
         BE    SX07_EXIT                'Y' - SET RC=0
         MVI   2(R2),X'04'              'N' - SET RC=4
         LA    R15,4                    RC=4
         SEXIT                          RETURN
*
*-------------------------------------------------------------------*
* EXIT ROUTINES
*-------------------------------------------------------------------*
*
SX07_EXIT                     DS  0H
         LA    R15,0                    RC=0
         SEXIT                          RETURN
*
*-------------------------------------------------------------------*
* DATA DEFINITIONS
*-------------------------------------------------------------------*
*
         LTORG
         END
