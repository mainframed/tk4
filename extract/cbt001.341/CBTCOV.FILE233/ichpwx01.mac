PASSWORD TITLE 'RACF EXIT ICHPWX01 -- RACF PASSWORD EXIT'
***********************************************************************
*                                                                     *
*                    ****************************                     *
*                    * AMERICAN EXPRESS COMPANY *                     *
*                    ****************************                     *
*                                                                     *
*        TITLE  -  ICHPWX01                                           *
*                                                                     *
*        STATUS -                                                     *
*                                                                     *
*                          ************                               *
*--------------------------* FUNCTION *-------------------------------*
*                          ************                               *
*                                                                     *
*     THIS IS THE RACF PASSWORD EXIT.  IT IS DESIGNED TO KEEP         *
*  USERS FROM CHANGING THEIR PASSWORDS MORE THAN 3 TIMES ON ANY       *
*  GIVEN DAY.  THE REASONING BEHIND THIS IS THAT USERS MAY BE         *
*  TEMPTED TO INVOKE THE PASSWORD COMMAND IN A CLIST TO CHANGE        *
*  THEIR PASSWORD XX CONSECUTIVE TIMES, ALLOWING THEM TO REUSE        *
*  THE SAME PASSWORD OVER AGAIN.  THIS IS A SECURITY VIOLATION.       *
*                                                                     *
*                             *********                               *
*-----------------------------* INPUT *-------------------------------*
*                             *********                               *
*                                                                     *
*     ON ENTRY, REGISTER 1 CONTAINS THE ADDRESS OF A PARAMETER        *
*  LIST CONTAINING INFORMATION PERTAINENT TO THE PASSWORD COMMAND.    *
*  CONSULT THE RACF SPL FOR FURTHER INFORMATION.                      *
*                                                                     *
*                             **********                              *
*-----------------------------* OUTPUT *------------------------------*
*                             **********                              *
*                                                                     *
*     WHEN CONTROL IS RETURNED, REGISTER 15 CONTAINS ONE OF 5         *
*  RETURN CODES.  CONSULT THE RACF SPL FOR FURTHER INFORMATION.       *
*                                                                     *
**************----------*--------------*------*--------------*--------*
* ATTRIBUTES *          * REUSUABLE-YES*      * REENTRANT-YES*        *
**************----------*--------------*------*--------------*--------*
*                                                                     *
***********************************************************************
*--* CODED  JULY 2, 1984 *--------------------------------------------*
***********************************************************************
         EJECT
***********************************************************************
*                                                                     *
*                       ******************                            *
*-----------------------* REGISTER USAGE *----------------------------*
*                       ******************                            *
*                                                                     *
*        REGISTER 0 - GENERAL USE                                     *
*                 1 - PARAMETER AND GENERAL USE                       *
*               2-9 - WORK REGS                                       *
*                10 - BASE REG                                        *
*             11-12 - WORK REGS                                       *
*                13 - POINTS TO SAVE AREA                             *
*                14 - STANDARD OS LINKAGE REGISTER                    *
*                15 - RETURN CODE AND LINKAGE REGISTER                *
*                                                                     *
*                      *******************                            *
*----------------------* MACRO LIBRARIES *----------------------------*
*                      *******************                            *
*                                                                     *
*        SYS1.MACLIB       MACROS USED  :  SAVE                       *
*        SYS1.AMODGEN                      GETMAIN                    *
*                                          ICHEACTN                   *
*                                          ICHETEST                   *
*                                          ICHEINTY                   *
*                                          FREEMAIN                   *
*                                          CVT                        *
*                                                                     *
*                     ********************                            *
*---------------------* MODIFICATION LOG *----------------------------*
*                     ********************                            *
*                                                                     *
*        DATE     MODIFIED BY         DESCRIPTION                     *
*       ------    -----------         -----------                     *
*                                                                     *
***********************************************************************
         EJECT
ICHPWX01 CSECT
         SAVE  (14,12),,ICHPWX01&SYSDATE&SYSTIME
         USING ICHPWX01,R10
         USING WORKAREA,R13
         LR    R10,R15           ESTABLISH ADDRESSABILITY
         LR    R8,R1             SAVE THE PARAMETER AREA
         L     R6,4(,R8)         R6=A(FUNCTION IDENTITY)
         CLI   0(R6),X'01'       X'01'=RACINIT
         BE    NOTPASS           IF RACINIT - EXIT
         LA    R3,WORKLEN        R3=L'WORK
         LR    R0,R3             R0=L'WORK
         GETMAIN R,LV=(0)        GET SOME MAIN
         LR    R2,R1             R2=A(MAIN AREA)
         SLR   R5,R5             CLEAR R5
         MVCL  R2,R4             CLEAR STORAGE
         ST    R1,8(,R13)        BACKWARD CHAIN SAVE AREA
         ST    R13,4(,R1)        FORWARD CHAIN SAVE AREA
         LR    R13,R1            ESTABLISH ADDRESSABILITY
         LA    R2,LOCATE         R2 = A(START OF MACRO DSECT AREA)
         LA    R3,LEN2           R3 = LENGTH OF AREA
         LA    R4,LOCATE1        R4 = A(IN CORE MACRO AREA)
         LR    R5,R3             R5 = R3
         MVCL  R2,R4             MOVE IT OVER
         LA    R2,LOCATE+28      LOAD ADDRESS OF LOCATE MACRO FLD DEF
         ST    R2,LOCATE+8       STORE IT INTO THE DSECT MACRO
         LA    R2,ALTER+28       LOAD ADDRESS OF ALTER MACRO FLD DEF
         ST    R2,ALTER+8        STORE IT
         EJECT
START    DS    0H
         LA    R2,LEN1           R2=L'WORKA
         STC   R2,WORKA+3        STORE THE LENGTH
         L     R6,20(,R8)        R6=A(USERID INFO)
         MVC   PROFNAM(1),0(R6)  MOVE THE NAME LENGTH
         MVC   PROFNM(8),1(R6)   MOVE NAME INTO PARM AREA
         L     R6,4(,R8)         R6=A(FUNCTION IDENTITY)
********************************************************************
*   CHECK FOR ALTUSER, IF SO,,, ASSUME(HAHA) THIS PERSON KNOWS     *
*   WHAT HE'S DOING.                                               *
********************************************************************
         CLI   0(R6),X'03'       X'03'=ALTUSER
         BE    UPDATE            IF ALU - CLEAR COUNT FIELD AND EXIT
********************************************************************
         ICHEACTN  TESTS=TEST1,MF=(E,ACTN1)
         ICHETEST  FLDATA=(,FLDNM),MF=(E,TEST1)
         ICHEINTY  MF=(E,LOCATE),                                      X
               ENTRY=PROFNAM,                                          X
               WKAREA=WORKA,                                           X
               OPTIONS=(FLDEF),                                        X
               TYPE='USR',                                             X
               ACTIONS=(FLAG2,PASSDATE,ACTN1)
         LTR   R15,R15           TEST FOR RETURN CODE
         BNZ   TESTRET           IF NOT ZERO, TEST RETURN CODE
         CLC   RETLEN,=F'10'     TEST FOR NO PASSWORD AREA
         BE    FIELDADD
         B     GOON
********************************************************************
TESTRET  DS    0H
         CH    R15,=H'52'        DOES THE FIELD EXIST
         BE    FIELDADD          NO - GO ADD IT
         CH    R15,=H'36'        SOME KIND OF ERROR ???
         BNE   WRONG1            YES - JUST EXIT
         CH    R0,=H'3'          IS LENGTH EQUAL TO 3
         BNE   WRONG1            NO - JUST EXIT
         B     FIELDADD          TRY TO ADD THE FIELD
********************************************************************
*  NECESSITY MAY EXIST FOR A SPECIAL USER TO HAVE TO CHANGE        *
*  HIS/HER PASSWORD MORE FREQUENTLY THAN REGULAR USER.  THIS       *
*  CAN EASILY BE CHANGED.                                          *
********************************************************************
GOON     DS    0H
         TM    SPECFLAG,SPECIAL  IS IT A SPECIAL USER
         BO    ISOK              YES - THEN RETURN
********************************************************************
DATE000  DS    0H
         L     R5,CVTPTR         LOAD A(CVT)
         MVC   FWORD,CVTDATE-CVT(R5)
         CP    PDATE(3),FWORD+1(3)
         BNE   UPDATE            IF NOT EQUAL - UPDATE THE RECORD
COUNT000 DS    0H
         MVC   FWORD,PCOUNT
         L     R2,FWORD
********************************************************************
*  THIS IS THE MAGIC NUMBER.  CURRENTLY CHANGES = 3                *
********************************************************************
         CH    R2,CHANGES        IS CURRENT HIGHER?
         BH    WRONG1            YES - DON'T LET 'EM DO IT
         AH    R2,=H'1'          ADD ONE TO CURRENT COUNT
         ST    R2,FWORD          STORE IT INTO FWORD
         MVC   UDATAV,FWORD      MOVE IT INTO UDATAV
         B     FIELDADD          BR TO FIELDADD
UPDATE   DS    0H
         MVC   UDATAV(4),=F'0'   CLEAR THE COUNT FIELD
FIELDADD DS    0H                IF NOT - THEN ADD THE FIELD
         MVC   UDATANM(8),FLDNM
         MVI   UDATAL1,X'04'     MOVE IN LENGTH OF UDATAV FIELD
         LA    R5,L'UDATANM+L'UDATAL1   FIXED LENGTHS
         LA    R5,1(,R5)         LENGTH OF USRFLG FIELD
         AH    R5,=H'4'          ADD L'DATA FIELD FOR TOTAL LENGTH
         MVI   UDATAV+4,128
         ICHEACTN MF=(E,ACTN3),TESTS=TEST3
         ICHETEST MF=(E,TEST3),FLDATA=(,UDATANM)
         ICHEACTN MF=(E,ACTN4),FLDATA=((R5),UDATA),TESTS=TEST4
         ICHETEST MF=(E,TEST4),FLDATA=(,UDATANM)
         ICHEINTY MF=(E,ALTER),                                        X
               ENTRY=PROFNAM,                                          X
               OPTIONS=(,FLDEF),                                       X
               TYPE='USR',                                             X
               ACTIONS=(ACTN3,ACTN4)
         LTR   R15,R15           TEST RETURN CODE
         BZ    ISOK              IF ZERO - JUST RETURN
WRONG1   DS    0H
         LA    R15,4
         B     FREEM
NOTPASS  DS    0H
         SLR   R15,R15
         B     EXIT
         EJECT
ISOK     DS    0H
         SLR   R15,R15           CLEAR R15
FREEM    DS    0H
         LR    R2,R15            SAVE R15
         LR    R1,R13
         L     R13,4(,R13)
         LA    R0,WORKLEN
         FREEMAIN R,LV=(0),A=(1)
         LR    R15,R2            RE-LOAD R15
EXIT     DS    0H
         L     R14,12(R13)
         LM    R0,R12,20(R13)
         BR    R14
         EJECT
********************************************************************
*  LITERALS                                                        *
********************************************************************
         LTORG
         EJECT
********************************************************************
*  CONSTANTS                                                       *
********************************************************************
FLDNM    DC    CL8'PASSCNT1'     PASSCNT FIELD NAME
CHANGES  DC    H'03'             NUMBER OF CHANGES ALLOWED IN A DAY
LOCATE1  ICHEINTY LOCATE,                                              X
               ENTRY=*-*,                                              X
               MF=L,                                                   X
               WKAREA=*-*,                                             X
               ACTIONS=(,,)
         EJECT
ALTER1   ICHEINTY ALTER,                                               X
               ENTRY=*-*,                                              X
               MF=L,                                                   X
               ACTIONS=(,)
         SPACE 1
ACTN11   ICHEACTN FIELD=USERDATA,MF=L,TESTS=*-*
         SPACE 1
ACTN31   ICHEACTN FIELD=USRNM,FLDATA='DEL',TESTS=*-*
         SPACE 1
ACTN41   ICHEACTN FIELD=USERDATA,FLDATA=(*-*,*-*),TESTS=*-*
         SPACE 1
TEST11   ICHETEST FIELD=USRNM,FLDATA=(8,*-*),MF=L
         SPACE 1
TEST31   ICHETEST FIELD=USRNM,FLDATA=(8,*-*),MF=L
         SPACE 1
TEST41   ICHETEST FIELD=USRNM,FLDATA=(8,*-*),MF=L,COND=NE
         SPACE 1
FLAG21   ICHEACTN FIELD=FLAG2,MF=L
         EJECT
PASSDAT1 ICHEACTN FIELD=PASSDATE,MF=L
         EJECT
********************************************************************
*  WORK AREA                                                       *
********************************************************************
WORKAREA DSECT
WORKSTRT DS    0H
REGSAVE  DS    18F
DBLWD    DS    01D               CURRENT TIME WORK AREA
FWORD    DS    01F               FULL WORD WORK AREA
JDATE    DS    PL4               CURRENT JULIAN DATE
*
UDATA    DS    0C                USER DATA FIELDS
UDATANM  DS    CL8               FIELD ID (PASSTIME)
UDATAL1  DS    AL1               FLAG BYTE
UDATAV   DS    CL4               COUNT FIELD
UDATAF   DS    CL1               FLAG FIELD
*
LOCATE   ICHEINTY LOCATE,                                              X
               ENTRY=*-*,                                              X
               MF=L,                                                   X
               WKAREA=*-*,                                             X
               ACTIONS=(,,)
         EJECT
ALTER    ICHEINTY ALTER,                                               X
               ENTRY=*-*,                                              X
               MF=L,                                                   X
               ACTIONS=(,)
         SPACE 1
ACTN1    ICHEACTN FIELD=USERDATA,MF=L,TESTS=*-*
         SPACE 1
ACTN3    ICHEACTN FIELD=USRNM,FLDATA='DEL',TESTS=*-*
         SPACE 1
ACTN4    ICHEACTN FIELD=USERDATA,FLDATA=(*-*,*-*),TESTS=*-*
         SPACE 1
TEST1    ICHETEST FIELD=USRNM,FLDATA=(8,*-*),MF=L
         SPACE 1
TEST3    ICHETEST FIELD=USRNM,FLDATA=(8,*-*),MF=L
         SPACE 1
TEST4    ICHETEST FIELD=USRNM,FLDATA=(8,*-*),MF=L,COND=NE
         SPACE 1
FLAG2    ICHEACTN FIELD=FLAG2,MF=L
         EJECT
PASSDATE ICHEACTN FIELD=PASSDATE,MF=L
LEN2     EQU   *-LOCATE          LENGTH MACRO AREA
*
PROFNAM  DS    AL1               PROFILE NAME LENGTH
PROFNM   DS    CL8               PROFILE NAME
*
WORKA    DS    F                 WORK AREA
         DS    CL6
         DS    CL2
         DS    F
         DS    CL8
RETLEN   DS    F
RETAREA  DS    CL200             DATA RETURNED INTO THIS FIELD
LEN1     EQU   *-WORKA           LENGTH OF WORK AREA
WORKLEN  EQU   *-WORKSTRT        LENGTH OF DSECT
         ORG   RETAREA
FLAGLEN  DS    XL2               FLAG LENGTH
SPECFLAG DS    XL1               FLAG BYTE
SPECIAL  EQU   X'80'             X'80' SPECIAL USER
DATELEN  DS    XL2               LENGTH OF DATE FIELD
PDATE    DS    PL3               PASSWORD DATE
TOTALEN  DS    XL2               TOTAL LENGTH OF TIME FIELD
PCOUNTDS DS    CL8               PASSWORD COUNT IDENTIFIER
PTIMELEN DS    XL1               DATA LENGTH
PCOUNT   DS    XL4               ACTUAL DATA
PTIMEFLG DS    XL1               FLAG
         EJECT
********************************************************************
*  REGISTER EQUATES                                                *
********************************************************************
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
********************************************************************
         SPACE
         PRINT NOGEN
         CVT   DSECT=YES
         PRINT GEN
         END
