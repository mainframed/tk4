//insert your job card here
//*********************************************************************
//* THE FORMAT OF THE OUTPUT RECORDS PRODUCED BY THIS PROGRAM IS:     *
//*                                                                   *
//*    COLUMNS  1-44: DATASET PROFILE NAME                            *
//*    COLUMNS 45-52: RESOURCE CLASS (SHOULD ALWAYS BE "DATASET")     *
//*    COLUMNS 53-60: OWNER                                           *
//*    COLUMN     61: UACC                                            *
//*    COLUMN  62-69: AUTHORIZED USER                                 *
//*    COLUMN     70: AUTHORIZED USER'S ACCESS AUTHORITY              *
//*    COLUMN  71-78: PROFILE TYPE ("GENERIC" OR "DISCRETE")          *
//*                                                                   *
//*********************************************************************
//RACFDSET EXEC PGM=RACFDSET
//STEPLIB    DD DSN=insert your authorized library name here,DISP=SHR
//DSETFILE   DD SYSOUT=*,DCB=(LRECL=78,BLKSIZE=23440,RECFM=FB)
//SYSUDUMP   DD SYSOUT=*
//SYSABEND   DD SYSOUT=*
//
RACFDSET START 0
***********************************************************************
* THE PURPOSE OF THIS PROGRAM IS TO EXTRACT INFORMATION FROM ALL      *
* DATASET PROFILES IN A RACF 1.7 DATASET. THE EXTRACTED DATA          *
* IS PUT INTO A FILE WITH RECORDS FORMATTED AS FOLLOWS:               *
*                                                                     *
*           COLUMNS  1-44: DATASET NAME                               *
*           COLUMNS 45-52: RESOURCE CLASS (SHOULD ALWAYS BE "DATASET")*
*           COLUMNS 53-60: OWNER                                      *
*           COLUMN     61: UACC                                       *
*           COLUMN  62-69: AUTHORIZED USER                            *
*           COLUMN     70: AUTHORIZED USER'S ACCESS AUTHORITY         *
*           COLUMN  71-78: PROFILE TYPE ("GENERIC" OR "DISCRETE")     *
*                                                                     *
* NOTE: THIS PROGRAM MUST BE LINKEDITED INTO AND RUN FROM AN APF      *
*       AUTHORIZED LIBRARY.                                           *
*                                                                     *
* CODED:  9/25/87 BY SCOTT WILSON                                     *
***********************************************************************
R0       EQU   0                       USED BY SYS. MACROS
R1       EQU   1                       USED BY SYS. MACROS
R2       EQU   2                       1ST BASE REGISTER FOR WORKSPACE
R3       EQU   3                       USER COUNT
R4       EQU   4                       MULTIPLIER REGISTER
R5       EQU   5                       2ND BASE REGISTER FOR WORKSPACE
R6       EQU   6                       DUMMY REGISTER FOR MVCL;
*                                      3RD BASE REGISTER FOR WORKSPACE
R7       EQU   7                       ZERO INDICATOR FOR MVCL
R8       EQU   8                       ADDRESS OF WORKAREA
R9       EQU   9                       LENGTH OF WORKAREA
R10      EQU   10                      ADDRESS OF END OF WORKAREA
R11      EQU   11                      LENGTH OF DATASET NAME
R12      EQU   12                      PROGRAM BASE REGISTER
R13      EQU   13                      ADDRESS OF MVS SAVEAREA
R14      EQU   14                      RETURN ADDRESS
R15      EQU   15                      COMPLETION CODE
*
***********************************************************************
* START OF JOB HOUSEKEEPING                                           *
***********************************************************************
         STM   R14,R12,12(R13)         STORE MVS REGISTERS
         BALR  R12,0                   ESTABLISH R12 AS BASE REGISTER
         USING *,R12                   IDENTIFY BASE REGISTER
         ST    R13,SAVEAREA+4          STORE ADD. OF MVS REG. SAVEAREA
         LA    R13,SAVEAREA            LOAD ADDRESS OF PGM. SAVEAREA
*
***********************************************************************
* SET ADDRESSABILITY TO A 32K WORKSPACE (MAXIMUM RACF PROFILE SIZE)   *
***********************************************************************
WORKADDR EQU   *
         LA    R2,WORKSPCE             LOAD ADDRESS OF WORKSPACE
         USING WORKSPCE,R2             SET ADDRESSABILITY TO WORKSPACE
*
***********************************************************************
* OPEN OUTPUT FILES                                                   *
***********************************************************************
         OPEN  (DSETFILE,(OUTPUT))     OPEN OUTPUT FILE
*
***********************************************************************
* SET THE LENGTH OF THE WORKAREA AND PROFILE ENTRY                    *
***********************************************************************
INITPROF MVI   PROFILE,X'01'           INITIAL PROFILE LENGTH = 1 BYTE
         MVC   WRKLNGTH(4),=XL4'00008000' PUT WORKAREA LENGTH WHERE...
*                                         ...RACF CAN FIND IT
         MVI   ENTRYNAM,X'00'             CLEAR DATASET PROFILE NAME...
         MVC   ENTRYNAM+1(43),ENTRYNAM    ...IN TWO STEPS
***********************************************************************
* GET A RACF PROFILE                                                  *
***********************************************************************
CLERWORK EQU   *
         L     R7,=XL4'00000000'       INDICATE CLEAR FUNCTION TO MVCL
         LA    R8,WORKDATA             LOAD ADDRESS OF WORK AREA
         L     R9,=XL4'00007FE4'       LOAD LENGTH OF DATA IN WORKAREA
         MVCL  R8,R6                   ZERO OUT THE WORK AREA
         MODESET MODE=SUP,KEY=ZERO          GO SUPERVISOR STATE/KEY 0
         CLI   GENFLAG,X'80'           WAS PREVIOUS PROFILE GENERIC?
         BNE   DISCPROF                IF NOT, ASSUME DISCRETE
         ICHEINTY NEXTC,MF=(E,GENERIC) IF SO, ASSUME ANOTHER GENERIC
         CLI   GENFLAG,X'80'           WAS RETURNED PROFILE GENERIC?
         BNE   MARKDISC                IF NOT, MARK IT AS DISCRETE
GENPROF  MVC   OUTPTYPE,=CL8'GENERIC ' INDICATE GENERIC PROFILE
         B     CHECKRC                 CHECK ICHEINTY RETURN CODE
DISCPROF EQU   *
         ICHEINTY NEXTC,MF=(E,DISCRETE)    DISCRETE ICHEINTY
         CLI   GENFLAG,X'80'           WAS RETURNED PROFILE GENERIC?
         BE    GENPROF                 IF SO, MARK IT AS GENERIC
MARKDISC MVC   OUTPTYPE,=CL8'DISCRETE' INDICATE DISCRETE PROFILE
         MVI   GENFLAG,X'00'           INDICATE DISCRETE LAST PROCESSED
CHECKRC  LTR   R15,R15                 CHECK RETURN CODE FROM ICHEINTY
         BNZ   WRAPUP                  IF NON-ZERO, EXIT THIS PROGRAM
         MODESET MODE=PROB,KEY=NZERO   RETURN TO PROBLEM STATE/NON-ZERO
*
***********************************************************************
* WRITE THE RETURNED DATA                                             *
***********************************************************************
         MVI   OUTRECRD,C' '           PROPOGATE BLANK SPACE TO...
         MVC   OUTRECRD+1(69),OUTRECRD ...OUTPUT RECORD IN TWO STEPS
         LA    R10,WORKDATA            LOAD ADDRESS OF RETURNED DATA
         A     R10,DATALNTH            ADD LENGTH OF RETURNED DATA
         L     R11,DSETLNTH            LOAD LENGTH OF DSET-ID
         S     R11,=XL4'00000001'      SUBTRACT 1 FROM DSET-ID LENGTH
         EX    R11,MOVEDSET            ALTER LENGTH OF DSET-ID FOR MVC
         B     MOVECLAS                DON'T EXECUTE MVC TWICE
MOVEDSET MVC   OUTDSET(0),ENTRYNAM     MOVE DATASET INTO OUTPUT FIELD
MOVECLAS MVC   OUTCLASS(8),=CL8'DATASET '  MOVE RESOURCE CLASS TO O'PUT
         MVC   OUTOWNER(8),WORKDATA+2  MOVE OWNER INTO OUTPUT FIELD
         MVC   OUTUACC(1),WORKDATA+12  MOVE UACC TO OUTPUT FIELD
         LH    R3,WORKDATA+15          LOAD NUMBER OF USER-IDS
         SLL   R3,16                   TRUNCATE SIGN FIELD
         SRL   R3,16                   RIGHT JUSTIFY NUMBER OF USERS
         LA    R6,WORKDATA+19          LOAD ADDRESS OF FIRST USER-ID
         USING USER$ID,R6              SET ADDRESSABILITY TO USER-ID
         SR    R4,R4                   CLEAR OUT REGISTER 4
         C     R3,=XL4'00000000'       ARE THERE AUTHORIZED USERS?
         BNZ   FINDACES                IF SO, LOCATE ACCESS LIST
         L     R3,=XL4'00000001'       IF NOT, WRITE INDICATIVE...
         MVC   OUTUSER(8),=CL8'* NONE *'  ...RECORD CONTAINING...
         MVI   OUTACESS,X'00'             ...THIS INFO
         B     WRITEIT                 WRITE THE OUTPUT RECORD
FINDACES LR    R5,R3                   LOAD # OF USERIDS
         M     R4,=XL4'00000008'       MULTIPLY BY USER-ID LENGTH (8)
         A     R5,=XL4'00000002'       ADD OFFSET FOR ACCESS PREFIX
         AR    R5,R6                   ADD ADDRESS OF FIRST USER-ID
         USING ACCESS,R5               SET ADDRESSABILITY TO ACCESS
NEXTUSER EQU   *
MOVEUSER EQU   *
         MVC   OUTUSER(8),0(R6)        MOVE USER-ID TO OUTPUT FIELD
         MVC   OUTACESS(1),0(R5)       MOVE ACCESS TO OUTPUT FIELD
WRITEIT  EQU   *
         PUT   DSETFILE,OUTRECRD       WRITE THE OUTPUT RECORD
         A     R6,=XL4'00000008'       POINT TO NEXT USER-ID
         USING USER$ID,R6              SET ADDRESSABILITY TO USER-ID
         A     R5,=XL4'00000001'       POINT TO NEXT USER ACCESS
         USING ACCESS,R5               SET ADDRESSABILITY TO ACCESS
         S     R3,=XL4'00000001'       DECREMENT NUMBER OF USERIDS
         C     R3,=XL4'00000000'       ANY MORE REMAINING USER-IDS?
         BZ    CLERWORK                IF NOT, GET NEXT PROFILE
         B     NEXTUSER                GET THE NEXT USER-ID
*
***********************************************************************
* END OF JOB HOUSEKEEPING                                             *
***********************************************************************
WRAPUP   EQU   *
         MODESET MODE=PROB,KEY=NZERO   RETURN TO PROBLEM STATE/NON-ZERO
CLOSING  CLOSE (DSETFILE)              CLOSE THE OUTPUT FILE
         DROP  R2,R5,R6                RELEASE BASE REGISTERS
         L     R13,SAVEAREA+4          LOAD RETURN ADDRESS (MVS R13)
         L     R14,12(R13)             LOAD MVS R14 VALUE
         LM    R0,R12,20(R13)          RESTORE MVS REGISTERS 0-12
         SR    R15,R15                 GUARANTEE ZERO RETURN CODE
         BR    R14                     RETURN TO MVS
***********************************************************************
* STORAGE USED BY PROGRAM                                             *
***********************************************************************
SAVEAREA DS    18F                     SAVE AREA FOR SYSTEM REGISTERS
*
GENERIC  ICHEINTY NEXTC,ENTRY=PROFILE,WKAREA=WORKAREA,MF=L,TYPE='DS',  +
               ACTIONS=(GETOWNR,GETUACC,GETCNT,GETUSER,GETUACS),       +
               GENERIC=UNCOND
*
DISCRETE ICHEINTY NEXTC,ENTRY=PROFILE,WKAREA=WORKAREA,MF=L,TYPE='DS',  +
               ACTIONS=(GETOWNR,GETUACC,GETCNT,GETUSER,GETUACS),       +
               GENERIC=NO
*
DSETLNTH DC    XL3'000000'             PADDING FOR REGISTER LOADS
PROFILE  DS    AL1                     ENTRY LENGTH FIELD
ENTRYNAM DS    XL44                    DATASET NAME
*
GETCNT   ICHEACTN FIELD=ACLCNT,MF=L    RETREIVE # OF AUTHORIZED USERS
GETOWNR  ICHEACTN FIELD=AUTHOR,MF=L    RETREIVE PROFILE OWNER
GETUACC  ICHEACTN FIELD=UNIVACS,MF=L   RETREIVE UACC
GETUSER  ICHEACTN FIELD=USERID,MF=L    RETREIVE AUTHORIZED USER
GETUACS  ICHEACTN FIELD=USERACS,MF=L   RETREIVE ACCESS AUTHORITY
*
ACLCNT   DC    CL8'ACLCNT  '           # OF AUTHORIZED USERS
OWNER    DC    CL8'AUTHOR  '           OWNER
UACC     DC    CL8'UNIVACS '           UACC
USERID   DC    CL8'USERID  '           AUTHORIZED USER-ID OR GROUP
USERACS  DC    CL8'USERACS '           AUTHORIZED USER ACCESS AUTHORITY
*                  *
OUTRECRD DS    0XL78                   OUTPUT RECORDS
OUTDSET  DS    XL44                    DATASET NAME
OUTCLASS DS    XL8                     DATASET CLASS
OUTOWNER DS    XL8                     OWNER
OUTUACC  DS    XL1                     UACC
OUTUSER  DS    XL8                     AUTHORIZED USER-ID OR GROUP
OUTACESS DS    XL1                     AUTHORIZED USER ACCESS AUTHORITY
OUTPTYPE DS    XL8                     PROFILE TYPE (GENERIC/DISCRETE)
*
***********************************************************************
* OUTPUT FILE DEFINITION                                              *
***********************************************************************
DSETFILE DCB   DDNAME=DSETFILE,MACRF=PM,DSORG=PS
*
         LTORG
*
WORKSPCE EQU   *
WORKAREA DS    0F                      WORKAREA ALIGNMENT ON FULLWORD
WRKLNGTH DS    F                       WORK AREA LENGTH
RBA      DS    CL6                     RBA OF RETURNED ENTRY
GENFLAG  EQU   *                       NEXT BYTE IS GENERIC FLAG
         DS    XL2                     RESERVED (BY IBM)
DUPCOUNT DS    F                       DUPLICATE DATASET NAME COUNT
         DS    2F                      RESERVED (BY IBM)
DATALNTH DS    F                       LENGTH OF RETURNED DATA
WORKDATA DS    CL32740                 32K MAXIMUM RETURNED DATA
ENDWORK  EQU   *                       END OF WORKAREA
*
ACCESS   DSECT
USRACESS DS    XL1
*
USER$ID  DSECT
USER     DS    CL8
*
FULLWORD DSECT
PARMADDR DS    F
PARM     DS    CL8
*
         END
