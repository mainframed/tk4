*---------------------------------------------------------------------*
*                                                                     *
*   PROGRAM:       SACXTRT                                            *
*                                                                     *
*   WRITTEN BY:                                                       *
*                                                                     *
*   DATE:          FEBRUARY 18, 1987                                  *
*                                                                     *
*   FUNCTION:      THIS PROGRAM WRITES A RECORD TO 'DDNAME=RACFOUT'   *
*                  FOR EACH GROUP IN THE RACF DATABASE.               *
*                                                                     *
*   SPECIAL NOTES: THIS MODULE MUST RESIDE IN APF AUTHORIZED LIBRARY  *
*                  AS IT ISSUES RACF MANAGER REQUESTS.                *
*                                                                     *
*                  THIS MODULE IS PROTECTED BY RACF PROGRAM           *
*                  PROTECTION.  IT SHOULD BE RESTRICTED TO SELECT     *
*                  AUTHORIZED USERS DUE TO THE NATURE OF IT'S         *
*                  FUNCTION.                                          *
*                                                                     *
*   GLOSSARY OF OUTPUT:                                               *
*   ===================                                               *
*                                                                     *
*   SMFID   - SMFID OF SYSTEM WHERE DATA RETRIEVED                    *
*   PROFLEN - LENGTH OF CONNECT                                       *
*   RACPROF - CONNECT NAME                                            *
*   AUTHDATE- DATE CREATED                                            *
*   LJDATE  - LAST JOB ACCESS DATE                                    *
*   AUTHOR  - OWNER                                                   *
*   UACC    - GROUP UNIVERSAL ACCESS                                  *
*   INITCNT - RACINIT COUNT                                           *
*   FLAG1   - GROUP-ADSP                                              *
*   FLAG2   - GROUP-SPECIAL                                           *
*   FLAG3   - GROUP-OPEATIONS                                         *
*   FLAG4   - GROUP-REVOKED                                           *
*   FLAG5   - GROUP-GRPACC                                            *
*   NOTRMUAC- TERMINAL UNIVERSAL ACCESS                               *
*   GRPAUDIT- GROUP-AUDITOR                                           *
*                                                                     *
*   REGISTER CONVENTIONS:                                             *
*                                                                     *
*      PROLOGUE : R2 - CVT BASE                                       *
*                 R3 - ASCB BASE                                      *
*                 R4 - ASXB BASE                                      *
*                 R5 - ACEE BASE                                      *
*                 R8 - WORKAREA BASE                                  *
*                                                                     *
*      MAIN     : R2 - GENERAL TARGET                                 *
*                 R3 - TARGET LENGTH                                  *
*                 R4 - GENERAL SOURCE                                 *
*                 R5 - SOURCE LENGTH                                  *
*                 R6 - GENERAL LENGTH                                 *
*                 R7 - GENERAL ADDRESS                                *
*                 R8 - WORKAREA BASE                                  *
*                 R9 - LINK CONTROL REGISTER                          *
*                 R10- LINK CONTROL REGISTER                          *
*                                                                     *
*      NOTE - OPERANDS ON ICHEINTY MACRO MUST BE ABSOLUTE             *
*                                                                     *
*      THE FOLLOWING SAMPLE JCL CAN BE USED TO EXECUTE SACXTRT.       *
*                                                                     *
*      //STEP1    EXEC PGM=SACXTRT,REGION=1024K                       *
*      //STEPLIB  DD DSN=YOUR.APFAUTH.LIBRARY,DISP=SHR                *
*      //SYSPRINT DD SYSOUT=*                                         *
*      //SYSUDUMP DD SYSOUT=*                                         *
*      //RACFOUT  DD DSN=YOUR.TARGET.DATASET,                         *
*      //            DISP=(NEW,CATLG,DELETE),                         *
*      //            UNIT=3380,                                       *
*      //            SPACE=(CYL,(2,1)),                               *
*      //            DCB=(LRECL=46,RECFM=VB,BLKSIZE=23460)            *
*      //*                                                            *
*                                                                     *
*---------------------------------------------------------------------*
         MACRO
         REGISTER
         LCLA  &REG
&REG     SETA  0
*  GENERAL REGISTER EQUATES
.RLOOP   ANOP
R&REG    EQU   &REG
&REG     SETA  &REG+1
         AIF   (&REG LE 15).RLOOP
&REG     SETA  0
*  CONTROL REGISTER EQUATES
.CLOOP   ANOP
C&REG    EQU   &REG
&REG     SETA  &REG+1
         AIF   (&REG LE 15).CLOOP
&REG     SETA  0
*  FLOATING-POINT REGISTER EQUATES
.FLOOP   ANOP
F&REG    EQU   &REG
&REG     SETA  &REG+2
         AIF   (&REG LE 6).FLOOP
         MEND
*
         MACRO
         SAVEAREA &DSECT=YES
         AIF   ('&DSECT' EQ 'NO').NODSECT
         AIF   ('&DSECT' NE 'YES').PARMERR
SAVEAREA DSECT                     FORMAT OF STANDARD SAVE AREA
         AGO   .COMMON
.NODSECT ANOP
SAVEAREA DS    0F                  FORMAT OF STANDARD SAVE AREA
.COMMON  ANOP
SAVEPLI  DS    F                   USED BY PL/I
SAVEOLD  DS    F                   OLD (BACK-CHAIN) SAVE AREA
SAVENEW  DS    F                   NEW (FORWARD-CHAIN) SAVE AREA
SAVER14  DS    F                   CALLER'S R14
SAVER15  DS    F                   CALLER'S R15
SAVER0   DS    F                   CALLER'S R0
SAVER1   DS    F                   CALLER'S R1
SAVER2   DS    F                   CALLER'S R2
SAVER3   DS    F                   CALLER'S R3
SAVER4   DS    F                   CALLER'S R4
SAVER5   DS    F                   CALLER'S R5
SAVER6   DS    F                   CALLER'S R6
SAVER7   DS    F                   CALLER'S R7
SAVER8   DS    F                   CALLER'S R8
SAVER9   DS    F                   CALLER'S R9
SAVER10  DS    F                   CALLER'S R10
SAVER11  DS    F                   CALLER'S R11
SAVER12  DS    F                   CALLER'S R12
SAVESIZE EQU   *-SAVEAREA          LENGTH OF SAVEAREA IN BYTES
         MEXIT
.PARMERR ANOP
         MNOTE 8,'*** ERROR *** INVALID PARM: DSECT=&DSECT'
         MEND
*
         REGISTER
*
         USING CVT,R2
         USING ASCB,R3
         USING ASXB,R4
         USING ACEE,R5
         USING WORKAREA,R8
         USING SACXTRT,R12
SACXTRT CSECT
         SAVE  (14,12),,'SACXTRT &SYSDATE.&SYSTIME'
         LR    R12,R15            ESTABLISH ADDRESSABILITY
*        LR    R11,R1             SET PARM SAVE REGISTER
         GETMAIN R,LV=WORKSIZE
         ST    R1,8(,R1)          FORWARD POINTER
         ST    R13,4(,R1)         BACKWARD POINTER
         LR    R8,R1              ESTABLISH ADDRESSABILITY
         LR    R13,R1             POINT TO NEW SAVE AREA
         MVC   LINE(133),BLANKS   BLANK OUT PRINT LINE
         OPEN  (RACFOUT,(OUTPUT),SYSPRINT,(OUTPUT))
         SPACE
         L     R2,CVTPTR          POINT TO CVT
         L     R3,CVTTCBP         POINT TO TCB AND ASCB POINTERS
         L     R3,12(,R3)         POINT TO CURRENT ASCB
         L     R4,ASCBASXB        POINT TO ASXB
         L     R5,ASXBSENV        POINT TO ACEE
         LTR   R5,R5              IS THERE AN ACEE?
         BNZ   RACFCHK           ..YES, CONTINUE
         MVC   LINE(L'MSG1),MSG1  ..NO, SETUP ERROR MESSAGE
         PUT   SYSPRINT,LINE      WRITE ERROR MESSGAE
         B     RETURN             AND TERMINATE THE PROGRAM
         SPACE
RACFCHK  RACSTAT
         LTR   R15,R15            IS RACF OPERATING?
         BZ    RACFOK             ..YES, CONTINUE
         MVC   LINE(L'MSG2),MSG2  ..NO, SETUP ERROR MESSAGE
         PUT   SYSPRINT,LINE      WRITE ERROR MESSGAE
         B     RETURN             AND TERMINATE THE PROGRAM
         EJECT
RACFOK   L     R3,CVTSMCA         POINT TO SMCA
         USING SMCABASE,R3
         MVC   SMFID(4),SMCASID   RETRIEVE SMFID FROM SMCA
         DROP  R2
         DROP  R3
         DROP  R4
         DROP  R5
         MVI   ENTRYNAM,X'00'
         MVC   ENTRYNAM+1(16),ENTRYNAM SET INITIAL PROFILE NAME
         MVI   ENTRY,X'11'        SET INITIAL PROFILE LENGTH
         EJECT
LOOP     LA    R2,RBA             POINT TO TARGET FIELD
         L     R3,=A(WKAREAL)     SET FIELD LENGTH
         XR    R4,R4              SET SOURCE FIELD TO NULL
         XR    R5,R5              SET SOURCE LENGTH AND PAD TO X'00'
         MVCL  R2,R4              CLEAR THE WORK AREA
         ICHEINTY NEXTC,TYPE='CON',ENTRY=ENTRY,WKAREA=WKAREA,          X
               ACTIONS=(ACTN1,ACTN2,ACTN3,ACTN4,ACTN5,ACTN6,           X
               ACTN7,ACTN8,ACTN9,ACTN10,ACTN11,ACTN12)
         LTR   R15,R15            WAS LOCATE SUCCESSFUL?
         BNZ   LOCATNZ            ..NO, PROCESS EXCEPTION
         BAL   R9,LOCAT0
         B     LOOP
         EJECT
LOCATNZ  C     R15,=F'12'         HAVE WE CHECKED ALL CONNECTS?
         BE    LASTCON            ..YES
         SPACE
BADRETCD CVD   R15,WORKD          CONVERT RETURN CODE TO DECIMAL
         UNPK  MSG3CD,WORKD+6(2)  MOVE TO ERROR MESSAGE
         OI    MSG3CD+1,X'F0'     MAKE LAST CHARACTER PRINTABLE
         MVC   LINE(L'MSG3),MSG3  SETUP ERROR MESSAGE
         PUT   SYSPRINT,LINE      WRITE ERROR MESSGAE
         B     RETURN             AND TERMINATE THE PROGRAM
         SPACE
LASTCON  MVC   LINE(L'MSG4),MSG4  SETUP MESSAGE
         PUT   SYSPRINT,LINE      WRITE SUCCESS MESSGAE
         SPACE
RETURN   CLOSE (RACFOUT,,SYSPRINT)
         L     R13,SAVEOLD        BACKWARD POINTER
         DROP  R8                 DROP R8 ADDRESSABILITY
         FREEMAIN R,LV=WORKSIZE,A=(R8)
         USING SAVEAREA,R13       USE OLD SAVE AREA FRO ADDRESSABILITY
         XC    SAVER15(4),SAVER15 CLEAR RETURN CODE
         XC    SAVENEW,SAVENEW    CLEAR FORWARD POINTER
         RETURN (14,12)           RETURN TO CALLER
         EJECT
LOCAT0   LA    R2,RACPROF         POINT TO TARGET FIELD
         XR    R3,R3              CLEAR WORK REGISTER
         IC    R3,ENTRY           GET LENGTH OF PROFILE NAME
         STC   R3,PROFLEN         GET LENGTH OF PROFILE NAME
         LR    R5,R3
         LA    R4,ENTRYNAM        POINT TO SOURCE FIELD
         MVC   RACPROF(17),BLANKS BLANK OUT TARGET FIELD
         MVCL  R2,R4              MOVE PROFILE NAME TO BUFFER
         XR    R6,R6              CLEAR R6
         LA    R6,RETDATA         SET INITIAL ADDRESS FOR RETDATA
$AUTHDTE LA    R2,AUTHDATE        POINT TO TARGET
         BAL   R10,MOVEIT
$AUTHOR  LA    R2,AUTHOR          POINT TO TARGET
         BAL   R10,MOVEIT
$LJDATE  LA    R2,LJDATE          POINT TO TARGET
         BAL   R10,MOVEIT
$UACC    LA    R2,UACC            POINT TO TARGET
         BAL   R10,MOVEIT
$INITCNT LA    R2,INITCNT         POINT TO TARGET
         BAL   R10,MOVEIT
$FLAG1   LA    R2,FLAG1           POINT TO TARGET
         BAL   R10,MOVEIT
$FLAG2   LA    R2,FLAG2           POINT TO TARGET
         BAL   R10,MOVEIT
$FLAG3   LA    R2,FLAG3           POINT TO TARGET
         BAL   R10,MOVEIT
$FLAG4   LA    R2,FLAG4           POINT TO TARGET
         BAL   R10,MOVEIT
$FLAG5   LA    R2,FLAG5           POINT TO TARGET
         BAL   R10,MOVEIT
$NOTRMU  LA    R2,NOTRMUAC        POINT TO TARGET
         BAL   R10,MOVEIT
$GRPAUDT LA    R2,GRPAUDIT        POINT TO TARGET
         BAL   R10,MOVEIT
         PUT   RACFOUT,RACREC
         BR    R9
MOVEIT   XR    R7,R7              CLEAR WORK REGISTER
         LH    R7,0(,R6)          LOAD RETURNED LENGTH
         LR    R3,R7
         LR    R5,R7
         LA    R4,2(,R6)          POINT TO SOURCE
         MVCL  R2,R4
         LA    R6,2(R7,R6)        POINT TO NEXT FIELD
         BR    R10
         LTORG
BLANKS   DC    CL255' '
         SPACE
MSG1     DC    C'1**ERROR** - TASK ACEE NOT FOUND'
MSG2     DC    C'1**ERROR** - RACF IS NOT OPERATING'
MSG3     DC    C'1**ERROR** - RACF INTERNAL ERROR CODE = XX'
         ORG   *-2
MSG3CD   DS    CL2
MSG4     DC    C'1SACXTRT COMPLETED SUCCESSFULLY'
         SPACE
SYSPRINT DCB   DSORG=PS,DDNAME=SYSPRINT,MACRF=PM,RECFM=FA,LRECL=133,   X
               BLKSIZE=133
RACFOUT  DCB   DSORG=PS,DDNAME=RACFOUT,MACRF=PM,RECFM=FB
         SPACE
ENTRY    DS    AL1(1)             LENGTH OF PROFILE NAME FIELD
ENTRYNAM DS    XL44               PROFILE NAME
WKAREA   DS    0F
WKAREAS  DC    AL4(WKAREAL+4)     LENGTH OF WORKAREA
RBA      DS    CL6                RBA OF RACF RECORD
FLAGS    DS    X                  FLAGS
         DS    C                  RESERVED
DUPCOUNT DS    AL4                DUPLICATE RECORD COUNT
         DS    CL8                RESERVED
RETLEN   DS    AL4                LENGTH OF RETURNED DATA
RETDATA  DS    CL58               RETURNED DATA
WKAREAE  EQU   *                  END OF WORK AREA
WKAREAL  EQU   *-RBA              CALCULATE WORK AREA LENGTH
ACTN1    ICHEACTN FIELD=AUTHDATE  CREATE DATE
ACTN2    ICHEACTN FIELD=AUTHOR    OWNER
ACTN3    ICHEACTN FIELD=LJDATE    LAST ACCESS DATE
ACTN4    ICHEACTN FIELD=UACC      UNIVERSAL ACCESS
ACTN5    ICHEACTN FIELD=INITCNT   RACINIT COUNT
ACTN6    ICHEACTN FIELD=FLAG1     ADSP
ACTN7    ICHEACTN FIELD=FLAG2     SPECIAL
ACTN8    ICHEACTN FIELD=FLAG3     OPERATIONS
ACTN9    ICHEACTN FIELD=FLAG4     REVOKED
ACTN10   ICHEACTN FIELD=FLAG5     GRPACC
ACTN11   ICHEACTN FIELD=NOTRMUAC  TERMINAL UNIVERSAL ACCESS
ACTN12   ICHEACTN FIELD=GRPAUDIT  AUDITOR
         EJECT
         PRINT NOGEN
WORKAREA DSECT
         SAVEAREA DSECT=NO
WORKD    DS    D                  DOUBLEWORD WORK AREA FOR ARITHMETIC
LINE     DS    CL133              DETAIL PRINT LINE
         DS    0D
RACREC   DS    CL46               DETAIL RAC RECORD
         ORG   RACREC
SMFID    DS    CL4                SMFID FROM SMCASID
PROFLEN  DS    B                  CONNECT NAME LENGTH
RACPROF  DS    CL17               CONNECT NAME
AUTHDATE DS    CL3                CREATE DATE
AUTHOR   DS    CL8                OWNER
LJDATE   DS    CL3                LAST ACCESS DATE
UACC     DS    B                  GROUP UNIVERSAL ACCESS
INITCNT  DS    CL2                RACINIT COUNT
FLAG1    DS    B                  ADSP
FLAG2    DS    B                  SPECIAL
FLAG3    DS    B                  OPERATIONS
FLAG4    DS    B                  REVOKED
FLAG5    DS    B                  GRPACC
NOTRMUAC DS    B                  TERMINAL UNIVERSAL ACCESS
GRPAUDIT DS    B                  AUDITOR
WORKSIZE EQU   *-WORKAREA
         EJECT
         CVT DSECT=YES
         IHAASCB
         IHAASXB
         IHAACEE
         IEESMCA ,
         END
