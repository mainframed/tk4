DSAEXIT  TITLE 'USER EXIT - DATA SET ACCESS CONTROL - RACF'
*---------------------------------------------------------------------*
*      ROSCOE - USER EXIT DATA ACCESS CONTROL                         *
*                                                                     *
***--->      USE SYS1.AMODGEN IN THE ASSEMBLY                         *
*                                                                     *
*   FUNCTION:                                                         *
*            THIS IS THE DATA SET ACCESS USER EXIT.                   *
*            IT EMPLOYS R.A.C.F. FOR ACCESS CONTROL.                  *
*            COMMUNICATION WITH ROSCOE IS VIA THE                     *
*            PARAMETER BLOCK 'UXDSECT'.                               *
*            THERE ARE VARIOUS TYPES OF CALLS, BUT THIS MODULE        *
*            DIFFERENTIATES ONLY BETWEEN THE FOLLOWING:               *
*                                                                     *
*            OPEN INPUT                                               *
*            OPEN UPDATE                                              *
*            SPECIAL DATA-SET FUNCTION (EG. SCRATCH, RENAME)          *
*                                                                     *
*                                                                     *
*   INPUT SPECIFICATIONS:                                             *
*                                                                     *
*         R1 -> PARAMETER LIST (UXDSECT)                              *
*                                                                     *
*         ADDITIONAL INPUT INFORMATION -                              *
*           DATA SET NAME                                             *
*           VOLUME(S)                                                 *
*           FUNCTION                                                  *
*           CHECK UXDSECT EXIT=DSA FOR MORE INFORMATION               *
*                                                                     *
*   OUTPUT SPECIFICATIONS:                                            *
*                                                                     *
*         R15 - RETURN CODE                                           *
*                0 - 0   OK                                           *
*                4 - 4   LOOK AT R0 FOR FUTHER INFORMATION.           *
*                                                                     *
*         R0 - EXTENDED RETURN CODE                                   *
*                0 - 0 OK                                             *
*                4 - 4 ACCESS NOT AUTHORIZED                          *
*                                                                     *
*                                                                     *
*         ADDITIONAL OUTPUT INFORMATION -                             *
*                                                                     *
*   INTERNAL REGISTER USAGE:                                          *
*                                                                     *
*                 R0 -                                                *
*                 R1 -                                                *
*                 R2 -                                                *
*                 R3 -                                                *
*                 R4 -                                                *
*                 R5 -                                                *
*                 R6 -                                                *
*                 R7 - PCB                                            *
*                 R8 - UXDSECT                                        *
*                 R9 - WORKAREA                                       *
*                 R10 -                                               *
*                 R11 -                                               *
*                 R12 - BASE                                          *
*                 R13 -                                               *
*                 R14 -                                               *
*                 R15 -                                               *
*                                                                     *
*         ADDITIONAL REGISTER HANDLING INFORMATION -                  *
*                                                                     *
*   MACROS:                                                           *
*                                                                     *
*                    UXDSECT                                          *
*                                                                     *
*       ATTRIBUTES:                                                   *
*                  REENTRANT                                          *
*                                                                     *
*                                                                     *
*       PREPARED BY:                 TACHER/BRANDES 04/13/82          *
*                                                                     *
*       MODIFIED:                                                     *
*                                                                     *
*   07/08/86   S. SABEL   CHANGED AUTHORITY REQUESTED FOR MEMBER      *
*                         RENAME AND SCRATCH FROM ALTER TO UPDATE.    *
*                                                                     *
*---------------------------------------------------------------------*
         EJECT
DSAEXIT  CSECT
         ROSID DSAEXIT
         STM   R14,R12,12(R13)   SAVE CALLER'S REGISTERS
         LR    R12,R15           SET UP BASE REG. FOR DSAEXIT
         USING DSAEXIT,R12
         L     R8,0(R1)          POINTER TO UXDSECT
         USING UXDSECT,R8
         LA    R2,WORKLEN        SIZE OF WORK AREA
         SPACE 1
         GETMAIN R,                                                    X
               LV=(R2)
         SPACE 1
         LR    R9,R1             ADDRESS OF ALLOCATED STORAGE
         USING WORKAREA,R9
         L     R4,UXDSADSN       ADDRESS OF DSN
         L     R3,UXDSAVOL       ADDRESS OF FIRST VOL
         LTR   R3,R3             VOLUME NOT SUPPLIED?
         BZ    DSAS10
         CLC   0(6,R3),=C'      ' VOLUME NOT SUPPLIED? **BCWA
****     CLC   0(R3),=C'      '  VOLUME NOT SUPPLIED?
         BNE   DSAS10            BYPASS CLEAR
         SLR   R3,R3             CLEAR VOLUME POINTER
         EJECT
DSAS10   DS    0H
         L     R2,UXCODE         A(CALL CODE)
         L     R2,0(R2)          CALL CODE
         SLL   R2,3              X 8 TO MAKE BRANCH TABLE
         B     *+4(R2)           SET UP APPPROPRIATE ATTR PARAMETER
         NOP   *                 00 - UNUSED
         NOP   *
         LA    R5,X'80'          01 - ALLOCATE - SPECIAL (DATASET)
         B     DSAS20            GO ISSUE RACHECK
         LA    R5,X'02'          02 - OPEN INPUT (DATASET)
         B     DSAS20            GO ISSUE RACHECK
         LA    R5,X'04'          03 - OPEN OUTPUT (DATASET)
         B     DSAS20            GO ISSUE RACHECK
         LA    R5,X'04'          04 - OPEN UPDATE (DATASET)
         B     DSAS20            GO ISSUE RACHECK
         LA    R5,X'80'          05 - SCRATCH - SPECIAL (DATASET)
         B     DSAS20            GO ISSUE RACHECK
         LA    R5,X'80'          06 - RENAME - SPECIAL (DATASET)
         B     DSAS20            GO ISSUE RACHECK
         NOP   *                 07 - UNUSED
         NOP   *
         NOP   *                 08 - UNUSED
         NOP   *
         NOP   *                 09 - UNUSED
         NOP   *
         NOP   *                 10 - UNUSED
         NOP   *
         NOP   *                 11 - UNUSED
         NOP   *
         LA    R5,X'02'          12 - OPEN INPUT (MEMBER)
         B     DSAS20            GO ISSUE RACHECK
         LA    R5,X'04'          13 - OPEN OUTPUT (MEMBER)
         B     DSAS20            GO ISSUE RACHECK
         LA    R5,X'04'          14 - OPEN UPDATE (MEMBER)
         B     DSAS20            GO ISSUE RACHECK
         LA    R5,X'04'          15 - SCRATCH (MEMBER)
         B     DSAS20            GO ISSUE RACHECK
         LA    R5,X'04'          16 - RENAME (MEMBER)
         B     DSAS20            GO ISSUE RACHECK
         B     DSAS50            17 - FIND - OK FOR ANYONE
         NOP   *                 PAD
         B     DSAS50            18 - LISTDSCB - OK FOR ANYONE
         NOP   *                 PAD
         LA    R5,X'80'          19 - CATALOG - SPECIAL
         B     DSAS20            GO ISSUE RACHECK
         LA    R5,X'80'          20 - UNCATALOG - SPECIAL
         B     DSASCAM           GO ISSUE A CAMLIST THEN RACHECK
         B     DSAS50            21 - BUILD INDEX - OK FOR ANYONE
         NOP   *                 PAD
         B     DSAS50            22 - DELETE INDEX - OK FOR ANYONE
         NOP   *                 PAD
         B     DSAS50            23 - CONNECT - OK FOR ANYONE
         NOP   *                 PAD
         B     DSAS50            24 - RELEASE - OK FOR ANYONE
         NOP   *                 PAD
         LA    R5,X'80'          25 - BUILD GDG - SPECIAL
         B     DSAS20            GO ISSUE RACHECK
         LA    R5,X'80'          26 - DELETE GDG - SPECIAL
         B     DSAS20            . . .
         EJECT
*---------------------------------------------------------------------*
*        CHECK FOR VOL=SER IF NOT PRESENT ISSUE A LOCATE              *
*---------------------------------------------------------------------*
DSASCAM  DS    0H
         LTR   R3,R3             CHECK FOR ZEROES
         BNZ   DSAS20            NO
         MVC   DCAMLST(LCAMLSTL),LCAMLST MOVE CAMLIST TEMPLATE
         ST    R4,DCAMDSNA       INSERT DSN ADDRESS IN CAMLIST
         LA    R1,DVCB           R1 = ADDRESS VOLUME CONTROL BLOCK
         ST    R1,DCAMVCBA       INSERT VCB ADDRESS IN CAMLIST
         LA    R1,DCAMLST        R1 --> PARMLIST
         LOCATE (R1)             . . .
         LA    R3,DVCBVOL        R3 --> VOLUME
         LTR   R15,R15           CHECK RETURN CODE
         BZ    DSAS20            OK
         L     R2,UXMSGADR       ADDRESS OF MESSAGE AREA
         MVC   0(MESSLEN2+4,R2),MESS20 MOVE IN PRIVATE MESSAGE
         L     R1,UXRETFLG       GET RETURN CODE ADDRESS
         OI    0(R1),PCBUXLMS    SET FOR MESSEGE
         LA    R15,8             INSURE BAD RETURN STATUS
         B     DSAS35            SET MESSAGE, FREE AREA AND EXIT
         SPACE 3
DSAS20   DS    0H
         L     R7,UXSCBADR       SCB ADDRESS
         USING SCB,R7
         L     R6,SCBACFB        ACEE ADDRESS POINTER
         LTR   R6,R6             ACEE PRESENT?
         BNZ   DSAS30            OK
         LA    R15,8             NO,SET BAD RETURN STATUS
         B     DSAS40            AND FREE AREA
DSAS30   DS    0H
         LA    R1,RACEXEC        EXECUTE FORM WORK AREA
         MVC   0(RACHLEN,R1),RACHECKL MOVE IN RACHECK SKELETON
         SPACE 3
         RACHECK ENTITY=((R4)),                                        X
               VOLSER=(R3),                                            X
               ATTR=(R5),                                              X
               ACEE=(R6),                                              X
               MF=(E,(1))
         DROP  R7
         SPACE 3
         CH    R15,=H'4'         CHECK FOR ERROR WITH MESSAGE
         BNH   DSAS40            OK
DSAS35   DS    0H
         L     R2,UXMSGADR       ADDRESS OF MESSAGE AREA
         MVC   0(MESSLEN+4,R2),MESS MOVE IN PRIVATE MESSAGE
         L     R1,UXRETFLG       GET RETURN CODE ADDRESS
         OI    0(R1),PCBUXLMS    SET FOR MESSEGE
DSAS40   DS    0H
         LR    R3,R15            SAVE RETURN CODE FROM RACF
         LA    R2,WORKLEN        GMB 010583 RF52-015
         FREEMAIN R,                                                   X
               LV=(R2),          GMB 010583 RF52-015                   X
               A=(R9)
         LR    R15,R3            RESTORE RETURN CODE FROM RACF
         CH    R15,=H'12'        CHECK FOR MAXIMUM RETURN CODE
         BH    DSASERR           ERROR
         B     *+4(R15)          INTERPRET RESPONSE CODE
         B     DSAS50            RC 00 - RACF PROTECTED - AUTHORIZED
         B     DSAS50            RC 04 - NOT RACF PROTECTED
         B     DSASERR           ERROR
         B     DSASERR           ERROR
DSAS50   DS    0H
         SLR   R15,R15           GOOD RETURN
         B     DSA$EXIT          RETURN
DSASERR  DS    0H                RETURN WITH BAD RESPONSE
         LA    R15,4             SET RETURN CODE - LOOK AT R0
         LA    R0,4              SET RETURN CODE - UNAUTHORIZED
         B     DSA$EXIT          RETURN
         TITLE 'DSAEXIT - COMMON EXIT POINT'
DSA$EXIT DS    0H
         STM   R15,R0,16(R13)    OVERWRITE R15 & R0 IN SAVE AREA
         LM    R14,R12,12(R13)   RESTORE CALLER'S REGISTERS
         BR    R14               RETURN
         TITLE 'DSAEXIT: DATA SET ACCESS EXIT - RACF - DATA AREAS'
         PRINT GEN
RACHECKL RACHECK CLASS='DATASET',MF=L
RACHLEN  EQU   *-RACHECKL
*---------------------------------------------------------------------*
*        MESSAGES FROM DSAEXIT                                        *
*---------------------------------------------------------------------*
MESS     DC    H'46'
         DC    H'0'
MESSAGE  DC    C'RACF - INSUFFICIENT ACCESS AUTHORITY: REJECTED'
MESSLEN  EQU   *-MESSAGE
MESS20   DC    H'46'
         DC    H'0'
MESSAGE2 DC    C'RACF - DATA SET NOT IN CATALOG: REJECTED      '
MESSLEN2 EQU   *-MESSAGE2
         SPACE 3
*---------------------------------------------------------------------*
*        CAMLIST WORKAREA                                             *
*---------------------------------------------------------------------*
LCAMLST  CAMLST NAME,LDSN,,LVCB
LDSN     DS    0C
LVCB     DS    0C
LCAMLSTL EQU   *-LCAMLST
*---------------------------------------------------------------------*
*        PATCH AREA                                                   *
*---------------------------------------------------------------------*
         PRINT DATA
PATCH    DC    36S(*)
         PRINT NODATA
         REGS
WORKAREA DSECT
RACEXEC  DS    XL(RACHLEN)
SAVERESP DS    F
*---------------------------------------------------------------------*
*        LOCATE WORKAREA                                              *
*---------------------------------------------------------------------*
DCAMLST  DS    (LCAMLSTL)X
DCAMDSNA EQU   DCAMLST+4         ADDRESS OF DSNAME
DCAMVCBA EQU   DCAMLST+12        ADDRESS OF VOLUME CONTROL BLOCK (DVCB)
DVCB     DS    0D,280C           VOLU CONTROL BLOCK
DVCBVOL  EQU   DVCB+6            FIRST VOLUME SERIAL NUMBER
         SPACE 3
WORKLEN  EQU   *-WORKAREA
         TITLE 'S C B '
         SCB
         TITLE 'P C B '
         PCB
         TITLE 'UXDSECT'
         UXDSECT EXIT=DSA
         ROPGMEND
