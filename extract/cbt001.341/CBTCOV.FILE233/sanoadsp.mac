*---------------------------------------------------------------------*
*   PROGRAM:       SANOADSP                                           *
*                                                                     *
*   AUTHOR:                                                           *
*                                                                     *
*   DATE:          02/13/87                                           *
*                                                                     *
*   FUNCTION:      FLIPS THE 'ADSP' ATTRIBUTE OFF ON ALL              *
*                  USER PROFILES.                                     *
*                                                                     *
*   SPECIAL NOTES: THIS MODULE MUST RESIDE IN APF AUTHORIZED LIBRARY  *
*                  SINCE IT ISSUES RACF MANAGER REQUESTS.             *
*                                                                     *
*                  THIS PROGRAM SHOULD BE PROTECTED BY RACF PROGRAM   *
*                  PROTECTION LIMITED TO SELECT USERS DUE TO THE      *
*                  NATURE OF IT'S FUNCTION.                           *
*   CHANGES:                                                          *
*---------------------------------------------------------------------*
NOADSP   CSECT
         STM   14,12,12(13)       SAVE CALLERS REGISTERS
         BALR  12,0               SET CURRENT ADDRESS
         USING *,12               ESTABLISH ADDRESSABILITY
         ST    13,SAVE+4          CHAIN SAVE AREA
         LR    11,13              POINT TO CALLERS SAVE AREA
         LA    13,SAVE            POINT TO OUR SAVE AREA
         ST    13,8(11)           CHAIN SAVE AREA
         OPEN  (SYSPRINT,(OUTPUT))
         SPACE
         L     2,16               POINT TO CVT
         USING CVT,2
         L     2,CVTTCBP          POINT TO TCB AND ASCB POINTERS
         L     3,12(2)            POINT TO CURRENT ASCB
         USING ASCB,3
         L     3,ASCBASXB         POINT TO ASXB
         USING ASXB,3
         L     3,ASXBSENV         POINT TO ACEE
         USING ACEE,3
         LTR   3,3                IS THERE AN ACEE?
         BNZ   ACEEOK             ..YES, CONTINUE
         MVC   LINE(L'MSG1),MSG1  ..NO, SETUP ERROR MESSAGE
         PUT   SYSPRINT,LINE      WRITE ERROR MESSGAE
         B     RACABND            AND TERMINATE THE PROGRAM
         EJECT
ACEEOK   TM    ACEEFLG1,ACEESPEC  DO WE HAVE RACF SPECIAL AUTHORITY?
         BO    APFAUTH            ..YES, CONTINUE
         MVC   LINE(L'MSG2),MSG2  ..NO, SETUP ERROR MESSAGE
         PUT   SYSPRINT,LINE      WRITE ERROR MESSGAE
         B     RACABND            AND TERMINATE THE PROGRAM
         EJECT
APFAUTH  L     3,4(2)             TCB POINTER
         USING TCB,3
         L     3,TCBJSCB          GET JSCB
         USING IEZJSCB,3
         XI    JSCBSWT1,JSCBPASS   FLIP THE APF AUTHORIZATION STATE
         XI    JSCBOPTS,JSCBAUTH   FLIP THE APF AUTHORIZATION STATE
         SPACE
         RACSTAT
         LTR   15,15              IS RACF OPERATING?
         BZ    RACFOK             ..YES, CONTINUE
         MVC   LINE(L'MSG3),MSG3  ..NO, SETUP ERROR MESSAGE
         PUT   SYSPRINT,LINE      WRITE ERROR MESSGAE
         B     RACABND            AND TERMINATE THE PROGRAM
         SPACE
RACFOK   DS    0H
         LA    2,RBA              POINT TO TARGET FIELD
         L     3,=A(WKAREAL)      SET FIELD LENGTH
         XR    4,4                SET SOURCE FIELD TO NULL
         XR    5,5                SET SOURCE LENGTH AND PAD TO X'00'
         MVCL  2,4                CLEAR THE WORK AREA
         MVI   ENTRYNAM,X'00'
         MVC   ENTRYNAM+1(7),ENTRYNAM SET INITIAL PROFILE NAME
         MVI   ENTRY,X'08'        SET INITIAL PROFILE LENGTH
LOOP     ICHEINTY NEXT,                                                X
               TYPE='USR',ENTRY=ENTRY,WKAREA=WKAREA
         LTR   15,15              WAS LOCATE SUCCESSFUL?
         BZ    MODIT              ..YES, CONTINUE
         C     15,=F'12'          HAVE WE CHECKED ALL USERS?
         BE    ALLDONE            ..YES, COMPLETED
         B     RACABND
MODIT    ICHEINTY ALTER,                                               X
               TYPE='USR',ENTRY=ENTRY,                                 X
               TESTS=(TEST1),                                          X
               ACTIONS=(ACTION1)
         LTR   15,15              WAS ALTER SUCCESSFUL?
         BZ    DIDMOD             ..YES, CONTINUE
         C     15,=F'52'          DID TEST FAIL?
         BE    TSTFAIL            ..YES, COMPLETED
         B     RACABND
DIDMOD   MVI   LINE,X'40'
         MVC   LINE+1(132),LINE
         MVC   MSG5USR(8),ENTRYNAM
         MVC   LINE(L'MSG5),MSG5
         PUT   SYSPRINT,LINE
         B     LOOP
TSTFAIL  MVI   LINE,X'40'
         MVC   LINE+1(132),LINE
         MVC   MSG6USR(8),ENTRYNAM
         MVC   LINE(L'MSG6),MSG6
         PUT   SYSPRINT,LINE
         B     LOOP
ALLDONE  CLOSE (SYSPRINT)
         L     13,SAVE+4          POINT TO CALLERS SAVE AREA
         RETURN (14,12),RC=0      RETURN TO CALLER
         EJECT
RACABND  CVD   15,WORKD           CONVERT RETURN CODE TO DECIMAL
         UNPK  MSG4CD,WORKD+6(2)  MOVE TO ERROR MESSAGE
         OI    MSG4CD+1,X'F0'     MAKE LAST CHARACTER PRINTABLE
         MVI   LINE,X'40'
         MVC   LINE+1(132),LINE
         MVC   LINE(L'MSG4),MSG4  SETUP ERROR MESSAGE
         PUT   SYSPRINT,LINE      WRITE ERROR MESSGAE
         EJECT
         LTORG
SAVE     DS    18F                SAVE AREA
WORKD    DS    D                  DOUBLEWORD WORK AREA
ENTRY    DC    AL1(1)             LENGTH OF PROFILE NAME FIELD
ENTRYNAM DC    XL8'00'            PROFILE NAME
         EJECT
MSG1     DC    C'1**ERROR** - TASK ACEE NOT FOUND'
MSG2     DC    C'1**ERROR** - USER MUST HAVE SPECIAL AUTHORITY'
MSG3     DC    C'1**ERROR** - RACF IS NOT OPERATING'
MSG4     DC    C'1**ERROR** - RACF INTERNAL ERROR CODE = XX'
         ORG   *-2
MSG4CD   DS    CL2
         ORG
MSG5     DC    C' RACF USERID XXXXXXXX RESET TO NOADSP'
         ORG   *-24
MSG5USR  DS    CL8
         ORG
MSG6     DC    C' RACF USERID XXXXXXXX ALREADY SET TO NOADSP'
         ORG   *-30
MSG6USR  DS    CL8
         ORG
LINE     DC    CL133' '           DETAIL PRINT LINE
SYSPRINT DCB   DSORG=PS,DDNAME=SYSPRINT,MACRF=PM,RECFM=FA,LRECL=133,   X
               BLKSIZE=133
ACTION1  ICHEACTN FIELD=FLAG1,                                         X
               FLDATA='DEL',                                           X
               MF=L
TEST1    ICHETEST FIELD=FLAG1,                                         X
               FLDATA=(1,ADSPON),                                      X
               COND=ONES,                                              X
               MF=L
ADSPON   DC    X'80'
WKAREA   DS    0F
WKAREAS  DC    AL4(WKAREAL+4)     LENGTH OF WORKAREA
RBA      DS    CL6                RBA OF RACF RECORD
FLAGS    DS    X                  FLAGS
         DS    C                  RESERVED
DUPCOUNT DS    AL4                DUPLICATE RECORD COUNT
         DS    CL8                RESERVED
RETLEN   DS    AL4                LENGTH OF RETURNED DATA
RETDATA  DS    CL30               RETURNED DATA/NO DATA TO BE RETURNED
WKAREAE  EQU   *                  END OF WORK AREA
WKAREAL  EQU   *-RBA              CALCULATE WORK AREA LENGTH
         EJECT
         CVT DSECT=YES
         IKJTCB
         IHAASCB
         IHAASXB
         IHAACEE
         IEZJSCB
         END
