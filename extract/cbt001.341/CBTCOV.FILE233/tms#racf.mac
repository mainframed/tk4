IEFUJI   TITLE 'MVS SMF JOB INITIATION EXIT'
IEFUJI   CSECT
* FUNCTION
*
* THE PURPOSE OF THIS ROUTINE IS TO PASS USERID AND ACCOUNTING
* INFORMATION TO THE TMS ACCOUNTING ROUTINE TMSACTRT.
* THE TMS USER ACCOUNTING FIELD IN THE TMC RECORD IS 41 BYTES.
* ALL TMS ROUTINES ASSUME 41 BYTES ARE USED. THIS ROUTINE
* PLACES RACFID IN 1-6 FOLLOWED BY AN 8 BYTE FIELD IN 7-14
* OF ACCOUNTING INFO FROM THE SMFJMR FOR A TOTAL 14 BYTES USED.
*
* INPUT
*
*    REGISTER 1 POINTS TO A LIST OF 4 BYTE ADDRESSES FOR THE
*    FOLLOWING FOUR PARAMETERS:
*       1 - JOB MANAGEMENT RECORD
*       2 - 20 BYTES CONTAINING THE PROGRAMMERS NAME
*       3 - 1 BYTE INDICATING THE EFFECTIVE JOB PRIORITY
*       4 - JOB ACCOUNTING FIELDS
*
*
* OUTPUT
*
*    REGISTER 15 WILL CONTAIN  THE FOLLOWING RETURN CODE:
*       0 - CONTINUE PROCESSING THE JOB
*
* ENTRY POINTS
*
*    IEFUJI
*
* EXTERNAL REFERENCES
*
*    TMSACTRT - VIA LINK
*    NOTE: TMACTRT SHOULD BE RESIDENT IN THE LINK PACK AREA
*
* EXITS, NORMAL - RETURN TO INITIATOR WITH A RETURN CODE OF ZERO
*
* ATTRIBUTES
*    REENTRANT (REQUIRED FOR LINK PACK AREA RESIDENCY)
*    AC=1 (REQUIRED FOR ISSUANCE OF RACXTRT MACRO)
***********************************************************************
*  CHANGE HISTORY
*    CREATED 05/22/86
*
***********************************************************************
         SPACE 3
***********************************************************************
*                                                                     *
*                 REGISTER EQUATES                                    *
*                                                                     *
***********************************************************************
         SPACE 3
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         EJECT
         SAVE  (14,12),,*     STANDARD LINKAGE
         SPACE 1
         LR    R12,R15
         USING IEFUJI,R12
         LR    R4,R1          SAVE PARAMETER LIST IN R4
         SPACE 1
         GETMAIN  R,LV=SAVELNTH,SP=245   #2  SPECIFY SUBPOOL 245
         SPACE 1
         LR    R3,R13         RETAIN ADDRESS OF HIGHER SAVE AREA IN R3
         LR    R13,R1
         ST    R13,8(R3)
         ST    R3,4(R13)
         USING SAVEAREA,R13
         SPACE 1
         XC    16(4,R3),16(R3)  SET DEFAULT RETURN CODE OF ZERO
         SPACE 1
*  GO GET THE ACCOUNTING INFORMATION AND RACFID
         USING SMFPARAM,R4            DSECT OF INPUT PARAMETERS
         L     R2,SMFACCT             ADDRESS OF JOB ACCOUNTING DATA
         MVC   TMSACTNC(8),=CL8'00000000' DEFAULT TO ZEROS
         LA    R5,41                  SET MAX MOVE AT 41 BYTES
         CLI   1(R2),X'00'            IS LENGTH ZERO
         BE    USERIDM                NO, GO FOR VAR NEW MOVE
         CLI   1(R2),X'29'            TMS MAXIMIM LENGTH IS 41 BYTES
         BH    EXMVC                  IF HIGHER THEN LIMIT IT
         LA    R5,0                   ZERO R5
         IC    R5,1(R2)               INSERT LENGTH CODE
         BCTR  R5,0                   DECREMENT BY 1
EXMVC    EX    R5,MVCACT              MOVE NEW VAR ACT NUMBER
USERIDM  DS    0H
         RACXTRT TYPE=EXTRACT,SUBPOOL=245 ASK RACF FOR PROFILE OF ACEE
         LTR   R15,R15                IF R15 NOT ZERO THEN I CANT
         BNZ   RETURN                 FREEMAIN OR DO TMSACTRT LINK
         MVC   TMSUSERI,24(R1)        MOVE IN RACFID
         L     R0,0(R1)               SET SUBPOOL AND LENGTH
         FREEMAIN  R,LV=(0),A=(1)     FREEMAIN THAT AREA
*
         LA    R1,72(R13)             ADDRESS OF TMSACCT IN R1
*                                    ADDRESS OF SAVE AREA ALREADY IN 13
         LINK EP=TMSACTRT             PASS CONTROL TO TMASCTRT
         SPACE 1
RETURN   DS    0H
         LR    R1,R13
         L     R13,4(R13)
         FREEMAIN  R,LV=SAVELNTH,SP=245,A=(1)  #2  SPECIFY SUBPOOL 245
         RETURN  (14,12),T
         EJECT
         SPACE 3
MVCACT   MVC   TMSACTNC(0),2(R2)
         LTORG
SAVEAREA DSECT
         SPACE 1
         DS    18F            STANDARD SAVE AREA
         SPACE 1
         TMACCT               TMS ACCT AREA DSECT
         SPACE 1
SAVEEND  EQU   *
SAVELNTH EQU   SAVEEND-SAVEAREA
         EJECT
         SPACE 3
*        DSECT OF INPUT PARAMETER LIST POINTED TO BY REG 1
         SPACE
SMFPARAM DSECT                        INPUT PARAMETER LIST
SMFJMR   DS    A                      ADDRESS OF SMF PARAM AREA
SMFPROG  DS    A                      ADDRESS OF PROGRAMMER NAME
SMFPRI   DS    A                      ADDRESS OF JOB SELECTION PRIORITY
SMFACCT  DS    A                      ADDRESS OF JOB ACCOUNTING DATA
*                                       FORMAT OF JOB ACCOUNTING DATA:
*                                         0 - NUMBER OF ACCOUNT FIELDS
*                                         1 - LENGTH OF FIRST FIELD
*                                         2 - FIRST ACCOUNT FIELD
*                                         ETC.
         END
