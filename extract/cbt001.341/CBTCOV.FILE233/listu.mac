LISTU    TITLE 'LISTU - DISPLAY A USER''S NAME AND DEFAULT GROUP'
**********************************************************************
*                                                                    *
*  PROGRAM NAME:  LISTU                                              *
*                                                                    *
*  FUNCTION:      TSO COMMAND PROCESSOR TO DISPLAY A USER'S NAME AND *
*                 DEFAULT GROUP FROM THE  RACF DATA BASE.            *
*                                                                    *
*  LINKEDIT:      NORENT,NOREUS,AC=1                                 *
*                                                                    *
*  12/19/88   SJS                                                    *
*                                                                    *
**********************************************************************
* SET UP PROGRAM ADDRESSABILITY                                      *
**********************************************************************
LISTU    CSECT
LISTU    AMODE 24
LISTU    RMODE 24
         STM   R14,R12,12(R13)         SAVE CALLERS REGISTERS
         BALR  R12,0                   SET CURRENT ADDRESS
         USING *,R12                   ESTABLISH ADDRESSABILITY
         ST    R13,SAVE+4              CHAIN SAVE AREA
         LR    R11,R13                 POINT TO CALLERS SAVE AREA
         LA    R13,SAVE                POINT TO OUR SAVE AREA
         ST    R13,8(R11)              CHAIN SAVE AREA
**********************************************************************
* MAP COMMAND PROCESSOR PARM LIST                                    *
**********************************************************************
         USING CPPL,R1                 ADDRESS CPPL
         L     R2,CPPLCBUF             POINT TO COMMAND BUFFER
         LA    R3,4(R2)                POINT TO START OF INPUT TEXT
         AH    R3,2(R2)                POINT TO USERID ENTERED
         AH    R2,0(R2)                POINT TO END OF COMMAND BUFFER
         SR    R2,R3                   POINT TO PARAMETER ENTERED
**********************************************************************
* SEE IF RACF IS ACTIVE                                              *
**********************************************************************
         RACSTAT                       CHECK RACF STATUS
         LTR   R15,R15                 IS RACF UP?
         BZ    RACFOK                  YUP, KEEP GOING
         TPUT  NORACF,L'NORACF         WRITE ERROR MESSGAE
         B     RETURN                  RETURN TO CALLER
         EJECT
**********************************************************************
* IF NO USER-ID WAS SPECIFIED, DEFAULT TO THE USER'S ID              *
**********************************************************************
RACFOK   DS    0H
         CH    R2,=H'0'                WAS A USER-ID ENTERED?
         BNE   PARMOK                  YUP, KEEP GOING
         L     R5,X'10'                POINT TO CVT
         USING CVT,R5                  CVT ADDRESSABILITY
         L     R7,CVTTCBP              FIND THE TCB POINTERS
         L     R6,12(0,R7)             LOAD CURRENT ASCB ADDRESS
         USING ASCB,R6                 ASCB ADDRESSABILITY
         L     R7,ASCBASXB             LOAD THE ASXB ADDRESS
         USING ASXB,R7                 ASXB ADDRESSABILITY
         DROP  R5,R6                   DROP BASES
         LA    R6,ASXBUSER+6           POINT TO END OF USER-ID
         LA    R8,7                    LOAD THE COUNTER
UIDLOOP  DS    0H
         CLC   =C' ',0(R6)             IS THIS BYTE A BLANK?
         BNE   UIDCONT                 IF NOT, WE HAVE USERID LENGTH
         BCTR  R6,0                    BUMP DOWN THE POINTER BY 1
         BCT   R8,UIDLOOP              BUMP DOWN THE COUNTER BY 1
UIDCONT  DS    0H
         BCTR  R8,0                    MINUS 1 MORE FOR EXECUTE
         MVC   USERID(0),ASXBUSER      EXECUTED MOVE FOR THE USERID
         EX    R8,*-6                  EXECUTE THE MOVE
         B     RACFCHK                 GO DO THE ICHEINTY
         EJECT
**********************************************************************
* GET THE USER-ID TO USE FROM THE CPPL                               *
**********************************************************************
PARMOK   BCTR  R2,0                    MINUS 1 FOR EXECUTE
         MVC   USERID(0),0(R3)         MOVE PARAMETER TO RACF PARMLIST
         EX    R2,*-6                  EXECUTED MOVE
         OC    USERID(0),=CL7' '       MAKE SURE IT'S UPPER CASE
         EX    R2,*-6                  EXECUTE THE UPPER CASING
         EJECT
**********************************************************************
* ISSUE SVC132 TO GET USER PROFILE INFO FROM RACF                    *
**********************************************************************
RACFCHK  ICHEINTY LOCATE,              LOCATE OUR USER'S PROFILE       X
               TYPE='USR',ENTRY=ENTRY,WKAREA=WKAREA,                   X
               ACTIONS=(ACTION1,ACTION2)
         LTR   R15,R15                 WAS THE LOCATE SUCCESSFUL?
         BZ    FOUND                   YUP, KEEP GOING
         C     R15,=F'12'              WAS THE USER-ID FOUND?
         BE    NOTFOUND                IF NOT, ISSUE ERROR MESSAGE
**********************************************************************
* IF THE RETURN CODE IS NOT ZERO OR 12, ISSUE AN ERROR MESSAGE       *
**********************************************************************
         CVD   R15,WORKD               CONVERT THE RETURN CODE TO DEC.
         UNPK  ERRORCD,WORKD+6(2)      MOVE IT INTO THE ERROR MSG
         OI    ERRORCD+1,X'F0'         MAKE LAST CHARACTER PRINTABLE
         TPUT  RACFERR,L'RACFERR       ISSUE THE ERROR MESSAGE
         B     RETURN                  RETURN TO CALLER
         EJECT
**********************************************************************
* USER-ID WAS FOUND, FORMAT AND DISPLAY A LINE OF DATA.              *
**********************************************************************
FOUND    DS    0H
         MVC   OUTLINE+5(8),USERID     MOVE USERID INTO OUTPUT MSG
         MVC   OUTLINE+19(20),NAME     MOVE USER NAME INTO OUTPUT MSG
         MVC   OUTLINE+54(8),GROUP     MOVE DFLT GROUP INTO OUTPUT MSG
         TPUT  OUTLINE,L'OUTLINE       WRITE MESSAGE
         B     RETURN                  RETURN TO CALLER
**********************************************************************
* USER-ID WAS NOT FOUND, ISSUE AN ERROR MESSAGE.                     *
**********************************************************************
NOTFOUND TPUT  NOTFND,L'NOTFND         ISSUE NOT FOUND MESSAGE
         B     RETURN                  RETURN TO CALLER
         EJECT
**********************************************************************
* RESTORE REGISTERS AND RETURN TO THE CALLER                         *
**********************************************************************
RETURN   L     R13,SAVE+4              LOAD CALLER'S SAVE AREA
         RETURN (14,12),RC=0           RETURN TO CALLER
         EJECT
**********************************************************************
* WORKING STORAGE AND DATA AREAS                                     *
**********************************************************************
SAVE     DS    18F                     MY SAVE AREA
WORKD    DS    D                       DOUBLEWORD WORK AREA
ENTRY    DC    AL1(7)                  LENGTH OF USERID FIELD
USERID   DC    XL8'00'                 USERID
         DS    0F                      ALIGNMENT
WKAREA   DC    AL4(WKAREAL)            LENGTH OF WORKAREA
RBA      DS    CL6                     RBA OF RACF RECORD
FLAGS    DS    X                       FLAGS
         DS    C                       RESERVED
DUPCOUNT DS    AL4                     DUPLICATE RECORD COUNT
         DS    CL8                     RESERVED
RETLEN   DS    AL4                     LENGTH OF RETURNED DATA
NAMEL    DS    AL2                     LENGTH OF USER NAME
NAME     DS    CL20                    USER NAME FROM USER PROFILE
GRPL     DS    AL2                     LENGTH OF GROUP-ID
GROUP    DS    CL8                     DFLT GROUP-ID FROM USER PROFILE
WKAREAL  EQU   *-WKAREA                WORK AREA LENGTH
OUTLINE  DC    CL62'USER=XXXXXXXX NAME=XXXXXXXXXXXXXXXXXXXX DEFAULT-GRO+
               UP=XXXXXXXX'
NORACF   DC    C'RACF IS NOT CURRENTLY ACTIVE'
NOTFND   DC    C'USERID NOT FOUND'
RACFERR  DC    C'RACF ERROR, RETURN CODE FROM ICHEINTY MACRO = XX'
         ORG   RACFERR+46
ERRORCD  DS    CL2                     ICHEINTY RETURN CODE
         ORG
         EJECT
ACTION1  ICHEACTN FIELD=PGMRNAME       TELL IT WE WANT PGMRNAME
ACTION2  ICHEACTN FIELD=DFLTGRP        TELL IT WE WANT DEFAULT GROUP
         EJECT
         PRINT NOGEN
         IKJCPPL                       COMMAND PROCESSOR PARM LIST
         IHAASCB                       ADDRESS SPACE CONTROL BLOCK
         IHAASXB                       ADDRESS SPACE EXTENSION BLOCK
         CVT   DSECT=YES,LIST=NO       COMMUNICATIONS VECTOR TABLE
**********************************************************************
* REGISTER EQUATES                                                   *
**********************************************************************
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END
