         MACRO
         $ATTR  &ATTR
.*
.*  FUNCTION : VALIDATE 3270 ATTRIBUTE OPTIONS AND GENERATE
.*             APPROPRIATE HEX CODE.
.*
.*  METHOD   : USE BIT ARRAY TO FLAG SELECTED OPTIONS THEN GENERATE
.*             APPROPRIATE HEX CODE.
.*
.*  INPUT    : &ATTR - ATTRIBUTE OPTIONS. IT MUST BE ONE OF
.*                    (PROT,NUM,MDT,HI,NONDISP,SKIP)
.*                    (UNPROT,ALPHA,NOMDT)
.*
.*  OUTPUT   : &RC      =  0 => ATTRIBUTE OPTIONS OK
.*                        ^0 => ATTRIBUTE OPTIONS ARE NOT OK.
.*           : &ATTRIB  = HEX CODE FOR ATTRIBUTE OPTIONS SELECTED.
.*
.*  AUTHOR   : ED PITKIN, DISTRIBUTED SERVICES
.*
.*  DATE     : SEPT 19/80
.*
         GBLC  &TAB3270(64),&ATTRIB
         GBLA  &RC
         LCLA  &I1,&BIT(8)
&I1      SETA  1
.LOOP    ANOP
.*   PROCESS PROT ATTRIBUTE
         AIF ('&ATTR(&I1)' NE 'PROT').UNPROT
&BIT(3)  SETA  1
         AGO    .NXTONE
.*   PROCESS UNPROT ATTRIBUTE
.UNPROT  AIF ('&ATTR(&I1)' NE 'UNPROT').NUM
&BIT(3)  SETA  0
         AGO    .NXTONE
.*   PROCESS NUM ATTRIBUTE
.NUM     AIF ('&ATTR(&I1)' NE 'NUM').ALPHA
&BIT(4)  SETA  1
         AGO    .NXTONE
.*   PROCESS ALPHA ATTRIBUTE
.ALPHA   AIF ('&ATTR(&I1)' NE 'ALPHA').SKIP
&BIT(4)  SETA  0
         AGO    .NXTONE
.*   PROCESS SKIP ATTRIBUTE
.SKIP    AIF ('&ATTR(&I1)' NE 'SKIP').NONDISP
&BIT(3)  SETA  1
&BIT(4)  SETA  1
         AGO    .NXTONE
.*   PROCESS NONDISP ATTRIBUTE
.NONDISP AIF ('&ATTR(&I1)' NE 'NONDISP').HI
&BIT(5)  SETA  1
&BIT(6)  SETA  1
         AGO    .NXTONE
.*   PROCESS HI ATTRIBUTE
.HI      AIF ('&ATTR(&I1)' NE 'HI').MDT
&BIT(5)  SETA  1
         AGO    .NXTONE
.*   PROCESS MDT ATTRIBUTE
.MDT     AIF ('&ATTR(&I1)' NE 'MDT').NOMDT
&BIT(8)  SETA  1
         AGO    .NXTONE
.*   PROCESS NOMDT ATTRIBUTE
.NOMDT   AIF ('&ATTR(&I1)' NE 'NOMDT').ERROR
&BIT(8)  SETA  0
         AGO    .NXTONE
.ERROR   MNOTE 8,'&ATTR(&I1) IS AN INVALID ATTRIBUTE OPTION'
.NXTONE  ANOP
&I1      SETA  &I1+1
         AIF (&I1 LE N'&ATTR).LOOP
.*   CALCULATE VALUE OF OPTION BYTE
&I1      SETA &BIT(8)+&BIT(6)*4+&BIT(5)*8+&BIT(4)*16+&BIT(3)*32+1
         AIF (&I1 LE 64).GETCHAR
         MNOTE 8,'&I1 IS GREATE THAN 64'
&RC      SETA  4
         MEXIT
.*   LOOK UP CHAR FOR CALCULATED OPTION BYTE.
.GETCHAR ANOP
&ATTRIB  SETC  '&TAB3270(&I1)'
&RC      SETA  0
         MEND
         MACRO
&LABEL   $CMD  &GAPPL,&VAPPL,&ROUT,&CMDTEXT
.*
.*  FUNCTION : GENERATE AN ENTRY FOR NETSOL'S COMMAND LOOK-UP TABLE.
.*
.*  INPUT    : &GAPPL   - GENERALIZED APPLICATION NAME.
.*             &VAPPL   - ASSOCIATED VTAM APPLICATION NAME.
.*             &ROUT    - ROUTINE TO PROCESS THE COMMAND.
.*             &CMDTEXT - COMMAND ENTERED FROM TERMINAL.
.*
.*  OUTPUT   : AN ENTRY IN NETSOL'S COMMAND ENTRY TABLE IS GENERATED.
.*
.*  AUTHOR   : ED PITKIN, DISTRIBUTED SERVICES
.*
.*  DATE     : APR  15/81
.*
         LCLA  &L
&L       SETA  K'&CMDTEXT-2
&LABEL   DC    CL8'&GAPPL',CL8'&VAPPL',CL8&CMDTEXT,AL4(&ROUT),AL2(&L,0)
         MEND
         MACRO
         $COL   &COL
.*
.*  FUNCTION : VALIDATE COLOUR OPTION AND SELECT
.*             APPROPRIATE HEX CODE.
.*
.*  INPUT    : &COL - COLOUR OPTION. IT MUST BE ONE OF
.*                  (DEFAULT,BLUE,RED,PINK,GREEN,TURQUOIS,YELLOW,WHITE)
.*                    ONLY 1ST 3 CHARS ARE CHECKED
.*
.*  OUTPUT   : &RC      =  0 => COLOUR OPTION IS OK.
.*                        ^0 => COLOUR OPTION IS NOT OK.
.*           : &ATTRIB  = HEX CODE FOR COLOUR OPTION SELECTED.
.*
.*  AUTHOR   : ED PITKIN, DISTRIBUTED SERVICES
.*
.*  DATE     : SEPT 19/80
.*
         GBLC  &ATTRIB
         GBLA  &RC
         AIF ('&COL'(1,3) NE 'DEF').BLUE
.*   SET HEX CODE FOR DEFAULT
&ATTRIB  SETC  '00'
         AGO    .COLFND
.BLUE    AIF ('&COL'(1,3) NE 'BLU').RED
.*   SET HEX CODE FOR BLUE
&ATTRIB  SETC  'F1'
         AGO    .COLFND
.RED     AIF ('&COL'(1,3) NE 'RED').PINK
.*   SET HEX CODE FOR RED
&ATTRIB  SETC  'F2'
         AGO    .COLFND
.PINK    AIF ('&COL'(1,3) NE 'PIN').GREEN
.*   SET HEX CODE FOR PINK
&ATTRIB  SETC  'F3'
         AGO    .COLFND
.GREEN   AIF ('&COL'(1,3) NE 'GRE').TURQ
.*   SET HEX CODE FOR GREEN
&ATTRIB  SETC  'F4'
         AGO    .COLFND
.TURQ    AIF ('&COL'(1,3) NE 'TUR').YELLOW
.*   SET HEX CODE FOR TURQUOISE
&ATTRIB  SETC  'F5'
         AGO    .COLFND
.YELLOW  AIF ('&COL'(1,3) NE 'YEL').WHITE
.*   SET HEX CODE FOR YELLOW
&ATTRIB  SETC  'F6'
         AGO    .COLFND
.WHITE   AIF ('&COL'(1,3) NE 'WHI').ERROR
.*   SET HEX CODE FOR WHITE
&ATTRIB  SETC  'F7'
         AGO    .COLFND
.*   REJECT COLOUR OPTION
.ERROR   MNOTE 8,'&COL IS AN INVALID COLOUR.'
&RC      SETA  4
         MEXIT
.*   ACCEPT COLOUR OPTION
.COLFND  ANOP
&RC      SETA  0
         MEND
         MACRO -- $FCB -- GENERATE 3211 FORMS CONTROL BUFFER
&LABEL   $FCB  &LPINCH,           LINES PER INCH                       *
               &LPPAGE,           LINES PER PAGE                       *
               &INDEX=,           PRINT POSITION INDEXING              *
               &DEFAULT=NO        DEFAULT FCB IMAGE
.*********************************************************************.
.*                                                                   *.
.*            $FCB - GENERATE 3211 FORMS CONTROL BUFFER              *.
.*                                                                   *.
.*  THE $FCB MACRO INSTRUCTION CAUSES THE CREATION OF A CSECT        *.
.*  CONTAINING A FORMS CONTROL BUFFER IMAGE FOR A 3211 PRINTER.      *.
.*  THE MACRO MAY BE USED IN A STAND ALONE ASSEMBLY AND LINK EDIT    *.
.*  TO PRODUCE AN FCB IMAGE LOAD MODULE IN SYS1.IMAGELIB.            *.
.*                                                                   *.
.*  THIS MACRO WAS DEVELOPED USING THE $FCB MACRO DISTRIBUTED WITH   *.
.*  HASP II VERSION 4.  THE DISTRIBUTED MACRO WAS WRITTEN TO         *.
.*  GENERATE IN-LINE CODE WITHIN HASP.  FURTHER, IT DID NOT SUPPORT  *.
.*  LEFT INDEXING.  (FOR MORE INFORMATION ABOUT THAT MACRO, REFER    *.
.*  TO "OS/VS2 HASP II VERSION 4 LOGIC", IBM FORM NUMBER GY27-7255.) *.
.*                                                                   *.
.*  USAGE                                                            *.
.*                                                                   *.
.*     FCB2???? $FCB  I,P,C-L,C-L,...,INDEX=X,DEFAULT=YES            *.
.*                                                                   *.
.*     ???? - THE 4 CHARACTER IMAGE NAME WHICH WILL BE CODED IN JCL  *.
.*            AND OPERATOR COMMANDS.                                 *.
.*                                                                   *.
.*     I    - THE NUMBER OF LINES PER INCH.  THIS VALUE MUST BE      *.
.*            CODED AS EITHER 6 OR 8.                                *.
.*                                                                   *.
.*     P    - THE NUMBER OF LINES PER PAGE.  THIS CORRESPONDS TO THE *.
.*            NUMBER OF LINES PER FOLD ON THE FORM.  THIS MEANS THAT *.
.*            FOR A 22 LINE PAGE WHERE THE FORMS ARE FOLDED ONCE     *.
.*            IN THREE PAGES, THIS PARAMETER WOULD BE CODED AS 66.   *.
.*            THE CARRIAGE CHANNEL PUNCHES WOULD THEN BE REPEATED    *.
.*            THREE TIMES IN THE FCB IMAGE - ONCE FOR EACH PAGE.     *.
.*            THIS VALUE MUST RANGE BETWEEN 2 AND 180.               *.
.*            NOTE...CHANNEL 1 MUST BE IN LINE 1                     *.
.*                   P CAN EASILY BE CALCULATED BY MULTIPLYING       *.
.*                    (LINES/INCH) * (PAGE LTH)                      *.
.*                  EG-  6*11=66                                     *.
.*                                                                   *.
.*     C-L  - CARRIAGE PUNCH - LINE NUMBER.  THIS PARAMETER SHOULD   *.
.*            BE SPECIFIED ONCE FOR EVERY CHANNEL PUNCH REQUIRED.    *.
.*            THE PARAMETER IS CODED BY SPECIFYING THE CHANNEL       *.
.*            NUMBER FOLLOWED BY A HYPHEN (-) AND THEN THE LINE      *.
.*            NUMBER.  THE CHANNEL NUMBER MUST RANGE BETWEEN 1 AND   *.
.*            12.  CHANNEL 1 MUST BE SPECIFIED AT LEAST ONCE.  THE   *.
.*            LINE NUMBER MUST BE A VALUE BETWEEN 1 AND THE NUMBER   *.
.*            OF LINES PER PAGE SPECIFIED.                           *.
.*                                                                   *.
.*     X    - THE INDEX AMOUNT.  THE KEYWORD PARAMETER "INDEX=X"     *.
.*            CAN BE OMITTED.  IF IT IS, THEN NO INDEXING WILL BE    *.
.*            PERFORMED.  IF SPECIFIED, X CAN BE A VALUE FROM 1 TO   *.
.*            31 INCLUSIVE FOR RIGHT INDEXING; OR -1 TO -31 FOR      *.
.*            LEFT INDEXING.  RIGHT INDEXING MEANS THAT X IS THE     *.
.*            NUMBER OF THE PRINTER PRINT POSITION WHERE THE FIRST   *.
.*            CHARACTER OF THE OUTPUT RECORD WILL BE PRINTED.  LEFT  *.
.*            INDEXING MEANS THAT X IS THE NUMBER OF THE DATA BYTE   *.
.*            THAT IS PRINTED IN PRINTER PRINT POSITION ONE.         *.
.*                                                                   *.
.*     YES  - INDICATES THAT THIS IS TO BE A DEFAULT IMAGE.  THE     *.
.*            KEYWORD PARAMETER "DEFAULT=YES" CAN BE OMITTED.  IF    *.
.*            IT IS OMITTED, THE IMAGE WILL NOT BE DEFINED AS A      *.
.*            DEFAULT IMAGE.  IF A DEFAULT IMAGE IS LOADED INTO THE  *.
.*            PRINTER AND A JOB DOES NOT REQUEST A SPECIFIC FCB,     *.
.*            THEN THE JOB WILL BE PRINTED USING THE FCB THAT IS     *.
.*            LOADED.  THIS IMPLIES THAT FOR ALL SPECIAL FORMS       *.
.*            FCB IMAGES THIS PARAMETER SHOULD NOT BE CODED.         *.
.*                                                                   *.
.*  COMMENTS                                                         *.
.*                                                                   *.
.*     NOTE THE SIGNIFICANCE OF THE CHANNEL 1 PUNCH.  IT IS REQUIRED *.
.*     BY THE PRINTER FOR THE CARRIAGE RESTORE FUNCTION.  FURTHER,   *.
.*     CONSIDER WHAT WOULD HAPPEN IF CHANNEL 1 IS DEFINED IN LINE 6. *.
.*     WITH HASP, AN FCB IS LOADED AFTER THE SEPARATOR PAGE IS       *.
.*     PRINTED.  AT THAT TIME, THE FORMS ARE POSITIONED AT THE FIRST *.
.*     PRINT LINE ON A PAGE.  WHEN THE LOAD OCCURS, THE PRINTER      *.
.*     ASSUMES THE FORMS ARE POSITIONED AT LINE 1.  A SKIP TO        *.
.*     CHANNEL 1 WILL CAUSE THE PRINTER TO SPACE 5 LINES.  THUS,     *.
.*     THE FIRST PRINT LINE IS NOW 5 LINES DOWN THE PAGE FROM WHERE  *.
.*     IT SHOULD BE.  ALL OUTPUT PRINTED FROM NOW UNTIL AN OPERATOR  *.
.*     WAKES UP WILL BE SKEWED BY 5 LINES.  IT IS RECOMMENDED THAT   *.
.*     CHANNEL ONE BE DEFINED AT LINE ONE FOR ALL FCBS.  THEN THIS   *.
.*     PROBLEM WILL NOT OCCUR.                                       *.
.*                                                                   *.
.*     UNSPECIFIED CHANNELS WILL BE AUTOMATICALLY INCLUDED BY THE    *.
.*     MACRO IF SUFFICIENT LINES ARE AVAILABLE TO DO SO.  THIS WILL  *.
.*     PREVENT FORMS RUNAWAY.                                        *.
.*                                                                   *.
.*  EXAMPLE                                                          *.
.*                                                                   *.
.*     //BUILD EXEC ASMFCL,PARM.LKED='LIST,NCAL,OL'                  *.
.*     //ASM.SYSIN DD *                                              *.
.*     FCB2LIN6 $FCB  6,66,1-1,2-7,3-28,12-57,12-59                  *.
.*              END                                                  *.
.*     /*                                                            *.
.*     //LKED.SYSLMOD DD DSN=SYS1.IMAGELIB,DISP=SHR                  *.
.*     //LKED.SYSIN DD *                                             *.
.*       NAME FCB2LIN6(R)                                            *.
.*     /*                                                            *.
.*                                                                   *.
.*  REFERENCES                                                       *.
.*                                                                   *.
.*     1.  IBM 3211 PRINTER, 3216 INTERCHANGEABLE TRAIN CARTRIDGE,   *.
.*         AND 3811 PRINTER CONTROL UNIT COMPONENT DESCRIPTION AND   *.
.*         OPERATOR'S GUIDE.  IBM FORM NUMBER GA24-3543.             *.
.*                                                                   *.
.*     2.  OS/VS2 SYSTEM PROGRAMMING LIBRARY: DATA MANAGEMENT.  IBM  *.
.*         FORM NUMBER GC26-3830.                                    *.
.*                                                                   *.
.*  SOURCE                                                           *.
.*                                                                   *.
.*     THIS CODE WAS WRITTEN AND DEBUGGED ON AN IBM 370/168MP UNDER  *.
.*     AN OS/VS2 RELEASE 3.7  (MVS) OPERATING SYSTEM.  WE ARE        *.
.*     RUNNING JES2 VERSION 4 WHICH HAS BEEN MODIFIED SLIGHTLY.      *.
.*                                                                   *.
.*     THIS MACRO WAS WRITTEN BY:                                    *.
.*                                                                   *.
.*            BILL FLEURY                                            *.
.*            OPERATING SYSTEMS AND HARDWARE PLANNING                *.
.*            COMPUTING AND SYSTEMS DIVISION                         *.
.*            DEPARTMENT OF GOVERNMENT SERVICES                      *.
.*            THE GOVERNMENT OF ALBERTA                              *.
.*            TERRACE BUILDING                                       *.
.*            9515 - 107 STREET                                      *.
.*            EDMONTON, ALBERTA, CANADA  T5K-2C4                     *.
.*                                                                   *.
.*                                          APRIL, 1977              *.
.*                                                                   *.
.*********************************************************************.
         LCLA  &CHANNEL           CURRENT CHANNEL NUMBER
         LCLA  &DNCTR             FOR ASSIGNING UNSPECIFIED CHANNELS
         LCLA  &INDXAMT           AMOUNT OF INDEXING
         LCLA  &LEN               NUMBER OF CHARACTERS IN OPERAND
         LCLA  &LINE(180)         FCB VALUE FOR EACH LINE ON THE FORM
         LCLA  &LINENO            CURRENT LINE NUMBER
         LCLA  &LPP               NUMBER OF LINES PER PAGE
         LCLA  &OPERAND           POSITIONAL OPERAND COUNTER
         LCLA  &POSOPS            NUMBER OF POSITIONAL OPERANDS
         LCLA  &PTR               TEMPORARY WORK VARIABLE
         LCLA  &SUM               TEMPORARY WORK VARIABLE
         LCLA  &UPCTR             FOR ASSIGNING UNSPECIFIED CHANNELS
.*
         LCLB  &CHANLST(12)       LIST OF CHANNEL PUNCHES SPECIFIED
         LCLB  &DEFBIT            IF ON MEANS THIS IS A DEFAULT IMAGE
         LCLB  &LINDEX            IF ON MEANS LEFT INDEXING
         LCLB  &LPIBIT            IF ON MEANS 8 LINES PER INCH
         LCLB  &QUITSW            IF ON WILL SUPPRESS FCB GENERATION
         LCLB  &RINDEX            IF ON MEANS RIGHT INDEXING
.*
         LCLC  &CHAR              TEMPORARY WORK VARIABLE
         LCLC  &CHAR1             TEMPORARY WORK VARIABLE
         LCLC  &FCB               FOUR CHARACTER FCB NAME
.*
.*********************************************************************.
.*                                                                   *.
.*             HAVE SUFFICIENT OPERANDS BEEN SPECIFIED?              *.
.*                                                                   *.
.*********************************************************************.
.*
         SPACE 2
&POSOPS  SETA  N'&SYSLIST         PICK UP NUMBER OF POSITIONAL OPERANDS
         AIF   (&POSOPS LT 3).ERROR1  ERROR IF THERE ARE LESS THAN 3
.*
.*********************************************************************.
.*                                                                   *.
.*                 VERIFY LINES PER INCH OPERAND                     *.
.*                                                                   *.
.*********************************************************************.
.*
.OP1     ANOP
&LPIBIT  SETB  0                  START OFF ASSUMING 6 LINES PER INCH
         AIF   ('&LPINCH' EQ '6').OP2 IF SO, THEN CHECK LINES PER PAGE
&LPIBIT  SETB  1                  WELL, THE ONLY OTHER CHOICE IS 8
         AIF   ('&LPINCH' NE '8').ERROR2  IF HE BOOBOO'ED THEN FLAG IT
.*
.*********************************************************************.
.*                                                                   *.
.*                 VERIFY LINES PER PAGE OPERAND                     *.
.*                                                                   *.
.*********************************************************************.
.*
.OP2     AIF   ('&LPPAGE' EQ '').ERROR3   IS IT A NULL STRING
         AIF   (T'&LPPAGE NE 'N').ERROR3  MUST BE A VALUE BETWEEN
&LPP     SETA  &LPPAGE                    2 AND 180 INCLUSIVE
         AIF   (&LPP LT 2 OR &LPP GT 180).ERROR3 BRANCH IF IT ISN'T
.*
.*********************************************************************.
.*                                                                   *.
.*               CHECK FOR AN INDEXING SPECIFICATION                 *.
.*                                                                   *.
.*********************************************************************.
.*
.OP3     ANOP
&RINDEX  SETB  0                  START OFF ASSUMING NO INDEXING
&LINDEX  SETB  0
         AIF   ('&INDEX' EQ '').OP4  IF NULL THEN NO INDEXING AT ALL
&CHAR    SETC  '&INDEX'           INDEX AMOUNT
&RINDEX  SETB  1                  ASSUME RIGHT INDEXING WAS SPECIFIED
         AIF   ('&CHAR'(1,1) NE '-').OP3A  BRANCH IF POSITIVE VALUE
&RINDEX  SETB  0                  OTHERWISE, HE HAS SPECIFIED LEFT
&LINDEX  SETB  1                  INDEXING
&LEN     SETA  K'&CHAR            HOW MANY CHARACTERS WERE SPECIFIED?
         AIF   (&LEN LT 2).ERROR4 NEED A "-" SIGN AND AT LEAST 1 DIGIT
&CHAR    SETC  '&CHAR'(2,&LEN-1)  STRIP OUT "-" SIGN
.OP3A    ANOP
&INDXAMT SETA  &CHAR              SET INDEXING AMOUNT
         AIF   (&INDXAMT LT 1 OR &INDXAMT GT 31).ERROR4 CHECK RANGE
.*
.*********************************************************************.
.*                                                                   *.
.*                  IS THIS A DEFAULT IMAGE?                         *.
.*                                                                   *.
.*********************************************************************.
.*
.OP4     ANOP
&DEFBIT  SETB  0                  ASSUME NOT A DEFAULT
         AIF   ('&DEFAULT' NE 'YES').OP5  BRANCH IF IT ISN'T
&DEFBIT  SETB  1                  SIGNAL THAT THIS IS A DEFAULT IMAGE
.*
.*********************************************************************.
.*                                                                   *.
.*      DECODE CHANNEL NUMBER -- LINE NUMBER SPECIFICATIONS          *.
.*                                                                   *.
.*********************************************************************.
.*
.OP5     ANOP
&OPERAND SETA  2                  FOR LOOKING AT POSITIONAL OPERANDS
.OP5A    ANOP
&OPERAND SETA  &OPERAND+1         BUMP OPERAND POINTER
&LEN     SETA  K'&SYSLIST(&OPERAND) NUMBER OF CHARACTERS IN OPERAND
         AIF   (&LEN EQ 0).ERROR5 WATCH FOR A NULL OPERAND
&PTR     SETA  0                  CHARACTER POINTER
&CHANNEL SETA  0                  FOR ACCUMULATING CHANNEL NUMBER
.*
.*
.*  VALIDATE CHANNEL NUMBER
.*
.*
.OP5B    ANOP
&PTR     SETA  &PTR+1             LOOK AT NEXT CHARACTER
&CHAR    SETC  '&SYSLIST(&OPERAND)'(&PTR,1)  FETCH IT OUT OF OPERAND
         AIF   ('&CHAR' LT '0' OR '&CHAR' GT '9').OP5C  IS IT A DIGIT?
&CHANNEL SETA  &CHANNEL*10+&CHAR  BUILD DECIMAL NUMBER
         AIF   (&PTR LT &LEN).OP5B  GO GET NEXT CHARACTER
         AGO   .ERROR6              MISSING LINE NUMBER
.OP5C    AIF   (&CHANNEL LT 1 OR &CHANNEL GT 12).ERROR7 IS IT VALID?
         AIF   ('&CHAR' NE '-').ERROR6  NEED A HYPHEN NOW
.*
.*
.*  VALIDATE LINE NUMBER
.*
.*
&LINENO  SETA  0                  FOR ACCUMULATING LINE NUMBER
.OP5D    ANOP
&PTR     SETA  &PTR+1             BUMP CHARACTER POINTER
         AIF   (&PTR GT &LEN).OP5E  COME TO END OF OPERAND?
&CHAR    SETC  '&SYSLIST(&OPERAND)'(&PTR,1)  FETCH IT OUT OF OPERAND
         AIF   ('&CHAR' LT '0' OR '&CHAR' GT '9').ERROR8 IS IT BAD?
&LINENO  SETA  &LINENO*10+&CHAR   BUILD DECIMAL NUMBER
         AGO   .OP5D              GO PICK UP NEXT CHARACTER
.OP5E    AIF   (&LINENO LT 1 OR &LINENO GT &LPP).ERROR8 CHECK RANGE
         AIF   (&LINE(&LINENO) NE 0).ERROR9 IS IT ALREADY DEFINED?
&LINE(&LINENO) SETA &CHANNEL      SET CHANNEL PUNCH
&CHANLST(&CHANNEL) SETB 1         INDICATE CHANNEL HAS BEEN PUNCHED
.*
.*********************************************************************.
.*                                                                   *.
.*                     END OF OPERAND SCAN                           *.
.*                                                                   *.
.*********************************************************************.
.*
.OP5F    AIF   (&OPERAND LT &POSOPS).OP5A GO LOOK AT NEXT OPERAND
.*
.*********************************************************************.
.*                                                                   *.
.*                      VALIDATE FCB LABEL                           *.
.*                                                                   *.
.*********************************************************************.
.*
&LEN     SETA  K'&LABEL           PICK UP NUMBER OF CHARACTERS
         AIF   (&LEN LT 5).ERROR11 CHECK MUST BE AT LEAST 5 LONG
         AIF   ('&LABEL'(1,4) NE 'FCB2').ERROR11  MUST START "FCB2"
&FCB     SETC  '&LABEL'(5,&LEN-4) FCB NAME
         AIF   (&QUITSW).ERROR10  EXIT IF QUIT SWITCH IS ON
.*
.*********************************************************************.
.*                                                                   *.
.*             INSURE THAT ALL CHANNELS ARE PUNCHED                  *.
.*                                                                   *.
.*********************************************************************.
.*
.OP6     ANOP
&PTR     SETA  1                  POSITION IN CHANNEL LIST
&SUM     SETA  0                  TOTAL NUMBER OF CHANNELS PUNCHED
.OP6AA   AIF   (&PTR GT 12).OP6A  COME TO END OF LIST?
&SUM     SETA  &SUM+&CHANLST(&PTR) HOW MANY CHANNELS HAVE BEEN PUNCHED?
&PTR     SETA  &PTR+1              BUMP POINTER
         AGO   .OP6AA              LOOK AT NEXT CHANNEL
.OP6A    AIF   (&SUM EQ 12).OP7    BRANCH IF ALL HAVE BEEN PUNCHED
         SPACE 1
         MNOTE *,'THE FOLLOWING ASSIGNMENTS OF UNSPECIFIED'
         MNOTE *,'CARRIAGE CHANNELS WILL PREVENT RUNAWAY FORMS:'
         AIF   (&CHANLST(1)).OP6B  HAS CHANNEL 1 BEEN PUNCHED?
         AIF   (&SUM GE &LPP).ERROR12 BRANCH IF IT CANNOT BE INSERTED
.OP6B    ANOP
&PTR     SETA  1                  CHANNEL NUMBER TO BE CHECKED
&UPCTR   SETA  0                  UP LINE COUNTER
&DNCTR   SETA  &LPP+1             DOWN LINE COUNTER
.OP6C    AIF   (&CHANLST(&PTR)).OP6I BRANCH IF CHANNEL HAS BEEN PUNCHED
         AIF   (&PTR EQ 9 OR &PTR EQ 12).OP6G TREAT 9 AND 12 SPECIALLY
.*
.*
.*  IF A CHANNEL 1, 2, 3, 4, 5, 6, 7, 8, 10, OR 11 PUNCH IS
.*  UNSPECIFIED, PLACE IT IN THE FIRST AVAILABLE LINE STARTING FROM
.*  THE TOP OF THE FORM.
.*
.*
.OP6D    ANOP
&UPCTR   SETA  &UPCTR+1           LOOK AT NEXT AVAILABLE LINE
         AIF   (&UPCTR GE &DNCTR).OP6H BRANCH IF NONE LEFT
         AIF   (&LINE(&UPCTR) NE 0).OP6D BRANCH IF LINE ALREADY IN USE
&LINE(&UPCTR) SETA &PTR           ASSIGN CHANNEL PUNCH TO THIS LINE
&CHANLST(&PTR) SETB 1             SIGNAL CHANNEL HAS BEEN ASSIGNED
&LINENO  SETA  &UPCTR             SAVE LINE NUMBER
.OP6E    ANOP
&CHAR    SETC  ' '                A BLANK TO LINE UP THE COLUMNS
         AIF   (&PTR LT 10).OP6F
&CHAR    SETC  ''                 TAKE IT OUT - NUMBER TOO BIG
.OP6F    ANOP
         MNOTE *,'   CARRIAGE CHANNEL &CHAR&PTR IN LINE &LINENO'
         AGO   .OP6I              NOW GO LOOK AT NEXT CHANNEL
.*
.*
.*  IF A CHANNEL 9 OR 12 PUNCH IS UNSPECIFIED, PLACE IT IN THE FIRST
.*  AVAILABLE LINE STARTING FROM THE BOTTOM OF THE FORM.
.*
.*
.OP6G    ANOP
&DNCTR   SETA  &DNCTR-1           LOOK AT NEXT AVAILABLE LINE
         AIF   (&DNCTR LE &UPCTR).OP6H BRANCH IF NONE LEFT
         AIF   (&LINE(&DNCTR) NE 0).OP6G BRANCH IF LINE ALREADY IN USE
&LINE(&DNCTR) SETA &PTR           ASSIGN CHANNEL PUNCH TO THIS LINE
&CHANLST(&PTR) SETB 1             SIGNAL CHANNEL HAS BEEN ASSIGNED
&LINENO  SETA  &DNCTR             SAVE LINE NUMBER
         AGO   .OP6E              NOW GO GENERATE A MESSAGE
.OP6H    ANOP
         SPACE 1
         MNOTE 0,'  WARNING - CHANNEL &PTR CANNOT BE SPECIFIED IN'
         MNOTE 0,'  THE FCB.  A SKIP TO THIS CHANNEL WILL CAUSE'
         MNOTE 0,'  RUNAWAY FORMS.'
&UPCTR   SETA   4                 HAVEN'T GOT ANY MORE LINES LEFT, SO
&DNCTR   SETA   5                 SET THESE TWO TO SAVE VALUES
.OP6I    ANOP
&PTR     SETA  &PTR+1             LOOK AT NEXT CHANNEL
         AIF   (&PTR LE 12).OP6C
.*
.*********************************************************************.
.*                                                                   *.
.*                  CREATE FORMS CONTROL BUFFER                      *.
.*                                                                   *.
.*********************************************************************.
.*
.OP7     ANOP
         AIF   (&QUITSW).ERROR10  QUIT IF ERRORS WERE DETECTED
         SPACE 1
***********************************************************************
*                                                                     *
         MNOTE *,'        3211 FORMS CONTROL BUFFER LOAD FOR "$T PRTN, *
               C=&FCB"        *'
*                                                                     *
***********************************************************************
         SPACE 1
.*
.*
.*  GENERATE OPERATING SYSTEM PREFIX
.*
.*
&LABEL   CSECT
         AIF   (&DEFBIT).OP7A
         DC    X'00'              THIS IS NOT A DEFAULT IMAGE
         AGO   .OP7B
.OP7A    ANOP
         DC    X'80'              THIS IS A DEFAULT IMAGE
.OP7B    ANOP
&SUM     SETA  &LPP               DETERMINE LENGTH OF FCB IMAGE
         AIF   (&RINDEX+&LINDEX EQ 0).OP7C WAS INDEXING SPECIFIED?
&SUM     SETA  &SUM+1             IF SO, ADD ONE TO THE LENGTH
.OP7C    ANOP
         DC    AL1(&SUM)          LENGTH OF FCB IMAGE
.*
.*
.*  GENERATE INDEX BYTE
.*
.*
.OP8     ANOP
         AIF   (&RINDEX+&LINDEX EQ 0).OP9  SKIP THIS PART IF NO INDEX
         AIF   (&LINDEX).OP8A      BRANCH IF LEFT INDEXING
&SUM     SETA  128+&INDXAMT        TURN ON RIGHT INDEX BIT
         DC    AL1(&SUM),0C'       RIGHT INDEX OF &INDXAMT'
         AGO   .OP9
.OP8A    ANOP
&SUM     SETA  64+&INDXAMT        TURN ON LEFT INDEX BIT
         DC    AL1(&SUM),0C'        LEFT INDEX OF &INDXAMT'
.*
.*
.*  SET LINES PER INCH SPACING
.*
.*
.OP9     ANOP
         AIF   (&LPIBIT).OP9A     CHECK SPACING BIT
*                                 SPACING IS SET AT 6 LINES PER INCH
         AGO   .OP10
.OP9A    ANOP
&LINE(1) SETA  &LINE(1)+16        TURN ON THE 8 LPI BIT
*                                 SPACING IS SET AT 8 LINES PER INCH
.*
.*********************************************************************.
.*                                                                   *.
.*                     DEFINE CHANNEL PUNCHES                        *.
.*                                                                   *.
.*********************************************************************.
.*
.OP10    ANOP
&LINE(&LPP) SETA &LINE(&LPP)+16   SIGNAL LAST BYTE OF FCB
&LINENO  SETA  1                  START AT LINE 1
.*
.*
.*  DEFINE ANY ZERO BYTES REQUIRED
.*
.*
.OP10A   ANOP
&UPCTR   SETA  &LINENO            FOR COUNTING ZERO BYTES
.OP10B   ANOP
         AIF   (&LINE(&LINENO) NE 0).OP10C IS THIS A ZERO BYTE?
&LINENO  SETA  &LINENO+1          IF NO, THEN LOOK AT NEXT BYTE
         AGO   .OP10B
.OP10C   ANOP
&SUM     SETA  &LINENO-&UPCTR     HOW MANY ZEROES DID I FIND?
         AIF   (&SUM EQ 0).OP10D  NONE - THEN SKIP DC OF ZEROES
         DC    &SUM.AL1(0)
.*
.*
.*  DEFINE THE CHANNEL PUNCH
.*
.*
.OP10D   ANOP
         AIF   (&LINENO EQ &LPP AND &LINE(&LINENO) EQ 16).OP10G
&SUM     SETA  &LINE(&LINENO)     WHAT IS THIS BYTE DEFINED AS?
&CHAR    SETC  ''                 NOW FIX UP THE SPACING
         AIF   (&SUM GE 10).OP10E
&CHAR    SETC  ' '
.OP10E   ANOP
&CHAR1   SETC  '&CHAR'
         AIF   (&SUM LE 16).OP10F IS THIS BYTE 8 LPI OR LAST BYTE?
&SUM     SETA  &SUM-16            GIVES CHANNEL NUMBER
&CHAR1   SETC  ''
         AIF   (&SUM GE 10).OP10F
&CHAR1   SETC  ' '
.OP10F   ANOP
&CHAR    SETC  '&CHAR.CHANNEL &CHAR1&SUM IS AT LINE &LINENO'
         DC    AL1(&LINE(&LINENO)),0C'        &CHAR'
&LINENO  SETA  &LINENO+1          BUMP LINE NUMBER
         AIF   (&LINENO LE &LPP).OP10A  GO LOOK AT NEXT BYTE
*                                 END OF FCB IMAGE
         SPACE 3
         MEXIT                    THAT'S ALL FOLKS.....
.OP10G   ANOP
         DC    AL1(16)            END OF FCB IMAGE
         MEXIT                    REALLY, THAT'S ALL FOLKS
.*
.*********************************************************************.
.*                                                                   *.
.*                     ERROR MESSAGES                                *.
.*                                                                   *.
.*********************************************************************.
.*
.ERROR1  SPACE 1
         MNOTE 8,'  NO CARRIAGE CONTROL PUNCHES WERE SPECIFIED.'
         AGO   .ERROR11           QUIT IMMEDIATELY
.*
.*
.ERROR2  SPACE 1
         MNOTE 4,'  INVALID SPACING OPERAND.  THE FIRST'
         MNOTE 0,'  OPERAND MUST BE EITHER 6 OR 8.'
&QUITSW  SETB  1
         AGO   .OP2               GO VERIFY LINES/PAGE
.*
.*
.ERROR3  SPACE 1
         MNOTE 4,'  INVALID LINES PER PAGE.  THIS MUST BE'
         MNOTE 0,'  A VALUE BETWEEN 2 AND 180 INCLUSIVE.'
&LPP     SETA  180                USE MAXIMUM FOR NOW
&QUITSW  SETB  1
         AGO   .OP3               GO CHECK INDEXING
.*
.*
.ERROR4  SPACE 1
         MNOTE 4,'  INVALID PRINT POSITION INDEX.  THIS MUST'
         MNOTE 0,'  BE A VALUE BETWEEN 1 AND 31 FOR RIGHT'
         MNOTE 0,'  INDEXING OR -1 AND -31 FOR LEFT INDEXING.'
&QUITSW  SETB  1
         AGO   .OP4               GO CHECK FOR A DEFAULT IMAGE
.*
.*
.ERROR5  SPACE 1
         MNOTE 0,'  NULL POSITIONAL OPERAND WAS SPECIFIED.'
         AGO   .OP5F              IGNORE IT AND CONTINUE
.*
.*
.ERROR6  SPACE 1
         MNOTE 4,'  OPERAND &SYSLIST(&OPERAND) IS INVALID - THE LINE'
         MNOTE 0,'  NUMBER IS MISSING TO CORRESPOND TO'
         MNOTE 0,'  CHANNEL &CHANNEL.'
&QUITSW  SETB  1
         AGO   .OP5F              CONTINUE OPERAND SCAN
.*
.*
.ERROR7  SPACE 1
         MNOTE 4,'  &SYSLIST(&OPERAND) IS INVALID.'
         MNOTE 0,'  THE CHANNEL NUMBER MUST BE IN THE'
         MNOTE 0,'  RANGE 1 TO 12 INCLUSIVE.'
&QUITSW  SETB  1
         AGO   .OP5F              CONTINUE OPERAND SCAN
.*
.*
.ERROR8  SPACE 1
         MNOTE 4,'  &SYSLIST(&OPERAND) IS INVALID.'
         MNOTE 0,'  THE LINE NUMBER MUST BE IN THE'
         MNOTE 0,'  RANGE 1 TO &LPP INCLUSIVE.'
&QUITSW  SETB  1
         AGO   .OP5F              CONTINUE OPERAND SCAN
.*
.*
.ERROR9  SPACE 1
         MNOTE 4,'  DUPLICATE SPECIFICATION - OPERAND &SYSLIST(&OPERAND*
               )'
         MNOTE 0,'  ASSIGNS CHANNEL &CHANNEL TO LINE &LINENO,'
         MNOTE 0,'  WHICH WAS PREVIOUSLY ASSIGNED CHANNEL &LINE(&LINENO*
               ).'
&QUITSW  SETB  1
         AGO   .OP5F              CONTINUE OPERAND SCAN
.*
.*
.ERROR10 SPACE 1
         MNOTE 8,'  *****  ERRORS HAVE BEEN ENCOUNTERED.  *****'
         MNOTE 0,'  *****  THE FCB WILL NOT BE GENERATED. *****'
         MEXIT
.*
.*
.ERROR11 SPACE 1
         MNOTE 4,'  THE FCB LABEL IS MISSING OR INVALID.'
         MNOTE 0,'  IT MUST BE OF THE FORM "FCB2?", WHERE ?'
         MNOTE 0,'  IS A 1 TO 4 CHARACTER FCB NAME.'
&QUITSW  SETB  1
         AGO   .ERROR10           PACK IT IN NOW
.*
.*
.ERROR12 SPACE 1
         MNOTE 4,'  CARRIAGE CHANNEL 1 IS NOT SPECIFIED AND'
         MNOTE 0,'  CANNOT BE INSERTED.  THE CHANNEL 1 PUNCH'
         MNOTE 0,'  IS REQUIRED FOR THE 3211 CARRIAGE RESTORE'
         MNOTE 0,'  FUNCTION.'
&QUITSW  SETB  1
         AGO   .OP6B              CONTINUE CHECKING CHANNEL PUNCHES
         MEND
         MACRO
&LABEL   $IC
.*
.*  FUNCTION : GENERATE 3270 INSERT CURSOR ORDER.
.*
.*  INPUT    : NONE
.*
.*  OUTPUT   : CODE IS GENERATED FOR THE IC ORDER
.*
.*  AUTHOR   : ED PITKIN, DISTRIBUTED SERVICES
.*
.*  DATE     : SEPT 19/80
.*
&LABEL   DC    X'13'               IC
         MEND
         MACRO
&LABEL   $IO   &CMD
.*
.*  FUNCTION : GENERATE VALID IO COMMAND FOR 3270 DATA STREAM.
.*
.*  INPUT    : &CMD     = WRITE
.*                        WSF         - WRITE STRUCTURED FIELD
.*                        ERASE/WRITE - USE &MODEL TO SELECT IO CMD
.*             &MODEL   - SPECIFIES MODEL NUMBER OF 3270 TERMINAL.
.*
.*  OUTPUT   : ASSEMBLER DC STATEMENT FOR IO COMMAND.
.*
.*  AUTHOR   : ED PITKIN, DISTRIBUTED SERVICES
.*
.*  DATE     : SEPT 19/80
.*
         GBLC  &TAB3270(64),&ATTRIB
         GBLA  &RC,&MODEL
         LCLC  &OPCODE,&COMMENT
.*   PROCESS WRITE COMMAND
         AIF ('&CMD' NE 'WRITE').WSF
&OPCODE  SETC  'F1'
         AGO    .DC
.*   PROCESS WRITE STRUCTURED FIELD COMMAND
.WSF     AIF ('&CMD' NE 'WSF').ERWRIT
&OPCODE  SETC  'F3'
         AGO    .DC
.*   PROCESS ERASE/WRITE COMMAND
.ERWRIT  AIF ('&CMD' NE 'ERASE/WRITE').ERROR
&OPCODE  SETC  'F5'
         AIF (&MODEL LE 2).DC
&OPCODE  SETC  '7E'
         AGO    .DC
.*   PROCESS INVALID COMMANDS
.ERROR   MNOTE 8,'&CMD IS AN INVALID 3270 COMMAND'
         MEXIT
.DC      ANOP
&COMMENT SETC '                &CMD FOR 3270 MODEL &MODEL'
&LABEL   DC    X'&OPCODE'&COMMENT
         MEND
         MACRO
         $MODEL &NUM
.*
.*  FUNCTION : SET MODEL NUMBER FOR SUBSEQUENT 3270 MACRO INSTRUCTIONS.
.*
.*  INPUT    : 3270 MODEL NUMBER (1,2,3,4 OR 5)
.*
.*  OUTPUT   : &MODEL SETA SYMBOL IS SET.
.*
.*  AUTHOR   : ED PITKIN, DISTRIBUTED SERVICES
.*
.*  DATE     : SEPT 19/80
.*
         GBLC  &TAB3270(64)
         GBLA  &RC,&MODEL
         AIF   ((&NUM GE 2) AND (&NUM LE 4)).OK
         MNOTE 8,'3270 MODEL &MODEL IS NOT SUPPORTED BY THESE MACROS.'
         MEXIT
.OK      ANOP
&MODEL   SETA  &NUM
         MEND
         MACRO
         $PSS   &PSS
.*
.*  FUNCTION : VALIDATE PROGRAMMED SYMBOL SET.
.*
.*  INPUT    : &PSS - PROGRAMMED SYMBOL SET ID
.*
.*  OUTPUT   : &RC      =  0 => SYMBOL SET OK.
.*                        ^0 => SYMBOL SET IS INVALID.
.*           : &ATTRIB  = VALID PROGRAMMED SYMBOL SET ID.
.*
.*  AUTHOR   : ED PITKIN, DISTRIBUTED SERVICES
.*
.*  DATE     : SEPT 19/80
.*
         GBLC  &ATTRIB
         GBLA  &RC
         AIF (('&PSS' GE '40') AND ('&PSS' LE 'EF')).PSSFND
.*   REJECT SYMBOL SET
         MNOTE 8,'&PSS IS AN INVALID SYMBOL SET'
&RC      SETA  4
         MEXIT
.*   ACCEPT SYMBOL SET
.PSSFND  ANOP
&ATTRIB  SETC  '&PSS'
&RC      SETA  0
         MEND
         MACRO
&LABEL   $RA   &OPTIONS
.*
.*  FUNCTION : GENERATE 3270 REPEAT TO ADDRESS ORDER. THE (ROW,COLUMN)
.*             FORMAT ADDRESS IS CONVERTED TO THE HEX CHARACTERS
.*             REQUIRED FOR THE ORDER AND THE REPEAT CHARACTER IS
.*             APPENDED TO THE END OF THE RA ORDER.
.*
.*  REFERENCE: BUFFER ADDRESS CONVERSION ALGORITHM IN
.*             GC27-6999 INTRO TO PROGRAMMING THE 3270 IDS
.*
.*  INPUT    : BUFFER ADDRESS AND CHARACTER IN (ROW,COLUMN,CHAR) FORMAT
.*             CHAR MUST BE IN THE FORMAT C'A' OR X'C1'
.*
.*  OUTPUT   : CODE IS GENERATED FOR THE RA ORDER
.*
.*  AUTHOR   : ED PITKIN, DISTRIBUTED SERVICES
.*
.*  DATE     : SEPT 19/80
.*
         GBLC  &TAB3270(64)
         GBLA  &RC
         LCLA  &ROW,&COLUMN,&BUFFLOC,&I1,&I2
         LCLC  &COMMENT,&CHAR
&ROW     SETA  &OPTIONS(1)
&COLUMN  SETA  &OPTIONS(2)
&CHAR    SETC  '&OPTIONS(3)'
.*  CALC OFFSET IN 3270 BUFFER
&BUFFLOC SETA  ((&ROW-1)*80)+(&COLUMN-1)
.*  CONVERT 12 BIT ADDR TO TWO 6 BIT ADDRS
&I1      SETA  &BUFFLOC/64
&I2      SETA  &BUFFLOC-(&I1*64)
.*  CHANGE INDICES INTO 3270 CONVERSION TABLE FROM 0-BASED TO 1-BASED
&I1      SETA  &I1+1
&I2      SETA  &I2+1
&COMMENT SETC  '    RA &OPTIONS'
&LABEL   DC    XL3'3C&TAB3270(&I1)&TAB3270(&I2)',&CHAR&COMMENT
         MEND
         MACRO
&LABEL   $SBA  &ADDR
.*
.*  FUNCTION : GENERATE 3270 SET BUFFER ADDRESS ORDER. THE (ROW,COLUMN)
.*             FORMAT ADDRESS IS CONVERTED TO THE HEX CHARACTERS
.*             REQUIRED FOR THE ORDER.
.*
.*  REFERENCE: BUFFER ADDRESS CONVERSION ALGORITHM IN
.*             GC27-6999 INTRO TO PROGRAMMING THE 3270 IDS
.*
.*  INPUT    : BUFFER ADDRESS IN (ROW,COLUMN FORMAT)
.*
.*  OUTPUT   : CODE IS GENERATED FOR THE SBA ORDER AND THE BUFFER ADDR
.*
.*  AUTHOR   : ED PITKIN, DISTRIBUTED SERVICES
.*
.*  DATE     : SEPT 19/80
.*
         GBLC  &TAB3270(64)
         GBLA  &RC
         LCLA  &ROW,&COLUMN,&BUFFLOC,&I1,&I2
         LCLC  &COMMENT
&ROW     SETA  &ADDR(1)
&COLUMN  SETA  &ADDR(2)
.*  CALC OFFSET IN 3270 BUFFER
&BUFFLOC SETA  ((&ROW-1)*80)+(&COLUMN-1)
.*  CONVERT 12 BIT ADDR TO TWO 6 BIT ADDRS
&I1      SETA  &BUFFLOC/64
&I2      SETA  &BUFFLOC-(&I1*64)
.*  CHANGE INDICES INTO 3270 CONVERSION TABLE FROM 0-BASED TO 1-BASED
&I1      SETA  &I1+1
&I2      SETA  &I2+1
&COMMENT SETC  '          SBA &ADDR'
&LABEL   DC    XL3'11&TAB3270(&I1)&TAB3270(&I2)'&COMMENT
         MEND
         MACRO
&LABEL   $SBATEST
.*
.*  FUNCTION : GENERATE ALL 3270 BUFFER ADDRESSES TO TEST $SBA.
.*
.*  OUTPUT   : CODE IS GENERATED FOR THE SBA ORDER AND THE BUFFER ADDR
.*
.*  AUTHOR   : ED PITKIN, DISTRIBUTED SERVICES
.*
.*  DATE     : SEPT 19/80
.*
         GBLC  &TAB3270(64)
         GBLA  &RC
         LCLA  &M,&N
&TAB3270(1) SETC  '40'
&TAB3270(2) SETC  'C1'
&TAB3270(3) SETC  'C2'
&TAB3270(4) SETC  'C3'
&TAB3270(5) SETC  'C4'
&TAB3270(6) SETC  'C5'
&TAB3270(7) SETC  'C6'
&TAB3270(8) SETC  'C7'
&TAB3270(9) SETC  'C8'
&TAB3270(10) SETC 'C9'
&TAB3270(11) SETC '4A'
&TAB3270(12) SETC '4B'
&TAB3270(13) SETC '4C'
&TAB3270(14) SETC '4D'
&TAB3270(15) SETC '4E'
&TAB3270(16) SETC '4F'
&TAB3270(17) SETC '50'
&TAB3270(18) SETC 'D1'
&TAB3270(19) SETC 'D2'
&TAB3270(20) SETC 'D3'
&TAB3270(21) SETC 'D4'
&TAB3270(22) SETC 'D5'
&TAB3270(23) SETC 'D6'
&TAB3270(24) SETC 'D7'
&TAB3270(25) SETC 'D8'
&TAB3270(26) SETC 'D9'
&TAB3270(27) SETC '5A'
&TAB3270(28) SETC '5B'
&TAB3270(29) SETC '5C'
&TAB3270(30) SETC '5D'
&TAB3270(31) SETC '5E'
&TAB3270(32) SETC '5F'
&TAB3270(33) SETC '60'
&TAB3270(34) SETC '61'
&TAB3270(35) SETC 'E2'
&TAB3270(36) SETC 'E3'
&TAB3270(37) SETC 'E4'
&TAB3270(38) SETC 'E5'
&TAB3270(39) SETC 'E6'
&TAB3270(40) SETC 'E7'
&TAB3270(41) SETC 'E8'
&TAB3270(42) SETC 'E9'
&TAB3270(43) SETC '6A'
&TAB3270(44) SETC '6B'
&TAB3270(45) SETC '6C'
&TAB3270(46) SETC '6D'
&TAB3270(47) SETC '6E'
&TAB3270(48) SETC '6F'
&TAB3270(49) SETC 'F0'
&TAB3270(50) SETC 'F1'
&TAB3270(51) SETC 'F2'
&TAB3270(52) SETC 'F3'
&TAB3270(53) SETC 'F4'
&TAB3270(54) SETC 'F5'
&TAB3270(55) SETC 'F6'
&TAB3270(56) SETC 'F7'
&TAB3270(57) SETC 'F8'
&TAB3270(58) SETC 'F9'
&TAB3270(59) SETC '7A'
&TAB3270(60) SETC '7B'
&TAB3270(61) SETC '7C'
&TAB3270(62) SETC '7D'
&TAB3270(63) SETC '7E'
&TAB3270(64) SETC '7F'
&M       SETA  1
.LOOP1   ANOP
&N       SETA  1
.LOOP2   ANOP
         $SBA  (&M,&N)
&N       SETA  &N+1
         AIF (&N LE 80).LOOP2
&M       SETA  &M+1
         AIF (&M LE 43).LOOP1
         MEND
         MACRO
&LABEL   $SF    &ATTR
.*
.*  FUNCTION : GENERATE START FIELD ORDER WITH SPECIFIED
.*             OPTIONS.
.*
.*  INPUT    : &ATTR   - ATTRIBUTE OPTIONS. IT MUST BE ONE OF
.*                       (PROT,NUM,MDT,HI,NONDISP,SKIP)
.*                       (UNPROT,ALPHA,NOMDT)
.*
.*  OUTPUT   : SFE ORDER PLUS OPTION BYTES.
.*
.*  AUTHOR   : ED PITKIN, DISTRIBUTED SERVICES
.*
.*  DATE     : SEPT 19/80
.*
         GBLC  &TAB3270(64),&ATTRIB
         GBLA  &RC
         LCLC  &COMMENT
.*   PROCESS ATTRIBUTE OPTIONS
         AIF (N'&ATTR  EQ 0).NOATTR
         $ATTR &ATTR
         AIF (&RC EQ 0).DC
.* USE DEFAULT ATTRIBUTE BYTE
.NOATTR  ANOP
&ATTRIB  SETC  '40'
.DC      ANOP
&COMMENT SETC  '              SF &ATTR'
&LABEL   DC    X'1D&ATTRIB'&COMMENT
         MEND
         MACRO
&LABEL   $SFE   &ATTR=,&XHL=,&COL=,&PSS=
.*
.*  FUNCTION : GENERATE START FIELD EXTENDED ORDER WITH SPECIFIED
.*             OPTIONS.
.*
.*  INPUT    : &ATTRIB - ATTRIBUTE OPTIONS. IT MUST BE ONE OF
.*                       (PROT,NUM,MDT,HI,NONDISP,SKIP)
.*                       (UNPROT,ALPHA,NOMDT)
.*           : &XHL    - EXTENDED HIGHLIGHTING OPTIONS. MUST BE ONE OF
.*                       (BLINKING,REVERSE,UNDERSCORE)
.*           : &COL    - COLOUR OPTION. IT MUST BE ONE OF
.*                       (BLUE,RED,PINK,GREEN,TURQUOISE,YELLOW,WHITE)
.*                       ONLY 1ST 3 CHARS ARE CHECKED
.*           : &PSS    - PROGRAMMED SYMBOL SET. THE VALUE MUST BE
.*                       BETWEEN X'40' AND X'EF'.
.*
.*  OUTPUT   : SFE ORDER PLUS OPTION BYTES.
.*
.*  AUTHOR   : ED PITKIN, DISTRIBUTED SERVICES
.*
.*  DATE     : SEPT 19/80
.*
         GBLC  &TAB3270(64),&ATTRIB
         GBLA  &RC
         LCLA  &CNT
         LCLC  &OPTIONS,&COMMENT
.*   PROCESS ATTRIBUTE OPTIONS
         AIF ('&ATTR' EQ '').XHL
         $ATTR &ATTR
         AIF (&RC NE 0).XHL
&CNT     SETA  &CNT+1
&OPTIONS SETC  '&OPTIONS'.'C0'.'&ATTRIB'
.*   PROCESS EXTENDED HIGHLIGHT OPTIONS
.XHL     AIF ('&XHL' EQ '').COL
         $XHL  &XHL
         AIF (&RC NE 0).COL
&CNT     SETA  &CNT+1
&OPTIONS SETC  '&OPTIONS'.'41'.'&ATTRIB'
.*   PROCESS COLOUR OPTION.
.COL     AIF ('&COL' EQ '').PSS
         $COL  &COL
         AIF (&RC NE 0).PSS
&CNT     SETA  &CNT+1
&OPTIONS SETC  '&OPTIONS'.'42'.'&ATTRIB'
.*   PROCESS PROGRAMMED SYMBOL SET OPTION
.PSS     AIF ('&PSS' EQ '').CHKCNT
         $PSS  &PSS
         AIF (&RC NE 0).CHKCNT
&CNT     SETA  &CNT+1
&OPTIONS SETC  '&OPTIONS'.'43'.'&ATTRIB'
.*   VERIFY ATLEAST ONE VALID OPTION.
.CHKCNT  AIF (&CNT GT 0).DC
         MNOTE 8,'NO VALID ATTRIBUTES SPECIFIED IN $SFE MACRO'
&RC      SETA  4
         MEXIT
.*   GENERATE SFE ORDER AND OPTIONS
.DC      ANOP
&COMMENT SETC  '      SFE &ATTR,&XHL,&COL,&PSS'
&LABEL   DC    X'290&CNT&OPTIONS'&COMMENT
         MEND
         MACRO
&LABEL   $WCC   &OPTIONS
.*
.*  FUNCTION : GENERATE VALID WRITE CONTROL CHARACTER FOR 3270 DATA
.*             STREAM.
.*
.*  METHOD   : USE BIT ARRAY TO FLAG SELECTED OPTIONS THEN GENERATE
.*             APPROPRIATE HEX CODE.
.*
.*  INPUT    : &OPTIONS - ATTRIBUTE OPTIONS. IT MUST BE ONE OF
.*                        (ALARM,RESETKBD,MDT)
.*
.*  OUTPUT   : ASSEMBLER DC STATEMENT FOR WCC CODE.
.*
.*  AUTHOR   : ED PITKIN, DISTRIBUTED SERVICES
.*
.*  DATE     : SEPT 19/80
.*
         GBLC  &TAB3270(64),&ATTRIB
         GBLA  &RC,&MODEL
         LCLA  &I1,&BIT(8)
         LCLC  &COMMENT
         AIF   (N'&OPTIONS EQ 0).DEFAULT
&I1      SETA  1
.LOOP    ANOP
.*   PROCESS ALARM ATTRIBUTE
         AIF ('&OPTIONS(&I1)' NE 'ALARM').RESETKB
&BIT(6)  SETA  1
         AGO    .NXTONE
.*   PROCESS RESETKBD ATTRIBUTE
.RESETKB AIF ('&OPTIONS(&I1)' NE 'RESETKBD').MDT
&BIT(7)  SETA  1
         AGO    .NXTONE
.*   PROCESS MDT ATTRIBUTE
.MDT     AIF ('&OPTIONS(&I1)' NE 'MDT').ERROR
&BIT(8)  SETA  1
         AGO    .NXTONE
.*   PROCESS INVALID ATTRIBUTES
.ERROR   MNOTE 8,'&OPTIONS(&I1) IS INVALID FOR WCC'
.NXTONE  ANOP
&I1      SETA  &I1+1
         AIF (&I1 LE N'&OPTIONS).LOOP
.*   CALCULATE VALUE OF OPTION BYTE PLUS ONE
.DEFAULT ANOP
&I1      SETA &BIT(8)+&BIT(7)*2+&BIT(6)*4+1
         AIF (&I1 LE 64).GETCHAR
         MNOTE 8,'&I1 IS GREATE THAN 64'
         MEXIT
.*   LOOK UP CHAR FOR CALCULATED OPTION BYTE.
.GETCHAR ANOP
&COMMENT SETC '                WCC &OPTIONS'
&LABEL   DC    X'&TAB3270(&I1)'&COMMENT
         MEND
         MACRO
         $XHL   &XHL
.*
.*  FUNCTION : VALIDATE EXTENDED HIGHLIGHTING OPTION AND SELECT
.*             APPROPRIATE HEX CODE.
.*
.*  INPUT    : &XHL - EXTENDED HIGHLIGHTING OPTION. IT MUST BE ONE OF
.*                    (BLINK,REVERSE,UNDERSCORE)
.*
.*  OUTPUT   : &RC      =  0 => XH OPTION IS OK.
.*                        ^0 => XH OPTION IS INVALID.
.*           : &ATTRIB  = HEX CODE FOR XH OPTION SELECTED.
.*
.*  AUTHOR   : ED PITKIN, DISTRIBUTED SERVICES
.*
.*  DATE     : SEPT 19/80
.*
         GBLC  &ATTRIB
         GBLA  &RC
         AIF ('&XHL' NE 'BLINK').REVID
.*   SET HEX CODE FOR BLINKING
&ATTRIB  SETC  'F1'
         AGO    .XHLFND
.REVID   AIF ('&XHL' NE 'REVERSE').UNDER
.*   SET HEX CODE FOR REVERSE
&ATTRIB  SETC  'F2'
         AGO    .XHLFND
.UNDER   AIF ('&XHL' NE 'UNDERSCORE').ERROR
.*   SET HEX CODE FOR UNDERSCORE
&ATTRIB  SETC  'F4'
         AGO    .XHLFND
.*   REJECT EXTENDED HIGHLIGHTING OPTION
.ERROR   MNOTE 8,'&XHL IS AN INVALID EXTENDED HIGHLIGHTING OPTION.'
&RC      SETA  4
         MEXIT
.*   ACCEPT XH OPTION
.XHLFND  ANOP
&RC      SETA  0
         MEND
*
*  FUNCTION : INITIALIZE MACRO SYMBOLS REQUIRED BY 3270 MACROS.
*
*  AUTHOR   : ED PITKIN, DISTRIBUTED SERVICES
*
*  DATE     : SEPT 19/80
*
         GBLC  &TAB3270(64),&ATTRIB
         GBLA  &RC,&MODEL
         PRINT OFF
&MODEL      SETA  2
&TAB3270(1) SETC  '40'
&TAB3270(2) SETC  'C1'
&TAB3270(3) SETC  'C2'
&TAB3270(4) SETC  'C3'
&TAB3270(5) SETC  'C4'
&TAB3270(6) SETC  'C5'
&TAB3270(7) SETC  'C6'
&TAB3270(8) SETC  'C7'
&TAB3270(9) SETC  'C8'
&TAB3270(10) SETC 'C9'
&TAB3270(11) SETC '4A'
&TAB3270(12) SETC '4B'
&TAB3270(13) SETC '4C'
&TAB3270(14) SETC '4D'
&TAB3270(15) SETC '4E'
&TAB3270(16) SETC '4F'
&TAB3270(17) SETC '50'
&TAB3270(18) SETC 'D1'
&TAB3270(19) SETC 'D2'
&TAB3270(20) SETC 'D3'
&TAB3270(21) SETC 'D4'
&TAB3270(22) SETC 'D5'
&TAB3270(23) SETC 'D6'
&TAB3270(24) SETC 'D7'
&TAB3270(25) SETC 'D8'
&TAB3270(26) SETC 'D9'
&TAB3270(27) SETC '5A'
&TAB3270(28) SETC '5B'
&TAB3270(29) SETC '5C'
&TAB3270(30) SETC '5D'
&TAB3270(31) SETC '5E'
&TAB3270(32) SETC '5F'
&TAB3270(33) SETC '60'
&TAB3270(34) SETC '61'
&TAB3270(35) SETC 'E2'
&TAB3270(36) SETC 'E3'
&TAB3270(37) SETC 'E4'
&TAB3270(38) SETC 'E5'
&TAB3270(39) SETC 'E6'
&TAB3270(40) SETC 'E7'
&TAB3270(41) SETC 'E8'
&TAB3270(42) SETC 'E9'
&TAB3270(43) SETC '6A'
&TAB3270(44) SETC '6B'
&TAB3270(45) SETC '6C'
&TAB3270(46) SETC '6D'
&TAB3270(47) SETC '6E'
&TAB3270(48) SETC '6F'
&TAB3270(49) SETC 'F0'
&TAB3270(50) SETC 'F1'
&TAB3270(51) SETC 'F2'
&TAB3270(52) SETC 'F3'
&TAB3270(53) SETC 'F4'
&TAB3270(54) SETC 'F5'
&TAB3270(55) SETC 'F6'
&TAB3270(56) SETC 'F7'
&TAB3270(57) SETC 'F8'
&TAB3270(58) SETC 'F9'
&TAB3270(59) SETC '7A'
&TAB3270(60) SETC '7B'
&TAB3270(61) SETC '7C'
&TAB3270(62) SETC '7D'
&TAB3270(63) SETC '7E'
&TAB3270(64) SETC '7F'
         PRINT ON
