LIST QSTART T='QUEUE COMMAND - PRINT A DATASET FROM SPOOL BY ID', TEC01C
               $BUFFER,                                           TEC01C
               $JCT                                               TEC01
         GBLB  &QSP33         MVS/SP1.3.3 OPTION                  TEC01
         GBLB  &QACF2         IS ACF2 AUTH CHECKING TO BE DONE     FCI*
         GBLB  &QRNB                                              RNB05
         USING QCKPT,R10      BASE REG FOR CKPT WORK AREA
         L     R10,QVCKPT     LOAD BASE REG
         USING WORK,R13       BASE REG FOR TEMP WORK
***********************************************************************
*                                                                     *
*   CALL FINDJOB TO LOCATE THE JQE, JCT, AND IOT                      *
*                                                                     *
***********************************************************************
         L     R15,=V(FINDJOB) ADDR OF MODULE TO FIND JOB
         BALR  R14,R15        GO TO IT
***********************************************************************
*                                                                     *
*   ENSURE JOBNAME BEGINS WITH USER ID                                *
*                                                                     *
***********************************************************************
         AIF   (&QRNB).RNB05A                                     RNB05
         SPACE 1                                                  UF005
         TM    QXAUTH,1       IS USER AUTHORIZED?                 UF005
         BO    OKJOB          YES, ALLOW TO PROCEED               UF005
         SPACE 1                                                  UF005
.RNB05A  ANOP                                                     RNB05
         L     R9,QCJCTA      ADDR OF JCT
         USING JCTSTART,R9    BASE REG FOR JCT
         LA    R2,7           MAXIMUM LENGTH OF USER ID
         LA    R3,QLOGON+7    LAST BYTE OF USER ID
LOOP     CLI   0(R3),C' '     IS THIS BYTE BLANK?
         BNE   CHECK          NO. CONTINUE.
         BCTR  R3,0           TRY PREVIOUS BYTE
         BCT   R2,LOOP        TRY AGAIN
CHECK    EX    R2,CLC         IS THE JOBNAME VALID?
         AIF   (&QRNB).RNB05B                                     RNB05
         AIF   (&QACF2).ACF1                                       FCI*
         BNE   TILT2          NO. TILT.                            FCI*
         AGO   .ACF2                                               FCI*
.ACF1    ANOP  ,                                                   FCI*
         NOP   TILT2          ACF2 HAS ALREADY CHECKED AUTHORITY   FCI*
.ACF2    ANOP  ,                                                   FCI*
*        BNE   TILT2          NO. TILT.
         AGO   .RNB05C                                            RNB05
.RNB05B  ANOP                                                     RNB05
         BE    OKJOB          /YES - GO CHECK DSID                RNB05
         CLC   =C'PJS',QLOGON IS THIS A PJS USER?                 RNB05
         BE    TILT2          INVALID JOB IF SO                   RNB05
         CLC   QLOGON(*-*),JCTTSUID  DOES THE USERID              RNB05
         EX    R2,*-6                MATCH THE NOTIFY ID?         RNB05
         BE    OKJOB                 GOOD JOB IF SO               RNB05
         CLC   =C'TEC',QLOGON        IS THIS A TEC USERID?        RNB05
         BNE   TILT2                 INVALID JOB IF NOT           RNB05
         CLC   =C'TEC',JCTJNAME      FOR A TEC USER, ALLOW LIST   RNB05
         BE    OKJOB                 FOR ANY TEC JOB OR ANY JOB   RNB05
         CLC   =C'TEC',JCTTSUID      WITH A TEC NOTIFY            RNB05
         BE    OKJOB                                              RNB05
         TM    JCTJOBFL,JCTSTCJB     ALSO ALLOW IF AN STC         RNB05
         BZ    TILT2                                              RNB05
.RNB05C  ANOP                                                     RNB05
OKJOB    DS    0H                                                 UF005
***********************************************************************
*                                                                     *
*   CHECK AND CONVERT THE DATASET ID NUMBER                           *
*                                                                     *
***********************************************************************
         LH    R2,QLNG2       LENGTH OF DATASET ID FIELD
         SH    R2,=H'1'       IS THE DATASET ID FIELD ZERO LENGTH?
         BM    TILT           YES. QUIT.
         LH    R4,QPDSID      GET CURRENT DSID VALUE              UF040
         LA    R3,DSIDPLUS    POINT TO ADD INSTRUCTION            UF040
         CLI   QPARM2,C'+'    RELATIVE DSID NUMBER?               UF040
         BE    DSIDREL        YES, GO PROCESS IT                  UF040
         LA    R3,DSIDSUB     POINT TO SUBTRACT INSTRUCTION       UF040
         CLI   QPARM2,C'-'    RELATIVE DSID NUMBER?               UF020
         BE    DSIDREL        YES, GO PROCESS IT                  UF040
         LA    R3,DSIDABS     POINT TO NOP INSTRUCTION            UF040
         B     DSIDTEST       NO, CONTINUE                        UF040
DSIDREL  CLI   QPARM1,C'*'    WANT THE CURRENT JOB?               UF040
         BNE   TILTREL        NO, RELATIVE REQUEST INVALID        UF040
         MVI   QPARM2,C'0'    REPLACE WITH NUMERIC VALUE          UF040
         LTR   R2,R2          WAS FIELD ONLY THE +/-?             UF040
         BP    DSIDTEST       NO, CONTINUE AS BEFORE              UF040
         LA    R2,1           YES, SET VALUE OF 1                 UF040
         B     DSIDCHK        AND SKIP NUMERIC CHECKS             UF040
DSIDTEST DS    0H                                                 UF040
         MVC   QFZONES,QFZONE INITIALIZE NUMERIC TEST
         EX    R2,MVZ         MOVE THE ZONES FOR VALIDITY CHECK
         CLC   QFZONES,QFZONE IS THE FIELD NUMERIC?
         BNE   TILT           NO. QUIT.
         EX    R2,PACK        PACK THE FIELD
         CVB   R2,CONVERT     CONVERT TO BINARY
         SPACE 1                                                  UF005
DSIDCHK  DS    0H                                                 UF040
         EX    0,0(R3)        ADJUST VALUE IN R4 BY OFFSET        UF040
         LR    R2,R4          PUT BACK IN R2                      UF040
         LTR   R2,R2          MAKE SURE IS POSITIVE               UF040
         BNP   TILT           ERROR IF NOT                        UF040
         TM    QXAUTH,1       IS USER AUTHORIZED?                 UF005
         BO    DSIDOK         YES, ALLOW ANY DSID                 UF005
         SPACE 1                                                  UF005
         CH    R2,=H'101'     IS THE DATASET ID LESS THAN 101?
         BL    TILT           YES. TILT.
DSIDOK   STH   R2,QPDSID      STORE DATASET ID
***********************************************************************
*                                                                     *
*   CHECK AND CONVERT THE PRINT OFFSET                                *
*                                                                     *
***********************************************************************
         MVC   QPOFFSET,=H'0' DEFAULT TO ZERO
         LH    R2,QLNG3       LENGTH OF OFFSET FIELD
         SH    R2,=H'1'       IS THE OFFSET FIELD ZERO LENGTH?
         BM    CALLLIST       YES. USE ZERO OFFSET.
         MVC   QFZONES,QFZONE INITIALIZE NUMERIC TEST
         EX    R2,MVZ2        MOVE THE ZONES FOR VALIDITY CHECK
         CLC   QFZONES,QFZONE IS THE FIELD NUMERIC?
         BNE   TILT           NO. QUIT.
         EX    R2,PACK2       PACK THE FIELD
         CVB   R2,CONVERT     CONVERT TO BINARY
         STH   R2,QPOFFSET    STORE OFFSET
***********************************************************************
*                                                                     *
*   CALL LISTDS TO LIST THE DATASET                                   *
*                                                                     *
***********************************************************************
CALLLIST L     R15,=V(LISTDS) ADDR OF LISTDS MODULE
         BALR  R14,R15        GO TO IT
         QSTOP
TILT     QTILT '*** DATASET ID INVALID ***'
TILT2    QTILT '*** JOBNAME MUST BEGIN WITH USERID ***'
TILTREL  QTILT '*** RELATIVE DATASET ID ONLY VALID IF JOBID IS "*" ***'
***********************************************************************
*                                                                     *
*   MISCELLANY                                                        *
*                                                                     *
***********************************************************************
DSIDPLUS AR    R4,R2          ADD TO PRESENT VALUE                UF040
DSIDSUB  SR    R4,R2          SUBTRACT FROM PRESENT VALUE         UF040
DSIDABS  LR    R4,R2          SET TO PRESENT VALUE                UF040
CLC      CLC   QLOGON(1),JCTJNAME IS THE JOBNAME EQUAL TO USERID
MVZ      MVZ   QFZONES(1),QPARM2 CHECK FOR NUMERIC
MVZ2     MVZ   QFZONES(1),QPARM3 CHECK FOR NUMERIC
PACK     PACK  CONVERT,QPARM2(1) CONVERT TO BINARY
PACK2    PACK  CONVERT,QPARM3(1) CONVERT TO BINARY
         LTORG
         DROP  ,                   DROP ALL ADDRESSINGS           NERDC
SYMDEL   DSECT ,                   KILL SYM CARD GENERATION       UF023
LIST     CSECT ,                                                  UF023
JCT      EQU   0
         AIF   (&QSP33).QSP33                                     TEC01
         $BUFFER                                                  UF020
         $JCT
.QSP33   ANOP                                                     TEC01
WORK     DSECT
         DS    72C
CONVERT  DS    D
         QCOMMON
SYMNODEL DSECT ,                   RESTORE SYM CARD GENERATION    UF023
         END
