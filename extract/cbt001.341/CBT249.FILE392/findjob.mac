FINDJOB  QSTART T='QUEUE COMMAND -LOCATE JQE, JCT, AND IOT BY JOBNAME',C
               $BUFFER,                                           TEC01C
               $JQE,                                              TEC01C
               $JCT,                                              TEC01C
               $HASPEQU,                                          TEC02X
               $MIT,                                              TEC02X
               $PCE,                                              TEC02X
               $CAT,                                              TEC02X
               $HCT                                               TEC02
         GBLB  &QSP33         MVS/SP1.3.3 OPTION                  TEC01
***********************************************************************
* RNB CHANGES:                                                        *
*     (1) RNB22 - IN CASE JOBNAME = * (FOR CURRENT JOB), AFTER READING*
*                 THE JCT ENSURE JQEJNAME = JCTJNAME AND QPJOBID =    *
*                 JCTJBKEY. THIS IS DONE IN CASE THE JOB PURGED SINCE *
*                 THE LAST FINDJOB.                                   *
***********************************************************************
         GBLB   &QACF2         ACF2 CHECKING FOR AUTH              FCI*
         GBLB   &QSP           MVS/SP OPTION                      UF020
         AIF    (NOT &QACF2).NACF1                                 FCI*
*******************************************************************FCI*
* MOD 1 - K TRUE  - 22 OCT 79 -                                    FCI*
*   ADD ACF2 AUTH CHKING FOR USER AUTH TO LOOK AT STUFF.           FCI*
*    OPER CHKS ALL..                                               FCI*
*    USER CHKS OWN GOODIES (LOGONID = LOGONID IN JCT)              FCI*
*    OTHER: ISSUE ACFVLD CHK FOR AUTH                              FCI*
*******************************************************************FCI*
.NACF1   ANOP                                                      FCI*
         USING QCKPT,R10      BASE REG FOR CHECKPOINT WORK AREA
         L     R10,QVCKPT     LOAD BASE REG
         USING WORK,R13       LOCAL WORK AREA
         MVI   SLID#,0        CLEAR ID NUMBER                     UF035
         LH    R1,QLNG1       LENGTH OF PARAMETER FIELD
         SH    R1,=H'1'       IS THE LENGTH ZERO?
         BM    TILT           YES. TILT.
******************************************************************UF007
*                                                                 UF007
*   ALLOW JOBNAME = "*" TO SIGNIFY CURRENT JOB & SKIP CKPT READ   UF007
*                                                                 UF007
******************************************************************UF007
         CLI   QPARM1,C'*'    WANT CURRENT JOB AGAIN?             UF007
         BE    JOBAGAIN       YES, SKIP CKPT READ                 UF007
******************************************************************UF006
*                                                                 UF006
*   CALL - READ JES2 CHECKPOINT ROUTINE                           UF006
*                                                                 UF006
******************************************************************UF006
CALLCKPT L     R15,=V(CKPT)   ADDR OF CKPT ROUTINE                UF006
         BALR  R14,R15        GO TO IT                            UF006
         MVI   SLID#,0        CLEAR ID NUMBER                     UF035
******************************************************************UF035
*                                                                 UF035
*        SPECIAL PROCESSING IF THE COMMAND WAS SYSLOG.            UF035
*                                                                 UF035
******************************************************************UF035
         CLC   =C'SL',QSUBNAME SYSLOG COMMAND?                    UF035
         BNE   SLZ            NO, BACK TO NORMAL PROCESSING       UF035
         ICM   R1,3,QLNG1     WAS A PARAMETER SPECIFIED?          UF035
         BZ    SLTHIS         NO, WANT THIS SYSTEMS SYSLOG        UF035
         CLI   QPARM1,C'0'    NUMERIC JOB NUMBER?                 UF035
         BNL   SLZ            YES, MUST BE THE NUMBER - DO NORMAL UF035
         CLC   QPARM1,=CL8'SYSLOG' WANT THIS SYSTEMS SYSLOG?      UF035
         BE    SLTHIS         YES, DO IT                          UF035
SLIDPRE  LA    R1,QSYSID      POINT TO SYSID TABLE                UF035
         LR    R14,R1         POINT TO SYSID TABLE                UF035
         LA    R15,7          MAX NUMBER OF SYSTEMS               UF035
SLTESTID LA    R1,8(,R1)      BUMP TO NEXT ENTRY                  UF035
         CLC   QPARM1(4),0(R1) DOES PARM MATCH SYSTEM ID?         UF035
         BE    SLGOTID        YES, GET OUT OF LOOP                UF035
         BCT   R15,SLTESTID   LOOP FOR ALL ID'S                   UF035
         B     SLZ            NOT FOUND - DO NORMAL PROCESSING    UF035
SLGOTID  SR    R1,R14         OFFSET TO ENTRY                     UF035
         SRL   R1,3           DIVIDE BY 8                         UF035
         STC   R1,SLID#       SAVE FOR LATER TEST                 UF035
         MVC   QPARM1,=CL8'SYSLOG' SET JOBNAME TO MATCH ON        UF035
         MVI   QLNG1+L'QLNG1-1,8   SET LENGTH OF NAME             UF035
         B     SLZ            DONE FOR NOW                        UF035
SLTHIS   DS    0H             NEED TO RETURN THIS SYSTEMS SYSLOG  UF035
         L     R1,16          POINT TO CVT                        UF035
         ICM   R1,15,CVTSMCA-CVTMAP(R1) POINT TO SMF CONTROL AREA UF035
         BZ    SLZ            NONE, DO NORMAL PROCESSING          UF035
         MVC   QPARM1(4),SMCASID-SMCABASE(R1)  MOVE SID TO AREA   UF035
         B     SLIDPRE        AND GO TO COMMON CODE               UF035
SLZ      DS    0H                                                 UF035
***********************************************************************
*                                                                     *
*   DETERMINE IF SEARCH IS BY JOBNUMBER OR JOBNAME                    *
*                                                                     *
***********************************************************************
         LH    R1,QLNG1       LENGTH OF PARAMETER FIELD
         SH    R1,=H'1'       IS THE LENGTH ZERO?
         BM    TILT           YES. TILT.
         CLI   QPARM1,C'0'    IS THE FIRST CHARACTER NUMERIC?
         BL    JOBNAME        NO. SEARCH BY JOBNAME
         MVC   QFZONES,QFZONE INITIALIZE NUMERIC TEST
         EX    R1,MVZ         MOVE THE ZONES FOR VALIDITY CHECK
         CLC   QFZONES,QFZONE IS THE FIELD NUMERIC?
         BNE   TILT           NO. TILT.
         EX    R1,PACK        PACK THE FIELD
         CVB   R5,CONVERT     CONVERT TO BINARY
***********************************************************************
*                                                                     *
*   LOCATE JQE BY JOB NUMBER                                          *
*                                                                     *
***********************************************************************
JOBNUM   DS    0H
         C     R5,=AL4($MAXJBNO)  IS JOB NUMBER VALID...?
         BH    TILT
         L     R3,QCJIXA      GET ADDRESS OF JOB INDEX
         SLL   R5,1           CONVERT JOB NUMBER TO OFFSET
         LH    R9,0(R5,R3)    GET JQE POSITION
         LTR   R9,R9          DOES JQE EXIST...?
         BZ    TILT
         MH    R9,=AL2(JQELNGTH) CONVERT POSITION TO OFFSET
         A     R9,QCJQTA      CONVERT OFFSET TO ADDRESS
         B     FOUND          ....JQE FOUND
***********************************************************************
*                                                                     *
*   LOCATE JQE BY JOBNAME                                             *
*                                                                     *
***********************************************************************
         USING JQEDSECT,R9
JOBNAME  LA    R3,QBATTAB     LOAD ADDR OF CLASS TABLE
JN1      BAL   R2,NEXTJQT     SET UP TO PROCESS QUEUE
         BAL   R2,SRCHJQE     FIND A JQT TO CHECK
         B     JN1            END OF QUEUE ...TRY NEXT
         CLC   QPARM1,JQEJNAME
         BNE   REJJQE
         B     FOUND
CHECKSYS LA    R9,$TSU
         BAL   R2,SRCHJQE     FIND A JQT TO CHECK
         B     JSTC           END OF QUEUE ...TRY NEXT
         CLC   QPARM1,JQEJNAME
         BNE   REJJQE
         B     FOUND
JSTC     LA    R9,$STC
         BAL   R2,SRCHJQE     FIND A JQT TO CHECK
         B     JOUT           END OF QUEUE ...TRY NEXT
         CLC   QPARM1,JQEJNAME
         BNE   REJJQE
         B     FOUND
JOUT     LA    R9,$OUTPUT
         BAL   R2,SRCHJQE     FIND A JQT TO CHECK
         B     JXEQ           END OF QUEUE ...TRY NEXT
         CLC   QPARM1,JQEJNAME
         BNE   REJJQE
         B     FOUND
JXEQ     LA    R9,$XEQ
         BAL   R2,SRCHJQE     FIND A JQT TO CHECK
         B     TILT           END OF QUEUE ...TRY NEXT
         CLC   QPARM1,JQEJNAME
         BNE   REJJQE
         B     FOUND
TILT     QTILT '*** JOBNAME NOT FOUND OR INVALID ***'
         SPACE 1                                                  UF007
JOBAGAIN L     R3,QCJQEA      PRIME JQE ADDRESS                   UF007
         LTR   R3,R3          ANYONE HOME?                        UF034
         BNZ   FOUND          YES, CONTINUE                       UF034
         QTILT '*** YOU MUST SPECIFY A JOBNAME OR NUMBER BEFORE USING "+
               *" ***'                                            UF034
***********************************************************************
*                                                                     *
*   SET UP TO PROCESS JOB CLASS QUEUE OF JQE'S                        *
*                                                                     *
***********************************************************************
NEXTJQT  SLR   R9,R9
         IC    R9,1(R3)
         CLI   1(R3),X'FF'    IS THIS THE LAST QUEUE?
         BE    CHECKSYS       YES....GO CHECK SYSTEM QUEUES
NEXTJQT2 STC   R9,QCLASS      MOVE CLASS NAME TO QCLASS
         LA    R3,1(R3)       BUMP POINTER FOR NEXT TIME
         N     R9,=A(QUECLASS) CHANGE CHAR TO INDEX
         BR    R2             RETURN TO CALLER
***********************************************************************
*                                                                     *
*   SEARCH A JQE QUEUE                                                *
*     ON ENTRY R9 = QUEUE TYPE TO SEARCH                              *
***********************************************************************
SRCHJQE  DS    0H
         IC    R9,QJQETAB(R9) GET OFFSET TO Q-HEAD
         LTR   R9,R9          IF NON-EXISTANT
         BZR   R2             TREAT AS END OF QUEUE. RETURN.
         LR    R0,R3          SAVE QUEUE CLASS POINTER
         L     R3,QCMSTA      POINT TO CKPT MASTER RECORD
         USING $SAVEBEG,R3
         LA    R9,$JQHEADS-$JQHEADL-QUENEXT(R9)  <-- HEAD OFFSET
         LR    R3,R0          RESTORE CLASS POINTER
         DROP  R3
         L     R9,QUENEXT(,R9) OFFSET TO NEXT JQE
NEXTJQE  N     R9,=X'00FFFFFF' TEST FOR END OF QUEUE
         BZR   R2             END OF QUEUE. RETURN.
         A     R9,QCJQTA      ADD BASE TO OFFSET
         B     4(R2)          DETERMINE ELIGIBILITY
REJJQE   L     R9,JQENEXT     LOAD OFFSET TO NEXT JQE             UF020
         B     NEXTJQE        GET THE NEXT JQE
***********************************************************************
*                                                                     *
*   READ JCT AND IOT                                                  *
*                                                                     *
***********************************************************************
FOUND    ST    R9,QCJQEA      SAVE THE ADDRESS
         CLI   SLID#,0        LOOKING FOR A SPECIFIC SYSLOG?      UF035
         BE    FOUND1         NO, CONTINUE                        UF035
         IC    R14,SLID#      GET DESIRED ID                      UF035
         IC    R15,JQEFLAGS   GET SYSTEM ID OF THIS JOB           UF035
         N     R14,=A(X'0000000F') MASK OFF EXTRA STUFF           UF035
         N     R15,=A(X'0000000F') MASK OFF EXTRA STUFF           UF035
         CR    R14,R15        SEE IF IT IS THE ONE WE WANTED      UF035
         BNE   REJJQE         NOPE, TRY AGAIN                     UF035
FOUND1   DS    0H                                                 UF035
         LR    R4,R9          SAVE THE ADDRESS FOR COMPARE        RNB22
         MVC   QCTRAK,JQETRAK DISK ADDR OF JCT
         DROP  R9
         L     R3,QCJCTA      ADDR OF IOAREA FOR JCT
         LR    R1,R3          PARM FOR READSPC
         L     R15,=V(READSPC) ROUTINE TO READ HASPACE
         BALR  R14,R15        GO TO IT
         USING JCTSTART,R3    BASE REG FOR JCT
         USING JQEDSECT,R4    BASE FOR JQE                        RNB22
         CLI   QPARM1,C'*'         WAS REQUEST FOR CURRENT JOB?   RNB22
         BNE   RNB22A              /NO  - DON'T NEED EXTRA CHECK  RNB22
         CLC   JQEJNAME,JCTJNAME   IS JOBNAME RIGHT?              RNB22
         BNE   TILT                /NO  - TILT                    RNB22
         CLC   QPJOBID,JCTJBKEY    IS JOBID RIGHT?                RNB22
         BNE   TILT                /NO  - TILT                    RNB22
RNB22A   EQU   *                                                  RNB22
         MVC   QPJOBID,JCTJBKEY JOB IDENTIFICATION
         AIF    (NOT &QACF2).NACF2                                 FCI*
*******************************************************************FCI*
* MOD 1 - K TRUE  - 22 OCT 79 -                                    FCI*
*                                                                  FCI*
         CLC   QLOGON,LIDLID  SEE IF USERS LOGONID=JOBS ACF LOGONIDKMT*
         BE    ACF2OK         YES..CONTINUE FORTHWITH              FCI*
         TM    QXAUTH,X'80'   IS USER OPER PRIVLEDGE ?             FCI*
         BO    ACF2OK         YES..CONTINUE FORTHWITH              FCI*
*                                                                  FCI*
         USING ACCVT,R8                                            FCI*
         ACFGACVT R8,NONE=NOTOK   GET THE ACF2 CVT                 FCI*
*                                                                  FCI*
         MVC   DSNAME,=CL44'SYSOUT. '  INITIALIZE DSNAME TO USE    FCI*
         XC    ACFSPARM(ACFSPRML),ACFSPARM   CLEAR REQUEST BLOCK   FCI*
         MVI   ACFSPREQ,ACFSPRDS  DSNAME ACCESS ONLY               FCI*
         MVI   ACFSPID1,ACFSPIUR  DIS AM DE USER TALKING....       FCI*
         LA    R7,DSNAME          GET DSNAME ADDRESS               FCI*
         ST    R7,ACFSPDSN        AND GIVE IT TO ACF PARM LIST     FCI*
*                                                                  FCI*
*  GENERATE DSNAME OF FORMAT 'SYSOUT.LOGONID.JOBNAME' FOR CHKING   FCI*
*                                                                  FCI*
         MVC   DSNAME+7(8),LIDLID  MOVE LID TO DSNAME              FCI*
         CLI   DSNAME+7,C' '       IS THE LID BLANK?               FCI*
         BNE   ADSNCHK0            NO..NORMAL PROCESS              FCI*
*                                                                  FCI*
*  GOT HERE BECAUSE LID IS ' ' (BLANK)..SUBSTITUTE 'SYSTEM'        FCI*
         MVC   DSNAME+7(8),=CL8'SYSTEM'                            FCI*
*                                                                  FCI*
ADSNCHK0 LA    R1,DSNAME+7         GET ADDRESS                     FCI*
         LA    R7,8                LOAD COUNT                      FCI*
ADSNCHK  CLI   0(R1),C' '          LOOK FOR BLANK                  FCI*
         BE    ADSNCHK1            GOTIT..                         FCI*
         LA    R1,1(R1)            BUMP AND                        FCI*
         BCT   R7,ADSNCHK            GRIND                         FCI*
ADSNCHK1 MVI   0(R1),C'.'          MOVE IN PERIOD..                FCI*
         MVC   1(8,R1),JCTJNAME    MOVE IN JOBNAME                 FCI*
*                                                                  FCI*
         LA    R1,ACFSPARM        GET ADDRESS OF PARM FIELD        FCI*
         ACFSVC (1),TYPE=S,NONE=NOTOK,CVT=HAVE   INVOKE A C F 2    FCI*
*                                                                  FCI*
         LTR   R15,R15            HOW DID YOU LIKE THEM APPLES?    FCI*
         BC    8,ACF2OK           ..OK BY YOU...CONTINUE..         FCI*
*                                                                  FCI*
NOTOK    QTILT '*** SORRY..NO ACF2 AUTHORITY TO LOOK AT THIS JOB'  FCI*
*******************************************************************FCI*
ACF2OK   DS    0H                                                  FCI*
.NACF2   ANOP                                                      FCI*
         MVC   QCTRAK,JCTIOT  DISK ADDR OF IOT
         DROP  R3
         L     R1,QCIOTA      ADDR OF IOAREA FOR IOT
         L     R15,=V(READSPC) ROUTINE TO READ HASPACE
         BALR  R14,R15        GO TO IT
STOP     QSTOP
***********************************************************************
*                                                                     *
*   MISCELLANY                                                        *
*                                                                     *
***********************************************************************
MVZ      MVZ   QFZONES(1),QPARM1 CHECK FOR NUMERIC
PACK     PACK  CONVERT,QPARM1(1) CONVERT TO BINARY
         COPY  QLOCTAB
         LTORG
SYMDEL   DSECT ,                   KILL SYM CARD GENERATION       UF023
FINDJOB  CSECT ,                                                  UF023
JCT      EQU   0
*UFSTART EQU   0                   MOVED                          TEC01
*UFDSECT EQU   0                      UP                          TEC01
         AIF   (&QSP33).QSP33                                     TEC01
         $JQE
         $JCT
         $JQT
.QSP33   ANOP                                                     TEC01
         CVT   DSECT=YES                                          UF035
         IEESMCA ,                                                UF035
         QCOMMON
WORK     DSECT
         DS    72C
CONVERT  DS    D
SLID#    DS    X                   SYSTEM ID OF DESIRED SYSLOG    UF035
         AIF    (NOT &QACF2).NACF3                                 FCI*
         ACDSV DSECT=NO                                            FCI*
DSNAME   DS    CL44                                                FCI*
         EJECT   ,                                                 FCI*
         ACCVT   ,                                                 FCI*
         ACUCB   ,                                                 FCI*
         PRINT OFF                                                 FCI*
         IHAPSA  ,                                                 FCI*
         PRINT ON                                                  FCI*
.NACF3   ANOP   ,                                                  FCI*
SYMNODEL DSECT ,                   RESTORE SYM CARD GENERATION    UF023
         END
