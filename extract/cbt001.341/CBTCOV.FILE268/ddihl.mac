      SUBROUTINE  DDIHL (X0,Y0,X1,Y1,VISBLE)
C
C     MODIFIED FOR USE WITH ABE
C
C     CALLS DDIGL
C
C     (DRAW HEAVY LINE)
C     GENERATE A THICK LINE BY MAKING MULTIPLE STROKES.  IN ORDER
C     TO SIMULATE A THICK PEN, THE LINE DRAWN SHOULD HAVE
C     CIRCULAR, RATHER THAN SQUARE, END POINTS.  IF THIS IS NOT
C     DONE, THICK LETTERS AND OTHER FIGURES HAVE AN ABNORMAL AND
C     UNPLEASANT APPEARANCE.  THE DESIRED LINE IS TRACED
C     IN ALTERNATING DIRECTIONS, OFFSETTING ONE PEN STEP EACH TIME.
C     THE SWEEPS ARE THEN IN THE ORDER 6 4 2 1 3 5, THE FIRST ONE
C     LYING ALONG THE INPUT VECTOR, THE SECOND TO THE LEFT AND SO
C     ON.  WE COMPUTE (VX,VY) AS A VECTOR OF LENGTH EQUAL TO ONE
C     PLOT STEP IN THE DIRECTION OF THE INPUT VECTOR, AND (PX,PY)
C     AS A VECTOR OF LENGTH ONE PLOT STEP PERPENDICULAR TO (VX,VY).
C     FOR SWEEP K, THE VECTOR DRAWN IS EXTENDED AT EACH END BY THE
C     AMOUNT H(K), WHICH IS THE DISTANCE TO THE CIRCUMFERENCE OF
C     THE TERMINAL SEMICIRCLE REPRESENTING THE PEN NIB, MEASURED
C     FROM A PERPENDICULAR TO THE INPUT VECTOR AT THE ENDPOINTS.
C
      LOGICAL          VISBLE
      REAL             DX,          DY,          FLOAT,       H
      REAL             HK,          PX,          PXK,         PY
      REAL             PYK,         SQRT,        VLEN,        VX
      REAL             VXK,         VY,          VYK,         X0
      REAL             X1,          Y0,          Y1
      INTEGER          K,           KARG
      LOGICAL          SINGLE
C
      INCLUDE (DDI01)
C
      H(KARG) = SQRT(FLOAT(LINEWT*LINEWT/4 - KARG*KARG/4))
      IF (.NOT.VISBLE) GO TO 20
C
C     IF ONLY ONE LINE IS REQUIRED, DRAW A LINE SEGMENT FROM
C     (X0,Y0) TO (X1,Y1).
C
      DX = X1 - X0
      DY = Y1 - Y0
      VLEN = SQRT(DX**2 + DY**2)
      SINGLE = (VLEN .EQ. 0.0) .OR. (LINEWT .LT. 2)
      IF (SINGLE) CALL DDIGL (X0,Y0,X1,Y1,VISBLE)
      IF (SINGLE) GO TO 20
C
C     MULTIPLE LINES ARE REQUIRED.  THE FIRST ONE IS ALONG THE
C     INPUT VECTOR, THE REMAINING ONES ARE DRAWN ON OPPOSITE
C     SIDES OF THE CENTRAL LINE, SWEEPING IN ALTERNATE DIRECTIONS.
C
      VX = DX/(VLEN*SX)
      VY = DY/(VLEN*SY)
      PX = VY
      PY = -VX
      HK = H(1)
      VXK = VX*HK
      VYK = VY*HK
      CALL DDIGL (X0-VXK,Y0-VYK,X1+VXK,Y1+VYK,VISBLE)
           DO 10 K = 2,LINEWT,2
           HK = H(K)
           PXK = PX*FLOAT(K/2)
           PYK = PY*FLOAT(K/2)
           VXK = VX*HK
           VYK = VY*HK
           CALL DDIGL (X1+PXK+VXK,Y1+PYK+VYK,X0+PXK-VXK,Y0+PYK-VYK,
     X                 VISBLE)
           IF (K .LT. LINEWT) CALL DDIGL (X0-PXK-VXK,Y0-PYK-VYK,
     X                                X1-PXK+VXK,Y1-PYK+VYK,VISBLE)
   10      CONTINUE
   20 RETURN
      END
