      SUBROUTINE  DDIIL (IX0,IY0,IX1,IY1,VISBLE)
C
C     MODIFIED FOR USE WITH ABE
C
C     CALLS DDISB
C
C     (INCREMENTAL LINE)
C     FILL IN THE STEPS ON THE PLOT FRAME BETWEEN THE POINT
C     (IX0,IY0) AND (IX1,IY1), WHERE IX0, IY0, IX1, AND IY1 ARE
C     IN ACTUAL RASTER UNITS FOR THE DEVICE BEING USED.  IF A
C     VECTOR PLOT DEVICE IS TO BE USED, THIS ROUTINE MIGHT
C     INSTEAD SIMPLY TRANSMIT A SINGLE VECTOR TO A BUFFER.
C
      LOGICAL          VISBLE
      INTEGER          DELTA,       REM,         SLOPE
      INTEGER          I,           IABS,        ICX,         ICY
      INTEGER          IDX,         IDXABS,      IDY,         IDYABS
      INTEGER          IX0,         IX1,         IY0,         IY1
      INTEGER          KX,          KY,          MAX0,        MIN0
C
      INCLUDE (DDI03)
C
      IF (.NOT.VISBLE) GO TO 60
      NUMSEG = NUMSEG + 1
      IYMAX = MAX0(IY0,IY1,IYMAX)
      IYMIN = MIN0(IY0,IY1,IYMIN)
      KX = IX0
      KY = IY0
      CALL DDISB (KX,KY)
      BITSON = BITSON + 1
      IDX = IX1 - IX0
      IDY = IY1 - IY0
      IF ((IDX .EQ. 0) .AND. (IDY .EQ. 0)) GO TO 60
      ICX = +1
      IF (IDX .LE. 0) ICX = -1
      ICY = +1
      IF (IDY .LE. 0) ICY = -1
      IDXABS = IABS(IDX)
      IDYABS = IABS(IDY)
      IF (IDXABS .LE. IDYABS) GO TO 30
C
C     X IS GREATER.
C
      REM = IDXABS
      DELTA = IDXABS + IDXABS
      SLOPE = IDYABS + IDYABS
           DO 20 I = 1,IDXABS
           REM = REM + SLOPE
           IF (REM .LT. DELTA) GO TO 10
           KY = KY + ICY
           REM = REM - DELTA
   10      KX = KX + ICX
   20      CALL DDISB (KX,KY)
      BITSON = BITSON + IDXABS
      GO TO 60
C
C     Y IS GREATER, OR (X0,Y0) = (X1,Y1), IN WHICH CASE WE RETURN.
C
   30 IF (IDY .EQ. 0) GO TO 60
      REM = IDYABS
      DELTA = IDYABS + IDYABS
      SLOPE = IDXABS + IDXABS
           DO 50 I = 1,IDYABS
           REM = REM + SLOPE
           IF (REM .LT. DELTA) GO TO 40
           KX = KX + ICX
           REM = REM - DELTA
   40      KY = KY + ICY
   50      CALL DDISB (KX,KY)
      BITSON = BITSON + IDYABS
   60 RETURN
      END
