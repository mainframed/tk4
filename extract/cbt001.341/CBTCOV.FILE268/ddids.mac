      SUBROUTINE  DDIDS (WIDTH,HEIGHT,DEPTH)
C
C     MODIFIED FOR USE WITH ABE
C
C     CALLS DDISZ
C
C     (NORMALIZED DEVICE COORDINATE SPACE)
C     DEFINE THE NORMALIZED DEVICE COORDINATE SPACE TO BE A
C     BOX OF SIZE (WIDTH,HEIGHT,DEPTH), WHERE EACH OF THE
C     COORDINATES LIES IN THE RANGE (0.0,1.0).  THE NDC SPACE
C     IS LEFT-HANDED, WITH X POSITIVE TO THE RIGHT, Y POSITIVE
C     UPWARD, AND Z POSITIVE AWAY FROM THE OBSERVER.  THE VIEWING
C     VOLUME WILL BE MAPPED INTO THE NDC SPACE, AND THE RECTANGLE
C     OF SIZE (WIDTH,HEIGHT) WILL BE MAPPED ONTO THE VIEWING
C     SURFACE OF THE DISPLAY DEVICE SO THAT THE ENTIRE RECTANGLE
C     IS VISIBLE.  THIS IS SOMETIMES USEFUL IF THE DISPLAY SURFACE
C     IS NOT SQUARE, AND ONE WISHES TO MAXIMIZE THE SIZE OF THE
C     PLOT WHICH CAN BE FIT ON IT.  IF THE DEFAULT NDC SPACE OF THE
C     UNIT CUBE WERE IN EFFECT, THEN THE DEVICE DRIVER WOULD BE
C     FORCED TO MAP NDC SPACE INTO THE LARGEST SQUARE WHICH COULD
C     FIT ON THE DISPLAY SURFACE, THUS LEAVING PART OF THE DISPLAY
C     SURFACE UNUSED.
C
C     AT LEAST ONE OF (WIDTH,HEIGHT,DEPTH) MUST BE UNITY, AND NEITHER
C     WIDTH NOR HEIGHT MAY BE ZERO.
C
      REAL             ARG,         DEPTH,       HEIGHT,      SIZE
      REAL             WIDTH
      LOGICAL          ERROR,       NULL,        UNIT,        UNITR
C
      INCLUDE (DDI01)
C
      UNITR(ARG) = (0.0 .LE. ARG) .AND. (ARG .LE. 1.0)
      NULL(ARG) = (0.0+ARG) .EQ. 0.0
      UNIT(ARG) = (1.0+ARG) .EQ. 2.0
      ERROR = (.NOT.UNITR(WIDTH)) .OR. (.NOT.UNITR(HEIGHT))
     X        .OR. (.NOT.UNITR(DEPTH))
      ERROR = ERROR .OR. NULL(WIDTH) .OR. NULL(HEIGHT)
      ERROR = ERROR .OR. (.NOT.UNIT(WIDTH) .AND. .NOT.UNIT(HEIGHT))
      IF (ERROR) GO TO 10
      XMAX = WIDTH
      YMAX = HEIGHT
      ZMAX = DEPTH
      SIZE = DSSIZE
      CALL DDISZ (SIZE)
   10 RETURN
      END
