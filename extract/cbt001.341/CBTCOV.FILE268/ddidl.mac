      SUBROUTINE  DDIDL (X0,Y0,Z0,X1,Y1,Z1,VISBLE)
C
C     MODIFIED FOR USE WITH ABE
C
C     CALLS DDIHL,DDIIL
C
C     (DRAW LINE)
C     DRAW A LINE BETWEEN THE POINTS (X0,Y0,Z0) AND (X1,Y1,Z1),
C     WHERE THE COORDINATES ARE EXPRESSED IN NORMALIZED DEVICE
C     UNITS (0..1), AND HAVE BEEN CLIPPED TO GUARANTEE THAT THEY
C     DO NOT EXCEED THIS RANGE.  THE Z COORDINATES ARE IGNORED
C     UNLESS AN IMAGE TRANSFORMATION IS IN EFFECT.
C
C     TO ALLOW CONVENIENT GENERATION OF MULTIPLE THICKNESSES, THE
C     ACTUAL POINT GENERATION IS RELEGATED TO SUBROUTINE DDIIL.
C     TO SIMULATE VARIABLE LINE WIDTHS, THICK LINES ARE TRACED
C     WITH ALTERNATING SWEEPS, THE PEN BEING OFFSET ONE PLOT STEP
C     BETWEEN LINES.
C
      LOGICAL          VISBLE
      INTEGER          IFIX,        MX0,         MX1,         MY0
      INTEGER          MY1,         ROUND
      REAL             ARG,         TEMP,        X0,          X1
      REAL             XX0,         XX1,         Y0,          Y1
      REAL             YY0,         YY1,         Z0,          Z1
C
      INCLUDE (DDI01)
C
      ROUND(ARG) = IFIX(ARG + 0.5)
      IF (.NOT.VISBLE) GO TO 50
      IF (.NOT.IMAGE) GO TO 10
      XX0 = X0*TIMAGE(1,1) + Y0*TIMAGE(2,1) + Z0*TIMAGE(3,1) +
     X      TIMAGE(4,1)
      YY0 = X0*TIMAGE(1,2) + Y0*TIMAGE(2,2) + Z0*TIMAGE(3,2) +
     X      TIMAGE(4,2)
      XX1 = X1*TIMAGE(1,1) + Y1*TIMAGE(2,1) + Z1*TIMAGE(3,1) +
     X      TIMAGE(4,1)
      YY1 = X1*TIMAGE(1,2) + Y1*TIMAGE(2,2) + Z1*TIMAGE(3,2) +
     X      TIMAGE(4,2)
      GO TO 20
   10 XX0 = X0
      YY0 = Y0
      XX1 = X1
      YY1 = Y1
C
C     IF A VERTICAL FRAME IS IN EFFECT, TRANSLATE AND ROTATE THE
C     COORDINATES TO PLACE THE VERTICAL FRAME HORIZONTAL, AVOIDING
C     THE PAPER CREASE.
C
   20 IF (HORZNT) GO TO 30
      TEMP = XX0
      XX0 = YY0
      YY0 = XMAX - TEMP
      TEMP = XX1
      XX1 = YY1
      YY1 = XMAX - TEMP
   30 IF (LINEWT .LE. 1) GO TO 40
      CALL DDIHL (XX0,YY0,XX1,YY1,VISBLE)
      GO TO 50
   40 MX0 = ROUND(SX*XX0) + 1
      MY0 = ROUND(SY*YY0) + 1
      MX1 = ROUND(SX*XX1) + 1
      MY1 = ROUND(SY*YY1) + 1
      CALL DDIIL (MX0,MY0,MX1,MY1,VISBLE)
   50 RETURN
      END
