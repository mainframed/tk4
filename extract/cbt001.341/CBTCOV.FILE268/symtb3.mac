      SUBROUTINE  SYMTB3 (TEXT,LOC,LEN,PL3,H,PENDIA)
C     (Boldface Text)
C     Generate boldface letters  by repeatedly  redrawing a  text
C     string, moving  the origin  about  the circumference  of  a
C     small circle whose diameter is that of the simulated  thick
C     pen.
C     (24-APR-82)
      EXTERNAL         PL3
      INTEGER          I,           IABS,        IFIX,        LEN
      INTEGER          LOC,         MAX0,        MAXTIM,      MIN0
      INTEGER          NTIMES,      TEXT(1)
      REAL             ALPHA,       COSD,        DIALET,      DIAPEN
      REAL             FLOAT,       H,           HALF,        PENDIA
      REAL             PI,          R,           SIND,        THETA
      REAL             UMAX,        UMIN,        VMAX,        VMIN
      REAL             X,           XX,          Y,           YY
      REAL             Z
      DATA MAXTIM/21/, HALF/0.5/, PI/3.14159/
      CALL INQWW (UMIN,UMAX,VMIN,VMAX)
      DIAPEN = PENDIA*(UMAX - UMIN)
      DIALET = H/20.0
      NTIMES = 1
      IF (DIAPEN .GT. 0.0) NTIMES = IFIX(PI*DIALET/DIAPEN)
      NTIMES = MAX0(1,MIN0(MAXTIM,2*(IABS(NTIMES)/2)+1))
      R = DIALET*HALF
      CALL INQCC (X,Y,Z)
           DO 10 I = 1,NTIMES
           ALPHA = 0.0
           IF (NTIMES .GT. 1) ALPHA = 360.0*FLOAT(I-1)/FLOAT(NTIMES-1)
           XX = X + R*COSD(ALPHA)
           YY = Y + R*SIND(ALPHA)
           CALL PL3 (XX,YY,Z,.FALSE.)
           CALL SYMTX3 (TEXT,LOC,LEN,PL3)
   10      CONTINUE
C
      RETURN
      END
