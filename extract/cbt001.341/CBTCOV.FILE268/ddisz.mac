      SUBROUTINE  DDISZ (SIZECM)
C
C     MODIFIED FOR USE WITH ABE
C
C     CALLS ERRMS,ERRAR,ERRCK
C
C     (DEVICE SIZE)
C     DEFINE THE SIZE OF THE DEVICE SURFACE ONTO WHICH THE
C     NORMALIZED DEVICE SPACE IS TO BE MAPPED.  THE SIZE OF THE
C     UNIT SQUARE IN CENTIMETERS IS GIVEN BY SIZECM.  NOTE THAT
C     1 INCH = 2.54 CM EXACTLY, SO THIS ROUTINE MAY BE CALLED WITH
C     ARGUMENT (2.54*(SIZE IN INCHES)).  THE REQUEST FOR A SPECIFIC
C     SIZE WILL BE HONORED IF POSSIBLE, BUT IF TOO LARGE A SIZE IS
C     REQUESTED, THE PLOT WILL BE SCALED DOWN IN ORDER TO FIT THE
C     REQUESTED NDC SPACE ON THE PAGE.  HOWEVER, NO ERROR MESSAGE
C     WILL BE ISSUED IN SUCH A CASE.
C
      INTEGER          IFIX,        MIN0,        NEEDX,       NEEDY
      INTEGER          NXDIM,       NYDIM
      REAL             AMIN1,       ERVAL(1),    FLOAT,       SIZECM
C
      INCLUDE (DDI01)
C
      IF (4.0 .LE. SIZECM) GO TO 10
      CALL ERRMS (6HDDISZ ,8,29HINVALID DEVICE SIZE SPECIFIED,29)
      ERVAL(1) = SIZECM
      CALL ERRAR (6HSIZECM,1,ERVAL,1)
      CALL ERRCK
      GO TO 20
   10 DSSIZE = AMIN1(2.54*PLMAXX/PLXDPI,SIZECM)
      PLSTPX = 2.54/PLXDPI
      PLSTPY = 2.54/PLYDPI
      HORZNT = (XMAX .GE. YMAX)
C
C     COMPUTE THE NUMBER OF BITS NEEDED IN THE X AND Y DIRECTIONS
C     FOR A UNIT SQUARE.
C
      NEEDX = IFIX(DSSIZE/PLSTPX)
      NEEDY = IFIX(DSSIZE/PLSTPY)
      SX = FLOAT(MIN0(NEEDX,PLMAXX-1))
      SY = FLOAT(MIN0(NEEDY,PLMAXY-1))
C
C     IF THE DEVICE SPACE HAS BEEN REDUCED, THE ACTUAL NUMBER OF
C     BITS, MAXBTX AND MAXBTY, REQUIRED IN THE X AND Y DIRECTIONS
C     CAN BE REDUCED ACCORDINGLY.  IN THE CASE OF A VERTICAL FRAME,
C     THE X AND Y COORDINATES WILL BE EXCHANGED BEFORE CALLING FOR
C     LINE GENERATION, SO THAT MAXBTX AND MAXBTY ARE REDUCED BY
C     FACTORS YMAX AND XMAX RESPECTIVELY.
C
      IF (     HORZNT) MAXBTX = IFIX(XMAX*SX+0.5) + 1
      IF (     HORZNT) MAXBTY = IFIX(YMAX*SY+0.5) + 1
      IF (.NOT.HORZNT) MAXBTX = IFIX(YMAX*SX+0.5) + 1
      IF (.NOT.HORZNT) MAXBTY = IFIX(XMAX*SY+0.5) + 1
   20 RETURN
      END
