LISTGDG  TITLE 'LISTGDGC - CONTROLLER MODULE FOR LISTGDGP'
         SPACE
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
* THIS IS THE CONTROL MODULE FOR THE LISTGDG COMMAND THAT WILL LIST   *
* OR CHANGE THE GDG-BASE ENTRY IN AN ICF CATALOG.                     *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
* THIS PROGRAM DE-ALLOCATES THE FOUR DD NAMES ALLOCATED FOR THE       *
* LISTGDGP PROGRAM, ALLOCATES TWO DD NAMES TO VIO FILES AND TWO       *
* DD NAMES TO THE TERMINAL AND LINKS TO LISTGDGP TO ACT AS THE REAL   *
* COMMAND TO PROCESS THE REQUESTS.  UPON RETURN FROM LISTGDGP THIS    *
* MODULE WILL DE-ALLOCATE THE FOUR DD NAMES PREVIOUSLY ALLOCATED.     *
* THIS MODULE AND THE LISTGDGP MODULE SHOULD BE LINKED INTO           *
* SYS1.CMDLIB .  THIS MODULE SHOULD BE NAMED LISTGDG OR SOME          *
* APPROPRIATE NAME.  LISTGDGP AND THE DDNAMES SHOULD REMAIN THE SAME  *
* UNLESS CHANGES ARE MADE IN BOTH THIS MODULE AND LISTGDGP.           *
* NEITHER THIS MODULE NOR LISTGDGP ARE WRITTEN TO BE RE-ENTRANT.      *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         SPACE
LISTGDGC CSECT
         SPACE
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3  - BASE REGISTER
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10 - LINK REGISTER TO DE ALLOCATE ROUTINE
R11      EQU   11 - WORK REGISTER
R12      EQU   12 - WORK REGISTER
R13      EQU   13
R14      EQU   14
R15      EQU   15
         SPACE 2
         SAVE  (14,12),,LISTGDGC.TEST.&SYSTIME..&SYSDATE
         LR    R3,R15        ESTABLISH BASE REGISTER
         USING LISTGDGC,R3   AND ADDRESSABILITY
         SPACE
         LA    R10,REGAREA
         ST    R13,4(R10)    CHAIN SAVE AREA ADDRESSES
         ST    R10,8(R13)
         LR    R13,R10
         EJECT
         BAL   R10,DEALLOC   BAL TO DE-ALLOCATE DD NAMES
         SPACE
* ALLOCATE DDNAME IDCAMIN TO VIO DATA SET
         SPACE
         MVC   RB1TU1+6(8),DDNAME01
         MVC   RB1TU2+6(8),DSNAME01
         LA    R1,APRB1      LOAD PARAMETER POINTER FOR SVC99
         DYNALLOC            ISSUE SVC99
         SPACE
         LTR   R15,R15       CHECK SVC99 RETURN CODE
         BNZ   ABEND
         SPACE
* ALLOCATE DDNAME IDCAMOU TO VIO DATA SET
         SPACE
         MVC   RB1TU1+6(8),DDNAME02
         MVC   RB1TU2+6(8),DSNAME02
         LA    R1,APRB1      LOAD PARAMETER POINTER FOR SVC99
         DYNALLOC            ISSUE SVC99
         SPACE
         LTR   R15,R15       CHECK SVC99 RETURN CODE
         BNZ   ABEND
         SPACE
* ALLOCATE DDNAME GDGLINP TO THE TERMINAL
         SPACE
         MVC   RB2TU1+6(8),DDNAME03
         LA    R1,APRB2      LOAD PARAMETER POINTER FOR SVC99
         DYNALLOC            ISSUE SVC99
         SPACE
         LTR   R15,R15       CHECK SVC99 RETURN CODE
         BNZ   ABEND
         SPACE
* ALLOCATE DDNAME GDGLOUT TO THE TERMINAL
         SPACE
         MVC   RB2TU1+6(8),DDNAME04
         LA    R1,APRB2      LOAD PARAMETER POINTER FOR SVC99
         DYNALLOC            ISSUE SVC99
         SPACE
         LTR   R15,R15       CHECK SVC99 RETURN CODE
         BNZ   ABEND
         SPACE
         LINK  EP=LISTGDGP,PARAM=(NULLPARM)
         SPACE
         BAL   R10,DEALLOC   BAL TO DE-ALLOCATE DD NAMES
         SPACE
RETURN   L     R13,4(R13)
         RETURN (14,12),RC=0 RETURN FROM WHENCE WE CAME
         EJECT
DEALLOC  LA    R12,4        LOAD NUMBER OF DDNAMES TO DE-ALLOCATE
         LA    R11,DDNAME01 LOAD ADDRESS OF FIRST NAME TO DEALLOC
DEALLOOP MVC   RB3TU1+6(8),0(R11) MOVE DDNAME TO T.U. BLOCK
         LA    R1,APRB3     LOAD ADDRESS OF R.B. POINTER
         DYNALLOC           ISSUE SVC99
         LTR   R15,R15      CHECK SVC99 RETURN CODE
         BZ    *+8          0 COND CODE OK
         BAL   R14,ALLOCERR BAL TO CHECK TO SEE IF ERR IS OK
         LA    R11,8(R11)   INCREMENT TO NEXT DDNAME
         BCT   R12,DEALLOOP BCT BACK TO START OF LOOP
         BR    R10          RETURN TO CALLER
         SPACE
ALLOCERR C     R15,=F'4'    SEE IF COND CODE 4
         BNE   ABEND        ANY OTHER CODE IS REAL BAD
         CLC   RB3+4(2),=X'0438' SEE IF NOT ALLOCATED
         BER   R14          NOT ALLOCATED IS STILL GOOD.
ABEND    ABEND 3999,DUMP    REAL PROBLEMS - TELL THE WORLD
         EJECT
         SPACE
DDNAME01 DC    C'IDCAMIN '  ***  THESE FOUR DDNAMES MUST REMAIN
DDNAME02 DC    C'IDCAMOU '    *  CONTIGUOUS FOR THE DEALLOC ROUTINE
DDNAME03 DC    C'GDGLINP '    *  TO BE ABLE TO LOOP  THROUGH.
DDNAME04 DC    C'GDGLOUT '  ***
DSNAME01 DC    C'&&IDCAMIN'      TEMP DSNAMES FOR VIO DATA SETS
DSNAME02 DC    C'&&IDCAMOU'
         SPACE
REGAREA  DS    18F        REGISTER SAVE AREA
         SPACE
* DYNALLOC REQUEST BLOCKS FOR VIO ALLOCATION
         SPACE
APRB1    DS    0F         POINTER TO SVC99 REQUEST BLOCKS
         DC    X'80',AL3(RB1)
         SPACE
RB1      DS    0F         SVC 99 REQUEST BLOCKS
         DC    X'14014000'
         DC    X'00000000'
         DC    A(RB1P1)
         DC    X'00000000'
         DC    X'00000000'
         SPACE
RB1P1    DC    A(RB1TU1)  POINTER TO SVC99 TEXT UNIT 1
RB1P2    DC    A(RB1TU2)  POINTER TO SVC99 TEXT UNIT 2
RB1P4    DC    A(RB1TU4)  POINTER TO SVC99 TEXT UNIT 4
RB1P5    DC    A(RB1TU5)  POINTER TO SVC99 TEXT UNIT 5
RB1P8    DC    A(RB1TU8)  POINTER TO SVC99 TEXT UNIT 8
RB1PA    DC    A(RB1TUA)  POINTER TO SVC99 TEXT UNIT A
RB1PB    DC    A(RB1TUB)  POINTER TO SVC99 TEXT UNIT B
RB1P15   DS    0F         POINTER TO SVC99 TEXT UNIT 15
         DC    X'80',AL3(RB1TU15)
         SPACE
RB1TU1   DC    H'1',H'1',H'8',C'DDNAME  '     DDNAME
RB1TU2   DC    H'2',H'1',H'8',C'DSNAME  '     DDNAME
RB1TU4   DC    H'4',H'1',H'1',X'04'           STATUS = NEW
RB1TU5   DC    H'5',H'1',H'1',X'04'           DISP = DELETE
RB1TU8   DC    H'8',H'0'                      CYLINDER
RB1TUA   DC    H'10',H'1',H'3',X'000001'      PRIMARY SPACE
RB1TUB   DC    H'11',H'1',H'3',X'000001'      SECONDARY SPACE
RB1TU15  DC    H'21',H'1',H'3',C'VIO'         UNIT
         SPACE
* DYNALLOC REQUEST BLOCKS FOR TERMINAL ALLOCATION
         SPACE
APRB2    DS    0F         POINTER TO SVC99 REQUEST BLOCKS
         DC    X'80',AL3(RB2)
         SPACE
RB2      DS    0F         SVC 99 REQUEST BLOCKS
         DC    X'14014000'
         DC    X'00000000'
         DC    A(RB2P1)
         DC    X'00000000'
         DC    X'00000000'
         SPACE
RB2P1    DC    A(RB2TU1)  POINTER TO SVC99 TEXT UNIT 1
RB2P4    DC    A(RB2TU4)  POINTER TO SVC99 TEXT UNIT 4
RB2P5    DC    A(RB2TU5)  POINTER TO SVC99 TEXT UNIT 5
RB2P28   DC    A(RB2TU28) POINTER TO SVC99 TEXT UNIT 28
RB2P52   DS    0F         POINTER TO SVC99 TEXT UNIT 52
         DC    X'80',AL3(RB2TU52)
         SPACE
RB2TU1   DC    H'1',H'1',H'8',C'DDNAME  '     DDNAME
RB2TU4   DC    H'4',H'1',H'1',X'04'           STATUS = NEW
RB2TU5   DC    H'5',H'1',H'1',X'04'           DISP = DELETE
RB2TU28  DC    H'40',H'0'                     TERMINAL
RB2TU52  DC    H'82',H'0'                     PERM ALLOCATION
         SPACE
* DYNALLOC REQUEST BLOCKS FOR DE-ALLOCATION
         SPACE
APRB3    DS    0F         POINTER TO SVC99 REQUEST BLOCKS
         DC    X'80',AL3(RB3)
         SPACE
RB3      DS    0F         SVC 99 REQUEST BLOCKS
         DC    X'14020000'
         DC    X'00000000'
         DC    A(RB3P1)
         DC    X'00000000'
         DC    X'00000000'
         SPACE
RB3P1    DC    A(RB3TU1)  POINTER TO SVC99 TEXT UNIT 1
RB3P7    DS    0F         POINTER TO SVC99 TEXT UNIT 7
         DC    X'80',AL3(RB3TU7)
         SPACE
RB3TU1   DC    H'1',H'1',H'8',C'DDNAME  '     DDNAME
RB3TU7   DC    H'7',H'0'                      UNALLOC
         SPACE 2
NULLPARM DC    H'0'
         SPACE
         LTORG
         SPACE
         END
