         MACRO
&NAME    BUF3270 &WCC=,&TYPE=
.********** S T A R T    O F    G L O B A L S / L O C A L S ***********
         GBLC  &DSECTNM           NAME OF SCREEN DSECT
         GBLC  &FLINE        LINE POSITION OF FIRST FIELD IN SCREEN
         GBLC  &FCOLMN     COLUMN POSITION OF FIRST FIELD IN SCREEN
         LCLA  &COUNT             DSECT NAME -1
.**********   E N D      O F    G L O B A L S / L O C A L S ***********
         AIF   ('&TYPE' EQ '').TYPERR
         AIF   ('&TYPE' EQ 'END').ENDBUF
         AIF   ('&TYPE' NE 'START').TYPERR
.STRTBUF ANOP
         AIF ('&NAME' EQ '').NAMERR
         AIF (K'&NAME GT 7).NAMERR
         AIF   ('&DSECTNM' NE '').ENDERR
&DSECTNM SETC  '&NAME.D'          SET SCREEN DSECT NAME
.START   ANOP
         SPACE 3
         AGO   .WCC
.NAMERR  ANOP
         MNOTE 12,'MACRO ''NAME'' SYMBOL NOT SPECIFIED OR GREATER THAN X
               7 CHARACTERS'
         MEXIT
.COMERR  ANOP
         MNOTE 12,'COMMAND-CODE AND WCC NOT SPECIFIED IN ''WCC'' PARAMEX
               TER.'
         MEXIT
.TYPERR  ANOP
         MNOTE 12,'''TYPE'' NOT SPECIFIED OR IS INVALID.'
         MEXIT
.ENDERR  ANOP
         MNOTE 12,'NO ''BUF3270 TYPE=END'' SPECIFIED FOR PREVIOUSLY DEFI
               INED SCREEN.'
         MEXIT
.WCC     ANOP
         SPACE 3
********************************************************************
***********  S T A R T    O F    3 2 7 0    B U F F E R ************
***********                                             ************
***********                                             ************
********************        W C C              *********************
&NAME    DS    0F                 START OF 3270 BUFFER
         AIF  ('&WCC' EQ '').WCCDFLT
         DC    XL1'&WCC'          3270(WCC)
         AGO  .CONTWCC
.WCCDFLT ANOP
         DC    XL1'C2'            3270(WCC)
.CONTWCC ANOP
         DC    XL3'115D7E'        SPF FULLSCR SIGNAL STRING(TYPE B)
&DSECTNM DSECT                    START OF SCREEN DSECT
&NAME.W  DS    XL1                3270(WCC)
         DS    XL3                SPF FULLSCR SIGNAL STRING(TYPE B)
&SYSECT  CSECT                    CONTINUE USERS CSECT
         MEXIT
.END     ANOP
*******************************************************************
         SPACE 2
         MEXIT
.ENDBUF  ANOP
         AIF   ('&WCC' EQ '').ENDCONT
         MNOTE '''WCC'' PARAMETER IGNORED WHEN ''TYPE=END'' SPECIFIED.'
.ENDCONT ANOP
         AIF   ('&FLINE' EQ '').GENLEN
         FIELD POS=(&FLINE,&FCOLMN),CUR=YES
.GENLEN  ANOP
&COUNT   SETA   K'&DSECTNM-1
&DSECTNM SETC '&DSECTNM'(1,&COUNT)
&DSECTNM.L EQU *-&DSECTNM        LENGTH OF 3270 SCREEN BUFFER
&DSECTNM SETC  ''                    SET DSECT NAME TO NULL
***********                                             ***********
***********                                             ***********
***********                                             ***********
***********    E N D      O F    3 2 7 0    B U F F E R ***********
*******************************************************************
         MEND
