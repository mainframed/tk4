//NSUBMT0 JOB ,'SUBMIT MSGCLASS=A',CLASS=9,TYPRUN=HOLD
/*PROCLIB NER.SP1IPO.CNTL
/*JOBPARM Q=F,I
//*
//ASM     EXEC PGM=IFOX00,REGION=256K,PARM='NODECK,NOLOAD,NOALIGN'
//SYSLIB   DD  DSN=SYS1.MACLIB,DISP=SHR
//SYSUT1   DD  UNIT=SYSDA,SPACE=(CYL,(1,1)),DCB=BLKSIZE=1700
//SYSUT2   DD  UNIT=SYSDA,SPACE=(CYL,(1,1)),DCB=BLKSIZE=1700
//SYSUT3   DD  UNIT=SYSDA,SPACE=(CYL,(1,1)),DCB=BLKSIZE=1700
//SYSPRINT DD  DSN=&&LIST,DISP=(,PASS),UNIT=SYSDA,SPACE=(TRK,(20,20))
//SYSIN    DD  *
*ZAP CARD ++USERMOD(NSUBMT0)     /*
*ZAP CARD   PROBLEM:  TSO SUBMIT COMMAND PROCESSOR DEFAULTS MSGCLASS
*ZAP CARD             TO TSO MSGCLASS CAUSING SUBMITTED JOB'S LOG,
*ZAP CARD             JCL AND MESSAGES (ALONG WITH ALL OUTPUT IF
*ZAP CARD             SYSOUT=* IS SPECIFIED ON SYSOUT DATA SETS)
*ZAP CARD             TO BE LOST UNLESS MSGCLASS=A IS SPECIFIED ON
*ZAP CARD             THE JOB CARD.
*ZAP CARD   SOLUTION: CHANGE IKJEFF15 TO DEFAULT MSGCLASS TO A.
*ZAP CARD                                     */     .
*ZAP CARD ++VER(Z038) FMID(EBB1102) PRE(UZ31465) .
*ZAP CARD ++ZAP(IKJEFF15) .
*ZAP CARD  NAME IKJEFF15
IKJEFF15 CSECT ,
         PRINT GEN
         PRINT DATA
         SPACE 2
@PSTART  EQU   IKJEFF15+X'001C'   BASE ADDRESS
HOOK     EQU   IKJEFF15+X'0084'   POINT OF MODIFICATION
@SIZDATD EQU   IKJEFF15+X'0334'   ADDR OF GETMAIN AMOUNT
PATCH    EQU   IKJEFF15+X'034D'   ADDR OF PATCH AREA
         SPACE 5
@DATD    DSECT
         ORG   @DATD+X'0104'
RENSYSOU DS    CL4                ORIGIONAL SYSOUT TEXT UNIT
         ORG   @DATD+X'01C0'
@ENDDATD EQU   *                  ORIGIONAL END OF @DATD
NEWSYSOU DS    CL7                NEW SYSOUT TEXT UNIT
NEWDTEND EQU   *                  NEW END OF @DATD
         SPACE 5
IKJEFF15 CSECT ,
         SPACE
@10      EQU   10                 REG 10
@11      EQU   11                 REG 11
@12      EQU   12                 REG 12
         SPACE 3
         USING @PSTART,@12        PROGRAM BASE REG
         USING @DATD,@11          DATA BASE REG
         EJECT ,
*ZAP START VER
         SPACE
         ORG   HOOK
         LA    @10,RENSYSOU       VERIFY HOOK
BACK     EQU   *
         SPACE 2
*ZAP START REP
         SPACE
         ORG   HOOK
         B     NEWCODE            BRANCH TO PATCH AREA
         SPACE 4
*ZAP START VER
         SPACE
         ORG   @SIZDATD
         DC    AL1(0)             SIZE FOR WORK AREA GETMAIN
         DC    AL3(@ENDDATD-@DATD)
         SPACE 2
*ZAP START REP
         SPACE
         ORG   @SIZDATD
         DC    AL1(0)             NEW SIZE FOR WORK AREA GETMAIN
         DC    AL3(NEWDTEND-@DATD)
         SPACE 4
*ZAP START VER
         SPACE
         ORG   PATCH
         DC    17CL3'ZAP'         VERIFY PATCH AREA
         SPACE 2
*ZAP START REP
         SPACE
         ORG   PATCH
NEWCODE  MVC   NEWSYSOU,SYSOUNEW  COPY NEW SYSOUT REQUEST T.U.
         LA    @10,NEWSYSOU       PTR TO SYSOUT REQUEST T.U.
         B     BACK               RETURN TO MAIN CODE
         SPACE 3
SYSOUNEW DC    X'0018',AL2(1),AL2(1),C'A'  NEW SYSOUT T.U.
         SPACE 3
*ZAP CARD  IDRDATA NSUBMT0
*ZAP STOP
         END
//*
//LIST1   EXEC PGM=IEBGENER
//SYSPRINT DD  DUMMY
//SYSIN    DD  DUMMY
//SYSUT1   DD  DSN=&&LIST,DISP=(OLD,PASS)
//SYSUT2   DD  SYSOUT=A
//*
//ZAPPER  EXEC PL1XCG,TEST=
//PL1.SYSIN DD DSN=NER.SP1IPO.CNTL(ASMTOZAP),DISP=SHR
//GO.IN    DD  DSN=&&LIST,DISP=(OLD,DELETE)
//OUT      DD  DSN=&&PTFIN,DISP=(NEW,PASS),
//             SPACE=(TRK,(1,1)),UNIT=SYSDA,
//             DCB=(RECFM=FB,LRECL=80,BLKSIZE=3120)
//*
//LIST2   EXEC PGM=IEBGENER
//SYSPRINT DD  DUMMY
//SYSIN    DD  DUMMY
//SYSUT1   DD  DSN=&&PTFIN,DISP=(OLD,PASS)
//SYSUT2   DD  SYSOUT=A
//*
//ZAP     EXEC SMP4APLY,COND=(4,LT)
//SMPCNTL  DD *
 RESTORE S(NSUBMT0) .
 RESETRC .
 REJECT  S(NSUBMT0) .
 RESETRC .
 RECEIVE .
 APPLY   S(NSUBMT0) DIS(NO) .
//SMPPTFIN DD  DSN=&&PTFIN,DISP=(OLD,DELETE)
