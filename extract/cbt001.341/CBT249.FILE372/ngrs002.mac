//NGRS002 JOB ,'REPLACE GRS RNLS',CLASS=9,COND=(4,LT),TYPRUN=HOLD
/*PROCLIB NER.SP1IPO.CNTL
/*JOBPARM Q=F,I
//*
//ISGGRNL0 EXEC ASMHXC,OPTIONS='LIST,XREF(SHORT),DECK'
//SYSLIB   DD  DSN=SYS1.SMPMTS,DISP=SHR,DCB=BLKSIZE=6400,
//             VOL=SER=IPORES,UNIT=3330-1
//         DD  DSN=SYS1.AMODGEN,DISP=SHR,
//             VOL=SER=IPODLB,UNIT=3330-1
//         DD  DSN=SYS1.MACLIB,DISP=SHR,
//             VOL=SER=IPORES,UNIT=3330-1
//         DD  DSN=SYS1.SYSMACS,DISP=SHR
//SYSPUNCH DD  DSN=&&PUNCH(ISGGRNL0),DISP=(NEW,PASS),
//             UNIT=SYSDA,SPACE=(TRK,(20,20,1),RLSE),
//             DCB=(RECFM=FB,LRECL=80,BLKSIZE=3120,DSORG=PO)
//SYSIN    DD  *
         TITLE ' ISGGRNL0 - GRS RESOURCE NAME LISTS'
ISGGRNL0 CSECT
***********************************************************************
*                                                                     *
* MODULE NAME:                                                        *
*   ISGGRNL0  --  GRS RESOURCE NAME LISTS                             *
*                                                                     *
* USAGE:                                                              *
*   THIS NON-EXECUTABLE MODULE CONTAINS THE NERDC RNL'S USED BY GRS.  *
*   FOR A DETAILED DESCRIPTION OF THIS MODULE SEE THE IBM SAMPLE      *
*   SOURCE CODE 'SYS1.SAMPLIB(ISGGRNLS)'.                             *
*                                                                     *
***********************************************************************
         EJECT
         MACRO
&NAME    RNLGEN &QNAME=,&RNAME=,&TYPE=,&LEN=,&END=
         LCLC  &R,&L
.*
.**********************************************************************
.*                                                                    *
.*    GRS RNL GENERATION MACRO OPERANDS:                              *
.*                                                                    *
.*       QNAME=        SEPECIFIES THE QNAME FOR THE ENTRY.            *
.*                                                                    *
.*       RNAME=        SPECIFIES THE RNAME FOR THE ENTRY.             *
.*                                                                    *
.*       TYPE=GENERIC  INDICATES THAT THIS IS A GENERIC ENTRY.        *
.*                                                                    *
.*       LEN=          SPECIFIES THE LENGTH OF THE RNAME FOR THE      *
.*                     MASTER CATALOG AND MUST BE 20 OR 44.           *
.*                                                                    *
.*       END=YES       INDICATES THAT THIS IS THE LAST ENTRY.         *
.*                     NOTE THAT A QNAME OF RNLESIEX, RNLERCEX OR     *
.*                     RNLESEEX MUST BE SPECIFIED.                    *
.*                                                                    *
.**********************************************************************
.*   WRITTEN BY: SCOTT C. CRUMPTON                                    *
.**********************************************************************
.*
         AIF   ('&END' NE 'YES').NOTEND
         AIF   ('&QNAME' EQ 'RNLESIEX' OR                              +
               '&QNAME' EQ 'RNLERCEX' OR                               +
               '&QNAME' EQ 'RNLESEEX').ENDOK0
         MNOTE 8,'QNAME=RNLESIEX OR RNLERCEX OR RNLESEEX IS REQUIRED FO+
               R END=YES'
         MEXIT
.ENDOK0  ANOP
         AIF   ('&TYPE' EQ '' AND '&RNAME' EQ '').ENDOK1
         MNOTE 8,'TYPE= AND RNAME= INVALID FOR END=YES'
         MEXIT
.ENDOK1  ANOP
&NAME    DC    X'80'                   END OF LIST
         AGO   .L
.NOTEND  ANOP
         AIF   ('&TYPE' NE 'GENERIC').NOTGEN
&NAME    DC    X'40'                   GENERIC ENTRY
         AGO   .L
.NOTGEN  ANOP
&NAME    DC    X'00'                   NON-GENERIC ENTRY
.L       ANOP
&R       SETC  'RNL&SYSNDX'
         AIF   ('&RNAME' NE '').RNAME1
         DC    AL1(0)                  NO RNAME
         AGO   .QNAME1
.RNAME1  AIF   ('&TYPE' NE 'GENERIC').RNAME2
         DC    AL1(L'&R)               PARTIAL DATASET NAME LENGTH
         AGO   .QNAME1
.RNAME2  ANOP
         DC    AL1(L'&R)               DATASET NAME LENGTH
.QNAME1  ANOP
         DC    CL8'&QNAME'             QNAME
         AIF   ('&RNAME' EQ '').RNAME3
&L       SETC  ''
         AIF   ('&LEN' EQ '').NOLEN
         AIF   (&LEN EQ 20 OR &LEN EQ 44).LENOK
         MNOTE 8,'LEN=&LEN INVALID, USE LEN=20 OR LEN=44'
.LENOK   ANOP
&L       SETC  'L&LEN'
.NOLEN   ANOP
&R       DC    C&L.'&RNAME'            RNAME
.RNAME3  ANOP
         SPACE 2
         MEND
         EJECT
         MODID BR=NO
         ENTRY ISGSIRNL
         ENTRY ISGRCRNL
         ENTRY ISGSERNL
         EJECT
***********************************************************************
*                                                                     *
*  ISGSIRNL - INCLUSION RESOURCE NAME LIST                            *
*                                                                     *
***********************************************************************
         DS    0D
         DC    CL8'ISGSIRNL'
         SPACE 3
ISGSIRNL EQU   *
         SPACE 2
M4LIB    RNLGEN QNAME=M4LIB,TYPE=GENERIC                            SCC
SPFDSN   RNLGEN QNAME=SPFDSN,TYPE=GENERIC                           SCC
SYSDSN   RNLGEN QNAME=SYSDSN,TYPE=GENERIC
TMS0     RNLGEN QNAME=TMS,TYPE=GENERIC                              SCC
RNLESIEX RNLGEN QNAME=RNLESIEX,END=YES
         EJECT
***********************************************************************
*                                                                     *
*  ISGRCRNL - RESERVE CONVERSION RESOURCE NAME LIST                   *
*                                                                     *
***********************************************************************
         DS    0D
         DC    CL8'ISGRCRNL'
         SPACE 3
ISGRCRNL EQU   *
         SPACE 2
FDRABR1  RNLGEN QNAME=FDRABR,RNAME=FDRABR.ARCHIVE,TYPE=GENERIC      SCC
         RNLGEN QNAME=FDRABR,RNAME=FDRABR.ABRARCH,TYPE=GENERIC      SCC
         RNLGEN QNAME=FDRABR,RNAME=FDRABR.ABRREST,TYPE=GENERIC      SCC
PANVALET RNLGEN QNAME=PANVALET,TYPE=GENERIC                         SCC
         RNLGEN QNAME=VALETPAN,TYPE=GENERIC                         SCC
SPFRESV  RNLGEN QNAME=SPFDSN,TYPE=GENERIC                           SCC
SPZAPLIB RNLGEN QNAME=SPZAPLIB,TYPE=GENERIC                         SCC
SYSIEWLP RNLGEN QNAME=SYSIEWLP,TYPE=GENERIC                         SCC
TMS1     RNLGEN QNAME=TMS,TYPE=GENERIC                              SCC
RNLERCEX RNLGEN QNAME=RNLERCEX,END=YES
         EJECT
***********************************************************************
*                                                                     *
*  ISGSERNL - EXCLUSION RESOURCE NAME LIST                            *
*                                                                     *
***********************************************************************
         DS    0D
         DC    CL8'ISGSERNL'
         SPACE 3
ISGSERNL EQU   *
         SPACE 2
ACFDSN   RNLGEN QNAME=SYSDSN,RNAME=ACF2V,TYPE=GENERIC               SCC
ACFVSAM  RNLGEN QNAME=ACFVSAM,TYPE=GENERIC                          SCC
BRODCAST RNLGEN QNAME=SYSDSN,RNAME=SYS1.BRODCAST
CMD1     RNLGEN QNAME=SYSDSN,RNAME=NER.CMD1.CSCDATA                 SCC
DCMLIB   RNLGEN QNAME=SYSDSN,RNAME=SYS1.DCMLIB
DUMPXX   RNLGEN QNAME=SYSDSN,RNAME=SYS1.DUMP,TYPE=GENERIC
GTS      RNLGEN QNAME=SYS..GTS,TYPE=GENERIC                         SCC
JES2     RNLGEN QNAME=SYSZJES2,TYPE=GENERIC                         SCC
LOGREC   RNLGEN QNAME=SYSDSN,RNAME=SYS1.LOGREC
MAN      RNLGEN QNAME=SYSDSN,RNAME=SYS1.MAN,TYPE=GENERIC
MASTCAT  RNLGEN QNAME=SYSIGGV2,RNAME=SYS1.NER1.MASTCAT,LEN=20       SCC
         RNLGEN QNAME=SYSIGGV2,RNAME=SYS1.NER2.MASTCAT,LEN=20       SCC
NUCLEUS  RNLGEN QNAME=SYSDSN,RNAME=SYS1.NUCLEUS
PAGE     RNLGEN QNAME=SYSDSN,RNAME=SYS1.PAGE,TYPE=GENERIC
PARMLIB  RNLGEN QNAME=SYSDSN,RNAME=SYS1.PARMLIB                     SCC
PASSWORD RNLGEN QNAME=SYSDSN,RNAME=PASSWORD
RJEXCTLG RNLGEN QNAME=RJEXCTLG,TYPE=GENERIC                         SCC
SPFSYS1  RNLGEN QNAME=SPFDSN,RNAME=SYS1,TYPE=GENERIC                SCC
STGINDEX RNLGEN QNAME=SYSDSN,RNAME=SYS1.STGINDEX
SVCLIB   RNLGEN QNAME=SYSDSN,RNAME=SYS1.SVCLIB
SWAP     RNLGEN QNAME=SYSDSN,RNAME=SYS1.SWAP,TYPE=GENERIC           SCC
SYSCTLG  RNLGEN QNAME=SYSCTLG,TYPE=GENERIC                          SCC
SYSIEFSD RNLGEN QNAME=SYSIEFSD,TYPE=GENERIC                         SCC
SYSIGGV2 RNLGEN QNAME=SYSIGGV2,TYPE=GENERIC                         SCC
SYSIKJBC RNLGEN QNAME=SYSIKJBC,TYPE=GENERIC                         SCC
SYSIKJUA RNLGEN QNAME=SYSIKJUA,TYPE=GENERIC                         SCC
SYSMOUNT RNLGEN QNAME=SYSDSN,RNAME=SYSMOUNT                         SCC
SYSVSAM  RNLGEN QNAME=SYSVSAM,TYPE=GENERIC                          SCC
SYSVTOC  RNLGEN QNAME=SYSVTOC,TYPE=GENERIC                          SCC
         RNLGEN QNAME=SYSVTOC,RNAME=NE1IPL                          SCC
         RNLGEN QNAME=SYSVTOC,RNAME=NE2IPL                          SCC
SYSZCAXW RNLGEN QNAME=SYSZCAXW,TYPE=GENERIC                         SCC
SYSZIGGI RNLGEN QNAME=SYSZIGGI,TYPE=GENERIC                         SCC
SYSZJWTP RNLGEN QNAME=SYSZJWTP,TYPE=GENERIC                         SCC
SYSZRPLW RNLGEN QNAME=SYSZRPLW,TYPE=GENERIC                         SCC
SYSZTIOT RNLGEN QNAME=SYSZTIOT,TYPE=GENERIC                         SCC
SYSZVMV  RNLGEN QNAME=SYSZVMV,TYPE=GENERIC                          SCC
SYSZWTOR RNLGEN QNAME=SYSZWTOR,TYPE=GENERIC                         SCC
UADS     RNLGEN QNAME=SYSDSN,RNAME=SYS1.UADS
RNLESEEX RNLGEN QNAME=RNLESEEX,END=YES
         END
//*
//GRSRNL  EXEC SMP4APLY
//SMPCNTL  DD *
 REJECT  S(NGRS002) .
 RESETRC .
 RECEIVE S(NGRS002) .
 APPLY   S(NGRS002) DIS(NO) .
//SMPPTFIN DD *
++USERMOD(NGRS002)    /*
   THIS USERMOD ADDS THE LOCAL VERSION OF THE GRS RNL
   TO THE SYSTEM.                   */ .
++VER(Z038) FMID(JBB1326) .
++MOD(ISGGRNL0) TXLIB(TEMPLIB) .
//TEMPLIB  DD DSN=&&PUNCH,DISP=(OLD,DELETE)
