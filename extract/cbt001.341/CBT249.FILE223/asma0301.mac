//ASMA0301  JOB
//ASM    EXEC ASMFCL,TIME=10,
//       PARM.LKED='LIST,LET,XREF,NCAL,RENT,REFR'
//ASM.SYSLIB  DD DSN=SYS1.AMODGEN,DISP=SHR,DCB=BLKSIZE=12960
//       DD DSN=SYS1.PGMACLIB,DISP=SHR
//       DD DSN=SYS1.MACLIB,DISP=SHR
//       DD DSN=SYS1.PVTMACS,DISP=SHR
//ASM.SYSIN DD *
         TITLE 'PGRRIM04 - FRONT-END RIM TO INITIALIZE USERCVT'
PGRRIM04   CSECT
***********************************************************************
*                                                                     *
*        MVS MODIFICATION PA03010.  FEBRUARY 1977.                    *
*                                                                     *
*        THIS CSECT IS LINK-EDITED AS A FRONT END TO THE RIM          *
*        IEAVNP04.  PGRRIM04 RECIVES CONTROL FROM THE                 *
*        NIP CONTROL MODULE IEAVNIPM, WHICH CALLS ALL THE RIMS.       *
*        PGRRIM04 INITIALIZES THE USERCVT BY:                         *
*                                                                     *
*        1) CREATING THE USERCVT ABOVE THE FIXED NUCLEUS,             *
*        2) PLACING THE USERCVT ADDRESS INTO THE CVTUSER              *
*        FIELD OF THE CVT
*                                                                     *
*        AS SOME OF THE RIMS RECEIVE CONTROL SEVERAL TIMES, THE       *
*        LOGIC IS SET UP SO THAT INITIALIZATION OCCURS ONLY ONCE.     *
*                                                                     *
*        WARNING:  THE TECHNIQUE FOR OBTAINING MEMORY FOR THE USERCVT *
*        IS TO GET SPACE FROM THE 'NUCLEUS BUFFER'.  THIS METHOD IS   *
*        VALID FOR SMALL AMOUNTS, BUT CARE MUST BE TAKEN IF MORE THAN *
*        32-64 BYTES ARE NEEDED, THE FICHE FOR NIP SHOULD BE          *
*        RESEARCHED MORE THOROUGHLY.                                  *
*                                                                     *
*        ALSO, THE CHOICE OF THE RIM THAT THIS CSECT IS LINKED WITH   *
*        IS IMPORTANT, BECAUSE THE RIMS ARE NOT ALL INDEPENDENT OF    *
*        IEAVNIPM.  E.G., THE BASIC SECTION OF THE LINKLIB DEB IS     *
*        BUILT BY IEAVNIPM, AND THEN THE RIM IEAVNP03 BUILDS THE      *
*        EXTENTS.                                                     *
*                                                                     *
***********************************************************************
         EJECT
*
*        DEFINE LOCAL SYMBOLS
*
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   1
R14      EQU   14
R15      EQU   15
         SPACE 1
REXIT    EQU   R14                     EXIT REG
RENTRY   EQU   R15                     ENTRY REG
RPARM    EQU   R1                      PARM REG
RWORK1   EQU   R4                      WORK REG
RWORK2   EQU   R5                      WORK REG
RUCB     EQU   R6                      UCB REG
RILK2    EQU   R7                      UCB LOOKUP REG
RILK2END EQU   R8                      UCB TABLE END-TEST REG
RUSERCVT EQU   R9                      CVT REG
      EJECT
***
*
*        THE FOLLOWING TWO EQU'S ARE OFFSETS INTO THE NVT.
*        THEY SHOULD BE VERIFIED IN EACH NEW RELEASE, AS
*        THERE CURRENTLY IS NO IBM-SUPPLIED MACRO TO MAP THE NVT.
*
***
NVTNUCND EQU   148+8*4                 END OF FIXED NUCLEUS
NVTWTO   EQU   232+5*4                 ADDRESS OF WTO SERVICE RTN
RNVT     EQU   R2                      NIP COMMON REGISTER
RCVT     EQU   R3                      NIP COMMON REGISTER
        SPACE 1
*
*        DEFINE USERCVT VALUES
*
PGPWUCB  EQU   0                       PASSWORD UCB ADDRESS OFFSET
PGCVTLEN EQU   16                      4 FULLWORDS FOR USERCVT
*        PGCVTLEN SHOULD ALWAYS BE IN FULLWORDS.
         EJECT
*
*        STANDARD INITIALIZATION
*
         SAVE  (14,12),,*
         LR    R12,RENTRY             COPY EPA
         USING PGRRIM04,R12            ESTABLISH ADDRESSABILITY
         LA    RWORK1,SAVE            GET A(SAVE)
         ST    RWORK1,8(R13)          CHAIN FORWARD
         ST    13,4(RWORK1)           CHAIN BACKWARD
         LR    R13,RWORK1             NEW SAVEAREA
*
*          ESTABLISH SYMBOLIC ADDRESSABILITY
*
         USING CVTMAP,RCVT             USE BASE OF CVT
         EJECT
*
*        TEST TO SEE IF THE USERCVT HAS ALREADY BEEN INITIALIZED.
*        THIS IS NECESSARY BECAUSE SOME OF THE RIMS
*        ARE CALLED MORE THAN ONCE.  THE FOLLOWING
*        CODE THUS INSURES THAT THE USERCVT IS INITIALIZED ONCE
*        INDEPENDENT OF THE RIM THAT PGRRIM04 IS LINK-EDITED WITH.
*
         L     RUSERCVT,CVTUSER        GET CVTUSER FIELD FROM CVT
         LTR   RUSERCVT,RUSERCVT       TEST THE FIELD FOR ZERO
         BZ    BUILDCVT                GO BUILD THE USERCVT
         B     CALLRIM                          CALL THE REAL RIM
BUILDCVT DS    0H
         L     RUSERCVT,NVTNUCND(RNVT) GET NUCLEUS BOUNDARY
         ST    RUSERCVT,CVTUSER        STORE IN CVTUSER FIELD
         LA    RWORK1,PGCVTLEN(RUSERCVT) BUMP CURRENT END OF NUC
         ST    RWORK1,NVTNUCND(RNVT)   UPDATE NEW CURRENT END
         XC    0(PGCVTLEN,RUSERCVT),0(RUSERCVT) CLEAR USERCVT
*
*        CALL THE RIM.  CONTROL IS GIVEN TO THE RIM AS THOUGH
*        PGRRIM04 NEVER EXISTED, AND THE RIM WILL RETURN
*        DIRECTLY TO THE NIP CONTROL MODULE.  THIS WILL RETURN
*        THE 'CORRECT' SAVEAREA VALUES TO THE CALLER.
*
CALLRIM  DS    0H
         L     R13,SAVE+4              RESTORE SAVEAREA
         DROP  R12
         LM    14,12,12(13)
         USING PGRRIM04,RENTRY          ADDRESSABILITY VIA RENTRY
         L     RENTRY,RIMVCON          GET ADDRESS OF RIM
         BR    RENTRY                  GIVE CONTROL TO RIM
         DROP  RENTRY                  DROP ADRESSABILITY.
         EJECT
*
*        CONSTANTS AND MESSAGES`
*
         SPACE 2
RIMVCON  DC    V(ILRASRIM)             RIM FOR PGRRIM04
SAVE     DC    18F'0'
         LTORG
        EJECT
*
*        EXPAND SYSTEM CONTROL BLOCKS
*
         SPACE 1
         PRINT NOGEN
         CVT   DSECT=YES,PREFIX=YES,LIST=YES
         END
//LKED.SYSLMOD DD DSN=SYS1.PGMOD.LINKLIB,DISP=OLD
//LKED.SYSIN   DD *
   NAME PGRRIM04(R)
/*
