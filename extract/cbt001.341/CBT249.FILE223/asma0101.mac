//ASMA0101  JOB
//ASM    EXEC ASMFCL,TIME=10,
//       PARM.ASM='RENT',
//       PARM.LKED='LIST,LET,XREF,NCAL,RENT,REFR'
//ASM.SYSLIB  DD DSN=SYS1.AMODGEN,DISP=SHR,DCB=BLKSIZE=12960
//       DD DSN=SYS1.PGMACLIB,DISP=SHR
//       DD DSN=SYS1.MACLIB,DISP=SHR
//       DD DSN=SYS1.PVTMACS,DISP=SHR
//ASM.SYSIN DD *
ACT      TITLE 'IEFACTRT - SMF EXIT - VS MOD PA01015'
         SPACE 10
         UCOMAREA
***********************************************************************
*                                                                     *
*                          REGISTER USAGE                             *
*                                                                     *
***********************************************************************
         SPACE 5
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R9       EQU   9
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
BASE1    EQU   10                  BASE FOR ROUTINE
BASE2    EQU   11                  BASE FOR ROUTINE (SECOND)
RSMFRCD4 EQU   12                  BASE FOR SMF REC 4 (STEP TERM)
RSMFRCD5 EQU   12                  BASE FOR SMF REC 5 (JOB TERM)
RCOMFLD  EQU   8                   BASE FOR USER COMMUNICATIONS AREA
         TITLE 'IEFACTRT - ENTRY AND SETUP'
***********************************************************************
*                                                                     *
*        THIS IS THE ENTRY POINT FOR IEFACTRT WHICH IS ENTERED        *
*        AT STEP TERMINATION AND AT JOB TERMINATION                   *
*                                                                     *
*                                                                     *
*        THE INPUT CONSISTS OF:                                       *
*                                                                     *
*        REG 0 - BINARY CODE 12 = STEP TERM                           *
*                            16 = JOB TERM                            *
*                                                                     *
*        REG 1 - LIST OF TEN FOUR-BYTE ADDRESSES POINTING TO          *
*                   1 - COMMON PARAMETER AREA                         *
*                   2 - STEP NAME (AT JOB TERM ADDRESS IS ZERO)       *
*                   3 - PROGRAMMERS NAME                              *
*                   4 - CPU TIME AND NUMBER OF FIELDS IN JOB ACCT INFO*
*                   5 - ACCOUNTING INFORMATION FOR JOB                *
*                   6 - CPU TIME AND NUMBER OF FIELDS IN STEP ACCT INFO
*                   7 - ACCOUNTING INFORMATION FOR STEP               *
*                   8 - INDICATIOR FIELD AND STEP NUMBER              *
*                   9 - TERMINATION STATUS                            *
*                  10 - SMF RECORD                                    *
*                                                                     *
*        REG 12 - POINTER TO LCT (FOR WRITING SMB'S).  IT IS SAVED    *
*              (R12SAVE1) FOR USE LATER                               *
*                                                                     *
*        COMMON PARAMETER AREA CONSISTS OF:                           *
*                   1 - JOBNAME                                       *
*                   2 - READER START TIME                             *
*                   3 - READER START DATE                             *
*                   4 - SYSTEM ID                                     *
*                   5 - USER IDENTIFICATION FIELD                     *
*                   6 - NUMBER OF STEP                                *
*                   7 - OPTION BITS                                   *
*                   8 - USER COMMUNICATION FIELD                      *
*                                                                     *
*        VS MODIFICATION  --  PA01010                                 *
*                                                                     *
** * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * **
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                     *
***********************************************************************
         EJECT
IEFACTRT CSECT
         SAVE  (14,12),T,*
******************************************************************
*                                                                *
*  THE FOLLOWING SECTION OF CODE HAS NOTHING TO DO WITH          *
*  ACCOUNTING.  RATHER, IT IS PART OF THE TSO PERFORMANCE        *
*  MEASUREMENT SYSTEM (TSOPMS) AND IS RESPONSIBLE FOR ENDING     *
*  THE LAST TRANSACTION BY CALLING THE RECORDER AND FOR WRITING  *
*  THE LAST SMF RECORD COLLECTED BY THE TSOPMS RECORDER          *
*  ROUTINE AFTER COMPLETEING THE INFORMATION IN THE RECORD       *
*  HEADER.  THIS CODE FORMS A CLOSED SUBROUTINE.                 *
*                                                                *
******************************************************************
         BALR  R3,0           GET VERY LOCAL ADDRESSABILITY
         USING *,R3
         CH    R0,=H'12'      IS THIS JOB TERMINATION?
         BNE   TSOPMSX        NO,...
         L     R2,CVTPTR      SEE IF THIS IS
         L     R4,(CVTTCBP-CVTTCBP)(R2)  A TSO USER
         L     R4,12(R4)       R4 => CURRENT ASCB
         L     R4,(ASCBCSCB-ASCB)(R4)   R4 => CSCB
         LTR   R4,R4          SPECIAL MEMORY (EG MASTER SCHED)?
         BZ    TSOPMSX        YES, EXIT
         TM    (CHTRKID-CHPTR)(R4),CHTSID   TSO USER?
         BZ    TSOPMSX        NO,...
         L     R15,CVTPTR     R15 => CVT
         L     R15,(CVTUSER-CVTTCBP)(R15)  R15 => CVTUSER ADDR PACKET
         L     R15,4(R15)     R15 => RECORDER
         LTR   R15,R15        IS THE RECORDER ACTIVE?
         BZ    TSOPMSX        NO,...
         BAL   R14,56(R15)    YES, ENTER RECORDER TO END CURRENT TRANS
         L     R1,0(R1)       R1 => COMMON PARAMETER AREA
         ICM   R1,7,(JMRUCOM-JMRPARM)+1(R1)  R1 => UCOMAREA
         BZ    TSOPMSX        NO UCOMAREA, ...
         LA    R5,(TSORWA-UCOMAREA)(R1)  R5 => RECORDER WORK AREA (RWA)
         USING RWA,R5
         CLC   RWAID,=CL4'RWA'   WAS RWA INITIALIZED?
         BNE   TSOPMSX        NO,...
         LH    R2,RWA#TRAN    R2 = # COMPLETE TRANS IN U249 RECORD
         LTR   R2,R2          ARE THERE ANY?
         BZ    TSOPMSX        NO, DON'T WRITE RECORD ...
         MH    R2,=AL2(U249TLEN)  R2 = LENGTH OF TRANS DATA IN RECORD
         LA    R2,U249HLEN+4(R2)  ADD HEADER PLUS RDW LENGTH
         STH   R2,U249RDW1    FILL IN THE RECORD DESCR WORD
         XR    R2,R2
         STH   R2,U249RDW2
         LH    R2,U249RCD#    INCREASE RECORD NUMBER BY 1
         LA    R2,1(R2)
         STH   R2,U249RCD#
         TIME  BIN
         STCM  R0,B'1111',U249TIME   PUT TIME AND
         STCM  R1,B'1111',U249DATE   DATE IN RECORD HEADER
         LA    R4,U249RDW1    R4 => U249 RECORD TO WRITE
         SMFWTM (R4)  WRITE THE RECORD
         B     TSOPMSX       GO AROUND CONSTANTS
         LTORG
TSOPMSX  DS    0H
         LM    R14,R12,12(R13)    RESTORE ALL REGS
         DROP  R3             DROP VERY LOCAL BASE
         DROP  R5
******************************************************************
*                                                                *
*  END OF TSO PERFORMANCE MEASUREMENT SYSTEM CODE                *
*                                                                *
******************************************************************
         BALR  BASE1,0
         USING *,BASE1,BASE2
         LA    BASE2,*+4095        SET UP SECOND BASE
         LA    BASE2,1(BASE2)
         LR    R4,R0               SAVE ENTRY CODE
*                                  12 = STEPTERM
*                                  16 = JOBTERM
         L     R2,0(R1)            POINT TO COMMON PARAMETER AREA
         SPACE 3
**********
*  SET UP TO USE COMMON WORK AREA IN THE USER COMMUNICATIONS AREA
*  PICK UP POINTER TO USER COM AREA FROM COMMON EXIT PARM AREA
**********
         L     RCOMFLD,32(R2)      UCOMFLD -> USER COM AREA
         LTR   RCOMFLD,RCOMFLD     IS THE POINTER ZERO?
         BZ    RETURN             YES, GET OUT OF HERE
         USING UCOMAREA,RCOMFLD   SET UP ADDRESSABILITY TO COMAREA
         SPACE 2
*  SET UP SAVE AREA CHAINING
         ST    R13,SAVE+4
         LR    R3,R13
         LA    R13,SAVE
         ST    R13,8(R3)
         SPACE 2
*        TEST  FOR STEP OR JOB TERM
*
         B     LIST(R4)
LIST     B     RETURNA             INVALID CODE
         B     RETURNA             INVALID CODE
         B     RETURNA             INVALID CODE
         B     RETURNA            CODE = 12 STEPTERM
         B     JOBTERM             CODE = 16 JOBTERM
         SPACE 2
EXIT     EQU   *
*  CLEAR WORK AREA AND RETURN
RETURNA  EQU   *
         L     R13,SAVE+4          SAVE AREA POINTER
RETURN   LM    14,12,12(13)
         SR    R1,R1                ENSURE R1 CLEARED
         SR    R15,R15              ENSURE R15 CLEARED
         BR    R14                  RETURN
JOBTERM  DS    0H
*
*        FREE THE USER COMMON AREA IN SWA AND RETURN
*
         SPACE 1
FREEUSER DS    0H
         L     R13,SAVE+4
         LA    R0,UCOMLEN      LENGTH OF AREA TO FREE
         ICM   R0,8,=AL1(237)       SUBPOOL OF AREA TO FREE
         FREEMAIN R,LV=(0),A=(8)    FREE USER COM AREA
*
*  ZERO THE POINTER TO THE UCOMAREA IN THE JMR SINCE AT TSO
*  RELOGON THE UCOMAREA IS FREED AND THE TSO PERF MEASURE SYS
*  MUST KNOW THAT IT WAS.
*
         L     R1,CVTPTR      R1 => CVT
         L     R1,(CVTTCBP-CVTTCBP)(R1)
         L     R1,0(R1)       R1 => OUR TCB
         ICM   R1,7,(TCBTCT-TCB)+1(R1)  R1 => TCT
         BZ    RETURN         CAN'T GET TO JMR
         ICM   R1,7,(TCTJMR-SMFTCT)+1(R1)  R1 => JMR
         BZ    RETURN         ...
         XR    R0,R0
         ST    R0,(JMRUCOM-JMRPARM)(R1)  ZERO THE POINTER
         B     RETURN              THAT'S IT FOR JOB TERM
         LTORG
         TITLE 'TSO PERF MEASURE SYS RECORDER WORK AREA'
         RWA
         PRINT NOGEN
         IEECHAIN
         IHAASCB
         IKJTCB
         IEFTCT
         CVT
         END   IEFACTRT
//LKED.SYSLMOD DD DSN=SYS1.PGMOD.LINKLIB,DISP=OLD
//LKED.SYSIN   DD *
   NAME IEFACTRT(R)
/*
