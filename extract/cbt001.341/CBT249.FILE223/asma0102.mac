//ASMA0102  JOB
//ASM    EXEC ASMFCL,TIME=10,
//       PARM.ASM='RENT',
//       PARM.LKED='LIST,LET,XREF,NCAL,RENT,REFR'
//ASM.SYSLIB  DD DSN=SYS1.AMODGEN,DISP=SHR,DCB=BLKSIZE=12960
//       DD DSN=SYS1.PGMACLIB,DISP=SHR
//       DD DSN=SYS1.MACLIB,DISP=SHR
//       DD DSN=SYS1.PVTMACS,DISP=SHR
//ASM.SYSIN DD *
UJI      TITLE 'SMF JOB INITIATION EXIT - VS MOD PA01020'
***********************************************************************
*
*  IEFUJI  -  SMF JOB INITIATION EXIT
*             VS MODIFICATION - PA01020
*
*
*  THE PURPOSE OF THIS EXIT IS TO OBTAIN STORAGE FOR THE USER
*  COMMUNICATIONS AREA AND PLACE THE ADDRESS OF THE AREA IN THE USER
*  COMMUNICATIONS FIELD IN THE EXIT PARAMETER LIST.
*  THIS AREA IS INITIALIZED TO BINARY ZEROS AND IS FREED BY IEFACTRT
*  AT JOB TERMINATION.
*
*
*
*  ON ENTRY REG 1 POINTS TO THE ADDRESS OF THE EXIT PARAMETER AREA.
*
***********************************************************************
         EJECT
COMPARM  DSECT
         SMFEXLST
         EJECT
         UCOMAREA
         EJECT
***********************************************************************
*  REGISTER EQUATES
***********************************************************************
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
BASE     EQU   11
COMPARMR EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         EJECT
IEFUJI   CSECT
         SAVE  (14,12),,*
         LR    BASE,R15
         USING IEFUJI,BASE
         SPACE 2
***********************************************************************
*  PICK UP POINTER TO EXIT PARAMETER AREA
***********************************************************************
         L     COMPARMR,0(R1)
         USING COMPARM,COMPARMR
         LR    R7,R1              SAVE ADDR OF PARM LIST IN R7 FOR TMS
         SPACE 5
***********************************************************************
*  GET STORAGE FOR USER COMMUNICATIONS AREA
***********************************************************************
         LA    R0,UCOMLEN         LENGTH OF AREA
         ICM   R0,8,=AL1(237)          SUBPOOL OF AREA
         GETMAIN R,LV=(0)
         LTR   R15,R15            DID WE GET IT
         BNZ   RETURN             NO, GET OUT
         SPACE 2
         ST    R1,JMRUCOM         SAVE ADDRESS IN COM FIELD PTR
         LR    R3,R0
         LA    R3,0(R3)               CLEAR HIGH ORDER BYTE
         LR    R2,R1              SET UP TO CLEAR AREA
         SR    R5,R5
         MVCL  R2,R4              CLEAR TO BINARY ZEROS
         LR    R2,R1              R2 IS BASE FOR USER COMM AREA
         USING  UCOMAREA,R2
         MVC   UCOMID,=C'UCOMAREA'   MOVE IDENTIFIER INTO AREA
* CLEAR THE JOB COST FIELDS TO PACKED ZEROS
         LA    R5,4               NUMBER OF FIELDS TO CLEAR
         LA    R6,UJOBINFO+4      ADDRESS OF PACKED FIELDS
MAKEPACK MVI   7(R6),X'0F'        SET SIGN OF PACKED FIELD
         LA    R6,8(R6)
         BCT   R5,MAKEPACK        TO NEXT FIELD
         EJECT
*********
* INITIALIZE THE USERID FIELD IN THE JMR
*********
         TM    JMRSTEP+1,X'01'    TSO SESSION?
         BZ    NOTTSO             NO, SKIP INIT
         MVI   JMRRIND,C'T'        INDICATE TSO SESSION
NOTTSO   MVC   JMRUPRIO+1(L'JMRCLASS),JMRCLASS  SET CLASS IN USERID
RETURN   RETURN (14,12),RC=0      RETURN
         EJECT
LENGTH   DC     F'800'
         END   IEFUJI
//LKED.SYSLMOD DD DSN=SYS1.PGMOD.LINKLIB,DISP=OLD
//LKED.SYSIN   DD *
   NAME IEFUJI(R)
/*
