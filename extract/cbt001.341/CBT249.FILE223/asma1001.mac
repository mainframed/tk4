//ASMA1001  JOB
//ASM    EXEC ASMFCL,TIME=10,
//       PARM.LKED='LIST,LET,XREF,NCAL,RENT,REFR'
//ASM.SYSLIB  DD DSN=SYS1.AMODGEN,DISP=SHR,DCB=BLKSIZE=12960
//       DD DSN=SYS1.PGMACLIB,DISP=SHR
//       DD DSN=SYS1.MACLIB,DISP=SHR
//       DD DSN=SYS1.PVTMACS,DISP=SHR
//ASM.SYSIN DD *
         TITLE 'PA10010 - MASTER SCHEDULER INITIALIZATION'
PGMSINIT CSECT
***********************************************************************
*                                                                     *
*        MVS MODIFICATION PA10010. FEBRUARY 1977.                     *
*                                                                     *
*        THIS CSECT IS LINK-EDITED AS A FRONT END FOR IEEVIPL,        *
*        WHICH IS THE M.S. INITIALIZATION ROUTINE. CURRENTLY,         *
*        THE ONLY FUNCTION OF PGMSINIT IS TO PLACE THE ADDRESS        *
*        OF THE TSO 'RECORDER' INTO THE SECOND WORD OF THE            *
*        USERCVT.  THIS CANNOT BE DONE IN NIP (PA03010) BECAUSE       *
*        PLPA HAS NOT BEEN BUILT YET.  PA03010 AND PA10010            *
*        SHOULD BE COMBINED BY BUILDING THE USERCVT IN SQA            *
*        INSTEAD OF THE NUCLEUS BUFFER.                               *
*                                                                     *
*        NO PARAMETERS ARE PASSED TO IEEVIPL.                         *
*                                                                     *
***********************************************************************
         EJECT
*
*        DEFINE LOCAL EQU'S
*
         SPACE 1
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         SPACE 1
RBASE    EQU   R12
REXIT    EQU   R14
RENTRY   EQU   R15
         SPACE 1
RCVT     EQU   R3
RUSERCVT EQU   R4
         EJECT
*
*        LOCATE AND VALIDATE THE USERCVT
*
         SPACE 1
         LR    RBASE,RENTRY        LOAD BASE REG
         USING PGMSINIT,RBASE
         L     RCVT,X'10'          GET CVT ADDRESS
         L     RUSERCVT,X'CC'(RCVT) GET USERCVT ADDRESS
         LTR   RUSERCVT,RUSERCVT   TEST ADDRESS
         BNZ   UCVTOK              OK IF NON-ZERO
         WTO   'PA10010 - WARNING, USERCVT IN NOT INITIALIZED',        X
               ROUTCDE=(1,2,4),DESC=2
         B     DONE                LEAVE RECORDER ADDRESS 0
         EJECT
UCVTOK   DS    0H
         SPACE 1
***********************************************************************
*                                                                     *
*        LOCATE THE ADDRESS OF THE RECORDER MODULE AND PUT IT INTO    *
*        THE SECOND WORD OF THE USERCVT.  THE LPA SEARCH ROUTINE,     *
*        IEAVVMSR, IS USED TO LOCATE THE LPDE FOR 'RECORDER' .        *
*                                                                     *
*                                                                     *
*        REGISTER USAGE FOR IEAVVMSR ( SEE FICH FOR IEAVLK00 ).       *
*                                                                     *
*        R0   - LEFT HALF OF MODULE NAME                              *
*        R1   - RIGHT HALF OF MODULE NAME                             *
*        R3   - CVT ADDRESS ( LOADED BY CALLER )                      *
*        R6   - BASE REG                                              *
*        R8,9 - ALTERED                                               *
*        R14  - RETURN ADDRESS                                        *
*        R15  - ENTRY ADDRESS                                         *
*                                                                     *
*        R0 WILL CONTAIN THE LPDE ADDRESS IF THE MODULE IS FOUND.     *
*        IEAVVMSR RETURNS TO R14 IF THE MODULE IS FOUND, TO 4(R14)    *
*        IF THE MODULE IS NOT FOUND.                                  *
*                                                                     *
***********************************************************************
         SPACE 1
         L     RENTRY,X'160'(RCVT) LPA SEARCH ROUTINE ADDRESS
         LM    R0,R1,RECORDER      INPUT PARM
         BALR  REXIT,RENTRY        SEARCH LPA
         B     FOUND               MODULE WAS FOUND
NOTFOUND B     DONE                MODULE WAS NOT FOUND
FOUND    DS    0H
*
*        PLACE THE EPA ADDRESS INTO THE USERCVT
*
         SPACE 1
         LR    R1,R0               COPY LPDE ADDRESS
         MVC   4(4,RUSERCVT),X'10'(R1) PUT EPA ADDRESS INTO USERCVT
DONE     DS    0H
         L     RENTRY,IEEVIPL      GET A(IEEVIPL)
         BR    RENTRY              CALL IEEVIPL
         EJECT
*
*        DATA AREAS
*
RECORDER DS    0F
         DC    CL8'RECORDER'
IEEVIPL  DC    V(IEEVIPL)          VCON FOR IEEVIPL
         END
//LKED.SYSLMOD DD DSN=SYS1.PGMOD.LINKLIB,DISP=OLD
//LKED.SYSIN   DD *
   NAME PGMSINIT(R)
/*
