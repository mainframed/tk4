         MACRO
         DCMSERV &FORCE=NO
         COPY  DCMSGBLS
         AIF   ('&FORCE' NE 'YES').NOSETS
&DCMSCLR SETB  1
&DCMSHEX SETB  1
&DCMSDEC SETB  1
&DCMSWRK SETB  1
&DCMSCST SETB  1
.NOSETS  ANOP
*                                                                     *
*                                                                     *
*        D C M S    S E R V I C E    S U B R O U T I N E S            *
*                                                                     *
*                                                                     *
         SPACE 3
         AIF   (&DCMSCLR).CLR
         AGO   .CLROUT
.CLR     ANOP
&DCMSWRK SETB  1
&DCMSCST SETB 1
         SPACE
@CLEAR   EQU       *
         STM       R2,R5,@CLEARSA
         LR        R2,R1
         LR        R3,R0
         LH        R3,0(R3)
         SR        R4,R4
         SR        R5,R5
         ICM       R5,8,BLANKS
         MVCL      R2,R4
         LM        R2,R5,@CLEARSA
         BR        R14
.CLROUT  ANOP
         AIF   (&DCMSDEC).DEC
         AGO   .DECOUT
.DEC     ANOP
&DCMSWRK SETB  1
&DCMSCST SETB 1
         SPACE
@DECIMAL EQU   *
         XC    DECWORK3(8),DECWORK3
         L     R2,DECWORK1
         CVD   R2,DECWORK3
         MVC   DECWORK4(4),EDITMASK
         ED    DECWORK4(4),DECWORK3+6
         XC    DECWORK1(4),DECWORK1
         BR    R14
         SPACE
.DECOUT  ANOP
         AIF   (&DCMSHEX).HEX
         AGO   .HEXOUT
.HEX     ANOP
&DCMSWRK SETB  1
&DCMSCST SETB 1
         SPACE
@HEX     EQU   *
         MVC   HEXWORK2(8),BLANKS
         MVI   HEXWORK1+4,X'FF'
         UNPK  HEXWORK2(9),HEXWORK1(5)
         OC    HEXWORK2(8),HEXMASKS
         MVI   HEXWORK3,C' '
         TR    HEXWORK2(8),TRTBL
         XC    HEXWORK1(5),HEXWORK1
         BR    R14
         SPACE
.HEXOUT  ANOP
         MEND
