         MACRO
         CBITEM &TYPE,&ROW,&COL,&OFF,&LEN,&DIGIT=0
         GBLA  &SEGOFF,&HEX
         LCLA  &IOFF,&ILEN
         AIF   ('&TYPE' NE 'LABEL').NOLABEL SKIP IF NOT LABEL TYPE
         DC    AL2(8)                  LABEL TYPE
&ILEN    SETA  K'&OFF-3                CALCULATE LENGTH
         DC    AL2(&ILEN)              LENGTH
         DC    AL2(&ROW*79-79+&COL-1)  SCREEN OFFSET
         DC    C&OFF                   EBCDIC CONSTANT
         MEXIT
.NOLABEL CBCONV &OFF,OFFSET            CONVERT DATA OFFSET FROM HEX
&IOFF    SETA  &HEX-&SEGOFF            TO DECIMAL
         AIF   (&HEX LT &SEGOFF).OFFLOW SEE IF TOO LOW FOR SEGMENT
         AIF   (&HEX GT (&SEGOFF+255)).OFFHI OR IF TOO HIGH
         CBCONV &LEN,LENGTH            JUST CHECK FOR VALID HEX
&ILEN    SETA  &HEX-1                  CALCULATE LENGTH
         AIF   ('&TYPE' NE 'HEX').NOHEX SKIP IF NOT HEX TYPE
         AIF   ('&DIGIT' NE '0' AND '&DIGIT' NE '1').BADDIG
         DC    AL2(4)                  HEX TYPE
         DC    AL1(&DIGIT,&ILEN)       DIGIT OFFSET AND LENGTH
         DC    AL2(&ROW*79-79+&COL-1)  SCREEN OFFSET
         DC    AL2(&IOFF)              DATA OFFSET
         MEXIT
.NOHEX   AIF   ('&TYPE' NE 'EBCD').NOTYPE SKIP IF NOT EBCDIC TYPE
         DC    AL2(12)                 EBCDIC DATA TYPE
         DC    AL2(&ILEN)              LENGTH
         DC    AL2(&ROW*79-79+&COL-1)  SCREEN OFFSET
         DC    AL2(&IOFF)              DATA OFFSET
         MEXIT
.NOTYPE  MNOTE 8,'TYPE FIELD MISSING OR INVALID'
         MEXIT
.BADDIG  MNOTE 8,'DIGIT FIELD INVALID'
         MEXIT
.OFFLOW  MNOTE 8,'OFFSET SPECIFIED TOO LOW FOR SEGMENT'
         MEXIT
.OFFHI   MNOTE 8,'OFFSET SPECIFIED TOO HIGH FOR SEGMENT'
         MEND
