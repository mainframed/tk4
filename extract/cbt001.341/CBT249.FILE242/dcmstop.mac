         MACRO
&NAME    DCMSTOP &RC=,&WORK=YES,&SCREEN=NO,&LIT=YES,&IOCB=,&SERV=YES
         COPY  DCMSGBLS
         AIF   (&LIST).LIST
         AGO   .LISTX
.LIST    ANOP
         EJECT
         SPACE 4
*                                                                     *
*                                                                     *
*        D C M S T O P  M A C R O                                     *
*                                                                     *
*                                                                     *
         SPACE 4
*                                                                     *
*        FUNCTION:                                                    *
*                   CREATES TERMINAL CODING (EXIT) FOR AN             *
*                   ASSEMBLY LANGUAGE MODULE:                         *
*                        2.   REGISTER RESTORATION                    *
*                        1.   DELETE SAVE AREA (FREEMAIN)             *
*                        3.   RETURN TO CALLER                        *
*                                                                     *
*        FORMAT:                                                      *
*                                                                     *
*              NAME  EPILOG                                           *
*                                                                     *
*              NOTE THAT THIS MACRO MUST BE USED WITH THE             *
*              - PROLOG -  MACRO                                      *
*                                                                     *
*                                                                     *
         MEXIT
.LISTX   ANOP
         AIF   (&PLG).CONT
         MNOTE 9,'PROLOG MACRO MUST PRECEED EPILOG MACRO'
         MEXIT
.CONT    ANOP
         DS    0F .
&PLG     SETB  0
         AIF   ('&RC' EQ '').STOFF
&RCD     SETB  1
         AGO   .RCDX
.STOFF   ANOP
&RCD     SETB  0
DCMSRCD  EQU   *
&NAME    L     14,4(13) .              GET OLD SA                BFG001
         XC    16(4,14),16(14) .       SET RC = 0                BFG001
         AGO   .GENX
.*
.*
.RCDX    ANOP
         AIF   ('&NAME' EQ '').RTS1
&NAME    EQU   *
.RTS1    ANOP
         SR    R15,R15
         AIF   (&RCD).GEN
         AGO   .GENX
.GEN     ANOP
         AIF   ('&RC' EQ 'ALL').GNALL
         AIF   ('&RC' EQ '(15)').GN15
         AIF   ('&RC' EQ '15').GN15
         AIF   ('&RC'(1,1) EQ '(').GNLMR
         AGO   .GENX
.GNLMR   ANOP
DCMSRCD  L     14,4(13) .              GET OLD SA                BFG001
         ST    &RC.,16(14) .           STORE RC                  BFG001
         AGO   .GENX
.GN15    ANOP
DCMSRCD  L     14,4(13) .              GET OLD SA                BFG001
         ST    15,16(14) .             STORE RC                  BFG001
         AGO   .GENX
.GNALL   ANOP
DCMSRCD  L     14,4(13) .              GET OLD SA                BFG001
         STM   15,1,16(14) .           STORE R0,R1,R1            BFG001
.GENX    ANOP
         AIF   (&RENT).RENT
         AGO   .NORENT
.RENT    ANOP
         LR    1,13
         L     13,4(13)
         FREEMAIN R,LV=&LVNUM,A=(1)
         RETURN (14,12),T
         SPACE 4
****         ****
*   REENTRANT   *
****         ****
         SPACE 2
         AGO   .END
.NORENT  ANOP
         L     13,4(13)
         RETURN (14,12),T
         LTORG
         SPACE 4
         AGO   .END
.END     ANOP
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                     *
*     D C M S    S T A N D A R D    E X I T    L I N K A G E          *
*                                                                     *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         EJECT
         AIF   ('&SERV' NE 'YES').NOSERV
&DCMSCLR SETB  1
&DCMSDEC SETB  1
&DCMSHEX SETB  1
         DCMSERV
         EJECT
.NOSERV  ANOP
         AIF   ('&LIT' NE 'YES').NOLIT
         LTORG
         EJECT
.NOLIT   ANOP
&DCMSCRN SETB   0
         AIF  ('&SCREEN' NE 'YES').NOSCR
&DCMSCRN SETB  1
.NOSCR   ANOP
         AIF  ('&WORK' NE 'YES').NOWRK
&DCMSWRK SETB  1
         DCMSWORK IOCB=&IOCB
.NOWRK   ANOP
         EJECT
         MEND
