         MACRO
&NAME    DCMSFMT &A
         COPY  DCMSGBLS
         LCLA  &K132
         LCLA  &N
         LCLA  &L
         LCLC  &OPED
         LCLC  &Q
         AIF   (&LIST).LIST
         AGO   .LISTX
         EJECT
*                                                                     *
*                                                                     *
*              F O R M A T   M A C R O                                *
*                                                                     *
*                                                                     *
         SPACE 4
*
*      FORMAT MACRO
*
*        THIS MACRO FORMATS A PRINT LINE
*
*      EXAMPLE:
*
* FILE1  FORMAT LINE1,
*              (FLD1,1,2,BH),
*              (FLD2,4,3,B),
*              (FLD3,8,5,ZU),
*              (FLD4,14,4,A),
*              (FLD5,19,10,P),
*              (FLD6,31,6,ZS),
*              (FLD7,38,4,BH.0),
*              (FLD8,43,4,B.2),
*              (FLD9,48,4,ZS.2),
*              (FLD10,53,4,P.0),
*              (FLD11,58,4,P.0,BIZ),
*              ('%',52,I,A)
*
*        FORM
* LABEL  FORMAT LINE,
*              (FFF,PP,PL,DD),
*              (FFF,PP,PL,DD),          OR
*              (FFF,PP,PL,DD,BIZ),      OR
*              ('AA99',PP,PL,A),
*
*              LABEL - FIXED BY PROGRAMMER
*
*              LINE  - FIXED BY PROGRAMMER
*
*
*              FFF - SEE DEFINE MACRO
*
*              PP  - STARTING PRINT POSITION
*
*              PL  - PRINT LENGTH - NUMBER OF PRINT POSITIONS UTILIZED
*                    (SIGNED FIELDS ACTUALLY USE ONE EXTRA PRINT
*                           POSITION FOR THE SIGN
*
         EJECT
*              DD  - DATA FORM   B      BINARY FULLWORD SIGNED
*                                BH     BINARY HALFWORD SIGNED
*                                ZU     ZONED DECIMAL UNSIGNED
*                                ZS     ZONED DECIMAL SIGNED
*                                 P     PACKED DECIMAL SIGNED
*                                 A     ALPHANUMERIC
*                                B.X    BINARY FULLWORD SIGNED
*                                BH.X   BINARY HALFWORD SIGNED
*                                ZS.X   ZONED DECIMAL SIGNED
*                                P.X    PACKED DECIMAL SIGNED
*
*                                 .X    STANDS FOR EDIT WORDS AS FLWS
*                                       B
*                                       BLANK  (N  DECIMAL POINTS OR
*                                               COMAS)
*                                              EX:  9999
*                                       .0     (COMMAS BETWEEN EVERY
*                                               THIRD DIGIT)
*                                              EX:  9,999
*                                       .2     (DECIMAL POINT TWO POS
*                                               FROM RIGHT AND COMAS
*                                               AS IN .0
*                                              EX:  9,999.99
*                                NEGATIVE SIGN APPEARS AS '9,999-'
*
*                                INVALZS  -  ZS FIELD WHICH MAY BE
*                                           INVALID
*                                              EX: 999A
*                                INVALP I -  P  FIELD WHICH MAY BE
*                                            INVALID
*                                              EX: 999H
*
*                           YOU MUST REMEMBER THAT USING THE '.0' AND
*                             '.2' OPTIONS REQUIRES THAT THE NUMBER
*                             OF DESIRED PRINT POSITIONS (PL) MUST
*                             INCLUDE THE APPROPRIATE DECIMAL POINT
*                             AND COMAS. THE SIGN POSITION IS NOT
*                             INCLUDED
*
*              BIZ  - BLANK IF ZERO - ON SIGNED FIELDS THIS WILL CAUSE
*                                     PRINTING BLANKS RATHER THAN '0'.
*
*              'AA99'    -    ANY ALPHANUMERIC CONSTANT WITHIN QUOTES
*                             THE (DD) MUST BE 'A .
*
*      SEQUENCE OF PRINT POSITIONING IS UP TO YOU  NO CHECK IS MADE FOR
*     OVERLAPPING FIELDS. BUT PP PLUS PL MUST NOT EXCEED 133.
*
.LISTX   ANOP
&NDX     SETC  '&SYSNDX'
&Q       SETC  ''''
&DCMSCNTS SETB   1
         SPACE 5
*                                                                     *
*                                                                     *
*              MOVE / EDIT FIELDS TO PRINT LINE (132)                 *
*                                                                     *
*                                                                     *
&NAME    INSUB
&N       SETA  1
         AIF   ('&A' EQ '').LPMN
.LP1     ANOP
&L       SETA  16
&OPED    SETC  'EDWD'
         SPACE
&N       SETA  &N+1
         AIF   ('&SYSLIST(&N)' EQ '').LP1E
         AIF   (N'&SYSLIST(&N) EQ 4).NO4
         AIF   (N'&SYSLIST(&N) EQ 5).NO5
         AGO   .LPMN
.NO5     ANOP
         AIF   ('&SYSLIST(&N,5)' NE 'BIZ').LPMN
.NO4     ANOP
         AIF   (T'&SYSLIST(&N,2) NE 'N').LPMN
         AIF   (T'&SYSLIST(&N,3) NE 'N').LPMN
&K132    SETA  &SYSLIST(&N,2)+&SYSLIST(&N,3)-1
         AIF   (&K132 GT 132).LPMN
         AIF   ('&SYSLIST(&N,4)' EQ 'A').A
         AIF   ('&SYSLIST(&N,1)'(1,1) EQ '''').LPMN
         AIF   ('&SYSLIST(&N,4)' EQ 'ZU').ZU
         AIF   ('&SYSLIST(&N,4)' EQ 'ZS').ZS
         AIF   ('&SYSLIST(&N,4)' EQ 'P').P
         AIF   ('&SYSLIST(&N,4)' EQ 'B').B
         AIF   ('&SYSLIST(&N,4)' EQ 'BH').BH
         AIF   ('&SYSLIST(&N,4)' EQ 'BH.0').BH0
         AIF   ('&SYSLIST(&N,4)' EQ 'BH.1').BH1
         AIF   ('&SYSLIST(&N,4)' EQ 'BH.2').BH2
         AIF   ('&SYSLIST(&N,4)' EQ 'INVALZS').INVALZS
         AIF   ('&SYSLIST(&N,4)' EQ 'INVALP').INVALP
         AIF   ('&SYSLIST(&N,4)' EQ 'B.0').B0
         AIF   ('&SYSLIST(&N,4)' EQ 'B.1').B1
         AIF   ('&SYSLIST(&N,4)' EQ 'B.2').B2
         AIF   ('&SYSLIST(&N,4)' EQ 'P.0').P0
         AIF   ('&SYSLIST(&N,4)' EQ 'P.1').P1
         AIF   ('&SYSLIST(&N,4)' EQ 'P.2').P2
         AIF   ('&SYSLIST(&N,4)' EQ 'ZS.0').ZS0
         AIF   ('&SYSLIST(&N,4)' EQ 'ZS.1').ZS1
         AIF   ('&SYSLIST(&N,4)' EQ 'ZS.2').ZS2
         AGO   .LPMN
.BH0     ANOP
&L       SETA  20
&OPED    SETC  'EDWD0'
         AGO   .L7
.BH1     ANOP
&L       SETA  21
&OPED    SETC  'EDWD1'
         AGO   .X4
.BH2     ANOP
&L       SETA  21
&OPED    SETC  'EDWD2'
         AGO   .L8
.BH      ANOP
.L7      ANOP
.X4      ANOP
.L8      ANOP
         MVC   DOUBLE(2),&SYSLIST(&N,1)
         LH    &REG,DOUBLE
         AGO   .BHB
.B2      ANOP
&L       SETA  21
&OPED    SETC  'EDWD2'
         AGO   .L1
.B1      ANOP
&L       SETA  21
&OPED    SETC  'EDWD1'
         AGO   .X3
.B0      ANOP
&L       SETA  20
&OPED    SETC  'EDWD0'
         AGO   .L2
.B       ANOP
.L1      ANOP
.X3      ANOP
.L2      ANOP
         MVC   DOUBLE(4),&SYSLIST(&N,1)
         L     &REG,DOUBLE
.BHB     ANOP
         CVD   &REG,DOUBLE
         AGO   .COMED
.A       ANOP
         AIF   ('&SYSLIST(&N,1)'(1,1) EQ '''').LIT
.ZU      ANOP
.INVALZS ANOP
         MVC   &A+&SYSLIST(&N,2)-1(&SYSLIST(&N,3)),&SYSLIST(&N,1)
         AGO   .LP1
.LIT     ANOP
         MVC   &A+&SYSLIST(&N,2)-1(&SYSLIST(&N,3)),=C&SYSLIST(&N,1)
         AGO   .LP1
.ZS1     ANOP
&OPED    SETC  'EDWD1'
&L       SETA  21
         AGO   .X1
.ZS2     ANOP
&L       SETA  21
&OPED    SETC  'EDWD2'
         AGO   .L3
.ZS0     ANOP
&L       SETA  20
&OPED    SETC  'EDWD0'
         AGO   .L4
.ZS      ANOP
.L3      ANOP
.X1      ANOP
.L4      ANOP
         PACK  DOUBLE,&SYSLIST(&N,1).(L&Q&SYSLIST(&N,1))
         AGO   .COMED
.P2      ANOP
&L       SETA  21
&OPED    SETC  'EDWD2'
         AGO   .L5
.P1      ANOP
&OPED    SETC  'EDWD1'
&L       SETA  21
         AGO   .X2
.P0      ANOP
&L       SETA  20
&OPED    SETC  'EDWD0'
         AGO   .L6
.P       ANOP
.L5      ANOP
.X2      ANOP
.L6      ANOP
         ZAP   DOUBLE,&SYSLIST(&N,1).(L&Q.&SYSLIST(&N,1))
         AGO   .COMED
.INVALP  ANOP
         UNPK  &A+&SYSLIST(&N,2)-1(&SYSLIST(&N,3)),&SYSLIST(&N,1).(L&Q.*
               &SYSLIST(&N,1))
         AGO   .LP1
.COMED   ANOP
         MVC   PWORK(&L+1),&OPED
         ED    PWORK(&L+1),DOUBLE
         AIF   ('&SYSLIST(&N,5)' NE 'BIZ').ABZ
         BZ    *+10
.ABZ     ANOP
         MVC   &A+&SYSLIST(&N,2)-1(&SYSLIST(&N,3)+1),PWORK+&L-&SYSLIST(*
               &N,3)
         AGO   .LP1
.LPMN    ANOP
         MNOTE 'INVALID FORMAT'
.LP1E    ANOP
         OUTSUB
         AIF   (&PWRK).PWRK
         B     @PWRKX
PWORK    DC    CL22' '
EDWD     DC    XL17'4020202020202020202020202020212060'
EDWD0    DC    XL21'402020206B2020206B2020206B2020206B20212060'
EDWD1    DC    XL22'4020206B2020206B2020206B2020206B2021204B2060'
EDWD2    DC    XL22'40206B2020206B2020206B2020206B2021204B202060'
&PWRK    SETB  1
@PWRKX   DS    0H
.PWRK    ANOP
         EJECT
         MEND
