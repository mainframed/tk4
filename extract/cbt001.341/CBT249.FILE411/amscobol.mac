000010 IDENTIFICATION DIVISION.
000020 PROGRAM-ID. SRMTDW99
000030 AUTHOR.     D.WEILERT.
000040 DATE-WRITTEN. AUG  1983.
000050 DATE-COMPILED.
000060*
000070* ================================================================
000080*   THIS PROGRAM IS USED TO PROCESS THE AUTHORIZATION, BUILD, AND
000090*   INVOKE THE ACCESS METHOD SERVICES (AMS).
000100* ================================================================
000110*
000120 EJECT
000130 ENVIRONMENT DIVISION.
000140 CONFIGURATION SECTION.
000150 SOURCE-COMPUTER.  IBM-370.
000160 OBJECT-COMPUTER.  IBM-370.
000170 INPUT-OUTPUT SECTION.
000180 FILE-CONTROL.
000190     SELECT FILE-IN1   ASSIGN TO UT-S-AUTHFILE.
000200     SELECT FILE-IN2   ASSIGN TO UT-S-AUDTFILE.
000210 EJECT
000220 DATA DIVISION.
000230 FILE SECTION.
000240 SKIP3
000250*=========================================================*
000260* CONTROL FILE.      THIS FILE IS USED TO CONTROL WHAT    *
000270* OPTIONS A USER WILL BE ALLOWED TO INVOKE.  A VALUE OF   *
000280* 'Y' MUST BE PLACED IN THE PROPER LOCATION WITHIN THE    *
000290* FILE PRIOR TO INVOKING THIS PROGRAM.  A DESCRIPTION OF  *
000300* OF THE FILE FOLLOWS:                                    *
000310*  COLUMNS    ---USE---    ---DEFAULT---                  *
000320*  00 - 04    USER ID       SPACES                        *
000330*  05 - 05    ALTER         Y                             *
000340*  06 - 06    BLDINDEX      N                             *
000350*  07 - 07    CHKLIST       Y                             *
000360*  08 - 08    CNVTCAT       N                             *
000370*  09 - 09    DEFINE        Y                             *
000380*  10 - 10    DIAGNOSE      N                             *
000390*  11 - 11    EXPORT        Y                             *
000400*  12 - 12    IMPORT        Y                             *
000410*  13 - 13    LISTCAT       Y                             *
000420*  14 - 14    PRINT         Y                             *
000430*  15 - 15    REPRO         Y                             *
000440*  16 - 16    VERFIY        Y                             *
000450*  17 - 80    FILLER        SPACES                        *
000460*=========================================================*
000470 SKIP3
000480 FD  FILE-IN1
000490     BLOCK CONTAINS 0 RECORDS
000500     RECORD CONTAINS 80 CHARACTERS
000510     LABEL RECORD IS STANDARD.
000520 01  IN-REC.
000530     05  USER-ID-FLD       PIC X(4).
000540     05  OPT-01            PIC X(1).
000550     05  OPT-02            PIC X(1).
000560     05  OPT-03            PIC X(1).
000570     05  OPT-04            PIC X(1).
000580     05  OPT-05            PIC X(1).
000590     05  OPT-06            PIC X(1).
000600     05  OPT-07            PIC X(1).
000610     05  OPT-08            PIC X(1).
000620     05  OPT-09            PIC X(1).
000630     05  OPT-10            PIC X(1).
000640     05  OPT-11            PIC X(1).
000650     05  OPT-12            PIC X(1).
000660     05  OPT-13            PIC X(1).
000670     05  FILLER            PIC X(63).
000680 SKIP3
000690*=========================================================*
000700* AUDIT TRAIL FILE.  THIS FILE IS USED TO RECORD HOW MANY *
000710* TIMES A USER INVOKED AN AMS PROCESS.  IF THE AUDIT      *
000720* INFORMATION IS NOT DESIRED A VALUE OF 'N' SHOULD BE     *
000730* PLACED IN VARIABLE 'AUDITUSE' BEFORE INVOKING THIS PGM. *
000740*=========================================================*
000750 SKIP3
000760 FD  FILE-IN2
000770     BLOCK CONTAINS 0 RECORDS
000780     RECORD CONTAINS 80 CHARACTERS
000790     LABEL RECORD IS STANDARD.
000800 01  IN-REC2.
000810     05  CNT-USER          PIC X(4).
000820     05  CNT OCCURS 13 TIMES.
000830         10  CNT-DATA          PIC 9(4).
000840     05  FILLER            PIC X(24).
000850 EJECT
000860 WORKING-STORAGE SECTION.
000870*===============================================================*
000880*          WORKING-STORAGE FIELDS FOR ISPF SERVICE CALLS        *
000890*===============================================================*
000900 77  SRTMSG                  PIC X(24)         VALUE
000910     'START OF WORKING STORAGE'.
000920 77  NOTHING                 PIC X(8)           VALUE SPACES.
000930 77  DEFAULT                 PIC X(80)          VALUE
000940     '    YNYNYYNYYYYYY                           '.
000950 77  SEL-PAN-SUB             PIC 9(2)      COMP VALUE ZERO.
000960 77  SUB-AUDT                PIC 9(2)      COMP VALUE ZERO.
000970 77  ISPF-ALT-NAME           PIC X(8)           VALUE SPACES.
000980 77  ISPF-BUFFER             PIC X(80)          VALUE SPACES.
000990*    ISPF-BUFFER FIELD LENGTH CAN BE INCREASED IF NECESSARY.
001000 77  ISPF-BUF-LENGTH         PIC 9(6)      COMP VALUE ZEROES.
001010 77  ISPF-CRP-NAME           PIC X(8)           VALUE SPACES.
001020 77  ISPF-DATA               PIC X(80)          VALUE SPACES.
001030*    ISPF-DATA FIELD LENGTH CAN BE INCREASED IF NECESSARY.
001040 77  ISPF-DSNAME             PIC X(56)          VALUE SPACES.
001050 77  ISPF-FIELD-NAME         PIC X(8)           VALUE SPACES.
001060 77  ISPF-FILEID             PIC X(22)          VALUE SPACES.
001070 77  ISPF-KEY-NAME           PIC X(8)           VALUE SPACES.
001080 77  ISPF-KEYNUM-NAME        PIC X(8)           VALUE SPACES.
001090 77  ISPF-LENGTH             PIC 9(6)      COMP VALUE ZEROES.
001100 77  ISPF-LINE-NUMBER        PIC 9(6)      COMP VALUE ZEROES.
001110 77  ISPF-MEMBER-NAME        PIC X(8)           VALUE SPACES.
001120 77  ISPF-MSG-ID             PIC X(8)           VALUE SPACES.
001130 77  ISPF-NAMENUM-NAME       PIC X(8)           VALUE SPACES.
001140 77  ISPF-NUMBER             PIC S9(6) COMP VALUE ZEROES.
001150 77  ISPF-OPTION1            PIC X(8)           VALUE SPACES.
001160 77  ISPF-OPTION2            PIC X(8)           VALUE SPACES.
001170 77  ISPF-PANEL-NAME         PIC X(8)           VALUE SPACES.
001180 77  ISPF-PERCENT            PIC 9(6)      COMP VALUE ZEROES.
001190 77  ISPF-PSWD-VALUE         PIC X(8)           VALUE SPACES.
001200 77  ISPF-ROW                PIC 9(6)      COMP VALUE ZEROES.
001210 77  ISPF-ROWNUM-NAME        PIC X(8)           VALUE SPACES.
001220 77  ISPF-SERIAL             PIC X(6)           VALUE SPACES.
001230 77  ISPF-SKEL-NAME          PIC X(8)           VALUE SPACES.
001240 77  ISPF-TABLE-NAME         PIC X(8)           VALUE SPACES.
001250 77  ISPF-TYPE               PIC X(8)           VALUE SPACES.
001260 77  ISPF-VAR-NAME           PIC X(8)           VALUE SPACES.
001270*===============================================================*
001280*                       ISPF BUFFERS                             *
001290*===============================================================*
001300 01  CMD-BUFFER.
001310     05  FILLER              PIC X(4)  VALUE 'CMD('.
001320     05  COMMND              PIC X(8)  VALUE SPACES.
001330     05  FILLER              PIC X(1)  VALUE ')'.
001340*===============================================================*
001350*                       ISPF SERVICES                            *
001360*===============================================================*
001370 01  ISPF-SERVICES.
001380     05  CONTROLX            PIC X(8)  VALUE 'CONTROL '.
001390     05  DISPLAYX            PIC X(8)  VALUE 'DISPLAY '.
001400     05  FTCLOSE             PIC X(8)  VALUE 'FTCLOSE '.
001410     05  FTERASE             PIC X(8)  VALUE 'FTERASE '.
001420     05  FTINCL              PIC X(8)  VALUE 'FTINCL  '.
001430     05  FTOPEN              PIC X(8)  VALUE 'FTOPEN  '.
001440     05  LOG                 PIC X(8)  VALUE 'LOG     '.
001450     05  SELECTX             PIC X(8)  VALUE 'SELECT  '.
001460     05  SETMSG              PIC X(8)  VALUE 'SETMSG  '.
001470     05  TBADD               PIC X(8)  VALUE 'TBADD   '.
001480     05  TBBOTTOM            PIC X(8)  VALUE 'TBBOTTOM'.
001490     05  TBCLOSE             PIC X(8)  VALUE 'TBCLOSE '.
001500     05  TBCREATE            PIC X(8)  VALUE 'TBCREATE'.
001510     05  TBDELETE            PIC X(8)  VALUE 'TBDELETE'.
001520     05  TBDISPL             PIC X(8)  VALUE 'TBDISPL '.
001530     05  TBEND               PIC X(8)  VALUE 'TBEND   '.
001540     05  TBERASE             PIC X(8)  VALUE 'TBERASE '.
001550     05  TBEXIST             PIC X(8)  VALUE 'TBEXIST '.
001560     05  TBGET               PIC X(8)  VALUE 'TBGET   '.
001570     05  TBMOD               PIC X(8)  VALUE 'TBMOD   '.
001580     05  TBOPEN              PIC X(8)  VALUE 'TBOPEN  '.
001590     05  TBPUT               PIC X(8)  VALUE 'TBPUT   '.
001600     05  TBQUERY             PIC X(8)  VALUE 'TBQUERY '.
001610     05  TBSARG              PIC X(8)  VALUE 'TBSARG  '.
001620     05  TBSAVE              PIC X(8)  VALUE 'TBSAVE  '.
001630     05  TBSCAN              PIC X(8)  VALUE 'TBSCAN  '.
001640     05  TBSKIP              PIC X(8)  VALUE 'TBSKIP  '.
001650     05  TBTOP               PIC X(8)  VALUE 'TBTOP   '.
001660     05  TBVCLEAR            PIC X(8)  VALUE 'TBVCLEAR'.
001670     05  VCOPY               PIC X(8)  VALUE 'VCOPY   '.
001680     05  VDEFINE             PIC X(8)  VALUE 'VDEFINE '.
001690     05  VDELETE             PIC X(8)  VALUE 'VDELETE '.
001700     05  VGET                PIC X(8)  VALUE 'VGET    '.
001710     05  VPUT                PIC X(8)  VALUE 'VPUT    '.
001720     05  VREPLACE            PIC X(8)  VALUE 'VREPLACE'.
001730     05  VRESET              PIC X(8)  VALUE 'VRESET  '.
001740*===============================================================*
001750*                 STORAGE AREA FOR SELECTION PANEL              *
001760*===============================================================*
001770 01  SELECTION-PANEL.
001780     05  SELECT-OPT OCCURS 13 TIMES.
001790         10 SEL-OPT              PIC X(50).
001800 01  CONSTANT-VALUES.
001810     05  OPT-01-VAL          PIC X(50) VALUE
001820     'ALTER       - ALTER PREVIOUSLY DEFINED ENTRIES   '.
001830     05  OPT-02-VAL          PIC X(50) VALUE
001840     'BLDINDEX    - CONSTRUCT ALTERNATE INDEX           '.
001850     05  OPT-03-VAL          PIC X(50) VALUE
001860     'CHKLIST     - IDENTIFY CHECKPOINT TAPE VOLUMES    '.
001870     05  OPT-04-VAL          PIC X(50) VALUE
001880     'CNVTCAT     - CONVERT CVOL OR VSAM CATALOGS TO ICF'.
001890     05  OPT-05-VAL          PIC X(50) VALUE
001900     'DEFINE      - CREATE CATALOG ENTIRES FOR DATA SETS'.
001910     05  OPT-06-VAL          PIC X(50) VALUE
001920     'DELETE      - DELETE CATALOG ENTIRES              '.
001930     05  OPT-07-VAL          PIC X(50) VALUE
001940     'DIAGNOSE    - VALIDATE BCS OR VVDS STRUCTURE      '.
001950     05  OPT-08-VAL          PIC X(50) VALUE
001960     'EXPORT      - CREATE COPY OF VSAM DATA SET OR ICF '.
001970     05  OPT-09-VAL          PIC X(50) VALUE
001980     'IMPORT      - LOAD DATA SET CREATED BY EXPORT     '.
001990     05  OPT-10-VAL          PIC X(50) VALUE
002000     'LISTCAT     - LIST CATALOG ENTRIES                '.
002010     05  OPT-11-VAL          PIC X(50) VALUE
002020     'PRINT       - PRINT VSAM, ISAM OR SAM DATA SETS   '.
002030     05  OPT-12-VAL          PIC X(50) VALUE
002040     'REPRO       - DATA SET UTILITY FEATURE            '.
002050     05  OPT-13-VAL          PIC X(50) VALUE
002060     'VERIFY      - RESET CATALOG ENTRIES FOR DATA SET  '.
002070*===============================================================*
002080* THE FOLLOWING AREA IS USED TO DEFINE THE VARIABLES USED IN    *
002090* THE PROGRAM.  EACH VARIABLES REQUIRES THREE LEVELS:           *
002100*     1ST - WORKING-STORAGE AREA FOR THE VARIABLE.  THIS AREA   *
002110*           STORES THE VARIABLE.                                *
002120*     2ND - THE VARIABLE LENGTH.                                *
002130*     3RD - THE VARIABLE NAMES FOR THE DIALOG SERVICE CALLS.    *
002140*===============================================================*
002150*
002160*=====================      LEVEL 1     ========================*
002170 01  ISPF-DIALOG-VARIABLES.
002180     05  AMSEL       PIC X(50)    VALUE SPACES.
002190     05  AMSOPT01    PIC X(50)    VALUE SPACES.
002200     05  AMSOPT02    PIC X(50)    VALUE SPACES.
002210     05  AMSOPT03    PIC X(50)    VALUE SPACES.
002220     05  AMSOPT04    PIC X(50)    VALUE SPACES.
002230     05  AMSOPT05    PIC X(50)    VALUE SPACES.
002240     05  AMSOPT06    PIC X(50)    VALUE SPACES.
002250     05  AMSOPT07    PIC X(50)    VALUE SPACES.
002260     05  AMSOPT08    PIC X(50)    VALUE SPACES.
002270     05  AMSOPT09    PIC X(50)    VALUE SPACES.
002280     05  AMSOPT10    PIC X(50)    VALUE SPACES.
002290     05  AMSOPT11    PIC X(50)    VALUE SPACES.
002300     05  AMSOPT12    PIC X(50)    VALUE SPACES.
002310     05  AMSOPT13    PIC X(50)    VALUE SPACES.
002320     05  USERID      PIC X(4)     VALUE SPACES.
002330     05  AUDITUSE    PIC X(1)     VALUE SPACES.
002340*
002350*=====================      LEVEL 2     ========================*
002360 01  ISPF-DIALOG-HOLDERS.
002370     05  LMSEL       PIC 9(6)     VALUE 50  COMP.
002380     05  LMSOPT01    PIC 9(6)     VALUE 50  COMP.
002390     05  LMSOPT02    PIC 9(6)     VALUE 50  COMP.
002400     05  LMSOPT03    PIC 9(6)     VALUE 50  COMP.
002410     05  LMSOPT04    PIC 9(6)     VALUE 50  COMP.
002420     05  LMSOPT05    PIC 9(6)     VALUE 50  COMP.
002430     05  LMSOPT06    PIC 9(6)     VALUE 50  COMP.
002440     05  LMSOPT07    PIC 9(6)     VALUE 50  COMP.
002450     05  LMSOPT08    PIC 9(6)     VALUE 50  COMP.
002460     05  LMSOPT09    PIC 9(6)     VALUE 50  COMP.
002470     05  LMSOPT10    PIC 9(6)     VALUE 50  COMP.
002480     05  LMSOPT11    PIC 9(6)     VALUE 50  COMP.
002490     05  LMSOPT12    PIC 9(6)     VALUE 50  COMP.
002500     05  LMSOPT13    PIC 9(6)     VALUE 50  COMP.
002510     05  LSERID      PIC 9(6)     VALUE  4  COMP.
002520     05  LUDITUSE    PIC 9(6)     VALUE  1  COMP.
002530*
002540*=====================      LEVEL 3     ========================*
002550 01  ISPF-DIALOG-VAR-NAMES.
002560     05  NMSEL       PIC X(10)    VALUE '(AMSEL   )'.
002570     05  NMSOPT01    PIC X(10)    VALUE '(AMSOPT01)'.
002580     05  NMSOPT02    PIC X(10)    VALUE '(AMSOPT02)'.
002590     05  NMSOPT03    PIC X(10)    VALUE '(AMSOPT03)'.
002600     05  NMSOPT04    PIC X(10)    VALUE '(AMSOPT04)'.
002610     05  NMSOPT05    PIC X(10)    VALUE '(AMSOPT05)'.
002620     05  NMSOPT06    PIC X(10)    VALUE '(AMSOPT06)'.
002630     05  NMSOPT07    PIC X(10)    VALUE '(AMSOPT07)'.
002640     05  NMSOPT08    PIC X(10)    VALUE '(AMSOPT08)'.
002650     05  NMSOPT09    PIC X(10)    VALUE '(AMSOPT09)'.
002660     05  NMSOPT10    PIC X(10)    VALUE '(AMSOPT10)'.
002670     05  NMSOPT11    PIC X(10)    VALUE '(AMSOPT11)'.
002680     05  NMSOPT12    PIC X(10)    VALUE '(AMSOPT12)'.
002690     05  NMSOPT13    PIC X(10)    VALUE '(AMSOPT13)'.
002700     05  NSERID      PIC X(8)     VALUE '(USERID)'.
002710     05  NUDITUSE    PIC X(10)    VALUE '(AUDITUSE)'.
002720*
002730 01  ENDMSG      PIC X(22)    VALUE 'END OF WORKING STORAGE'.
002740 EJECT
002750 PROCEDURE DIVISION.
002760     PERFORM HOUSE-KEEPING THRU HOUSE-KEEPING-EXIT.
002770     PERFORM DISPLAY-PANEL THRU DISPLAY-EXIT.
002780     PERFORM END-PROGRAM   THRU END-PROGRAM-EXIT.
002790     GOBACK.
002800 SKIP3
002810 HOUSE-KEEPING.
002820     OPEN INPUT FILE-IN1.
002830 DEFINE-VARIABLES.
002840     MOVE 'CHAR    ' TO ISPF-OPTION1.
002850     CALL 'ISPLINK' USING  VDEFINE
002860         NMSEL     AMSEL     ISPF-OPTION1  LMSEL.
002870     CALL 'ISPLINK' USING  VDEFINE
002880         NMSOPT01  AMSOPT01  ISPF-OPTION1  LMSOPT01.
002890     CALL 'ISPLINK' USING  VDEFINE
002900         NMSOPT02  AMSOPT02  ISPF-OPTION1  LMSOPT02.
002910     CALL 'ISPLINK' USING  VDEFINE
002920         NMSOPT03  AMSOPT03  ISPF-OPTION1  LMSOPT03.
002930     CALL 'ISPLINK' USING  VDEFINE
002940         NMSOPT04  AMSOPT04  ISPF-OPTION1  LMSOPT04.
002950     CALL 'ISPLINK' USING  VDEFINE
002960         NMSOPT05  AMSOPT05  ISPF-OPTION1  LMSOPT05.
002970     CALL 'ISPLINK' USING  VDEFINE
002980         NMSOPT06  AMSOPT06  ISPF-OPTION1  LMSOPT06.
002990     CALL 'ISPLINK' USING  VDEFINE
003000         NMSOPT07  AMSOPT07  ISPF-OPTION1  LMSOPT07.
003010     CALL 'ISPLINK' USING  VDEFINE
003020         NMSOPT08  AMSOPT08  ISPF-OPTION1  LMSOPT08.
003030     CALL 'ISPLINK' USING  VDEFINE
003040         NMSOPT09  AMSOPT09  ISPF-OPTION1  LMSOPT09.
003050     CALL 'ISPLINK' USING  VDEFINE
003060         NMSOPT10  AMSOPT10  ISPF-OPTION1  LMSOPT10.
003070     CALL 'ISPLINK' USING  VDEFINE
003080         NMSOPT11  AMSOPT11  ISPF-OPTION1  LMSOPT11.
003090     CALL 'ISPLINK' USING  VDEFINE
003100         NMSOPT12  AMSOPT12  ISPF-OPTION1  LMSOPT12.
003110     CALL 'ISPLINK' USING  VDEFINE
003120         NMSOPT13  AMSOPT13  ISPF-OPTION1  LMSOPT13.
003130     CALL 'ISPLINK' USING  VDEFINE
003140         NSERID  USERID  ISPF-OPTION1  LSERID.
003150     CALL 'ISPLINK' USING  VDEFINE
003160         NUDITUSE  AUDITUSE  ISPF-OPTION1  LUDITUSE.
003170 CONTIN-PROCESS.
003180     MOVE ZERO TO SEL-PAN-SUB.
003190     MOVE 'ASIS    '  TO ISPF-OPTION1.
003200     CALL 'ISPLINK' USING  VGET
003210         NSERID  ISPF-OPTION1.
003220     CALL 'ISPLINK' USING  VGET
003230         NUDITUSE  ISPF-OPTION1.
003240 CHECK-AUDIT.
003250     IF AUDITUSE IS EQUAL 'N' THEN
003260         GO TO READ-FOR-ID.
003270     OPEN I-O FILE-IN2.
003280 READ-AUDIT.
003290     READ FILE-IN2 AT END
003300         MOVE SPACES TO  IN-REC2
003310         MOVE USERID TO  CNT-USER
003320         MOVE 0      TO  CNT-DATA (1), CNT-DATA (2), CNT-DATA (3),
003330                         CNT-DATA (4), CNT-DATA (5), CNT-DATA (6),
003340                         CNT-DATA (7), CNT-DATA (8), CNT-DATA (9),
003350                         CNT-DATA (10), CNT-DATA (11),
003360                         CNT-DATA (12), CNT-DATA (13)
003370         GO TO READ-FOR-ID.
003380     IF CNT-USER    IS EQUAL USERID
003390         NEXT SENTENCE
003400     ELSE,
003410         GO TO READ-AUDIT.
003420 READ-FOR-ID.
003430     READ FILE-IN1 AT END
003440         MOVE DEFAULT TO IN-REC
003450         GO TO HOUSE-KEEPING-EXIT.
003460     IF USER-ID-FLD IS EQUAL USERID
003470         NEXT SENTENCE
003480     ELSE,
003490         GO TO READ-FOR-ID.
003500 HOUSE-KEEPING-EXIT.
003510     EXIT.
003520 SKIP3
003530 DISPLAY-PANEL.
003540     MOVE SPACES TO SELECTION-PANEL.
003550     IF OPT-01 IS EQUAL 'Y' THEN
003560         ADD 1 TO SEL-PAN-SUB
003570         MOVE OPT-01-VAL TO SEL-OPT (SEL-PAN-SUB).
003580     IF OPT-02 IS EQUAL 'Y' THEN
003590         ADD 1 TO SEL-PAN-SUB
003600         MOVE OPT-02-VAL TO SEL-OPT (SEL-PAN-SUB).
003610     IF OPT-03 IS EQUAL 'Y' THEN
003620         ADD 1 TO SEL-PAN-SUB
003630         MOVE OPT-03-VAL TO SEL-OPT (SEL-PAN-SUB).
003640     IF OPT-04 IS EQUAL 'Y' THEN
003650         ADD 1 TO SEL-PAN-SUB
003660         MOVE OPT-04-VAL TO SEL-OPT (SEL-PAN-SUB).
003670     IF OPT-05 IS EQUAL 'Y' THEN
003680         ADD 1 TO SEL-PAN-SUB
003690         MOVE OPT-05-VAL TO SEL-OPT (SEL-PAN-SUB).
003700     IF OPT-06 IS EQUAL 'Y' THEN
003710         ADD 1 TO SEL-PAN-SUB
003720         MOVE OPT-06-VAL TO SEL-OPT (SEL-PAN-SUB).
003730     IF OPT-07 IS EQUAL 'Y' THEN
003740         ADD 1 TO SEL-PAN-SUB
003750         MOVE OPT-07-VAL TO SEL-OPT (SEL-PAN-SUB).
003760     IF OPT-08 IS EQUAL 'Y' THEN
003770         ADD 1 TO SEL-PAN-SUB
003780         MOVE OPT-08-VAL TO SEL-OPT (SEL-PAN-SUB).
003790     IF OPT-09 IS EQUAL 'Y' THEN
003800         ADD 1 TO SEL-PAN-SUB
003810         MOVE OPT-09-VAL TO SEL-OPT (SEL-PAN-SUB).
003820     IF OPT-10 IS EQUAL 'Y' THEN
003830         ADD 1 TO SEL-PAN-SUB
003840         MOVE OPT-10-VAL TO SEL-OPT (SEL-PAN-SUB).
003850     IF OPT-11 IS EQUAL 'Y' THEN
003860         ADD 1 TO SEL-PAN-SUB
003870         MOVE OPT-11-VAL TO SEL-OPT (SEL-PAN-SUB).
003880     IF OPT-12 IS EQUAL 'Y' THEN
003890         ADD 1 TO SEL-PAN-SUB
003900         MOVE OPT-12-VAL TO SEL-OPT (SEL-PAN-SUB).
003910     IF OPT-13 IS EQUAL 'Y' THEN
003920         ADD 1 TO SEL-PAN-SUB
003930         MOVE OPT-13-VAL TO SEL-OPT (SEL-PAN-SUB).
003940     MOVE 'ASIS    '  TO ISPF-OPTION1.
003950     MOVE SEL-OPT (1) TO AMSOPT01
003960     CALL 'ISPLINK' USING  VPUT
003970         NMSOPT01  ISPF-OPTION1
003980     MOVE SEL-OPT (2) TO AMSOPT02
003990     CALL 'ISPLINK' USING  VPUT
004000         NMSOPT02  ISPF-OPTION1
004010     MOVE SEL-OPT (3) TO AMSOPT03
004020     CALL 'ISPLINK' USING  VPUT
004030         NMSOPT03  ISPF-OPTION1
004040     MOVE SEL-OPT (4) TO AMSOPT04
004050     CALL 'ISPLINK' USING  VPUT
004060         NMSOPT04  ISPF-OPTION1
004070     MOVE SEL-OPT (5) TO AMSOPT05
004080     CALL 'ISPLINK' USING  VPUT
004090         NMSOPT05  ISPF-OPTION1
004100     MOVE SEL-OPT (6) TO AMSOPT06
004110     CALL 'ISPLINK' USING  VPUT
004120         NMSOPT06  ISPF-OPTION1
004130     MOVE SEL-OPT (7) TO AMSOPT07
004140     CALL 'ISPLINK' USING  VPUT
004150         NMSOPT07  ISPF-OPTION1
004160     MOVE SEL-OPT (8) TO AMSOPT08
004170     CALL 'ISPLINK' USING  VPUT
004180         NMSOPT08  ISPF-OPTION1
004190     MOVE SEL-OPT (9) TO AMSOPT09
004200     CALL 'ISPLINK' USING  VPUT
004210         NMSOPT09  ISPF-OPTION1
004220     MOVE SEL-OPT (10) TO AMSOPT10
004230     CALL 'ISPLINK' USING  VPUT
004240         NMSOPT10  ISPF-OPTION1
004250     MOVE SEL-OPT (11) TO AMSOPT11
004260     CALL 'ISPLINK' USING  VPUT
004270         NMSOPT11  ISPF-OPTION1
004280     MOVE SEL-OPT (12) TO AMSOPT12
004290     CALL 'ISPLINK' USING  VPUT
004300         NMSOPT12  ISPF-OPTION1
004310     MOVE SEL-OPT (13) TO AMSOPT13
004320     CALL 'ISPLINK' USING  VPUT
004330         NMSOPT13  ISPF-OPTION1.
004340     MOVE SPACES TO ISPF-MSG-ID.
004350 DISPLAY-PRIM.
004360     MOVE 'AMS@PRIM' TO ISPF-PANEL-NAME.
004370     MOVE '        ' TO ISPF-FIELD-NAME.
004380     CALL 'ISPLINK' USING  DISPLAYX
004390         ISPF-PANEL-NAME  ISPF-MSG-ID  ISPF-FIELD-NAME.
004400     MOVE '        ' TO ISPF-MSG-ID.
004410     IF RETURN-CODE NOT = 0
004420         GO TO DISPLAY-EXIT.
004430 CHECK-AMSEL.
004440     IF AMSEL = 'A' OR 'ALT' OR 'ALTER'
004450         IF OPT-01 = 'Y' THEN
004460         MOVE 'AMSALT  ' TO COMMND
004470         MOVE 1          TO SUB-AUDT
004480         GO TO INVOKE-CMD.
004490     IF AMSEL = 'B' OR 'BDL' OR 'BLDINDEX'
004500         IF OPT-02 = 'Y' THEN
004510         MOVE 'AMSBDL  ' TO COMMND
004520         MOVE 2          TO SUB-AUDT
004530         GO TO INVOKE-CMD.
004540     IF AMSEL = 'CHK' OR 'CHKLIST'
004550         IF OPT-03 = 'Y' THEN
004560         MOVE 'AMSCHK  ' TO COMMND
004570         MOVE 3          TO SUB-AUDT
004580         GO TO INVOKE-CMD.
004590     IF AMSEL = 'CNV' OR 'CNVTCAT'
004600         IF OPT-04 = 'Y' THEN
004610         MOVE 'AMSCNV  ' TO COMMND
004620         MOVE 4          TO SUB-AUDT
004630         GO TO INVOKE-CMD.
004640     IF AMSEL = 'DEF' OR 'DEFINE'
004650         IF OPT-05 = 'Y' THEN
004660         MOVE 'AMSDEF  ' TO COMMND
004670         MOVE 5          TO SUB-AUDT
004680         GO TO INVOKE-CMD.
004690     IF AMSEL = 'DEL' OR 'DELETE'
004700         IF OPT-06 = 'Y' THEN
004710         MOVE 'AMSDEL  ' TO COMMND
004720         MOVE 6          TO SUB-AUDT
004730         GO TO INVOKE-CMD.
004740     IF AMSEL = 'DIA' OR 'DIAGNOSE'
004750         IF OPT-07 = 'Y' THEN
004760         MOVE 'AMSDIA  ' TO COMMND
004770         MOVE 7          TO SUB-AUDT
004780         GO TO INVOKE-CMD.
004790     IF AMSEL = 'E' OR 'EXP' OR 'EXPORT'
004800         IF OPT-08 = 'Y' THEN
004810         MOVE 'AMSEXP  ' TO COMMND
004820         MOVE 8          TO SUB-AUDT
004830         GO TO INVOKE-CMD.
004840     IF AMSEL = 'I' OR 'IMP' OR 'IMPORT'
004850         IF OPT-09 = 'Y' THEN
004860         MOVE 'AMSIMP  ' TO COMMND
004870         MOVE 9          TO SUB-AUDT
004880         GO TO INVOKE-CMD.
004890     IF AMSEL = 'L' OR 'LIS' OR 'LISTCAT'
004900         IF OPT-10 = 'Y' THEN
004910         MOVE 'AMSLIS  ' TO COMMND
004920         MOVE 10         TO SUB-AUDT
004930         GO TO INVOKE-CMD.
004940     IF AMSEL = 'P' OR 'PRI' OR 'PRINT'
004950         IF OPT-11 = 'Y' THEN
004960         MOVE 'AMSPRI  ' TO COMMND
004970         MOVE 11         TO SUB-AUDT
004980         GO TO INVOKE-CMD.
004990     IF AMSEL = 'R' OR 'REP' OR 'REPRO'
005000         IF OPT-12 = 'Y' THEN
005010         MOVE 'AMSREP  ' TO COMMND
005020         MOVE 12         TO SUB-AUDT
005030         GO TO INVOKE-CMD.
005040     IF AMSEL = 'V' OR 'VER' OR 'VERIFY'
005050         IF OPT-13 = 'Y' THEN
005060         MOVE 'AMSVER  ' TO COMMND
005070         MOVE 13         TO SUB-AUDT
005080         GO TO INVOKE-CMD.
005090     MOVE 'AMS019' TO ISPF-MSG-ID.
005100     GO TO DISPLAY-PRIM.
005110 INVOKE-CMD.
005120     IF AUDITUSE IS EQUAL 'N' THEN
005130         NEXT SENTENCE
005140     ELSE
005150         ADD 1 TO CNT-DATA (SUB-AUDT).
005160     MOVE 13 TO ISPF-BUF-LENGTH
005170     CALL 'ISPLINK' USING  SELECTX
005180         ISPF-BUF-LENGTH  CMD-BUFFER.
005190     MOVE SPACES TO AMSEL
005200     GO TO DISPLAY-PRIM.
005210 DISPLAY-EXIT.
005220     EXIT.
005230 SKIP3
005240 END-PROGRAM.
005250     MOVE SPACES TO NOTHING.
005260     CLOSE FILE-IN1.
005270     IF AUDITUSE IS EQUAL 'N' THEN
005280         NEXT SENTENCE
005290     ELSE
005300         WRITE IN-REC2
005310         CLOSE FILE-IN2.
005320 END-PROGRAM-EXIT.
005330     EXIT.
005340 EJECT
