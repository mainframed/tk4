000010*******************************
000100 ID DIVISION.
000110*******************************
000200 PROGRAM-ID.     BREAKDSN.
000300 AUTHOR.         GARY DUFFIELD.
000310 REMARKS.
000320                 THIS PROGRAM EDITS ANY DATASET NAME PASSED TO IT
000330                 AND SPLITS OUT THE PDS MEMBER NAME IF IT EXISTS.
000340
000350*******************************
000400 ENVIRONMENT DIVISION.
000410*******************************
000500 CONFIGURATION SECTION.
000600 SOURCE-COMPUTER. IBM-370.
000700 OBJECT-COMPUTER. IBM-370.
000800
000810*******************************
000900 DATA DIVISION.
000910*******************************
001100 WORKING-STORAGE SECTION.
001300 01  TABLES.
001400     05  WORK-DSN-INPUT                  PIC X(44).
001500     05  DSN-CHARACTER REDEFINES WORK-DSN-INPUT
001600         OCCURS 44 TIMES INDEXED BY INX  PIC X.
001700
001800     05  WORK-DSN-OUTPUT                 PIC X(44).
001900     05  DSN-CHAR-OUT REDEFINES WORK-DSN-OUTPUT
002000         OCCURS 44 TIMES INDEXED BY INY  PIC X.
002100
002200     05  WORK-MEMBER                     PIC X(8).
002300     05  DSN-CHAR-MEMBER REDEFINES WORK-MEMBER
002400         OCCURS 8 TIMES INDEXED BY INZ   PIC X.
002500
002600 01  SWITCHES.
002700     05  BAD-CHAR                        PIC X(3).
002800         88  BAD-CHARACTER                           VALUE 'YES'.
002850     05  QUOTE-CHAR                      PIC 9(5) COMP-3.
002860         88  TOO-MANY-QUOTES                    VALUE 3 THRU 44.
002870         88  NOT-ENOUGH-QUOTES                  VALUE 1.
002900
003000 01  ACCUMULATORS.
003100     05  PERFORM-COUNT                   PIC 9(5) COMP-3.
003110     05  CHAR-COUNT                      PIC 9(5) COMP-3.
003200
003300     EJECT
003310*******************************
003400 LINKAGE SECTION.
003410*******************************
003500
003600 01  BREAKDSN-IO-AREA.
003700     05  INPUT-DSN                       PIC X(44).
003800     05  OUTPUT-DSN                      PIC X(44).
003900     05  OUTPUT-MEMBER-NAME              PIC X(8).
004000     05  MEMBER-NAME-PROVIDED            PIC X(3).
004010     05  GDG-TYPE-DATASET                PIC X(3).
004100     05  VALID-DSN-PASSED                PIC X(3).
004200     05  ERROR-MESSAGE                   PIC X(30).
004300     EJECT
004400********************************************
004500 PROCEDURE DIVISION USING BREAKDSN-IO-AREA.
004600********************************************
004700 0100-MAIN-MODULE.
004800********************************************
004900
004910     PERFORM 0110-INITIALIZE-MODULE.
004911     IF DSN-CHARACTER (INX) = SPACE OR '('
004912        MOVE 'YES' TO BAD-CHAR.
004920     IF DSN-CHARACTER (INX) = QUOTE
004930        COMPUTE QUOTE-CHAR = QUOTE-CHAR + 1
004940        MOVE DSN-CHARACTER (INX) TO DSN-CHAR-OUT (INY)
004950        SET INX UP BY 1
004960        SET INY UP BY 1.
005400     PERFORM 0200-EXAMINE-DSN-MODULE
005500        VARYING INX FROM INX BY 1
005600        UNTIL (DSN-CHARACTER (INX) = ' ' OR '(' OR QUOTE)
005610           OR (INX > 44)
005700           OR (BAD-CHARACTER).
005710     IF DSN-CHARACTER (INX) = QUOTE
005720        COMPUTE QUOTE-CHAR = QUOTE-CHAR + 1.
005730     SET INX DOWN BY 1.
005740     IF DSN-CHARACTER (INX) = '.'
005750        MOVE 'YES' TO BAD-CHAR.
005760     SET INX UP BY 1.
005800     IF BAD-CHARACTER
005900        MOVE 'NO ' TO VALID-DSN-PASSED
006000        MOVE 'INVALID SYNTAX FOR DSNAME' TO ERROR-MESSAGE
006100     ELSE
006200        IF INX > 44
006210           IF NOT-ENOUGH-QUOTES
006220              MOVE 'NO ' TO VALID-DSN-PASSED
006230              MOVE 'INVALID SYNTAX FOR DSNAME' TO ERROR-MESSAGE
006240           ELSE
006300              MOVE WORK-DSN-OUTPUT TO OUTPUT-DSN
006400              MOVE 'NO ' TO MEMBER-NAME-PROVIDED
006410              MOVE 'NO ' TO GDG-TYPE-DATASET
006500              MOVE 'YES' TO VALID-DSN-PASSED
006700        ELSE
006800           IF DSN-CHARACTER (INX) = ' ' OR QUOTE
006900              PERFORM 0300-BASIC-DSN-MODULE
007000           ELSE
007100              PERFORM 0310-OTHER-DSN-MODULE.
007110     GOBACK.
007111
007112********************************
007113 0110-INITIALIZE-MODULE.
007114********************************
007115
007120     SET INX TO 1.
007130     SET INY TO 1.
007140     SET INZ TO 1.
007150     MOVE INPUT-DSN TO WORK-DSN-INPUT.
007160     MOVE SPACE TO WORK-DSN-OUTPUT WORK-MEMBER OUTPUT-DSN
007161                   OUTPUT-MEMBER-NAME MEMBER-NAME-PROVIDED
007162                   GDG-TYPE-DATASET VALID-DSN-PASSED
007163                   ERROR-MESSAGE.
007170     MOVE 'NO ' TO BAD-CHAR.
007180     MOVE ZERO TO QUOTE-CHAR PERFORM-COUNT CHAR-COUNT.
007200     EJECT
007300
007400********************************
007500 0200-EXAMINE-DSN-MODULE.
007600********************************
007700
007800     COMPUTE PERFORM-COUNT = PERFORM-COUNT + 1.
007900     IF (DSN-CHARACTER (INX) NOT NUMERIC)
008000        AND (DSN-CHARACTER (INX) NOT ALPHABETIC)
008100        AND (DSN-CHARACTER (INX) NOT = '@' AND '#' AND '$'
008110           AND QUOTE AND '.' AND SPACE)
008200        MOVE 'YES' TO BAD-CHAR.
008210     IF DSN-CHARACTER (INX) = '.'
008211        IF CHAR-COUNT = 0
008212           MOVE 'YES' TO BAD-CHAR
008213        ELSE
008220           COMPUTE CHAR-COUNT = 0
008230     ELSE
008231        IF DSN-CHARACTER (INX) NOT = SPACE
008240           COMPUTE CHAR-COUNT = CHAR-COUNT + 1.
008250     IF CHAR-COUNT > 8
008260        MOVE 'YES' TO BAD-CHAR.
008261     IF CHAR-COUNT = 1
008262        IF DSN-CHARACTER (INX) NUMERIC
008263           MOVE 'YES' TO BAD-CHAR.
008264     IF DSN-CHARACTER (INX) = QUOTE
008265        COMPUTE QUOTE-CHAR = QUOTE-CHAR + 1.
008266     IF TOO-MANY-QUOTES
008267        MOVE 'YES' TO BAD-CHAR.
008300     MOVE DSN-CHARACTER (INX) TO DSN-CHAR-OUT (INY).
008400     SET INY UP BY 1.
008500
008600********************************
008700 0205-MOVE-MEMBER-MODULE.
008800********************************
008900
009000     COMPUTE PERFORM-COUNT = PERFORM-COUNT + 1.
009100     IF (DSN-CHARACTER (INX) NOT NUMERIC)
009200        AND (DSN-CHARACTER (INX) NOT ALPHABETIC)
009300        AND (DSN-CHARACTER (INX) NOT = '@' AND '#' AND '$')
009400        MOVE 'YES' TO BAD-CHAR.
009500     MOVE DSN-CHARACTER (INX) TO DSN-CHAR-MEMBER (INZ).
009600     SET INZ UP BY 1.
009700     EJECT
009800********************************
009900 0300-BASIC-DSN-MODULE.
010000********************************
010100
010110     MOVE DSN-CHARACTER (INX) TO DSN-CHAR-OUT (INY)
010120     SET INX UP BY 1
010200     SET INY UP BY 1
010300     PERFORM 0200-EXAMINE-DSN-MODULE
010400        VARYING INX FROM INX BY 1
010500        UNTIL (DSN-CHARACTER (INX) NOT = ' ' AND QUOTE)
010600           OR (INX > 44).
010610     IF BAD-CHARACTER
010620        MOVE 'NO ' TO VALID-DSN-PASSED
010630        MOVE 'INVALID SYNTAX FOR DSNAME' TO ERROR-MESSAGE
010640     ELSE
010700        IF INX > 44
010710           IF NOT-ENOUGH-QUOTES
010720              MOVE 'NO ' TO VALID-DSN-PASSED
010730              MOVE 'INVALID SYNTAX FOR DSNAME' TO ERROR-MESSAGE
010740           ELSE
010800              MOVE WORK-DSN-OUTPUT TO OUTPUT-DSN
010900              MOVE 'NO ' TO MEMBER-NAME-PROVIDED
010910              MOVE 'NO ' TO GDG-TYPE-DATASET
011000              MOVE 'YES' TO VALID-DSN-PASSED
011100        ELSE
011200           MOVE 'NO ' TO VALID-DSN-PASSED
011300           MOVE 'INVALID SYNTAX FOR DSNAME' TO ERROR-MESSAGE.
011500
011600********************************
011700 0310-OTHER-DSN-MODULE.
011800********************************
011900
012000     SET INX UP BY 1
012100     IF INX > 44
012200        MOVE 'NO ' TO VALID-DSN-PASSED
012300        MOVE 'INVALID SYNTAX FOR DSNAME' TO ERROR-MESSAGE
012400     ELSE
012500       IF (DSN-CHARACTER (INX) NUMERIC)
012600          OR (DSN-CHARACTER (INX) = '-' OR '+')
012700          PERFORM 0400-GDG-DSN-MODULE
012800       ELSE
012900          IF DSN-CHARACTER (INX) ALPHABETIC
012910             OR DSN-CHARACTER (INX) = '@' OR '#' OR '$'
013000             PERFORM 0410-PDS-DSN-MODULE
013100          ELSE
013200             MOVE 'NO ' TO VALID-DSN-PASSED
013300             MOVE 'INVALID MEMBER NAME' TO ERROR-MESSAGE.
013400     EJECT
013500********************************
013600 0400-GDG-DSN-MODULE.
013700********************************
013710
013800     SET INX DOWN BY 1.
013810     MOVE DSN-CHARACTER (INX) TO DSN-CHAR-OUT (INY).
013820     SET INX UP BY 1.
013821     SET INY UP BY 1.
013830     IF DSN-CHARACTER (INX) = '+' OR '-'
013840        MOVE DSN-CHARACTER (INX) TO DSN-CHAR-OUT (INY)
013900        SET INX UP BY 1
013910        SET INY UP BY 1.
014000     COMPUTE PERFORM-COUNT = 0
014010     COMPUTE CHAR-COUNT = 1
014100     PERFORM 0200-EXAMINE-DSN-MODULE
014200        VARYING INX FROM INX BY 1
014300        UNTIL (DSN-CHARACTER (INX) NOT NUMERIC)
014400           OR (INX > 44)
014500           OR (PERFORM-COUNT > 3).
014600     IF (INX > 44) OR (PERFORM-COUNT > 3)
014700        MOVE 'NO ' TO VALID-DSN-PASSED
014800        MOVE 'INVALID SYNTAX FOR DSNAME' TO ERROR-MESSAGE
014900     ELSE
015000        IF  DSN-CHARACTER (INX) = ')'
015010           IF PERFORM-COUNT = 0
015011              MOVE 'NO ' TO VALID-DSN-PASSED
015012              MOVE 'INVALID SYNTAX FOR DSNAME' TO ERROR-MESSAGE
015020           ELSE
015100              PERFORM 0500-GDG-CONT-MODULE
015200        ELSE
015300           MOVE 'NO ' TO VALID-DSN-PASSED
015400           MOVE 'INVALID SYNTAX FOR DSNAME' TO ERROR-MESSAGE.
015600
015700********************************
015800 0410-PDS-DSN-MODULE.
015900********************************
016000
016200     COMPUTE PERFORM-COUNT = 0.
016210     COMPUTE CHAR-COUNT = 0.
016300     PERFORM 0205-MOVE-MEMBER-MODULE
016400        VARYING INX FROM INX BY 1
016500        UNTIL (DSN-CHARACTER (INX) = ')' OR ' ')
016600           OR (INX > 44)
016700           OR (PERFORM-COUNT > 8)
016800           OR (BAD-CHARACTER).
016900     IF BAD-CHARACTER
017000        MOVE 'NO ' TO VALID-DSN-PASSED
017100        MOVE 'INVALID MEMBER NAME' TO ERROR-MESSAGE
017200     ELSE
017300        IF INX > 44
017400           MOVE 'NO ' TO VALID-DSN-PASSED
017500           MOVE 'INVALID SYNTAX FOR DSNAME' TO ERROR-MESSAGE
017600        ELSE
017700           IF PERFORM-COUNT > 8
017800              MOVE 'NO ' TO VALID-DSN-PASSED
017900              MOVE 'MEMBER NAME TOO LONG' TO ERROR-MESSAGE
018000           ELSE
018100              IF  DSN-CHARACTER (INX) = ')'
018200                 PERFORM 0510-PDS-CONT-MODULE
018300              ELSE
018400                 MOVE 'NO ' TO VALID-DSN-PASSED
018500                 MOVE 'INVALID MEMBER NAME' TO ERROR-MESSAGE.
018600     EJECT
018700********************************
018800 0500-GDG-CONT-MODULE.
018900********************************
019000
019010     MOVE DSN-CHARACTER (INX) TO DSN-CHAR-OUT (INY)
019100     SET INX UP BY 1
019110     SET INY UP BY 1
019200     PERFORM 0200-EXAMINE-DSN-MODULE
019300        VARYING INX FROM INX BY 1
019400        UNTIL (DSN-CHARACTER (INX) NOT = ' ' AND QUOTE)
019500           OR (INX > 44).
019510     IF BAD-CHARACTER
019520        MOVE 'NO ' TO VALID-DSN-PASSED
019530        MOVE 'INVALID SYNTAX FOR DSNAME' TO ERROR-MESSAGE
019540     ELSE
019600        IF INX > 44
019610           IF NOT-ENOUGH-QUOTES
019611              MOVE 'NO ' TO VALID-DSN-PASSED
019612              MOVE 'INVALID SYNTAX FOR DSNAME' TO ERROR-MESSAGE
019620           ELSE
019700              MOVE WORK-DSN-OUTPUT TO OUTPUT-DSN
019800              MOVE 'NO ' TO MEMBER-NAME-PROVIDED
019900              MOVE 'YES' TO VALID-DSN-PASSED
019910              MOVE 'YES' TO GDG-TYPE-DATASET
020000        ELSE
020100           MOVE 'NO ' TO VALID-DSN-PASSED
020200           MOVE 'INVALID SYNTAX FOR DSNAME' TO ERROR-MESSAGE.
020400
020500********************************
020600 0510-PDS-CONT-MODULE.
020700********************************
020800
020900     SET INX UP BY 1
021000     PERFORM 0200-EXAMINE-DSN-MODULE
021100        VARYING INX FROM INX BY 1
021200        UNTIL (DSN-CHARACTER (INX) NOT = ' ' AND QUOTE)
021300           OR (INX > 44).
021310     IF BAD-CHARACTER
021320        MOVE 'NO ' TO VALID-DSN-PASSED
021330        MOVE 'INVALID SYNTAX FOR DSNAME' TO ERROR-MESSAGE
021340     ELSE
021400        IF INX > 44
021410           IF NOT-ENOUGH-QUOTES
021420              MOVE 'NO ' TO VALID-DSN-PASSED
021430              MOVE 'INVALID MEMBER NAME' TO ERROR-MESSAGE
021440           ELSE
021500              MOVE WORK-DSN-OUTPUT TO OUTPUT-DSN
021600              MOVE WORK-MEMBER TO OUTPUT-MEMBER-NAME
021700              MOVE 'YES' TO MEMBER-NAME-PROVIDED
021710              MOVE 'NO ' TO GDG-TYPE-DATASET
021800              MOVE 'YES' TO VALID-DSN-PASSED
021900        ELSE
022000           MOVE 'NO ' TO VALID-DSN-PASSED
022100           MOVE 'INVALID SYNTAX FOR DSNAME' TO ERROR-MESSAGE.
