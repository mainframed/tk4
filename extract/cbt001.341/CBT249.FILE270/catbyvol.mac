//DNC00150 JOB (2100,56-D),'DPARS 0682 T',CLASS=H,MSGCLASS=T
//* FROM WDPSC.DN00150.FILE(CATOFF)
//D EXEC ASMFCL,LIBRARY='WDPSC.TSAEXAM',OPTION1=TERM,
//  LNDSP=SHR
    TITLE 'CATALOG VERIFICATION FOR NON-VSAM'
*
* INPUT IS AN AMS CARD IMAGE TO UNCATALOG A DATA SET.
* COLUMN 73-74 CONTAINS THE LAST TWO BYTES OF UNIT TYPE
* COLUMN 75-80 CONTAINS THE VOLUME SERIAL NUMBER
*
* INPUT IS IN VOLUME SERIAL NUMBER SEQUENCE
* CARDS FOR TAPE ARE NOT  WRITTEN TO OUTPUT
* CARDS FOR DASD ARE CHECKED FOR A VALID VOLUME.
*   IF THE VOLUME EXISTS, IT IS CHECKED FOR THE DATA SET.
*   IF THE VOLUME DOES NOT CONTAIN THE DATA SET, THE RECORD IS
*   WRITTEN FOR IDCAMS PROCESSING.
*
CATBYVOL CSECT
         SAVE  (14,12),,CATBYVOL
         REGISTER
         BALR  R12,0
         USING *,R12
         B     GO
         DC    C'&SYSDATE'
         DC    C'&SYSTIME'
GO       CNOP  0,4
         ST    R13,SAVE+4
         LA    R11,SAVE
         ST    R11,8(,R13)
         LR    R13,R11
* PARM FIELD ANALYSIS
         L     R2,0(,R1)
         LH    R3,0(,R2)
         LTR   R3,R3
         BZ    OPENS
         OI    DEFLG,X'80'
OPENS    EQU   *
         OPEN  (PRINT,OUTPUT)
         TM    PRINT+48,X'10'
         BO    PR
         ABEND 12
PR       OPEN  (SYSIN,INPUT)
         TM    SYSIN+48,X'10'
         BZ    ERROR4              NO SYSIN DATA SET
         OPEN  (AMSDCB,OUTPUT)
PROCESS  GET   SYSIN,AMSCARD
         CLI   AMSDEVT,X'20'      IF NOT A DASD DEVICE TYPE, IT IS
         BNE   PROCESS            WILL BE PROCESSED ELSEWHERE
         CLC   AMSDSN(9),=C'SYSCTLG.V'
         BE    PROCESS             DO NOT DELETE CVOL ENTRIES
         CLC   WORKDEVT(8),AMSDEVT SAME VOLUME AS LAST TIME
         BE    VOLTHERE
         MVC   WORKDEVT(8),AMSDEVT
         L     R8,16               GET CVT ADDRESS
         L     R8,40(,R8)          POINTER TO UCB ADDRESSES
         SR    R7,R7
FINDVOL  ICM   R7,3,0(R8)          UCB ADDRESS
         BZ    UPUCB
         CLM   R7,3,=X'FFFF'       END OF ADDRESS LIST
         BE    NOTHERE
         CLC   28(6,R7),WORKVOL    IS THIS THE VOLUME
         BE    DSNCHECK
UPUCB    LA    R8,2(,R8)           NEXT UCB ADDRESS
         B     FINDVOL
DSNCHECK ST    R7,WORKUCB          KEEP ADDRESS OF UCB
         B     PROCESSB
NOTHERE  XC    WORKUCB,WORKUCB     MARK VOLUME SERIAL INVALID
VOLTHERE CLC   WORKUCB,=F'0000'
         BE    KEEPAMS             KEEP THE UNCATALOG RECORD
         SPACE 4
PROCESSB EQU   *
* VTOC VERIFY BY USE OF DF/DS FACILITY
         L     R7,WORKUCB
         XC    BFLHSP(6),BFLHSP         ZERO THE BUFFER LIST
         XC    BFLEARG,BFLEARG        ZERO THE BUFFER LIST
CVPLON   CVAFDIR ACCESS=READ,BRANCH=(YES,PGM),UCB=(R7),                X
               DSN=AMSDSN,BUFLIST=EMPTY,                               X
               MAPRCDS=NO,IXRCDS=NOKEEP,IOAREA=NOKEEP
         PRINT NOGEN
         ORG   CVPLON+4
         ICVAFPL DSECT=NO    MAP THE PARAMETER LIST
         ORG
         PRINT GEN
         C     R15,=F'4'           TEST RETURN CODE
         BH    ERROR8        TELL ABOUT ERROR AND ABEND FOR ANALYSIS
         CLI   CVSTAT,X'01'   DATA SET NOT FOUND
         BNE   PROCESS        SKIP ALL ERRORS FOR NOW
KEEPAMS  EQU   *
         PUT   AMSDCB,AMSCARD
         B     PROCESS
OUTOF    EQU   *
         CLOSE (PRINT,,AMSDCB,,SYSIN)
         L     R13,SAVE+4
         RETURN  (14,12),RC=0   ,,,,,,,,,,,,,,,,,S
ERROR8   ABEND 567,DUMP
ERROR4   PUT   PRINT,NOSYSIN
         LA    R15,62
         B     OUTOF
SAVE     DS    18F
LINES    DC    PL5'77'
         PRINT NOGEN
PRINT    DCB   DDNAME=SYSPRINT,RECFM=FA,BLKSIZE=133,LRECL=133,DSORG=PS,X
               MACRF=(PM)
AMSDCB   DCB   DDNAME=AMSCARD,RECFM=FB,MACRF=PM,LRECL=80,DSORG=PS
SYSIN    DCB      DDNAME=SYSIN,RECFM=FB,LRECL=80,DSORG=PS,             X
               MACRF=(GM),EODAD=OUTOF
         PRINT GEN
OUT      DC    136C' '
NOSYSIN  DC    CL130'  NO SYSIN DATA SET  '
AMSCARD  DS    0CL80
         DS    CL15
AMSDSN   DS    CL44
         DS    CL13
AMSDEVT  DS    CL2
AMSVOL   DS    CL6
*
DEFLG    DC    X'00'
WORKDEVT DC    XL2'00'          DEVICETYPE FOR ADDITIONAL CHECK
WORKVOL  DC    CL6' '           VOLUME SERIAL IN PROCESS
WORKUCB  DC    F'0'             ADDRESS OF UCB
EMPTY    ICVAFBFL  DSECT=NO    THE BUFFER LIST FOR CVAFDIR
         ORG   BFLHNOE         NUMBER OF ENTRIES
         DC    X'0104'
         ORG   BFLELTH
         DC    AL1(96)         LENGTH OF DSCB DATA PORTION
         ORG   BFLEBUF
         DC    A(STUFF)
         ORG
STUFF    DC    XL180'0'        THE BUFFER
         END
//LKED.SYSIN DD *
  SETCODE AC(1)
  NAME CATBYVOL(R)
