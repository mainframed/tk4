*          DATA SET 761KKSUBEX AT LEVEL 001 AS OF 10/04/83
         REGEQU
         TITLE 'PARSE SUBROUTINE  TO SAVE LEN AND DATA FOR A SUBFIELD'
SUBEX    CSECT
         SAVE  (14,12),,SUBEX-V1M0
         LR    R12,R15            R12->SUBEX
         USING SUBEX,R12
         LR    R11,R1             R11->SUBSECT
         USING SUBSECT,R11
         LR    R9,R13             R9->OLD SAVE
         L     R13,SUBWP          R13->NEW SAVE
         USING SUBWORK,R13
         ST    R9,SAVE+4
         ST    R13,8(,R9)
         L     R10,BPPLP          R10->BPPL
         USING BPPL,R10
         EJECT
         L     R7,OPDESCP         R7->OPER DESCRIPTOR
         L     R7,4(,R7)
         L     R7,4(,R7)          R7=SUBFIELD OFFSET
         L     R6,0(,R11)         R6->USER WORKAREA
         LA    R7,0(R6,R7)        R7->SUBFIELD AREA
         LH    R2,0(,R7)          R2=LENGTH OF AREA
         SH    R2,=H'2'           R2-2 FOR MVI AND EX
         BNP   NOFILL             NO LENGTH TO FILL
         MVI   2(R7),C' '         A BLANK
KMVC1    MVC   3(0,R7),2(R7)      BLANK FILLER
         EX    R2,KMVC1           FILL IT
*
NOFILL   L     R9,SUBPTR          R9->SUBFIELD
         LTR   R9,R9              ANY?
         BZ    SETERR             NONE
*
         L     R8,SUBLENPT        R8->LENGTH OF SUBFIELD
         LH    R8,0(,R8)          R8=LEN
         LTR   R8,R8              >0?
         BNP   SETERR             NO
         SH    R8,=H'3'           -3 FOR EX
         BM    SETERR             OOPS
SFM      MVC   2(0,R7),1(R9)      EXED MVC
         EX    R8,SFM             MOVE THE DATA
         LA    R8,1(,R8)          R8+1
         STH   R8,0(,R7)          PASS DATA LENGTH
         EJECT
SUBXX    SLR   R15,R15            RC=0
SUBX2    L     13,SAVE+4
         RETURN (14,12),RC=(15)
         BR    R14
         SPACE 3
SETERR   MVC   0(2,R7),=H'-1'
         B     SUBXX
         SPACE 3
         LTORG
         EJECT
SUBWORK  DSECT
SAVE     DS    18F
SUBFLG   DS    F
SUBSECT  DSECT
XP1      DS    F                  UNKN EXIT PARM
BPPLP    DS    F                  PTR TO BPPL
WORKP    DS    F                  PTR TO KMDPARS WORKAREA
*
SUBOL    DS    F                  PTR TO SUBWD OPLIST
SUBWP    DS    F                  PTR TO SUB WORKAREA
TUX      DS    F                  PTR TO TRUE UNKN EXIT
         BPPL  DSECT
         END
