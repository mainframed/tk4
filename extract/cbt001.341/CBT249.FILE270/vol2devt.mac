         PRINT ON,NOGEN
         SPACE 3
*        THIS PROGRAM IS A SUBROUTINE WHICH WILL ACCEPT A VOLUME SERIAL
*        NUMBER AND PASS BACK TO THE INVOKER THE DEVICE TYPE
*
*        TO CALL THIS PROGRAM, DO THE FOLLOWING :
*
*           MOVE YOUR VOL SER TO VOL-SER-NO OF VOL2DEVT-I-O-AREA.
*
*           CALL 'VOL2DEVT' USING VOL2DEVT-I-O-AREA.
*
*           AFTER THE CALL, DEVICE-TYPE WILL CONTAIN THE DEVICE TYPE
*           AND SIGNIF-LENGTH-OF-DEVICE-TYPE WILL CONTAIN THE
*           SIGNIFICANT LENGTH OF THE DEVICE TYPE
*
*                WHERE:
*
*              01  VOL2DEVT-I-O-AREA.
*                  05  VOL-SER-NO                    PIC X(6).
*                  05  DEVICE-TYPE                   PIC X(8).
*                  05  SIGNIF-LENGTH-OF-DEVICE-TYPE  PIC S9(3) COMP.
*
*
*         RETURN CODES        MEANING
*         -----------------   -----------------------------------
*
*                 0           SUCCESSFUL COMPLETION
*                 4           VOLUME NOT MOUNTED
*                 12          NON RECOGNIZABLE UNIT TYPE
         EJECT
         SPACE 3
*
**********************************************
*                                            *
*        I N I T I A L I Z A T I O N         *
*                                            *
**********************************************
*
VOL2DEVT CSECT
         SAVE  (14,12)
         BALR  12,0
         USING *,12
         ST    13,SAVEAREA+4
         LA    13,SAVEAREA
         B     STARTIT
         SPACE
SAVEAREA DC    18F'0'
         SPACE 3
********************************************************************
*        EQUATE RESISTERS 0 THRU 15 TO MORE MEANINGFUL NAMES       *
********************************************************************
         SPACE 3
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         EJECT
         SPACE 3
*
********************************************************************
*                                                                  *
*        SAVE REGISTER 1 IN REGISTER 11 AND LOAD PARAMETER FIELD   *
*        INTO IOAREA                                               *
*                                                                  *
********************************************************************
*
         SPACE 3
STARTIT  DS    0D
         LR    R11,R1              SAVE REGISTER 1 CONTENTS IN REG 11
         L     R9,0(R11)
         MVC   IOAREA,0(R9)
         EJECT
         SPACE 3
*
********************************************************************
*                                                                  *
*        M A I N   L I N E   L O G I C                             *
*                                                                  *
********************************************************************
*
         L     R9,16                   ADDRESS OF CVT INTO R9
         XC    UCBWA,UCBWA             UCB LOOKUP LIST FROM CVT
UCBSCAN  L     R15,1076(R9)
         LA    R1,PLIST
         BALR  R14,R15
         LTR   R15,R15
         BNZ   UCBERR1
         L     R6,UCBADD               GET ADDRESS OF A UCB
         TM    0(R6),X'80'             IS IT VIO?
         BO    UCBSCAN                 IF YES, BRANCH TO UCBSCAN
         TM    3(R6),X'80'             IS IT ONLINE?
         BNO   UCBSCAN                 IF NOT, BRANCH TO UCBSCAN
         CLC   VOLSER,28(R6)           TABLE ENTRY VS VOL IN UCB
         BNE   UCBSCAN                 IF NOT EQUAL, BRANCH TO UCBSCAN
         CLI   19(R6),X'0B'            3350 DA?
         BE    D3350                   IF YES, BRANCH TO D3350
         CLI   19(R6),X'09'            3330 DA?
         BE    D3330                   IF YES, BRANCH TO D3330
         CLI   19(R6),X'0D'            3330 DA?
         BE    D3330                   IF YES, BRANCH TO D3330
         CLI   19(R6),X'06'            2305 DA?
         BE    D2305                   IF YES, BRANCH TO D2305
         CLI   19(R6),X'07'            2305-2 DA?
         BE    D23052                  IF YES, BRANCH TO D23052
         CLI   19(R6),X'0E'            3380 DA?
         BE    D3380                   IF YES, BRANCH TO 3380
         B     UCBERR2                 WHAT IN THE HECK IS IT THEN?
UCBERR1  LA    R4,4                   SET R4 = 4
         B     SHUTDOWN               BRANCH TO SHUTDOWN
UCBERR2  LA    R4,12                  SET R4 = 12
         B     SHUTDOWN               BRANCH TO SHUTDOWN
D3350    MVC   DEVTTYPE,=C'3350    '  MOVE 3350 TO DEVTTYPE
         LA    R7,4                   ---
         STH   R7,LEN                    --- DEVTLEN = 4
         MVC   DEVTLEN,LEN            ---
         B     ALLGOOD                BRANCH TO ALLGOOD
D3330    MVC   DEVTTYPE,=C'3330-1  '  MOVE 3330-1 TO DEVTTYPE
         LA    R7,6                   ---
         STH   R7,LEN                    --- DEVTLEN = 6
         MVC   DEVTLEN,LEN            ---
         B     ALLGOOD                BRANCH TO ALLGOOD
D2305    MVC   DEVTTYPE,=C'2305    '  MOVE 2305 TO DEVTTYPE
         LA    R7,4                   ---
         STH   R7,LEN                    --- DEVTLEN = 4
         MVC   DEVTLEN,LEN            ---
         B     ALLGOOD                BRANCH TO ALLGOOD
D23052   MVC   DEVTTYPE,=C'2305-2  '  MOVE 2305-2 TO DEVTTYPE
         LA    R7,6                   ---
         STH   R7,LEN                    --- DEVTLEN = 6
         MVC   DEVTLEN,LEN            ---
         B     ALLGOOD                BRANCH TO ALLGOOD
D3380    MVC   DEVTTYPE,=C'3380    '  MOVE 3380 TO DEVTTYPE
         LA    R7,4                   ---
         STH   R7,LEN                    --- DEVTLEN = 4
         MVC   DEVTLEN,LEN            ---
ALLGOOD  LA    R4,0                   SET R4 = 0
         B     SHUTDOWN               BRANCH TO SHUTDOWN
         EJECT
         SPACE 3
*
********************************************************************
*                                                                  *
*        E N D  O F   J O B   P R O C E S S I N G                  *
*                                                                  *
********************************************************************
*
SHUTDOWN LR    R15,R4
         L     R9,0(R11)
         MVC   0(16,R9),IOAREA
         L     R13,4(R13)
         RETURN (14,12),RC=(15)
         EJECT
         SPACE 3
*
********************************************************************
*                                                                  *
*        BREAKDOWN OF IOAREA                                       *
*                                                                  *
********************************************************************
*
IOAREA   DS    0CL16
VOLSER   DS    CL6
DEVTTYPE DS    CL8
DEVTLEN  DS    CL2
         EJECT
         SPACE 3
*
********************************************************************
*                                                                  *
*        D A T A   A R E A S                                       *
*                                                                  *
********************************************************************
*
         SPACE 3
         DS    0F
LEN      DS    H
PLIST    DS    0F
UCBWAP   DC    A(UCBWA)
UCBCLP   DC    A(DEVCL)
UCBPTR   DC    X'80',AL3(UCBADD)
UCBWA    DS    XL100
DEVCL    DC    X'20'
UCBADD   DS    A
         END
