*          DATA SET 752ABTIMED AT LEVEL 001 AS OF 07/19/74
         PUNCH ' ALIAS TIDATE '
         ENTRY TIDATE         ENTRYPOINT FOR FORTRAN CALLING PROGRAMS
TIMEDATE CSECT
TIDATE   EQU   *
* THIS ROUTINE RETURNS INF IN 3 AREAS PROVIDED BY THE CALLING PROG.
* AREA ADDRESSES MUST BE GIVEN IN THIS ORDER 1)TIME HHMMSSTH 8 BYTES
* 2)DATE-YYDDD 5 BYTES 3)DATE-YYMMDD 6 BYTES. ALL INF RETURNED IS ZONED
* DECTMAL, ALL ZONES=X'F'.
         SAVE  (14,12)
         BALR  11,0
         USING *,11
         ST    1,PARMADDR               STORE A(ARGLIST)
         ST    13,SAVE1+4               ADDR HSA IN SAVE1
         LA    12,SAVE1
         ST    12,8(13)                 ADDR SAVE1 IN HSA
         LR    13,12                    ADDR SAVE1 IN R13
TRYAGAIN TIME  DEC                      R1=DATE=00YYDDDC  C IS SIGN
*                                       R0=TIME=HHMMSSTH    NO SIGN
         ST    1,DATI
         ST    0,TIMI
         UNPK  DATIU,DATI+1(3)
         MVZ   DATIU+4(1),=X'FF'        SET ZONE OF LAST DIGIT =F
         UNPK  TIMIU(9),TIMI(5)
         PACK  WPYY,DATIU(2)            SET UP YEAR FOR DIVIDE
         DP    WPYY,=P'4'               QQQSRS
         CP    WPYY+2(1),=P'0'          TEST REMAINDER=0 (= LEAP YEAR)
         BC    7,NOTLEAP
         AP    FEB,=P'1'                SET FEB=29
         AP    MAXDAYS,=P'1'
NOTLEAP  PACK  WPDDD,DATIU+2(3)         SET UP DATE FOR MO CALC
         CP    WPDDD,MAXDAYS
         BC    2,ERR
         LA    3,MONTHS
NXTMO    AP    MONTHCT,=P'1'
         SP    WPDDD,0(2,3)
         BC    12,MFOUND                IF 0 OR -VE
         LA    3,2(3)                   STEP R3 BY 2
         BC    15,NXTMO
MFOUND   AP    WPDDD,0(2,3)
*              AT THIS POINT SHOULD HAVE DATE AS FOLLOWS EG 31 DEC 67
*                             DATIU     F6F7F3F6F5
*                             MONTHCT   0125
*                             WPDDD     000315
         MVN   MONTHCT+1(1),=X'FF'
         MVN   WPDDD+2(1),=X'FF'
         MVC   DATOU(2),DATIU           SET UP YEAR
         UNPK  DATOU+2(2),MONTHCT       SET UP MONTH
         UNPK  DATOU+4(2),WPDDD
         L     1,PARMADDR               A(ARGLIST)
         L     2,0(1)                   A(ARG1)
         MVC   0(8,2),TIMIU             FHFHFMFMFSFSFTFH
         L     2,4(1)
         MVC   0(5,2),DATIU             FYFYFDFDFD
         L     2,8(1)
         MVC   0(6,2),DATOU             FYFYFMFMFDFD
         BAL   4,RESTORE
         L     13,SAVE1+4
       RETURN  (14,12),T,RC=0
RESTORE  MVI   SW,X'FF'                 RESTORE CONST TO ORIG STATE
         ZAP   FEB,=P'28'               ROUTINE SHOULD BE REUSEABLE
         ZAP   MAXDAYS,=P'365'
         ZAP   MONTHCT,=P'0'
         BCR   15,4
ERR      WTO   'OPERATOR NOTE. COMPUTER HAS BAD DATE-PLEASE RESET.'
         BAL   4,RESTORE
        STIMER WAIT,DINTVL=DURATION
         BC    15,TRYAGAIN
SAVE1     DS    18F
MONTHS   DS    0C
JAN      DC    P'31'
FEB      DC    P'28'                    CHANGES
MAR      DC    P'31'
APR      DC    P'30'
MAY      DC    P'31'
JUN      DC    P'30'
JUL      DC    P'31'
AUG      DC    P'31'
SEP      DC    P'30'
OCT      DC    P'31'
NOV      DC    P'30'
DEC      DC    P'31'
MAXDAYS  DC    P'365'                   CHANGES
MONTHCT  DC    PL2'0'                   CHANGES
         DS    0D
DURATION DC    CL8'00001000'
PARMADDR DC    A(0)
         DS    0F
DATI     DS    PL4                      00YYDDDC
TIMI     DS    PL4                      HHMMSSTH
         DC    XL1'0F'                  DUMMY PACKED SIGN
DATIU    DS    CL5                      FYFYFDFDCD
TIMIU    DS    CL8                      FHFHFMFMFSFSFTFH
         DS    CL1                      B0   !YTE FOR DUMMY SIGN
DATYMD   DS    0C
DATYY    DS    CL2
DATMM    DS    CL2
DATDD    DS    CL2
WPYY     DS    PL3
WPDDD    DS    PL3
SW       DC    X'FF'                    CHANGES
DATOU    DS    CL6
         END
TESTDATE CSECT      TEST DRIVER FOR VARIOUS DATE PROGRAMS
         USING TESTDATE,R15
         SAVE  (14,12),,*
         LR    R14,R13
         ST    R13,SAVE+4
         CNOP  0,4
         BAL   R13,AROUND
         DROP  R15
         USING SAVE,R13
SAVE     DC    18A(0)
AROUND   DS    0H
         ST    R13,8(R14)
         CALL  TIMEDATE,(TIME,JDATE,SDATE)
         ST    R15,RC              SAVE   RETURN CODE
         NOP   END
         CNOP  0,4
         BAL   R1,SVC35
         DC    AL2(43)        LENGTH
         DC    XL2'8000'      MCS FLAGS
         DC    CL6' TIME-'
TIME     DC    CL8' '
         DC    CL7' JDATE-'
JDATE    DC    CL5' '
         DC    CL7' SDATE-'
SDATE    DC    CL6' '
         DC    XL2'0000'      DESCRIPTOR CODES
         DC    XL2'FFFF'      ROUTING FLAGS
SVC35    DS    0H
         SVC   35             WTO SVC
         SPACE 1
END      EQU   *
         L     R15,RC              RESTORE RETURN CODE
         L     R13,SAVE+4
       RETURN  (14,12),T,RC=(15)
RC       DC    F'2048'
         SPACE
R15 EQU 15
R14 EQU 14
R13 EQU 13
R1  EQU 1
R0  EQU 0
         END   TESTDATE
