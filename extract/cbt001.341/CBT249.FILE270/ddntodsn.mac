         PRINT ON,NOGEN
         SPACE 3
*        THIS PROGRAM IS A SUBROUTINE WHICH WILL ACCEPT A DDNAME
*        PASSED TO IT IN THE PARAMETER LIST AND PASS BACK THE DSNAME
*        ALLOCATED TO THAT DDNAME AND THE VOLUME SERIAL NUMBER(S)
*        WHERE THE DATASET RESIDES.
*
*        TO CALL THIS PROGRAM, DO THE FOLLOWING :
*
*           MOVE YOUR 8 BYTE DDNAME TO DDNAME OF DDNTODSN-I-O-AREA.
*
*           CALL 'DDNTODSN' USING DDNTODSN-I-O-AREA.
*
*                WHERE:
*
*                      01  DDNTODSN-I-O-AREA.
*                          05  FILLER  VALUE 'VS' PIC X(2).
*                          05  DDNAME             PIC X(8).
*                          05  DSNAME             PIC X(44).
*                          05  VOL-COUNT          PIC S9(5) COMP.
*                          05  VOL-TABLE
*                              OCCURS 5 TIMES     PIC X(6).
*                          05  SUCCESS-INDICATOR  PIC 99.
*
*
*         SUCCESS-INDICATOR   MEANING
*         -----------------   -----------------------------------
*
*                00           DSNAME WAS SUCCESSFULLY GENERATED
*                04           DDNAME WAS NOT ALLOCATED
*                                (I.E. DSNAME COULD NOT BE GENERATED)
         EJECT
         SPACE 3
*
*
*
*
*
*
*        ASSIGNMENT OF REGISTERS
*        -----------------------
*
*        REGISTER      USAGE
*        --------      -------------------------------------------
*
*           1          ADDRESS OF PARAMETER LIST
*           2          NOT USED
*           3          WORK REGISTER
*           4          WORK REGISTER
*           5          WORK REGISTER
*           6          WORK REGISTER
*           7          ADDRESS OF TIOT
*           8          ADDRESS OF TCB
*           9          ADDRESS OF CVT
*           10         ADDRESS OF THE JFCB
*           11         SAVE REGISTER 1
*           12         BASE REGISTER
*           13         ADDRESS OF SAVE AREA
*           14         RETURN ADDRESS
*           15         ENTRY POINT ADDRESS
*
*
*
*
         EJECT
         SPACE 3
*
**********************************************
*                                            *
*        I N I T I A L I Z A T I O N         *
*                                            *
**********************************************
*
DDNTODSN CSECT
         SAVE  (14,12)
         BALR  12,0
         USING *,12
         ST    13,SAVEAREA+4
         LA    13,SAVEAREA
         B     STARTIT
         SPACE
SAVEAREA DC    18F'0'
         SPACE 3
********************************************************************
*        EQUATE RESISTERS 0 THRU 15 TO MORE MEANINGFUL NAMES       *
********************************************************************
         SPACE 3
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         EJECT
         SPACE 3
*
********************************************************************
*                                                                  *
*        SAVE REGISTER 1 IN REGISTER 11 AND LOAD PARAMETER FIELD   *
*        INTO IOAREA                                               *
*                                                                  *
********************************************************************
*
         SPACE 3
STARTIT  DS    0D
         LR    R11,R1              SAVE REGISTER 1 CONTENTS IN REG 11
         L     R9,0(R11)
         MVC   IOAREA,0(R9)
         MVC   DSN(44),=CL44' '       INIT DSN OF IOAREA WITH SPACES
         MVC   VOLTABLE(30),=CL30' '  INIT VOLTABLE OF IOAREA W SPACES
         MVC   VOLCNT(4),=X'00000000' INIT VOLCNT OF IOAREA WITH
*                                        BINARY ZAROES
         EJECT
         SPACE 3
*
********************************************************************
*                                                                  *
*        M A I N   L I N E   L O G I C                             *
*                                                                  *
********************************************************************
*
         BAL   R14,FINDDDN         PERFORM ROUTINE TO FIND THE
*                                     DDNAME IN THE TIOT
         CLC   FINDDNAM,=C'YES'    WAS DDNAME ALLOCATED ?
         BNE   SHUTDOWN            IF NOT, BRANCH TO SHUTDOWN
         BAL   R14,PRCSJFCB        PERFORM ROUTINE TO LOAD THE DSN
*                                     AND VOL(S) FROM THE JFCB
         B     SHUTDOWN            BRANCH TO SHUTDOWN
         EJECT
         SPACE 3
*
********************************************************************
*                                                                  *
*        THIS ROUTINE IS RESPONSIBLE FOR CHAINING FROM THE CVT     *
*        TO THE TCB TO THE TIOT AND LOCATING THE DDNAME AND THE    *
*        FINDING THE POINTER TO THE APPROPRIATE JFCB               *
*                                                                  *
********************************************************************
*
FINDDDN  ST    R14,SAVEIT1      STORE R14 CONTENTS IN SAVEIT1
         L     R9,16            R9 ==> ADDRESS OF CVT
         L     R6,0(R9)         R6 ==> ADDRESS OF A DOUBLE WORD
*                                        POINTING TO THE TCB
         LA    R5,TCBDWRD       R5 ==> ADDRESS OF DOUBLE WORD TCBDWRD
         MVC   0(8,R5),0(R6)    MOVE TCB ADDRESSES TO TCBDWRD
         L     R8,TCBDWRD+4     R8 ==> ADDRESS OF TCB
         L     R7,12(R8)        R7 ==> ADDRESS OF THE TIOT
         LR    R4,R7            R4 ==> ADDRESS OF THE TIOT
         A     R4,=F'24'        R4 ==> FIRST LENGTH FIELD
TIOTLOOP CLC   0(4,R4),=X'00000000'  END OF TIOT ?
         BE    NOTALLOC         IF YES, BRANCH TO NOTALLOC
         LR    R3,R4            R3 ==> R4
         A     R3,=F'4'         ADD 4 TO R3
         CLC   DDNAME(8),0(R3)  DDNAME PASSED = DDNAME IN TIOT ?
         BE    YESALLOC         IF YES, BRANCH TO YESALLOC
         LA    R6,0
         IC    R6,0(R4)
         AR    R4,R6
         B     TIOTLOOP         BRANCH TO TIOTLOOP
NOTALLOC MVC   FINDDNAM,=C'NO ' MOVE 'NO ' TO FINDDNAM
         B     EXITDDN          BRANCH TO EXITDDN
YESALLOC MVC   FINDDNAM,=C'YES' MOVE 'YES' TO FINDDNAM
         A     R3,=F'8'         ADD 8 TO REGISTER 3
         MVC   JFCBADDR+1(3),0(R3)
         L     R10,JFCBADDR     R10 ==> ADDRESS OF THE JFCB
EXITDDN  L     R14,SAVEIT1      RESTORE REG 14 WITH SAVEIT1 CONTENTS
         BR    14               RETURN
         EJECT
         SPACE 3
*
********************************************************************
*                                                                  *
*        THIS ROUTINE IS RESPONSIBLE FOR PROCESSING THE JFCB.      *
*        THE ADDRESS OF THE JFCB IS IN REGISTER 10.  THIS          *
*        ROUTINE LOADS THE DSNAME, VOL-CNT, AND VOL-TABLE          *
*        FROM THE APPROPRIATE FIELDS IN THE JFCB                   *
*                                                                  *
********************************************************************
*
PRCSJFCB ST    R14,SAVEIT2      STORE R14 CONTENTS IN SAVEIT2
         MVC   DSN(44),16(R10)  MOVE DSNAME IN JFCB TO DSN IN IOAREA
         LA    R5,0             R5 ==> ZERO
         IC    R5,133(R10)      STICK R5 WITH JFCBNVOL
         ST    R5,VOLNO
         MVC   VOLCNT(4),VOLNO        GENERATE VOL-CNT OF IOAREA
         MVC   VOLTABLE(30),134(R10)  GENERATE VOL-TABLE OF IOAREA
         L     R14,SAVEIT2      RESTORE REG 14 WITH SAVEIT2 CONTENTS
         BR    14               RETURN
         EJECT
         SPACE 3
*
********************************************************************
*                                                                  *
*        E N D  O F   J O B   P R O C E S S I N G                  *
*                                                                  *
********************************************************************
*
SHUTDOWN CLC   FINDDNAM,=C'YES'    WAS DDNAME ALLOCATED ?
         BNE   NOPE                IF NOT, BRANCH TO NOPE
         LA    R15,0               SET RC = 0
         MVC   INDICATR,=C'00'     MOVE '00' TO INDICATR
         B     PLESQUIT            BRANCH TO PLESQUIT
NOPE     LA    R15,4               SET RC = 4
         MVC   INDICATR,=C'04'     MOVE '04' TO INDICATR
PLESQUIT L     R9,0(R11)
         MVC   0(90,R9),IOAREA
         L     R13,4(R13)
         RETURN (14,12),RC=(15)
         EJECT
         SPACE 3
*
********************************************************************
*                                                                  *
*        BREAKDOWN OF IOAREA                                       *
*                                                                  *
********************************************************************
*
IOAREA   DS    0CL90
         DS    CL2
DDNAME   DS    CL8
DSN      DS    CL44
VOLCNT   DS    BL4
VOLTABLE DS    CL30
INDICATR DS    CL2
         EJECT
         SPACE 3
*
********************************************************************
*                                                                  *
*        D A T A   A R E A S                                       *
*                                                                  *
********************************************************************
*
         SPACE 3
         DS    0F
TCBDWRD  DS    D
SAVEIT1  DS    F
SAVEIT2  DS    F
JFCBADDR DC    F'0'
VOLNO    DS    F
FINDDNAM DS    CL3
         END
