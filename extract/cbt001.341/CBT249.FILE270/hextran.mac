*          DATA SET 761KKHXTRN AT LEVEL 001 AS OF 06/07/78
         TITLE 'HEXTRAN  -  SUBROUTINE FOR HEX TRANSLATIONS'
* WRITTEN BY KERMIT KISER - WASHINGTON STATE DP SERVICE CENTER (WDPSC)
*
HEXTRAN  ENTERR
         LR    R6,R1             SAVE PARM PTR
         LM    R10,R11,0(R1)      R10,R11->FIRST TWO PARMS
         TM    4(R1),X'80'        MORE PARMS?
         BNO   VL1                YES
         LA    R9,8               R9=8=DEFAULT CHAR LEN
         B     CTH                CHAR TO HEX IS DEFAULT
         EJECT
*  PICK  UP  PARMS IF VARIABLE LENGTH LIST
VL1      SLR   R15,R15            R15=0
         SLR   R9,R9              R9=0
         SLR   R8,R8              R8=0
         L     R2,8(,R1)          R2->3RD PARM
WHATPARM CLC   0(2,R2),=CL2'CT'   CHAR TO HEX?
         BE    CKCTH              MAYBE
         CLC   0(2,R2),=CL2'HT'   HEX TO CHAR
         BNE   TRYLEN             NO
CKHTC    CLI   2(R2),C'C'         HTC?  (VERIFY)
         BNE   BADPARM            NO->BAD PARM
         LA    R15,4              R15=4->SAVE HTC FLAG
         B     MOREPARM           SEE IF MORE
CKCTH    CLI   2(R2),C'H'         CTH?  (VERIFY)
         BNE   BADPARM            NO->BAD PARM
         B     MOREPARM           SEE IF MORE
TRYLEN   LH    R9,0(,R2)          R9=LENGTH?
         LTR   R9,R9              R9>0?
         BNP   BADPARM            NO-> BAD PARM
*
MOREPARM LTR   R8,R8              R8=0?
         BNZ   NOMORE             NO
         LA    R8,4               YES->FLAG HERE
         TM    8(R1),X'80'        3 PARMS?
         BO    NOMORE             YES->NO MORE TO LOOK AT
         L     R2,12(,R1)         R2->4TH PARM
         B     WHATPARM           SEE WHAT IT IS
NOMORE   LTR   R9,R9              R9=0?
         BNZ   WHATTRAN           NO
         LA    R9,8               YES->R9=8=DEFAULT LENGTH
WHATTRAN LTR   R15,R15            HTC?
         BNZ   HTC                YES-> DO THAT
*
         TITLE 'HEXTRAN  -  TRANSLATE ROUTINES'
CTH      BAL   R14,LENCHK         R8=WORKING LENGTH
         BCTR  R8,R0              R8=R8-1
         EX    R8,VALCK           NON-HEX CHARS?
         BNZ   BADCHAR            YES
         EX    R8,MVCHAR          NO->PUT IN CHARAREA
         EX    R8,TRANCHAR        MAKE IT PACKABLE
         PACK  HEXAREA(5),CHARAREA(9)  PACK IT
         SRL   R8,1(R0)           R8=R8/2
         EX    R8,MVCHEX          GIVE HIM THE HEX
         BAL   R14,DONECHK        DONE?
         B     CTH                NO
*
*
HTC      BAL   R14,LENCHK         GET LENGTH TO USE
         LR    R7,R8              R7=R8
         SRL   R7,1(R0)           R7=R7/2
         EX    R7,MVHEX           GET THE HEX
         UNPK  CHARAREA(9),HEXAREA(5)  UNPACK IT
         TR    CHARAREA(8),TABLE3  TRANSLATE TO EBCDIC
         BCTR  R8,R0              R8=R8-1
         EX    R8,GIVECHAR        GIVE IT TO HIM
         BAL   R14,DONECHK        DONE?
         B     HTC                NO
*
         TITLE 'HEXTRAN  -  SUBROUTINES'
LENCHK   CH    R9,=H'8'           LEN TO TRAN > 8 ?
         BH    USE8               YES
         LR    R8,R9              R8=R9
         SLR   R9,R9              R9=0
         BR    R14                RETURN
USE8     LA    R8,8               R8=8
         SH    R9,=H'8'           R9=R9-8
         BR    R14                RETURN
*
DONECHK  LTR   R9,R9              R9=0?
         BZ    DONE               YES->MUST BE THRU
         LA    R10,8(,R10)        R10=R10+8 NEXT CHUNK
         LA    R11,4(,R11)        R11=R11+4  "     "
         BR    R14                RETURN
*
VALCK    TRT   0(0,R10),TABLE1
MVCHAR   MVC   CHARAREA(0),0(R10)
TRANCHAR TR    CHARAREA(0),TABLE2
MVCHEX   MVC   0(0,R11),HEXAREA
*
MVHEX    MVC   HEXAREA(0),0(R11)
GIVECHAR MVC   0(0,R10),CHARAREA
*
BADPARM  LA    R15,4              R15=RC=4
         B     RETURN             EXIT
*
BADCHAR  LA    R15,8              R15=RC=8
         B     RETURN             EXIT
*
*                                   CALL IT A DAY
DONE     SLR    R15,R15             RETURN CODE = 0
RETURN   LEAVER
*
         TITLE 'HEXTRAN  -  CONSTANTS'
*
TABLE1   DC    256X'04'
         ORG   TABLE1+X'C1'
         DC    6X'00'
         ORG   TABLE1+X'F0'
         DC    10X'00'
         ORG
*
TABLE2   DC    256X'00'
         ORG   TABLE2+X'C1'
         DC    X'FAFBFCFDFEFF'
         ORG   TABLE2+X'F0'
         DC    C'0123456789'
         ORG
         DC     C'0123456789ABCDEF'
TABLE3   EQU   *-256
         LTORG
         TITLE 'HEXTRAN  -  DYNAMIC WORKAREA'
WORKAREA DSECT
SAVEAREA DS    18F                SAVEAREA
CHARAREA DS    CL10
HEXAREA  DS    XL6
WORKLEN  EQU   *-WORKAREA
         END
