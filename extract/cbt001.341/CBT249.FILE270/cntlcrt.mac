*          DATA SET 761KKCNTL  AT LEVEL 001 AS OF 01/08/79
         TITLE 'CNTLCRT  -  ROUTINE FOR CRT TTY CONTROL'
* WRITTEN BY KERMIT KISER - WASHINGTON STATE DP SERVICE CENTER (WDPSC)
*
CNTLCRT  ENTERR
         L     R4,0(R1)           R4 -> PARM FIELD
         LA    R10,4(R4)          R10-> DATA FIELD
         LH    R9,0(R4)           R9 =  PARM LEN
         SH    R9,=H'2'           R9=R9-2
         LTR   R9,R9              R9>0?
         BNP   BADPARM            NO -> TROUBLE
         SLR   R6,R6              R6=0
         CLI   3(R4),C'X'         TRAN TO HEX?
         BNE   TPUT               NO -> WRITE IT NOW
*
         TITLE 'CNTLCRT  -  TRANSLATE ROUTINE'
*
         LR    R5,R9              R5 = SAVE LENGTH
         SRL   R5,1(0)            R5=R5/2
         LR    R0,R5              R0= LEN TO GET
         GETMAIN R,LV=(0)
         LR    R6,R1              R6 -> SAVE OUT FIELD
         LR    R11,R1             R11 -> HEXAREA FOR RESULT
*
*
*
CTH      BAL   R14,LENCHK         R8=WORKING LENGTH
         BCTR  R8,R0              R8=R8-1
         EX    R8,VALCK           NON-HEX CHARS?
         BNZ   BADCHAR            YES
         EX    R8,MVCHAR          NO->PUT IN CHARAREA
         EX    R8,TRANCHAR        MAKE IT PACKABLE
         PACK  HEXAREA(5),CHARAREA(9)  PACK IT
         SRL   R8,1(R0)           R8=R8/2
         EX    R8,MVCHEX          GIVE HIM THE HEX
         BAL   R14,DONECHK        DONE?
         B     CTH                NO
*
*
*
TRANDONE LR    R9,R5              R9 = LEN TO WRITE
         LR    R10,R6             R10 -> DATA TO WRITE
         B     TPUT
         TITLE 'CNTLCRT  -  TPUT ROUTINES'
TPUT     DS    0H
         CLI   2(R4),C'C'         CONTROL?
         BNE   TRYASIS            NO
         TPUT  (R10),(R9),CONTROL
         B     EXIT
*
TRYASIS  CLI   2(R4),C'A'         ASIS?
         BNE   TRYEDIT            NO
         TPUT  (R10),(R9),ASIS
         B     EXIT
*
         EJECT
TRYEDIT  CLI   2(R4),C'E'         EDIT?
         BNE   TRYFULL            NO
         TPUT  (R10),(R9),EDIT
         B     EXIT
*
TRYFULL  CLI   2(R4),C'F'         FULLSCRN?
         BNE   TYPERR             NO
         TPUT  (R10),(R9),FULLSCR
         B     EXIT
*
         TITLE 'CNTLCRT  -  SUBROUTINES'
*
*
LENCHK   CH    R9,=H'8'           LEN TO TRAN > 8 ?
         BH    USE8               YES
         LR    R8,R9              R8=R9
         SLR   R9,R9              R9=0
         BR    R14                RETURN
USE8     LA    R8,8               R8=8
         SH    R9,=H'8'           R9=R9-8
         BR    R14                RETURN
*
*
DONECHK  LTR   R9,R9              R9=0?
         BZ    TRANDONE           YES->MUST BE THRU
         LA    R10,8(,R10)        R10=R10+8 NEXT CHUNK
         LA    R11,4(,R11)        R11=R11+4  "     "
         BR    R14                RETURN
*
*
*
VALCK    TRT   0(0,R10),TABLE1
MVCHAR   MVC   CHARAREA(0),0(R10)
TRANCHAR TR    CHARAREA(0),TABLE2
MVCHEX   MVC   0(0,R11),HEXAREA
*
*
*
TYPERR   TPUT  TYPEMESS,40        GIVE ERROR
         B     EXIT
TYPEMESS DC    CL40'TPUT TYPE ERROR'
*
*
*
BADPARM  LA    R15,4              R15=RC=4
         B     RETURN             EXIT
*
*
BADCHAR  LA    R15,8              R15=RC=8
         B     RETURN             EXIT
         TITLE 'CNTLCRT  -  EXIT ROUTINES'
*
*                                   CALL IT A DAY
EXIT     EQU   *
         LTR   R6,R6              R6=0?
         BZ    DONE               YES->NOTHING TO FREE HERE
         LR    R1,R6              R1->AREA TO FREE
         LR    R0,R5              R0= LENGTH TO FREE
         FREEMAIN R,LV=(0),A=(1)  FREE THE TRANS MSG
*
DONE     SLR    R15,R15             RETURN CODE = 0
*
RETURN   LEAVER
*
         TITLE 'CNTLCRT  -  CONSTANTS'
*
TABLE1   DC    256X'04'
         ORG   TABLE1+X'C1'
         DC    6X'00'
         ORG   TABLE1+X'F0'
         DC    10X'00'
         ORG
*
TABLE2   DC    256X'00'
         ORG   TABLE2+X'C1'
         DC    X'FAFBFCFDFEFF'
         ORG   TABLE2+X'F0'
         DC    C'0123456789'
         ORG
         DC     C'0123456789ABCDEF'
TABLE3   EQU   *-256
         LTORG
         TITLE 'CNTLCRT  -  DYNAMIC WORKAREA'
WORKAREA DSECT
SAVEAREA DS    18F                SAVEAREA
CHARAREA DS    CL10
HEXAREA  DS    XL6
WORKLEN  EQU   *-WORKAREA
         END
