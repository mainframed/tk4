//DN300150 JOB (2100,56-D),'DPARS 0682 T',CLASS=H,MSGCLASS=A
//X EXEC ASMHCL,LIBRARY='WDPSC.PROGLIB',LNDSP=SHR,
//   OPTION1=TERM
REPROENQ CSECT
         STM   R14,R12,12(R13)            SAVE REGISTERS
         LR    R11,R13
         BALR  R12,0                      LOAD BASE REGISTER
         USING *,R12
         B     IDENTITY
         DC    C'REPROENQ'
         DC    C'&SYSDATE'
         DC    C'&SYSTIME'
IDENTITY DS    0H
         LA    R13,SAVE                   SAVE AREA
         ST    R13,8(0,R11)               AND BACK CHAIN
         ST    R11,4(0,R13)
         SPACE 5
         L     R6,540       TCB ADDRESS
         L     R6,12(,R6)   TIOT ADDRESS
         LA    R6,24(,R6)   DD ENTRIES WITHIN TIOT
DDNAME   CLC   4(8,R6),=CL8'STEPCAT'    FIND STEPCAT DDNAME
         BE    SETUP
         SR    R7,R7
         ICM   R7,1,0(R6)
         BZ    CALLSANS
         AR    R6,R7
         B     DDNAME
SETUP    EQU   *
         WTO   'CATALOG BACKUP HAS STARTED. ENQUEUE BUILDUP MAY OCCUR OX
               N THE CATALOG. DO NOT CANCEL',ROUTCDE=(1,2,5),DESC=4
         ICM   R8,7,17(R6)         UCB  ADDRESS
         ICM   R7,7,12(R6)         JFCB ADDRESS - 16
         MVC   RNAME,16(R7)        ADJUST TO JFCB
         ST    R8,UCBAD         PUT UCB ADDRESS AWAY
         CLI   RNAME+21,C' '    IS NAME SHORT
         MVI   RLENGTH,44
         BNE   RSVX
         MVI   RLENGTH,20
*   ISSUE RESERVE/ENQ FOR CATALOG
RSVX     EQU   *
         RESERVE (QNAME,RLENGTH,E,00,SYSTEMS),RET=HAVE,UCB=UCBAD
         B     CALLCON
CALLSANS WTO   'IDCAMS CALLED WITHOUT RESERVE',ROUTCDE=11
CALLCON  LINK  EP=IDCAMS,PARAM=(PARMLIST),VL=1
         LR    R5,R15
         DEQ   (QNAME,RLENGTH,00),UCB=UCBAD
         WTO   'CATALOG BACKUP HAS COMPLETED. ENQUEUE BUILDUP SHOULD DIX
               SSAPEAR',ROUTCDE=(1,2,5),DESC=4
FINIT    EQU   *                      FINISHED FOR SURE
         L     R13,SAVE+4
         LR    R15,R5
         RETURN  (14,12),T,RC=(15)         AND LEAVE
SAVE     DS    18F
PARMLIST DC    F'0'
UCBAD    DC    F'0'
QNAME    DC    C'SYSIGGV2'       MAJOR NAME FOR RESERVE
RLENGTH  DC    X'0'              RNAME LENGTH, MUST BE ADJACENT
RNAME    DC    CL44' '           MINOR NAME FOR RESERVE
         REGISTER ,
         END
//LKED.SYSIN  DD *
   SETCODE AC(1)
   NAME REPROENQ(R)
