*          DATA SET 761KKMODT  AT LEVEL 001 AS OF 04/02/81
         TITLE 'MODTEST - PROGRAM TO TEST MODSCR3 SUBROUTINE'
MODTEST  ENTERR LEVEL=V1M0
         LR    R11,R1                   R11 -> PARMAREA
         LA    R4,4095(,R13)
         USING WORKAREA+4095,R4
         EJECT
**********************************************************************
*       MAINLINE PROCESSING CONTROL ROUTINE:                         *
**********************************************************************
*
*              FIRST STEP IS GET MENU AND MOD NAMES:
*
B4       BAL   R14,PP                   SCAN PARMS
*
*              NEXT GET MENUS:
*
         BAL   R14,GETMENU              GET THE MENU
*
*              THEN GET ANY MODS:
*
         BAL   R14,GETMODS              GET THE MODBLKS
*
*              NOW BUILD A MODSCR3 PARM LIST:
*
         BAL   R14,BLDPARM              BLD PARMLIST
*
*              FINALLY DO THE MODSCR3 CALL:
*
         BAL   R14,MODCALL              CALL MODSCR3
         EJECT
TERM     LEAVER
         EJECT
*
*        PP -  SCAN THE PARMS FOR MENU  NAME AND MODBLKS NAME:
*
PP       ST    R14,L1LS
*
         L     R2,0(,R11)               R2->PARMS
         LH    R10,0(,R2)               R10==PARM LEN
         LA    R9,2(,R2)                R9->PARMS
         MVC   MENUNAME(8),=CL8'X'      INIT
         MVC   MODNAME(8),=CL8'X'       INIT
LL1      CLI   0(R9),C' '               BLANK?
         BNE   GOP1                     NO->GOT OP 1
         LA    R9,1(,R9)                R9->NEXT BYTE
         BCT   R10,LL1                  MORE?
         B     PPX                      NO
GOP1     LA    R8,MENUNAME              R8->SAVE AREA
LL2      CLI   0(R9),C' '               BLANK?
         BE    LFM                      YES->LOOK FOR MOD NAME
         MVC   0(1,R8),0(R9)            NO->SAVE IT
         LA    R8,1(,R8)                R8+1 PT PAST
         LA    R9,1(,R9)                R9+1 PT PAST
         BCT   R10,LL2                  MORE?
         B     PPX                      NO
LFM      CLI   0(R9),C' '               BLNAK?
         BNE   GOP2                     NO->GOT PARM 2
         LA    R9,1(,R9)                R9+1
         BCT   R10,LFM                  MORE?
         B     PPX                      NO
GOP2     LA    R8,MODNAME               R8->SAVE AREA
LL3      CLI   0(R9),C' '               BLANK?
         BE    PPX                      YES->MUST BE DONE
         MVC   0(1,R8),0(R9)            NO->SAVE IT
         LA    R8,1(,R8)                R8+1 PT PAST
         LA    R9,1(,R9)                R9+1 PT PAST
         BCT   R10,LL2                  MORE?
*
*
PPX      L     R14,L1LS
         BR    R14
         EJECT
*
*        GETMENU - USE SUBRTN GETSCRN  TO LOAD THE 'PANEL' :
*
GETMENU  ST    R14,L1LS
*
         CALL  GETSCRN,(=CL8'MENULIB',MENUNAME,MENLEN,MENUAREA),VL,  XXX
               MF=(E,CALLPL)
*
GMX      SLR   R15,R15                  RC=0
         L     R14,L1LS
         BR    R14
         EJECT
*
*        GETMODS - USE SUBRTN GETSCRN  TO LOAD THE MODBLKS :
*
GETMODS  ST    R14,L1LS
*
         CALL  GETSCRN,(=CL8'MODLIB',MODNAME,MODLEN,MODAREA),VL,     XXX
               MF=(E,CALLPL)
*
GMDX     SLR   R15,R15                  RC=0
         L     R14,L1LS
         BR    R14
         EJECT
*
*        BLDPARM  - CONSTRUCT A PARMLIST FOR MODSCR3 :
*
BLDPARM  ST    R14,L1LS
*                                       FULLSCR3 STUFF FIRST:
         LA    R1,IOPARM                IOPARM
         ST    R1,MODPARM
         LA    R1,MENLEN                OUTLEN
         ST    R1,MODPARM+4
         LA    R1,MENUAREA              OUTBUFF
         ST    R1,MODPARM+8
         MVC   INLEN(2),=H'3000'        INLEN
         LA    R1,INLEN
         ST    R1,MODPARM+12
         LA    R1,INBUFF                INBUFF
         ST    R1,MODPARM+16
         LA    R1,RETLEN                RETLEN
         ST    R1,MODPARM+20
         LA    R1,SCRUFF                SCRUFF
         ST    R1,MODPARM+24
         SPACE 2
*                                       NOW THE MODBLKS:
         LA    R10,MODPARM+24
         LA    R9,MODAREA
MPL      CLC   0(4,R9),=F'0'            ANY?
         BE    FLGEND                   NO
         MVC   4(4,R10),0(R9)           YES->PASS IT
         LA    R10,4(,R10)              R10->PARM JUST MOVED
         TM    0(R9),X'80'              LAST ONE?
         BO    FLGEND                   YES
         LA    R9,4(,R9)                R9->NEXT BLK PTR
         B     MPL                      GO CHK IT
FLGEND   OI    0(R10),X'80'             MAKE SURE LAST FLG ON
*
BLDX     L     R14,L1LS
         BR    R14
         EJECT
*
*        MODCALL - INVOKE THE MODSCR3 SUBRTN TO DO THE SCREEN IO:
*
MODCALL  ST    R14,L1LS
*
         LA    R1,MODPARM
         CALL  MODSCR3
*
MCX      L     R14,L1LS
         BR    R14
         EJECT
STATWORK EQU   *
*
IOPARM   DC    CL8'SBXXXXXB'
         LTORG
         EJECT
WORKAREA DSECT
SAVEAREA DS    18F
L1LS     DS    F
L2LS     DS    F
L3LS     DS    F
DDN      DS    D
*
CALLPL   CALL  ,(DDN,MENUNAME,MENLEN,MENUAREA),VL,MF=L
*
MODPARM  DS    20F
*
MENUNAME DS    D
MENLEN   DS    H
MENUAREA DS    XL3000
MODNAME  DS    D
MODLEN   DS    H
MODAREA  DS    XL1000
INLEN    DS    H                                                  WDPSC
INBUFF   DS    XL3000
RETLEN   DS    H                                                  WDPSC
SCRUFF   DS    XL2000
*
WORKLEN  EQU   *-WORKAREA
         END
