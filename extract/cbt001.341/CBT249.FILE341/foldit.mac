******************** FOLDIT *******************************************
*                                                                     *
*  FOLDIT IS A PROGRAM FOR PRINTING DATA SETS WITH UPPER AND LOWER    *
*  CASE CHARACTERS ON PRINTERS LACKING THE LUXURY OF LOWER CASE.      *
*  DDNAMES: SYSUT1 - INPUT DATASET
*           SYSUT2 - PRINT FILE
*  THE INPUT DS IS ASSUMED TO HAVE A CONTROL CHARACTER IN THE         *
*  FIRST BYTE OF EACH RECORD.  THE DS MAY BE FIXED OR VARIABLE LENGTH *
*  BLOCKED OR UNBLOCKED.  THE MAIN DIFFERENCE BETWEEN THIS AND        *
*  PRINTOFF (IPO SUPPLIED COMMAND PROCESSOR) IS THAT THIS PROGRAM     *
*  MAINTAINS THE PRINTER CONTROL CHARACTERS (LEAVES THEM ALONE).      *
*                                                                     *
***********************************************************************
         TITLE 'FOLDIT - PROGRAM TO TRANSLATE LOWER TO UPPER CASE '
FOLDIT   CSECT
         STM   R14,R12,12(R13)
         LR    R11,R15
         USING FOLDIT,R11
         ST    R13,SAVEAREA+4
         LR    R6,R13
         LA    R13,SAVEAREA
         ST    R13,8(R6)
* END OF PROLOUGE
         MVI   OUTAREA,C' '        SET UP AND
         MVC   OUTAREA+1(132),OUTAREA    CLEAR OUTPUT RECORD AREA
         OPEN  (INDCB,(INPUT),OUTDCB,(OUTPUT))
         LA    R7,INDCB
         USING IHADCB,R7
READIT   GET   INDCB               GET A RECORD ADDRESS IN R1
         LR    R6,R1               MOVE IT TO R6
         SLR   R5,R5               ZERO R5
         LH    R5,DCBLRECL         GET ITS LENGTH
         BCTR  R5,R0               DECREMENT BY ONE FOR INST
         BCTR  R5,R0               DECREMENT BY ONE FOR CTL CHAR
         LA    R6,1(R6)            INCREMENT PAST CTL CHAR
         TM    DCBRECFM,X'40'      IS IT RECFM = V
         BNO   NOTV                NO GO TO NOTV
         SH    R5,=H'4'            ELSE DECREMENT LENGTH BY RDW LENGTH
         LA    R6,4(R6)            AND INCREMENT ADDRESS PAST RDW
         LTR   R5,R5                    CHECK FOR NEGATIVE NUMBER
         BM    NOTRT                    TRT WOULD HAVE BAD LENGTH
NOTV     EX    R5,TRTINST          TRANSLATE IT
NOTRT    S     R6,=F'1'            SET ADDRESS BACK TO CTL CHAR
         LA    R5,1(R5)            SET LENGTH UP TO INCLUDE CTL CHAR
         MVI   OUTAREA,C' '        SET UP AND
         MVC   OUTAREA+1(132),OUTAREA    CLEAR OUTPUT RECORD AREA
         LA    R4,OUTAREA
         EX    R5,MVCINST          MOVE RECORD IN
         PUT   OUTDCB,OUTAREA
         B     READIT
CLOSIT   CLOSE (INDCB,,OUTDCB)
* BEGINNING OF EPILOUGE
         L     R13,SAVEAREA+4
         LM    R14,R12,12(R13)
         RETURN RC=0
* END OF EPILOUGE
TRTINST  TR    0(0,R6),TTAB
MVCINST  MVC   0(0,R4),0(R6)
INDCB    DCB   DDNAME=SYSUT1,DSORG=PS,MACRF=(GL),EODAD=CLOSIT
OUTDCB   DCB   DDNAME=SYSUT2,DSORG=PS,MACRF=(PM),LRECL=133,BLKSIZE=133
OUTAREA  DS    CL133
SAVEAREA DC    18F'0'
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
TTAB     DS    0CL256
         DC    XL16'000102030405060708090A0B0C0D0E0F'
         DC    XL16'101112131415161718191A1B1C1D1E1F'
         DC    XL16'202122232425262728292A2B2C2D2E2F'
         DC    XL16'303132333435363738393A3B3C3D3E3F'
         DC    XL16'404142434445464748494A4B4C4D4E4F'
         DC    XL16'505152535455565758595A5B5C5D5E5F'
         DC    XL16'606162636465666768696A6B6C6D6E6F'
         DC    XL16'707172737475767778797A7B7C7D7E7F'
         DC    XL16'80C1C2C3C4C5C6C7C8C98A8B8C8D8E8F'
         DC    XL16'90D1D2D3D4D5D6D7D8D99A9B9C9D9E9F'
         DC    XL16'A0A1E2E3E4E5E6E7E8E9AAABACADAEAF'
         DC    XL16'B0B1B2B3B4B5B6B7B8B9BABBBCBDBEBF'
         DC    XL16'C0C1C2C3C4C5C6C7C8C9CACBCCCDCECF'
         DC    XL16'D0D1D2D3D4D5D6D7D8D9DADBDCDDDEDF'
         DC    XL16'E0E1E2E3E4E5E6E7E8E9EAEBECEDEEEF'
         DC    XL16'F0F1F2F3F4F5F6F7F8F9FAFBFCFDFEFF'
         DCBD  DSORG=QS
         END   FOLDIT
