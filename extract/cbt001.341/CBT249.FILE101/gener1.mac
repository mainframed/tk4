PROC 0  DEBUG
CONTROL NOLIST MSG PROMPT NOFLUSH
IF &DEBUG NE &STR() THEN +
  CONTROL LIST CONLIST SYMLIST
SET &INDSN EQ &STR()
SET &INUNIT EQ &STR()
SET &INVOL EQ &STR()
SET &INRECFM EQ &STR()
SET &INLR EQ &STR()
SET &INBLK EQ &STR()
SET &INDENS EQ &STR()
SET &INLABEL EQ &STR()
SET &INPOS EQ &STR()
SET &OUTDSN EQ &STR()
SET &OUTUNIT EQ &STR()
SET &OUTVOL EQ &STR()
SET &OUTRECFM EQ &STR()
SET &OUTLR EQ &STR()
SET &OUTBLK EQ &STR()
SET &OUTDENS EQ &STR()
SET &OUTLABEL EQ &STR()
SET &OUTPOS EQ &STR()
SET &SPCU EQ &STR()
SET &PRIQ EQ &STR()
SET &SECQ EQ &STR()
SET &DIRB EQ &STR()
SET &DISP EQ &STR()
SET &PROGNAME EQ &STR(IEBGENER)
ISPEXEC CONTROL ERRORS RETURN
TRYAGIN: +
CONTROL NOMSG
FREE F(SYSIN,SYSPRINT,SYSUT1,SYSUT2)
FREE A(DCBIN,DCBOUT)
CONTROL MSG
SET &INATTR EQ &STR()
SET &OUTATTR EQ &STR()
SET &INTAPE EQ &STR()
SET &OUTTAPE EQ &STR()
SET &INUV EQ &STR()
SET &OUTUV EQ &STR()
SET &INUSE EQ &STR()
SET &OUTUSE EQ &STR()
SET &OUTNEW EQ &STR()
SET &ODISP EQ &STR()
SET &INRFM EQ &STR()
SET &OUTRFM EQ &STR()
ISPEXEC DISPLAY PANEL(GENER1) CURSOR(INDSN)
IF &LASTCC EQ 8 THEN +
  GOTO ALLDONE
ALLOC F(SYSIN) DUMMY
ALLOC F(SYSPRINT) DUMMY
IF &INRECFM NE &STR() THEN +
  DO
    IF &EVAL(&LENGTH(&INRECFM)) EQ 3 THEN +
      DO
        SET &INRFM EQ &STR(&SUBSTR(1:1,&INRECFM)&STR( )&SUBSTR(2:2,&INR+
ECFM)&STR( )&SUBSTR(3:3,&INRECFM))
      END
    ELSE +
      IF &EVAL(&LENGTH(&INRECFM)) EQ 2 THEN +
        DO
          SET &INRFM EQ &STR(&SUBSTR(1:1,&INRECFM)&STR( )&SUBSTR(2:2,&I+
NRECFM))
        END
  END
IF &INRFM NE &STR() THEN +
  SET &INATTR EQ &STR(&STR(&INATTR) RECFM(&INRFM) )
IF &INLR NE &STR() THEN +
  SET &INATTR EQ &STR(&STR(&INATTR) LRECL(&INLR) )
IF &INBLK NE &STR() THEN +
  SET &INATTR EQ &STR(&STR(&INATTR) BLKSIZE(&INBLK) )
IF &INDENS NE &STR() THEN +
  SET &INATTR EQ &STR(&STR(&INATTR) DEN(&INDENS) )
IF &STR(&INLABEL) NE &STR() THEN +
  SET &INTAPE EQ &STR(&INTAPE LABEL(&INLABEL) )
IF &INPOS NE &STR() THEN +
  SET &INTAPE EQ &STR(&INTAPE POSITION(&INPOS) )
IF &INUNIT NE &STR() THEN +
  SET &INUV EQ &STR(&INUV UNIT(&INUNIT) )
IF &INVOL NE &STR() THEN +
  SET &INUV EQ &STR(&INUV VOLUME(&INVOL) )
IF &INATTR NE &STR() THEN +
  DO
    ATTR DCBIN &INATTR
    SET &INUSE EQ &STR( USING(DCBIN) )
  END
IF &SUBSTR(1:1,&STR(&INDSN)) EQ &SUBSTR(3:3,&STR(12(456))  THEN +
  DO
    ALLOC F(SYSUT1) DA&INDSN SHR &INUV &INTAPE &INUSE
    GOTO ALCSYS2
  END
ALLOC F(SYSUT1) DA('&INDSN') SHR &INUV &INTAPE &INUSE
ALCSYS2: +
IF &OUTRECFM NE &STR() THEN +
  DO
    IF &EVAL(&LENGTH(&OUTRECFM)) EQ 3 THEN +
      DO
        SET &OUTRFM EQ &STR(&SUBSTR(1:1,&OUTRECFM)&STR( )&SUBSTR(2:2,&O+
UTRECFM)&STR( )&SUBSTR(3:3,&OUTRECFM))
      END
    ELSE +
      IF &EVAL(&LENGTH(&OUTRECFM)) EQ 2 THEN +
        DO
          SET &OUTRFM EQ &STR(&SUBSTR(1:1,&OUTRECFM)&STR( )&SUBSTR(2:2,+
&OUTRECFM))
        END
  END
IF &OUTRFM NE &STR() THEN +
  SET &OUTATTR EQ &STR(&STR(&OUTATTR) RECFM(&OUTRFM) )
IF &OUTLR NE &STR() THEN +
  SET &OUTATTR EQ &STR(&STR(&OUTATTR) LRECL(&OUTLR) )
IF &OUTBLK NE &STR() THEN +
  SET &OUTATTR EQ &STR(&STR(&OUTATTR) BLKSIZE(&OUTBLK) )
IF &OUTDENS NE &STR() THEN +
  SET &OUTATTR EQ &STR(&STR(&OUTATTR) DEN(&OUTDENS) )
IF &STR(&OUTLABEL) NE &STR() THEN +
  SET &OUTTAPE EQ &STR(&OUTTAPE LABEL(&OUTLABEL) )
IF &OUTPOS NE &STR() THEN +
  SET &OUTTAPE EQ &STR(&OUTTAPE POSITION(&OUTPOS) )
IF &OUTUNIT NE &STR() THEN +
  SET &OUTUV EQ &STR(&OUTUV UNIT(&OUTUNIT) )
IF &OUTVOL NE &STR() THEN +
  SET &OUTUV EQ &STR(&OUTUV VOLUME(&OUTVOL) )
IF &OUTATTR NE &STR() THEN +
  DO
    ATTR DCBOUT &OUTATTR
    SET &OUTUSE EQ &STR( USING(DCBOUT) )
  END
IF &SPCU NE &STR() THEN +
  SET &OUTNEW EQ &STR( &OUTNEW &SPCU )
IF &PRIQ NE &STR() THEN +
  DO
    IF &SECQ NE &STR() THEN +
      SET &OUTNEW EQ &STR( &OUTNEW SPACE(&PRIQ &SECQ) )
    ELSE +
      SET &OUTNEW EQ &STR( &OUTNEW SPACE(&PRIQ) )
  END
IF &DIRB NE &STR() THEN +
  SET &OUTNEW EQ &STR( &OUTNEW DIR(&DIRB) )
IF &OUTNEW NE &STR() THEN +
  SET &ODISP EQ &STR( &ODISP NEW )
ELSE +
  SET &ODISP EQ &STR( &ODISP SHR )
IF &DISP NE &STR() THEN +
  SET &ODISP EQ &STR( &ODISP &DISP )
IF &EVAL(&LENGTH(&STR(&OUTDSN))) GE 8 THEN +
  DO
    IF &SUBSTR(1:7,&OUTDSN) EQ &STR(SYSOUT=) THEN +
      DO
        IF &DEST NE &STR() THEN +
         DO
            ALLOC F(SYSUT2) SYSOUT(&SUBSTR(8:8,&OUTDSN)) NOHOLD KEEP +
              &OUTUSE DEST(&DEST)
            GOTO EXECPROG
          END
        ELSE +
          DO
            ALLOC F(SYSUT2) SYSOUT(&SUBSTR(8:8,&OUTDSN)) NOHOLD KEEP +
              &OUTUSE
            GOTO EXECPROG
          END
      END
  END
ALLOC F(SYSUT2) DA('&OUTDSN') &OUTUV &OUTTAPE &OUTUSE &OUTNEW +
 &ODISP
EXECPROG:$ &PROGNAME
WRITE &PROGNAME COMPLETED RETURN CODE = &LASTCC
CONTROL NOMSG
FREE F(SYSUT1,SYSUT2,SYSIN,SYSPRINT)
FREE A(DCBIN,DCBOUT)
CONTROL MSG
GOTO TRYAGIN
ALLDONE: +
ALLOC F(SYSIN) DA(*)
ALLOC F(SYSPRINT) DA(*)
ALLOC F(SYSUT1) NEW DELETE UNIT(VIO) SPACE(4) CYL
ALLOC F(SYSUT2) NEW DELETE UNIT(VIO) SPACE(4) CYL
END
