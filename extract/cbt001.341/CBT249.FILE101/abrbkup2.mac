PROC 0 DEBUG
CONTROL NOLIST NOMSG NOFLUSH PROMPT
IF &DEBUG NE &STR() THEN +
  CONTROL LIST MSG NOFLUSH PROMPT CONLIST SYMLIST
ISPEXEC CONTROL ERRORS RETURN
SET &OPT EQ &STR( )
SET &ADSN EQ &STR()
SET &ACYCLE EQ &STR()
SET &ACYL EQ &STR()
SET &AGEN EQ &STR()
SET &ANDSN EQ &STR()
SET &ANDSG EQ &STR()
SET &ANVOL EQ &STR()
SET &ATRK EQ &STR()
SET &AVOL EQ &STR()
SET &DSPRFX EQ &STR()
RESTART: +
SET &ACMD EQ &STR( RESTORE BACKUP,)
SET &ACMD2 EQ &STR(  )
SET &CONT EQ &STR(NO)
SET &COMMA EQ &STR()
ISPEXEC DISPLAY PANEL(ABRBKUP2)
IF &LASTCC EQ 8 THEN +
  EXIT
IF &SYSPREF EQ &STR() THEN +
  DO
    SET &DSPRFX EQ &SYSUID
  END
ELSE +
  IF &SYSPREF EQ &SYSUID THEN +
    DO
      SET &DSPRFX EQ &SYSUID
    END
  ELSE +
    DO
      SET &DSPRFX EQ &SYSPREF
    END
SET &DSYS EQ &STR(D&SUBSTR(1:2,&SYSDATE)&SUBSTR(4:5,&SYSDATE)&SUBSTR(7:+
8,&SYSDATE))
SET &TSYS EQ &STR(T&SUBSTR(1:2,&SYSTIME)&SUBSTR(4:5,&SYSTIME)&SUBSTR(7:+
8,&SYSTIME))
IF &DEBUG EQ &STR() THEN +
  CONTROL NOMSG
FREE F(SYSPRINT,SYSIN,ABRARCH,ABRREST)
FREE A(FB80,FBA121)
IF &DEBUG EQ &STR() THEN +
  CONTROL MSG
ATTR FB80 LRECL(80) BLKSIZE(4000) RECFM(F B)
ATTR FBA121 LRECL(121) BLKSIZE(18997) RECFM(F B A)
ALLOC F(SYSIN) UNIT(VIO) SPACE(1 1) TRACKS NEW DELETE USING(FB80)
ALLOC F(ABRARCH) DA('FDRABR.ABRARCH.USERDATA') SHR
ALLOC F(ABRREST) DA('FDRABR.ABRREST.USERDATA') SHR
ALLOC F(SYSPRINT) DA('&DSPRFX..&DSYS..&TSYS..SYSPRINT') NEW CATALOG +
 SPACE(5 2) TRACKS USING(FBA121)
SET &ACMD EQ &STR(&ACMD&STR(DSN=&ADSN))
IF &ACYCLE NE &STR() THEN +
  DO
    SET &CONT EQ &STR(YES)
    SET &ACMD2 EQ &STR(&ACMD2&STR(&COMMA)&STR(CYCLE=&ACYCLE))
    SET &COMMA EQ &STR(,)
  END
IF &ACYL NE &STR() THEN +
  DO
    SET &CONT EQ &STR(YES)
    SET &ACMD2 EQ &STR(&ACMD2&STR(&COMMA)&STR(CYL=&ACYL))
    SET &COMMA EQ &STR(,)
  END
IF &AGEN NE &STR() THEN +
  DO
    SET &CONT EQ &STR(YES)
    SET &ACMD2 EQ &STR(&ACMD2&STR(&COMMA)&STR(GEN=&AGEN))
    SET &COMMA EQ &STR(,)
  END
IF &ANDSN NE &STR() THEN +
  DO
    SET &CONT EQ &STR(YES)
    SET &ACMD2 EQ &STR(&ACMD2&STR(&COMMA)&STR(NEWN=&ANDSN))
    SET &COMMA EQ &STR(,)
  END
IF &ANDSG NE &STR() THEN +
  DO
    SET &CONT EQ &STR(YES)
    SET &ACMD2 EQ &STR(&ACMD2&STR(&COMMA)&STR(NEWG=&ANDSG))
    SET &COMMA EQ &STR(,)
  END
IF &ANVOL EQ &STR() THEN +
  DO
    %ABRVOLDF &DEBUG
    IF &LASTCC EQ &STR(0) THEN +
      DO
        SET &ANVOL EQ &STR(DLX170)
      END
    ELSE +
      DO
        SET &TIME EQ &SUBSTR(8:8,&STR(&SYSTIME))
        IF &STR(&TIME) GE &STR(6) THEN +
          DO
            SET &ANVOL EQ &STR(DLXTS3)
          END
        ELSE +
          IF &STR(&TIME) GE &STR(3) THEN +
            DO
              SET &ANVOL EQ &STR(DLXTS2)
            END
          ELSE +
            DO
              SET &ANVOL EQ &STR(DLXTSO)
            END
      END
  END
IF &ANVOL NE &STR() THEN +
  DO
    SET &CONT EQ &STR(YES)
    SET &ACMD2 EQ &STR(&ACMD2&STR(&COMMA)&STR(NVOL=&ANVOL))
    SET &COMMA EQ &STR(,)
  END
IF &ATRK NE &STR() THEN +
  DO
    SET &CONT EQ &STR(YES)
    SET &ACMD2 EQ &STR(&ACMD2&STR(&COMMA)&STR(TRK=&ATRK))
    SET &COMMA EQ &STR(,)
  END
IF &AVOL NE &STR() THEN +
  DO
    SET &CONT EQ &STR(YES)
    SET &ACMD2 EQ &STR(&ACMD2&STR(&COMMA)&STR(VOL=&AVOL))
    SET &COMMA EQ &STR(,)
  END
IF &CONT EQ &STR(YES) THEN +
  SET &ACMD EQ &STR(&ACMD&STR(&COMMA))
OPENFILE SYSIN OUTPUT
SET &SYSIN EQ &STR(&ACMD)
PUTFILE SYSIN
IF &CONT EQ &STR(YES) THEN +
  DO
    SET &SYSIN EQ &STR(&ACMD2)
    PUTFILE SYSIN
  END
CLOSFILE SYSIN
$ FDRABRUT
FREE F(SYSPRINT)
ALLOC F(SYSPRINT) DA('&DSPRFX..&DSYS..&TSYS..SYSPRINT') MOD
OPENFILE SYSPRINT OUTPUT
SET &SYSPRINT EQ &STR(0 THE ABOVE DATASET WILL BE RESTORED FROM BACKUP +
 AT THE NEXT RESTORE RUN)
PUTFILE SYSPRINT
SET &SYSPRINT EQ &STR(0 BACKUP RESTORE RUNS ARE DONE AT 12:00 NOON AND +
6:00 PM)
PUTFILE SYSPRINT
CLOSFILE SYSPRINT
ISPEXEC BROWSE DATASET('&DSPRFX..&DSYS..&TSYS..SYSPRINT')
IF &DEBUG EQ &STR() THEN +
  CONTROL NOMSG
DELETE '&DSPRFX..&DSYS..&TSYS..SYSPRINT'
FREE F(SYSIN,SYSPRINT,ABRARCH,ABRREST)
FREE A(FB80,FBA121)
IF &DEBUG EQ &STR() THEN +
  CONTROL MSG
GOTO RESTART
END
