 TITLE 'D A T E   ---   D A T E  /  T I M E   C O M M A N D'
DATE     CSECT
         JVDENTER DSA=(DSAEND-DSAWRK),BASE=(R11),VERSION=&SYSPARM
         USING DSAWRK,R13
         USING CPPL,R1
         USING UPT,R2
         USING PSCB,R4
         USING ECT,R5
         XC    ECB,ECB
         LM    R2,R5,CPPL
         SPACE 2
         TIME  DEC
         SPACE
         ST    R1,WORKDATE
         ST    R0,WORKTIME
         MVI   WORKDATE,X'0D'
         MVI   WORKPFX,0
         NI    WORKDATE+3,X'F0'
         OI    WORKDATE+3,X'0D'
         LA    R1,WORKDATE+1
         LA    R15,WORKGREG
         BAL   R14,DATERTN
         MVC   MSG,MASK
         ED    MSG,WORKAREA
         LA    R0,L'MSG-3+4
         STCM  R0,B'0011',OUT+0
         STCM  R0,B'1100',OUT+2
         SPACE
         PUTLINE                                                       /
               PARM=PUTLINE,MF=(E,IOPL),ECB=ECB,ECT=ECT,UPT=UPT,       /
               TERMPUT=(EDIT,WAIT,NOHOLD,NOBREAK),                     /
               OUTPUT=(OUT,TERM,SINGLE,DATA)
         EJECT
         JVDLEAVE RC=0
         EJECT
DATERTN  DS    0H
         SPACE
         ST    R14,R14SAVE
         MVC   DAYTABLE,DAYMONTH
         XC    DWD,DWD
         LR    R14,R1
         MVO   DWD+6(2),0(1,R1)
         OI    DWD+7,X'0F'
         CVB   R1,DWD
         TM    X03,*-*
         EX    R1,*-4
         BNZ   *+L'*+4
         MVI   DAYTABLE+1,29
         MVC   2(1,R15),0(R14)
         SPACE
         MVC   DWD+6(2),1(R14)
         OI    DWD+7,X'0F'
         XR    R0,R0
         XR    R14,R14
         CVB   R1,DWD
         SPACE 2
DAYLOOP  DS    0H
         IC    R0,DAYTABLE(R14)
         LA    R14,1(R14)
         SR    R1,R0
         BP    DAYLOOP
         SPACE 2
DAYNOW   DS    0H
         CVD   R14,DWD
         MVO   DWD+3(3),DWD+6(2)
         MVC   0(1,R15),DWD+4
         AR    R1,R0
         CVD   R1,DWD
         MVO   DWD+3(3),DWD+6(2)
         MVC   1(1,R15),DWD+4
         SPACE
         L     R14,R14SAVE
         BR    R14
         EJECT
MASK     DC    C' ',X'2021'
         DC    C'DATE = ',X'2020',C'/',X'2020',C'/',X'2020',X'2221'
         DC    C'(',X'2020',C'.',X'202020',C')    TIME = '
         DC    X'2120',C'.',X'2020',C'.',X'2020'
MASKEND  EQU   *
DAYMONTH DC    AL1(31,28,31,30,31,30,31,31,30,31,30,31)
X03      DC    X'03'
         LTORG
         SPACE 2
DSAWRK   DSECT
SSA      DS    18F
DWD      DS    D
WORKAREA DS    0D
WORKPFX  DS    XL1
WORKGREG DS    XL3
WORKDATE DS    PL4
WORKTIME DS    XL4
R14SAVE  DS    F
ECB      DS    F
IOPL     DS    10F
OUT      DS    X
MSG      DS    CL(MASKEND-MASK)
DAYTABLE DS    XL12
PUTLINE  PUTLINE MF=L
DSAEND   DS    0D
         SPACE 2
         JVDEQU
         EJECT
         IKJCPPL
         IKJECT
         IKJUPT
         IKJPSCB
         SPACE 2
         END   DATE
