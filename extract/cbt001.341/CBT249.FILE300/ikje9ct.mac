         TITLE ' C O U N T   S U B C O M M A N D   O F   E D I T '
************************************************************
*                                                          *
*        'COUNT'                                           *
*                                                          *
************************************************************
         SPACE
*        WRITTEN BY. BILL GODFREY,  PLANNING RESEARCH CORPORATION.
*        INSTALLATION. PRC COMPUTER CENTER INC, MCLEAN VA.
*        DATE WRITTEN. NOVEMBER 8 1976.
*        DATE UPDATED. DECEMBER 6 1978.
*        ATTRIBUTES. RE-ENTRANT.
*        COMMENTS.
*            THIS EDIT SUBCOMMAND DISPLAYS THE NUMBER OF LINES
*            IN THE EDIT DATA SET.
         SPACE
         MACRO
&NAME    LINK  &EP=
.*             SIMPLIFIED LINK FOR PUTLINE MACRO
&NAME    LA    15,LINKAREA
         LA    0,=CL8'&EP'
         ST    0,0(,15)
         SVC   6
         MEND
         SPACE
COUNT    START
         USING *,R12
         B     @BEGIN-*(,15)
         DC    AL1(11),CL11'COUNT '
         DC    CL16' &SYSDATE &SYSTIME '
@SIZE    DC    0F'0',AL1(1),AL3(@DATAL)
@BEGIN   STM   14,12,12(R13)       SAVE REGISTERS
         LR    R12,R15             LOAD BASE REGISTER
         LR    R9,R1               EDIT COMM AREA POINTER
         USING IKJEBECA,R9
         L     R0,@SIZE            WORKAREA SUBPOOL AND LENGTH
         GETMAIN R,LV=(0)
         LR    R8,R1               INITIALIZE WORKAREA POINTER
         USING @DATA,R8
         SPACE 1
         ST    R13,4(,R8)          CHAIN FORWARD
         ST    R8,8(,R13)          CHAIN BACK
         LR    R13,R8              UPDATE SAVEAREA POINTER
         SPACE 1
         XC    LINKAREA(8),LINKAREA
         LA    R2,MESSAGE
         MVI   0(R2),C' '
         MVC   1(L'MESSAGE-1,R2),0(R2)
         SPACE
         L     R1,CAUTILNO
         CVD   R1,DOUBLE
         MVC   0(8,R2),=X'4020202020202120'
         ED    0(8,R2),DOUBLE+4
COUNT1   CLI   0(R2),C' '          LEADING BLANK?
         BNE   COUNT3              NO, BRANCH
         MVC   0(7,R2),1(R2)       YES, SHIFT LEFT ONE BYTE
         MVI   7(R2),C' '          AND TACK ON A TRAILING BLANK
         B     COUNT1              LOOP UNTIL NON BLANK
COUNT2   CLI   0(R2),C' '          TRAILING BLANK FOUND?
         BE    COUNT4              YES, BRANCH
COUNT3   LA    R2,1(,R2)           POINT TO NEXT BYTE
         B     COUNT2              LOOP UNTIL TRAILING BLANK
COUNT4   MVC   1(5,R2),=C'LINES'
         CH    R1,=H'1'
         BNE   *+8
         MVI   5(R2),C' '
         SPACE
         LA    R1,MESSAGE
         LA    R0,L'MESSAGE
         BAL   R7,PUTLINE
         B     EXIT0
         SPACE
PUTLINE  XC    MYSEG1(4),MYSEG1
         MVC   MYPTPB(12),MODLPTPB
         LR    R14,R0              LENGTH IN R0
         LA    R14,4(,R14)         ADD 4
         STH   R14,MYSEG1
         LR    R14,R0              GET LENGTH FOR MOVE
         BCTR  R14,0
         B     *+10
         MVC   MYSEG1+4(0),0(R1)   MOVE TEXT IN
         EX    R14,*-6
         LA    R1,CATMPLST         IOPL
         SPACE
         PUTLINE PARM=MYPTPB,OUTPUT=(MYSEG1,DATA),MF=(E,(1))
         SPACE
         BR    R7
         SPACE 1
EXIT8    LA    R15,8               RETURN CODE 8
         B     EXIT
EXIT0    SR    R15,R15             RETURN CODE ZERO
EXIT     LR    R1,R13              POINT R1 TO AREA TO BE FREED
         L     R0,@SIZE            SUBPOOL AND LENGTH
         L     R13,4(,R13)         RESTORE PREVIOUS SAVEAREA
         ST    R15,16(,R13)        STORE RETURN CODE FOR LM
         FREEMAIN R,A=(1),LV=(0)
         LM    14,12,12(R13)       LOAD RETURN ADDRESS AND RC
         BR    14                  RETURN
         SPACE
************************************************************
*                                                          *
*        CONSTANTS                                         *
*                                                          *
************************************************************
         SPACE
MODLPTPB PUTLINE OUTPUT=(1,TERM,SINGLE,DATA),                          X
               TERMPUT=(EDIT,WAIT,NOHOLD,NOBREAK),MF=L
         SPACE
         LTORG
         SPACE
         DC    0D'0'
         EJECT
************************************************************
*                                                          *
*        DSECTS                                            *
*                                                          *
************************************************************
         SPACE
@DATA    DSECT
         DS    18F                 REGISTER SAVEAREA
DOUBLE   DS    D                   DOUBLEWORD WORK AREA
LINKAREA DS    2F
MESSAGE  DS    CL16
MYPTPB   DS    3F                  USED BY PUTLINE ROUTINE
MYSEG1   DS    2H,CL32             USED BY PUTLINE ROUTINE
         DS    0D
@DATAL   EQU   *-@DATA
         SPACE
         PRINT NOGEN
         IKJEBECA
         PRINT GEN
         SPACE
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END
