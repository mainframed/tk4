ASM2     TITLE '''GSFTSOA2'' -- ASSEMBLER G PROMPTER - SECOND LOAD'
** 'GSFTSOA2'  J. SCHINDLER SEPTEMBER 1974
**    (SECOND LOAD)
**
**
** ATTR - RENT
**
**
** THIS IS THE SECOND LOAD OF THE ASMG PROMPTER. THE FIRST LOAD
** ALLOCATED AND CONCATENATED ALL THE DATA SETS. THIS LOAD WILL ATTACH
** ASMG, DECONCATENATE, AND FREE THE DDNAME. THE NECESSARY DDNAME,
** ADDRS, ETC ARE IN SP=0 WHOSE ADDR IS IN REG 1 AT ENTRY.
**
         EJECT
GSFTSOA2 CSECT
         EQUREGS
**
SFLG     EQU   B'10000000' SYSOUT FLAG
NOTSFLG  EQU   B'01111111'
PFLG     EQU   B'01000000' SYSPRINT FLAG
NOTPFLG  EQU   B'10111111'
**
START    SAVE  (14,12),,GSFTSOA2-SECOND-LOAD-OF-ASMG-PROMPTER
         USING START,R15
         LM    R10,R11,BASEADDR
         DROP  R15
         USING START,R10,R11 WILL SET UP 2 BASE REGS, BUT THE SECOND
         B     BEGIN         WILL PROBABLY NOT BE NEEDED
BASEADDR DC    A(START,START+X'1000')
BEGIN    LR    R12,R1
         USING MAIN,R12 BASE FOR ADDRS IN SP=0
         LA    R0,RENTLEN GET MAIN FOR REENTRANCY
         GETMAIN R,LV=(0)
         ST    R1,8(R13)
         ST    R13,4(R1)
         LR    R13,R1
         USING SAVE,R13
         NI    FLAGS,SFLG
**
**   FLAGS SETTINGS FOR THIS ROUTINE -
**             1... .... - INDICATES SYSOUT OUTPUT (SET IN FIRST LOAD)
**             .1.. .... - INDICATES SYSPRINT IS DDNAME TO BE FREED
**             ..11 1111 - NOT USED
**
         LA    R2,DAIRPB FIX UP DAIRPL WITH THE CORRECT ADDR
         ST    R2,DAIRPL+16
         LA    R1,ASMPARML
         ST    R1,ASMGARG
         LA    R1,ASMDDNAM
         ST    R1,ASMGARG+4
         MVI   ASMGARG+4,X'80' SET HIGH BIT
         XC    ECB,ECB ZERO ECB
         MVC   ATTACH(ATREFL),ATREF
         LA    R2,ECB
         LA    R1,ASMGARG LOAD ADDR OF ARG LIST
         LA    R15,ATTACH LOAD ADDR OF LIST FORM
** ATTACH ASMGASM
         ATTACH EPLOC=EPNAME,ECB=(R2),MF=(E,(1)),SF=(E,(15))
         ST    R1,TCB STORE THE TCB FOR DETACH
         WAIT  ECB=ECB WAIT TILL ASMG IS FINISHED
         MVC   COMPCDE,ECB+3 SAVE RETURN CODE FROM ASMG
DETACH   DETACH TCB DETACH ASMG - IT IS FINISHED
RETURN   DC    0H'0'
         LH    R2,LIBDSCNT
         CH    R2,=H'1' TEST FOR MORE THAN ONE MACRO LIB
         BNH   RTRNX1AA BRANCH TO FREE ASM DATA SETS
         LA    R8,DAIRPB DECONCATENATE SYSLIB'S
         USING DAPB10,R8
         MVC   DA10CD,=X'0010'
         XC    DA10FLG(6),DA10FLG
         MVC   DA10DDN,DDNLIST+24
         XC    ECB,ECB
         LA    R1,DAIRPL
         LINK  EP=IKJEFD00 LINK TO DAIR
         DROP  R8
RTRNX1   XC    DDNLIST+24,DDNLIST+24
RTRNLP1  LR    R3,R2 FREE DDNAMES CONT. TO SYSLIB
         BCTR  R3,R0
         SLL   R3,3
         LA    R1,LIBDDN(R3)
         BAL   R14,FREEDDN
         BCT   R2,RTRNLP1
RTRNX1AA LH    R2,ASMLISTL TEST FOR CONT. DDNAME FOR ASM
         CH    R2,=H'1'
         BNH   RTRNX2 ONLY ONE DDNAME
         LA    R8,DAIRPB DECONCATENATE ASM DSNAMES
         USING DAPB10,R8
         MVC   DA10CD,=X'0010'
         XC    DA10FLG(6),DA10FLG
         MVC   DA10DDN,DDNLIST+32
         XC    ECB,ECB
         LA    R1,DAIRPL
         LINK  EP=IKJEFD00 LINK TO DAIR
         DROP  R8
RTRNX1A  XC    DDNLIST+32,DDNLIST+32
RTRNLP1A LR    R3,R2 FREE ALL DDN'S CONT. FOR ASM
         BCTR  R3,R0
         SLL   R3,3
         LA    R1,ASMLIST(R3)
         BAL   R14,FREEDDN
         BCT   R2,RTRNLP1A
RTRNX2   LA    R2,10  FREE DDNLIST
RTRNLP2  LR    R3,R2
         BCTR  R3,R0
         SLL   R3,3
         LA    R1,DDNLIST(R3)
         CLI   0(R1),0 TEST FOR NO DDNAME IN LIST
         BE    RTRNELP1
         CH    R2,=H'6'
         BNE   RTRNX3
         OI    FLAGS,PFLG SET BIT TO PUT CLASS 'A' IN DAIRPB
RTRNX3   BAL   R14,FREEDDN
RTRNELP1 BCT   R2,RTRNLP2
         L     R0,MAINLNTH LOAD LENGTH OF MAIN IN SP=0
         L     R1,MAINADDR LOAD ADDR OF MAIN IN SP=0, HIGH BYTE=0
         FREEMAIN R,LV=(0),A=(1)
RTRNEND  DC    0H'0'
         XR    R2,R2
         IC    R2,COMPCDE
         LR    R1,R13
         L     R13,SAVE+4
         LA    R0,RENTLEN
         FREEMAIN R,LV=(0),A=(1)
         LR    R15,R2
         RETURN (14,12),T,RC=(15) RETURN TO SYSTEM
**
** FREE DDNAME SECTION
**
** R1=ADDR OF DDNAME, R14=RETURN ADDR
** OUTSWT=00 -- NO SYSOUT, OUTSWT=FF SYSOUT=A DATA SET
**
FREEDDN  STM   R2,R9,FREESV29
         ST    R14,FREESV14
         LA    R8,DAIRPB
         USING DAPB18,R8
         MVC   DA18CD,=X'0018'
         XC    DA18FLG(10),DA18FLG
         MVC   DA18DDN,0(R1) MOVE DDNAME
         MVC   DA18MNM,BLANK MEMBER NAME
         TM    FLAGS,PFLG TEST FOR SYSPRINT
         BZ    FREEX1
         NI    FLAGS,NOTPFLG RESET FLAG BIT
         TM    FLAGS,SFLG TEST FOR SYSOUT OUTPUT
         BZ    FREEX1
         MVC   DA18SCLS,=C'A '
         B     FREEX2
FREEX1   MVC   DA18SCLS,BLANK SYSOUT CLASS
FREEX2   MVI   DA18DPS2,B'00001000'
         MVI   DA18CTL,X'10'
         MVC   DA18JBNM,BLANK
         XC    ECB,ECB
         LA    R1,DAIRPL
         LINK  EP=IKJEFD00 LINK TO DAIR
         LM    R2,R9,FREESV29
         L     R14,FREESV14
         BR    R14 RETURN
         DROP  R8
**
** END OF FREEDDN SECTION
**
**
** ADDRS, ETC
**
BLANK    DC    CL8' '
EPNAME   DC    CL8'ASMGASM'
ATREF    ATTACH SF=L
ATREFL   EQU   *-ATREF
         EJECT
**
** ADDRS, ETC IN SP=0
**
MAIN     DSECT
MAINADDR DS    F ADDR OF MAIN
MAINLNTH DS    F LENGTH OF MAIN
DAIRPL   DS    5F DAIR PARM LIST
ASMDDNAM DS    H NUMBER OF DDNAME IN LIST
DDNLIST  DS    10XL8 LIST OF DDNAMES FOR ASMG
ASMLISTL DS    H NO. OF DDNAME FOR ASM
ASMLIST  DS    10CL8 LIST FO CONT. DDNAMES FOR ASM
ASMPARML DS    H LENGTH OF PARM LIST FOR ASMG
ASMPARM  DS    CL256
LIBDSCNT DS    H NUMBER OF DDNAMES CONCATENATED FOR SYSLIB
LIBDDN   DS    7CL8 DDNAMES CONCATENATED FOR SYSLIB
FLAGS    DS    X
**
** DSECT FOR REENTRANCY
**
RENTDSCT DSECT  FOR REENTRANCY
SAVE     DS    18F
FREESV29 DS    8F
ATTACH   ATTACH SF=L
FREESV14 DS    F
ASMGARG  DS    2F
ECB      DS    F
TCB      DS    F
DAIRPB   DS    CL80
COMPCDE  DS    X
OUTSWT   DS    X
RENTLEN  EQU   *-RENTDSCT
         PRINT NOGEN
**
** MAPPING DSECTS
**
         IKJDAP10
         IKJDAP18
GSFTSOA2 CSECT
         LTORG
         END   GSFTSOA2
