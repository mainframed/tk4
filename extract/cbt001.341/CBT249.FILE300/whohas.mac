DSN TITLE 'NASA/GSFC (TSO) CMD CHECK FOR ENQUE OF DATA SET NAME'
DSN      CSECT
**
** THIS COMMAND PROCESSOR ISBASED ON THE DSN COMMAND PROCESSOR
** OBTAINED FROM COMSAT.
** THIS CSECT IS THE FIRST LOAD OF A TWO LOAD COMMAND PROCESSOR
*
* UPDATED 10APR77, GENE CZARCINSKI, NASA/GSFC, 360/95, GT02401
*
*      . RENAME CSECT(S) FROM GSFTSODS TO DSN AND FROM GSFTSOD2
*        TO DSN2$ ... MORE CONSISTANCY AND CLEANER CMDLIB ...
*
* UPDATED 24SEP80, BILL GODFREY, AFDSC/PENTAGON
*
*      . ADD 'USID' KEYWORD TO IKJPOSIT MACRO SO IKJPARS WILL
*        PREFIX UNQUOTED DSNAMES WITH USER'S PROFILE PREFIX.
*      . IF OPERAND OMITTED AND PROFILE NOPREFIX, USE USERID.
*      . CALL IKJPARS THRU CVT POINTER INSTEAD OF LINK.
*      . REPLACE 'EQUREGS' MACRO WITH INLINE REGISTER EQUATES.
**
         SAVE  (14,12),,*
         LR    R12,R15
         USING DSN,R12
         ST    R13,SAVE+4
         LA    R10,SAVE
         ST    R10,8(R13)
         LR    R13,R10
         LR    R11,R1              CPPL
         B     BYPASS  ZAP THIS INSTR. TO CHECK FOR 'OPER' PRIVILEGES
         EXTRACT ANSWER,FIELDS=(PSB)
         L     R2,ANSWER
         TM    16(R2),X'80'  DOES USER HAVE OPER PRIVILEGES?
         BO    BYPASS  YES
         TPUT  OPERMSG,L'OPERMSG
         B     RETURN
BYPASS   DC    0H'0'
         LA    R9,PARSEPPL
         USING PPL,R9
         LM    R2,R5,0(R11)        CPPL
         ST    R2,PPLCBUF
         ST    R3,PPLUPT
         ST    R5,PPLECT
         LA    R2,ECB
         ST    R2,PPLECB
         L     R2,=V(DSNPCL)
         ST    R2,PPLPCL
         LA    R2,ANSWER
         ST    R2,PPLANS
         XC    PPLUWA,PPLUWA
         DROP  R9
         LA    R1,PARSEPPL
         L     R15,16              CVTPTR
         TM    520(R15),X'80'      IF HI ORDER BIT NOT ON
         BNO   PARSELNK               THEN DO LINK, NOT CALL
         L     R15,524(,R15)       CVTPARS
         BALR  R14,R15             CALL IKJPARS
         B     PARSERET            SKIP AROUND LINK
PARSELNK EQU   *
         LINK  EP=IKJPARS  LINK TO PARSE
PARSERET LTR   R15,R15  ANY ERRORS?
         BZ    DSN1  NO
         TPUT  PARSERR,L'PARSERR
         B     RETURN
DSN1     DC    0H'0'
         L     R9,ANSWER
         USING DSNPDL,R9
         MVC   SWT1,PARSOLD+1
         LA    R7,PARSDSN
         XR    R1,R1  ZERO DSNAME COUNTER
         LA    R6,DSNLEN
         LA    R3,DSNLEN+2  LOAD ADDR FOR DSNAME
DSN1A    DC    0H'0'
         XR    R5,R5
         TM    6(R7),X'80'  WAS DSNAME INPUT?
         BZ    DSN1B  NO, SEARCH ON USER ID ONLY
         TM    6(R7),X'40'  DSNAME INPUT IN QUOTES?
         BO    DSN2  YES
         B     DSN2                UNQUOTED DSNAME PREFIXED BY PARSE
DSN1B    DC    0H'0'
         L     R4,4(R11)           CPPLUPT, PROFILE TABLE
         USING UPT,R4
         XR    R2,R2
         IC    R2,UPTPREFL         PREFIX LENGTH
         LA    R4,UPTPREFX         PREFIX
         DROP  R4
         LTR   R2,R2               IS THERE A PREFIX IN THE PROFILE
         BNZ   USEPREF             YES, USE IT
         L     R4,8(,R11)          CPPLPSCB
         IC    R2,7(,R4)           USERID LENGTH
USEPREF  LR    R5,R2               SAVE LENGTH OF PREFIX OR USERID
         BCTR  R2,R0               LENGTH MINUS 1 FOR EX
         EX    R2,MVCR3R4
*MVCR3R4 MVC   0(0,R3),0(R4)
         TM    6(R7),X'80'  WAS DSNAME INPUT?
         BZ    DSN2A  NO
         LA    R3,1(R2,R3)
         MVI   0(R3),C'.'
         LA    R3,1(R3)
         LA    R5,1(R5)
DSN2     DC    0H'0'
         L     R4,0(R7)
         LH    R2,4(R7)  LOAD LENGTH OF DSNAME
         AR    R5,R2
         BCTR  R2,R0
         EX    R2,MVCR3R4
DSN2A    DC    0H'0'
         STH   R5,0(R6)
         LA    R1,1(R1)  INCREMENT DSNAME COUNTER
         LA    R6,46(R6)  FOR NEXT LENGTH
         LA    R3,2(R6)  FOR NEXT DSNAME
         CH    R1,=H'10'  MAX NO. OF DSNAMES?
         BE    DSN3  YES
         CLI   24(R7),X'FF'  LAST DSNAME IN LIST?
         BE    DSN3
         L     R7,24(R7)  LOAD ADDR OF NEXT DSNAME PDE
         B     DSN1A  GO PROCESS NEXT DSNAME
DSN3     DC    0H'0'
         IKJRLSA ANSWER
         L     R1,=V(DSN2$)
         IDENTIFY EP=DSNLOAD2,ENTRY=(1)
         XC    TASKECB,TASKECB
         XC    TIMERECB,TIMERECB
**  PASS ADDR OF TIMERECB SO THAT IF THE TIME EXPIRES, THE TASK CAN BE
**  DETACHED.
         ATTACH EP=DSNLOAD2,ECB=TASKECB,PARAM=(TIMERECB,DSNLEN,SWT1)
         ST    R1,TCB
         WAIT  1,ECBLIST=ECBLIST
         MVC   SWT,TIMERECB
         DETACH TCB
         TM    SWT,X'40'  DID TIMER EXPIRE?
         BZ    RETURN  NO
         TPUT  TIMEMSG,L'TIMEMSG
         B     RETURN
ERROR    DC    0H'0'
         TPUT  ERMSG1,L'ERMSG1
RETURN   DC    0H'0'
         L     R13,SAVE+4
         RETURN (14,12),T,RC=0
**
MVCR3R4  MVC   0(0,R3),0(R4)
**
**
**
SAVE     DC    18F'0'
ANSWER   DC    F'0'
TASKECB  DC    F'0'
TIMERECB DC    F'0'
TCB      DC    F'0'
ECBLIST  DC    A(TASKECB)
         DC    XL1'80'
         DC    AL3(TIMERECB)
PARSEPPL DC    7F'0'
ECB      DC    F'0'
DSNLEN   DC    462H'0'  LENGTH OF DSNAME AND DSNAME INPUT
SWT      DC    X'00'
SWT1     DC    X'00'
PARSERR  DC    C'PARSE ERROR'
ERMSG1   DC    C'ERROR IN DATA SET NAME'
OPERMSG  DC    C'YOU MUST HAVE ''OPER'' PRIVILEGES TO USE THE ''DSN'' CX
               OMMAND'
TIMEMSG  DC    C'DSN TIMED OUT'
         LTORG
         DC    0D'0'
*        PRINT NOGEN
DSNPCL   IKJPARM DSECT=DSNPDL
PARSDSN  IKJPOSIT DSNAME,LIST,USID
PARSOLD  IKJKEYWD
         IKJNAME 'OLD'
         IKJENDP
         PRINT GEN
         SPACE
         IKJUPT
         SPACE
         IKJPPL
         SPACE
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END
