         TITLE '   S P Z A P   '
***********************************************************************
*                                                                     *
*        'SPZAP' TSO COMMAND                                          *
*                                                                     *
***********************************************************************
         SPACE
*        WRITTEN BY. BILL GODFREY, PRC (PLANNING RESEARCH CORPORATION).
*        INSTALLATION. PRC, MCLEAN, VIRGINIA.
*        DATE WRITTEN. MAY 25 1982.
*        DATE UPDATED. MAY 26 1982.
*        ATTRIBUTES. RE-ENTRANT.
*        DESCRIPTION.
*         THIS TSO COMMAND INVOKES THE AMASPZAP SERVICE AID PROGRAM.
*         IT ALLOCATES THE SYSLIB DATA SET AND OPTIONALLY ALLOCATES
*         THE SYSIN AND SYSPRINT DATA SETS.
*
*         DDNAMES RETURNED BY DYNAMIC ALLOCATION ARE PASSED TO
*         AMASPZAP, SO EXISTING ALLOCATIONS TO SYSPRINT, SYSIN,
*         AND SYSLIB WILL NOT BE CHANGED.
*
*        LOG OF CHANGES.
*         26MAY82 - MAKE BLKSIZE OF PRINT 1210.
*
*        DOCUMENTATION.
*
*         THE SYNTAX IS:
*            SPZAP   'DSNAME'   INPUT(DSNAME)   PRINT(DSNAME)
*
         SPACE
         GBLB  &MVS
&MVS     SETB  1                   1 - MVS   0 - SVS,MVT
         SPACE
SPZAP    START
         USING *,R12,R11
         B     @PROLOG-*(,R15)
         DC    AL1(11),CL11'SPZAP'
         DC    CL16' &SYSDATE &SYSTIME '
@SIZE    DC    0F'0',AL1(1),AL3(@DATAL)
@PROLOG  STM   14,12,12(13)
         LR    R12,R15             BASE
         LA    R15,1
         LA    R11,4095(R15,R12)   BASE
         LR    R2,R1
         USING CPPL,R2
         L     R0,@SIZE
         GETMAIN R,LV=(0)
         LR    R9,R1
         USING @DATA,R9
         ST    13,4(,1)            CHAIN SAVEAREA
         ST    1,8(,13)            CHAIN SAVEAREA
         LR    13,1                NEW SAVEAREA
         SPACE 1
         MVI   STATUS,0
         XC    LINKAREA(8),LINKAREA
         SLR   R15,R15
         STH   R15,RC              SET RC = 0
         XC    DDSAVEL(24),DDSAVEL
         SPACE
************************************************************
*                                                          *
*        SET UP IOPL FOR PUTLINE                           *
*                                                          *
************************************************************
         SPACE
         LA    R15,MYIOPL
         USING IOPL,R15
         MVC   IOPLUPT(4),CPPLUPT
         MVC   IOPLECT(4),CPPLECT
         LA    R0,MYECB
         ST    R0,IOPLECB
         XC    MYECB,MYECB
         LA    R0,MYPTPB
         ST    R0,IOPLIOPB
         DROP  R15                 IOPL
         SPACE
         AIF   (NOT &MVS).SKIP1
         L     R15,16              LOAD CVT POINTER
         TM    444(R15),X'80'      IS PUTLINE LOADED? (VS2)
         BNO   PUTLOAD             NO - BRANCH TO LOAD
         L     R15,444(,R15)       YES - USE CVTPUTL
         B     PUTLOADX            BRANCH AROUND LOAD
.SKIP1   ANOP
PUTLOAD  LA    R0,=CL8'IKJPUTL '
         LOAD  EPLOC=(0)
         LR    R15,R0              GET ENTRY ADDRESS
         LA    R15,0(,R15)         CLEAR HI BYTE FOR DELETE ROUTINE
PUTLOADX ST    R15,MYPUTLEP        SAVE PUTLINE ENTRY ADDRESS
         SPACE
************************************************************
*                                                          *
*        SET UP PPL FOR PARSE                              *
*                                                          *
************************************************************
         SPACE
         LA    R15,MYPPL
         USING PPL,R15
         MVC   PPLUPT(4),CPPLUPT
         MVC   PPLECT(4),CPPLECT
         LA    R0,MYECB
         ST    R0,PPLECB
         XC    MYECB,MYECB
*        L     R0,=A(SPZAPPCL)
         LA    R0,PCLADDR
         ST    R0,PPLPCL
         LA    R0,MYANS
         ST    R0,PPLANS
         XC    MYANS(4),MYANS
         MVC   PPLCBUF(4),CPPLCBUF
         ST    R9,PPLUWA
         DROP  R15                 PPL
         SPACE 1
************************************************************
*                                                          *
*        CALL THE PARSE SERVICE ROUTINE                    *
*                                                          *
************************************************************
         SPACE 1
         LR    R1,R15              POINT TO PPL
         AIF   (NOT &MVS).SKIP2
         L     R15,16              CVTPTR
         TM    524(R15),X'80'      IF HI ORDER BIT NOT ON
         BNO   PARSELNK               THEN DO LINK, NOT CALL
         L     R15,524(,R15)       CVTPARS
         BALR  R14,R15             CALL IKJPARS
         B     PARSEEXT            SKIP AROUND LINK
PARSELNK EQU   *
.SKIP2   ANOP
         LINK  EP=IKJPARS,SF=(E,LINKAREA)
PARSEEXT EQU   *
         SPACE 1
         LTR   R15,R15
         BZ    PARSEOK
         LA    R1,MSG01
         LA    R0,L'MSG01
         BAL   R14,PUTMSG
         LA    R15,12
         B     EXIT
PARSEOK  EQU   *
         SPACE
         L     R3,MYANS
         USING IKJPARMD,R3
         SPACE
************************************************************
*                                                          *
*         GET THE SYSLIB UNIT NAME  IF SPECIFIED           *
*                                                          *
************************************************************
         SPACE
         MVC   UNITNAME(8),=CL8' '
         LA    R4,LUNIT
         TM    6(R4),X'80'         IS UNIT SPECIFIED
         BZ    NOUNITI
         LH    R14,4(,R4)          GET LENGTH OF UNIT
         BCTR  R14,0
         L     R1,0(,R4)           POINT TO UNIT NAME
         B     *+10
         MVC   UNITNAME(0),0(R1)   (EXECUTED)
         EX    R14,*-6
NOUNITI  EQU   *
         SPACE
************************************************************
*                                                          *
*         GET THE SYSLIB VOLUME IF SPECIFIED               *
*                                                          *
************************************************************
         SPACE
         MVC   VOLUME(6),=CL8' '
         LA    R4,LVOL
         TM    6(R4),X'80'         IS VOLUME SPECIFIED
         BZ    NOVOLI
         LH    R14,4(,R4)          GET LENGTH OF UNIT
         BCTR  R14,0
         L     R1,0(,R4)           POINT TO VOLUME NAME
         B     *+10
         MVC   VOLUME(0),0(R1)     (EXECUTED)
         EX    R14,*-6
NOVOLI   EQU   *
         SPACE
************************************************************
*                                                          *
*        GET THE SYSLIB DSNAME                             *
*                                                          *
************************************************************
         SPACE
         LA    R4,DSNL             POINT TO FIRST DSN PDE
         TM    6(R4),X'80'         IS DATASET NAME SPECIFIED?
         BO    OKDSNL              YES - BRANCH
         LA    R1,MSG05            NO - JUST MEMBER NAME
         LA    R0,L'MSG05
         BAL   R14,PUTMSG
         LA    R15,12
         B     NEXTDSN
OKDSNL   EQU   *
         LA    R15,DSNAME+2
         MVI   0(R15),C' '         BLANK THE DSNAME AREA
         MVC   1(43,R15),0(R15)
         LH    R1,4(,R4)           GET LENGTH
         STH   R1,DSNAME           SET DSNAME LENGTH
         L     R14,0(,R4)          POINT TO DSN VALUE
         BCTR  R1,0                LENGTH MINUS 1 FOR EX
         B     *+10                BRANCH AROUND EXECUTED MVC
         MVC   0(0,R15),0(R14)     (EXECUTED)
         EX    R1,*-6              MOVE DSN TO DSNAME
         SPACE
************************************************************
*                                                          *
*        ALLOCATE THE SYSLIB DATA SET                      *
*                                                          *
************************************************************
         SPACE
         LA    R1,MYDAPL
         USING DAPL,R1
         MVC   DAPLUPT(4),CPPLUPT
         MVC   DAPLECT(4),CPPLECT
         LA    R0,MYECB
         ST    R0,DAPLECB
         MVC   DAPLPSCB(4),CPPLPSCB
         LA    R15,MYDAPB
         ST    R15,DAPLDAPB
         DROP  R1                  DAPL
         USING DAPB08,R15
         XC    0(84,R15),0(R15)
         MVI   DA08CD+1,X'08'
         LA    R0,DSNAME
         ST    R0,DA08PDSN
         MVC   DA08DDN(8),=CL8' '
         MVC   DA08UNIT,UNITNAME
         MVC   DA08SER,=CL8' '
         MVC   DA08SER(6),VOLUME
         MVC   DA08MNM,=CL8' '
         MVC   DA08PSWD,=CL8' '
         MVI   DA08DSP1,DA08SHR
         MVI   DA08DPS2,DA08KEEP
         MVI   DA08DPS3,DA08KEP
         B     MEMBXL              *** IGNORE MEMBER ***
         TM    14(R4),X'80'        MEMBER SPECIFIED?
         BZ    MEMBXL              NO - BRANCH
         LH    R1,12(,R4)          GET LENGTH OF MEMBER
         BCTR  R1,0                MINUS 1 FOR EX
         L     R14,8(,R4)          GET ADDRESS OF MEMBER NAME
         B     *+10
         MVC   DA08MNM(0),0(R14)   MOVE MEMBER NAME
         EX    R1,*-6
MEMBXL   EQU   *
         TM    22(R4),X'80'        PASSWORD SPECIFIED?
         BZ    PASSXL              NO - BRANCH
         LH    R1,20(,R4)          GET LENGTH OF PSWD
         BCTR  R1,0                MINUS 1 FOR EX
         L     R14,16(,R4)         GET ADDRESS OF PSWD
         B     *+10
         MVC   DA08PSWD(0),0(R14)  MOVE PSWD
         EX    R1,*-6
PASSXL   EQU   *
         LA    R1,MYDAPL
         SPACE
         BAL   R14,CALLDAIR
         LTR   R15,R15
         BZ    OKDAIRL
         BAL   R14,DAIRFAIL
         LA    R15,12
         B     NEXTDSN
OKDAIRL  EQU   *
         OI    STATUS,STATLF       TELL CLEANUP TO FREE IT
         LA    R15,MYDAPB
         MVC   DDSAVEL,DA08DDN
         DROP  R15                 DAPB08
         SPACE
************************************************************
*                                                          *
*        GET THE SYSIN DSNAME                              *
*                                                          *
************************************************************
         SPACE
         CLI   INPUTKW+1,1
         BNE   INPUTX
         LA    R4,DSNI             POINT TO DSN PDE
         TM    6(R4),X'80'         IS DATASET NAME SPECIFIED?
         BO    OKDSNI              YES - BRANCH
         LA    R1,MSG05            NO - JUST MEMBER NAME
         LA    R0,L'MSG05
         BAL   R14,PUTMSG
         LA    R15,12
         B     NEXTDSN
OKDSNI   EQU   *
         LA    R15,DSNAMI+2
         MVI   0(R15),C' '         BLANK THE DSNAME AREA
         MVC   1(43,R15),0(R15)
         LH    R1,4(,R4)           GET LENGTH
         STH   R1,DSNAMI           SET DSNAME LENGTH
         L     R14,0(,R4)          POINT TO DSN VALUE
         BCTR  R1,0                LENGTH MINUS 1 FOR EX
         B     *+10                BRANCH AROUND EXECUTED MVC
         MVC   0(0,R15),0(R14)     (EXECUTED)
         EX    R1,*-6              MOVE DSN TO DSNAME
         SPACE
************************************************************
*                                                          *
*        ALLOCATE THE SYSIN DATA SET                       *
*                                                          *
************************************************************
         SPACE
         LA    R1,MYDAPL
         USING DAPL,R1
         MVC   DAPLUPT(4),CPPLUPT
         MVC   DAPLECT(4),CPPLECT
         LA    R0,MYECB
         ST    R0,DAPLECB
         MVC   DAPLPSCB(4),CPPLPSCB
         LA    R15,MYDAPB
         ST    R15,DAPLDAPB
         DROP  R1                  DAPL
         USING DAPB08,R15
         XC    0(84,R15),0(R15)
         MVI   DA08CD+1,X'08'
         LA    R0,DSNAMI
         ST    R0,DA08PDSN
         MVC   DA08DDN(8),=CL8' '
         MVC   DA08UNIT,=CL8' '
         MVC   DA08SER,=CL8' '
         MVC   DA08MNM,=CL8' '
         MVC   DA08PSWD,=CL8' '
         MVI   DA08DSP1,DA08SHR
         MVI   DA08DPS2,DA08KEEP
         MVI   DA08DPS3,DA08KEP
         TM    14(R4),X'80'        MEMBER SPECIFIED?
         BZ    MEMBXI              NO - BRANCH
         LH    R1,12(,R4)          GET LENGTH OF MEMBER
         BCTR  R1,0                MINUS 1 FOR EX
         L     R14,8(,R4)          GET ADDRESS OF MEMBER NAME
         B     *+10
         MVC   DA08MNM(0),0(R14)   MOVE MEMBER NAME
         EX    R1,*-6
MEMBXI   EQU   *
         TM    22(R4),X'80'        PASSWORD SPECIFIED?
         BZ    PASSXI              NO - BRANCH
         LH    R1,20(,R4)          GET LENGTH OF PSWD
         BCTR  R1,0                MINUS 1 FOR EX
         L     R14,16(,R4)         GET ADDRESS OF PSWD
         B     *+10
         MVC   DA08PSWD(0),0(R14)  MOVE PSWD
         EX    R1,*-6
PASSXI   EQU   *
         LA    R1,MYDAPL
         SPACE
         BAL   R14,CALLDAIR
         LTR   R15,R15
         BZ    OKDAIRI
         BAL   R14,DAIRFAIL
         LA    R15,12
         B     NEXTDSN
OKDAIRI  EQU   *
         OI    STATUS,STATIF       TELL CLEANUP TO FREE IT
         LA    R15,MYDAPB
         MVC   DDSAVEI,DA08DDN
         TM    DA08DSO,X'02'       IS SYSIN A PDS
         BZ    OKDSORI             NO, BRANCH
OKDSORIP EQU   *
         CLC   DA08MNM,=CL8' '     WAS MEMBER SPECIFIED?
         BNE   OKDSORI             YES - BRANCH
         LA    R1,MSG07            NO - MEMBER IS REQUIRED
         LA    R0,L'MSG07
         BAL   R14,PUTMSG
         LA    R15,12
         B     NEXTDSN
OKDSORI  EQU   *
INPUTX   EQU   *
         DROP  R15                 DAPB08
         SPACE
************************************************************
*                                                          *
*        GET THE SYSPRINT DSNAME                           *
*                                                          *
************************************************************
         SPACE
         CLI   PRINTKW+1,1
         BNE   PRINTX
         LA    R4,DSNP             POINT TO DSN PDE
         TM    6(R4),X'80'         IS DATASET NAME SPECIFIED?
         BO    OKDSNP              YES - BRANCH
         LA    R1,MSG05            NO - JUST MEMBER NAME
         LA    R0,L'MSG05
         BAL   R14,PUTMSG
         LA    R15,12
         B     NEXTDSN
OKDSNP   EQU   *
         LA    R15,DSNAMP+2
         MVI   0(R15),C' '         BLANK THE DSNAME AREA
         MVC   1(43,R15),0(R15)
         LH    R1,4(,R4)           GET LENGTH
         STH   R1,DSNAMP           SET DSNAME LENGTH
         L     R14,0(,R4)          POINT TO DSN VALUE
         BCTR  R1,0                LENGTH MINUS 1 FOR EX
         B     *+10                BRANCH AROUND EXECUTED MVC
         MVC   0(0,R15),0(R14)     (EXECUTED)
         EX    R1,*-6              MOVE DSN TO DSNAME
         SPACE
************************************************************
*                                                          *
*        ALLOCATE THE SYSPRINT DATA SET                    *
*                                                          *
************************************************************
         SPACE
         LA    R1,MYDAPL
         USING DAPL,R1
         MVC   DAPLUPT(4),CPPLUPT
         MVC   DAPLECT(4),CPPLECT
         LA    R0,MYECB
         ST    R0,DAPLECB
         MVC   DAPLPSCB(4),CPPLPSCB
         LA    R15,MYDAPB
         ST    R15,DAPLDAPB
         DROP  R1                  DAPL
         USING DAPB08,R15
         XC    0(84,R15),0(R15)
         MVI   DA08CD+1,X'08'
         LA    R0,DSNAMP
         ST    R0,DA08PDSN
         MVC   DA08DDN(8),=CL8' '
         MVC   DA08UNIT,=CL8' '
         MVC   DA08SER,=CL8' '
         MVC   DA08MNM,=CL8' '
         MVC   DA08PSWD,=CL8' '
         MVI   DA08DSP1,DA08SHR
         MVI   DA08DPS2,DA08KEEP
         MVI   DA08DPS3,DA08KEP
         TM    14(R4),X'80'        MEMBER SPECIFIED?
         BZ    MEMBXP              NO - BRANCH
         LH    R1,12(,R4)          GET LENGTH OF MEMBER
         BCTR  R1,0                MINUS 1 FOR EX
         L     R14,8(,R4)          GET ADDRESS OF MEMBER NAME
         B     *+10
         MVC   DA08MNM(0),0(R14)   MOVE MEMBER NAME
         EX    R1,*-6
MEMBXP   EQU   *
         TM    22(R4),X'80'        PASSWORD SPECIFIED?
         BZ    PASSXP              NO - BRANCH
         LH    R1,20(,R4)          GET LENGTH OF PSWD
         BCTR  R1,0                MINUS 1 FOR EX
         L     R14,16(,R4)         GET ADDRESS OF PSWD
         B     *+10
         MVC   DA08PSWD(0),0(R14)  MOVE PSWD
         EX    R1,*-6
PASSXP   EQU   *
         LA    R1,MYDAPL
         BAL   R14,CALLDAIR        ALLOCATE SYSPRINT OLD
         LTR   R0,R15              DID WE GET IT
         BZ    OKDAIRP             YES, BRANCH
         CH    R15,=H'8'           NO, WAS RC = 8
         BNE   DAIRFP              NO, ISSUE ERROR MESSAGE
         LA    R15,MYDAPB          YES, CHECK FOR NOT IN CATALOG
         CLC   DA08DARC(4),=X'17080008' NOT IN CATALOG
         BNE   DAIRFP              BRANCH IF SOME OTHER ERROR
         XC    DA08DARC(4),DA08DARC
         MVI   DA08DSP1,DA08NEW
         MVI   DA08DPS2,DA08CAT
         MVI   DA08DPS3,DA08CATL
         MVC   DA08PQTY,=F'3'
         MVC   DA08SQTY,=F'3'
         MVI   DA08CTL,DA08TRKS
         MVC   DA08BLK,=F'1210'
         CLI   DA08MNM,C' '        MEMBER SPECIFIED
         BE    *+10                NO
         MVC   DA08DQTY,=F'1'      YES, NEED DIRECTORY SPACE
         LA    R1,MYDAPL
         BAL   R14,CALLDAIR        ALLOCATE SYSPRINT NEW
         LTR   R0,R15              DID WE CREATE IT
         BZ    OKDAIRP             YES, BRANCH
DAIRFP   LR    R15,R0              RESTORE R15
         BAL   R14,DAIRFAIL
         LA    R15,12
         B     NEXTDSN
OKDAIRP  EQU   *
         OI    STATUS,STATPF       TELL CLEANUP TO FREE IT
         LA    R15,MYDAPB
         MVC   DDSAVEP,DA08DDN
         TM    DA08DSO,X'02'       IS SYSPRINT A PDS
         BZ    OKDSORP             NO
OKDSORPP EQU   *
         CLC   DA08MNM,=CL8' '     WAS MEMBER SPECIFIED?
         BNE   OKDSORP             YES - BRANCH
         LA    R1,MSG07            NO - MEMBER IS REQUIRED
         LA    R0,L'MSG07
         BAL   R14,PUTMSG
         LA    R15,12
         B     NEXTDSN
OKDSORP  EQU   *
         DROP  R15                 DAPB08
PRINTX   EQU   *
         SPACE
************************************************************
*                                                          *
*         FILL IN DDNAMES                                  *
*                                                          *
************************************************************
         SPACE
         XC    DDNAMES(DDNAMESL),DDNAMES
         MVC   DDNAMES+2+24(8),DDSAVEL
         MVC   DDNAMES+2+32(8),DDSAVEI
         MVC   DDNAMES+2+40(8),DDSAVEP
         SPACE
************************************************************
*                                                          *
*         INVOKE AMASPZAP                                  *
*                                                          *
************************************************************
         SPACE
         LA    R1,PARMPTR
         LA    R15,PARM            ADDRESS OF PARM
         ST    R15,0(,R1)          IN PARAMETER LIST
         SR    R0,R0               ZERO = PARM LENGTH
         STH   R0,0(,R15)          LENGTH OF PARM
         LA    R15,DDNAMES         ADDRESS OF DDNAMES
         ST    R15,4(,R1)          IN PARAMETER LIST
         LA    R0,48               LENGTH OF DDNAMES
         STH   R0,0(,R15)          LENGTH OF DDNAMES
         OI    4(R1),X'80'         END OF PARAMETER LIST
         SPACE
         LINK  EP=AMASPZAP,SF=(E,LINKAREA)
         SPACE
************************************************************
*                                                          *
*         CLEAN UP                                         *
*                                                          *
************************************************************
         SPACE
         B     NEXTDSN
NEXTD21  MVI   RC+1,12
NEXTDSN  EQU   *
         TM    STATUS,STATPF       FREE REQUIRED?
         BZ    NOFREEP
         LA    R1,MYDAPL
         LA    R15,MYDAPB
         USING DAPB18,R15
         XC    0(40,R15),0(R15)
         MVI   DA18CD+1,X'18'
         MVC   DA18DDN,DDSAVEP
         MVC   DA18MNM(8),=CL8' '
         MVC   DA18SCLS(2),=CL8' '
         BAL   R14,CALLDAIR        UNALLOCATE SYSPRINT
         NI    STATUS,255-STATPF   UNALLOCATED
         DROP  R15                 DAPB18
NOFREEP  EQU   *
         TM    STATUS,STATIF       FREE REQUIRED?
         BZ    NOFREEI
         LA    R1,MYDAPL
         LA    R15,MYDAPB
         USING DAPB18,R15
         XC    0(40,R15),0(R15)
         MVI   DA18CD+1,X'18'
         MVC   DA18DDN,DDSAVEI
         MVC   DA18MNM(8),=CL8' '
         MVC   DA18SCLS(2),=CL8' '
         BAL   R14,CALLDAIR        UNALLOCATE SYSIN
         NI    STATUS,255-STATIF   UNALLOCATED
         DROP  R15                 DAPB18
NOFREEI  EQU   *
         SPACE
         TM    STATUS,STATLF       FREE REQUIRED?
         BZ    NOFREEI
         LA    R1,MYDAPL
         LA    R15,MYDAPB
         USING DAPB18,R15
         XC    0(40,R15),0(R15)
         MVI   DA18CD+1,X'18'
         MVC   DA18DDN,DDSAVEL
         MVC   DA18MNM(8),=CL8' '
         MVC   DA18SCLS(2),=CL8' '
         BAL   R14,CALLDAIR        UNALLOCATE
         NI    STATUS,255-STATLF   UNALLOCATED
         DROP  R15                 DAPB18
NOFREEL  EQU   *
         SPACE
         IKJRLSA MYANS
         SPACE
         CLI   RC+1,0              IS RC ZERO?
         BZ    STACKDX             YES, BRANCH
         MVC   MYSTPB(STACKDL),STACKD
         SPACE
         STACK DELETE=ALL,PARM=MYSTPB,MF=(E,MYIOPL)
         SPACE
         TCLEARQ
STACKDX  EQU   *
         SPACE
         LH    R15,RC
         B     EXIT
         SPACE
************************************************************
*                                                          *
*         CALL IKJDAIR SERVICE ROUTINE                     *
*                                                          *
************************************************************
          SPACE
CALLDAIR ST    R14,DAIRREGS
         AIF   (NOT &MVS).SKIP6
         L     R15,16
         TM    X'2DC'(R15),X'80'  CVTDAIR
         BNO   DAIRLINK
         L     R15,X'2DC'(,R15)
         BALR  R14,R15
         B     DAIRFINI
DAIRLINK EQU   *
.SKIP6   ANOP
         LINK  EP=IKJDAIR,SF=(E,LINKAREA)
DAIRFINI L     R14,DAIRREGS
         BR    R14
         SPACE
************************************************************
*                                                          *
*        DYNAMIC ALLOCATION FAILURE ROUTINE                *
*                                                          *
************************************************************
         SPACE
DAIRFAIL ST    R14,MYDFREGS
         AIF   (NOT &MVS).SKIP7
         LA    R1,MYDFPARM
         USING DFDSECTD,R1
         ST    R15,MYDFRC
         LA    R15,MYDFRC
         ST    R15,DFRCP
         LA    R15,MYDAPL
         ST    R15,DFDAPLP
         SLR   R15,R15
         ST    R15,MYJEFF02
         LA    R15,MYJEFF02
         ST    R15,DFJEFF02
         LA    R15,DFDAIR
         STH   R15,MYDFID
         LA    R15,MYDFID
         ST    R15,DFIDP
         SLR   R15,R15
         ST    R15,DFCPPLP
         LINK  EP=IKJEFF18,SF=(E,LINKAREA)
         L     R15,MYDFRC
         DROP  R1                  DFDSECTD
.SKIP7   ANOP
         AIF   (&MVS).SKIP8
         LA    R1,MSGDAIR
         LA    R0,L'MSGDAIR
         BAL   R14,PUTMSG
.SKIP8   ANOP
         L     R14,MYDFREGS
         BR    R14
         SPACE
************************************************************
*                                                          *
*        PUTMSG ROUTINE                                    *
*                                                          *
************************************************************
         SPACE
PUTMSG   STM   R14,R1,PUTLINS
         XC    MYOLD(8),MYOLD
         XC    MYSEG1(4),MYSEG1
         MVC   MYPTPB(12),MODLPTPM
         LA    R14,1               NO. OF MESSAGE SEGMENTS
         ST    R14,MYOLD
         LA    R14,MYSEG1          POINT TO 1ST SEGMENT
         ST    R14,MYOLD+4
         LR    R14,R0              LENGTH IN R0
         LA    R14,4(,R14)         ADD 4
         LA    R15,MYSEG1+4
         CLC   0(3,R1),=C'IKJ'     IS DATA PRECEEDED BY MESSAGE ID?
         BE    *+16                YES - BRANCH
         LA    R14,1(,R14)         ADD 1 TO LENGTH
         MVI   0(R15),C' '         INSERT LEADING BLANK
         LA    R15,1(,R15)         BUMP POINTER
         STH   R14,MYSEG1
         LR    R14,R0
         BCTR  R14,0
         B     *+10
         MVC   0(0,R15),0(R1)      MOVE MESSAGE IN
         EX    R14,*-6
         L     R15,MYPUTLEP
         SPACE
         PUTLINE PARM=MYPTPB,OUTPUT=(MYOLD),ENTRY=(15),MF=(E,MYIOPL)
         SPACE
         LM    R14,R1,PUTLINS
         BR    R14
         SPACE
************************************************************
*                                                          *
*        PUTLINE ROUTINE                                   *
*                                                          *
************************************************************
         SPACE
PUTLINE  STM   R14,R1,PUTLINS
         XC    MYSEG1(4),MYSEG1
         MVC   MYPTPB(12),MODLPTPB
         LR    R14,R0              LENGTH IN R0
         LA    R14,4(,R14)         ADD 4
         STH   R14,MYSEG1
         LR    R14,R0
         BCTR  R14,0
         B     *+10
         MVC   MYSEG1+4(0),0(R1)   MOVE TEXT IN
         EX    R14,*-6
         LA    R1,MYIOPL
         L     R15,MYPUTLEP
         SPACE
         PUTLINE PARM=MYPTPB,OUTPUT=(MYSEG1,DATA),ENTRY=(15),MF=(E,(1))
         SPACE
         LM    R14,R1,PUTLINS
         BR    R14
         SPACE 1
EXIT     LR    1,13
         L     R0,@SIZE
         L     13,4(,13)
         ST    15,16(,13)
         FREEMAIN R,A=(1),LV=(0)
         LM    14,12,12(13)
         BR    14
         SPACE
************************************************************
*                                                          *
*        CONSTANTS                                         *
*                                                          *
************************************************************
         SPACE
         LTORG
         SPACE
MODLPTPM PUTLINE OUTPUT=(1,TERM,SINGLE,INFOR),                         X
               TERMPUT=(EDIT,WAIT,NOHOLD,NOBREAK),MF=L
         SPACE
MODLPTPB PUTLINE OUTPUT=(1,TERM,SINGLE,DATA),                          X
               TERMPUT=(EDIT,WAIT,NOHOLD,NOBREAK),MF=L
         SPACE
STACKD   STACK DELETE=ALL,MF=L
STACKDL  EQU   *-STACKD
         SPACE
MSG01    DC    C'ERROR IN PARSE SERVICE ROUTINE'
MSG05    DC    C'IKJ58509I DATA SET NAME REQUIRED WHEN MEMBER IS SPECIF+
               IED'
MSG07    DC    C'DATA SET IS PARTITIONED, MEMBER NOT SPECIFIED'
MSGDAIR  DC    C'UNABLE TO ALLOCATE'
PCLADDR  DC    0D'0'               END OF CSECT
         SPACE
************************************************************
*                                                          *
*        PARSE PARAMETERS                                  *
*                                                          *
************************************************************
         SPACE
         PRINT NOGEN
SPZAPPCL IKJPARM
DSNL     IKJPOSIT DSNAME,USID,PROMPT='SYSLIB DATA SET NAME'
LUNITKW  IKJKEYWD
         IKJNAME 'UNIT',SUBFLD=LUNITSF
LVOLKW   IKJKEYWD
         IKJNAME 'VOLUME',SUBFLD=LVOLSF
INPUTKW  IKJKEYWD
         IKJNAME 'INPUT',SUBFLD=INPUTSF
PRINTKW  IKJKEYWD
         IKJNAME 'PRINT',SUBFLD=PRINTSF
LUNITSF  IKJSUBF
LUNIT    IKJIDENT 'UNIT NAME',                                         +
               FIRST=ALPHANUM,OTHER=ANY,MAXLNTH=8,                     +
               PROMPT='UNIT NAME'
LVOLSF   IKJSUBF
LVOL     IKJIDENT 'VOLUME SERIAL',                                     +
               FIRST=ALPHANUM,OTHER=ANY,MAXLNTH=6,                     +
               PROMPT='VOLUME SERIAL'
INPUTSF  IKJSUBF
DSNI     IKJPOSIT DSNAME,USID,PROMPT='INPUT DATA SET NAME'
PRINTSF  IKJSUBF
DSNP     IKJPOSIT DSNAME,USID,PROMPT='PRINT DATA SET NAME'
         IKJENDP
         PRINT GEN
         SPACE
************************************************************
*                                                          *
*        DSECTS                                            *
*                                                          *
************************************************************
         SPACE
@DATA    DSECT
         DS    18F                 REGISTER SAVEAREA
LINKAREA DS    2F
MYPPL    DS    7F
MYANS    DS    F
MYECB    DS    F                  USED BY PUTLINE ROUTINE
MYIOPL   DS    4F                 USED BY PUTLINE ROUTINE
MYPTPB   DS    3F                 USED BY PUTLINE ROUTINE
MYOLD    DS    2F                 USED BY PUTLINE ROUTINE
MYSEG1   DS    2H,CL256           USED BY PUTLINE ROUTINE
PUTLINS  DS    4F                 USED BY PUTLINE ROUTINE
MYPUTLEP DS    F                  ADDRESS OF IKJPUTL
MYSTPB   DS    0F                 5 WORDS USED BY STACK DELETE
MYDAPL   DS    5F
MYDAPB   DS    21F
MYDFPB   DS    5F
DSNAME   DS    H,CL44
DSNAMI   DS    H,CL44
DSNAMP   DS    H,CL44
VOLSER   DS    CL6                 VOLUME FROM UCB
UNITNAME DS    CL8
VOLUME   DS    CL8                 VOLUME SPECIFIED BY USER
MSGWK    DS    CL128
STATUS   DS    X
STATLF   EQU   X'80'               UNALLOCATE SYSLIB
STATIF   EQU   X'40'               UNALLOCATE SYSIN
STATPF   EQU   X'20'               UNALLOCATE SYSPRINT
RC       DS    H
MYDFPARM DS    5F  USED BY DAIRFAIL
MYDFREGS DS    F   USED BY DAIRFAIL
MYDFRC   DS    F   USED BY DAIRFAIL
MYJEFF02 DS    F   USED BY DAIRFAIL
MYDFID   DS    H   USED BY DAIRFAIL
HALF     DS    H
DOUBLE   DS    D
DDSAVEL  DS    CL8
DDSAVEI  DS    CL8
DDSAVEP  DS    CL8
DAIRREGS DS    F
PARMPTR  DS    2F
PARM     DS    H
         DS    0F                  ALIGN DDNAMES
         DS    H                   ALIGN DDNAMES
DDNAMES  DS    H
         DS    CL8                 +0
         DS    CL8                 +8
         DS    CL8                 +16
         DS    CL8                 +24 SYSLIB
         DS    CL8                 +32 SYSIN
         DS    CL8                 +40 SYSPRINT
DDNAMESL EQU   *-DDNAMES
         DS    0D
@DATAL   EQU   *-@DATA
         SPACE
         IKJCPPL
         SPACE 3
         IKJPPL
         SPACE
         IKJDFPB
         SPACE 2
         IKJUPT
         SPACE 2
         IKJIOPL
         SPACE 2
         IKJDAPL
         SPACE 2
         IKJDAP08
         SPACE 2
         IKJDAP18
         SPACE 2
         IKJPSCB
         SPACE 2
         AIF   (NOT &MVS).SKIP12
         IKJEFFDF DFDSECT=YES
.SKIP12  ANOP
         SPACE 2
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END
