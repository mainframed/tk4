PRINTOFF TITLE 'IPO-SUPPLIED PRINT COMMAND                             C
                        '
*$DOC@*****************************************************************
*                                                                     *
*             MODULE NAME = PRINTOFF                                  *
*                                                                     *
*             DESCRIPTIVE NAME =  IPO SUPPLIED PRINT COMMAND FOR      *
*                TSO FOREGROUND EXECUTION                             *
*                                                                     *
*             COPYRIGHT = NONE                                        *
*                                                                     *
*             STATUS = RELEASE 01.0 OF IPO                            *
*                                                                     *
*             FUNCTION =                                              *
*                PRINTS A DATASET(S) WHICH IS SEQUENTIAL OR PARTIONED *
*                ALLOWING THE USER TO SPECIFY OUTPUT CLASS, COPIES,   *
*                DESTINATION AND HOLD/NOHOLD.  ALSO ALLOWS THE USER   *
*                TO SPECIFY LIST/NOLIST, PRINT/NOPRINT, VOLUME,       *
*                OR FOLD/NOFOLD.                                      *
*                LIST IF PDS MEMBER NAMES ARE ONLY TO BE LISTED.      *
*                PRINT IF PDS MEMBERS ARE ONLY TO BE PRINTED.         *
*                                                                     *
*                MODS=                                                *
*                .  NEW KEYWORDS FORMS, TRAIN (OR UCS), FCB,      .GTE.
*                   CAPS (ALIAS OF FOLD), ASIS (ALIAS OF NOFOLD), .GTE.
*                   TEXT (ASIS,UCS(TN)), PROG, AND NOHEADING.     .GTE.
*                .  NEW KEYWORDS ASA (FOR DATA SETS CONTAINING    .PRC.
*                   ASA CONTROLS BUT NOT MARKED AS SUCH),         .PRC.
*                   SNUM TO SUPPRESS SEQUENCE NUMBERS, AND        .PRC.
*                   NOMSG TO SUPPRESS TERMINAL MESSAGES.          .PRC.
*                .  CALL DEFAULT SERVICE ROUTINE.                 .GTE.
*                .  FREE INPUT DATA SET.                          .GTE.
*                .  REPLACE CVT MACRO WITH INLINE CODE SO         .PRC.
*                   ASSEMBLER DOES NOT NEED AMODGEN LIBRARY.      .PRC.
*                .  GTE MODS RE-TAGGED IN COLUMNS 67-71.          .PRC.
*                .  GTE MODS FROM CBT TAPE VERSION 92, 23JUL78.   .PRC.
*                   THIS WAS REPLACED ON  VERSION 128, 01OCT79    .PRC.
*                   WITH FURTHER GTE MODS NOT INCLUDED HERE.      .PRC.
*                .  PRC MODS BY BILL GODFREY,                     .PRC.
*                   PRC COMPUTER CENTER INC, MCLEAN VA.           .PRC.
*                .  SADSC MODS BY JIM MARSHALL, SAN ANTONIO DATA  .SAD.
*                   SERVICES CENTER, 8630 BROADWAY, SAN ANTONIO,  .SAD.
*                   TEXAS  78217    512-828-0201                  .SAD.
*                   CHANGED THE DEST PARAMETER BECAUSE IF YOU TRY .SAD.
*                   TO USE 8 CHARACTER DESTINATION ID YOU GET A   .SAD.
*                   AN INVALID USERID MSG ????  CHANGED SUGGESTED .SAD.
*                   BY JIM PETERSON                               .SAD.
*                                                                     *
*                OPERATION =                                          *
*                   BUILD PARS, DAIR, AND PUTLINE PARAMETER LISTS.    *
*                   CALL PARS TO PARSE COMMAND BUFFER.                *
*                   ALLOCATE DATASET AND DETERMINE IF PDS OR          *
*                      SEQUENTIAL.                                    *
*                   DETERMINE WHAT FUNCTIONS WERE TO BE PERFORMED     *
*                      FOR THIS DATASET IF PDS.                       *
*                   ALLOCATE SYSOUT DATASETS WHEN SOMETHING IS        *
*                      FOUND TO BE PRINTED.                           *
*                   PROCESS NEXT DATASET IN LIST.                     *
*                   AT END OF LIST CLOSE DATASETS AND FREE ANY        *
*                      SYSOUT DATASETS.                               *
*                   DATSETS WILL BE PRINTED AS EITHER VBA OR VBM.     *
*                                                                     *
*              NOTES =                                                *
*                                                                     *
*                 DEPENDENCIES = CHARACTER SET IS EBCDIC.  REASSEMBLE *
*                    IF A DIFFERENT CHARACTER SET IS NEEDED.          *
*                                                                     *
*                 RESTRICTIONS = DATASETS MUST HAVE LRECL LESS THAN   *
*                    255 AND MUST BE EITHER FIXED OR VARIABLE FORMAT. *
*                                                                     *
*                 REGISTER CONVENTIONS = STANDARD CONVENTIONS.        *
*                    REGISTERS 0 TO 10 = WORK REGISTERS               *
*                    REGISTER  11      = UNUSED                       *
*                    REGISTER  12      = ADDRESSABILITY TO PRINTOFF   *
*                                        CSECT                        *
*                    REGISTER  13      = SAVE AREA REGISTER AND       *
*                                        ADDRESSABILITY TO GETMAINED  *
*                                        WORK AREA                    *
*                    REGISTERS 14,15   = WORK REGISTERS               *
*                                                                     *
*                PATCH LABEL = PATCH (UNUSED AND INTIALIZED TO        *
*                   'ZAP*'S)                                          *
*                                                                     *
*         TSO COMMAND SYNTAX = PRINTOFF (DATASET LIST)                *
*             CLASS(SYSOUT CLASS) DEST(DESTID) COPIES(NUMBER)     .SAD.
*             HOLD/NOHOLD LIST/NOLIST PRINT/NOPRINT FOLD/NOFOLD       *
*                                                                     *
*             MODULE TYPE = PROCEDURE                                 *
*                                                                     *
*                PROCESSOR = ASM                                      *
*                                                                     *
*                MODULE SIZE = 5K BYTES                               *
*                                                                     *
*                ATTRIBUTES = SCHEDULER KEY 8, REENTRANT,             *
*                   PROBLEM PROGRAM STATE                             *
*                                                                     *
*             ENTRY POINTS = PRINTOFF (ONLY ENTRY POINT)              *
*                                                                     *
*                LINKAGE =                                            *
*                   FROM TERMINAL MONITOR PROGRAM AS A COMMAND        *
*                                                                     *
*             INPUT = REGISTER 1 POINTS TO COMMAND PROCESSOR          *
*                PARAMETER LIST MAPPED BY IKJCPPL MACRO               *
*                                                                     *
*             OUTPUT = NONE                                           *
*                                                                     *
*             EXIT - NORMAL = AT PROGRAM END VIA BRANCH REGISTER 14   *
*                                                                     *
*                OUTPUT = NONE                                        *
*                                                                     *
*                RETURN CODE = ZERO                                   *
*                                                                     *
*             EXIT - ERROR = NONE - PROGRAM WILL DISPLAY DECIMAL      *
*                RETURN CODE AND RETURN VIA BRANCH REGISTER 14        *
*                                                                     *
*                OUTPUT = NONE                                        *
*                                                                     *
*                RETURN CODE = ZERO                                   *
*                                                                     *
*             EXTERNAL REFERENCES =                                   *
*                                                                     *
*                ROUTINES = IKJPARS, DAIRFAIL, GNRLFAIL, PUTLINE      *
*                                                                     *
*                DATA AREAS = NONE                                    *
*                                                                     *
*                CONTROL BLOCKS = CPPL, PPL, DAPL, DCB, DAPB, CVT,    *
*                   S99RBP, S99RB, S99TUPL, S99TUNIT, S99TUFLD,       *
*                   GFPARMS, DFPARMS, IOPL, IOPB                      *
*                                                                     *
*             TABLES = DATA AREA TO BE GETMAINED.  MAPPED BY DSECT    *
*                BEGINNING AT LABEL RDATD.                            *
*                                                                     *
*             MACROS = SAVE, GETMAIN, CALLTSSR, LINK, TPUT, FREEMAIN, *
*                   OPEN, PUT, GET, CLOSE, RDJFCB, IKJRLSA, DYNALLOC, *
*                   DCB, IKJPARM, IKJPOSIT, IKJKEYWD, IKJNAME,        *
*                   IKJSUBF, IKJIDENT, IKJENDP, IKJDAP08, IKJDAP18,   *
*                   IKJDAPL, IKJCPPL, IKJPPL, CVT, IEFZB4D0, IKJIOPL, *
*                   IEFZB4D2, DCBD, IKJEFFGF, IKJEFFDF, PUTLINE       *
*                                                                     *
*             CHANGE ACTIVITY = NONE                                  *
*                                                                     *
*             MESSAGES =                                              *
*                                                                     *
*                BOTH THE DARIFAIL AND GNRLFAIL SERVICE ROUTINES ARE  *
*                USED TO ISSUE MESSAGES.                              *
*                THOSE ISSUED BY THE COMMAND ARE FOUND FOLLOWING THE  *
*                LABEL GNRLERR AND ARE ISSUED USING PUTLINE.          *
*                                                                     *
*             ABEND CODES = NONE                                      *
*                                                                     *
*$DOC$*****************************************************************
*
PRINTOFF CSECT
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         SAVE  (14,12),,HARDCOPY.IPO.&SYSTIME_&SYSDATE SAVE REGISTERS
         LR    R12,R15                 LOAD REGISTER 12 WITH ENTRY
*                                      ADDRESS
         USING PRINTOFF,R12            ESTABLISH ADDRESSABILITY TO
*                                      PROGRAM CSECT
         LA    R11,4095(R12)           LOAD REGISTER 11 WITH ENTRY
*                                      ADDRESS 4095
         USING PRINTOFF+4095,R11       ESTABLISH ADDRESSABILITY TO THE
*                                      REST OF THE PROGRAM CSECT
         LR    R8,R1                   LOAD REGISTER 8 TO PRESERVE
*                                      POINTER TO COMMAND PROCESSOR
*                                      PARAMETER LIST
         GETMAIN R,LV=LDYNAMIC         GETMAIN DYNAMIC WORKAREA
         LR    R10,R1                  LOAD REGISTER 10 TO PRESERVE
*                                      POINTER TO WORKAREA
         ST    R13,4(R10)              STORE ADDRESS OF PREVIOUS
*                                      SAVEAREA
         ST    R10,8(,R13)             STORE ADDRESS OF CURRENT
*                                      SAVEAREA IN PREVIOUS SAVEAREA
         LR    R13,R10                 LOAD REGISTER 13 WITH ADDRESS OF
*                                      CURRENT SAVEAREA
         USING WORKAREA,R13            ESTABLISH ADDRESSABILITY TO
*                                      DYNAMIC WORKAREA
*
***********************************************************************
*                                                                     *
*        BUILD PARSE AND DAIR PARAMETER LISTS USING COMMAND           *
*           PROCESSOR PARAMETER LIST                                  *
*        INITIALIZE OUTPUT DATA CONTROL BLOCKS IN WORKAREA            *
*                                                                     *
***********************************************************************
*
         USING CPPL,R8                 ESTABLISH ADDRESSABILITY TO CPPL
         LA    R4,PPLSECT              LOAD ADDRESS OF PARSE PARAMETER
*                                      LIST
         USING PPL,R4                  ESTABLISH ADDRESSABILITY TO PPL
         LA    R6,DAPLSECT             LOAD ADDRESS OF DAIR PARAMETER
*                                      LIST
         USING DAPL,R6                 ESTABLISH ADDRESSABILITY TO DAPL
         LA    R5,IOPLSECT             LOAD ADDRESS OF PUTLINE
*                                      PARAMETER LIST
         USING IOPL,R5                 ESTABLISH ADDRESSABILITY TO IOPL
         L     R1,CPPLUPT              LOAD POINTER TO USER PROFILE
*                                      TABLE FROM CPPL
         L     R2,CPPLECT              LOAD POINTER TO ENVIRONMENT
*                                      CONTROL TABLE FROM CPPL
         SLR   R3,R3                   ZERO REGISTER 3
         ST    R3,ECB                  STORE REGISTER AS ECB FOR PPL
         LA    R3,ECB                  LOAD ADDRESS OF ECB
         STM   R1,R3,DFPLSECT          STORE UPT,ECT,ECB IN DFPL  .GTE.
         STM   R1,R3,PPLSECT           STORE ADDRESS OF UPT, ECT, AND
*                                      ECB IN PPL
         STM   R1,R3,DAPLSECT          STORE ADDRESS OF UPT, ECT, AND
*                                      ECB IN DAPL
         STM   R1,R3,IOPLSECT          STORE ADDRESS OF UPT, ECT, AND
*                                      ECB IN IOPL
         LA    R3,IOPB                 LOAD ADDRESS OF PUTLINE
*                                      PARAMETER BLOCK
         ST    R3,IOPLIOPB             STORE IOPB ADDRESS IN IOPL
         LA    R3,DFPBSECT             GET ADDRESS OF DFPB        .GTE.
         ST    R3,DFPLSECT+12          PUT IT IN THE DFPL         .GTE.
         USING  DFPB,R3                                           .GTE.
         LA     R2,DFPBDSL                                        .GTE.
         ST     R2,DFPBDSN                                        .GTE.
         LA     R2,DFPBQUA                                        .GTE.
         ST     R2,DFPBQUAL                                       .GTE.
         MVI    DFPBCODE,DFPB04                                   .GTE.
         MVC    DFPBPSCB,CPPLPSCB                                 .GTE.
         MVI    DFPBCNTL,DFPBRET                                  .GTE.
         XC     DFPBCAT(8),DFPBCAT                                .GTE.
         DROP    R3                                               .GTE.
         MVC   PPLPCL,PCLADDR          MOVE POINTER TO PARAMETER
*                                      CONTROL LIST INTO PPL
         LA    R2,PARSBACK             LOAD ADDRESS OF FULLWORD TO
*                                      CONTAIN ADDRESS OF PDE RETURNED
*                                      BY PARS
         ST    R2,PPLANS               STORE POINTER TO RETURN ADDRESS
         MVC   PPLCBUF,CPPLCBUF        MOVE POINTER TO COMMAND BUFFER
*                                      INTO PPL
         LA    R2,PPLUWA               LOAD ADDRESS OF USER WORK AREA
         ST    R2,PPLUWA               STORE ADDRESS IN PPL
         MVC   DAPLPSCB,CPPLPSCB       MOVE POINTER TO PSCB INTO DAPL
         DROP  R4,R5,R6,R8             DROP ADDRESSABILITY TO CPPL,
*                                      IOPL, DAPL, AND PPL
         MVC   OUTPUTA(OUTPUTAL),OUTPUTAC  MOVE ASA DCB CONSTANT INTO
*                                      ASA DCB IN WORKAREA
         MVC   OUTPUTM(OUTPUTML),OUTPUTMC  MOVE MACHINE DCB CONSTANT
*                                      INTO MACHINE DCB IN WORKAREA
*
***********************************************************************
*                                                                     *
*        CALLTSSR TO PARSE (ENTRY NAME IKJPARS)                       *
*        ON RETURN PARSBACK -> PDL                                    *
*        HANDLE ALL ERROR CODES RETURNED BY PARS                      *
*        IF NECESSARY CALL GENERAL FAIL SERVICE ROUTINE               *
*                                                                     *
***********************************************************************
*
         CALLTSSR  EP=IKJPARS,MF=(E,PPLSECT)
*                                      CALL PARS TO PARSE COMMAND
         LTR   R15,R15                 SEE IF RETURN CODE IS ZERO
         BZ    GOODPARS                YES....GO TO GOOD PARS ROUTINE
         C     R15,FULL4               SEE IF RETURN CODE IS FOUR
         BE    RETURN                  YES....PARS ISSUED ERROR
*                                      MESSAGE.  GO CLEANUP AND RETURN
         C     R15,FULL20              SEE IF RETURN CODE IS TWENTY
         BE    RETURN                  YES....VALIDITY CHECKING
*                                      ROUTINE ISSUED ERROR MESSAGE.
*                                      GO CLEANUP AND RETURN
         XC    GFPARMS(GFLENGF),GFPARMS  INITIALIZE PARMLIST FOR
*                                      GENERAL FAIL TO ZEROS
         LA    R1,GFPARMS              LOAD ADDRESS OF PARMLIST
         ST    R1,GFPARMP              STORE ADDRESS AS POINTER TO
*                                      PARMLIST
         ST    R15,GFRCODE             STORE RETURN CODE IN PARMLIST
         LA    R1,GFPARSE              LOAD CALLER ID FOR PARS
         STH   R1,GFCALLID             STORE CALLER ID IN PARMLIST
         L     R1,SAVEAREA+4           LOAD ADDRESS OF PEVIOUS
*                                      SAVEAREA
         L     R1,24(R1)               LOAD POINTER TO CPPL
         ST    R1,GFCPPLP              STORE POINTER TO CPPL IN
*                                      PARMLIST
         LA    R1,ECB                  LOAD ADDRESS OF OPTIONAL ECB
         ST    R1,GFECBP               STORE POINTER TO ECB IN
*                                      PARMLIST
         SLR   R1,R1                   ZERO REGISTER 1
         ST    R1,ECB                  SET OPTIONAL ECB TO ZERO
         LINK  EP=IKJEFF19,MF=(E,GFPARMP)  LINK TO GENERAL FAIL SERVICE
*                                      ROUTINE TO HANDLE RETURN CODE
         LTR   R15,R15                 SEE IF RETURN CODE IS ZERO
         BZ    RETURN                  YES....GO CLEANUP AND RETURN
         LR    R8,R15                  LOAD REGISTER 8 WITH RETURN
*                                      CODE
         BAL   R14,SETCODE             GO CONVERT CODE TO DECIMAL
         PUTLINE OUTPUT=(GNRLERR,SINGLE,DATA),MF=(E,IOPLSECT)  PUT OUT
*                                      GENERAL FAIL ERROR MESSAGE
         PUTLINE OUTPUT=(RETNCD,SINGLE,DATA),MF=(E,IOPLSECT)  PUT OUT
*                                      DECIMAL RETURN CODE
         B     RETURN                  GO CLEANUP AND RETURN
*
***********************************************************************
*                                                                     *
*        SET UP ADDRESSABILITY TO PDL                                 *
*           AND LOAD REGISTER 6 WITH ADDRESS OF DSNAME PDE            *
*                                                                     *
***********************************************************************
*
GOODPARS DS    0H
         L     R9,PARSBACK             LOAD POINTER TO PARS ANSWER
         USING IKJPARMD,R9             ESTABLISH ADDRESSABILITY TO PDL
         MVC   PRCKW+0(1),ASAKW+1      SAVE 'ASA'                 .PRC.
         MVC   PRCKW+1(1),SNUMKW+1     SAVE 'SNUM'                .PRC.
         MVC   PRCKW+2(1),MSGKW+1      SAVE 'NOMSG'               .PRC.
         LA    R6,DSNAMES              LOAD POINTER TO DSNAME LIST
         MVI   VOLBIT,BLANK            BLANK OUT THE VOLUME WORK AREA
         MVC   VOLUME,VOLBIT           CONTINUE BLANKING THE VOLUME
         CLC   PVOL(2),HALF1           SEE IF VOLUME SPECIFIED
         BNE   CHKFOLD                 NO.....GO SEE IF FOLD IS DESIRED
         L     R7,SVOL                 LOAD POINTER TO VOLUME
         LH    R8,SVOL+4               LOAD LENGTH OF VOLUME
         BCTR  R8,0                    DECREMENT LENGTH FOR EXECUTE OF
*                                      MOVE
         EX    R8,MOVEVOL              MOVE VOLUME INTO WORK AREA
         OI    VOLBIT,HIGH             INDICATE VOLUME PRESENT
CHKFOLD  DS    0H
         CLC   PFOLD(2),HALF1          SEE IF FOLD SPECIFIED
         BNE   CHKHEAD                 NO.....GO LOOP THROUGH DSNAMES
         OI    VOLBIT,LOW              INDICATE FOLD PRESENT
CHKHEAD  DS    0H
         CLC   PHEAD(2),HALF1          SEE IF HEAD SPECIFIED
         BNE   NEXT                    NO.....GO LOOP THROUGH DSNAMES
         OI    VOLBIT,X'08'              INDICATE HEAD PRESENT
         DROP  R9                      DROP ADDRESSABILITY TO PDL
*
***********************************************************************
*                                                                     *
*        LOOP THROUGH DSNAME LIST RETURNED BY PARS                    *
*                                                                     *
***********************************************************************
*
NEXT     DS    0H
         LA    R1,PRINTI               LOAD POINTER TO DDNAME TO BE
*                                      FREED
         BAL   R2,FREEDD               BRANCH TO FREE ROUTINE
         LTR   R8,R8                   SEE IF RETURN CODE FROM FREE IS
*                                      ZERO
         BNZ   CLOSDCBS                NO.....GO CLOSE DCBS AND RETURN
         L     R5,0(R6)                LOAD POINTER TO DSNAME
         LH    R4,4(R6)                LOAD LENGTH OF DSNAME
         STH   R4,DFPBDSL                                         .GTE.
         TM    6(R6),X'40'             IS DSNAME IN QUOTES        .GTE.
         BO    GTENOQ                  YES, BYPASS DEFAULT        .GTE.
         BCTR  R4,0                    LENGTH MINUS 1 FOR EX      .GTE.
         MVC   DFPBNAME,BLANKS         PAD WITH BLANKS            .GTE.
         EX    R4,DFPBMVE              COPY DSNAME                .GTE.
         LA    R1,DFPLSECT                                        .GTE.
         MVC   DFPBQUA,=CL8'   '                                  .GTE.
         CALLTSSR  EP=IKJEHDEF                                    .GTE.
GTEDEF   LTR    R15,R15                                           .GTE.
GTEOK    LA     R5,DFPBNAME                                       .GTE.
         LH     R4,DFPBDSL                                        .GTE.
GTENOQ   DS    0H                                                 .GTE.
         STCM  R4,3,DSNLEN             STORE LENGTH OF DSNAME
         BCTR  R4,0                    DECREMENT LENGTH FOR EXECUTE OF
*                                      MOVE
         LA    R8,DAP08                LOAD ADDRESS OF DAPB FOR 08
         USING DAPB08,R8               ESTABLISH ADDRESSABILITY TO DAPB
         MVC   DAPB08(DAPB08L),DAPB08C MOVE CONSTANTS INTO DAPB
         LA    R1,DSNLEN               LOAD POINTER TO DSN BUFFER
         ST    R1,DA08PDSN             STORE POINTER TO DSN BUFFER IN
*                                      DAPB
         MVI   DSNBUF,BLANK            BLANK OUT THE DSNAME BUFFER
         MVC   DSNBUF+1(43),DSNBUF     FINISH BLANKING OUT THE BUFFER
         EX    R4,MOVEDSN              MOVE IN THE DSNAME
         TM    14(R6),HIGH             SEE IF MEMBER NAME IS PRESENT
         BNO   CHKPASS                 NO.....GO CHECK IF THERE IS A
*                                      PASSWORD
         L     R5,8(R6)                LOAD POINTER TO THE MEMBER NAME
         LH    R4,12(R6)               LOAD LENGTH OF MEMBER NAME
         BCTR  R4,0                    DECREMENT LENGTH FOR EXECUTE OF
*                                      MOVE
         EX    R4,MOVEMEM              MOVE IN THE MEMBER NAME
CHKPASS  DS    0H
         MVC   MEMNAME,DA08MNM         MOVE MEMBER NAME INTO HEADER 2
         TM    22(R6),HIGH             SEE IF PASSWORD IS PRESENT
         BNO   CHKVOL                  NO.....GO SEE IF VOLUME PRESENT
         L     R5,16(R6)               LOAD POINTER TO THE PASSWORD
         LH    R4,20(R6)               LOAD LENGTH OF PASSWORD
         BCTR  R4,0                    DECREMENT LENGTH FOR EXECUTE OF
*                                      MOVE
         EX    R4,MOVEPASS             MOVE IN THE PASSWORD
CHKVOL   DS    0H
         TM    VOLBIT,HIGH             SEE IF VOLUME IS PRESENT
         BNO   ALLOCDD                 NO.....GO ALLOCATE DATA SET
         MVC   DA08SER,VOLUME          MOVE IN THE VOLUME SERIAL
ALLOCDD  DS    0H
         SLR   R4,R4                   ZERO REGISTER 4
         ST    R4,ECB                  STORE REGISTER 4 AS ECB
         LA    R1,DAPLSECT             LOAD ADDRESS OF DAPL
         USING DAPL,R1                 ESTABLISH ADDRESSABILITY TO DAPL
         ST    R8,DAPLDAPB             STORE ADDRESS OF DAPB IN DAPL
         DROP  R1                      DROP ADDRESSABILITY TO DAPL
         CALLTSSR EP=IKJDAIR           CALL DAIR TO ALLOCATE DATASET
         LTR   R15,R15                 SEE IF RETURN CODE IS ZERO
         BZ    OUTPUT                  YES....GO PRINT DATASET
         MVI   DFID+1,DFDAIR           SET DAIRFAIL ID TO INDICATE DAIR
         BAL   R2,DAIRFAIL             GO TO DAIRFAIL ROUTINE TO HAVE
*                                      ERROR MESSAGE PUT OUT
NOTPMESS DS    0H
         PUTLINE OUTPUT=(DSNNOTP,SINGLE,DATA),MF=(E,IOPLSECT)  PUT OUT
*                                      DATASET NOT PRINTED MESSAGE
         B     EXITMSG                 GO PUT OUT DATASET AND MEMBER
*                                      NAMES
*
***********************************************************************
*                                                                     *
*        DETERMINE DATASET TYPE AND CALL PRINT ROUTINE                *
*                                                                     *
***********************************************************************
*
OUTPUT   DS    0H
         TM    DA08DSO,DSOPS           X'40' PHYSICAL SEQUENTIAL
         BO    PRINTIT                 YES....GO PRINT DATASET
         CLC   DA08MNM,BLANKS          SEE IF MEMBER NAME IS BLANK
         BNE   PRINTIT                 NO.....GO TREAT AS SEQUENTIAL
         TM    DA08DSO,DSOPO           X'02' PARTITIONED
         BO    SCROLLIT                YES....GO SCROLL DIRECTORY
         DROP  R8                      DROP ADDRESSABILITY TO DAPB
         PUTLINE OUTPUT=(NOTPSPO,SINGLE,DATA),MF=(E,IOPLSECT)  PUT OUT
*                                      NOT PARTITIONED OR SEQUENTIAL
*                                      MESSAGE
         B     NOTPMESS                GO PUT NOT PRINTED MESSAGE
*
***********************************************************************
*                                                                     *
*        READS RECORDS USING GET MOVE                                 *
*           AND ATTRIBUTES OF GIVEN DATASET                           *
*        WRITES THESE SAME RECORDS USING PUT MOVE                     *
*           AND THE INPUT DATASET ATTRIBUTES                          *
*                                                                     *
***********************************************************************
*
PRINTIT  DS    0H
         LA    R8,INPUT                LOAD POINTER TO INPUT DCB
         USING IHADCB,R8               ESTABLISH ADDRESSABILITY TO DCB
         MVC   INPUT(INPUTL),INPUTC    MOVE INPUT DCB CONSTANT INTO
*                                      INPUT DCB IN WORKAREA
         MVC   OPENLST,OPENCLOS        MOVE OPEN(CLOSE) LIST CONSTANT
*                                      INTO OPEN LIST IN WORKAREA
         OPEN  ((R8),INPUT),MF=(E,OPENLST)  OPEN INPUT DCB
PRINTS   DS    0H                      BRANCH ADDRESS FOR PRINTING
*                                      MEMBERS
         TM    DCBOFLGS,DCBOFOPN       SEE IF DCB OPENED SUCCESSFULLY
         BNO   NOTPMESS                NO.....GO PUT OUT NOT PRINTED
*                                      MESSAGE
         TM    DCBRECFM,DCBRECU        SEE IF RECFM IS UNDEFINED
         BO    ERRORU                  NO.....GO PUT OUT ERROR MESSAGE
         BZ    ERRORU                  ZERO...NOT FIXED OR VARIABLE SO
*                                      GO PUT OUT ERROR MESSAGE
         CLC   DCBLRECL,HALF256        SEE IF LRECL IS GREATER THAN 256
         BH    ERRORL                  YES....GO PUT OUT RECORD LENGTH
*                                      MESSAGE
         LA    R9,LINE                 LOAD ADDRESS FOR INPUT
         SLR   R4,R4                   ZERO OUT REGISTER 4
         ST    R4,LENGTH               STORE ZERO IN LENGTH (PLUS 2
*                                      RESERVED BYTES) FIELD
         TM    DCBRECFM,DCBRECV        SEE IF RECFM IS VARIABLE
         BO    CHKCNTRL                YES....GO PUT OUT DSNAME
*                                      NO.....RECFM MUST BE FIXED
*                                      SO CONTINUE
         A     R9,FULL4                SET TO HAVE FIXED LENGTH
         LH    R4,DCBLRECL             LOAD LOGICAL RECORD LENGTH
         A     R4,FULL4                ADD 4 FOR LENGTH FIELD PREFIX
         CLI   PRCKW+1,1               'SNUM' FOR FIXED LENGTH    .PRC.
         BNE   *+10                    NO, SKIP NEXT 2 INSTR      .PRC.
         LA    R0,8                    LENGTH OF SEQUENCE NUMBERS .PRC.
         SR    R4,R0                   REDUCE LENGTH BY 8         .PRC.
         STH   R4,LENGTH               STORE IN LENGTH FIELD
CHKCNTRL DS    0H
         MVC   HEADER,HEADERC          MOVE HEADER INTO DATA AREA
         TM    DCBRECFM,DCBRECCM       SEE IF CONTROL IS MACHINE
         BNO   USEA                    NO.....GO TO USE ASA CONTROL
         LA    R5,OUTPUTM              LOAD ADDRESS OF OUTPUT DCB FOR
*                                      MACHINE CODE
         LA    R4,EJECTM               LOAD ADDRESS OF EJECT DATA
         MVI   HDRCNTL,SKIPM           MOVE INTO HEADER MACHINE CODE
*                                      FOR SPACE
         TM    48(R5),DCBOFOPN         SEE IF DCB IS ALREADY OPEN
         BO    PUTDSN                  YES....GO PUT OUT HEADERS
         LA    R3,PRINTM               LOAD POINTER TO PRINTM
         LA    R14,PUTDSN              LOAD ADDRESS FROM RETURN FROM
*                                      SYSOUT ALLOCATION
         B     ALLOCOUT                GO TO ALLOCATE SYSOUT
USEA     DS    0H
         TM    DCBRECFM,DCBRECCA       SEE IF CONTROL IS ASA
         BO    GOODLENA                YES....GO SEE IF DCB IS OPEN
         CLI   PRCKW+0,1               'ASA'                      .PRC.
         BE    GOODLENA                YES, BRANCH                .PRC.
         A     R9,FULL1                SET GET AREA TO ALLOW FOR NO
*                                      CONTROL
         A     R4,FULL1                ADD ONE FOR A CONTROL BYTE
         STH   R4,LENGTH               STORE LENGTH IN PUT AREA
GOODLENA DS    0H
         LA    R5,OUTPUTA              LOAD ADDRESS OF OUTPUT DCB FOR
*                                      ASA CODE
         LA    R4,EJECTA               LOAD ADDRESS OF EJECT DATA
         TM    48(R5),DCBOFOPN         SEE IF DCB IS ALREADY OPEN
         BO    PUTDSN                  YES....GO PUT OUT HEADERS
         LA    R3,PRINTA               LOAD POINTER TO PRINTA
         BAL   R14,ALLOCOUT            LOAD ADDRESS FOR RETURN AND GO
*                                      DO SYSOUT ALLOCATION
PUTDSN   DS    0H
         PUT   (R5),(R4)               PUT OUT PAGE EJECT
         TM    VOLBIT,X'08'            TEST FOR NOHEAD            .GTE.
         BO    GTEHEAD                 NOHEAD                     .GTE.
         PUT   (R5),HEADER             PUT OUT PAGE 1 HEADER
         MVC   HEADER2,LDSNAME         MOVE LENGTH AND 'DSNAME=' INTO
*                                      INTO SECOND HEADER
         MVC   HEADER2+4(1),HDRCNTL    MOVE IN CONTROL BYTE
         CLI   MEMNAME,BLANK           SEE IF MEMBER NAME IS BLANK
         BE    NOMEM                   YES....NO NEED TO PUT IN PARENS
         MVI   OPENPRN,C'('            MOVE OPEN PAREN INTO DSNAME
         MVI   CLOSEPRN,C')'           MOVE CLOSE PAREN INTO DSNAME
         B     PUTHDR2                 GO PUT OUT HEADER 2
NOMEM    DS    0H
         MVI   OPENPRN,BLANK           MOVE BLANK INSTEAD OF OPEN AND
         MVI   CLOSEPRN,BLANK          CLOSE PARENS
PUTHDR2  DS    0H
         PUT   (R5),HEADER2            PUT OUT PAGE 1 HEADER 2
         MVC   HEADER2(4),SPACE        CHANGE HEADER 2 FOR PUT OF BLANK
*                                      LINE
         PUT   (R5),HEADER2            PUT OUT BLANK LINE
GTEHEAD  EQU   * .GTE.
         MVC   CNTLBYTE,HDRCNTL        MOVE IN SPACE AS CONTROL
*                                      CHARACTER
         TM    VOLBIT,LOW              SEE IF FOLD IS REQUESTED
         BO    FOLDIT                  YES....GO FOLD OUTPUT
         TM    DCBRECFM,DCBRECV        SEE IF RECFM IS VARIABLE
         BNO   COPYF                   NO.....GO HANDLE FIXED INPUT
         CLI   PRCKW+1,1               'SNUM'                     .PRC.
         BE    COPYS                   YES, BRANCH                .PRC.
         CLI   PRCKW+0,1               'ASA'                      .PRC.
         BE    COPYV                   YES, BRANCH                .PRC.
         TM    DCBRECFM,DCBRECCC       SEE IF THERE IS A CONTROL BYTE
         BZ    COPYVNO                 NO.....GO TO ADD CONTROL FOR
*                                      VARIABLE
COPYV    DS    0H
         GET   INPUT,(R9)              GET INPUT RECORD
         PUT   (R5),LINE               PUT OUT OUTPUT LINE
         B     COPYV                   GO GET NEXT RECORD
*
COPYVNO  DS    0H
         GET   INPUT,(R9)              GET INPUT RECORD INTO LINE+1
         SLR   R15,R15                 ZERO REGISTER 15 FOR INSERT
         ICM   R15,7,LENGTH+1          INSERT LENGTH AND RESERVED BYTE
         A     R15,FULL256             ADD 1 TO LENGTH
         STCM  R15,7,LENGTH            STORE LENGTH AND RESERVED BYTE
         MVC   CNTLBYTE,HDRCNTL        MOVE IN CONTROL SAVED IN HEADER
         PUT   (R5),LINE               PUT OUT LINE
         B     COPYVNO                 GO GET NEXT INPUT RECORD
*
COPYS    CLI   PRCKW+0,1               'ASA' RECFM V              .PRC.
         BE    COPYVS                  YES, BRANCH                .PRC.
         TM    DCBRECFM,DCBRECCC       SEE IF THERE IS A CONTROL  .PRC.
         BZ    COPYVNOS                NO.....GO TO ADD CONTROL   .PRC.
*                                                                 .PRC.
COPYVS   DS    0H                                                 .PRC.
         GET   INPUT,(R9)              GET INPUT RECORD           .PRC.
         MVC   8(4,R9),0(R9)           SHIFT PREFIX RIGHT 8       .PRC.
         LH    R15,8(,R7)              GET LENGTH                 .PRC.
         LA    R0,8                    LENGTH OF SEQUENCE NUM     .PRC.
         SLR   R15,R0                  REDUCE LENGTH BY 8         .PRC.
         STH   R15,8(,R9)              STORE NEW LENGTH           .PRC.
         PUT   (R5),LINE+8             PUT OUT OUTPUT LINE        .PRC.
         B     COPYVS                  GO GET NEXT RECORD         .PRC.
*                                                                 .PRC.
COPYVNOS DS    0H                                                 .PRC.
         GET   INPUT,(R9)              GET INPUT RECORD , LINE+1  .PRC.
         MVC   7(4,R9),0(R9)           LENGTH TO LINE+8           .PRC.
         LH    R15,7(,R9)              GET LENGTH FROM LINE+8     .PRC.
         LA    R0,7                    LENGTH OF SEQ NUM MINUS 1  .PRC.
         SLR   R15,R0                  REDUCE LENGTH              .PRC.
         STH   R15,7(,R9)              STORE NEW LENGTH           .PRC.
         MVC   CNTLBYTE+8,HDRCNTL      MOVE IN CONTROL            .PRC.
         PUT   (R5),LINE+8             PUT OUT LINE               .PRC.
         B     COPYVNOS                GO GET NEXT INPUT RECORD   .PRC.
*                                                                 .PRC.
COPYF    DS    0H
         GET   INPUT,(R9)              GET INPUT RECORD
         PUT   (R5),LINE               PUT OUT OUTPUT LINE
         B     COPYF                   GO GET NEXT RECORD
*
FOLDIT   DS    0H
         TM    DCBRECFM,DCBRECV        SEE IF RECFM IS VARIABLE
         BNO   FOLDF                   NO.....GO HANDLE FIXED INPUT
         CLI   PRCKW+1,1               'SNUM'                     .PRC.
         BE    FOLDS                   YES, BRANCH                .PRC.
         CLI   PRCKW+0,1               'ASA'                      .PRC.
         BE    FOLDV                   YES, BRANCH                .PRC.
         TM    DCBRECFM,DCBRECCC       SEE IF THERE IS A CONTROL BYTE
         BZ    FOLDVNO                 NO.....GO TO ADD CONTROL FOR
*                                      VARIABLE
FOLDV    DS    0H
         GET   INPUT,(R9)              GET INPUT RECORD
         LH    R15,LENGTH              LOAD LENGTH OF LINE
         SH    R15,HALF6               DECREMENT FOR EXECUTE
         EX    R15,FOLDLINE            EXECUTE FOLD OF LINE
         PUT   (R5),LINE               PUT OUT OUTPUT LINE
         B     FOLDV                   GO GET NEXT RECORD
*
FOLDVNO  DS    0H
         GET   INPUT,(R9)              GET INPUT RECORD INTO LINE+1
         SLR   R15,R15                 ZERO REGISTER 15 FOR INSERT
         ICM   R15,7,LENGTH+1          INSERT LENGTH AND RESERVED BYTE
         A     R15,FULL256             ADD 1 TO LENGTH
         STCM  R15,7,LENGTH            STORE LENGTH AND RESERVED BYTE
         MVC   CNTLBYTE,HDRCNTL        MOVE IN CONTROL SAVED IN HEADER
         LH    R15,LENGTH              LOAD LENGTH OF LINE
         SH    R15,HALF6               DECREMENT FOR EXECUTE
         EX    R15,FOLDLINE            EXECUTE FOLD OF LINE
         PUT   (R5),LINE               PUT OUT LINE
         B     FOLDVNO                 GO GET NEXT INPUT RECORD
*
FOLDS    CLI   PRCKW+0,1               'ASA' RECFM V              .PRC.
         BE    FOLDVS                  YES, BRANCH                .PRC.
         TM    DCBRECFM,DCBRECCC       SEE IF THERE IS A CONTROL  .PRC.
         BZ    FOLDVNOS                NO.....GO TO ADD CONTROL   .PRC.
*                                                                 .PRC.
FOLDVS   DS    0H                                                 .PRC.
         GET   INPUT,(R9)              GET INPUT RECORD           .PRC.
         MVC   8(4,R9),0(R9)           SHIFT PREFIX RIGHT 8       .PRC.
         LH    R15,8(,R7)              GET LENGTH                 .PRC.
         LA    R0,8                    LENGTH OF SEQUENCE NUM     .PRC.
         SLR   R15,R0                  REDUCE LENGTH BY 8         .PRC.
         STH   R15,8(,R9)              STORE NEW LENGTH           .PRC.
         SH    R15,HALF6               DECREMENT FOR EXECUTE      .PRC.
         EX    R15,FOLDLINS            EXECUTE FOLD OF LINE       .PRC.
         PUT   (R5),LINE+8             PUT OUT OUTPUT LINE        .PRC.
         B     FOLDVS                  GO GET NEXT RECORD         .PRC.
*                                                                 .PRC.
FOLDVNOS DS    0H                                                 .PRC.
         GET   INPUT,(R9)              GET INPUT RECORD , LINE+1  .PRC.
         MVC   7(4,R9),0(R9)           LENGTH TO LINE+8           .PRC.
         LH    R15,7(,R9)              GET LENGTH FROM LINE+8     .PRC.
         LA    R0,7                    LENGTH OF SEQ NUM MINUS 1  .PRC.
         SLR   R15,R0                  REDUCE LENGTH              .PRC.
         STH   R15,7(,R9)              STORE NEW LENGTH           .PRC.
         SH    R15,HALF6               DECREMENT FOR EXECUTE      .PRC.
         EX    R15,FOLDLINS            EXECUTE FOLD OF LINE       .PRC.
         MVC   CNTLBYTE+8,HDRCNTL      MOVE IN CONTROL            .PRC.
         PUT   (R5),LINE+8             PUT OUT LINE               .PRC.
         B     FOLDVNOS                GO GET NEXT INPUT RECORD   .PRC.
*                                                                 .PRC.
FOLDF    DS    0H
         LH    R4,LENGTH               LOAD LENGTH INTO REGISTER 4
         SH    R4,HALF6                SUBTRACT TO SET UP FOR EXECUTE
FOLDFLP  DS    0H
         GET   INPUT,(R9)              GET INPUT RECORD
         EX    R4,FOLDLINE             FOLD DATA
         PUT   (R5),LINE               PUT OUT OUTPUT LINE
         B     FOLDFLP                 GO GET NEXT RECORD
FOLDLINE OC    DATA(0),BLANKS          OC INSTRUCTION TO BE EXECUTED
FOLDLINS OC    DATA+8(0),BLANKS        OC INSTRUCTION TO BE EX    .PRC.
*
EXIT     DS    0H
         TM    DCBOFLGS,DCBOFOPN       SEE IF INPUT DCB IS OPEN
         BNO   EXITMSG                 NO....GO PUT OUT DATASET AND
*                                      MEMBER NAMES
         MVC   CLOSLST,OPENCLOS        MOVE OPEN(CLOSE) LIST CONSTANT
*                                      INTO CLOSE LIST IN WORKAREA
         CLOSE ((R8),),MF=(E,CLOSLST)  CLOSE INPUT DCB
EXITMSG  DS    0H
         CLI   PRCKW+2,1               'NOMSG'                    .PRC.
         BE    CONTINUE                BYPASS MESSAGES IF 'NOMSG' .PRC.
         PUTLINE OUTPUT=(DSNMSG,SINGLE,DATA),MF=(E,IOPLSECT)  PUT OUT
*                                      DATA SET NAME MESSAGE
*        LH    R4,4(,R6)               LOAD LENGTH OF DSNAME     *.GTE.
         LH    R4,DFPBDSL              LOAD LENGTH OF DSNAME      .GTE.
         A     R4,FULL4                INCREMENT LENGTH FOR PUTLINE
         SLL   R4,16                   SHIFT LENGTH TO HIGH ORDER BYTES
         STCM  R4,15,DSNLENP           STORE LENGTH OF PUTLINE
         PUTLINE OUTPUT=(DSNLENP,SINGLE,DATA),MF=(E,IOPLSECT)  PUT OUT
*                                      DATA SET NAME MESSAGE
         TM    14(R6),HIGH             SEE IF MEMBER NAME IS PRESENT
         BNO   CONTINUE                NO.....GO CHECK FOR ANOTHER
*                                      DATASET NAME
         PUTLINE OUTPUT=(MEMMSG,SINGLE,DATA),MF=(E,IOPLSECT)  PUT OUT
*                                      DATA SET NAME MESSAGE
         LH    R4,12(R6)               LOAD LENGTH OF MEMBER NAME
         A     R4,FULL4                INCREMENT LENGTH FOR PUTLINE
         SLL   R4,16                   SHIFT LENGTH TO HIGH ORDER BYTES
         STCM  R4,15,MEMNAMEL          STORE LENGTH OF PUTLINE
         PUTLINE OUTPUT=(MEMNAMEL,SINGLE,DATA),MF=(E,IOPLSECT)  PUT OUT
*                                      MEMBER NAME
         B     CONTINUE                GO CONTINUE WITH NEXT DATASET
         DROP  R8                      DROP ADDRESSABILITY TO INPUT DCB
ERRORL   DS    0H
         PUTLINE OUTPUT=(DSNISL,SINGLE,DATA),MF=(E,IOPLSECT)  PUT OUT
*                                      DATASET NOT PRINTED DUE TO
*                                      RECORD LENGTH TOO LONG MESSAGE
         B     EXIT                    GO SEE IF INPUT DCB IS OPEN
ERRORU   DS    0H
         PUTLINE OUTPUT=(DSNISU,SINGLE,DATA),MF=(E,IOPLSECT)  PUT OUT
*                                      DATASET NOT PRINTED DUE TO
*                                      UNDEFINED RECORD FORMAT MESSAGE
         B     EXIT                    GO SEE IF INPUT DCB IS OPEN
ERRI     DS    0H
         PUTLINE OUTPUT=(SYNADI,SINGLE,DATA),MF=(E,IOPLSECT)  PUT OUT
*                                      DATASET NOT PRINTED DUE TO SYNAD
*                                      ERROR ON INPUT DATASET MESSAGE
         B     EXIT                    GO SEE IF INPUT DCB IS OPEN
ERRO     DS    0H
         PUTLINE OUTPUT=(SYNADO,SINGLE,DATA),MF=(E,IOPLSECT)  PUT OUT
*                                      DATASET NOT PRINTED DUE TO
*                                      UNDEFINED RECORD FORMAT MESSAGE
         B     CLOSDCBS                GO CLOSE OPEN DCBS AND RETURN
*                                                                     *
***********************************************************************
*                                                                     *
*        READ THE DIRECTORY OF A PDS                                  *
*           THEN EITHER LIST MEMBER NAMES,                            *
*           PRINT MEMBERS,                                            *
*           OR LIST MEMBER NAMES AND PRINT MEMBERS                    *
*                                                                     *
***********************************************************************
*
SCROLLIT DS    0H
         ST    R6,SAVER6               SAVE REGISTER 6
         L     R9,PARSBACK             LOAD ADDRESS PARS ANSWER
         USING IKJPARMD,R9             ESTABLISH ADDRESSABILITY TO PDL
         CLC   PLIST,HALF2             SEE IF NOLIST SPECIFIED
         BL    CONTSCR                 LOW....CONTINUE WITH SCROLL
         CLC   PPRINT,HALF2            SEE IF NOPRINT SPECIFIED
         BE    CONTINUE                YES....SKIP SCROLL
         DROP  R9                      DROP ADDRESSABILITY TO PDL
CONTSCR  DS    0H
         LA    R8,DIRECT               LOAD POINTER TO DIRECTORY DCB
         USING IHADCB,R8               ESTABLISH ADDRESSABILITY TO DCB
         MVC   DIRECT(DIRECTL),DIRECTC MOVE DIRECTORY DCB CONSTANT INTO
*                                      DIRECTORY DCB IN WORKAREA
         MVC   OPENLST,OPENCLOS        MOVE OPEN(CLOSE) LIST CONSTANT
*                                      INTO OPEN LIST IN WORKAREA
         OPEN  ((R8),INPUT),MF=(E,OPENLST)  OPEN DIRECTORY DCB
         TM    DCBOFLGS,DCBOFOPN       SEE IF DCB OPENED SUCCESSFULLY
         BNO   NOTPMESS                NO.....GO PUT OUT NOT PRINTED
*                                      MESSAGE
         DROP  R8                      DROP ADDRESSABILITY TO DCB
*                                                                     *
***********************************************************************
*                                                                     *
*        GETMAIN MEMBER TABLE FOR STORAGE OF DIRECTORY NAMES, ETC.    *
*           INITIALIZE TABLE                                          *
*                                                                     *
***********************************************************************
*
         L     R0,MEMDATL              LOAD LENGTH OF GETMAIN FOR
*                                      MEMBER TABLE
         LR    R2,R0                   SAVE IN REGISTER 2
         GETMAIN R,LV=(0)              GETMAIN MEMBER TABLE STORAGE
         ST    R1,MEMTABLE             SAVE ADDRESS OF MEMBER TABLE
         ST    R1,PRESENT              ALSO SAVE ADDRESS AS TABLE
*                                      IN USE
         LA    R3,0(R2,R1)             LOAD ADDRESS OF END OF TABLE
         ST    R3,ENDTABLE             STORE ADDRESS OF TABLE END
         SLR   R4,R4                   ZERO REGISTER 4
         ST    R4,0(R1)                STORE AS FIRST WORD OF MEMBER
*                                      TABLE
         LA    R4,8(R1)                LOAD REGISTER 4 AS POINTER TO
*                                      THE FIRST ENTRY
         USING MEMDSECT,R4             ESTABLISH ADDRESSABILITY TO
*                                      MEMBER TABLE DSECT
         SLR   R5,R5                   INITIALIZE MEMBER COUNT TO ZERO
*                                                                     *
***********************************************************************
*                                                                     *
*        READ DIRECTORY AND BUILD MEMBER TABLE                        *
*                                                                     *
***********************************************************************
*
GETDIR   EQU   *
         GET   (R8),DATA               GET A DIRECTORY BLOCK
         CLC   DATA,HALF12             SEE IF BLOCK IS UNUSED
         BL    FREEBLK                 YES....GO HANDLE DIRECTORY END
         LA    R9,DATA+2               LOAD POINTER TO FIRST DIRECTORY
*                                      ENTRY
         SLR   R7,R7                   ZERO REGISTER 7 FOR INSERT
         ICM   R7,3,DATA               INSERT LENGTH USED OF THIS BLOCK
         LA    R7,DATA-1(R7)           LOAD LAST USED BYTE OF BLOCK
UNBLOCK  DS    0H
         MVC   MEMNTTR,0(R9)           PUT NAME AND TTR IN TABLE
         MVC   CFIELD,11(R9)           PUT C FIELD IN TABLE
         CLI   MEMBER,HEXFF            SEE IF LAST MEMBER
         BE    FREEBLK                 YES....GO HANDLE DIRECTORY END
         IC    R6,CFIELD               INSERT CFIELD INTO REGISTER 6
         SLL   R6,27                   SHIFT OUT HIGH ORDER BITS
         SRL   R6,26                   SHIFT RIGHT TO GET LENGTH OF
*                                      USER DATA
         LA    R6,12(R6)               ADD STANDARD ENTRY LENGTH
         LA    R4,MEMSECTN             LOAD PINTER TO NEXT TABLE ENTRY
         LA    R5,1(R5)                INCREMENT NUMBER OF MEMBERS IN
*                                      TABLE
         C     R4,ENDTABLE             SEE IF END OF TABLE
         BL    NEXTNTRY                LOW.....GO PROCESS NEXT ENTRY
         LR    R0,R2                   LOAD LENGTH OF TABLE FOR GETMAIN
         GETMAIN R,LV=(0)              GET NEXT TABLE
         L     R14,PRESENT             LOAD REGISTER 14 WITH ADDRESS OF
*                                      LAST TABLE
         ST    R1,0(R14)               CHAIN MEMBER TABLES TOGETHER
         ST    R1,PRESENT              ALSO SAVE ADDRESS AS TABLE
*                                      IN USE
         LA    R3,0(R2,R1)             LOAD ADDRESS OF TABLE END
         ST    R3,ENDTABLE             STORE ADDRESS OF TABLE END
         SLR   R4,R4                   ZERO REGISTER 4
         ST    R4,0(R1)                STORE AS FIRST WORD OF MEMBER
*                                      TABLE
         LA    R4,8(R1)                LOAD REGISTER 4 AS POINTER TO
*                                      THE FIRST ENTRY
         STH   R5,4(R14)               STORE MEMBER COUNT IN OLD TABLE
         SLR   R5,R5                   INITIALIZE MEMBER COUNT TO ZERO
NEXTNTRY DS    0H
         BXLE  R9,R6,UNBLOCK           INCREMENT REGISTER 9 WITH
*                                      REGISTER 6 AND BRANCH
*                                      LESS THAN OR EQUAL TO
*                                      REGISTER 7 TO UNBLOCK
         B     GETDIR                  NO.....GO GET NEXT DIRECTORY
*                                      BLOCK
         DROP  R4                      DROP ADDRESSABILITY TO TABLE
*
***********************************************************************
*                                                                     *
*        LIST MEMBERS IF:                                             *
*           LIST SPECIFIED OR                                         *
*           BOTH PRINT AND NOLIST WERE NOT SPECIFIED                  *
*                                                                     *
***********************************************************************
*
FREEBLK  DS    0H
         MVC   OPENLST,OPENCLOS        MOVE CONSTANT INTO CLOSE LIST
         CLOSE ((R8),),MF=(E,OPENLST)  CLOSE DIRECTORY DCB
         L     R2,PRESENT              LOAD POINTER TO LAST TABLE
         STH   R5,4(R2)                STORE COUNT OF MEMBERS
         L     R4,MEMTABLE             LOAD POINTER TO FIRST TABLE
         CLC   4(2,R4),HALF0           SEE IF THERE WERE ANY MEMBERS
         BE    ENDMEMB                 ZERO...GO FREE MEMBER TABLE
         LA    R8,INPUT                LOAD POINTER TO INPUT DCB
         USING IHADCB,R8               ESTABLISH ADDRESSABILITY TO DCB
         MVC   INPUT(INPUTL),INPUTC    MOVE INPUT DCB CONSTANT INTO
*                                      INPUT DCB IN WORKAREA
         MVC   OPENLST,OPENCLOS        MOVE OPEN(CLOSE) LIST CONSTANT
*                                      INTO OPEN LIST IN WORKAREA
         LA    R1,XLIST                LOAD ADDRESS OF EXIT LIST
         ST    R1,DCBEXLST             STORE ADDRESS OF EXIT LIST IN
*                                      DCB
         LA    R1,SEXIT                LOAD ADDRESS OF EODAD EXIT
         ST    R1,DCBEODAD             STORE ADDRESS EODAD IN DCB
         LA    R2,JFCBAREA             LOAD ADDRESS OF JFCBAREA
         ST    R2,XLIST                STORE ADDRESS IN EXIT LIST
         MVI   XLIST,X'87'             SET END OF LIST AND JFCB BYTE
         MVC   RDJL,OPENCLOS           MOVE OPEN(CLOSE) LIST CONSTANT
*                                      INTO READJFCB LIST IN
*                                      WORKAREA
         RDJFCB (INPUT,),MF=(E,RDJL)   READ JFCB INTO JFCBAREA
         MVC   JFCBELNM,8(R4)          MOVE MEMBER NAME INTO JFCB
         OI    JFCBIND1,JFCPDS         SET JFCB TO INDICATE PDS MEMBER
         MVI   JFCDSRG1,HEX00          SET JFCB TO REMOVE OTHER DSORG
*                                      INDICATORS
         OPEN  ((R8),INPUT),TYPE=J,MF=(E,OPENLST)  OPEN INPUT DCB
         L     R9,PARSBACK             LOAD ADDRESS PARS ANSWER
         USING IKJPARMD,R9             ESTABLISH ADDRESSABILITY TO PDL
         CLC   PLIST,HALF2             SEE IF NOLIST SPECIFIED
         BE    NOLIST                  YES....SKIP LISTING
         CLC   PLIST,HALF1             SEE IF LIST SPECIFIED
         BE    LISTIT                  YES....GO LISTIT
         CLC   PPRINT,HALF1            SEE IF PRINT SPECIFIED
         BE    NOLIST                  YES....SKIP LISTING
         DROP  R9                      DROP ADDRESSABILITY TO PDL
LISTIT   DS    0H
         MVC   HEADER,HEADERM          MOVE HEADER INTO DATA AREA
         TM    DCBRECFM,DCBRECCM       SEE IF CONTROL IS MACHINE
         DROP  R8                      DROP ADDRESSABILITY TO DCB
         BNO   SUSEA                   NO.....GO TO USE ASA CONTROL
         LA    R5,OUTPUTM              LOAD ADDRESS OF OUTPUT DCB FOR
*                                      MACHINE CODE
         LA    R4,EJECTM               LOAD ADDRESS OF EJECT DATA
         MVI   HDRCNTL,SKIPM           MOVE INTO HEADER MACHINE CODE
*                                      FOR SPACE
         TM    48(R5),DCBOFOPN         SEE IF DCB IS ALREADY OPEN
         BO    SPUTDSN                 YES....GO PUT OUT HEADERS
         LA    R3,PRINTM               LOAD POINTER TO PRINTM
         LA    R14,SPUTDSN             LOAD ADDRESS FOR RETURN FROM
*                                      SYSOUT ALLOCATION
         B     ALLOCOUT                GO TO ALLOCATE SYSOUT
SUSEA    DS    0H
         LA    R5,OUTPUTA              LOAD ADDRESS OF OUTPUT DCB FOR
*                                      ASA CODE
         LA    R4,EJECTA               LOAD ADDRESS OF EJECT DATA
         TM    48(R5),DCBOFOPN         SEE IF DCB IS ALREADY OPEN
         BO    SPUTDSN                 YES....GO PUT OUT HEADERS
         LA    R3,PRINTA               LOAD POINTER TO PRINTA
         BAL   R14,ALLOCOUT            LOAD ADDRESS FOR RETURN AND GO
*                                      DO SYSOUT ALLOCATION
SPUTDSN  DS    0H
         PUT   (R5),(R4)               PUT OUT PAGE EJECT
         PUT   (R5),HEADER             PUT OUT PAGE 1 HEADER
         MVC   HEADER2,LDSNAME         MOVE LENGTH AND 'DSNAME=' INTO
*                                      INTO SECOND HEADER
         MVC   HEADER2+4(1),HDRCNTL    MOVE IN CONTROL BYTE
         MVI   OPENPRN,BLANK           BLANK OUT MEMBER NAME
         MVC   MEMNAME(9),OPENPRN      FINISH BLANKING OUT MEMBER NAME
         PUT   (R5),HEADER2            PUT OUT PAGE 1 HEADER 2
         L     R2,MEMTABLE             LOAD ADDRESS OF FIRST TABLE
         MVC   LENGTH,HALF85           SET LENGTH TO 85 BYTES
         MVC   LENGTH+2,HALF0          ZERO RESERVED BYTES
         MVC   CNTLBYTE,HDRCNTL        MOVE IN HEADER CONTROL BYTE
*
***********************************************************************
*                                                                     *
*        LIST MEMBER NAMES                                            *
*                                                                     *
***********************************************************************
*
SCROLLER DS    0H
         LA    R10,50                  LOAD LINE COUNT
         ST    R2,PRESENT              SAVE ADDRESS OF CURRENT TABLE
         LH    R7,4(R2)                LOAD NUMBER OF MEMBERS IN TABLE
         LTR   R7,R7                   SEE IF THERE ARE ENTRIES
         BZ    NOLIST                  NO.....GO PRINT MEMBERS
         LA    R2,8(R2)                LOAD ADDRESS OF FIRST MEMBER
         USING MEMDSECT,R2             ESTABLISH ADDRESSABILITY TO
*                                      MEMBER TABLE ENTRY
LOOP1    DS    0H
         LA    R6,DATA                 LOAD ADDRESS OF OUTPUT BUFFER
         MVI   DATA,BLANK              BLANK OUT OUTPUT BUFFER
         MVC   DATA+1(79),DATA         FINISH BLANKING BUFFER
         TM    CFIELD,HIGH             SEE IF THIS IS AN ALIAS
         BZ    NOTALIAS                NO.....CONTINUE WITH MAIN NAME
         BAL   R14,LOOKLOOP            GO LOOK FOR MAIN NAME
NOTALIAS DS    0H
         MVC   2(8,R6),MEMBER          PUT MEMBER NAME IN OUTPUT BUFFER
         ST    R2,SAVER2               SAVE REGISTER 2
         LA    R3,2                    LOAD NUMBER OF COLUMNS LEFT
         LA    R8,1                    LOAD ENTRY NUMBER
LOOP2    LA    R8,50(R8)               LOAD POINTER TO NEXT COLUMN
*                                      ENTRY
         CR    R8,R7                   SEE IF VALID ENTRY
         BH    END2                    NO.....GO PRINT LINE
         LA    R2,600(R2)              LOAD POINTER TO MEMBER NAME
         LA    R6,30(R6)               LOAD POINTER TO NEXT OUTPUT
*                                      BUFFER LOCATION
         TM    CFIELD,HIGH             SEE IF THIS IS AN ALIAS
         BZ    NOTAL2                  NO.....CONTINUE WITH MAIN NAME
         BAL   R14,LOOKLOOP            GO LOOK FOR MAIN NAME
NOTAL2   DS    0H
         MVC   2(8,R6),MEMBER          PUT MEMBER NAME IN OUTPUT BUFFER
         BCT   R3,LOOP2                DECREMENT COLUMN COUNT
END2     DS    0H
         PUT   (R5),LINE               PUT OUT OUTPUT LINE
         L     R2,SAVER2               RELOAD REGISTER 2
         LA    R2,MEMSECTN             LOAD POINTER TO NEXT MEMBER NAME
         BCT   R10,MOREROW             DECREMENT ROW COUNT
         PUT   (R5),(R4)               PUT OUT PAGE EJECT WHEN ZERO
         B     LOOP3                   RESET COUNTERS
MOREROW  DS    0H
         BCT   R7,LOOP1                DECREMENT MEMBER COUNT FOR
*                                      CURRENT TABLE
LOOP3    L     R2,PRESENT              LOAD POINTER TO CURRENT TABLE
         L     R2,0(R2)                LOAD POINTER TO NEXT TABLE
         LTR   R2,R2                   SEE IF IT WAS THE LAST TABLE
         BNZ   SCROLLER                NO.....GO LIST OUT NEXT TABLE
*
***********************************************************************
*                                                                     *
*        PRINT MEMBERS IF:                                            *
*           PRINT SPECIFIED OR                                        *
*           BOTH LIST AND NOPRINT WERE NOT SPECIFIED                  *
*                                                                     *
***********************************************************************
*
NOLIST   DS    0H
         L     R9,PARSBACK             LOAD ADDRESS PARS ANSWER
         USING IKJPARMD,R9             ESTABLISH ADDRESSABILITY TO PDL
         CLC   PPRINT,HALF2            SEE IF NOPRINT SPECIFIED
         BE    ENDMEMB                 YES....GO SKIP PRINT
         CLC   PPRINT,HALF1            SEE IF PRINT SPECIFIED
         BE    CONTPRT                 YES....GO PRINT MEMBERS
         CLC   PLIST,HALF1             SEE IF LIST SPECIFIED
         BE    ENDMEMB                 YES....GO SKIP PRINT
         DROP  R9                      DROP ADDRESSABILITY TO PDL
CONTPRT  DS    0H
         L     R6,SAVER6               RESTORE REGISTER 6
         LA    R8,INPUT                LOAD ADDRESS OF INPUT DCB
         USING IHADCB,R8               ESTABLISH ADDRESSABILITY TO DCB
         TM    DCBRECFM,DCBRECU        SEE IF RECFM IS UNDEFINED
         BO    ERRORU                  NO.....GO PUT OUT ERROR MESSAGE
         BZ    ERRORU                  ZERO...NOT FIXED OR VARIABLE SO
*                                      GO PUT OUT ERROR MESSAGE
         MVC   CLOSLST,OPENCLOS        MOVE OPEN(CLOSE) LIST CONSTANT
*                                      INTO CLOSE LIST IN WORKAREA
         CLOSE ((R8),),MF=(E,CLOSLST)  CLOSE INPUT DCB
         DROP  R8                      DROP ADDRESSABILITY TO INPUT DCB
         CLI   PRCKW+2,1               'NOMSG'                    .PRC.
         BE    PRCNOM                  BYPASS MESSAGE IF 'NOMSG'  .PRC.
         PUTLINE OUTPUT=(PRTMSG,SINGLE,DATA),MF=(E,IOPLSECT)  PUT OUT
*                                      DATASET PRINT MESSAGE
         LH    R4,4(R6)                LOAD LENGTH OF DSNAME
         A     R4,FULL4                INCREMENT LENGTH FOR PUTLINE
         SLL   R4,16                   SHIFT LENGTH TO HIGH ORDER BYTES
         STCM  R4,15,DSNLENP           STORE LENGTH OF PUTLINE
         PUTLINE OUTPUT=(DSNLENP,SINGLE,DATA),MF=(E,IOPLSECT)  PUT OUT
*                                      DATA SET NAME MESSAGE
PRCNOM   EQU   *                                                  .PRC.
         L     R2,MEMTABLE             LOAD ADDRESS OF FIRST MEMBER
*                                      TABLE
         LH    R3,4(R2)                LOAD NUMBER OF MEMBERS IN TABLE
         LTR   R3,R3                   SEE IF THERE ARE ANY MEMBERS
         BZ    ENDMEMB                 NO.....GO TO END OF SCROLLER
         ST    R2,PRESENT              SAVE ADDRESS OF CURRENT TABLE
         LA    R2,8(R2)                LOAD POINTER TO MEMBER NAME
NEXTMEMB DS    0H
         TM    CFIELD,HIGH             SEE IF THIS IS AN ALIAS ENTRY
         BO    AGAIN                   YES....DO NOT PRINT IT
PRINTMEM DS    0H
         MVC   MEMNAME,MEMBER          MOVE MEMBER NAME INTO HEADER
         MVC   JFCBELNM,MEMBER         MOVE MEMBER NAME INTO JFCBAREA
         MVC   OPENLST,OPENCLOS        MOVE CONSTANT INTO OPEN LIST
         OPEN  ((R8),INPUT),TYPE=J,MF=(E,OPENLST)
*                                      OPEN INPUT DCB
         STM   R2,R3,SAVER2            SAVE REGISTERS 2 AND 3
         B     PRINTS                  GO PRINT MEMBER
SEXIT    DS    0H
         LM    R2,R3,SAVER2            RESTORE REGISTERS 2 AND 3
         MVC   CLOSLST,OPENCLOS        MOVE OPEN(CLOSE) LIST CONSTANT
*                                      INTO CLOSE LIST IN WORKAREA
         CLOSE ((R8),),MF=(E,CLOSLST)  CLOSE INPUT DCB
AGAIN    DS    0H
         LA    R2,MEMSECTN             LOAD ADDRESS OF NEXT MEMBER
         BCT   R3,NEXTMEMB             BRANCH TO HANDLE NEXT MEMBER
         L     R14,PRESENT             ZERO...LOAD CURRENT TABLE
*                                      ADDRESS
         L     R2,0(R14)               LOAD ADDRESS OF NEXT TABLE
         LTR   R2,R2                   SEE IF ZERO, LAST TABLE
         BZ    ENDMEMB                 NO.....GO HANDLE END OF MEMBERS
         ST    R2,PRESENT              SAVE ADDRESS OF CURRENT TABLE
         LH    R3,4(R2)                LOAD NUMBER OF MEMBERS IN NEXT
*                                      TABLE
         LTR   R3,R3                   SEE IF ANY MEMBERS IN TABLE
         BZ    ENDMEMB                 NO.....GO HANDLE END OF MEMBERS
         LA    R2,8(R2)                LOAD POINTER TO MEMBER NAME
         B     NEXTMEMB                GO GET NEXT MEMBER
ENDMEMB  DS    0H
         L     R2,MEMTABLE             LOAD POINTER TO FIRST TABLE
FREETABS DS    0H
         LR    R1,R2                   LOAD AREA TO BE FREED
         L     R2,0(R2)                LOAD POINTER TO NEXT TABLE
         L     R0,MEMDATL              LOAD SIZE OF AREA TO BE FREED
         FREEMAIN R,LV=(0),A=(1)       FREE STORAGE GETMAINED FOR TABLE
         LTR   R2,R2                   SEE IF THERE WAS ANOTHER TABLE
         BNZ   FREETABS                YES....GO FREE NEXT TABLE
         L     R6,SAVER6               RESTORE REGISTER 6
         LA    R8,INPUT                LOAD POINTER TO INPUT DCB
         USING IHADCB,R8               ESTABLISH ADDRESSABILITY TO DCB
         TM    DCBOFLGS,DCBOFOPN       SEE IF INPUT DCB IS STILL OPEN
         BNO   EXITMSG                 NO.....GO HANDLE NEXT DATASET
         MVC   CLOSLST,OPENCLOS        MOVE OPEN(CLOSE) LIST CONSTANT
*                                      INTO CLOSE LIST IN WORKAREA
         CLOSE ((R8),),MF=(E,CLOSLST)  CLOSE INPUT DCB
         DROP  R8                      DROP ADDRESSABILITY TO DCB
         B     EXITMSG                 GO HANDLE NEXT DATASET
*                                                                     *
***********************************************************************
*                                                                     *
*        PUT OUT SYNAD ERROR MESSAGE,                                 *
*           GO FREE MEMBER TABLES, AND                                *
*           CONTINUE WITH NEXT DATASET                                *
*                                                                     *
***********************************************************************
*
SERRI    DS    0H
         PUTLINE OUTPUT=(SYNADI,SINGLE,DATA),MF=(E,IOPLSECT)  PUT OUT
*                                      DATASET NOT PRINTED DUE TO SYNAD
*                                      ERROR ON INPUT DATASET MESSAGE
         B     ENDMEMB                 GO FREE MEMBER TABLES
*                                                                     *
***********************************************************************
*                                                                     *
*        SEARCH FOR MAIN NAME,                                        *
*           SAME TTR AND NOT AN ALIAS                                 *
*                                                                     *
***********************************************************************
*
LOOKLOOP DS    0H
         STM   R3,R4,SAVER3            SAVE REGISTERS 3 AND 4
         L     R3,MEMTABLE             LOAD ADDRESS OF FIRST TABLE
         USING MEMDSECT,R3             ESTABLISH ADDRESSABILITY TO
*                                      MEMBER ENTRY
LOOP4    DS    0H
         ST    R3,CURRENT              SAVE ADDRESS OF PRESENT TABLE
         LH    R4,4(R3)                LOAD NUMBER OF ELEMENTS IN TABLE
         LTR   R4,R4                   SEE IF THERE ARE ZERO ELEMENTS
         BZ    NOMAIN                  YES....GO HANDLE MAIN NOT FOUND
         LA    R3,8(R3)                LOAD ADDRESS OF FIRST MEMBER
LOOP5    DS    0H
         CLC   MEMTTR,8(R2)            SEE IF TTRS ARE THE SAME
         BE    SAMETTR                 YES....GO SEE IF IT IS AN ALIAS
ENDLP5   DS    0H
         LA    R3,MEMSECTN             LOAD ADDRESS OF NEXT MEMBER
         BCT   R4,LOOP5                DECREMENT MEMBER COUNT
         L     R3,CURRENT              LOAD ADDRESS OF THIS TABLE
         L     R3,0(R3)                LOAD ADDRESS OF NEXT TABLE
         LTR   R3,R3                   SEE IF IT WAS THE LAST TABLE
         BNZ   LOOP4                   NO.....GO CHECK NEXT TABLE
         B     NOMAIN                  GO HANDLE MAIN NOT FOUND
SAMETTR  DS    0H
         TM    CFIELD,HIGH             SEE IF THIS IS ALSO AN ALIAS
         BO    ENDLP5                  YES....GO LOOK FOR ANOTHER TTR
         MVC   11(8,R6),MEMBER         MOVE MEMBER NAME INTO OUTPUT
         B     ENDLOOK                 GO TO RETURN TO CALLER
NOMAIN   DS    0H
         MVC   11(8,R6),ALIAS          INDICATE THIS IS AN ALIAS
ENDLOOK  DS    0H
         LM    R3,R4,SAVER3            RESTORE REGISTERS 3 AND 4
         BR    R14                     RETURN TO CALLER
         DROP  R3                      DROP ADDRESSABILITY TO TABLE
*
CONTINUE DS    0H
         L     R6,24(R6)               LOAD POINTER TO NEXT DSNAME ON
*                                      CHAIN
         C     R6,ENDCHAIN             SEE IF END OF CHAIN IS REACHED
         BNE   NEXT                    NO.....GO DO NEXT DATASET NAME
CLOSDCBS DS    0H
         TM    INPUT+48,DCBOFOPN       SEE IF INPUT DCB IS STILL OPEN
         BNO   CHKOUTA                 NO.....GO CHECK DCB FOR ASA CODE
         MVC   CLOSLST,OPENCLOS        MOVE OPEN(CLOSE) LIST CONSTANT
*                                      INTO CLOSE LIST WORKAREA
         LA    R5,INPUT                LOAD POINTER TO INPUT DCB
         CLOSE ((R5),),MF=(E,CLOSLST)  CLOSE INPUT DCB
CHKOUTA  DS    0H
         LA    R1,PRINTI               FREE INPUT DATASET         .GTE.
         BAL   R2,FREEDD               FREE INPUT DATASET         .GTE.
         TM    OUTPUTA+48,DCBOFOPN     SEE IF OUTPUT DCB FOR ASA WAS
*                                      OPENED
         BNO   CHKOUTM                 NO.....GO CHECK DCB FOR MACHINE
*                                      CODE
         MVC   CLOSLST,OPENCLOS        MOVE OPEN(CLOSE) LIST CONSTANT
*                                      INTO CLOSE LIST WORKAREA
         LA    R5,OUTPUTA              LOAD POINTER TO ASA CODE DCB
         CLOSE ((R5),),MF=(E,CLOSLST)  CLOSE ASA CODE OUTPUT DCB
         LA    R1,PRINTA               LOAD ADDRESS OF PRINT DDNAME
         BAL   R2,FREEDD               GO FREE ASA CODE OUTPUT DDNAME
CHKOUTM  DS    0H
         TM    OUTPUTM+48,DCBOFOPN     SEE IF OUTPUT DCB FOR MACHINE
*                                      CODE WAS OPENED
         BNO   RETURN                  NO.....GO FREE GETMAINED AREAS
*                                      AND RETURN
         MVC   CLOSLST,OPENCLOS        MOVE OPEN(CLOSE) LIST CONSTANT
*                                      INTO CLOSE LIST WORKAREA
         LA    R5,OUTPUTM              LOAD POINTER TO MACHINE CODE DCB
         CLOSE ((R5),),MF=(E,CLOSLST)  CLOSE MACHINE CODE OUTPUT DCB
         LA    R1,PRINTM               LOAD ADDRESS OF PRINT DDNAME
         BAL   R2,FREEDD               GO FREE MACHINE CODE OUTPUT
*                                      DDNAME
*
***********************************************************************
*                                                                     *
*        CLEANUP AREAS GETMAINED BY PARS,                             *
*           SET RETURN CODE, RESTORE REGISTERS AND                    *
*           RETURN TO CALLER                                          *
*                                                                     *
***********************************************************************
*
RETURN   DS    0H
         IKJRLSA PARSBACK              RELEASE AREAS GETMAINED BY PARS
         LR    R1,R13                  LOAD ADDRESS OF GETMAINED AREA
         L     R13,4(,R13)             RELOAD ADDRESS OF PREVIOUS SAVE
*                                      AREA
         LA    R0,LDYNAMIC             LOAD LENGTH OF GETMAINED AREA
         FREEMAIN R,LV=(0),A=(1)       FREE GETMINED AREA
         SLR   R15,R15                 ZERO REGISTER 15.  RETURN CODE
*                                      CODE WILL ALWAYS BE ZERO
         L     R14,12(,R13)            LOAD REGISTER 14 WITH RETURN
*                                      ADDRESS
         LM    R0,R12,20(R13)          RESTORE REGISTERS
         BR    R14                     RETURN
*
***********************************************************************
*                                                                     *
*        ROUTINE TO CONVERT NONZERO RETURN CODES TO DECIMAL           *
*           FOR DISPLAY                                               *
*                                                                     *
***********************************************************************
*
SETCODE  DS    0H
         CVD   R8,DEC                  CONVERT RETURN CODE TO DECIMAL
         UNPK  CONV(8),DEC(8)          UNPACK RETURN CODE
         MVZ   CONV+1(7),CONV          MOVE ZONES IN CONVERTED RETURN
*                                      CODE
         MVC   RETNCD(2),HALF12        MOVE LENGTH OF DATA FOR PUTLINE
         MVC   RETNCD+2(2),HALF0       MOVE IN ZEROES FOR PUTLINE
         BR    R14                     RETURN
*
***********************************************************************
*                                                                     *
*        FREE AND REALOCATE PRINTX FILE AS SYSOUT                     *
*                                                                     *
***********************************************************************
*
ALLOCOUT DS    0H
         STM   14,9,ALCSAVE            SAVE WORK REGISTERS
         LR    R1,R3                   LOAD POINTER TO DDNAME
         BAL   R2,FREEDD               BRANCH TO FREE ROUTINE
         LTR   R8,R8                   SEE IF RETURN CODE FROM FREE IS
*                                      ZERO
         BNZ   OPENFAIL                NO.....GO PUT OUT OPEN FAILED
         LA    R8,REQBLK               LOAD POINTER TO REQUEST BLOCK
         ST    R8,RBPTR                INITIALIZE REQUEST BLOCK POINTER
         MVI   RBPTR,S99RBPND          INDICATE END OF PARAMETER LIST
         USING S99RB,R8                ESTABLISH ADDRESSABILITY TO
*                                      REQUEST BLOCK
         MVC   S99RBLN(RBLEN),REQBLKC  INITALIZE RB
         LA    R1,TEXTPTRS             LOAD POINTER TO TEXT POINTERS
         ST    R1,S99TXTPP             STORE POINTER IN RB
         DROP  R8                      DROP ADDRESSABILITY TO RB
         USING S99TUPL,R1              ESTABLISH ADDRESSABILITY TO
*                                      TEXT UNIT POINTER LIST
         LA    R2,TEXTA                LOAD POINTER TO FIRST TEXT UNIT
         ST    R2,S99TUPTR             STORE POINTER IN TUPL
         USING S99TUNIT,R2             ESTABLISH ADDRESSABILITY TO
*                                      TEXT UNIT
         MVC   S99TUKEY(TEXTALEN),TEXTAC  INITIALIZE TEXT UNIT A
         MVC   S99TUPAR(8),0(R3)       MOVE IN PARAMETER (DDNAME)
         LA    R1,4(R1)                LOAD POINTER TO NEXT TEXT UNIT
*                                      POINTER
         LA    R2,TEXTB                LOAD POINTER TO TEXT UNIT B
         ST    R2,S99TUPTR             STORE POINTER IN TUPL
         MVC   S99TUKEY(TEXTBLEN),TEXTBC  INITIALIZE TEXT UNIT B
*
***********************************************************************
*                                                                     *
*        SET UP ADDRESSABILITY TO PDL                                 *
*           AND SEE IF SYSOUT CLASS/DEST ARE PRESENT                  *
*           ALSO FORMS/TRAIN/FCB/PROG.                            .GTE.
***********************************************************************
*
         L     R9,PARSBACK             LOAD ADDRESS PARS ANSWER
         USING IKJPARMD,R9             ESTABLISH ADDRESSABILITY TO PDL
         CLC   PCLASS(2),HALF1         SEE IF CLASS SPECIFIED
         BNE   NOCLASS                 NO.....GO CHECK FOR DESTINATION
         L     R7,SCLASS               LOAD POINTER TO SYSOUT CLASS
         MVC   S99TUPAR,0(R7)          MOVE CLASS INTO TEXT UNIT B
NOCLASS  DS    0H
         CLC   PDEST(2),HALF1          SEE IF DEST SPECIFIED
         BNE   NODEST                  NO.....GO CHECK FOR HOLD
         LA    R1,4(R1)                LOAD POINTER TO NEXT TEXT UNIT
*                                      POINTER
         LA    R2,TEXTC                LOAD POINTER TO TEXT UNIT C
         ST    R2,S99TUPTR             STORE POINTER IN TUPL
         MVC   S99TUKEY(TEXTCLEN),TEXTCC  INITIALIZE TEXT UNIT C
         L     R7,SDEST                LOAD POINTER TO DESTINATION
         LH    R6,SDEST+4              LOAD LENGTH OF DESTINATION ID
         STH   R6,S99TULNG             STORE LENGTH OF DEST IN TU
         BCTR  R6,0                    DECREMENT LENGTH FOR EXECUTE OF
*                                      MOVE
         EX    R6,MOVEDEST             MOVE DEST INTO TU
MOVEDEST MVC   S99TUPAR,0(R7)          MOVE TO BE EXECUTED
*
NODEST   DS    0H
         CLC   PHOLD(2),HALF1          SEE IF HOLD SPECIFIED
         BNE   NOHOLD                  NO.....GO CHECK FOR COPIES
         LA    R1,4(R1)                LOAD POINTER TO NEXT TEXT UNIT
*                                      POINTER
         LA    R2,TEXTDC               LOAD POINTER TO TEXT UNIT C
         ST    R2,S99TUPTR             STORE POINTER IN TUPL
NOHOLD   DS    0H
         CLC   PCOPIES(2),HALF1        SEE IF COPY SPECIFIED
         BNE   NOCOPY                  NO.....GO ZERO ECB
         LA    R1,4(R1)                LOAD POINTER TO NEXT TEXT UNIT
*                                      POINTER
         LA    R2,TEXTE                LOAD POINTER TO TEXT UNIT E
         ST    R2,S99TUPTR             STORE POINTER IN TUPL
         MVC   S99TUKEY(TEXTELEN),TEXTEC  INITIALIZE TEXT UNIT E
         L     R7,SCOPIES              LOAD POINTER TO COPY FIELD
         LH    R6,SCOPIES+4            LOAD LENGTH OF COPY FIELD
         BCTR  R6,0                    DECREMENT REGISTER 6 FOR EXECUTE
*                                      OF PACK INSTRUCTION
         EX    R6,PACKCOPY             EXECUTE PACK TO CONVERT COPIES
*                                      TO ZONED DECIMAL
         CVB   R6,DEC                  CONVERT ZONED TO BINARY
         STCM  R6,1,S99TUPAR           STORE NUMBER OF COPIES IN TU
NOCOPY   DS    0H
*********** START OF GTE MOD                                      .GTE.
         CLC   PFORMS(2),HALF1         SEE IF FORMS SPECIFIED     .GTE.
         BNE   NOFORMS                 NO.....GO CHECK TRAIN      .GTE.
         LA    R1,4(R1)                LOAD POINTER TO NEXT       .GTE.
*                                      TEXT UNIT POINTER          .GTE.
         LA    R2,TEXTI                LOAD PTR TO TEXT UNIT I    .GTE.
         ST    R2,S99TUPTR             STORE POINTER IN TUPL      .GTE.
         MVC   S99TUKEY(TEXTILEN),TEXTIC  INITIALIZE TEXT UNIT I  .GTE.
         L     R7,RFORMS               LOAD PTR TO FORMS FIELD    .GTE.
         LH    R6,RFORMS+4             LOAD LENGTH OF FORMS FIELD .GTE.
         STH   R6,S99TULNG             STORE LENGTH IN TUE        .GTE.
         BCTR  R6,0                    DECREMENT LENGTH           .GTE.
         EX    R6,MOVEDEST             MOVE FORMS INTO FIELD      .GTE.
NOFORMS  DS    0H                                                 .GTE.
         CLC   PTRAIN(2),HALF1         SEE IF TRAIN SPECIFIED     .GTE.
         BNE   NOTRAIN                 NO.....GO CHECK FCB        .GTE.
         LA    R1,4(R1)                LOAD POINTER TO NEXT       .GTE.
*                                      TEXT UNIT POINTER          .GTE.
         LA    R2,TEXTH                LOAD PTR TO TEXT UNIT G    .GTE.
         ST    R2,S99TUPTR             STORE POINTER IN TUPL      .GTE.
         MVC   S99TUKEY(TEXTHLEN),TEXTHC  INITIALIZE TEXT UNIT H  .GTE.
         L     R7,RTRAIN               LOAD PTR TO TRAIN FIELD    .GTE.
         LH    R6,RTRAIN+4             LOAD LENGTH OF TRAIN FIELD .GTE.
         STH   R6,S99TULNG             SAVE LENGTH OF FCBE        .GTE.
         BCTR  R6,0                    DECREMENT FOR EX           .GTE.
         EX    R6,MOVEDEST             MOVE FCB INTO TU           .GTE.
NOTRAIN  DS    0H                                                 .GTE.
         CLC   PFCB(2),HALF1           SEE IF FCB  SPECIFIED      .GTE.
         BNE   NOFCB                   NO.....GO ZERO ECB         .GTE.
         LA    R1,4(R1)                LOAD POINTER TO NEXT       .GTE.
*                                      TEXT UNIT POINTER          .GTE.
         LA    R2,TEXTG                LOAD PTR TO TEXT UNIT G    .GTE.
         ST    R2,S99TUPTR             STORE POINTER IN TUPL      .GTE.
         MVC   S99TUKEY(TEXTGLEN),TEXTGC  INITIALIZE TEXT UNIT G  .GTE.
         L     R7,RFCB                 LOAD POINTER TO FCB FIELD  .GTE.
         LH    R6,RFCB+4               LOAD LENGTH OF FCB FIELD   .GTE.
         STH   R6,S99TULNG             SAVE LENGTH OF FCB IN TUE  .GTE.
         BCTR  R6,0                    DECREMENT                  .GTE.
         EX    R6,MOVEDEST             MOVE FCB TO TU             .GTE.
NOFCB    DS    0H                                                 .GTE.
         CLC   PPROG(2),HALF1          SEE IF PROG SPECIFIED      .GTE.
         BNE   NOPROG                  NO.....GO ZERO ECB         .GTE.
         LA    R1,4(R1)                LOAD POINTER TO NEXT       .GTE.
*                                      TEXT UNIT POINTER          .GTE.
         LA    R2,TEXTJ                LOAD PTR TO TEXT UNIT J    .GTE.
         ST    R2,S99TUPTR             STORE POINTER IN TUPL      .GTE.
         MVC   S99TUKEY(TEXTJLEN),TEXTJC  INITIALIZE TEXT UNIT J  .GTE.
         L     R7,RPROG                LOAD PTR TO PROG FIELD     .GTE.
         LH    R6,RPROG+4              LOAD LENGTH OF PROG FIELD  .GTE.
         STH   R6,S99TULNG             SAVE LENGTH OF PROG IN TUE .GTE.
         BCTR  R6,0                    DECREMENT FOR MOVE         .GTE.
         EX    R6,MOVEDEST             MOVE PROG TO TU            .GTE.
NOPROG   DS    0H                                                 .GTE.
********* END OF GTE MOD                                          .GTE.
         LA    R1,4(R1)                LOAD POINTER TO NEXT TEXT UNIT
*                                      POINTER
         LA    R2,TEXTFC               LOAD POINTER TO TEXT UNIT F
*                                      PERMANENTLY ALLOCATED
*                                      ATTRIBUTE
         ST    R2,S99TUPTR             STORE POINTER IN TUPL
         MVI   S99TUPTR,S99TUPLN       INDICATE END OF TEXT UNIT
*                                      POINTER LIST
         DROP  R1,R2,R9                DROP ADDRESSABILITY TO DSECTS
         LA    R1,RBPTR                LOAD ADDRESS OF REQUEST BLOCK
*                                      POINTER
         DYNALLOC                      ISSUE DYNAMIC ALLOCATION SVC
         LTR   R15,R15                 SEE IF RETURN CODE IS ZERO
         BNZ   ALCFAIL                 NO.....GO AND CALL DAIR FAIL
         MVC   OPENLST,OPENCLOS        MOVE OPEN(CLOSE) LIST CONSTANT
*                                      INTO WORKAREA
         OPEN  ((R5),(OUTPUT)),MF=(E,OPENLST)
*                                      OPEN OUTPUT DCB
         TM    48(R5),DCBOFOPN         SEE IF DCB OPENED SUCCESSFULLY
         BNO   OPENFAIL                NO.....GO PUT OUT OUTPUT DCB
*                                      NOT OPENED MESSAGE
         LM    14,9,ALCSAVE            RESTORE REGISTERS
         BR    R14                     RETURN TO CALLER
ALCFAIL  DS    0H
         LA    R1,REQBLK               LOAD ADDRESS OF REQUEST
*                                      BLOCK
         MVI   DFID+1,DFSVC99          SET DAIR FAIL FOR SVC 99 ERROR
         BAL   R2,DAIRFAIL             LOAD ADDRESS OF OPEN FAIL AND
*                                      BRANCH TO DAIRFAIL ROUTINE
OPENFAIL DS    0H
         PUTLINE OUTPUT=(OPENMSG,SINGLE,DATA),MF=(E,IOPLSECT)  PUT OUT
*                                      OUTPUT DCB NOT OPEND MESSAGE
         S     R3,FULL4                GET POINTER TO PUTLINE DDNAME
         PUTLINE OUTPUT=((R3),SINGLE,DATA),MF=(E,IOPLSECT)  PUT OUT
*                                      DDNAME
         B     CLOSDCBS                GO CLOSE DCBS AND RETURN
*
***********************************************************************
*                                                                     *
*        INVOKE DAIR TO FREE FILENAME VIA CALLTSSR                    *
*           AND CHECK FOR ZERO RETURN CODE                            *
*                                                                     *
***********************************************************************
*
FREEDD   DS    0H
         SLR   R8,R8                   INITIALIZE REGISTER 8 TO ZERO
*                                      RETURN CODE
         MVC   DAP18(DAPB18L),DAPB18C  INITIALIZE DAIR PARAMETER BLOCK
*                                      18 (FREE BY DATASET)
         LA    R4,DAP18                LOAD ADDRESS OF DAPB
         USING DAPB18,R4               ESTABLISH ADDRESSABILITY TO
*                                      DAPB
         MVC   DA18DDN,0(R1)           MOVE DDNAME INTO DAPB
         SLR   R7,R7                   ZERO REGISTER 7
         ST    R7,ECB                  STORE REGISTER 7 AS DUMMY ECB
         LA    R1,DAPLSECT             LOAD ADDRESS OF DAPL
         USING DAPL,R1                 ESTABLISH ADDRESSABILITY TO DAPL
         ST    R4,DAPLDAPB             STORE ADDRESS OF DAPB IN DAPL
         DROP  R1,R4                   DROP ADDRESSABILITY TO DAPL AND
*                                      DAPB
         CALLTSSR EP=IKJDAIR           CALL DAIR TO FREE DDNAME
         C     R15,FULL28              SEE IF RETURN CODE IS 28
*                                      (DDNAME NOT ALLOCATED)
         BER   R2                      YES....GO CONTINUE PROCESSING
         LTR   R15,R15                 SEE IF RETURN CODE IS ZERO
         BZR   R2                      YES....GO CONTINUE PROCESSING
         MVI   DFID+1,DFDAIR           SET DAIRFAIL ID TO INDICATE DAIR
*
***********************************************************************
*                                                                     *
*        INVOKE DAIRFAIL TO DISPLAY AN ERROR MESSAGE                  *
*                                                                     *
***********************************************************************
*
DAIRFAIL XC    DFPARMS(DFLEN),DFPARMS  INITIALIZE PARMLIST FOR DAIR
*                                      FAIL TO ZEROS
         ST    R1,DFDAPLP              STORE POINTER TO DAPL/RB IN
*                                      PARAMETER LIST
         ST    R15,RETCODE             STORE RETURN CODE
         LA    R1,RETCODE              LOAD ADDRESS OF RETURN CODE
         ST    R1,DFRCP                STORE POINTER TO RETURN CODE IN
*                                      PARAMETER LIST
         LA    R1,FULL0                LOAD ADDRESS OF FULLWORD OF
*                                      ZEROS
         ST    R1,DFJEFF02             STORE DUMMY POINTER TO IKJEFF02
*                                      IN PARMLIST
         LA    R1,DFID                 LOAD ADDRESS OF DAIR FAIL ID
*                                      FIELD
         ST    R1,DFIDP                STORE POINTER TO DFID IN
*                                      PARMLIST
         MVI   DFID,NOWTP              SET FOR NO WRITE TO PROGRAMMER
         L     R1,SAVEAREA+4           LOAD ADDRESS OF PEVIOUS
*                                      SAVEAREA
         L     R1,24(R1)               LOAD POINTER TO CPPL
         ST    R1,DFCPPLP              STORE POINTER TO CPPL IN
*                                      PARMLIST
         LINK  EP=IKJEFF18,MF=(E,DFPARMS)  LINK TO DAIR FAIL SERVICE
*                                      ROUTINE TO HANDLE RETURN
*                                      CODE
         LTR   R15,R15                 SEE IF RETURN CODE IS ZERO
         BZR   R2                      YES....GO RETURN TO CALLER
         LR    R8,R15                  LOAD REGISTER 8 WITH RETURN CODE
         BAL   R14,SETCODE             GO CONVERT CODE TO DECIMAL
         PUTLINE OUTPUT=(DRFLERR,SINGLE,DATA),MF=(E,IOPLSECT)  PUT OUT
*                                      DAIR FAIL ERROR MESSAGE
         PUTLINE OUTPUT=(RETNCD,SINGLE,DATA),MF=(E,IOPLSECT)  PUT OUT
*                                      DECIMAL RETURN CODE
         BR    R2                      GO RETURN TO CALLER
*
******************** INSTRUCTIONS TO BE EXECUTED **********************
*
MOVEDSN  MVC   DSNBUF(0),0(R5)
MOVEVOL  MVC   VOLUME(0),0(R7)
PACKCOPY PACK  DEC(8),0(0,R7)
         USING DAPB08,R8
MOVEMEM  MVC   DA08MNM(0),0(R5)
MOVEPASS MVC   DA08PSWD(0),0(R5)
         DROP  R8
DFPBMVE  MVC   DFPBNAME(0),0(R5)   EXECUTED                       .GTE.
*
***************************** CONSTANTS *******************************
*
******************** DAIR CONTROL BLOCK CONSTANTS *********************
DAPB08C  DS    0F
         DC    X'0008'                 DA08CD
         DC    H'0'                    DA08FLG
         DC    H'0'                    DA08DARC
         DC    H'0'                    DA08CTRC
         DC    A(0)                    DA08PDSN
         DC    CL8'PRINTI  '           DA08DDN
         DC    CL8' '                  DA08UNIT
         DC    CL8' '                  DA08SER
         DC    F'0'                    DA08BLK
         DC    F'0'                    DA08PQTY
         DC    F'0'                    DA08SQTY
         DC    F'0'                    DA08DQTY
         DC    CL8' '                  DA08MNM
         DC    CL8' '                  DA08PSWD
         DC    X'08'                   DA08DSP1      SHR
         DC    X'08'                   DA08DSP2      KEEP
         DC    X'08'                   DA08DSP3      KEEP
         DC    X'08'                   DA08CTL       PERM
         DC    F'0'                    DA08DSO
         DC    CL8' '                  DA08ALN
DAPB08L  EQU   *-DAPB08C               LENGTH
         SPACE 2
DAPB18C  DS    0F
         DC    X'0018'                 DA18CD
         DC    X'0000'                 DA18FLG
         DC    H'0'                    DA18DARC
         DC    H'0'                    DA18CTRC
         DC    F'0'                    DA18PDSN
         DC    CL8'PRINTI'             DA18DDN
         DC    CL8' '                  DA18MNM
         DC    CL2' '                  DA18SCLS
         DC    X'08'                   DA18DPS2
         DC    X'10'                   DA18CTL
         DC    CL8' '                  DA18JBNM
DAPB18L  EQU   *-DAPB18C               LENGTH
         SPACE 2
INPUTC   DCB   DSORG=PS,MACRF=(GM),EODAD=EXIT,SYNAD=ERRI,DDNAME=PRINTI
INPUTL   EQU   *-INPUTC
*
OUTPUTAC DCB   DSORG=PS,MACRF=(PM),SYNAD=ERRO,DDNAME=PRINTA,RECFM=VBA, X
               LRECL=256,BLKSIZE=3120
OUTPUTAL EQU   *-OUTPUTAC
*
OUTPUTMC DCB   DSORG=PS,MACRF=(PM),SYNAD=ERRO,DDNAME=PRINTM,RECFM=VBM, X
               LRECL=256,BLKSIZE=3120
OUTPUTML EQU   *-OUTPUTMC
*
DIRECTC  DCB   DDNAME=PRINTI,DSORG=PS,MACRF=(GM),RECFM=U,BLKSIZE=256,  X
               SYNAD=SERRI,EODAD=FREEBLK
DIRECTL  EQU   *-DIRECTC
*
OPENCLOS OPEN  (,),MF=L
         SPACE 2
HEADERC  DS    0F
         DC    H'48'
         DC    H'0'
HDRCNTLC DC    C' '
         DC    CL43'**** TSO FOREGROUND HARDCOPY ****'
HEADERM  DS    0F
         DC    H'48'
         DC    H'0'
         DC    C' '
         DC    CL43'*** TSO FOREGROUND MEMBER LIST **'
LDSNAME  DS    0CL12
         ORG   LDSNAME
         DC    H'66'
         DC    H'0'
         DC    CL8' DSNAME='
EJECTM   DS    0F
         DC    H'5'
         DC    H'0'
         DC    X'8B'
EJECTA   DS    0F
         DC    H'5'
         DC    H'0'
         DC    C'1'
SPACE    EQU   EJECTA
         SPACE 2
FULL0    DC    F'0'
FULL1    DC    F'1'
FULL4    DC    F'4'
FULL20   DC    F'20'
FULL28   DC    F'28'
FULL256  DC    F'256'
PRINTILN DC    H'12',H'0'
PRINTI   DC    CL8'PRINTI'
PRINTALN DC    H'12',H'0'
PRINTA   DC    CL8'PRINTA'
PRINTMLN DC    H'12',H'0'
PRINTM   DC    CL8'PRINTM'
BLANKS   DC    256CL1' '
ALIAS    DC    CL8' *ALIAS*'
ENDCHAIN DS    0F
         DC    X'FF000000'
MEMDATL  DC    X'00'                   SUBPOOL NUMBER
         DC    AL3(1808)
HALF0    DC    H'0'
HALF1    DC    H'1'
HALF2    DC    H'2'
HALF6    DC    H'6'
HALF12   DC    H'12'
HALF85   DC    H'85'
HALF256  DC    H'256'
PATCH    DC    8CL4'ZAP*'
HEXFF    EQU   X'FF'
HIGH     EQU   X'80'
BLANK    EQU   X'40'
LOW      EQU   X'01'
JFCPDS   EQU   X'01'
HEX00    EQU   X'00'
SCLSA    EQU   C'A'
SKIPM    EQU   X'09'
DSOPS    EQU   X'40'
DSOPO    EQU   X'02'
NOWTP    EQU   X'00'
         SPACE 2
****************** POINTER TO PARS PARAMETER CSECT ********************
PCLADDR  DC    A(PARMTAB)
         SPACE 2
************************ PARS PARAMETER LIST **************************
         PRINT NOGEN                                              .PRC.
PARMTAB  IKJPARM  DSECT=IKJPARMD
DSNAMES  IKJPOSIT  DSNAME,LIST,USID,                                   X
               PROMPT='DSNAME',                                        X
               HELP='DSNAME TO BE PRINTED'
PCLASS   IKJKEYWD
         IKJNAME 'CLASS',SUBFLD=CLASSUB
PDEST    IKJKEYWD
         IKJNAME 'DEST',SUBFLD=DESTSUB
PHOLD    IKJKEYWD
         IKJNAME 'HOLD'
         IKJNAME 'NOHOLD'
PCOPIES  IKJKEYWD
         IKJNAME 'COPIES',SUBFLD=COPYSUB
PPRINT   IKJKEYWD
         IKJNAME 'PRINT'
         IKJNAME 'NOPRINT'
PLIST    IKJKEYWD
         IKJNAME 'LIST'
         IKJNAME 'NOLIST'
PHEAD    IKJKEYWD
         IKJNAME 'NOHEADING'                                      .GTE.
         IKJNAME 'HEADING'                                        .GTE.
PVOL     IKJKEYWD
         IKJNAME 'VOLUME',SUBFLD=VOLSUB
PFOLD    IKJKEYWD
         IKJNAME 'FOLD',ALIAS=('CAPS')
         IKJNAME 'NOFOLD',ALIAS=('ASIS')
PFORMS   IKJKEYWD
         IKJNAME  'FORMS',SUBFLD=SFORMS                          .GTE.
PTRAIN   IKJKEYWD
         IKJNAME  'TRAIN',SUBFLD=STRAIN,ALIAS=('UCS')            .GTE.
PFCB     IKJKEYWD
         IKJNAME    'FCB',SUBFLD=SFCB                            .GTE.
PPROG    IKJKEYWD
         IKJNAME    'PROG',SUBFLD=SPROG                          .GTE.
PTEXT    IKJKEYWD
         IKJNAME    'TEXT',INSERT='UCS(TN) ASIS'                 .GTE.
ASAKW    IKJKEYWD
         IKJNAME 'ASA'                                           .PRC.
SNUMKW   IKJKEYWD
         IKJNAME 'SNUM'                                          .PRC.
MSGKW    IKJKEYWD
         IKJNAME 'NOMSG'                                         .PRC.
*        B E G I N   S U B F I E L D S
CLASSUB  IKJSUBF
SCLASS   IKJIDENT 'CLASSNAME',LIST,FIRST=NONATNUM,MAXLNTH=1,           X
               PROMPT='CLASS NAME'
DESTSUB  IKJSUBF
*-----------------------------------------------------------------.SAD.
*      CHANGE THE DEST ID FROM YOUR TSO ID TO ANY VALUE. USERID   .SAD.
*      IS A MAX OF 7 CHARACTERS AND DEST ID'S CAN BE 8 CHARACTERS .SAD.
*-----------------------------------------------------------------.SAD.
SDEST    IKJIDENT 'DESTINATION',MAXLNTH=8,                             X
               FIRST=ALPHANUM,OTHER=ALPHANUM,                          X
               PROMPT='REMOTE DESTINATION IDENTIFY FOR PRINTED FILE',  X
               HELP=('DEST ID OF YOUR PRINTER ASSIGNED BY SADSC')
COPYSUB  IKJSUBF
SCOPIES  IKJIDENT 'COPIES',MAXLNTH=3,                                  X
               FIRST=NUMERIC,OTHER=NUMERIC,                            X
               PROMPT='1-3 DIGITS - NUMBER OF COPIES OF OUTPUT',       X
               HELP=('NUMBER OF COPIES OF PRINTOUT DESIRED')
VOLSUB   IKJSUBF
SVOL     IKJIDENT 'VOLUME',MAXLNTH=8,                                  X
               FIRST=ALPHANUM,OTHER=ALPHANUM,                          X
               PROMPT='VOLUME SERIAL FOR DATA SETS TO BE PRINTED',     X
               HELP=('VOLUME SERIAL WILL BE ASSUMED FOR ALL DATA SETS')
SFORMS   IKJSUBF
RFORMS   IKJIDENT 'FORMS',MAXLNTH=4,FIRST=ALPHANUM,               .GTE.X
               OTHER=ALPHANUM,                                    .GTE.X
               PROMPT='FORMS DESIGNATION FOR PRINTED OUTPUT',     .GTE.X
               HELP=('AN ALPANUMERIC STRING TO SPECIFY THE FORM') .GTE.
STRAIN   IKJSUBF
RTRAIN   IKJIDENT   'UCS',MAXLNTH=4,FIRST=ALPHANUM,               .GTE.X
               OTHER=ALPHANUM,                                    .GTE.X
               PROMPT='UCS   DESIGNATION FOR PRINTED OUTPUT',     .GTE.X
               HELP=('AN ALPANUMERIC STRING TO SPECIFY THE TRAIN') GTE.
SFCB     IKJSUBF
RFCB     IKJIDENT 'FCB',MAXLNTH=4,FIRST=ALPHANUM,                 .GTE.X
               OTHER=ALPHANUM,                                    .GTE.X
               PROMPT='FCB DESIGNATION FOR PRINTED OUTPUT',       .GTE.X
               HELP=('AN ALPANUMERIC STRING TO SPECIFY THE FCB')  .GTE.
SPROG    IKJSUBF
RPROG    IKJIDENT 'PROG',MAXLNTH=8,FIRST=ALPHA,                   .GTE.X
               OTHER=ALPHANUM,                                    .GTE.X
               PROMPT='PROG DESIGNATION FOR PRINTED OUTPUT',      .GTE.X
               HELP=('AN ALPANUMERIC STRING TO SPECIFY THE PROG') .GTE.
         IKJENDP
         PRINT GEN                                                .PRC.
         SPACE 2
****************************** MESSAGES *******************************
GNRLERR  DC    AL2(GNRLERRL),AL2(0)
         DC    C'AN ERROR WAS ENCOUNTERED IN THE GENERAL FAIL SERVICE RX
               OUTINE.  THE RETURN CODE FROM GNRLFAIL IS: '
GNRLERRL EQU   *-GNRLERR
*
DRFLERR  DC    AL2(DRFLERRL),AL2(0)
         DC    C'AN ERROR WAS ENCOUNTERED IN THE DAIR FAIL SERVICE ROUTX
               INE.  THE RETURN CODE FROM DAIR FAIL IS: '
DRFLERRL EQU   *-DRFLERR
*
OPENMSG  DC    AL2(OPENMSGL),AL2(0)
         DC    C'OUPUT PROCESSING IS BEING TERMINATED.  AN ERROR WAS ENX
               COUNTERED ATTEMPTING TO OPEN THE OUTPUT DATASET ASSOCIATX
               ED WITH DDNAME: '
OPENMSGL EQU   *-OPENMSG
*
DSNNOTP  DC    AL2(DSNNOTPL),AL2(0)
         DC    C'AN ERROR OCCURRED AND YOUR DATASET WAS NOT PRINTED'
DSNNOTPL EQU   *-DSNNOTP
*
DSNMSG   DC    AL2(DSNMSGL),AL2(0)
         DC    C'PROCESSING HAS BEEN COMPLETED FOR DATASET: '
DSNMSGL  EQU   *-DSNMSG
*
MEMMSG   DC    AL2(MEMMSGL),AL2(0)
         DC    C' MEMBER: '
MEMMSGL  EQU   *-MEMMSG
*
PRTMSG   DC    AL2(PRTMSGL),AL2(0)
         DC    C'MEMBERS WILL NOW BE PRINTED FOR DATASET: '
PRTMSGL  EQU   *-PRTMSG
*
NOTPSPO  DC    AL2(NOTPSPOL),AL2(0)
         DC    C'DATASET IS NOT SEQUENTIAL OR PARTITIONED AND WAS NOT PX
               RINTED.'
NOTPSPOL EQU   *-NOTPSPO
*
SYNADI   DC    AL2(SYNADIL),AL2(0)
         DC    C'A SYNAD ERROR OCURRED READING YOU INPUT DATASET AND ONX
               LY PART OF YOUR DATASET MAY HAVE BEEN PRINTED.'
SYNADIL  EQU   *-SYNADI
*
SYNADO   DC    AL2(SYNADOL),AL2(0)
         DC    C'A SYNAD ERROR OCURRED WRITING THE OUTPUT DATASET AND NX
               O MORE DATASETS WILL BE PRINTED.'
SYNADOL  EQU   *-SYNADO
*
DSNISU   DC    AL2(DSNISUL),AL2(0)
         DC    C'DATASET HAS UNDEFINED RECORD LENGTH AND WILL NOT BE PRX
               INTED.'
DSNISUL  EQU   *-DSNISU
*
DSNISL   DC    AL2(DSNISLL),AL2(0)
         DC    C'DATASET HAS RECORD LENGTH GREATER THAN 256.'
DSNISLL  EQU   *-DSNISL
         SPACE 2
************************ DSECTS (MAPPING MACROS) **********************
MEMDSECT DSECT
MEMNTTR  DS    CL11
         ORG   MEMNTTR
MEMBER   DS    CL8
MEMTTR   DS    CL3
CFIELD   DS    CL1
MEMSECTN EQU   *
         IKJDAP08
DAP08LEN EQU   *-DAPB08
         IKJDAP18
DAP18LEN EQU   *-DAPB18
         IKJDAPL
DAPLLEN  EQU   *-DAPL
         IKJCPPL
CPPLLEN  EQU   *-CPPL
         IKJPPL
PPLLEN   EQU   *-PPL
         IKJIOPL
IOPLLEN  EQU   *-IOPL
         IHADSAB
TIOEDDNM EQU   4           X'1C'(OFFSET) - X'18'(JOB STEP)
*        CVT                                                     *.PRC.
CVTPTR   EQU   16                                                 .PRC.
CVT      DSECT                                                    .PRC.
CVTMAP   EQU   *                                                  .PRC.
CVTPARS  EQU   CVTMAP+X'20C'                                      .PRC.
CVTDAIR  EQU   CVTMAP+X'2DC'                                      .PRC.
CVTEHDEF EQU   CVTMAP+X'2E0'                                      .PRC.
         IEFZB4D0
         IEFZB4D2
         IKJDFPL                                                  .GTE.
         IKJDFPB                                                  .GTE.
RBLEN    EQU   S99RBEND-S99RB
         PRINT NOGEN
         DCBD  DSORG=(PS,PO)
         PRINT GEN
         EJECT
******************** CONSTANTS FOR DYNAMIC ALLOCATION *****************
PRINTOFF CSECT
REQBLKC  DC    AL1(S99RBEND-S99RB),AL1(S99VRBAL),18AL1(0)
TEXTAC   DC    AL2(DALDDNAM),AL2(1),AL2(8),CL8'PRINT'  DDNAME
TEXTALEN EQU   *-TEXTAC
TEXTBC   DC    AL2(DALSYSOU),AL2(1),AL2(1),C'A'        SYSOUT CLASS
TEXTBLEN EQU   *-TEXTBC
TEXTCC   DC    AL2(DALSUSER),AL2(1),AL2(8),CL8'LOCAL'  DESTINATION
TEXTCLEN EQU   *-TEXTCC
TEXTDC   DC    AL2(DALSHOLD),AL2(0)              SYSOUT HOLD
TEXTDLEN EQU   *-TEXTDC
TEXTEC   DC    AL2(DALCOPYS),AL2(1),AL2(1),AL2(1)      SYSOUT COPIES
TEXTELEN EQU   *-TEXTEC
TEXTFC   DC    AL2(DALPERMA),AL2(0)              PERM ALLOCATION
TEXTFLEN EQU   *-TEXTFC
TEXTGC   DC    AL2(DALFCBIM),AL2(1),AL2(4),CL4'STD.'  FCB         .GTE.
TEXTGLEN EQU   *-TEXTCC                                           .GTE.
TEXTHC   DC    AL2(DALUCS),AL2(1),AL2(2),CL4'PN'      UCS         .GTE.
TEXTHLEN EQU   *-TEXTDC                                           .GTE.
TEXTIC   DC    AL2(DALSFMNO),AL2(1),AL2(2),CL4'STD.'  FORMS       .GTE.
TEXTILEN EQU   *-TEXTEC                                           .GTE.
TEXTJC   DC    AL2(DALSPGNM),AL2(1),AL2(8),CL8'PROG'  PROG        .GTE.
TEXTJLEN EQU   *-TEXTFC                                           .GTE.
*********************** DSECT FOR DYNAMIC WORKAREA ********************
WORKAREA DSECT
SAVEAREA DS    18F
ALCSAVE  DS    12F
         DS    0D
DEC      DS    2F
RETNCD   DS    F
CONV     DS    2F
IOPB     PUTLINE MF=L
DAP08    DS    0F
         ORG   DAP08+DAP08LEN
DAP18    DS    0F
         ORG   DAP18+DAP18LEN
DAPLSECT DS    0F
         ORG   DAPLSECT+DAPLLEN
PPLSECT  DS    0F
         ORG   PPLSECT+PPLLEN
IOPLSECT DS    0F
         ORG   IOPLSECT+IOPLLEN
DFPLSECT DS    4F                   SPACE FOR DFPL                .GTE.
DFPBSECT DS    5F                   SPACE FOR DFPB                .GTE.
         DS    H                    TO GET ALLIGNMENT             .GTE.
DFPBDSL  DS    H                    DSNAME LENGTH                 .GTE.
DFPBNAME DS    CL44                 DSNAME                        .GTE.
DFPBQUA  DS    CL8
ECB      DS    F
PARSBACK DS    F
LINE     DS    0H
LENGTH   DS    H
         DS    H
CNTLBYTE DS    CL1
DATA     DS    CL256
HEADER2  DS    CL12
DSNAME   DS    CL44
OPENPRN  DS    CL1
MEMNAME  DS    CL8
CLOSEPRN DS    CL1
         ORG   DSNAME-4
DSNLENP  DS    CL2
DSNLEN   DS    CL2
DSNBUF   DS    CL44
         ORG   MEMNAME-4
MEMNAMEL DS    CL2
         ORG
HEADER   DS    CL48
         ORG   HEADER+4
HDRCNTL  DS    CL1
DFID     DS    CL2
         ORG
VOLBIT   DS    CL1
VOLUME   DS    CL8
MEMTABLE DS    F
ENDTABLE DS    F
CURRENT  DS    F
PRESENT  DS    F
COLUMN   DS    F
XLIST    DS    F
RDJL     RDJFCB (,),MF=L
JFCBAREA DS    44F
         ORG   JFCBAREA+44
JFCBELNM DS    CL8
         ORG   JFCBAREA+86
JFCBIND1 DS    CL1
         ORG   JFCBAREA+98
JFCDSRG1 DS    CL1
         ORG
SAVER6   DS    F
SAVER2   DS    2F
SAVER3   DS    2F
PRCKW    DS    F                                                  .PRC.
*
         PRINT NOGEN
*
INPUT    DCB   DSORG=PS,MACRF=(GM),EODAD=EXIT,SYNAD=ERRI,DDNAME=PRINTI
*
OUTPUTA  DCB   DSORG=PS,MACRF=(PM),SYNAD=ERRO,DDNAME=PRINTA,RECFM=VBA, X
               LRECL=256,BLKSIZE=3120
*
OUTPUTM  DCB   DSORG=PS,MACRF=(PM),SYNAD=ERRO,DDNAME=PRINTM,RECFM=VBM, X
               LRECL=256,BLKSIZE=3120
*
DIRECT   DCB   DDNAME=PRINTI,DSORG=PS,MACRF=(GM),RECFM=U,BLKSIZE=256,  X
               SYNAD=SERRI,EODAD=FREEBLK
*
OPENLST  OPEN  (,),MF=L
*
CLOSLST  CLOSE (,),MF=L
*
         PRINT GEN
*
RETCODE  DS    F
GFPARMP  DS    F
         IKJEFFGF
         IKJEFFDF
RBPTR    DS    F
REQBLK   DS    10F (WAS 5F)                                       .GTE.
TEXTPTRS DS    10F (WAS 5F)                                       .GTE.
TEXTA    DS    0F                                   DDNAME
         ORG   TEXTA+TEXTALEN
TEXTB    DS    0F                                   SYSOUT CLASS
         ORG   TEXTB+TEXTBLEN
TEXTC    DS    0F                                   DESTINATION
         ORG   TEXTC+TEXTCLEN
TEXTE    DS    0F                                   SYSOUT COPIES
         ORG   TEXTE+TEXTELEN
TEXTG    DS    0F                                   FCB           .GTE.
         ORG   TEXTG+TEXTGLEN                                     .GTE.
TEXTH    DS    0F                                   UCB           .GTE.
         ORG   TEXTH+TEXTHLEN                                     .GTE.
TEXTI    DS    0F                                   FORMS         .GTE.
         ORG   TEXTI+TEXTILEN                                     .GTE.
TEXTJ    DS    0F                                   PROG          .GTE.
         ORG   TEXTJ+TEXTJLEN                                     .GTE.
LDYNAMIC EQU   *-WORKAREA
         END
