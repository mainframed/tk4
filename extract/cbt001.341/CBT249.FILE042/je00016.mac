./       CHANGE NAME=$JCT     /* JOB MULTIPLE ROUTING            */ KHP
         JE00016A 1           /* ADD JCT FIELDS FOR MULT ROUTING */ KHP
./       CHANGE NAME=$OCR     /* JOB MULTIPLE ROUTING            */ KHP
         JE00016A 2           /* ADD JOB OCR FLAG DEFINITION     */ KHP
./       CHANGE NAME=HASPPRPU /* JOB MULTIPLE ROUTING            */ KHP
         JE00016A 3           /* FIRST PART OF PRPU MODIFICATION */ KHP
         JE00016A 4           /* SECOND PART PF PRPU MOD         */ KHP
./       CHANGE NAME=HASPRDR  /* JOB MULTIPLE ROUTING            */ KHP
         JE00016A 5           /* FIRST PART OF RDR MODIFICATION  */ KHP
         JE00016A 6           /* SECOND PART OF RDR MOD          */ KHP
./       CHANGE NAME=HASPSSSM /* JOB MULTIPLE ROUTING            */ KHP
         JE00016A 7           /* SSSM MODIFICATION               */ KHP
./       ADD   NAME=JE00016A  /* JOB MULTIPLE ROUTING            */ KHP
DUMMY ENTRY FOR REPL
./       REPL  NAME=JE00016A  /* JOB MULTIPLE ROUTING            */ KHP
         MACRO
&L       JE00016A &V
.**********************************************************************
.*                                                                    *
.*   KHPRDC MODIFICATION TO ALLOW MULTIPLE ROUTING OF JOB OUTPUT      *
.*   VIA /*OUTPUT CARD FOR JOB FORMS SPECIFIED ON /*JOBPARM CARD.     *
.*   /*OUTPUT JCL IS ALSO A LEGAL FORM.                               *
.*                                                                    *
.*       KAISER/PERMANENTE MEDICAL CARE PROGRAM                       *
.*       NORTHERN CALIFORNIA REGIONAL DATA CENTER                     *
.*       945 CAMELIA STREET, BERKELEY, CALIFORNIA 94710               *
.*       D. THEWLIS, JUNE 1, 1980.   (415) 428-6350                   *
.*                                                                    *
.*       MODIFICATION LEVEL:  FOR SU 825, JES2 RELEASE 4.1,           *
.*                            MSE/IPO 4.0 (3.8B) AT PTF LEVEL 8001,   *
.*                            INCL.   AZ27300 (CKPT DUPLEXING).       *
.*                                                                    *
.*                                                                    *
.*   THIS MODIFICATION IS ORIGINALLY FROM MSP AND WAS TAILORED        *
.*   FROM THE SHARE JES MODS TAPE VERSION 12, MOD SMSP 23.            *
.*   THANK YOU MSP.                                                   *
.*                                                                    *
.**********************************************************************
         AIF   ('&V' EQ '1').FIRST
         AIF   ('&V' EQ '2').SECOND
         AIF   ('&V' EQ '3').THIRD
         AIF   ('&V' EQ '4').FOURTH
         AIF   ('&V' EQ '5').FIFTH
         AIF   ('&V' EQ '6').SIXTH
         AIF   ('&V' EQ '7').SEVENTH
         MNOTE 'INVALID PARAMETER - &V'
         MEXIT
.FIRST   ANOP
***********************************************************************
* KHPRDC MODIFICATION TO IMPLEMENT /*OUTPUT FOR JOB FORMS AND 'JCL'.  *
* $JCT MODIFICATION.  PART 1 FOLLOWS LINE EB296000 AT LINE EB296016   *
* FOR JES2 MODIFICATION JE00016.                                      *
***********************************************************************
JCTJOCRK DS  CL4                   JOB OCR KEY                      KHP
JCTJROUT DS  CL(3*2)               LAST 3 DESTINATIONS FROM OCR     KHP
*<-------RESUME VANILLA   $JCT   CODE AT THIS POINT-------------->* KHP
***********************************************************************
         MEXIT
.SECOND  ANOP
***********************************************************************
* KHPRDC MODIFICATION TO IMPLEMENT /*OUTPUT FOR JOB FORMS AND 'JCL'.  *
* $OCR MODIFICATION.  PART 2 FOLLOWS LINE FF086000 AT LINE FF086016   *
* FOR JES2 MODIFICATION JE00016.                                      *
***********************************************************************
OCRJOCR  EQU   OCRFLAG7            OCR IS A JOB OCR                 KHP
*<-------RESUME VANILLA   $OCR   CODE AT THIS POINT-------------->* KHP
***********************************************************************
         MEXIT
.THIRD   ANOP
***********************************************************************
* KHPRDC MODIFICATION TO IMPLEMENT /*OUTPUT FOR JOB FORMS AND 'JCL'.  *
* HASPPRPU MODIFICATIONS.  PART 3 FOLLOWS LINE Q0685000.              *
***********************************************************************
         LR    R4,JCT              SAVE JCT BEFORE DESTROYED        KHP
         USING JCTDSECT,R4         JCT ADDRESSABILITY               KHP
*<-------RESUME VANILLA HASPPRPU CODE AT THIS POINT-------------->* KHP
***********************************************************************
         MEXIT
.FOURTH  ANOP
***********************************************************************
* KHPRDC MODIFICATION TO IMPLEMENT /*OUTPUT FOR JOB FORMS AND 'JCL'.  *
* HASPPRPU MODIFICATIONS.  PART 4 FOLLOWS LINE Q0728000.              *
***********************************************************************
*        IF THERE IS A JOB OCR WITH EXTRA DESTINATIONS, CREATE      KHP
*        A NEW IOT WITH MULTIPLE PDDB'S FOR PDDB'S WITH A KEY       KHP
*        LESS THAN 100 AND ARE ACTIVE SYSOUT PDDB'S.                KHP
         OC    JCTJROUT(L'PDBDEST),JCTJROUT JOB OCR DESTS?          KHP
         BZ    OPDSTEND            NO...CONTINUE                    KHP
        $GETBUF WAIT=YES           GET ADDITIONAL BUFFER            KHP
         LR    R5,R1               SAVE ADDRESS OF NEW BUFFER       KHP
         LA    R0,IOTID-IOTDSECT(,R1) START OF MOVE LONG            KHP
         LA    R1,IOTID-IOTDSECT   LENGTH OF IOT PREFIX             KHP
         LNR   R1,R1               NEGATE                           KHP
         A     R1,$IOTPDDB         LENGTH OF MOVE                   KHP
         LR    R15,R1              LENGTH OF SOURCE FIELDS          KHP
         LA    R14,IOTID           ADDRESS OF SOURCE FIELDS         KHP
         MVCL  R0,R14              MOVE DATA TO NEW IOT             KHP
         LR    R1,R0               ADDRESS OF NEW IOT               KHP
*        COMPUTE ABSOLUTE ADDRESS OF END OF PDDBS IN SOURCE BUFFER  KHP
*        TO BE USED FOR DELIMITER ON PDDB SEARCH BELOW (SINCE ZERO  KHP
*        AREAS OF BUFFER WILL LOOK LIKE VALID PDDBS AND CODE COULD  KHP
*        THUS GENERATE AN IOTPDDBP VALUE > IOT LENGTH)              KHP
         L     R3,IOTPDDBP         OFFSET IN IOT BUFFER PAST PDDBS  KHP
         AR    R3,JCT              ADD CURRENT BUFFER ORIGIN        KHP
         ST    R3,OPDBEND          SAVE POINTER TO END IN THIS IOT  KHP
OPJOCRCK C     R14,OPDBEND         END OF PDDBS IN THIS IOT?        KHP
         BE    OPIOTDON            YES, DONE WITH JOB OCR           KHP
         CLI   PDBDSKEY-PDBDSECT+1(R14),100   STILL IN SPECIAL      KHP
         BNL   OPIOTDON            NO...DONE WITH JOB OCR           KHP
         TM    PDBFLAG1-PDBDSECT(R14),PDB1NULL+PDB1NSOT ACTIVE?     KHP
         BNZ   OPJOCRNX            NO...TRY NEXT PDDB               KHP
         MVC   JCTWORK(L'JCTJROUT),JCTJROUT  SET ROUTE IN WORK      KHP
   XC  JCTWORK+L'JCTJROUT(L'PDBDEST),JCTWORK+L'JCTJROUT STOPPER     KHP
         MVC   0(PDBLENG,R1),0(R14) YES...COPY PDDB                 KHP
         OI    PDBFLAG1-PDBDSECT(R14),PDB1NSOT NULLIFY OLD PDDB     KHP
OPOCRNXD MVC   PDBLENG(PDBLENG,R1),0(R1)   PDDB FOR NEXT DEST       KHP
         OI    PDBFLAG1-PDBDSECT(R1),PDB1MDES SET MULT DEST         KHP
         LA    R1,PDBLENG(,R1)     POINT TO NEXT PDDB               KHP
         MVC   PDBDEST-PDBDSECT(,R1),JCTWORK MOVE NEXT DEST         KHP
         MVC   JCTWORK(L'JCTJROUT),JCTWORK+L'PDBDEST SHIFT DOWN     KHP
         CLC   JCTWORK(L'PDBDEST),$ZEROES END OF DESTS?             KHP
         BNE   OPOCRNXD            NO...PROCESS NEXT DEST           KHP
         LA    R1,PDBLENG(,R1)     NEXT NEW PDDB LOCATION           KHP
OPJOCRNX LA    R14,PDBLENG(,R14)   NEXT OLD PDDB LOCATION           KHP
         B     OPJOCRCK            CHECK NEXT OLD PDDB              KHP
         DROP  R4                  DROP TEMP JCT ADDRESSABILITY     KHP
         SPACE 1                                                    KHP
OPIOTDON SR    R1,R5               CALCULATE IOTPDDBP               KHP
         CL    R1,$IOTPDDB         SEE IF ANY WERE CREATED          KHP
         BH    OPIOTCHN            YES...CHAIN AND WRITE NEW IOT    KHP
        $FREEBUF (R5)              NO....FREE BUFFER                KHP
         B     OPDSTEND              AND CONTINUE                   KHP
         SPACE 1                                                    KHP
OPIOTCHN ST    R1,IOTPDDBP-IOTDSECT(,R5) SET END OF PDDB'S          KHP
         ST    JCT,IOTIOT-IOTDSECT(,R5) CHAIN NEW IOT TO OLD        KHP
         ST    R5,OPIOTBUF         SET NEW FIRST IOT                KHP
         NI    IOTFLAG1,255-IOT1ALOC OLD NOT ALLOCATION             KHP
OPTRACK $TRACK IOTMSTAB-IOTDSECT(,R5),WAIT=NO FOR 2ND IOT           KHP
         LTR   R1,R1               WAS TRACK ALLOCATED?             KHP
         BNZ   OPTRKOK             BRANCH IF TRACK AVAILABLE        KHP
        $DORMANT                   ALLOW $PJES2 WHILE WAITING       KHP
        $WAIT  TRAK                WAIT FOR A TRACK                 KHP
        $ACTIVE                    SHOW HASPHOPE ACTIVE AGAIN       KHP
         B     OPTRACK             TRY AGAIN FOR A TRACK            KHP
OPTRKOK  ST    R1,IOTTRACK         NEW SECOND IOT                   KHP
         ST    R1,IOTIOTTR-IOTDSECT(,R5) POINT NEW 1ST AT 2ND       KHP
         MVI   PCEDEVTP,PCEDAWR    SET WRITE REQUEST                KHP
         ST    JCT,PCEBUFAD        SET 2ND IOT BUFFER ADDRESS       KHP
         LR    JCT,R5              SET NEW FIRST IOT                KHP
         ST    R1,PCESEEK          SET 2ND IOT TRACK ADDRESS        KHP
         LA    R1,PCEDADCT         POINT TO DA DCT                  KHP
        $EXCP  (R1),WAIT=YES       WRITE NEW 2ND IOT                KHP
         BNM   OPIOT2OK            REWRITE FIRST IOT IF OK          KHP
OPIOT2  $DISTERR                   INDICATE ERROR                   KHP
         B     OPDSTEND            BUT BUILD JOES                   KHP
         SPACE 1                                                    KHP
OPIOT2OK ST    R5,PCEBUFAD         SET NEW 1ST BUFFER ADDRESS       KHP
         MVC   PCESEEK,IOTTRACK-IOTDSECT(R5) AND TRACK ADDRESS      KHP
         LA    R1,PCEDADCT         POINT TO DA DCT                  KHP
        $EXCP  (R1),WAIT=YES       WRITE NEW 2ND IOT                KHP
         BNM   OPDSTEND            CONTINUE IF OK                   KHP
OPIOT1  $DISTERR                   INDICATE ERROR                   KHP
*        IN THE CASE OF EITHER DISASTER ERROR THE OUTPUT            KHP
*        PROCESSOR WILL CREATE JOES AS IF THE IO HAD COMPLETED      KHP
*        NORMALLY.  FOR OPIOT2, THIS WILL MEAN SECONDARY            KHP
*        DESTINATIONS, THE JOB WILL NOT GET JCL.  FOR OPIOT1,       KHP
*        THIS MAY ALSO HAPPEN, OR THE ENTIRE JOB MAY BE LOST,       KHP
*        PRINTING ONLY SEPARATOR PAGES.                             KHP
OPDSTEND DS    0H                  END OF MULTIPLE DESTINATION      KHP
*<-------RESUME VANILLA HASPPRPU CODE AT THIS POINT-------------->* KHP
***********************************************************************
         MEXIT
.FIFTH   ANOP
***********************************************************************
* KHPRDC MODIFICATION TO IMPLEMENT /*OUTPUT FOR JOB FORMS AND 'JCL'.  *
* HASPRDR  MODIFICATIONS.  PART 5 FOLLOWS LINE R2034000.              *
***********************************************************************
         USING OCRDSECT,RW         ESTABLISH OCR ADDRESSABILITY     KHP
         TM    OCRFLAGS,OCRJOCR    IS THIS A JOB OCR?               KHP
         BO    ROPJOCRC            PROCESS IF YES                   KHP
*        THIS BIT CAN BE SET IF A /*OUTPUT WITH JOB FORMS           KHP
*        HAS ALREADY BEEN PROCESSED AND THIS CARD IS A              KHP
*        CONTINUATION (KEY=*).                                      KHP
         CLC   JCTFORMS,OCRCODE    SEE IF THIS JOB OCR              KHP
         BNE   ROPNJOCR            BRANCH IF NOT JOB OCR            KHP
         OI    OCRFLAGS,OCRJOCR    SET JOB OCR FLAG                 KHP
         MVC   JCTFORMS,$STDFORM   RESET STANDARD FORMS IN JCT      KHP
         CLC   OCRCODE,=C'JCL '    CHECK IF JCL ROUTE ONLY          KHP
         BE    *+10                SKIP SET OF JOB OCR IF SO        KHP
         MVC   JCTJOCRK,OCRCODE    SET JOB OCR CODE                 KHP
ROPJOCRC L     RL2,RDRIOT          ADDRESS OF FIRST IOT             KHP
         AL    RL2,$IOTPDDB        ADDRESS OF FIRST PDDB            KHP
         MVC   PDBFORMS-PDBDSECT+IOTPDBOJ(,RL2),OCRFORMS FORMS      KHP
         ICM   R14,15,OCRFCB       GET OCR FCB AND SET IN...        KHP
         BZ    *+8                 (SKIP IF NONE)                   KHP
         ST    R14,PDBFCB-PDBDSECT+IOTPDBOJ(,RL2)    JCL            KHP
         ICM   R14,15,OCRUCS       GET OCR UCS AND SET IN...        KHP
         BZ    *+8                 (SKIP IF NONE)                   KHP
         ST    R14,PDBUCS-PDBDSECT+IOTPDBOJ(,RL2)    JCL            KHP
         MVC   PDBINDEX-PDBDSECT+IOTPDBOJ(,RL2),OCRINDEX JCL        KHP
         MVC   PDBCPTN-PDBDSECT+IOTPDBOJ(,RL2),OCRCPTN COMPACT      KHP
         ICM   R14,3,OCRDEST1      GET FIRST DESTINATION            KHP
         BZ    *+8                 SKIP IF NOT SPECIFIED            KHP
         STCM  R14,3,PDBDEST-PDBDSECT+IOTPDBOJ(RL2)     JCL         KHP
         ICM   R14,8,OCRCOPY       GET COPIES                       KHP
         BZ    *+8                 SKIP IF NOT SPECIFIED            KHP
         STCM  R14,8,PDBCOPYS-PDBDSECT+IOTPDBOJ(RL2)    JCL         KHP
*                                                                   KHP
*        MERGE 3800 OCR FIELDS WITH PDDB                            KHP
*                                                                   KHP
         TM    OCRFLAGS,OCRBRSTY   TEST FOR BURST=YES               KHP
         BZ    *+5*4               SKIP IF NOT SPECIFIED            KHP
      OI  PDBFLAG2-PDBDSECT+IOTPDBOJ(RL2),PDB2BRST BURST/JCL        KHP
      OI  PDBFLAG2-PDBDSECT+IOTPDBOL(RL2),PDB2BRST BURST/LOG        KHP
      OI  PDBFLAG2-PDBDSECT+IOTPDBOI(RL2),PDB2BRST BURST/IMAGES     KHP
      OI  PDBFLAG2-PDBDSECT+IOTPDBOM(RL2),PDB2BRST BURST/MSGS       KHP
         TM    OCRFLAGS,OCRBRSTN   TEST FOR BURST=NO                KHP
         BZ    *+5*4               SKIP IF NOT SPECIFIED            KHP
 NI PDBFLAG2-PDBDSECT+IOTPDBOJ(RL2),255-PDB2BRST NO BRST JCL        KHP
 NI PDBFLAG2-PDBDSECT+IOTPDBOL(RL2),255-PDB2BRST NO BRST LOG        KHP
 NI PDBFLAG2-PDBDSECT+IOTPDBOI(RL2),255-PDB2BRST NO BRST IMAGES     KHP
 NI PDBFLAG2-PDBDSECT+IOTPDBOM(RL2),255-PDB2BRST NO BRST MSGS       KHP
         ICM   R14,15,OCRMODF      GET OCR MODIFY IMAGE ID          KHP
         BZ    *+8                 SKIP IF NOT SPECIFIED            KHP
         STCM  R14,15,PDBMODF-PDBDSECT+IOTPDBOJ(RL2)  PDBMODF       KHP
        MVC  PDBMODFT-PDBDSECT+IOTPDBOJ(,RL2),OCRMODFT PDBMODFT     KHP
         ICM   R14,15,OCRFLASH     GET OCR FLASH FRAME ID           KHP
         BZ    *+8                 SKIP IF NOT SPECIFIED            KHP
         STCM  R14,15,PDBFLASH-PDBDSECT+IOTPDBOJ(RL2) PDBFLASH      KHP
        MVC  PDBFLSHC-PDBDSECT+IOTPDBOJ(,RL2),OCRFLSHC PDBFLSHC     KHP
         ICM   R14,15,OCRCHAR1     GET OCR CHAR1                    KHP
         BZ    *+8                 SKIP IF NOT SPECIFIED            KHP
         STCM  R14,15,PDBCHAR1-PDBDSECT+IOTPDBOJ(RL2) PDBCHAR1      KHP
         ICM   R14,15,OCRCHAR2     GET OCR CHAR2                    KHP
         BZ    *+8                 SKIP IF NOT SPECIFIED            KHP
         STCM  R14,15,PDBCHAR2-PDBDSECT+IOTPDBOJ(RL2) PDBCHAR2      KHP
         ICM   R14,15,OCRCHAR3     GET OCR CHAR3                    KHP
         BZ    *+8                 SKIP IF NOT SPECIFIED            KHP
         STCM  R14,15,PDBCHAR3-PDBDSECT+IOTPDBOJ(RL2) PDBCHAR3      KHP
         ICM   R14,15,OCRCHAR4     GET OCR CHAR4                    KHP
         BZ    *+8                 SKIP IF NOT SPECIFIED            KHP
         STCM  R14,15,PDBCHAR4-PDBDSECT+IOTPDBOJ(RL2) PDBCHAR4      KHP
         CLI   OCRCOPYG,0          TEST FOR COPY GROUPS             KHP
         BZ    *+10                SKIP IF NOT SPECIFIED            KHP
        MVC  PDBCOPYG-PDBDSECT+IOTPDBOJ(,RL2),OCRCOPYG PDBCOPYG     KHP
RMVLEN   EQU  PDBCOPYG+L'PDBCOPYG-PDBFORMS LENGTH OF MOVE BELOW     KHP
RJDATA   EQU  PDBFORMS-PDBDSECT+IOTPDBOJ,RMVLEN JCL PDDB DATA       KHP
RLDATA   EQU  PDBFORMS-PDBDSECT+IOTPDBOL,RMVLEN LOG PDDB DATA       KHP
RIDATA   EQU  PDBFORMS-PDBDSECT+IOTPDBOI,RMVLEN IMGS PDDB DATA      KHP
RMDATA   EQU  PDBFORMS-PDBDSECT+IOTPDBOM,RMVLEN MSGS PDDB DATA      KHP
RMVLEN2  EQU  PDBCPTN+L'PDBCPTN-PDBCOPYS   LENGTH OF MOVE BELOW     KHP
RJDATA2  EQU  PDBCOPYS-PDBDSECT+IOTPDBOJ,RMVLEN2 JCL PDDB DATA      KHP
RLDATA2  EQU  PDBCOPYS-PDBDSECT+IOTPDBOL,RMVLEN2 LOG PDDB DATA      KHP
RIDATA2  EQU  PDBCOPYS-PDBDSECT+IOTPDBOI,RMVLEN2 IMGS PDDB DATA     KHP
RMDATA2  EQU  PDBCOPYS-PDBDSECT+IOTPDBOM,RMVLEN2 MSGS PDDB DATA     KHP
*        MOVES BELOW DEPEND ON THE DATA IN THE FOUR PDDB'S          KHP
*        BEING THE SAME BETWEEN PDBFORMS AND PDBCOPYG               KHP
         MVC   RLDATA(,RL2),RJDATA(RL2) PROPAGATE TO LOG            KHP
         MVC   RIDATA(,RL2),RJDATA(RL2) PROPAGATE TO IMAGES         KHP
         MVC   RMDATA(,RL2),RJDATA(RL2) PROPAGATE TO MESSAGES       KHP
         MVC   RLDATA2(,RL2),RJDATA2(RL2) PROPAGATE TO LOG          KHP
         MVC   RIDATA2(,RL2),RJDATA2(RL2) PROPAGATE TO IMAGES       KHP
         MVC   RMDATA2(,RL2),RJDATA2(RL2) PROPAGATE TO MESSAGES     KHP
         MVC   JCTJROUT,OCRDEST2   SET SECONDARY DESTINATIONS       KHP
         DROP  RW                  DROP OCR ADDRESSABILITY          KHP
ROPNJOCR DS    0H                  NOT A JOB OCR                    KHP
*<-------RESUME VANILLA HASPRDR  CODE AT THIS POINT-------------->* KHP
***********************************************************************
         MEXIT
.SIXTH   ANOP
***********************************************************************
* KHPRDC MODIFICATION TO IMPLEMENT /*OUTPUT FOR JOB FORMS AND 'JCL'.  *
* HASPRDR  MODIFICATIONS.  PART 6 FOLLOWS LINE R5054000.              *
***********************************************************************
         CLI   PDBFORMS-PDBDSECT+IOTPDBOJ(R1),0  OCR FORMS SET?     KHP
         BNE   *+9*4               YES...LEAVE IT ALONE             KHP
*<-------RESUME VANILLA HASPRDR  CODE AT THIS POINT-------------->* KHP
***********************************************************************
         MEXIT
.SEVENTH ANOP
***********************************************************************
* KHPRDC MODIFICATION TO IMPLEMENT /*OUTPUT FOR JOB FORMS AND 'JCL'.  *
* HASPSSSM MODIFICATION.  PART 7 FOLLOWS LINE T3331300.               *
***********************************************************************
         BNZ   *+4*4               SKIP NEXT 2 INST IF PRESENT      KHP
         L     R1,SJBJCT           LOAD ADDRESS OF JCT              KHP
         ICM   R0,15,JCTJOCRK-JCTDSECT(R1) JOB OCR KEY IF ANY       KHP
*<-------RESUME VANILLA HASPSSSM CODE AT THIS POINT-------------->* KHP
***********************************************************************
         MEND
