./       CHANGE NAME=HASPCOMM   /* $DD AND $D COMMANDS          */  KHP
         AGO   .JE140D1         /* REMOVE $COMTAB D7D           */  KHP
.JE140D1 ANOP                   /* AT LINE K1686000             */  KHP
         JE00014A 1             /* INSERT $COMTAB D40, D7D      */  KHP
         JE00014A 2             /* INSERT $COMTAB DD            */  KHP
         AGO   .JE140D3         /* REMOVE $COMGRUP DO,DU        */  KHP
.JE140D3 ANOP                   /* AT LINE K4477000             */  KHP
         JE00014A 3             /* INSERT $COMGRUP DD,DO,D40,DU */  KHP
         JE00014A 4             /* INSERT D40                   */  KHP
./       ADD   NAME=JE00014A
DUMMY IMAGE TO BE REPLACED BY REPL
./       REPL  NAME=JE00014A
         MACRO
&L       JE00014A &V
.**********************************************************************
.*                                                                    *
.*   KHPRDC HASPCOMM MODIFICATION TO ADD $DD (DISPLAY DASD) AND       *
.*   $D  (DISPLAY DATE/TIME).                                         *
.*                                                                    *
.*       KAISER/PERMANENTE MEDICAL CARE PROGRAM                       *
.*       NORTHERN CALIFORNIA REGIONAL DATA CENTER                     *
.*       945 CAMELIA STREET, BERKELEY, CALIFORNIA 94710               *
.*       D. THEWLIS, JUNE 1, 1980.   (415) 428-6350                   *
.*                                                                    *
.*       MODIFICATION LEVEL:  FOR SU 825, JES2 RELEASE 4.1,           *
.*                            MSE/IPO 4.0 (3.8B) AT PTF LEVEL 8001,   *
.*                            INCL.   AZ27300 (CKPT DUPLEXING).       *
.*                                                                    *
.*   THIS MODIFICATION IS ORIGINALLY FROM CSG AND WAS TAILORED        *
.*   FROM THE SHARE JES MODS TAPE VERSION 12, MOD CSG-1U.             *
.*   THANK YOU CSG.                                                   *
.*                                                                    *
.**********************************************************************
         AIF   ('&V' EQ '1').FIRST
         AIF   ('&V' EQ '2').SECOND
         AIF   ('&V' EQ '3').THIRD
         AIF   ('&V' EQ '4').FOURTH
         MNOTE 'INVALID PARAMETER - &V'
         MEXIT
.FIRST   ANOP
***********************************************************************
* KHPRDC MODIFICATION TO HASPCOMM TO ADD $D AND $DD COMMANDS (DISPLAY *
* DATE/TIME AND DISPLAY DASD).  PART 1 REPLACES LINE K1686000.        *
***********************************************************************
COMTBLD  $COMTAB D40,HASPCMS1      DISPLAY TIME AND DATE            KHP
         $COMTAB D7D,HASPCJB3,REDIR=$D7D  DISPLAY BY JOBNAME        KHP
*<-------RESUME VANILLA HASPCOMM CODE AT THIS POINT-------------->* KHP
***********************************************************************
         MEXIT
.SECOND  ANOP
***********************************************************************
* KHPRDC MODIFICATION TO HASPCOMM TO ADD $D AND $DD COMMANDS (DISPLAY *
* DATE/TIME AND DISPLAY DASD).  PART 2 FOLLOWS LINE K1686500.         *
***********************************************************************
         $COMTAB DD,HASPCMS1                                        KHP
*<-------RESUME VANILLA HASPCOMM CODE AT THIS POINT-------------->* KHP
***********************************************************************
         MEXIT
.THIRD   ANOP
***********************************************************************
* KHPRDC MODIFICATION TO HASPCOMM TO ADD $D AND $DD COMMANDS (DISPLAY *
* DATE/TIME AND DISPLAY DASD).  PART 3 REPLACES LINE K4477000: $DD.   *
***********************************************************************
HASPCMS1 $COMGRUP DD,DO,D40,DU      DISPLAY COMMANDS                KHP
***************************************************************     KHP
*                                                             *     KHP
*        $D D  -  DISPLAY DISKS                               *     KHP
*                                                             *     KHP
*               $DD,XXX  WHERE XXX IS THE UCB ADDR            *     KHP
*               $DD'YYYYYY'  WHERE YYYYYY IS THE VOLSER       *     KHP
*               $DD          FOR ALL DASD ONLINE DEVICES      *     KHP
*                                                             *     KHP
*         THE $DD IS ONLY ALLOWED LOCALLY                     *     KHP
*                                                             *     KHP
***************************************************************     KHP
CDD      NULL                                                       KHP
         L     WC,$CVTPTR         PIC UP CVT POINTER                KHP
         L     WB,CVTILK2-CVTDSECT(,WC) GET UCB LOOK UP TABLE       KHP
         L     WD,0(,WD)          GET FIRST OPERAND                 KHP
         CLI   1(WD),C','         IS IT $DD,XXX                     KHP
         BE    CDDSPEC            YES, GO LIST ADDR XXX             KHP
         CLI   1(WD),C''''        IS IT $DD'VOLSER'                 KHP
         BE    CDDSPEC            YES, GO LIST THIS VOLSER          KHP
         TM    COMFLAG,CMBFLAGW   IS COMMAND FROM A REMOTE    WR    KHP
         BO    CDDINVO            YES, ERROR - MUST GIVE PARM       KHP
         MVI   COMMAND+3,C' '     SET SPACER TO BLANK               KHP
CDDLOOP  NULL                                                       KHP
         LH    WA,0(0,WB)         POINT TO UCB                      KHP
         LA    WB,2(0,WB)         NEXT IN CASE NOT A HIT            KHP
         LTR   WA,WA              END OF NULL ?                     KHP
         BZ    CDDLOOP            NULL, LOOP BACK                   KHP
         BM    CDDRET             END, RETURN                       KHP
         SPACE 1                                                    KHP
         USING UCBDSECT,WA                                          KHP
         TM    UCBTBYT3,UCB3DACC  IS THIS DIRECT ACCESS             KHP
         BZ    CDDLOOP            NO, GO BACK                       KHP
         TM    SRTESTAT,SRTEONLI  IS DEVICE ONLINE                  KHP
         BZ    CDDLOOP            NO, GO BACK                       KHP
         L     R1,CVTILK2-CVTDSECT(,WC)  GET UCB LOOK UP TABLE      KHP
CDDLOOP1 LH    R0,0(,R1)          POINT TO UCB                      KHP
         LA    R1,2(,R1)          NEXT IN CASE NOT A HIT            KHP
         LTR   R0,R0              NULL ?                            KHP
         BZ    CDDLOOP1           YES, GO BACK                      KHP
         CLR   R0,WA              COMPARE UCB WITH CURRENT          KHP
         BNE   CDDLOOP1           LOOP IF NOT CURRENT               KHP
         CLR   R1,WB              COMPARE LOOKUP ENTRY              KHP
         BNE   CDDLOOP            IGNORE IF DUPLICATE               KHP
         MVC   COMMAND(3),UCBNAME SET NAME                          KHP
         MVC   COMMAND+4(6),SRTEVOLI  SET VOLUME SERIAL             KHP
         CLI   SRTEVOLI,X'00'     LOOK FOR SERIAL PRESENT           KHP
         BNE   *+10               SKIP                              KHP
         MVC   COMMAND+4(6),CDDNOID   SET ' NO ID'                  KHP
         $CWTO L=10               REPLY TO OPERATOR                 KHP
         B     CDDLOOP            LOOP                              KHP
        SPACE 1                                                     KHP
CDDSPEC  NULL                                                       KHP
         LH    WA,0(,WB)          POINT TO UCB                      KHP
         LA    WB,2(,WB)          FOR NEXT TIME IF NOT A HIT        KHP
         LTR   WA,WA              END OR NULL ?                     KHP
         BZ    CDDSPEC            NULL, LOOP                        KHP
         N     WA,=A(X'FFFF')     CHECK FOR END         R41 WR      KHP
         CL    WA,=A(X'FFFF')      EVEN IF OVER CHAN. 8 R41 WR      KHP
         BE    CDDERRA                                  R41 WR      KHP
         TM    UCBTBYT3,UCB3DACC  DASD ?                            KHP
         BZ    CDDSPEC            NO                                KHP
         TM    SRTESTAT,SRTEONLI  ONLINE ?                          KHP
         BZ    CDDSPEC                                              KHP
         CLI   1(WD),C','         $DD,XXX ?                         KHP
         BNE   CDDSER             NO                                KHP
         CLC   2(3,WD),UCBNAME    IS THIS THE ADDR                  KHP
         BNE   CDDSPEC            NO, GO BACK                       KHP
         SPACE 1                                                    KHP
CDDADDR  NULL                                                       KHP
         MVC   COMMAND(3),UCBNAME SET NAME                          KHP
         MVI   COMMAND+3,C' '     ADD SPACER                        KHP
         MVC   COMMAND+4(6),SRTEVOLI   SET VOLUME SERIAL            KHP
         CLI   SRTEVOLI,X'00'     WAS VOLSER THERE                  KHP
         BNE   *+10               YES, GO AROUND                    KHP
         MVC   COMMAND+4(6),CDDNOID    SET ' NO ID'                 KHP
         $CWTO L=10               REPLY TO OPERATOR                 KHP
         B     CDDRET             GET OUT                           KHP
         SPACE 1                                                    KHP
CDDSER   NULL                                                       KHP
         CLC   2(6,WD),SRTEVOLI   IS THIS THE VOL                   KHP
         BE    CDDADDR            YES, LIST IT                      KHP
         B     CDDSPEC            NO, KEEP LOOKING                  KHP
         SPACE 2                                                    KHP
CDDERRA  NULL                                                       KHP
         CLI   1(WD),C','         WAS IT A $DD,XXX                  KHP
         BNE   CDDERRB            NO                                KHP
         MVC   COMMAND(3),2(WD)   XXX NOT ONLINE                    KHP
         MVC   COMMAND+3(11),CDDMSGA                                KHP
         $CWTO L=14                                                 KHP
         B     CDDRET                                               KHP
CDDERRB  NULL                                                       KHP
         MVC   COMMAND(6),2(WD)   XXXXXX NOT MOUNTED                KHP
         MVC   COMMAND+6(12),CDDMSGB                                KHP
         $CWTO L=18                                                 KHP
         B     CDDRET                                               KHP
         SPACE 2                                                    KHP
CDDINVO  NULL                                                       KHP
         LR    R1,WD              POINT TO BAD FIELD                KHP
         $CFINVO OPERAND=(R1)     SEND ERROR                        KHP
         SPACE 1                                                    KHP
CDDRET   NULL                                                       KHP
         $CRET ,                  EXIT                              KHP
         DROP  WA                                                   KHP
CDDNOID  DC    C' NO ID'                                            KHP
CDDMSGA  DC    C' NOT ONLINE'                                       KHP
CDDMSGB  DC    C' NOT MOUNTED'                                      KHP
         EJECT                                                      KHP
*<-------RESUME VANILLA HASPCOMM CODE AT THIS POINT-------------->* KHP
***********************************************************************
         MEXIT
.FOURTH  ANOP
***********************************************************************
* KHPRDC MODIFICATION TO HASPCOMM TO ADD $D AND $DD COMMANDS (DISPLAY *
* DATE/TIME AND DISPLAY DASD).  PART 4 FOLLOWS LINE K4494000: $D .    *
***********************************************************************
         EJECT                                                      KHP
******************************************************************  KHP
*                                                                *  KHP
*       $D W/O OPERANDS WILL DISPLAY TIME AND DATE IN FORMAT:    *  KHP
*       *DATE* DAYNAME MON  DATE, 19XX (JULIAN) TIME             *  KHP
*  EX:                                                           *  KHP
*  $HASP000 *DATE*  SATURDAY    JAN. 26  1980  (80026) 11.26.56  *  KHP
*                                                                *  KHP
******************************************************************  KHP
CD40     $TIME                                                      KHP
         ST    R0,CD40FULW                  SAVE TIME OF DAY        KHP
         SR    R0,R0                                                KHP
         STM   R0,R1,CD40DBLW                                       KHP
         MVI   COMMAND,C' '                                         KHP
         MVC   COMMAND+1(55),COMMAND                                KHP
         UNPK  COMMAND+36(5),CD40DBLW+5(3)  JULIAN DATA             KHP
         MVI   COMMAND+35,C'('                                      KHP
         MVI   COMMAND+41,C')'                                      KHP
         UNPK  COMMAND+31(3),CD40DBLW+5(2)  YEAR                    KHP
         MVC   COMMAND+29(2),=C'19'       PREFIX                    KHP
         MVI   COMMAND+33,C' '                                      KHP
         SR    R0,R0                                                KHP
         CVB   R1,CD40DBLW                                          KHP
         D     R0,=F'1000'    R0=DAY  R1=YR                         KHP
         BCTR  R1,0           R1=YR-1                               KHP
         LR    WB,R1          WB=YR-1                               KHP
         N     R1,=F'3'       R1=(YR-1)MOD4                         KHP
         SRL   WB,2           WB=(YR-1)/4                           KHP
         MH    WB,=H'5'       WB=((YR-1)/4)*5                       KHP
         AR    WB,R1          WB=((YR-1)/4*5)+((YR-1)MOD4)          KHP
         AR    WB,R0          WB=(((YR-1)/4*5)+((YR-1)MOD4)+DAY     KHP
         SR    WA,WA                                                KHP
         D     WA,=F'7'            WA=(WB)MOD7 =>DAY 0=>MON         KHP
         MH    WA,=AL2(L'CD40DAYS)                                  KHP
         LA    WA,CD40DTAB(WA)                                      KHP
         MVC   COMMAND+7(9),0(WA)    NAME OF DAY                    KHP
         LA    WA,CD40MTAB-L'CD40MTHS                               KHP
         LA    R1,1(,R1)                                            KHP
         SRA   R1,2                LEAP YEAR?                       KHP
         BZ    CD40LOOP                                             KHP
         LA    R1,29                                                KHP
         STH   R1,CD40FEB                                           KHP
CD40LOOP LA    WA,L'CD40MTHS(,WA)    NEXT ENTRY                     KHP
         SH    R0,0(,WA)           MONTH?                           KHP
         BP    CD40LOOP            NO                               KHP
         AH    R0,0(,WA)           ADD BACK                         KHP
         CVD   R0,CD40DBLW                                          KHP
         UNPK  COMMAND+25(2),CD40DBLW+6(2)   DATE OF MONTH          KHP
         OI    COMMAND+26,C'0'                                      KHP
         MVC   COMMAND+20(4),2(WA)    MONTH NAME                    KHP
         SR    R0,R0                  CLEAR R0                      KHP
         L     R1,CD40FULW            GET TIME IN 100THS SECS       KHP
         D     R0,=F'100'             STRIP 100THS/SECS             KHP
         SR    R0,R0                  AND DISCARD                   KHP
         D     R0,=F'60'              GET SECONDS                   KHP
         CVD   R0,CD40DBLW            CONVERT TO DECIMAL            KHP
         OI    CD40DBLW+7,X'0F'       SET SIGN                      KHP
         UNPK  COMMAND+48(3),CD40DBLW+6(2)   GET SECONDS            KHP
         MVI   COMMAND+48,C'.'        SET DELIMITER                 KHP
         SR    R0,R0                  SETUP FOR MINUTES             KHP
         D     R0,=F'60'              GET MINUTES                   KHP
         CVD   R0,CD40DBLW            CONVERT TO DECIMAL            KHP
         OI    CD40DBLW+7,X'0F'       SET SIGN                      KHP
         UNPK  COMMAND+45(3),CD40DBLW+6(2)  GET MINUTES             KHP
         MVI   COMMAND+45,C'.'        SET DELIMITER                 KHP
         CVD   R1,CD40DBLW            CONVERT HOURS TO DECIMAL      KHP
         OI    CD40DBLW+7,X'0F'       SET SIGN                      KHP
         UNPK  COMMAND+42(3),CD40DBLW+6(2)  GET HOURS               KHP
         MVI   COMMAND+42,C' '        SET DELIMITER                 KHP
         MVC   COMMAND(6),CD40TEXT    SET IDENTIFIER                KHP
         $CWTO L=55                                                 KHP
CD40RET  $CRET ,                   EXIT                             KHP
CD40DBLW DS    D                                                    KHP
CD40FULW DS    F                                                    KHP
CD40TEXT DC    C'*DATE*'                                            KHP
CD40DAYS DS    0CL9                                                 KHP
CD40DTAB DC    CL9'   MONDAY'                                       KHP
         DC    CL9'  TUESDAY'                                       KHP
         DC    CL9'WEDNESDAY'                                       KHP
         DC    CL9' THURSDAY'                                       KHP
         DC    CL9'   FRIDAY'                                       KHP
         DC    CL9' SATURDAY'                                       KHP
         DC    CL9'   SUNDAY'                                       KHP
CD40MTHS DS    0CL6                                                 KHP
CD40MTAB DC    H'31',C'JAN.'                                        KHP
CD40FEB  DC    H'28',C'FEB.'                                        KHP
         DC    H'31',C'MAR.'                                        KHP
         DC    H'30',C'APR.'                                        KHP
         DC    H'31',C' MAY'                                        KHP
         DC    H'30',C'JUNE'                                        KHP
         DC    H'31',C'JULY'                                        KHP
         DC    H'31',C'AUG.'                                        KHP
         DC    H'30',C'SEP.'                                        KHP
         DC    H'31',C'OCT.'                                        KHP
         DC    H'30',C'NOV.'                                        KHP
         DC    H'31',C'DEC.'                                        KHP
*<-------RESUME VANILLA HASPCOMM CODE AT THIS POINT-------------->* KHP
***********************************************************************
         MEND
