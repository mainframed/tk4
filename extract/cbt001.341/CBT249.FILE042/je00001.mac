./       CHANGE  NAME=HASPPRPU     /* KHPRDC JOB SEPARATOR MODS */  KHP
         AGO   .JE010D1  /* REMOVE 'LA R1,BUFSTART-BUFDSECT'   */   KHP
.JE010D1 ANOP            /* AND 'B BLKSKIP' IF LINECT < 30     */   KHP
         JE00001A 1 /* PART 1 OF KHPRDC JOB SEPARATOR PAGE MOD */   KHP
         AGO   .JE010D2  /* REMOVE MVC 'ROOM' AND ROOM NUMBER  */   KHP
.JE010D2 ANOP            /* TO INFO LINE AND REPLACE WITH XEQ  */   KHP
         JE00001A 2 /* PART 2 OF KHPRDC JOB SEPARATOR PAGE MOD */   KHP
         AGO   .JE010D3  /* REMOVE 'BCT JCT,PPPUT' REPLICATION */   KHP
.JE010D3 ANOP            /* AND PRTR EJECT UNLESS NEWS PRESENT */   KHP
         JE00001A 3 /* PART 3 OF KHPRDC JOB SEPARATOR PAGE MOD */   KHP
         JE00001A 4 /* PART 4 OF KHPRDC JOB SEPARATOR PAGE MOD */   KHP
         AGO   .JE010D5  /* REMOVE 'LM PC1,PC2,PRCCWEJ'        */   KHP
.JE010D5 ANOP            /* THROUGH 'BAL PL,PPCHECK'           */   KHP
         JE00001A 5 /* PART 5 OF KHPRDC JOB SEPARATOR PAGE MOD */   KHP
         JE00001A 6 /* PART 6.1  KHPRDC JOB SEPARATOR PAGE MOD */   KHP
         JE00001A 6 /* PART 6.2  KHPRDC JOB SEPARATOR PAGE MOD */   KHP
./       ADD   NAME=JE00001A
         DUMMY IMAGE TO BE REPLACED BY REPL
./       REPL  NAME=JE00001A
         MACRO
&L       JE00001A &V
.**********************************************************************
.*                                                                    *
.*       KHPRDC HASPPRPU JOB SEPARATOR PAGE MODIFICATION.             *
.*                                                                    *
.*       KAISER/PERMANENTE MEDICAL CARE PROGRAM                       *
.*       NORTHERN CALIFORNIA REGIONAL DATA CENTER                     *
.*       945 CAMELIA STREET, BERKELEY, CALIFORNIA 94710               *
.*       D. THEWLIS, JUNE 1, 1980.   (415) 428-6350                   *
.*                                                                    *
.*       MODIFICATION LEVEL:  FOR SU 825, JES2 RELEASE 4.1,           *
.*                            MSE/IPO 4.0 (3.8B) AT PTF LEVEL 8001,   *
.*                            INCL.   AZ27300 (CKPT DUPLEXING).       *
.*                                                                    *
.*       THIS MACRO GENERATES DIFFERENT SEGMENTS OF CODE              *
.*       DEPENDING UPON THE CALLING PARAMETER AND IS USED TO GENERATE *
.*       ALL PORTIONS OF THE SEPARATOR PAGE MODIFICATION.             *
.*                                                                    *
.*       *****NOTE NOTE NOTE NOTE NOTE NOTE NOTE NOTE NOTE NOTE*****  *
.*                                                                    *
.*       THIS MODIFICATION REQUIRES THAT ALL 'STANDARD' FCB IMAGES    *
.*       HAVE CHANNEL 7/8 PUNCHES CORRESPONDING (RESPECTIVELY)        *
.*       WITH THE FANFOLD SERRATION LINE PRECEEDING THE 'UP' PAGE     *
.*       AND THE FANFOLD SERRATION LINE PRECEEDING THE 'DOWN' PAGE.   *
.*                                                                    *
.*       THIS MODIFICATION CHANGES THE STANDARD JES2 POSITIONING      *
.*       OF FORMS IN THE PRINTER IN TWO WAYS:                         *
.*                                                                    *
.*       1.  THE PRINTER WILL ALWAYS BE EJECTED AFTER PRINTING A      *
.*           'START' OR 'CONT' PAGE EVEN IF JES2 NEWS IS AVAILABLE.   *
.*           (VANILLA JES2 LEAVES THE SEPARATOR PAGE POSITIONED AFTER *
.*           THE LAST 'JES2 INFO LINE' IF JES2 NEWS IS AVAILABLE AND  *
.*           EJECTS FORMS IF NO JES2 NEWS IS AVAILABLE).              *
.*                                                                    *
.*       2.  THE PRINTER IS *NOT* EJECTED AFTER PRINTING AN 'END'     *
.*           SEPARATOR PAGE (AND UP/DOWN ASTERISKS LINES ARE NOT DONE *
.*           FOR THE 'END' PAGE).                                     *
.*                                                                    *
.*       THE FORMS ARE ALWAYS EJECTED AFTER A START/CONT PAGE         *
.*       BECAUSE THE FORMS ARE POSITIONED JUST PAST THE FANFOLD.      *
.*       THE FORMS ARE NOT EJECTED AFTER THE END PAGE, WHICH DOES NOT *
.*       PRINT THE UP/DOWN ASTERISKS LINE, SO THAT THE NEXT           *
.*       START/CONT PAGE WILL BEGIN ON THE FIRST POSSIBLE UP PAGE.    *
.*                                                                    *
.*       THIS MODIFICATION ALSO ELIMINATES THE MEANING OF THE         *
.*       $PRIDCT AND $TPIDCT PARAMETERS UNLESS THEY ARE SET TO ZERO.  *
.*                                                                    *
.**********************************************************************
         AIF   ('&V' EQ '1').PART1
         AIF   ('&V' EQ '2').PART2
         AIF   ('&V' EQ '3').PART3
         AIF   ('&V' EQ '4').PART4
         AIF   ('&V' EQ '5').PART5
         AIF   ('&V' EQ '6').PART6
         MNOTE 12,'UNRECOGNISED PARAMETER - &V'
         MEXIT
.PART1   ANOP
         EJECT
***********************************************************************
*                                                                     *
*   KHPRDC HASPPRPU JOB SEPARATOR PAGE MODIFICATION -- PART 1         *
*                                                                     *
*   1.  IF DOING 'END' SEPARATOR PAGE *AND* RUNNING TO REMOTE,        *
*       BYPASS SEPARATOR PAGE TO REDUCE PRINT VOLUME.                 *
*                                                                     *
*   2.  IF DOING 'CONT' OR 'START' SEPARATOR PAGE AND FCB MOUNTED     *
*       ON PRINTER IS 'STANDARD' FCB, SKIP TO 'UP' PAGE FANFOLD LINE  *
*       (CHANNEL 7) AND PRINT LINE OF ASTERISKS.                      *
*                                                                     *
*   3.  EJECT PRINTER TO CHANNEL 1 FOR 'CONT', 'START', AND 'END'     *
*       ('END' IF RUNNING TO LOCAL PRINTER).                          *
*                                                                     *
*        THIS PART OF THE MODIFICATION FOLLOWS LINE Q4840000;         *
*        LINES Q4838500 THROUGH Q4840000 MUST BE DELETED.             *
*                                                                     *
***********************************************************************
         LA    R1,BUFSTART-BUFDSECT STARTING LINE DISPLACEMENT      KHP
         ST    R1,PLNDISPL         SAVE CURRENT DISPL IN BUFFER     KHP
         L     R1,PCEFORM          ADDRESS OF ID TYPE               KHP
         CLC   0(5,R1),=CL5' END'  DOING JOB END PAGE?      DCT79287KHP
         BNE   JE011STD            NO - GO CHECK FCB TYPE.          KHP
         TM    PCEID,PCERJEID      IS THIS A REMOTE DEVICE?         KHP
         BO    PRNOID              YES, BYPASS SEPARATOR ENTIRELY   KHP
         B     JE011NOS            NO, BYPASS 'UP' PAGE SKIP        KHP
JE011STD L     R1,PCEDCT           ADDRESS OF PRINTER DCT           KHP
         TM    DCTPPSW-DCTDSECT(R1),DCTPPSWB 'STD' FCB IMAGE?       KHP
         BO    JE011NOS            NO - BYPASS 'UP' PAGE SKIP       KHP
*        SKIP TO CHANNEL 7 ('UP' FANFOLD) AND PRINT ASTERISKS       KHP
         L     PC1,=A(KHPCCWUP)    ADDRESS OF CCW                   KHP
         LM    PC1,PC2,0(PC1)      SKIP TO CHANNEL 7                KHP
         BAL   PL,PPPUT            ADD CCW TO CHAIN                 KHP
         L     R1,PLNDISPL         CURRENT LINE DISPLACEMENT        KHP
         LA    PL,132(,R1)         GET NEW DISPL AFTER ASTERISKS    KHP
         ST    PL,PLNDISPL         SET FUTURE LINE DISPLACEMENT     KHP
         LA    PC1,0(R1,PBUF)      CURRENT LINE SLOT IN BUFFER      KHP
         MVI   0(PC1),C'*'         PRINT LINE OF ASTERISKS          KHP
         MVC   1(131,PC1),0(PC1)   TO BUFFER                        KHP
         AL    PC1,PRCCWID         SETUP PRINT CCW STRING           KHP
         L     PC2,PRCCWID+4       TO PRINT ASTERISKS ON FOLD       KHP
         BAL   PL,PPPUT            GO ADD TO CCW CHAIN              KHP
*        ALWAYS EJECT TO NEW PAGE                                   KHP
JE011NOS DS    0H                  HERE FOR 'END' OR NONSTD FCB     KHP
         LM    PC1,PC2,PRCCWEJ     LOAD EJECT (SKIP-TO-CHANNEL-U)   KHP
         BAL   PL,PPPUT            CCW AND ADD TO CCW CHAIN         KHP
         L     R1,PLNDISPL         SET UP CURRENT LINE DISPLACEMENT KHP
*<------ RESUME VANILLA JES2 HASPPRPU CODE AT THIS POINT--------->* KHP
***********************************************************************
         EJECT
         MEXIT
.PART2   ANOP
***********************************************************************
*                                                                     *
*   KHPRDC HASPPRPU JOB SEPARATOR PAGE MODIFICATION -- PART 2         *
*                                                                     *
*        REPLACE 'ROOM XXXX' IN STANDARD JES2 INFO LINE WITH          *
*        ' XEQ XXXX' (EXECUTION PROCESSOR) AND 'SYS' WITH 'PRT'       *
*        AHEAD OF PRINT PROCESSOR ID.                                 *
*                                                                     *
*        THIS MODIFICATION REPLACES LINES Q4888000 THROUGH Q4889000   *
*        AND OVERLAYS THE CONSTANT 'SYS' INSERTED IN THE PRINT LINE   *
*        BY LINE Q4885000.                                            *
*                                                                     *
***********************************************************************
         MVC   56(4,PC1),=C' XEQ'  PP057  XEQ (EXECUTION PROCESSOR) KHP
         MVC   61(4,PC1),JCTEXSID  PP062  EXECUTION PROCESSOR SYSID KHP
         MVC   100(3,PC1),=C'PRT'  PP101  PRT (PRINT PROCESSOR)     KHP
*<------ RESUME VANILLA JES2 HASPPRPU CODE AT THIS POINT--------->* KHP
***********************************************************************
         MEXIT
.PART3   ANOP
         EJECT
***********************************************************************
*                                                                     *
*   KHPRDC HASPPRPU JOB SEPARATOR PAGE MODIFICATION -- PART 3         *
*                                                                     *
*        1.    KEPT JOB NAME AND NUMBER BLOCK LETTER LINES LOGIC      *
*              AND DOUBLE-SPACE AFTER JOB NUMBER LINE.                *
*        2.    PRINT ONLY ONE LINE OF MODIFIED JES2 INFO LINE.        *
*        3.    PRINT A LINE IDENTIFYING THE KFHP DATA CENTER.         *
*        4.    SPACE 2 LINES.                                         *
*        5.    IF DOING 'START' OR 'CONT' SEPARATOR PAGE,             *
*              PRINT BLOCK LETTER LINE OF ROOM # AND PROGRAMMER ID    *
*              (BY CONVENTION PROGRAMMER-ID IS FIRST 3 CHARACTERS     *
*              OF PROGRAMMER NAME FIELD.  IF FOREGROUND USER,         *
*              CONVENTION NOT ENFORCED SO SET TO ' TSO').             *
*        6.    IF DOING 'END' SEPARATOR PAGE, PRINT 'END END' IN      *
*              IN BLOCK LETTERS AFTER KPMCP DATA CENTER ID LINE.      *
*        7.    SPACE 3 LINES.                                         *
*        8.    PRINT BLOCK LETTER LINE OF JOB SOURCE (JCTINDEV)       *
*              IF INPUT ROUTE CODE DIFFERENT THAN PRINTER ROUTE CODE. *
*        9.    SKIP TO DOWN FANFOLD LINE AND PRINT LINE OF ASTERISKS  *
*              IF 'STD' FCB AND NOT END; SKIP TO 1 UNLESS 'END'.      *
*              IF 'END', LEAVE POSITIONED AFTER #7.                   *
*                                                                     *
*        THIS PART OF THE MODIFICATION FOLLOWS LINE Q4950000          *
*        WHICH MUST BE DELETED.                                       *
*                                                                     *
***********************************************************************
         L     R1,PLNDISPL         GET OLD LINE DISPLACEMENT        KHP
         LA    R1,132(,R1)         ADJUST FOR JES2 INFO LINE        KHP
         LA    R1,132(,R1)         ADJUST TO NEW LINE DISPLACEMENT  KHP
         CH    R1,$BUFLENG         ROOM IN BUFFER?                  KHP
         BNH   JE013SK1            YES, CONTINUE                    KHP
         BAL   PL,PPWRITE          FORCE WRITE                      KHP
         BAL   PL,PPCHECK          FORCE CHECK                      KHP
         LA    R1,BUFSTART-BUFDSECT SET STARTING LINE DISPLACEMENT  KHP
JE013SK1 ST    R1,PLNDISPL         SAVE LINE DISPLACEMENT           KHP
         LA    PC1,0(R1,PBUF)      GET NEW LINE BUFFER ADDRESS      KHP
         L     PC2,=A(KHPIDLIN)    POINT TO ID LINE                 KHP
         MVC   0(132,PC1),0(PC2)   ID LINE TO FIXED BUFFER          KHP
         AL    PC1,PRCCWID         CONSTRUCT                        KHP
         L     PC2,PRCCWID+4        PRINT CCW                       KHP
         BAL   PL,PPPUT            ADD CCW TO CHAIN                 KHP
         LM    PC1,PC2,PRCCWSP     LOAD SPACE CCW                   KHP
         ICM   PC1,8,=X'13'        SET CCW TO DOUBLE SPACE          KHP
         BAL   PL,PPPUT            ADD CCW TO CHAIN                 KHP
         L     JCT,PJCTBUF         RESTORE JCT ADDRESSIBILITY       KHP
         L     R1,PCEFORM          POINT TO CALL TYPE               KHP
         MVC   PCCWORK(4),1(R1)    FILL AREA WITH CALL TYPE         KHP
         MVC   PCCWORK+4(4),1(R1)  (WILL LEAVE IF TYPE IS 'END')    KHP
         CLI   PCCWORK,C'E'        IS THIS 'END'?                   KHP
         BE    JE013SK2            YES - SKIP OUT                   KHP
         MVC   PCCWORK(4),=CL4' '  NO, RESET TO                     KHP
         MVC   PCCWORK+4(4),=CL4' TSO'  SETUP FOR TSO               KHP
         TM    JCTJOBFL,JCTTSUJB        IS THIS A FOREGROUND USER?  KHP
         BO    JE013SK2                 YES, LEAVE ' TSO'           KHP
         MVC   PCCWORK(4),JCTROOMN      ROOM # OR USER GROUP ID     KHP
         MVC   PCCWORK+5(3),JCTPNAME    MOVE PROGRAMMER ID          KHP
JE013SK2 L     R1,PLNDISPL         GET NEW LINE DISPLACEMENT        KHP
         LA    R1,132(,R1)          AND POINT TO NEW LINE DISPL.    KHP
         CH    R1,$BUFLENG          IS THERE ROOM TO START?         KHP
         BNH   JE013SK3             YES, BYPASS                     KHP
         BAL   PL,PPWRITE           FORCE WRITE                     KHP
         BAL   PL,PPCHECK           FORCE CHECK                     KHP
         LA    R1,BUFSTART-BUFDSECT SET STARTING LINE DISPL.        KHP
JE013SK3 ST    R1,PLNDISPL          SAVE NEW DISPLACEMENT           KHP
         LA    PL,BLKPRT           POINT TO BLOCK LETTER ROUTINE    KHP
         BAL   PL,0(,PL)           PRINT NON-SLANTED                KHP
         LM    PC1,PC2,PRCCWSP     LOAD SPACE CCW                   KHP
         BAL   PL,PPPUT            ADD CCW TO CHAIN                 KHP
         L     R1,PCEDCT           ADDRESS PRINTER DCT              KHP
         CLC   JCTROUTE,DCTNO-DCTDSECT(R1)   INPUT ROUTE = PRINTER? KHP
         BE    JE013SK4            YES, SKIP DEVICE NAME PRINT      KHP
         MVC   PCCWORK,JCTINDEV    GET INPUT DEVICE NAME            KHP
         L     R1,PLNDISPL         GET NEW LINE DISPLACEMENT        KHP
         LA    PL,BLKPRT           POINT TO BLOCK LETTER ROUTINE    KHP
         BALR  PL,PL               PRINT SLANTED                    KHP
         NOPR  0                   * PRESENT BY JES2 CONVENTION     KHP
*        DETERMINE IF TO DO SKIP TO DOWN FANFOLD AND ASTERISKS      KHP
JE013SK4 L     R1,PCEFORM          ADDRESS OF ID TYPE               KHP
         CLC   0(5,R1),=CL5' END'  DOING JOB END PAGE?      DCT79287KHP
         BE    JE013OUT            YES - BYPASS SKIP/EJECT          KHP
         L     R1,PCEDCT           ADDRESS OF PRINTER DCT           KHP
         TM    DCTPPSW-DCTDSECT(R1),DCTPPSWB 'STD' FCB IMAGE?       KHP
         BO    JE013EJ             NO - GO TO EJECT PAGE            KHP
*        SKIP TO CHANNEL 8 (DOWN FANFOLD) AND PRINT ASTERISKS       KHP
         L     PC1,=A(KHPCCWDN)    POINT TO CCW                     KHP
         LM    PC1,PC2,0(PC1)      ADD CCW TO CHAIN                 KHP
         BAL   PL,PPPUT            ADD CCW TO CHAIN                 KHP
         L     R1,PLNDISPL         CURRENT LINE DISPL.      DCT79287KHP
         LA    R1,132(,R1)         ADJUST FOR ASTERISKS LINE        KHP
         CH    R1,$BUFLENG         ROOM IN BUFFER FOR ASTERISKS?    KHP
         BNH   JE013SK5            YES - CONTINUE                   KHP
         BAL   PL,PPWRITE          FORCE WRITE OF CURRENT BUFFER    KHP
         BAL   PL,PPCHECK          FORCE CHECK                      KHP
         LA    R1,BUFSTART-BUFDSECT SET STARTING LINE DISPLACEMENT  KHP
JE013SK5 ST    R1,PLNDISPL         SAVE LINE DISPLACEMENT           KHP
         LA    PC1,0(R1,PBUF)      GET NEW LINE BUFFER ADDRESS      KHP
         MVI   0(PC1),C'*'         LINE OF ASTERISKS                KHP
         MVC   1(131,PC1),0(PC1)   TO BUFFER                        KHP
         AL    PC1,PRCCWID         CONSTRUCT                        KHP
         L     PC2,PRCCWID+4       PRINT CCW                        KHP
         BAL   PL,PPPUT            ADD CCW TO CHAIN                 KHP
*        ENTER HERE TO EJECT SEPARATOR PAGE                         KHP
JE013EJ  DS    0H                  ENTRY FOR NONSTD FCB TO EJECT    KHP
         LM    PC1,PC2,PRCCWEJ     FOR START/CONT, ALWAYS EJECT     KHP
         BAL   PL,PPPUT            EVEN IF JES NEWS AVAILABLE       KHP
*        ENTER HERE FOR 'END' SEPARATOR PAGE; BYPASS EJECT          KHP
JE013OUT DS    0H                  ENTRY FOR 'END'                  KHP
*<------ RESUME VANILLA JES2 HASPPRPU CODE AT THIS POINT--------->* KHP
***********************************************************************
         EJECT
         MEXIT
.PART4   ANOP
         EJECT
***********************************************************************
*                                                                     *
*   KHPRDC HASPPRPU JOB SEPARATOR PAGE MODIFICATION -- PART 4.        *
*                                                                     *
*        CCWS AND CONSTANT PRINT LINES FOR THE SEPARATOR PAGE         *
*        MODIFICATION.                                                *
*                                                                     *
*        THIS CODE  FOLLOWS LINE Q5229000                             *
*                                                                     *
***********************************************************************
*                                                                   KHP
*        CCWS FOR SKIP TO UP/DOWN PAGE FANFOLD SERRATION LINE       KHP
*                                                                   KHP
KHPCCWUP CCW   X'BB',0,X'60',1     SKIP TO CH 7 (UP PAGE FOLD)      KHP
KHPCCWDN CCW   X'C3',0,X'60',1     SKIP TO CH 8 (DOWN PAGE FOLD)    KHP
*                                                                   KHP
*        DATA CENTER ID LINE                                        KHP
*                                                                   KHP
KHPIDLIN DS    0H                  DATA CENTER ID LINE              KHP
         DC    CL24'       ---------------'                         KHP
         DC    C'KAISER FOUNDATION HEALTH PLAN ------------- NORTHERN CC
               ALIFORNIA REGIONAL DATA CENTER'                      KHP
         DC    CL24'  ---------------'                              KHP
*<------ RESUME VANILLA JES2 HASPPRPU CODE AT THIS POINT--------->* KHP
***********************************************************************
         MEXIT
.PART5   ANOP
**********************************************************************
*                                                                    *
*     KHPRDC HASPPRPU JOB SEPARATOR PAGE MODIFICATION - PART 5.      *
*                                                                    *
*            THIS SECTION DELETES LINES Q549100 THROUGH Q5494000     *
*            WHICH GENERATED AN UNCONDITIONAL EJECT OF THE BANNER    *
*            PAGE.  THAT CODE WILL BE REPLACED BY PART 6 OF THIS     *
*            MODIFICATION WHICH EJECTS THE BANNER PAGE ONLY IF       *
*            DEVICE SETUP IS CHANGING.  PART 5 GENERATES NO ACTUAL   *
*            CODE AND THIS SECTION IS A COMMENT BLOCK ONLY.          *
*                                                                    *
**********************************************************************
*<------ RESUME VANILLA JES2 HASPPRPU CODE AT THIS POINT--------->* KHP
***********************************************************************
         MEXIT
.PART6   ANOP
         EJECT
***********************************************************************
*                                                                     *
*   KHPRDC HASPPRPU JOB SEPARATOR PAGE MODIFICATION -- PART 6.        *
*                                                                     *
*        THIS PART  CONTAINS CODE TO EJECT THE PRINTER AND FORCE A    *
*        WRITE OF THE CCW STRING.  IN VANILLA JES2 THE EJECT IS       *
*        DONE UNCONDITIONALLY FOLLOWING LABEL 'DSVLUCSB' EVEN IF      *
*        NO SETUP CHANGES ARE REQUIRED -- THIS CAUSES AN EXTRA EJECT  *
*        AND TWO BLANK PAGES BEFORE THE NEXT 'UP' PAGE IF THE PAPER   *
*        IS POSITIONED ON THE 'DOWN' PAGE AFTER THE END SEPARATOR.    *
*                                                                     *
*        THAT CODE IS DELETED (LINES Q5491000 THROUGH Q5494000)       *
*        AND THIS CODE  IS INVOKED IF THE UCSB OR FCB IS ACTUALLY     *
*        CHANGED.                                                     *
*                                                                     *
*        THIS CODE  FOLLOWS LINE Q5502000 (UCSB LOAD)                 *
*                       AND LINE Q5577000 (FCB LOAD)                  *
*                                                                     *
***********************************************************************
         LM    PC1,PC2,PRCCWEJ     GET EJECT CCW                    KHP
         BAL   PL,PPPUT            ADD TO CHAIN                     KHP
         BAL   PL,PPWRITE          STAGE WRITE                      KHP
         BAL   PL,PPCHECK          CHECK WRITE                      KHP
         L     PC1,PCEDCT          RESTORE PRINT/PUNCH DCT ADDRESS  KHP
*<------ RESUME VANILLA JES2 HASPPRPU CODE AT THIS POINT--------->* KHP
***********************************************************************
         EJECT
         MEND
