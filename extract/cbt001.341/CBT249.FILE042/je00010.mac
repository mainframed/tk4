./       CHANGE NAME=HASPRDR
         JE00010A A     /* SET DEFAULT ROUTE = SUBMITTER */
         JE00010A B     /* SET DEFAULT ROUTE = SUBMITTER */
         JE00010A C     /* SET DEFAULT ROUTE = SUBMITTER */
         JE00010A D     /* SET DEFAULT ROUTE = SUBMITTER */
./       ADD   NAME=JE00010A
         DUMMY ENTRY TO BE REPLACED BY REPL
./       REPL  NAME=JE00010A
         MACRO
&L       JE00010A &V
         GBLC  &DOC     /* FOR INTERNAL DOC MACRO CALLS */          KHP
         GBLC  &LIST    /* FOR INTERNAL DOC MACRO CALLS */          KHP
.**********************************************************************
.*                                                                    *
.*       KHPRDC HASPRDR MODIFICATION TO SET DEFAULT PRINT/PUNCH       *
.*       ROUTING FOR JOB TO THAT OF SUBMITTING JOB IF ANY.            *
.*                                                                    *
.*       KAISER/PERMANENTE MEDICAL CARE PROGRAM                       *
.*       NORTHERN CALIFORNIA REGIONAL DATA CENTER                     *
.*       945 CAMELIA STREET, BERKELEY, CALIFORNIA 94710               *
.*       D. THEWLIS, JUNE 1, 1980.   (415) 428-6350                   *
.*                                                                    *
.*       MODIFICATION LEVEL:  FOR SU 825, JES2 RELEASE 4.1,           *
.*                            MSE/IPO 4.0 (3.8B) AT PTF LEVEL 8001,   *
.*                            INCL.   AZ27300 (CKPT DUPLEXING).       *
.*                                                                    *
.*       THIS MODIFICATION WAS TAILORED FROM THE SHARE JES MODS       *
.*       TAPE VERSION 5 MODIFICATION CSG-1S BY CANADA SYSTEMS GROUP   *
.*       THANK YOU, CSG AND TERRY BURR.                               *
.*                                                                    *
.**********************************************************************
         AIF   ('&V' EQ 'A').PART1
         AIF   ('&V' EQ 'B').PART2
         AIF   ('&V' EQ 'C').PART3
         AIF   ('&V' EQ 'D').PART4
         MNOTE 12,'INVALID PARAMETER - &V'
         MEXIT
.PART1   ANOP
***********************************************************************
*   KHPRDC MODIFICATION TO HASPRDR TO SET DEFAULT ROUTE = SUBMITTER.  *
*   PART 1 FOLLOWS LINE R0284000.                                     *
***********************************************************************
         $ASCB LIST=&LIST          GENERATE OS ASCB DSECT           KHP
*<-------RESUME VANILLA HASPRDR CODE AT THIS POINT--------------->* KHP
***********************************************************************
         MEXIT
.PART2   ANOP
***********************************************************************
*   KHPRDC MODIFICATION TO HASPRDR TO SET DEFAULT ROUTE = SUBMITTER.  *
*   PART 2 FOLLOWS LINE R0294000.                                     *
***********************************************************************
         $SJB  DOC=&DOC            GENERATE HASP SJB DSECT          KHP
*<-------RESUME VANILLA HASPRDR CODE AT THIS POINT--------------->* KHP
***********************************************************************
         MEXIT
.PART3   ANOP
***********************************************************************
*   KHPRDC MODIFICATION TO HASPRDR TO SET DEFAULT ROUTE = SUBMITTER.  *
*   PART 3 FOLLOWS LINE R0324000.                                     *
***********************************************************************
         $PIT  DOC=&DOC            GENERATE HASP PIT DSECT          KHP
*<-------RESUME VANILLA HASPRDR CODE AT THIS POINT--------------->* KHP
***********************************************************************
         MEXIT
.PART4   ANOP
***********************************************************************
*   KHPRDC MODIFICATION TO HASPRDR TO SET DEFAULT ROUTE = SUBMITTER.  *
*   PART 4 FOLLOWS LINE R2598000.                                     *
***********************************************************************
         TM    PCEID,PCEINRID     IS THIS AN INTERNAL READER?       KHP
         BZ    CSG1S$99           NO, BYPASS THIS SECTION           KHP
         L     R1,RIDASCBP        GET ADDR OF ASCB                  KHP
         LA    R1,0(,R1)           PURIFY                           KHP
         LTR   R1,R1               HAS EOM OCCURRED ?               KHP
         BZ    CSG1S$99            YES: EXIT                        KHP
         L     R1,ASCBJBNI-ASCB(,R1) GET ADDR OF JOBNAME            KHP
         L     R14,$PITABLE       GET ADDR OF PITS                  KHP
CSG1S$05 TM    PITSTAT-PITDSECT(R14),PITBUSY    IS IT BUSY          KHP
         BZ    CSG1S$10                     NO, GETNEXT PIT         KHP
         L     R15,PITSJB-PITDSECT(,R14)    IS THIS THE             KHP
*        L     R15,SJBJQE-SJBDSECT(,R15)      PIT FOR THE           KHP
         L     R15,SJBJQOFF-SJBDSECT(,R15)    PIT FOR THE           KHP
         AL    R15,$JOBQPTR                                         KHP
*                                                                   KHP
         CLC   0(8,R1),QUEJNAME(R15)            SUBMITTING JOB      KHP
         BE    CSG1S$15                     YES, GO GET ROUTECDE    KHP
CSG1S$10 NULL                                                       KHP
         L     R14,PITNEXT-PITDSECT(,R14)  ADDR OF NEXT PIT         KHP
         LTR   R14,R14                 TEST FOR VALID ADDR          KHP
         BZ    CSG1S$90                NO TAKE DEFAULTS             KHP
         B     CSG1S$05                GO TEST IT                   KHP
         SPACE 1                                                    KHP
CSG1S$15 NULL                                                       KHP
         MVC   JCTPROUT(4),QUEPRTRT(R15)                            KHP
         L     R15,PITSJB-PITDSECT(,R14)    GET INPUT ROUTE CODE    KHP
         L     R15,SJBJCT-SJBDSECT(,R15)         FROM THE JCT OF    KHP
         MVC   JCTROUTE(2),JCTROUTE-JCTDSECT(R15)   SUBMITTING JOB  KHP
CSG1S$90 NULL                                                       KHP
         L     R1,PCEDCT          RESTORE INPUT DCT ACCRESS         KHP
CSG1S$99 NULL                                                       KHP
*<-------RESUME VANILLA HASPRDR CODE AT THIS POINT--------------->* KHP
***********************************************************************
         MEND
