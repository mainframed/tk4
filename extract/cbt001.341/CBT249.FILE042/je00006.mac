./       CHANGE NAME=HASPSSSM
         JE00006A S1   /* NOTIFY/DISPLAY RC/CC MODIFICATION */      KHP
         JE00006A S2   /* NOTIFY/DISPLAY RC/CC MODIFICATION */      KHP
         AGO   .JE060D1   /* REMOVE HJEM1 WTO ......        */      KHP
.JE060D1 ANOP                                                       KHP
         JE00006A S3   /* NOTIFY/DISPLAY RC/CC MODIFICATION */      KHP
./       CHANGE NAME=HASPPRPU
         JE00006A P1   /* NOTIFY/DISPLAY RC/CC MODIFICATION */      KHP
         JE00006A P2   /* NOTIFY/DISPLAY RC/CC MODIFICATION */      KHP
         AGO   .JE06PD3   /* REMOVE MSG165 DC .......       */      KHP
.JE06PD3 ANOP                                                       KHP
         JE00006A P3   /* NOTIFY/DISPLAY RC/CC MODIFICATION */      KHP
./       ADD   NAME=JE00006A
         DUMMY ENTRY TO BE REPLACED BY REPL
./       REPL  NAME=JE00006A
         MACRO
&L       JE00006A &V
         GBLC  &MID         /* FOR INTERNAL WTO MACRO */            KHP
.**********************************************************************
.*                                                                    *
.*       KHPRDC HASPSSSM/HASPPRPU MODIFICATION TO DISPLAY             *
.*       JOB RETURN/COMPLETION CODE AS PART OF NOTIFY FUNCTION,       *
.*       AND TO DISPLAY JOB RC/CC OR 'FAILED-JCL ERROR' ON $HASP395   *
.*       JOB ENDED MESSAGE (REPLACE IEF453 MESSAGE + GIVE OPERATORS   *
.*       JOB RETURN/COMPLETION CODES WHICH THEY SEEM TO WANT).        *
.*                                                                    *
.*       KAISER/PERMANENTE MEDICAL CARE PROGRAM                       *
.*       NORTHERN CALIFORNIA REGIONAL DATA CENTER                     *
.*       945 CAMELIA STREET, BERKELEY, CALIFORNIA 94710               *
.*       D. THEWLIS, JUNE 1, 1980.   (415) 428-6350                   *
.*                                                                    *
.*       MODIFICATION LEVEL:  FOR SU 825, JES2 RELEASE 4.1,           *
.*                            MSE/IPO 4.0 (3.8B) AT PTF LEVEL 8001,   *
.*                            INCL.   AZ27300 (CKPT DUPLEXING).       *
.*                                                                    *
.*       NOTE THAT JE00006A IS CALLED WITH PARM 'S1/S2/S3' TO EXPAND  *
.*       HASPSSSM CODE; PARM 'P' TO EXPAND HASPPRPU CODE.             *
.*                                                                    *
.*       AN EARLIER VERSION OF THIS MODIFICATION WAS ORIGINALLY       *
.*       DESIGNED TO DO NOTIFY ONLY -- FROM GTE LABORATORIES --       *
.*       AND WAS TAILORED FROM THE MOD ON THE CBTMODS TAPE            *
.*       VERSION 110, FILE 232.  THANK YOU GTE.                       *
.*                                                                    *
.*       NOTE THAT THIS MODIFICATION REQUIRES IEFACTRT TO SET UNUSED  *
.*       FIELD SCTNSMSG+1 OF THE SCT TO '77' IF JOBSTEP SEEN BY       *
.*       IEFACTRT (NOT BYPASSED BY RESTART) AND WAS NOT FLUSHED BY    *
.*       CONDITION CODES (IF THE STEP WAS NOT EXECUTED, SCTSEXEC IS   *
.*       THE LENGTH OF THE PARM FIELD AND THE HIGH CC WOULD BE        *
.*       MEANINGLESS).                                                *
.*                                                                    *
.*       (AT ENTRY TO IEFACTRT, R12 POINTS TO THE LCT; LCTSCTAD       *
.*       (X'14' OFFSET INTO THE LCT) POINTS TO THE CURRENT SCT;       *
.*       SCTNSMSG (X'42' OFFSET INTO THE SCT) + 1 (1-BYTE) UNUSED     *
.*       SET TO X'77' IF IEFACTRT ENTERED FOR STEP TERMINATION AND    *
.*       BIT 7 OF SMF4STI (FLUSH BIT) IS NOT ON.)                     *
.*                                                                    *
.*       D. THEWLIS, NOVEMBER 3, 1979.                                *
.*                                                                    *
.**********************************************************************
         AIF   ('&V' EQ 'P1').PRPU1
         AIF   ('&V' EQ 'P2').PRPU2
         AIF   ('&V' EQ 'P3').PRPU3
         AIF   ('&V' EQ 'S1').SSSM1
         AIF   ('&V' EQ 'S2').SSSM2
         AIF   ('&V' EQ 'S3').SSSM3
         MNOTE 'INVALID PARAMETER - &V'
         MEXIT
.SSSM1   ANOP
         EJECT
***********************************************************************
*   KHPRDC MODIFICATON TO PUT RETURN/COMPLETION CODE IN JES2          *
*   NOTIFY MESSAGE TO TSO USERS ABOUT BACKGROUND JOB COMPLETION;      *
*   PUT RC/CC IN $HASP395 ENDED MSG TO OPERATORS; AND PUT JCL ERROR   *
*   MESSAGE ON END OF $HASP395 MESSAGE TO REPLACE IEF453 MESSAGE.     *
*   SCTNSMSG+1 (RESERVED) MUST BE SET TO X'77'  BY PARALLEL IEFACTRT  *
*   EXTENSION IF CONTENTS ARE VALID (STEP NOT FLUSHED OR BYPASSED     *
*   DURING RESTART); ELSE THIS LOGIC WILL NEVER GET A RETURN CODE.    *
*   PART 1A: HASPSSSM MODIFICATION.  THIS CODE FOLLOWS LINE T2269000. *
***********************************************************************
         CLI    SJBXQFN1+1,SSOBTERM WAS ENTRY FROM JOB TERM?        KHP
         BNE    JE061OUT      IF NOT, BYPASS COMPUTATION ENTIRELY   KHP
         SR     R0,R0         ZERO REGISTER                         KHP
         PUSH   USING           SAVE USING STATUS                   KHP
         USING  INJMJCT,R4      TELL ASSEMBLER                      KHP
         TM     JCTJSTAT,JCTABEND WAS JOB ABENDED                   KHP
         BNO    JE0611           NO                                 KHP
         L      R0,JCTACODE      GET ABEND CODE                     KHP
         B      JE0613           GO TO FINISH                       KHP
JE0611   L      R1,JCTSDKAD           FIRST SCT   ADDRESS           KHP
         USING  IEFASCTB,R1      TELL ASSEMBLER                     KHP
JE0614   SRL    R1,8             GET ADDRESS IN PROPER FORM         KHP
         LTR    R1,R1                  TEST FOR ZERO (END)          KHP
         BZ     JE0613           GO TO FINISH                       KHP
         CLI    SCTNSMSG+1,X'77' MARKED AS OKAY FROM IEFACTRT?      KHP
         BNE    JE0615           NO, IGNORE THIS STEP               KHP
         CH     R0,SCTSEXEC      TEST FOR NEW MAX                   KHP
         BNL    JE0615            NO                                KHP
         LH     R0,SCTSEXEC       YES SAVE NEW MAX                  KHP
JE0615   L      R1,SCTANSCT      GET NEXT SCT                       KHP
         B      JE0614           LOOP                               KHP
JE0613   ST     R0,JCTCNVRC      SAVE IT                            KHP
         MVI    JCTCNVRC,X'77'   MARK IT AS OURS                    KHP
JE061OUT DS     0H               RETURN TO NORMAL PROCESSING        KHP
         POP    USING            RESTORE USINGS                     KHP
*--------RESUME JES2 HASPSSSM VANILLA CODE AT THIS POINT -------->* KHP
***********************************************************************
         MEXIT
.SSSM2   ANOP
***********************************************************************
*   KHPRDC MODIFICATON TO PUT RETURN/COMPLETION CODE IN JES2          *
*   $HASP395 MESSAGE TO CONSOLE OPERATOR; ALSO JCL FLUSH MESSAGE.     *
*   PART 1B: HASPSSSM MODIFICATION.  THIS CODE FOLLOWS LINE T2271000. *
***********************************************************************
         TM     SJBFLG4,SJB4MEND   IS THIS ENDED CONDITION?         KHP
         BNO    JE0612EN           NOT ENDED; BYPASS MODIFICATION   KHP
         LH     R14,0(,R1)         PICK UP WTO L-FORM LENGTH        KHP
         LA     R14,3(,R14)        INCREMENT BY DESCODES LENGTH     KHP
         EX     R14,*+8            EX-MOVE MSG  TO JCT WORK AREA    KHP
         B      *+10               BRANCH AROUND EX/MOVE            KHP
         MVC    JCTWORK(0),0(R1)   MOVE MSG TO MODIFIABLE AREA      KHP
*        BEGIN MODIFICATION OF $HASP395 MESSAGE                     KHP
         LA     R1,JCTWORK         POINT TO MODIFIABLE MESSAGE      KHP
         TM     JCTJTFLG,JCTJTJF      IS IT JCL FLUSH?              KHP
         BNO    JE061277              NO SKIP                       KHP
         MVC    18(22,R1),=CL22'- JOB FAILED-JCL ERROR'             KHP
         B      JE0612EN              GO ISSUE MESSAGE              KHP
JE061277 CLI    JCTCNVRC,X'77'  IS IT OURS ?                        KHP
         BNE    JE0612EN        NO SKIP                             KHP
         PUSH   USING           SAVE USING STATUS AGAIN             KHP
         USING  INJMJCT,R4      ADDRESS OS JCT                      KHP
         TM     JCTJSTAT,JCTABEND   ABENDED?                        KHP
         POP    USING          RESTORE USING STATUS                 KHP
         BZ     JE0612R        NO ABEND                             KHP
         MVC    18(22,R1),=CL22'- ABENDED SYSTEM XXX  '             KHP
         SR     R14,R14         CLEAR REGISTER TO RECEIVE CODE      KHP
         ICM    R14,7,JCTCNVRC+1    GET SYSTEM/USER CC/RC           KHP
         SRL    R14,12          GET SYSTEM CODE IN                  KHP
         LTR    R14,R14         TEST SYSTEM CODE                    KHP
         BZ     JE0612U         USER CODE                           KHP
         STH    R14,48(,R1)     3 HEX CHARACTERS + GARBAGE          KHP
         MVI    50(R1),X'0F'    GIVE THEM A BOGUS 3RD BYTE          KHP
         UNPK   43(5,R1),48(3,R1)   UNPACK BINARY STRING            KHP
         XC     35(3,R1),35(R1) ZERO RECEIVE FIELD                  KHP
         MVN    35(3,R1),44(R1) MOVE ONLY NUMERIC PORTION TO MSG    KHP
         TR     35(3,R1),JE0612TL        TRANSLATE TO HEX           KHP
         B      JE0612EN        EXIT TO ISSUE MESSAGE               KHP
JE0612R  MVC    18(22,R1),=CL22'- MAX COND CODE XXXX  '             KHP
         B      JE0612C     GO CONVERT TO DEC/PRINTABLE             KHP
JE0612U  MVC    18(22,R1),=CL22'- ABENDED  USER XXXX  '             KHP
JE0612C  LH     R14,JCTCNVRC+2 GET MAX COND/USER ABEND              KHP
         CVD    R14,48(R1)     CONVERT TO DECIMAL                   KHP
         UNPK   34(4,R1),48(8,R1)   UNPK FOUR LOW DIGITS            KHP
         OI     37(R1),X'F0'  SET ZONE                              KHP
         B      JE0612EN      BRANCH AROUND CONVERSION TABLE        KHP
JE0612TL DC     CL16'0123456789ABCDEF'    CONVERSION TABLE          KHP
JE0612EN DS     0H       BE SURE OF ALLIGNMENT                      KHP
*--------RESUME JES2 HASPSSSM VANILLA CODE AT THIS POINT -------->* KHP
***********************************************************************
         MEXIT
.SSSM3   ANOP
***********************************************************************
*   KHPRDC MODIFICATON TO PUT RETURN/COMPLETION CODE IN JES2          *
*   $HASP395 MESSAGE TO CONSOLE OPERATOR; ALSO JCL FLUSH MESSAGE.     *
*   PART 1C: HASPSSSM MODIFICATION.  THIS CODE REPLACES LINE T2282000 *
***********************************************************************
HJEM1    WTO   '&MID.ENDED- NO RC/CC AVAILABLE  ',ROUTCDE=2,DESC=6, KHPC
               MF=L                                                 KHP
*--------RESUME JES2 HASPSSSM VANILLA CODE AT THIS POINT -------->* KHP
***********************************************************************
         MEXIT
.PRPU1   ANOP
***********************************************************************
*   KHPRDC MODIFICATON TO PUT RETURN/COMPLETION CODE IN JES2          *
*   NOTIFY MESSAGE TO TSO USERS ABOUT BACKGROUND JOB COMPLETION.      *
*   PART 2A - HASPPRPU MODIFICATION.  THIS CODE FOLLOWS LINE Q0557000 *
***********************************************************************
         CLI    JCTCNVRC,X'77'  IS IT OURS ?                        KHP
         BNE    JE0621          NO SKIP                             KHP
         CLI    JCTJTFLG,JCTJTJF      IS IT OURS ?                  KHP
         BE     JE0621                NO SKIP                       KHP
         TM     JCTJTFLG,JCTJTABD   ABENDED?                        KHP
         BZ     JE0622         NO ABEND                             KHP
         MVC    0(22,R1),=C'- ABENDED  SYSTEM XXX '                 KHP
         L      R2,JCTCNVRC     GET CODE                            KHP
         LA     R2,0(R2)        CLEAR HIGH ORDER                    KHP
         SRL    R2,12           GET SYSTEM CODE IN                  KHP
         LTR    R2,R2           TEST SYSTEM CODE                    KHP
         BZ     JE0623          USER CODE                           KHP
         SRDL   R2,12           CONVERT IT TO HEX                   KHP
         SR     R2,R2                   "                           KHP
         SLDL   R2,4                    "                           KHP
         IC     R2,JE062TL(R2)          "                           KHP
         STC    R2,18(R1)               "                           KHP
         SR     R2,R2                   "                           KHP
         SLDL   R2,4                    "                           KHP
         IC     R2,JE062TL(R2)          "                           KHP
         STC    R2,19(R1)               "                           KHP
         SR     R2,R2                   "                           KHP
         SLDL   R2,4                    "                           KHP
         IC     R2,JE062TL(R2)          "                           KHP
         STC    R2,20(R1)               "                           KHP
         B      JE0624      GO INCREMENT LINE LENGTH                KHP
JE0622   MVC    0(22,R1),=C'- MAX COND CODE XXXX  '                 KHP
         B      JE0625      GO CONVERT TO DEC/PRINTABLE             KHP
JE0623   MVC    0(22,R1),=C'- ABENDED  USER XXXX  '                 KHP
JE0625   LH     R2,JCTCNVRC+2  GET MAX COND/USER ABEND              KHP
         CVD    R2,16(R1)      CONVERT TO DECIMAL                   KHP
         UNPK   16(4,R1),16(8,R1)   UNPK FOUR LOW DIGITS            KHP
         OI     19(R1),X'F0'  SET ZONE                              KHP
         MVI    20(R1),X'40'  CLEAR LAST BYTES                      KHP
         MVI    21(R1),X'40'  CLEAR LAST BYTE                       KHP
JE0624   LA     R1,22(R1)    INCREMENT LENGTH                       KHP
         B      OPLOUSM2     RETURN TO NORMAL PROCESSING            KHP
JE062TL  DC     CL16'0123456789ABCDEF'    CONVERSION TABLE          KHP
JE0621   DS     0H       BE SURE OF ALLIGNMENT                      KHP
*--------RESUME JES2 HASPPRPU VANILLA CODE AT THIS POINT -------->* KHP
***********************************************************************
         EJECT
         MEXIT
.PRPU2   ANOP
***********************************************************************
*   KHPRDC MODIFICATON TO PUT DATE/TIME AND RC/CC CODE IN JES2        *
*   NOTIFY MESSAGE TO TSO USERS ABOUT BACKGROUND JOB COMPLETION.      *
*   PART 2B - HASPPRPU MODIFICATION.  THIS CODE FOLLOWS LINE Q0575000 *
***********************************************************************
         UNPK  2(5,R1),JCTXDTOF+1(3) DATE OFF EXECUTION PROCESSOR   KHP
         MVC   1(2,R1),2(R1)       SHIFT THE YEAR LEFT BY ONE BYTE  KHP
         MVI   3(R1),C'.'          SET '.' IN 'YY.DDD'              KHP
         SR    R14,R14             SETUP TO DO TIME OF DAY          KHP
         ICM   R15,15,JCTXEQOF       TIME OFF EXECUTION PROCESSOR   KHP
         D     R14,=F'100'         STRIP OFF 100THS SECONDS         KHP
         SR    R14,R14             AND DISCARD                      KHP
         D     R14,=F'60'          GET SECONDS                      KHP
         CVD   R14,JCTWORK+116     CONVERT TO DECIMAL               KHP
         OI    JCTWORK+123,X'0F'   SET SIGN                         KHP
         UNPK  13(3,R1),JCTWORK+122(2) UNPACK SECONDS               KHP
         MVI   13(R1),C'.'         SET '      .SS'                  KHP
         SR    R14,R14             SETUP FOR MINUTES                KHP
         D     R14,=F'60'          GET MINUTES                      KHP
         CVD   R14,JCTWORK+116     CONVERT TO DECIMAL               KHP
         OI    JCTWORK+123,X'0F'   SET SIGN                         KHP
         UNPK  10(3,R1),JCTWORK+122(2) UNPACK MINUTES               KHP
         MVI   10(R1),C'.'         SET '   .MM.SS'                  KHP
         CVD   R15,JCTWORK+116     CONVERT HOURS TO DECIMAL         KHP
         OI    JCTWORK+123,X'0F'   SET SIGN                         KHP
         UNPK  7(3,R1),JCTWORK+122(2)  UNPACK HOURS                 KHP
         MVI   7(R1),C' '          SET ' HH.MM.SS'                  KHP
         LA    R1,16(,R1)          INCREMENT LENGTH                 KHP
*--------RESUME JES2 HASPPRPU VANILLA CODE AT THIS POINT -------->* KHP
***********************************************************************
         MEXIT
.PRPU3   ANOP
***********************************************************************
*   KHPRDC MODIFICATON TO PUT DATE/TIME AND RC/CC CODE IN JES2        *
*   NOTIFY MESSAGE TO TSO USERS ABOUT BACKGROUND JOB COMPLETION.      *
*   PART 2C - HASPPRPU MOD.  THIS CODE REPLACES LINE Q0990000.        *
***********************************************************************
MSG165   DC    CL102'IDSE ''&MID.JOBNNNNN  JOBNAMES'                KHP
*--------RESUME JES2 HASPPRPU VANILLA CODE AT THIS POINT -------->* KHP
***********************************************************************
         MEND
