./       CHANGE  NAME=HASPRDR
         JE00003A R    /* ROUTE CARD MODIFICATION FOR JCTPROUT FLAG */
         JE00003A J    /* JOB CARD MODIFICATIONS */
./       ADD   NAME=JE00003A
         DUMMY TO BE REPLACED BY REPL
./       REPL  NAME=JE00003A
         MACRO
&L       JE00003A &V
.**********************************************************************
.*                                                                    *
.*       KHPRDC HASPRDR MODIFICATION FOR ROUTE CARD PROCESSING        *
.*       AND JOB CARD PROCESSING TO PASS VALUES TO IEFUJV EXIT.       *
.*       THE JOB CARD MODIFICATION ALSO SETS DEFAULT OUTPUT LIMITS    *
.*       FOR SCHEDULED JOBS AND THE DEFAULT MSGCLASS FOR TSO-         *
.*       SUBMITTED BACKGROUND JOBS.                                   *
.*                                                                    *
.*       KAISER/PERMANENTE MEDICAL CARE PROGRAM                       *
.*       NORTHERN CALIFORNIA REGIONAL DATA CENTER                     *
.*       945 CAMELIA STREET, BERKELEY, CALIFORNIA 94710               *
.*       D. THEWLIS, JUNE 1, 1980.   (415) 428-6350                   *
.*                                                                    *
.*       MODIFICATION LEVEL:  FOR SU 825, JES2 RELEASE 4.1,           *
.*                            MSE/IPO 4.0 (3.8B) AT PTF LEVEL 8001,   *
.*                            INCL.   AZ27300 (CKPT DUPLEXING).       *
.*                                                                    *
.*       THIS MACRO GENERATES THE ROUTE CARD CODE IF 'R' IS CODED     *
.*       AS THE PARAMETER, AND THE JOB CARD CODE IF 'J' IS CODED      *
.*       AS THE PARAMETER.                                            *
.*                                                                    *
.**********************************************************************
         AIF   ('&V' EQ 'J').JOBCARD
         AIF   ('&V' EQ 'R').ROUTEC
         MNOTE 12,'INVALID PARAMETER - &V'
         MEXIT
.ROUTEC  ANOP
         EJECT
***********************************************************************
*                                                                     *
*   KHPRDC HASPRDR ROUTE CARD PROCESSING MODIFICATION                 *
*                                                                     *
*        1.  IF JCTPROUT = ANY LOCAL PRINTER (ROUTE = 0N00)           *
*            SET HIGH-ORDER BIT OF JCTROUTE IN JCTUSEID ON            *
*            FOR IEFUJV DEST=LOCAL REMOVAL SCAN.  (THIS CODE IS       *
*            DUPLICATED IN THE JOB CARD PROCESSING CODE IN CASE       *
*            JOB HAS NO ROUTE CARD FOR PRINT.)                        *
*                                                                     *
*        2.  IF JCTPROUT = SPECIAL LOCAL PRINTER (ROUTE = 00NN)       *
*            OR REMOTE ROUTING (ROUTE = 0N0N) TURN BIT OFF IN         *
*            JCTUSEID (IN CASE WAS TURNED ON BY JOB CARD ROUTINE      *
*            AND ROUTE CARD HAS CHANGED DEFAULT PRINT ROUTING).       *
*                                                                     *
*        THIS PART  FOLLOWS LINE R1526000 (INVALID DESTINATION)       *
*        AND CONSTITUTES VALID DESTINATION (FROM RDESTSCN)            *
*        PROCESSING PRIOR TO BRANCHING TO ROPSHIFT (R1526100).        *
*                                                                     *
***********************************************************************
*        SET   PRINT ROUTE FOR IEFUJV AS APPROPRIATE.               KHP
         CLI   JCTPROUT,X'00'      ROUTE = 00NN IS SPECIAL LOCAL    KHP
         BE    JE03ROFF            YES, TURN OFF DEFLOCAL BIT       KHP
         CLI   JCTPROUT+1,X'00'    ROUTE = NN00 IS DEFAULT LOCAL    KHP
         BNE   JE03ROFF            BIT OFF; 0N0N IS REMOTE ROUTE    KHP
         OI    JCTUSEID,X'80'      IEFUJV: JCTPROUT IS ANY LOCAL    KHP
         B     JE03ROUT            EXIT FROM MODIFICATION           KHP
JE03ROFF NI    JCTUSEID,X'7F'      IEFUJV: JCTPROUT IS RMT OR SPCL  KHP
JE03ROUT EQU   *                   EXIT FROM ROUTE CARD MOD.        KHP
*<------ RESUME VANILLA JES2 HASPRDR CODE AT THIS POINT --------->* KHP
***********************************************************************
         EJECT
         MEXIT
.JOBCARD ANOP
         EJECT
***********************************************************************
*                                                                     *
*   KHPRDC HASPRDR JOB CARD PROCESSING MODIFICATIONS                  *
*                                                                     *
*        1.  JCTPASS ----> JMRUSEID+2(6) FOR IEFUJV EXIT ROUTINE      *
*            (NOTE:  THIS MEANS ONLY FIRST 6 CHARACTERS OF USER       *
*            SUPPLIED PASSWORD IS CHECKED AGAINST TABLE PASSWORD).    *
*        2.  JCTROUTE ---> JMRUSEID(2) FOR IEFUJV EXIT ROUTINE.       *
*        3.  IF JCTPROUT = ANY LOCAL PRINTER (ROUTE = 0N00)           *
*            SET HIGH-ORDER BIT OF JCTROUTE IN JMRUSEID ON            *
*            FOR IEFUJV DEST=LOCAL REMOVAL SCAN.  (THIS CODE IS       *
*            DUPLICATED IN ROUTE CARD PROCESSING MODIFICATIONS        *
*            IN CASE JOB HAS ROUTE CARD FOR PRINT.)                   *
*        4.  IF JCTMCLAS = 0 AND JCTINRDR ON THEN JCTMCLAS = A        *
*            IF BACKGROUND BATCH JOB.  (TSUMCLAS=0 AND $$0=DUMMY SO   *
*            TSO LOGON JCL GOES TO DUMMY SYSOUT CLASS.  THIS MOD      *
*            REVERTS JOB MESSAGE CLASS FOR TSO-SUBMITTED JOB TO A     *
*            IF DEFAULTED TO TSUMCLAS OF 0 DUMMY).                    *
*        6.  IF JCLJCLAS >= 0 (SCHEDULED JOB CLASS) THEN SET          *
*            JCTLINCT, JCTESTLN AND JCTESTPU TO SCHED JOB DEFAULTS    *
*            (VERY HIGH).  MAY BE SUBSEQUENTLY OVERRIDEN BY /*JOBPARM *
*            VALUES WHICH IS FINE -- WE ARE JUST REDEFINING DEFAULTS. *
*                                                                     *
*        THIS CODE  FOLLOWS LINE R2988500                             *
*                                                                     *
***********************************************************************
*        SET   PASSWORD, JOB ROUTE, PRINT ROUTE FOR IEFUJV.         KHP
         MVC   JCTUSEID(2),JCTROUTE JOB INPUT ROUTE CODE FOR IEFUJV KHP
         MVC   JCTUSEID+2(6),JCTPASS PASSWORD (6 BYTES) FOR IEFUJV  KHP
         CLI   JCTPROUT,X'00'      ROUTE = 00NN IS SPECIAL LOCAL    KHP
         BE    JE03JSK1            YES, IGNORE IT                   KHP
         CLI   JCTPROUT+1,X'00'    ROUTE = NN00 IS DEFAULT LOCAL    KHP
         BNE   JE03JSK1            IGNORE; 0N0N IS REMOTE ROUTE     KHP
         OI    JCTUSEID,X'80'      IEFUJV: JCTPROUT IS ANY LOCAL    KHP
*        MSGCLASS ALTERATION IF BATCH SUBMITTED FROM TSO INTRDR.    KHP
JE03JSK1 TM    JCTJOBFL,JCTBATCH   IS THIS A BACKGROUND BATCH JOB?  KHP
         BNO   JE03JSK2            NO, SKIP MSGCLASS ALTER LOGIC.   KHP
         TM    JCTJBOPT,JCTINRDR   *********************************KHP
         BZ    JE03JSK2            * TSUMCLAS=0 + $$0=DUMMY TO KEEP*KHP
         CLI   JCTMCLAS,C'0'       * TSO LOGON JCL FROM PRINTING.  *KHP
         BNE   JE03JSK2            * THIS LOGIC MAKES THE DEFAULT  *KHP
         MVI   JCTMCLAS,C'A'       * MSGCLASS FOR TSO-SUB JOBS 'A'.*KHP
*                                  *********************************KHP
*        RESET DEFAULT JCTLINCT, JCTESTLN, JCTESTPU IF PRODUCTION   KHP
JE03JSK2 CLI   JCTJCLAS,C'0'       IF ^= PRODUCTION (NUMERIC CLASS) KHP
         BL    JE03JOUT            DO NOT CHANGE.                   KHP
*        MVI   JCTLINCT,0          ***NEW DEFAULT FOR SCHED JOBS*** KHP
*------> CHANGE TO 'MVI JCTLINCT,VAL TO ENFORCE NEW DEFAULT <------ KHP
         MVC   JCTESTLN,=F'9999'   ***MAX DEFAULT FOR SCHED JOBS*** KHP
         MVC   JCTESTPU,=F'9999999' **MAX DEFAULT FOR SCHED JOBS*** KHP
JE03JOUT DS    0H                  BYPASS DEFAULT VALUE CHANGES     KHP
*<------ RESUME VANILLA JES2 HASPRDR CODE AT THIS POINT --------->* KHP
***********************************************************************
         MEND
         EJECT
