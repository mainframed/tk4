./       CHANGE NAME=HASPCOMM
         JE00005A S        /* INITIALIZE LINE COUNT */
         JE00005A I        /* ADD # LINES TO TOTAL */
         JE00005A O        /* # LINES TO $DF MESSAGE*/
./       ADD   NAME=JE00005A
         DUMMY TO BE REPLACED BY REPL
./       REPL  NAME=JE00005A
         MACRO
&L       JE00005A &V
.**********************************************************************
.*                                                                    *
.*       KHPRDC HASPCOMM $DF MODIFICATION TO ADD OUTPUT               *
.*       COUNT TO $DF COMMAND.                                        *
.*                                                                    *
.*       KAISER/PERMANENTE MEDICAL CARE PROGRAM                       *
.*       NORTHERN CALIFORNIA REGIONAL DATA CENTER                     *
.*       945 CAMELIA STREET, BERKELEY, CALIFORNIA 94710               *
.*       D. THEWLIS, JUNE 1, 1980.   (415) 428-6350                   *
.*                                                                    *
.*       MODIFICATION LEVEL:  FOR SU 825, JES2 RELEASE 4.1,           *
.*                            MSE/IPO 4.0 (3.8B) AT PTF LEVEL 8001,   *
.*                            INCL.   AZ27300 (CKPT DUPLEXING).       *
.*                                                                    *
.*       JE00005A GENERATES ALL CODE FOR THE MODIFICATION, AND        *
.*       IS INVOKED AT THREE PLACES WITH A PARAMETER OF 'S'           *
.*       (SETUP COUNT REGISTER); 'I' (INCREMENT COUNT REGISTER);      *
.*       'O' (OUTPUT COUNT IN $DF RESPONSE).                          *
.*                                                                    *
.*       THIS MODIFICATION IS ORIGINALLY FROM GET LABORATORIES AND    *
.*       WAS TAILORED FROM THE MOD ON THE CBTMODS TAPE VERSION 110,   *
.*       FILE 230.  THANK YOU GTE.                                    *
.*                                                                    *
.**********************************************************************
         AIF   ('&V' EQ 'O').OUTPUT
         AIF   ('&V' EQ 'I').INCR
         AIF   ('&V' EQ 'S').SETUP
         MNOTE 12,'INVALID PARAMETER -- &V'
         MEXIT
.SETUP   ANOP
***********************************************************************
*   KHPRDC HASPCOMM MODIFICATION TO ADD # LINES TO $DF COMMAND.       *
*   PART 1 - SETUP COUNTER.  THIS INVOCATION FOLLOWS LINE K2292500    *
***********************************************************************
         SR    R10,R10   INITIALIZE RECORD COUNT                    KHP
*<-------RESUME VANILLA JES2 HASPCOMM CODE AT THIS POINT--------->* KHP
***********************************************************************
         MEXIT
.INCR    ANOP
***********************************************************************
*   KHPRDC HASPCOMM MODIFICATION TO ADD # LINES TO $DF COMMAND        *
*   PART 2 - ADD # LINES TO RUNNING COUNTER.  THIS INVOCATION         *
*   FOLLOWS LINE K2319500.                                            *
***********************************************************************
         A     R10,JOERECCT   ADD JOE RECORD COUNT TO TOTAL         KHP
*<-------RESUME VANILLA JES2 HASPCOMM CODE AT THIS POINT--------->* KHP
***********************************************************************
         MEXIT
.OUTPUT  ANOP
***********************************************************************
*   KHPRDC JES2 HASPCOMM MODIFICATION TO PUT # LINES IN $DF COMMAND.  *
*   PART 3 - OUTPUT TOTAL # LINES IN $DF COMMAND RESPONSE.            *
*   THIS INVOCATION FOLLOWS LINE # K2330000.                          *
***********************************************************************
         LR    R0,R10    GET TOTAL RECORD COUNT                     KHP
         CVD   R0,COMDWORK    CONVERT TO DECIMAL                    KHP
         MVC   5(8,WD),=XL8'4020202020202021'  SET MASK             KHP
         ED    5(8,WD),COMDWORK+4 EDIT IN TOTAL # LINES             KHP
         MVI   4(WD),C'('     MOVE IN (                             KHP
         MVI   13(WD),C')'    MOVE IN )                             KHP
         LA    WD,10(0,WD)    UP CURSOR BY LENGTH OF #LINES         KHP
*<-------RESUME VANILLA JES2 HASPCOMM CODE AT THIS POINT--------->* KHP
***********************************************************************
         MEND
