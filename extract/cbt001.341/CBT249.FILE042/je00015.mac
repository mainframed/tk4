./       CHANGE NAME=HASPSSSM  /* DEFAULT OCR TABLE           */    KHP
         AGO   .JE015D1        /* REMOVE BZ HCP495            */    KHP
.JE015D1 ANOP                  /* REMOVE BZ HCP495            */    KHP
         JE00015A 1            /* CHECK DEFAULT OCR TABLE     */    KHP
         JE00015A 2            /* SEARCH DEFAULT OCR TABLE    */    KHP
         JE00015A 3            /* INSERT LTORG                */    KHP
         JE00015A 4            /* DEFAULT OCR TABLE           */    KHP
./       ADD   NAME=JE00015A
DUMMY ENTRY FOR REPLACEMENT
./       REPL  NAME=JE00015A
         MACRO
&L       JE00015A &V
.**********************************************************************
.*                                                                    *
.*   KHPRDC MODIFICATION TO ESTABLISH A SET OF 'DEFAULT' OCRS FOR     *
.*   PREDEFINED PSEUDO-FORMS.  THIS MODIFICATION HAS SPECIAL USE IN   *
.*   CONJUNCTION WITH MOD JE00016 (EXTENSION OF /*OUTPUT TO JOBFORMS  *
.*   AND SPECIAL FORMS TYPE 'JCL ').                                  *
.*                                                                    *
.*   THE /*OUTPUT CARD RESULTS IN THE CREATION OF AN OUTPUT CONTROL   *
.*   RECORD (OCR).  THIS MOD PROVIDES AND USES A MACRO, $OCRD,        *
.*   WHICH IS USED TO ASSEMBLE A TABLE OF PERMANENT, PRE-BUILT        *
.*   OCR'S IN HASPSSSM, AND THE SUPPORT CODE TO SEARCH THE DEFAULT    *
.*   OCR TABLE AFTER THE SCAN OF OCR'S PROVIDED BY /*OUTPUT CARDS     *
.*   DURING PDDB CREATE.                                              *
.*                                                                    *
.*   NOTE ON DESTINATION AS SPECIFIED IN THE DEST PARAMETER OF THE    *
.*   $OCRD MACRO:  THE DEST FIELD IS 2 BYTES LONG.  IN NON-NJE        *
.*   VERSIONS OF JES2, THE HIGH-ORDER BYTE IS ALWAYS X'01', WHICH     *
.*   HAS THE EFFECT OF INCREMENTING ALL DESTINATIONS BY 256.  THUS,   *
.*   DEST=(256,258) WOULD ROUTE A COPY LOCALLY AND A COPY TO REMOTE2. *
.*                                                                    *
.*       KAISER/PERMANENTE MEDICAL CARE PROGRAM                       *
.*       NORTHERN CALIFORNIA REGIONAL DATA CENTER                     *
.*       945 CAMELIA STREET, BERKELEY, CALIFORNIA 94710               *
.*       D. THEWLIS, JUNE 1, 1980.   (415) 428-6350                   *
.*                                                                    *
.*       MODIFICATION LEVEL:  FOR SU 825, JES2 RELEASE 4.1,           *
.*                            MSE/IPO 4.0 (3.8B) AT PTF LEVEL 8001,   *
.*                            INCL.   AZ27300 (CKPT DUPLEXING).       *
.*                                                                    *
.*   THIS MODIFICATION IS ORIGINALLY FROM AMERICAN ELECTRIC POWER     *
.*   COMPANY AND WAS TAKEN FROM THE SHARE JES2 MODS TAPE, VERSION 16. *
.*   THANK YOU, AEP.                                                  *
.*                                                                    *
.**********************************************************************
         AIF   ('&V' EQ '1').FIRST
         AIF   ('&V' EQ '2').SECOND
         AIF   ('&V' EQ '3').THIRD
         AIF   ('&V' EQ '4').FOURTH
         MNOTE 'INVALID PARAMETER - &V'
         MEXIT
.FIRST   ANOP
***********************************************************************
* KHPRDC MODIFICATION TO HASPSSSM TO IMPLEMENT DEFAULT OCRS FOR PRE-  *
* DEFINED PSEUDOFORMS.  PART 1 - EXIT TO CHECK DEFAULT OCR TABLE IF   *
* NO /*OUTPUT FOR FORMS TYPE.  PART 1 REPLACES LINE T3333000.         *
***********************************************************************
         BZ    JE1501     IF NOT CHECK FOR DEFAULT OCR  OZ11741     KHP
*<-------RESUME VANILLA HASPSSSM CODE AT THIS POINT-------------->* KHP
***********************************************************************
         MEXIT
.SECOND  ANOP
***********************************************************************
* KHPRDC MODIFICATION TO HASPSSSM TO IMPLEMENT DEFAULT OCRS FOR PRE-  *
* DEFINED PSEUDOFORMS.  PART 2 - SEARCH DEFAULT OCR TABLE FOR FORMS   *
* TYPE.  PART 2 FOLLOWS LINE T339500.                                 *
***********************************************************************
JE1501   LA    R2,JE15OCRD       POINT TO DEFAULT OCR TABLE         KHP
JE1502   CL    R0,OCRCODE        DOES OCR ID MATCH SYSOUT FORMS     KHP
         BE    HCP400            YES - GO MERGE OCRD IN PDDB        KHP
         LA    R2,OCREND         NO  - POINT TO NEXT OCR IN TABLE   KHP
         CLI   0(R2),X'FF'       LAST OCRD IN TABLE?                KHP
         BNE   JE1502            NO  - GO CHECK THIS OCRD FOR FORMS KHP
         B     HCP495            NO DEFAULT OCR                     KHP
*<-------RESUME VANILLA HASPSSSM CODE AT THIS POINT-------------->* KHP
***********************************************************************
         MEXIT
.THIRD   ANOP
***********************************************************************
* KHPRDC MODIFICATION TO HASPSSSM TO IMPLEMENT DEFAULT OCRS FOR PRE-  *
* DEFINED PSEUDOFORMS.  PART 3 - LTORG TO AVOID ADDRESSIBILITY        *
* PROBLEMS FROM $OCRD MACRO.   PART 3 FOLLOWS LINE T3387500.          *
***********************************************************************
         LTORG ,                                                    KHP
*<-------RESUME VANILLA HASPSSSM CODE AT THIS POINT-------------->* KHP
***********************************************************************
         MEXIT
.FOURTH  ANOP
         EJECT
***********************************************************************
* KHPRDC MODIFICATION TO HASPSSSM TO IMPLEMENT DEFAULT OCRS FOR PRE-  *
* DEFINED PSEUDOFORMS.  PART 4 - DEFAULT OCR TABLE.                   *
* PART 4 FOLLOWS LINE T3411500.                                       *
***********************************************************************
* 1411 IS STANDARD 1-PART, LOCAL ROUTING ONLY.                      KHP
JE15OCRD $OCRD PFORMS=1411                                          KHP
* 5081 IS STANDARD CARDS;  LOCAL ROUTING ONLY.                      KHP
         $OCRD PFORMS=5081                                          KHP
* LOCL WILL BE STANDARD FORMS, LOCAL ROUTING ONLY.                  KHP
         $OCRD PFORMS=LOCL                                          KHP
* FICH WILL BE STANDARD FORMS, FICHE ROUTING ONLY (R1).             KHP
         $OCRD PFORMS=FICH,DEST=257                                 KHP
* BOTH WILL BE STANDARD FORMS, FICHE AND LOCAL ROUTING.             KHP
         $OCRD PFORMS=BOTH,DEST=(256,257)                           KHP
         $OCRD PFORMS=0000,END=YES                                  KHP
*<-------RESUME VANILLA HASPSSSM CODE AT THIS POINT-------------->* KHP
***********************************************************************
         MEND
./       ADD   NAME=$OCRD
         MACRO -- $OCRD GENERATES OCR DEFAULT TABLE ENTRY --        KHP
&NAME    $OCRD &PFORMS=,&COPY=0,&INDEX=0,&FORMS=,&FCB=,             KHP*
               &UCS=****,&RCL=0,&DEST=0,&END=NO                     KHP
         LCLA  &D1,&D2,&D3,&D4                                      KHP
         AIF   ('&PFORMS' NE '').M1                                 KHP
         MNOTE 12,'PSEUDO FORMS NOT SPECIFIED'                      KHP
         MEXIT                                                      KHP
.M1      ANOP                                                       KHP
         DC    0F'0'                                                KHP
&NAME    DC    CL4'&PFORMS'        OCRCODE  - PSEUDO FORMS TYPE     KHP
         DC    X'0000'             OCRFLAGS; RESERVED               KHP
         DC    AL1(&COPY)          OCRCOPY  - COPY COUNT            KHP
         DC    AL1(&INDEX)         OCRINDEX - 3211 INDEX            KHP
         AIF   ('&FORMS' EQ '').M4                                  KHP
         DC    CL4'&FORMS'         OCRFORMS - FORMS SPECIFICATION   KHP
         AGO   .M5                                                  KHP
.M4      DC    AL4(0)              OCRFORMS - FORMS SPECIFICATION   KHP
.M5      ANOP                                                       KHP
         AIF   ('&FCB' EQ '').M6                                    KHP
         DC    CL4'&FCB'           OCRFCB   - FCB SPECIFICATION     KHP
         AGO   .M7                                                  KHP
.M6      DC    AL4(0)              OCRFCB   - FCB SPECIFICATION     KHP
.M7      ANOP                                                       KHP
         DC    CL4'&UCS'           OCRUCS   - UCS SPECIFICATION     KHP
         DC    AL4(&RCL)           OCRRECNT - RECORD COUNT LIMIT    KHP
         DC    6CL4'****'     3800 OCRCHAR1-4; OCRFLASH; OCRMODF    KHP
         DC    8AL1(0)        3800 OCRCOPYG - COPY GROUPS           KHP
         DC    X'0000'        3800 OCRFLSHC; OCRMODFT               KHP
&D2      SETA  0                                                    KHP
&D3      SETA  0                                                    KHP
&D4      SETA  0                                                    KHP
&D1      SETA  &DEST(1)                                             KHP
         AIF   (N'&DEST LE 1).M2                                    KHP
&D2      SETA  &DEST(2)                                             KHP
         AIF   (N'&DEST LE 2).M2                                    KHP
&D3      SETA  &DEST(3)                                             KHP
         AIF   (N'&DEST LE 3).M2                                    KHP
&D4      SETA  &DEST(4)                                             KHP
.M2      DC    AL2(&D1,&D2,&D3,&D4) OCRDEST1-4 - DESTINATIONS       KHP
         DC    2AL1(0)             OCRCPTN; RESERVED                KHP
         DS    0F
         AIF   ('&END' EQ 'NO').M3                                  KHP
         DC    X'FF'               EOT                              KHP
.M3      MEND                                                       KHP
