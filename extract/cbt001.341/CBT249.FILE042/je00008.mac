./       CHANGE NAME=HASPRDR
         JE00008A R    /* FORCE STD FORMS/MSGCLASS ON JCL-FLUSH */
./       CHANGE NAME=HASPXEQ
         JE00008A X    /* FORCE STD FORMS/MSGCLASS ON JCL-FLUSH */
./       ADD   NAME=JE00008A
         DUMMY ENTRY TO BE REPLACED BY REPL
./       REPL  NAME=JE00008A
         MACRO
&L       JE00008A &V
.**********************************************************************
.*                                                                    *
.*       KHPRDC HASPRDR/HASXEQ MODIFICATION TO FORCE STANDARD FORMS   *
.*       AND MSGCLASS FOR JOBS WHICH JCL-FLUCH IN RDR OR CONVERSION   *
.*       (UNLESS JCL PDDB IS HELD FOR TSO RETRIEVAL)                  *
.*                                                                    *
.*       KAISER/PERMANENTE MEDICAL CARE PROGRAM                       *
.*       NORTHERN CALIFORNIA REGIONAL DATA CENTER                     *
.*       945 CAMELIA STREET, BERKELEY, CALIFORNIA 94710               *
.*       D. THEWLIS, JUNE 1, 1980.   (415) 428-6350                   *
.*                                                                    *
.*       MODIFICATION LEVEL:  FOR SU 825, JES2 RELEASE 4.1,           *
.*                            MSE/IPO 4.0 (3.8B) AT PTF LEVEL 8001,   *
.*                            INCL.   AZ27300 (CKPT DUPLEXING).       *
.*                                                                    *
.*                                                                    *
.*       NOTE THAT JE00008A IS CALLED WITH PARM 'R' TO EXPAND         *
.*       HASPRDR CODE; PARM 'X' TO EXPAND HASPXEQ CODE.               *
.*                                                                    *
.*       THIS MODIFICATION IS ORIGINALLY FROM BCG AND WAS TAILORED    *
.*       FROM THE MOD ON VERSION 5 OF THE JES2 MODS TAPE.             *
.*       THANK YOU BCG.                                               *
.*                                                                    *
.**********************************************************************
         AIF   ('&V' EQ 'R').RDR
         AIF   ('&V' EQ 'X').XEQ
         MNOTE 'INVALID PARAMETER - &V'
         MEXIT
.RDR     ANOP
***********************************************************************
*   KHPRDC MODIFICATON TO FORCE STANDARD FORMS AND MSGCLASS FOR JCL-  *
*   FLUSH JOBS UNLESS MSGCLASS IS HELD FOR TSO FOREGROUND RETRIEVAL.  *
*   PART 1 - HASPRDR MODIFICATION.  THIS CODE FOLLOWS LINE R4828000.  *
***********************************************************************
         MVC   JCTFORMS,$STDFORM  FORCE STANDARD FORMS              KHP
         CLI   JCTMCLAS,C'X'      KHPRDC STANDARD HELD MSGCLASS     KHP
         BE    *+8                YES, AVOID RESET OF MSGCLASS      KHP
         MVI   JCTMCLAS,C'A'      FORCE STANDARD MSGCLASS           KHP
*--------RESUME JES2 HASPRDR VANILLA CODE AT THIS POINT --------->* KHP
***********************************************************************
         MEXIT
.XEQ     ANOP
***********************************************************************
*   KHPRDC MODIFICATON TO FORCE STANDARD FORMS AND MSGCLASS FOR JCL-  *
*   FLUSH JOBS UNLESS MSGCLASS IS HELD FOR TSO FOREGROUND RETRIEVAL.  *
*   PART 2 - HASPXEQ MODIFICATION.  THIS CODE FOLLOWS LINE U1062000.  *
***********************************************************************
         CLC   JCTCNVRC,=A(JCTCOK) CONVERTER RETURN CODE OKAY?      KHP
         BE    XCNOJCLE            NO; JCL ERROR                    KHP
         L     R1,$STDFORM        GET STANDARD FORMS                KHP
         L     WA,JPCEIOT         GET ADDRESS OF FIRST IOT          KHP
         AL    WA,$IOTPDDB        ADDRESS OF FIRST PDDB             KHP
XCFORMS  EQU   PDBFORMS-PDBDSECT  COMPUTE RELATIVE OFFSET           KHP
*        SET STANDARD FORMS IN SYSTEM PDDBS                         KHP
* (IOTPDBOJ (JCL) PDDB NOT DONE BECAUSE USED FOR RDR-FLUSH ONLY)    KHP
         ST    R1,XCFORMS+IOTPDBOL(WA) JOB LOG                      KHP
         ST    R1,XCFORMS+IOTPDBOI(WA) JCL IMAGES                   KHP
         ST    R1,XCFORMS+IOTPDBOM(WA) MESSAGES                     KHP
*        RESET MSGCLASS TO KHPRDC STANDARD IF NOT HELD MSGCLASS     KHP
XCMCLASS EQU   PDBCLASS-PDBDSECT  COMPUTE RELATIVE OFFSET           KHP
         CLI   XCMCLASS+IOTPDBOJ(WA),C'X' JCL THE KHPRDC HELD CLASS?KHP
         BE    XCNOJCLE                YES, SKIP CLASS RESET        KHP
         MVI   XCMCLASS+IOTPDBOL(WA),C'A'  JOB LOG                  KHP
         MVI   XCMCLASS+IOTPDBOI(WA),C'A'  JCL IMAGES               KHP
         MVI   XCMCLASS+IOTPDBOM(WA),C'A'  MESSAGES                 KHP
*        REMOVE NOPRINT (DUMMY) BIT IF ON FOR SYSTEM PDDBS          KHP
XCMFLAG1 EQU   PDBFLAG1-PDBDSECT  COMPUTE RELATIVE OFFSET           KHP
         NI    XCMFLAG1+IOTPDBOL(WA),255-PDB1NSOT  JOB LOG          KHP
         NI    XCMFLAG1+IOTPDBOI(WA),255-PDB1NSOT  JCL IMAGES       KHP
         NI    XCMFLAG1+IOTPDBOM(WA),255-PDB1NSOT  MESSAGES         KHP
XCNOJCLE DS    0H       BYPASS FORMS/MSGCLASS RESET                 KHP
*--------RESUME JES2 HASPPRPU VANILLA CODE AT THIS POINT -------->* KHP
***********************************************************************
         MEND
