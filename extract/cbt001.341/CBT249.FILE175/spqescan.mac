SPQESCAN OSENTRY R12,*
         L     R1,0(,R1)                GET ADDRESS OF PARMS
         TM    1(R1),X'FF'              TEST FOR LENGTH OF PARM
         BZ    TIMES                    BRANCH AROUND SWITCH SET
         CLI   2(R1),C'Y'               TEST FOR CORRECT PARM
         BNE   TIME                     NO, BYPASS SWITCH
         MVI   FQESW,C'Y'               SET SWITCH TO DO FQE SCAN
TIMES    EQU   *
         TIME  DEC
         STM   R0,R1,TIMEDAY            SAVE DATE AND TIME
         OI    TIME+3,X'0F'             SET SIGN
         ED    EDTIME(8),TIME           FORMAT TIME
         ED    EDDAY(6),DAY+1           FORMAT DATE
         MVC   FQETIME(16),EDDAYTIM     MOVE DATE TO HEADING
         MVC   DQETIME(16),EDDAYTIM     MOVE DATE TO HEADING
         MVC   SQATIME(16),EDDAYTIM     MOVE DATE TO HEADING
         MVC   CSATIME(16),EDDAYTIM     MOVE DATE TO HEADING
         LA    R11,WORKAREA             GET ADDRESS OF WORKAREA
         LA    R10,WORKAREA             GET ADDRESS OF WORKAREA
         XR    R9,R9                    ZERO LINE COUNTER
         L     R2,16                    GET CVT ADDRESS
         L     R2,560(,R2)              GET GDA ADDRESS
         L     R3,24(,R2)               GET CSA SPQE ADDRESS
         B     SPQE                     GO LOOK AT SPQE
EXITSPQE BC    0,FORMAT                 SET UP DUMMY BRANCH
         MVI   EXITSPQE+1,X'F0'         SET UP BRANCH
         LA    R4,WORKAREA              GET START OF WORKAREA
         MVI   0(R4),C'S'               SET TO SQA
         MVI   FQESW,C'N'               SET SWITCH FOR NO FQE SCAN
         L     R3,52(,R2)               GET CSA SPQE ADDRESS
         B     SPQE                     GO LOOK AT SPQE
FORMAT   EQU   *
         OPEN  (LIST,OUTPUT)
         L     R2,16                    GET CVT ADDRESS
         L     R2,556(,R2)              GET ASVT ADDRESS
         LA    R2,524(,R2)              GET START OF ASCB POINTERS
         LA    R5,PRINT+1               SETUP PRINT AREA POINTER
         LA    R6,10                    SETUP LOOP COUNT
         MVI   PRTCC,C'1'               SET SKIP TO CHANNEL 1
JOBNAME  EQU   *
         LA    R2,4(,R2)                INCR INTO ASVT
         CLC   1(3,R2),=F'0'            IS THIS END OF ASVT
         BE    ENDJOBNM                 YES, EXIT FROM JOBNAME LOOP
         TM    0(R2),X'80'              IS ENTRY AVAILABLE
         BO    JOBNAME                  YES, CHECK NEXT
         L     R3,0(,R2)                GET ASCB ADDRESS
         L     R4,172(,R3)              GET INITIATED JOBNAME POINTER
         LTR   R4,R4                    IS IT THERE
         BNZ   NAME                     YES, GO PRINT
         L     R4,176(,R3)              GET STC/TSU/MOUNTS POINTER
         LTR   R4,R4                    IS IT THERE
         BNZ   NAME                     YES, GO PRINT
         LA    R4,DUMMYNM               INSERT DUMMY NAME
NAME     EQU   *
         MVC   0(8,R5),0(R4)            MOVE IN JOBNAME
         LA    R5,10(,R5)               POINT TO NEXT PRINT LOCATION
         BCT   R6,JOBNAME               LOOP
         LA    R11,JOBNAME              SETUP DUMMY RETURN
JOBPRINT EQU   *
         PUT   LIST,PRINT               PRINT FORMATED OUTPUT
         MVI   PRINT,C' '               CLEAR PRINT AREA
         MVC   PRINT+1(99),PRINT        CLEAR PRINT AREA
         LA    R5,PRINT+1               SETUP PRINT AREA POINTER
         LA    R6,10                    SETUP LOOP COUNT
         BR    R11                      EXIT
ENDJOBNM EQU   *
         BAL   R11,JOBPRINT             GO PRINT LAST LINE
         LR    R4,R9                    LOAD PRINT LINES
         LA    R5,WORKAREA              GET ADDRESS OF WORKAREA
SPQE#    EQU   *
         CLI   0(R5),C'C'               IS THIS CSA
         BNE   SQA#                     NO, GO SEE IF SQA
         MVI   0(R5),X'00'              PATCH UP ADDRESS
         MVI   FCSA,C'N'                YES, SET TO NOT FIRST
         MVI   FSQA,C'Y'                SET SQA TO FIRST
         MVI   FDQE,C'Y'                SET DQE TO FIRST
         MVI   FFQE,C'Y'                SET FQE TO FIRST
         CLI   SAVEKEY,X'FF'            IS IT FIRST TIME
         BNE   TESTKEY                  NO,
         MVC   SAVEKEY,11(R5)           YES, MAKE EQUAL
TESTKEY  EQU   *
         CLC   SAVEKEY,11(R5)           TEST IF EQUAL
         BE    SAMEKEY                  NO,
         MVC   SAVEKEY,11(R5)           SAVE NEW KEY
         L     R2,TOTKEY                GET TOTAL TO DATE BY KEY
         CVD   R2,DOUBLE                CONVERT TO DECIMAL
         MVC   CSATOT(8),=X'402020202120D240'
         ED    CSATOT(6),DOUBLE+5       EDIT IN TOTAL
         LA    R7,TOTLINE               SETUP FOR PRINTING
         BAL   R11,PRINTER              PRINT OUTPUT
         XC    TOTKEY,TOTKEY            ZERO COUNT
SAMEKEY  EQU   *
         BAL   R11,HEADERS              GO INSERT HEADERS
         B     FORMATX                  GO FORMAT RECORD
SQA#     EQU   *
         CLI   0(R5),C'S'               IS THIS CSA
         BNE   DQE#                     NO, GO SEE IF DQE
         MVI   0(R5),X'00'              PATCH UP ADDRESS
         CLI   FSQA,C'N'                IS THIS THE FIRST SQA SPQE
         BE    FORMATX                  NO, GO FORMAT
         MVI   FSQA,C'N'                YES, SET TO NOT FIRST
         MVI   FCSA,C'Y'                SET SQA TO FIRST
         MVI   FDQE,C'Y'                SET DQE TO FIRST
         MVI   FFQE,C'Y'                SET FQE TO FIRST
         BAL   R11,HEADERS              GO INSERT HEADERS
         B     FORMATX                  GO FORMAT RECORD
DQE#     EQU   *
         CLI   0(R5),C'F'               IS THIS DQE
         BE    FQE#                     NO, GO SEE IF FQE
         CLI   FDQE,C'N'                IS THIS THE FIRST DQE
         BE    FORMATX                  NO, GO FORMAT
         MVI   FDQE,C'N'                YES, SET TO NOT FIRST
         MVI   FSQA,C'Y'                SET SQA TO FIRST
         MVI   FCSA,C'Y'                SET CSA TO FIRST
         MVI   FFQE,C'Y'                SET FQE TO FIRST
         MVI   DQEHEAD,C' '             SET TO SINGLE SPACE
         BAL   R11,HEADERS              GO INSERT HEADERS
         MVI   DQEHEAD,C'1'             SET TO SKIP TO NEXT PAGE
         B     FORMATX                  GO FORMAT RECORD
FQE#     EQU   *
         MVI   0(R5),X'00'              PATCH UP ADDRESS
         CLI   FFQE,C'N'                IS THIS THE FIRST FQE
         BE    FORMATY                  NO, GO FORMAT
         MVI   FFQE,C'N'                YES, SET TO NOT FIRST
         MVI   FSQA,C'Y'                SET SQA TO FIRST
         MVI   FCSA,C'Y'                SET CSA TO FIRST
         MVI   FDQE,C'Y'                SET DQE TO FIRST
         MVI   FQEHEAD,C' '             SET TO SINGLE SPACE
         BAL   R11,HEADERS              GO INSERT HEADERS
         MVI   FQEHEAD,C'1'             SET TO SKIP TO NEXT PAGE
         B     FORMATY                  GO FORMAT RECORD
FORMATX  EQU   *
         L     R2,12(,R5)               GET LENGTH TO CONVER TO K
         SRL   R2,10                    DIVIDE BY 1K
         CVD   R2,DOUBLE                CONVERT TO DECIMAL
         CLI   FDQE,C'N'                IS THIS A DQE
         BNE   NOTDQE                   NO, BYPASS
         MVC   PRTSPQE(8),=X'402020202120D240'
         ED    PRTSPQE(6),DOUBLE+5      EDIT IN CORE
         B     FORMATY                  EXIT
NOTDQE   EQU   *
         MVC   PRTSPQE+10(8),=X'402020202120D240'
         ED    PRTSPQE+10(6),DOUBLE+5   EDIT IN CORE
         CLI   FCSA,C'N'                IS THIS A CSA SPQE
         BNE   FORMATY                  NO, BY PASS COUNTS
         LR    R9,R2                    DUPLEX COUNT
         A     R9,TOTKEY                ADD IN COUNT
         ST    R9,TOTKEY                SAVE COUNT
         A     R2,CSACOUNT              ADD TO COUNT
         ST    R2,CSACOUNT              SAVE CORE COUNT
FORMATY  EQU   *
         LA    R6,PRTDATA               PRINT AREA ADDR
         LA    R3,4                     NO. OF FULLWORDS PER LINE
FORMAT2  EQU   *
         UNPK  0(9,R6),0(5,R5)          UNPACK DATA
         MVC   8(2,R6),=CL4' '          BLANK 2 BYTES
         NC    0(8,R6),=8X'0F'          TRANSLATE TO PRINT
         TR    0(8,R6),=C'0123456789ABCDEF'
         LA    R6,10(0,R6)              INCR INTO PRINT AREA
         LA    R5,4(0,R5)               INCR INTO DATA
         BCT   R3,FORMAT2               GO FORMAT
         LA    R7,PRINT                 GET ADDRESS OF PRINT AREA
         BAL   R11,PRINTER              GO DO PRINT
         MVI   PRINT,C' '               CLEAR PRINT AREA
         MVC   PRINT+1(79),PRINT        CLEAR PRINT AREA
         BCT   R4,SPQE#                 NEXT LINE
         L     R2,TOTKEY                GET KEY COUNT
         CVD   R2,DOUBLE                CONVERT TO DECIMAL
         MVC   CSATOT(8),=X'402020202120D240'
         ED    CSATOT(6),DOUBLE+5       EDIT IN CORE
         LA    R7,TOTLINE               GET TOTAL PRINT LINE
         BAL   R11,PRINTER              GO PRINT TOTAL LINE
         L     R2,CSACOUNT              GET CSA COUNT
         CVD   R2,DOUBLE                CONVERT TO DECIMAL
         MVC   CSATOT(8),=X'402020202120D240'
         ED    CSATOT(6),DOUBLE+5       EDIT IN CORE
         LA    R7,TOTLINE               GET TOTAL PRINT LINE
         BAL   R11,PRINTER              GO PRINT TOTAL LINE
         CLOSE LIST
         OSRETURN
PRINTER  EQU   *
         PUT   LIST,(R7)                PRINT FORMATED OUTPUT
         BCTR  R10,R11                  CONTROL PRINT SIZE
HEADERS  EQU   *
         LA    R10,56                   SET PAGE LENGTH AT 56 LINES
         LA    R7,DQEHEAD               SET PRINT LINE
         CLI   FDQE,C'N'                IS IT A DQE
         BE    PRINTER                  YES, GO DO PRINT
         LA    R7,SQAHEAD               NO, SET PRINT LINE
         CLI   FSQA,C'N'                IS IT A SQA
         BE    PRINTER                  YES, GO DO PRINT
         LA    R7,CSAHEAD               NO, SET PRINT LINE
         CLI   FCSA,C'N'                IS IT A CSA
         BE    PRINTER                  YES, GO DO PRINT
         LA    R7,FQEHEAD               NO, SET PRINT LINE
         CLI   FFQE,C'N'                IS IT A FQE
         BE    PRINTER                  YES, GO DO PRINT
         LA    R7,UNKNOWN               NO, SET PRINT LINE
         B     PRINTER                  GO DO PRINT
SPQE     EQU   *
         XR    R5,R5                    ZERO REGISTER FIVE
         MVC   0(16,R11),0(R3)          MOVE SPQE TO WORKAREA
         LA    R9,1(,R9)                INCREMENT COUNT
         MVI   0(R11),C'C'              IDENTIFY SPQE
         L     R4,4(,R3)                GET DQE ADDRESS
         LTR   R4,R4                    LOAD AND TEST IF DQE
         BNZ   DQE                      GO LOOK AT DQE
EXITDQE  EQU   *
         ST    R5,12(,R11)              SAVE DQE COUNTS
         LA    R11,16(,R10)             GET ADDRESS OF NEXT WORKAREA
         LR    R10,R11                  GET CURRENT WORKAREA POINTER
         L     R3,0(,R3)                GET NEXT SPQE
         LTR   R3,R3                    LOAD AND TEST IF SPQE
         BZ    EXITSPQE                 END OF SPQE LOOP
         B     SPQE                     GO PROCESS NEXT SPQE
DQE      EQU   *
         LA    R10,16(,R10)             GET NEXT WORKAREA ADDRESS
         MVC   0(16,R10),0(R4)          MOVE DQE TO WORKAREA
         LA    R9,1(,R9)                INCREMENT COUNT
         A     R5,12(,R4)               ADD CURRENT LENGTH THE ACCUM
         BAL   R14,FQE                  GO SEE IF FQE TO BE DONE
         L     R4,4(,R4)                GET NEXT DQE
         LTR   R4,R4                    LOAD AND TEST IF DQE
         BNZ   DQE                      GO PROCESS NEXT DQE
         B     EXITDQE                  GO GET NEXT SPQE
FQE      EQU   *
         CLI   FQESW,C'Y'               IS FQE TO BE SCANNED
         BNE   0(,R14)                   NO, GO BACK TO MAIN STREAM
         L     R6,0(,R4)                GET FQE POINTER
         LTR   R6,R6                    TEST TO SEE IF FQE
         BE    0(,R14)                  NO, RETURN TO MAIN STREAM
FQESCN   EQU   *
         LA    R10,16(,R10)             GET NEXT WORKAREA ADDRESS
         MVC   0(16,R10),0(R6)          MOVE FQE TO WORKAREA
         LA    R9,1(,R9)                INCREMENT COUNT
         MVI   0(R10),C'F'              SET FQE INDICATOR
         L     R6,0(,R6)                GET NXT FQE POINTER
         LTR   R6,R6                    TEST TO SEE IF FQE
         BNZ   FQESCN                   YES, GO WORK ON NEXT FQE
         BR    R14                      EXIT BACK TO MAIN STREAM
LIST     DCB   DSORG=PS,MACRF=PM,DDNAME=LIST,LRECL=100,RECFM=FA,       *
               BLKSIZE=100
FQEHEAD  DS    0CL80
         DC    C'1'
         DC    C'   FQE    '
         DC    C'PTR-NFQE  '
         DC    C' LENGTH   '
         DC    C' ADDRESS  '
         DC    C'RESERVED  '
         DC    29C' '
FQETIME  DC    20C' '
DQEHEAD  DS    0CL80
         DC    C'1'
         DC    C'   DQE    '
         DC    C'PTR-FREE  '
         DC    C'PTR-NDQE  '
         DC    C' ADDRESS  '
         DC    C' LENGTH   '
         DC    3C' '
         DC    C' NO. OF K '
         DC    16C' '
DQETIME  DC    20C' '
SQAHEAD  DS    0CL80
         DC    C'1'
         DC    C' SQA SPQE '
         DC    C' N SPQE   '
         DC    C'PTR  DQE  '
         DC    C'SP && KEY  '
         DC    C' LENGTH   '
         DC    13C' '
         DC    C' NO. OF K '
         DC    6C' '
SQATIME  DC    20C' '
CSAHEAD  DS    0CL80
         DC    C'1'
         DC    C' CSA SPQE '
         DC    C' N SPQE   '
         DC    C'PTR  DQE  '
         DC    C'SP && KEY  '
         DC    C' LENGTH   '
         DC    13C' '
         DC    C' NO. OF K '
         DC    6C' '
CSATIME  DC    20C' '
UNKNOWN  DS    0CL80
         DC    C'1'
DUMMYNM  DC    C'UNKNOWN'
         DC    89C' '
PRINT    DS    100C' '
         ORG   PRINT
PRTCC    DC    C' '
         DC    10C' '
PRTDATA  DC    40C' '
         DC    3C' '
PRTSPQE  DC    46C' '
TOTLINE  DS    100C' '
         ORG   TOTLINE
TOTCC    DC    C' '
         DC    63C' '
CSATOT   DC    16C' '
         ORG   TOTLINE+100
FSQA     DC    C'Y'                     SET UP HEADER SWITCHES
FCSA     DC    C'Y'                     SET UP HEADER SWITCHES
FDQE     DC    C'Y'                     SET UP HEADER SWITCHES
FFQE     DC    C'Y'                     SET UP HEADER SWITCHES
FQESW    DC    C'N'
SAVEKEY  DC    X'FF'
EDDAYTIM DS    0C
EDDAY    DC    X'20204B202020'
         DC    2C' '
EDTIME   DC    X'20214B20204B2020'
TIMEO    DC    5C' '
TIMEDAY  DC    D'0'
         ORG   TIMEDAY
TIME     DC    F'0'
DAY      DC    F'0'
DOUBLE   DC    D'0'
CSACOUNT DC    F'0'
TOTKEY   DC    F'0'
         LTORG
         DC    C'START OF WORKAREA'
WORKAREA DS    0D
         DC    12000F'0'
         END
