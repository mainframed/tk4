SQAPRT   OSENTRY R12,*
*              R2                     POINT TO DATA
*              R3                     LOOP START ADDRESS
*              R4                     LOOP COUNT ONE
*              R5                     LOOP COUNT TWO
*              R11                    BAL REGISTER
         OPEN  SYSDUMP
         OPEN  (OUTPUT,(OUTPUT))
         BAL   R11,READ               GO READ FILE
         B     LOOP1                  GO PROCESS RECORD
READ     EQU   *
         GET   SYSDUMP                READ SQA DUMP
         LR    R2,R1                  SETUP INPUT DATA POINTER
         CLC   112(4,R2),=C'PAGE'     COMPARE FOR START OF DUMP
         BE    READ                   RETRY
         CLC   44(7,R2),=C'* * * *'   COMPARE FOR START OF DUMP
NOP      BNE   READ                   RETRY
         MVI   NOP+1,X'00'            RESET BRANCH
         CLC   44(7,R2),=C'ING AT '   COMPARE FOR START OF DUMP
         BE    READ                   GO GET NEXT RECORD
         CLC   44(7,R2),=C'       '   COMPARE FOR BLANK LINE
         BE    READ                   GO GET NEXT RECORD
         LA    R3,1(R2)               GET START OF DATA
         LA    R4,4                   SETUP LOOP ONE COUNTER
         LA    R5,2                   SETUP LOOP TWO COUNTER
         BR    R11                    RETURN CALLER
LOOP1    EQU   *
         MVC   ADDR(6),1(R2)          MOVE IN ADDRESS
         LA    R3,9(R3)               POINT TO NEXT RECORD
         CLC   0(3,R3),=C' FE'        TEST FOR EYECATCHER
         BE    EXTRACT                GO PROCESS EYE
ENDEXTRA EQU   *
         BAL   R10,COUNT              GET NEXT RECORD
         B     LOOP1                  GO PROCESS RECORD
EXTRACT  EQU   *
         MVC   EYE,1(R3)              MOVE IN EYE
         BAL   R10,COUNT              GO REDUCE COUNTS
JOBNAMEX EQU   *
         LA    R3,9(R3)               POINT TO RECORD
         MVC   HOLDNAME,1(R3)         MOVE IN JOBNAME
         MVI   JOBNAME,C' '           CLEAR AREA
         MVC   JOBNAME+1(L'JOBNAME-1),JOBNAME
         LA    R6,HOLDNAME            GET START OF JOBNAME
         LA    R7,JOBNAME             OUTPUT AREA
         LA    R8,4                   LOOP COUNT
         CLC   HOLDNAME(2),=C'5C'     TEST IF ASTERICK
         BNE   JOBC                   GO DO TRANSLATE
         B     JOBBUMP
JOBC     EQU   *
         CLI   0(R6),C'C'             TEST IF A - I
         BNE   JOBD
         MVC   0(1,R7),1(R6)          MOVE IN CHARACTER
         NI    0(R7),X'CF'            CHANGE SIGN
         B     JOBBUMP                GET NEXT CHARACTER
JOBD     EQU   *
         CLI   0(R6),C'D'             TEST IF J - R
         BNE   JOBE
         MVC   0(1,R7),1(R6)          MOVE IN CHARACTER
         NI    0(R7),X'DF'            CHANGE SIGN
         B     JOBBUMP                GET NEXT CHARACTER
JOBE     EQU   *
         CLI   0(R6),C'E'             TEST IF S - Z
         BNE   JOBF
         MVC   0(1,R7),1(R6)          MOVE IN CHARACTER
         NI    0(R7),X'EF'            CHANGE SIGN
         B     JOBBUMP                GET NEXT CHARACTER
JOBF     EQU   *
         CLI   0(R6),C'F'             TEST IF 0 - 9
         BNE   JOBBLANK
         MVC   0(1,R7),1(R6)          MOVE IN CHARACTER
         B     JOBBUMP                GET NEXT CHARACTER
JOBBLANK EQU   *
         CLC   0(2,R6),=C'40'         TEST FOR BLANK
         BNE   JOBBAD                 JOBNAME OVERLAID
         MVI   0(R6),C' '             MOVE IN BLANK
         B     JOBBUMP                GET NEXT CHARACTER
JOBBAD   EQU   *
         MVC   JOBNAME,HOLDNAME       MOVE IN EBCDIC
JOBBUMP  EQU   *
         LA    R6,2(R6)               MOVE TO NEXT CHARACTER
         LA    R7,1(R7)               MOVE TO NEXT CHARACTER
         BCT   R8,JOBC                START OVER
         BAL   R10,COUNT              GO REDUCE COUNTS
LENGTHX  EQU   *
         LA    R3,9(R3)               POINT TO RECORD
         MVC   LENGTH,1(R3)           MOVE IN LENGTH
         PUT   OUTPUT,AREA            WRITE OUT RECORDS
         B     ENDEXTRA               START OVER
         MVI   AREA,C' '              CLEAR AREA
         MVC   AREA+1(L'AREA-1),AREA
COUNT    EQU   *
         BCTR  R4,R10                 REDUCE COUNTER AND BRANCH TO MOVE
         LA    R3,2(R3)               INCREMENT POINTER
         LA    R4,4                   RESET COUNTER
         BCTR  R5,R10                 REDUCE COUNTER AND BRANCH TO MOVE
         BAL   R11,READ               GO READ NEXT RECORD
         BR    R10                    RETURN TO CALLER
EOJ      EQU   *
         CLOSE (SYSDUMP,,OUTPUT)
         OSRETURN
SYSDUMP  DCB   DSORG=PS,MACRF=GL,DDNAME=SYSDUMP,EODAD=EOJ
OUTPUT   DCB   DSORG=PS,MACRF=PM,DDNAME=OUTPUT
HOLDNAME DC    CL8' '
         DS    0F
AREA     DS    0CL32
ADDR     DC    CL8' '
EYE      DC    CL8' '
JOBNAME  DC    CL8' '
LENGTH   DC    CL8' '
         END
