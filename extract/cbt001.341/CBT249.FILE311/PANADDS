00010004PROC 3 NAME TYPE OK MEMBER()
00020000CONTROL MAIN NOFLUSH
00030000/*===================================================================*/
00040000/*                                                                   */
00050000/*    ADD TO PANVALET FROM SPF                                       */
00060000/*   TEST DATA:  1. PROJ.LIB.DATA  NO MEMBER                         */
00070000/*               2. PROJ.LIB.TEXT  NO MEMBER                         */
00080000/*               3. PROJ.LIB.DATA     MEMBER                         */
00090000/*               4. PROJ.LIB.TEXT     MEMBER                         */
00100000/*               5. DSN      DATA  NO MEMBER                         */
00110000/*               6. DSN      DATA     MEMBER                         */
00120000/*               7. 'DSN'    DATA  NO MEMBER                         */
00130000/*               8. 'DSN'    DATA     MEMBER                         */
00140000/*               9. 'DSN'   TEXT   NO MEMBER                         */
00150000/*              10. 'DSN'   TEXT      MEMBER                         */
00160000/*              11. DSN     TEXT   NO MEMBER                         */
00170000/*              12. DSN     TEXT      MEMBER                         */
00180000/*                                                                   */
00190000/*                                                                   */
00200001/*  UPDATED BY JDO 6/82 FOR NEW PANSPF PROGRAM: SUPERSETS, SUBSETS,  */
00210001/*  AUTOMATIC TRANSLATION OF ++ AND --; FEWER DISPLAYS               */
00220001/*                                                                   */
00230000/*===================================================================*/
00240000FREE F(SYSIN,SYSPRINT)
00250000ALLOC F(SYSPRINT) DUMMY
00260000IF &MEMBER EQ  THEN DO  /* SEQUENTIAL */
00270000               IF &SUBSTR(1:1,&NAME) EQ &STR(') THEN -
00280000                  SET &DSN EQ &NAME
00290000               ELSE -
00300000                  SET &DSN EQ &STR(')&NAME&STR(')
00310000END
00320000ELSE DO    /* PDS */
00330000               IF &SUBSTR(1:1,&NAME) NE &STR(') THEN -
00340000                    SET &DSN EQ &STR(')&NAME(&MEMBER)&STR(')
00350000               ELSE DO   /* INSERT MEMBER INTO DSN */
00360000                SET &LENG EQ &LENGTH(&NAME) - 1 /* QUOTES SUPPLIED*/
00370000                SET &DSN EQ &SUBSTR(1:&LENG,&NAME)(&MEMBER)&STR(')
00380000               END
00390000END
00400000/*  LOOK FOR DATASETS THAT REQUIRE CONVERSION BEFORE PUTTING ON PANVALET */
00410000IF &TYPE  EQ VSBASIC OR &TYPE EQ CLIST OR &TYPE EQ TEXT THEN GOTO VBP
00420000IF &TYPE EQ LIST THEN GOTO LIST2P
00430000/*                           */
00440000/*  SEQUENTIAL OR PDS : NO CONVERSION NEEDED */
00450000/*                           */
00460000ALLOC F(SYSIN) DA(&DSN) SHR
00470000CONTROL NOFLUSH
00480000ERROR  DO
00490000   ERROR OFF
00500000   WRITE  ****** PANADD FAILED FOR SOME REASON *********************
00510000   WRITE  * THE ERROR COULD BE: 1) BAD MEMBER NAME IN A PDS        *
00520000   WRITE  * 2) MISSPELLED PANVALET NAME  3) MISSING DATA TYPE      *
00530000   WRITE  * 4) WRONG DATA TYPE           5) PANVALET LIBRARY FULL  *
00540000   WRITE  ****** CORRECT YOUR ERROR AND RETRY PANADD ***************
00550000   FREE F(SYSIN,SYSPRINT)
00560000   ALLOC F(SYSIN) DA(*)
00570000   ALLOC F(SYSPRINT) DA(*)
00580000   GOTO DONE
00590000   END
00600008CALLIT: ISPEXEC SELECT PGM(JOPANTST)
00610000   /*  IF AN ERROR, FORCE ENTRY INTO THE ERROR RECOVERY ROUTINE */
00620000IF &LASTCC NE 0 THEN ALLOC F(X) DA(X1234567.X1234567) OLD
00630000   ERROR OFF
00640002/* ISPEXEC DISPLAY PANEL(PANFINA)   */
00650006   IF &OK EQ YES THEN DO
00660005     ISPEXEC CONTROL DISPLAY LINE START(23)
00670005     DELETE  &DSN  FILE(SYSIN)
00680005   END
00690000FREE F(SYSIN,SYSPRINT)
00700000ALLOC F(SYSIN) DA(*)
00710000ALLOC F(SYSPRINT) DA(*)
00720000GOTO DONE
00730000 /*                                                        */
00740000 /*       HANDLE VSBASIC, CLIST, LIST, AND TEXT DATASETS   */
00750000 /*                                                        */
00760000LIST2P: SET &OPTION EQ LIST2PAN
00770000WRITE NOTE: LIST DATASETS MUST HAVE LRECL=133
00780000GOTO VBP2
00790000VBP: SET &OPTION EQ VB2PAN
00800000VBP2: ALLOC F(READIN) DA(&DSN) SHR
00810000ATTR JDX LRECL(80) BLKSIZE(6080) RECFM(F B S)
00820000CONTROL NOFLUSH
00830000  ERROR DO
00840000         ERROR OFF
00850000         FREE F(SYSIN,READIN,RITEOUT,SYSPRINT)ATTR(JDX)
00860000         WRITE ******* ERROR IN CONVERSION ROUTINE, RETRY PANADD *****
00870000         IF &MEMBER NE  THEN -
00880000         WRITE  PERHAPS YOU SPELLED THE MEMBER NAME WRONG ....
00890000         DEL PANDATA
00900000         ALLOC F(SYSIN) DA(*)
00910000         ALLOC F(SYSPRINT) DA(*)
00920000         GOTO DONE
00930000         END
00940000ALLOC FILE(RITEOUT) SP(5 1) NEW CYL USING(JDX) DATASET(PANDATA)
00950000/*                                                               */
00960000/*   ====== CALL CONVERSION PROGRAM FOR VARIABLE / LIST DATASETS */
00970000/*
00980000CALL 'SYS1.JOBLIB(JOVBPAN)' 'NOREPORT/&OPTION'
00990000   /*  IF AN ERROR, FORCE ENTRY INTO THE ERROR RECOVERY ROUTINE */
01000000IF &LASTCC NE 0 THEN ALLOC F(X) DA(X1234567.X1234567) OLD
01010000ALLOC F(SYSIN) DA(PANDATA) SHR
01020000/*                                                               */
01030000/*     PERFORM THE PANADD OPERATION ...   VARIABLE-LIST DATASETS */
01040000/*
01050000ERROR  DO
01060000   ERROR OFF
01070000   WRITE  ****** PANADD FAILED FOR SOME REASON *********************
01080000   WRITE  * THE ERROR COULD BE: 1) BAD MEMBER NAME IN A PDS        *
01090000   WRITE  * 2) MISSPELLED PANVALET NAME  3) MISSING DATA TYPE      *
01100000   WRITE  * 4) WRONG DATA TYPE           5) PANVALET LIBRARY FULL  *
01110000   WRITE  ****** CORRECT YOUR ERROR AND RETRY PANADD ***************
01120000   CONTROL NOMSG
01130000   FREE F(SYSPRINT,SYSIN,READIN,RITEOUT) ATTR(JDX)
01140000   DEL PANDATA
01150000   ALLOC F(SYSIN) DA(*)
01160000   ALLOC F(SYSPRINT) DA(*)
01170000   GOTO DONE
01180000END
01190008ISPEXEC SELECT PGM(JOPANTST)
01200000   /*  IF AN ERROR, FORCE ENTRY INTO THE ERROR RECOVERY ROUTINE */
01210000IF &LASTCC NE 0 THEN ALLOC F(X) DA(X1234567.X1234567) OLD
01220000   ERROR OFF
01230006   IF &OK EQ YES THEN DO
01240005     ISPEXEC CONTROL DISPLAY LINE START(23)
01250005     DELETE  &DSN  FILE(SYSIN)
01260005   END
01270000CONTROL NOMSG
01280000FREE F(SYSPRINT,SYSIN,READIN,RITEOUT) ATTR(JDX)
01290000DEL PANDATA
01300000ALLOC F(SYSIN) DA(*)
01310000ALLOC F(SYSPRINT) DA(*)
01320000DONE: END
