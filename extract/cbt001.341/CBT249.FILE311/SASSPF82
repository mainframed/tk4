00010007  PROC 0  WORK(75)  OPTIONS()  SORT(75) PGM(NOTHING) -
00020007          PLOT(NONE) ENTRY(SAS)
00021007          GLOBAL SASPRNT SASCL KEEP
00030007 /*********************************************************************/
00040007 /*** SAS                                                           ***/
00050007 /***      EXECUTES SAS, OPTIONS FOR HARDCOPY AND SORT SPACE        ***/
00060007 /*** NORTHEAST UTILITIES SERVICE COMPANY    666-6911               ***/
00070007 /*** WRITTEN BY CHRISTOPHER JOHN FINEGAN    EXT. 1103              ***/
00080007 /*** WRITTEN:        08/08/78 BY CJF - FOR SAS CLASSES             ***/
00090007 /*** LAST UPDATED:   4 /23/81 BY JDO SAS79.5 AND SASGRAPH          ***/
00100007 /*** LAST UPDATED:   10/20/81 BY JDO FOR SPF                       ***/
00110007 /*** LAST UPDATED:   03/24/82 BY JDO FOR ATTR IN SASOUT DATASET.   ***/
00120007 /*** LAST UPDATED:   04/28/82 BY LAH SAS79.6 AND SASGRAPH          ***/
00130007 /***                                                               ***/
00140007 /*********************************************************************/
00150007 CONTROL NOMSG MAIN NOFLUSH
00160007        ERROR DO
00170007             ERROR OFF
00180007             CONTROL   MSG NOFLUSH
00190007             WRITE ** AN ERROR OCCURRED IN SAS / SPF. TRY THE PANEL AGAIN **
00200007             GOTO QUIT
00210007        END
00220007 CONTROL MSG
00230007 /*********************************************************************/
00240007 /***                                                               ***/
00250007 /***   ALLOCATE SPACE ON FOR PROC SORT REGARDLESS IF THE USER NEEDS***/
00260007 /***   IT OR NOT.. TOO OFTEN THEY FORGET TO ASK FOR IT WHEN NEEDED.***/
00270007 /***                                                               ***/
00280007 /*********************************************************************/
00290007         SET &KEEP EQ NO
00300007         SET &OPTION EQ 5
00310007         QALLOC SORTLIB 'SYS1.SORTLIB'  SHR
00320007         QALLOC SORTWK01 $  SP(&SORT &SORT) BLO(8192) UNIT(SCRATCHA)
00330007         QALLOC SORTWK02 $  SP(&SORT &SORT) BLO(8192) UNIT(SCRATCHA)
00340007         QALLOC SORTWK03 $  SP(&SORT &SORT) BLO(8192) UNIT(SCRATCHA)
00350007         ATTRIB PRPR RECFM(V B A) LRECL(137) BLKSIZE(4096)
00360007         QALLOC SASLIB $  BLOCK(6400) SPACE(20) DIR(3)
00370007         QALLOC WORK   $  UNIT(SCRATCHA) SPACE(&WORK 3) BLO(8192)
00380007         QALLOC SASHELP 'USER.SAS82.SASHELP'
00390007 /*******************************************************************/
00400007 /***    PLOTTER FILE CHECKS:  ALLOCATE FILES FOR VERSATEC OR CALCOMP ***/
00410007 /***********************************************************************/
00420007 IF &PLOT EQ VERSATEC THEN DO
00430007     SET &PLOTCLAS EQ V
00440007     SET &PLOTDEST EQ LOCAL
00450007     ALLOC F(SYSVECTR) SYSOUT(V)
00460007     ALLOC F(VECTR1) SP(1 1) TR
00470007     ALLOC F(VECTR2) SP(75 75) BLO(8192)
00480007     ALLOC F(PLOTLOG) DA(*)
00490007     ALLOC F(PLOTPARM) DA('USER.COMMDATA(VERPLOT)') SHR
00500007 END
00510007 IF &PLOT EQ VERSBIG  THEN DO
00520007     SET &PLOTCLAS EQ W
00530007     SET &PLOTDEST EQ LOCAL
00540007     ALLOC F(SYSVECTR) SYSOUT(W)
00550007     ALLOC F(VECTR1) SP(1 1) TR
00560007     ALLOC F(VECTR2) SP(75 75) BLO(8192)
00570007     ALLOC F(PLOTLOG) DA(*)
00580007     ALLOC F(PLOTPARM) DA('USER.COMMDATA(VERPLOTW)') SHR
00590007 END
00600007 IF &PLOT EQ VERSMP1  THEN DO
00610007     SET &PLOTCLAS EQ B
00620007     SET &PLOTDEST EQ RMT14
00630007     ALLOC F(RJEVECTR) SYSOUT(B) DEST(RMT14)
00640007     ALLOC F(VECTR1) SP(1 1) TR
00650007     ALLOC F(VECTR2) SP(75 75) BLO(8192)
00660007     ALLOC F(PLOTLOG) DA(*)
00670007     ALLOC F(PLOTPARM) DA('USER.COMMDATA(VERPLOTB)') SHR
00680007 END
00690007 IF &PLOT EQ VERSCTY  THEN DO
00700007     SET &PLOTCLAS EQ B
00710007     SET &PLOTDEST EQ RMT13
00720007     ALLOC F(RJEVECTR) SYSOUT(B) DEST(RMT13)
00730007     ALLOC F(VECTR1) SP(1 1) TR
00740007     ALLOC F(VECTR2) SP(75 75) BLO(8192)
00750007     ALLOC F(PLOTLOG) DA(*)
00760007     ALLOC F(PLOTPARM) DA('USER.COMMDATA(VERPLOTB)') SHR
00770007 END
00780007 IF &PLOT EQ IBM3287 THEN DO
00790007    SET &OPTIONS EQ &STR(&OPTIONS  DEVICE=IBM3287F,DEVADDR=(SAS,.,PRINTER1))
00800007 END
00810007       SET &SYSIN = &STR(')&STR(&PGM)&STR(')
00820007   /*  ISPEXEC CONTROL ERRORS RETURN  */
00830007 /*********************************************************************/
00840007 /*** ERROR ROUTINE                                                 ***/
00850007 /***      CHECKS TO SEE IF THE ALLOCATE COMMAND FAILED             ***/
00860007 /*** IF SO IT MEANS THAT THE DATASET NAME ENTERED DOES NOT EXIST   ***/
00870007 /***                                                               ***/
00880007 /*********************************************************************/
00890007 /*********************************************************************/
00900007           GOTO EDITDONE
00910007EDIT:      ERROR OFF
00920007           ISPEXEC EDIT DATASET(&SYSIN)
00930007           IF &LASTCC EQ 4 THEN GOTO OUTLOOP
00940007           IF &OPTION EQ 5 THEN GOTO OUTLOOP
00950007 EDITOK:   CONTROL NOMSG
00960007           DEL ('&SYSUID..SASOUT.DATA')
00970007           CONTROL MSG
00980007 EDITDONE: ALLOC DA('&SYSUID..SASOUT.DATA')  US(PRPR) SP(75 75) BLO(8192) MOD FILE(TRANS) UNIT(SCRATCHA)
00990007           /* THE FOLLOWING PROGRAM INITIALIZES THE OUTPUT FILE TO EMPTY */
01000007           CALL 'SYS1.JOBLIB(BPCLRFIL)'  /* OPEN/CLOSE FILE*/
01010007           FREE F(TRANS)
01020007           QALLOC FT12F001 '&SYSUID..SASOUT.DATA' MOD
01030007           ALLOC FILE(INFILE) DA(&SYSIN) SHR
01040007           ERROR OFF
01050007           ISPEXEC CONTROL DISPLAY REFRESH
01060007           ISPEXEC CONTROL DISPLAY SM START(18)
01070007       SET OPTIONSX=&STR(WORKINIT D=TSO SYSIN=INFILE &OPTIONS.)
01080007       SASCP82 TASKLIB('USER.SAS82.LIBRARY') ENTRY(&ENTRY) +
01090007               OPTIONS('&OPTIONSX') SHARE
01100007           FREE F(FT12F001) KEEP
01110007CONTROL MSG
01120007 OUTLOOP: ERROR OFF
01130007          ISPEXEC DISPLAY PANEL(SASDONE)
01140007          IF &LASTCC NE 0 THEN GOTO QUIT
01150007 /********************************************************************/
01160007 /***                                                              ***/
01170007 /***         CHECK ANSWER FOR ANSWER TO QUESTION ABOUT TERMINAL   ***/
01180007 /***                                                   OUTPUT     ***/
01190007 /********************************************************************/
01200007          IF &OPTION EQ 1 THEN DO
01210007             ISPEXEC BROWSE DATASET('&SYSUID..SASOUT.DATA')
01220007             GOTO OUTLOOP
01230007          END
01240007 /********************************************************************/
01250007 /***                                                              ***/
01260007 /***      BRANCH TO EDIT IF SO DESIRED                            ***/
01270007 /***                                                              ***/
01280007 /********************************************************************/
01290007       IF &OPTION EQ 4 THEN GOTO EDIT
01300007       IF &OPTION EQ 5 THEN GOTO EDIT
01310007 /********************************************************************/
01320007 /***                                                              ***/
01330007 /***      EXECUTE SAS AGAIN IF SO DESIRED                         ***/
01340007 /***                                                              ***/
01350007 /********************************************************************/
01360007       IF &OPTION EQ 6 THEN GOTO EDITOK
01370007 /********************************************************************/
01380007 /***                                                              ***/
01390007 /***       HARDCOPY PROCESSING                                    ***/
01400007 /***                                                              ***/
01410007 /********************************************************************/
01420007       IF &OPTION EQ 2 THEN DO
01430007           IF &SASPRNT EQ ELPRT01 | &SASPRNT EQ NSPRT01 THEN DO
01440007              ISPEXEC CONTROL DISPLAY LINE START(18)
01450007              DSPRINT '&SYSUID..SASOUT.DATA' &SASPRNT CCHAR NONUM
01460007              END
01470007           ELSE DO
01480007              FREE F(INFILE)
01490007              ALLOC F(INFILE) DA('&SYSUID..SASOUT.DATA') SHR
01500007              IF &SASCL  EQ 8 THEN SET &SASCL  EQ &STR(#8)
01510007              QALLOC OUTFILE SYSOUT(&SASCL)  DEST(&SASPRNT) US(PRPR) COPIES(&COPIES)
01511010              IF &SASCL  EQ &STR(#8) THEN SET &SASCL  EQ &STR(8)
01520007              CALL 'SYS1.JOBLIB(JOCOPY)'
01530007              FREE F(INFILE,OUTFILE)
01540007           END
01550007           IF &KEEP NE YES  THEN DO
01560007                   CONTROL NOMSG
01570007                   DEL ('&SYSUID..SASOUT.DATA')
01580007                   CONTROL MSG
01590007                   ALLOC DA('&SYSUID..SASOUT.DATA') US(PRPR) SP(75 75) BLO(8192) MOD FILE(TRANS) UNIT(SCRATCHA)
01610007                   CALL 'SYS1.JOBLIB(BPCLRFIL)'  /* OPEN/CLOSE FILE*/
01610107                   FREE F(TRANS)
01611007                   QALLOC FT12F001 '&SYSUID..SASOUT.DATA' OLD
01630007           END
01640007           ISPEXEC CONTROL DISPLAY LINE START(18)
01650007           WRITE **** &COPIES COPY(S) OF YOUR OUTPUT ARE QUEUED TO THE PRINTER ****
01660007           GOTO OUTLOOP
01670007       END
01680007 /********************************************************************/
01690007 /***                                                              ***/
01700007 /***         CHECK IF PLOTTED OUTPUT IS WANTED                    ***/
01710007 /***                                                              ***/
01720007 /********************************************************************/
01730007IF &OPTION EQ 3 THEN DO
01740007    IF &PLOT  = VERSATEC OR &PLOT EQ CALCOMP -
01750007    OR &PLOT  = VERSBIG  OR &PLOT EQ VERSMP1 OR &PLOT EQ VERSCTY THEN DO
01760007       IF &PLOT EQ CALCOMP THEN DO
01770007              FREE F(FT14F001) SYSOUT(P)
01780007              WRITE *** YOUR CALCOMP OUTPUT IS WAITING FOR THE PLOTTER ***
01790007              ALLOC F(FT14F001) SYSOUT US(Y)
01800007       END
01810007       IF &PLOT EQ VERSATEC OR &PLOT EQ VERSBIG OR &PLOT EQ VERSMP1 OR &PLOT EQ VERSCTY THEN DO
01820007              WRITE *** PLOT OPERATION BEGINS ***
01830007              CALL 'USER.PLOT.VERSATEC(VTPLOT)'
01840007              CONTROL NOMSG
01850007              FREE  F(SYSVECTR) SYSOUT(&PLOTCLAS)
01860007              FREE  F(RJEVECTR) SYSOUT(&PLOTCLAS)
01870007              WRITE ***  PLOT OPERATION ENDS  ***
01880007              ALLOC F(SYSVECTR) SYSOUT(&PLOTCLAS) DEST(&PLOTDEST)
01890007              ALLOC F(RJEVECTR) SYSOUT(&PLOTCLAS) DEST(&PLOTDEST)
01900007              CONTROL MSG
01910007       END
01920007    END
01930007  END
01940007 /********************************************************************/
01950007 /***                                                              ***/
01960007 /***      EDIT ANOTHER DATASET IF NEEDED                          ***/
01970007 /***                                                              ***/
01980007 /********************************************************************/
01990007    IF &OPTION EQ 7 THEN DO
02000007           ISPEXEC EDIT DATASET(&EDITDS)
02010007           GOTO OUTLOOP
02020007    END
02030007 /********************************************************************/
02040007 /***                                                              ***/
02050007 /***       HARDCOPY PROCESSING -- OTHER DATASET                   ***/
02060007 /***                                                              ***/
02070007 /********************************************************************/
02080007       IF &OPTION EQ 8 THEN DO
02090007           IF &SASPRNT EQ ELPRT01 | &SASPRNT EQ NSPRT01 THEN DO
02100007              ISPEXEC CONTROL DISPLAY LINE START(18)
02110007              DSPRINT &PRINTDS &SASPRNT CCHAR NONUM
02120007              END
02130007           ELSE DO
02140007              CONTROL NOMSG
02150007              FREE F(SYSUT1,SYSUT2,SYSIN,SYSPRINT)
02160007              CONTROL MSG
02170007              ALLOC F(SYSUT1) DA(&PRINTDS) SHR
02180007              ALLOC F(SYSUT2) SYSOUT(&SASCL) DEST(&SASPRNT)
02190007              ALLOC F(SYSIN) DUMMY
02200007              ALLOC F(SYSPRINT) DUMMY
02210007              CALL 'USER.LINKLIB(IEBGENER)'
02220007              FREE F(SYSUT1,SYSUT2,SYSIN,SYSPRINT)
02230007              ALLOC F(SYSIN) DA(*)
02240007              ALLOC F(SYSPRINT) DA(*)
02250007           END
02260007           ISPEXEC CONTROL DISPLAY LINE START(18)
02270007           WRITE **** PRINT COMPLETE ****
02280007           GOTO OUTLOOP
02290007       END
02300007 ELSE ISPEXEC DISPLAY MSG(SASP003)
02310007    GOTO OUTLOOP
02320007 END
02330007 QUIT: CONTROL NOFLUSH NOMSG
02340007    ERROR OFF
02350007    IF &KEEP NE YES THEN DELETE '&SYSUID..SASOUT.DATA'
02360007  /*  IF &LENGTH(&PLOT) GT 0 THEN -  */
02370007  /*  FREE ALL FILES WITHOUT A MESSAGE*/
02380007          FREE ATTR(PRPR) FILE(FT12F001 INFILE)
02390007           FREEZE
