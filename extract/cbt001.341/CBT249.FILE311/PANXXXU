00010004PROC 0
00020004  /*                                                         */
00030004  /*  UPDATE PANVALET FROM SPF                               */
00040004  /*                                                         */
00050004  /*  LAST UPDATE: 6/15/82  JDO   HANDLE MISSING TYPE FROM   */
00060004  /*  MENU; USE NEW SHARED POOL TEXT RESULTS.                */
00070004  /*                                                         */
00080004  SET &FLAG EQ RUN
00090004  SET &PANWORK EQ PANUPD
00100004  /*   INITIALIZE PREVIOUS RESULTS TO BLANKS                 */
00110004  SET &TEXT1 EQ
00120004  SET &TEXT2 EQ
00130004  SET &TEXT3 EQ
00140004  SET &TEXT4 EQ
00150004  SET &TEXT5 EQ
00151004  SET &MSG1  EQ
00160004  ISPEXEC VGET (PRJ1,TYP1,LIB1) PROFILE
00170004  /*   LOOP IN PANADD FUNCTION                               */
00180004  DO WHILE(&FLAG EQ RUN)
00190004     DIS:ISPEXEC DISPLAY PANEL(PANXXXU)
00200004     IF &LASTCC EQ 0 THEN DO
00210004             ISPEXEC VPUT (NAMEPAN,PANWORK) SHARED
00220004             /*  DSN FIELD HAS A VALUE. GET MEMBER AND TYPE */
00230004             IF &LENGTH(&DSN) GT 0 THEN DO
00240004                IF &LENGTH(&OTHMEM) GT 0 -
00250004                      THEN SET &MEMBER EQ &OTHMEM
00260004                 /* LOOK FOR A PERIOD (.) IN THE DSNAME FIELD */
00270004                 SET &LIMIT EQ &LENGTH(&DSN)
00280004                 SET &COUNTER EQ 1
00290004                 DO WHILE &COUNTER LE &LIMIT
00300004                    IF &SUBSTR(&COUNTER:&COUNTER,&DSN)=&STR(.) THEN GOTO SKIPIT
00310004                    SET &COUNTER EQ &COUNTER + 1
00320004                 END
00330004                 /* NO PERIOD FOUND, APPEND DATASET TYPE TO CREATE NAME*/
00340004                 SET &DSN EQ &DSN..&TYPE
00350004                 SKIPIT:  IF &SUBSTR(1:1,&DSN) NE &STR(') THEN -
00360004                          SET &DSN EQ &SYSPREF..&DSN
00370004             END
00380004             ELSE DO  /* DSN FIELD WAS BLANK */
00390004                SET &DSN EQ &PRJ1..&LIB1..&TYP1
00400004                SET &TYPE EQ &TYP1
00410004             END
00420004             IF &LENGTH(&MEMBER) EQ 0 THEN %PANUPDS &DSN &TYPE &OK
00430004                        ELSE %PANUPDS &DSN &TYPE &OK MEMBER(&MEMBER)
00440004             ISPEXEC VGET (TEXT1, TEXT2, TEXT3, TEXT4, TEXT5) SHARED
00441004             SET &MSG1 EQ &STR(ENTER ANOTHER PANVALET NAME OR PRESS PF3 TO END)
00450004     END
00460004     ELSE GOTO DONE
00470004  END
00480005DONE:  -
00490004END
