00010003PROC 3 NAME TYPE OK MEMBER()
00020000/*===================================================================*/
00030000/*                                                                   */
00040000/* UPDATE TO PANVALET FROM SPF                                       */
00050000/*   TEST DATA:  1. PROJ.LIB.DATA  NO MEMBER                         */
00060000/*               2. PROJ.LIB.TEXT  NO MEMBER                         */
00070000/*               3. PROJ.LIB.DATA     MEMBER                         */
00080000/*               4. PROJ.LIB.TEXT     MEMBER                         */
00090000/*               5. DSN      DATA  NO MEMBER                         */
00100000/*               6. DSN      DATA     MEMBER                         */
00110000/*               7. 'DSN'    DATA  NO MEMBER                         */
00120000/*               8. 'DSN'    DATA     MEMBER                         */
00130000/*               9. 'DSN'   TEXT   NO MEMBER                         */
00140000/*              10. 'DSN'   TEXT      MEMBER                         */
00150000/*              11. DSN     TEXT   NO MEMBER                         */
00160000/*              12. DSN     TEXT      MEMBER                         */
00170000/*                                                                   */
00180001/*  UPDATED BY JDO 6/82 FOR NEW VERSION OF PANVALET INTERFACE CODE   */
00190001/*    DELETE MENUS AFTER SUCCESSFUL UPDATE                           */
00200001/*                                                                   */
00210000/*                                                                   */
00220000/*===================================================================*/
00230000FREE F(SYSIN,SYSPRINT)
00240000ALLOC F(SYSPRINT) DUMMY
00250000IF &MEMBER EQ  THEN DO  /* SEQUENTIAL */
00260000               IF &SUBSTR(1:1,&NAME) EQ &STR(') THEN -
00270000                  SET &DSN EQ &NAME
00280000               ELSE -
00290000                  SET &DSN EQ &STR(')&NAME&STR(')
00300000END
00310000ELSE DO    /* PDS */
00320000               IF &SUBSTR(1:1,&NAME) NE &STR(') THEN -
00330000                    SET &DSN EQ &STR(')&NAME(&MEMBER)&STR(')
00340000               ELSE DO   /* INSERT MEMBER INTO DSN */
00350000                SET &LENG EQ &LENGTH(&NAME) - 1 /* QUOTES SUPPLIED*/
00360000                SET &DSN EQ &SUBSTR(1:&LENG,&NAME)(&MEMBER)&STR(')
00370000               END
00380000END
00390000/*  LOOK FOR DATASETS THAT REQUIRE CONVERSION BEFORE PUTTING ON PANVALET */
00400000IF &TYPE  EQ VSBASIC OR &TYPE EQ CLIST OR &TYPE EQ TEXT THEN GOTO VBP
00410000IF &TYPE EQ LIST THEN GOTO LIST2P
00420000/*                           */
00430000/*  SEQUENTIAL OR PDS : NO CONVERSION NEEDED */
00440000/*                           */
00450000ALLOC F(SYSIN) DA(&DSN) SHR
00460000CONTROL NOFLUSH
00470000ERROR  DO
00480000   ERROR OFF
00490000   WRITE  ****** PANUPD FAILED FOR SOME REASON *********************
00500000   WRITE  * 2) MISSPELLED PANVALET NAME  3) MEMBER NOT ON LIBRARY  *
00510000   WRITE  * 4) WRONG PANVALET LIBRARY    5) PANVALET LIBRARY FULL  *
00520000   WRITE  * 4) WRONG DATA TYPE           5) PANVALET LIBRARY FULL  *
00530000   WRITE  ****** CORRECT YOUR ERROR AND RETRY PANUPD ***************
00540000   FREE F(SYSIN,SYSPRINT)
00550000   ALLOC F(SYSIN) DA(*)
00560000   ALLOC F(SYSPRINT) DA(*)
00570000   GOTO DONE
00580000   END
00590005CALLIT: ISPEXEC SELECT PGM(JOPANTST)
00600000   /*  IF AN ERROR, FORCE ENTRY INTO THE ERROR RECOVERY ROUTINE */
00610000IF &LASTCC NE 0 THEN ALLOC F(X) DA(X1234567.X1234567) OLD
00620000ERROR OFF
00630003IF &LASTCC EQ 0 AND &OK EQ YES THEN DO
00640003    ISPEXEC CONTROL DISPLAY LINE START(23)
00650003    DELETE  &DSN  FILE(SYSIN)
00660003END
00670000CONTROL MSG
00680000FREE F(SYSIN,SYSPRINT)
00690000ALLOC F(SYSIN) DA(*)
00700000ALLOC F(SYSPRINT) DA(*)
00710000GOTO DONE
00720000 /*                                                        */
00730000 /*       HANDLE VSBASIC, CLIST, LIST, AND TEXT DATASETS   */
00740000 /*                                                        */
00750000LIST2P: SET &OPTION EQ LIST2PAN
00760000WRITE NOTE: LIST DATASETS MUST HAVE LRECL=133
00770000GOTO VBP2
00780000VBP: SET &OPTION EQ VB2PAN
00790000VBP2: ALLOC F(READIN) DA(&DSN) SHR
00800000ATTR JDX LRECL(80) BLKSIZE(6080) RECFM(F B S)
00810000CONTROL NOFLUSH
00820000  ERROR DO
00830000         ERROR OFF
00840000         FREE F(SYSIN,READIN,RITEOUT,SYSPRINT)ATTR(JDX)
00850000         WRITE ******* ERROR IN CONVERSION ROUTINE, RETRY PANUPD *****
00860000         IF &MEMBER NE  THEN -
00870000         WRITE  PERHAPS YOU SPELLED THE MEMBER NAME WRONG ....
00880000         DEL PANDATA
00890000         ALLOC F(SYSIN) DA(*)
00900000         ALLOC F(SYSPRINT) DA(*)
00910000         GOTO DONE
00920000         END
00930000ALLOC FILE(RITEOUT) SP(5 1) NEW CYL USING(JDX) DATASET(PANDATA)
00940000/*                                                               */
00950000/*   ====== CALL CONVERSION PROGRAM FOR VARIABLE / LIST DATASETS */
00960000/*
00970000CALL 'SYS1.JOBLIB(JOVBPAN)' 'NOREPORT/&OPTION'
00980000   /*  IF AN ERROR, FORCE ENTRY INTO THE ERROR RECOVERY ROUTINE */
00990000IF &LASTCC NE 0 THEN ALLOC F(X) DA(X1234567.X1234567) OLD
01000000ALLOC F(SYSIN) DA(PANDATA) SHR
01010000/*                                                               */
01020000/*     PERFORM THE PANUPD OPERATION ...   VARIABLE-LIST DATASETS */
01030000/*
01040000ERROR  DO
01050000   ERROR OFF
01060000   WRITE  ****** PANUPD FAILED FOR SOME REASON *********************
01070000   WRITE  * THE ERROR COULD BE: 1) BAD MEMBER NAME IN A PDS        *
01080000   WRITE  * 2) MISSPELLED PANVALET NAME  3) MEMBER NOT ON LIBRARY  *
01090000   WRITE  * 4) WRONG PANVALET LIBRARY    5) PANVALET LIBRARY FULL  *
01100000   WRITE  ****** CORRECT YOUR ERROR AND RETRY PANUPD ***************
01110000   CONTROL NOMSG
01120000   FREE F(SYSPRINT,SYSIN,READIN,RITEOUT) ATTR(JDX)
01130000   DEL PANDATA
01140000   ALLOC F(SYSIN) DA(*)
01150000   ALLOC F(SYSPRINT) DA(*)
01160000   GOTO DONE
01170000   END
01180005ISPEXEC SELECT PGM(JOPANTST)
01190000   /*  IF AN ERROR, FORCE ENTRY INTO THE ERROR RECOVERY ROUTINE */
01200000IF &LASTCC NE 0 THEN ALLOC F(X) DA(X1234567.X1234567) OLD
01210000ERROR OFF
01220003IF &LASTCC EQ 0 AND &OK EQ YES THEN DO
01230003    ISPEXEC CONTROL DISPLAY LINE START(23)
01240003    DELETE  &DSN  FILE(READIN)
01250003END
01260000CONTROL NOMSG
01270000FREE F(SYSPRINT,SYSIN,READIN,RITEOUT) ATTR(JDX)
01280000DEL PANDATA
01290000ALLOC F(SYSIN) DA(*)
01300000ALLOC F(SYSPRINT) DA(*)
01310000DONE: END
