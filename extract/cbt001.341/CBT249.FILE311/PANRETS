00010000PROC 2 TSONAME    TYPE BLO(6080) SP(1) MEMBER()
00020000CONTROL MAIN NOFLUSH
00040000/*                                                  */
00050000/*   PANRETS COMMAND  9/09/81  J.D.O.               */
00060000/*   DEVELOPED FOR THE PANVALET-SPF INTERFACE       */
00070000/*   CHANGED 12/4/81 TO HANDLE QUOTED DATASETS      */
00080000/*   UPDATED 6/15/82 TO CALL NEW INTERFACE PROGRAM  */
00090000/*                                                  */
00100000FREE F(SYSPRINT)
00110000ALLOC F(SYSPRINT) DUMMY
00120000DSNE: ERROR DO
00130000        ERROR OFF
00140000        WRITE => ERROR <= THE DATASET '&TSONAME' ALREADY EXISTS
00150000        WRITE OR ELSE YOU CREATED AN INVALID TSO NAME
00160000        WRITE ENTER THE LISTV COMMAND TO SEE IF YOUR DATASETS ALREADY EXISTS
00170000        WRITE MAKE SURE THE DATASET NAME IS A LEGAL ONE.
00180000        FREE ATTR(PL) F(SYSPRINT)
00190000        ALLOC F(SYSPRINT) DA(*)
00200000        GOTO DONE
00210000      END
00220000      /*                                                /*
00230000      /*    SEE IF THIS IS A CLIST (VARIABLE BLOCKED) DATASET */
00240000      /*                                                /*
00250000IF &TYPE EQ VSBASIC OR &TYPE EQ LIST OR &TYPE EQ CLIST OR &TYPE EQ TEXT  -
00260000         THEN DO
00270000 /*                                                              */
00280000 /*
00290000 /*  C L I S T   T E X T    L I S T     V S B A S  I C           */
00300000 /*
00310000              CONTROL MSG NOFLUSH
00320000              ATTR PL LRECL(80) RECFM(F B S) BLKSIZE(&BLO)
00330000              ALLOC DA(PANDATA)SP(&SP 1)US(PL)TRACKS F(PANDD2
00340000              ERROR DO
00350000   NOGO:    ERROR OFF
00360000              WRITE *ERROR*  THERE WAS SOME ERROR IN THE PANVALET RETRIEV
00370000         /*  ERROR DETECTED  - GIVE A MESSAGE   */
00380000              WRITE THE TSO DATASET WILL NOT BE CREATED:
00390000              WRITE POSSIBLE ERRORS ARE
00400000              WRITE  1) YOU PUT A SPACE IN FRONT OF, OR AFTER, YOUR PANVALET NAME
00410000              WRITE  2) THE PANVALET NAME SPECIFIED IS NOT ON THE PRESENT PANVALET LIBRARY
00420000              WRITE  3) YOUR PAN DATASET HAS MORE THAN 3400 LINES (ERROR B-37)
00430000              WRITE     (USE THE SP PARAMETER IN THIS CASE: TRY THE HELP PANRET COMMA
00440000              WRITE  4) YOU HAVE NO PANVALET LIBRARY ALLOCATED
00450000              WRITE   ==> FIGURE OUT YOUR ERROR AND RETRY PANRET <====
00460000       /*    END OF ERROR DESCRIPTION...   CLEAN UP AND TERMINATE */
00470000              CONTROL NOMSG NOFLUSH
00480000              FREE ATTR(PL,JDX) FILE(PANDD2,READIN,RITEOUT,SYSPRINT)
00490000              ALLOC F(SYSPRINT) DA(*)
00500000              DEL PANDATA
00510000                 GOTO DONE
00520000              END
00530002              ISPEXEC SELECT PGM(JOPANTST)
00540000              /*    CHECK FOR AN                        */
00550000              /* ERROR - DSN ILLEGAL OR NOT ON PANVALET */
00560000              /*                                        */
00570000              IF &LASTCC NE 0 THEN -
00580000                    ALLOC DA(X1234567.X1234567) OLD
00590000              /* THE ABOVE ALLOCATION SHOULD CAUSE AN   */
00600000              /* ALLOCATION FAILURE AND PUT YOU INTO    */
00610000              /* ERROR RECOVERY.                        */
00620000              /*                                        */
00630000              /* NO BUGS ... CONTINUE ... CONVERSION    */
00640000              /*                                        */
00650000           FREE ATTR(PL)
00660000           ALLOC FILE(READIN) DA(PANDATA)
00670000           CONTROL MSG
00680000           IF &TYPE EQ LIST THEN DO
00690000                  SET &OPT EQ PAN2LIST
00700000                  ATTR JDX LRECL(133) BLKSIZE(3990) RECFM(F B A)
00710000                  END
00720000           ELSE DO    /* CLIST - TEXT - VSBASIC */
00730000                ATTR JDX LRECL(255) BLKSIZE(4096) RECFM(V B)
00740000                SET &OPT EQ PAN2VB
00750000           END
00760000           ERROR GOTO NOGO
00770000           IF &MEMBER EQ   THEN DO /* NO MEMBER - SEQUENTIAL */
00780000              IF &SUBSTR(1:1,&TSONAME) EQ &STR(') THEN -
00790000                ALLOC FILE(RITEOUT) SP(5 1) NEW TRACK -
00800000                         USING(JDX) DATASET(&TSONAME) /* QUOTES SUPPLIED */
00810000              ELSE ALLOC FILE(RITEOUT) SP(5 1) NEW TRACK -
00820000                         USING(JDX) DATASET('&TSONAME')
00830000           END
00840000           ELSE DO
00850000              IF &SUBSTR(1:1,&TSONAME) EQ &STR(') THEN DO /*PDS*/
00860000                SET &LENG EQ &LENGTH(&TSONAME) - 1 /* QUOTES SUPPLIED*/
00870000                SET &TSONAME EQ &SUBSTR(1:&LENG,&TSONAME)(&MEMBER)&STR(')
00880000                ALLOC  FILE(RITEOUT) DA(&TSONAME)SHR
00890000              END
00900000              ELSE ALLOC  FILE(RITEOUT) DA('&TSONAME.(&MEMBER.)')SHR
00910000           END
00920000           CALL 'SYS1.JOBLIB(JOVBPAN)' 'NOREPORT/&OPT'
00930000         CONTROL NOMSG
00940000         FREE F(RITEOUT,READIN,SYSPRINT) ATTR(JDX)
00950000         ALLOC F(SYSPRINT) DA(*)
00960000         DELETE PANDATA
00970000         GOTO DONE
00980000   END
00990000 /*                                                              */
01000000 /*   NON - VARIABLE LENGTH OR  133 LRECL DATASETS: 80 6080      */
01010000 /*                                                              */
01020000/*  FIRST, CHECK FOR A PDS...              */
01030000/*  IF IT IS, ALLOCATE MEMBER IN IT ..     */
01040000/*  IF NOT A PDS, ALLOCATE DSN  NEW        */
01050000       CONTROL NOFLUSH
01060000         IF &MEMBER NE   THEN DO
01070000           /*  PDS */
01080000           ERROR DO
01090000             WRITE SORRY, YOU MUST ALLOCATE A PDS FOR THE FIRST TIME YOURSELF
01100000             WRITE PERHAPS YOU MISSPELLED THE PDSNAME  &TSONAME
01110000             FREE F(SYSPRINT)
01120000             ALLOC F(SYSPRINT) DA(*)
01130000             GOTO DONE
01140000           END
01150000              IF &SUBSTR(1:1,&TSONAME) EQ &STR(') THEN DO
01160000                SET &LENG EQ &LENGTH(&TSONAME) - 1 /* QUOTES SUPPLIED*/
01170000                SET &TSONAME EQ &SUBSTR(1:&LENG,&TSONAME)(&MEMBER)&STR(')
01180000                ALLOC  FILE(PANDD2) DA(&TSONAME)SHR
01190000              END
01200000              ELSE  ALLOC F(PANDD2) DA('&TSONAME.(&MEMBER.)') SHR
01210000         END
01220000         ELSE DO  /*  ORDINARY TSO DATASET 98% OF ALL PANRET COMMANDS*/
01230000           ERROR DO
01240000             WRITE IT LOOKS LIKE THIS '&TSONAME' ALREADY EXISTS SOMEWHERE
01250000             WRITE OR ELSE THIS IS AN INVALID DATASET NAME.
01260000             WRITE THE COMPUTER IS TOO STUPID TO FIGURE OUT YOUR ERROR, SO YOU
01270000             WRITE MUST CORRECT IT YOURSELF AND RERUN PANRET. SORRY, &SYSUID .
01280000             FREE ATTR(PL) F(SYSPRINT)
01290000             ALLOC F(SYSPRINT) DA(*)
01300000             GOTO DONE
01310000           END
01320000           ATTR PL  RECFM(F B S)  LRECL(80)  BLKSIZE(&BLO)
01330000           IF &SUBSTR(1:1,&TSONAME) EQ &STR(') THEN -
01340000              ALLOC DA(&TSONAME) SP(&SP 1) US(PL) F(PANDD2) TRACKS
01350000           ELSE ALLOC DA('&TSONAME') SP(&SP 1) US(PL) F(PANDD2) TRACKS
01360000       END
01370000       ERROR DO
01380000              ERROR OFF
01390000              WRITE *ERROR*  THERE WAS SOME ERROR IN THE PANVALET RETRIEVE **
01400000         /*  ERROR DETECTED  - GIVE A MESSAGE   */
01410000              WRITE THE TSO DATASET WILL NOT BE CREATED:
01420000              WRITE POSSIBLE ERRORS ARE
01430000              WRITE  1) YOU PUT A SPACE IN FRONT OF, OR AFTER, YOUR PANVALET NAME
01440000              WRITE  2) THE PANVALET NAME SPECIFIED IS NOT ON THE PRESENT PANVALET LIBRARY
01450000              WRITE  3) YOUR PAN DATASET HAS MORE THAN 3400 LINES (ERROR B-37)
01460000              WRITE     (USE THE SP PARAMETER IN THIS CASE: TRY THE HELP PANRET COMMA
01470000              WRITE  4) YOU HAVE NO PANVALET LIBRARY ALLOCATED
01480000              WRITE   ==> FIGURE OUT YOUR ERROR AND RETRY PANRET <====
01490000       /*    END OF ERROR DESCRIPTION...   CLEAN UP AND TERMINATE */
01500000              CONTROL NOMSG NOFLUSH
01510000              FREE ATTR(PL) FILE(PANDD2,SYSPRINT)
01520000              IF &MEMBER EQ  THEN DEL '&TSONAME'
01530000              ALLOC F(SYSPRINT) DA(*)
01540000              GOTO DONE
01550000         END
01560002         ISPEXEC SELECT PGM(JOPANTST)
01570000         IF &LASTCC NE 0 THEN ALLOC DA(XX123456.XX123456) OLD
01580000         ERROR OFF
01590000         CONTROL NOMSG
01600000         FREE F(PANDD2,SYSPRINT) ATTR(PL)
01610000         ALLOC F(SYSPRINT) DA(*)
01620000         DONE: END
