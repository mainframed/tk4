JRP220 TITLE 'JRP DYNAMIC ALLOCATE/UNALLOCATE ROUTINE'
***********************************************************************
*                                                                     *
*             MODULE NAME = JRP220                                    *
*                                                                     *
*             DESCRIPTIVE NAME =  ALLOCATE/DEALLOCATE ROUTINE         *
*                FOR JRP     328X PRINTER"S                           *
*                                                                     *
*             AUTHOR = MIKE SCHORTMAN -                               *
*                                                                     *
*             COPYRIGHT = 9/8/82                                      *
*                                                                     *
*              MODIFIED =                                             *
*                                                                     *
*             STATUS =                                                *
*                                                                     *
*                                                                     *
*             FUNCTION =                                              *
*                                                                     *
*                                                                     *
*                                                                     *
*                OPERATION =                                          *
*                                                                     *
*                                                                     *
*                                                                     *
*                                                                     *
*              NOTES =                                                *
*                                                                     *
*                 DEPENDENCIES =                                      *
*                                                                     *
*                                                                     *
*                                                                     *
*                 REGISTER CONVENTIONS = STANDARD CONVENTIONS.        *
*                    REGISTERS 1 TO 11 = WORK REGISTERS               *
*                    REGISTER  12      = ADDRESSABILITY TO JRP220     *
*                                        CSECT                        *
*                    REGISTER  13      = SAVE AREA REGISTER AND       *
*                                        ADDRESSABILITY TO GETMAINED  *
*                                        WORK AREA                    *
*                    REGISTERS 14,15   = WORK REGISTERS               *
*                                                                     *
*                                                                     *
*                PROCESSOR = ASM                                      *
*                                                                     *
*                MODULE SIZE = -- BYTES                               *
*                                                                     *
*                ATTRIBUTES = SCHEDULER KEY 8, PROBLEM STATE          *
*                   PROBLEM PROGRAM STATE                             *
*                                                                     *
*             ENTRY POINTS = JRP220 (ONLY ENTRY POINT)                *
*                                                                     *
*                LINKAGE =                                            *
*                   FROM JRP200 AS A SUBROUTINE                       *
*                                                                     *
*             INPUT = DYNAMIC AREA ADDRESS FOR ALLOCATE OR DEALLOCATE *
*                                                                     *
*             OUTPUT = NONE                                           *
*                                                                     *
*             EXIT - NORMAL = AT PROGRAM END VIA BRANCH REGISTER 14   *
*                                                                     *
*                                                                     *
*             EXTERNAL REFERENCES =                                   *
*                                                                     *
*                ROUTINES = NONE                                      *
*                                                                     *
*                DATA AREAS = NONE                                    *
*                                                                     *
*                CONTROL BLOCKS = PWT                                 *
*                                                                     *
*                                                                     *
*             TABLES = DATA AREA TO BE GETMAINED.  MAPPED BY DSECT    *
*                BEGINNING AT LABEL MSDWA.                            *
*                                                                     *
*             MACROS = JRPSAVE,JRPRTRN,TPUT,                          *
*                                                                     *
*             CHANGE ACTIVITY = NONE                                  *
*                                                                     *
*             MESSAGES =                                              *
*                                                                     *
*                THIS PROGRAM IS THE PROPERTY OF MIKE SCHORTMAN       *
*                CONN. MUTUAL LIFE INS. CO..                          *
*                                                                     *
*             ABEND CODES = U220                                      *
*                                                                     *
***********************************************************************
JRP220 CSECT
         JRPSAVE REG=12,ID=*
         L     R11,0(R1)            GETMAINED AREA
         USING J1DWA,R11
         TM    J1DFLAG1,J1DALC
         BO    J1DUNALC
***                                                                 ***
*        INITIALIZE DYNAMIC ALLOCATION PARAMETER AREA                 *
**                                                                  ***
J1DINDYN DS    0H
         LA    R8,J1DS99
         LR    R1,R8                 ADDRESS OF PARALLOC
         LA    R8,4(R8)            INCREMENT TO DAL REQUEST BLOCK
         ST    R8,0(R1)            SET UP RB PTR ADDR.
         OI    0(R1),S99RBPND
         USING S99RB,R8
         XC    S99RB(DYNRBSIZ),S99RB
         MVI   S99RBLN,DYNRBSIZ    SIZE OF RB
         LA    R3,DYNRBSIZ(R8)     INCREMENT TO FIRST TEXT POINTER
         USING DYNTXTPT,R3
         LA    R4,TXTPTSIZ(R3)     INCREMENT TO TEXT UNITS
         USING S99TUNIT,R4
*        DSNAME TEXT UNIT
         ST    R4,DSNTXTPT         SET UP ADDR OF FIRST TEXT UNIT
         XC    S99TUKEY(DYNEND-DYNTXTND),S99TUKEY ZERO TEXT UNITS
         MVI   S99TUKEY+1,DALDSNAM DSNAME IS SUPPLIED TO SVC99
         MVI   S99TUNUM+1,X'01'    NUMBER OF ENTRIES
         MVI   S99TULNG+1,44       SIZE OF DSNAME
         LA    R4,L'DSNTXTUT(R4)   INCREMENT TO SSREQ TEXT UNIT
*        SSREQ TEXT UNIT
         ST    R4,SSRTXTPT         ADDRESS OF SSREQ TEXT UNIT
         MVI   S99TUKEY+1,DALSSREQ THIS IS A REQUEST FOR SUBSYSTEM
         MVI   S99TULNG+1,4        LENGTH OF PARM FIELD
         L     R2,PSATNEW-PSA      TCB NEW
         L     R2,TCBJSCB-TCB(R2)  JSCB
         L     R2,JSCBSSIB-IEZJSCB(R2) SSIB
         USING SSIB,R2
         MVC   S99TUPAR(4),SSIBSSNM PRIMARY SUBSYSTEM NAME
         DROP  R2                  UNUSE SSIB
         MVI   S99TUNUM+1,X'01'    NUMBER OF REQUESTS
         LA    R4,L'SSRTXTUT(R4)   INCREMENT TO DDN TEXT UNIT
*        DDNAME RETURN TEXT UNIT
         ST    R4,DDNTXTPT         ADDR OF DDNAME RETURNED TEXT UNIT
         MVI   DDNTXTPT,S99TUPLN   LAST TEXT UNIT PTR. IN LIST
         MVI   S99TUKEY+1,DALRTDDN RETURN DDNAME TO ME
         MVI   S99TUNUM+1,X'01'    NUMBER OF ENTRIES
         MVI   S99TULNG+1,8        LENGTH OF PARM FIELD
         MVC   S99TUPAR(8),=8CL1' ' INITIALIZE TO BLANKS
         LA    R4,L'DDNTXTUT(R4)   INCREMENT TO DDNAME UNALLOC TEXT
*        DDNAME UNALLOCATE TEXT UNIT
         ST    R4,UDNTXTPT         SAVE UNALLOC TEXT ADDRESS
         MVI   S99TUKEY+1,DUNDDNAM USE DDNAME TO UNALLOCATE
         MVI   S99TUNUM+1,X'01'    NUMBER OF ENTRIES
         MVI   S99TULNG+1,8        LENGTH OF PARM FIELD
         LA    R4,L'UDNTXTUT(R4)   INCREMENT TO OVERRIDE TEXT
*        DISPOSITION OVERRIDE TEXT UNIT
         ST    R4,UORTXTPT         SAVE ADDRESS OF OVERRIDE TEXT
         MVI   UORTXTPT,S99TUPLN   LAST TEXT UNIT PTR. IN LIST
         MVI   S99TUKEY+1,DUNOVDSP OVERRIDE DISPOSITION KEY
         MVI   S99TULNG+1,1        LENGTH OF PARM FIELD
         MVI   S99TUNUM+1,X'01'    NUMBER OF ENTRIES
         DROP  R4
***                                                                 ***
*    SET UP TO DYNAMICALLY ALLOCATE DATA SET                          *
***                                                                 ***
         MVI   S99VERB,S99VRBAL    ALLOCATION VERB
         LA    R3,DISPDSN(R8)      ADDR. DSNAME TEXT UNIT
         ST    R3,S99TXTPP         STORE ADDR OF TEXT UNIT PTRS
         L     R4,0(,R3)           ADDR OF FIRST TEXT UNIT
         MVC   6(44,R4),J1DDSN     GET DATA SET NAME FROM SSOB
         BAL   R10,DYNAL           DYNAMICALLY ALLOCATE DATA SET
         L     R3,S99TXTPP         ADDRESS OF TEXT UNITS
         L     R4,DDNTXTPT-DYNTXTPT(R3) ADDRESS OF DDNAME TEXT UNIT
         MVC   J1DDNM(8),6(R4) PUT DDNAME IN DCB
         ST    R3,J1DST3
         ST    R8,J1DST8
         XR    R15,R15
         B     JRPRTRN
*****************
J1DUNALC DS    0H
         L     R8,J1DST8
         L     R3,J1DST3
         USING S99RB,R8
         MVI   S99VERB,S99VRBUN    UNALLOCATION VERB
         LA    R3,DISPUAL(R8)      INCREMENT TO UNAL
         ST    R3,S99TXTPP         STORE ADDR. OF TEXT UNIT PTRS.
         L     R3,0(R3)            ADDRESS OF DDNAME FOR UNALLOCATE
         L     R4,DISPDDN(R8)      ADDRESS OF DDNAME UNIT
         MVC   6(8,R3),6(R4)       PUT DDNAME INTO TEXT FOR UNALLOC
         L     R4,DISPOVR(R8)      ADDRESS OF OVERIDE UNIT
         TM    J1DFLAG1,J1DKEEP    IS INPUT TO BE KEPT
         BO    KEEPDS              YES
         MVI   6(R4),X'04'         DELETE DATA SET
         B     DYNUN               UNALLOCATE WITH DELETE DISP
KEEPDS   MVI   6(R4),X'08'         KEEP DATA SET
DYNUN    BAL   R10,DYNAL           DYNAMICALLY UNALLOCATE DATA SET
         NI    J1DFLAG1,255-J1DKEEP RESET KEEP OPTION
         MVC   J1DHNME(18),=18CL1' ' RESET STATUS FIELD
         XR    R15,R15
JRPRTRN JRPRTRN
DYNAL    DS    0H
         LA    R1,J1DS99
         SVC   99
         LTR   R15,R15
         BZR   R10
         L     R10,S99ERROR-S99RB(R8)
         MVC   J1DWERR1(25),=C'BAD DYNALLOC RETURN CODE '
         TITLE 'F A T A L  E R R O R  M E S S A G E S'
P2FATALF DS    0H
         CVD   R15,J1DWORK         CONVERT CALLERS RETURN CODE IN R15
         STM   R14,R12,12(R13)     SAVE MY JRPREGS
         UNPK  J1DWERR2,J1DWORK+6(2) 999 MAX RC SUPPORTED
         OI    J1DWERR2+L'J1DWERR2-1,C'0' MAKE PRINTABLE
         LA    R1,J1DWERR          GET MF=L FORM OF WTO
         SVC   35                  ISSUE WTO
         LA     R15,4
         ABEND  220,DUMP
         B      JRPRTRN
J2SYN    EQU    *
P2GEOD   EQU    *
         TITLE 'DYNAMIC ALLOCATION PARAMETER LIST DSECT'
         IEFZB4D0
         TITLE 'DYNAMIC ALLOCATION TEXT UNIT KEYS'
         IEFZB4D2
         TITLE 'SUBSYSTEM OPTION BLOCK - ACCESS SYSOUT DATA SETS'
         IEFJSSOB (SO)
         TITLE 'JOB ENTRY SUBSYSTEM COMMUNICATIONS TABLE'
         IEFJESCT
         TITLE 'SUBSYSTEM IDENTIFICATION BLOCK'
         IEFJSSIB ,                SSIB MAPPING MACRO
         TITLE 'WRITER DYNAMIC ALLOCATION WORK AREA'
DYNTXTPT DSECT
DSNTXTPT DS    F                   POINTER TO DSNAME TEXT UNIT
SSRTXTPT DS    F                   PTR. TO SUBSYSTEM REQUEST TEXT UNIT
DDNTXTPT DS    F                   PTR TO DDNAME RETURNED TEXT UNIT
UDNTXTPT DS    F                   DDNAME TEXT PTR. FOR UNALLOCATE
UORTXTPT DS    F                   OVERRIDE TEXT PTR. FOR UNALLOCATE
DYNTXTND EQU   *                   END OF TEXT POINTERS
DSNTXTUT DS    CL50                DSNAME TEXT UNIT
SSRTXTUT DS    CL10                SUBSYSTEM REQUEST TEXT UNIT
DDNTXTUT DS    CL14                DDNAME RETURNED  TEXT UNIT
UDNTXTUT DS    CL14                UNALLOCATE DDNAME TEXT UNIT
UORTXTUT DS    CL7                 OVERRIDE DISPOSITION FOR UNALLOCATE
DYNEND   EQU   *
TXTPTSIZ EQU   DYNTXTND-DYNTXTPT   SIZE OF TEXT UNIT POINTERS
DYNRBSIZ EQU   S99RBEND-S99RB      SIZE OF DYN ALLOC REQUEST BLOCK
DYN      EQU   TXTPTSIZ+DYNRBSIZ+(DYNEND-DSNTXTUT)+L'S99RBPTR
DISPDDN  EQU   DYNRBSIZ+DDNTXTPT-DYNTXTPT    ADDR. OF DDNAME IN DYN LST
DISPOVR  EQU   DYNRBSIZ+UORTXTPT-DYNTXTPT    ADDR. OVRIDE IN DYN LST
DISPUAL  EQU   DYNRBSIZ+UDNTXTPT-DYNTXTPT    ADDR. UNAL IN DYN LST
DISPDSN  EQU   DYNRBSIZ+DSNTXTPT-DYNTXTPT    ADDR. DSNAME IN DYN LIST
         TITLE 'MISC EQUATES AND DSECTS'
         JRPREGS
         PUSH  PRINT
         POP   PRINT
         CVT     DSECT=YES          CVT
         IHAPSA  DSECT=YES          PSA
         IKJTCB  DSECT=YES          TCB
         IEZJSCB                   JSCB
         JRPDMAC
         END
