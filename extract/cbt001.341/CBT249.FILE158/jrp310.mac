JRP310 TITLE 'JRP UNSOLICITED MESSAGE PROCESSOR'
***********************************************************************
*                                                                     *
*             MODULE NAME = JRP310                                    *
*                                                                     *
*             DESCRIPTIVE NAME =  UNSOLICITED MESSAGE PROCESSOR       *
*                                 FOR JRP CONTROL TERMINALS           *
*                                                                     *
*             AUTHOR = MIKE SCHORTMAN -                               *
*                                                                     *
*             COPYRIGHT = 1/8/84                                      *
*                                                                     *
*              MODIFIED = *
*                                                                     *
*             STATUS = ONE INVALID MESSAGE ISSUED ON FIRST COMMAND    *
*                      PASS,NOT CRITICAL,WILL BE FIXED.               *
*                                                                     *
*             FUNCTION =                                              *
*                PARSES MVS OPERATOR COMMANDS AND INPUT FROM          *
*                JRP INITIALIZATION DATA SET.                         *
*                                                                     *
*                OPERATION =                                          *
*                   SCAN COMMANDS OR INPUT PASSED BY JRP100           *
*                   FOR JRP KEYWORDS AND MAKES APPROPRIATE            *
*                   MODIFICATIONS TO DYNAMIC PRINTER ENTRY AREAS.     *
*                                                                     *
*              NOTES =                                                *
*                                                                     *
*                 DEPENDENCIES = JRPDMAC AND/OR JRPSMAC MUST NOT      *
*                              BE CHANGED WITHOUT REASSEMBLY.         *
*                                                                     *
*                                                                     *
*                 REGISTER CONVENTIONS = STANDARD CONVENTIONS.        *
*                    REGISTERS 1 TO 11 = WORK REGISTERS               *
*                    REGISTER  12      = ADDRESSABILITY TO JRP310     *
*                                        CSECT                        *
*                    REGISTER  13      = SAVE AREA REGISTER AND       *
*                                        ADDRESSABILITY TO GETMAINED  *
*                                        WORK AREA                    *
*                    REGISTERS 14,15   = WORK REGISTERS               *
*                                                                     *
*                                                                     *
*                PROCESSOR = ASM                                      *
*                                                                     *
*                MODULE SIZE = 5K BYTES                               *
*                                                                     *
*                ATTRIBUTES = SCHEDULER KEY 8, PROBLEM STATE          *
*                   PROBLEM PROGRAM STATE                             *
*                                                                     *
*             ENTRY POINTS = JRP310 (ONLY ENTRY POINT)                *
*                                                                     *
*                LINKAGE =                                            *
*                   FROM JRP300 AS A SUBROUTINE                       *
*                                                                     *
*             INPUT = R8 POINTS TO JRPPFLG ,R10 POINTS TO JRP"S       *
*                DYNAMIC PRINTER AREAS,R11 POINTS TO COMMAND OR       *
*                INITIALIZATION INPUT.                                *
*                                                                     *
*             OUTPUT = NONE                                           *
*                                                                     *
*             EXIT - NORMAL = AT PROGRAM END VIA BRANCH REGISTER 14   *
*                                                                     *
*                                                                     *
*             EXTERNAL REFERENCES =                                   *
*                                                                     *
*                ROUTINES = IKJPARS, DAIRFAIL, GNRLFAIL               *
*                                                                     *
*                DATA AREAS = NONE                                    *
*                                                                     *
*                CONTROL BLOCKS = PWT                                 *
*                                                                     *
*                                                                     *
*             TABLES = DATA AREA TO BE GETMAINED.  MAPPED BY DSECT    *
*                BEGINNING AT LABEL P1DWA.                            *
*                                                                     *
*             MACROS = JRPSAVE,JRPRTRN,TPUT,                          *
*                                                                     *
*             CHANGE ACTIVITY = NONE                                  *
*                                                                     *
*             MESSAGES =                                              *
*                                                                     *
*                THIS PROGRAM IS THE PROPERTY OF MIKE SCHORTMAN,      *
*                                                                     *
*                                                                     *
*             ABEND CODES = NONE                                      *
*                                                                     *
***********************************************************************
JRP310 CSECT
         JRPSAVE REG=12,ID=*,LV=P1DSASIZ
         USING P1DSA,R13
         L     R4,0(R1)
         USING J1TWA,R7                   TERMINAL DSECT
         USING J1DWA,R4                   PRINTER  DSECT
*
**       MOVE MODEL DATA TO GOTTEN AREA ZERO REST FOR RENT REUS
*
         LA    R0,P1DWA
         LA    R1,(P1DCLRE-P1DWA)
         LA    R14,P1SWA
         LA    R15,P1SMDL
         MVCL  R0,R14
         L     R11,J1DTWT
*
**       FIND TERMINALS THAT WANT UNSOLICITED MESSAGES AND SEND THEM
*
TABEND   DS    0H
         CLC   0(4,11),=C'PEND'
         BE    JRPRTRN
RETBUSY  L     R7,0(11)
         LTR   R7,R7
         BZ    UNSTBMP
         TM    J1TFLG,J1TUNSOL
         BNO   UNSTBMP
         TM    J1TFLG,J1TBUSY
         BO    TIMER
         OI    J1TFLG,J1TBUSY
         L     R2,J1TRPL
         TM    J1TFLG1,J1TSNA                   ;SNA DEVICE ???
         BNO   MODCB
         L     R5,=X'00010000'
         SEND  RPL=(R2),STYPE=REQ,CONTROL=SIGNAL,SIGDATA=(R5),         X
               OPTCD=(SPEC,SYN)
TESTDIR  TM    J1TFLG,J1TRECD
         BZ    MODCB
         STIMER WAIT,BINTVL==F'100'         WAIT 1 SECOND
         B     TESTDIR
MODCB    MODCB AM=VTAM,RPL=(R2),CHNGDIR=NCMD,CONTROL=DATA
         OI    RPLOPT5-IFGRPL(R2),RPLDLGIN      OPTCD=CS
         NI    RPLOPT1-IFGRPL(R2),255-RPLASY    OPTCD=SYN
         NI    RPLOPT5-IFGRPL(R2),255-RPLNODE   OPTCD=SPEC
         NI    RPLRH3-IFGRPL(R2),255-RPLBB      BRACKET=NBB
         NI    RPLRH3-IFGRPL(R2),255-RPLEB      BRACKET=NEB
         MVI   RPLCHN-IFGRPL(R2),RPLONLY        CHAIN=ONLY
         LM    R5,R6,J1DMSGLN
         BAL   R10,P1TSEND
         B     P1CMOK
UNSTBMP  DS    0H
         LA    R11,4(R11)
         B     TABEND
TIMER    STIMER WAIT,BINTVL==F'100'         WAIT 1 SECONDS
         B     RETBUSY
P1TSEND  DS    0H
         C     R5,J1DCOLS
         BNL   P1CSETMR
         NI    J1TFLG,255-J1TMRMSG
         B     P1CSETNM
P1CSETMR DS    0H
         OI    J1TFLG,J1TMRMSG      MORE MESSAGE
         LR    R15,R6
         A     R15,J1DCOLS           NEW ADDRESS
         LR    R14,R5
         S     R14,J1DCOLM1
         ST    R15,J1TNEWAD
         ST    R14,J1TNEWLN
         LA    R5,J1DCOLM1
P1CSETNM L     R14,J1TADDSV
         LTR   R14,R14
         BZ    TOP
         CLC   6(4,R14),=C'TEND'
         BE    TOP
         CLC   8(4,R14),=C'TEND'
         BNE   NOBUMP
         MVI   J1DAREA2,C' '
         MVC   J1DAREA2+1(78),J1DAREA2
         B     NOBUMP
TOP      DS    0H
         MVI   J1DAREA2,C'-'
         MVC   J1DAREA2+1(78),J1DAREA2
         L     R14,J1DADTBL
NOBUMP   MVC   J1DADD1(2),0(R14)
         MVC   J1DADD2(2),2(R14)
         MVC   J1DAREA1(80),=80C' '
         EX    R5,PRTMVC
         LA    R14,2(R14)
         ST    R14,J1TADDSV
         LA    R5,J1DOUTLN
         LA    R6,J1DOUTBF
         L     R2,J1TRPL
         SEND  RPL=(R2),RECLEN=(R5),AREA=(R6),STYPE=REQ
         LTR   R15,R15
         BNZ   J1FATALF
         TM    J1TFLG,J1TMRMSG     MORE DATA
         BZR   R10
         L     R6,J1TNEWAD
         L     R5,J1TNEWLN
         B     P1TSEND
**********************************************************************
*
**             GIVE OPER STATUS TO MODIFY COMMAND
*
P1CMOK   DS    0H                  GIVE OPER OK P1G
         MVC   J2DMSS(7),=CL7'OK'
P1CMPOK  DS    0H
         LA    R5,L'J2DOK-1
         LA    R6,J2DOK
         L     R2,J1TRPL
         MODCB AM=VTAM,RPL=(R2),CHNGDIR=CMD,OPTCD=CA
*        OI    RPLRH3-IFGRPL(R2),RPLCMD         CHNGDIR=CMD
*        NI    RPLOPT5-IFGRPL(R2),255-RPLDLGIN  OPTCD=CA
         BAL   R10,P1TSEND
         NI    J1TFLG,255-J1TBUSY
         OI    J1TFLG,J1TRECD
         B     UNSTBMP
PRTMVC   MVC   J1DAREA1,0(R6)
J1FATALF EQU   *
JRPRTRN  DS    0H
         DROP  R7
         DROP  R4
         JRPRTRN
         TITLE 'LITERAL POOL'
         LTORG
         TITLE 'STATIC STORAGE'
P1SWA    DS    0D
**********************************************************************
****NOTE NOTE NOTE  MUST KEEP STATIC AND DYNAMIC AREAS IN SYNC***
****DYNAMIC IS GETMAINED AND STATIC IS USED TO INITIALIZE IT.
**********************************************************************
J2SOK    DC    CL15'JRP100I INVALID'
P1SMDL   EQU   *-P1SWA
         SPACE 2
         TITLE 'DYNAMIC - GOTTEN AREA'
P1DSA    DSECT
         DS    9D                  O/S SAVE
****NOTE NOTE NOTE  MUST KEEP STATIC AND DYNAMIC AREAS IN SYNC***
****DYNAMIC IS GETMAINED AND STATIC IS USED TO INITIALIZE IT.
P1DWA    DS    0D
J2DOK    DC    CL15'JRP100I INVALID'
J2DMSS   EQU   J2DOK+8,7
***********************************************************************
*  EVERYTHING FROM HERE ON IS SET TO LOW VALUES AT INIT TIME          *
***********************************************************************
         DS    0F
P1DCLRE  EQU   *                   END OF DYNAMIC AREA
P1DSASIZ EQU   *-P1DWA             SIZE OF DYNAMIC AREA
         TITLE 'MISC EQUATES AND DSECTS'
         JRPTMAC
         JRPDMAC
         JRPREGS
         IFGRPL AM=VTAM
         PUSH  PRINT
*        PRINT NOGEN               SAVE A LITTLE PAPER
         PRINT ON,GEN
         END
