JRP122 TITLE 'JRP USERID PASSWORD PROCESSING'
***********************************************************************
*                                                                     *
*             MODULE NAME = JRP122                                    *
*                                                                     *
*             DESCRIPTIVE NAME =  UID AND PASSWORD PROCESSING.        *
*                328X PRINTER"S                                       *
*                                                                     *
*             AUTHOR = MIKE SCHORTMAN -                               *
*                                                                     *
*             COPYRIGHT = 1/8/84                                      *
*                                                                     *
*              MODIFIED = *
*                                                                     *
*             STATUS =                                                *
*                                                                     *
*                                                                     *
*             FUNCTION =                                              *
*                PARSES UID AND PASSWORD FIELDS                       *
*                                                                     *
*                OPERATION =                                          *
*                                                                     *
*              NOTES =                                                *
*                                                                     *
*                 DEPENDENCIES = JRPTMAC, ACF/VTAM                    *
*                                                                     *
*                                                                     *
*                 REGISTER CONVENTIONS = STANDARD CONVENTIONS.        *
*                    REGISTERS 1 TO 11 = WORK REGISTERS               *
*                    REGISTER  12      = ADDRESSABILITY TO JRP122     *
*                                        CSECT                        *
*                    REGISTER  13      = SAVE AREA REGISTER AND       *
*                                        ADDRESSABILITY TO GETMAINED  *
*                                        WORK AREA                    *
*                    REGISTERS 14,15   = WORK REGISTERS               *
*                                                                     *
*                                                                     *
*                PROCESSOR = ASM                                      *
*                                                                     *
*                MODULE SIZE =                                        *
*                                                                     *
*                ATTRIBUTES = SCHEDULER KEY 8, PROBLEM STATE          *
*                   PROBLEM PROGRAM STATE                             *
*                                                                     *
*             ENTRY POINTS = JRP122 (ONLY ENTRY POINT)                *
*                                                                     *
*                LINKAGE =                                            *
*                   FROM JRP120 AS A SUBROUTINE                       *
*                                                                     *
*             INPUT = R8 POINTS TO JRPPFLG ,R10 POINTS TO JRP"S       *
*                DYNAMIC PRINTER AREAS,R11 POINTS TO COMMAND OR       *
*                INITIALIZATION INPUT.                                *
*                                                                     *
*             OUTPUT = NONE                                           *
*                                                                     *
*             EXIT - NORMAL = AT PROGRAM END VIA BRANCH REGISTER 14   *
*                                                                     *
*                                                                     *
*             EXTERNAL REFERENCES =                                   *
*                                                                     *
*                ROUTINES =                                           *
*                                                                     *
*                DATA AREAS = NONE                                    *
*                                                                     *
*                CONTROL BLOCKS = TWT                                 *
*                                                                     *
*                                                                     *
*             TABLES = DATA AREA TO BE GETMAINED.  MAPPED BY DSECT    *
*                BEGINNING AT LABEL P1DWA.                            *
*                                                                     *
*             MACROS = JRPSAVE,JRPRTRN,TPUT,                          *
*                                                                     *
*             CHANGE ACTIVITY = NONE                                  *
*                                                                     *
*             MESSAGES =                                              *
*                                                                     *
*                THIS PROGRAM IS THE PROPERTY OF MIKE SCHORTMAN,      *
*                CONN. MUTUAL LIFE INS. CO.                           *
*                                                                     *
*             ABEND CODES = NONE                                      *
*                                                                     *
***********************************************************************
JRP122 CSECT
         JRPSAVE REG=12,ID=*,LV=P1DSASIZ
         USING P1DSA,R13
         L     R11,0(R1)
         USING J1TWA,R11                  TERMINAL DSECT
*
**       MOVE MODEL DATA TO GOTTEN AREA, ZERO REST.
*
P1WINIT  DS    0H
         LA    R0,P1DWA          START OF DYNAMIC AREA
         LA    R1,(P1DCLRE-P1DWA) LENGTH OF DYNAMIC AREA
         LA    R14,P1SWA           START OF STATIC DATA
         LA    R15,P1SMDL          LENGTH OF MODEL DATA
         MVCL  R0,R14              MOVE W/ZERO TO BE RENT,REUS
P1PROCSS DS    0H
         CLI   J1DTXT,C' '          USER ID
         BE    P1INVUSR
         MVC   J1DUSER(7),J1DTXT
         CLI   J1DTXT+10,C' '
         BE    P1INVPSS
         MVC   J1DPSWD(5),J1DTXT+10
*
****PROCESS LOGON TEXT
*
         MVC   J1TPWD(5),J1DPSWD
         MVC   J1TUID(7),J1DUSER
         LINK  EP=JRP123,PARAM=((R11)),MF=(E,JTDLINK)
         B     *+4(R15)
         B     P1CMPOK               ALL O.K.            RC=0
         B     P1NWPSS               PASSWORD EXPIRED    RC=4
         B     P1INVPSS              INVALID PASSWORD    RC=8
         B     P1INVUSR              INVALID USERID      RC=12
         B     P1NOAUTH              USER NOT AUTHORIZED RC=16
         B     P1UNNOWN              CONDITION UNKNOWN   RC=20
P1UNNOWN EQU   *
P1CMPOK  DS    0H
         MVI   J1DTXT,C' '
         MVC   J1DTXT+1(79),J1DTXT
         OI    J1TFLG,J1TLOGN
         LA    R5,J1DOUTBF
         LA    R6,J1DOUTLN
         B     P1TNOERR
P1INVPSS DS    0H
         LA    R5,J1TRQBF
         LA    R6,J1TRQBFL
         MVC   J1TERRLN(80),JTPWERR
         B     P1TLGERR
P1NWPSS  DS    0H
         LA    R5,J1TRQBF
         LA    R6,J1TRQBFL
         MVC   J1TERRLN(80),JTPWERR1
         B     P1TLGERR
P1NOAUTH DS    0H
         LA    R5,J1TRQBF
         LA    R6,J1TRQBFL
         MVC   J1TERRLN(80),JTUNERR
         B     P1TLGERR
P1INVUSR DS    0H
         LA    R5,J1TRQBF
         LA    R6,J1TRQBFL
         MVC   J1TERRLN(80),JTIDERR
P1TNOERR DS    0H
P1TLGERR L     R2,J1TRPL
         SEND  RPL=(R2),RECLEN=(R6),AREA=(R5),CHAIN=ONLY,              X
               OPTCD=(SYN,SPEC,CA),STYPE=REQ,BRACKET=(NEB,NBB),        X
               CHNGDIR=(CMD)
         LTR   R15,R15
         BNZ   J1FATALF
JRPRTRN  DS    0H
         DROP  R11
         JRPRTRN
J1FATALF DS    0H
         ABEND 121
J2SYN    EQU   *    DUMMY LABEL
P1GEOD   EQU   *    DUMMY LABEL
         LTORG
         TITLE 'STATIC STORAGE'
P1SWA    DS    0D
**********************************************************************
****NOTE NOTE NOTE  MUST KEEP STATIC AND DYNAMIC AREAS IN SYNC***
****DYNAMIC IS GETMAINED AND STATIC IS USED TO INITIALIZE IT.
**********************************************************************
JSIDERR  DC    CL80'JRP001T - INVALID OR BLANK USERID'
JSPWERR  DC    CL80'JRP002T - INVALID OR BLANK PASSWORD'
JSIDERR1 DC    CL80'JRP003T - USER ID DOES NOT EXIST '
JSPWERR1 DC    CL80'JRP004T - PASSWORD HAS EXPIRED'
JSUNERR  DC    CL80'JRP005T - CONDITION UKNOWN..TERMINATING'
JTSLINK  LINK  EP=JRP123,SF=L
P1SMDL   EQU   *-P1SWA
         SPACE 2
         TITLE 'DYNAMIC - GOTTEN AREA'
P1DSA    DSECT
         DS    9D                  O/S SAVE
****NOTE NOTE NOTE  MUST KEEP STATIC AND DYNAMIC AREAS IN SYNC***
****DYNAMIC IS GETMAINED AND STATIC IS USED TO INITIALIZE IT.
P1DWA    DS    0D
JTIDERR  DC    CL80'JRP001T - INVALID OR BLANK USERID'
JTPWERR  DC    CL80'JRP002T - INVALID OR BLANK PASSWORD'
JTIDERR1 DC    CL80'JRP003T - USER ID DOES NOT EXIST '
JTPWERR1 DC    CL80'JRP004T - PASSWORD HAS EXPIRED'
JTUNERR  DC    CL80'JRP005T - CONDITION UKNOWN..TERMINATING'
JTDLINK  LINK  EP=JRP123,SF=L
***********************************************************************
*  EVERYTHING FROM HERE ON IS SET TO LOW VALUES AT INIT TIME          *
***********************************************************************
         DS    0F
P1DWORK  DS    D                   WORK FIELD
P1DCLRE  EQU   *
P1DSASIZ EQU   *-P1DWA             SIZE OF DYNAMIC AREA
         TITLE 'MISC EQUATES AND DSECTS'
         JRPTMAC
         JRPREGS
         ISTDNIB
         IFGRPL AM=VTAM
         PUSH  PRINT
*        PRINT NOGEN               SAVE A LITTLE PAPER
         PRINT ON,GEN
         END
