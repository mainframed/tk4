         MACRO
&CSNAME  LINKSAVE &BASE=,&GETMAIN=NO
.*********************************************************************
.*  MACRO FOR STANDARD LINKAGE CONVENTIONS AT PROGRAM ENTRY.       ***
.*  USED IN CONJUNCTION WITH THE LINKBACK MACRO.                   ***
.*                                                                 ***
.*  BASE=NN     REGISTER NUMBER OF BASE REGISTER.                  ***
.*                                                                 ***
.*  GETMAIN=YES WILL DO A GETMAIN FOR STORAGE SPACE.  SPACE        ***
.*              OBTAINED WILL BE 72 BYTES FOR SAVE AREA, PLUS      ***
.*              64 BYTES FOR PROGRAMMER'S USE.  R13 WILL BE        ***
.*              THE BASE ADDRESS OF THE STORAGE AREA.              ***
.*                                                                 ***
.*  CHUCK HOFFMAN, SYSTEMS PROGRAMMING, GTEL COMPUTING CENTER      ***
.*********************************************************************
         GBLC  &GMN
         LCLA  &C,&D
&GMN     SETC  '&GETMAIN'
         AIF   ('&GMN' EQ 'YES').FORM2
&CSNAME  CSECT
&C       SETA  K'&CSNAME
&D       SETA  ((20+&C)/2)*2
         B     &D.(0,15)                BRANCH AROUND ID
         DC    AL1(&C)                    LENGTH
         DC    CL&C'&CSNAME'              ID
         DC    CL8'&SYSDATE'              SYSTEM DATE:  MM/DD/YY
         DC    CL6' &SYSTIME'             SYSTEM TIME:  HH.MM
         STM   14,12,12(13)             SAVE R0-R12, R14-R15
         LR    &BASE,15                 LOAD BASE ADDRESS
         USING &CSNAME,&BASE            ASSIGN BASE REGISTER
         ST    13,MYSV&SYSNDX+4         STORE ADDR OF PREV SAVE AREA
*                                         INTO MY SAVE AREA
         LA    14,MYSV&SYSNDX           LOAD ADDR OF MY SAVE AREA
         ST    14,8(0,13)                 STORE IT INTO PREV SAVE AREA
         LR    13,14                      ALSO LOAD IT INTO R13
         B     MYSV&SYSNDX+72           BRANCH AROUND MY SAVE AREA
MYSV&SYSNDX DC 18F'0'                   MY SAVE AREA
         MEXIT
.FORM2   ANOP
&CSNAME  CSECT
&C       SETA  K'&CSNAME
&D       SETA  ((20+&C)/2)*2
         B     &D.(0,15)                BRANCH AROUND ID
         DC    AL1(&C)                    LENGTH
         DC    CL&C'&CSNAME'              ID
         DC    CL8'&SYSDATE'              SYSTEM DATE:  MM/DD/YY
         DC    CL6' &SYSTIME'             SYSTEM TIME:  HH.MM
         STM   14,12,12(13)             SAVE R0-R12, R14-R15
         LR    &BASE,15                 LOAD BASE ADDRESS
         USING &CSNAME,&BASE            ASSIGN BASE REGISTER
*        GETMAIN R,LV=136               GET STORAGE FOR S.A. + 64 BYTES
         GETMAIN R,LV=136               GET STORAGE FOR S.A. + 64 BYTES
         ST    13,4(0,1)                STORE ADDR OF PREV SAVE AREA
*                                         INTO MY SAVE AREA
         ST    1,8(0,13)                ADDR OF MY SA TO PREV SAVE AREA
         LM    0,1,20(13)               RESTORE R0-R1 TO ORIGINAL VALUE
         L     13,8(0,13)               POINT R13 TO MY SAVE AREA
         MEND
