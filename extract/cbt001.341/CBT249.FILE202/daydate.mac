DAYDATE  CSECT
***********************************************************************
*
*  DAYDATE:
*      WRITTEN BY S. HANSEN
*      GTE LABORATORIES
*      40 SYLVAN ROAD
*      WALTHAM, MASSACHUSETTS
*      MARCH 1, 1978
*
*   PURPOSE:
*      THIS PROGRAM WAS WRITTEN FOR THE PURPOSE OF MONTH, WEEK, OR
*   DAY DEPENDENT PROCESSING.  ITS MAIN USE LIES IN EXECUTING IT AS
*   A STANDALONE PROGRAM IN A INITIAL STEP AND EXECUTING A SUBSEQUENT
*   STEP WHICH CHECKS FOR THE CONDITION CODE RETURNED BY DAYDATE.
*   ALSO, DAYDATE MAY BE CALLED FROM WITHIN ANOTHER PROGRAM WHICH
*   SUBSEQUENTLY USES THE PARM RETURNED BY DAYDATE IN ITS PROCESSING.
*
*   PARM:
*      THERE ARE SIX DIFFERENT PARM OPTIONS WHICH MAY BE PASSED TO
*   DAYDATE. THEY ARE:
*          DOW - FOR THE 1 BYTE NUMERIC DAY OF WEEK
*          WOM - FOR THE 1 BYTE NUMERIC WEEK OF MONTH - SEE NOTE
*          MOY - FOR THE 1 BYTE NUMERIC MONTH OF YEAR
*          LWM - FOR THE 1 BYTE CHARACTER INDICATION OF
*                THE LAST WEEK OF THE MONTH
*          DAY - FOR THE 3 BYTE CHARACTER REPRESENTATION
*                OF DAY OF WEEK
*          MON - FOR THE 3 BYTE CHARACTER REPRESENTATION
*                OF MONTH OF YEAR
*
*   INFORMATION RETURNED:
*      THE 1 BYTE NUMERIC INFORMATION RETURNED BY DAYDATE IS PASSED
*   AS THE CONDITION CODE OF DAYDATE TO THE NEXT STEP IN THE JCL
*   JOBSTREAM.  THE 1 BYTE OR 3 BYTE NUMERIC OR CHARACTER INFORMATION
*   IS PASSED TO A CALLING PROGRAM THROUGH STANDARD PARM LINKAGE
*   CONVENTIONS VIA AN ADDRESS IN R1.
*
* *** NOTE - THE ALGORITHM FOR DETERMINING WEEK OF MONTH IS BASED UPON
*      THE LOCATION OF FRIDAY.  IN OTHER WORDS, WEEK 1 IS THE WEEK
*      WHICH CONTAINS THE FIRST FRIDAY.  BY THIS ALGORITHM, WHEN THE
*      FIRST OF THE MONTH FALLS ON OR NEAR FRIDAY, THE "FIRST WEEK"
*      MAY NOT CONTAIN A MONDAY - THURSDAY.  UPON CONSIDERATION, YOU
*      MAY WISH TO ALTER THIS ALGORITHM TO FIT YOUR OWN NEEDS. ***
*
***********************************************************************
START    STM   14,12,12(13)        SAVE CALLING REGS
         BALR  11,0
         USING *,11
         LA    2,SAVEAREA          ESTABLISH RETURN LINKAGE
         ST    2,8(13)
         ST    13,SAVEAREA+4
         LR    13,2
*
* SAVE PARM INFORMATION
*
SAVEPARM LTR   1,1                 ANY PARM SPECIFIED?
         BZ    DEFAULT             IF NOT, SPECIFY DAY OF WEEK
         L     1,0(1)              LOAD POINTER INTO R1
         LH    10,0(1)             LOAD LENGTH INTO R10
         S     10,ONE              DECREMENT FOR INSERTION
         STC   10,*+5              INSERT LENGTH
         MVC   PARMSAV,2(1)        SAVE PARM
         MVC   PARMSAV+3,BLANK     BLANK OUT REMAINDER OF WORD
         B     CHECKD
DEFAULT  MVC   PARMSAV,DOW         SPECIFY DAY OF WEEK DEFAULT
         B     CHECKD
*
* GET JULIAN DAY AND YEAR
*
CHECKD   SR    1,1                 ZERO OUT R1
         TIME
         STH   1,TODAY+6           SAVE DAY FROM R1
         SRL   1,12
         STH   1,TOYEAR+6          SAVE YEAR FROM R1
         OI    TOYEAR+7,X'0F'      MAKE INTO PACKED DECIMAL
         CVB   3,TODAY             STORE DAY IN R3
         CVB   5,TOYEAR            STORE YEAR IN R5
*
* GET NUMERIC DAY OF WEEK
*
         SR    4,4                 ZERO OUT R4
         D     4,TWENTY8           DIVIDE YEAR BY 28
         LR    5,4                 PUT REMAINDER IN R5
         SR    4,4
         D     4,FOUR              DIVIDE BY 4
TEST0    CL    5,ZERO              IS QUOTIENT 0?
         BNE   TEST1               IF NO, BRANCH
         L     7,ZERO              IF YES, LOAD 0 INTO R7
         B     TESTREM             CHECK REMAINDER
TEST1    CL    5,THREE             IS QUOTIENT 3?
         BNE   TEST2               IF NO, BRANCH
         L     7,ONE               IF YES, LOAD 1 INTO R7
         B     TESTREM             CHECK REMAINDER
TEST2    CL    5,SIX               IS QUOTIENT 6?
         BNE   TEST3               IF NO, BRANCH
         L     7,TWO               IF YES, LOAD 2 INTO R7
         B     TESTREM             CHECK REMAINDER
TEST3    CL    5,TWO               IS QUOTIENT 2?
         BNE   TEST4               IF NO, BRANCH
         L     7,THREE             IF YES, LOAD 3 INTO R7
         B     TESTREM             CHECK REMAINDER
TEST4    CL    5,FIVE              IS QUOTIENT 5?
         BNE   TEST5               IF NO, BRANCH
         L     7,FOUR              IF YES, LOAD 4 INTO R7
         B     TESTREM             CHECK REMAINDER
TEST5    CL    5,ONE               IS QUOTIENT 1?
         BNE   TEST6               IF NO, BRANCH
         L     7,FIVE              IF YES, LOAD 5 INTO R7
         B     TESTREM             CHECK REMAINDER
TEST6    L     7,SIX               LOAD 6 INTO R7
TESTREM  CL    4,ZERO              IS REMAINDER 0?
         BNE   TEST11              NO, BRANCH
LEAPYEAR L     8,MON28             IF YES, IT IS LEAPYEAR
         A     8,ONE               ADD 1 TO FEBRUARY
         ST    8,MON28
         B     COMPDAY
TEST11   CL    4,ONE               IS REMAINDER 1?
         BNE   TEST12              NO, BRANCH
         A     7,TWO               ADD FUDGE FACTOR
         B     COMPDAY
TEST12   CL    4,TWO               IS REMAINDER 2?
         BNE   TEST13              NO, BRANCH
         A     7,THREE             ADD FUDGE FACTOR
         B     COMPDAY
TEST13   CL    4,THREE             IS REMAINDER 3?
         BNE   TEST14              NO, BRANCH
         A     7,FOUR              ADD FUDGE FACTOR
         B     COMPDAY
TEST14   CL    4,FOUR              IS REMAINDER 4?
         BNE   TEST15              NO, BRANCH
         A     7,FIVE              ADD FUDGE FACTOR
         B     COMPDAY
TEST15   CL    4,FIVE              IS REMAINDER 5?
         BNE   COMPDAY             NO, BRANCH
         A     7,SIX               ADD FUDGE FACTOR
COMPDAY  AR    7,3                 ADD DAY OF YEAR
         S     7,ONE               SUBTRACT ONE
         SR    6,6
         D     6,SEVEN             DIVIDE TOTAL BY 7
DAYNUM   CLC   PARMSAV,DAY         IS NAME OF DAY WANTED?
         BE    DAYNAME             YES, BRANCH
         CLC   PARMSAV,DOW         IS NUMBER OF DAY WANTED?
         BNE   MONTHLY             NO, BRANCH
         LR    15,6                YES, NUM IN R15
         B     PARM1               PASS PARM AND RETURN
*
* GET THE THREE CHARACTER REPRESENTATION OF DAY
*
DAYNAME  CL    6,ONE               IS THE NUM 1?
         BNE   TUEDAY              NO, BRANCH
         L     10,MON              YES, IT IS MON
         B     PARM3               PASS PARM AND RETURN
TUEDAY   CL    6,TWO               IS THE NUM 2?
         BNE   WEDDAY              NO, BRANCH
         L     10,TUE              YES, IT IS TUE
         B     PARM3               PASS PARM AND RETURN
WEDDAY   CL    6,THREE             IS THE NUM 3?
         BNE   THUDAY              NO, BRANCH
         L     10,WED              YES, IT IS WED
         B     PARM3               PASS PARM AND RETURN
THUDAY   CL    6,FOUR              IS THE NUM 4?
         BNE   FRIDAY              NO, BRANCH
         L     10,THU              YES, IT IS THU
         B     PARM3               PASS PARM AND RETURN
FRIDAY   CL    6,FIVE              IS THE NUM 5?
         BNE   SATDAY              NO, BRANCH
         L     10,FRI              YES, IT IS FRI
         B     PARM3               PASS PARM AND RETURN
SATDAY   CL    6,SIX               IS THE NUM 6?
         BNE   SUNDAY              NO, BRANCH
         L     10,SAT              YES, IT IS SAT
         B     PARM3               PASS PARM AND RETURN
SUNDAY   L     10,SUN              IT MUST BE SUN
         B     PARM3               PASS PARM AND RETURN
*
* GET THE NUMERIC AND CHARACTER REPRESENTIONS OF MONTH OF YEAR
*
MONTHLY  LR    8,3                 LOAD DAY OF YEAR INTO R8
         C     8,MON31             IS IT JANUARY?
         BH    TESTFEB             NO, BRANCH
         L     15,ONE              YES, PUT 1 IN R15
         L     10,JAN              PUT JAN IN R6
         L     4,MON31             PUT DAYS IN MONTH IN R4
         B     SETMON
TESTFEB  S     8,MON31
         C     8,MON28             IS IT FEBRUARY?
         BH    TESTMAR             NO, BRANCH
         L     15,TWO              YES, PUT 2 IN R15
         L     10,FEB              PUT FEB IN R6
         L     4,MON28             PUT DAYS IN MONTH IN R4
         B     SETMON
TESTMAR  S     8,MON28
         C     8,MON31             IS IT MARCH?
         BH    TESTAPR             NO, BRANCH
         L     15,THREE            YES, PUT 3 IN R15
         L     10,MAR              PUT MAR IN R6
         L     4,MON31             PUT DAYS IN MONTH IN R4
         B     SETMON
TESTAPR  S     8,MON31
         C     8,MON30             IS IT APRIL?
         BH    TESTMAY             NO, BRANCH
         L     15,FOUR             YES, PUT 4 IN R15
         L     10,APR              PUT APR IN R6
         L     4,MON30             PUT DAYS IN MONTH IN R4
         B     SETMON
TESTMAY  S     8,MON30
         C     8,MON31             IS IT MAY?
         BH    TESTJUN             NO, BRANCH
         L     15,FIVE             YES, PUT 5 IN R15
         L     10,MAY              PUT MAY IN R6
         L     4,MON31             PUT DAYS IN MONTH IN R4
         B     SETMON
TESTJUN  S     8,MON31
         C     8,MON30             IS IT JUNE?
         BH    TESTJUL             NO, BRANCH
         L     15,SIX              YES, PUT 6 IN R15
         L     10,JUN              PUT JUN IN R6
         L     4,MON30             PUT DAYS IN MONTH IN R4
         B     SETMON
TESTJUL  S     8,MON30
         C     8,MON31             IS IT JULY?
         BH    TESTAUG             NO, BRANCH
         L     15,SEVEN            YES, PUT 7 IN R15
         L     10,JUL              PUT JUL IN R6
         L     4,MON31             PUT DAYS IN MONTH IN R4
         B     SETMON
TESTAUG  S     8,MON31
         C     8,MON31             IS IT AUGUST?
         BH    TESTSEP             NO, BRANCH
         L     15,EIGHT            YES, PUT 8 IN R15
         L     10,AUG              PUT AUG IN R6
         L     4,MON31             PUT DAYS IN MONTH IN R4
         B     SETMON
TESTSEP  S     8,MON31
         C     8,MON30             IS IT SEPTEMBER?
         BH    TESTOCT             NO, BRANCH
         L     15,NINE             YES, PUT 9 IN R15
         L     10,SEP              PUT SEP IN R6
         L     4,MON30             PUT DAYS IN MONTH IN R4
         B     SETMON
TESTOCT  S     8,MON30
         C     8,MON31             IS IT OCTOBER?
         BH    TESTNOV             NO, BRANCH
         L     15,TEN              YES, PUT 10 IN R15
         L     10,OCT              PUT OCT IN R6
         L     4,MON31             PUT DAYS IN MONTH IN R4
         B     SETMON
TESTNOV  S     8,MON31
         C     8,MON30             IS IT NOVEMBER?
         BH    TESTDEC             NO, BRANCH
         L     15,ELEVEN           YES, PUT 11 IN R15
         L     10,NOV              PUT NOV IN R6
         L     4,MON30             PUT DAYS IN MONTH IN R4
         B     SETMON
TESTDEC  S     8,MON30             IT MUST BE DECEMBER
         L     15,TWELVE           PUT 12 IN R15
         L     10,DEC              PUT DEC IN R6
         L     4,MON31             PUT DAYS IN MONTH IN R4
SETMON   CLC   PARMSAV,MOY         DO YOU WANT THE NUMERIC MONTH?
         BNE   MONNAME             NO, BRANCH
         B     PARM1               YES, SET PARM AND RETURN
MONNAME  CLC   PARMSAV,MONTH       DO YOU WANT THE CHARACTER MONTH?
         BNE   MONWEEK             NO, BRANCH
         B     PARM3               YES, SET PARM AND RETURN
*
* GET WEEK OF MONTH
*
MONWEEK  LR    9,8                 LOAD DAY OF MONTH INTO R9
         S     6,FIVE              HOW FAR IS FRIDAY?
         SR    9,6                 GET DATE OF CLOSEST FRIDAY
TESTLAST CLC   PARMSAV,LASTWK      DO YOU WANT LAST WEEK INDICATED?
         BNE   NOTLAST             IF NOT, PUT NUMERIC WEEK
         SR    4,9                 GET DAYS REMAINING IN MONTH
         C     4,SEVEN             IS IT SEVEN OR MORE?
         BL    PUTLAST             BRANCH IF LESS THAN SEVEN
         L     15,ALL              INDICATE NOT LAST WEEK OF MONTH
         B     PARM1               PUT PARM AND RETURN
PUTLAST  L     15,LAST             INDICATE LAST WEEK OF MONTH
         B     PARM1               PUT PARM AND RETURN
NOTLAST  S     9,ONE               DECREMENT FOR DIVISION
         SR    8,8                 EMPTY R8 FOR DIVIDE
         D     8,SEVEN             DIVIDE BY SEVEN
         A     9,ONE               ADD ONE FOR WEEK OF MONTH
SETWEEK  LR    15,9                LOAD WEEK OF MONTH INTO R15
*
* PASS 1-BYTE NUMERIC PARM DATA
*
PARM1    STH   15,HALFWD
         MVC   NEWPARM+2(1),HALFWD+1  MOVE NUMBER INTO PARM AREA
         MVI   NEWPARM+1,X'01'     MOVE PARM LENGTH INTO PARM AREA
         LA    10,NEWPARM          PUT PARM ADDRESS INTO R10
         ST    10,PARMADDR         PUT PARM ADDRESS INTO ADDRESS AREA
         MVI   PARMADDR,X'80'      INDICATE LAST PARM
         LA    1,PARMADDR          LOAD ADDRESS INTO R1
         B     RETURN              LEAVE
*
* PASS 3-BYTE CHARACTER PARM DATA
*
PARM3    L     15,ZERO             ZERO OUT R15
         ST    10,NEWPARM+2        MOVE CHARACTERS INTO PARM AREA
         MVI   NEWPARM+1,X'03'     MOVE PARM LENGTH INTO PARM AREA
         LA    10,NEWPARM          PUT PARM ADDRESS INTO R10
         ST    10,PARMADDR         PUT PARM ADDRESS INTO ADDRESS AREA
         MVI   PARMADDR,X'80'      INDICATE LAST PARM
         LA    1,PARMADDR          LOAD ADDRESS INTO R1 AND LEAVE
*
* RETURN TO CALLING PROGRAM
*
RETURN   L     13,SAVEAREA+4
         L     0,20(13)
         L     14,12(13)
         LM    2,12,28(13)
         BR    14
FULLWRD  DS    F
JAN      DC    C'JAN '
FEB      DC    C'FEB '
MAR      DC    C'MAR '
APR      DC    C'APR '
MAY      DC    C'MAY '
JUN      DC    C'JUN '
JUL      DC    C'JUL '
AUG      DC    C'AUG '
SEP      DC    C'SEP '
OCT      DC    C'OCT '
NOV      DC    C'NOV '
DEC      DC    C'DEC '
MON28    DC    F'28'
MON30    DC    F'30'
MON31    DC    F'31'
MON      DC    C'MON '
TUE      DC    C'TUE '
WED      DC    C'WED '
THU      DC    C'THU '
FRI      DC    C'FRI '
SAT      DC    C'SAT '
SUN      DC    C'SUN '
BLANK    DC    C' '
ZERO     DC    F'0'
ONE      DC    F'1'
TWO      DC    F'2'
THREE    DC    F'3'
FOUR     DC    F'4'
FIVE     DC    F'5'
SIX      DC    F'6'
SEVEN    DC    F'7'
EIGHT    DC    F'8'
NINE     DC    F'9'
TEN      DC    F'10'
ELEVEN   DC    F'11'
TWELVE   DC    F'12'
TWENTY8  DC    F'28'
DOW      DC    C'DOW '
DAY      DC    C'DAY '
WOM      DC    C'WOM '
MOY      DC    C'MOY '
MONTH    DC    C'MON '
LASTWK   DC    C'LWM '
LAST     DC    C'   L'
ALL      DC    C'   A'
TODAY    DC    D'0'
TOYEAR   DC    D'0'
PARMADDR DC    F'0'
HALFWD   DC    H'0'
NEWPARM  DC    3H'0'
PARMSAV  DC    F'0'
SAVEAREA DC    18F'0'
         END
