AUTOCMD  CSECT
***********************************************************************
*
*  AUTOCMD:
*      WRITTEN BY S. HANSEN
*      GTE LABORATORIES
*      40 SYLVAN ROAD
*      WALTHAM, MASSACHUSETTS
*      MARCH 1, 1978
*
*  PURPOSE:
*      THIS PROGRAM WAS WRITTEN FOR THE PURPOSE OF ISSUING AUTOMATIC
*  JES2 COMMANDS BASED ON DAY OF WEEK OR WEEK OF MONTH.  WE FOUND THAT
*  SUPPORT FOR TIME OF DAY PROCESSING WAS NOT SUFFICIENT FOR OUR NEEDS
*  AT GTE LABS BECAUSE MANY JOBS NEEDED TO BE ISSUED ONLY ON SPECIFIC
*  DAYS.  AS A RESULT, THIS PROGRAM AND 'DAYDATE', THE PROGRAM WHICH
*  COMPUTES THE ACTUAL DAY OF WEEK AND WEEK OF MONTH, WERE WRITTEN TO
*  PROVIDE SUCH SUPPORT.
*
*  INPUT:
*     THE INPUT TO AUTOCMD IS A FILE OF STANDARD JES2 AUTOMATIC
*  COMMANDS WITH ADDITIONAL PARM BEGINNING IN COLUMN 60.
*
*  PARM OPTIONS:
*     TIME OF DAY: AS IN THE STANDARD AUTOMATIC COMMAND, TIME OF DAY
*  FOR ISSUING THE COMMAND MAY BE SPECIFIED BY 'T=HH.MM' IN COLUMN 6
*  WHERE HH.MM ARE THE HOUR AND MINUTES ON THE 24-HR CLOCK.  IF THE
*  TIME OF DAY SPECIFIED HAS ALREADY OCCURRED, PGM=AUTOCMD WILL NOT
*  ISSUE THAT COMMAND.
*
*     DAY OF WEEK: THE DAYS OF THE WEEK FOR ISSUING A COMMAND CAN BE
*  SPECIFIED BY ENTER 'DXXX' IN COL 60 OF THE COMMAND WHERE 'XXX' IS A
*  STRING OF LENGTH 1-6 COMPRISED OF DIGITS 1-7, (EX. D13 - ISSUE
*  COMMAND ONLY ON MONDAYS AND FRIDAYS). IF NOTHING IS GIVEN IN THE
*  COMMENT FIELD, THE COMMAND WILL BE ISSUED DAILY.
*
*     WEEK OF MONTH: THE WEEK OF THE MONTH FOR ISSUING A GIVEN COMMAND
*  CAN BE SPECIFIED BY ENTERING 'WYYY' IN COL 60 OF THE COMMAND WHERE
*  'YYY' IS STRING OF LENGTH 1-4 COMPRISED OF DIGITS 1-5, WHERE 1 IS
*  MONDAY, OR LETTERS 'A' OR 'L', WHERE 'A' SPECIFIES ALL WEEKS EXCEPT
*  THE LAST AND 'L' SPECIFIES ONLY THE LAST WEEK.  (EX1. W13 - ISSUE
*  COMMAND ONLY DURING THE FIRST AND THIRD WEEK OF THE MONTH.
*  EX2. WA - ISSUE COMMAND ON WEEKS 1-4 IN A 5-WEEK MONTH AND ON WEEKS
*  1-3 IN A 4-WEEK MONTH. EX3. WL - ISSUE COMMAND ON WEEK 5 IN A 5-WEEK
*  MONTH AND ON WEEK 4 IN A 4-WEEK MONTH.)
*
*     DAY OF MONTH: PARM FOR BOTH DAY AND WEEK CAN BE SPECIFIED FOR ANY
*  COMMAND BY ENTER 'DXXXWYYY' IN COL. 60.  IN THIS CASE, CONDITIONS
*  MUST BE SATISFIED FOR BOTH DAY OF WEEK AND WEEK OF MONTH IN ORDER
*  FOR THE COMMAND TO BE ISSUED.  (EX1. D1W13 - ISSUE COMMAND ONLY ON
*  MONDAY OF THE FIRST AND THIRD WEEKS OF THE MONTH.  EX2. D5WL - ISSUE
*  COMMAND ONLY ON FRIDAY OF THE LAST WEEK OF THE MONTH.)  SEE NOTE.
*
*     SYMBOLIC SUBSTITUTION: THE PGM AUTOCMD ALSO ALLOWS FOR SOME
*  SYMBOLIC SUBSTITUTION WITHIN THE COMMAND FOR DAY, WEEK, OR MONTH.
*  THE SYMBOL '&DAY.' WILL BE REPLACED WITH THE ONE DIGIT DAY OF WEEK
*  WHERE 1 IS MONDAY, THE SYMBOL '&WEEK.' WILL BE REPLACED WITH THE
*  ONE DIGIT WEEK OF MONTH WHERE 1 IS THE WEEK OF THE FIRST FRIDAY, AND
*  THE SYMBOL '&MONTH.' WILL BE REPLACE WITH THE 2-DIGIT MONTH OF YEAR
*  WHERE 01 IS JANUARY AND 12 IS DECEMBER.
*
*
*  ***  NOTE: WHEN SPECIFYING DAY OF MONTH PROCESSING, CONSIDER
*  CAREFULLY THE ALGORITHM FOR DETERMINING THE CURRENT WEEK OF THE
*  MONTH. (I.E., THE FIRST WEEK IS THE WEEK ENDING ON SUNDAY WHICH
*  CONTAINS THE FIRST FRIDAY OF THE MONTH.)  WHEN THE FIRST OF THE
*  MONTH HAPPENS TO FALL NEAR THE END OF WEEK, BY THIS ALGORITHM,
*  MONDAY - THURSDAY MAY NOT EVEN OCCUR DURING W1. ***
*
***********************************************************************
START    STM   14,12,12(13)        SAVE CALLING REGS
         BALR  11,0
         USING *,11
         LA    2,SAVEAREA          ESTABLISH RETURN LINKAGE
         ST    2,8(13)
         ST    13,SAVEAREA+4
         LR    13,2
         SPACE 2
*
* GET PARM INFORMATION
*
         LA    3,WOM               CALL DAYDATE FOR WEEK OF MONTH
WKPARM   LINK  EP=DAYDATE,PARAM=((3)),DCB=0
         L     1,0(1)              LOAD POINTER INTO R1
         LH    10,0(1)             LOAD LENGTH INTO R10
         S     10,ONE              DECREMENT FOR INSERTION
         STC   10,*+5              INSERT LENGTH
         MVC   WEEK,2(1)           SAVE PARM
         OI    WEEK,X'F0'          MAKE INTO CHARACTER
         LA    3,LWM               CALL DAYDATE FOR LAST WEEK OF MONTH
LSPARM   LINK  EP=DAYDATE,PARAM=((3)),DCB=0
         L     1,0(1)              LOAD POINTER INTO R1
         LH    10,0(1)             LOAD LENGTH INTO R10
         S     10,ONE              DECREMENT FOR INSERTION
         STC   10,*+5              INSERT LENGTH
         MVC   LAST,2(1)           SAVE PARM
         LA    3,DOW               CALL DAYDATE FOR DAY OF WEEK
DYPARM   LINK  EP=DAYDATE,PARAM=((3)),DCB=0
         L     1,0(1)              LOAD POINTER INTO R1
         LH    10,0(1)             LOAD LENGTH INTO R10
         S     10,ONE              DECREMENT FOR INSERTION
         STC   10,*+5              INSERT LENGTH
         MVC   DAY,2(1)            SAVE PARM
         OI    DAY,X'F0'           MAKE INTO CHARACTER
         LA    3,MOY               CALL DAYDATE FOR MONTH OF YEAR
MNPARM   LINK  EP=DAYDATE,PARAM=((3)),DCB=0
         L     1,0(1)              LOAD POINTER INTO R1
         LH    10,0(1)             LOAD LENGTH INTO R10
         S     10,ONE              DECREMENT FOR INSERTION
         STC   10,*+5              INSERT LENGTH
         MVC   FULWORK+3,2(1)      SAVE PARM
         L     4,FULWORK           LOAD PARM INTO R4
         CVD   4,DBLWORK           CONVERT PARM TO DECIMAL
         UNPK  MONTH,DBLWORK       UNPACK PARM INTO MONTH
         OI    MONTH+3,X'F0'       MAKE LAST BYTE INTO CHARACTER
TMPARM   TIME  DEC,ZONE=LT         GET TIME OF DAY
         SRL   0,12                SHIFT RIGHT THREE BYTES
         ST    0,TOD               SAVE TIME OF DAY
         OI    TOD+3,X'0F'         MAKE INTO PACKED DECIMAL
         UNPK  TME,TOD             UNPACK TIME
         SPACE 2
*
* OPEN FILES
*
OPENFILS OPEN  (CMDS)              OPEN FILE OF JES2 COMMANDS
         OPEN  (RDR,(OUTPUT))      OPEN INTRDR
         SPACE 2
*
* THROW OUT COMMENT CARDS
*
CMDLOOP  GET   CMDS                READ COMMAND
         CLC   0(3,1),=C'//*'      IS IT A COMMENT CARD?
         BE    CMDLOOP             YES, GET NEXT COMMAND
*
* CHECK FOR PARM KEYWORDS IN COL. 60
*
PARMKEY  LA    6,71(1)             PLACE END OF CARD ADDRESS IN R6
         LA    7,58(1)             PLACE BEGINNING PARM ADDRESS IN R7
KEYLOOP  A     7,ONE               INCREMENT
         CR    7,6                 HAVE WE HIT COL. 72?
         BH    ALLRUN              YES, THERE IS NO PARM; ACCEPT IT
         CLC   0(1,7),=C'D'        IS THERE DAY PARM IN COL 60?
         BE    DAYRUN              YES, SEARCH FOR A HIT
         CLC   0(1,7),=C'W'        IS THERE WEEK PARM IN COL 60?
         BE    WEEKRUN             YES, SEARCH FOR A HIT
         B     KEYLOOP
*
* CHECK FOR DAY OF WEEK PARM
*
DAYRUN   A     7,ONE               INCREMENT
         CR    7,6                 HAVE WE HIT COL. 72?
         BH    CMDLOOP             DO NOT ACCEPT COMMAND
         CLC   0(1,7),DAY          IS IT TODAY?
         BE    WEEKCHEK            IF SO, CHECK FOR WEEK PARM ALSO
         B     DAYRUN              LOOP
*
* CHECK FOR WEEK OF MONTH AFTER HIT ON DAY
*
WEEKCHEK A     7,ONE               INCREMENT
         CR    7,6                 HAVE WE HIT COL. 72?
         BH    ALLRUN              OK, WE HAD A HIT ON DAY
         CLC   0(1,7),=C'W'        IS THERE WEEK PARM?
         BE    WEEKTOO             YES, CHECK FOR A HIT
         B     WEEKCHEK            LOOP
WEEKTOO  A     7,ONE               INCREMENT
         CR    7,6                 HAVE WE HIT COL. 72?
         BH    CMDLOOP             DO NOT ACCEPT COMMAND
         CLC   0(1,7),WEEK         IS IT THIS WEEK?
         BE    ALLRUN              YES, ACCEPT COMMAND
         CLC   0(1,7),LAST         IS THERE A HIT ON LAST PARM?
         BE    ALLRUN              YES, ACCEPT COMMAND
         B     WEEKTOO
*
* CHECK FOR WEEK OF MONTH PARM
*
WEEKRUN  A     7,ONE               INCREMENT
         CR    7,6                 HAVE WE HIT COL. 72?
         BH    CMDLOOP             DO NOT ACCEPT COMMAND
         CLC   0(1,7),WEEK         IS IT THIS WEEK?
         BE    DAYCHEK             IF SO, CHECK FOR DAY PARM ALSO
         CLC   0(1,7),LAST         IS THERE A HIT ON LAST PARM?
         BE    DAYCHEK             IF SO, CHECK FOR DAY PARM ALSO
         B     DAYRUN              LOOP
*
* CHECK FOR DAY OF WEEK AFTER HIT ON WEEK
*
DAYCHEK  A     7,ONE               INCREMENT
         CR    7,6                 HAVE WE HIT COL. 72?
         BH    ALLRUN              OK, WE HAD A HIT ON DAY
         CLC   0(1,7),=C'D'        IS THERE DAY PARM?
         BE    DAYTOO              YES, CHECK FOR HIT
         B     DAYCHEK             LOOP
DAYTOO   A     7,ONE               INCREMENT
         CR    7,6                 HAVE WE HIT COL. 72?
         BH    CMDLOOP             DO NOT ACCEPT COMMAND
         CLC   0(1,7),DAY          IS IT THIS WEEK?
         BE    ALLRUN              YES, ACCEPT COMMAND
         B     DAYTOO              LOOP
*
* CHECK FOR TIME PARM
*
ALLRUN   MVC   59(13,1),ALLBLANK   BLANK OUT COMMENT AREA
         CLC   6(1,1),=C'T'        IS THERE A TIME SPECIFIED?
         BNE   PROCPARM            NO, ACCEPT THE COMMAND
         CLC   8(2,1),TME          HAS HOUR SPECIFIED PASSED?
         BL    CMDLOOP             YES, GET NEXT COMMAND
         BH    PROCPARM            NO, ACCEPT COMMAND
         CLC   11(2,1),TME+2       HAS MINUTE SPECIFIED PASSED?
         BL    CMDLOOP             YES, GET NEXT COMMAND
*
* CHECK FOR SYMBOLIC SUBSTITUTION
*
PROCPARM LR    6,1                 NO, CONTINUE PROCESSING COMMAND
         L     7,=F'72'            SET COUNTER TO 72
PARMLOOP CLC   0(1,6),=C'&&'       IS THERE A SYMBOLIC STRING HERE?
         BE    DAYPARM             YES, WHAT IS IT?
         A     6,ONE               NO, CHECK NEXT BYTE
         BCT   7,PARMLOOP          DECREMENT COUNTER
         B     PUTRDR              IF NO SUBSTITUTION, ACCEPT COMMAND
*
* CHECK FOR DAY SUBSTITUTION
*
DAYPARM  CLC   0(5,6),=C'&&DAY.'   IS THERE DAY SUBSTITUTION?
         BNE   WEEKPARM            NO, CHECK FOR WEEK
         MVC   0(1,6),DAY          REPLACE SYMBOLIC WITH NUMERIC DAY
         LR    8,7                 PUT REMAINING LENGTH IN R8
         S     8,SIX               DECREMENT LENGTH OF SYMBOLIC STRING
         STC   8,*+5               MOVE IN LENGTH
         MVC   1(,6),5(6)          SLIDE OVER REMAINDER OF COMMAND
         B     PARMLOOP            CHECK FOR MORE SUBSTITUTIONS
*
* CHECK FOR WEEK SUBSTITUTION
*
WEEKPARM CLC   0(6,6),=C'&&WEEK.'  IS THERE WEEK SUBSTITUTION?
         BNE   MONPARM             NO, CHECK FOR MONTH PARM
         MVC   0(1,6),WEEK         REPLACE SYMBOL WITH NUMERIC WEEK
         LR    8,7                 PUT REMAINING LENGTH IN R8
         S     8,SEVEN             DECREMENT LENGTH OF SYMBOLIC STRING
         STC   8,*+5               MOVE IN LENGTH
         MVC   1(,6),6(6)          SLIDE OVER REMAINDER OF COMMAND
         B     PARMLOOP            CHECK FOR MORE SUBSTITUTIONS
*
* CHECK FOR MONTH SUBSTITUTION
*
MONPARM  CLC   0(7,6),=C'&&MONTH.' IS THERE MONTH SUBSTITUTION?
         BNE   CMDLOOP             NO, SYMBOL IS INVALID
         MVC   0(2,6),MONTH+2      REPLACE SYMBOLIC WITH NUMERIC MONTH
         LR    8,7                 PUT REMAINING LENGTH IN R8
         S     8,EIGHT             DECREMENT LENGTH OF SYMBOLIC STRING
         STC   8,*+5               MOVE IN LENGTH
         MVC   2(,6),7(6)          SLIDE OVER REMAINDER OF COMMAND
         B     PARMLOOP            CHECK FOR MORE SUBSTITUTIONS
*
* PUT ACCEPTED COMMAND ON INTRDR
*
PUTRDR   LR    8,1                 PUT ADDRESS OF COMMAND IN R8
         PUT   RDR,(8)             WRITE TO INTRDR
         B     CMDLOOP             GET NEXT COMMAND
*
* CLOSE FILES AND RETURN
*
EOCMDS   CLOSE CMDS
         CLOSE RDR
         L     13,SAVEAREA+4
         LM    14,12,12(13)
         SR    15,15
         BR    14
FULLWRD1 DS    F
SAVEAREA DC    18F'0'
BLANK    DC    C' '
ALLBLANK DC    13C' '
FULWORK  DC    F'0'
DBLWORK  DC    D'0'
ONE      DC    F'1'
TWO      DC    F'2'
SIX      DC    F'6'
SEVEN    DC    F'7'
EIGHT    DC    F'8'
DOW      DC    H'3'
         DC    C'DOW '
WOM      DC    H'3'
         DC    C'WOM '
LWM      DC    H'3'
         DC    C'LWM '
MOY      DC    H'3'
         DC    C'MOY '
TME      DS    CL4
DAY      DC    F'0'
WEEK     DC    F'0'
LAST     DC    F'0'
MONTH    DC    F'0'
TOD      DC    F'0'
CMDS     DCB   DDNAME=COMMANDS,MACRF=(GL),EODAD=EOCMDS,DSORG=PS
RDR      DCB   DDNAME=INTRDR,MACRF=(PM),DSORG=PS,BLKSIZE=80
         END
