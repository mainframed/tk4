*          DATA SET MSSPDATE   AT LEVEL 001 AS OF 02/16/78
SHOWDATE TITLE 'ROUTINE TO PUT DATE/TIME ON MSS/RACF CONSOLE'
*
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
*
SHOWDATE SAVE (14,12),,*
         LR    R12,R15                 SETUP BASE REG
         USING SHOWDATE,R12
         LA    R15,SAVEAREA            INIT SAVE AREA
         ST    R15,8(R13)
         ST    R13,4(R15)
         LR    R13,R15
         TIME  DEC
         ST    R1,DATE                 SAVE DATE FOR LATER
***FORMAT THE PRESENT TIME (AS SET BY THE OPERATOR)
         ST    R0,DBLWORK              CNVT PACKED TO DEC
         MVI   DBLWORK+4,X'0F'
         UNPK  WORKWORD(9),DBLWORK(5)
         MVC   TIMEHRS,WORKWORD        MOVE TIME TO MSG
         MVC   TIMMINS,WORKWORD+2
         MVC   TIMSECS,WORKWORD+4
         MVC   TIMHUNS,WORKWORD+6
***FORMAT TODAY'S DATE INTO THE OUTPUT MSG
         MVC   WORKWORD(4),DATE        MOVE DATE TO WORK AREA.
         MVO   WORKWORD+1(3),WORKWORD(2)   MAKE 00YYDDDS INTO 00000YYS.
         UNPK  CYR(2),WORKWORD+2(2)    FORMAT YEAR.
         XC    DBLWORK,DBLWORK
         MVC   DBLWORK+4(4),WORKWORD   CNVT YEAR TO BIN
         CVB   R0,DBLWORK
         ST    R0,BINYEAR              AND SAVE
         TM    BINYEAR+3,X'03'         LEAP YEAR?
         BNZ   MON1                    NO.
         MVI   MONMSK+10,29            YES, CORRECT
MON1     XC    DBLWORK,DBLWORK         CNVT DAYS TO BIN
         MVC   DBLWORK+6(2),DATE+2
         CVB   R7,DBLWORK
         ST    R7,BINDAYS              SAVE FOR LATER
         XR    R1,R1
         LA    R15,11
MON2     IC    R1,MONMSK(R15)          COMPUTE MON & DAY
         SR    R7,R1
         BNP   MONOVR
         BCT   R15,MON2
         B     *+6
MONOVR   AR    R7,R1                   CORRECT OVERDRAW
         CVD   R7,DBLWORK              FORMAT DAY
         UNPK  CDAYN(2),DBLWORK+6(2)
         OI    CDAYN+1,X'F0'           FIX UP SIGN
         MH    R15,=H'3'               GET TABLE OFFSET
         LA    R15,MONTAB(R15)         AND POINT AT MONTH
         MVC   CMON(3),0(R15)          MOVE IT TO THE MSG
***DAY OF WEEK
         L     R15,BINYEAR
         SH    R15,=H'69'
         BNP   OUTT                    MUST BE GREATER OR BAD
         XR    R14,R14                 CLEAR DAY REG
         D     R14,=F'4'
         MH    R15,=H'5'
         AR    R14,R15
         A     R14,BINDAYS
         SRDA  R14,32
         D     R14,=F'7'               MODULO 7 FOR WEEK
         MH    R14,=H'3'
         LA    R14,DAYTAB(R14)         POINT AT TODAY
         MVC   CDAY(3),0(R14)          INSERT IN MSG
***
OUTT     MVC   M1DATE(LEN),TIMEHRS     COPY TIME & DATE TO USER'S AREA
         MVC   M1JDATE,=X'4020204B202020'
         ED    M1JDATE,DATE+1
*
DOWTO    WTO   MF=(E,MSG1)
         L     R13,4(R13)              RESTORE REGS & RETURN
         RETURN (14,12),T,RC=0
 EJECT
DBLWORK  DC    D'0'
SAVEAREA DC    18F'0'
BINYEAR  DC    F'0'
BINDAYS  DC    F'0'
DATE     DC    F'0'
WORKWORD DC    2F'0'
         LTORG
MONMSK   DC    AL1(31,30,31,30,31,31,30,31,30,31,28,31)
TIMEHRS  DC    CL2' ',C'.'
TIMMINS  DC    CL2' ',C'.'
TIMSECS  DC    CL2' ',C'.'
TIMHUNS  DC    CL2' ',C' '
CDAY     DC    CL3' ',C' '
CMON     DC    CL3' ',C' '
CDAYN    DC    CL2' ',C',19'
CYR      DC    CL2'  ',C' '
LEN      EQU   *-TIMEHRS
DAYTAB   DC    C'TUEWEDTHRFRISATSUNMON'
MONTAB   DC    C'DECNOVOCTSEPAUGJULJUNMAYAPRMARFEBJAN'
MSG1     WTO   '****** YY.DDD  HH.MM.SS.TH DAY MON DD,19XX ************X
               ********************',ROUTCDE=(11,13),MF=L
         ORG   MSG1+10
M1JDATE  DS    CL7
         ORG   MSG1+19
M1DATE   DS    CL28
         ORG
         END
