*          DATA SET UCBZAP     AT LEVEL 002 AS OF 10/31/78
*          DATA SET UCBZAP     AT LEVEL 001 AS OF 07/14/78
UZAP     TITLE 'UCBZAP --- SET OFF NEGATIVE USE COUNT FOR MSS VOLS'
***********************************************************************
*                                                                     *
* THIS IS THE UCBZAP PROGRAM:                                         *
*                                                                     *
* PURPOSE:  TO FIND ALL 3330V UCBS THAT ARE ALLOC AND HAVE A USE      *
*           COUNT OF 240->255, AND ARE UNALLOCATED (NO ENTRY IN       *
*           SYSZVOLS FOR VOLUME).  IF THESE CONDITIONS ARE MET,       *
*           THEN RESET ALLOC BIT, SET USECNT TO ZERO AND ISSUE AN     *
*           UNLOAD COMMAND FOR THE VOLUME.                            *
*                                                                     *
*                                                                     *
* OPERATION-- ACTUAL WORK IS DONE ONLY AS A STARTED TASK..REQUIRES    *
*           AUTHORIZATION FOR MODESET MACRO.                          *
*                                                                     *
*           IF RUN ONLINE UNDER TSO...IT MERELY SHOWS WHAT IT WOULD   *
*           DO IF GIVEN THE CHANCE AS A STARTED TASK.                 *
*                                                                     *
*                                                                     *
* VERSION 1 MOD 0  - BOB JUCK/KEN TRUE   FAIRCHILD CAMERA & INSTRU.   *
*                    JULY 14 1978.                                    *
*                                                                     *
* VERSION 1 MOD 1  - K TRUE - 31OCT78 - CLEAN USE CNT FOR ONLINE,NRDY *
*                    3330V VOLUMES TOO.                               *
*          MACROS  REGEQU,MODESET,CVT,IEFUCBOB,WTO,SAVE,RETURN        *
*                                                                     *
*                                                                     *
***********************************************************************
         MACRO
&LABEL   WRTMSG &ADDR
&LABEL   LA     R1,&ADDR
         BAL    LINKREG,PUTMSG
         MEND
         SPACE 2
         REGEQU
         EJECT
*.....................................................................*
*    MAINLINE - INIT ADDRESSABILITY AND ESTAB IF ONLINE               *
*.....................................................................*
         SPACE
UCBZAP   CSECT
         SAVE  (14,12),,UCBZAP.&SYSDATE..&SYSTIME..V1M0
         LR    R12,R15
         USING UCBZAP,R12
         LA    R2,SAVEAREA
         ST    R13,4(R2)
         ST    R2,8(R13)
         LR    R13,R2
         EXTRACT MF=(E,EXTRACT)
         L     R1,ANSWER            (R1)=ADDR OF TSO FLAG FROM EXTRACT
         MVC   TSOFLAG,0(R1)        MOVE IN THE ANSWER TO OUR FLAG
         SPACE 2
START    L     R7,CVTPTR           -> CVT
         L     R7,CVTILK2-CVT(R7)  -> UCB TABLE
         SLR   R8,R8
         EJECT
*.....................................................................*
*    LOOP OVER UCBS                                                   *
*.....................................................................*
         SPACE
LOOPTOP  ICM   R8,B'0011',0(R7)    -> GET UCB
         CLC   0(2,R7),=X'FFFF'    END OF THE WORLD
         BE    DONE                YES....EXIT STAGE LEFT
         LTR   R8,R8               ANYONE HOME
         BZ    INDEX                DUMMY ENTRY   TRY AGAIN
         LA    10,2(10)            -> NEXT UCB ENTRY
         LTR   R8,R8
         BZ    INDEX               IF DUMMY GO TRY AGAIN
         USING UCB,R8
         TM    UCBSTAT,UCBONLI     ONLINE ?
         BZ    INDEX
         TM    UCBTBYT3,UCB3DACC   DIRECT ACCESS?
         BNO   INDEX
         TM    UCBTBYT2,UCBRVDEV   VIRT DEVICE ?
         BNO   INDEX
         TM    UCBUSER,X'F0'       NEGATIVE USE COUNT ?           V1M1
         BNO   INDEX                                              V1M1
         TM    UCBSTAT,UCBALOC     ALLOCATED ?
         BO    GOTALLC             YES....PROCESS                 V1M1
         TM    UCBFLA,UCBNRY       ONLINE/NOTREADY?               V1M1
         BNO   INDEX               NO..GO GET ANOTHER             V1M1
         TM    UCBDMCT,UCBMOUNT    IS MOUNT PENDING?              V1M1
         BO    INDEX               YES..KISS OFF                  V1M1
         B     CLNNRDY             GOT ONE..GO CLEAN NOTRDY UNIT  V1M1
         SPACE
GOTALLC  MVC   MSGUCB,UCBNAME      MOVE IN UNIT ADDR TO MSG       V1M1
         MVC   CMDUCB,UCBNAME      MOVE IN UNIT TO COMMAND
         MVC   MSGVOL,UCBVOLI      MOVE IN VOLSER TO MSG
         BAL   LINKREG,CKUNIQUE    GO CHECK TO SEE IF WEVE SEEN IT
         B     CLNAGAIN            YES--CLEAN ALT PATH TOO..
         SPACE
*.....................................................................*
*   FOUND A CANDIDATE..NOW CHECK FOR ALLOCATION IN SYSZVOLS           *
*.....................................................................*
         SPACE
GOTONE   XC    WHOGOTIT,WHOGOTIT   CLEAN OUT STRUCTURE
         XC    RETCD,RETCD         AND RETURN CODE
         MVC   VOLUME,UCBVOLI      MOVE IN VOLUME TO PARM LIST
         MVC   #ENTRIES,=H'2'      SAY 2 ENTRIES
         L     R15,=V(SRCHVOL)     LOAD ADDR OF SUBROUTINE
         LA    R1,PARMLIST         LOAD PARMLIST ADDRESS
         BALR  R14,R15             SEE IF VOLUME IS ENQUEUED ON
         SPACE
CHKRTN   LH    R15,RETCD           LOAD RETURN FROM SRCHVOL
         LTR   R15,R15             OK RETURN..SOMBODY GOTIT
         BZ    ITSALLOC              ITS ALLOCATED..POST MSG
         CH    R15,=H'4'           CHK RETURN CODE
         BE    DOZAP                 NOT ALLOC...ZAP IT
         CH    R15,=H'12'          ALLOC BUT OUT OF ROOM?
         BE    ITSALLOC              OOPS...ERROR QUIT AND DUMP
OOPS     DC    H'0'                GUARANTEED S0C1 TO FINISH...
         EJECT
*.....................................................................*
*   HERE WE DO THE DIRTY DEED..UNLOAD THE TURKEY                      *
*.....................................................................*
         SPACE
DOZAP    WRTMSG WTOMSG             SAY WERE DOING IT
         WRTMSG WTOCMD             SAY WE ISSUED THE COMMAND
         TM    TSOFLAG,X'80'       IS THIS ONLINE???
         BO    INDEX               YES...SKIP THE MODESET AND WORK
GOSUPER  MODESET MF=(E,SUPER)      GO SUPER
         SPACE
         MVI   UCBUSER,X'00'       SET USE TO ZERO
         XI    UCBSTAT,UCBALOC     SET OFF ALLOC BIT
         LA    R1,WTOCMD           ISSUE THE COMMAND
         SLR   R0,R0                 TO UNLOAD TO
         SVC   34                       OS
         SPACE
FINSUPER MODESET MF=(E,PBLM)       BACK TO PROBLEM STATE...
         B     INDEX               LOOP TO NEXT UCB
         SPACE
ITSALLOC MVC   MSGACTN,MSGBYPAS    MOVE IN BYPASS MESSAGE
         MVC   MSGUSER,JOBNAME     MOVE IN JOBNAME
         WRTMSG WTOMSG             WRITE THE MESSAGE
         MVC   MSGACTN,MSGDIDIT    CORRECT THE MESSAGE
         B     INDEX
         EJECT
CLNAGAIN TM    UCBSTAT,UCBALOC     IS IT ALLOC?
         BNO   INDEX                 NO KISSOFF.
         TM    UCBUSER,X'F0'        WITH A BIG COUNT...?
         BNO   INDEX                 NO...KISSOFF
         TM    TSOFLAG,X'80'       IS THIS ONLINE???
         BO    CLNAFTR             YES...SKIP THE MODESET AND WORK
         SPACE
GOSUP2   MODESET MF=(E,SUPER)      -----GO SUPERVISOR-----
         MVI   UCBUSER,X'00'       SET USE COUNT TO ZERO
         XI    UCBSTAT,UCBALOC     SET OFF ALLOC BIT
FINSUP2  MODESET MF=(E,PBLM)       -----BACK TO PROBLEM STATE----
         SPACE
CLNAFTR  MVC   MSGACTN,MSGPATH     SAY WE CLEANED THE ALT PATH TOO
         WRTMSG WTOMSG
         MVC   MSGACTN,MSGDIDIT
         SPACE 2
INDEX    LA    R7,2(R7)            BUMP UCB PTR
*
         B     LOOPTOP             AND GO TRY AGAIN
         SPACE 2
DONE     L     13,SAVEAREA+4       FINISH---RETURN TO OS......
         RETURN (14,12),RC=0
         EJECT
*.....................................................................*
*   HERE WE CLEAN OFF ONLINE,NOTREADY UCBS WITH NO MOUNTS PENDING     *
*.....................................................................*
         SPACE
CLNNRDY  MVC   WTOUNIN,UCBNAME
         WRTMSG WTOMSGN            SAY WERE DOING IT
         TM    TSOFLAG,X'80'       IS THIS ONLINE???
         BO    INDEX               YES...SKIP THE MODESET AND WORK
GOSUPR   MODESET MF=(E,SUPER)      GO SUPER
         SPACE
         MVI   UCBUSER,X'00'       SET USE TO ZERO
         SPACE
FINSUPR  MODESET MF=(E,PBLM)       BACK TO PROBLEM STATE...
         B     INDEX               LOOP TO NEXT UCB
         EJECT
         SPACE 3
*.....................................................................*
*    LOCAL SUBROUTINE CKUNIQUE   - TO CHECK IF WEVE SEEN THIS UCB     *
*                                  BEFORE                             *
*                                                                     *
*    ENTER WITH R8 = ADDR UCB                                         *
*    EXIT: TO (LINKREG)  IF ALREADY ON LIST                           *
*          TO 4(LINKREG) IF NOT PREVIOUSLY FOUND                      *
*.....................................................................*
LINKREG  EQU   R6
*
CKUNIQUE SR    R10,R10
COMPLIST CH    R10,LCT                  TOP OF LIST AND STILL NOT SEEN?
         BNL   ADDLIST                  YES-> SO GO ADD IT TO LIST
         CH    R8,LIST(R10)             CHK UCB
         BE    0(LINKREG)                SEEN BEFORE--EXIT
         LA    R10,2(R10)               BUMP LIST PTR
         B     COMPLIST                 AND GO CHECK AGAIN
ADDLIST  LH    R10,LCT                  LOAD CURRENT TOP OF LIST
         CH    R10,LMAX                 COMPARE TO MAX # ENTRIES IN LST
         BNL   4(LINKREG)               NO ROOM-> SAY WE HAVENT SEEN IT
         STH   R8,LIST(R10)              GOT ROOM->SAVE UCB ADDR
         LA    R10,2(R10)                BUMP TOP OF LIST
         STH   R10,LCT                   AND SAVE IT
         B     4(LINKREG)                GOOD RETURN
*
         EJECT
*.....................................................................*
*        LOCAL SUBROUTINE FOR IO TO OPER/USER                         *
*.....................................................................*
         SPACE
*
* ENTER WITH WTO,WTOR LIST FORM POINTED TO BY R1
*
*    BAL  LINKREG,PUTMSG    OR
*    BAL  LINKREG,PUTGET
*
*  USES RXX FOR WORK
*
RXX      EQU   14                  WORK REGISTER
*
PUTMSG   DS    0H
         TM    TSOFLAG,X'80'       IS IT TSO?
         BO    TPUTIT              YES-> GO DO TPUT
         SVC   35                  NO-> USE WTO
         BR    LINKREG
TPUTIT   DS    0H
         LH    R0,0(,R1)           GET LENGTH OF WTO MESSAGE
         S     R0,=F'4'            SUBTRACT OFF HEADER
         LA    R1,4(,R1)           BUMP MSG ADDRESS PAST HDR
         TPUT  (1),(0),R           DO THE TPUT
         BR    LINKREG
         SPACE 2
PUTGET   DS    0H
         TM    TSOFLAG,X'80'       IS IT TSO?
         BO    TSOIT               YES-> GO USE TGET
         LR    RXX,R1              NO-> USE WTOR .. SAVE MSG ADDRESS
         SVC   35                  DO THE WTOR...
         L     R1,4(,RXX)          GET ADDR OF ECB
         XC    0(4,R1),0(R1)       CLEAN OUT THE ECB....
         LA    R0,1
         SVC   1                   WAIT FOR WTOR TO COMPLETE
         B     PUTRET              RETURN
TSOIT    DS    0H
         LR    RXX,R1              SAVE
         LH    R0,8(,R1)           PUT LENGTH
         S     R0,=F'4'
         LA    R1,12(,R1)          PAST HDR
         TPUT  (1),(0),R
         SR    R0,R0
         IC    R0,0(,RXX)          GET REPLY LENGTH
         L     R1,0(,RXX)          GET REPLY ADRS
         LA    R1,0(,R1)
         ICM   R1,B'1000',=X'80'   INDICATE TGET...
         TGET  (1),(0),R
PUTRET   DS    0H
         L     R1,0(,RXX)          GET REPLY ADRS
         XR    R0,R0
         IC    R0,0(,RXX)          GET REPLY LENGTH
UPPER    DS    0H
         OI    0(R1),C' '          UPPER CASE
         LA    R1,1(,R1)           NXT
         BCT   R0,UPPER
         BR    LINKREG
         EJECT
*.....................................................................*
*      CONSTANTS                                                      *
*.....................................................................*
         SPACE
PARMLIST CALL  ,(VOLUME,WHOGOTIT,#ENTRIES,RETCD),VL,MF=L
         SPACE
EXTRACT  EXTRACT ANSWER,'S',FIELDS=(TSO),MF=L
         SPACE
SUPER    MODESET KEY=ZERO,MF=L
         SPACE
PBLM     MODESET KEY=NZERO,MF=L
         SPACE
MSGBYPAS DC    CL30'BYPASSED..ALLOC BY XXXXXXXX '
MSGPATH  DC    CL30'ALT PATH UNALLOCATED'
MSGDIDIT DC    CL30'UNALLOCATED..UNLOADED'
         SPACE
*.....................................................................*
*      VARIABLES                                                      *
*.....................................................................*
         SPACE
SAVEAREA DC    18F'0'
ANSWER   DC    F'0'                ADDRESS POINTER FOR EXTRACT
VOLUME   DC    CL6' '
TSOFLAG  DC    X'00'               ='80' IF TSO. '00' IF NOT TSO
WHOGOTIT DS    0CL18       2 ENTRIES
JOBNAME  DC    CL8' '
FLAG     DC    BL1'0'
FOUND    EQU   B'10000000'
JOB      EQU   B'01000000'
OLD      EQU   B'00100000'
         DC    CL9' '
         DS    0H
#ENTRIES DC    H'2'
RETCD    DC    H'0'
         EJECT
*.....................................................................*
*      MESSAGES                                                       *
*.....................................................................*
         SPACE
WTOMSG   WTO   'UCBZAP - UNIT=XXX, VOL=XXXXXX UNALLOCATED..UNLOADED    X
                  ',ROUTCDE=(1,13),MF=L
         SPACE
         ORG   WTOMSG+18
MSGUCB   DS    CL3
         ORG   WTOMSG+27
MSGVOL   DS    CL6
         ORG   WTOMSG+34
MSGACTN  DS    CL29
         ORG   WTOMSG+53
MSGUSER  DS    CL8
         ORG
         SPACE
WTOMSGN  WTO   'UCBZAP - UNIT=XXX,  SET USE COUNT TO ZERO',            X
               ROUTCDE=(1,13),MF=L
         ORG   WTOMSGN+18
WTOUNIN  DS    CL3
         ORG
         SPACE
WTOCMD   WTO   'U XXX  ',MF=L,ROUTCDE=(1,13)
         ORG   WTOCMD+6
CMDUCB   DS    CL3
         SPACE
         ORG
         LTORG
         SPACE 2
*
* THIS DUDE MUST BE AT THE END TO PRESERVE THE FAITH OF
*   ADDRESSABILITY..........
*
LCT      DC    H'0'                     TO REMEMBER COUNT
LMAX     DC    H'1000'                  TO REMEMBER UCB MAX ADDR
LIST     DS    500H                     TO REMEMBER EACH UCB ADDR
         EJECT
CVT      DSECT
         CVT   SYS=VMS,TSO=YES
         EJECT
UCB      DSECT
         IEFUCBOB
         END
