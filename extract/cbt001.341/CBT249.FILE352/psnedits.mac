*          DATA SET PSNEDITS   AT LEVEL 001 AS OF 10/30/80
         TITLE 'PSNEDITS - EDIT THE ENTIRE STEP FOR LOCAL ENFORCEMENT'
PSNEDITS PSNMSTRT  SASIZ=WKLENG
         EJECT
*                    APPLY STANDARDS FOR THE ENTIRE STEP
*                    SUCH AS THE ABSENCE OF ANYTHING
*
         L     R9,PSWEXEND        ADDRESS THE CURRENT EXEC BLOCK
         USING EXECBLOK,R9        PASSED FROM EXECUTE BUILDER
         USING DDBLOCK,R8         CHAINED OFF THE EXEC OR ANOTHER DD
*
         BAL   R14,UDUMP          BE SURE THE DUMP CARD IS HERE
*
         B     RETURN
               EJECT
*                    TO VERIFY THE SYSUDUMP CARD IS IN PLACE
*
UDUMP    PSNMBLOK  UDP,RENT=YES
         L     R8,EXCDDCHN        PICK UP THE DDBLOCK POINTER
UDPLOOP  EQU   *
         LTR   R8,R8              IS THERE A POINTER THERE
         BZ    UDPERROR           NO - MUST NOT BE ONE
         CLC   DDBDDNAM,UDPDUMP   IS THIS THE PROPER ONE
         BE    UDPEXIT            YES - CARRY ON
         L     R8,DDBNEXT         SO LOOK AT THE NEXT ONE
         B     UDPLOOP            AND BACK FOR MORE
*
UDPERROR EQU   *                  NO SYSUDUMP STATEMENT INCLUDED
         SPACE 1                                                    NTL
         LA    R7,EXCEPGM         SEE IF PROGRAM IS EXEMPT          NTL
         USING EXCENTRY,R7         FROM THIS RULE                   NTL
         L     R6,EXCPSUBS        GET SUBSTITUTED NAME OF PGM       NTL
         SR    R2,R2                                                NTL
         LM    R15,R1,UDPLIST     SET UP FOR BXLE CONTROL           NTL
UDPLOOP2 IC    R2,0(R15)          GET LENGTH                        NTL
         EX    R2,UDPCOMPR        COMPARE TO EXCEPTION LIST         NTL
         BE    UDPEXIT            GOTCHA..NO SYSUDUMP NEEDED        NTL
         BXLE  R15,R0,UDPLOOP2    BUMP AND GRIND                    NTL
         SPACE 1                                                    NTL
UDPERR1  OI    EXCIND1,EXCI1SUD   SHOW REFORMATTER THE OMISSION     NTL
         PSNMERR  ERRREQDF,UDPDUMP,EXCSTEP
         B     UDPEXIT
         SPACE 1                                                    NTL
UDPCOMPR CLC   0(0,R6),1(R15)     **EXECUTED**                      NTL
         SPACE 1                                                    NTL
UDPLIST  DC    A(*+12,9,UDPLSTE)  FOR BXLE CONTROL                  NTL
UDPLIST1 DC    AL1(6),CL8'IEFBR14'  EXEMPT FROM SYSUDUMP RULE       NTL
         DC    AL1(2),CL8'WTO'                                      NTL
UDPLSTE  DC    AL1(6),CL8'DELETDS'                                  NTL
         SPACE 1                                                    NTL
UDPDUMP  DC    CL8'SYSUDUMP'
         EJECT
         PSNMEXEC
         EJECT
         PSNMDD
*
*
WORKAREA DSECT
         DS    18F
WKDOUBL  DS    D                  USED TO CONVERT COLUMN NUMBER
         PSNMBLOK  RENT=REGS
WKLENG   EQU   *-WORKAREA
         EJECT
         PSNMWORK
         END
