*          DATA SET PSNEDITX   AT LEVEL 002 AS OF 10/30/80
         TITLE 'PSNEDITX - EDIT THE EXECUTE CARD FOR STANDARDS'
PSNEDITX PSNMSTRT  SASIZ=WKLENG
         EJECT
*                    APPLY LOCAL STANDARDS AND PREJUDICES
*                    CONCERNING WHAT IS REALLY IN THIS MONTH
*                    FOR THE WELL-DEVELOPED EXECUTE CARD.
*
         L     R9,PSWEXEND        ADDRESS THE CURRENT EXEC BLOCK
         USING EXECBLOK,R9        PASSED FROM EXECUTE BUILDER
*
         BAL   R14,STEPNAME       VALIDATE STEPNAME FOR PROPER FORM
*
         BAL   R14,COMMENTB       BE SURE THERE IS A COMMENT BLOCK
*
         BAL   R14,PGMNAME        CHECK NAME FOR STANDARDS
*
         BAL   R14,THETIME        CHECK TIME PARAMETER FOR BAD STUFF
*
         BAL   R14,COLUMNS        LOOK AT THE COLUMN ALIGNMENTS
*
         B     RETURN
               EJECT
*                    TO EDIT THE STEP NAME FOR OUR STANDARDS
*
STEPNAME PSNMBLOK  STN,RENT=YES
         CLC   =C'STEP',EXCSTEP   STARTING WITH PROPER CHARACTERS
         BNE   STNERROR           NO - PROBABLY WONT RUN
         L     R15,PSWTRNUM       NEXT THREE MUST BE NUMERIC
         TRT   EXCSTEP+4(3),0(R15)
         BNZ   STNERROR           SO IT IS NONSTANDARD
*
         B     STNEXIT
*
STNERROR EQU   *                  STEPNAME IS NONSTANDARD
         PSNMERR  ERRNONST,EXCSTEP,STEPNM
         B     STNEXIT
STEPNM   DC    C'STEP NAME'
         EJECT
*                    TO CHECK THE PROGRAM NAME FOR PROPER
*                    STRUCTURE
*
PGMNAME  PSNMBLOK  PGN
         LA    R7,EXCEPGM         ADDRESS THE PGM POINTER BLOCK
         USING EXCENTRY,R7
         L     R6,EXCPSUBS        POINT TO SUBSTITUTED ENTRY
         SR    R1,R1              CLEAR THE LENGTH REGISTER
         IC    R1,EXCPSUBS        AND PICK UP LENGTH OF THE PGM NAME
         LA    R14,L'WKPGM        WILL IT FIT IN HOLD AREA
         CR    R1,R14             MATCH UP THE LENGTHS
         BH    PGNBADNM           TOO LONG - THEREFORE NONSTANDARD
         BCTR  R1,0               SET FOR PROPER LENGTH....
         MVC   WKPGM,PSWBLANK     CLEAR THE HOLDING FOR PGM
         EX    R1,PGNMOVE         AND MOVE IN THE NAME
         CLI   WKPGM,C'I'          IS IT LIKE AN IBM UTILITY
         BE    PGNCKSUB
         CLC   WKPGM(3),PSWPNAME  PGM MATCH PROC                    NTL
         BE    PGNCKSUB           YES - THIS IS REALLY FINE
         LM    R15,R1,PGNAMES      TABLE OF EXEMPT NAMES
PGNLOOP  EQU   *
         CLC   WKPGM,0(R15)     IS THIS ONE SAFE
         BE    PGNCKSUB           YES - ON THE LIST
         BXLE  R15,R0,PGNLOOP
*                   NOT ON THE SAFE LIST
PGNBADNM EQU   *
         OI    EXCIND1,EXCI1PGM   SHOW THE VIOLATION HERE....
         PSNMERR  ERRNONST,PGM,WKPGM
*
*                  ALSO CHECK IF PGM IS SYMBOLICLY SUBSTITUTED
PGNCKSUB EQU   *
         CLI   EXCPNUMS+1,X'00'   WERE ANY SUBSTITUTIONS MADE
         BE    PGNEXIT            NO - LOOKING GOOD
         PSNMERR  ERRHSYM,WKPGM,PGM
         B     PGNEXIT
PGNMOVE  MVC   WKPGM(0),0(R6)     MOVE IN PGM NAME
PGM      DC    C'PGM'
         DROP  R7
*
*                    LIST OF APPROVED NON-STANDARD PROGRAMS
PGNAMES  DC    A(*+12,8,PGNEND)
         DC    CL8'SORT'
         DC    CL8'WTO'
         DC    CL8'ABENDPGM'
         DC    CL8'DELETDS'
         DC    CL8'PROCSCAN'      THIS PROGRAM IS ALWAYS GOOD
         DC    CL8'DFSRRC00'      IMS IS ALLOWED AN EXCEPTION
         DC    CL8'BKUPUTIL'      UTILITY FOR BACKUPS
         DC    CL8'WAAPSPLT'      STAR UTILITY                      NTL
         DC    CL8'WAAPDSUT'      STAR UTILITY                      NTL
         DC    CL8'WAAPISUT'      STAR UTILITY                      NTL
         DC    CL8'DSFORMT'       STAR UTILITY                      NTL
         DC    CL8' '             FILLER
         DC    CL8' '             FILLER
         DC    CL8' '             FILLER
PGNEND   EQU   *-8                LAST PROPER ENTRY
         EJECT
*                   CHECK THE TIME FOR THOSE PEOPLE THAT USE
*                    1440, OR SO.....
*
THETIME  PSNMBLOK  TTIM
         LA    R7,EXCETIME        ADDRESS THE TIME PARAMETER IF THERE
         USING EXCENTRY,R7
         L     R6,EXCPSUBS        POINT TO SUBSTITUTED ENTRY
         LTR   R6,R6              IS THERE ONE HERE AT ALL
         BZ    TTIMEXIT           NONE - THAT IS APPROVED
         CLC   T1440,0(R6)        IS THIS A BAD TIME
         BNE   TTIMEXIT           GOOD ENOUGH
*
         OI    EXCIND1,EXCI1TIM   SHOW TIME=1440 SPECIFIED
         PSNMERR  ERRNONST,TIME,T1440
         B     TTIMEXIT
TIME     DC    C'TIME'
T1440    DC    C'1440'            THE TIME TO AVOID
         DROP  R7
         EJECT
*                   TO BE SURE A COMMENT BLOCK PRECEEDS THIS EXEC
*                   PROC OBVIOUSLY WILL NOT WORK WITHOUT IT.
*
COMMENTB PSNMBLOK  COM
         TM    PSWMFLG1,PSWM1NCM  COMMENT CHECK WANTED?             NTL
         BO    COMEXIT            NO..SO SPLIT                      NTL
         L     R8,EXCCOMNT        ADDRESS THE COMMENT BLOCK
         LTR   R8,R8              IS THERE ONE
         BZ    COMNONE            OH MY GRACIOUS
*                    IS IT IN PROPER FORM
         USING COMMENT,R8
         TM    CMTIND1,CMTI1AST   IS FIRST LINE ASTERISKS
         BZ    COMNOSTD           NO BOX - BADDDDD
         L     R8,CMTNEXT         LOOK AT THE FIRST LINE OF COMMENT
         LTR   R8,R8              IS IT REALLY THERE
         BZ    COMNOSTD           NON-STANDARD COMMENT
         LA    R1,11              COLUMN DISPLACEMENT FOR START
         CH    R1,CMTCOL          IS IT IN THIS COLUMN
         BNE   COMNOSTD           NO - REALLY BAD
         CLC   CMTTEXT(7),EXCSTEP MUST START WITH STEPNAME
         BNE   COMNOSTD           NOBODY IS PERFECT
COMCKALL EQU   *
         TM    CMTIND1,CMTI1S71   HOW ABOUT THE LAST CHARACTER
         BZ    COMNOSTD           NON-STANDARD COMMENT
         LR    R1,R8              SAVE THE LAST COMMENT POINTER
         L     R8,CMTNEXT         MOVE ON TO NEXT LINE
         LTR   R8,R8              ARE THERE ANY MORE
         BNZ   COMCKALL           NO MORE - END OF THE LIST
         LR    R8,R1              REPOINT TO THE LAST COMMENT LINE
         TM    CMTIND1,CMTI1AST   LAST MUST BE ALL *** AS WELL
         BZ    COMNOSTD           HOW SEVERE CAN YOU GET
         B     COMEXIT
*
COMNONE  EQU   *
         PSNMERR  ERRNOCMT,EXCSTEP    NO COMMENT ON THE STEP
         B     COMEXIT
*
COMNOSTD EQU   *
         PSNMERR  ERRCOMNS,EXCSTEP   COMMENT IS NON-STANDARD FORM
         B     COMEXIT
         EJECT
*                    CHECK EACH OF THE COLUMNS TO BE SURE THEY ARE
*                    ALIGNED ON THE RIGHT BOUNDARY
*
COLUMNS  PSNMBLOK  COL
         B     COLEXIT            SKIP THIS SHIT....                NTL
         TM    PSWMFLG1,PSWM1NCL  NO COLUMN CHK WANTED              NTL
         BO    COLEXIT            NO THANKS..                       NTL
         LA    R7,EXCEPGM         POINT TO THE FIRST ENTRY
         USING EXCENTRY,R7
         LA    R5,EXCFLENG-EXCELENG(,R9) POINT TO LAST ENTRY
         LA    R4,EXCELENG        THE LENGTH OF ONE ENTRY
*
COLLOOP  EQU   *
         CLI   EXCPCOL+1,X'00'    IS THIS ONE TOTALLY OMITTED
         BE    COLNEXT            YES - CARRY ON
         CLI   EXCPCOL+1,X'0F'    IS THIS ONE ALIGNED PROPERLY
         BE    COLNEXT            YES - CARRY ON
         LA    R1,EXCEPGM         CHECK FOR BEING THE FIRST (PGM) ENTRY
         CR    R1,R7
         BNE   COLERROR           NOT THE PGM - BAD PROBLEM HERE
         CLI   EXCPCOL+1,X'10'    THE COLUMN ALLOWED FOR PGM=
         BE    COLNEXT            THEN THIS IS PROPER
*                   HORRORS - WRONG COLUMN
COLERROR EQU   *
         LH    R1,EXCPCOL         PICK UP THAT COLUMN NUMBER
         LA    R1,1(,R1)          MAKE IT A TRUE COLUMN NUMBER
         CVD   R1,WKDOUBL
         MVC   WKMASK,COLMASK     PREPARE TO MAKE IT READABLE
         ED    WKMASK,WKDOUBL+6   MAKE IT READABLE
         PSNMERR  ERRMISAL,EXCPKEYW,WKMASK
*
COLNEXT  EQU   *
         BXLE  R7,R4,COLLOOP      TO LOOK AT THEM ALL
         B     COLEXIT
COLMASK  DC    X'40202120'        MASK FOR DISPLAY OF COLUMN NUMBER
         EJECT
         PSNMEXEC
         EJECT
         PSNMCMT
*
*
WORKAREA DSECT
         DS    18F
WKDOUBL  DS    D                  USED TO CONVERT COLUMN NUMBER
WKMASK   DS    CL4                TARGET OF EDIT OF COLUMN NUMBER
WKPGM    DS    CL8                HOLD PGM NAME FOR TABLE MATCHING
         PSNMBLOK  RENT=REGS
WKLENG   EQU   *-WORKAREA
         EJECT
         PSNMWORK
         END
