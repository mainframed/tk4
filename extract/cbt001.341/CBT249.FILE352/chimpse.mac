*          DATA SET TSO045     AT LEVEL 002 AS OF 01/23/80
*          DATA SET TSO045     AT LEVEL 001 AS OF 01/02/80
         TITLE 'CHIMP - MAIN STORAGE MONITOR FOR TSO - 3270S'
***********************************************************************
* IN CASE YOU ARE INTERESTED, THE SOURCE OF THE NAME (OR RATHER THE
* JUSTIFICATION FOR THE NAME) OF THIS SILLY PROGRAM IS/WAS:
*
*  COMPUTERIZED HEURISTIC INFORMATION MONITORING PROGRAM  (WHEW!)
*
* THIS COMMAND HAS SEVERAL DIFFERENT MODES OF DISPLAY:
*
*     I   - THIS MODE WILL DISPLAY ONLY THOSE MEMORIES WHICH ARE EITHER
*           SWAPPED IN AND RUNNING OR SWAPPED OUT BUT READY TO RUN.
*     O   - THIS MODE WILL DISPLAY ALL ACTIVE MEMORIES IN THE SYSTEM,
*           REGARDLESS OF LOCATION.
*
*   TO SWITCH BETWEEN MODES, TYPE EITHER I OR O.
*
*
*      YOU MAY ALSO ENTER OPTIONS TO RESTRICT THE OUTPUT TO ONLY
*      TSO MEMORIES (IN EITHER IN/OUT OR WAITING STATUS ACCORDING
*      TO YOUR USE OF THE I AND O OPTIONS ABOVE) OR ONLY STARTED
*      TASK/BATCH JOB MEMORIES:
*
*     T  - RESTRICTS THE OUTPUT TO ONLY TSO MEMORIES
*
*     B  - RESTRICTS THE OUTPUT TO STARTED TASK/BATCH MEMORIES
*
*     A  - RESETS THE TSO/STARTED TASK/BATCH JOB SELECTION.
*
*     S  - SELECTS ONLY SWAPPABLE ADDRESS SPACES FOR MONITORING    NTL1
*                                                                  NTL1
*     D  - DISPLAY DOMAIN TABLE                                    NTL1
*                                                                  NTL1
*   TO EXIT FROM THIS COMMAND, SIMPLY TYPE S
*
*  GLOSSARY OF TERMS FOR OUTPUT DISPLAY:
*
*    JOB       JOB NAME OF MEMORY
*    STEP      FOR BATCH JOBS, WILL BE 'STEPNAME' OR '-PROCSTEP'   NTL1
*              FOR TSO,  SAME AS FOR JOB                           NTL1
*              FOR STC,  STEPNAME OR UNIT (FOR IEFRDER/IEFPROC)    NTL1
*    STATUS    REASON CODE FOR SWAP OUT FROM OUCBPFL.              NTL1
*              THE FOLLOWING ARE THE REASON CODES DISPLAYED:
*    IN        IN MEMORY AND RUNNING                               NTL1
*    N/S       IN MEMORY AND NON-SWAPPABLE                         NTL1
*    WT        OUT OF MEMORY- WAITING                              NTL1
*    OUT       OUT OF MEMORY- ON OUT QUEUE                         NTL1
*    LSW       LOGICAL SWAP                                        NTL1
*    <->       TRANSITIONING IN, OUT OR EITHER DIRECTION           NTL1
*                                                                  NTL1
*        SWAP  0A MEMORY WAS UNILATERALLY SWAPPED                  NTL1
*        EXCH  09 MEMORY WAS EXCHANGE SWAPPED                      NTL1
*        ENQ   08 AN ENQHOLD HAS BEEN RECEIVED FOR THIS MEMORY     NTL1
*        REQS  07 MEMORY REQUESTED SWAP OUT                        NTL1
*        WAIT  06 INVOLUNTARY SWAPOUT DUE TO DETECTED WAIT         NTL1
*        RS<   05 SWAPOUT CAUSED BY REAL STORAGE SHORTAGE          NTL1
*        AS<   04 SWAPOUT CAUSED BY AUX. STORAGE SHORTAGE          NTL1
*        LONG  03 MEMORY SWAPPED OUT BY MS6 LONG WAIT DETECTIO     NTL1
*        TGET  02 MEMORY IS IN INPUT TERMINAL WAIT                 NTL1
*        TPUT  01 MEMORY IS IN OUTPUT TERMINAL WAIT                NTL1
*        ????     UNABLE TO DETERMINE SWAPOUT REASON.              NTL1
*                                                                  NTL1
*    MEMORY    CURRENT AMOUNT OF MEMORY ALLOCATED TO THIS MEMORY.
*    WSS       SRM'S VIEW OF THE WORKING SET SIZE FOR THIS MEMORY.
*    T         TYPE OF MEMORY (S=STC OR BATCH, T=TSO USER)
*    DP        DISPATCHING PRIORITY OF MEMORY (IN HEX).
*    PG        PERFORMANCE GROUP.
*    SC        NUMBER OF TIMES THE MEMORY HAS BEEN SWAPPED IN THE
*              CURRENT TRANSACTION.
*    EXCP      EXCP COUNT FROM OUCBIOSM    SE2                     NTL1
*    SRV       SERVICE ACCUMULATED IN THE LAST SWAPPED IN PERIOD.
*    CPU       CPU TIME IN SECONDS USED BY THE MEMORY (SRB PLUS TASK).
*
*
* MODS:  A) 11/26/79  K. TRUE
*          - NO 'DRY INITS' DISPLAY, DEFAULT OF 'BATCH,OUT' FOR DISPLAY
*          - MISC FIXES ETC TO ORIG CBT CHIMP COMMAND.
*          - ADDED DYNAMIC DOMAIN DISPLAY AND SHUFFLED PGM TO FIT..
*
*        B) 1/2/80  M. FARMER
*          - CORRECT 0C4 IN SWITCH FROM DOMAIN TO JOBS DISPLAY
*          - NAMING CONVENTION TO TSO... - LATEST NEW STANDARD
*
*        C) 4/14/81 K. TRUE  INTEL CIS                             NTL1
*          - ADAPT PROGRAM FOR MVS/SE2 AND CHANGE SCREEN HANDLING  NTL1
*            TO NOT ERASE/WRITE TO SPEED OPERATION ON 3274-1D CNTLRNTL1
*          - ADDED 'EQUATE' MACRO TO SUPPORT SE/NON SE ASSEMBLY    NTL1
*          - ADDED SYMBOLIC NAMES FOR MOST CONTROL BLOCKS VIA EQU  NTL1
*          - CHANGED 'WMR' TO 'EXCP' COUNT- MORE USEFUL            NTL1
*
***********************************************************************
         SPACE  ,                                                  NTL1
         MACRO  ,                                                  NTL1
&NAME    EQUATE ,                                                  NTL1
         LCLC  &SYMBOL                                             NTL1
         GBLA  &SYSTEM                                             NTL1
&NAME    EQU   &SYSLIST(&SYSTEM)      SYSTEM=&SYSTEM               NTL1
         MEND                                                      NTL1
         SPACE 1                                                   NTL1
         GBLA   &SYSTEM                                            NTL1
&SYSTEM  SETA   2         - ='1' FOR NON-SE, ='2' FOR SE2 SYSTEM   NTL1
         SPACE 1                                                   NTL1
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         EJECT ,                                                   NTL1
*        PUNCH '         ALIAS CHIMP'                              NTL1
TSO045   CSECT
*
         SAVE  (14,12),,CHIMP-V1M1-&SYSDATE-&SYSTIME               NTL1
         LR    R12,R15          ESTABLISH
         USING TSO045,R12          ADDRESSABILITY.
         LA    R11,SAVE         SET-UP
         ST    R13,SAVE+4         SAVE
         ST    R11,8(,R13)           AREA
         LR    R13,R11                CHAINING.
FINDCVT  L     R2,CVTPTR        LOAD PTR TO CVT.                   NTL1
         L     R2,CVTASVT(,R2)  TO ASVT                            NTL1
         L     R3,ASVTMAXU(,R2) MAXIMUM NUMBER OF ENTRIES          NTL1
         LA    R4,ASVTENTY-4(,R2)  FIRST ENTRY MINUS 4             NTL1
         LA    R1,BUFFER        LOAD ADDRESS OF OUTPUT BUFFER.
         MVC   HEADING,HEAD1    MOVE IN SCREEN HEADING           KMT
         SPACE
ASCBLOOP LA    R4,4(,R4)        BUMP BY FOUR.
         ICM   R5,B'1111',0(R4) LOAD THE PTR.
         BM    NOGOOD           BRANCH IF BAD PTR.
         TM    ASCBRCTF(R5),ASCBOUT  TEST FOR SWAPPED OUT          NTL1
INOROUT  BC    0,NOGOOD         BRANCH IF OUT AND NOT READY TO RUN.
         L     R6,ASCBOUCB(,R5)  LOAD PTR TO OUCB FOR QUICK TSO CHKNTL1
         TM    OUCBYFL(R6),OUCBLOG  IS IT TSO ?                    NTL1
TSOORNO  BC    1,NOGOOD         TO BE FILLED IN IF BATCH ONLY.
BATCHORN BC    0,NOGOOD         TO BE FILLED IN LATER.
         TM    OUCBSFL(R6),OUCBNSW  CHK FOR NON-SWAP               NTL1
*        BO    NOGOOD           BRANCH IF NON-SWAP.
SWAPABLE BC    0,NOGOOD         TO BE FILLED IN LATER.
         SPACE
         MVI   LINE+2,C' '      CLEAR OUTPUT LINE TO BLANKS.
         MVC   LINE+3(77),LINE+2 DITTO.
         MVC   JOB(8),=CL8'STARTING' TO BYPASS GARBAGE
         ICM   R6,B'1111',ASCBJBNI(R5)  LOAD PTR TO JOBNAME.       NTL1
         BZ    NOTJOB           BRANCH IF ZERO POINTER.
         MVC   JOB,0(R6)        OTHERWISE, MOVE IN JOBNAME.
         CLC   =CL8' ',X'18'(R6)  IS THERE A PROC STEP NAME?    KMT
         BE    NOTSTEP                                          KMT
         MVI   PSTEP,C'-'                                       KMT
         MVC   STEP(8),X'18'(R6)  MOVE IN PROC STEP NAME        KMT
         B     STEPOK                                           KMT
NOTSTEP  MVI   PSTEP,C' '                                       KMT
         MVC   STEP(8),X'38'(R6)  MOVE IN STEP NAME (NO PSTEP)  KMT
         B     STEPOK                                           KMT
NOTJOB   ICM   R6,B'1111',ASCBJBNS(R5)  JOBNAME IF S/M/LOGON       NTL1
         BZ    JOBOK            BYPASS THIS MOVE IF POINTER IS ZERO.
         MVC   JOB,0(R6)        MOVE IN JOBNAME FOR S/M/L MEMORIES.
JOBOK    DS    0H
         SPACE
         ICM   R7,B'1111',ASCBCSCB(R5)  GET PTR TO TOP CSCB.       NTL1
         BZ    STEPOK           BRANCH IF CSCB POINTER IS ZERO.
         CLI   4(R7),X'04'      CHECK TO SEE IF BATCH JOB OR S/M/L
         BE    STORMT           JOB IS START OR MOUNT
         CLI   4(R7),X'0C'      CHECK TO SEE IF BATCH JOB OR S/M/L
         BE    STORMT           JOB IS START OR MOUNT
         MVC   STEP(8),16(R7)   PICK UP CHCLS.
         B     STEPOK           BYPASS FURTHER CSCB PROCESSING.
STORMT   MVC   STEP(8),8(R7)    PICK UP CHKEY.
STEPOK   DS    0H
         CLC   JOB(8),=CL8'INIT' IS IT A 'DRY' INIT??           KMT
         BE    NOGOOD           YEP...KISS OFF                  KMT
         SPACE
         LH    R6,ASCBFMCT(,R5)  LOAD ALLOC FRAME COUNT.           NTL1
         SLL   R6,2             MULTIPLY BY FOUR.
         CVD   R6,WORK          CONVERT TO DECIMAL FORMAT.
         MVC   SIZE(6),=X'2020202120D2' GET READY FOR EDIT.
         ED    SIZE-1(6),WORK+5 EDIT THE RESULT.
         SPACE
         L     R6,ASCBOUCB(,R5) PTR TO OUCB.                       NTL1
         LH    R7,OUCBWSS(,R6)   LOAD OUCBWSS                      NTL1
*        SLL   R7,2             TIMES FOUR FOR K.
         CVD   R7,WORK          CONVERT TO DECIMAL.
         MVC   WSS(6),=X'202020212040' MOVE IN THE EDIT MASK.      NTL1
         ED    WSS-1(6),WORK+5 EDIT THE RESULT.                    NTL1
         SPACE
         TM    OUCBYFL(R6),OUCBLOG  TEST FOR TSO                   NTL1
         BZ    NOTTSO           BRANCH IF NOT TSO.
         MVI   TYPE,C'T'        MOVE IN TSO MEMORY INDICATION.
         B     TYPEOK           BYPASS REST OF TYPE PROCESSING.
NOTTSO   MVI   TYPE,C'S'        MOVE IN S/M/L INDICATION.
TYPEOK   DS    0H
         SPACE
         MVC   WHERE,=CL3'IN'   DEFAULT MEMORY POSITION IS IN.
         MVI   LINE+1,X'E8'     DEFAULT TO HI-INTENSITY PROTECTED.
         TM    OUCBSFL(R6),OUCBNSW  TEST FOR NON-SWAP              NTL1
         BZ    TRYWAIT          BRANCH IF NOT NON-SWAP.
         MVC   WHERE,=CL3'N/S'  MOVE IN NON-SWAP INDICATION.
         B     WHEREOK          BY-PASS REST OF POSITION PROCESSING.
TRYWAIT  DS    0H
         TM    OUCBQFL(R6),OUCBOFF CHECK FOR WAIT                  NTL1
         BZ    TRYOUT           BRANCH IF NOT.
         MVC   WHERE,=CL3'WT'   MOVE IN WAIT POSITION INDICATION.
         MVI   LINE+1,X'60'     MOVE IN 3270 LOW-INTENSITY ATTR BYTE.
         B     WHEREOK          BY-PASS REST OF POSITION PROCESSING.
TRYOUT   TM    OUCBQFL(R6),OUCBOUT TRY OUT QUEUE                   NTL1
         BZ    TRYTRIV          IF NOT, GO TRY TRIVIAL QUEUE STATUS.
         MVC   WHERE,=CL3'OUT'  MOVE IN OUT QUEUE POSITION.
         MVI   LINE+1,X'60'     MOVE IN 3270 LOW-INTENSITY ATTR BYTE.
         B     WHEREOK          BY-PASS REST OF POSITION PROCESSING.
TRYTRIV  TM    OUCBQFL(R6),OUCBLSW CHECK FOR TRIVIAL QUEUE POSITIONNTL1
         BZ    WHEREOK          IF NOT, EXIT POSITION PROCESSING.
         AIF   (&SYSTEM EQ 2).$1                                   NTL1
         MVC   WHERE,=CL3'TRV'  MOVE IN TRIVIAL QUEUE POSITION.
         AGO   .$1E                                                NTL1
.$1      ANOP  ,                                                   NTL1
         MVC   WHERE,=CL3'LSW'  MOVE IN LOGICAL SWAP  POSITION.
.$1E     ANOP  ,                                                   NTL1
WHEREOK  DS    0H
         TM    OUCBQFL(R6),OUCBGOO+OUCBGOI+OUCBGOB  TRANSITIONING? NTL1
         BZ    NOTTRANS         BRANCH IF MEMORY NOT TRANSITIONING.
         MVC   WHERE(3),=CL3'<->' INDICATE TRANSITIONING.
NOTTRANS DS    0H
         SPACE
         SR    R7,R7            PREPARE FOR THE INSERT CHARACTERS.
         IC    R7,ASCBDP(,R5)   LOAD ASCBDP                        NTL1
         SRL   R7,4             SHIFT OUT LOW ORDER NIBBLE.
         IC    R8,HEX(R7)       PICK THE EBCDIC CHARACTER.
         STC   R8,DP            STORE INTO OUTPUT FIELD.
         IC    R7,ASCBDP(,R5)   LOAD THE DISPATCHING PRIORITY AGN. NTL1
         N     R7,=F'15'        TURN OFF HIGH ORDER NIBBLE.
         IC    R8,HEX(R7)       LOAD THE EBCDIC CHARACTER.
         STC   R8,DP+1          STORE INTO OUTPUT LINE.
         SPACE
         SR    R7,R7            PREPARE FOR THE IC.
         AIF   (&SYSTEM EQ 2).$2                                   NTL1
         LH    R7,OUCBNPG(,R6)  GET NEW PERF GROUP  SE2            NTL1
         AGO   .$2E                                                NTL1
.$2      ANOP  ,                                                   NTL1
         IC    R7,24(,R6)       NEW PGN
.$2E     ANOP  ,                                                   NTL1
         CVD   R7,WORK          CONVERT IT TO DECIMAL.
         MVC   PGN(3),=X'202120' MOVE IN THE EDIT MASK.            NTL1
         ED    PGN-1(4),WORK+6 EDIT THE OUTPUT FIELD.              NTL1
         SPACE
         LH    R7,OUCBSWC(,R6)  LOAD OUCBSWC (SWAP COUNT)          NTL1
         CVD   R7,WORK          CONVERT IT TO DECIMAL.
         MVC   SWC(3),=X'202120' MOVE IN THE EDIT MASK.            NTL1
         ED    SWC-1(4),WORK+6 EDIT THE OUTPUT FIELD.              NTL1
         SPACE
         AGO   .$S3             SKIP FOR EXCP MODS                 NTL1
         L     R7,OUCBWMR(,R6)  LOAD OUCBWMR                       NTL1
         LTR   R7,R7            SEE IF HI-ORDER BIT IS ON
         BNM   WMROK             INDICATING WMR INVALID.
         MVC   WMR(5),=C'*N/A*' IF SO, MOVE IN NOT AVAIL INDIC.    NTL1
         MVI   WMS+6,C'-'       AND BLANK OUT THE SERVICE COUNTER. NTL1
         B     NOWMS            BY-PASS THE SERVICE PROCESSING.
WMROK    DS    0H
         SRL   R7,8             DIVIDE BY 256
         CVD   R7,WORK          CONVERT IT TO DECIMAL.
         MVC   WMR(5),=X'2020202120' MOVE IN EDIT MASK.            NTL1
         ED    WMR-1(6),WORK+5 EDIT THE RESULT.                    NTL1
WMROKA   DS    0H
.$S3     ANOP  ,                                                   NTL1
         L     R7,OUCBIOSM(,R6)  GET IO COUNT                      NTL1
         LTR   R7,R7             ANYTHING THERE?                   NTL1
         BNP   NOEXCP                                              NTL1
         C     R7,=F'9999999'    TOO BBIIIGGG?                     NTL1
         BL    DOEXCP                                              NTL1
         MVC   EXCP(7),=CL7'.> 10 M'                               NTL1
         B     NOEXCP                                              NTL1
DOEXCP   CVD   R7,WORK                                             NTL1
         MVC   EXCP(7),=X'20202020202120'                          NTL1
         ED    EXCP-1(8),WORK+4                                    NTL1
NOEXCP   DS    0H                                                  NTL1
         SPACE
         TM    OUCBQFL(R6),OUCBOFF+OUCBGOO+OUCBGOI TRANSITIONING?  NTL1
         BC    9,PFLDONE        MUST BE IN.
CHKSRC   DS    0H
         CLI   OUCBSRC(R6),X'0A' OUCBPFL (SWAP REASON CODE)        NTL1
         BH    UNKNOWN          TOO BIG REASON UNKNOWN
         SR    R7,R7            CLEAR OUT WORK REGISTER        KMT
         IC    R7,OUCBSRC(R6)   GET OUCBPFL (SWAP REASON CODE) KMT NTL1
         SLL   R7,2             MULTIPLY BY 4 TO GET INDEX     KMT
         LA    R7,REASONS(R7)   GET ADDRESS OF REASON CODE     KMT
         MVC   STATUS,0(R7)     MOVE IT INTO THE MESSAGE       KMT
         B     PFLDONE                                         KMT
UNKNOWN  MVC   STATUS,=CL4'????'       SORRY..DONT KNOW        KMT
PFLDONE  DS    0H
         SPACE
         L     R7,OUCBWMS(,R6)  OUCBWMS LOAD SVCE UNITS FOR XACTIONNTL1
         CVD   R7,WORK          CONVERT TO DECIMAL.
         MVC   WMS(7),=X'20202020202120' MOVE IN THE EDIT MASK.
         ED    WMS-1(8),WORK+4 EDIT THE RESULT.
         SPACE
NOWMS    LM    R8,R9,ASCBEJST(R5)  LOAD EJST (CPU TIME - TASK TYPE)NTL1
         SRDL  R8,12            TO GET MICROSEC
         D     R8,=F'10000'     TO GET HUNDREDTHS AND MICROS
         LM    10,R11,200(R5)   LOAD UP SRB TIME.
         SRDL  R10,12           TO GET MICROSEC
         D     R10,=F'10000'    TO GET HUNDREDTHS AND MICROS
         LTR   R9,R9            TEST JST CPU TIME
         BP    TESTSRB          IF ITS POISTIVE GO ON
         NOPR  R9               DUMMY INSTR FOR TSO TEST TO STOP ON
TESTSRB  LTR   R11,R11          TEST SRB CPU TIME
         BP    ADSRBJST         IF ITS OK GO ADD SRB AND JST CPU TIME
         NOPR  R9               ANOTHER DUMMY INSTR.
ADSRBJST AR    R9,R11           ADD TOGETHER.
         CVD   R9,WORK          CONVERT IT TO DECIMAL.
         MVC   CPUTIME(8),=X'20202021204B2020' MOVE IN THE EDIT MASK.
         ED    CPUTIME-1(9),WORK+4 EDIT THE RESULT.
         SPACE
         MVC   0(81,R1),LINE    MOVE THE LINE TO THE 3270 BUFFER.
         LH    R7,SCRSIZE       BUMP BUFFER COUNTER.
         LA    R7,81(,R7)
         STH   R7,SCRSIZE
         LA    R1,81(,R1)       SEE IF THE BUFFER IS FULL YET.
         C     R1,ADBUF
         BE    PUTIT            IF YES, THE GO PUT THIS SCREEN.
NOGOOD   BCT   R3,ASCBLOOP      BRANCH BACK TO LOOK AT NEXT MEMORY.
         LA    R4,BUFFER        LOAD ADDRESS OF OUTPUT BUFFER.
         CR    R4,R1            ARE WE STILL AT THE BEGINNING.
         BE    FINDCVT          IF SO, GO START AT THE TOP AGAIN.
         SPACE
*
**  LOOK THRU SRM CONTROL BLOCKS TO BUILD HEADER INFO
*
BUILDHDR DS    0H
         L     R4,CVTPTR       ---> CVT...                         NTL1
         L     R7,CVTPVTP(,R4) ---> PVT...                         NTL1
         L     R4,CVTOPCTP(,R4) --> RMCT... 1ST MODULE OF IRARMCNS NTL1
         L     R5,PVTCCTP(,R4) ---> CCT                            NTL1
         LH    R6,CCVUTILP(,R5)   CPU UTILIZATION                  NTL1
         CVD   R6,WORK             CONVERT TO DECIMAL...
         OI    WORK+7,X'0F'        FIX THE SIGN
         UNPK  HDRCPU,WORK+6(2)     ...AND EDIT
         SPACE
         LH    R6,PVTAFC(,R7)     AVERAGE AVAILABLE FRAME COUNT    NTL1
         CVD   R6,WORK             CONVERT TO DECIMAL...
         OI    WORK+7,X'0F'        FIX THE SIGN
         UNPK  HDRAFC,WORK+6(2)     ...AND EDIT
         SPACE
         LH    R6,X'226'(R4)      PAGE FAULT RATE
         CVD   R6,WORK             CONVERT TO DECIMAL...
         OI    WORK+7,X'0F'        FIX THE SIGN
         UNPK  HDRAPR,WORK+6(2)     ...AND EDIT
         SPACE
         LH    R6,X'224'(R4)      AUX. STORAGE MANAGER QUE LENGTH
         CVD   R6,WORK             CONVERT TO DECIMAL...
         OI    WORK+7,X'0F'        FIX THE SIGN
         UNPK  HDRASQ,WORK+6(2)     ...AND EDIT
         SPACE
         LH    R6,X'21E'(R4)      UNREFERENCED INTERVAL COUNT
         CVD   R6,WORK             CONVERT TO DECIMAL...
         OI    WORK+7,X'0F'        FIX THE SIGN
         UNPK  HDRUIC,WORK+6(2)     ...AND EDIT
         SPACE
         LH    R6,X'240'(R4)      DEFERRED IO REQUESTS
         CVD   R6,WORK             CONVERT TO DECIMAL...
         OI    WORK+7,X'0F'        FIX THE SIGN
         UNPK  HDRDIO,WORK+6(2)     ...AND EDIT
         SPACE
         L     R7,RMCTDMDT(,R4) --> DMDT..     R4--> RMCT          NTL1
         SR    R15,R15         CLEAR A REG
         LA    R5,3            NUMBER OF DOMAIN ENTRIES TO PROCESS
         LA    R14,HDRDMN      POINT TO FIRST DOMAIN ENTRY
         USING DMDT,R7                                             NTL1
         LA    R7,DMDTLEN(R7)  BUMP PAST FIRST DOMAIN              NTL1
DODMNS   DS    0H
*        IC    R15,DMDTNO      GET DOMAIN NUMBER                   NTL1
*        CVD   R15,WORK        CHANGE IT TO DECIMAL
*        OI    WORK+7,X'0F'    FIX THE SIGN
*        UNPK  1(2,R14),WORK+6(2) MOVE IT TO THE OUTPUT AREA
         LH    R15,DMDTMPLT    GET MULTI-PROGRAMMING LEVEL TARGET  NTL1
         CVD   R15,WORK        CHANGE IT TO DECIMAL
         OI    WORK+7,X'0F'    FIX THE SIGN
         UNPK  1(2,R14),WORK+6(2) MOVE IT TO THE OUTPUT AREA
         LH    R15,DMDTRUA     GET NUMBER OF READY USERS           NTL1
         CVD   R15,WORK        CHANGE IT TO DECIMAL
         OI    WORK+7,X'0F'    FIX THE SIGN
         UNPK  4(2,R14),WORK+6(2) MOVE IT TO THE OUTPUT AREA
         LA    R14,6(R14)     POINT TO NEXT DOMAIN ENTRY
         LA    R7,DMDTLEN(R7)  BUMP PAST THIS DOMAIN               NTL1
         BCT   R5,DODMNS       GO PROCESS ANOTHER
         DROP  R7                                                  NTL1
         SPACE
*        STCK  TIME            STORE TIME OF DAY CLOCK
PUTIT    LA    R1,CLEAR         PREPARE
         LA    0,Z-CLEAR-1         FOR
         BAL   R15,ERASESCR           THE
         ICM   R1,8,TPUTFLG              FULL SCREEN
         TPUT  (1),(0),R                   TPUT
         TGET  BUFFER,1         READ THE INPUT DATA FROM USER.
         OI    BUFFER,X'40'     GET TO UPPER CASE BEFORE COMPARE.
         CLI   BUFFER,C' '      IS ANYONE HOME?
         BNE   CHKITOUT
         TM    MODESW,DOMAIN    IS DOMAIN MODE IN PROCESS       KMT
         BO    DISPDMN          YES....GO DO IT                 KMT
CHKITOUT CLI   BUFFER,C'S'      CHECK IF TERMINATION REQUESTED.
         BE    EODAD            BYE BYE BABY...
ISDOMAIN CLI   BUFFER,C'D'      CHECK FOR DOMAIN MODE
         BNE   NOTDMN
         OI    MODESW,DOMAIN+MORSTUF    ON WITH THE SHOW....    MF1-80
         B     GOODGRIF         FINITO..
NOTDMN   NI    MODESW,255-DOMAIN SOMETHING ELSE..TURN OFF DOMAINKMT
         CLI   BUFFER,C'T'      CHECK FOR TSO ONLY.
         BNE   BATCHCHK         IF NOT, TRY FOR BATCH ONLY MODE.
         MVI   BATCHORN+1,X'80' RESET THE BRANCHES TO GET
         MVI   TSOORNO+1,X'00'     ONLY TSO MEMORIES.
         MVC   OPTMODE,=C'TSO'  INDICATE MODE OF OPERATION      KMT
         B     GOODGRIF         BY-PASS FURTHER INPUT CHECKING.
BATCHCHK CLI   BUFFER,C'B'      CHECK FOR BATCH ONLY
         BNE   SWAPCHK          BRANCH TO CHECK OTHER OPTIONS.
         MVI   TSOORNO+1,X'10'  RESET THE BRANCHES TO GET
         MVI   BATCHORN+1,X'00'    ONLY BATCH (STC) MEMORIES.
         MVC   OPTMODE,=C'BAT'  INDICATE MODE OF OPERATION      KMT
         B     GOODGRIF         BY-PASS FURTHER INPUT CHECKING.
SWAPCHK  CLI   BUFFER,C'N'      CHECK FOR SWAPPABLE ONLY
         BNE   OTHERCHK         BRANCH TO CHECK OTHER OPTIONS.
         MVI   SWAPABLE+1,X'10' ONLY SWAPABLE MEMORIES.
         MVC   OPTMODE,=C'SWP'  INDICATE MODE OF OPERATION      KMT
         B     GOODGRIF         BY-PASS FURTHER INPUT CHECKING.
OTHERCHK CLI   BUFFER,C'A'      CHECK FOR EVERYTHING
         BNE   XYZ              IF NOT, TRY FOR IN OR OUT MODES.
         MVI   TSOORNO+1,X'00'  IF EVERYTHING, NOP ALL THE
         MVI   BATCHORN+1,X'00'    SELECTION BRANCHES.
         MVI   SWAPABLE+1,X'00' ONLY SWAPABLE MEMORIES.
         MVC   OPTMODE,=C'ALL'  INDICATE MODE OF OPERATION      KMT
XYZ      DS    0H
         CLI   BUFFER,C'O'      CHECK FOR OUT MODE
         BNE   WHATHECK
         MVI   INOROUT+1,X'00'  NOP THE BRANCH
         OI    MODESW,MORSTUF   INDICATE CHANGE OF MODES.
         MVC   OPTINO,=C'ANY'   INDICATE IN/OUT SELECTION       KMT
         B     GOODGRIF
WHATHECK CLI   BUFFER,C'I'      CHECK FOR IN OR READY MODE
         BNE   GOODGRIF
         MVI   INOROUT+1,X'10'  TAKE THE BRANCH
         OI    MODESW,MORSTUF   INDICATE MODE SWITCH.
         MVC   OPTINO,=C'IN '   INDICATE IN/OUT SELECTION       KMT
         B     GOODGRIF
         SPACE
GOODGRIF DS    0H
         MVC   SCRSIZE,=H'0'    RESET SCREEN SIZE.
         LA    R1,BUFFER        PREPARE
         LA    R6,BUFFER           FOR
         ICM   R6,8,C' '              THE
         LA    R7,BUFLEN                 MOVE
         LA    R8,BUFFER                    CHARACTER
         SR    R9,R9                           LONG
         MVCL  R6,R8                              TO CLEAR BUFFER.
         TM    MODESW,DOMAIN    IS DOMAIN THE NAME?            KMT
         BO    DISPDMN          GO DOIT SAM                    KMT
         TM    MODESW,MORSTUF   CHECK FOR CHANGE OF MODES.
         BNO   MODEOK           BRANCH IF SAME MODE.
         NI    MODESW,255-MORSTUF RESET MODE CHANGE.
         B     FINDCVT          START OVER FROM BEGINNING.
         SPACE
MODEOK   DS    0H
         LTR   R3,R3            CHECK TO SEE IF MORE MEMORIES TO PROCES
         BZ    FINDCVT          NO, TAKE IT FROM THE TOP
         BCT   R3,ASCBLOOP      WAS THERE ONLY ONE LEFT AT BUFFER FULL
         B     FINDCVT          YES, START OVER FROM THE TOP.
         EJECT
*.....................................................................*
*     FCI MOD TO DISPLAY DOMAINS ON THE SCREEN                        *
*.....................................................................*
         SPACE
DISPDMN  L     R5,16               (R5)=ADDR OF CVT                FCI1
         L     R5,CVTOPCTP(R5)     (R5)=ADDR OF RMCT               FCI1
         L     R9,RMCTDMDT(R5)     (R9)=ADDR OF DMDT (FIRST ENTRY) FCI1
         XR    R8,R8               CLEAR REG 4                     FCI1
         ICM   R8,B'0011',RMCTDMNC(R5)   (R4)=NUMBER OF DOMAINS    FCI1
         LTR   R8,R8               ANYBODY HOME?                   FCI1
         BNP   BUILDHDR             NOPE-> EXIT STAGE LEFT         FCI1
         USING DMDT,R9             ESTABLISH ADDR TO DOMAIN DESC. TABLE
         LA    R1,BUFFER                                           KMT
         MVC   SCRSIZE,=H'0'       INITIALIZE POINTERS AND BUFFER  KMT
         MVC   HEADING,FCIHEAD     INIT DOMAIN DISPLAY HEADING     KMT
         SPACE
DMNLOOP  MVC   FCIBUF(2),=X'1DE8' MOVE IN HIGH INTENSITY FIELD.    FCI1
         MVI   FCIBUF+2,X'40'     CLEAR OUTPUT LINE                FCI1
         MVC   FCIBUF+3(78),FCIBUF+2 TO BLANKS.                    FCI1
         SPACE 2
         LA    R4,DMDTNO                                           FCI1
         LA    R5,MNO                                              FCI1
         BAL   R6,EDIT             GO EDIT DOMAIN NUMBER INTO MSG  FCI1
         CLI   MNO+2,C'-'          DOMAIN=0?                       FCI1
         BNE   DMNCONT              NO-> SKIP FIXUP                FCI1
         MVI   MNO+2,C'0'                                          FCI1
DMNCONT  DS    0H                                                  FCI1
         SPACE
         LA    R4,DMDTLO                                           FCI1
         LA    R5,MLO                                              FCI1
         BAL   R6,EDIT             GO EDIT MIN MPL INTO MSG        FCI1
         SPACE
         LA    R4,DMDTHI                                           FCI1
         LA    R5,MHI                                              FCI1
         BAL   R6,EDIT             GO EDIT MAX MPL INTO MSG        FCI1
         SPACE
         LA    R4,DMDTWT                                           FCI1
         LA    R5,MWT                                              FCI1
         BAL   R6,EDIT             GO EDIT WGHT FACTOR INTO MSG    FCI1
         SPACE
         LA    R4,DMDTMPLT                                         FCI1
         LA    R5,MMPLT                                            FCI1
         BAL   R6,EDITH            GO EDIT CURRENT MPL TGT INTO MSGFCI1
         SPACE
         LA    R4,DMDTRUA                                          FCI1
         LA    R5,MRUA                                             FCI1
         BAL   R6,EDITH            GO EDIT AVE # OF READY USRS     FCI1
         SPACE
         LA    R4,DMDTGOOU                                         FCI1
         LA    R5,MGOOU                                            FCI1
         BAL   R6,EDITH            GO EDIT CURRENT # USRS GOING OUTFCI1
         SPACE
         LA    R4,DMDTCMPL                                         FCI1
         LA    R5,MCMPL                                            FCI1
         BAL   R6,EDITH            GO EDIT CURRENT MPL INTO MSG    FCI1
         SPACE
         LA    R4,DMDTOUTU                                         FCI1
         LA    R5,MOUTU                                            FCI1
         BAL   R6,EDITH            GO EDIT CURNT # USRS ON OUT Q   FCI1
         SPACE
         LA    R4,DMDTINCU                                         FCI1
         LA    R5,MINCU                                            FCI1
         BAL   R6,EDITH            GO EDIT CURNT # SWAPBL IN C USR FCI1
         SPACE
         CLC   2(79,R1),FCIBUF+2    ANY CHANGE FROM LAST TIME      FCI1
         BNE   MVLIN                                               FCI1
         MVI   FCIBUF+1,X'60'      INDICATE NO CHANGE (LOW INTENS) FCI1
MVLIN    MVC   0(81,R1),FCIBUF     MOVE BUFFER TO LINE             FCI1
         SPACE 2
         LA    R9,DMDTLEN(R9)      BUMP POINTER                    FCI1
         LH    R7,SCRSIZE         SAVE ADDRESS OF LAST ENTRY       FCI1
         LA    R7,81(R7)           AND BUMP                        FCI1
         STH   R7,SCRSIZE           SIZE FOR TPUT TO USE           FCI1
         LA    R1,81(R1)           BUMP BUFFER POINTER             FCI1
         BCT   R8,DMNLOOP          LOOP THROUGH ALL DOMAINS        FCI1
         B     BUILDHDR                                            FCI1
         EJECT
*.....................................................................*
*     LOCAL SUBROUTINES   EDIT,EDITH                                  *
*.....................................................................*
         SPACE
EDIT     ICM   R4,B'0001',0(R4)    GET BYTE TO CONVERT             FCI1
         N     R4,=X'000000FF'                                     FCI1
         B     DOEDIT                                              FCI1
EDITH    ICM   R4,B'0011',0(R4)                                    FCI1
         N     R4,=X'0000FFFF'                                     FCI1
DOEDIT   C     R4,=F'999'          TO LARGE?                       FCI1
         BNH   EDIT1                                               FCI1
         MVC   0(3,R5),=C'***'     INDICATE TOO LARGE              FCI1
         BR    R6                    AND RETURN                    FCI1
EDIT1    CVD   R4,WORK             CONVERT TO DECIMAL              FCI1
         MVC   0(3,R5),=X'202020'  MOVE IN MASK                    FCI1
         BCTR  R5,0                DECREMENT POSITION FOR SPACE    FCI1
         ED    0(4,R5),WORK+6        BEFORE NUMBER AND EDIT        FCI1
         CLC   1(3,R5),=C'   '     ANYTHING THERE                  FCI1
         BE    EDIT2                                               FCI1
         BR    R6                                                  FCI1
EDIT2    MVI   3(R5),C'-'          INDICATE PLACE HOLDER           FCI1
         BR    R6                  RETURN                          FCI1
         SPACE 2                                                   NTL1
ERASESCR STM   R4,R7,SAVE47                                        NTL1
         LA    R4,BUFFER          POINT TO TARGET                  NTL1
         AH    R4,SCRSIZE          PAST NUMBER OF BYTES USED       NTL1
         L     R5,=A(BUFLEN)      GET TOTAL LENGTH AND             NTL1
         SH    R5,SCRSIZE         DEDUCT FOR AMOUNT USED           NTL1
         SLR   R6,R6              SOURCE LENGTH = 0                NTL1
         SLR   R7,R7              NO SOURCE ADDRESS                NTL1
MVCL     MVCL  R4,R6              CLEAR OUT THE TARGET             NTL1
         LM    R4,R7,SAVE47       RESTORE REGS                     NTL1
         BR    R15                AND RETURN                       NTL1
SAVE47   DC    4F'0'              REG SAVE AREA                    NTL1
         EJECT
EODAD    DS    0H
BYEBY    LA    R1,CLR           PREPARE FOR THE
         LA    0,L'CLR             FULL SCREEN
         ICM   R1,8,TPUTFLG           TPUT TO
         TPUT  (1),(0),R                CLEAR THE SCREEN.
         L     R13,SAVE+4       LOAD CALLERS SAVE AREA POINTER.
         RETURN (14,12),RC=0    AND AWAY WE GO...
         EJECT
MODESW   DC    X'00'  MODE CHANGE.
MORSTUF  EQU   X'80'  MORE JUNK TO DO.....
DOMAIN   EQU   X'08'  PLEASE DO DOMAIN STUFF                       FCI1
WORK     DS    D
HEX      DC    C'0123456789ABCDEF'
SAVE     DS    18F
REASONS  DC    C' ?? TPUTTGETLONGAUX<RS< WAITREQSENQ EXCHSWAP'
*                ×...×...×...×...×...×...×...×...×...×...×...
X        DC    CL80' '
         ORG   X
LINE     DC    X'1D60'
JOB      DC    CL8' '
         DC    C' '
PSTEP    DC    C' '
STEP     DC    CL8' '
         DC    C' '
WHERE    DC    CL3' '
         DC    C' '
STATUS   DC    CL4' '
         DC    C' '
SIZE     DC    CL5' '
         DC    CL2' '
WSS      DC    CL5' '                                              NTL1
         DC    CL2' '
TYPE     DC    C' '
         DC    C' '
DP       DC    CL2' '
         DC    C' '
PGN      DC    CL3' '                                              NTL1
         DC    C' '
SWC      DC    CL3' '                                              NTL1
         DC    C' '
*        DC    C' '                                                NTL1
*        DC    C' '                                                NTL1
EXCP     DC    CL7' '                                              NTL1
         DC    C' '
WMS      DC    CL7' '                                              NTL1
         DC    C' '
CPUTIME  DC    CL8' '
         DC    C' '
         ORG X+90
SCRSIZE  DC    H'0'
TPUTFLG  DC    X'03'
ADBUF    DC    A(Z)
CLR      DC    X'C3115D7E1140403C40400011404013'
         SPACE 2
*   C7= WCC FOR CLEAR  11= SET BUFFER ADDRESS 4040 = BUFADD POS 0000
*   3C= REPEAT TO ADDRESS 4040(0000) CHAR(00) 11 = SET BUFFER ADDRESS
*   4040 = POSITION 0000(R=1 C=1)  13 = INSERT CURSOR
         SPACE 2
*             XXXXXXXX -XXXXXXXX XXX XXXX XXXXX  XXXXX  X XX XXX XXX
HEAD1   DC CL77'  JOB     STEP   .STATUS. MEMORY   WSS  T DP  PG SWAPS X
                EXCP    SRV    CPU'
*              XXXXX XXXXXXX XXXXXXXX
FCIHEAD  DC   CL77'         DOMAIN MIN MAX WGT CUR TGT RDY  IN >OT OUT'
*                   456789.123456789.123456789.123456789.123456789.012
*                            XXX   XXX XXX XXX XXX XXX XXX XXX XXX XXX
FCIBUF   DS    CL81                                                FCI1
FCIOFST  EQU   9                 OFFSET INTO FCIBUFFER             FCI1
         ORG   FCIBUF+FCIOFST+4                                    FCI1
MSG      DS    CL43                                                FCI1
         ORG   FCIBUF+FCIOFST+5                                    FCI1
MNO      DS    CL3                                                 FCI1
         ORG   FCIBUF+FCIOFST+11                                   FCI1
MLO      DS    CL3                                                 FCI1
         ORG   FCIBUF+FCIOFST+15                                   FCI1
MHI      DS    CL3                                                 FCI1
         ORG   FCIBUF+FCIOFST+19                                   FCI1
MWT      DS    CL3                                                 FCI1
         ORG   FCIBUF+FCIOFST+23                                   FCI1
MCMPL    DS    CL3                                                 FCI1
         ORG   FCIBUF+FCIOFST+27                                   FCI1
MMPLT    DS    CL3                                                 FCI1
         ORG   FCIBUF+FCIOFST+31                                   FCI1
MRUA     DS    CL3                                                 FCI1
         ORG   FCIBUF+FCIOFST+35                                   FCI1
MINCU    DS    CL3                                                 FCI1
         ORG   FCIBUF+FCIOFST+39                                   FCI1
MGOOU    DS    CL3                                                 FCI1
         ORG   FCIBUF+FCIOFST+43                                   FCI1
MOUTU    DS    CL3                                                 FCI1
         ORG
         SPACE 2
         LTORG
         EJECT
**********************************************************************
* NOTE: JUNK FROM THIS POINT UNTIL 'Z' ARE ALL PART OF THE           *
*   'BIG SCREEN'  LEAVE THIS STUFF TOGETHER AND PUT CLEAR AT THE     *
*   END OF THE PROGRAM IF YOU DONT WANT TO OVERFLOW MY ONE CHEAP     *
*   BASE REGISTER..THAT DC 21CL81' ' WILL CHEW UP A LOT OF SPACE.... *
**********************************************************************
         SPACE 1
* EAR    DC    X'C3115D7F1140403C4040001140401D4013401DE8'
CLEAR    DC    X'C3115D7F1140401D4013401DE8'
         SPACE 2
*   C7= WCC FOR CLEAR  11= SET BUFFER ADDRESS 4040 = BUFADD POS 0000
*   3C= REPEAT TO ADDRESS 4040(0000) CHAR(00) 11 = SET BUFFER ADDRESS
*   4040 = POSITION 0000(R=1 C=1)  1D = START FIELD 40= UNPROTECTED
*   13 = INSERT CURSOR 40 = BLANK 1D = START FIELD E8 = PROTECT HILITE
         SPACE 2
SYSHDR   DS    0CL77
         DC    CL4'CPU='         CPU UTILIZATION
HDRCPU   DC    CL3'XXX',C'%'
         DC    CL5' AFC='        AVERAGE AVAILABLE FRAME COUNT
HDRAFC   DC    CL4'XXXX'
         DC    CL5' PFR='        PAGE FAULT RATE
HDRAPR   DC    CL4'XXXX'
         DC    CL5' ASQ='        AUX STORAGE MANAGER QUE LENGTH
HDRASQ   DC    CL4'XXXX'
         DC    CL5' UIC='        UNIREFERENCED INTERVAL COUNT
HDRUIC   DC    CL4'XXXX'
         DC    CL5' DIO='        DEFERRED IO REQUESTS
HDRDIO   DC    CL4'XXXX'
         DC    CL6' DMNS='       # OF DOMAINS IN ENTRIES
HDRDMN   DC    3CL6' MT:RU'      MULTI-PROGRAMMING LEVEL TARGET
OPTDEF   DC    80C'-'            # OF READY USERS
         ORG   OPTDEF+3
         DC    C'OPTIONS='
OPTMODE  DC    C'BAT',C','
OPTINO   DC    C'ANY'
         ORG
         DC    X'404040'
HEADING  DC    CL77' '
BUFFER   DC    21CL81' '
LENGTH   EQU   BUFFER-CLEAR
Z        DS    X
BUFLEN   EQU   Z-BUFFER
         EJECT
*.....................................................................*
*        MAPPING DSECT FOR DMDT (DOMAIN DESCRIPTOR TABLE)             *
*.....................................................................*
         SPACE
DMDT     DSECT
DMDTNO   DS    XL1             DOMAIN NUMBER
DMDTLO   DS    XL1             MIN  MPL LEVEL
DMDTHI   DS    XL1             MAX  MPL LEVEL
DMDTWT   DS    XL1             WEIGHTING FACTOR
DMDTMPLT DS    XL2             CURRENT MPL TARGET
DMDTRUA  DS    XL2             AVE # OF READY USERS
DMDTGOOU DS    XL2             CURRENT # USERS GOING OUT
DMDTCMPL DS    XL2             CURRENT MPL
DMDTOUTU DS    XL2             CURRENT # USERS ON OUT Q
DMDTINCU DS    XL2             CURRENT # SWAPPABLE INCORE USERS
DMDTRUC  DS    XL4             ACCUM FOR READY USER AVERAGE
DMDTEND  DS    0X              END OF DMT
         DS    9F                                                  NTL1
DMDTLEN  EQU   *-DMDT
         EJECT  ,
*
* EQUATES....
*
CVTOPCTP EQU   X'25C'          POINTER TO RMCT
CVTPVTP  EQU   X'164'          POINTER TO PVT                      NTL1
CVTPTR   EQU   16                                                  NTL1
CVTASVT  EQU   X'22C'          POINTER TO ASVT                     NTL1
*                                                                  NTL1
PVTCCTP  EQU   X'004'          POINTER TO CCT FROM PVT             NTL1
*                                                                  NTL1
ASVTMAXU EQU   X'204'          MAX USERS (ASIDS)                   NTL1
ASVTENTY EQU   X'210'          FIRST ENTITY IN ASVT                NTL1
*                                                                  NTL1
ASCBDP   EQU   X'02B'          DISPATCHING PRIORITY                NTL1
ASCBCSCB EQU   X'038'          CSCB POINTER                        NTL1
ASCBEJST EQU   X'040'          TCB AND SRB CPU TIME                NTL1
ASCBRCTF EQU   X'066'                                              NTL1
ASCBOUT  EQU  X'04'                                                NTL1
ASCBOUCB EQU   X'090'                                              NTL1
ASCBFMCT EQU   X'098'                                              NTL1
ASCBJBNI EQU   X'0AC'                                              NTL1
ASCBJBNS EQU   X'0B0'                                              NTL1
*                                                                  NTL1
OUCBQFL  EQU   X'010'                                              NTL1
OUCBGOO  EQU X'80'                                                 NTL1
OUCBGOI  EQU X'40'                                                 NTL1
OUCBGOB  EQU X'20'                                                 NTL1
OUCBOFF  EQU X'08'                                                 NTL1
OUCBOUT  EQU X'04'                                                 NTL1
OUCBLSW  EQU X'02'                                                 NTL1
OUCBSFL  EQU   X'011'                                              NTL1
OUCBNSW  EQU X'80'                                                 NTL1
OUCBYFL  EQU   X'012'                                              NTL1
OUCBLOG  EQU X'20'                                                 NTL1
OUCBSRC  EQUATE X'025',X'029'                                      NTL1
OUCBSWC  EQUATE X'026',X'02A'                                      NTL1
OUCBWMS  EQUATE X'034',X'038'                                      NTL1
OUCBWSS  EQUATE X'04E',X'052'                                      NTL1
OUCBWMR  EQUATE X'058',X'060'                                      NTL1
OUCBIOSM EQUATE X'06C',X'06C'                                      NTL1
OUCBNPG  EQU   X'0B8'                                              NTL1
*                                                                  NTL1
CCVUTILP EQU   X'066'                                              NTL1
PVTAFC   EQU   X'002'                                              NTL1
*                                                                  NTL1
RMCTDMDT EQU   X'B4'           POINTER TO DOMAIN TABLE
RMCTDMNC EQU   X'BC'           COUNT OF NUMBER OF DOMAINS
RMCTWMST EQU   X'18'           ADDRESS OF WLM SPEC TABLE
         SPACE 2
         END
