*          DATA SET PSNMFDD    AT LEVEL 002 AS OF 10/30/80
         MACRO
&TAG     PSNMFDD   &NAM,&NONE
.*                   TO FIND THE DEFINITION OF A KEYWORD WITHIN
.*                   THE DD TABLE, GIVEN YOU KNOW WHAT YOU WANT
.*
         LCLC  &WK
         AIF   (T'&TAG EQ 'O').NOTAG
&TAG     DS    0H
.NOTAG   ANOP
         AIF   ('&NAM'(1,1) EQ '(').NREG
         LA    R1,&NAM-1
         AGO   .XREG2
.NREG    ANOP
         AIF   ('&NAM' EQ '(1)' OR '&NAM' EQ '(R1)').XREG1
&WK      SETC  '&NAM'(2,K'&NAM-2)
         LR    R1,&WK
.XREG1   ANOP
         BCTR  R1,0
.XREG2   ANOP
         SLL   R1,1               NUMBER OF HALFWORDS
         LH    R7,DDBDISPL(R1)    PROPER POINTER TO ENTRY
         LTR   R7,R7              DOES IT EXIST
         AIF   (T'&NONE EQ 'O').NO1
         BZ    &NONE              NO
         AGO   .NO2
.NO1     ANOP
         BZ    *+8                NO - PASS ZERO
.NO2     ANOP
         LA    R7,DDBLOCK(R7)
.EXIT    ANOP
         SPACE 1
         MEND
