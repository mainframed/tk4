*          DATA SET PANFETCH   AT LEVEL 011 AS OF 01/27/83
*          DATA SET SYSJOB     AT LEVEL 001 AS OF 05/08/78
PVFETCH  TITLE 'FIND MEMBER OF PANVALET LIBRARY AND SHOVEL TO OS DATASET
                 VER 1.0'
PVFETCH  START 0
*
* THIS MODULE DOES THE FOLLOWING:
*
*   1) EXTRACTS A MEMBER NAME FROM THE PARM FIELD
*   2) ISSUES A PANVALET FIND FOR THE MEMBER
*   3) USES PANGET TO READ DDNAME 'IN' FOR MEMBER SELECTED
*   4) ISSUES QSAM PUTS FOR CARDS FROM THE MEMBER
*   5) CLOSES ALL AND EXITS
*
*   ERROR MESSAGES IF MEMBER CANT BE FOUND. AND NON ZERO RETURN CODES
*
*    //PVREAD  EXEC  PGM=PANFETCH,PARM=MEMBERNAME
*    //PANDD1   DD   DSN=YOUR.PANVALET.LIBRARY,DISP=SHR
*    //OUT      DD   DSN=......,DCB=LRECL=80....
*
* CODE FROM SYSJOB, AND PVACCESS FROM TSO048
*
* MOD1 - KEN TRUE - 30 SEP 82                                       NTL
*    IF MEMBER NAME IS FOLLOWED BY A '/', THEN DON'T EXPAND         NTL
*    ANY IMBEDDED ++INCLUDE CARDS.                                  NTL
*                                                                   NTL
* MOD2  -KEN TRUE - 26 JAN 83                                       NTL
*    ACCEPT MEMBER NAMES OF 10 CHARS (UP FROM 8 CHARS)              NTL
*                                                                   NTL
* KEN TRUE - INTEL CORP - 18 AUG 81
*
         EJECT
         SAVE  (14,12),,PVFETCH.V1M2.&SYSDATE..&SYSTIME
         LR    R12,R15
         USING PVFETCH,R12          BASE REG
         B     START
*
SAVEA    DC    18F'0'              CUR REG SAVEA
*
START    EQU   *
         ST    R13,SAVEA+4         SAVE ADDR PREV SAVEA
         LR    R11,R13             ADDR PREV SAVEA
         LA    R13,SAVEA           ADDR CUR  SAVEA
         ST    R13,8(R11)          SAVE ADDR CUR SAVEA IN PREV
         L     R3,0(,R1)           SAVE PARM ADDR PTR
LINKREG  EQU   6                   LINK REGISTER
RXX      EQU   14                  WORK REGISTER
         EXTRACT MF=(E,EXTRACT)
         L     R1,ANSWER            (R1)=ADDR OF TSO FLAG FROM EXTRACT
         MVC   TSOFLAG,0(R1)        MOVE IN THE ANSWER TO OUR FLAG
         SPACE
*
* MAKE SURE DDNAME IS PRESENT BY SEARCH OF TIOT
*
         L     R1,16              (R1)= A(CVT)
         L     R1,0(R1)           (R1)= A(NEW/OLD TCB:ASCB)
         L     R1,4(R1)           (R1)= A(MY TCB)
         L     R1,12(R1)          (R1)= A(TIOT)
         LA    R1,24(R1)          SKIP OVER JOBNAMES
*
         SR    R2,R2              CLEAR REGISTER FOR LENGTH USE
LOOPTIOT CLC   DDNAME,4(R1)       IS THIS THE ONE?
         BE    FOUNDIT            YES-> GO SAVE ADDRESS
         IC    R2,0(R1)           NO-> GET LENGTH OF ENTRY
         AR    R1,R2                ADD TO BUMP TO NEXT..
         CLI   0(R1),X'00'        IS THAT THE END OF TIOT?
         BNE   LOOPTIOT             NO-> GO TRY AGAIN
         SPACE 1
ENDTIOT  LA    R1,EMSG1             SIGNAL NO IN DD..NO WORK
         BAL   LINKREG,PUTMSG       GO PUT OUT MESSAGE
         MVC   ACTION,=F'204'       SIGNAL MISSING DDNAME
         B     EXIT                 GO EXIT
         SPACE 1
FOUNDIT  BAL   R10,GOOPEN          OPEN THE INPUT PVLIB FOR ACCESS
*
         EJECT
**********************************************************************
*  PVLIB DATASET IS THERE..NOW USE PARM FIELD TO GET MEMBER NAME     *
**********************************************************************
         SPACE
CHKPARM  LH    R5,0(R3)            GET HALFWORD LENGTH
         LTR   R5,R5               ANY THING THERE?
         BNP   NOPARM              NO..GRIPE AND QUIT
         LA    R4,1(R3,R5)         POINT TO END OF PARM             NTL
         CLI   0(R4),C'/'          WANT NO EXPANSION OF ++INCLUDES? NTL
         BNE   CHKPARM1            ..NO..EXPANDIT                   NTL
         MVC   INCLUDES,=CL8'NO-ENTRY'  SHUT OFF EXPANSION          NTL
         BCTR  R5,0                SAY ONE LESS CHAR IN PARM        NTL
         SPACE 1                                                    NTL
CHKPARM1 CH    R5,=H'10'           MORE THAN 10 CHARACTERS?         NTL
         BH    NOPARM               YEP..GRIPE AND QUIT
         SPACE
         MVC   NAME1,=CL22' '      CLEAR MEMBER NAME
         BCTR  R5,0                DECREMENT FOR MVC
         EX    R5,MOVEMEMB         MOVE MEMBER NAME TO FIND LIST
         B     OPENOUT
         SPACE
NOPARM   LA    R1,EMSG2            SAY BAD PARM
         BAL   LINKREG,PUTMSG       GO PUT OUT MESSAGE
         B     EXIT
MOVEMEMB MVC   NAME1(0),2(R3)    EXECUTED INSTRUCTION
         SPACE
**********************************************************************
*  EXTRACTED MEMBER .. NOW OPEN OUTPUT AND BEGIN SHOVEL              *
**********************************************************************
OPENOUT  OPEN  (OUT,(OUTPUT))
         TM    OUT+48,X'10'        DID IT OPEN?
         BNZ   SRCH                YES...GO SHOVEL THE JCL
         LA    R1,EMSG4            NO..GRIPE
         BAL   LINKREG,PUTMSG       GO PUT OUT MESSAGE
         B     EXIT
         SPACE 1
SRCH     MVC   ACTION,=F'0'        INIT THE STAGE
         MVC   MODE,=CL5'PSRCH'
         L     R15,=V(PSRCH)
         LA    R1,@PSRCH
         BALR  R14,R15             GO SEE IF THE MEMBER IS THERE
         CLC   ACTION,=F'0'        ANY PROBLEMS?
         BE    READ                NOPE..GO DOIT
         SPACE 1
         CLC   ACTION,=F'23'       MEMBER NOT FOUND?
         BNE   ERROR               NOPE..SOMETHING ELSE..GO REPORT
         MVC   E3M3MB,NAME1        NO MEMBER..MOVE NAME TO MSG
         LA    R1,EMSG3
         BAL   LINKREG,PUTMSG      GO GROUSE
         B     EXIT
         EJECT
*.....................................................................*
*  READ THE MEMBER INTO THE BUFFER                                    *
*.....................................................................*
         SPACE
READ     EQU   *
         XC    ACTION,ACTION
* ****   MVC   INCLUDES,NOENTRY             DO NOT PROCESS INCLUDES
         MVC   COMMENT,NOENTRY              DO NOT PROCESS COMMENTS
         MVC   MODE,=CL5'READ'
READAGN  L     R15,=V(PREAD)
         LA    R1,@PREAD
         BALR  R14,R15             GO READ A RECORD VIA PAM
         SPACE 1
         OC    ACTION,ACTION
         BNZ   ERROR               RETURN CODE SHOULD BE ZERO
READCONT CLC   RECORD(2),=C'$*'    TEST FOR END OF DATA
         BE    EOJ00               YEP..NORMAL FINISH
         SPACE 1
*PROCESS A RECORD
         PUT   OUT,RECORD          PUT OUT THE RECORD
         B     READAGN             AND GO GET ANOTHER
         SPACE 2
ERROR    CLC   ACTION,=F'4'        IS IT A ++INCLUDE FAILURE?       NTL
         BNE   ERROR1                                               NTL
         CLC   MODE,=CL5'READ'     FOR A READ?                      NTL
         BNE   ERROR1                                               NTL
         MVC   E5INCMEM,RECORD+7   MOVE IN OFFENDING ++INCLUDE      NTL
         MVC   E5MEMBER,DNAME      MOVE IN MEMBER NAME              NTL
         LA    R1,EMSG5                                             NTL
         BAL   LINKREG,PUTMSG                                       NTL
         CLC   INCLUDES,=CL8'NO-ENTRY' AND NO EXPAND WANTED?        NTL
         BNE   ERROR1                                               NTL
         LA    R1,EMSG6                                             NTL
         BAL   LINKREG,PUTMSG                                       NTL
         B     READCONT             NO PROBLEM......                NTL
ERROR1   L     R15,ACTION          GET ACTION RETURN CODE
         CVD   R15,TEMP8           CONVERT TO DECIMAL
         MVC   ACODE,=XL6'402020202120'
         ED    ACODE,TEMP8+5
         LA    R1,EMSGGEN
         BAL   LINKREG,PUTMSG
         SPACE 2
*
* END OF JOB RTN
*
EOJ00    EQU   *
*
         CLOSE (OUT)
EXIT     EQU   *
         BAL   R10,GOCLOSE         CLOSE THE PV LIBRARY
         L     R13,SAVEA+4         ADDR PREV SAVEA
         MVC   16(4,R13),ACTION    SET RETURN CODE
         LM    R14,R12,12(R13)     RESTORE REGS
         BR    R14                 RETURN
         EJECT
*.....................................................................*
* OPEN  PANVALET PROCESSING FOR THIS DDN                              *
*.....................................................................*
         SPACE
GOOPEN   XC    ACTION,ACTION
         MVC   MODE,=CL5'OPEN'
         L     R15,=V(POPEN)
         LA    R1,@POPEN
         BALR  R14,R15               INVOKE OPEN PANVALET FILE
         SPACE 1
         CLC   ACTION,=F'0'
         BNE   ERROR               OPEN ERROR
         MVI   OPENFLAG,1          SAY ITS OPEN
         BR    R10
         SPACE 2
*.....................................................................*
* CLOSE PANVALET PROCESSING FOR THIS DDN                              *
*.....................................................................*
         SPACE
GOCLOSE  EQU   *
         MVC   OLDACTN,ACTION      SAVE PREVIOUS ACTION CODE
         XC    ACTION,ACTION       CLEAR ACTION WORD
         MVC   MODE,=CL5'CLOSE'
         L     R15,=V(PCLOSE)
         LA    R1,@PCLOSE
         BALR  R14,R15             GO CLOSE OUT PANVALET FILE
**
*  NOTE  UPON RETURN FROM PCLOSE ACTION WILL CONTAIN STANDARD
*        PAN#1 RETURN CODES 0-(SUCCESSFUL),4-(++INCLUDE NOT FOUND)
*        8-(ANY UNSUCCESSFUL ACTION), 16-(HARDWARE ERROR)
**
         MVI   OPENFLAG,0          INDICATE FILE CLOSED
         MVC   ACTION,OLDACTN      RESTORE ACTION CODE
         BR    R10
         EJECT
*.....................................................................*
*        LOCAL SUBROUTINE FOR IO TO OPER/USER                         *
*.....................................................................*
         SPACE
*
* ENTER WITH WTO,WTOR LIST FORM POINTED TO BY R1
*
*    BAL  LINKREG,PUTMSG    OR
*    BAL  LINKREG,PUTGET
*
*  USES RXX FOR WORK
*
*
PUTMSG   DS    0H
         TM    TSOFLAG,X'80'       IS IT TSO?
         BO    TPUTIT              YES-> GO DO TPUT
         SVC   35                  NO-> USE WTO
         BR    LINKREG
TPUTIT   DS    0H
         LH    R0,0(,R1)           GET LENGTH OF WTO MESSAGE
         S     R0,=F'4'            SUBTRACT OFF HEADER
         LA    R1,4(,R1)           BUMP MSG ADDRESS PAST HDR
         TPUT  (1),(0),R           DO THE TPUT
         BR    LINKREG
         SPACE 2
PUTGET   DS    0H
         TM    TSOFLAG,X'80'       IS IT TSO?
         BO    TSOIT               YES-> GO USE TGET
         LR    RXX,R1              NO-> USE WTOR .. SAVE MSG ADDRESS
         SVC   35                  DO THE WTOR...
         L     R1,4(,RXX)          GET ADDR OF ECB
         XC    0(4,R1),0(R1)       CLEAN OUT THE ECB....
         LA    R0,1
         SVC   1                   WAIT FOR WTOR TO COMPLETE
         B     PUTRET              RETURN
TSOIT    DS    0H
         LR    RXX,R1              SAVE
         LH    R0,8(,R1)           PUT LENGTH
         S     R0,=F'4'
         LA    R1,12(,R1)          PAST HDR
         TPUT  (1),(0),R
         SR    R0,R0
         IC    R0,0(,RXX)          GET REPLY LENGTH
         L     R1,0(,RXX)          GET REPLY ADRS
         LA    R1,0(,R1)
         ICM   R1,B'1000',=X'80'   INDICATE TGET...
         TGET  (1),(0),R
PUTRET   DS    0H
         L     R1,0(,RXX)          GET REPLY ADRS
         XR    R0,R0
         IC    R0,0(,RXX)          GET REPLY LENGTH
UPPER    DS    0H
         OI    0(R1),C' '          UPPER CASE
         LA    R1,1(,R1)           NXT
         BCT   R0,UPPER
         BR    LINKREG
         SPACE 2
*
* OPEN EXIT TO MAKE SURE THAT A BLOCK SIZE IS GIVEN BY SOMEONE
*    FOR OUT
*
         USING BLKEXIT,R15
BLKEXIT  CLC   62(2,R1),=H'0'       ANY BLOCK SIZE SPECIFIED
         BNER  R14                  YES-> USE IT
         MVC   62(2,R1),=H'6160'    NO-> USE BIG BLOCKS
         BR    R14                  AND RETURN
         EJECT
*.....................................................................*
*        MESSAGES                                                     *
*.....................................................................*
         SPACE
EMSG1    WTO   'PVFETCH - PANDD1 DD MISSING-NO PVLIB TO READ',         X
               ROUTCDE=(11),MF=L
         SPACE
EMSG2    WTO   'PVFETCH - MEMBER NAME IN PARM FIELD IS <=0 OR >10 CHARSX
               LONG..IGNORED',ROUTCDE=(11),MF=L
         SPACE
EMSG3    WTO   'PVFETCH - MEMBER ''XXXXXXXX'' NOT FOUND ',             X
               ROUTCDE=(11),MF=L
         SPACE
E3M3MB   EQU   EMSG3+22,8
EMSG4    WTO   'PVFETCH - DDNAME ''OUT'' FAILED TO OPEN',              X
               ROUTCDE=(11),MF=L
         SPACE
EMSGGEN  WTO   'PVFETCH - ACTION ERROR XXXXX DURING XXXXX - TERMINATINGX
                ',ROUTCDE=(11),MF=L
ACODE    EQU   EMSGGEN+26,6
MODE     EQU   EMSGGEN+40,5
         EJECT ,                                                    NTL
EMSG5    WTO   'PVFETCH - A XXXXXXXXXXXXXXXXXXXXXX WAS FOUND IN XXXXXXX*
               XXX AND MEMBER NOT IN PVLIB-',ROUTCDE=(11),MF=L      NTL
E5INCMEM EQU   EMSG5+16,22                                          NTL
E5MEMBER EQU   EMSG5+52,10                                          NTL
         SPACE 1                                                    NTL
EMSG6    WTO   'PVFETCH - CONTINUING UNLOAD OPERATION SINCE ++INCLUDE EX
               XPANSION WAS NOT REQUESTED',ROUTCDE=(11),MF=L        NTL
         EJECT
*.....................................................................*
*  LIST FORM OF MACROS                                                *
*.....................................................................*
         SPACE
EXTRACT  EXTRACT ANSWER,'S',FIELDS=(TSO),MF=L
         SPACE
@POPEN   CALL  ,(ACTION,DDNAME,BACKUP),VL,MF=L
         SPACE
@PSRCH   CALL ,(ACTION,DIRENTRY,NAME1,NAME2,COMMENT,SUBSET),VL,MF=L
         SPACE
@PREAD   CALL  ,(ACTION,RECORD,NAME1,INCLUDES,COMMENT),VL,MF=L
         SPACE
@PCLOSE  CALL  ,(ACTION),VL,MF=L
         EJECT
*.....................................................................*
*  CONSTRUCTS FOR PANVALET                                            *
*.....................................................................*
         SPACE
ACTION   DC    F'0'         RETURN CODE FROM PAN, SHOULD BE SET TO ZERO
OLDACTN  DC    F'0'   SAVED RETURN CODE FROM PAN, SHOULD BE SET TO ZERO
DDNAME   DC    CL8'PANDD1'       DDNAME TO READ FROM
*  POPEN PARAMETERS
BACKUP   DC    CL8'NO-ENTRY'     MUST BE CL8'BACKUP' IF PROTECTION FILE
         SPACE
*  PSRCH PARAMETERS
DIRENTRY DS    0CL80               DIR ENTRY RETURNED IN 0-UP FORMAT
DNAME    DS    CL10                NAME LEFT JUSTIFIED
DLEVEL   DS    CL3                 LEVEL NUMBER
DUSER    DS    CL4                 USER CODE
DSECURE  DS    CL1                 SECURITY CODE
DTYPE    DS    CL5                 LANGUAGE TYPE
DSTATUS  DS    0CL3                3 CHAR STATUS
DPRODT   DS    CL1                 P-PROD  T-TEST
DEORDD   DS    CL1                 E-ENABLE  D-DISABLE
DAORDI   DS    CL1                 A-ACTIVE  I-INACTIVE
DDATEM   DS    CL8                 DATE OF LAST MAINTENANCE MM/DD/YY
DDATEA   DS    CL8                 DATE OF LAST ACCESS  MM/DD/YY
DBLOCKS  DS    CL5                 NO. OF BLOCKS
DSTMTS   DS    CL8                 NO. OF STATEMENTS
DLASTACT DS    CL4                 LAST ACTION, 1ST POS. IS * IF PROD
DBYTES   DS    CL2                 NO. BYTES PER STATMENT
DSUBSET  DS    CL4                 NO. OF SUBSETS(SUPERSETS ONLY)
DNAME2   DS    CL10                NAME RIGHT JUSTIFIED
         DS    CL1                 NOT USED
         DS    CL1                 N=NOFORMAT, T=TSO, ELSE BLANK
DINC     DS    CL1                 I=INCLUDE BEING EXPND (EXIT ONLY)
DVERSION DS    XL2                 VER. #(FOR PROTECTION FILES ONLY)
         EJECT
NAME1    DC    CL22'PAN'           SEARCH NAME1
NAME2    DC    CL11'NO-ENTRY'      SEARCH NAME2
COMMENT  DC    CL52'NO-ENTRY'      NO COMMENTS WANTED, ELSE='COMMENT'
**
* NOTE  COMMENTS WILL BE PROCEDED AND ENDED WITH AN *
**
SUBSET   DC    CL27'NO-ENTRY'      NO SUBSETS WANTED, ELSE='SUBSET'
**
*  NOTE  IF SUBSETS ARE REQUESTED THE FOLLOWING WILL BE A DEFINITION OF
*        THE SUBSET DIRECTORY ENTRY
**
SUBENTRY DS    0CL27
SUBNAME  DS    CL11                FORMAT .XXXXXXXXXX X'S = SUBSET NME
         DS    CL1                 BLANK
SUBDATE  DS    CL8                 DATE ATTACHED IN FORM MM/DD/YY
         DS    CL2                 BLANKS
SUBSTMT  DS    CL5                 # OF STMTS IN SUBSET
         SPACE 2
*  PREAD PARAMETERS
RECORD   DC    CL80' '             STATMENT WILL BE RETURNED HERE
INCLUDES DC    CL8'YES'            ='YES' OR 'NO-ENTRY'
**
* SPECIAL RETURN CODES FROM EACH CALL TO PAM WILL BE RETNED IN ACTION
*   2    NAME NOT ALPHANUMERIC (SAME AS PV002)
*   4    ++INCLUDE NOT FOUND (PREAD ONLY)
*   7    PARAMETER TO LONG (SAME AS PV007)
*   23   NAME NOT FOUND (SAME AS PV023)
*   245  PARAMETER LIST ERROR IN CALL TO PAM
**
         EJECT
*.....................................................................*
*  MISC VARIABLES                                                     *
*.....................................................................*
         SPACE
NOENTRY  DC    CL92'NO-ENTRY'            NO-ENTRY FOR PANVALET
BLANKS   EQU   NOENTRY+8,80              BLANKS FOR US
TEMP8    DS    D
ANSWER   DC    F'0'                ADDRESS POINTER FOR EXTRACT
TSOFLAG  DC    X'00'               ='80' IF TSO. '00' IF NOT TSO
OPENFLAG DC    X'00'             LIBRARY STATUS FLAG =X'00' IF NOT OPEN
MODESW   DC    X'00'             FAIRCHILD MODE SWITCHES
DIRMODE  EQU   X'80'               DIR LIST MODE
SKIP     EQU   X'01'             LEADING ZERO SUPPRESS ROUTINE FLAGS
NOSKIP   EQU   255-SKIP
         SPACE 2
*.....................................................................*
*    LITERAL POOL                                                     *
*.....................................................................*
         SPACE
         LTORG
         SPACE 2
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         EJECT
*
* OUTPUT TO INTERNAL READER.
*
OUT      DCB   DDNAME=OUT,DSORG=PS,MACRF=(PM),LRECL=80,RECFM=FB,       X
               EXLST=EXLSTB
         SPACE 3
EXLSTB   DC    0F'0',X'85',AL3(BLKEXIT)
         SPACE 3
         END   PVFETCH
