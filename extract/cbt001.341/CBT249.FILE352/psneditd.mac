*          DATA SET PSNEDITD   AT LEVEL 003 AS OF 10/31/80
         TITLE 'PSNEDITD - EDIT A SINGLE DD STATEMENT'
PSNEDITD PSNMSTRT  SASIZ=WKLENG
         EJECT
*                    APPLY LOCAL STANDARDS AND PREJUDICES
*                    AS THEY CONCERN HOW A DD CARD SHOULD BE
*                    BUILT - ONCE IT WORKS, THAT IS.
*
         USING DDBLOCK,R9         PASSED FROM THE DD REFORMATTER
         USING DDBDDPTR,R7        RETURNED BY PSNMFDD
         L     R9,PSWDDLST        DDBLOCK POINTER
*
         BAL   R14,COLUMNS        BE SURE THE COLUMNS ARE RIGHT
*
         BAL   R14,COMMENTS       CHECK FOR NONSTANDARD COMMENT LINE
*
         BAL   R14,EDITDSN        TRY ENFORCING DSNAME-ING STANDARDS
*
         BAL   R14,CKDDNAME       LOOK FOR SPECIAL DDNAMES
*
         BAL   R14,DISPOSIT       DISPOSITION IS FERTILE GROUND
*
         BAL   R14,NEWCREAT       EXAMINE NEWLY CREATED ONES
*
*        BAL   R14,MASSTORE       SPECIAL CHECKIN FOR MSSSSSSS      NTL
*
         BAL   R14,UNITEDIT       CHECK HOW HE DEFINES A UNIT, TOO
*
         BAL   R14,RLSROUND       LOOK AT RLSE AND ROUND USAGE
*
         B     RETURN
         EJECT
*                    TO CHECK THE DS NAME FOR PROPER
*                    STRUCTURE
*
EDITDSN  PSNMBLOK  EDN,RENT=YES
         PSNMFDD  EDSN,EDNEXIT    FIND THE DSN POINTER
         L     R6,DDBPSUBS        POINT TO THE DSNAME
         CLI   0(R6),C'&&'        IS IT A TEMPORARY
         BE    EDNEXIT            YES - NOT INTERESTED
         CLC   0(4,R6),=C'PROD'   IS IT ONE OF OURS                 NTL
         BE    EDNPRD             YES                               NTL
         CLC   0(4,R6),=C'TEMP'   IS IT ONE OF OURS                 NTL
         BE    EDNPRD             YES                               NTL
         CLC   0(3,R6),=C'SYS'    ONE OF THEIRS
         BE    EDNSYS
*        CLC   0(5,R6),=C'IMSP.'  IMS PRODUCTION                    NTL
*        BE    EDNPROD            YES - DIRECTLY TO PRODUCTION AUDI NTL
*        CLC   0(8,R6),=C'IMSPROD.'  ONLINE DATABASE                NTL
*        BE    EDNEXIT            YES - RULE UNTO SELF              NTL
*        CLC   0(6,R6),=C'VSAM1.'  NON-IMS VSAM                     NTL
*        BE    EDNEXIT            YES - CHICKEN-OUT                 NTL
         CLC   0(6,R6),=C'INTRO.'  THE SPECIAL INTRO FORM
         BE    EDNINTRO           YES - ALLOW LOAD AND CTL
*        CLC   0(4,R6),=C'VCI.'    SPECIAL VALULIB PREFIX....       NTL
*        BE    EDNEXIT            YES - CHICKEN-OUT                 NTL
*                    THERE IS ONLY US AND THEM
EDNBAD   EQU   *
         PSNMERR ERRDSN1
EDNBAD1  L     R1,DDBPSUBS        POINTER AND LENGTH OF DSN
         PSNMERR ERRDSN2,((R1))
         B     EDNEXIT
EDNDSNAM DC    C'DATASET NAME'
*
*                                                                   NTL
*  DSNAMES  PROD.SSS.XXXXXXXX.TNNN.YYYYYYY                          NTL
*                                                                   NTL
EDNPRD   EQU   *                                                    NTL
*        TM    DDBIND1,DDBI1GDG   IS THIS FCI2 A GDG                NTL
*        BO    EDNBAD             YES - NOT ALLOWED                 NTL
EDNPRD1  EQU   *                                                    NTL
         CLI   4(R6),C'.'         CHECK FOR END OF NODE 'PROD.'     NTL
         BNE   EDNBAD
         CLC   0(11,R6),=C'PROD.NTL.' SPECIAL LIBRARIES?            NTL
         BNE   EDNPROD            NO..KEEP ON TRUCKIN..             NTL
         TM    DDBEDISP,DDBEDSHR  SHARE IS THE ONLY DISP            NTL
         BNO   EDNBAD                                               NTL
         CLC   9(7,R6),=C'PARMLIB'  IS IT THE PARMLIB?              NTL
         BE    EDNCTLCD           YES..CHK OUT MEMBER NAME          NTL
         B     EDNEXIT            PASS ON THIS ONE
*
EDNPROD  EQU   *                  KNOWN TO BE PROD FROM SEV SOURCES
         CLC   5(3,R6),PSWPNAME   CHK FOR SSS IN PROD.SSS           NTL
         BNE   EDNOTHER           CHK FOR NOT OURS                  NTL
         CLI   8(R6),C'.'         END OF THE LINE?                  NTL
         BE    EDNEXIT            YES..HE PASSES                    NTL
EDNOTHER CLI   8(R6),C'.'         DOES IT LOOK LIKE ANOTHERS SYSTEMSNTL
         BNE   EDNBAD             NO..JUST GROUSE                   NTL
         PSNMERR ERRDSN3          WRITE NOT OWNED BY SYSTEM MSG     NTL
         B     EDNBAD1            AND GO POST DSNAME IN MSG         NTL
         AGO   .SKIPIT1                                             NTL
         LM    R15,R1,EDNOWNER    CODES OF WHO OWNS THE DATA
EDNOWNL  EQU   *
         CLC   5(1,R6),0(R15)     IS OWNERSHIP CODE VALID
         BE    EDNOWNH            YES
         BXLE  R15,R0,EDNOWNL
         B     EDNBAD             TERRIBLE - UNOWNED DATA
EDNOWNH  EQU   *
         CLC   6(2,R6),PSWCZERO
         BNE   EDNBAD
         L     R15,PSWTRNUM       CHECK THAT SSI IS NUMERIC
         TRT   8(3,R6),0(R15)
         BNZ   EDNBAD
         CLI   11(R6),C'.'        FOR END OF SECOND NODE
         CLC   7(5,R6),13(R6)     THIRD NODE MATCH SECOND
         BNE   EDNBAD
         LM    R15,R1,EDNTYPE     BE SURE DSNTYPE IS CORRECT
EDNTYNX  EQU   *
         CLC   0(1,R15),18(R6)
         BE    EDNTYHIT           THIS IS PROPER
         BXLE  R15,R0,EDNTYNX
         B     EDNBAD
EDNTYHIT EQU   *
         TM    DDBEDISP,DDBEDOLD+DDBEDSHR DOES IT ALREADY EXIST
         BNZ   EDNEXIT            YES - END OF EDIT
         CLC   8(3,R6),PSWPNAME+1 NEW ONE - MUST MATCH PROCNAME
         BNE   EDNBAD
.SKIPIT1 ANOP  ,                                                    NTL
*
         B     EDNEXIT                                              NTL
*
*                    SPECIAL ENTRY FOR INTRO DATASETS
EDNINTRO EQU   *
         CLC   DDBDDNAM,CDNSLIB   STEPLIB IS ALREADY CHECKED
         BE    EDNEXIT
         CLC   6(7,R6),=C'PARMLIB' THE SPECIAL ONE ALLOWED          NTL
         BE    EDNCTLCD           YES - EDIT THE CTL CARD
         CLC   6(7,R6),=C'LOADLIB' LOADLIB IS OK...                 NTL
         BE    EDNEXIT                                              NTL
         B     EDNBAD             NOPE - CHECK IT
*
*
EDNSYS   EQU   *
         B     EDNEXIT            NO EDIT FOR SYS - YET
*
*
EDNCTLCD EQU   *                  HAVE A CTLCARDS ENTRY
*                                                                   NTL
* CTLCARDS FORMAT    SSSUXTXX                                       NTL
*                       -                                           NTL
         TM    DDBIND1,DDBI1MBR   DOES A MEMBER NAME EXIST
         BZ    EDNBAD             NO - THIS IS REALLY BAD
         CLC   DDBPMBR(3),PSWPNAME MBR NAME MATCH PROC ON SSA       NTL
         BNE   EDNBADMB
         CLI   DDBPMBR+3,C'U'     REQUIRED NAMING CONVENTIONS       NTL
         BNE   EDNBADMB                                             NTL
         LM    R15,R1,EDNCTLTY    TYPES OF CONTROL CARDS
EDNCLOOP EQU   *
         CLC   0(1,R15),DDBPMBR+5
         BE    EDNCHIT
         BXLE  R15,R0,EDNCLOOP
         B     EDNBADMB
EDNCHIT  EQU   *
*        L     R15,PSWTRNUM       CHECK FOR NUMERIC SUFFIX          NTL
*        TRT   DDBPMBR+6(2),0(R15)                                  NTL
*        BNZ   EDNBADMB                                             NTL
*
         B     EDNEXIT
*
EDNBADMB EQU   *
         PSNMERR   ERRSTDFB,DDBPMBR,EDNMBRNM
         B     EDNEXIT
EDNMBRNM DC    C'MEMBER NAME'
EDNCTLTY DC    A(*+12,1,EDNCL),C'STDCU'    CTL CARD TYPES           NTL
EDNCL    EQU   *-1
*
*                    AND FOR THE DSNAME TYPES
EDNTYPE  DC    A(*+12,1,EDNTL),C'LXMF'       FOURTH NODE OF DSN
EDNTL    EQU   *-1
*
*                    AND FOR THE OWNERSHIP CODE
EDNOWNER DC    A(*+12,1,EDNOL),C'AEHJKPSW'    SECOND NODE OF DSN
EDNOL    EQU   *-1
         LTORG
         EJECT
*                    CHECK FOR AN EMBEDDED COMMENT LINE WITHIN
*                    THE BODY OF THE STEP - ALL COMMENTS ARE RESTRICTED
*                    TO APPEAR WITHIN THE COMMENT BOX
*
COMMENTS PSNMBLOK  CMTS
         TM    PSWMFLG1,PSWM1NCM  COMMENT CHECK WANTED?             NTL
         BO    CMTSEXIT           NO..SO SPLIT                      NTL
         L     R1,DDBCOMNT        PICK UP THE COMMENT LINE POINTER
*                   NOTE THIS IS NOT THE COMMENT ON THE SAME
*                   LINE AS THE KEYWORD - THIS IS ON THE
*                   PREVIOUS LINE
         LTR   R1,R1              DOES IT EXIST
         BZ    CMTSEXIT           NO COMMENT ON THIS LINE
         PSNMERR  ERRCOMBK        ALL COMMENTS MUST BE IN BLOCK
         B     CMTSEXIT
         EJECT
*                    TO BE CERTAIN THE COLUMNAR ALIGNMENT
*                    IS ALL THAT IS SHOULD BE FOR SO IMPORTANT
*                    A DOCUMENT AS IS THIS
*
COLUMNS  PSNMBLOK  COL
         TM    PSWMFLG1,PSWM1NCL  NO COLUMN CHK WANTED              NTL
         BO    COLEXIT            NO THANKS..                       NTL
         LA    R1,11              THE COLUMN FOR THE LITERAL DD
         CH    R1,PSWCOLTP        DO THEY MATCH
         BE    COLDDOK
         LH    R1,PSWCOLTP
         LA    R2,COLDD
         BAL   R14,COLERROR
COLDDOK  EQU   *
*                    CHECK EACH OF THE KEYWORDS
         LH    R6,DDBNENT         NUMBER OF ENTRIES IN THIS DD
         LTR   R6,R6
         BNP   COLEXIT            OH, DEAR
         LA    R7,DDBBLENG(,R9)   POINT TO START OF THEM
         SR    R5,R5              CLEAR THE PRIOR ENTRY POINTER
COLLOOP  EQU   *
         LA    R1,15              NORMAL COLUMN NUMBER TO START WITH
         CH    R1,DDBPCOL
         BE    COLLNEXT           THIS COLUMN LOOKS FINE
*                    NOT IN LINE - WAS THERE A PREVIOUS
         BL    COLLDBL            CHECK FOR PROPER DOUBLING UP
*                    THE ENTRY STARTS BEFORE COLUMN 16 - NEVER...
COLLMISS EQU   *
         LH    R1,DDBPNBR         THE NUMBER OF THIS KEYWORD
         PSNMFKEY  (R1)
         LA    R2,0(,R8)          POINT TO THE KEYWORD
         LH    R1,DDBPCOL         THE COLUMN NUMBER
         BAL   R14,COLERROR       COMPLAIN ABOUT THIS INDENTION
         B     COLLNEXT
COLLDBL  EQU   *
         LTR   R5,R5              WAS THERE A PREVIOUS ONE
         BZ    COLLMISS
         CLC   DDBPCARD,DDBPCARD-DDBDDPTR(R5)  SAME CARD
         BNE   COLLMISS           NO - THIS IS WRONG ANYWAY
         BAL   R14,COLSAME        ALLOW THESE ON SAME CARD
         LTR   R15,R15
         BNZ   COLLMISS           NO - FLAG THIS ONE
COLLNEXT EQU   *
         LR    R5,R7              SAVE THE PREVIOUS POINTER
         LA    R7,DDBDDPLN(,R7)   ON TO THE NEXT ENTRY
         BCT   R6,COLLOOP         AND CHECK THEM ALL OUT
         B     COLEXIT
*
COLDD    DC    CL8'DD'            LITERAL IN ERROR INTERFACE
COLERROR PSNMBLOK  CERR
         LA    R1,1(,R1)          MAKE IT THE REAL NUMBER
         CVD   R1,WKDOUBL         MAKE THE COLUMN NUMBER READABLE
         MVC   WKMASK,COLMASK
         ED    WKMASK,WKDOUBL+6   PUT IN THE COLUMN NUMBER
         PSNMERR  ERRMISAL,((R2),8),WKMASK
         B     CERREXIT
COLMASK  DC    X'40202120'        MASK FOR EDITING COLUMN NUMBERS
         EJECT
*                    TO MATCH UP KEYWORDS - CAN THESE TWO COEXIST ON
*                    ONE CARD
*
COLSAME  PSNMBLOK  CSAM
         L     R15,PSWDDTAB       ADDRESS THE DD-TABLE
         BALR  R14,R15            AND ASK FOR THE POINTER LIST OF IT
         L     R15,12(,R15)       PICK UP THE OTHER DATA POINTER
         LM    R15,R1,0(R15)      ADDRESS THE EXCEPTION LIST OF 2CARD
CSAMLOOP EQU   *                  SEARCH THE EXCEPTION LIST
         CLC   DDBPNBR-DDBDDPTR(2,R5),4(R15) DO THE FIELDS MATCH
         BNE   CSAMNEXT           NO - ON TO NEXT POSSIBILITY
         CLC   DDBPNBR,6(R15)     HOW ABOUT SECOND FLD ON LINE
         BNE   CSAMNEXT           NO MATCH - CONTINUE
         L     R1,0(,R15)         PICK UP POINTER TO ROUTINE
         LA    R0,CSAMRTN         NUMBER OF ROUTINES ALLOWED
         CR    R1,R0              DOES THIS FIT
         BH    CSAMBAD            OOPS - INVALID ROUTINE ID
         SLL   R1,2               MAKE INTO A BRANCH INDEX
         B     *+4(R1)            ON TO THE PROPER ROUTINE
CSAMEDTS EQU   *
         B     CSAMOK             THIS IS SUFFICIENT
         B     CSAMONLY           IF THESE ARE THE ONLY TWO
CSAMRTN  EQU   (*-CSAMEDTS-4)/4   NUMBER OF ROUTINES
         EX    R0,*               ALWAYS AT END OF BRANCH TABLE
*
CSAMNEXT EQU   *                  ON TO THE NEXT ENTRY
         BXLE  R15,R0,CSAMLOOP    UNTIL THEY ARE ALL DONE
         LA    R15,4              NOT IN LIST - DONT GO TOGETHER
         B     CSAMEXIT
*
CSAMOK   EQU   *                  IF THE COMBINATION IS ALWAYS PROPER
         SR    R15,R15
         B     CSAMEXIT
*
*                    IF THESE TWO MUST BE THE ONLY TWO
CSAMONLY EQU   *
         SR    R15,R15            PRESET GOOD RETURN CODE
         LA    R1,2               NUMBER OF ENTRIES ALLOWED ALTOGETHER
         CH    R1,DDBNENT         NUMBER OF ENTRIES THERE ARE
         BE    CSAMEXIT           LOOKS GOOD FROM HERE
         LA    R15,4              OOPS - BAD COMBINATION
         B     CSAMEXIT           AND EXIT FOR THE ERROR
*
*                    AND IF THE ROUTINE-ID IS INVALID
CSAMBAD  EQU   *
         PSNMERR  ERRTBLSY,CSAMDDTB
         LA    R15,4              PRETEND ITS JUST AN ERROR
         B     CSAMEXIT
CSAMDDTB DC    C'DD-TABLE COEXIST LIST'
         EJECT
*                   LOOK FOR NONSTANDARD DDNAMES ON THE LIST
*
CKDDNAME PSNMBLOK  CDN
         LM    R15,R1,CDNNAMES
CDNLOOP  EQU   *
         CLC   DDBDDNAM,0(R15)    IS THIS ONE OF THEM
         BE    CDNHIT             YES
         BXLE  R15,R0,CDNLOOP
         B     CDNEXIT
CDNHIT   EQU   *
         CLC   DDBDDNAM,CDNSLIB   IS THIS FOR STEPLIB
         BE    CDNCKINT           CHECK FOR INTROLIB
CDNBAD   EQU   *                  WELL, IT WASN'T
         OI    DDBIND1,DDBI1DDO   DD SHOULD BE OUT.....
         PSNMERR  ERRSTDFB,DDBDDNAM,CDNLIT
         B     CDNEXIT
*
CDNCKINT EQU   *
         PSNMFDD  EDSN,CDNBAD
         L     R6,DDBPSUBS
         CLC   CDNINTLB,0(R6)     INTRO.LOAD IS ALLOWED
         BNE   CDNBAD             BUT NONE OTHER
         B     CDNEXIT
CDNINTLB DC    C'INTRO.LINKLIB'                                     NTL
CDNLIT   DC    C'DDNAME'
CDNNAMES DC    A(*+12,8,CDNLAST)
CDNSLIB  DC    CL8'STEPLIB'
         DC    CL8'SYSABEND'
         DC    CL8'SYSMDUMP'
         DC    CL8'ABNLDUMP'                                        NTL
         DC    CL8'SORTLIB'     THIS IS JUST NOT NEEDED
CDNLAST  DC    CL8'MFUTIL'
         EJECT
*                   TO CHECK THE DISPOSITION OF THIS DATASET
*                   (GROUCHY IN MORNING......)
*
DISPOSIT PSNMBLOK  DIS
         CLI   DDBEDISP,X'00'     ANY DISPOSITION AT ALL
         BE    DISEXIT            NO - DEFAULT TO NEW DELETE.....
         TM    DDBEDISP,DDBEDMOD+DDBEDDEL  MOD,DEL                  NTL
         BNO   DISPOT1                                              NTL
         TM    DDBEDIS3,DDBE3DEL  MOD,DEL,DEL ?                     NTL
         BNO   DISPOT1                                              NTL
*        CLC   PGMNAME,=C'IEFBR14'  IEFBR14?                        NTL
*        BNE   DISPOT1            NOPE...NOGOOD                     NTL
         B     DISONEOK                                             NTL
*                                                                   NTL
DISPOT1  TM    DDBEDISP,DDBEDMOD+DDBEDPAS MOD OR PASS CODED
         BNZ   DISBAD             HOW TERRIBLE
         TM    DDBEDISP,DDBEDOLD+DDBEDSHR CAN IT HAVE SINGLE
         BNZ   DISONEOK           YES - NO NEED FOR NEXT TWO
         TM    DDBEDISP,DDBEDKEP+DDBEDDEL+DDBEDCAT+DDBEDUNC
         BZ    DISBAD             NEED SOME SECOND DISPOSITION
         TM    DDBEDIS3,DDBE3DEL+DDBE3KEP ONLY THIRD ONES
         BNZ   DISCKNEW           WELL, IF HE SAID SOMETHING
         CLC   DDBDDNAM,=CL8'IEFRDER' SPECIAL CHECK FOR IMS LOG
         BE    DIS3CAT            THIS ONE CAN BE CAT IN THIRD
         CLC   DDBDDNAM,=CL8'FOLP'  SPECIAL FOR THE OLD REPORT ROUTER
         BE    DIS3CAT            AS CAN THIS ONE
         B     DISBAD             BUT NO OTHER ONE CAN
DIS3CAT  EQU   *                  IF DISP3 IS CAT, THIS IS PROPER
         TM    DDBEDIS3,DDBE3CAT  IMS LOG CAN CATALOG AT ABEND
         BO    DISEXIT            ALL IS IN ORDER
         B     DISBAD             BUT HE DID NOT....
*
DISCKNEW EQU   *                  CHECK FOR A NEW DISPOSITION
         TM    DDBEDISP,DDBEDCAT  USUALLY YOU CATALOG THE NEW ONES
         BO    DISEXIT            AS HE DID HERE
         TM    DDBEDISP,DDBEDKEP  IS THIS FOR A KEEPER
         BZ    DISKEEPX           BIF NO KEEP
*                    NEW,KEEP IS ALLOWED IF MSVGP=XMIT
         PSNMFDD EMSVGP,DISBAD    FIND MSVGP-IF NONE, IS BAD
         L     R6,DDBPSUBS        PICK UP POINTER TO VALUE
         CLC   DISXMIT,0(R6)      ID IT A MATCH
         BNE   DISBAD             NO MATCH - ERROR
DISKEEPX EQU   *
         B     DISBAD             ANY OTHER OPTION IS EVEN WORSE
DISONEOK EQU   *
         B     DISEXIT
*
DISBAD   EQU   *
         PSNMFDD  EDISP,DISEXIT    EXIT IF NONE CODED
         L     R1,DDBPSUBS        POINT TO BAD DATA
         PSNMERR  ERRSTDFB,((R1)),DISDISP
         B     DISEXIT
DISDISP  DC    C'DISPOSITION'
DISXMIT  DC    C'XMIT'            MSVGP ALLOWED WITH NEW,KEEP
         EJECT
*                    LOOK AT A NEWLY CREATED DSN WITH EMPHASIS
*                    ON ITS DISPOSITION AND SPACE (AND DCB)
*
NEWCREAT PSNMBLOK  NCR
         TM    DDBEDISP,DDBEDOLD+DDBEDSHR DOES IT EXIST
         BNZ   NCREXIT            YES - CREATION DOES NOT APPLY
*                    CHECK FOR DISK ALLOCATION
         TM    DDBUNIT,DDBUSYSD+DDBUSDAV  CHECK FOR DISK
         BZ    NCREXIT            NOT DISK - SKIP SPACE EDITTING
*                    CHECK SPACE ALLOCATION
         TM    DDBSPKEY,DDBSPKBK  ALLOCATED IN BLOCKS
         BO    NCRTYPX            YES - THE ORIGINAL GOOD GUY
         TM    DDBEDISP,DDBEDMOD+DDBEDDEL   MOD,DELETE?             NTL
         BO    NCREXIT            YES..LET EM BE                    NTL
*        TM    DDBEDIS3,DDBE3DEL  HOWABOUT MOD,DELETE,DELETE?       NTL
*        BZ    NCRSPBAD           NO WAY.. POINT OUT THE PROBLEM    NTL
         SPACE 1                                                    NTL
*                    ISAM ALLOCATION IS SOMETHING SPECIAL AGAIN
         TM    DDBDCB,DDBDIS      IS DIS ISAM
         BZ    NCRSPBAD           NO - BACK TO BAD GUY
         TM    DDBSPKEY,DDBSPKCY+DDBSPKTK  ALLOWED CYL OR TRK
         BZ    NCRSPBAD           STILL COULDN'T FIT IT IN
NCRTYPX  EQU   *
*
         CLC   DDBSPPRI,PSWHEXZ   IS A PRIMARY GIVEN
         BE    NCRMATSB           NO - SKIP THE 10% RULE
         LA    R1,1               MAXIMUM SECONDARY ALLOWED STANDALONG
         C     R1,DDBSPSEC        MATCH THIS AGAINST THIS SECONDARY
         BNL   NCRMATSB           SMALL ENOUGH - THEREFORE ALLOWED
         L     R1,DDBSPSEC        NOW FOR THEAT SECONDARY VALUE
         MH    R1,NCR10
         C     R1,DDBSPPRI
         BH    NCRSPSEC           SECONDARY OVER 10% OF PRIMARY
NCRMATSB EQU   *                  BLOCKSIZE IN SPACE EQUAL TO DCBBLKSZ
         TM    DDBSPKEY,DDBSPKBK  IS THIS ALLOCATED IN BLOCKS
         BZ    NCRSPBLX           NO - CANNOT MATCH TO BLOCKSIZE, THEN
         LH    R1,DDBBLKSZ        PICK UP THE BLOCKSIZE
         LTR   R1,R1              WAS ONE SPECIFIED
         BZ    NCRSPBLX           NO SPACE-BLOCK EXIT
         CH    R1,DDBSPBLK        DOES BLKSIZE MATCH ALLOCATION SIZE
         BNE   NCRSPBK            SPECIAL ERROR HERE
NCRSPBLX EQU   *
*                    CHECK THE BLOCKSIZE FOR STANDARD SIZES
         LH    R1,DDBBLKSZ        RECAPTURE BLOCKSIZE
         LTR   R1,R1              WAS ONE SPECIFIED
         BZ    NCRBLKX            NO BLOCKSIZE EDIT, THEN
         CH    R1,NCRHIBLK        MATCH AGAINST LARGEST ALLOWED
         BH    NCRBADBK
         CH    R1,NCRLOBLK        AND AGAINST THE ARBITRARY BOTTOM
         BNL   NCRBLKX            THIS IS GOOD ENOUGH
*                    CHECK EXCUSES FOR BAD BLOCKSIZE
         LA    R5,1               HOW ABOUT ONLY ONE BLOCK
         C     R5,DDBSPPRI        .
         BE    NCRBLKX            WELL, THAT WAS IT
*                    ISAM CAN HAVE A SMALLER BLOCKSIZE
         TM    DDBDCB,DDBDIS      IS THIS ISAM
         BO    NCRBLKX            YES - PASS, SEMI-FRIEND
*                    HOW ABOUT A LARGE LRECL
         CLC   DDBLRECL,PSWHEXZ   IF THERE IS AN LRECL, THAT IS
         BE    NCRBADBK           NO - OUT OF EXCUSES, THEN
         LH    R5,NCRHIBLK        THE MAX BLOCKSIZE
         SH    R5,DDBLRECL        LESS THAT LRECL
         CH    R5,DDBBLKSZ        MATCHED TO HIS BLOCKSIZE
         BNL   NCRBADBK           COULD HAVE HAD LARGER BLOCK
NCRBLKX  EQU   *
*
*                    REASSURE THAT FCIMODEL HAS WORK
         TM    DDBIND1,DDBI1GDG   IS THIS A GDG
         BZ    NCRMODLX           NO - FORGET IT
         TM    DDBDCB,DDBDMODL    IS A MODEL DSCB CODED
         BZ    NCRMODLX           WHY I CANNOT SAY
         PSNMFDD  EDCB,NCRMODLX
         L     R1,DDBPSUBS        POINT TO THE REAL DATA STRING
         LA    R2,0(,R1)          USE THIS AS A POINTER
         CLI   0(R2),C'('         DID IT START WITH (
         BNE   *+8                NO - CARRY ON
         LA    R2,1(,R2)          PAST THAT (
         CLC   0(3,R2),=C'GDG'                                      NTL
         BNE   NCRMODLB
NCRMODLX EQU   *
         B     NCREXIT
NCR10    DC    H'10'              TO MATCH SECONDARD TO PRIMARY
NCRLOBLK DC    H'6000'            SHOULD BE ABOVE THIS
NCRHIBLK DC    H'6233'            AND BELOW THIS ONE
*
NCRSPBAD DS    0H
         OI    DDBIND1,DDBI1ESP   SHOW SOME ERROR IN SPACE
         LA    R1,ESPACE      REXIT
         B     NCRERROR
*
NCRERROR EQU   *
         PSNMFDD  (1),NCREXIT
         L     R1,DDBPSUBS        PICK UP THE POINTER TO DATA
         PSNMERR  ERRSTDFB,((R1)),NCRCREAT
         B     NCREXIT
NCRCREAT DC    C'NEW DSN'
*
NCRSPBK  DS    0H
         OI    DDBIND1,DDBI1ESP   SHOW SOME ERROR IN SPACE
         PSNMERR  ERRSPBK
         B     NCREXIT
*
NCRBADBK EQU   *
         PSNMERR  ERRLIMIT,NCRBLOCK
         B     NCREXIT
NCRBLOCK DC    C'BLOCKSIZE'
*
NCRMODLB DS    0H                 MODEL DSCB NAME INCORRECT
         PSNMERR ERRVALUE,((R1),8),NCRDSCBL
         B     NCREXIT
NCRDSCBL DC    C'MODEL DSCB'
*
*                    SECONDARY OVER 10% OF PRIMARY
NCRSPSEC DS    0H
         OI    DDBIND1,DDBI1ESP   SHOW SOME ERROR IN SPACE
         PSNMERR   ERRSPSEC,DDBDDNAM
         B     NCREXIT
         EJECT
*                    SPECIAL RULES COME INTO PLAY FOR THE MSS
*
MASSTORE PSNMBLOK  MSS
         TM    DDBUNIT,DDBUSDAV   IS IT THE SYSDAV....
         BZ    MSSEXIT            NO - SKIP THE WHOLE IDEA
         TM    DDBVOL,DDBVSER     WAS A VOL-SER GIVEN
         BO    MSSERROR
         TM    DDBSPKEY,DDBSPKCT  WAS CONTIG SPECIFIED
         BO    MSSERROR           YES - BAD IDEA
         PSNMFDD  EMSVGP,MSSERROR   FIND THE MDVGP OPERAND
         L     R1,DDBPSUBS        POINT TO NAME OF THE MSVGP
         CLI   0(R1),C'('         DOES IT HAVE OVER 1 PARAM
         BNE   *+8
         LA    R1,1(,R1)          POINT UP TO THE GROUP NAME
         CLI   0(R1),C'D'         IS IT SPECIAL DAILY ONE
         BE    MSSSPECL           YES - SPECIAL CHECKING
         CLI   0(R1),C'W'         OR EVEN A WEEKLY ONE
         BE    MSSSPECL           THAT WAS IT
*                    A NORMAL ONE, THEN
MSSNORML EQU   *
         TM    DDBSPKEY,DDBSPKRL+DDBSPKRD MUST RLSE AND ROUND
         BNO   MSSERROR           AND HE DID NOT.....
         B     MSSEXIT
*
MSSSPECL EQU   *                  SPECIAL GROUPS
         CLC   0(4,R1),=C'WORK'   HOW ABOUT NON-SPECIAL WORK
         BE    MSSNORML           YES - BACK IN LINE
         CLI   DDBSPKEY,X'00'     ANY SPACE INFORMATION
         BNE   MSSERROR           YES - VERY BAD IDEA HERE
         B     MSSEXIT
*
MSSERROR EQU   *
         PSNMERR  ERRMSS
         B     MSSEXIT
         EJECT
*                    TO LOOK AT THE USAGE OF RELEASE AND ROUND
*                    PARAMETERS - OUTSIDE OF MASS-STORE, OF COURSE.
*
RLSROUND  PSNMBLOK  RLR
         TM    DDBSPKEY,DDBSPKRL+DDBSPKRD
         BZ    RLREXIT            OUR SPECIALS NOT INVOLVED
         TM    DDBUNIT,DDBUSDAV   IS THIS FOR MASS STORE
         BO    RLREXIT            YES - THAT IS DONE ELSEWHERE....
*
*                    NOW CHECK THE SPECIAL CASES AS GIVEN
         TM    DDBSPKEY,DDBSPKRL  IS RELEASE SPECIFIED
         BZ    RLRCAS1X           NOT OUR CASE
         CLC   DDBDDNAM(6),=C'SORTWK' IS IT FOR SORTWORK
         BNE   RLRCAS1X
*                    HE USES RLSE IN HIS SORTWORKS - PROBABLY OTHER
*                    BAD HABITS AS WELL
         PSNMERR  ERRSTDFB,RLRRLSE,RLRSORTW
         B     RLRCAS1X
RLRRLSE  DC    C'RLSE'
RLRSORTW DC    C'SORT-WORK'
*
RLRCAS1X DS    0H
         TM    DDBSPKEY,DDBSPKRD  DID IT REQUEST ROUND ON REAL
         BZ    RLRCAS2X           NO - END OF CASE 2
         TM    DDBUNIT,DDBUSYSD   IS THIS FOR REAL DASD
         BZ    RLRCAS2X           NO - END OF THE SECOND CASE
*                    ROUNDING UP TO CYLINDERS, EH
         PSNMERR  ERRSTDFB,RLRROUND,RLRREAL
         B     RLRCAS2X           AND EXIT THE SECOND CASE
RLRROUND DC    C'ROUND'
RLRREAL  DC    C'REAL DASD'
RLRCAS2X DS    0H
*
         B     RLREXIT
         EJECT
*                    EDITTING OF THE UNIT FIELD TO GUARD
*                    AGAINST THE INEVITABLE SHORT-CUTS DONE
*                    BY THE UNINFORMED
*
UNITEDIT PSNMBLOK  UED
         TM    DDBUNIT,DDBUHARD   CHECK FOR HARD-CODED DEVICE
         BO    UEDCHKIT           YEP...GO CHECK IT OUT....         NTL
         TM    DDBUNIT,DDBUSDAV   IS THIS MSS
         BZ    UEDMSSX
         PSNMFDD  EMSVGP,UEDBAD    REQUIRE A MSVGP ALSO
*                    BE SURE CERTAIN VOLUME PARAMETERS WERE NOT GIVEN
         TM    DDBVOL,255-DDBVCNT WAS ONLY A COUNT ENTERED
         BNZ   UEDBAD             CANNOT HAVE VOLUME WITH MSS HERE....
UEDMSSX  EQU   *
*
         TM    DDBUNIT,DDBUTAPE+DDBUTP62+DDBUTPDD ANY TAPE REQUEST
         BNZ   UEDDEFX            YES - NO DEFER CHECK NEEDED
         TM    DDBIND1,DDBI1DEF   CHECK FOR DEFERRED REQUEST
         BNZ   UEDBAD             CANNOT DEFER AND NON-TAPE
UEDDEFX  EQU   *
         B     UEDEXIT
*
*
UEDBAD   EQU   *
         PSNMFDD  EUNIT           FIND THE POINTER TO DATA
         L     R1,DDBPSUBS        PICK UP THE VALUES
         PSNMERR   ERRSTDFB,((R1)),UEDNAME
         B     UEDEXIT
UEDNAME  DC    C'UNIT'
         SPACE 1                                                    NTL
UEDCHKIT EQU   *     CHK OUT ALLOWED UNITS                          NTL
*                                                                   NTL
         PSNMFDD  EUNIT           FIND POINTER TO UNIT DATA         NTL
         L     R5,DDBPSUBS        GET SUBSTITUTED VALUES ADDRESS    NTL
         SR    R2,R2              GET SET FOR ICM                   NTL
         LM    R15,R1,UEDLIST                                       NTL
UEDLOOP  EQU   *                                                    NTL
         IC    R2,0(R15)          GET LENGTH OF FIELD               NTL
         EX    R2,UEDCOMPR        COMPARE                           NTL
         BE    UEDEXIT            YES                               NTL
         BXLE  R15,R0,UEDLOOP                                       NTL
         B     UEDBAD                                               NTL
         SPACE 1                                                    NTL
UEDCOMPR CLC   0(0,R5),1(R15)     COMPARE TABLE-SUBSTITUED VALUE    NTL
UEDLIST  DC    A(*+12,9,UEDLIST1)                                   NTL
UEDLIST1 DC    AL1(3),CL8'WORK'                                     NTL
         DC    AL1(3),CL8'TAPE'                                     NTL
UEDLAST  DC    AL1(3),CL8'PROD'   SEE TABLE IN PSNDD TOO......      NTL
         EJECT
         LTORG
*
         PSNMDDLS  TYPE=EQU
         EJECT
         PSNMDD
         EJECT
*
WORKAREA DSECT
         DS    18F
WKDOUBL  DS    D                  FOR CONVERSION OF COLUMN NUMBERS
WKMASK   DS    XL4
*
         PSNMBLOK  RENT=REGS
WKLENG   EQU   *-WORKAREA
         EJECT
         PSNMWORK
         END
