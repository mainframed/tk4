*          DATA SET PSNMDDE    AT LEVEL 002 AS OF 10/30/80
         MACRO
&TAG     PSNMDDE &NAME
.*                  TO GENERATE THE TABLE ENTRIES FOR THE
.*                   VARIOUS SUBPARAMETERS OF A DD STATEMENT
.*
         GBLA  &PSNGNUM           NUMBER OF THIS FIELD
         GBLC  &PSNGTDD           TYPE OF GENERATION DESIRED
         LCLA  &LN                LENGTH OF THE KEYWORD
.*                   THE VALUES FOR OI AND TM
         LCLA  &BYTE,&MASK,&I
         LCLA  &M(8)
&M(1)    SETA  128
&M(2)    SETA  64
&M(3)    SETA  32
&M(4)    SETA  16
&M(5)    SETA  8
&M(6)    SETA  4
&M(7)    SETA  2
&M(8)    SETA  1
.*
.*
&PSNGNUM SETA  &PSNGNUM+1         TO NEXT ENTRY NUMBER
         AIF   ('&PSNGTDD' EQ 'BRTAB').BRTAB   WANT THE BRANCH TABLE
E&NAME   EQU   &PSNGNUM           THE ENTRY NUMBER
         AIF   ('&PSNGTDD' EQ 'EQU').EXIT ONLY WANT THE EQUATES HERE
&LN      SETA  K'&NAME            LENGTH OF THIS WORD
         DC    CL8'&NAME'         NAME FOR TABLE MATCHING
         DC    AL2(&PSNGNUM)      THE NUMBER ITSELF
         DC    AL2(&LN)           LENGTH OF THIS NAME
&BYTE    SETA  (&PSNGNUM-1)/8     THE BYTE NUMBER OF THE FLAG
&I       SETA  &PSNGNUM-&BYTE*8   WHICH BIT MASK TO USE
&MASK    SETA  &M(&I)
         TM    DDBMASK+&BYTE,&MASK TEST FOR THE PRESENCE
         OI    DDBMASK+&BYTE,&MASK SHOW THE PRESENCE
         SPACE 2
         AGO   .EXIT
.*
.*                   THE BRANCH TABLE FOR MATCHUP WITH EQUATES
.BRTAB   ANOP
         B     D&NAME
.EXIT    ANOP
         MEND
