*          DATA SET IEECVXIT   AT LEVEL 002 AS OF 05/12/78
*          DATA SET IEECVXIT   AT LEVEL 001 AS OF 12/12/77
IEECVXIT TITLE  'INSTALLATION WTO EXIT - CHG ROUTE CODES FOR SELECTED MX
               ESSAGES TO GO TO MSS/RACF CONSOLE'
*.....................................................................*
*.                                                                   .*
*. MODULE - IEECVXIT   (WTO EXIT)                                    .*
*.                                                                   .*
*. SOURCE FROM 'SYS1.SAMPLIB'  IPO1.2 SYSTEM.                        .*
*.                                                                   .*
*.                                                                   .*
*.....................................................................*
         SPACE
*.....................................................................*
*    MACRO DEFINITIONS                                                *
*.....................................................................*
         SPACE
         MACRO
         IEECODES &ROUTCDE=13,&DESC=,&ID=IEE
         LCLC  &CD(4)
         LCLA  &I,&N
         LCLB  &B(32)
.*
.* DESCRIPTOR CODES
.*
&I       SETA  1
.DCHK    AIF   (T'&DESC EQ 'O').RCHK
&N       SETA  &DESC(&I)
&I       SETA  &I+1
         AIF   (&N GE 1 AND &N LE 16).ASSIGND
         MNOTE 8,'&DESC(&I) IS INVALID DESCRIPTOR - IGNORED'
         AGO   .NXTD
.ASSIGND ANOP
&B(&N)   SETB  1
.NXTD    AIF  (&I LE N'&DESC).DCHK
&I       SETA  1
.*
.* ROUTE CODES
.*
.RCHK    AIF   (T'&ROUTCDE EQ 'O').ASSIGNC
&N       SETA  &ROUTCDE(&I)
&I       SETA  &I+1
         AIF   (&N GE 1 AND &N LE 16).ASSIGNR
         MNOTE 8,'ROUTCDE(&I) IS INVALID ROUTE - IGNORED'
         AGO   .NXTR
.ASSIGNR ANOP
&B(&N+16) SETB  1
.NXTR    AIF   (&I LE N'&ROUTCDE).RCHK
.ASSIGNC ANOP
&I       SETA  1
&CD(&I)   SETC  '&B(1)&B(2)&B(3)&B(4)&B(5)&B(6)&B(7)&B(8)'
&CD(&I+1) SETC  '&B(9)&B(10)&B(11)&B(12)&B(13)&B(14)&B(15)&B(16)'
&CD(&I+2) SETC  '&B(17)&B(18)&B(19)&B(20)&B(21)&B(22)&B(23)&B(24)'
&CD(&I+3) SETC  '&B(25)&B(26)&B(27)&B(28)&B(29)&B(30)&B(31)&B(32)'
&ID.DESCD DC   BL2'&CD(1)&CD(2)'
&ID.ROUTC DC   BL2'&CD(3)&CD(4)'
         MEND
         EJECT
*.....................................................................*
*     PROGRAM START                                                   *
*.....................................................................*
         SPACE
IEECVXIT CSECT
         USING *,R15                   EST. ADDRESSABILITY
         B     PROLOG                  BYPASS PROLOGUE
         DC    AL1(18)                 PROLOGUE LENGTH
         DC    C'IEECVXIT &SYSDATE'    MODULE NAME AND COMPILE DATE
         DS    0H                      ALLIGNMENT
PROLOG   SAVE  (14,12)                 SAVE CALLERS REGS
         LR    R12,R15                 SAVE ENTRY ADRESS
         DROP  R15
         USING IEECVXIT,R12            REAL BASE ADDRESS=R12
         L     R2,0(R1)                SET ADDR TO IEECUCM DSECT
         USING IEECUCM,R2              EST. ADDR TO IEECUCM DSECT
         CLC   UCMMSTXT(L'CICB085I),CICB085I   TRACE SWITCH MSG
         BE    DOSTART                 YES=> GO DO START CMD
         CLC   UCMMSTXT(L'CICB085I-2),CICB085I+2   TRACE SWITCH MSG
         BE    DOSTART                 YES=> GO DO START CMD
         SPACE 1
CHKMSG   LA    R7,MSGTAB               SET R7 TO OPTIONAL MSG TABLE
         LA    R8,L'MSGTAB             SET R8 VALUE
         LA    R9,MSGEND-L'MSGTAB      SET END OF TABLE ADDR
CHKMSG1  CLI   0(R7),BLANK             OPTINAL MSG ID
         BE    CHKMSG2                 NO, RETURN
         CLC   UCMMSTXT(L'MSGTAB),0(R7) OPTIONAL MSG
         BE    UPDROUT1                YES, UPDATE ROUTING CODE
         BXLE  R7,R8,CHKMSG1           SCAN ENTIRE OPTIONAL MSG TAB
         SPACE 1
CHKMSG2  CLC   UCMMSTXT(L'CICB),CICB   ICB MSG
         BE    UPDROUT                 YES, UPDATE ROUTING CODE
         CLC   UCMMSTXT(L'CBBICB),CBBICB MSSC ICB MSG
         BE    UPDROUT                 YES, UPDATE ROUTING CODE
         CLC   UCMMSTXT(L'CIDC),CIDC   IDC MSG
         BE    UPDROUT                 YES, UPDATE ROUTING CODE
         CLC   UCMMSTXT(L'CIGF),CIGF   IGF MISSING INTERRUPT?
         BE    UPDROUT1                YES, UPDATE ROUTING CODE
         CLC   UCMMSTXT(L'CICH),CICH   ICH RACF MESSAGE
         BE    UPDROUT                 YES, UPDATE ROUTING CODE
         CLC   UCMMSTXT(L'CIEA),CIEA   IEA000  MSG
         BE    CHKSWAP                 YES, MSS IEA000  MSG
         SPACE 1
RETURN   RETURN (14,12)                RETURN TO CALLER
         SPACE 1
UPDROUT  OC    UCMROUTC(L'IEEROUTC),IEEROUTC ADD ROUTING CODE
         OC    UCMDESCD(L'IEEDESCD),IEEDESCD ADD DESCRIPTOR CODE
         B     RETURN                  RETURN TO CALLER
         SPACE 1
UPDROUT1 OC    UCMROUTC(L'FCIROUTC),FCIROUTC ADD ROUTING CODE
         OC    UCMDESCD(L'FCIDESCD),FCIDESCD ADD DESCRIPTOR CODE
         B     RETURN                  RETURN TO CALLER
         EJECT
CHKSWAP  CLC   UCMMSTXT(L'MSCSWAP),MSCSWAP MSS ERP MSC SWITCH
         BE    UPDROUT                 YES, UPDATE ROUTING CODE
         CLC   UCMMSTXT(L'SYSSWAP),SYSSWAP MSS ERP SYS SWITCH
         BE    UPDROUT                 YES, UPDATE ROUTING CODE
         LA    R3,CVTLOC               SET LOCATION OF CVT ADDR
         L     R3,0(R3)                GET CVT ADDRESS
         USING CVT,R3                  EST. ADDR TO CVT
         L     R4,CVTIK2               GET ADDRESS OF UCB LOOKUP TABLE
         B     *+EIGHT                 BYPASS FIRST ADDR UPDATE
CHKUCBAD LA    R4,TWO(R4)              SET NEXT UCB LOOKUP TAB ADDRESS
         LH    R5,0(R4)                GET UCB ADDRESS
         N     R5,MASKFFFF             ALLOW FOR FULL 16-BIT ADDR
         C     R5,MASKFFFF             END OF UCB LOOKUP TABLE
         BE    RETURN                  YES, RETURN TO CALLER
         LTR   R5,R5                   POINTER TO UCB
         BZ    CHKUCBAD                NO, GET NEXT UCB LOOKUP TAB ADDR
         USING UCB,R5                  EST, ADDR OF CVT
         CLC   UCMMSTXT+EIGHT(L'UCBNAME),UCBNAME UNIT NAME AS IS MSG
         BNE   CHKUCBAD                NO, CHECK NEXT UCB
         TM    UCBTYP3,UCB3DACC        DASD UCB
         BNO   CHKMSC                  NO, CHECK MSC
         TM    UCBTYP2,UCBRVDEV        VUA
         BO    UPDROUT                 YES, UPDATE ROUTING CODE
         B     RETURN                  NO, RETURN TO CALLER
CHKMSC   TM    UCBTYP3,UCB3UREC        UNIT RECORD DEVICE
         BNO   RETURN                  NO, RETURN TO CALLER
         CLI   UCBTYP4,UCBMSC          MSC
         BE    UPDROUT                 YES, UPDATE ROUTING CODE
         B     RETURN                  NO, RETURN TO CALLER
         EJECT
*.....................................................................*
*        ISSUE START COMMAND DO DUMP MSS TRACE AREAS                  *
*.....................................................................*
         SPACE
DOSTART  L     R0,GETPARM              LOAD SP+LENGTH
         GETMAIN R,LV=(0)              GET STORAGE
         LR    R11,R1                  SAVE ADDRESS OF STORAGE
         USING DATA,R11
         MVC   WTOAREA(WTOLEN),WTOL    MOVE IN WTO MESSAGE
         MVC   CMDAREA(CMDLEN),CMDL    MOVE IN START COMMAND
         SLR   R0,R0                   CLEAR REG ZERO FOR SVC 34
         LA    R1,CMDAREA              POINT TO START COMMAND
         SVC   34                      ISSUE START COMMAND
         WTO   MF=(E,WTOAREA)          ISSUE MSG
         LR    R1,R11                  RELOAD AREA ADDRESS
         DROP  R11
         L     R0,GETPARM             LOAD SP+LENGTH
         FREEMAIN R,LV=(0),A=(1)       FREE GETMAINED STORAGE
         B     UPDROUT1               GO UPDATE ROUTECODE
         SPACE 2
         DS    0F                      ALLIGNMENT
IEEPATCH EQU   *                       PATCH AREA
PATCHA   NOP   *                       LET ASSEMBLER CALC DISP
         DC    5F'0'                   SELF DEFINED
PATCHB   NOP   *                       LET ASSEMBLER CALC DISP
         DC    5F'0'                   SELF DEFINED
         EJECT
*.....................................................................*
*        MESSAGES AND START COMMAND FORMAT  (DATA DSECT TOO)          *
*.....................................................................*
         SPACE
CMDL     DS    0F                      START COMMAND FORMAT FOR SVC 34
         DC    AL2(CMDLEN),AL2(00)     LENGTH OF STRING
         DC    C'START SYS,JOB=MSSTRACE           ' . COMMAND + BLANK
*                456789012345678901234567890123456  . CHECK COLUMNS
CMDLEN   EQU   *-CMDL             LENGTH OF COMMAND STRING
*
WTOL     WTO   'IEECVXIT HAS ISSUED COMMAND ''S SYS,JOB=MSSTRACE'' TO DC
               UMP MSS TRACE ',ROUTCDE=(1,2,11,13),DESC=(2),MF=L
*               456789.123456789.123456789. 123456789.1234567 89.123456
WTOLEN   EQU   *-WTOL             LENGTH OF WTO STRING
         SPACE 2
*
* DATA DSECT - AREA TO BE GETMAIN'ED
*
DATA     DSECT
CMDAREA  DS    0F,XL(CMDLEN)      AREA FOR COMMAND
WTOAREA  DS    0F,XL(WTOLEN)      AREA FOR WTO PARM LIST
RESERVED DS    4D                 RESERVED
DATALEN  EQU   *-DATA             EQUATE FOR LENGTH OF DATA DSECT
         SPACE 2
*
* DEFINE SUB-POOL AND LENGTH FOR GETMAIN/FREEMAIN
*
IEECVXIT CSECT                    GET BACK TO CSECT
         DS    0D                 FORCE TO DOUBLE WORD
GETPARM  DC    0F'0',AL1(230),AL3(DATALEN) GETMAIN PARM SP+LENGTH
         EJECT
*.....................................................................*
*        EQUATES                                                      *
*.....................................................................*
         SPACE
R0       EQU   0
R1       EQU   1                       ADDRESS OF SUPPLIED PARAMETERS
R2       EQU   2                       ADDRESS OF EXIT PARAMETERS
R3       EQU   3                       ADDRESS OF CVT
R4       EQU   4                       ADDRESS OF UCB LOOKUP TABLE
R5       EQU   5                       ADDRESS OF UCB
R7       EQU   7                       ADDRESS IN OPTIONAL MSG TABLE
R8       EQU   8                       INCREMENT FOR MSG TABLE
R9       EQU   9                       END ADDRESS OF MSG TABLE
R10      EQU   10
R11      EQU   11
R12      EQU   12                      BASE REGISTER
R13      EQU   13                      SAVE AREA POINTER
R14      EQU   14                      RETURN ADDRESS
R15      EQU   15                      ENTRY ADDRESS
TWO      EQU   2                       SELF DEFINED
FOUR     EQU   4                       SELF DEFINED
EIGHT    EQU   8                       SELF DEFINED
CVTLOC   EQU   16                      LOCATION OF CVT
BLANK    EQU   C' '                    SELF DEFINED
         EJECT
*.....................................................................*
*    CONSTANTS                                                        *
*.....................................................................*
         SPACE
         IEECODES
         SPACE 2
         IEECODES ID=FCI,ROUTCDE=(1,2,13),DESC=(2)
         SPACE 2
CICB085I DC    C'  ICB085I'
CICB     DC    C'ICB'
CBBICB   DC    C'  ICB'
CIDC     DC    C'IDC'
CICH     DC    C'ICH'
CIGF     DC    C'IGF'
CIEA     DC    C'IEA000'
*
* MSGIDS IN MSGTAB WILL BE UPDATED TO HANG ON CONSOLE, AND LOG TO
*     MSS/RACF CONSOLE  .. ROUTCDE=13
*
MSGTAB   DC    C'ICB463E'    2 TRACKS LEFT IN JOURNAL
         DC    C'ICB464E'    1 TRACK  LEFT IN JOURNAL
         DC    C'ICB465E'    JOURNAL FULL..NOT RECORDING
         DC    C'IEC150I'    PASSWORD/SECURITY PROBLEMS....
         DC    C'       '
         DC    C'       '
         DC    C'       '
         DC    C'       '
         DC    C'       '
         DC    C'       '
         DC    C'       '
         DC    C'       '
         DC    C'       '
         DC    C'       '
         DC    C'       '
         DC    C'       '
         DC    C'       '
         DC    C'       '
         DC    C'       '
         DC    C'       '   <= LAST ONE MUST BE BLANK......
MSGEND   EQU   *
SYSSWAP  DC    C'IEA000I SYS SWITCH'
MSCSWAP  DC    C'IEA000I MSC SWITCH'
         DS    0F
MASKFFFF DC    XL4'0000FFFF'
         EJECT
*.....................................................................*
*        DSECTS                                                       *
*.....................................................................*
         SPACE
IEECUCM  DSECT
UCMMSTXT DS    CL128
UCMROUTC DS    CL4
UCMDESCD DS    CL4
         SPACE
CVT      DSECT
         DS    CL40
CVTIK2   DS    F
         SPACE
UCB      DSECT
         DS    CL13
UCBNAME  DS    CL3
UCBTYP1  DS    C
UCBTYP2  DS    C
UCBTYP3  DS    C
UCBTYP4  DS    C
UCBRVDEV EQU   X'08'
UCB3DACC EQU   X'20'
UCB3UREC EQU   X'08'
UCBMSC   EQU   X'42'
         END   IEECVXIT
