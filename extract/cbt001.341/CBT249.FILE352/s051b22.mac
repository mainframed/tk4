*          DATA SET S051B22    AT LEVEL 001 AS OF 03/09/77
S051B22  CSECT
*
* SUBROUTINE  -F I N D-
*
* TO LOCATE A STRING OF CHARACTERS IN AN ARRAY OF CHARACTERS.
*
* CALLING SEQUENCE:  CALL FIND(ARRAY,STRING,STRLEN,LOCATION,START,END)
*
* WHERE:
*
* -ARRAY-      IS THE ARRAY NAME OF THE ARRAY CONTAINING THE CHARACTERS
*        TO BE SCANNED FOR -STRING-.
*
* -STRING-     IS THE NAME OF THE ARRAY (OR A LITERAL) CONTAINING
*        THE CHARACTER STRING TO BE SEARCHED FOR.
*
* -STRLEN-     IS THE NUMBER OF CHARACTERS IN -STRING- TO BE USED
*        IN THE SEARCH.
*
* -START- IS THE OPTIONAL CHARACTER POSITION AT WHICH SCANNING
*        IS TO BEGIN.  IF OMMITTED, START=1 IS ASSUMED.
*
* -END-  IS THE OPTIONAL CHARACTER POSITION AT WHICH SCANNING
*        IS TO END.  IF OMMITTED, END=80 IS ASSUMED.
*
* ON RETURN:
*
* -LOCATION- WILL CONTAIN THE CHARACTER POSITION  IN -ARRAY
*        AT WHICH THE NEXT -STRLEN- CHARACTERS IN BOTH -ARRAY- AND
*        -STRING- ARE EQUAL.
*
*        IF THE STRING COULD NOT BE FOUND, -LOCATION- IS SET
*        TO INTEGER ZERO.
*
* K TRUE 8JAN76
*
         ENTRY FIND
         REGEQU
         EJECT
         USING *,R12
FIND     SAVE  (14,12),,*
         LR    R12,R15
         LA    R2,6
         LR    R3,R1
*
PARMLOOP TM    0(R3),X'80'
         BO    ENDPARM
         LA    R3,4(R3)
         BCT   R2,PARMLOOP
*
ENDPARM  LA    R3,4           R2=3, 4 PARMS PASSED
         CR    R2,R3          R2=2, 5 PARMS PASSED
         BL    FIVEPARM       R2=1, 6 PARMS PASSED
         LA    R4,0           R4=START OFFSET
         LA    R5,79          R5=END OFFSET=79
         B     INIT
FIVEPARM L     R4,16(R1)
         L     R4,0(R4)       (R4)=START OFFSET+1
         BCTR  R4,0           R4=START OFFSET=STARTCHAR-1)
         LA    R3,3
         CR    R2,R3
         BL    SIXPARM
         LA    R5,79          R5=END SCAN AT 79
         B     INIT
*
SIXPARM  L     R5,20(R1)
         L     R5,0(R5)
         BCTR  R5,0           R5=END OFFSET=ENDCHAR-1
*
         EJECT
INIT     L     R3,8(R1)
         L     R3,0(R3)
         LTR   R3,R3          TEST FOR NOTHING TO DO
         BP    CONTINUE
         B     NOTFOUND       ..NO COMPARE..NO WORK..
*
CONTINUE SR    R5,R3          ADJUST ENDSCAN FOR STR LENGTH
         LA    R5,2(R5)       R5=END SCAN BYTE LOCATION
*
         CR    R4,R5          CHECK FOR SOMETHING TO DO.
         BH    NOTFOUND       SKIP IF STRT>END SCAN
*
         L     R2,0(R1)       (R2)=ADDR(STRING START)
         AR    R2,R4          R2=ADDR START OF SCAN
         LR    R10,R2         SAVE SCAN ADDR
         LR    R7,R3
         LR    R9,R3          SAVE LENGTH OF COMPARE
         L     R6,4(R1)       (R6)=ADDR(COMPARE STRING START)
         LR    R11,R6         SAVE ADDR FOR COMPARE STRING
*
COMPLOOP CLCL  R2,R6          COMPARE.....
         BE    FOUND          IF FOUND, SET RETURN VALUE
         LA    R4,1(R4)       BUMP OFFSET
         LA    R10,1(R10)     BUMP SCAN ADDR
         LR    R2,R10         RESTORE SCAN ADDR
         LR    R3,R9          RESTORE COUNT
         LR    R7,R9          RESTORE COUNT
         LR    R6,R11         RESTORE COMPARE STR ADDR
         CR    R4,R5
         BNH   COMPLOOP
*
NOTFOUND SR    R4,R4          RETURN 0 (NOT FOUND)
         BCTR  R4,0
FOUND    L     R9,12(R1)      R9=ADDR OF VARAIBLE TO RCVE LOC
         LA    R4,1(R4)       BUMP CNT FOR FORTRAN
         ST    R4,0(R9)       PUT ANSWER(LOC) AWAY
         SR    R15,R15
         L     R14,12(R13)
         LM    R0,R12,20(R13) RESTORE REGISTERS
         MVI   12(R13),X'FF'  SAY 'SOK FORTRAN
         BR    R14            EXIT...
         END
