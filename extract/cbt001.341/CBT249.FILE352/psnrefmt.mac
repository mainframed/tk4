*          DATA SET PSNREFMT   AT LEVEL 001 AS OF 10/30/80
         TITLE 'PSNREFMT - DIRECTOR OF THE REFORMAT EFFORT'
PSNREFMT PSNMSTRT  SASIZ=WKLENG
         EJECT
*                    DIRECT THE PROCESSING OF THE REFORMAT EFFORT
*                    FOR THE PROC, EXEC, AND DD STATEMENTS.
*
         L     R9,PSWEXCHN        ADDRESS THE START OF THE EXEC'S
         USING EXECBLOK,R9        PASSED FROM EXECUTE BUILDER
         USING DDBLOCK,R8         CHAINED OFF THE EXEC OR ANOTHER DD
*
         MVC   PSWRMODE,=CL11'REFORMAT OF'                          NTL
         MVC   PSWLINCT,PSWMAXLN  FORCE NEW PAGE FOR REFORMATTED MBR
         MVC   PSWPNAME,PSWMNEWN  PLUG IN PROPER MEMBER NAME
         ZAP   PSWLINUM,PSWMNEW1  SET THE STARTING SEQUENCE NUMBER
         OI    PSWWFLAG,PSWWFOPN  CODE TO START FOR A MEMBER
         L     R15,PSWWRITE       ADDRESS THE WRITER TO START IT OFF
         BALR  R14,R15            MAKE THE INITIAL CALL
*
         L     R15,PSWRFPRC       REFORMAT THE PROC STATEMENT
         BALR  R14,R15            AND WRITE IT OUT
*
EXECLOOP EQU   *                  LOOP THROUGH THE STEPS
         LTR   R9,R9              ANY MORE TO COME
         BZ    EXECEND            END OF THE TRAIL
         ST    R9,PSWEXEND        STORE IN CURRENT EXEC POINTER
         L     R15,PSWRFEXC       REFORMAT THE EXEC CARD
         BALR  R14,R15
         L     R8,EXCDDCHN        ADDRESS THE DDCHAIN OF THIS STEP
*
DDLOOP   EQU   *
         LTR   R8,R8              ANY MORE DD STATEMENTS
         BZ    DDEND              NO - END OF THIS STEP
         ST    R8,PSWDDLST        STORE IN CURRENT DD POINTER
         L     R15,PSWRFDD        ADDRESS THE SPECIALIST IN DD LINES
         BALR  R14,R15
         L     R8,DDBNEXT         AND ON TO THE NEXT STATEMENT
         B     DDLOOP
*
DDEND    EQU   *                  END OF THE DD'S FOR THIS STEP
         L     R9,EXCNEXT         ON TO THE NEXT STEP ENTRY
         B     EXECLOOP
*
EXECEND  EQU   *                  END OF ALL THE STEPS
         OI    PSWWFLAG,PSWWFEOF  CLOSE OFF THE REFORMATTING NOW
         L     R15,PSWWRITE
         BALR  R14,R15            FINAL CALL FOR THE STOW
*
         B     RETURN
         EJECT
         PSNMEXEC
         EJECT
         PSNMDD
*
*
WORKAREA DSECT
         DS    18F
         DS    X
         PSNMBLOK  RENT=REGS
WKLENG   EQU   *-WORKAREA
         EJECT
         PSNMWORK
         END
