        /* DATA SET SMFDSNPR   AT LEVEL 001 AS OF 03/03/80    */
        /* DATA SET SMFDSNPR   AT LEVEL 001 AS OF 07/07/78    */
 SMFDSNP: PROC OPTIONS(MAIN);
 /*********************************************************************/
 /* READ SORTED RECORDS PRODUCED BY SMFDSN AND PRINT THE REPORT       */
 /*    11AUG77      K TRUE .....                                      */
 /*********************************************************************/
 DCL INPUT FILE
            INPUT
            RECORD
            ENV (CONSECUTIVE);
 DCL SYSPRINT FILE
              STREAM
              OUTPUT
              PRINT;
 DCL VERSION CHAR(17) INIT('VER 1.1 (09MAY79)');
 DCL RECORDS       FIXED BIN (15,0) INIT(0);
 DCL PAGENO        FIXED BIN(15) INIT(0);
 DCL DSN           CHAR(44) INIT((44)' ');
 DCL (INMODE,OUTMODE,SCRMODE) FIXED BIN(15) INIT(0);

 DCL PACK5    PIC'99999'    INIT(0),
     PACK6    PIC'999999'   INIT(0),
     PDATE    PIC'Z9/99/99' INIT(0),
     R941X12  ENTRY EXTERNAL OPTIONS(ASSEMBLER,INTER);

 DCL RUNDATE  CHAR(8)       INIT(' '),
     RUNTIME  CHAR(8)       INIT(' '),
     DATTIM   EXTERNAL ENTRY OPTIONS(ASSEMBLER,INTER);


 DCL EOFIN         BIT(1) INIT('0'B);

 DCL 1 INREC,
       2 PJOBNAME       CHAR(8),
       2 JOBDAT         PIC'99.999' INIT(0),
       2 JOBTIM         PIC'99.99.99' INIT(0),
       2 MODE1          CHAR(6) INIT(' '),
       2 DDNAME         CHAR(8) INIT(' '),
       2 ODSNAME        CHAR(44);
     ON ERROR BEGIN;
        ON ERROR SYSTEM;
        PUT DATA;
     END;
     ON ENDFILE(INPUT) STOP;
     ON FINISH BEGIN ;
        IF RECORDS ^= 0 THEN
        PUT FILE(SYSPRINT) SKIP(2) EDIT
           ('ACCESSES: ',INMODE,' FOR INPUT ',OUTMODE,
            ' FOR OUTPUT, ',SCRMODE,' SCRATCHED, ',
           INMODE+OUTMODE+SCRMODE,' TOTAL')
           (COL(10),A,(3)(F(5),A),F(8),A);
        PUT FILE(SYSPRINT) SKIP(2) EDIT
            (RECORDS,' TOTAL DATASET RECORDS PRINTED')
            ((1)(F(9),A,SKIP));
        CLOSE FILE (INPUT),
              FILE (SYSPRINT) ;
     END ;
     ON  ENDPAGE(SYSPRINT) BEGIN;
         PAGENO = PAGENO + 1;
         PUT FILE(SYSPRINT) PAGE EDIT
            ('SMFDSN  SORTED PRINT LISTING  ',VERSION,
             RUNDATE,RUNTIME,
             ' PAGE',PAGENO)
            (A,A,COL(90),A,X(2),A,COL(115),A,F(4),SKIP(2));
         PUT FILE(SYSPRINT) SKIP(2);
                           END;
     OPEN FILE (INPUT)  INPUT,
          FILE (SYSPRINT) PRINT LINESIZE(125);

     ON ENDFILE(INPUT) EOFIN='1'B;

  CALL DATTIM(RUNDATE,RUNTIME);

  DO WHILE(^EOFIN);
    READ FILE(INPUT) INTO(INREC);
    IF ^EOFIN THEN
     DO;
       IF ODSNAME ^= DSN THEN
         DO;
           IF RECORDS ^= 0 THEN
            PUT FILE(SYSPRINT) SKIP(2) EDIT
               ('ACCESSES: ',INMODE,' FOR INPUT ',OUTMODE,
                ' FOR OUTPUT, ',SCRMODE,' SCRATCHED, ',
               INMODE+OUTMODE+SCRMODE,' TOTAL')
               (COL(10),A,(3)(F(5),A),F(8),A);
           INMODE,OUTMODE,SCRMODE=0;
           SIGNAL ENDPAGE(SYSPRINT);
         END;
       PACK5 = JOBDAT;
       CALL R941X12(PACK5,PACK6);
       IF PACK6 ^= 0 THEN PDATE = PACK6;
                     ELSE PDATE = 000000;

       SELECT (MODE1);
          WHEN('INPUT ','RDBACK')    INMODE=INMODE+1;
          WHEN('SCRTCH')             SCRMODE=SCRMODE+1;
          OTHERWISE                  OUTMODE=OUTMODE+1;
       END;

       PUT FILE(SYSPRINT) SKIP EDIT
          (PJOBNAME,PDATE,JOBTIM,' MODE=',MODE1,' DDNAME=',DDNAME,
           ODSNAME)
          ((3)(X(3),A),A,A,X(2),A,A,X(2),A);

       RECORDS = RECORDS + 1;
       DSN = ODSNAME;
     END;
  END;

     END SMFDSNP ; /* END OF MAIN PROGRAM */
