*******************************************************************NTL*
*                                                                  NTL*
*   DR - PRINT SUMMARY OF ALL OUTPUT IN QUEUE BY DESTINATION       NTL*
*                                                                  NTL*
*******************************************************************NTL*
DR       MVI   QCLASS,192           START WITH CLASS A             NTL*
         MVC   QDHLINE,DRHEADER     MOVE IN THE HEADING            NTL*
         BAL   R2,HOWFULL           GO GET/FORMAT % FULL OF SPOOL  NTL*
DR1      BAL   R2,NEXTJOT           FIND NEXT QUEUE                NTL*
         ZAP   QCOUNTE,=P'0'        ZERO COUNT FOR JOBS PRINTING   NTL*
         ZAP   QCOUNTA,=P'0'        ZERO COUNT FOR LOCAL           NTL*
         ZAP   QCOUNTH,=P'0'        ZERO COUNT FOR REMOTE          NTL*
         SLR   R5,R5                ACCUMULATOR FOR LINE COUNT     NTL*
         BAL   R2,SRCHJOE           SEARCH THE QUEUE               NTL*
         B     DR2                  END OF QUEUE                   NTL*
         A     R5,JOERECCT          ADD UP COUNT OF RECS           NTL*
         CLI   JOEROUT+1,X'00'      ROUTE RMT00=LOCAL?             NTL*
         BNE   DR1R                 NO..ITS REMOTE                 NTL*
         AP    QCOUNTA,=P'1'        YES..ITS LOCAL                 NTL*
         B     DR1A                   SKIP                         NTL*
DR1R     AP    QCOUNTH,=P'1'        ITS REMOTE                     NTL*
DR1A     TM    JOEFLAG,X'20'        IS JOB PRINTING                NTL*
         BNO   SKIPJOE              NO, PROCESS NEXT JOE           NTL*
         AP    QCOUNTE,=P'1'        YES, BUMP COUNTER              NTL*
         B     SKIPJOE              PROCESS NEXT JOE               NTL*
DR2      CLC   QCOUNT,=PL3'0'       QUEUE EMPTY                    NTL*
         BE    DR1                  YES, TRY THE NEXT QUEUE        NTL*
         MVC   QDMSG,DRLINE         MOVE IN DETAIL LINE            NTL*
         MVC   FCOUNT,ED5           MOVE IN                        NTL*
         ED    FCOUNT,QCOUNTE            NUMBER OF JOBS            NTL*
         MVC   QECOUNT,FCOUNT+3                    PRINTING        NTL*
         MVC   FCOUNT,ED5           MOVE IN                        NTL*
         ED    FCOUNT,QCOUNTA            NUMBER OF JOBS            NTL*
         MVC   QACOUNT,FCOUNT+3                    FOR LOCAL       NTL*
         MVC   FCOUNT,ED5           MOVE IN                        NTL*
         ED    FCOUNT,QCOUNTH            NUMBER OF JOBS            NTL*
         MVC   QHCOUNT,FCOUNT+3                    FOR REMOTE      NTL*
         MVC   FCOUNT,ED5           MOVE IN TOTAL NUMBER OF JOBS   NTL*
         ED    FCOUNT,QCOUNT             IN THIS QUEUE             NTL*
         MVC   CLASS,QCLASS         MOVE IN QUEUE CLASS            NTL*
         CVD   R5,CONVERT           CONVERT RECS TO DECIMAL        NTL*
         A     R5,TOTRECS           ADD UP TOTAL RECORDS           NTL*
         ST    R5,TOTRECS           AND SAVE IT AWAY               NTL*
         MVC   QTRECS,ED12          MOVE IN MASK                   NTL*
         ED    QTRECS,CONVERT+3     MAKE IT PRETTY                 NTL*
         BAL   R2,DISPLAY           GO DISPLAY THIS LINE           NTL*
         B     DR1                  PROCESS NEXT QUEUE             NTL*
