*          DATA SET RTNCODE    AT LEVEL 001 AS OF 04/06/81
RTNCODE  TITLE 'R T N C O D E  UTILITY - SET OS RETURN CODE DEPENDING OX
               N PARM FIELD VALUE'
*---------------------------------------------------------------------*
*                                                                     *
* PURPOSE: TO SET OS RETURN CODE IN STEP TO DESIRED VALUE SO SUBSE-   *
*          QUENT STEP EXECUTION CAN BE CONTROLLED BY THE 'COND=' PARM *
*                                                                     *
*                                                                     *
* EXEC PGM=RTNCODE,PARM=YES   - OS RETURN CODE = 0001                 *
*                       NO      OS RETURN CODE = 0002                 *
*                       NNNN    OS RETURN CODE = NNNN                 *
*                               WHEN  1 <= NNNN <= 4095               *
*                        OTHERWISE, RETURN CODE SET TO 4095           *
*                                                                     *
*  KEN TRUE   APR 6 1981 - RECONSTITUTED FROM DISASM SOURCE OF LOAD   *
*             MODULE 'RTNCODE' IN 'SYS2.PRODLIB1'                     *
*             ADDED COMMENTS AND LABELS...                            *
*---------------------------------------------------------------------*
         SPACE 2
RTNCODE  CSECT
         SAVE  (14,12)             SAVE OFF REGS SO
         LR    R12,R15             OS AND US WONT BE TOO UNHAPPY
         USING RTNCODE,R12
         ST    R13,SAVE+4          CHAIN SAVE
         LA    R11,SAVE                   AREAS
         ST    R11,8(R13)                      TOGETHER
         LR    R13,R11                              AND ADDRESS OURS
         B     START               GO TO THE REAL CODE
         SPACE 2
DBLEWORD DC    D'0'
SAVE     DC    18F'0'
TRTNUMB  DC    256XL1'FF'          TRTAB FOR TESTING FOR NUMERICS
         ORG   TRTNUMB+C'0'
         DC    XL10'00'
         ORG
         EJECT
START    L     R10,0(R1)           (R10) = PTR TO OS PARM FIELD
         LH    R9,0(R10)           (R9)= LENGTH OF PARM FIELD
         CH    R9,=H'4'            IS THIS MORE THAN WE WANT TO HANDLE?
         BH    SET4095             YEP..SPLIT WITH MAX VALUE
         LTR   R9,R9               ANY PARM THERE?
         BNP   SET4095             NO..SPLIT WITH MAX VALUE
         SPACE 1
         CH    R9,=H'1'            IS THIS PARM LENGTH OF ONE?
         BNE   SKIP01              NO..TRY FOR LENGTH OF 2
         TRT   2(1,R10),TRTNUMB    CHECK 1 FOR NUMERIC
         BNZ   SET4095               NO.....SPLIT WITH MAX VAL
         PACK  DBLEWORD,2(1,R10)     YES..GET NUMBER
         B     CHKRANGE              AND GO FINISH UP
         SPACE 1
SKIP01   CH    R9,=H'2'            IS THE PARM OF LENGTH 2?
         BNE   SKIP02              NO..TRY FOR 3
         CLC   2(2,R10),=CL2'NO'   THE INFAMOUS 'NO' WORD
         BE    SETRC2              YES..SET RC=2 AND FINISH UP
         TRT   2(2,R10),TRTNUMB    NO..SO IS IT A NUMBER?
         BNZ   SET4095               NOT NUM..SPLIT
         PACK  DBLEWORD,2(2,R10)     GET NUMBER AND
         B     CHKRANGE                   GO FINISH UP
         SPACE 1
SKIP02   CH    R9,=H'3'            BE THEY 3 CHARS MAITY?  ARRRRHHHH
         BNE   SKIP03              NO..TRY AT LAST FOR 4
         CLC   2(3,R10),=CL3'YES'  IS THE MAGIC WORD ='YES'?
         BE    SETRC1              AH HA..SET RC1 AND FINISH UP
         TRT   2(3,R10),TRTNUMB    EXAMINE SUSPECTED NUMERICS
         BNZ   SET4095             LIKE I THOUGHT, CANT TRUST A USER..
         PACK  DBLEWORD,2(3,R10)   AN HONEST SOUL..SO GET NUMBER
         B     CHKRANGE               AND FINISH UP
         SPACE 1
SKIP03   TRT   2(4,R10),TRTNUMB    LAST TRAIN TO BROOKLYN?
         BNZ   SET4095             NOT FOUR DIGITS..TAKE THAT YOU CAD
         PACK  DBLEWORD,2(4,R10)   SIGH..GET THE NUMBER AND
         B     CHKRANGE                FINISH UP THIS MESS
         SPACE 1
SETRC1   ZAP   DBLEWORD,=PL1'1'    INDICATE      Y  E  S
         B     CHKRANGE
         SPACE 1
SETRC2   ZAP   DBLEWORD,=PL1'2'    INDICATE       N  O
         B     CHKRANGE
         SPACE 1
SET4095  ZAP   DBLEWORD,=PL3'4095' INDICATE      W T F ?
         SPACE 1
CHKRANGE CP    DBLEWORD,=PL1'0'    WAS ANYONE HONEST?
         BL    SET4095               TSK, TSK
         CP    DBLEWORD,=PL3'4096' HOW ABOUT THE HIGH RANGE?
         BNL   SET4095               TSK, TSK
         CVB   R15,DBLEWORD        NOW DONE...GET IN BINARY
         L     R13,SAVE+4          RESTORE SAP
         RETURN (14,12),RC=(15)      AND SPLIT BACK........
         EJECT
         LTORG
         SPACE 1
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END
