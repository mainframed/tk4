*          DATA SET TSO032     AT LEVEL 001 AS OF 07/15/77
TSO032   TITLE 'TSO032 - THE DISPLAY DOMAIN COMMAND'
*.....................................................................*
*                                                                     *
*  THIS IS THE DDMN (DISPLAY DOMAIN) COMMAND FOR TSO.                 *
*                                                                     *
*  IT WILL DISPLAY THE STATUS OF THE DOMAINS FOR THE CURRENT IPS      *
*    IN THE SYSTEM.                                                   *
*                                                                     *
*  CODE IS PROBLEM-STATE.      K TRUE    15 JUL 77                    *
*                                                                     *
*  FOR VS2.03.807 (MVS)                                               *
*                                                                     *
*                                                                     *
*.....................................................................*
         SPACE
TSO031   PGM
         EJECT
STARTIT  EXTRACT MF=(E,EXTRACT)
         L     R1,ANSWER            (R1)=ADDR OF TSO FLAG FROM EXTRACT
         MVC   TSOFLAG,0(R1)        MOVE IN THE ANSWER TO OUR FLAG
         SPACE
INIT     L     R2,16               (R2)=ADDR OF CVT
         L     R2,CVTOPCTP(R2)     (R2)=ADDR OF RMCT
         L     R3,RMCTDMDT(R2)     (R3)=ADDR OF DMDT (FIRST ENTRY)
         XR    R4,R4               CLEAR REG 4
         ICM   R4,B'0011',RMCTDMNC(R2)   (R4)=NUMBER OF DOMAINS
         LTR   R4,R4               ANYBODY HOME?
         BNP   $$EOJ                NOPE-> EXIT STAGE LEFT
         USING DMDT,R3             ESTABLISH ADDR TO DOMAIN DESC. TABLE
         SPACE
         L     R5,RMCTWMST(R2)     (R5)=ADDR WMST
         MVC   MIPS,4(R5)          MOVE IN IPS NUMBER TO MSGHDR1
         LA    R1,MSGHDR1
         BAL   R6,PUTMSG
         LA    R1,MSGHDR2          POKE OUT THE HEADER MESSAGES
         BAL   R6,PUTMSG
         EJECT
LOOP     MVI   MSG,C' '            CLEAR OUT THE MESSAGE BUFFER
         MVC   MSG+1(L'MSG-1),MSG    WITH BLANKS
         LA    R1,DMDTNO
         LA    R2,MNO
         BAL   R6,EDIT             GO EDIT DOMAIN NUMBER INTO MSG
         CLI   MNO+2,C'-'          DOMAIN=0?
         BNE   CONTINUE             NO-> SKIP FIXUP
         MVI   MNO+2,C'0'
CONTINUE DS    0H
         SPACE
         LA    R1,DMDTLO
         LA    R2,MLO
         BAL   R6,EDIT             GO EDIT MIN MPL INTO MSG
         SPACE
         LA    R1,DMDTHI
         LA    R2,MHI
         BAL   R6,EDIT             GO EDIT MAX MPL INTO MSG
         SPACE
         LA    R1,DMDTWT
         LA    R2,MWT
         BAL   R6,EDIT             GO EDIT WEIGHTING FACTOR INTO MSG
         SPACE
         LA    R1,DMDTMPLT
         LA    R2,MMPLT
         BAL   R6,EDITH            GO EDIT CURRENT MPL TARGET INTO MSG
         SPACE
         LA    R1,DMDTRUA
         LA    R2,MRUA
         BAL   R6,EDITH            GO EDIT AVE # OF READY USRS INTO MSG
         SPACE
         LA    R1,DMDTGOOU
         LA    R2,MGOOU
         BAL   R6,EDITH            GO EDIT CURRENT # USRS GOING OUT
         SPACE
         LA    R1,DMDTCMPL
         LA    R2,MCMPL
         BAL   R6,EDITH            GO EDIT CURRENT MPL INTO MSG
         SPACE
         LA    R1,DMDTOUTU
         LA    R2,MOUTU
         BAL   R6,EDITH            GO EDIT CURNT # USRS ON OUT QUEUE
         SPACE
         LA    R1,DMDTINCU
         LA    R2,MINCU
         BAL   R6,EDITH            GO EDIT CURNT # SWAPBL IN CORE USERS
         SPACE
         LA    R1,WTOMSG
         BAL   R6,PUTMSG
         SPACE 2
         LA    R3,DMDTLEN(R3)      BUMP POINTER
         BCT   R4,LOOP             LOOP THROUGH ALL DOMAINS
*
         B     $$EOJ
         EJECT
*.....................................................................*
*     LOCAL SUBROUTINES   EDIT,EDITH  (USES REG 5)                    *
*.....................................................................*
         SPACE
EDIT     SR    R5,R5               CLEAR WORK REGISTER
         ICM   R5,B'0001',0(R1)    GET BYTE TO CONVERT
         C     R5,=F'999'          TO LARGE?
         BNH   EDIT1
         MVC   0(3,R2),=C'***'     INDICATE TOO LARGE
         BR    R6                    AND RETURN
EDIT1    CVD   R5,CVDWORK          CONVERT TO DECIMAL
         MVC   0(3,R2),=X'202020'  MOVE IN MASK
         BCTR  R2,0                DECREMENT POSITION FOR SPACE
         ED    0(4,R2),CVDWORK+6     BEFORE NUMBER AND EDIT
         CLC   1(3,R2),=C'   '     ANYTHING THERE
         BE    EDIT2
         BR    R6
EDIT2    MVI   3(R2),C'-'          INDICATE PLACE HOLDER
         BR    R6                  RETURN
         SPACE 3
EDITH    SR    R5,R5               CLEAR WORK REGISTER
         ICM   R5,B'0011',0(R1)    GET HALFWORD TO CONVERT
         C     R5,=F'999'          TO LARGE?
         BNH   EDITH1
         MVC   0(3,R2),=C'***'     INDICATE TOO LARGE
         BR    R6                    AND RETURN
EDITH1   CVD   R5,CVDWORK          CONVERT TO DECIMAL
         MVC   0(3,R2),=X'202020'  MOVE IN MASK
         BCTR  R2,0                DECREMENT POSITION FOR SPACE
         ED    0(4,R2),CVDWORK+6     BEFORE NUMBER AND EDIT
         CLC   1(3,R2),=C'   '     ANYTHING THERE
         BE    EDITH2
         BR    R6
EDITH2   MVI   3(R2),C'-'          INDICATE PLACE HOLDER
         BR    R6                  RETURN
         EJECT
*.....................................................................*
*        LOCAL SUBROUTINE FOR IO TO OPER/USER                         *
*.....................................................................*
         SPACE
*
* ENTER WITH WTO,WTOR LIST FORM POINTED TO BY R1
*
*    BAL  R6,PUTMSG         OR
*    BAL  R6,PUTGET
*
*  USES R14 FOR WORK
*
*
PUTMSG   DS    0H
         TM    TSOFLAG,X'80'       IS IT TSO?
         BO    TPUTIT              YES-> GO DO TPUT
         SVC   35                  NO-> USE WTO
         BR    R6
TPUTIT   DS    0H
         LH    R0,0(,R1)           GET LENGTH OF WTO MESSAGE
         S     R0,=F'4'            SUBTRACT OFF HEADER
         LA    R1,4(,R1)           BUMP MSG ADDRESS PAST HDR
         TPUT  (1),(0),R           DO THE TPUT
         BR    R6
         SPACE 2
PUTGET   DS    0H
         TM    TSOFLAG,X'80'       IS IT TSO?
         BO    TSOIT               YES-> GO USE TGET
         LR    R14,R1              NO-> USE WTOR .. SAVE MSG ADDRESS
         SVC   35                  DO THE WTOR...
         L     R1,4(,R14)          GET ADDR OF ECB
         XC    0(4,R1),0(R1)       CLEAN OUT THE ECB....
         LA    R0,1
         SVC   1                   WAIT FOR WTOR TO COMPLETE
         B     PUTRET              RETURN
TSOIT    DS    0H
         LR    R14,R1              SAVE
         LH    R0,8(,R1)           PUT LENGTH
         S     R0,=F'4'
         LA    R1,12(,R1)          PAST HDR
         TPUT  (1),(0),R
         SR    R0,R0
         IC    R0,0(,R14)          GET REPLY LENGTH
         L     R1,0(,R14)          GET REPLY ADRS
         LA    R1,0(,R1)
         ICM   R1,B'1000',=X'80'   INDICATE TGET...
         TGET  (1),(0),R
PUTRET   DS    0H
         L     R1,0(,R14)          GET REPLY ADRS
         XR    R0,R0
         IC    R0,0(,R14)          GET REPLY LENGTH
UPPER    DS    0H
         OI    0(R1),C' '          UPPER CASE
         LA    R1,1(,R1)           NXT
         BCT   R0,UPPER
         BR    R6
         EJECT
*.....................................................................*
*        VARIABLES                                                    *
*.....................................................................*
         SPACE 2
CVDWORK  DC    D'0'                WORK AREA
ANSWER   DC    F'0'                ADDRESS POINTER FOR EXTRACT
TSOFLAG  DC    X'00'               ='80' IF TSO. '00' IF NOT TSO
*
         SPACE 2
         EJECT
*.....................................................................*
*        LIST FORMS OF MACROS                                         *
*.....................................................................*
         SPACE 2
EXTRACT  EXTRACT ANSWER,'S',FIELDS=(TSO),MF=L
         EJECT
*.....................................................................*
*        MESSAGES AND MESSAGE MAPPING OVERLAYS                        *
*.....................................................................*
         SPACE
MSGHDR1  WTO   'IPS=XX MPL-LMT DMN ..MPL.. ....# USERS....',           X
               ROUTCDE=(11),MF=L
         ORG   MSGHDR1+8
MIPS     DS    CL2
         ORG
MSGHDR2  WTO   'DOMAIN MIN MAX WGT CUR TGT RDY  IN >OT OUT',           X
               ROUTCDE=(11),MF=L
*               456789.123456789.123456789.123456789.123456789.12345
*                XXX   XXX XXX XXX XXX XXX XXX XXX XXX XXX
WTOMSG   WTO   '                                            ',         X
               ROUTCDE=(11),MF=L
         ORG   WTOMSG+4
MSG      DS    CL43
         ORG   WTOMSG+5
MNO      DS    CL3
         ORG   WTOMSG+11
MLO      DS    CL3
         ORG   WTOMSG+15
MHI      DS    CL3
         ORG   WTOMSG+19
MWT      DS    CL3
         ORG   WTOMSG+23
MCMPL    DS    CL3
         ORG   WTOMSG+27
MMPLT    DS    CL3
         ORG   WTOMSG+31
MRUA     DS    CL3
         ORG   WTOMSG+35
MINCU    DS    CL3
         ORG   WTOMSG+39
MGOOU    DS    CL3
         ORG   WTOMSG+43
MOUTU    DS    CL3
         ORG
         EJECT
*.....................................................................*
*        LITERAL POOL                                                 *
*.....................................................................*
         SPACE
         LTORG
         EJECT
*.....................................................................*
*        MAPPING DSECT FOR DMDT (DOMAIN DESCRIPTOR TABLE)             *
*.....................................................................*
         SPACE
DMDT     DSECT
DMDTNO   DS    XL1             DOMAIN NUMBER
DMDTLO   DS    XL1             MIN  MPL LEVEL
DMDTHI   DS    XL1             MAX  MPL LEVEL
DMDTWT   DS    XL1             WEIGHTING FACTOR
DMDTMPLT DS    XL2             CURRENT MPL TARGET
DMDTRUA  DS    XL2             AVE # OF READY USERS
DMDTGOOU DS    XL2             CURRENT # USERS GOING OUT
DMDTCMPL DS    XL2             CURRENT MPL
DMDTOUTU DS    XL2             CURRENT # USERS ON OUT Q
DMDTINCU DS    XL2             CURRENT # SWAPPABLE INCORE USERS
DMDTRUC  DS    XL4             ACCUM FOR READY USER AVERAGE
DMDTEND  DS    0X              END OF DMT
DMDTLEN  EQU   *-DMDT
         SPACE 2
*
* EQUATES....
*
CVTOPCTP EQU   X'25C'          POINTER TO RMCT
RMCTDMDT EQU   X'B4'           POINTER TO DOMAIN TABLE
RMCTDMNC EQU   X'BC'           COUNT OF NUMBER OF DOMAINS
RMCTWMST EQU   X'18'           ADDRESS OF WLM SPEC TABLE
         SPACE 2
         END
