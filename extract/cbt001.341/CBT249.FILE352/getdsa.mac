*          DATA SET GETDSA     AT LEVEL 001 AS OF 06/29/78
         MACRO
&L      GETDSA &LABEL=,&FLAG=CDSA+CDPR,&END=SAVEND,&BASE=AR,&PARM=BR
         LCLA  &I,&J
         LCLC  &LB
         AIF   (T'&L EQ 'O').NOLAB
&LB      SETC  '&LABEL'
         AIF   (T'&LABEL NE 'O').LABOK
&LB      SETC  '@&SYSNDX'
.LABOK   USING IBMBZTCA,CR
         USING IBMBZDSA,DR
         USING &L,BR
&I       SETA  K'&L
         DS    0H
         AIF   (&I-2*(&I/2) EQ 1).LENODD
         DS    X .                      PAD TO FORCE LEN TO ODD LOC
.LENODD  DC    C'&L',AL1(&I)
         ENTRY &L
&L       STM   LR,CR,OFLR(DR)
         L     R1,CNAB .                CALCULATE NEW NAB
         LA    R0,&END-IBMBZDSA
         ALR   R0,R1
         CL    R0,TEOS .                WILL NEW DSA FIT IN CUR SEG?
         BNH   &LB
         DROP  BR
         L     BR,TOVF .                V(IBMBPGRC) FROM TCA
         BALR  LR,BR
&LB      XC    0(COCL-IBMBZDSA,R1),0(R1)
         BALR  &BASE,0
         USING *,&BASE
         AIF   (T'&PARM EQ 'O').NOPARM
         L     &PARM,OFR1(,DR)
.NOPARM  ST    R0,OFNB(,R1) .           STORE NEW NEXT_AVAILABLE_BYTE
         ST    DR,OFDR(,R1) .           SAVE DYNAMIC BACK CHAIN
         ST    R1,OFCF(,DR) .           SAVE DYNAMIC FORWARD CHAIN
         MVC   OFLW(4,R1),CLWS
         LR    DR,R1 .                  ESTABLISH NEW DISPLAY
         ST    R5,CAAD .                SAVE STATIC BACK CHAIN
         MVI   CFF0,&FLAG
         MEXIT
.NOLAB   MNOTE 12,'LABEL REQUIRED - GENERATION TERMINATED'
         MEND
