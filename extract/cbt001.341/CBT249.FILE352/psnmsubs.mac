*          DATA SET PSNMSUBS   AT LEVEL 002 AS OF 10/30/80
         MACRO
&TAG     PSNMSUBS   &TO,&LTO
         LCLC  &L,&C
&L       SETC  'L'''
         AIF   ('&TO'(1,1) NE '(').TONR
&C       SETC  '&TO'(2,K'&TO-2)
         AIF   ('&C' EQ '1' OR '&C' EQ 'R1').SETLENG
         LR    R1,&C
         AGO   .SETLENG
.TONR    ANOP
         LA    R1,&TO             RECEIVING FIELD
.SETLENG ANOP
         AIF   (T'&LTO EQ 'O').NOTOL
         AIF   ('&LTO'(1,1) NE '(').NOTLR
&C       SETC  '&LTO'(2,K'&LTO-2)
         AIF   ('&C' EQ '0' OR '&C' EQ 'R0').CALL
         LR    R0,&C              LENGTH IN REGISTER
         AGO   .CALL
.NOTLR   ANOP
         AIF   ('&LTO' EQ '0').ZLENG
         LA    R0,&LTO            LITERAL LENGTH
         AGO   .CALL
.ZLENG   ANOP
         SR    R0,R0              NO LENGTH RESTRICTION
         AGO   .CALL
.NOTOL   ANOP
         LA    R0,&L&TO           IMPLIED LENGTH
.CALL    ANOP
         L     R15,PSWSUBST
         BALR  R14,R15
.*
.EXIT    ANOP
         MEND
