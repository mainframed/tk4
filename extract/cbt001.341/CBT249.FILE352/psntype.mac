*          DATA SET PSNTYPE    AT LEVEL 002 AS OF 10/30/80
         TITLE 'PSNTYPE - DETERMINE TYPE OF JCL CARD'
PSNTYPE  PSNMSTRT  SASIZ=WKLENG
         EJECT
*                    TO DETERMINE THE TYPE OF CARD COMING NEXT
*                    READ IN THE NEXT CARD, AND SCAN FOR A NAME
*                    DDNAME, STEPNAME, OR PROCNAME, AND FOR THE
*                    TYPE OF CARD IT IS...IT IS....IT IS.
*                    CAN ALSO RETURN END OF FILE, COMMENT,
*                    OR UNRECOVERABLE ERROR - GO TO NEXT CARD.
*
         MVC   PSWTNAME,PSWBLANK  CLEAR THE NAME
         ZAP   PSWCARDN,PSWPZERO+3(2) ZERO THE COUNTER
         L     R15,PSWREAD        ADDRESS THE READER
         BALR  R14,R15            AND FETCH IN THE NEXT CARD
         LTR   R15,R15            DID IT GO PROPERLY
         BNZ   ENDOFIT            END OF MEMBER REACHED
         L     R8,PSWRNEXT        ADDRESS START OF THE CARD
         LA    R8,2(,R8)          POINT TO START OF NAME
         CLI   0(R8),C'*'         IS IT A COMMENT CARD
         BE    COMMENT            YES - EASY CHOICE TO MAKE
         CLI   0(R8),C' '         IS THERE A NAME HERE
         BE    NAMEDONE           NO - BUT NOT NECESSARILY BAD, YET
*                    SCAN FOR THE NAME
         L     R15,PSWTRKEY       ADDRESS TRT TABLE FOR KEYS
         L     R1,PSWREND         POINT TO LAST GOOD BYTE OF DATA
         LR    R7,R8              SAVE STARTING POINT HERE
         SR    R1,R8              COMPUTE LENGTH FOR THE SCAN
         EX    R1,SCAN            AND SCAN FOR THE DELIMITER
         BZ    BADSCAN            COME NOW - NEED A DELIMITER
         SR    R1,R8              COMPUTE LENGTH OF THE NAME
         BCTR  R1,0               LESS FOR THE EX, OF COURSE
         CH    R1,PSWHALF8
         BNL   BADNAME            THIS IS BAD, TOO
         EX    R1,SAVENAME        MOVE THIS NAME TO HOLDAREA
         LA    R8,2(R1,R8)        INDEX ON PAST THAT NAME
         LR    R7,R8              CHECKPOINT FOR POSSIBLE ERROR
*
NAMEDONE EQU   *
         CLI   0(R8),C' '         LOOK FOR THE KEYWORD, NOW
         BNE   ENDSCAN            AHA...
         LA    R8,1(,R8)          AND BEGIN THE CREEPING SEARCH
         C     R8,PSWREND         BUT NOT TO GO TOO FAR
         BL    NAMEDONE           KEEP LOOKING
         B     BADSCAN            OOPS - REPORT AND LEAVE
ENDSCAN  EQU   *                  HAVE FOUND START OF IT
         LR    R1,R8              COMPUTE THE COLUMN NUMBER
         S     R1,PSWRNEXT        IT STARTS IN
         STH   R1,PSWCOLTP        COLUMN OF THE TYPE CODE
         LR    R7,R8              SET UP THE ERROR POINTER
         CLC   PSWCPROC,0(R8)    CHECK FOR PROC STATEMENT
         BE    HAVPROC
         CLC   PSWCEXEC,0(R8)    AND THE EXEC STATEMENT
         BE    HAVEXEC
         CLC   PSWCDD,0(R8)      DD STATEMENTS COME OFTEN
         BE    HAVDD
*                    THE TYPE IS NOT KNOWN TO US
         PSNMERR  ERRNOTYP
         LA    R15,5              CODE FOR HIGHLY UNKNOWN
         B     RETURN
SCAN     TRT   0(0,R8),0(R15)     SCAN FOR DELIMITERS
SAVENAME MVC   PSWTNAME(0),0(R8)  SAVE NAME OF THIS ENTRY
*
*                    THREE WAYS TO GO HERE, THEN RECOMBINE
HAVPROC  EQU   *
         OI    PSWSTAT1,PSWS1PRC  SHOW A PROC FOUND
         LA    R15,2              CODE FOR A PROC STATEMENT
         LA    R8,5(,R8)          INDEX PAST THE PROC STATEMENT
         B     LASTSCAN
*
HAVEXEC  EQU   *
         OI    PSWSTAT1,PSWS1EXC  SHOW AN EXEC STATEMENT FOUND
         LA    R15,1              CODE FOR THE EXEC
         LA    R8,5(,R8)          INDEX PAST THIS ONE
         B     LASTSCAN
*
HAVDD    EQU   *
         OI    PSWSTAT1,PSWS1DD   SHOW THE DDCARD CAME IN
         LA    R15,0              CODE FOR THE DD STATEMENT
         LA    R8,3(,R8)          AND PAST THE DD
         B     LASTSCAN
*
*                    POINT TO START OF THE REAL 'STUFF'
LASTSCAN EQU   *
         CLI   0(R8),C' '         LOOK FOR FIRST NON-BLANK
         BNE   LASTHIT            AND THIS IS THE ONE
         LA    R8,1(,R8)          WELL, MAYBE NOT YET
         C     R8,PSWREND         AND HAS IT GONE TOO LONG
         BL    LASTSCAN           NOT YET, ANYWAY
*                  PASS BACK START AND END ADDRESS AS IDENTICAL
         MVC   PSWRSTRT,PSWREND   MAKE THEM THE SAME, THEN
         B     RETURN             AND LEAVE HERE
*
LASTHIT  EQU   *
         ST    R8,PSWRSTRT        PASS BACK STARTING POINT
         B     RETURN
*
*
ENDOFIT  EQU   *                  END OF FILE ON THIS PROC
         LA    R15,4              PASS BACK EOF CODE
         B     RETURN
*
COMMENT  EQU   *                  THIS IS A COMMENT CARD
         LA    R15,3              CODED AS A COMMENT
         B     RETURN
*
BADSCAN  EQU   *                  STUMBLED SOMEWHERE, HERE
         PSNMERR  ERRISCAN,((R7),8)
         LA    R15,5        CODE FOR A REAL PROBLEM
         B     RETURN
*
BADNAME  EQU   *                  NAME WENT OVER 8 CHARACTERS
         PSNMERR  ERRNGT8
         LA    R15,5              CODE TO GO NO FURTHER
         B     RETURN
         EJECT
WORKAREA DSECT
         DS    18F
         DS    X
WKLENG   EQU   *-WORKAREA
         EJECT
         PSNMWORK
         END
