*          DATA SET MAPDISK3   AT LEVEL 002 AS OF 05/24/78
*          DATA SET FASTMAP3   AT LEVEL 001 AS OF 11/09/77
SORT TITLE '                 FASTMAP3 -- SORTIT'
*     FUNCTION TO SORT FORMAT1 DSCBS INTO A ADDRESS TABLE BY DSNAMES
* INPUT   R1 = A(START OF THE DATA CHAIN FROM FASTDISK)
*         R0 = A(TABLE SPACE TO BE USED)
*         R2 = A(MAX DYNAMIC CORE)
* OUTPUT  R15= A(MAX CORE USED)
*
*  F.G.ROSS  1/10/73
*  UPDATED 29MAR73,  GENE CZARCINSKI
*
FASTMAP3 START 0
         ENTRY SORTIT
R0       EQU   0             A(TABLE SPACE TO BE USED)
R1       EQU   1             A(START OF THE DSCB DATA CHAIN)
R2       EQU   2             BASE REG
R3       EQU   3             START OF TABLE
R4       EQU   4             CURRENT TABLE POSITION
R5       EQU   5             A(CURRENT DSCB)
R6       EQU   6             CURRENT END OF TABLE
R7       EQU   7             A(TABLE DSCB)
R8       EQU   8             INDEX VALUE
R9       EQU   9             WORK
R10      EQU   10            WORK
R11      EQU   11            ZERO VALUE
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
*
SORTIT   SAVE  (14,12),,*
         LR    R12,R15                 INIT THE BASE REG.
         USING SORTIT,R12
* NEW SAVE AREA NOT GOTTEN...NOT NEEEDED
         XR    R11,R11
         LR    R3,R0      GET A(TABLE
         ST    R1,0(R3)    SAVE A(VTOC)
         LA    R3,4(R3)      BUMP TO THE FORMAT 1 AREA
         ST    R3,SORTAB    SAVE A(TABLE+4)
         LR    R6,R3
         LA    R8,4    SET INDEX VALUE
         LR    R5,R1     GET FIRST DSCB ADDRESS
CKFST    CLI   60(R5),C'1'    FORMAT ONE
         BE    STFST
         L     R5,4(R5)    GET NEXT ENTRY
         LTR   R5,R5
         BZ    END
         B     CKFST
STFST    ST    R5,0(R3)     PUT IN THE FIRST POS
         ST    R11,4(R6)     MARK THE END
         AR    R6,R8    INCR THE TABLE END
         CR    R6,R2                   END OF CORE?
         BNL   END2                    YES...ABORT
INCR     L     R5,4(R5)     GET THE NEXT DATA AREA
         LTR   R5,R5     CHECK FOR END
         BZ    END
NOTFRST  L     R4,SORTAB
         CLI   60(R5),C'1'    FORMAT ONE
         BNE   INCR
CKIT     L     R7,0(R4)     GET A(DSCB)
         CLC   16(44,R5),16(R7)      SEE IF HIGH OR LOW
         BL    INSERT  IF LOW THEN INSERT IT BEFORE
         AR    R4,R8         BUMP TO NEXT TABLE ENTRY
         CR    R4,R6     SEE IF AT END OF TABLE
         BNE   CKIT                    IF NOT, LOOP.
STEND    ST    R5,0(R6)     STORE ADDRESS AT END OF TABLE
         ST    R11,4(R6)    MARK END OF TABLE
         AR    R6,R8    UP CURRENT TABLE END ADDRESS
         CR    R6,R2                   END OF CORE?
         BNL   END2                    YES...ABORT
         B     INCR     GO DO ANOTHER
INSERT   LR    R9,R6     GET EOT
         SR    R9,R4      GET NUMBDR OF BYTES TO MOVE
         SRL   R9,2     DIVIDE BY FOUR TO FIND NO OF ENTRYS
         LR    R10,R6    SAVE
         SR    R10,R8    BACK UP ONE
         AR    R6,R8     UP TO THE NEW END
         CR    R6,R2                   END OF CORE?
         BNL   END2                    YES...ABORT
         ST    R11,0(R6)     MARK THE END OF THE TABLE
MOV      MVC   4(4,R10),0(R10)     SHOVE IT DOWN
         SR    R10,R8     BACK UP BY FOUR
         BCT   R9,MOV
         ST    R5,0(R4)      MOV IN THE NEW ENTRY
         B     INCR    GO GET ANOTHER
END      ST    R11,0(R6)    MARK THE END OF THE TABLE
END2     LR    R15,R6                  RETURN A(MAX CORE USED)
         RETURN (14,12),T,RC=(15)      RESTORE REGS & RETURN
*
SORTAB   DC    F'0'
         END
