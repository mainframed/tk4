*          DATA SET R050A30    AT LEVEL 001 AS OF 08/15/79
R050A30 TITLE 'DNYAMIC ALLOC VOL'
*.....................................................................*
*.                                                                   .*
*.  SUBROUTINE - R050A30                                             .*
*.                                                                   .*
*.  PURPOSE - DYNAMICALLY ALLOCATE A VOLUME                          .*
*.                                                                   .*
*.  CALLING SEQUENCE:                                                .*
*.                                                                   .*
*.    CALL R050A30(MOUNT,VOLUME,DEVTYPE,RETCD,MESSAGE);              .*
*.                                                                   .*
*.  WHERE:                                                           .*
*.                                                                   .*
*.    MOUNT  - CL44 - COMMENT TO BE PASSED - FREE                    .*
*.    VOLUME - CL6  - VOLSER OF DSN TO BE SCRATCHED                  .*
*.    DEVTYPE- CL8  - ESOTERIC UNITYPE (E.G. C'3330-1',C'3330V' ETC) .*
*.    RETCDE - H    - RETURN CODE (SEE BELOW)                        .*
*.    MESSAGE- CL80 - SUCCESS/FAIL MESSAGES                          .*
*.                                                                   .*
*. ON RETURN -                                                       .*
*.                                                                   .*
*.    RETCD = 0    - NO PROBLEMS                                     .*
*.          =16    - UNABLE TO ALLOCATE VOLUME..SEE MESSAGE          .*
*.                                                                   .*
*.    MESSAGE - TEXT FILLED IN WITH RESULT OF OPERATIONS             .*
*.                                                                   .*
*.    THIS PGM IS A SHORTEN COPY OF KEN TRUE'S R963A30 FOR THE       .*
*.    DASDMON SYSTEM.                                                .*
*.                                                                   .*
*.                                                                   .*
*.                                                                   .*
*.  B WILLIAMS V1M0 10 QUG 79                                        .*
*.                                                                   .*
*.....................................................................*
         SPACE 2
*
R0       EQU   0
R1       EQU   1             PARM POINTER
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12            BASE REGISTER
R13      EQU   13            SAVE AREA
R14      EQU   14            RETURN ADDRESS
R15      EQU   15            EPA/RETURN CODE
         EJECT
R050A30  CSECT
         SAVE  (14,12),,*
         LR    R12,R15                 INIT BASE REG
         USING R050A30,R12
         LA    R15,SAVEAREA            INIT SAVE AREA
         ST    R13,4(R15)              BACKWARD CHAIN
         ST    R15,8(R13)              FORWARD CHAIN
         LR    R13,R15                 POINT TO CURRENT SAVE AREA
         SPACE
         EJECT
*.....................................................................*
*.       INITIALIZE AND PERFORM DYNAMIC ALLOCATION OF VOLUME         .*
*.....................................................................*
         SPACE 2
         ST    R1,PARMPTR         SAVE PARM POINTER FOR LATER
         LM    R2,R6,0(R1)        LOAD ADDRESS OF PARMS
         MVC   VOLUME,0(R3)       SAVE VOLUME
         MVC   UNIT,0(R4)         SAVE UNIT
         XC    RETCD,RETCD        CLEAR OUT RETURN CODE
         MVI   MESSAGE,C' '       CLEAR OUT MESSAGE WITH
         MVC   MESSAGE+1(L'MESSAGE-1),MESSAGE   BLANKS
         MVI   FLAGS,X'00'        INIT FLAGS
         SPACE
         CLC   VOLUME,LASTVOL     HAVE WE BEEN WITH THIS ONE?
         BE    RETURN             YES-> SKIP THE ALLOCATE
         SPACE
         LA    R1,8               SET TO SCAN 8 CHARACTERS
         LA    R2,UNIT            START AT UNIT
BLNKLOOP CLI   0(R2),C' '         IS IT A BLANK
         BE    BLNKFND            YES-> GO USE COUNT
         LA    R2,1(R2)
         BCT   R1,BLNKLOOP        AND LOOP BACK
BLNKFND  LA    R1,UNIT
         SR    R2,R1              (R2) = LENGTH OF UNIT
         STCM  R2,B'0011',UNITLEN SAVE IT FOR ALLOCATION
*
         XC    S99ERC(4),S99ERC   CLEAR OUT ALLOCATE ERC/INFO
         XC    FREERC(4),FREERC   CLEAR OUT FREE     ERC/INFO
         LA    R1,FREEPTR         LOAD ADDR OF SVC99 'FREE'
         DYNALLOC                  AND INVOKE DYNAM ALLOCATION
*
         LA    R1,S99RBPTR        LOAD ADDR OF SVC99 'ALLOCATE'
         DYNALLOC                  GO GET VTOC OF MSSVOL
*
         LTR   R15,R15            ANY PROBLEMS
         BNZ   CANTALLC           YES-> GO REPORT THEM
*                                 NO->  FIND DDCARD IN TIOT
         L     R1,16              (R1)= A(CVT)
         L     R1,0(R1)           (R1)= A(NEW/OLD TCB:ASCB)
         L     R1,4(R1)           (R1)= A(MY TCB)
         L     R1,12(R1)          (R1)= A(TIOT)
         LA    R1,24(R1)          SKIP OVER JOBNAMES
         EJECT
         SR    R2,R2              CLEAR REGISTER FOR LENGTH USE
LOOPTIOT CLC   =C'ALLOCDD',4(R1)  IS THIS THE ONE?
         BE    FOUNDIT            YES-> GO SAVE ADDRESS
         IC    R2,0(R1)           NO-> GET LENGTH OF ENTRY
         AR    R1,R2                ADD TO BUMP TO NEXT..
         CLI   0(R1),X'00'        IS THAT THE END OF TIOT?
         BNE   LOOPTIOT             NO-> GO TRY AGAIN
ENDTIOT  MVC   MESSAGE,EMSG3      MOVE IN MESSAGE
         MVC   E3VOL,VOLUME       MOVE IN VOLUME
         MVC   RETCD,=F'16'       SET RC=16
         B     RETURN
         SPACE
FOUNDIT  DS    0H
         LH    R4,18(R1)          (R4) = ADDR(UCB)
         LTR   R4,R4                ANY ONE HOME?
         BZ    ENDTIOT              NO--GRIPE AND QUIT
         MVC   DEVTYPE,16(R4)     SAVE DEVICE TYPE FROM UCB
         MVC   LASTVOL,28(R4)     SAVE VOL SER FOR LATER INVOKE
         B     RETURN
         EJECT
*.....................................................................*
*.       FORMULATE TEXT FOR SVC99 ALLOCATE FAILURE                   .*
*.....................................................................*
         SPACE 3
CANTALLC MVC   MESSAGE,EMSG2      MOVE IN ERROR MESSAGE
         MVC   E2VOL,VOLUME       MOVE VOLSER INTO MESSAGE
         CVD   R15,DBLWORD        CONVERT SVC 99 RETURN CODE
         MVC   EDITWRK,=X'40202120' TO NICE PRINTABLE DECIMAL
         ED    EDITWRK(4),DBLWORD+6
         MVC   E2RC,EDITWRK+2       ONLY USE LAST 2 BYTES..
*
         UNPK  E2ERC(5),S99ERC(3)   CONVERT DYNAM ALLOC ERR CODE
         NC    E2ERC,HEXMASK         TO PRINTABLE HEXADECIMAL
         TR    E2ERC,HEXTAB          AND FIX IT UP PRETTY
         MVI   E2ERC+4,C' '
*
         UNPK  E2INFO(5),S99INFO(3) CONVERT DYNAM ALLOC INFO
         NC    E2INFO,HEXMASK        CODE TO PRNTABLE HEX
         TR    E2INFO,HEXTAB         AND FIX IT UP PRETTY
         MVI   E2INFO+4,C' '
*
         MVC   RETCD,=F'16'         SET RETURN CODE
         B     RETURN               AND GO GET ANOTHER RECORD
         EJECT
*.....................................................................*
*.       RETURN TO CALLING PROGRAM                                   .*
*.....................................................................*
         SPACE 2
RETURN   L     R15,RETCD               LOAD ADDRESS OF TIOT ENTRY
         L     R1,PARMPTR
         LM    R1,R2,12(R1)
         STCM  R15,B'0011',0(R1)       SAVE RETURN CODE
         MVC   0(L'MESSAGE,R2),MESSAGE SAVE MESSAGE
*
         L     R13,SAVEAREA+4          GET PRIOR SAVE AREA ADDRESS
         L     R14,12(R13)             LOAD RETURN ADDRESS
         LM    R0,R12,20(R13)          RESTORE REGS..LEAVE R15 INTACT
         BR    R14                     AND RETURN
         EJECT
*.....................................................................*
*.       VARIABLES                                                   .*
*.....................................................................*
         SPACE 2
SAVEAREA DC    18F'0'
DBLWORD  DC    D'0'               DOUBLE WORD WORK AREA
PARMPTR  DC    F'0'
RETCD    DC    F'0'
EDITWRK  DC    CL8' '
DSNAME   DC    CL44' '
LASTVOL  DC    CL6' '
DEVTYPE  DC    XL4'00'
VOLIST   DS    XL14          VOLUME LIST FOR SCR/UNCAT
         ORG   VOLIST        VOLUME LIST FOR OBTAIN/SCRATCH
         DC    AL2(1)        ONE ENTRY
V#DEVTYP DC    XL4'00'       DEVICE TYPE
V#VOLUME DC    CL6' '        VOLUME
V#ERC    DC    XL2'00'       SCRATCH ERROR CODE
         ORG
         SPACE
FLAGS    DC    B'00000000'
OKSCR    EQU   B'10000000'
OKLOCAT  EQU   B'01000000'
OKUNCAT  EQU   B'00100000'
         SPACE
         DS    0D
LOCAREA  DC    265X'00'      AREA TO READ IN THE CATALOG
         EJECT
*.....................................................................*
*.       CONSTANTS                                                   .*
*.....................................................................*
         SPACE 2
HEXTAB   DC    C'0123456789ABCDEF'
HEXMASK  DC    X'0F0F0F0F0F0F0F0F'
         EJECT
*.....................................................................*
*.       MESSAGES                                                    .*
*.....................................................................*
         SPACE 3
MESSAGE  DC    CL80' '
MSG1     DC    CL80'SCRATCHED FROM VOL=XXXXXX '
         ORG   MESSAGE+19
M1VOL    DS    CL6
         ORG   MESSAGE+27
M1UNCAT  DS    CL15
         ORG   MESSAGE+40
M1RC     DS    CL2
         ORG
         SPACE
MSG2     DC    CL80'ALREADY SCRATCHED FROM VOLUME (OBTAIN RC=08)'
         SPACE
EMSG1    DC    CL80'UNABLE TO OBTAIN FMT1 DSCB FOR DSN; OBTAIN RC=XX'
         ORG   MESSAGE+46
E1RC     DS    CL2
         ORG
         SPACE
EMSG2    DC    CL80'DYNAM ALLOC OF VOL=XXXXXX FAILED: R15=XX INFO=XXXX X
               DARC=XXXX'
         SPACE
         ORG   MESSAGE+19
E2VOL    DC    CL6' '
         ORG   MESSAGE+38
E2RC     DC    CL2' '
         ORG   MESSAGE+46
E2INFO   DC    CL4' '
         ORG   MESSAGE+56
E2ERC    DC    CL4' '
         ORG
         SPACE
EMSG3    DC    CL80'CAN''T LOCATE TIOT ENTRY FOR VOL=XXXXXX ..SCRATCH IX
               S BYPASSED'
         ORG   MESSAGE+32
E3VOL    DS    CL6
         ORG
         SPACE
EMSG4    DC    CL80'UNABLE TO SCRATCH..DSN IS PASSWORD PROTECTED FOR '
         ORG   MESSAGE+49
E4PWMODE DS    CL10
         ORG
         SPACE
EMSG5    DC    CL80'UNABLE TO SCRATCH DATSET; R15=XX  REASON=XX'
E5RC     ORG   MESSAGE+30
         DS    CL2
E5SCRCD  ORG   MESSAGE+41
         DS    CL2
         ORG
*
         EJECT
*.....................................................................*
*.       CAMLST FOR VTOC/CATALOG USE                                 .*
*.....................................................................*
         SPACE 2
SRCHLST  CAMLST SEARCH,DSNAME,VOLUME,LOCAREA  OBTAIN FMT1 DSCB
         SPACE 2
LOCLST   CAMLST NAME,DSNAME,,LOCAREA      LOCATE DSNAME IN CATALOG
         SPACE 2
UNCATLST CAMLST UNCAT,DSNAME              UNCATLG DSNAME
         SPACE 2
SCRLST   CAMLST SCRATCH,DSNAME,,VOLIST,,OVRD  SCRATCH (PURGE)
         SPACE 2
         EJECT
*.....................................................................*
*.       DYNAMIC ALLOCATION REQUEST BLOCK TO ALLOC VTOC OF MSS VOL   .*
*.....................................................................*
         SPACE 3
         DS    0F                 GET FULLWORD BOUNDARY
S99RBPTR DC    X'80',AL3(S99RB)   THE POINTER TO THE MESS..
*
S99RB    DC    FL1'20'            LENGTH OF RB IN BYTES = 20
         DC    XL1'01'            VERB CODE=X'01'..ALLOC BY DSN
         DC    AL2(0)             FLAGS1..NO OPTIONS
S99ERC   DC    XL2'0000'          ERROR CODE
S99INFO  DC    XL2'0000'          INFO CODE
         DC    AL4(S99TXPTR)      ADDRESS OF TEXT UNITS
         DC    XL4'00'            RESERVED
         DC    XL4'00'            FLAGS2
         SPACE 2
S99TXPTR DC    AL4(DDTU)          ADDR OF DDNAME TEXT UNIT
         DC    AL4(VOLTU)         ADDR OF VOLSER TEXT UNIT
         DC    AL4(UNITTU)        ADDR OF UNIT NAME TEXT UNIT
         DC    X'80',AL3(DISPTU)  ADDR OF DISP TEXT UNIT (LAST)
         SPACE 2
DDTU     DC    X'0001',X'0001',FL2'7',C'ALLOCDD' DDNAME
VOLTU    DC    X'0010',X'0001',FL2'6'
VOLUME   DC    CL6' '                       VOLUME TO ALLOCATE
UNITTU   DC    X'0015',X'0001'              UNIT NAME(ESOTERIC)
UNITLEN  DC    FL2'5'                   LENGTH OF UNIT NAME
UNIT     DC    CL8'3330V'               UNIT NAME AREA
DISPTU   DC    X'0004',X'0001',FL2'1',X'08'      SHR
         EJECT
*.....................................................................*
*.       DYNAMIC ALLOCATION REQUEST BLOCK TO FREE DDNAME ALLOCDD     .*
*.....................................................................*
         SPACE 3
         DS    0F                  GET FULLWORD BOUNDARY
FREEPTR  DC    X'80',AL3(FREERB)   THE POINTER TO THE MESS..
*
FREERB   DC    FL1'20'            LENGTH OF RB IN BYTES = 20
         DC    XL1'02'            VERB CODE=X'02'..FREE BY DDN
         DC    AL2(0)             FLAGS1..NO OPTIONS
FREERC   DC    XL2'0000'          ERROR CODE
FREEINFO DC    XL2'0000'          INFO CODE
         DC    AL4(FREETXPT)      ADDRESS OF TEXT UNITS
         DC    XL4'00'            RESERVED
         DC    XL4'00'            FLAGS2
         SPACE 2
FREETXPT DC    AL4(FDDNTU)        ADDR OF DSN TEXT UNIT
         DC    X'80',AL3(UNALCTU) ADDR OF UNALLOC TEXT TU(LAST)
         SPACE 2
FDDNTU   DC    X'0001',X'0001',FL2'7',C'ALLOCDD'  DDNAME
UNALCTU  DC    X'0007',X'0000'        UNALLOC EVEN IF PERM ALLOC
         EJECT
*.....................................................................*
*.       LITERAL POOL                                                .*
*.....................................................................*
         SPACE 2
         LTORG
         END
