*          DATA SET TSO044     AT LEVEL 001 AS OF 12/14/79
*          DATA SET UCBZAP     AT LEVEL 001 AS OF 07/14/78
UZAP     TITLE 'TSO044 - UNLOAD PRIV,RSVD 3330V VOLS-EXCEPT LIST '
***********************************************************************
*                                                                     *
* THIS IS THE TSO044 PROGRAM:                                         *
*                                                                     *
* PURPOSE:  TO FIND ALL 3330V UCBS THAT ARE MOUNTED PRIVATE RESERVED  *
*           AND NOT IN THE EXCLUDE LIST, AND ISSUE UNLOADS FOR THEM   *
*                                                                     *
*                                                                     *
* OPERATION-- ACTUAL WORK IS DONE ONLY AS A STARTED TASK..REQUIRES    *
*           AUTHORIZATION FOR MODESET MACRO.                          *
*                                                                     *
*           IF RUN ONLINE UNDER TSO...IT MERELY SHOWS WHAT IT WOULD   *
*           DO IF GIVEN THE CHANCE AS A STARTED TASK.                 *
*                                                                     *
*                                                                     *
* VERSION 1 MOD 0  - BOB JUCH/KEN TRUE   FAIRCHILD CAMERA & INSTRU.   *
*                    JULY 14 1978.                                    *
*                                                                     *
* VERSION 1 MOD 1  - K TRUE - 31OCT78 - CLEAN USE CNT FOR ONLINE,NRDY *
*                    3330V VOLUMES TOO.                               *
*                                                                     *
*          MACROS  REGEQU,MODESET,CVT,IEFUCBOB,WTO,SAVE,RETURN        *
*                                                                     *
*                                                                     *
* VERSION 1 MOD 0 - REFURBISHED UCBZAP INTO TSO044..K TRUE            *
*                   13DEC79                                           *
*                                                                     *
***********************************************************************
         MACRO
&LABEL   WRTMSG &ADDR
&LABEL   LA     R1,&ADDR
         BAL    LINKREG,PUTMSG
         MEND
         SPACE 2
         REGEQU
         EJECT
*.....................................................................*
*    MAINLINE - INIT ADDRESSABILITY AND ESTAB IF ONLINE               *
*.....................................................................*
         SPACE
TSO044   CSECT
         SAVE  (14,12),,TSO044.&SYSDATE..&SYSTIME..V1M0
         LR    R12,R15
         USING TSO044,R12
         LA    R2,SAVEAREA
         ST    R13,4(R2)
         ST    R2,8(R13)
         LR    R13,R2
         LA    R0,WORKLEN
         GETMAIN R,LV=(0)
         LR    R11,R1
         USING WORKAREA,R11
         SPACE 2
         EXTRACT MF=(E,EXTRACT)
         L     R1,ANSWER            (R1)=ADDR OF TSO FLAG FROM EXTRACT
         MVC   TSOFLAG,0(R1)        MOVE IN THE ANSWER TO OUR FLAG
         SPACE 2
START    BAL   LINKREG,EXCLUDE
         L     R7,CVTPTR           -> CVT
         L     R7,CVTILK2-CVT(R7)  -> UCB TABLE
         SLR   R8,R8
         EJECT
*.....................................................................*
*    LOOP OVER UCBS                                                   *
*.....................................................................*
         SPACE
LOOPTOP  ICM   R8,B'0011',0(R7)    -> GET UCB
         CLC   0(2,R7),=X'FFFF'    END OF THE WORLD
         BE    DONE                YES....EXIT STAGE LEFT
         LTR   R8,R8               ANYONE HOME
         BZ    INDEX                DUMMY ENTRY   TRY AGAIN
         LA    10,2(10)            -> NEXT UCB ENTRY
         LTR   R8,R8
         BZ    INDEX               IF DUMMY GO TRY AGAIN
         USING UCB,R8
         TM    UCBSTAT,UCBONLI     ONLINE ?
         BNO   INDEX
         TM    UCBTBYT3,UCB3DACC   DIRECT ACCESS?
         BNO   INDEX
         TM    UCBTBYT2,UCBRVDEV   VIRT DEVICE ?
         BNO   INDEX
         TM    UCBFLA,UCBNRY       ONLINE/NOTREADY?
         BO    INDEX               YES..SKIP
         TM    UCBDMCT,UCBMOUNT    IS MOUNT PENDING?
         BO    INDEX               YES..KISS OFF
         TM    UCBSTAB,UCBBPRV     PRIVATE   ?
         BNO   INDEX               NO..SKIP
         TM    UCBSTAT,UCBRESV     IS IT RESERVED??
         BNO   INDEX               NO ..
         BAL   LINKREG,CKUNIQUE
         B     INDEX
         SPACE
         EJECT
*.....................................................................*
*   HERE WE DO THE DIRTY DEED..UNLOAD THE TURKEY                      *
*.....................................................................*
         SPACE
DOUNLD   MVC   MSGUCB,UCBNAME      MOVE IN UNIT ADDRESS
         MVC   CMDUCB,UCBNAME        TO UNLOAD TOO
         MVC   MSGVOL,UCBVOLI      MOVE IN VOLSER
         WRTMSG WTOMSG             SAY WERE DOING IT
*        WRTMSG WTOCMD             SAY WE ISSUED THE COMMAND
         TM    TSOFLAG,X'80'       IS THIS ONLINE???
         BO    INDEX               YES...SKIP THE MODESET AND WORK
GOSUPER  MODESET MF=(E,SUPER)      GO SUPER
         SPACE
         LA    R1,WTOCMD           ISSUE THE COMMAND
         SLR   R0,R0                 TO UNLOAD TO
         SVC   34                       OS
         SPACE
FINSUPER MODESET MF=(E,PBLM)       BACK TO PROBLEM STATE...
         B     INDEX               LOOP TO NEXT UCB
         SPACE
         EJECT
INDEX    LA    R7,2(R7)            BUMP UCB PTR
*
         B     LOOPTOP             AND GO TRY AGAIN
         SPACE 2
DONE     L     13,SAVEAREA+4       FINISH---RETURN TO OS......
         RETURN (14,12),RC=0
         EJECT
*.....................................................................*
*    LOCAL SUBROUTINE CKUNIQUE   - TO CHECK IF WEVE SEEN THIS UCB     *
*                                  BEFORE                             *
*                                                                     *
*    ENTER WITH R8 = ADDR UCB                                         *
*    EXIT: TO (LINKREG)  IF ALREADY ON LIST                           *
*          TO 4(LINKREG) IF NOT PREVIOUSLY FOUND                      *
*.....................................................................*
LINKREG  EQU   R6
*
CKUNIQUE SR    R10,R10
COMPLIST CH    R10,LCT                  TOP OF LIST AND STILL NOT SEEN?
         BNL   ADDLIST                  YES-> SO GO ADD IT TO LIST
         CH    R8,LIST(R10)             CHK UCB
         BE    0(LINKREG)                SEEN BEFORE--EXIT
         LA    R10,2(R10)               BUMP LIST PTR
         B     COMPLIST                 AND GO CHECK AGAIN
ADDLIST  LH    R10,LCT                  LOAD CURRENT TOP OF LIST
         CH    R10,LMAX                 COMPARE TO MAX # ENTRIES IN LST
         BNL   0(LINKREG)               NO ROOM-> SAY WEVE SEEN IT
         STH   R8,LIST(R10)              GOT ROOM->SAVE UCB ADDR
         LA    R10,2(R10)                BUMP TOP OF LIST
         STH   R10,LCT                   AND SAVE IT
         SPACE
CHKEXCL  LH    R10,VOLCNT                LOOK IN VOLUME LIST
         LTR   R10,R10
         BZ    4(LINKREG)                 NOBODY HOME...NOT IN LIST
         LA    R1,VOLTABLE               LOAD ADDRESS OF VOLTABLE
CHKEXCL1 CLC   0(6,R1),UCBVOLI           IS THIS OUR VOLUME
         BE    0(LINKREG)                YES...NOT OUR BOY..
         LA    R1,6(R1)                  BUMP VOL POINTER
         BCT   R10,CHKEXCL1               AND TRY ANOTHER
         B     4(LINKREG)                NOT ON LIST...GOOD RETURN
*
         EJECT
*.....................................................................*
*    LOCAL SUBROUTINE EXCLUDE                                         *
*                                                                     *
*     OPEN SYSIN DATASET AND READ CARDS,                              *
*        IF A 'VOL=(SL,' STRING IS FOUND (A MOUNT COMMAND)            *
*        THEN EXTRACT VOLSER AND PUT IN VOLTABLE                      *
*                                                                     *
*     AT END, VOLTABLE WILL HAVE A LIST OF VOLSERS TO EXCLUDE FROM    *
*        THE AUTO UNLOAD PROCESS.                                     *
*.....................................................................*
         SPACE
*
EXCLUDE  OPEN  (SYSIN)
         TM    SYSIN+48,X'10'      DID IT OPEN?
         BZ    0(LINKREG)          NO...SKIP THIS MESS
         LA    R10,VOLTABLE        THIS IS EASIER THAN A MULTIPLY..
*
GETLOOP  GET   SYSIN,CARD          GO GET A CARD
         CALL  FIND,MF=(E,PARMLIST)   GO SEARCH THE CARD
FINFIND  CLC   LOC,=F'0'           ANYONE HOME?
         BE    GETLOOP               NO..GET ANOTHER
         L     R1,LOC              GET CHARACTER OFFSET
         LA    R2,CARD             GET CARD ADDRESS
         LA    R2,L'STRING-1(R2,R1) POINT TO AFTER 'VOL=(SL,'
MVCVOL   MVC   VOLSAVE,0(R2)       AND SAVE THE DUDE
*
         LA    R1,6                FIND THE TRAILING ) AND JUNK
         LA    R2,VOLSAVE
CHKJUNK  CLI   0(R2),C'A'          ANYTHING LESS THAN ALPHA/NUM?
         BNL   CHKJUNK1            NO..AGAIN
CLRJUNK  MVI   0(R2),C' '          YES..SET IT TO BLANK..
CHKJUNK1 LA    R2,1(R2)
         BCT   R1,CHKJUNK          LOOP, BUMP, AND GRIND....
*
         LH    R1,VOLCNT           LOAD VOLCNT
         CH    R1,VOLMAX           IS THERE ROOM
         BNL   EODAD                 NNOO....QUIT THIS MESS
MVCTABL  MVC   0(6,R10),VOLSAVE    MOVE IN VOLUME
         LA    R10,6(R10)          BUMP POINTER TO NEXT STORE LOC
         LA    R1,1(R1)
         STH   R1,VOLCNT           BUMP VOLCNT
         B     GETLOOP               AND GO GET ANOTHER
*
EODAD    CLOSE (SYSIN)
         BR    LINKREG             RETURN TO MAINLINE
         EJECT
*.....................................................................*
*        LOCAL SUBROUTINE FOR IO TO OPER/USER                         *
*.....................................................................*
         SPACE
*
* ENTER WITH WTO,WTOR LIST FORM POINTED TO BY R1
*
*    BAL  LINKREG,PUTMSG    OR
*    BAL  LINKREG,PUTGET
*
*  USES RXX FOR WORK
*
RXX      EQU   14                  WORK REGISTER
*
PUTMSG   DS    0H
         TM    TSOFLAG,X'80'       IS IT TSO?
         BO    TPUTIT              YES-> GO DO TPUT
         SVC   35                  NO-> USE WTO
         BR    LINKREG
TPUTIT   DS    0H
         LH    R0,0(,R1)           GET LENGTH OF WTO MESSAGE
         S     R0,=F'4'            SUBTRACT OFF HEADER
         LA    R1,4(,R1)           BUMP MSG ADDRESS PAST HDR
         TPUT  (1),(0),R           DO THE TPUT
         BR    LINKREG
         SPACE 2
PUTGET   DS    0H
         TM    TSOFLAG,X'80'       IS IT TSO?
         BO    TSOIT               YES-> GO USE TGET
         LR    RXX,R1              NO-> USE WTOR .. SAVE MSG ADDRESS
         SVC   35                  DO THE WTOR...
         L     R1,4(,RXX)          GET ADDR OF ECB
         XC    0(4,R1),0(R1)       CLEAN OUT THE ECB....
         LA    R0,1
         SVC   1                   WAIT FOR WTOR TO COMPLETE
         B     PUTRET              RETURN
TSOIT    DS    0H
         LR    RXX,R1              SAVE
         LH    R0,8(,R1)           PUT LENGTH
         S     R0,=F'4'
         LA    R1,12(,R1)          PAST HDR
         TPUT  (1),(0),R
         SR    R0,R0
         IC    R0,0(,RXX)          GET REPLY LENGTH
         L     R1,0(,RXX)          GET REPLY ADRS
         LA    R1,0(,R1)
         ICM   R1,B'1000',=X'80'   INDICATE TGET...
         TGET  (1),(0),R
PUTRET   DS    0H
         L     R1,0(,RXX)          GET REPLY ADRS
         XR    R0,R0
         IC    R0,0(,RXX)          GET REPLY LENGTH
UPPER    DS    0H
         OI    0(R1),C' '          UPPER CASE
         LA    R1,1(,R1)           NXT
         BCT   R0,UPPER
         BR    LINKREG
         EJECT
*.....................................................................*
*      CONSTANTS                                                      *
*.....................................................................*
         SPACE
EXTRACT  EXTRACT ANSWER,'S',FIELDS=(TSO),MF=L
         SPACE
SUPER    MODESET KEY=ZERO,MF=L
         SPACE
PBLM     MODESET KEY=NZERO,MF=L
         SPACE
PARMLIST CALL  ,(CARD,STRING,STRLEN,LOC,SCANSTRT,SCANEND),VL,MF=L
         SPACE
STRING   DC    C'VOL=(SL,'
         DS    0F
STRLEN   DC    AL4(L'STRING)
SCANSTRT DC    F'1'
SCANEND  DC    F'71'
         EJECT
*.....................................................................*
*      VARIABLES                                                      *
*.....................................................................*
         SPACE
SAVEAREA DC    18F'0'
ANSWER   DC    F'0'                ADDRESS POINTER FOR EXTRACT
VOLSAVE  DC    CL6' '
         DS    CL2
TSOFLAG  DC    X'00'               ='80' IF TSO. '00' IF NOT TSO
LOC      DC    F'0'
         SPACE
*.....................................................................*
*      MESSAGES                                                       *
*.....................................................................*
         SPACE
WTOMSG   WTO   'UCBUNLD - UNIT=XXX, VOL=XXXXXX  - UNLOAD ISSUED        X
                  ',ROUTCDE=(1,13),MF=L
         SPACE
         ORG   WTOMSG+19
MSGUCB   DS    CL3
         ORG   WTOMSG+28
MSGVOL   DS    CL6
         ORG
         SPACE
WTOCMD   WTO   'U XXX  ISSUED BY UCBUNLD',MF=L,ROUTCDE=(1,13)
         ORG   WTOCMD+6
CMDUCB   DS    CL3
         SPACE
         ORG
         EJECT
*.....................................................................*
*      LITERAL POOL                                                   *
*.....................................................................*
         SPACE
         LTORG
         EJECT
*.....................................................................*
*      INPUT CARD DCB                                                 *
*.....................................................................*
         SPACE
CARD     DC     CL80' '
         SPACE
SYSIN    DCB    DDNAME=SYSIN,LRECL=80,MACRF=(GM),DSORG=PS,EODAD=EODAD
         EJECT
*.....................................................................*
*      LARGE WORK AREA DSECT                                          *
*.....................................................................*
         SPACE
WORKAREA DSECT
*
VOLCNT   DC    H'0'                     NUMBER OF VOLS TO EXEMPT
LCT      DC    H'0'                     TO REMEMBER COUNT
LIST     DC    200H'0'                  TO REMEMBER EACH UCB ADDR
LSTLEN   EQU   *-LIST
VOLTABLE DC    60CL6' '                 VOLSERS TO EXEMPT FROM UNLOAD
VLSTLEN  EQU   *-VOLTABLE
         DS    0D
WORKLEN  EQU   *-WORKAREA
         SPACE 2
TSO044   CSECT
         DS    0H
LMAX     DC    AL2(LSTLEN)              TO REMEMBER UCB MAX ADDR
VOLMAX   DC    AL2(VLSTLEN/6)           NUMBER OF VOLS TO EXEMPT
         EJECT
CVT      DSECT
         CVT   SYS=VMS,TSO=YES
         EJECT
UCB      DSECT
         IEFUCBOB
         END
