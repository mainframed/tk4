*          DATA SET PSNPROC    AT LEVEL 002 AS OF 10/30/80
         TITLE 'PSNPROC - PARSE PROC STATEMENT'
PSNPROC  PSNMSTRT  SASIZ=WKLENG
         EJECT
*                 TO BEGIN PROCESSING THE PROCEDURE BY
*                 SCANNING THE PROC STATEMENT, LOOKING
*                 FOR ALL SUBSTITUTIONS, AND TABLING
*                 THEM FOR LATER PROCESSING BY THE REST
*                 OF THE PROCESSES
*
         L     R8,PSWASYM         ADDRESS THE SYMBOLIC BUILD AREA
         USING SYMBOL,R8
         MVC   PSWPCID,PSWTNAME   AS FOUND BY TYPE
         CLC   PSWPCID,PSWPNAME   DO THE NAMES MATCH
         BE    NAMEOK             YES - CARRY ON WITH TABLING
         PSNMERR  ERRPNAM,PSWPNAME,PSWPCID
NAMEOK   EQU   *
*                    FIND AND ACQUIRE THE COMMENTS, IF ANY
         L     R2,PSWTHISC        IS THERE A COMMENT STRING
         ST    R2,PSWHEADC        STORE COMMENT POINTER FOR THE PROC
         SR    R1,R1              NEED A HANDY ZEROED REGISTER
         ST    R1,PSWTHISC        CLEAR COMMENT POINTER
         ST    R1,PSWLASTC        FOR NEXT STEP, OR INSERTED ONE
         LTR   R2,R2              WAIT - WAS THERE A COMMENT BLOCK
         BNZ   HDCOMX             YES
         TM    PSWMFLG1,PSWM1NCM  NO COMMENT CHECK WANTED?          NTL
         BO    HDCOMX             RIGHT ON..SKIP THE GROUSE         NTL
         PSNMERR  ERRNOCMT,PSWCPROC
HDCOMX   EQU   *
*
         CLC   PSWRSTRT,PSWREND   ANY VALUE AT ALL
         BNE   NONULL             RELAX - HAVE SOME TO SUBSTITUTE
         XC    SYMBOL(SYMBLENG),SYMBOL   CLEAR THIS FIRST ENTRY
         LA    R1,SYMBOL+SYMBLENG MAKE IT JUST THIS LONG
         ST    R1,PSWASYM         .
         SR    R15,R15            RETURN WITH A SHORT RUN
         B     RETURN
NONULL   EQU   *
*
*
PROCFILL EQU   *
         L     R15,PSWSCAN        GO SCAN THIS
         BALR  R14,R15            AND DO A GOOD JOB
         LTR   R15,R15
         BNZ   RETURN             ANY PROBLEMS AND OUT....
*
         XC    SYMBOL(SYMBLENG),SYMBOL CLEAR THE AREA
         L     R15,PSWSKEY        PICK UP KEY ADDRESS
         LH    R1,PSWSKEYL        AND ITS LENGTH
         LTR   R15,R15            IS ANYTHING THERE
         BZ    NOKEY              NO - WHAT IS THIS, THEN
         CH    R1,PSWHALF8        CHECK FOR MAXIMUM LENGTH
         BL    KEYOK              MUST BE 7 OR UNDER
         PSNMERR ERRPKEYL,((R15),9)   REPORT IT
         LA    R1,8               AND USE THE FIRST 8
KEYOK    EQU   *
         MVC   SYMVALU,PSWBLANK   CLEAR FIELD TO BLANKS
         STH   R1,SYMLVALU        LENGTH OF THE VALUE
         BCTR  R1,0               SET FOR THE MOVE
         EX    R1,MOVNAME         PUT VALUE INTO SYM TABLE
         TM    PSWSCANI,PSWSIVAL+PSWSINOD   MUST HAVE AN EQUAL SIGN
         BNZ   EQULOK             WELL - LET THIS ONE GO
         PSNMERR  ERRNOEQL,SYMVALU,PSWCPROC    REPORT IT
EQULOK   EQU   *
         TM    PSWSCANI,PSWSICOM  ANY COMMENT INVOLVED
         BZ    *+8                NO - NO REAL PROBLEM HERE
         BAL   R14,INSRTCOM       YES - INSERT COMMENT POINTER
*
         LH    R1,PSWSDATL        LENGTH OF THE DATA PART
         TM    PSWSCANI,PSWSIVAL  WAIT - IS THERE ONE
         BZ    NOVALU             NO - BUT ZERO LENGTH IS USEFUL
         STH   R1,SYMLSUBS        LENGTH OF THE SUBSTITUTION DATA
         BCTR  R1,0               SETTING FOR THE MOVE
         L     R15,PSWSDAT        POINTER TO THE DATA
         EX    R1,MOVDATA         AND MOVE INTO PLACE
         TM    PSWSCANI,PSWSIVQT  WAS THIS VALUE QUOTED
         BZ    NOVALU             NO - FORGET I ASKED
         OI    SYMIND1,SYMI1VQT   TELL SUBSTITUTE ABOUT THE QUOTES
*
NOVALU   EQU   *
         LA    R1,SYMSUBS+4(R1)   POINT PAST THIS ENTRY
         SRL   R1,2               OF COURSE
         SLL   R1,2               ROUNDED,
         ST    R1,SYMNEXT         MAKE IT THE NEXT THING
         ST    R1,PSWASYM         SHOW AS NEXT OPEN SPOT
         LR    R9,R8              SAVE THIS OLD ONE FOR LAST CALL
         LR    R8,R1              NOW ADDRESSING THE NEW ONE
         LA    R1,256(,R1)        LOOK AHEAD A WAYS
         C     R1,PSWASYM+4       WOULD NEXT STILL FIT
         BH    SYMFULL            NO - REPORT AND QUIT
*
*                    CHECK WHAT TO DO NEXT
CKNEXT   EQU   *
         TM    PSWSCANI,PSWSIMOR  MORE DATA ON THIS CARD
         BZ    GETCHECK           NO - CHECK FOR MORE CARDS
         PSNMERR  ERR2P1L         REPORT AS AN EXCEPTION
         B     PROCFILL           AND HANDLE THE NEXT PART
*
GETCHECK EQU   *
         TM    PSWSCANI,PSWSIEND  IS THIS LAST CARD IN PROC
         BO    ENDSYMB            YES - CLEAR OUT
FETCHCRD EQU   *                  FETCH IN THE NEXT CARD
         L     R15,PSWREAD
         BALR  R14,R15            FETCH IT IN
         LTR   R15,R15
         BNZ   PROBEOF            PROBABLY EOF - COMPLAIN
         L     R1,PSWRNEXT        POINT TO NEXT/CURRENT CARD
         CLI   2(R1),C'*'         SNEAK A COMMENT INTO THE MIDDLE
         BNE   PROCFILL           ALL IN ORDER - GO AGAIN
*                    COMMENT IMBEDDED IN CONTINUATIONS - EAT IT
         B     FETCHCRD           BACK UNTIL ONE LOOKS GOOD
*
PROBEOF  EQU   *                  PROBABLY END OF FILE REACHED
         PSNMERR  ERREOF,PSWCPROC  PREMATURE EOF
         LA    R15,4              NO MORE ON THIS MEMBER
         B     RETURN
*
MOVNAME  MVC   SYMVALU(0),0(R15)  MOVE NAME TO ENTRY
MOVDATA  MVC   SYMSUBS(0),0(R15)  MOVE THE SUBST DATA
*
ENDSYMB  EQU   *
         LR    R8,R9              PICK OLD SAVED POINTER TO LAST
         SR    R15,R15            CODE FOR GOOD END
         ST    R15,SYMNEXT        SHOW END OF CHAIN, TOO
         B     RETURN
*
         EJECT
NOKEY    EQU   *
         PSNMERR   ERRNOKEY
         B     CKNEXT             TRY THE NEXT ONE, THEN
*
SYMFULL  EQU   *
         PSNMERR   ERRTBLFL,(PSWASYM+16,8)
         SR    R15,R15            GO WITH WHAT YOU GOT
         B     RETURN
         EJECT
*                    TO INSERT THE COMMENT POINTER AND
*                    SAVE THE COMMENT DATA
*
INSRTCOM PSNMBLOK  IC,RENT=YES
         L     R3,PSWSCOM         POINTER TO THE COMMENT STRING
         LH    R2,PSWSCOML        AND ITS LENGTH
         ST    R3,SYMCOMNT       WHERE THE COMMENT STARTS
         STC   R2,SYMCOMNT        AND ITS LENGTH
         B     ICEXIT
         EJECT
         PSNMSYM
*
*
WORKAREA DSECT
         DS    18F
         PSNMBLOK  RENT=REGS
WKLENG   EQU   *-WORKAREA
         EJECT
         PSNMWORK
         END
