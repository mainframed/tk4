*          DATA SET ACFDSTBL   AT LEVEL 008 AS OF 02/15/83
*          DATA SET ACFDSTBL   AT LEVEL 007 AS OF 05/09/82
*          DATA SET ACFDSTBL   AT LEVEL 006 AS OF 10/20/81
*          DATA SET ACFDSTBL   AT LEVEL 005 AS OF 08/16/81
ACFDSTBL TITLE 'DATASET NAME AND PREFIX TABLES FOR ACF     '
***********************************************************************
*                                                                     *
* NAME:     ACFDSTBL TABLES :                                         *
*                                                                     *
*  TABLES:  ADDRTAB   CONTAINS ADDR OF TABLES IN THIS MODULE          *
*                                                                     *
*           DSNTABLE  HAS ENTRIES FOR FASTPATH USAGE                  *
*                                                                     *
*           QUALTAB   HAS ENTRIES FOR EXTRACTING GROUP FROM DSNAME    *
*                                                                     *
*                                                                     *
* NOTE: MODULE CONTAINS NO EXECUTABLE CODE                            *
*                                                                     *
* AUTHOR: KEN TRUE - FAIRCHILD CAMERA AND INSTRUMENT  AUG 78          *
*        FOR RACF EXITS                                               *
*                                                                     *
*  MODS 1 - KEN TRUE - INTEL CORP - 14 AUG 81                         *
*           ADAPTED FOR INTEL NAMING CONVENTIONS- MODIFIED PREFIX     *
*           MACRO TO ADD 'APPEND' KEYWORD                             *
*  MODS 2 - KEN TRUE - INTEL CORP - 24 APR 82                         *
*           MADE 'PROD.IIC.' SELECT THIRD NODE EXCEPT FOR             *
*           'RESIDENT' IMIC DATASETS.                                 *
*                                                                     *
*  MODS 3 - KEN TRUE - INTEL CORP - 03 FEB 83                         *
*           ADDED 'OA=' OPERAND FOR OPS-ANALYST SUPPORT               *
*                                                                     *
*                                                                     *
*                                                                     *
*                                                                     *
*                                                                     *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*                                                                     *
* DSN MACRO..DEFINE DATASET FOR FASTPATH USAGE                        *
*                                                                     *
***********************************************************************
         MACRO
&LABEL   DSN   &ACCESS=READ,&GROUP=,&EXACT=YES,&OA=NO               NTL
         LCLC  &ACS,&GRP
         LCLB  &EX,&GP,&OO                                          NTL
         LCLA  &LEN                                                NTLC
&LEN     SETA  K'&SYSLIST(1)                                       NTLC
&GRP     SETC  ' '                    DEFAULT..NO GROUP CHANGE
&EX      SETB  1                      EXACT MATCH REQUIRED
&GP      SETB  0                      NO GROUP SUPPLIED
&OO      SETB  0                      DEFAULT=OPS ANALYST=NO        NTL
         AIF   ('&OA' NE 'YES').SKIPOA                              NTL
&OO      SETB  1                                                    NTL
.SKIPOA  ANOP  ,                                                    NTL
         AIF   (K'&GROUP EQ 0).NOGRP
         AIF   (K'&GROUP LE 8).DOGRP
 MNOTE 8,'GROUP NAME EXCEEDS 8 CHARACTERS..NO GROUP USED'
         AGO   .NOGRP
.DOGRP   ANOP
&GRP     SETC  '&GROUP'
&GP      SETB  1
.NOGRP   ANOP
         AIF   ('&EXACT' NE 'NO').SKPXACT
&EX      SETB  0
.SKPXACT ANOP
&ACS     SETC  '03'            SET DEFAULT ACCESS TO READ
         AIF   ('&ACCESS' EQ 'READ').LACCS
         AIF   ('&ACCESS' NE 'NONE').LWRITE
&ACS     SETC  '00'            SET TO 'NONE'
         AGO   .LACCS
.LWRITE  ANOP
         AIF   ('&ACCESS' NE 'WRITE').LALLOC
&ACS     SETC  '07'            SET ACCESS TO WRITE,READ,EXEC
         AGO   .LACCS
.LALLOC  ANOP
         AIF    ('&ACCESS' NE 'ALLOC').BADACS
&ACS     SETC   '0F'           SET ACCESS TO ALLOC,WRITE,READ,EXEC
         AGO    .LACCS
.BADACS  ANOP
         MNOTE  8,'INVALID ACCESS SPECIFIED, READ ACCESS ASSUMED'
.LACCS   ANOP
         LCLA   &GRPSIZ
&GRPSIZ  SETA   K'&GROUP
         SPACE  1
&LABEL   DC     AL1(&LEN)            LENGTH OF ENTRY               NTLC
         DC     X'&ACS'              ACCESS
         DC     BL1'&EX.&GP.&OO.00000'  FLAGS  EXACT,GROUP,OA
         DC     AL1(&GRPSIZ)         # OF CHARACTERS IN GROUP NAME NTL1
         DC     CL8'&GRP'            GROUP TO USE =' ' FOR NO CHANGE
         DC     C'&SYSLIST(1)'       ENTITY NAME                   NTLC
         SPACE  1
         MEND
         EJECT
***********************************************************************
*                                                                     *
* PREFIX  MACRO..DEFINE QUALIFER EXTRACTION RULES                     *
*                                                                     *
***********************************************************************
         MACRO
&LABEL  PREFIX &QUALNO=2,&MAXLEN=8,&NUMOF=0,&NUMLEN=0,&APPEND=,    NTLCX
               &OA=NO                                               NTL
         LCLA  &QCNT,&MLEN,&NO,&NL,&ALEN,&LEN                     NTLC
         LCLB  &APND,&ASUFX,&OO                                    NTL
         LCLC  &ASTRNG                                            NTLC
&QCNT    SETA  &QUALNO
&MLEN    SETA  &MAXLEN
&NO      SETA  &NUMOF
&NL      SETA  &NUMLEN
&LEN     SETA  K'&SYSLIST(1)                                      NTLC
&APND    SETB  0          ASSUME NO APPEND OPERATION DESIRED      NTLC
&ASUFX   SETB  0          ASSUME PREFIX FOR APPEND OPERATION      NTLC
&OO      SETB  0          ASSUME NO OPSANALYST ACCESS AUTH         NTL
         AIF   ('&OA' NE 'YES').SKPOA                              NTL
&OO      SETB  1          OA=YES                                   NTL
.SKPOA   ANOP  ,                                                   NTL
&ALEN    SETA   K'&APPEND(1)      SET LENGTH FOR APPEND STRING    NTLC
         AIF   (&QCNT GE 2 AND &QCNT LE 8).CHKMLEN
 MNOTE 8,'ERROR: QUALNO=&QCNT NOT ALLOWED..MUST BE >=2 AND <= 8'
         MEXIT
.CHKMLEN AIF   (&MLEN GE 0 AND &MLEN LE 8).CNUMOF
 MNOTE 8,'ERROR: MAXLEN=&MLEN NOT ALLOWED..MUST BE >=0 AND <= 8'
         MEXIT
.CNUMOF  AIF   (&NO GE 0 AND &NO LE 7).CNUMLEN
 MNOTE 8,'ERROR: NUMOF=&NO NOT ALLOWED..MUST BE >=0 AND <=7'
         MEXIT
.CNUMLEN AIF   (&NL GE 0 AND &NO LE 8).DOIT
 MNOTE 8,'ERROR: NUMLEN=&NL NOT ALLOWED..MUST BE >=0 AND <=8'
         MEXIT
.DOIT    ANOP
.*                                                                 NTLC
.* SETUP FOR APPEND OPERATION IF NEED BE                           NTLC
.*                                                                 NTLC
&ASTRNG  SETC  '&APPEND(1)'                                        NTLC
         AIF   (K'&APPEND(1) GE 1).NOFIX                           NTLC
&ASTRNG  SETC  ' '                                                 NTLC
.*                                                                 NTLC
.NOFIX   ANOP  ,                                                   NTLC
         AIF    (T'&APPEND(1) EQ 'O').GEN                          NTLC
&APND    SETB  1                                                   NTLC
         AIF   (T'&APPEND(2) EQ 'O' OR '&APPEND(2)' EQ 'PREFIX').PREFIX
         AIF   ('&APPEND(2)' EQ 'SUFFIX').SUFFIX                  NTLC
 MNOTE 8,'**APPEND POSITION ''&APPEND(2)'' UNKNOWN- USE PREFIX OR SUFFIX
               X FOR KEYWORDS'                                    NTLC
         MEXIT                                                     NTLC
.SUFFIX  ANOP                                                      NTLC
.* ITS SUFFIX                                                      NTLC
&ASUFX   SETB  1                                                  NTLC
.PREFIX  ANOP                                                      NTLC
         AIF    (&ALEN+&MLEN LE 8).GEN                             NTLC
 MNOTE 8,'**APPEND LENGTH OF &ALEN + QUAL LEN OF &MLEN > 8 - INVALID'
         MEXIT                                                     NTLC
.*                                                                 NTLC
.*                                                                 NTLC
.GEN     ANOP                                                      NTLC
         SPACE 1
&LABEL   DC    AL1(&LEN)             LENGTH OF PREFIX
         DC    AL1(&QCNT)            QUALIFIER TO USE
         DC    AL1(&MLEN)            LENGTH OF QUALIFER MAX
         DC    AL1(&NUMOF)           OFFSET FOR NUMCHECK
         DC    AL1(&NUMLEN)          # CHARS TO CHK FOR NUMERIC 0=NONE
         DC    BL1'&APND.&ASUFX.&OO.00000'  APPEND, SUFFIX, OA=YES
         DC    AL1(&ALEN)            LENGTH OF APPEND STRING      NTLC
         DC    CL8'&ASTRNG'          APPEND STRING TO USE         NTLC
         DC    C'&SYSLIST(1)'        BEGINNING OF DSNAME TO CHK
         SPACE 1
         MEND
 TITLE 'DSNTABLE - DEFINE FASTPATH ACCESS TO DATASETS'
ACFDSTBL CSECT
DTAB     DC    A(DSNTABLE)           ADDRESS OF START OF DSNTABLE
QTAB     DC    A(QUALTAB)            ADDRESS OF START OF QUALIF TABLE
         DC    F'0'
         SPACE 2
         DC    CL8'ACFDSTBL'
         DC    CL8'&SYSDATE'
         DC    CL8' &SYSTIME'
         SPACE 2
DSNTABLE DS    0F
***********************************************************************
*                                                                     *
* THIS TABLE OF DATASETS IS USED FOR:                                 *
*                                                                     *
* 1) FASTPATH ACCESS (BY ACFVAL)                                      *
*                                                                     *
* 2) QUALIFIER LOOKUP (BY ACFVAL)                                     *
*                                                                     *
*                                                                     *
***********************************************************************
        SPACE  2
***********************************************************************
*                                                                     *
* SAMPLE EXPANSION OF DSN MACRO:                                      *
*                                                                     *
***********************************************************************
        SPACE 2
   DSN     PASSWORD,GROUP=SYS1,ACCESS=NONE
        SPACE 2
        PRINT NOGEN
        EJECT
***********************************************************************
* SYSTEM DATASETS                                                     *
***********************************************************************
   DSN     SYS1.BRODCAST,ACCESS=WRITE
   DSN     SYS1.CMDLIB
   DSN     SYS1.COBLIB
   DSN     SYS1.FORTLIB
   DSN     SYS1.HELP
   DSN     SYS1.LINKLIB
   DSN     SYS1.LPALIB
   DSN     SYS1.PARMLIB
   DSN     SYS1.MACLIB
   DSN     SYS1.PKGLIB                                             NTLC
   DSN     SYS1.PLIBASE
   DSN     SYS1.PROCLIB
   DSN     SYS1.SORTLIB
   DSN     SYS1.STGINDX,ACCESS=NONE
   DSN     SYS1.SSPLIB
   DSN     SYS1.UADS,ACCESS=NONE
   DSN     SYS2.LINKLIB                                            NTLC
   DSN     SYS2.PROCLIB
   DSN     INTEL.PROCLIB                                           NTLC
   DSN     TECH.MVS.ACF2TEST,GROUP=SYS1,ACCESS=NONE,OA=YES  TESTING NTL
   DSN     TECH.MVS.,GROUP=SYS1,EXACT=NO,ACCESS=NONE               NTLC
       EJECT
***********************************************************************
* SYSTEM CATALOGS                                                     *
***********************************************************************
   DSN     SYSCTLG.,ACCESS=WRITE,EXACT=NO
***********************************************************************
* PRODUCTION CONTROL/APPLICATION SYSTEM INSTALLATION DATASETS         *
***********************************************************************
   DSN     INTRO.PANLIB,GROUP=PRODCNTL,ACCESS=WRITE
   DSN     INTRO.,GROUP=PRODCNTL,EXACT=NO
***********************************************************************
*  TSO RELATED COMMON DATASETS                                        *
***********************************************************************
   DSN     NTL1.CMDPROC                                            NTLC
   DSN     NTL1.ISPF.PARMS,ACCESS=WRITE                            NTLD
   DSN     NTL1.ISPF.,EXACT=NO                                     NTLD
   DSN     SPF22.MOD1.PARMS,ACCESS=WRITE,GROUP=SYS1
   DSN     SPF22.MOD1.,EXACT=NO,GROUP=SYS1
   DSN     SPF22.MASTER.PARMS,ACCESS=WRITE,GROUP=SYS1
   DSN     SPF22.MASTER.,EXACT=NO,GROUP=SYS1
   DSN     PROD.IIC.SPF.,                                              X
               EXACT=NO,GROUP=IICPROD,ACCESS=NONE,OA=YES
   DSN     PROD.IIC.R1M0.,                                             X
               EXACT=NO,GROUP=IICPROD,ACCESS=NONE,OA=YES
   DSN     PROD.IIC.MASTER.,                                           X
               EXACT=NO,GROUP=IICPROD,ACCESS=NONE,OA=YES
   SPACE   2                                                       NTLC
         DC    XL8'00'    END OF TABLE
 TITLE 'QUALTAB  - DEFINE RULES FOR EXTRACTION OF QUALIFIER FROM DSN'
QUALTAB    DS   0F
***********************************************************************
*  DEFINE QUALIFIER PROCESSING.                                       *
*                                                                     *
*  THIS TABLE ESTABLISHES THE RULES FOR QUALIFIER EXTRACTION FROM THE *
*  DATASET NAME BASED ON THE LEADING CHARACTERS OF THE DSNAME.        *
*                                                                     *
*  SAMPLE MACRO EXPANTION:                                            *
*                                                                     *
           PRINT GEN
           SPACE 2
*                                                                   NTL
* TRANSFORM      PROD.IIC.XXXXXXX.YYYYYYY.   INTO          IMIC 2   NTL
*                XXXXXXXX.PROD.IIC.XXXXXXXX.YYYYYYYY.               NTL
*                                                                   NTL
IIC    PREFIX  PROD.IIC.,QUALNO=3,MAXLEN=8,OA=YES          IMIC 2   NTL
*                                                                   NTL
*                                                                  NTLC
* TRANSFORM      PROD.STR.XXX.YYY INTO                             NTLC
*        STRPROD.PROD.STR.XXX.YYY                                  NTLC
*                                                                  NTLC
MAIN   PREFIX  PROD.,                                              NTLCX
               QUALNO=2,MAXLEN=4,APPEND=(PROD,SUFFIX),OA=YES       NTLC
           SPACE 2
*          PRINT NOGEN
*                                                                     *
***********************************************************************
           EJECT
       PREFIX  TEST.,                                              NTLCX
               QUALNO=2,MAXLEN=4,APPEND=(TEST,SUFFIX),OA=YES       NTLC
           SPACE
       PREFIX  JUNK.,     FOR TESTING ONLY                         NTLCX
               QUALNO=2,MAXLEN=4,APPEND=(AARD,PREFIX)              NTLC
           SPACE
           DC   XL8'00'
         END
