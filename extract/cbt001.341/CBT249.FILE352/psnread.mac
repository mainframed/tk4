*          DATA SET PSNREAD    AT LEVEL 002 AS OF 10/30/80
         TITLE 'PSNREAD - RETURN NEXT LOGICAL RECORD'
PSNREAD  PSNMSTRT  SASIZ=WKLENG
         EJECT
*                    TO RETURN THE NEXT SEQUENTIAL RECORD OF
*                    A BLOCKED PDS MEMBER READ WITH BPAM.
*                    RC=4 IS END OF FILE;  RC=8 IS SERIOUS ERROR
*
         LM    R4,R7,PSWRSAV     PICK UP PROPER POINTERS
AGAIN    EQU   *
         BXLE  R7,R4,HAVNEXT     LOOP THROUGH THIS BUFFER
*                    EXHAUSTED CURRENT BUFFER - READ NEXT ONE
         XC    PSWRDECB,PSWRDECB
         L     R5,PSWRDECB+8     ADDRESS THE DCB FROM THE DECB
         LA    R1,ENDOFILE        INSERT EOF ADDRESS
         ST    R1,DCBEODAD-IHADCB(,R5) INTO OUR DCB
         READ  PSWRDECB,SF,MF=E
         CHECK PSWRDECB
*                   AND WE NOW HAVE A SHINY NEW BLOCK
         L     R7,PSWABUFR        RESET POINTER TO START
         L     R2,PSWRDECB+16     PICK UP IOB POINTER
         LH    R2,14(,R2)         PLUCK OUT THE RESIDUAL COUNT
         LH    R5,DCBBLKSI-IHADCB(,R5) PICK UP THE BLOCKSIZE
         SR    R5,R2             AND THIS IS THE AMOUNT READ
         LA    R5,0(R7,R5)      POINT TO END OF IT
         LA    R4,80              SIZE OF A LOGICAL RECORD
         SR    R5,R4              POINT TO START OF LAST CARD
*
HAVNEXT  EQU   *                  THE NEXT RECORD IS READY
         AP    PSWCARDN,PSWPONE   ADD TO THE CARD NUMBER
         AP    PSWIREC#,PSWPONE   ADD TO THE INPUT RECORD NUMBER    NTL
         L     R15,PSWTRNUM       CHECK FOR NUMERIC                 NTL
         TRT   72(8,R7),0(R15)      IN LINE NUMBER FIELD            NTL
         BNE   NOTNUM             BLEAH..NOT NUMERIC...SKIP         NTL
         PACK  PSWLINUM,72(8,R7)  PICK UP LINE NUMBER
         B     ISNUM                                                NTL
NOTNUM   ZAP   PSWLINUM,PSWIREC#  USE INPUT RECORD NUMBER INSTEAD   NTL
ISNUM    EQU   *                                                    NTL
         PSNMREPT                 REPORT THIS LINE
         MVC   1(80,R15),0(R7)    REPORT THIS LINE
*
*                    R7 POINTS TO ENTRY - COMPUTE THE BOUNDS
         CLC   0(2,R7),PSWSJCL    IS THIS TRULY A JCL STATEMENT
         BNE   NOTJCL             SHOULD HAVE BEEN PRESCANNED
*                    FIND THE REAL BOUNDS
         LA    R9,2(,R7)          POINT TO FIRST REAL CHARACTER
         LA    R3,70(,R7)         AND TO THE LAST POSSIBILITY
         LA    R2,1               HOW OFTEN TO STOP AND LOOK
         CLI   2(R7),C'*'         IS IT THEN A COMMENT
         BNE   FLOOP              NO - STRAIGHT INTO SEARCH
         LA    R9,1(,R9)          FIND THE REAL START OF IT
         BCTR  R3,0               BACK UP ENDING POINTER ONE MORE
*
FLOOP    EQU   *                  FIND THE FIRST NON-BLANK
         CLI   0(R9),C' '
         BNE   FRONT              HAVE FOUND THE FRONT OF IT
         BXLE  R9,R2,FLOOP        AND KEEP LOOKING
         B     ALLBLANK           OOPS - NOTHING THERE
*
FRONT    EQU   *                  HAVE FOUND FRONT - NOW FOR THE BACK
         LCR   R8,R2              NEED THE NEGATIVE ONE NOW
BLOOP    EQU   *
         CLI   0(R3),C' '         CHECK FOR SAME BLANK
         BNE   BACK               AND THIS IS THE ONE
         BXH   R3,R8,BLOOP        KEEP LOOKING BACK DOWN
BACK     EQU   *
         ST    R9,PSWRSTRT        REAL START OF CARD
         LA    R3,1(,R3)          POINT TO PSEUDO-DELIMITER
         ST    R3,PSWREND         AND ITS REAL END
         SR    R9,R7              COMPUTE COLUMN NUMBER OF START
         STH   R9,PSWSCOLN        SHOW STARTING COLUMN
*
         STM   R4,R7,PSWRSAV      SAVE THEM ALL BACK
         SR    R15,R15            THE PROPER RETURN CODE
         B     RETURN
*
ENDOFILE EQU   *                  END OF THIS MEMBER REACHED
         LA    R15,4
         XC    PSWRSAV(16),PSWRSAV CLEAR THE POINTER AREA
         B     RETURN
*
*                    EXTEMPORANEOUS ERROR ROUTINES AS THEY OCCUR
NOTJCL   EQU   *
         PSNMERR ERRNJCL
         B     AGAIN              GO ON TO NEXT MEMBER
*
ALLBLANK EQU   *
         CLI   2(R7),C'*'         BLANK, BUT A COMMENT LINE
         BNE   BLKERR             NO - A REAL ERROR
         LA    R9,3(,R7)          POINT TO A SPARE BLANK
         LR    R3,R9              WITH BOTH BOUNDARY POINTERS
         B     BACK               AND PASS THIS BACK FOR COMMENTARY
BLKERR   EQU   *
         PSNMERR ERRBLANK
         B     AGAIN              AND TRY AGAIN
*
         LTORG
         EJECT
WORKAREA DSECT
         DS    18F
         DS    X
         PSNMBLOK  RENT=REGS
WKLENG   EQU   *-WORKAREA
         EJECT
         PSNMWORK
         SPACE 4
         PRINT NOGEN
         IHADCB  DSORG=PO,DEVD=DA
         END
