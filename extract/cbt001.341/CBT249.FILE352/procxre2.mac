*          DATA SET PROCXRE2   AT LEVEL 005 AS OF 02/15/83
*          DATA SET PROCXRE2   AT LEVEL 004 AS OF 11/02/82
PROCXRE2  TITLE '*---PROCXRE2---*  JCL XREF -- SYSOUT CLASS CROSSREFERE*
               NCE'
***********************************************************************
*                                                                     *
*     'SYSOUT' CROSS REFERENCE PROGRAM-  'PROCXRE2'                   *
*                                                                     *
*                                                                     *
*     THIS PROGRAM SORTS AND PRINTS OUTPUT FROM 'PROCXREF'.           *
*                                                                   NTL
* WRITTEN  BY KEN TRUE-INTEL CORP - 25 OCT 82                       NTL
*                                                                   NTL
***********************************************************************
         PRINT OFF
         MACRO
&NAME    PGM   &BASE=R12,&SAVE=$$SAVE,&START=$$START,&EOJ=$$EOJ,&RC=16
         LCLC  &BASE2,&BASE1
&BASE1   SETC  '&BASE(1)'
&BASE2   SETC  '&BASE(2)'
&NAME    CSECT
         SAVE  (14,12),,&NAME_&SYSDATE_&SYSTIME
         LR    &BASE1,R15          ESTABLISH ADDRESSABILITY.
         USING &NAME,&BASE1         TELL ASSEMBLER ABOUT BASE REGISTER.
         AIF   (T'&BASE(2) EQ 'O').NOBAS2
         LA    &BASE2,4095(,&BASE1)    ESTABLISH SECOND BASE
         LA    &BASE2,1(,&BASE2)       OFF OF FIRST BASE REGISTER
         USING &NAME+4096,&BASE2
.NOBAS2  ANOP
R0       EQU   0                   REGISTER 0.
R1       EQU   1                   REGISTER 1.
R2       EQU   2                   REGISTER 2.
R3       EQU   3                   REGISTER 3.
R4       EQU   4                   REGISTER 4.
R5       EQU   5                   REGISTER 5.
R6       EQU   6                   REGISTER 6.
R7       EQU   7                   REGISTER 7.
R8       EQU   8                   REGISTER 8.
R9       EQU   9                   REGISTER 9.
R10      EQU   10                  REGISTER 10.
R11      EQU   11                  REGISTER 11.
R12      EQU   12                  REGISTER 12.
R13      EQU   13                  REGISTER 13.
R14      EQU   14                  REGISTER 14.
R15      EQU   15                  REGISTER 15.
         LA    R15,&SAVE           ADDRESS OF OUR SAVE AREA.
         ST    R13,4(R15)          BACKWARD SAVE AREA CHAIN.
         ST    R15,8(R13)          FORWARD SAVE AREA CHAIN.
         LR    R13,R15             POINT R13 TO CURRENT SAVE AREA.
         B     &START              BRANCH TO ENTRY CODE.
&EOJ     CH    R15,$$RC            IS RETURN CODE HIGHER THAN &RC?
         BNH   *+6                 YES - LETS ZERO R15.
         SR    R15,R15             ZERO OUT REGISTER 15.
         L     R13,&SAVE+4         POINT R13 TO PREVIOUS SAVE AREA.
         L     R14,12(R13)         RESTORE REGISTER 14.
         LM    R0,R12,20(R13)      RESTORE THE REGISTERS.
         BR    R14                 RETURN TO OS.
&SAVE    DS    18F                 OUR SAVE AREA.
$$RC     DC    H'&RC'              RETURN CODE.
$$START  DS    0H                  DEFAULT ENTRY POINT.
         MEND
         MACRO
&NAME    PRINTX &DCB,&TITLE,&BUFFER,&LINES,&NOPAGE,&SIZE=132
         LCLC  &A,&B,&C,&D,&E,&A1,&F
         LCLA  &S,&NTITLE,&I
&S       SETA  &SIZE-1
&NTITLE  SETA  N'&TITLE
&I       SETA  0
&A       SETC  '$$A'.'&SYSNDX'
&A1      SETC  '$#A'.'&SYSNDX'
&B       SETC  '$$B'.'&SYSNDX'
&C       SETC  '$$C'.'&SYSNDX'
&D       SETC  '$$D'.'&SYSNDX'
&E       SETC  '$$E'.'&SYSNDX'
&F       SETC  '$$F'.'&SYSNDX'
         SPACE
&NAME    BAL   R14,&F              GO EXAMINE LINE FOR ASA
*                                  RETURN WITH R15=LINE COUNT
         CH    R15,=AL2(&LINES)    WILL THIS HAVE OVFLWED THE PAGE?
         BNH   &A                  NO - PRINT A REGULAR LINE.
         SR    R15,R15             CLEAR REGISTER 15.
         STH   R15,&B              STORE LINES COUNTER.
         AIF   ('TITLE(1)' EQ '').$$LINE
         AIF   ('NOPAGE' EQ '').NOPAGE
         MVC   &TITLE(1)+&S-12(12),&E PAGE CONSTANT.
         ED    &TITLE(1)+&S-7(6),&D.+5 UNPACK PAGE NUMBER
         CVB   R15,&D              CURRENT PAGE COUNTER.
         LA    R15,1(R15)          INCREMENT BY ONE.
         CVD   R15,&D              CONVERT TO PACK FORMAT.
.NOPAGE  ANOP
         PUT   &DCB,&TITLE(1)      PRINT TITLE.
         AIF   (&NTITLE LE 1).$$LINE
&I       SETA  1
.$$LOOP  ANOP
&I       SETA  &I+1
         AIF   (&I GT &NTITLE).$$LINE
         PUT   &DCB,&TITLE(&I)     PRINT SUBTITLE NUMBER &I
         AGO   .$$LOOP
.$$LINE  ANOP
         LH    R15,&B              GET CURRENT LINES PRINTED
         LA    R15,&NTITLE         BUMP BY NUMBER OF TITLE LINES
         STH   R15,&B
         MVI   &BUFFER,C' '        MAKE SURE STARTS WITH NO EXTRA SKIPS
*
&A       PUT   &DCB,&BUFFER        PRINT A REGULAR LINE.
         BAL   R14,&F              GO CHECK OUT LINE COUNT
         STH   R15,&B              SAVE LINE COUNT
         B     &C                  GO EXIT
         SPACE 1
*
*  CHECK OUT ASA CONTROL CHARACTER FOR CARRIAGE COUNT
*
&F       LH    R15,&B              CURRENT LINES PRINTED.
         CLI   &BUFFER,C'+'        IS IT A OVERPRINT?
         BE    &A1                       YES..NO BUMP
         LA    R15,1(R15)          INCREMENT BY ONE. ..
         CLI   &BUFFER,C' '        IS IT A BLANK?
         BE    &A1
         LA    R15,1(R15)
         CLI   &BUFFER,C'0'        IS IT A DOUBLE SPACE?
         BE    &A1
         LA    R15,1(R15)
* NOTE...TRIPLE SPACE ASSUMED C'-'
&A1      BR    R14                 EXIT OUT.
         SPACE 1
         AIF   ('NOPAGE' EQ '').SKIPD
         DS    0D                  DOUBLE WORD ALIGNMENT..
&D       DC    PL8'+1'             TITLE PAGE COUNTER.
&E       DC    C' PAGE ',X'202020202140' CONST.  IN HEADING.&L
.SKIPD   ANOP
&B       DC    AL2(&LINES)         NUMBER OF LINES PRINTED ON PAGE.
&C       DS    0H                  EXIT OUT.
         MEND
         PRINT ON
         EJECT
         PRINT NOGEN
PROCXRE2 PGM   ,                                                    NTL
*                                                                     *
BLANK    EQU   C' '                     EQUATE
CHR0     EQU   C'0'
CHRI     EQU   C'I'
CHRO     EQU   C'O'
*                                                                     *
HEX00    EQU   X'00'
HEXFF    EQU   X'FF'
*                                                                     *
A10      EQU   *
         TM    0(R1),X'80'              IS IT THERE?                NTL
         BZ    A10$                     NO..SKIPIT                  NTL
         L     R5,0(,R1)                GET ADDRESS                 NTL
         MVC   HDR1,0(R5)               MOVE IN HEADER              NTL
         MVC   HEAD1RPT,=CL17'SYSOUT CROSS REF.'                    NTL
         MVC   HEAD1PGL,=CL8'   PAGE  '                             NTL
A10$     EQU   *                                                    NTL
         LA    R1,SORTLST2              SORT BY SYSOUTC NAME
         LINK  EP=SORT
*                                                                     *
         OPEN  (INFILE,(INPUT),PRTFILE,(OUTPUT))
         MVC   PRTREC,PRTREC-1
         BAL   R10,HEADER10             PRINT HEADERS
A20      EQU   *
         GET   INFILE,XREC              GET INPUT RECORD
         CLI   XIO,C'S'                 IS THIS A SYSOUT RECORD?    NTL
         BNE   A20                      NO.....SKIPIT....           NTL
         CLC   SYSOUTC,XDSN             DID SYSOUTC CHANGE
         BE    A100                     NO
         MVC   SYSOUTC,XDSN             SAVE NEW SYSOUTC
         CLI   FIRSTSW,HEX00            IS THIS 1ST TIME THRU
         BE    A30
         PUT   PRTFILE,HDR3             PUT SEPARATOR LINE
         L     R1,LINECTR
         A     R1,=F'1'                 BUMP LINE COUNTER
         ST    R1,LINECTR
         C     R1,LINEMAX1              IS IT PAGE OVERFLOW
         BNH   A30                      NO
         BAL   R10,HEADER10             PRINT HEADERS
A30      EQU   *
         MVI   FIRSTSW,HEXFF
         MVI   PCC,BLANK                SET TO SINGLE SPACE
         MVC   PSYSOUT,XDSN             MOVE SYSOUTC TO PRINT
         B     A102
A100     EQU   *
         CLC   PROC,XPROC                DONT PRINT IF PROC AND STEP
         BNE   A102                       AND DDNAME ARE THE SAME   NTL
         CLC   STEP,XSTEP
         BNE   A102
         CLC   PDDN,XDDNAME                                         NTL
         BNE   A102                                                 NTL
         B     A20
A102     EQU   *
         MVC   PROC,XPROC
         MVC   STEP,XSTEP
         MVC   PGM,XPGM                                             NTL
         L     R1,LINECTR
         C     R1,LINEMAX2              IS IT PAGE OVERFLOW
         BNH   A110                     NO
         BAL   R10,HEADER10             YES - PRINT HEADERS
         MVC   PSYSOUT,XDSN
         MVC   PCONT,=C' * CONT'        CONTINUE ON NEXT PAGE
A110     EQU   *
         MVC   PPROC,XPROC              MOVE PROC TO PRINT
         MVC   PSTEP,XSTEP              MOVE STEP TO PRINT
         MVC   PPGM,XPGM                MOVE PROGRAM TO PRINT       NTL
         MVC   PDDN,XDDNAME             MOVE DDNAME TO PRINT        NTL
*                                                                     *
         PUT   PRTFILE,PRTREC           PRINT RECORD
         MVC   PRTREC,PRTREC-1          CLEAR
         L     R1,LINECTR
         A     R1,=F'1'                 BUMP LINE COUNTER
         ST    R1,LINECTR
         B     A20
*                                                                     *
         EJECT
HEADER10 EQU   *                        HEADER ROUTINE
         L     R1,PAGECTR
         A     R1,=F'1'                 BUMP PAGE COUNTER
         ST    R1,PAGECTR
         CVD   R1,DWRD                  MOVE PAGE NO.
         MVC   H1PAGE,MASK
         ED    H1PAGE,DWRD+6
         PUT   PRTFILE,HDR1             PRINT HEADER 1
         PUT   PRTFILE,HDR2             PRINT HEADER 2
         PUT   PRTFILE,PRTREC           PRINT BLANK LINE
         SR    R1,R1
         ST    R1,LINECTR
         BR    R10
*                                                                     *
INEOF    EQU   *                        END OF FILE ROUTINE
         MVI   H3CC,BLANK
         PUT   PRTFILE,HDR3
         CLOSE (INFILE,,PRTFILE)
         B     $$EOJ                                                NTL
*                                                                     *
         EJECT
INDCB    DS    0F
INFILE   DCB   DDNAME=SORTOUT,DSORG=PS,MACRF=GM,EODAD=INEOF
*                                                                     *
PRTDCB   DS    0F
PRTFILE  DCB   DDNAME=SYSPRNT2,DSORG=PS,MACRF=PM,                      X
               RECFM=FBA,LRECL=133,BLKSIZE=3990
*                                                                     *
         EJECT
LINECTR  DC    F'0'                     LINE COUNTER
LINEMAX1 DC    F'49'                    MAX LINES PER PAGE
LINEMAX2 DC    F'54'                    MAX LINES PER PAGE
PAGECTR  DC    F'0'                     PAGE COUNTER
DWRD     DS    D
MASK     DC    X'40202021'
FIRSTSW  DC    X'00'
*                                                                     *
SYSOUTC  DC    CL15' '                                              NTL
PROC     DC    CL8' '
STEP     DC    CL8' '
PGM      DC    CL8' '                                               NTL
*                                                                     *
DATEWK   DS    0CL55                    DATE AREA
         DS    CL19
DATE3    DS    CL8                      MO/DA/YR
         DS    CL28
*                                                                     *
         EJECT
         DS    0F                       SORT PARMS
SORTLST2 DC    X'80',AL3(SORTPRM2)
         DC    H'0'
SORTPRM2 DC    AL2(PARMEND2-PARMBEG2)
PARMBEG2 EQU   *
         DC    A(SORT2,SORTEND2,REC2,RECEND2,0,0)
SORTSIZ2 DC    F'100000'
SORTMSG2 DC    X'FF00'
         DC    C'AP'
PARMEND2 EQU   *
*                                                                     *
SORT2    DS    0H
*                              PGM       PROC      STEPNO           NTL
         DC    C' SORT FIELDS=(1,44,CH,A,46,8,CH,A,145,2,BI,A),'    NTL
         DC    C'SIZE=E40000 '
SORTEND2 EQU   *-1
*                                                                     *
REC2     DS    0H
         DC    C' RECORD LENGTH=166,TYPE=F '                        NTL
RECEND2  EQU   *-1
*                                                                     *
         EJECT
XREC     DS    CL166                    INPUT RECORD                NTL
         ORG   XREC                                                 NTL
XDSN     DC    CL44' '                  DSNAME
XIO      DC    CL1' '                   INPUT/OUTPUT
XPROC    DC    CL8' '                   PROC
XPGM     DC    CL8' '                   PROGRAM
XSTEP    DC    CL8' '                   STEP
XRELGEN  DC    CL4' '                   RELATIVE GEN
XDISP    DS    0CL9                     DISP
XDISP1   DC    CL3' '
XDISP2   DC    CL3' '
XDISP3   DC    CL3' '
XUNIT    DC    CL8' '                   UNIT
XEXPDT   DC    CL5' '                   EXPDT/RETPD
XSPACE   DC    CL25' '                  SPACE
XDCB     DS    0CL15                    DCB
XDSORG   DC    CL2' '                        DSORG
XRECFM   DC    CL3' '                        RECFM
XLRECL   DC    CL5' '                        LRECL
XBLKSIZE DC    CL5' '                        BLKSIZE
XDDNAME  DC    CL8' '                                               NTL
         DC    CL1' '                   FILLER                      NTL
XSTEPNO  DC    H'0'                     STEP NUMBER                 NTL
XVOLUME  DC    CL20' '                  VOL=SER=, VOL=REF=          NTL
         ORG   ,
*                                                                     *
         DC    C' '
PRTREC   DS    CL133                    PRINT RECORD
         ORG   PRTREC                                               NTL
PCC      DS    CL1
         DS    CL5
PSYSOUT  DS    CL15                     SYSOUTC                     NTL
PCONT    DS    CL7
         DS    CL8
PPROC    DS    CL8                      PROC
         DS    CL7
PSTEP    DS    CL8                      STEP
         DS    CL7                                                  NTL
PPGM     DS    CL8                      PROGRAM                     NTL
         DS    CL7                                                  NTL
PDDN     DS    CL8                                                  NTL
         ORG   ,                                                    NTL
*                                                                     *
*                                                                     *
*           1         2         3         4         5               NTL
* 0....5....×....5....×....5....×....5....×....5....×....5....      NTL
* 1PROCXREF -V1.1- UNLOAD UTILITY LOG  DSN=XXXXXXXXX.XXXXXXXXX      NTL
*                  D S N  CROSS REF.                                NTL
*                  P G M  CROSS REF.                                NTL
* 6         7         8         9         0         1               NTL
* 0....5....×....5....×....5....×....5....×....5....×....5....      NTL
* .XXXXXXXXX.XXXXXXXXX.XXXX MBRS=XXXXXXXX  XXXXXXXX XXXXXXXX        NTL
*                                                                   NTL
* 2         3                                                       NTL
* 0....5....×..                                                     NTL
*    PAGE NNNNN                                                     NTL
*                                                                   NTL
HDR1     DS    0CL133                   HEADER 1
 DC CL133'1PROCXREF -V1.1- P G M  CROSS REF.   DSN=                    +
                                        MBRS='                      NTL
HEAD1RPT EQU   HDR1+017,18                                          NTL
HEAD1DSN EQU   HDR1+041,44                                          NTL
HEAD1MBR EQU   HDR1+091,8                                           NTL
HEAD1DAT EQU   HDR1+101,8                                           NTL
HEAD1TIM EQU   HDR1+110,8                                           NTL
HEAD1PGL EQU   HDR1+120,8                                           NTL
H1PAGE   EQU   HDR1+129,4                                           NTL
*                                                                     *
HDR2     DS    0CL133                   HEADER 2
*                                                                   NTL
*           1         2         3         4         5               NTL
* 0....5....×....5....×....5....×....5....×....5....×....5....      NTL
*  -----XXXXXXXXXXXXXXX-------________XXXXXXXX_______XXXXXXXX_      NTL
*                                                                   NTL
* 6         7         8         9         0         1               NTL
* 0....5....×....5....×....5....×....5....×....5....×....5....      NTL
* ______XXXXXXXX_______XXXXXXXX_______XXXXXXXX                      NTL
*                                                                   NTL
         DC    CL133'0    SYSOUT CLASS                   PROC          +
                STEP           PROGRAM        DDNAME '              NTL
*                                                                     *
HDR3     DS    CL133' '                 SEPARATOR LINE
         ORG   HDR3                                                 NTL
H3CC     DC    C' '
         DC    CL15' '
         DC    86C'-'                                               NTL
         ORG   ,                                                    NTL
*                                                                     *
         LTORG
         END
