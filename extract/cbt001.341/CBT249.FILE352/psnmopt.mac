*          DATA SET PSNMOPT    AT LEVEL 002 AS OF 10/30/80
         MACRO
&TAG     PSNMOPT   &P=OPT,&T=C,&DDNAMES=,&OPTION=
.*                   TO DEFINE USER-RELATED CHOICES IF THEY
.*                   ARE LONG-TERM, BUT SUBJECT TO CHANGE
.*                   THIS STOPS THE CODE FROM CHANGING.
         LCLC  &OP(32)            THE OPTION LIST
         LCLB  &B(32)             STRING OF OPTIONS REQUESTED
         LCLA  &I1,&I2            POINTERS FOR OPTION DECODING
         LCLC  &CB1,&CB2,&CB3,&CB4  CONSOLIDATE BIT STRINGS
.*
         LCLA  &SX                SHORT FORM OF SYSNDX
         LCLC  &DDLIB,&DDSIN,&DDTERM,&DDPRT
.*
.*
         PSNMI001  &TAG,PSNOPT,&P,&T
.*
&SX      SETA  &SYSNDX            FOR THE ABBREVIATION
.*                   DEFINE THE OPTIONS REQUESTED
         AIF   (T'&OPTION EQ 'O').OPTX  NO OPTIONS GIVEN - EXIT
&OP(1)   SETC  'NODUMP'           BYPASS DUMP REQUESTS
&OP(2)   SETC  'NOTDATA'          DO NOT WRITE DATA TO SYSTERM
.OPTN    ANOP                     LOOP THROUGH THE OPTIONS
&I2      SETA  &I2+1              ON TO NEXT INPUT OPTION
         AIF   (&I2 GT N'&OPTION).OPTX     END OF THE STRING
&I1      SETA  1                  POINT TO FIRST OF POSSIBILITIES
.OPTL    ANOP
         AIF   ('&OPTION(&I2)' EQ '&OP(&I1)').OPTM   MATCHED
&I1      SETA  &I1+1              INDEX ON TO NEXT ONE
         AIF   (&I1 LE 32).OPTL
         MNOTE  8,'INVALID OPTION - &OPTION(&I2)'
         AGO   .OPTN              ON TO THE NEXT OPTION
.OPTM    ANOP                     HAVE AMATCH - REPEATED?
         AIF   (NOT &B(&I1)).OPTS CHECK FOR DUPLICATION
         MNOTE  4,'&OPTION(&I2) ENTERED TWICE IN OPTION'
.OPTS    ANOP
&B(&I1)  SETB  1                  SHOW THIS OPTION IS RECOGNIZED
         AGO   .OPTN
.OPTX    ANOP                     END OF OPTION DECODING
&CB1     SETC  '&B(1)&B(2)&B(3)&B(4)&B(5)&B(6)&B(7)&B(8)'
&CB2     SETC  '&B(9)&B(10)&B(11)&B(12)&B(13)&B(14)&B(15)&B(16)'
&CB3     SETC  '&B(17)&B(18)&B(19)&B(20)&B(21)&B(22)&B(23)&B(24)'
&CB4     SETC  '&B(25)&B(26)&B(27)&B(28)&B(29)&B(30)&B(31)&B(32)'
.*
.*                   DEFINE THE DDNAMES FROM THE DEFAULTS
.*                   PLUS THE USER INPUT
&DDLIB   SETC  'SYSUT1'           THE DEFAULT
         AIF   (T'&DDNAMES(1) EQ 'O').DD1X
&DDLIB   SETC  '&DDNAMES(1)'
.DD1X    ANOP
.*
&DDSIN   SETC  'SYSIN'            THE DEFAULT
         AIF   (T'&DDNAMES(2) EQ 'O').DD2X
&DDSIN   SETC  '&DDNAMES(2)'
.DD2X    ANOP
.*
&DDTERM  SETC  'SYSTERM'          THE DEFAULT
         AIF   (T'&DDNAMES(3) EQ 'O').DD3X
&DDTERM  SETC  '&DDNAMES(3)'
.DD3X    ANOP
.*
&DDPRT   SETC  'SYSPRINT'         THE DEFAULT
         AIF   (T'&DDNAMES(4) EQ 'O').DD4X
&DDPRT   SETC  '&DDNAMES(4)'
.DD4X    ANOP
.*
.*
.*                   AND GENERATE THE ENTRY IN FIXED FORM
&P.SYLIB DC    CL8'&DDLIB'        DD NAME OF SYSLIB
&P.SYSIN DC    CL8'&DDSIN'        DD NAME OF SYSIN
&P.STERM DC    CL8'&DDTERM'       DD NAME OF SYSTERM
&P.SYPRT DC    CL8'&DDPRT'        DD NAME OF SYSPRINT
*
*                    THE ON/OFF CHOICES FROM OPTION
&P.OPTN  DC    B'&CB1&CB2&CB3&CB4'
*
.EXIT    ANOP
         MEND
