*          DATA SET Q3CDN      AT LEVEL 001 AS OF 04/01/83
CDN      QSTART 'QUEUE COMMAND - HD AND DN COMMANDS             ' INTEL
         GBLB  &QSP                                               INTEL
         USING QCKPT,R10      BASE REG FOR CKPT WORK AREA         INTEL
         L     R10,QVCKPT     LOAD BASE REG                       INTEL
         USING WORK,R13       BASE REG FOR LOCAL WORK AREA        INTEL
         USING JQEDSECT,R9    BASE REG FOR JQE DSECT              INTEL
         USING JOEDSECT,R8    BASE REG FOR JOE DSECT              INTEL
***********************************************************************
*                                                                 INTEL
*   SEARCH THE JQE QUEUES:        FOR EACH JOB, GET JCT AND       INTEL
*   FORMAT THE PRINT JCT LINES/CARDS AND EXEC FIN DATE/TIME       INTEL
*   FOR 'DN' COMMAND: ALSO FIND/FORMAT JOES FOR THIS JQE AND      INTEL
*   PROVIDE TOTALING INFORMATION.                                 INTEL
*   NOTE: JOBS MAY BE SELECTED VIA A 'JOBMASK' LIKE STATUS CMD    INTEL
*                                                                 INTEL
*   NOTE: THIS IS A RESTRICTED USE (OPER PRIVLEDGE COMMAND        INTEL
*         SINCE IT IS EXPENSIVE TO RUN (A JCT READ FOR EVERY      INTEL
*         JQE SELECTED.                                           INTEL
*                                                                 INTEL
***********************************************************************
*   V1.0   KEN TRUE - INTEL CORP   21 JAN 82                      INTEL
*   V1.1   KEN TRUE - INTEL CORP   30 MAR 83 - ADAPT FOR SP1.3    INTEL
***********************************************************************
******************************************************************UF006
*                                                                 UF006
*   CALL - READ JES2 CHECKPOINT FILE                              UF006
*                                                                 UF006
******************************************************************UF006
         L     R15,=V(CKPT)   ADDR OF CKPT ROUTINE                UF006
         BALR  R14,R15        GO TO IT                            UF006
******************************************************************INTEL
*                                                                 INTEL
*   RUN COMMAND ONLY IF AUTHORIZED                                INTEL
*                                                                 INTEL
******************************************************************INTEL
         TM    QFLAG1,QFLG1OPR  OPER PRIVLEDGE USER?              INTEL
         BO    HD                                                 INTEL
         CLI   QCODE,4          IS THIS A 'DN' DISPLAY            INTEL
         BE    TILTDN                                             INTEL
         QTILT 'HELD OUTPUT DETAIL IS ONLY FOR OPER. PRIV. USERS' INTEL
TILTDN   QTILT 'DISPLAY NAMES IS ONLY FOR OPER. PRIV. USERS'      INTEL
         SPACE 1                                                  INTEL
HD       CLI   QPARM1,C' '    ANY PARM?                           INTEL
         BNE   HD1                                                INTEL
         MVC   QPARM1,=CL8'........' INDICATE ANY JOBNAME         INTEL
HD1      BAL   R2,PARMLEN     DETERMINE PARM LENGTH AND MASK      INTEL
*                                                                 INTEL
         SPACE 1                                                  INTEL
         XC    PRTTOTAL,PRTTOTAL                                  INTEL
         XC    PUNTOTAL,PUNTOTAL                                  INTEL
*                                                                 INTEL
******************************************************************INTEL
*                                                                 INTEL
*   SCAN THE JQE QUEUES FOR JOBS TO LOOK AT                       INTEL
*                                                                 INTEL
******************************************************************INTEL
         USING JQTDSECT,R1    BASE REG FOR JQT                    INTEL
         L     R1,QCJQHEAD    LOAD ADDR OF JQT                    INTEL
         LA    R4,JQTOUT      ADDR OF $OUTPUT QUEUE               INTEL
         DROP  R1                                                 INTEL
         MVI   QCLASS,8       INDICATE THIS IS THE HELD OUT QUEUE INTEL
         BAL   R2,SRCHJQE     SEARCH THE QUEUE                    INTEL
         B     STOP           END OF QUEUE                        INTEL
         BAL   R14,MASKOFF    SET JOBNAME MASK IN PLACE           INTEL
         LH    R6,QLNG1       GET LENGTH OF COMPARE               INTEL
         EX    R6,STCLC       IS THE JOBNAME EQUAL TO LEVEL?      INTEL
         BNE   REJJQE         NO REJECT                           INTEL
         CLI   QCODE,4        IS THIS A 'DN' REQUEST              INTEL
         BE    PRTJQE         YES...GO DOIT                       INTEL
         CLI   JQEHLDCT,0     ARE THERE ANY HELD DATASETS? (PART  INTEL
         BNE   PRTJQE         YES. PRINT THE RECORD.              INTEL
         TM    JQEHQLOK,X'F0' ARE THERE ANY HELD DATASETS? (PART  INTEL
         BNZ   PRTJQE         YES. PRINT THE RECORD.              INTEL
         B     REJJQE         REJECT THE RECORD                   INTEL
*                                                                 INTEL
* COMPARE USED TO CHECK LEVEL
STCLC    CLC   MASKNAME(1),WORKNAME IS THE JOBNAME EQUAL TO LEVEL INTEL
         EJECT ,                                                  INTEL
***********************************************************************
*                                                                 INTEL
*   SEARCH A JQE QUEUE                                            INTEL
*                                                                 INTEL
***********************************************************************
SRCHJQE  ZAP   QCOUNT,=P'0'   ZERO THE QUEUE COUNT
         AIF   (&QSP).QSP3A                                       UF020
         LH    R9,0(R4)       LOAD FIRST JQE OFFSET
         N     R9,=X'0000FFFF' KILL EXTRANEOUS BITS
NEXTJQE  SLA   R9,2           MULTIPLY BY 4
         AGO   .QSP4A                                             UF020
.QSP3A   ANOP                                                     UF020
         L     R9,0(R4)       LOAD FIRST JQE OFFSET               UF020
NEXTJQE  LA    R9,0(,R9)      KILL EXTRANEOUS BITS                UF020
         LTR   R9,R9          TEST FOR END OF QUEUE               UF020
.QSP4A   ANOP                                                     UF020
         BZR   R2             END OF QUEUE. RETURN.
         A     R9,QCJQTA      ADD BASE TO OFFSET
         B     4(R2)          DETERMINE ELIGIBILITY
PRTJQE   AP    QCOUNT,=P'1'   INCREMENT COUNT
PRT2JQE  SR    R1,R1          INDICATE THIS IS A JQE
         SPACE 1                                                  INTEL
         BAL   R7,FORMATIT    GO FORMAT THE LINE                  INTEL
*                                                                 INTEL
         CLI   QCODE,4        IS THIS A 'DA' REQUEST              INTEL
         BNE   SHOWBLNK       NO..SKIP JOE LIST                   INTEL
         BAL   R7,FRMTJOES    GO FORMAT JOE INFO                  INTEL
         SPACE 2                                                  INTEL
SHOWBLNK BAL   R7,BLNKLINE    TOSS UP A BLANK LINE                INTEL
*                                                                 INTEL
         AIF   (&QSP).QSP5                                        UF020
REJJQE   LH    R9,JQECHAIN    LOAD OFFSET TO NEXT JQE
         AGO   .QSP6                                              UF020
.QSP5    ANOP                                                     UF020
REJJQE   L     R9,JQENEXT     LOAD OFFSET TO NEXT JQE             UF020
.QSP6    ANOP                                                     UF020
         B     NEXTJQE        GET THE NEXT JQE
SKIPJQE  AP    QCOUNT,=P'1'   INCREMENT COUNT
         B     REJJQE         CONTINUE
***********************************************************************
*                                                                 INTEL
*   GO HOME                                                       INTEL
*                                                                 INTEL
***********************************************************************
STOP     MVC   QDMSG,QBLANK                                       INTEL
         L     R1,PRTTOTAL    GET PRINT LINES TOTAL               INTEL
         MVC   LINESTOT,EDMASK9          MOVE IN MASK             INTEL
         CVD   R1,DOUBLEWD    CONVERT TO DECIMAL                  INTEL
         ED    LINESTOT,DOUBLEWD+3                                INTEL
*                                                                 INTEL
         L     R1,PUNTOTAL    GET PRINT LINES TOTAL               INTEL
         MVC   CARDSTOT,EDMASK9          MOVE IN MASK             INTEL
         CVD   R1,DOUBLEWD    CONVERT TO DECIMAL                  INTEL
         ED    CARDSTOT,DOUBLEWD+3                                INTEL
         MVC   TIMESTMP,=CL26'--PRINT/PUNCH HELD TOTALS-'         INTEL
         CLI   QCODE,4                                            INTEL
         BNE   *+4+6                                              INTEL
         MVC   TIMESTMP,=CL26'--TOTAL HELD/NONHELD RECS-'         INTEL
*                                                                 INTEL
         BAL   R7,DISPLAY                                         INTEL
*                                                                 INTEL
         QSTOP ,                                                  INTEL
         EJECT ,                                                  INTEL
***********************************************************************
*                                                                     *
*   DETERMINE LENGTH OF LEVEL                                         *
*                                                                     *
***********************************************************************
PARMLEN  LA    R3,7           MAXIMUM LENGTH OF 8
         LA    R4,QPARM1+7    END OF FIELD
PARMLEN2 CLI   0(R4),C' '     IS THIS BYTE BLANK?
         BNE   PARMLEN3       NO. THIS IS THE LENGTH
         BCTR  R4,0           TRY PREVIOUS BYTE
         BCT   R3,PARMLEN2    LOOP
PARMLEN3 STH   R3,QLNG1       STORE THE LENGTH OF LEVEL
         MVC   MASKAREA,QPARM1 MOVE PARM INTO MASK AREA           INTEL
         TR    MASKAREA,TRMASK GENERATE MASK                      INTEL
         MVC   MASKNAME,QPARM1 MOVE COMPARE MASK TO HOLDING AREA  INTEL
         OC    MASKNAME,MASKAREA AND MASK OFF THE DONT CARES      INTEL
         BR    R2             RETURN TO CALLER
***********************************************************************
*                                                                     *
*   MASK OFF BYTES FOR COMPARE OF JQEJNAME TO GENERIC MASKING         *
*                                                                     *
***********************************************************************
MASKOFF  MVC   WORKNAME,JQEJNAME  MOVE NAME TO WORK AREA          INTEL
         OC    WORKNAME,MASKAREA  PUT X'FF' IN DONT CARE BYTES    INTEL
         BR    R14                                                INTEL
         EJECT ,                                                  INTEL
***********************************************************************
*                                                                 INTEL
*   FORMAT THE DETAIL LINE: READ JCT VIA FIND JOB....             INTEL
*                                                                 INTEL
***********************************************************************
FORMATIT STM   R2,R9,REGSAVE1  SAVE GOOD REGISTERS                INTEL
         MVC   QCTRAK,JQETRAK  SAVE DISK ADDR OF JCT              INTEL
         XC    JOBTOTAL,JOBTOTAL  WIPE CLEAN TOTAL JOB RECORD COU INTEL
         L     R1,QCJCTA       GET IO AREA ADDRESS FOR JCT        INTEL
         L     R15,=V(READSPC) GO READ JCT                        INTEL
         BALR  R14,R15                                            INTEL
         L     R6,QCJCTA      GET ADDRESS OF JCT                  INTEL
         USING JCTSTART,R6    ADDRESSABILITY                      INTEL
         SPACE 2                                                  INTEL
         MVC   QDHLINE,HEADER                                     INTEL
         MVC   QDMSG,QBLANK                                       INTEL
         MVC   JOBNAME,JQEJNAME     JOBNAME                       INTEL
         CLC   JCTID,=CL4'JCT ' IS THIS A JCT?                    INTEL
         BE    FMTCONT        YES..PROCEED                        INTEL
         MVC   TIMESTMP,=CL26'---JCT NOT AVAILABLE---  '          INTEL
         B     FMTDISP        SORRY...JUST DISPLAY AND SPLIT      INTEL
FMTCONT  MVC   JOBNUMB,JCTJOBID     JOB ID                        INTEL
         ZAP   DOUBLEWD,QCOUNT      GET COUNT                     INTEL
         MVC   QUEUE#,EDMASK5       MOVE IN MASK                  INTEL
         ED    QUEUE#,DOUBLEWD+5    EDIT IN POSIT IN QUEUE        INTEL
         LA    R15,PRTDEST                                        INTEL
         LA    R1,JCTPROUT                                        INTEL
         BAL   R7,RMTORLCL       CONVERT PRT= FIELD               INTEL
         LA    R15,PUNDEST                                        INTEL
         LA    R1,JCTPUOUT                                        INTEL
         BAL   R7,RMTORLCL       CONVERT PUN= FIELD               INTEL
*                                                                 INTEL
         LM    R0,R1,JCTXEQOF    GET XEQ STOP TIME/DATE           INTEL
         LTR   R1,R1             ANYBODY (DATE) HOME              INTEL
         BNZ   USEDATE                                            INTEL
         LM    R0,R1,JCTXEQON    GET XEQ START TIME/DATE          INTEL
         LTR   R1,R1                                              INTEL
         BNZ   USEDATE                                            INTEL
         LM    R0,R1,JCTRDRON    GET ON READER TIME/DATE          INTEL
         LTR   R1,R1                                              INTEL
         BZ    NODATE7                                            INTEL
USEDATE  BAL   R7,TIMECONV       FORMAT IT PRETTY                 INTEL
         MVC   TIMESTMP,CDAYN    MOVE IN TO RECORD                INTEL
NODATE7  DS    0H                                                 INTEL
         EJECT ,                                                  INTEL
         AIF   (&QSP).$SP6                                        INTEL
         L     R1,JCTLINES       GET VALUE                        INTEL
         LR    R0,R1             SAVE IT FOR LATER                INTEL
         MVC   LINESOUT(8),EDMASK7       MOVE IN MASK             INTEL
         CVD   R1,DOUBLEWD       CONVERT TO DECIMAL               INTEL
         ED    LINESOUT(8),DOUBLEWD+4                             INTEL
         AL    R1,PRTTOTAL       SAVE TOTALS                      INTEL
         ST    R1,PRTTOTAL          AWAY FOR FINAL SUMMARY        INTEL
*                                                                 INTEL
         L     R1,JCTPUNCH       GET VALUE                        INTEL
         A     R1,JCTCARDS       CARDS IN + OUT                   INTEL
         MVC   CARDSOUT(8),EDMASK7       MOVE IN MASK             INTEL
         CVD   R1,DOUBLEWD       CONVERT TO DECIMAL               INTEL
         ED    CARDSOUT(8),DOUBLEWD+4                             INTEL
         AR    R0,R1             ACCUMULATE FOR JOB TOTALS        INTEL
         AL    R1,PUNTOTAL       SAVE TOTALS                      INTEL
         ST    R1,PUNTOTAL          AWAY FOR FINAL SUMMARY        INTEL
         A     R0,JOBTOTAL       ACCUMULATE FOR JOB TOTALS        INTEL
         ST    R0,JOBTOTAL          AND STUFF IT AWAY;            INTEL
         AGO   .$SP6A                                             INTEL
.$SP6    ANOP  ,                                                  INTEL
******************************************************************INTEL
*                                                                 INTEL
* CHECK TO SEE IF JOB WAS RELOADED                                INTEL
*   IF SO, THEN USE TOTPDDB SUBROUTINE INSTEAD OF JCT...          INTEL
*          TO FIND TOTAL LINES.                                   INTEL
*                                                                 INTEL
******************************************************************INTEL
         CLC   =C'LOD',JCTINDEV  SEE IF INPUT DEVICE WAS RELOAD   INTEL
         BNE   USEJCT                                             INTEL
         BAL   R7,TOTPDDB        GO TOTAL PDDB LINES              INTEL
         B     USEJCT            IF HAD DSID=1-5, USE JCT         INTEL
         L     R1,RECSPDDB       GET TOTAL RECS FROM PDDB         INTEL
         MVC   LINESOUT(8),EDMASK7  MOVE IN MASK                  INTEL
         CVD   R1,DOUBLEWD       CONVERT TO DECIMAL               INTEL
         ED    LINESOUT(8),DOUBLEWD+4                             INTEL
         LR    R0,R1                                              INTEL
         AL    R1,PRTTOTAL       ACCUMULATE TOTALS                INTEL
         ST    R1,PRTTOTAL          AND SAVE IT AWAY              INTEL
         AL    R0,JOBTOTAL       SAVE JOB TOTAL                   INTEL
         ST    R0,JOBTOTAL                                        INTEL
         MVC   PUNDEST(13),=C'< $RELOADED >'                      INTEL
         B     NOUSEJCT          SKIP JCT STUFF                   INTEL
         SPACE 1                                                  INTEL
USEJCT   L     R1,JCTLINES       GET VALUE                        INTEL
         LR    R0,R1             SAVE IT FOR LATER                INTEL
         MVC   LINESOUT(8),EDMASK7       MOVE IN MASK             INTEL
         CVD   R1,DOUBLEWD       CONVERT TO DECIMAL               INTEL
         ED    LINESOUT(8),DOUBLEWD+4                             INTEL
         AL    R1,PRTTOTAL       SAVE TOTALS                      INTEL
         ST    R1,PRTTOTAL          AWAY FOR FINAL SUMMARY        INTEL
*                                                                 INTEL
         L     R1,JCTPUNCH       GET VALUE                        INTEL
         A     R1,JCTCARDS       CARDS IN + OUT                   INTEL
         MVC   CARDSOUT(8),EDMASK7       MOVE IN MASK             INTEL
         CVD   R1,DOUBLEWD       CONVERT TO DECIMAL               INTEL
         ED    CARDSOUT(8),DOUBLEWD+4                             INTEL
         AR    R0,R1             ACCUMULATE FOR JOB TOTALS        INTEL
         AL    R1,PUNTOTAL       SAVE TOTALS                      INTEL
         ST    R1,PUNTOTAL          AWAY FOR FINAL SUMMARY        INTEL
         A     R0,JOBTOTAL       ACCUMULATE FOR JOB TOTALS        INTEL
         ST    R0,JOBTOTAL          AND STUFF IT AWAY;            INTEL
NOUSEJCT DS    0H                                                 INTEL
.$SP6A   ANOP  ,                                                  INTEL
*                                                                 INTEL
         SPACE 1                                                  INTEL
FMTDISP  BAL   R7,DISPLAY                                         INTEL
         SPACE 1                                                  INTEL
FMTJCTE  LM    R2,R9,REGSAVE1  RESTORE GOOD REGISTERS             INTEL
         BR    R7             RETURN TO MAINLINE...               INTEL
         SPACE 2                                                  INTEL
EDMASK9  DC    X'4040402020206B2020206B202120'                    INTEL
EDMASK7  DC    X'4020202020202120'                                INTEL
EDMASK5  DC    X'402020202120'                                    INTEL
         EJECT ,                                                  INTEL
***********************************************************************
*                                                                 INTEL
*     FORMAT THE JOES ATTACHED TO THE JQE DISPLAYED               INTEL
*                                                                 INTEL
***********************************************************************
FRMTJOES ST    R7,FRMTJOR7     SAVE RETURN ADDRESS                INTEL
         XC    TOTJOECT,TOTJOECT  CLEAR OUT OUR ACCUMULATOR       INTEL
         AIF   (&QSP).$SP1                                        INTEL
         LH    R8,JQEJOE       GO GET THE FIRST JOE OFFSET        INTEL
AGAINJOE N     R8,=X'0000FFFF' MASK OFF THE TRASH                 INTEL
         AGO   .$SP1A                                             INTEL
.$SP1    ANOP  ,                                                  INTEL
         ICM   R8,B'0111',JQEJOEB  GO GET FIRST JOE OFFSET        INTEL
AGAINJOE LA    R8,0(,R8)       MASK OFF THE TRASH                 INTEL
.$SP1A   ANOP  ,                                                  INTEL
         LTR   R8,R8           ANYBODY HOME                       INTEL
         BZ    FINJOES         NO...SPLIT BACK TO MAINLINE        INTEL
         AIF   (&QSP).$SP2                                        INTEL
NEXTJOE  SLA   R8,2            MULTIPLY BY 4                      INTEL
         AGO   .$SP2A                                             INTEL
.$SP2    ANOP  ,                                                  INTEL
NEXTJOE  DS    0H                                                 INTEL
.$SP2A   ANOP  ,                                                  INTEL
         BZ    FINJOES         LAST ONE...SPLIT                   INTEL
         A     R8,QCJOTA       ADDRESS THIS BUGGER                INTEL
*                                                                 INTEL
* CHK TO SEE IF WE WANT TO FORMAT JQE FOR HELD OUTPUT OR          INTEL
*                                 JOE FOR ALL OTHERS              INTEL
         CLI   JQEHLDCT,0      ANYTHING HELD (PART 1)             INTEL
         BNE   DOJQE           YES..FORMAT THE JQE                INTEL
         TM    JQEHQLOK,X'F0'  ANY HELD DS    (PART2)             INTEL
         BZ    DOJOE           YEP...FORMAT THE JQE               INTEL
         SPACE 1                                                  INTEL
DOJQE    BAL   R7,JQEHOLDS     GO SHOW HELD DS COUNT              INTEL
         B     DODISPLA        GO FORMAT IT                       INTEL
         SPACE 1                                                  INTEL
DOJOE    BAL   R7,OUTJOES      GO FORMAT THE JOE INFORMATION      INTEL
         SPACE 1                                                  INTEL
DODISPLA BAL   R7,DISPLAY      GO DISPLAY LINE                    INTEL
         SPACE 1                                                  INTEL
*                                                                 INTEL
         L     R7,JOERECCT     GET COUNT OF RECORDS               INTEL
         A     R7,TOTJOECT     ADD IT UP                          INTEL
         ST    R7,TOTJOECT       AND PUT IT BACK                  INTEL
*                                                                 INTEL
         AIF   (&QSP).$SP0                                        INTEL
         LH    R8,JOEJOE       GET NEXT JOB JOE                   INTEL
         AGO   .$SP0A                                             INTEL
.$SP0    ANOP  ,                                                  INTEL
         ICM   R8,B'0111',JOEJQNXB GET NEXT JOE ADDRESS (SP3)     INTEL
         LA    R8,0(,R8)           AND CLEAR THE TOP TRASH        INTEL
.$SP0A   ANOP  ,                                                  INTEL
         B     AGAINJOE            BUMP AND GRIND                 INTEL
*                                                                 INTEL
FINJOES  MVC   QDMSG,QBLANK                                       INTEL
         L     R1,TOTJOECT    GET JOE LINES TOTAL                 INTEL
         LTR   R1,R1                                              INTEL
         BZ    FINJOES1                                           INTEL
         CVD   R1,DOUBLEWD    CONVERT TO DECIMAL                  INTEL
         MVC   LINESOUT(8),EDMASK7       MOVE IN MASK             INTEL
         ED    LINESOUT(8),DOUBLEWD+4                             INTEL
         MVC   TIMESTMP,=CL26'--------------OUTPUT DS --'         INTEL
         MVC   PRTDEST,=C'OUTTOT'                                 INTEL
         SPACE 1                                                  INTEL
         L     R1,JOBTOTAL    GET JOB TOTAL LINES+CARDSIN+CARDS O INTEL
         LTR   R1,R1                                              INTEL
         BZ    FINJOES1                                           INTEL
         CVD   R1,DOUBLEWD    CONVERT TO DECIMAL                  INTEL
         MVC   CARDSOUT(8),EDMASK7       MOVE IN MASK             INTEL
         ED    CARDSOUT(8),DOUBLEWD+4                             INTEL
         MVC   PUNDEST,=C'JOBTOT'                                 INTEL
         BAL   R7,DISPLAY                                         INTEL
*                                                                 INTEL
FINJOES1 L     R7,FRMTJOR7     GET RETURN ADDRESS                 INTEL
         BR    R7                                                 INTEL
         EJECT ,                                                  INTEL
***********************************************************************
*                                                                 INTEL
*   LOCAL SUBROUTINES.. BLNKLINE                                  INTEL
*                       DISPLAY                                   INTEL
***********************************************************************
BLNKLINE MVC   QDMSG,QBLANK   SET UP A BLANK LINE                 INTEL
*                                                                 INTEL
DISPLAY  MVC   QDMLNG,=H'80'  SET LENGTH                          INTEL
         LA    R0,QDMSG       GET ADDRESS OF MESSAGE              INTEL
         ST    R0,QDMSGA      AND SAVE ADDRESS                    INTEL
         MVC   QDHLINE,HEADER  RESET HEADING FROM FORMATS STUFF.. INTEL
         L     R15,=V(DISPLAY) GET ADDRESS OF MODULE FOR DISPLAY  INTEL
         BALR  R14,R15        CALL THE MOTHER                     INTEL
         BR    R7                                                 INTEL
*                                                                 INTEL
         EJECT
***********************************************************************
* OUTJOES SUBROUTINE - FORMAT JOE DATA                            INTEL
*                                                                 INTEL
*   CALL SUBROUTINES:                                             INTEL
*       INITMSG   - INITIALIZE MESSAGE                            INTEL
*       SETDEVIC  - SHOW PRINT/PUNCH ACTIVE ON DEVICE             INTEL
*       RMTORLCL  - FORMAT OUT DEST LOCAL OR RMTXXX               INTEL
*       FITNUM    - PUT NUMBER IN LEFT JUSTIFIED FORM             INTEL
*                                                                 INTEL
***********************************************************************
OUTJOES  ST    R7,JOESHOLD SAVE THE RETURN ADDRESS                INTEL
         BAL   R7,INITMSG     INITIALIZE THE OUTPUT MSG           INTEL
         TM    JOEFLAG,$JOEBUSY ACTIVE OUTPUT?                    INTEL
         BZ    WAITOUT        NO => AWAITING OUTPUT               INTEL
*                             YES= ACTIVE , SO PROCESS DEVICE     INTEL
         LA    R3,JOEDEVID        A(OUTPUT DEVICE DESCRIPTOR)     INTEL
         BAL   R7,SETDEVIC SET THE OUTPUT DEVICE                  INTEL
         B     OUTJOERT       GO EXIT..ALL DONE HERE              INTEL
*                                                                 INTEL
WAITOUT  MVC   TIMESTMP(L'AWAITOUT),AWAITOUT AWAITING OUTPUT      INTEL
         MVC   MSGOUTCL,JOECURCL  OUTPUT CLASS                    INTEL
*                                                                 INTEL
         LA    R15,MSGOUTDS             GET ADDRESS TO PUT TEXT   INTEL
         LA    R1,JOEROUT     POINT TO ROUTING INFO               INTEL
         BAL   R7,RMTORLCL    GO CONVERT TO TEXT                  INTEL
*                                                                 INTEL
         ST    R8,DOUBLEWD    SAVE CURRENT JOE ADDRESS            INTEL
         AIF   (&QSP).$SP3                                        INTEL
         LH    R8,JOECHAR     GET CHARACTERISTIC JOE ADDRESS      INTEL
         N     R8,=X'0000FFFF'  MASK OFF POSSIBLE JUNK            INTEL
         SLL   R8,2           CHEAP MULTIPLY BY 4                 INTEL
         AGO   .$SP3A                                             INTEL
.$SP3    ANOP  ,                                                  INTEL
         L     R8,JOECHAR     GET CHAR JOE ADDRESS (SP3)          INTEL
         LA    R8,0(,R8)      MASK OFF TRASH                      INTEL
.$SP3A   ANOP  ,                                                  INTEL
         A     R8,QCJOTA      BASE+OFFSET YIELDS CHAR JOE ADDRESS INTEL
         MVC   MSGOUTFO,JOEFORM    COPY ACROSS  FORM              INTEL
         MVC   MSGOUTFC,JOEFCB     COPY ACROSS  FCB               INTEL
         MVC   MSGOUTUC,JOEUCS     COPY ACROSS  UCS               INTEL
         L     R8,DOUBLEWD    RESTORE REAL WORK JOE ADDRESS       INTEL
*                                                                 INTEL
DOJOELNE L     R1,JOERECCT     GET COUNT OF LINES OR CARDS        INTEL
         MVC   LINESOUT(8),EDMASK7       MOVE IN MASK             INTEL
         CVD   R1,DOUBLEWD       CONVERT TO DECIMAL               INTEL
         ED    LINESOUT(8),DOUBLEWD+4                             INTEL
*                                                                 INTEL
OUTJOERT L     R7,JOESHOLD YES => RETURN                          INTEL
         BR    R7             TO OUR CALLER                       INTEL
         SPACE 3                                                  INTEL
JQELEN   DC    AL2(JQELNGTH)  LENGTH OF A JQE                     INTEL
*                                                                 INTEL
AWAITOUT DC    C'Q=X F=XXXX C=XXXX T=XXXX '
*                0123456789×123456789×123456789×123456789×1       INTEL
         EJECT
***********************************************************************
* INITMSG SUBROUTINE - INITIALIZE MSG BUFFER WITH STATS           INTEL
*                                                                 INTEL
***********************************************************************
INITMSG  MVC   QDMSG,QBLANK   BLANK OUT THE LINE                  INTEL
         TM    JQEFLAGS,QUEHOLDA+QUEHOLD1+QUEHOLD2+QUEPURGE+QUEOPCAN KT
         BZR   R7             NO SPECIAL FLAGS                    INTEL
         MVC   MSGSPCST(L'HOLD),HOLD ASSUME THE JOB'S ON HOLD     INTEL
         TM    JQEFLAGS,QUEHOLDA+QUEHOLD1 JOB ON HOLD?            INTEL
         BNZR  R7             YES => RETURN TO OUR CALLER         INTEL
         MVC   MSGSPCST(L'PURGE),PURGE ASSUME THE JOB'S TO BE PUR INTEL
         TM    JQEFLAGS,QUEPURGE JOB TO BE PURGED?                INTEL
         BOR   R7             YES => RETURN TO OUR CALLER         INTEL
         MVC   MSGSPCST(L'CANCEL),CANCEL ASSUME JOB CANCELLED     INTEL
         TM    JQEFLAGS,QUEOPCAN JOB CANCELLED?                   INTEL
         BOR   R7             YES => RETURN TO OUR CALLER         INTEL
         MVC   MSGSPCST(L'DUPLICAT),DUPLICAT MUST BE DUPLICATE JOBINTEL
         BR    R7             RETURN TO OUR CALLER                INTEL
         SPACE 5                                                  INTEL
HOLD     DC    C'HOLD'                                            INTEL
PURGE    DC    C'PURGE'                                           INTEL
CANCEL   DC    C'CANCEL'                                          INTEL
DUPLICAT DC    C'DUPLIC'                                          INTEL
         EJECT
***********************************************************************
* SETDEVIC SUBROUTINE - GET DEVICE DATA (R3) POINTS TO DEVID      INTEL
*                                                                 INTEL
***********************************************************************
SETDEVIC ST    R7,SETDHOLD       SAVE LINK ADDRESS                INTEL
         MVC   TIMESTMP(L'ON),ON PUT IN THE 'Q=X ON ........'     INTEL
         TM    0(R3),HIGHBIT REMOTE DEVICE?                       INTEL
         BO    RMTDEV         YES => OUTPUT IT                    INTEL
         CLI   0(R3),X'0F'    IS IT PROCESS SYSOUT JOE?           INTEL
         BE    PSO            YES => SPEAK ABOUT IT               INTEL
         SPACE 1                                                  INTEL
         SR    R1,R1          FOR THE INSERT CHARACTER            INTEL
         IC    R1,0(R3)         DEVICE TYPE                       INTEL
         SRL   R1,4           RIGHT JUSTIFIED                     INTEL
         MH    R1,DEVTYPEL    TYPE * LENGTH OF A DEVICE ENTRY     INTEL
         LA    R1,DEVTABLE(R1) A(DEVICE TYPE)                     INTEL
         MVC   MSGDEV,1(R1)   PUT IN THE DEVICE TYPE              INTEL
         CLI   0(R3),0          INTERNAL READER?                  INTEL
         BE    SETDRTN        YES => GIVE THE USER THE INFO       INTEL
         SR    R15,R15        FOR THE INSERT CHARACTER            INTEL
         AIF   (&QSP).RNB21A                                      RNB21
         IC    R15,1(R3)        DEVICE NUMBER                     INTEL
         AGO   .RNB21B                                            RNB21
.RNB21A  ANOP                                                     RNB21
         ICM   R15,3,1(R3)      DEVICE NUMBER  (SP2)              RNB21
.RNB21B  ANOP                                                     RNB21
         CVD   R15,DOUBLEWD   IN PACKED DECIMAL                   INTEL
         IC    R15,0(R1)      OFFSET TO WHERE THE DEV # GOES      INTEL
         LA    R15,MSGDEV(R15) A(WHERE THE DEV # GOES)            INTEL
         MVC   1(L'DIGITS3,R15),DIGITS3 SET UP THE EDIT OF 3 DIGI INTEL
         EDMK  0(L'DIGITS3+1,R15),DOUBLEWD+6 DEV # IN EBCDIC      INTEL
         MVC   0(L'DIGITS3+1,R15),0(R1) ADJUST FOR BLANKS         INTEL
*                                                                 INTEL
         B     SETDLINE       AND GO PROCESS LINES/CARDS LEFT     INTEL
*                                                                 INTEL
PSO      MVC   MSGDEV,=CL8'PRSYSOUT'   HEY, HEY, HEY..EXTERNAL WTRINTEL
         B     SETDLINE       GO FORMAT CLASS/LINES/TOGO          INTEL
*                                                                 INTEL
RMTDEV   SR    R0,R0          FOR THE INSERT CHARACTER            INTEL
         AIF   (&QSP).QSP3                                        UF020
         IC    R0,1(R3)         REMOTE NUMBER                     INTEL
         AGO   .QSP4                                              UF020
.QSP3    ANOP                                                     UF020
         IC    R0,2(R3)         REMOTE NUMBER                     RNB21
.QSP4    ANOP                                                     UF020
         CVD   R0,DOUBLEWD    IN PACKED DECIMAL                   INTEL
         MVI   MSGDEV,C'R'    INDICATE A REMOTE DEVICE            INTEL
         MVC   MSGDEV+2(L'THREEPT),THREEPT SET UP THE EDIT MASK   INTEL
         EDMK  MSGDEV+1(L'THREEPT),DOUBLEWD+6 CONVERT TO EBCDIC   INTEL
         MVC   MSGDEV+1(L'THREEPT),0(R1) ADJUST FOR BLANKS        INTEL
         LA    R1,MSGDEV+1    A(SPOT JUST BEFORE POSSIBLE SEP)    INTEL
FINDPT   LA    R1,1(R1)       A(NEXT BYTE)                        INTEL
         CLI   0(R1),C'.'     FOUND THE SEPARATOR?                INTEL
         BNE   FINDPT         NO => KEEP LOOKING                  INTEL
         SR    R15,R15        FOR THE INSERT CHARACTER            INTEL
         IC    R15,0(R3)        DEVICE TYPE INDICATOR             INTEL
         SRL   R15,3          RIGHT JUSTIFIED                     INTEL
         LA    R15,RMTDEVS-HIGHBIT/8(R15) A(DEVICE TYPE)          INTEL
         MVC   1(2,R1),0(R15) PUT IN THE DEVICE TYPE              INTEL
         MVC   3(1,R1),0(R3) PUT IN THE DEVICE NUMBER             INTEL
         OI    3(R1),C'0'     MAKE THE DEVICE NUMBER PRINTABLE    INTEL
*                                                                 INTEL
*  HAVE TO BE WORKING ON A JOE TO GET HERE (I THINK)              INTEL
*                                                                 INTEL
SETDLINE MVC   MSGLCLAS,JOECURCL   MOVE IN CLASS                  INTEL
         LA    R15,PRTDEST                                        INTEL
         LA    R1,JOEROUT     POINT TO WHERE TO...                INTEL
         BAL   R7,RMTORLCL    CONVERT TO NICE FORMAT              INTEL
*                                                                 INTEL
         USING QCKPT,R7       BASE REG FOR CKPT WORK AREA         INTEL
         L     R7,QVCKPT      LOAD BASE REGISTER                  INTEL
         ST    R8,DOUBLEWD    SAVE THIS JOE ADDRESS               INTEL
         AIF   (&QSP).$SP4                                        INTEL
         LH    R8,JOECHAR     GET CHAR JOE ADDRESS                INTEL
         N     R8,=X'0000FFFF' MASK OFF TRASH                     INTEL
         SLL   R8,2           MULTIPLY BY 4                       INTEL
         AGO   .$SP4A                                             INTEL
.$SP4    ANOP  ,                                                  INTEL
         L     R8,JOECHAR     GET CHAR JOE ADDRESS                INTEL
         LA    R8,0(,R8)      MASK OFF TRASH                      INTEL
.$SP4A   ANOP  ,                                                  INTEL
         A     R8,QCJOTA      BASE+DISPL = ADDR OF CHAR JOE       INTEL
         MVC   MSGFORM,JOEFORM MOVE IN FORM                       INTEL
         L     R8,DOUBLEWD    GET REAL JOE ADDRESS BACK           INTEL
*                                                                 INTEL
         L     R1,JOERECCT    GET AMOUNT OF STUFF                 INTEL
         MVC   LINESOUT(8),EDMASK7       MOVE IN MASK             INTEL
         CVD   R1,DOUBLEWD       CONVERT TO DECIMAL               INTEL
         ED    LINESOUT(8),DOUBLEWD+4                             INTEL
*                                                                 INTEL
         TM    JOEFLAG,X'80'  IS IT NOW BUSY                      INTEL
         BZ    SETDRTN        NO..EXIT                            INTEL
         AIF   (&QSP).$SP5                                        INTEL
         LH    R14,JOECKPT    GET OFFSET FOR CHKPT JOE            INTEL
         SLA   R14,2          MULTIPLY BY 4                       INTEL
         AGO   .$SP5A                                             INTEL
.$SP5    ANOP  ,                                                  INTEL
         L     R14,JOECKPT    GET CKPT JOE ADDRESS                INTEL
         LA    R14,0(,R14)    MASK OFF TRASH                      INTEL
         LTR   R14,R14        ANYBODY THERE?                      INTEL
.$SP5A   ANOP  ,                                                  INTEL
         BZ    SETDRTN        NO CKPT JOE..EXIT                   INTEL
         A     R14,QCJOTA     ADD BASE TO OFFSET                  INTEL
         DROP  R7                                                 INTEL
         L     R1,JOERECCT    GET TOTAL COUNT OF RECS             INTEL
         S     R1,JOETLNC-JOEDSECT(R14)  SUBTRACT OFF RECORDS DONEINTEL
         MVC   CARDSOUT(8),EDMASK7       MOVE IN MASK             INTEL
         CVD   R1,DOUBLEWD       CONVERT TO DECIMAL               INTEL
         ED    CARDSOUT(8),DOUBLEWD+4                             INTEL
         MVC   PUNDEST,=CL6'TO GO='                               INTEL
*                                                                 INTEL
SETDRTN  L     R7,SETDHOLD    GET RETURN ADDRESS                  INTEL
         BR    R7             RETURN TO OUR CALLER                INTEL
         EJECT ,                                                  INTEL
*                DDMONYY (DDD) DAY HH:MM:SS RMTXXX XXXXXXX RMTXXX INTEL
*                0123456789×123456789×123456789×123456789×1       INTEL
ON       DC    C'Q=X F=XXXX NOW ON X       '                      INTEL
DIGITS3  DC    X'202020'                                          INTEL
THREEPT  DC    X'2020204B'                                        INTEL
         DS    0H                                                 INTEL
DEVTABLE DC    AL1(0),CL8'INTRDR',AL1(6),CL8'READER'              INTEL
         DC    AL1(7),CL8'PRINTER',AL1(5),CL8'PUNCH'              INTEL
DEVTYPEL DC    AL2((*-DEVTABLE)/4)                                INTEL
RMTDEVS  DC    C'**',C'RD',C'PR',C'PU'                            INTEL
HIGHBIT  EQU   X'80'                                              INTEL
         EJECT
***********************************************************************
* JQEHOLDS SUBROUTINE - FIND HELD DATASET INFO                    INTEL
*                                                                 INTEL
*                                                                 INTEL
***********************************************************************
JQEHOLDS CLI   JQETYPE,$HARDCPY JOB ON THE OUTPUT QUEUE?          INTEL
         BNER  R7             NO => RETURN TO CALLER              INTEL
         LH    R1,JQEHLDCT    BL12'HELD DS COUNT',BL4'HELD DS LOC'INTEL
         SRL   R1,4           HELD DS COUNT                       INTEL
         LTR   R1,R1          ANY DS HELD BY THIS JOB?            INTEL
         BZR   R7             NO => RETURN TO OUR CALLER          INTEL
         ST    R7,HOLDSLNK SAVE OUR RETURN ADDRESS                INTEL
         BAL   R7,INITMSG     INITIALIZE THE OUTPUT MESSAGE       INTEL
         MVC   TIMESTMP(L'WATOUTOF),WATOUTOF C'AWAITING OUTPUT OF INTEL
         LA    R15,TIMESTMP+L'WATOUTOF+1 A(SPOT FOR THE HELD DS C INTEL
         LH    R1,JQEHLDCT    BL12'HELD DS COUNT',BL4'HELD DS LOC INTEL
         SRL   R1,4           HELD DS COUNT                       INTEL
         BAL   R7,FITINUM     PUT THE HELD DS COUNT IN THE MSG    INTEL
         MVC   1(L'HLDSMSG,R15),HLDSMSG C'HELD SYSOUT(S)'         INTEL
         L     R7,HOLDSLNK RESTORE OUR RETURN ADDRESS             INTEL
         BR    R7             RETURN TO OUR CALLER                INTEL
         SPACE 5                                                  INTEL
WATOUTOF DC    C'AWAITING OUTPUT OF'                              INTEL
HLDSMSG  DC    C'HELD SYSOUT(S)'                                  INTEL
         EJECT ,                                                  INTEL
***********************************************************************
*RMTORLCL SUBROUTINE - DETERMINE REMOTE OR LOCAL DESTINATION      INTEL
* R1 POINTS TO PRTRT/PUNRT, R15 TO ASSEMBLY POINTER               INTEL
***********************************************************************
         AIF   (&QSP).QSP1                                        UF020
RMTORLCL CLI   1(R1),0        IS IT FOR REMOTE 00=LOCAL           INTEL
         AGO   .QSP2                                              UF020
.QSP1    ANOP                                                     UF020
RMTORLCL CLI   3(R1),0        IS IT FOR REMOTE 00=LOCAL           INTEL
.QSP2    ANOP                                                     UF020
         BE    RMTORLC0       YES......                           INTEL
         AIF   (&QSP).RNB18A                                      RNB18
         CLI   0(R1),0        SPECIAL LOCAL ROUTING?              RNB18
         AGO   .RNB18B                                            RNB18
.RNB18A  ANOP                                                     RNB18
         CLC   =H'0',0(R1)    SPECIAL LOCAL ROUTING (SP2) ?       RNB18
.RNB18B  ANOP                                                     RNB18
         BNE   RMTORLC1          /NO  - GO FORMAT RMTNN           RNB18
RMTORLC0 MVC   0(L'LOCAL,R15),LOCAL /YES - MOVE IN LOCAL          RNB18
         LA    R15,L'LOCAL(R15)  BUMP POINTER                     RNB18
         B     RMTORLC2                                           RNB18
RMTORLC1 EQU   *                                                  RNB18
         MVC   0(L'RMT,R15),RMT       MOVE IN 'RMT'               INTEL
         LA    R15,L'RMT(R15)  BUMP POINTER                       INTEL
RMTORLC2 EQU   *                                                  RNB18
         SR    R14,R14        LOAD RMT FLAGS                      INTEL
         AIF   (&QSP).RNB18C                                      RNB18
         IC    R14,1(R1)      FROM PRT/PUN                        INTEL
         AGO   .RNB18D                                            RNB18
.RNB18C  ANOP                                                     RNB18
         IC    R14,3(R1)      FROM JOEROUT                        RNB18
.RNB18D  ANOP                                                     RNB18
         LTR   R14,R14        ANYBODY HOME?                       INTEL
         BZR   R7                                                 INTEL
         CVD   R14,DOUBLEWD   RMT NUMBER TO DECIMAL               INTEL
         B     FITINUM1       FIT THE NUMBER IN RMT MESSAGE       INTEL
         SPACE 2
***********************************************************************
* FITINUM SUBROUTINE - CONVERT BIN NUMBER TO NICE FORMAT          INTEL
*                                                                 INTEL
***********************************************************************
FITINUM  CVD   R1,DOUBLEWD    CONVERT TO PACKED DECIMAL           INTEL
FITINUM1 MVC   EDITWORK(L'NORMAL),NORMAL INIT THE EDIT FORMAT     INTEL
         LA    R1,EDITWORK+SIGNORM IN CASE OF ZEROES              INTEL
         EDMK  EDITWORK(L'NORMAL),DOUBLEWD+2 CONVERT TO EBCDIC    INTEL
         LA    R14,EDITWORK+L'NORMAL-1 A(END OF CONVERTED NUMBER) INTEL
         SLR   R14,R1         LENGTH OF THE CONVERTED NUMBER - 1  INTEL
         EX    R14,MVNUMBER   PUT THE NUMBER IN THE MSG           INTEL
         LA    R15,1(R14,R15) A(NEXT SPOT IN MSG)                 INTEL
         BR    R7             RETURN TO OUR CALLER                INTEL
         SPACE 3                                                  INTEL
MVNUMBER MVC   0(0,R15),0(R1) TO BE EXECUTED                      INTEL
         SPACE 2                                                  INTEL
NORMAL   DC    X'402020202020202020202120' EDIT MASK              INTEL
SIGNORM  EQU   11             OFFSET TO LAST DIGIT                INTEL
RMT      DC    C'RMT'                                             INTEL
LOCAL    DC    C'LOCAL'                                           INTEL
         EJECT ,                                                  INTEL
***********************************************************************
*                                                                     *
* LOCAL SUBROUTINE: TOTPDDB                                           *
*                                                                     *
*  BASED ON CPDDB COMMAND                                             *
*                                                                     *
*  PURPOSE: TOTAL 'REAL' RECORD COUNT FOR JOB                         *
*                                                                     *
***********************************************************************
TOTPDDB  STM   R0,R10,REGSAVE2    SAVE OFF THE REGS               INTEL
         XC    RECSPDDB,RECSPDDB  CLEAR OUT COUNTER               INTEL
         USING QCKPT,R10          BASE REG FOR CHECKPONT WORK AREA
         L     R10,QVCKPT         LOAD BASE REG
***********************************************************************
*                                                                     *
*   FIND ALL PDDBS FOR THIS JOB                                       *
*                                                                     *
***********************************************************************
         USING PDBDSECT,R2        BASE REG FOR PDDB
         USING IOTSTART,R3        BASE REG FOR IOT
         MVI   SWITCH,0           CLEAR FLAG BYTE
         L     R3,QCIOTA          LOAD BASE REG
         LR    R5,R3              IOAREA FOR READ IOT BLOCK
         L     R4,JCTIOT          FIRST IOT ADDRESS
         BAL   R8,READ            READ THE IOT
         SPACE 1
NEXTIOT  LR    R4,R3              BASE OF IOT
         A     R4,IOTPDDBP        OFFSET BEYOND LAST PDDB
         LR    R2,R3              BASE OF IOT
         A     R2,QCPDDB1         OFFSET TO FIRST PDDB IN IOT
PDDBLOOP LH    R0,PDBDSKEY        GET THE DSID
         SPACE 1
PDDBLP1  LTR   R0,R0              TEST FOR NULL PDDB#
         BZ    NEXTPDDB           SKIP IF SO
         CH    R0,=H'5'           IS THIS INTERNAL TEXT           INTEL
         BNE   *+8        =====+  IF NOT..SKIP SWITCH SET         INTEL
         OI    SWITCH,X'80'    ×  INDICATE INTERNAL TEXT FOUND    INTEL
         L     R0,PDBRECCT  <==+  GET THE RECORD COUNT
         A     R0,RECSPDDB        ADD TO WHAT HAS BEEN FOUND BEFO INTEL
         ST    R0,RECSPDDB        AND ACCUMULATE                  INTEL
         SPACE 1                                                  INTEL
NEXTPDDB LA    R2,PDBLENG(R2)     POINT TO NEXT PDDB
         TM    SWITCH,X'80'                                       INTEL
         BO    TOT#RTN4           WHY DO THIS MESS..JCT IS OK..   INTEL
         SPACE 1                                                  INTEL
         CR    R2,R4              HAVE WE GONE PAST THE LAST PDDB
         BL    PDDBLOOP           NO. KEEP TRYING
         L     R4,IOTIOTTR        DISK ADDR OF NEXT IOT
SPIN     LTR   R4,R4              IS THERE ANOTHER IOT?
         BZ    SPINIOT            NO. TRY THE SPIN IOT.
         BAL   R8,READ            READ THE IOT
         B     NEXTIOT            SEARCH THE NEXT IOT
SPINIOT  TM    SWITCH,1           DID WE SEARCH THE SPINIOT ALREADY
         B     TOT#RTN0           YES, DONE
         OI    SWITCH,1           SET SWITCH
         L     R4,JCTSPIOT        DISK ADDR OF SPIN IOT
         B     SPIN               SEARCH THE SPIN IOT CHAIN
TOT#RTN0 LM    R0,R10,REGSAVE2    GET REGS                        INTEL
         B     4(,R7)             SAY PDDB STUFF IS OK            INTEL
         SPACE 1                                                  INTEL
TOT#RTN4 LM    R0,R10,REGSAVE2    GET REGS                        INTEL
         BR    R7                 SAY JCT STUFF IS OK             INTEL
         SPACE 1                                                  INTEL
***********************************************************************
*                                                                     *
*   READ A BLOCK FROM HASPACE                                         *
*                                                                     *
***********************************************************************
READ     ST    R4,QCTRAK          STORE DISK ADDR
         LR    R1,R5              IOAREA ADDRESS
         L     R15,=V(READSPC)    ADDR OF ROUTINE TO READ HASPACE
         BALR  R14,R15            GO TO IT
         BR    R8                 RETURN TO CALLER
         EJECT ,                                                  INTEL
***********************************************************************
*                                                                 INTEL
* FORMAT DATE TIME  HEADING   ENTER WITH BAL R7,TIMECONV          INTEL
*   R0=TIME IN 100THS OF SECOND SINCE MIDNIGHT                    INTEL
*   R1=DATE IN 00YYDDDS FORMAT                                    INTEL
*                                                                 INTEL
* RETURNS WITH CDAYN(LEN) = DDMONYY (DDD) DAY HH:MM:SS.TH         INTEL
*                E.G.       07JUL80 (189) MON 13:45:24.56         INTEL
*                           123456789.123456789.123456789         INTEL
*                                                                 INTEL
***********************************************************************
TIMECONV MVC   MONMSK(MVCLEN),XMONMSK  INIT DATE/TIME WORK AREA   INTEL
         ST    R1,DATE                 SAVE DATE FOR LATER        INTEL
         CVD   R0,DOUBLEWD            GET TIME IN DEC 100TH/SEC   INTEL
         DP    DOUBLEWD(8),DEC6000(3) /6000 : REM IS SS.TH        INTEL
         DP    DOUBLEWD(5),DEC60(2)   /60   : REM IS MM           INTEL
         MVC   EDITWORK(L'HHMASK),HHMASK   SETUP MULT EDIT MASK   INTEL
         ED    EDITWORK(L'HHMASK),DOUBLEWD EDIT HH,MM,SS          INTEL
FINTIME  MVC   CHH(2),EDITWORK+4      MOVE IN HOURS               INTEL
         MVC   CMM(2),EDITWORK+8      MOVE IN MM                  INTEL
         MVC   CSS(5),EDITWORK+12     MOVE IN SS.TH               INTEL
*                                                                 INTEL
***FORMAT THE DATE INTO THE OUTPUT MSG                            INTEL
*                                                                 INTEL
         MVC   WORKWORD(4),DATE        MOVE DATE TO WORK AREA.    INTEL
         MVO   WORKWORD+1(3),WORKWORD(2)   MAKE 00YYDDDS INTO 00000YYS.
         UNPK  CYR(2),WORKWORD+2(2)    FORMAT YEAR.               INTEL
         XC    DOUBLEWD,DOUBLEWD                                  INTEL
         MVC   DOUBLEWD+4(4),WORKWORD  CNVT YEAR TO BIN           INTEL
         CVB   R0,DOUBLEWD                                        INTEL
         ST    R0,BINYEAR              AND SAVE                   INTEL
         TM    BINYEAR+3,X'03'         LEAP YEAR?                 INTEL
         BNZ   MON1                    NO.                        INTEL
         MVI   MONMSK+10,29            YES, CORRECT               INTEL
MON1     XC    DOUBLEWD,DOUBLEWD       CNVT DAYS TO BIN           INTEL
         MVC   DOUBLEWD+6(2),DATE+2                               INTEL
         CVB   R3,DOUBLEWD                                        INTEL
         ST    R3,BINDAYS              SAVE FOR LATER             INTEL
         XR    R1,R1                                              INTEL
         LA    R15,11                                             INTEL
MON2     IC    R1,MONMSK(R15)          COMPUTE MON & DAY          INTEL
         SR    R3,R1                                              INTEL
         BNP   MONOVR                                             INTEL
         BCT   R15,MON2                                           INTEL
         B     *+6                                                INTEL
MONOVR   AR    R3,R1                   CORRECT OVERDRAW           INTEL
         CVD   R3,DOUBLEWD             FORMAT DAY                 INTEL
         UNPK  CDAYN(2),DOUBLEWD+6(2)                             INTEL
         OI    CDAYN+1,X'F0'           FIX UP SIGN                INTEL
         MH    R15,=H'3'               GET TABLE OFFSET           INTEL
         LA    R15,MONTAB(R15)         AND POINT AT MONTH         INTEL
         MVC   CMON(3),0(R15)          MOVE IT TO THE MSG         INTEL
         EJECT ,                                                  INTEL
***DAY OF WEEK                                                    INTEL
         L     R15,BINYEAR                                        INTEL
         SH    R15,=H'69'                                         INTEL
         BNP   OUTT                    MUST BE GREATER OR BAD     INTEL
         XR    R14,R14                 CLEAR DAY REG              INTEL
         D     R14,=F'4'                                          INTEL
         MH    R15,=H'5'                                          INTEL
         AR    R14,R15                                            INTEL
         A     R14,BINDAYS                                        INTEL
         SRDA  R14,32                                             INTEL
         D     R14,=F'7'               MODULO 7 FOR WEEK          INTEL
         MH    R14,=H'3'                                          INTEL
         LA    R14,DAYTAB(R14)         POINT AT TODAY             INTEL
         MVC   CDAY(3),0(R14)          INSERT IN MSG              INTEL
***                                                               INTEL
MOVEJD   MVC   JDATE-1(7),=X'4020204B202020'                      INTEL
         ED    JDATE-1(7),DATE+1                                  INTEL
         MVC   CJDAY(3),JDATE+3         MOVE DAY TO MESSAGE       INTEL
*                                                                 INTEL
*                                                                 INTEL
DATERTN  BR    R7                      RETURN TO MAINLINE         INTEL
*                                                                 INTEL
OUTT     MVC   CDAY(3),=CL3'???'                                  INTEL
         B     MOVEJD                                             INTEL
         SPACE 2                                                  INTEL
         EJECT ,                                                  INTEL
***********************************************************************
*                                                                 INTEL
*       CONSTANTS FOR DATE ROUTINE USAGE                          INTEL
*                                                                 INTEL
***********************************************************************
         SPACE ,                                                  INTEL
HHMASK   DC    X'40202021202022212020222120204B2020'              INTEL
DEC60    DC    X'060C'                                            INTEL
DEC6000  DC    X'06000C'                                          INTEL
DEC50    DC    X'050C'                                            INTEL
***********************************************************************
* LEAVE FIELDS TOGETHER.. INITIALIZED BY ONE MVC WITH VALUES      INTEL
*                                                                 INTEL
XMONMSK  DC    AL1(31,30,31,30,31,31,30,31,30,31,28,31)        )  INTEL
         DC    CL1' '                                          )  INTEL
         DC    C'XX.XXX'                           L           )  INTEL
*                                                  E  F        )  INTEL
         DC    CL2' '                              A  I  T     )  INTEL
         DC    CL3' '                              V  E  O  F  )  INTEL
         DC    CL2' ',C' ('                        E  L  G  O  )  INTEL
         DC    CL3' ',C') '                           D  E  R  )  INTEL
         DC    CL3' ',C' '                            S  T     )  INTEL
         DC    CL2' ',C':'                               H  I  )  INTEL
         DC    CL2' ',C':'                               E  N  )  INTEL
         DC    CL5'SS.TH'                                R  I  )  INTEL
*                                                           T  )  INTEL
MVCLEN   EQU   *-XMONMSK                                          INTEL
*                                                                 INTEL
***********************************************************************
DAYTAB   DC    C'TUEWEDTHRFRISATSUNMON'                           INTEL
MONTAB   DC    C'DECNOVOCTSEPAUGJULJUNMAYAPRMARFEBJAN'            INTEL
         EJECT ,                                                  INTEL
***********************************************************************
*                                                                 INTEL
* LITERAL POOL                                                    INTEL
*                                                                 INTEL
***********************************************************************
         LTORG   ,                                                INTEL
         EJECT ,                                                  INTEL
***********************************************************************
*                                                                 INTEL
* LINE MAPPING                                                    INTEL
*                                                                 INTEL
***********************************************************************
*                   0123456789.123456789.123456789.123456789.123456789.
*                   XXXXXXXX XXXXXXXX XXXXX DDMONYY (DDD) DAY HH:MM:SS
HEADER   DC    CL80' JES2 #  JOBNAME  #IN Q ---EXECUTION STOP DATE---- X
               ----PRINT----- -CARDS IN+OUT-'                     INTEL
*              123456789.123456789.123456789                      INTEL
*              RMTXXX XXXXXXX RMTXXX XXXXXXX                      INTEL
HEADER1  DC    CL80'          PRIO=XX  XXXXX XXXXXXXXXXXXXXXX'    INTEL
*                   XXXNNNNNN XXXXXXXX XXNNNNNNNN (42)X   XXXXXX  INTEL
         SPACE 2                                                  INTEL
***********************************************************************
*                                                                 INTEL
*    TRTABLE FOR GENERIC JOBNAME MASKING                          INTEL
*                                                                 INTEL
***********************************************************************
TRMASK   DC    XL256'00'      USED TO SET MASK FOR STATUS COMPARE INTEL
         ORG   TRMASK+C'?'                                        INTEL
         DC    XL1'FF'                                            INTEL
         ORG   TRMASK+C'.'                                        INTEL
         DC    XL1'FF'                                            INTEL
         ORG   TRMASK+C'*'                                        INTEL
         DC    XL1'FF'                                            INTEL
         ORG   ,                                                  INTEL
         EJECT ,                                                  INTEL
***********************************************************************
*                                                                 INTEL
* DSECT DEFINITIONS                                               INTEL
*                                                                 INTEL
***********************************************************************
SYMDEL   DSECT ,                   KILL SYM CARD GENERATION       UF023
CDN      CSECT ,                                                  INTEL
JCT      EQU   0                                                  INTEL
         AIF   (&QSP).$SP9                                        INTEL
BUFSTART EQU   0                                                  INTEL
BUFDSECT EQU   0                                                  INTEL
         AGO   .$SP9A                                             INTEL
.$SP9    ANOP  ,                                                  INTEL
         $BUFFER ,                                                INTEL
$MAXNODE EQU   99           FOR NJE $JOT EXPANSION                UF001
JQEHQLOK EQU   JQEJBLOK     NEW LABEL FOR OLD FIELD               UF020
.$SP9A   ANOP  ,                                                  INTEL
         $TAB
         $JCT  ,                                                  INTEL
         $CVT   ,                                                 INTEL
         $JESCT ,                                                 INTEL
         $SSCT  ,                                                 INTEL
         $SVT   ,                                                 INTEL
         $JQE  ,                                                  INTEL
         $JOE  ,                                                  INTEL
         $JOT  ,                                                  INTEL
         $JQT  ,                                                  INTEL
CDN      CSECT ,                                                  INTEL
         $PDDB
         $IOT
***********************************************************************
         EJECT  ,                                                 INTEL
***********************************************************************
*                                                                 INTEL
* QUEUE COMMON AND LINE OVERLAY DEFINITIONS                       INTEL
*                                                                 INTEL
***********************************************************************
         QCOMMON                                                  INTEL
         SPACE 2                                                  INTEL
*                            OVERLAY FOR DETAIL LINE              INTEL
JOBNUMB  EQU   QDMSG+00,8                                         INTEL
JOBNAME  EQU   QDMSG+09,8                                         INTEL
QUEUE#   EQU   QDMSG+17,6                                         INTEL
TIMESTMP EQU   QDMSG+24,26                                        INTEL
MSGOUTCL EQU   TIMESTMP+2,1                                       INTEL
MSGOUTFO EQU   TIMESTMP+06,4                                      INTEL
MSGOUTFC EQU   TIMESTMP+13,4                                      INTEL
MSGOUTUC EQU   TIMESTMP+20,4                                      INTEL
MSGOUTDS EQU   QDMSG+51,6                                         INTEL
MSGOUTLI EQU   QDMSG+57,8                                         INTEL
MSGLCLAS EQU   TIMESTMP+02,1                                      INTEL
MSGFORM  EQU   TIMESTMP+06,4                                      INTEL
MSGDEV   EQU   TIMESTMP+18,8                                      INTEL
MSGSPCST EQU   QDMSG+66,6     SPECIAL STATUS                      INTEL
LINESTOT EQU   QDMSG+51,14                                        INTEL
PRTDEST  EQU   QDMSG+51,6                                         INTEL
LINESOUT EQU   QDMSG+57,8                                         INTEL
CARDSTOT EQU   QDMSG+66,14                                        INTEL
PUNDEST  EQU   QDMSG+66,6                                         INTEL
CARDSOUT EQU   QDMSG+72,8                                         INTEL
         ORG   ,                                                  INTEL
*                                                                 INTEL
******END OF MESSAGE MAPPING                                      INTEL
         EJECT ,                                                  INTEL
***********************************************************************
*                                                                 INTEL
* WORK AREA DSECT                                                 INTEL
*                                                                 INTEL
***********************************************************************
WORK     DSECT  ,                                                 INTEL
         DS    CL72                                               INTEL
DOUBLEWD DC    D'0'                                               INTEL
REGSAVE1 DC    10F'0'                                             INTEL
REGSAVE2 DC    11F'0'                                             INTEL
FRMTJOR7 DC    F'0'       R7 RETURN ADDRESS FOR FORMJOES          INTEL
JOESHOLD DS    A              RETURN ADDRESS HOLD FOR OUTJOES     INTEL
HOLDSLNK DS    A              RETURN ADDRESS HOLD FOR JQEHOLDS    INTEL
SETDHOLD DS    A              RETURN ADDRESS HOLD FOR SETDEVIC    INTEL
*                                                                 INTEL
JOBTOTAL DC    F'0'                                               INTEL
TOTJOECT DC    F'0'                                               INTEL
PRTTOTAL DC    F'0'                                               INTEL
PUNTOTAL DC    F'0'                                               INTEL
RECSPDDB DC    F'0'                                               INTEL
         SPACE 1                                                  INTEL
WORKNAME DS    CL8                                                INTEL
MASKAREA DC    CL8' '              AREA TO HOLD MASK FOR NAME     INTEL
MASKNAME DC    CL8' '              AREA TO MASKED TARGET FOR JOBN INTEL
         SPACE 1                                                  INTEL
BINYEAR  DC    F'0'                                               INTEL
BINDAYS  DC    F'0'                                               INTEL
DATE     DC    F'0'                                               INTEL
WORKWORD DC    2F'0'                                              INTEL
EDITWORK DC    12H'0'                                             INTEL
***********************************************************************
* LEAVE FIELDS TOGETHER.. INITIALIZED BY ONE MVC WITH VALUES      INTEL
*                                                                 INTEL
MONMSK   DC    AL1(31,30,31,30,31,31,30,31,30,31,28,31)        )  INTEL
         DC    CL1' '                                          )  INTEL
*        0123456789.123456789.123456789.                       )  INTEL
*        DDMONYY (DDD) DAY HH:MM:SS.TH                         )  INTEL
*                                                              )  INTEL
JDATE    DC    C'XX.XXX'                           L           )  INTEL
*                                                  E  F        )  INTEL
CDAYN    DC    CL2' '                              A  I  T     )  INTEL
CMON     DC    CL3' '                              V  E  O  F  )  INTEL
CYR      DC    CL2' ',C' ('                        E  L  G  O  )  INTEL
CJDAY    DC    CL3' ',C') '                           D  E  R  )  INTEL
CDAY     DC    CL3' ',C' '                            S  T     )  INTEL
CHH      DC    CL2' ',C':'                               H  I  )  INTEL
CMM      DC    CL2' ',C':'                               E  N  )  INTEL
CSS      DC    CL5'SS.TH'                                R  I  )  INTEL
*                                                           T  )  INTEL
***********************************************************************
LEN      EQU   *-CDAYN                                            INTEL
SWITCH   DS    C
SYMNODEL DSECT ,                   RESTORE SYM CARD GENERATION    UF023
         END  ,                                                   INTEL
