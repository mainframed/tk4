*          DATA SET S051B16A   AT LEVEL 002 AS OF 05/24/78
*          DATA SET S051B16A1  AT LEVEL 002 AS OF 03/09/77
*          DATA SET GETATTR    AT LEVEL 002 AS OF 03/02/77
         TITLE 'GETATTR - DOCUMENTATION'
*.....................................................................*
*.                                                                   .*
*.   GETATTR                                                         .*
*.                                                                   .*
*.....................................................................*
*.                                                                   .*
*.   1.0  GENERAL DESCRIPTION                                        .*
*.                                                                   .*
*.                                                                   .*
*.....................................................................*
*.                                                                   .*
*.....................................................................*
*.                                                                   .*
*.   2.0  PARAMETER LIST AND RETURN CODE DESCRIPTION                 .*
*.                                                                   .*
*.   THE PARAMETER LIST IS A FOUR WORD LIST CONTAINING THE           .*
*.   FOLLOWING:                                                      .*
*.                                                                   .*
*.    WORD         DESCRIPTION                                       .*
*.                                                                   .*
*.      1          ADDRESS OF THE 256 BYTE BUFFER CONTAINING THE     .*
*.                 FORMAT 1 DSCB TO BE ANALYZED.                     .*
*.      2          ADDRESS OF THE 80-BYTE FIELD INTO WHICH           .*
*.                 GETATTR WILL PLACE ATTR AND USAGE LINE FOR PRINT. .*
*.      3          ADDRESS OF THE 1-BYTE FIELD WHICH CONTAINS THE    .*
*.                 FLAGS TO CONTROL GETATTR OPERATION.               .*
*.      4          ADDRESS OF THE 6 BYTE FIELD CONTAING THE          .*
*.                 VOLSER OF THE VOLUME THAT THE DATASET IS ON.      .*
*.                                                                   .*
*.....................................................................*
*.                                                                   .*
         TITLE 'GETATTR -- REGISTER USAGE'
*.....................................................................*
*.                                                                   .*
*. R2 = PTR TO FMT 1 DSCB                                            .*
*. R3 = PTR TO MESSAGE AREA  (80 BYTES)                              .*
*. R4 = PTR TO FLAG BYTE  (1 BYTE)                                   .*
*. R5 = PTR TO VOLSER  (6 BYTES)                                     .*
*.                                                                   .*
*. R6 = PTR TO ATTRMSG OR USAGE MESSAGE AREA                         .*
*.                                                                   .*
*. R12 = BASE                                                        .*
*. R13 = SAVE AREA POINTER                                           .*
*. OTHERS (R7,R8,R9,R10,R11) = WORK  (R1,R0) FOR MACROS              .*
*.....................................................................*
         TITLE 'GETATTR -- MAIN CODE'
GETATTR  PGM
REALSTRT LM    R2,R5,0(R1)         LOAD ADDRESSES OF PARMS
         USING DSCB1,R2             AND ESTAB. ADDRESSABILITY
         USING MSGDEF,R3
         USING FLAGDEF,R4
         USING VOLDEF,R5
         MVI   MSG,C' '                INITIALIZE MSG WITH BLANKS
         MVC   MSG+1(L'MSG-1),MSG        TO BE NEAT..
*
         EJECT
         TM    FLAG,DOATTR             WAS ATTR REQUESTED?
         BNO   TESTUSAG                 NO SKIP TO USAGE TEST
         LR    R6,R3                   LOAD MESSAGE ADDRESS
         TM    FLAG,DOUSAGE            WAS USAGE REQUESTED TOO?
         BNO   LBL010                  ->NO USAGE NOT ASKED FOR
         LA    R6,MOFFBOTH(R6)         ADJUST ADDRESS
         B     STARTAT                 GO DO THE ATTR
LBL010   LA    R6,MOFFSET(R6)          ADJUST ADDRESS
         USING ATTRMSG,R6
STARTAT  MVC   MVOLSER,VOLSER          MOVE IN VOLSER
         LA    R7,MRECFMEQ              POINT TO START OF RECFM AREA
         AR    R7,R6                     TO DO ASSEMBLY OF RECFM
         OI    FLAG,PRNTATTR            AND SET FLAG TO SAY 'PRINT'
*
*
TRECFM   CLI   RECFM,X'00'             ANY RECFM?
         BNE   TTUNDEF                 -> YES..CONTINUE
         MVI   0(R7),C'?'               SAY I DONT KNOW..
         B     TLRECL                  AND GO CHECK LRECL
TTUNDEF  TM    RECFM,X'C0'                                UNDEFINED?
         BNO   TTFIXED                                    NO
         MVI   0(R7),C'U'
         LA    R7,1(R7)
         B     TTTRKOV
TTFIXED  TM    RECFM,X'80'                                FIXED?
         BNO   TTVARIBL                                   NO
         MVI   0(R7),C'F'
         LA    R7,1(R7)
TTVARIBL TM    RECFM,X'40'                                VARIBLE
         BNO   TTTRKOV                                    NO
         MVI   0(R7),C'V'
         LA    R7,1(R7)
TTTRKOV  TM    RECFM,X'20'                                TRKOVERFLW?
         BNO   TTBLKED                                    NO
         MVI   0(R7),C'T'
         LA    R7,1(R7)
TTBLKED  TM    RECFM,X'10'                                BLOCKED?
         BNO   TTSTDBL                                    NO
         MVI   0(R7),C'B'
         LA    R7,1(R7)
TTSTDBL  TM    RECFM,X'08'                                STANDARD?
         BNO   TTASAPC                                    NO
         MVI   0(R7),C'S'
         LA    R7,1(R7)
TTASAPC  TM    RECFM,X'04'                                ASCI CAR CNT?
         BNO   TTMACHCC                                   NO
         MVI   0(R7),C'A'
         LA    R7,1(R7)
TTMACHCC TM    RECFM,X'02'                                MCHN CAR CNT?
         BNO   TLRECL                                     NO
         MVI   0(R7),C'M'
         LA    R7,1(R7)
TLRECL   CLC   LRECL(2),=XL2'0000'                    ANY LRECL?
         BE    TBLKSIZE                               NO
         SR    R7,R7                                      PICK
         LH    R7,LRECL                                    UP
         CVD   R7,DBLEWORD                                  LRECL
         MVC   MLRECL,EDMASK1                         MOVE IN PATTERN
         ED    MLRECL,DBLEWORD+5
TBLKSIZE CLC   BLKSIZE(2),=XL2'0000'                  ANY BLKSIZE?
         BE    TESTSALC                               NO
         SR    R7,R7                                      PICK
         LH    R7,BLKSIZE                                  UP
         CVD   R7,DBLEWORD                                  BLKSIZE
         MVC   MBLKSIZE,EDMASK1                       MOVE IN PATTERN
         ED    MBLKSIZE,DBLEWORD+5
         SPACE 1
TESTSALC TM    SECALLOC,DS1CYL     CYLINDER ALLOCATION
         BNO   TRYTRK              NO-> TRY TRACKS
         MVC   MSECTYPE,=C'CYL'    MOVE IN CYL
         B     DOSECQTY            GO DO THE EDIT
TRYTRK   TM    SECALLOC,DS1TRK     IS IT IN TRACKS?
         BNO   TRYBLK              NO-> TRY BLKS
         MVC   MSECTYPE,=C'TRK'    INDICATE TRACKS
         B     DOSECQTY              AND GO EDIT THE QUANTITY
TRYBLK   TM    SECALLOC,DS1BLK     IS IT IN BLKS
         BNO   SECHUH              NO..SKIP THIS JAZZ
         MVC   MSECTYPE,=C'BLK'    ITS BLOCKS
DOSECQTY XC    DBLEWORD(4),DBLEWORD CLEAR OUT AREA
         MVC   DBLEWORD+1(3),SECALLOC+1 MOVE IN QUANTITY
         L     R7,DBLEWORD         GET QTY IN REG AND
         CVD   R7,DBLEWORD           CONVERT TO DECIMAL
         MVC   MSECALLC,EDMASK3    MOVE IN EDIT MASK
         ED    MSECALLC,DBLEWORD+5   AND EDIT THE GOODIES
         B     TESTPROT
SECHUH   MVC   MSECTYPE,=C'???'    SAY WE DONT KNOW THE TYPE..
TESTPROT TM    DS1IND,B'00010100'  TEST PROTECT BITS
         BZ    TESTRACF            NONE-> GO DO USAGE
         MVC   MPROTECT,=C'WPWD'   SAY WRITE PROTECTED
         BO    TESTRACF            THAT WAS IT
         MVC   MPROTECT,=C'R/W '   READ/WRITE PROTECTED
TESTRACF TM    DS1IND,B'01000000'  IS IT RACF PROTECTED?
         BZ    TESTUSAG
         MVC   MPROTECT,=C'RACF'   YEP...SAY ITS RACF........
         DROP  R6
*
TESTUSAG DS    0H
         TM    FLAG,DOUSAGE            WAS USAGE REQUESTED?
         BNO   $$EOJ                    NO -> THATS ALL FOLKS...
         OI    FLAG,PRNTATTR           SAY PRINT SOMETHING..
         LR    R6,R3                   LOAD MESSAGE ADDRESS
         TM    FLAG,DOATTR             WAS ATTR  REQUESTED TOO?
         BO    STARTUS                 ->NO ATTR  NOT ASKED FOR
         LA    R6,MOFFSET(R6)          ADJUST ADDRESS
         USING USAGEMSG,R6
STARTUS  DS    0H
*
* THIS SECTION DOES USAGE INFO FROM ASM2 SYSTEM
*
         CLC   USEYEAR,=FL1'99'        YEAR < 99 ?
         BH    CHKOSDT                 NO -> USE OS CREATE DATE
         LA    R7,MUDATE
         LA    R8,USEYEAR
         BAL   R9,DATECONV             GO CONVERT THE DATE
*
         CLC   MODYEAR,=FL1'99'        MOD YEAR < 99 ?
         BNH   DOLSTMOD                YES-> DO NORMAL MOD DATE/TIME
         CLC   GENYEAR,=FL1'99'          IS THE OS CREATE DATE OK
         BH    CHKUSER                 NO-> JUST CHECK USER
         LA    R7,MMDATE
         LA    R8,GENYEAR
         BAL   R9,DATECONV             GO CONVERT DATE
         B     CHKUSER                 GO DO USERID..
DOLSTMOD LA    R7,MMDATE               PROCESS NORMAL MOD DATE/TIME
         LA    R8,MODYEAR
         BAL   R9,DATECONV             GO CONVERT THE DATE
*
         UNPK  DBLEWORD(5),MODTIME(3)  CONVERT THE TIME
         MVC   MMTIME(2),DBLEWORD      MOVE IN HH
         MVI   MMTIME+2,C':'           MOVE IN   :
         MVC   MMTIME+3(2),DBLEWORD+2  MOVE IN    MM
         B     CHKUSER                 GO TRY THE USER ID..
*
*
*
CHKOSDT  CLC   GENYEAR,=FL1'99'        OS CREATE YEAR < 99 ?
         BH    CHKUSER                 NO-> GO CHK USERID
         LA    R7,MUDATE
         LA    R8,GENYEAR
         BAL   R9,DATECONV             GO CONVERT THE DATE
*
CHKUSER  CLC   WHOMOD,=CL8'IBMOSVS2'   ANY USER GIVEN?
         BE    CHKUSECT                NO-> GO CHECK USE CNT
         MVC   MMUSER,WHOMOD           MOVE IN JOBNAME/USERID
CHKUSECT CLC   USECNT,=FL4'999'        > 999
         BNH   DOUSECNT                NO-> CONVERT AND DISPLAY
         MVC   MUCNT,=C'>999'           SAY TOO BIG
         B     $$EOJ                    AND EXIT
DOUSECNT MVC   FULLWORD,USECNT
         L     R7,FULLWORD
         CVD   R7,DBLEWORD
         MVC   MUCNT,EDMASK2
         ED    MUCNT,DBLEWORD+6
         B     $$EOJ                                  RETURN
*
* SUBROUTINE DATECONV
*
*  ON ENTRY:  R7  POINTS TO 8 CHAR AREA TO RECEIVE DATE
*             R8  POINTS TO 3 BYTE YDD FIELD
*      BAL R9,DATECONV
*
*  REGS USED R10,R11   OTHERS PRESERVED
*  USED DBLEWORD TOO....
DATECONV DS    0H
         STM   R1,R5,SAVEREGS      SAVE REGISTERS...
         SR    R10,R10             CLEAR WORK REG
         IC    R10,0(R8)           GET YEAR
         LTR   R10,R10             ANYONE HOME?
         BNP   RTN                  NO -> KISS OFF
         CVD   R10,DBLEWORD
         MVC   WRKC,EDMASK2
         ED    WRKC,DBLEWORD+6
         MVC   6(2,R7),WRKC+2       MOVE YEAR IN
* NOW DO MONTH/DAY
*
         MVI   2(R7),C'-'
         MVI   5(R7),C'-'           MOVE IN FORMAT CHARS
         MVC   FULLWORD(2),1(R8)    GET JULIAN DAYS
         SR    R10,R10               INTO
         LH    R10,FULLWORD            R10
         LA    R1,TABLE1            ASSUME NON LEAP
         TM    0(R8),X'03'          IS YEAR LEAP?
         BC    5,NOADJ
         LA    R1,TABLE2            USE LEAP YEAR TABLE
NOADJ    LA    R2,2                 INIT INDEX REG
LOOPDATE SH    R10,0(R2,R1)         SUB DAYS OF MONTH INDEXED
         BC    12,CHKD              RESULT <= 0..GO FIX
         LA    R2,2(R2)             BUMP TABLE PTR
         CH    R2,=H'26'            ERROR?
         BC    12,LOOPDATE          NO-> TRY AGAIN
         B     DATERR               YES-> DO ERROR THING
CHKD     AH    R10,0(R2,R1)         ADD BACK DAYS...
         CVD   R10,DBLEWORD          CONVERT DD TO PACKED DEC
         OI    DBLEWORD+7,X'0F'     INSERT SIGN..
         UNPK  3(2,R7),DBLEWORD+6(2) PUT IN DATE
         SRA   R2,1                 DIVIDE BY 2 TO GET MONTH
         CVD   R2,DBLEWORD          CONVERT MONTH TO PACKED DEC
         MVC   WRKC,EDMASK2         MOVE IN MASK
         ED    WRKC,DBLEWORD+6      AND EDIT TO SUPPRESS LEAD ZERO
         MVC   0(2,R7),WRKC+2       AND MOVE IN MONTH
*
RTN      LM    R1,R5,SAVEREGS       RESTORE REGISTERS
         BR    R9                    AND SPLIT
*
DATERR   MVC   0(6,R7),=C'**/**/'
         B     RTN
         EJECT
DBLEWORD DS    D
FULLWORD DS    F
SAVEREGS DS    5F
WRKC     DS    CL4
EDMASK1  DC    X'402020202020'                  MASK FOR EDIT OP.
EDMASK2  DC    X'40202020'                      MASK FOR EDIT OP.
EDMASK3  DC    X'402020202120'                  MASK FOR EDIT OP.
TABLE1   DS    H                 TABLE OF DAYS/MONTH
         DC    H'31'
         DC    H'28'              NON LEAP
         DC    H'31'
         DC    H'30'
         DC    H'31'
         DC    H'30'
         DC    H'31'
         DC    H'31'
         DC    H'30'
         DC    H'31'
         DC    H'30'
         DC    H'31'
TABLE2   DS    H                 TABLE OF DAYS/MONTH
         DC    H'31'
         DC    H'29'              LEAP YEAR
         DC    H'31'
         DC    H'30'
         DC    H'31'
         DC    H'30'
         DC    H'31'
         DC    H'31'
         DC    H'30'
         DC    H'31'
         DC    H'30'
         DC    H'31'
         EJECT
*  DSECT TO MAP FORMAT 1 DSCB WITH ASM2 MODS IN RESERVED FIELDS
DSCB1    DSECT
         DS    CL1                 FORMAT ID =C'1'
***      DS    CL6                 DATASET SERIAL NUMBER
USEYEAR  DS    CL1                 YEAR OF LAST USE
USEJDAY  DS    CL2                 JULIAN DAY OF LAST USE
MODYEAR  DS    CL1                 YEAR OF LAST MOD (AFTER ASM2 ONLY)
MODJDAY  DS    CL2                 JULIAN DAY OF LAST MOD (AFTER ASM2)
         DS    CL2                 VOL SEQ NUMBER
***      DS    CL3                 CREATE DATE FOR DATASET
GENYEAR  DS    CL1                   CREATE YEAR FOR DATASET
GENJDAY  DS    CL2                   CREATE JUL DAY FOR DATASET
***      DS    CL3                 EXPIRE DATE FOR DATASET
EXPYEAR  DS    CL1                   EXPIRE YEAR
EXPJDAY  DS    CL2                   EXPIRE JULIAN DATE
         DS    CL1                 NUMBER OF EXTENTS ON VOLUME
         DS    CL1                 NUMB OF BYTES USED IN LAST DIR BLK
         DS    CL1                 RESERVED
***      DS    CL13                SYSTEM CODE
WHOMOD   DS    CL8                 USERID/JOBNAME LAST MODIFIED
         DS    CL5                    JUNK...
***      DS    CL7                 MORE RESERVED
         DS    CL1
USECNT   DS    CL4                 # TIMES USED
MODTIME  DS    CL2                 HHMM OF LAST MOD (POST ASM2..)
         DS    CL2                 HERE BE THE DSORG ME HEARTIES...
RECFM    DS    CL1                  DITTO THE RECORD FORMAT
         DS    CL1                 OPTION CODE
BLKSIZE  DS    CL2                 BLOCK SIZE
LRECL    DS    CL2                 LOGICAL RECORD LENGTH
KEYLEN   DS    CL1                 KEYLENGTH
RELKP    DS    CL2                 RELATIVE KEY POSITION
DS1IND   DS    CL1                 DATASET INDICATORS
SECALLOC DS    CL4                 SECONDARY ALLOCATION
DS1CYL   EQU   X'C0'               CYL ALLOCATION
DS1TRK   EQU   X'80'               TRK ALLOCATION
DS1BLK   EQU   X'40'               BLK ALLOCATION
DS1CNTIG EQU   X'08'               CONTIGUOUS ALLOCATION
DS1MAXA  EQU   X'04'               MAX AVAILABLE ALLOCATION
DS15EXT  EQU   X'02'               FIVE EXTENTS MAX
DS1ROUND EQU   X'01'               ROUND
*
* MORE JUNK AFTER THIS, BUT WE DONT NEED IT ...
*
MSGDEF   DSECT                     , MAPPING FOR MESSAGE
MSG      DS    CL80
MOFFSET  EQU   28
MOFFBOTH EQU   40
*
ATTRMSG  DSECT                     , MAPPING FOR ATTRIB MESSAGE
MVOLSER  DS    CL6                 PLACE FOR VOLSER
         DS    CL1
MRECFMEQ EQU   *-ATTRMSG
MRECFM   DS    CL1                 POKE RECFM HERE (A BYTE AT A TIME)
         DS    CL3
MLRECL   DS    CL6                 EDIT LRECL IN HERE
MBLKSIZE DS    CL6                 EDIT BLKSIZE IN HERE
         DS    CL1
MSECTYPE DS    CL3                 SEC ALLOC TYPE 'TRK' 'CYL' OR 'BLK'
MSECALLC DS    CL6                 EDIT SECONDARY ALLOCATION HERE
         DS    CL1
MPROTECT DS    CL4                 DATASET PROTECTION
*
*                XX/XX/XX XX/XX/XX XX:XX XXXXXXXX XXXX
USAGEMSG DSECT                     , MAPPING FOR USAGE MESSAGE
MUDATE   DS    CL8
         DS    CL1
MMDATE   DS    CL8
         DS    CL1
MMTIME   DS    CL5
         DS    CL1
MMUSER   DS    CL8
         DS    CL1
MUCNT    DS    CL4
*
         EJECT
FLAGDEF  DSECT                     , MAPPING FOR FLAG BYTE
FLAG     DS    XL1
AUTHUSER EQU   B'10000000'         AUTHORIZED USER 1=YES 0=NO
DOATTR   EQU   B'00001000'         ATTRIB SPECED   1=YES 0=NO
DOUSAGE  EQU   B'00000100'         USAGE  SPECED   1=YES 0=NO
DOBOTH   EQU   B'00001100'
PRNTATTR EQU   B'00000010'         ATTR/USAGE TO PRNT 1=YES 0=NO
RSETPRNT EQU   B'11111101'
*
VOLDEF   DSECT                     , MAPPING FOR VOLSER OF DS
VOLSER   DS    CL6
*
         END
