*          DATA SET MOP        AT LEVEL 001 AS OF 07/31/79
MOP      TITLE 'MOP - TSO MASTER OPERATOR COMMAND'
MOP      CSECT
         REGEQU
         SAVE  (14,12),,MOP_V1M1_&SYSDATE_&SYSTIME
         LR    R12,R15
         USING MOP,R12
         LA    R11,MAINSAVE
         ST    R11,8(,R13)
         ST    R13,4(,R11)
         LR    R13,R11
         USING MAINSAVE,R13
         LM    R1,R4,0(R1)              PICK UP PARMS
         MVC   AUTHLVL,16(R3)           PICK UP PSCBATR1
         TM    AUTHLVL,X'80'            OPER PRIVLEDGE?
         BZ    NOAUTH                   NOPE...EXIT
         MVC   LOGUSER,0(R3)            YEP-SET UP TO LOG COMMAND
         LR    R3,R1
         SPACE
         LH    R1,0(,R3)                GET BUFFER LENGTH
         LH    R2,2(,R3)                GET OFFSET
         LA    R2,4(R2)                 ADD FOUR FOR LENGTH & OFFSET
         SLR   R1,R2                    COMPUTE OPERAND LENGTH
         CH    R1,=H'80'                IS CMD TOO LONG?
         BH    ERROR
         ALR   R3,R2                    ADD OFFSET TO BUFFER PTR
         BCTR  R1,0                     SUBTRACT FOR MVC
         EX    R1,MOVE                  MOVE INTO WTOLIST
         EX    R1,MOVELOG               MOVE INTO WTOLIST FOR LOG
         EX    R1,UPCASE                CONVERT TO UPPER CASE LOG
         SPACE
         L     R1,16                    FIND CVT
         L     R1,0(,R1)                DISP DOUB WD
         L     R1,4(,R1)                FIND TCB
         SR    R2,R2                    FOR SVC 34 R0
         CLI   WTOLIST+4,C'$'           MASTER CONSOLE ROUTING?
         BE    SLDOSVC                  GO DO IT
         L     R1,X'B4'(,R1)            FIND JSCB
         LH    R2,X'10C'(,R1)           LOAD ASID
         N     R2,=X'00007FFF'
         O     R2,=X'00008000'
         SPACE
SLDOSVC  DS    0H
         PRINT OFF
         SLR   R1,R1
         L     R0,=X'ABCFEDAD'
         SVC   254
         ST    R1,KEYSAVE
         PRINT ON
*        MODESET KEY=ZERO
         WTO   MF=(E,WTOLOG)
*
         LA    R1,WTOLIST
         LR    R0,R2
         SVC   34
*        MODESET KEY=NZERO
         PRINT OFF
         L     R1,KEYSAVE
         L     R0,=X'ABCFEDAD'
         SVC   254
         PRINT ON
         EJECT
EXIT     L     R13,4(,R13)
         LM    R14,R12,12(R13)
         SLR   R15,R15
         BR    R14
*
ERROR    TPUT  EMSG,L'EMSG
         B     EXIT
NOAUTH   TPUT  NOMSG,L'NOMSG
         B     EXIT
         SPACE 2
MOVE     MVC   WTOLIST+4(0),0(R3)       ** EXECUTED **
MOVELOG  MVC   LOGCMD(0),0(R3)          ** EXECUTED **
UPCASE   OC    LOGCMD(0),LOTSBLNK       ** EXECUTED **
         SPACE 2
MAINSAVE DC    18F'0'
KEYSAVE  DC    F'0'
EMSG     DC    C'OPER CMD > 80 CHARS..CMD NOT ISSUED'
NOMSG    DC    C'SORRY..MOP SERVICES ONLY AVAIL TO OPER PRIV. USERS'
WTOLIST  WTO   '                                                       *
                                        ',MF=L
WTOLOG   WTO   'XXXXXXX CMD:                                           *
                                                     ',ROUTCDE=(1),MF=L
         ORG   WTOLOG+4
LOGUSER  DS    CL7
         ORG   WTOLOG+17
LOGCMD   DS    CL80
         ORG
AUTHLVL  DC    X'00'                    USER CMD LEVEL
LOTSBLNK DC    CL90' '
         SPACE 3
         LTORG
         END
