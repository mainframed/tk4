*          DATA SET Q3SYSOUT   AT LEVEL 001 AS OF 04/01/83
SYSOUT   QSTART 'QUEUE COMMAND - MANIPULATE SYSOUT'
***********************************************************************
* RNB CHANGES:                                                        *
*      (1) RNB00 - MOVED THINGS AROUND FOR FOX ASSEMBLER              *
*      (2) RNB08 - ALLOW CANCEL/REQ/DEL IF JOBNAME STARTS WITH USERID *
*                  OR NOTIFY IS FOR USERID, UNLESS JOB SUBMITTED FROM *
*                  PJS. ALLOW TEC USERS TO MANIPULATE OTHER TEC USER'S*
*                  JOBS, AND ALSO STC'S.  KEYED TO QRNB=1             *
*      (3) RNB09 - FOR A REQ COMMAND, IF NO NEWCLASS GIVEN USE CLASS C*
*                  KEYED TO QRNB=1.                                   *
*                                                                 INTEL
* INTEL CHANGES:  KEN TRUE                                        INTEL
* 1)ADD ACF2 AUTH CHKING FOR USER AUTH TO PROCESS SYSOUT,         INTEL
*    IF &QACF2=1                                                  INTEL
*    OPER DOES IT TO ALL                                          INTEL
*    USERID=JOBNAME (FOR LENGTH OF USERID) ACCESS IS ALLOWED      INTEL
*    USER CHKS OWN GOODIES (LOGONID = LOGONID IN JCT)             INTEL
*    OTHER: ISSUE ACFVLD CHK FOR AUTH .. ACCESS TYPE=ALLOC        INTEL
*    SUPPORT FOR ACF 3.1.3 ENABLED BY &QSP FLAG                   INTEL
* 2)BYPASS TESTAUTH IF &QMAGIC=1                                  INTEL
***********************************************************************
         GBLB  &QRNB                                              RNB08
         GBLB  &QACF2,&QMAGIC,&QSP                                INTEL
         GBLC  &ACF2JCT       ACF2 3.1.3                          INTEL
         AIF   ((NOT &QACF2) OR (NOT &QSP)).$$SKP0                INTEL
         PUSH  PRINT                                              INTEL
         PRINT NOGEN                                              INTEL
         #ACFJES2   ,         ESTABLISH OFFSETS TO USE ACF2 3.1.3 INTEL
         POP   PRINT                                              INTEL
.$$SKP0  ANOP   ,                                                 INTEL
         USING QCKPT,R10
         L     R10,QVCKPT
         USING QDISPLAY,R9
         L     R9,QVDSPL
         USING WORK,R13
         AIF   (&QMAGIC).ISAUTH                                   INTEL
***********************************************************************
*                                                                     *
*   TEST AUTHORIZATION OF QUEUE                                       *
*                                                                     *
***********************************************************************
         TESTAUTH FCTN=1          TEST AUTHORIZATION OF USER
         LTR   R15,R15            ?/AUTHORIZED
         BZ    FJOB               YES. KEEP ON TRUCKING
         QTILT ' *** FUNCTION IS NOT AUTHORIZED ***'
.ISAUTH  ANOP  ,                                                  INTEL
***********************************************************************
*                                                                     *
*   CALL FINDJOB TO LOCATE THE JQE, JCT AND IOT                       *
*                                                                     *
***********************************************************************
FJOB     L     R15,=V(FINDJOB)    ADDR OF MODULE TO FIND JOB
         BALR  R14,R15            GO TO IT
***********************************************************************
*                                                                     *
*   VALIDATE THE JOBNAME                                              *
*                                                                     *
***********************************************************************
         LA    R2,QLOGON          START ADDR OF QLOGON
         LR    R3,R2              SAVE START ADDR
         LA    R4,7               MAX LENGTH OF LOGON ID
VALID000 CLI   0(R2),C' '         ?/END OF LOGON ID
         BE    VALID010           YES. CONTINUE PROCESSING
         LA    R2,1(R2)           NO.  POINT TO NEXT FIELD
         BCT   R4,VALID000        GO CHECK AGAIN
VALID010 SR    R2,R3              LENGTH OF LOGON ID
         BCTR  R2,R0              DECREMENT FOR EXECUTE INSTRUCTION
         USING JCTSTART,R4        BASE REG FOR JCT
         L     R4,QCJCTA          A(JCT)
         AIF    (&QACF2).NACF2A                                   INTEL
         EX    R2,VERJOBN         ?/JOB BELONGS TO USER
         AGO   .NACF2                                             INTEL
.NACF2A  ANOP  ,                                                  INTEL
         AIF   ((NOT &QACF2) OR (NOT &QSP)).$$SKP1  ACF2 3.1.3    INTEL
         ICM   R6,15,&ACF2JCT      TEST, GET ACF2 AREA OFFSET     INTEL
         LA    R15,JCTSTART-JCTDSECT  FIND LENGTH OF BUFFER AREA  INTEL
         SR    R6,R15              DECREMENT ACF2 POINTER         INTEL
         LA    R6,0(R4,R6)         CALCULATE ADDRESS              INTEL
         USING #ACFJCTX,R6         ADDRESSING                     INTEL
.$$SKP1  ANOP  ,                                                  INTEL
***************************************************************** INTEL
* CHECK FOR ACF2 AUTHORITY TO JOB                                 INTEL
***************************************************************** INTEL
         L     R14,DAPLPSCB   GET PSCB ADDRESS                    INTEL
         USING PSCB,R14       AND ADDRESS IT                      INTEL
         SR    R15,R15        CLEAR OUT REGISTER                  INTEL
         IC    R15,PSCBUSRL   GET LENGTH OF PSCB USER             INTEL
         BCTR  R15,0          AND ADJUST FOR MACHINE LENGTH       INTEL
         LA    R14,PSCBUSER   GET ADDRESS OF USER ID              INTEL
         DROP  R14                                                INTEL
         EX    R15,CLCPSCBU   COMPARE JOBNAME:PSCB USERID         INTEL
         BE    ACF2OK         IF JOBNAMES MATCH..ALLOW IT         INTEL
         CLC   QLOGON,LIDLID  SEE IF USERS LOGONID=JOBS ACF LOGON INTEL
         BE    ACF2OK         YES..CONTINUE FORTHWITH             INTEL
         TM    QFLAG1,QFLG1OPR IS USER OPER PRIVLEDGE ?           INTEL
         BO    ACF2OK         YES..CONTINUE FORTHWITH             INTEL
*                                                                 INTEL
         USING ACCVT,R8                                           INTEL
         ACFGACVT R8,NONE=NOTOK   GET THE ACF2 CVT                INTEL
*                                                                 INTEL
         MVC   DSNAME,=CL44'SYSOUT. '  INITIALIZE DSNAME TO USE   INTEL
         XC    ACFSPARM(ACFSPRML),ACFSPARM   CLEAR REQUEST BLOCK  INTEL
         MVI   ACFSPREQ,ACFSPRDS  DSNAME ACCESS ONLY              INTEL
         MVI   ACFSPID1,ACFSPIUR  DIS AM DE USER TALKING....      INTEL
         MVI   ACFSPACC,ACFSPAOT  SET TO RENAME/ALLOC/SCR REQUEST INTEL
*                                                                 INTEL
         LA    R7,DSNAME          GET DSNAME ADDRESS              INTEL
         ST    R7,ACFSPDSN        AND GIVE IT TO ACF PARM LIST    INTEL
*                                                                 INTEL
*  GENERATE DSNAME OF FORMAT 'SYSOUT.LOGONID.JOBNAME' FOR CHKING  INTEL
*                                                                 INTEL
         MVC   DSNAME+7(8),LIDLID  MOVE LID TO DSNAME             INTEL
         CLI   DSNAME+7,C' '       IS THE LID BLANK?              INTEL
         BNE   ADSNCHK0            NO..NORMAL PROCESS             INTEL
*                                                                 INTEL
*  GOT HERE BECAUSE LID IS ' ' (BLANK)..SUBSTITUTE 'SYSTEM'       INTEL
         MVC   DSNAME+7(8),=CL8'SYSTEM'                           INTEL
*                                                                 INTEL
ADSNCHK0 LA    R1,DSNAME+7         GET ADDRESS                    INTEL
         LA    R7,8                LOAD COUNT                     INTEL
ADSNCHK  CLI   0(R1),C' '          LOOK FOR BLANK                 INTEL
         BE    ADSNCHK1            GOTIT..                        INTEL
         LA    R1,1(R1)            BUMP AND                       INTEL
         BCT   R7,ADSNCHK            GRIND                        INTEL
ADSNCHK1 MVI   0(R1),C'.'          MOVE IN PERIOD..               INTEL
         MVC   1(8,R1),JCTJNAME    MOVE IN JOBNAME                INTEL
*                                                                 INTEL
         LA    R1,ACFSPARM        GET ADDRESS OF PARM FIELD       INTEL
         ACFSVC (1),TYPE=S,NONE=NOTOK,CVT=HAVE   INVOKE A C F 2   INTEL
*                                                                 INTEL
         LTR   R15,R15            HOW DID YOU LIKE THEM APPLES?   INTEL
         BC    8,ACF2OK           ..OK BY YOU...CONTINUE..        INTEL
*                                                                 INTEL
NOTOK    QTILT '*** SORRY..NO ACF2 AUTHORITY TO PROCESS THIS JOB' INTEL
***************************************************************** INTEL
CLCPSCBU CLC   JCTJNAME(0),0(R14)  COMPARE PSCBUSER:JOBNAME       INTEL
ACF2OK   B     OK                                                 INTEL
.NACF2   ANOP                                                     INTEL
         AIF   (NOT &QRNB).RNB08A                                 RNB08
         BE    OK                 YES - LET IT GO                 RNB08
         CLC   =C'PJS',QLOGON IS THIS A PJS USER?                 RNB08
         BE    WRONGJOB       YES - INVALID                       RNB08
         BAL   R6,NOPJSJOB    ENSURE NOT A PJS JOB FOR CAN/DEL    RNB08
         CLC   QLOGON(*-*),JCTTSUID  ** EXECUTED **               RNB08
         EX    R2,*-6         IS THE USERID SAME AS NOTIFY ID?    RNB08
         BE    OK             YES - OK                            RNB08
         CLC   =C'TEC',QLOGON   IS THIS A TEC USER?               RNB08
         BNE   WRONGJOB         NO  - INVALID                     RNB08
         TM    JCTJOBFL,JCTSTCJB   IS IT AN STC?                  RNB08
         BO    OK                  /YES - OK FOR TEC USER         RNB08
         CLC   =C'TEC',JCTJNAME IS JOBNAME FOR A TEC USER?        RNB08
         BE    OK               YES - OK                          RNB08
         CLC   =C'TEC',JCTTSUID IS NOTIFY FOR A TEC USER?         RNB08
.RNB08A  ANOP                                                     RNB08
         BNE   WRONGJOB           NO. GO TELL HIM
OK       EQU   *                                                  RNB08
***********************************************************************
*                                                                     *
*   VALIDATE THE FUNCTION CODE                                        *
*                                                                     *
***********************************************************************
         LH    R1,QCODEH          GET FUNCTION CODE
         CH    R1,=H'8'           ?/VALID FUNCTION CODE
         BH    STOP               NO. EXIT
***********************************************************************
*                                                                     *
*   BRANCH TO THE APPROPIATE PROCESSOR                                *
*                                                                     *
***********************************************************************
         LA    R7,SSOBHDR         ADDR FOR SUBSYSTEM OPTION BLOCK
         USING SSOB,R7
         B     *+4(R1)
         B     DELETE             0   DELETE REQUEST
         B     REQUEUE            4   REQUEUE REQUEST
         B     CANCEL             8   CANCEL REQUEST
***********************************************************************
*                                                                     *
*   PROCESS CANCEL REQUEST                                            *
*                                                                     *
***********************************************************************
CANCEL   LA    R5,SSCSBGN         A(CANCEL PARAMETER LIST)
         BAL   R6,INITSSOB        GO INITIALIZE THE SSOB.
         MVC   SSOBFUNC,=AL2(SSOBCANC)  SET THE FUNCTION
         XC    SSCSBGN(SSCSIZE),SSCSBGN CLEAR PARM LIST
         MVC   SSCSLEN,=AL2(SSCSIZE) SET LENGTH OF CANCEL PARM LIST
         CLI   QPARM2,C'P'        ?/PURGE THE OUTPUT
         BNE   CAN000             NO. DON'T SET THE FLAG
         OI    SSCSFLGS,SSCSCOUT  YES
CAN000   MVC   SSCSJOBN,JCTJNAME  JOBNAME
         MVC   SSCSJOBI,JCTJOBID  JES2 JOB ID
         MVC   SSCSDIMP,=H'16'
         LA    R5,SSCSJOBI        A(JES2 JOB ID)
         BAL   R6,FIXJOBID        ENSURE JOBID HAS NO IMBEDDED BLANKS
         BAL   R6,VERREQ          VERIFY THE REQUEST
         BAL   R6,CALLSSI         GO CALL SUBSYSTEM INTERFACE RTN
         L     R1,SSOBRETN        GET RC FOR CANCEL FUNCTION
         B     *+4(R1)
         B     FUNCTOK            0 -  CANCEL COMPLETED
         B     TILTNOJB           4 -  JOBNAME NOT FOUND
         B     TILTBADI           8 -  INVALID JOBNAME/JOB ID COMB.
         B     TILTNCAN           12 - JOB NOT CANCELLED - DUP JOBN
         B     TILTMALL           16 - STATUS ARRAY TOO SMALL
         B     TILTOUTP           20 - JOB NOT CANCELLED - ON OUTPUT Q
         B     TILTYNTX           24 - JOB ID WITH INVALID SYNTAX
         B     TILTICAN           28 - INVALID CANCEL REQUEST
***********************************************************************
*                                                                     *
*   PROCESS REQUEUE REQUEST                                           *
*                                                                     *
***********************************************************************
REQUEUE  LA    R5,SSSOBGN         A(REQUEUE SYSOUT PARM LIST)
         BAL   R6,INITSSOB        GO INITIALIZE THE SSOB
         AIF   (NOT &QRNB).RNB09B                                 RNB09
         CLI   QPARM2,C' '        WAS NEW CLASS GIVEN?            RNB09
         BNE   RNB09A             /YES - USE IT                   RNB09
         MVI   QPARM2,C'C'        /NO  - DEFAULT TO CLASS C       RNB09
RNB09A   EQU   *                                                  RNB09
.RNB09B  ANOP                                                     RNB09
         MVC   SSOBFUNC,=AL2(SSOBSOUT) INDICATE SYSOUT IS THE FUNCTION
         XC    SSSOBGN(SSSOSIZE),SSSOBGN CLEAR PARM LIST
         MVC   SSSOLEN,=AL2(SSSOSIZE) SET LENGTH OF SYSOUT PARM LIST
         OI    SSSOUFLG,SSSOSETC  USE SSSOCLAS AS DISP
         OI    SSSOUFLG,SSSORLSE  RELEASE ALL SELECTED DATA SETS
         OI    SSSOFLG1,SSSOHLD   SELECTION INCLUDES HELD SYSOUT DS
         OI    SSSOFLG1,SSSOSJBN  JOB NAME IS PRESENT
         OI    SSSOFLG1,SSSOSJBI  JOB ID IS PRESENT
         OI    SSSOFLG2,SSSOCTRL  PROCESSING COMPLETED
         MVC   SSSOJOBN,JCTJNAME  JOBNAME
         MVC   SSSOJOBI,JCTJOBID  JOB ID
         MVC   SSSOCLAS,QPARM2    NEWCLASS
         LA    R5,SSSOJOBI        A(JES JOB ID)
         BAL   R6,FIXJOBID        ENSURE JOBID HAS NO EMBEDDED BLANKS
         BAL   R6,VERREQ          VERIFY THE REQUEST
         BAL   R6,CALLSSI         GO CALL SUBSYSTEM INTERFACE RTN
CHKSORC  L     R1,SSOBRETN        GET RETURN CODE FOR SYSOUT FUNCTION
         B     *+4(R1)
         B     FUNCTOK            0 -  SYSOUT COMPLETED
         B     TILTEODS           4 -  NO MORE DS TO SELECT
         B     TILTNJOB           8 -  JOB NOT FOUND
         B     TILTINVA           12 - INVALID SEARCH ARGUMENTS
         B     TILTUNAV           16 - UNABLE TO PROCESS NOW
         B     TILTDUPJ           20 - DUPLICATE JOB NAMES
         B     TILTINVJ           24 - INVALID JOBN/JOBID COMBO
         B     TILTIDST           28 - INVALID DEST SPECIFIED
***********************************************************************
*                                                                     *
*   PROCESS DELETE  REQUEST                                           *
*                                                                     *
***********************************************************************
DELETE   LA    R5,SSSOBGN         A(DELETE SYSOUT PARM LIST)
         BAL   R6,INITSSOB        GO INITIALIZE THE SSOB
         MVC   SSOBFUNC,=AL2(SSOBSOUT) INDICATE SYSOUT IS THE FUNCTION
         XC    SSSOBGN(SSSOSIZE),SSSOBGN CLEAR PARM LIST
         MVC   SSSOLEN,=AL2(SSSOSIZE) SET LENGTH OF SYSOUT PARM LIST
         OI    SSSOUFLG,SSSODELC  INDICATE DELETE REQUEST
         OI    SSSOFLG1,SSSOHLD   SELECTION INCLUDES HELD DS
         OI    SSSOFLG1,SSSOSJBN  JOB NAME PRESENT
         OI    SSSOFLG1,SSSOSJBI  JES2 JOB ID PRESENT
         OI    SSSOFLG2,SSSOCTRL  PROCESSING COMPLETED
         MVC   SSSOJOBN,JCTJNAME  JOBNAME
         MVC   SSSOJOBI,JCTJOBID  JES2 JOBID
         LA    R5,SSSOJOBI        A(JES2 JOBID)
         BAL   R6,FIXJOBID        ENSURE JOBID HAS NO EMBEDDED BLANKS
         BAL   R6,VERREQ          VERIFY THE REQUEST
         BAL   R6,CALLSSI         GO CALL SUBSYSTEM INTERFACE RTN.
         B     CHKSORC            GO CHECK RC
***********************************************************************
*                                                                     *
*   INITIALIZE THE SUBSYSTEM OPTION BLOCK (SSOB)                      *
*                                                                     *
*        R5 - ADDRESS OF FUNCTION PARM LIST                           *
*        R6 - RETURN ADDRESS                                          *
*        R7 - A(SSOB)                                                 *
*                                                                     *
***********************************************************************
INITSSOB XC    SSOBEGIN(SSOBHSIZ),SSOBEGIN CLEAR THE SSOB
         MVC   SSOBID,=C'SSOB'
         MVC   SSOBLEN,=AL2(SSOBHSIZ) LENGTH OF SSOB HEADER
         ST    R5,SSOBINDV        FUNCTION DEPENDENT AREA POINTER
         ST    R7,SSOBPTR         SAVE ADDR OF SSOB
         OI    SSOBPTR,X'80'      REQUIRED FOR IEFSSREQ INTERFACE
         BR    R6
***********************************************************************
*                                                                     *
*   REMOVE EMBEDDED BLANKS IN JES2 JOB ID                             *
*                                                                     *
***********************************************************************
FIXJOBID LA    R8,5               MAX LENGTH OF SCAN
         LA    R5,3(R5)           START LOCATION FOR SCAN
FIX000   CLI   0(R5),C' '         ?/EMBEDDED BLANK
         BNE   FIX010             NO. CONTINUE WITH THE SCAN
         MVI   0(R5),C'0'         YES. REPLACE WITH 0
FIX010   LA    R5,1(R5)           POINT TO NEXT BYTE
         BCT   R8,FIX000          GO DO IT AGAIN
         BR    R6                 ALL OVER. RETURN TO CALLER
***********************************************************************
*                                                                     *
*   TELL THE USER WHAT HE IS ABOUT TO DO                              *
*                                                                     *
***********************************************************************
VERREQ   MVC   VCLEAR,WARNING     MOVE IN THE WARNING MSG
         MVC   VJOBN(8),JCTJNAME
         MVC   VJOBID(8),JCTJOBID
         LR    R2,R6              SAVE RETURN ADDR
         LA    R5,VJOBID          A(JES JOB ID)
         BAL   R6,FIXJOBID        INSURE NO IMBEDDED BLANKS
         LR    R6,R2              RESTORE RETURN ADDR
         CLI   QSUBNAME,C'D'      ?/DELETE COMMAND
         BNE   VER000
         MVC   VCMD(6),=C'DELETE'
         B     VER020
VER000   CLI   QSUBNAME,C'C'      ?/CANCEL COMMAND
         BNE   VER010
         MVC   VCMD(6),=C'CANCEL'
         CLI   QPARM2,C'P'        PURGE SPECIFIED
         BNE   VER020
         MVC   VACTION(5),QPARM2
         B     VER020
VER010   MVC   VCMD(7),=C'REQUEUE'
         MVC   VACTION,RQACTION
         MVC   VCLASS(1),QPARM2
VER020   MVC   QDMLNG,=H'0'       TELL DISPLAY TO PRINT IT NOW
         L     R15,=V(DISPLAY)    A(MODULE TO DISPLAY THE MESSAGE)
         BALR  R14,R15            GO DISPLAY THE WARNING
         BR    R6                 RETURN TO THE CALLER
***********************************************************************
*                                                                     *
*   INTERFACE TO THE SUBSYSTEM                                        *
*                                                                     *
***********************************************************************
CALLSSI  L     R2,16              A(CVT)
         L     R2,296(R2)         A(JESCT)
         MODESET MODE=SUP         GET SUPER
         AIF   (NOT &QMAGIC).$SKP1                                INTEL
         ST    R1,KEY             SAVE KEY                        INTEL
.$SKP1   ANOP  ,                                                  INTEL
         LA    R1,SSOBPTR         ADDR OF PTR TO SSOB
         L     R15,20(R2)         A(JESSSREQ)
         BALR  R14,R15
         LR    R2,R15             SAVE RETURN CODE
         AIF   (NOT &QMAGIC).$SKP2                                INTEL
         L     R1,KEY             SAVE KEY                        INTEL
.$SKP2   ANOP  ,                                                  INTEL
         MODESET MODE=PROB        BACK TO NORMAL
         B     *+4(R2)
         BR    R6                 0 -  SUCCESSFUL INSTRUCTION
         BR    R6                      DUMMY INSTRUCTION
         B     TILTNSUP           4 -  SS DOESN'T SUPPORT THIS FUNCTION
         B     TILTNTUP           8 -  SS EXIST, BUT IS NOT UP
         B     TILTNOSS           12 - SS DOES NOT EXIST
         B     TILTDIST           16 - FUNCTION NOT SUPPORTED
         B     TILTLERR           20 - LOGICAL ERROR
         AIF   (NOT &QRNB).RNB08B                                 RNB08
******************************************************************RNB08
*                                                                *RNB08
*   TILT IF PJS JOB (ONLY FOR CANCEL OR DELETE)                  *RNB08
*                                                                *RNB08
******************************************************************RNB08
         USING PDBDSECT,R1                                        RNB08
         USING IOTDSECT,R3                                        RNB08
*        USING USERIDLEN,R2                                       RNB08
NOPJSJOB CLI   QCODEH+1,4     IS THIS A REQUEUE?                  RNB08
         BER   R6             /YES - OK FOR NOW                   RNB08
*                             /NO  - ENSURE NOT A PJS JOB         RNB08
         L     R3,QCIOTA      LOAD BASE REG                       RNB08
         LR    R5,R3          BASE OF IOT                         RNB08
         A     R5,IOTPDDBP    OFFSET BEYOND LAST PDDB             RNB08
         LR    R1,R3          BASE OF IOT                         RNB08
         A     R1,QCPDDB1     OFFSET TO FIRST PDDB IN IOT         RNB08
         MVC   QPDSID,=H'0'   NULLIFY VALIDITY FOR LISTDS         RNB08
FINDDS   CLC   =H'5',PDBDSKEY IS THIS THE DATASET?                RNB08
         BE    FOUNDDS        YES. CONTINUE.                      RNB08
         LA    R1,PDBLENG(R1) NO. LOOK AT NEXT PDDB.              RNB08
         CR    R1,R5          HAVE WE GONE PAST THE LAST PDDB?    RNB08
         BL    FINDDS         NO. TRY AGAIN.                      RNB08
         B     BADDDTAB       ELSE BAD DD TABLE (INTERP. JCL)     RNB08
FOUNDDS  L     R5,PDBMTTR     DISK ADDR OF FIRST BLOCK            RNB08
         DROP  R1                                                 RNB08
         L     R7,QCBLKA      ADDR OF DATASET BLOCK IOAREA        RNB08
         MVC   QDMSG,QBLANK   BLANK OUT THE MESSAGE AREA          RNB08
         ST    R5,QCTRAK      STORE DISK ADDR                     RNB08
         LR    R1,R7          IOAREA ADDRESS                      RNB08
         L     R15,=V(READSPC) ADDR OF ROUTINE TO READ HASPACE    RNB08
         BALR  R14,R15        GO TO IT                            RNB08
         LA    R5,10(R7)      ADDR OF FIRST RECORD IN BLOCK       RNB08
         SR    R7,R7          ZERO OUT REG                        RNB08
         IC    R7,0(R5)       INSERT LENGTH                       RNB08
         CLI   5(R5),1        IS THIS A JOB RECORD?               RNB08
         BNE   BADDDTAB       /NO  - INVALID DD TABLE             RNB08
         LA    R5,9(R5)       ADDR OF FIRST KEY                   RNB08
         LR    R8,R7          REMAINING LENGTH OF RECORD          RNB08
         SR    R15,15         ZERO OUT R15                        RNB08
         SR    R14,R14        ZERO OUT R14                        RNB08
         SR    R1,R1          ZERO OUT R1                         RNB08
TRYFLD   CLI   0(R5),X'A5'    IS THIS THE USER= PARM?             RNB08
         BE    GOTUSER        YES. PROCESS IT.                    RNB08
NEXTFLD  IC    R1,1(R5)       NUMBER OF SUBFIELDS                 RNB08
         LA    R5,2(R5)       UPDATE LOCATION                     RNB08
         SH    R8,=H'2'       REMAINING COUNT                     RNB08
         SR    R8,R1          REMAINING COUNT                     RNB08
         BNP   BADDDTAB       RECORD IS EXHAUSTED                 RNB08
         LTR   R1,R1          ARE THERE ANY SUBFIELDS?            RNB08
         BZ    TRYFLD         NO. TRY NEXT FIELD.                 RNB08
LOOPFLD  TM    0(R5),X'80'    IS THIS A SUB-SUB-FIELD             RNB08
         BZ    NOSUB          NO. CONTINUE.                       RNB08
         NI    0(R5),X'7F'    CLEAR THE HEX 80 BIT                RNB08
         IC    R14,0(R5)      NUMBER OF SUB-SUB-FIELDS            RNB08
         LA    R5,1(R5)       UPDATE LOCATION                     RNB08
         SH    R8,=H'1'       REMAINING COUNT                     RNB08
         SR    R8,R14         REMAINING COUNT                     RNB08
         BNP   BADDDTAB       RECORD IS EXHAUSTED                 RNB08
         AR    R1,R14         INCREASE NUMBER OF SUBFIELDS        RNB08
         B     YESSUB         DECREMENT AND TRY AGAIN             RNB08
NOSUB    IC    R15,0(R5)      SUBFIELD LENGTH                     RNB08
         LA    R5,1(R15,R5)   ADD TO LOCATION                     RNB08
         SR    R8,R15         REMAINING COUNT                     RNB08
         BNP   BADDDTAB       RECORD IS EXHAUSTED                 RNB08
YESSUB   BCT   R1,LOOPFLD     DO NEXT SUBFIELD                    RNB08
         B     TRYFLD         TRY NEXT FIELD                      RNB08
GOTUSER  CLI   2(R5),7        IS USER ID LENGTH = 7?              RNB08
         BNER  R6             /NO  - NOT A PJS JOB, OK TO PROCESS RNB08
         CLI   2(R5),0        IS THE LENGTH ZERO?                 RNB08
         BER   R6             YES. SKIP THE FIELD.                RNB08
         CLC   =C'PROD',6(R5) IS IT A PJS JOB? (USER = ???PROD)   RNB08
         BE    PJSMSG         /YES - BAD                          RNB08
         BR    R6             /NO  - GO PROCESS                   RNB08
BADDDTAB QTILT '*** CANNOT PROCESS JOB - DDTABLE MISSING/INVALID' RNB08
PJSMSG   QTILT '*** CANNOT CAN/DEL JOB SUBMITTED VIA PJS ***'     RNB08
.RNB08B  ANOP                                                     RNB08
***********************************************************************
*                                                                     *
*   RETURN TO CALLER                                                  *
*                                                                     *
***********************************************************************
STOP     QSTOP
FUNCTOK  QTILT ' *** COMMAND SUCCESSFULLY PROCESSED ***'
***********************************************************************
*                                                                     *
*   ERROR MESSAGES                                                    *
*                                                                     *
***********************************************************************
WRONGJOB QTILT ' *** JOBNAME DOES NOT BELONG TO YOU ***'
TILTNSUP QTILT ' *** QUEUE LOGIC ERROR -- RC =4  FROM SSI ***'
TILTNTUP QTILT ' *** JES2 IS NOT UP ***'
TILTNOSS QTILT ' *** QUEUE LOGIC ERROR -- RC =12 FROM SSI ***'
TILTDIST QTILT ' *** DISASTROUS ERROR DURING PROCESSING ***'
TILTLERR QTILT ' *** QUEUE LOGIC ERROR -- RC =20 FROM SSI ***'
TILTNOJB EQU   *
TILTNJOB QTILT ' *** JOBNAME NOT FOUND ***'
TILTBADI EQU   *
TILTINVJ QTILT ' *** INVALID JOBNAME/JOB ID COMBINATION ***'
TILTNCAN EQU   *
TILTDUPJ QTILT ' *** DUPLICATE JOBNAME AND NO JOBID GIVEN ***'
TILTEODS QTILT ' *** JOB HAS NO HELD DATA SETS ***'
TILTICAN QTILT ' *** CAN''T CANCEL YOUR TSO SESSION OR A STARTED TASK *X
               **'
TILTOUTP QTILT ' *** JOB NOT CANCELLED - JOB ON OUTPUT QUEUE ***'
TILTMALL EQU   *
TILTYNTX EQU   *
TILTINVA EQU   *
TILTUNAV EQU   *
TILTIDST QTILT ' *** QUEUE LOGIC ERROR ***'
***********************************************************************
*                                                                     *
*   MISCELLANEOUS NUTS, BOLTS, ETC.                                   *
*                                                                     *
***********************************************************************
         AIF   (NOT &QACF2).NACF3                                 INTEL
VERJOBN  CLC   QLOGON(*-*),LIDLID   COMPARE TO ACF2 LOGONID       INTEL
         AGO   .NACF3A                                            INTEL
.NACF3   ANOP  ,                                                  INTEL
VERJOBN  CLC   QLOGON(*-*),JCTJNAME
.NACF3A  ANOP  ,                                                  INTEL
WARNING  DS    0CL80
         DC    CL21' *** '
         DC    CL1'('
         DC    CL8' '
         DC    CL1')'
         DC    CL12' '
         DC    CL37'. HIT ENTER IF OK OR RESPECIFY. ***'
RQACTION DC    CL11'NEWCLASS( )'
         LTORG
         DROP  ,                   DROP ALL ADDRESSINGS           NERDC
SYMDEL   DSECT ,                   KILL SYM CARD GENERATION       UF023
SYSOUT   CSECT ,                                                  UF023
JCT      EQU   0
         AIF   ((NOT &QACF2) OR (NOT &QSP)).$$SKP9                INTEL
         $BUFFER   ,                                              INTEL
         #ACFJCTX  ,       ACF2 3.1.3                             INTEL
SYSOUT   CSECT ,                                                  INTEL
         AGO   .$$SKP9A                                           INTEL
.$$SKP9  ANOP  ,                                                  INTEL
BUFSTART EQU   0                                                  INTEL
BUFDSECT EQU   0                                                  INTEL
.$$SKP9A ANOP  ,                                                  INTEL
         $TAB
         $JCT
         $PDDB                                                    RNB00
         $IOT                                                     RNB00
         IEFJSSOB (SO,CS),CONTIG=YES
         QCOMMON
         ORG   QDHLINE
VCLEAR   DS    0CL80
         DS    CL5
VCMD     DS    CL8
VJOBN    DS    CL9
VJOBID   DS    CL10
VACTION  DS    CL11
         DS    CL37
VCLASS   EQU   VACTION+9
WORK     DSECT
FILLER   DS    CL512              BIG FILLER
SSOBPTR  DS    F
SSOBHDR  DS    CL140
         AIF   (NOT &QMAGIC).$SKP3                                INTEL
         DS    0D                                                 INTEL
KEY      DS    F                                                  INTEL
.$SKP3   ANOP  ,                                                  INTEL
         AIF    (NOT &QACF2).NACF4                                INTEL
         ACDSV DSECT=NO                                           INTEL
DSNAME   DS    CL44                                               INTEL
         SPACE  2                                                 INTEL
         ACCVT   ,                                                INTEL
         ACUCB   ,                                                INTEL
         PRINT NOGEN                                              INTEL
         IHAPSA  ,                                                INTEL
         IKJPSCB                                                  INTEL
         EJECT ,                                                  INTEL
.NACF4   ANOP   ,                                                 INTEL
SYMNODEL DSECT ,                   RESTORE SYM CARD GENERATION    UF023
         END
