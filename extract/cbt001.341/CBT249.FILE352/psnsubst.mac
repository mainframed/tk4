*          DATA SET PSNSUBST   AT LEVEL 002 AS OF 10/30/80
         TITLE 'PSNSUBST - TO REPLACE A SYMBOLIC WITH ITS VALUE'
PSNSUBST PSNMSTRT  SASIZ=WKLENG
         EJECT
*                    TO REPLACE A GIVEN SYMBOLIC WITH ITS
*                    VALUE SPECIFIED IN THE PROC STATEMENT
*                    CALLED WHEN THE PROCESSOR ENCOUNTERS AN
*                    &, PASSING BACK THE SUBSTITUTED VALUE AND
*                    LENGTHS OF THE OLD AND NEW ONES.
*
         USING SYMBOL,R8
         XC    PSWSYMDT(PSWLSD),PSWSYMDT   CLEAR THE COMM AREA
         STM   R0,R1,WKSAVPT      SAVE AS DOCUMENTATION
         LR    R7,R1              THE POINTER TO RECEIVING FIELD
         LTR   R6,R0              POSSIBLE LENGTH OF RECEIVING
         BP    *+8                IF POSITIVE IS A LENGTH
         LA    R6,128             FREEFORM - USE MAX LENGTH IN JCL
         LA    R6,0(R6,R7)        POINT TO END OF RECEIVING
*
         L     R5,PSWSDAT         POINTER TO SENDING FIELD
         LH    R4,PSWSDATL        AND ITS LENGTH
         LA    R4,0(R4,R5)        AND TO ITS END
         BCTR  R4,0               POINT TO LAST GOOD CHARACTER
*
NEXTSCAN EQU   *                  OUTER LOOP - SCAN FOR SUBSTITUTIONS
         LA    R3,0(,R5)          STARTING POINT OF SEARCH
RESCAN   EQU   *                  REENTRY FROM SPECIAL SITUATIONS
         LR    R1,R4              COMPUTE LENGTH OF THE SCAN
         SR    R1,R3              FROM THE STARTING POINT
         BNP   LASTSEG            IF DONE, FINISH UP
         L     R15,PSWTRSUB       PROPER TRT TABLE
         EX    R1,FINDSUBS        LOOK FOR SUBSTITUTE CANDIDATES
         BZ    LASTSEG            NO HITS - MOVE REST OVER
         CLI   1(R1),C'&&'        CHECK FOR NEGATED SUBSTITUTION
         BNE   CANSUBS            NOT NEGATED - CAN SUBSTITUTE
         LA    R3,2(,R1)          GO BACK AND TRY AGAIN
         B     RESCAN             .
CANSUBS  EQU   *                  PROPER START FOR A SYMBOLIC
         LR    R9,R1              POINT TO START OF SYMBOLIC
         LR    R1,R4              COMPUTE REMAINING LENGTH
         SR    R1,R9              .
         L     R15,PSWTRSYM       WAYS TO END THE SYM NAME
         EX    R1,FINDEND         AND SCAN FOR IT
         BNZ   STILLIN            IF HAVE A HIT
         LA    R1,1(,R4)          POINT PAST END OF NAME
         SR    R2,R2              SHOW END OF THE SHOW
         B     *+6
STILLIN  EQU   *
         LR    R2,R1              SAVE POINTER TO DELIMITER
         SR    R1,R9              LENGTH OF THAT SYMBOLIC
*                    LOOK UP THAT VALUE IN THE TABLE
         BAL   R14,FINDSYM
         LTR   R15,R15            DID IT SUBSTITUTE PROPERLY
         BZ    MOVEOVER           YES - MAKE THE SUBS
*                    NO MATCH - CARRY ON WITH SCAN
         LTR   R3,R2              NEW SCAN STARTING POINT
         BNZ   RESCAN             AND KEEP SCANNING
         B     LASTSEG            IF THE PRIOR SCAN FOUND END
*                    HAVE A HIT - MOVE DATA AND SUBSTITUTION
MOVEOVER EQU   *
*                    RECAP OF REGISTERS AT THIS POINT
*              R2 - BYTE PAST END OF SYMBOLIC - PAST R9
*              R4 - LAST BYTE IN SENDING FIELD
*              R5 - START OF SENDING DATA
*              R6 - LAST BYTE IN RECEIVING FIELD
*              R7 - START OF RECEIVING AREA
*              R8 - ENTRY IN SYMBOLIC TABLE
*              R9 - FIRST & IN THE SENDING DATA
*
*                    MOVE THE LEADING CONSTANT IN
         LA    R15,0(,R9)         START OF THE DATA
         SR    R15,R5             LENGTH OF THE DATA
         BZ    NOMOVE             BUT IF IT STARTED WITH &
         BCTR  R15,0              SPECIFIC LENGTHS HERE
         EX    R15,MOVDATA        AND MOVE IN LEADING DATA
         LA    R7,1(R15,R7)       POINT RECEIVER UP
NOMOVE   EQU   *
*
*                    MOVE IN THE SUBSTITUTION DATA
         LA    R15,1              COUNT NUMBER OF SUBSTITUTIONS MADE
         AH    R15,PSWSYNUM
         STH   R15,PSWSYNUM
         LH    R15,SYMLSUBS       LENGTH OF SUBSTITUTION DATA
         BCTR  R15,0
         TM    SYMIND1,SYMI1VQT   IS THIS VALUE IN QUOTES
         BZ    MOVEIT             NO - WHY WORRY
         SH    R15,PSWHALF2       IGNORE THE QUOTES - NOT IN SUBST DATA
         LA    R8,1(,R8)          SNEAKY WAY TO START AT POS 2 OF DATA
*
MOVEIT   EQU   *
         EX    R15,MOVSUBS        AND MOVE IN THAT DATA
         LA    R7,1(R15,R7)       NEW END OF OUTPUT DATA
         CR    R7,R6              CHECK FOR OVERFLOW
         BH    SUBS2LNG           OOPS - TERMINAL PROBLEM, TOO
         LTR   R5,R2              POINTER TO END OF IT ALL
         BZ    REPOINT            IF ZERO, THAT WAS ALL TO IT
         CLI   0(R5),C'.'         ENDED ON SPECIAL PERIOD
         BNE   *+8                NO - NEVER MIND
         LA    R5,1(,R5)          YES - EAT IT
         CR    R5,R4              IS THIS ALL OF IT
         BNH   NEXTSCAN           NO - CARRY ON WITH IT
         B     REPOINT            YES - SET POINTER AND EXIT
*
*                   PRIMARY SCAN FOUND NO MORE SUBSTITUTIONS
LASTSEG  EQU   *
         SR    R4,R5              LENGTH OF DATA TO MOVE
         EX    R4,MOVDATA         MOVE REST OF DATA IN
         LA    R7,1(R4,R7)        POINT BEYOND END OF DATA
         CR    R7,R6              FINAL CHECK FOR OVERFLOW
         BH    SUBS2LNG           WELL - HAD TO HAPPEN SOMETIME
*
REPOINT  EQU   *                  SCANNING IS DONE
         ST    R7,PSWSYEND        PASS BACK NEW ENDING ADDRESS
         SR    R15,R15            SET REAL GOOD RETURN CODE
         B     RETURN
*
FINDSUBS TRT   0(0,R3),0(R15)     LOOK FOR &'S
FINDEND  TRT   1(0,R9),0(R15)     LOOK FOR END OF SYMBOLIC
MOVDATA  MVC   0(0,R7),0(R5)      MOVE DATA ACROSS
MOVSUBS  MVC   0(0,R7),SYMSUBS    SUBSTITUTE NEW DATA
*
         EJECT
*                  TO SCAN THE SUBSTITUTION TABLE TRYING
*                  TO MATCH THE KEYWORD JUST FOUND
*
FINDSYM  PSNMBLOK  FSY,RENT=YES
         L     R8,PSWASYM+12      POINT TO SYMBOLIC SUBST TABLE
         LR    R15,R1             LENGTH OF THE VALUE
         LR    R14,R1             LENGTH OF THE VALUE
         BCTR  R14,0              DOWN ONE FOR LENGTH MATCH
         SH    R15,PSWHALF2       LESS SOME FOR ADJUSTMENT
FSYLOOP  EQU   *
         CH    R14,SYMLVALU       DO THE LENGTHS MATCH
         BNE   FSYNEXT            NO - LOOK NO FURTHER
         EX    R15,FSYMATCH       DOES THE DATA MATCH
         BE    FSYHIT             YES - THIS IS THE ONE
FSYNEXT  EQU   *
         L     R8,SYMNEXT         PICK UP THE FORWARD POINTER
         LTR   R8,R8              WAIT - IS THERE ONE
         BNZ   FSYLOOP            YES - BACK TO PROCESS IT
         PSNMERR  ERRNOSUB,(1(,R9),(R14))
         LA    R15,4              SIGNAL FAILURE
         B     FSYEXIT
*
FSYMATCH CLC   1(0,R9),SYMVALU    EX TO MATCH VALUES
*
FSYHIT   EQU   *                  THIS IS THE PROPER ENTRY
         SR    R15,R15            CODE TO SHOW IT
         B     FSYEXIT
         EJECT
*                    PROCESS SITUATION WHERE THE SUBSTITUTION
*                    DATA IS TOO BIG FOR THE RECEIVING FIELD -
*                   PROBABLY DUE TO INTERNALLY GENERATED CONSTRAINTS.
*
SUBS2LNG EQU   *
         PSNMERR  ERRPKEYL,(SYMSUBS,8)
         LA    R15,8              PROBLEM HERE - PROBABLY WITH TABLES
         B     RETURN
*
*                   OTHER TERMINAL ERRORS
*
BADSYMB  EQU   *                  THE SYMBOLIC IS TOO LONG
         PSNMERR  ERRSYLNG,((R9),9)
         LA    R15,8
         B     RETURN
*
NOSYMB   EQU   *                  INTERNAL SUBSTITUTION ERROR
         PSNMERR  ERRISUBS,((R9),8)
         LA    R15,8
         B     RETURN
         EJECT
         PSNMSYM
*
*
WORKAREA DSECT
         DS    18F
WKSAVPT  DS    2F                 SAVE INPUT POINTERS
         PSNMBLOK  RENT=REGS
*
WKLENG   EQU   *-WORKAREA
         EJECT
         PSNMWORK
         END
