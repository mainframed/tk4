*          DATA SET ABENDPGM   AT LEVEL 001 AS OF 05/12/80
ABENDPGM TITLE 'ISSUE WTO MSG TO OPER TO GET THEIR ATTENTION ON JOB FAIX
               LURE   V1M0'
*.....................................................................*
* ABENDPGM - V1M0                                                     *
*   FUNCTION:                                                         *
*     TO ISSUE A WTO/WTOR MESSAGE TO OPERATOR TO INFORM THEM OF JOB   *
*     FAILURE.  IF PARM FIELD IS INCLUDED, THEN IT IS FORMATTED AND   *
*     ISSUED TO OPER AS MESSAGE.                                      *
*                                                                     *
*     OPERATOR IS REQUIRED TO RESPOND WITH JOBNAME IN ORDER FOR       *
*     PGM TO END.                                                     *
*                                                                     *
* V1M0 - K TRUE  5/12/80  - RECONSTRUCTED ROUTINE FROM DISASSEMBLED   *
*       SOURCE FROM 1974 PGM IN 'SYS2.FCILIB'                         *
*       ADDED DESC=2 TO PGM MESSAGE AND DOM SO OPER WONT HAVE TO PAGE *
*       BACK THROUGH HARDCOPY TO FIND MESSAGE.                        *
*.....................................................................*
         SPACE
ABENDPGM CSECT
         SAVE  (14,12),,ABENDPGM_V1M0_&SYSDATE_&SYSTIME
         LR    R12,R15                     ADDRESS SET
         USING ABENDPGM,R12                ESTABLISH BASE
         LR    R2,R13                       CHAIN
         LA    R13,SAVE                       THE
         ST    R2,SAVE+4                        SAVE
         ST    R13,8(R2)                           AREAS
DOIT     L     R2,16                       CVT ADDRESS
         L     R2,0(R2)                    TCB POINTERS
         L     R2,4(R2)                    (R2)=OUR TCB
         L     R2,12(R2)                   (R2)= JOBNAME
         LA    R3,WTO+4
         MVC   0(8,R3),0(R2)            MOVE JOBNAME INTO WTO MSG
BLNKLOOP CLI   0(R3),C' '               IS END OF JOBNAME BORIS?
         BE    GOTJOB                   YES NATASHA..SPLITSKI
         LA    R3,1(R3)                   BUMP   AND
         B     BLNKLOOP                           GRIND
         EJECT
GOTJOB   LA    R3,1(R3)                 ADD A BLANK FOR NEATNESS
         L     R5,0(R1)                 GET PARM PTR
         LH    R4,0(R5)                 GET LENGTH OF PARM FIELD
         LTR   R4,R4                    ANYONE HOME?
         BZ    NOPARM                     NOPE..USE DEFAULT
         C     R4,F80                   IS PARM LONGER THAN 80 CHARS
         BL    MOVEIT                     TOOOBIIIGGGG..USE 80 INSTEAD
         L     R4,F80                   USE FIRST 80 BYTES
MOVEIT   BCTR  R4,R0                    MAKE INTO MACHINE LENGTH
         EX    R4,MOVEPARM              MOVE PARM TO WTO
         LA    R4,1(R4)                 BACK TO HUMAN LENGTH
         B     DOWTO                    GO STORE LENGTH
         SPACE 2
MOVEPARM MVC   0(1,R3),2(R5)     **EXECUTED**
NOPARM   MVC   0(L'DEFAULT,R3),DEFAULT MOVE IN DEFAULT MESSAGE
         SPACE 2
DOWTO    WTO   MF=(E,WTO)               DO THE WTO
         LR    R7,R1                    SAVE ADDR OF WTO MSG FOR DOM
         EJECT
WTORLOOP MVC   REPLY,BLANKS             CLEAR OUT REPLY
         XC    ECB,ECB                  CLEAR OUT ECB
WTOR     WTOR  'ACKNOWLEDGE ABEND WITH JOB NAME',                      X
               REPLY,L'REPLY,ECB,ROUTCDE=(1,2,11),DESC=2
         SPACE
         WAIT  ECB=ECB
         SPACE
         OC    REPLY,BLANKS             UPPERCASE THE ANSWER
         CLC   REPLY,0(R2)              IS IT OUR JOBNAME
         BNE   WTORLOOP                 NOPE....SKIP BACK
         SPACE
         DOM   MSG=(R7)                 DELETE THE HANGER ON MSG
         SPACE
RETURN   L     R13,SAVE+4               GET OLD SAVE AREA
         RETURN (14,12),RC=4095         AND SPLIT......
         EJECT
SAVE     DC    18F'0'
F80      DC    F'80'
DEFAULT  DC    C'HAS ABNORMALLY TERMINATED-FOLLOW CORRECTIVE ACTION'
         DC    H'0'
ECB      DC    F'0'
REPLY    DC    CL8' '
BLANKS   DC    CL8' '
         SPACE
WTO      WTO   '                                                       X
                                                 ',    THIS SPACE      X
               ROUTCDE=(1,2,11),DESC=2,MF=L         TO LET -- INQUIRE
         REGEQU
         END
