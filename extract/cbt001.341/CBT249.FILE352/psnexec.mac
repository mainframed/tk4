*          DATA SET PSNEXEC    AT LEVEL 002 AS OF 10/30/80
         TITLE 'PSNEXEC - PROCESS THE EXEC CARD'
PSNEXEC  PSNMSTRT  BASE=R12,SASIZ=WKLENG
         EJECT
*                   TO PROCESS A GIVEN EXEC STATEMENT
*                    BREAKING IT UP INTO ITS PARTS AND
*                    PREPARING FOR THE EXEC EDITOR TO TAKE
*                    OVER - THIS IS ONLY A PARSER, NOT AN
*                    EDITOR
*
         BAL   R14,BLDBLOCK       BUILD UP THE BASIC BLOCK
         USING EXECBLOK,R9
         USING EXCENTRY,R7        THE STANDARD ENTRY POINTER FOR ALL
         BAL   R14,UPDTBASE       UPDATE BASE INFORMATION IN IT
*
EACHWORD EQU   *                  FOR EACH KEYWORD IN THE EXEC
         L     R15,PSWSCAN        GO LOOK FOR THE WORD
         BALR  R14,R15
         LTR   R15,R15            WAS IT SUCCESSFUL
         BNZ   RETURN             NO - LOST - GIVE UP
         BAL   R14,FINDKWRD       WHAT WORD WAS IT, JOHNNIE
         LTR   R4,R15             IT WAS SOOOOOOO BAD
         BZ    CHKFEND            WE IGNORE IT COMPLETELY
         BAL   R14,MOVEDATA       CREATE THE BASIC POINTER BLOCK
         BALR  R14,R4             GO TO THE PROPER ROUTINE
         LA    R1,1               COUNT NUMBER OF KEYWORD ENTRIES
         AH    R1,EXCNENT         .
         STH   R1,EXCNENT         .
*                    NOW CHECK IF DONE, MORE CARDS, ....
CHKFEND  EQU   *
         TM    PSWSCANI,PSWSIEND  WAS THAT THE LAST ONE
         BO    EXECDONE
         TM    PSWSCANI,PSWSIMOR  MORE ON SAME LINE
         BO    EACHWORD           YES - CARRY ON WITH IT
READCARD EQU   *
         L     R15,PSWREAD        READ THE NEXT CARD, INSTEAD
         BALR  R14,R15
         LTR   R15,R15            HOW DID IT ALL GO
         BNZ   PROBEOF            PROBABLE EOF PASSED
         L     R1,PSWRNEXT        ADDRESS THIS CARD
         CLI   2(R1),C'*'         IS IT JUST AN IMBEDDED COMMENT
         BNE   EACHWORD           NO - CARRY STRAIGHT ON
*                    IMBEDDED COMMENT - BENIGN NEGLECT IN FORCE
         B     READCARD
*
PROBEOF  EQU   *            SOMETHING UNTOWARD IN THE READ - ASSUME EOF
         PSNMERR  ERREOF,PSWCEXEC
         LA    R15,4              CODE NOT TO PROCEED
         B     RETURN
*
EXECDONE EQU   *                  ALL KEYWORDS PROCESSED
         BAL   R14,XCORELAT       CORRELATE DATA FOR SUMMARY
         L     R15,PSWEDITX       CALL THE EXEC CARD EDITOR
         BALR  R14,R15
         SR    R15,R15            LOOKS GOOD AT THIS LEVEL
         B     RETURN
         EJECT
*                    TO SET UP THE POINTERS FROM THE ANCHOR OR
*                    PREVIOUS EXEC BLOCK TO THIS ONE, AND THE
*                    PSW POINTER TO CURRENT EXEC BLOCK.
*
BLDBLOCK PSNMBLOK  BBK,RENT=YES
         L     R9,PSWAEXEC        ADDRESS START OF EXEC FREE AREA
         C     R9,PSWAEXEC+12     IS THIS FIRST TIME IN
         BE    *+10               YES - FIRST STEP HERE
         L     R15,PSWEDITS       NO - EDIT THE PREVIOUS STEP
         BALR  R14,R15            EDIT THE FELLOW......
*
         L     R8,PSWEXEND        POINTER TO LAST EXEC ENTRY
         LTR   R8,R8              IS THIS ALSO FIRST
         BNZ   *+8                HAVE A PREVIOUS
         LA    R8,PSWEXCHN        FIRST - START THE CHAIN
         ST    R9,0(,R8)          CHAIN TO PREVIOUS BLOCK, OR START
         ST    R9,PSWEXEND        NEW END OF THE CHAIN
         LA    R1,EXCFLENG(,R9)    CHECK FOR THE GOOD FIT
         ST    R1,PSWAEXEC        POINTED FOR NEXT TIME
         C     R1,PSWAEXEC+4      TOO MANY STEPS?
         BNL   BBKFULL            TOOOOOO BAD
         XC    EXECBLOK(256),EXECBLOK CLEAR THE WHOLE MASS
         XC    EXECBLOK+256(EXCFLENG-256),EXECBLOK+256
         XC    PSWDDLST,PSWDDLST  CLEAR THE DD POINTER - START OVER
         B     BBKEXIT
*
BBKFULL  EQU   *                  EXEC ENTRIES HAVE OVERFLOWED
         PSNMERR  ERRTBLFL,(PSWAEXEC+16,8)
         LA    R15,4
         B     RETURN             AND ESCAPE THIS SITUATION
         EJECT
*                  TO UPDATE THE BASIC INFORMATION IN THE EXCLOCK
*                  SUCH AS THE STEPNAME AND THE COMMENTS ENTERED JUST
*                  ABOVE THIS STATEMENT
*
UPDTBASE PSNMBLOK  UDB
         MVC   EXCSTEP,PSWTNAME  NAME FROM THE TYPE PROCESSOR
         L     R1,PSWTHISC        POINTER TO COMMENT STRING
         LTR   R1,R1              ANY COMMENTS HERE
         BZ    UDBEXIT            NO - SKIP IT
         ST    R1,EXCCOMNT        SAVE THIS POINTER TO COMMENTS
         SR    R15,R15            PROPER RETURN
         ST    R15,PSWTHISC       CLEAR THE COMMENT COMMON POINTER
         ST    R15,PSWLASTC       AND ITS CHAIN BUILDER POINTER
         B     UDBEXIT
         EJECT
*                    TO FIND THE PROPER KEYWORD FROM THE SCAN
*
FINDKWRD PSNMBLOK  FKW
         LM    R15,R1,KEYPOINT    PICK UP POINTERS TO KEYWORDS
         L     R14,PSWSKEY        POINTER TO THE KEYWORD
         LH    R2,PSWSKEYL        AND ITS LENGTH
         LR    R3,R2              NEED ANOTHER LENGTH FOR THE CLC
         BCTR  R3,0               .
FKWLOOP  EQU   *
         CH    R2,8(,R15)         DO THE LENGTHS MATCH
         BNE   FKWNEXT            NO - LOOK NO FURTHER
         EX    R3,FKWMATCH        SO - DO THE WORDS MATCH
         BE    FKWHIT             THIS IS THE ONE....
FKWNEXT  EQU   *
         BXLE  R15,R0,FKWLOOP     KEEP LOOKING
         PSNMERR  ERRBADKY,((R14),(R2)),PSWCEXEC
         SR    R15,R15            CODE FOR NO MATCHING KEYWORD
         B     FKWEXIT
*
FKWHIT   EQU   *                  HAVE FOUND THE MAGIC MATCH
         LR    R8,R15             PASS BACK THE TABLE ENTRY ADDRESS
         LH    R1,10(,R15)        PICK UP RELATIVE DISPLACEMENT
         L     R15,12(,R15)       PICK UP THE ROUTINE ADDRESS
         B     FKWEXIT
FKWMATCH CLC   0(0,R15),0(R14)    MATCH FOR KEYWORD
*
*                    THE TABLE OF KEYWORDS, AND THEIR PROCESSING
*                    ROUTINES
KEYPOINT DC    A(*+12,KPL,KPEND)
         DC    CL8'PGM     ',AL2(3,EXCEPGM-EXECBLOK),A(KRPGM)
KPL      EQU   *-KEYPOINT-12
         DC    CL8'COND    ',AL2(4,EXCECOND-EXECBLOK),A(KRCOND)
         DC    CL8'DPRTY   ',AL2(5,EXCEDPRT-EXECBLOK),A(KRDPRTY)
         DC    CL8'DYNAMNBR',AL2(8,EXCEDYNM-EXECBLOK),A(KRDYNUM)
         DC    CL8'PARM    ',AL2(4,EXCEPARM-EXECBLOK),A(KRPARM)
         DC    CL8'REGION  ',AL2(6,EXCEREGN-EXECBLOK),A(KRREGN)
         DC    CL8'TIME    ',AL2(4,EXCETIME-EXECBLOK),A(KRTIME)
         DC    CL8'PERFORM ',AL2(7,EXCEPERF-EXECBLOK),A(KRPERF)
KPEND    EQU   *-KPL
         EJECT
*                    TO MOVE THE DATA FROM THE SCAN POINTER
*                    AREA TO THE TEXT HOLDING AREA, MAKING
*                    THE SUBSTITUTIONS AS REQUIRED.
*
MOVEDATA PSNMBLOK  MD
*                    NOW SIMPLY FILL IN THE BLOCK
         LA    R7,EXECBLOK(R1)    POINT TO THE PROPER SUB-BLOCK
         ZAP   EXCPCARD,PSWCARDN  THE CARD NUMBER
         MVC   EXCPCOL,PSWSCOLN   THE COLUMN NUMBER
         MVC   EXCPKEYW,0(R8)     THE LITERAL OF THE KEYWORD ITSELF
         MVC   EXCPKEYL,8(R8)     AND LENGTH OF THAT KEYWORD
*
         L     R1,PSWSCOM         MOVE THE COMMENT POINTER OVER
         ST    R1,EXCPCOM         .
         LH    R1,PSWSCOML        .
         STC   R1,EXCPCOM         .
*
*                    MOVE ORIGINAL TEXT TO ATEXT WORKAREA
         L     R15,PSWSDAT        POINT TO THAT DATA
         LTR   R15,R15            WAIT - WAS THERE ANY TEXT
         BZ    MDMOVED            NO - SKIP MOST OF THIS
         L     R5,PSWATEXT        POINTER TO RECEIVING AREA
         ST    R5,EXCPORIG        INTO THE POINTER AREA
         LH    R2,PSWSDATL        LENGTH OF THIS DATA
         LA    R3,0(R2,R5)        POINT TO END OF THE TEXT AREA
         ST    R3,PSWATEXT        AND UPDATE THAT POINTER
         STC   R2,EXCPORIG        LENGTH OF THAT ORIGINAL DATA
         MVC   EXCPSUBS(4),EXCPORIG DUPLICATE POINTER, IN CASE
         BCTR  R2,0               PREPARE TO MOVE DATA
         EX    R2,MDMOVORG        MOVE DATA FROM CARD TO WORKAREA
         L     R15,PSWTRSUB       NOW CHECK FOR SUBSTITUTION
         EX    R2,MDCKSYM         ANY & IN THE STRING
         BZ    MDMOVED            NO - THIS IS ALL NEEDED
*                    HAVE A SUBSTITUTION - CALL THE SPECIALIST
         PSNMSUBS  (R3),0
         LH    R1,PSWSYNUM        NUMBER OF SUBSTITUTIONS MADE
         STH   R1,EXCPNUMS        SAVE THIS NUMBER IN THE BLOCK
         LTR   R1,R1              WERE ANY SUBSTITUTIONS MADE
         BZ    MDMOVED            NO - SKIP THE REST
         L     R1,PSWATEXT        ADDRESS WHERE IT WAS MOVED
         ST    R1,EXCPSUBS        AND SHOW LOCATION OF SUBSTITUTION
         L     R3,PSWSYEND        SUBSTITUTE SAID THIS IS END OF IT
         ST    R3,PSWATEXT        NEW END OF TEXT AREA
         SR    R3,R1              COMPUTE LENGTH OF THIS DATA
         STC   R3,EXCPSUBS        SHOW ITS LENGTH
*
MDMOVED  EQU   *
         L     R2,PSWATEXT        PLUCK BACK THE LAST TEXT POINTER
         C     R2,PSWATEXT+4      HAVE WE OVERFLOWED THIS
         BNL   MDFULL             WELL, YES
         MVC   0(4,R2),PSWHEXZ    CLEAR SOME SPACE HERE
*
*                    BUILD MESSAGE POINTERS FOR THE PROCESSORS
         SR    R5,R5              TO PROCESS LENGTH OF DATA FIELD
         IC    R5,EXCPSUBS        THUSLY
         BCTR  R5,0               HANDY FOR EXEC PROCESSING
         L     R6,EXCPSUBS        LOOK AT THE REAL VALUE
         LA    R6,0(,R6)          CLEAR THIS POINTER OUT FIRST
         LA    R5,0(R6,R5)        POINT TO LAST GOOD CHARACTER
         ST    R6,PSWIPBEG        START OF THE INPUT FOR SUBPARSER
         ST    R5,PSWIPEND        AND THE LAST GOOD CHARACTER FOR IT
*
         SR    R15,R15
         B     MDEXIT
*
MDFULL   EQU   *                  TEXT TABLE HAS FILLED UP
         PSNMERR  ERRTBLFL,(PSWATEXT+16,8)
         LA    R15,4              CODE TO QUIT THIS PROC
         B     RETURN
*
MDMOVORG MVC   0(0,R5),0(R15)     MOVE ORIG DATA TO TEXT
MDCKSYM  TRT   0(0,R5),0(R15)     CHECK FOR POSSIBLE SUBSTITUTION
         EJECT
*                    FOR EACH OF THE KEYWORD UNIQUE ROUTINES
*                   TO FOLLOW, THE POINTERS OF NOTE ARE
*              R5 - POINTS TO LAST CHARACTER OF SUBSTITUTED TEXT
*              R6 - TO FIRST CHARACTER OF SUBSTITUTED TEXT
*              R7 - POINTS TO HIS POINTER BLOCK
*              R8 - TO THE ENTRY IN THE KEYWORD TABLE
*              R9 - TO THE BASE DD BLOCK
*                    NO OTHER POINTERS ARE PASSED.
*
KRPGM    EQU   *
KRCOND   EQU   *
KRDPRTY  EQU   *
KRDYNUM  EQU   *
KRPARM   EQU   *
KRREGN   EQU   *
KRTIME   EQU   *
KRPERF   EQU   *
         SR    R15,R15            SIGNAL REALLY GOOD TIME HAD HERE
         BR    R14                A QUICK RETURN FOR ALL
         EJECT
*                    TO CORRELATE THE DATA FROM THE INDIVIDUAL
*                    KEYWORDS TO DESCRIBE SPECIAL SITUATIONS
*
XCORELAT PSNMBLOK  XCR
*
         B     XCREXIT
         EJECT
         PSNMEXEC
         EJECT
*
*
WORKAREA DSECT
         DS    18F
         PSNMBLOK  RENT=REGS
WKLENG   EQU   *-WORKAREA
         EJECT
         PSNMWORK
         END
