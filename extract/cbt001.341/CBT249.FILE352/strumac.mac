./ ADD NAME=AAI                                    13MAR81 15.31.38
*          DATA SET AM009600   AT LEVEL 001 AS OF 03/13/73
         MACRO
&N       AAI   &R,&I
         AIF   (&I LT 1).ERR
         AIF   (&I GT 32767).ERR
         XREFI &I
         AIF   (&I GT 4095).AHI
&N       LA    &R,#&I.(&R)
         MEXIT
.AHI     ANOP
&N       AH    &R,*+6
         DC    X'4700',AL2(#&I)
         MEXIT
.ERR     MNOTE 4,'IMMEDIATE DATA NOT 1-32767'
         MEND
./ ADD NAME=AI                                     13MAR81 15.31.38
*          DATA SET AM008800   AT LEVEL 001 AS OF 03/13/73
         MACRO
&N       AI    &R,&I
         AIF   (&I LT 1).ERR
         AIF   (&I GT 32767).ERR
         XREFI &I
&N       AH    &R,*+6
         DC    X'4700',AL2(#&I)
         MEXIT
.ERR     MNOTE 4,'IMMEDIATE DATA NOT 1-32767'
         MEND
./ ADD NAME=CASE                                   13MAR81 15.31.38
*          DATA SET AM011400   AT LEVEL 003 AS OF 05/24/76
         MACRO
         CASE
         GBLA  &DOCLVL,&DOCNO(10),&CASENO(10),&DOCELSE(10),&DOCNT
         LCLA  &N,&M,&L,&H
.*             AUTHOR.   DON HIGGINS.
.*             DATE.     07/02/74.
.*             REMARKS.  SELECTION CONTROL STRUCTURE.
.*                       SEE DOCASE, CASE, ESAC, AND ESACOD.
.*                       SEE 'STRUCTURED FORTRAN TRANSLATOR' IN
.*                       IN 1975 FEBRUARY ACM SIGPLAN NOTICES.
.*             REV1 05/24/76 DSH - ALLOW SYMBOLIC FOR CASE VALUE.
         AIF   (&DOCLVL LT 1).ERR1
&N       SETA  N'&SYSLIST
         AIF   (&N GT 1).CHKN
&N       SETA  N'&SYSLIST(1)
         AIF   (&N GT 1).CHKN
         AIF   ('&SYSLIST(1)' EQ 'ELSE').ELSE
         AIF   ('&SYSLIST(1)' NE 'MISC').CHKN
.ELSE    AIF   (&DOCELSE(&DOCLVL) GT 0).ERR3
&DOCELSE(&DOCLVL) SETA 1
DC&DOCNO(&DOCLVL)ELSE DS 0H
         MEXIT
.CHKN    ANOP
         AIF   (&CASENO(&DOCLVL) EQ 63).ERR7
&CASENO(&DOCLVL) SETA &CASENO(&DOCLVL)+1
.MLOOP   ANOP
&M       SETA  &M+1
         AIF   (&M GT &N).GENL
&L       SETA  N'&SYSLIST(&M)
         AIF   (&L LT 1).MLOOP
         AIF   (&L GT 2).ERR4
         AIF   (&L EQ 1).GEN1
         AIF   (T'&SYSLIST(&M,1) NE 'N').ERR5
         AIF   (T'&SYSLIST(&M,2) NE 'N').ERR5
&L       SETA  &SYSLIST(&M,1)
&H       SETA  &SYSLIST(&M,2)
         AIF   (&L GT &H).ERR6
         AIF   (&H GT 255).ERR6
         AIF   (&L LT 1).ERR6
         ORG   CASEI&DOCNO(&DOCLVL)+&L
         DC    (&H-&L+1)AL1(&CASENO(&DOCLVL)*4)
         AGO   .MLOOP
.GEN1    ANOP
         ORG   CASEI&DOCNO(&DOCLVL)+&SYSLIST(&M)
         DC    AL1(&CASENO(&DOCLVL)*4)
         AGO   .MLOOP
.GENL    ANOP
         ORG
DC&DOCNO(&DOCLVL)C&CASENO(&DOCLVL) DS 0H
         MEXIT
.ERR1    MNOTE 8,'CASE - MISSING DOCASE'
         MEXIT
.ERR2    MNOTE 8,'CASE - INVALID OPERAND'
         MEXIT
.ERR3    MNOTE 8,'CASE - DUPLICATE ELSE CASE'
         MEXIT
.ERR4    MNOTE 8,'CASE - MORE THAN 2 SUBLIST OPERANDS'
         MEXIT
.ERR5    MNOTE 8,'CASE - OPERAND NOT SELF DEFINING TERM'
         MEXIT
.ERR6    MNOTE 8,'CASE - OPERAND NOT IN RANGE 1-255'
         MEXIT
.ERR7    MNOTE 8,'CASE - MAXIMUM OF 63 CASES EXCEEDED'
         MEXIT
         MEND
./ ADD NAME=DCWV                                   13MAR81 15.31.38
*          DATA SET AM011900   AT LEVEL 002 AS OF 01/13/75
         MACRO
&VLABEL  DCWV  &VNAME
.*             AUTHOR.   DON HIGGINS.
.*             DATE.     11/03/74.
.*             REMARKS.  DEFINE VERY WEAK V TYPE ADDRESS WHICH IS
.*                       SET BY DYNAMICALLY LOADING NAMED MODULE
.*                       THE FIRST TIME THE V ADDRESS IS USED.
DCWV&SYSNDX DS 0H
         USING DCWV&SYSNDX,R15     ENTER HERE ON FIRST USE OF VLABEL
         STM   R14,R2,&VLABEL+12   SAVE REGS USED BY LOAD
         LR    R2,R15              NEW BASE
         DROP  R15
         USING DCWV&SYSNDX,R2
         FINDCDE EPLOC=&VLABEL+4
         IF    (LTR,R1,R1,Z)
               LOAD EPLOC=&VLABEL+4
         ELSE
               L    R0,16(R1) GET CDE ENTRY
         FI
         ST    R0,&VLABEL          STORE ENTRY POINT IN VLABEL
         LR    R15,R0              SET R15 TO CORRECT ENTRY POINT
         L     R14,&VLABEL+12      RESTORE REGS
         LM    R0,R2,&VLABEL+20
         BR    R15                 ENTER VNAME FIRST TIME
&VLABEL  DC    A(DCWV&SYSNDX)      VERY WEAK V ADDRESS
         DC    CL8'&VNAME'         NAME OF MODULE
         DC    5F'0'               SAVE AREA
         MEND
./ ADD NAME=DO                                     13MAR81 15.31.38
*          DATA SET AM010800   AT LEVEL 004 AS OF 08/14/74
         MACRO
         DO
         GBLA  &DO(10),&DOSEQ(10),&DOLVL,&DOLAB
         GBLA  &ELSE(10),&SEQ(10),&LEVEL,&LAB
         LCLA  &N,&M,&S,&LAB1,&L,&I
         LCLA  &UNTIL
         LCLC  &OP,&OPR
         LCLB  &TESTSW
.*             AUTHOR.   DON HIGGINS.
.*             DATE.     03/27/74.
.*             REMARKS.  MACRO TO ALLOW STRUCTURED PROGRAMMING USING
.*                       IF, THEN, ELSE, AND DO UP TO TEN LEVELS DEEP
.*                       AND NO BRANCH INSTRUCTIONS. SEE IF MACRO.
         AIF   (&DOLVL EQ 10).ERR1
&N       SETA  N'&SYSLIST
         AIF   (&N LT 1).ERR2
         AIF   ('&SYSLIST(1)' EQ 'END').END
         AIF   ('&SYSLIST(1)' EQ 'FOREVER').WHILE
         AIF   (&N LT 2).ERR2
         AIF   ('&SYSLIST(1)' EQ 'WHILE').WHILE
         AIF   ('&SYSLIST(1)' NE 'UNTIL').ERR3
.UNTIL   ANOP
&DOLVL   SETA  &DOLVL+1
&UNTIL   SETA  &DOLAB+1
&DOLAB   SETA  &DOLAB+2
         B     DO&UNTIL
         AGO   .COMM
.WHILE   ANOP
&DOLVL   SETA  &DOLVL+1
&DOLAB   SETA  &DOLAB+1
.COMM    ANOP
&DOSEQ(&DOLVL) SETA 1
&DO(&DOLVL) SETA &DOLAB
DO&DOLAB EQU   *
         AIF   (&LEVEL EQ 10).ERR1
&LEVEL   SETA  &LEVEL+1
&SEQ(&LEVEL) SETA 1
&LAB     SETA  &LAB+1
&ELSE(&LEVEL) SETA &LAB
         AIF   ('&SYSLIST(1)' EQ 'FOREVER').EXIT
&S       SETA  1
.NEXTAND ANOP
&TESTSW  SETB  0
.TEST    ANOP
&S       SETA  &S+1
         AIF   (&S GT &N).ERR2
&M       SETA  N'&SYSLIST(&S)
         AIF   (&M LT 2).ERR3
         AIF   (&M GT 5).ERR3
&I       SETA  2
&OPR     SETC  ''
&OP      SETC  '&SYSLIST(&S,1)'
         AIF   (&M GT 2).TESTOPR
         &OP
         AGO   .TESTEND
.TESTOPR ANOP
         AIF   ('&SYSLIST(&S,&I)'(1,1) NE '''').NOLIT
&L       SETA  K'&SYSLIST(&S,&I)
&OPR     SETC  '&OPR'.'&SYSLIST(&S,&I)'(2,&L-2)
         AGO   .NEXTOPR
.NOLIT   ANOP
&OPR     SETC  '&OPR'.'&SYSLIST(&S,&I)'
.NEXTOPR ANOP
&I       SETA  &I+1
         AIF   (&I EQ &M).TESTGEN
&OPR     SETC  '&OPR'.','
         AGO   .TESTOPR
.TESTGEN ANOP
         &OP   &OPR
.TESTEND ANOP
         AIF   (&TESTSW EQ 1).ORBC
.AMBC    ANOP
         BC    15-&SYSLIST(&S,&M),IF0&LAB
.CONNECT ANOP
&S       SETA  &S+1
         AIF   (&S GT &N).CHKUNTL
&M       SETA  N'&SYSLIST(&S)
         AIF   (&M NE 1).ERR3  BAD CONNECTOR
         AIF   ('&SYSLIST(&S)' EQ 'AND').NEXTAND
         AIF   ('&SYSLIST(&S)' NE 'OR').ERR3  BAD CONNECTOR
.NEXTOR   ANOP
&LAB1    SETA  &LAB+1
         B     IF0&LAB1  SKIP TEST IF LAST ONE TRUE
IF0&LAB   EQU *          DO TEST IF LAST ONE FALSE
&LAB     SETA  &LAB+2
&ELSE(&LEVEL) SETA &LAB
&TESTSW  SETB  1
         AGO   .TEST
.ORBC    ANOP
         BC    15-&SYSLIST(&S,&M),IF0&LAB
IF0&LAB1 EQU *
         AGO   .CONNECT
.CHKUNTL ANOP
         AIF   (&UNTIL EQ 0).EXIT
&LAB     SETA  &LAB+1
         B     IF0&LAB
IF0&ELSE(&LEVEL) EQU *
&ELSE(&LEVEL) SETA &LAB
DO&UNTIL EQU *
         AGO   .EXIT
.END     ANOP
         AIF   (&N GT 1).ERR3
         AIF   (&DOLVL EQ 0).ERR4
         AIF   (&DOSEQ(&DOLVL) NE 1).ERR4
&DOSEQ(&DOLVL) SETA 0
         B     DO&DO(&DOLVL)
&DOLVL   SETA  &DOLVL-1
IF0&ELSE(&LEVEL) EQU *
&LEVEL   SETA  &LEVEL-1
         AGO   .EXIT
.ERR1    MNOTE 8,'DO - LEVEL OVERFLOW'
         AGO   .EXIT
.ERR2    MNOTE 8,'DO - MISSING OPERAND'
         AGO   .EXIT
.ERR3    MNOTE 8,'DO - INVALID OPERAND'
         AGO   .EXIT
.ERR4    MNOTE 8,'DO - END OUT OF SEQUENCE'
.EXIT    ANOP
         MEND
./ ADD NAME=DOCASE                                 13MAR81 15.31.38
*          DATA SET AM011300   AT LEVEL 003 AS OF 01/26/78
*          DATA SET AM011300   AT LEVEL 002 AS OF 05/24/76
         MACRO
&N       DOCASE &INDEX
         GBLA  &DOCLVL,&DOCNO(10),&CASENO(10),&DOCELSE(10),&DOCNT
.*             AUTHOR.   DON HIGGINS.
.*             DATE.     07/02/74.
.*             REMARKS.  SELECTION CONTROL STRUCTURE.
.*                       SEE DOCASE, CASE, ESAC, AND ESACOD.
.*                       SEE 'STRUCTURED FORTRAN TRANSLATOR' IN
.*                       IN 1975 FEBRUARY ACM SIGPLAN NOTICES.
.*       REV1 01/04/78 DSH - ELIMINATE RLDS BY USING BRANCH TABLE
         AIF   (T'&INDEX EQ 'U').REG
&N       ZR    R15
         IC    R15,&INDEX
         AGO   .PROC
.REG     ANOP
&N       LR    R15,&INDEX
.PROC    ANOP
         AIF   (&DOCLVL EQ 10).ERR1
&DOCLVL  SETA  &DOCLVL+1
&DOCNT   SETA  &DOCNT+1
&DOCNO(&DOCLVL) SETA &DOCNT
&CASENO(&DOCLVL) SETA 0
&DOCELSE(&DOCLVL) SETA 0
         IC    R15,CASEI&DOCNO(&DOCLVL).(R15)
         B     CASEA&DOCNO(&DOCLVL).(R15)
CASEI&DOCNO(&DOCLVL) DC 256X'00'
         MEXIT
.ERR1    MNOTE 8,'DOCASE NESTED LEVELS EXCEEDED'
         MEND
./ ADD NAME=DOEXIT                                 13MAR81 15.31.38
*          DATA SET AM013000   AT LEVEL 001 AS OF 01/29/75
         MACRO
&N       DOEXIT
         GBLA  &DO(10),&DOSEQ(10),&DOLVL,&DOLAB
         GBLA  &ELSE(10),&SEQ(10),&LEVEL,&LAB
.*             AUTHOR.   DON HIGGINS.
.*             DATE.     01/29/75.
.*             REMARKS.  EXIT TO END OF CURRENT DO LOOP.
         AIF   (&DOLVL EQ 0).ERR1
         B     IF0&ELSE(&LEVEL)
         MEXIT
.ERR1    MNOTE 8,'DOEXIT NOT WITHIN DO LOOP'
         MEND
./ ADD NAME=DTIME                                  13MAR81 15.31.38
*          DATA SET AM000500   AT LEVEL 001 AS OF 01/11/73
         MACRO
&N       DTIME &FIELD
&N       TIME  DEC
         CNOP  0,4
         STM   0,1,*+8
         B     *+12
         DC    2F'0'
         EDIT  TO=&FIELD,FROM=*-14,MASK=' 99/99/99.99F ZZ99.999'
         MEND
./ ADD NAME=EDIT                                   13MAR81 15.31.38
*          DATA SET AM000600   AT LEVEL 001 AS OF 01/11/73
         MACRO
&NAME    EDIT  &TO=,&FROM=,&MASK=
.*
.*             MACRO-ID. 'EDIT'.
.*             AUTHOR.   DON HIGGINS.
.*             DATE WRITTEN.  07/18/70.
.*             REMARKS.
.*                  FUNCTION - GENERATE MASK AND/OR INSTRUCTIONS TO
.*                             EDIT PACKED DECIMAL DATA FOR PRINTING.
.*                  OPERANDS
.*                   TO=   LABEL OF DISPLAY FIELD INTO WHICH THE EDITED
.*                         DATA WILL BE PLACED IF SPECIFIED.
.*                   FROM= LABEL OF PACKED DECIMAL DATA TO BE EDITED
.*                         IF SPECIFIED.
.*                   MASK= STRING OF UP TO 48 CHARACTERS IN SINGLE
.*                         QUOTES DEFINING EDIT MASK SIMILAR
.*                         TO A COBOL PICTURE.  THIS OPERAND IS
.*                         REQUIRED.  IF THE OTHER TWO OPERANDS ARE NOT
.*                         DEFINED, A DC WITH THE DESIRED MASK IS
.*                         GENERATED.  SPECIAL EDIT CHARACTERS ARE AS
.*                         FOLLOWS:
.*                            Z - DIGIT SELECT WITH ZERO SURPRESSION
.*                            9 - DIGIT SELECT WITHOUT ZERO SURPRESSION
.*                            F - FIELD SELECT RESETS SIGNIFICANCE TO
.*                                ALLOW MULTIPLE FIELD EDITING
.*                            $ - DIGIT SELECT WITH ZERO SURPRESSION
.*                                AND FLOATING DOLLAR SIGN.
.*                  NOTES:
.*                   1.  THE FIRST MASK CHARACTER IS USED AS THE FILL
.*                       CHARACTER IN ALL FIELDS.
.*                   2.  THE FOLLOWING EXAMPLE WOULD EDIT BOTH THE TIME
.*                       AND DATE OBTAINED USING THE TIME MACRO
.*                            .
.*                            TIME DEC
.*                            STM  0,1,PDATA
.*                            EDIT TO=LINE+10,FROM=PDATA,
.*                                 MASK=' 99/99/99.99F ZZ99.999'
.*                            .
.*                       PDATA DS  2F     (HHMMSSTH00YYDDDC)
.*                       LINE  DC  CL133' '
.*                            .
.*                   3.  THE FOLLOWING EXAMPLE WOULD GENERATE A DC
.*                       LABELED MASKPAY TO BE USED AS A MASK FOR
.*                       EDITING DIFFERENT FILEDS.
.*                            .
.*                       MASKPAY   EDIT MASK=' Z,ZZZ,ZZ9.99 CR'
.*                            .
.*                   4.  THE MASK CHARACTERS DETERMINE EXPLICIT LENGTH.
.*                   5.  MULTIPLE FIELDS CANNOT CONTAIN FLOATING
.*                       DOLLAR SIGNS.
.*
         LCLA  &LMASK,&SIGNF,&NEXT,&FIELDS,&FLOAT
         LCLC  &C,&C1,&C2,&C3,&C4,&C5,&C6
         AIF   (T'&MASK NE 'U').BADMASK
&LMASK   SETA  K'&MASK-2
         AIF   (&LMASK GT 48).BADMASK
         AIF   (&LMASK LT 1).BADMASK
         AIF   ('&MASK'(1,1) NE '''').BADMASK
         AIF   ('&MASK'(&LMASK+2,1) NE '''').BADMASK
.LOOP    ANOP
&NEXT    SETA  &NEXT+1
         AIF   (&NEXT GT &LMASK).GEN
&C       SETC  '&MASK'(&NEXT+1,1)
         AIF   ('&C' EQ '9').NINE
         AIF   ('&C' EQ 'Z').ZZZZ
         AIF   ('&C' EQ '$').DOLLAR
         AIF   ('&C' EQ 'F').FIELD
.AD1     AIF   ('&C1' GT '9999999').AD2
&C1      SETC  '&C1&C'
         AGO   .LOOP
.AD2     AIF   ('&C2' GT '9999999').AD3
&C2      SETC  '&C2&C'
         AGO   .LOOP
.AD3     AIF   ('&C3' GT '9999999').AD4
&C3      SETC  '&C3&C'
         AGO   .LOOP
.AD4     AIF   ('&C4' GT '9999999').AD5
&C4      SETC  '&C4&C'
         AGO   .LOOP
.AD5     AIF   ('&C5' GT '9999999').AD6
&C5      SETC  '&C5&C'
         AGO   .LOOP
.AD6     AIF   ('&C6' GT '9999999').BADMASK
&C6      SETC  '&C6&C'
         AGO   .LOOP
.NINE    AIF   (&SIGNF EQ 0).SETSIG
.ZZZZ    ANOP
&C       SETC  '½'       (11-0-1-8-9)=X'20'  DIGIT SELECT
         AGO   .AD1
.DOLLAR  ANOP
&FLOAT   SETA  1
         AGO   .ZZZZ
.FIELD   ANOP
&C       SETC  ''       (0-2-9)=X'22'       FIELD SELECT
&SIGNF   SETA  0
&FIELDS  SETA  1
         AGO   .AD1
.SETSIG  ANOP
&C       SETC  'À'       (0-1-9)=X'21'       SIGNIFICANCE STARTER
&SIGNF   SETA  &NEXT
         AGO   .AD1
.GEN     AIF   (T'&TO EQ 'O').GENDC
         AIF   (T'&FROM EQ 'O').BADFROM
         AIF   (&FLOAT EQ 1).GENFL
&NAME    MVC   &TO.(&LMASK.),=CL&LMASK.'&C1&C2&C3&C4&C5&C6'
         ED    &TO.(&LMASK.),&FROM
         MEXIT
.GENDC   AIF   (T'&FROM NE 'O').BADTO
&NAME    DC    CL&LMASK.'&C1&C2&C3&C4&C5&C6'
         MEXIT
.GENFL   AIF   (&FIELDS NE 0).BADMIX
         AIF   (&SIGNF  GT 0).GENLA
&NAME    LA    1,1
         MVC   &TO.(&LMASK.),=CL&LMASK.'&C1&C2&C3&C4&C5&C6'
         EDMK  &TO.(&LMASK.),&FROM
         BCT   1,*+8
         B     *+8
         MVI   0(1),C'$'
         MEXIT
.GENLA   ANOP
&NAME    LA    1,&TO.+&SIGNF
         MVC   &TO.(&LMASK.),=CL&LMASK.'&C1&C2&C3&C4&C5&C6'
         EDMK  &TO.(&LMASK.),&FROM
         BCTR  1,0
         MVI   0(1),C'$'
         MEXIT
.BADMIX  MNOTE 0,'MULTIPLE FIELDS CANNOT CONTAIN FLOATING DOLLAR SIGNS'
         MEXIT
.BADMASK MNOTE 0,'INVALID MASK (1-48 CHARACTERS IN SINGLE QUOTES)'
         MEXIT
.BADTO   MNOTE 0,'MISSING TO FIELD'
         MEXIT
.BADFROM MNOTE 0,'MISSING FROM FIELD'
         MEXIT
         MEND
./ ADD NAME=EDSA                                   13MAR81 15.31.38
*          DATA SET AM014100   AT LEVEL 001 AS OF 12/01/79
         MACRO
         EDSA
.*            PGMID.  AM014100.
.*            AUTHOR. DON HIGGINS.
.*            DATE.   11/30/79
.*            REMARKS. END TO DEFINE STORAGE AREA.
.*                     DEFINE LABEL STARTED AT LAST SDSA.
         GBLC &DSAN(10),&DSAS(10),&DSAE(10)
         GBLA &DSALVL
&DSAE(&DSALVL) DS 0X
         ORG  &DSAS(&DSALVL)
&DSAN(&DSALVL) DS XL(&DSAE(&DSALVL)-&DSAS(&DSALVL))
&DSALVL  SETA &DSALVL-1
         MEND
./ ADD NAME=ELSE                                   13MAR81 15.31.38
*          DATA SET AM010700   AT LEVEL 003 AS OF 05/23/76
         MACRO
         ELSE  &OPT
         GBLA  &ELSE(10),&SEQ(10),&LEVEL,&LAB
         GBLA  &ELSEIF(10)
.*             AUTHOR.   DON HIGGINS.
.*             DATE.     03/27/74.
.*             REMARKS.  MACRO TO ALLOW STRUCTURED PROGRAMMING USING
.*                       IF, THEN, ELSE, AND DO UP TO TEN LEVELS DEEP
.*                       AND NO BRANCH INSTRUCTIONS. SEE IF MACRO.
.*             REV1 05/23/76 DSH - SUPPORT ELSEIF
         AIF   (&LEVEL EQ 0).ERR0
         AIF   (T'&OPT EQ 'O').BEGIN
         AIF   ('&OPT' EQ 'BEGIN').BEGIN
         AIF   ('&OPT' EQ 'END').END
         AIF   ('&OPT' EQ 'CONTINUE').CONT
         MNOTE 8,'ELSE - INVALID OPERAND'
         AGO   .EXIT
.BEGIN   ANOP
         AIF   (&SEQ(&LEVEL) GT 3).ERR1
&LAB     SETA  &LAB+1
         B     IF0&LAB
IF0&ELSE(&LEVEL) EQU *
&ELSE(&LEVEL) SETA &LAB
&SEQ(&LEVEL) SETA 4
         AGO .EXIT
.END     ANOP
         AIF   (&SEQ(&LEVEL) NE 4).ERR2
.DEQUE   ANOP
IF0&ELSE(&LEVEL) EQU *
&LEVEL   SETA &LEVEL-1
         AIF   (&ELSEIF(&LEVEL+1) EQ 1).DEQUE
         AGO   .EXIT
.CONT    ANOP
         AIF   (&SEQ(&LEVEL) GT 3).ERR3
         AGO   .DEQUE
.ERR0    MNOTE 8,'ELSE - MISSING IF'
         AGO   .EXIT
.ERR1    MNOTE 8,'ELSE BEGIN - OUT OF SEQUENCE'
         AGO   .EXIT
.ERR2    MNOTE 8,'ELSE END - OUT OF SEQUENCE'
         AGO   .EXIT
.ERR3    MNOTE 8,'ELSE CONTINUE - OUT OF SEQUENCE'
.EXIT    ANOP
         MEND
./ ADD NAME=ELSEIF                                 13MAR81 15.31.38
*          DATA SET AM013600   AT LEVEL 002 AS OF 05/23/76
         MACRO
         ELSEIF
         GBLA  &ELSE(10),&SEQ(10),&LEVEL,&LAB
         GBLA  &ELSEIF(10)
         LCLA  &N,&M,&S,&LAB1,&L,&I
         LCLC  &OP,&OPR
         LCLB  &TESTSW
.*             AUTHOR.   DON HIGGINS.
.*             DATE.     05/23/76.
.*             REMARKS.  CONTROL STRUCTURE FOR CONDITIONAL EXECUTION.
         AIF   (&LEVEL EQ 10).ERR1
         ELSE
&LEVEL   SETA  &LEVEL+1
&ELSEIF(&LEVEL) SETA 1
&SEQ(&LEVEL) SETA 1
&LAB     SETA  &LAB+1
&ELSE(&LEVEL) SETA &LAB
&N       SETA N'&SYSLIST
.NEXTAND ANOP
&TESTSW  SETB  0
.TEST    ANOP
&S       SETA  &S+1
         AIF   (&S GT &N).ERR2
&M       SETA  N'&SYSLIST(&S)
         AIF   (&M EQ 1).TESTEND
         AIF   (&M LT 2).ERR3
         AIF   (&M GT 5).ERR4
&I       SETA  2
&OPR     SETC  ''
&OP      SETC  '&SYSLIST(&S,1)'
         AIF   (&M GT 2).TESTOPR
         &OP
         AGO   .TESTEND
.TESTOPR ANOP
         AIF   ('&SYSLIST(&S,&I)'(1,1) NE '''').NOLIT
&L       SETA  K'&SYSLIST(&S,&I)
&OPR     SETC  '&OPR'.'&SYSLIST(&S,&I)'(2,&L-2)
         AGO   .NEXTOPR
.NOLIT   ANOP
&OPR     SETC  '&OPR'.'&SYSLIST(&S,&I)'
.NEXTOPR ANOP
&I       SETA  &I+1
         AIF   (&I EQ &M).TESTGEN
&OPR     SETC  '&OPR'.','
         AGO   .TESTOPR
.TESTGEN ANOP
         &OP   &OPR
.TESTEND ANOP
         AIF   (&TESTSW EQ 1).ORBC
.AMBC    ANOP
         BC    15-&SYSLIST(&S,&M),IF0&LAB
.CONNECT ANOP
&S       SETA  &S+1
         AIF   (&S GT &N).EXIT
&M       SETA  N'&SYSLIST(&S)
         AIF   (&M NE 1).ERR5  BAD CONNECTOR
         AIF   ('&SYSLIST(&S)' EQ 'AND').NEXTAND
         AIF   ('&SYSLIST(&S)' NE 'OR').ERR5  BAD CONNECTOR
.NEXTOR   ANOP
&LAB1    SETA  &LAB+1
         B     IF0&LAB1  SKIP TEST IF LAST ONE TRUE
IF0&LAB   EQU *          DO TEST IF LAST ONE FALSE
&LAB     SETA  &LAB+2
&ELSE(&LEVEL) SETA &LAB
&TESTSW  SETB  1
         AGO   .TEST
.ORBC    ANOP
         BC    15-&SYSLIST(&S,&M),IF0&LAB
IF0&LAB1 EQU *
         AGO   .CONNECT
.ERR1    MNOTE 8,'IF - LEVEL OVERFLOW'
         AGO   .EXIT
.ERR2    MNOTE 8,'IF - MISSING TEST LIST'
         AGO   .EXIT
.ERR3    MNOTE 8,'IF - TEST LIST TOO SHORT'
         AGO   .EXIT
.ERR4    MNOTE 8,'IF - TEST LIST TOO LONG'
         AGO   .EXIT
.ERR5    MNOTE 8,'IF - INVALID CONNECTOR'
.EXIT    ANOP
         MEND
./ ADD NAME=EQUAL                                  13MAR81 15.31.38
*          DATA SET AM008400   AT LEVEL 002 AS OF 04/02/74
         MACRO
         EQUAL
*              COMMON EQU'S AND LITERALS
*
*              EQU'S FOR REGISTERS
         EQUREGS
*              EQU'S FOR IMMEDIATE DATA INSTRUCTIONS
         EQUI
*              LITERAL POOL
*              EQU'S FOR BRANCH ON CONDITIONS
         EQUBC
         LTORG
         MEND
./ ADD NAME=EQUBC                                  13MAR81 15.31.38
*          DATA SET AM010900   AT LEVEL 002 AS OF 04/02/74
         MACRO
         EQUBC
H        EQU   2
L        EQU   4
E        EQU   8
NH       EQU   13
NL       EQU   11
NE       EQU   7
O        EQU   1
P        EQU   2
M        EQU   4
Z        EQU   8
NP       EQU   13
NM       EQU   11
NZ       EQU   7
NO       EQU   14
EQ       EQU   8
GT       EQU   2
LT       EQU   4
GE       EQU   11
LE       EQU   13
         MEND
./ ADD NAME=EQUI                                   13MAR81 15.31.38
*          DATA SET AM008500   AT LEVEL 001 AS OF 03/13/73
         MACRO
         EQUI
         GBLA  &N,&V(20)
.LOOP    ANOP
         AIF   (&N EQ 0).EXIT
#&V(&N)  EQU   &V(&N)
&N       SETA  &N-1
         AGO   .LOOP
.EXIT    MEND
./ ADD NAME=EQUREGS                                13MAR81 15.31.38
*          DATA SET AM000700   AT LEVEL 001 AS OF 01/11/73
         MACRO
         EQUREGS &LIMITS
.*
.*             MACRO-ID. 'EQUREGS'.
.*             AUTHOR. DON HIGGINS.
.*             DATE WRITTEN. 08/04/70.
.*             REMARKS.
.*                 THIS MACRO GENERATES EQU STATEMENTS FOR 1 TO 16
.*                 SYMBOLIC REGISTER NAMES OF THE FORM R0 UP TO R15
.*             THE REGISTER EQU DESIRED CAN BE SPECIFIED IN THE FORM
.*             OF  (0,15), (14,1), (2,6), ETC.
         LCLA  &LOW,&HIGH
         AIF   (N'&SYSLIST EQ 1).GETLIM
.DEFAULT ANOP
&LOW     SETA  0
&HIGH    SETA  15
.GEN     ANOP
R&LOW    EQU   &LOW
         AIF   (&LOW EQ &HIGH).DONE
&LOW     SETA  &LOW+1
         AIF   (&LOW LE 15).GEN
&LOW     SETA  0
         AGO   .GEN
.GETLIM  AIF   (N'&SYSLIST(1) NE 2).DEFAULT
         AIF   (T'&SYSLIST(1,1) NE 'N').DEFAULT
         AIF   (T'&SYSLIST(1,2) NE 'N').DEFAULT
&LOW     SETA  &SYSLIST(1,1)
         AIF   (&LOW LT 0).DEFAULT
         AIF   (&LOW GT 15).DEFAULT
&HIGH    SETA  &SYSLIST(1,2)
         AIF   (&HIGH LT 0).DEFAULT
         AIF   (&HIGH GT 15).DEFAULT
         AGO   .GEN
.DONE    MEXIT
         MEND
./ ADD NAME=ESAC                                   13MAR81 15.31.38
*          DATA SET AM011500   AT LEVEL 002 AS OF 05/24/76
         MACRO
&N       ESAC
         GBLA  &DOCLVL,&DOCNO(10),&CASENO(10),&DOCELSE(10),&DOCNT
.*             AUTHOR.   DON HIGGINS.
.*             DATE.     07/02/74.
.*             REMARKS.  SELECTION CONTROL STRUCTURE.
.*                       SEE DOCASE, CASE, ESAC, AND ESACOD.
.*                       SEE 'STRUCTURED FORTRAN TRANSLATOR' IN
.*                       IN 1975 FEBRUARY ACM SIGPLAN NOTICES.
&N       B     DC&DOCNO(&DOCLVL).END
         MEND
./ ADD NAME=ESACOD                                 13MAR81 15.31.38
*          DATA SET AM011600   AT LEVEL 003 AS OF 01/26/78
*          DATA SET AM011600   AT LEVEL 002 AS OF 05/24/76
         MACRO
         ESACOD
         GBLA  &DOCLVL,&DOCNO(10),&CASENO(10),&DOCELSE(10),&DOCNT
.*             AUTHOR.   DON HIGGINS.
.*             DATE.     07/02/74.
.*             REMARKS.  SELECTION CONTROL STRUCTURE.
.*                       SEE DOCASE, CASE, ESAC, AND ESACOD.
.*                       SEE 'STRUCTURED FORTRAN TRANSLATOR' IN
.*                       IN 1975 FEBRUARY ACM SIGPLAN NOTICES.
.*       REV1 01/04/78 DSH - ELIMINATE RLDS BY USING BRANCH TABLE
         LCLA  &C
         AIF   (&DOCLVL LT 1).ERR1
CASEA&DOCNO(&DOCLVL) DS 0F
         AIF   (&DOCELSE(&DOCLVL) GT 0).GENELSE
         B     DC&DOCNO(&DOCLVL).END
         AGO   .GENCASE
.GENELSE ANOP
         B     DC&DOCNO(&DOCLVL).ELSE
.GENCASE ANOP
&C       SETA  &C+1
         AIF   (&C GT &CASENO(&DOCLVL)).GEND
         B     DC&DOCNO(&DOCLVL).C&C
         AGO   .GENCASE
.GEND    ANOP
DC&DOCNO(&DOCLVL).END EQU *
&DOCLVL SETA &DOCLVL-1
         MEXIT
.ERR1    MNOTE 8,'ESACOD - MISSING DOCASE'
         MEND
./ ADD NAME=FI                                     13MAR81 15.31.38
*          DATA SET AM011000   AT LEVEL 001 AS OF 06/28/74
         MACRO
&N       FI
         GBLA  &ELSE(10),&SEQ(10),&LEVEL,&LAB
         AIF   (&SEQ(&LEVEL) NE 4).CONT
&N       ELSE  END
         MEXIT
.CONT    ANOP
&N       ELSE  CONTINUE
         MEND
./ ADD NAME=FILL                                   13MAR81 15.31.38
*          DATA SET AM000900   AT LEVEL 001 AS OF 01/11/73
         MACRO
         FILL   &TAG,&FILL=C' '
         LCLA  &LTAG
.*             AUTHOR.   DON HIGGINS.
.*             DATE.     09/16/71.
.*             REMARKS.  FILL REMAINDER OF AREA WITH FILL CHARACTER.
&LTAG    SETA  L'&TAG
         DC    (&LTAG)&FILL
         ORG   &TAG+&LTAG
         MEND
./ ADD NAME=FINDCDE                                13MAR81 15.31.38
*          DATA SET AM012900   AT LEVEL 002 AS OF 01/10/75
         MACRO
&N       FINDCDE &EP=0,&EPLOC=0
.*             AUTHOR.   DON HIGGINS.
.*             DATE.     01/10/75.
.*             REMARKS.  SET R1 TO CDE ADDRESS OR ZERO
         AIF   ('&EP'(1,1) EQ '0').EPLOC
.EP      ANOP
         BAL   R14,*+12
         DC    CL8'&EP'
         AGO   .FIND
.EPLOC   ANOP
         LA    R14,&EPLOC
.FIND    ANOP
         LOADTCB R15
         ZR    R1
         L     R15,36(R15) TCBLLS FE PG 289
         DO    WHILE,(LTR,R15,R15,NZ)
               L    R1,4(R15)      LLE CDE ADDRESS FE PG 280
               IF   (CLC,8(8,R1),0(R14),EQ)
                    ZR   R15
               ELSE
                    L    R15,0(R15)
                    ZR   R1
               FI
         OD
         MEND
./ ADD NAME=FINDDD                                 13MAR81 15.31.38
*          DATA SET AM012200   AT LEVEL 005 AS OF 03/15/78
         MACRO
&N       FINDDD &DDNAME,&DDLNG=8
.*             AUTHOR.   DON HIGGINS.
.*             DATE.     11/06/74.
.*             REMARKS.  SET R1 TO ADDRESS OF TIOT DDNAME ENTRY OR 0.
.*             EXAMPLE.  FINDDD =CL8'CALLLIB'
.*             REVISIONS:
.*                  REV 01    R D NIMS  JUNE 1976
.*                       DO NOT ALLOW A BLANK FILENAME TO PASS AS VALID
.*                  REV 02    D S HIGGINS 03/02/78 ADD DDLNG OPTION
.*
&N       LOADTIOT R14
         AAI   R14,24
         LA    R15,&DDNAME
         ZR    R0
         ZR    R1
         IC    R0,0(R14)
         DO    WHILE,(LTR,R0,R0,P)
               IF   (CLC,4(&DDLNG,R14),0(R15),EQ)
                    LR   R1,R14
                    ZR   R0
               ELSE
                    AR   R14,R0
                    IC   R0,0(R14)
               FI
         IF    (CLC,0(8,R15),=CL8' ',EQ)                         REV01
               ZR   R1                                           REV01
               ZR   R0                                           REV01
         FI
         OD
         MEND
./ ADD NAME=IF                                     13MAR81 15.31.38
*          DATA SET AM010500   AT LEVEL 002 AS OF 05/23/76
         MACRO
         IF
         GBLA  &ELSE(10),&SEQ(10),&LEVEL,&LAB
         GBLA  &ELSEIF(10)
         LCLA  &N,&M,&S,&LAB1,&L,&I
         LCLC  &OP,&OPR
         LCLB  &TESTSW
.*             AUTHOR.   DON HIGGINS.
.*             DATE.     03/27/74.
.*             REMARKS.  CONTROL STRUCTURE FOR CONDITIONAL EXECUTION.
.*             REV1 05/23/76 DSH - SUPPORT ELSEIF.
         AIF   (&LEVEL EQ 10).ERR1
&LEVEL   SETA  &LEVEL+1
&ELSEIF(&LEVEL) SETA 0
&SEQ(&LEVEL) SETA 1
&LAB     SETA  &LAB+1
&ELSE(&LEVEL) SETA &LAB
&N       SETA N'&SYSLIST
.NEXTAND ANOP
&TESTSW  SETB  0
.TEST    ANOP
&S       SETA  &S+1
         AIF   (&S GT &N).ERR2
&M       SETA  N'&SYSLIST(&S)
         AIF   (&M EQ 1).TESTEND
         AIF   (&M LT 2).ERR3
         AIF   (&M GT 5).ERR4
&I       SETA  2
&OPR     SETC  ''
&OP      SETC  '&SYSLIST(&S,1)'
         AIF   (&M GT 2).TESTOPR
         &OP
         AGO   .TESTEND
.TESTOPR ANOP
         AIF   ('&SYSLIST(&S,&I)'(1,1) NE '''').NOLIT
&L       SETA  K'&SYSLIST(&S,&I)
&OPR     SETC  '&OPR'.'&SYSLIST(&S,&I)'(2,&L-2)
         AGO   .NEXTOPR
.NOLIT   ANOP
&OPR     SETC  '&OPR'.'&SYSLIST(&S,&I)'
.NEXTOPR ANOP
&I       SETA  &I+1
         AIF   (&I EQ &M).TESTGEN
&OPR     SETC  '&OPR'.','
         AGO   .TESTOPR
.TESTGEN ANOP
         &OP   &OPR
.TESTEND ANOP
         AIF   (&TESTSW EQ 1).ORBC
.AMBC    ANOP
         BC    15-&SYSLIST(&S,&M),IF0&LAB
.CONNECT ANOP
&S       SETA  &S+1
         AIF   (&S GT &N).EXIT
&M       SETA  N'&SYSLIST(&S)
         AIF   (&M NE 1).ERR5  BAD CONNECTOR
         AIF   ('&SYSLIST(&S)' EQ 'AND').NEXTAND
         AIF   ('&SYSLIST(&S)' NE 'OR').ERR5  BAD CONNECTOR
.NEXTOR   ANOP
&LAB1    SETA  &LAB+1
         B     IF0&LAB1  SKIP TEST IF LAST ONE TRUE
IF0&LAB   EQU *          DO TEST IF LAST ONE FALSE
&LAB     SETA  &LAB+2
&ELSE(&LEVEL) SETA &LAB
&TESTSW  SETB  1
         AGO   .TEST
.ORBC    ANOP
         BC    15-&SYSLIST(&S,&M),IF0&LAB
IF0&LAB1 EQU *
         AGO   .CONNECT
.ERR1    MNOTE 8,'IF - LEVEL OVERFLOW'
         AGO   .EXIT
.ERR2    MNOTE 8,'IF - MISSING TEST LIST'
         AGO   .EXIT
.ERR3    MNOTE 8,'IF - TEST LIST TOO SHORT'
         AGO   .EXIT
.ERR4    MNOTE 8,'IF - TEST LIST TOO LONG'
         AGO   .EXIT
.ERR5    MNOTE 8,'IF - INVALID CONNECTOR'
.EXIT    ANOP
         MEND
./ ADD NAME=LOADTCB                                13MAR81 15.31.38
*          DATA SET AM012000   AT LEVEL 001 AS OF 11/08/74
         MACRO
&N       LOADTCB &R
&N       L     &R,16     CVT
         L     &R,0(&R)  TCB WORDS
         L     &R,4(&R)  TCB
         MEND
./ ADD NAME=LOADTIOT                               13MAR81 15.31.38
*          DATA SET AM012100   AT LEVEL 001 AS OF 11/08/74
         MACRO
&N       LOADTIOT &R
&N       LOADTCB &R
         L     &R,12(&R) TIOT ADDRESS FROM TCB
         MEND
./ ADD NAME=LOGICAL                                13MAR81 15.31.38
*          DATA SET AM011800   AT LEVEL 001 AS OF 10/03/74
         MACRO
&NAME    LOGICAL
.*             PGMID.    LOGICAL VARIABLE DEFINITION AND ENUMERATION
.*             AUTHOR.   DON HIGGINS.
.*             DATE.     09/27/74.
.*             REMARKS.  DEFINES A LOGICAL VARIABLE AS A ONE BYTE
.*                       AL1 FIELD WITH AN INITIAL VALUE AND STORES
.*                       IN GLOBAL TABLE POSSIBLE VALUES BY NAME.
.*                       ALL NAMES ARE EQUATED TO UNIQUE VALUES FOR
.*                       USE IN TEST AND SET STATEMENTS.
         GBLC  &LOGICN(100)
         GBLA  &LOGICV
         LCLA  &N,&S,&SL
&N       SETA  N'&SYSLIST(2)
.L1      ANOP
&S       SETA  &S+1
         AIF   (&S GT &N).GENDC
&SL      SETA  0
.L2      ANOP
&SL      SETA  &SL+1
         AIF   (&SL GT &LOGICV).GENEQU
         AIF   ('&SYSLIST(2,&S)' EQ '&LOGICN(&SL)').L1
         AGO   .L2
.GENEQU  ANOP
&LOGICV  SETA  &LOGICV+1
&LOGICN(&LOGICV) SETC '&SYSLIST(2,&S)'
&SYSLIST(2,&S) EQU &LOGICV
         AGO   .L1
.GENDC   ANOP
&NAME    DC    AL1(&SYSLIST(1))
         MEND
./ ADD NAME=OD                                     13MAR81 15.31.38
*          DATA SET AM011200   AT LEVEL 001 AS OF 07/01/74
         MACRO
&N       OD
&N       DO    END
         MEND
./ ADD NAME=PENTRY                                 13MAR81 15.31.38
*          DATA SET AM001100   AT LEVEL 006 AS OF 01/26/78
         MACRO
&N       PENTRY
.*             AUTHOR.   DON HIGGINS.
.*             DATE.     01/06/75.
.*             REMARKS.  PERFORM ROUTINE ENTRY POINT WITH TRACE OPTION.
.*                       SEE PTRACE MACRO FOR TRACE INFO.
.*       REV1 01/04/78 DSH - ADD STACK OPTIONS (SEE SUBENTRY)
         GBLC  &PSTKREG,&PBASREG,&PBASLAB
         GBLB  &PSTKSW,&PBASESW,&PSAVESW,&PCHKSW
         LCLC  &STKREG
         LCLA  &OFFSET
         GBLB  &PTRACE,&STKOPT
         GBLC  &PTNAME
         GBLC  &PENAME
         AIF   (&PBASESW EQ 0).L0
         LTORG
.L0      ANOP
&PENAME  SETC  '&N'
         DC    0H'0',CL8'&N'
         AIF   (&STKOPT EQ 1).STK
         B     *+4
&N       EQU   *
         AGO   .TRACE
.STK     AIF   (K'&N EQ 0).STK1
&N       DS    0H
.STK1    ANOP
         AIF   (&PSTKSW EQ 1).L1
&STKREG  SETC  '15'
         L     15,80(13)                    LOAD STACK POINTER
&OFFSET  SETA  &OFFSET+4
         AGO   .L2
.L1      ANOP
&STKREG  SETC  '&PSTKREG'
.L2      AIF   (&PCHKSW EQ 1).L3
         ST    14,0(&STKREG)  STORE LINK - NO CHECKING
&OFFSET  SETA  &OFFSET+4
         AGO   .L4
.L3      ANOP
         CLI   0(&STKREG),X'00'
         BE    *+6
         DC    X'0000'
         STCM  14,7,1(&STKREG) STORE LINK - WITH CHECKING
&OFFSET  SETA  &OFFSET+14
.L4      ANOP
         AIF   (&PBASESW EQ 0).L4A
         ST    &PBASREG,4(&STKREG) SAVE BASE
         BALR  &PBASREG,0 SET NEW BASE
         USING *,&PBASREG
&OFFSET  SETA  &OFFSET+6
&PBASLAB SETC  '&N+&OFFSET'
         LA    &STKREG,8(&STKREG)  INCREMENT STACK WITH PBASE=YES
         AGO   .L4B
.L4A     ANOP
         LA    &STKREG,4(&STKREG)  INCREMENT STACK WITHOUT BASES
.L4B     ANOP
         AIF   (&PSTKSW EQ 1).L5
         ST    15,80(13) STORE STACK
.L5      AIF   (&PSAVESW EQ 0).L6
         LM    14,15,72(13)
.L6      ANOP
.TRACE   AIF   (&PTRACE EQ 0).EXIT
         PM    &PTNAME
.EXIT    ANOP
         MEND
./ ADD NAME=PERFORM                                13MAR81 15.31.38
*          DATA SET AM001200   AT LEVEL 005 AS OF 10/12/78
*          DATA SET AM001200   AT LEVEL 004 AS OF 01/26/78
         MACRO
&N       PERFORM &PENTRY
         GBLC  &PSTKREG,&PBASREG,&PBASLAB
         GBLB  &PSTKSW,&PBASESW,&PSAVESW,&PCHKSW
         GBLB  &STKOPT
.*
.*       SAME AS PM      - SEE SUBENTRY FOR OPTIONS
.*
.*       REV1 01/04/78 DSH - ADD OPTIONS TO STACK MODE
.*
         AIF   (&STKOPT EQ 1).STK
&N       MVC   &PENTRY-2(2),*+10
         B     &PENTRY
         DC    S(*+2)
         MEXIT
.STK     AIF   (K'&N EQ 0).STK1
&N       DS    0H
.STK1    ANOP
         AIF   (&PSAVESW EQ 0).L3
         STM   14,15,72(13)
         AIF   (&PBASESW EQ 1).L1
         BAL   14,&PENTRY
         AGO   .L2
.L1      ANOP
         LR    15,&PBASREG
         A     15,=A(&PENTRY-PBASLAB)
         BALR  14,15
.L2      ANOP
         LM    14,15,72(13)
         AGO   .L5
.L3      ANOP
         AIF   (&PBASESW EQ 1).L4
         BAL   14,&PENTRY
         AGO   .L5
.L4      ANOP
         LR    15,&PBASREG
         A     15,=A(&PENTRY-(&PBASLAB))
         BALR  14,15
.L5      ANOP
         MEND
./ ADD NAME=PEXIT                                  13MAR81 15.31.38
*          DATA SET AM001300   AT LEVEL 005 AS OF 01/17/80
*          DATA SET AM001300   AT LEVEL 004 AS OF 01/26/78
         MACRO
&N       PEXIT
.*
.*       EXIT FROM PERFORMED PROCEDURE DEFINED BY PENTRY
.*       SEE SUBENTRY FOR OPTIONS
.*
.*       REV1 01/04/78 DSH - ADD STACK OPTIONS
.*       REV2 01/17/80 DSH - CHANGE DEFAULT TEMP STACK REG. TO R14
.*                           TO ALLOW RC IN R15 TO BE RETURNED FROM
.*                           PERFORMED ROUTINE.
.*
.*
.*
         GBLC  &PSTKREG,&PBASREG,&PBASLAB
         GBLB  &PSTKSW,&PBASESW,&PSAVESW,&PCHKSW
         LCLC  &STKREG
         GBLB  &STKOPT
         GBLC  &PENAME
         AIF   (&STKOPT EQ 1).STK
&N       B     &PENAME-4
         MEXIT
.STK     AIF   (K'&N EQ 0).STK1
&N       DS    0H
.STK1    ANOP
         AIF   (&PSAVESW EQ 0).L1
         STM   14,15,72(13)
.L1      AIF   (&PSTKSW  EQ 1).L2
&STKREG  SETC  '14'
         L     14,80(13) LOAD STACK POINTER
         AGO   .L3
.L2      ANOP
&STKREG  SETC  '&PSTKREG'
.L3      ANOP
         SL    &STKREG,84(13) DECREMENT STACK
         AIF   (&PSTKSW EQ 1).L4
         ST    14,80(13) SAVE STACK
.L4      AIF   (&PBASESW EQ 0).L4A
         L     &PBASREG,4(&STKREG) RELOAD BASE
.L4A     AIF   (&PCHKSW EQ 0).L5
         ICM   14,15,0(&STKREG)  LOAD LINK WITH CHECKING
         BCR   P,14
         DC    X'0000'
         AGO   .L6
.L5      ANOP
         L     14,0(&STKREG)  LOAD LINK WITHOUT CHECKING
         BR    14
.L6      ANOP
         MEND
./ ADD NAME=PM                                     13MAR81 15.31.38
*          DATA SET AM001201   AT LEVEL 001 AS OF 10/12/78
*          DATA SET AM001200   AT LEVEL 004 AS OF 01/26/78
         MACRO
&N       PM    &PENTRY
         GBLC  &PSTKREG,&PBASREG,&PBASLAB
         GBLB  &PSTKSW,&PBASESW,&PSAVESW,&PCHKSW
         GBLB  &STKOPT
.*
.*       SAME AS PERFORM - SEE SUBENTRY FOR OPTIONS
.*
.*       REV1 01/04/78 DSH - ADD OPTIONS TO STACK MODE
.*
         AIF   (&STKOPT EQ 1).STK
&N       MVC   &PENTRY-2(2),*+10
         B     &PENTRY
         DC    S(*+2)
         MEXIT
.STK     AIF   (K'&N EQ 0).STK1
&N       DS    0H
.STK1    ANOP
         AIF   (&PSAVESW EQ 0).L3
         STM   14,15,72(13)
         AIF   (&PBASESW EQ 1).L1
         BAL   14,&PENTRY
         AGO   .L2
.L1      ANOP
         LR    15,&PBASREG
         A     15,=A(&PENTRY-PBASLAB)
         BALR  14,15
.L2      ANOP
         LM    14,15,72(13)
         AGO   .L5
.L3      ANOP
         AIF   (&PBASESW EQ 1).L4
         BAL   14,&PENTRY
         AGO   .L5
.L4      ANOP
         LR    15,&PBASREG
         A     15,=A(&PENTRY-(&PBASLAB))
         BALR  14,15
.L5      ANOP
         MEND
./ ADD NAME=SCALL                                  13MAR81 15.31.38
*          DATA SET AM002200   AT LEVEL 002 AS OF 09/02/75
         MACRO
&NAME    SCALL &SUBNAME,&ARGLIST
.*             AUTHOR.   DON HIGGINS.
.*             DATE.     11/09/70.
.*             REMARKS.  SHORT FORM OF IBM CALL MACRO WITH NO VL.
         AIF   (T'&ARGLIST EQ 'O').NOARG
&NAME    LA    1,=A&ARGLIST        LOAD ARGUMENT LIST ADDRESS
         L     15,=V(&SUBNAME)     LOAD SUBPROGRAM ENTRY ADDRESS
         BALR  14,15               BRANCH AND LINK
         MEXIT
.NOARG   ANOP
&NAME    L     15,=V(&SUBNAME)     LOAD SUBPROGRAM ENTRY ADDRESS
         BALR  14,15               BRANCH AND LINK
         MEND
./ ADD NAME=SDSA                                   13MAR81 15.31.38
*          DATA SET AM014000   AT LEVEL 001 AS OF 12/01/79
         MACRO
&N       SDSA
.*             PGMID.  AM014000.
.*             AUTHOR. DON HIGGINS
.*             DATE.   11/30/79
.*             REMARKS. START TO DEFINE STORAGE AREA WITH UNNOWN LENGTH
.*                      SEE EDSA WHICH ACTUALLY DEFINES LABEL.
         GBLC  &DSAN(10),&DSAS(10),&DSAE(10)
         GBLA  &DSALVL
&DSALVL  SETA  &DSALVL+1
&DSAN(&DSALVL) SETC  '&N.'
&DSAS(&DSALVL) SETC  'DSAS&SYSNDX.'
&DSAE(&DSALVL) SETC  'DSAE&SYSNDX.'
&DSAS(&DSALVL) DS    0X
         MEND
./ ADD NAME=SUBENTRY                               13MAR81 15.31.38
*          DATA SET AM002500   AT LEVEL 011 AS OF 01/26/78
         MACRO
&NAME    SUBENTRY &CSECT=YES,&BASES=(13),                              X
               &RENT=NO,&RWA=,&RWALNG=0,&SP=0,&STACK=0,                X
               &PSTACK=0,&PBASE=NO,&PSAVE=YES,&PCHECK=YES
         GBLC  &PSTKREG,&PBASREG,&PBASLAB
         GBLB  &PSTKSW,&PBASESW,&PSAVESW,&PCHKSW
         GBLA  &STKSIZE,&STKENT
         GBLB  &STKOPT,&RENTOPT
         LCLA  &NBASE,&S,&OFFSET
&PSTKSW  SETB  0
&PBASESW SETB  0
&PSAVESW SETB  0
&PCHKSW  SETB  0
&STKOPT  SETB  0
&RENTOPT SETB  0
.*             MACRO-ID. 'SUBENTRY'
.*             AUTHOR.   DON HIGGINS.
.*             DATE WRITTEN.  07/28/70.
.*             REMARKS.  THIS MACRO GENERATES THE CODE TO ESTABLISH A
.*                       CSECT WITH SAVE AREA AND BASE REGISTER USING
.*                       STANDARD OS LINKAGE CONVENTIONS.
.*             REV1 11/14/74 ADD REENTRANT OPTION WITH WORK AREA
.*                 1.  FOR REENTRANT LINKAGE YOU MUST SPECIFY
.*                     A.  RENT=YES
.*                     B.  AT LEAST ONE BASE REGISTER OTHER THAN 13
.*                         SUCH AS BASES=(R12)
.*                 2.  OPTIONALLY YOU CAN SPECIFY A DSECT NAME AND
.*                     LENGTH FOR A WORK AREA BUILT BEHIND SAVE AREA.
.*                     FOR EXAMPLE  RWADSECT=MYWORK,RWALNG=8
.*             REV2 02/10/76 DSH - ADD SUBPOOL OPTION
.*             REV3 XX/XX/77 FDB - ADD STACK OPTION TO SUPPORT
.*                                 REENTRANT PERFORM, PENTRY, PEXIT
.*
.*                                 STACK=  SETS MAXIMUM DEPTH
.*                                         OF PERFORMS
.*
.*             REV4 01/04/78 DSH - ADD OPTIONS TO OPTIMIZE STACK CODE
.*                                 AND PROVIDE EXTENDED BASE REGISTERS
.*
.*                                 PSTACK= DEFINES PERMANENT REGISTER
.*                                         FOR STACK TO SAVE LOADS
.*                                         AND STORES
.*
.*                                 PBASE=YES CAUSES BASE REGISTER
.*                                           DEFINED BY BASES= TO
.*                                           BE STACKED AND A NEW
.*                                           BASE ESTABLISHED FOR
.*                                           EACH PERFORMED ROUTINE
.*
.*                                 PSAVE=NO  CAUSES R14 AND R15
.*                                           TO NOT BE SAVED ACROSS
.*                                           PERFORMS TO SAVE LOADS
.*                                           AND STORES
.*
.*                                 PCHECK=NO ELIMINATES CODE TO
.*                                           CHECK FOR STACK
.*                                           OVERFLOW/UNDERFLOW
.*
.*
.*
         AIF   ('&CSECT'(1,1) EQ 'N').NOCSECT
&NAME    CSECT
         AGO   .STM
.NOCSECT ANOP
&NAME    DS    0D
.STM     ANOP
         AIF   ('&RENT'(1,1) EQ 'N').NORENT
&RENTOPT SETB  1
         STM   14,12,12(13)
         BAL   &BASES(1),40(15)
         USING &NAME+8,&BASES(1)
         DC    CL8'&NAME'
         DC    CL8'SESA'
         DC    CL8'&SYSDATE'
         DC    CL8'&SYSTIME'
         AIF   ('&STACK' EQ '0').NOSTK1
&STKOPT  SETB  1
&STKENT  SETA  4
         AIF   ('&PBASE'(1,1) EQ 'N').STK1
&PBASESW SETB  1
&STKENT  SETA  8
&PBASREG SETC  '&BASES(1)'
&PBASLAB SETC  '&NAME+8'
.STK1    ANOP
&STKSIZE SETA  20+&STKENT*&STACK+4
.NOSTK1  ANOP
         LA    0,80+&RWALNG+&STKSIZE
         AIF   ('&SP' EQ '0').SKIPSP
         LA    1,&SP
         SLL   1,24
         OR    0,1
.SKIPSP  ANOP
         LR    2,0
         GETMAIN R,LV=(0)
.*
.*       SAVE AREA, STACK, AND WORK AREA LAYOUT
.*
.*       -8     4  'SESA'
.*       -4     4  SUB POOL AND TOTAL LENGTH FOR SUBEXIT FREEMAIN
.*        0    72  STANDARD SAVE AREA
.*       72     4  R14 SAVE AREA DURING PERFORM IF PSAVE=YES
.*       76     4  R15 SAVE AREA DURING PERFORM IF PSAVE=YES
.*       80     4  STACK POINTER SAVE AREA IF NO PSTACK DEFINED
.*       84     4  LENGTH OF STACK ENTRY (4 OR 8 IF PBASE DEFINED)
.*       88     4  LOW STACK ENTRY WITH HIGH BIT ON FOR CHECKING
.*       92     X  STACK OF LENGTH DEFINED BY STACK=
.*       92+X   4  HIGH STACK ENTRY WITH HIGH BIT ON FOR CHECKING
.*       96+X   Y  WORK AREA DEFINED BY RWA= AND RWALNG=
.*
.*
         MVC   0(4,1),8(&BASES(1))
         ST    2,4(,1)
         LA    1,8(,1)
         AIF   ('&STACK' EQ '0').NOSTK2
         AIF   ('&PSAVE'(1,1) EQ 'N').L1
.*                                          IF SAVE REQUIRED
.*                                             SET SWITCH
&PSAVESW SETB  1
.L1      AIF   ('&PCHECK'(1,1) EQ 'N').L2
.*                                          IF CHECKING REQUIRED
.*                                             SET SWITCH
&PCHKSW  SETB  1
         XC    88(8+&STKENT*&STACK,1),88(1) CLEAR STACK AREA
         MVI   88(1),X'80'                  SET LOW BAD BIT
         MVI   92+&STKENT*&STACK.(1),X'80'  SET HIGH BAD BIT
.L2      ANOP
         LA    15,&STKENT
         ST    15,84(1)                     SET ENTRY LENGTH
         AIF   ('&PSTACK' NE '0').L3
.*                                          IF NO STACK REGISTER
         LA    15,92(1)
         ST    15,80(1)                       SAVE STACK POINTER
         AGO   .L4
.*                                          ELSE
.L3      ANOP
&PSTKSW  SETB  1                               SET PSTACK SWITCH
&PSTKREG SETC  '&PSTACK'                       SAVE REG FOR MACROS
         LA    &PSTKREG,92(1)                  LOAD STACK REGISTER
.L4      ANOP
.NOSTK2  ANOP
         ST    1,8(13)
         ST    13,4(1)
         LM    14,2,12(13)
         L     13,8(13)
         AIF   ('&RWALNG' EQ '0').GEN
         USING &RWA-(72+&STKSIZE),13
         AGO   .GEN
.NORENT  ANOP
         STM   14,12,12(13)
         BAL   15,104(15)
         DC    18F'0'
         DC    CL8'&NAME'
         DC    CL8'&SYSDATE'
         DC    CL8'&SYSTIME'
         ST    15,8(13)
         ST    13,4(15)
         LR    13,15
         AIF   (T'&BASES(1) NE 'N').CHKR13
         AIF   (&BASES(1) EQ 13).GENB13
         AGO   .GENBX
.CHKR13  ANOP
         AIF   ('&BASES'(2,3) EQ 'R13').GENB13
.GENBX   ANOP
         LR    &BASES(1),13
         USING &NAME+8,&BASES(1)
         AGO   .GEN
.GENB13  ANOP
         USING &NAME+8,13
.GEN     ANOP
&NBASE   SETA  N'&BASES
&S       SETA  1
.LOOP    ANOP
&S       SETA  &S+1
         AIF   (&S GT &NBASE).EXIT
&OFFSET  SETA  &OFFSET+4095
         LA    &BASES(&S),4095(&BASES(&S-1))
         USING &NAME+8+&OFFSET,&BASES(&S)
         AGO   .LOOP
.EXIT    ANOP
         MEND
./ ADD NAME=SUBEXIT                                13MAR81 15.31.38
*          DATA SET AM002600   AT LEVEL 012 AS OF 07/18/77
         MACRO
&NAME    SUBEXIT &RC=0,&RENT=NO,&RWALNG=0,&SP=0
         GBLB  &RENTOPT
.*             MACRO-ID. 'SUBEXIT'.
.*             AUTHOR.   DON HIGGINS.
.*             DATE WRITTEN.  07/28/70.
.*             REMARKS.  THIS MACRO GENERATES THE CODE TO RESTORE
.*                       REGISTERS AND RETURN FROM A CSECT.  A RETURN
.*                       CODE OF ZERO IS RETURNED UNLESS A DIFFERENT
.*                       CODE IS SPECIFIED USING THE KEY WORD RC=.
.*             REV1 11/14/74 ADD REENTRANT OPTION. SEE SUBENTRY.
.*             REV2 12/09/74 ADD REG OPTION FOR RC OPERAND.
.*             REV3 02/10/76 DSH - ADD SUBPOOL OPTION
         AIF   ('&RC'(1,1) EQ '(').RCREG
         LA    15,&RC
         AGO   .CHKRENT
.RCREG   ANOP
         LR    15,&RC
.CHKRENT ANOP
         AIF   (&RENTOPT EQ 0).NORENT
&NAME    L     13,4(,13)
         STM   15,1,16(13)
         L     1,8(,13)
         LA    15,8
         SLR   1,15
         L     0,4(,1)
         FREEMAIN R,LV=(0),A=(1)
         LM    14,12,12(13)
         BR    14
         MEXIT
.NORENT  ANOP
&NAME    L     13,4(,13)
         L     14,12(,13)
         LM    2,12,28(13)
         BR    14
         MEND
./ ADD NAME=TAG                                    13MAR81 15.31.38
*          DATA SET AM002900   AT LEVEL 001 AS OF 01/11/73
         MACRO
&NAME    TAG
&NAME    DS    0H
         MEND
./ ADD NAME=TRTTAB                                 13MAR81 15.31.38
*          DATA SET AM012700   AT LEVEL 004 AS OF 01/22/75
         MACRO
&N       TRTTAB &TYPE,&BAD=X'FF',&GOOD=X'00',&SKIP=,&FIND=,&FILL=C' '
         AIF   ('&TYPE' EQ 'PRINT').PRINT
         AIF   ('&TYPE' EQ 'NUMERIC').NUMERIC
         AIF   ('&TYPE' EQ 'ALPHA').ALPHA
         AIF   ('&TYPE' EQ 'ALPHANUM').ALPHAN
         AIF   ('&TYPE' EQ 'ZFILL').ZFILL
         AIF   ('&TYPE' EQ 'BAD').BAD
         AIF   ('&TYPE' EQ 'GOOD').GOOD
         AIF   ('&TYPE' EQ 'SCAN').SCAN
         MNOTE 8,'TABLE TYPE INVALID'
         MEXIT
.PRINT   ANOP
&N       DC    (X'49'-X'00'+1)&FILL
         DC    (X'50'-X'4A'+1)AL1(*-&N)
         DC    (X'59'-X'51'+1)&FILL
         DC    (X'61'-X'5A'+1)AL1(*-&N)
         DC    (X'6A'-X'62'+1)&FILL
         DC    (X'6F'-X'6B'+1)AL1(*-&N)
         DC    (X'79'-X'70'+1)&FILL
         DC    (X'7F'-X'7A'+1)AL1(*-&N)
         DC    (X'C0'-X'80'+1)&FILL
         DC    (X'C9'-X'C1'+1)AL1(*-&N)
         DC    (X'D0'-X'CA'+1)&FILL
         DC    (X'D9'-X'D1'+1)AL1(*-&N)
         DC    (X'E1'-X'DA'+1)&FILL
         DC    (X'E9'-X'E2'+1)AL1(*-&N)
         DC    (X'EF'-X'EA'+1)&FILL
         DC    (X'F9'-X'F0'+1)AL1(*-&N)
         DC    (X'FF'-X'FA'+1)&FILL
         MEXIT
.NUMERIC ANOP
&N       DC    240&BAD,10&GOOD,6&BAD
         MEXIT
.ALPHA   ANOP
&N       DC    193&BAD,9&GOOD,7&BAD,9&GOOD,8&BAD,8&GOOD
         DC    22&BAD
         MEXIT
.ALPHAN  ANOP
&N       DC    193&BAD,9&GOOD,7&BAD,9&GOOD,8&BAD,8&GOOD
         DC    6&BAD,10&GOOD,6&BAD
         MEXIT
.ZFILL   ANOP
&N       DC    64AL1(*-&N),C'0',191AL1(*-&N)
         MEXIT
.BAD     ANOP
&N       DC    256&BAD
         MEXIT
.GOOD    ANOP
&N       DC    256&GOOD
         MEXIT
.SCAN    ANOP
         AIF   (T'&SKIP NE 'O').SKIP
         AIF   (T'&FIND NE 'O').FIND
         MNOTE 8,'TRT SCAN SKIP OR FIND CHARACTER NOT FOUND'
         MEXIT
.SKIP    ANOP
&N       DC    256&BAD
         ORG   &N+&SKIP
         DC    &GOOD
         ORG
         MEXIT
.FIND    ANOP
&N       DC    256&GOOD
         ORG   &N+&FIND
         DC    &BAD
         ORG
         MEXIT
         MEND
./ ADD NAME=XREFI                                  13MAR81 15.31.38
*          DATA SET AM008600   AT LEVEL 001 AS OF 03/13/73
         MACRO
         XREFI &I
         GBLA  &N,&V(20)
         LCLA  &S,&K,&NK
&NK      SETA  N'&I
.LOOP1   ANOP
&K       SETA  &K+1
         AIF   (&K GT &NK).EXIT
         AIF   (&N EQ 0).ADD
&S       SETA  0
.LOOP2   ANOP
&S       SETA  &S+1
         AIF   (&I(&K) EQ &V(&S)).REF
         AIF   (&S LT &N).LOOP2
.ADD     ANOP
         AIF   (&N EQ 20).ERR
&N       SETA  &N+1
&V(&N)   SETA  &I(&K)
.REF     ANOP
         AGO   .LOOP1
.ERR     MNOTE 4,'IMMEDIATE DATA XREF TABLE OVERFLOW'
.EXIT    MEND
./ ADD NAME=ZB                                     13MAR81 15.31.38
*          DATA SET AM009200   AT LEVEL 002 AS OF 09/18/73
         MACRO
&N       ZB    &FIELD,&BITS
         LCLA  &NBITS,&B(8),&S
&NBITS   SETA  N'&BITS
         AIF   (&NBITS LT 1).ERR
         AIF   (&NBITS GT 8).ERR
.LOOP    ANOP
&S       SETA  &BITS(&NBITS)
         AIF   (&S LT 0).ERR
         AIF   (&S GT 7).ERR
&B(&S+1) SETA  1
&NBITS   SETA  &NBITS-1
         AIF   (&NBITS GT 0).LOOP
&N       NI    &FIELD,X'FF'-B'&B(1)&B(2)&B(3)&B(4)&B(5)&B(6)&B(7)&B(8)'
         MEXIT
.ERR     MNOTE 8,'INVALID BIT SPECIFICATION'
         MEND
./ ADD NAME=ZHB                                    13MAR81 15.31.38
*          DATA SET AM009800   AT LEVEL 001 AS OF 03/13/73
         MACRO
&N       ZHB   &R
&N       LA    &R,0(&R)
         MEND
./ ADD NAME=ZR                                     13MAR81 15.31.38
*          DATA SET AM009500   AT LEVEL 001 AS OF 03/13/73
         MACRO
&N       ZR    &R
&N       SR    &R,&R
         MEND
