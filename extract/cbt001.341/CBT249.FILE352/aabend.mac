*          DATA SET S052G01    AT LEVEL 001 AS OF 05/16/74
         TITLE '(S052G01).  ABEND SUBROUTINE.'
S052G01  CSECT
         SPACE
         ENTRY ABEND
         SPACE
* THIS SUBROUTINE WILL ABEND THE CALLING PROGRAM.
*
* THIS SUBROUTINE WILL PRESERVE THE CALLING PROGRAM'S REGISTERS 2-14
* SO THAT THE REGISTERS 2-14 OF THE REGISTERS AT ENTRY TO ABEND WILL BE
* THOSE OF THE CALLING PROGRAM.
*
* THE CALLER MUST USE STANDARD LINKAGE CONVENTIONS.  THE PARAMETER
* LIST PASSED BY THE CALLING PROGRAM MUST CONTAIN 2 ENTRIES.  EACH
* ENTRY MUST BE THE ADDRESS OF A FULLWORD BINARY NUMBER.  THE FIRST
* NUMBER MUST BE A 1 IF NO CORE DUMP IS DESIRED.  ANY OTHER VALUE WILL
* CAUSE A CORE DUMP.  THE SECOND NUMBER MUST HAVE A VALUE FROM 1 TO
* 4095.  THIS SECOND NUMBER WILL BE USED FOR THE VALUE OF THE USER
* COMPLETION CODE.  IF THE SECOND NUMBER CONTAINS AN INVALID VALUE A
* CORE DUMP WILL BE FORCED WITH A USER COMPLETION CODE OF 4095.  THE
* CORE DUMP WILL TAKE PLACE REGARDLESS OF THE VALUE OF THE FIRST
* NUMBER.
         SPACE
R0       EQU   0
R1       EQU   1
R15      EQU   15
         EJECT
         USING *,R15
ABEND    EQU   *
         LM    R0,R1,0(R1)         PICK UP ADDRESSES OF THE 2 NUMBERS.
         L     R1,0(0,R1)          R1 NOW CONTAINS USER COMPLETION CODE
         XR    R0,R1               INTERCHANGE R0 AND R1.
         XR    R1,R0
         XR    R0,R1
         L     R1,0(0,R1)          R1 NOW CONTAINS DUMP INDICATOR.
*                                  R0 CONTAINS COMPLETION CODE.
         LTR   R0,R0               IS IT LESS THAN 1?
         BNP   XXX                 IF YES...BRANCH.
         C     R0,=F'4095'         IS IT GREATER THAN 4095?
         BNH   YYY                 IF NOT...BRANCH.
XXX      EQU   *
         ST    R0,HOLDCODE         SAVE CALLER'S BAD CODE.
         LA    R0,4095             FORCE 4095 COMPLETION CODE.
         B     ZZZ
YYY      EQU   *
         BCT   R1,ZZZ              BRANCH IF USER WANTS DUMP.
         B     *+8
ZZZ      EQU   *
         L     R1,=XL4'80000000'   HIGH ORDER BIT ON IN R1 INDICATES
*                                       CORE DUMP WANTED.
         OR    R1,R0               OR COMPLETION CODE INTO R1.
         ABEND (1)
HOLDCODE DC    F'0'
         LTORG
         END
