*          DATA SET PSNSUBOP   AT LEVEL 002 AS OF 10/30/80
         TITLE 'PSNSUBOP - BREAK UP SUBOPERANDS - LOWEST LEVEL'
PSNSUBOP PSNMSTRT  SASIZ=WKLENG
         EJECT
*                    TO BREAK UP THE FIELDS WITHIN A GIVEN
*                    KEYWORD INTO THE LOWEST LEVEL OF OPERANDS
*                    RESULTING IN A LIST OF DATA ELEMENTS AND
*                    THEIR LENGTHS
*
         L     R9,PSWIPBEG        BEGINNING OF THE STRINGQ
         L     R8,PSWIPEND        END OF THE STRING
         XC    PSWINTPS(PSWIPLNG),PSWINTPS CLEAR THE POINTERS
         CLI   0(R9),C''''        IS THE STRING QUOTED
         BE    ALLONEQ            YES - ALL ONE QUOTED STRING
         CLI   0(R9),C'('         IS IT INDEED A SUBLIST
         BNE   ALLONE             NO - ALL ONE OPERAND
         LA    R9,1(,R9)          PAST THE SURROUNDING
         BCTR  R8,0               ()
         LA    R7,PSWIPOP1        START OF THE POINTER STRING
*
GOAGAIN  EQU   *                  AND PARSE OUT THIS STRING
         BAL   R14,SCANSUBS       SCAN FOR SUBSTITUTIONS
         B     *+4(R15)           BASED ON RETURN CODES
         B     RETURN             WHOLE THING IS DONE
         B     BADSCAN            SUBSTRING END - WRONG
*                    A SUBSTRING START FOUND - REPOINT AND REGO
         LA    R1,PSWIPSB1        POINT TO THE SUBSTRING AREA
         ST    R1,0(,R7)          SHOW THIS IS SUBSTRINGED
         MVI   0(R7),X'FF'        THE REAL INDICATOR
         LR    R6,R7              SAVE THE REAL POINTER
         LR    R7,R1              AND SUBSTITUTE SUBSTRING POINTER
         CLC   0(12,R7),PSWHEXZ   HAS IT BEEN USED ALREADY
         BNE   TWOSUBS
         LA    R9,1(,R9)          ON PAST THE (
         BAL   R14,SCANSUBS       BREAK UP THE SUB SUB SUB STRING
         B     *+4(R15)           AND DETERMINE WHAT TO DO NOW
         B     BADSCAN            END - CAN NOT BE
         B     *+8                END OF SUB - CAN BE
         B     BADSCAN            ANOTHER SUB STRING - IMPROBABLE
         LA    R7,4(,R6)          REESTABLIST MAIN POINTER
         LA    R9,1(,R9)          PAST THE NEXT DELIMITER, TOO
         CR    R9,R8              DID THAT FINISH US UP
         BNH   GOAGAIN            NO - STILL MORE TO COME
         SR    R15,R15            END OF THE PROCESS
         B     RETURN
*
BADSCAN  EQU   *                  SOME ERROR IN THE SCANNING
         LR    R0,R9              COMPUTE LENGTH OF FIELD
         SR    R0,R8
         PSNMERR  ERRINTSC,((R9),(R0)),SAVR=YES
         LA    R15,4
         B     RETURN
*
TWOSUBS  EQU   *                  TWO SUBSTRINGS ON ONE ENTRY
         L     R9,PSWINTPS        POINT TO START OF STRING
         LR    R0,R9              COMPUTE LENGTH OF FIELD
         SR    R0,R8
         PSNMERR  ERR2SUB,((R9),(R0)),SAVR=YES
         LA    R15,4
         B     RETURN
*
*                    AND IF THE SUBFIELDS ARE ALL ONE
ALLONEQ  EQU   *
         LA    R9,1(,R9)          IGNORE THE QUOTES
         BCTR  R8,0               .
ALLONE   EQU   *                  NO LEADING ( - MUST BE ONE ONLY
         SR    R8,R9              LENGTH OF THE FIELD
         LA    R8,1(,R8)          BUILD THE TRUE LENGTH
         ST    R9,PSWIPOP1        SAVE THE SINGLE POINTER
         STC   R8,PSWIPOP1        AND ITS LENGTH
         SR    R15,R15
         B     RETURN
         EJECT
*                    TO DO THE ACTUAL SCANNING, FILLING
*                    IN EACH OF THE POINTER FIELDS AS
*                    THE VALUE IS INTERPRETED
*
SCANSUBS PSNMBLOK  SSB,RENT=YES
         L     R15,PSWTRSPA       PROPER TRT TABLE FOR THIS TASK
SSBAGAIN EQU   *
         CLI   0(R9),C'('         START OF SUBLIST
         BE    SSBSUBLS           YES - BACK TO YOU, DAVID
         SR    R2,R2              THE INDICATOR OF WHAT COMES NEXT
         LA    R1,0(,R8)          COMPUTE LENGTH LEFT TO SCAN
         SR    R1,R9
         BM    SSBDONE            IN CASE THAT WAS ALL
         EX    R1,SSBSCAN         LOOK FOR DELIMITER
         BNZ   *+8
         LA    R1,1(,R8)          POINT TO PSEUDO SCAN-STOPPER
         SR    R1,R9              LENGTH OF THIS FIELD
         ST    R9,0(,R7)          SAVE THS START OF IT
         STC   R1,0(,R7)          AND ITS LENGTH
         LA    R9,1(R1,R9)        POINT PAST THAT DELIMITER
         LA    R7,4(,R7)          AND TO NEXT POINTER
         B     *+4(R2)            AND WHAT TO DO NEXT
         B     SSBDONE            THAT WAS THE LAST ONE - EXIT
         B     BADSCAN            WHAT IS A QUOTE DOING HERE
         B     SSBAGAIN           COMMA - NORMAL KEEP GOING
         B     SSBILIST           START OF SUBLIST - LIKE VOL=(..
         B     SSBENDSB           END OF SUBLIST - PASS RETURN
         EX    R0,*               WHO FOOLED WITH THE TABLE
*
SSBSCAN  TRT   0(0,R9),0(R15)     LOOK FOR NEXT DELIMITER
*
SSBDONE  EQU   *
         SR    R15,R15            CODE FOR ALLLLLL DONE
         B     SSBEXIT
*
SSBENDSB EQU   *                  END OF A SUBSTRING - HIT )
         LA    R15,4
         B     SSBEXIT
*
SSBSUBLS EQU   *                  FOUND START OF A SUBLIST
         LA    R15,8
         B     SSBEXIT
*
SSBILIST EQU   *                  OBVIOUSLY AN AFTERTHOUGHT FOR
*                                 THE VOLUME PARAMETER
         SR    R2,R2              CLEAR THE INDICATOR OF SCANNING
         L     R14,PSWTRPAR       LOOKING FOR ( AND ) ONLY
         LR    R1,R8              RECOMPUTE THE LENGTH
         SR    R1,R9
         EX    R1,SSBFPAR         FOR PARENTHESIS
         CH    R2,PSWHALF8        THE ONLY GOOD RETURN
         BNE   BADSCAN            TOO DEEP FOR ME
         SR    R1,R9              HOW LONG WAS THIS THING
         ST    R9,0(,R7)          SAVE START OF THE LIST
         STC   R1,0(,R7)          AND ITS LENGTH
         LA    R9,2(R1,R9)        POINT PAST ) AND WHATEVER IS NEXT
         CR    R9,R8              THEN WAS THAT THE END
         BNL   SSBDONE            BY GEORGE
         B     SSBAGAIN           BACK INTO THE DUST CLOUD
SSBFPAR  TRT   0(0,R9),0(R14)
         EJECT
WORKAREA DSECT
         DS    18F
         PSNMBLOK  RENT=REGS
WKLENG   EQU   *-WORKAREA
         EJECT
         PSNMWORK
         END
