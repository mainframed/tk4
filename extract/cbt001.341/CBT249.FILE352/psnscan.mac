*          DATA SET PSNSCAN    AT LEVEL 002 AS OF 10/30/80
         TITLE 'PSNSCAN - LOOK FOR DELIMITERS'
PSNSCAN  PSNMSTRT  SASIZ=WKLENG
         EJECT
*                    TO SCAN FROM THE INDICATED START THROUGH
*                    END OF THE RECORD, RETURNING THE POINTER
*                    TO START OF DATA, LENGTH OF THAT DATA,
*                    OPTIONAL START OF VALUE IF AN = IS FOUND
*                    AND LENGTH OF THAT VALUE.
*
         XC    PSWSCAND(PSWSCANL),PSWSCAND CLEAR THE COMM AREA
         L     R8,PSWRSTRT        PICK UP STARTING POINT
*
         LA    R15,0(,R8)         COMPUTE THE STARTING COLUMN
         S     R15,PSWRNEXT       LESS START OF THE CARD
         STH   R15,PSWSCOLN       STASH IT FOR ALL TO USE
*
RESCAN   EQU   *
         L     R15,PSWTRKEY       THE PROPER TRANSLATE TABLE
         L     R3,PSWREND         LAST POSSIBLE POSITION TO WORRY ABOUT
         SR    R2,R2              PREPARE FOR BRANCH INDEX FROM TRT
         SR    R3,R8              LENGTH TO SCAN (MAXIMUM)
         BNP   NOTHING            WHY EVEN CALL ME, THEN
         LA    R4,0(,R8)          ALWAYS SCAN STARTING AT R4
         EX    R3,SCANLINE        GO LOOKING FOR DELIMITERS
         BNZ   *+12               MAYBE HIT RIGHT AWAY???
         L     R1,PSWREND         SO WE ENDED HERE
         LA    R2,4               AND IT IS THE END OF IT
*
         SR    R1,R8              HOW LONG WAS THIS WORD
         BNP   BADSCAN            PROBABLE PROGRAM ERROR
         ST    R8,PSWSKEY         STASH ITS POSITION
         STH   R1,PSWSKEYL        AND LENGTH
*                    WHAT WAS IT THAT STOPPED US
         LA    R8,1(R1,R8)        PAST THE DELIMITER
CHKFEND  EQU   *                  MOST COME BACK HERE
         CH    R2,PSWHALF8        WHAT WAS THE DELIMITER
         BL    ENDOFIT            STOPPED ON A BLANK
         BH    HAVVALUE           STOPPED ON EQUAL
*                    ENDED ON A COMMA - CHECK FOR MORE ON LINE
         CLI   0(R8),C' '         IS IT CONTINUED
         BE    ENDTHISL           ONLY ON THE NEXT CARD
*                    NO VALUE, BUT MORE ON THIS CARD
         OI    PSWSCANI,PSWSIMOR  SIGNAL MORE ON CARD
         ST    R8,PSWRSTRT        FOR USE NEXT TIME
         SR    R15,R15            TRADITIONAL GOOD LUCK CHARM
         B     RETURN
*
*
SCANLINE TRT   0(0,R4),0(R15)     EX BY EVERYONE HERE
         EJECT
*                    STOPPED ON AN EQUAL - LOOK FOR VALUE
HAVVALUE EQU   *
         LA    R4,0(,R8)          START YOUR SCAN HERE....
         CLI   0(R8),C''''        IS VALUE IN QUOTES
         BE    VALUQUOT           YES - SEPARATE ROUTINE
         CLI   0(R8),C'('         HOW ABOUT WRAPPED UP
         BE    VALUPARN           VALUE IN PARENTHESESESESES
*                    AN ORDINARY - PLAIN - VALUE STRING
VALUAGN  EQU   *                  REENTRY POINT
         L     R3,PSWREND         ADDRESS END OF IT AGAIN
         SR    R3,R4              LENGTH TO SCAN
         BNP   BADSCAN            NOT GOOD
         L     R15,PSWTRKEY       TRANSLATE AGAIN WITH SAME ONE
         EX    R3,SCANLINE
         BNZ   *+12               COULD BE ALL OVER QUICK
         L     R1,PSWREND         POINT TO THE END
         LA    R2,4               AND SIMULATE FINAL END
*
         CH    R2,PSWHALF8        WAIT - WAS IT AN INTERNAL EQUAL
         BNH   VALUHIT            NO - NO PROBLEM
*                                                                   NTL
* SEE IF INNER LIST  VOL=SER=(XXXX,XXX,XXX)                         NTL
*                                                                   NTL
         CLI   1(R1),C'('         START OF LIST?                    NTL
         BNE   VALUBUMP           NO BUMP AND TRY AGAIN             NTL
         LA    R4,1(,R1)          POINT TO THE '('                  NTL
         B     VALUPARN           AND GO PROCESS THE PARENS....     NTL
         SPACE 1                                                    NTL
VALUBUMP LA    R4,1(,R1)          YES - INCLUDE HIM IN THE LIST
         B     VALUAGN            AND KEEP TRYING
VALUHIT  EQU   *
         SR    R1,R8              HOW LONG WAS THIS THING
         BM    BADSCAN            MORE TROUBLE HERE
         BZ    NODATA             ZERO LENGTH - NO DATA
*                    RECORD LOCATION OF THE DATA
         ST    R8,PSWSDAT         DATA LOCATION
         STH   R1,PSWSDATL        AND ITS LENGTH
         LA    R8,1(R1,R8)        POINT PAST THIS RECENT TRIUMPH
         OI    PSWSCANI,PSWSIVAL  AND REEMPHASIZE A VALUE HERE
         B     CHKFEND            CHECK IF THIS IS ALL ON LINE
*
*                    IF THE DATA IS ZERO LENGTH
NODATA   EQU   *
         OI    PSWSCANI,PSWSINOD  SHOW NO DATA AFTER EQUAL
         LA    R8,1(,R8)          POINT PAST ALL OF THIS
         B     CHKFEND            TO COMMON END-CHECKING ROUTINE
         EJECT
*                    IF THE VALUE STARTED WITH A QUOTE
*
VALUQUOT EQU   *
         OI    PSWSCANI,PSWSIVQT  VALUE IS IN QUOTES
VALUQSCN EQU   *
         LA    R4,1(,R4)          PAST THE QUOTE MARK....
         L     R3,PSWREND
         SR    R3,R4
         BNP   BADSCAN            EVER ON THE ALERT
         L     R15,PSWTRQOT       SCANNING ONLY FOR QUOTES, NOW
         EX    R3,SCANLINE
         BZ    BADSCAN            NOT SO SOON
         CLI   1(R1),C''''        IS THIS THE DOUBLE QUOTE
         BNE   VQFOUND            NO - HAVE FOUND OTHER END
         LA    R4,1(,R1)          PREPARE TO INDEX PAST THIS ONE
         B     VALUQSCN           AND DO IT AGAIN
VQFOUND  EQU   *                  END OF STRING FOUND
         LA    R1,1(,R1)          INCLUDE THAT LAST QUOTE (OR PAREN)
         SR    R1,R8              LENGTH OF THE STRING
         ST    R8,PSWSDAT         POINT TO THE VALUE
         STH   R1,PSWSDATL        AND ITS LENGTH
         OI    PSWSCANI,PSWSIVAL  SHOW A VALUE HERE
         LA    R8,0(R1,R8)        POINT PAST END OF IT
         L     R15,PSWTRKEY       GET OPINION ON WHATS NEXT
         TRT   0(1,R8),0(R15)     TO SET THE R2
         BZ    BADSCAN            SHOULD HAVE HIT SOMETHING
         LA    R8,1(,R8)          AND PAST THAT ONE, TOO
         B     CHKFEND            DO END AND COMMENT CHECKING
         EJECT
*                    STARTED WITH A PARENTHESES
*
VALUPARN EQU   *
         LA    R4,1(,R4)          ON PAST THAT FIRST (
         L     R15,PSWTRPAR       THE TABLE FOR COUNTING PARENTHESES
         LA    R7,1               THE COUNT OF ( FOUND
*
VPAGAIN  EQU   *
         L     R3,PSWREND         SCAN UNTIL ALL PAREN MATCHED....
         SR    R3,R4              LENGTH LEFT ELIGIBLE
         BNP   BADSCAN
         EX    R3,SCANLINE
         BZ    BADSCAN            REALLY NEED MATCHING SETS
         B     *(R2)              GO ACCORDING TO ARGUMENT BYTE
         B     VPLEFT             HAD A LEFT PARENTHESES
         B     VPHITR             HAD A RIGHT PARENTHESIS
         B     VPBLANK            HAD A BLANK - CONTINUATION????
VPHITR   EQU   *                  HIT A RIGHT PARENTHESES
         BCT   R7,VPRIGHT         SUBTRACT ONE, AND SEARCH ON
         B     VQFOUND            ALL MATCHED - HAVE DEFINED VALUE
VPLEFT   EQU   *
         LA    R7,1(,R7)         COUNT THE LEFT PAREN FOUND
VPRIGHT  EQU   *
         LA    R4,1(,R1)          AND CARRY ON WITH THE SEARCH
         B     VPAGAIN
*
*                    AND IF THE VALUE IS CONTINUED TO NEXT CARD
VPBLANK  EQU   *
         BCTR  R1,0               LESS ONE FOR THE EXEC
         CLI   0(R1),C','         IS IT THE COMMA,BLANK COMBO
         BNE   BADSCAN            NO - BUGOUT
         S     R1,PSWRSTRT        LENGTH OF THE WHOLE THING
         L     R15,PSWRSTRT       START OF WHOLE THING
         EX    R1,VPMOVIT         AND MOVE TO HOLDING AREA
         LA    R7,PSWSCHLD+1(R1)  END OF THIS DATA FIELD
         L     R15,PSWREAD        NOW READ IN THE NEXT CARD
         BALR  R14,R15
         L     R15,PSWRSTRT       START OF DATA ON THAT CARD
         L     R1,PSWREND         AND END OF THAT DATA
         SR    R1,R15             SO, LENGTH OF THAT DATA
         EX    R1,VPMOVCT         MOVE IN THE CONTINUATION
         LA    R1,0(R1,R7)        END OF THE CONCATANATION
         ST    R1,PSWREND         THIS IS NEW END OF IT
         LA    R8,PSWSCHLD        THIS IS THE START OF FIELD
         ST    R8,PSWRSTRT        INTO POINTER AREA
         B     RESCAN             AND BACK TO TRY AGAIN
VPMOVIT  MVC   PSWSCHLD(0),0(R15) MOVE FIRST PART
VPMOVCT  MVC   0(0,R7),0(R15)     MOVE CONTINUATION
         EJECT
*                    HAVE FOUND END OF DATA - CHECK FOR COMMENTS
ENDOFIT  EQU   *
         OI    PSWSCANI,PSWSIEND  AND OF LINE - NO MORE
*                    CHECK FOR THE COMMENT
*
ENDTHISL EQU   *                  COMMA FOLLOWED BY BLANK
         SR    R15,R15            CLEAR THE RETURN CODE
         C     R8,PSWREND         ANY COMMENT HERE
         BNL   RETURN             NO COMMENT, EITHER
*                   DEFINE RANGE OF COMMENT
         LA    R4,0(,R8)          POINT TO START OF BLANKS
         L     R3,PSWREND         END OF THE DATA LINE
         SR    R3,R8              LENGTH TO SCAN FOR
         L     R15,PSWTRBLK       TABLE TO SKIP BLANKS
         EX    R3,SCANLINE
         BZ    BADSCAN            COME NOW - WHO RUNS THIS THING
         L     R8,PSWREND         REVERSE REGS FOR THIS COMPUTE
         SR    R8,R1              THIS HAS LENGTH OF COMMENT
*
*                    TO INSERT THE COMMENT POINTER AND
*                    SAVE THE COMMENT DATA
         L     R15,PSWALIT        POINT TO START OF TABLE
         LA    R3,0(R8,R15)       POINT TO PROPOSED END OF IT
         C     R3,PSWALIT+4       WILL IT FIT
         BH    ICFULL             OOPS - LITERAL AREA IS FULL
         ST    R3,PSWALIT         NEW AVAILABLE AREA
         ST    R15,PSWSCOM        WHERE THE COMMENT STARTS
         STH   R8,PSWSCOML        AND ITS LENGTH
         BCTR  R8,0
         EX    R8,ICMOVCOM
         OI    PSWSCANI,PSWSICOM  SHOW IT EXISTS
         SR    R15,R15
         B     RETURN
*
ICMOVCOM MVC   0(0,R15),0(R1)     MOVE COMMENT TO LITERAL AREA
*
ICFULL   EQU   *
         PSNMERR  ERRTBLFL,(PSWALIT+16,8)
         LA    R15,4              CANNOT CONTINUE LIKE THIS
         B     RETURN
         EJECT
*
*                    AND THOSE MISCELLANEOUS ROUTINES THAT
*                    ACT AS A KNACKER, IF IT DIES OTHERWISE
*
NOTHING  EQU   *                  NO DATA WITHIN THE BOUNDS GIVEN
         PSNMERR  ERRBLANK
         LA    R15,4              CODE FOR NOTHING REALLY TO RETURN
         B     RETURN
*
BADSCAN  EQU   *                  INVALID ARITHMETIC SOMEWHERE
         PSNMERR  ERRISCAN,((R8),8)
         LA    R15,12             REALLY BAD RETURN HERE
         B     RETURN
         EJECT
WORKAREA DSECT
         DS    18F
         DS    X
WKLENG   EQU   *-WORKAREA
         EJECT
         PSNMWORK
         END
