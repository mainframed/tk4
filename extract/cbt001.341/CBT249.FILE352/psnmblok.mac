*          DATA SET PSNMBLOK   AT LEVEL 002 AS OF 10/30/80
         MACRO
&TAG     PSNMBLOK  &P,&RENT=NO
         GBLC  &PSNPREF(50)       SAVE ROOM FOR PREFIXES
         GBLA  &PSNSUB            POINTER TO NEXT PREFIX
         LCLC  &PF                THE PREFIX DECIDED UPON FOR THIS ONE
         LCLA  &WPF               USED IN GENERATING THE RENT=REGS
.*
         AIF   ('&RENT' NE 'REGS').GBLK GENERATE NORMAL BLOCK
         AIF   (&PSNSUB NE 0).GREGS GEN REGS IF ANY NEEDED
         MNOTE *,'NO ENTRIES FOR REG TABLE'
         AGO   .EXIT
.GREGS   ANOP                     GENERATE THE REG SAVE-WORDS
&WPF     SETA  &WPF+1             POINT ON TO NEXT ENTRY
&PF      SETC  '&PSNPREF(&WPF)'   NAME OF THE NEXT PREFIX
&PF.R14  DS    F'0'
         AIF   (&WPF LT &PSNSUB).GREGS AND DO THEM ALL
&PSNSUB  SETA  0                  RE-CLEAR THE VALUE
         MEXIT
.*
.*
.GBLK    ANOP
         AIF   (T'&P EQ 'O').DEFLP DEFAULT THE PREFIX
         AIF   (K'&P LE 4).PLNOK  LENGTH OF PROVIDED IS OK
         MNOTE 8,'ONLY 4 CHARACTERS MAX FOR PREFIX'
         AGO   .EXIT
.PLNOK   ANOP
&PF      SETC  '&P'               USE PROVIDED PREFIX
         AGO   .PFSET
.DEFLP   ANOP                     BUILD DEFAULT PREFIX ID
         AIF   (K'&TAG LE 4).SMALTAG
&PF      SETC  '&TAG'(1,4)        USE FIRST FOUR CHARS OF TAG
         AGO   .PFSET
.SMALTAG ANOP                     USE FULL TAG AS PREFIX
&PF      SETC  '&TAG'
.PFSET   ANOP                     THE PREFIX IS DECIDED UPON.....
         AIF   ('&RENT' EQ 'YES').HAVRENT
         AIF   (&PSNSUB NE 0).HAVRENT
&PF.R14  DC    F'0'               SAVE RETURN ADDR
         AGO   .GENCODE
.HAVRENT ANOP
&PSNSUB  SETA  &PSNSUB+1
&PSNPREF(&PSNSUB)  SETC  '&PF'    SAVE PREFIX IN TABLE FOR LATER
.*
.GENCODE ANOP
&PF.EXIT L     R14,&PF.R14        RETURN ADDR
         MVI   &PF.R14,X'FF'      SHOW EXIT
         BR    R14                EXIT
&TAG     ST    R14,&PF.R14
.*
.EXIT    ANOP
         SPACE 1
         MEND
