*          DATA SET VTOCUPD    AT LEVEL 001 AS OF 07/23/81
*          DATA SET VTOCUPD    AT LEVEL 002 AS OF 07/24/81
VTOCUPD   TITLE 'PUT CURRENT DATE IN DATE-LAST-REFERENCED FIELD'
*/* REFER:  IPO1.SAMPLIB(VTOCUPD)
*/* COMPID: SYSTEM
*/* DOCL:   THIS IS A SAMPLE ROUTINE TO SHOW HOW TO PUT THE CURRENT
*/*         DATE IN THE DATE-LAST-REFERENCED FIELD ADDED BY SU60.
*/*         THE ROUTINE MAY BE CHANGED/EXPANDED TO CREATE A LIST
*/*         OF DATA SETS NOT IN USE FOR A SPECIFIC PERIOD OF TIME,
*/*         OR TO DUMP ONLY PORTIONS OF A DISK PACK THAT HAS BEEN
*/*         REFERENCED.  THE NEW BIT DS1IND02 CAN BE USED TO
*/*         DETERMINE, WHETHER THE DATA SET HAS BEEN CHANGED.
*/*         DON'T FORGET TO RESET THIS BIT AFTER REFERENCING IT.
*/*
***********************************************************************
*                                                                     *
*             MODULE NAME = VTOCUPD                                   *
*                                                                     *
*             DESCRIPTIVE NAME =  IPO SUPPLIED ROUTINE TO PUT THE     *
*                CURRENT DATE IN THE DATE-LAST-REFERENCED FIELD       *
*                ADDED BY SU60.                                       *
*                                                                     *
*             COPYRIGHT = NONE                                        *
*                                                                     *
*             FUNCTION =                                              *
*                PUT CURRENT DATE IN DS1REFD FIELD IF ZERO            *
*                                                                     *
*              NOTES =                                                *
*                                                                     *
*                 DEPENDENCIES = NONE.                                *
*                                                                     *
*                 RESTRICTIONS = NONE                                 *
*                                                                     *
*                 REGISTER CONVENTIONS = STANDARD CONVENTIONS.        *
*                    REGISTERS 0 - 2   = WORK REGISTERS               *
*                    REGISTERS 3 - 11  = UNUSED                       *
*                    REGISTERS 12      = BASE REGISTER                *
*                    REGISTER  13 -15  = STANDARD OS USAGE            *
*                                                                     *
*                PATCH LABEL = PATCH (UNUSED AND INTIALIZED TO        *
*                   BINARY ZEROES)                                    *
*                                                                     *
*             MODULE TYPE = PROCEDURE                                 *
*                                                                     *
*                PROCESSOR = ASM                                      *
*                                                                     *
*                MODULE SIZE = 1000 BYTES                             *
*                                                                     *
*                ATTRIBUTES = PROBLEM STATE, AUTHORIZED               *
*                                                                     *
*             ENTRY POINTS = VTOCUPD                                  *
*                                                                     *
*             INPUT = NONE                                            *
*                                                                     *
*             OUTPUT = NONE                                           *
*                                                                     *
*             EXIT - NORMAL = AT PROGRAM END VIA BRANCH REGISTER 14   *
*                                                                     *
*                OUTPUT = NONE                                        *
*                                                                     *
*                RETURN CODE = ZERO                                   *
*                                                                     *
*             EXIT - ERROR = NONE                                     *
*                                                                     *
*                OUTPUT = NONE                                        *
*                                                                     *
*                RETURN CODE = 16  DD-STATEMENT 'VTOC' IS MISSING     *
*                WITH VOLALLOC LOAD, NO VTOC DD STATEMENT IS USED     *
*                ONLY DDNAME ALLOCDD(F 80) WITH VOLUMES AND DEVICE    *
*                TO BE USED WITH VTOCUPD.                             *
*                                                                     *
*             EXTERNAL REFERENCES = VOLALLOC                          *
*                                                                     *
*                ROUTINES = NONE                                      *
*                                                                     *
*                DATA AREAS = NONE                                    *
*                                                                     *
*                CONTROL BLOCKS = DSCB, JFCB, TIOT, DCB               *
*                                                                     *
*             TABLES = NONE                                           *
*                                                                     *
*             MACROS = SAVE, RETURN, RDJFCB, OPEN, CLOSE, EXTRACT,    *
*                      RESERVE, DEQ, DCB, DCBD                        *
*                                                                     *
*             CHANGE ACTIVITY = 05/08/79 HIGH-ORDER BIT IN EXLST      *
*                                                                     *
*             MESSAGES = NONE                                         *
*                                                                     *
*             ABEND CODES = NONE                                      *
*                                                                     *
*.  B WILLIAMS V1M0 24 JUL 81   ---  SAME CODE FOR VTOCUPD BUT ADDED .*
*                                    CALL TO VOLALLOC TO DYNALLOC ALL *
*                                    VOLUMES TO HAVE VTOC UPDATE.     *
*                                                                     *
*                                                                     *
***********************************************************************
         EJECT
VTOCUPD  CSECT
         USING VTOCUPD,R15
         B     SAVEREGS
PROGID   DC    C'VTOCUPD_R01.0_&SYSDATE'
$$$COIBM DC    C'                                          '
SAVEREGS SAVE  (14,12)                 SAVE REGISTERS
         DROP  R15
         LR    R12,R15                 SET BASE REG
         USING VTOCUPD,R12             TELL ASSEMBLER
         LA    R1,SAVEAREA             GET SAVEAREA TO CHAIN
         ST    R13,4(R1)               FORWARD CHAIN
         ST    R1,8(R13)               BACKWARD CHAIN
         LR    R13,R1                  END PROLOGUE
         SPACE 3
***********************************************************************
*                                                                     *
*              GET CURRENT DATE AND CONVERT TO BINARY                 *
*                                                                     *
***********************************************************************
         TIME  DEC                     GET TIME AND DATE   - 00YYDDDF
         STCM  R1,3,DOUBLE+6           STORE DATE          -     DDDF
         CVB   R0,DOUBLE               CONVERT DATE TO BINARY
         STCM  R0,3,CURRDATE+1         STORE BINARY DATE   -     DDDD
         SRL   R1,12                   SHIFT YEAR          - 00000YYD
         STCM  R1,3,DOUBLE+6           STORE YEAR          -     0YYD
         OI    DOUBLE+7,X'0F'          MAKE VALID PACKED DEC 00000YYF
         CVB   R0,DOUBLE               CONVERT YEAR TO BINARY
         STC   R0,CURRDATE             STORE BINARY YEAR   -   YYDDDD
         SPACE 3
***********************************************************************
*                                                                     *
*              GO ALLOCATE A VOLUME TO UPDATE                         *
*                                                                     *
***********************************************************************
ALOCDD   L     R15,=V(VOLALLOC)       LOAD ADDRESS TO DYNALLOC   V1M0
         BALR  R14,R15                GO ALLOCATE VOLUMEV1M0
         LTR   R15,R15                DO I HAVE TIOT ADDRESS     V1M0
         BZ    RETURN                 NO --- ALL DONE V1M0
***********************************************************************
*                                                                     *
*              UPDATE THE JFCB                                        *
*                                                                     *
***********************************************************************
         RDJFCB VTOC                   READ THE JFCB
         USING INFMJFCB,R2             ESTABLISH ADDRESSABILITY
         LA    R2,JFCBAREA             POINT TO JFCB
         LTR   R15,R15                 TEST IF DD CARD PRESENT
         BNZ   BADRETRN                NO DD CARD - BAD RETURN
         MVI   JFCBDSNM,X'04'          CREATE DSNAME
         MVC   JFCBDSNM+1(43),JFCBDSNM    - 44X'04'
         OI    JFCBTSDM,JFCNWRIT       DO NOT WRITE JFCB DURING OPEN
         MVC   RNAME,JFCBVOLS          SAVE VOLUME SERIAL NUMBER
         DROP  R2
         SPACE 3
***********************************************************************
*                                                                     *
*              OPEN THE VTOC                                          *
*                                                                     *
***********************************************************************
         OPEN  (VTOC,(UPDAT)),TYPE=J
         SPACE 3
***********************************************************************
*                                                                     *
*              ENQUEUE ON VTOC                                        *
*                                                                     *
***********************************************************************
         L     R1,DCBDEBAD-IHADCB+VTOC              GET A(DEB)
         LA    R2,DEBUCBAD-DEBDASD+DEBBASND-DEBBASIC(R1) GET A(UCB)
         RESERVE (QNAME,RNAME,E,,SYSTEMS),UCB=(R2)  ISSUE ENQUEUE/RESVE
         SPACE 3
***********************************************************************
*                                                                     *
*              READ AND UPDATE THE VTOC                               *
*                                                                     *
***********************************************************************
READVT   READ  DECB,SF,MF=E            GET NEXT VTOC RECORD
         CHECK DECB                    WAIT FOR I/O COMPLETE
         USING IECSDSL1,R2             ESTABLISH DSCB ADDRESSABILITY
         LA    R2,DSCB                 POINT TO DSCB
         CLI   DS1FMTID,C'1'           IS THIS A FORMAT 1 DSCB
         BNE   READVT                  NO - READ NEXT RECORD
         OC    DS1REFD,DS1REFD         IS LAST REFERENCED FIELD ZERO
         BNZ   READVT                  NO - READ NEXT RECORD
         MVC   DS1REFD,CURRDATE        YES - UPDATE FIELD WITH CURRDATE
         WRITE DECB,SF,MF=E            UPDATE VTOC RECORD
         CHECK DECB                    WAIT FOR I/O COMPLETE
         B     READVT                  GO AND GET NEXT RECORD
         DROP  R2
         SPACE 3
***********************************************************************
*                                                                     *
*              RETURN TO OS                                           *
*                                                                     *
***********************************************************************
BADRETRN LA    R15,16                  INDICATE DD STATEMENT MISSING
         B     RETURN                  RETURN TO OS
CLOSE    DEQ   (QNAME,RNAME,,SYSTEMS)  DEQUEUE ON VTOC
         CLOSE VTOC                    CLOSE IT
         SR    R15,R15                 INDICATE GOOD RETURN
*
         B     ALOCDD                  GO LOOP AND DO IT AGAIN     V1M0
*
RETURN   L     R13,4(R13)              LOAD CALLER'S SAVE AREA PTR
         RETURN (14,12),RC=(15)        RETURN TO OS
         EJECT
***********************************************************************
*                                                                     *
*              DEFINITIONS AND CONTROL BLOCKS                         *
*                                                                     *
***********************************************************************
*
* ENQUEUE NAMES
*
QNAME    DC    CL8'SYSVTOC'
RNAME    DC    CL6' '
         SPACE 3
*
* MISC FIELDS
*
CURRDATE DC    XL3'0'
DOUBLE   DC    D'0'
SAVEAREA DC    18F'0'
         SPACE 3
*
* DSCB I/O AREA AND CONTROL BLOCKS
*
DSCB     DC    XL140'00'
         READ  DECB,SF,VTOC,DSCB,'S',MF=L
         PRINT NOGEN
VTOC     DCB   DDNAME=VTOC,DSORG=PS,RECFM=U,KEYLEN=44,BLKSIZE=96,      X
               MACRF=(R,W),EODAD=CLOSE,EXLST=RDJFLIST
RDJFLIST DC    X'87',AL3(JFCBAREA)                             05/08/79
JFCBAREA DC    176X'0'
         SPACE 3
*
* PATCH AREA
*
PATCH    DC    64X'0'
         LTORG
         SPACE 3
*
* REGISTER EQUATES
*
R0       EQU   00
R1       EQU   01
R2       EQU   02
R3       EQU   03
R4       EQU   04
R5       EQU   05
R6       EQU   06
R7       EQU   07
R8       EQU   08
R9       EQU   09
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         SPACE 3
*
* DUMMY SECTIONS
*
* DUMMY DCB
         DCBD  DSORG=PS,DEVD=DA
         SPACE 3
* DUMMY DEB
         IEZDEB ,
         SPACE 3
* DUMMY DSCB
DSCB1    DSECT
         IECSDSL1 1
         SPACE 3
* DUMMY JFCB
JFCB     DSECT
         IEFJFCBN ,
         END   VTOCUPD
