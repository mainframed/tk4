LOGONXIT TITLE 'LINKAGE'
LOGONXIT CSECT
         SAVE  (14,12),,LOGONXIT-P3.2-&SYSDATE-&SYSTIME SAVE REGS  P3.2
         USING LOGONXIT,R9,R10,R11,R12 ESTABLISH ADDRESSABILITY    P3.1
         LR    R2,R1               SAVE CALLER'S R1
         LR    R9,R15              LOAD FIRST BASE                 P3.1
         LA    R10,2048(,R9)       START ON SECOND BASE            P3.1
         LA    R10,2048(,R10)      LOAD SECOND BASE                P3.1
         LA    R11,2048(,R10)      START ON THIRD BASE             P3.1
         LA    R11,2048(,R11)      LOAD THIRD BASE                 P3.1
         LA    R12,2048(,R11)      START ON FOURTH BASE            P3.1
         LA    R12,2048(,R12)      LOAD FOURTH BASE                P3.1
         GETMAIN RU,LV=GOTNLEN,BNDRY=PAGE GETMAIN DYNAMIC AREA       P3
         LR    R15,R1              GET ADDRESS OF DYNAMIC AREA
         LR    R0,R1               GET ADDRESS OF DYNAMIC AREA
         LA    R1,GOTNLEN          GET LENGTH OF DYNAMIC AREA
         SLR   R4,R4               ZERO FROM ADDRESS
         SLR   R5,R5               ZERO FROM LENGTH
         MVCL  R0,R4               CLEAR DYNAMIC AREA
         ST    R13,4(,R15)         CHAIN
         ST    R15,8(,R13)           SAVEAREAS
         LR    R13,R15             POINT TO MY SAVE AREA
         USING GOTNAREA,R13        ESTABLISH ADDRESSABILITY
         TITLE 'MAIN LINE'
         BAL   R14,HOUSKEEP        GO DO HOUSEKEEPING
         LTR   R15,R15             WAS HOUSEKEEPING SUCCESSFUL
         BNZ   RETURN              NO - EXIT
         BAL   R14,SCAN            YES - GO SCAN COMMAND BUFFER
         LTR   R15,R15             WAS SCAN SUCCESSFUL
         BNZ   RETURN              NO - EXIT
         BAL   R14,PARSE           YES - GO PARSE COMMAND BUFFER
         LTR   R15,R15             WAS PARSE SUCCESSFUL
         BNZ   RETURN              NO - EXIT
         BAL   R14,GETDATA         YES - GO GET DATA FROM PDL
         LTR   R15,R15             WAS DATA RETRIVAL SUCCESSFUL
         BNZ   RETURN              NO - EXIT
         BAL   R14,VERDATA         YES - GO VERIFY DATA AGAINST UADS
         LTR   R15,R15             WAS DATA VERIFICATION SUCCESSFUL
         BNZ   RETURN              NO - EXIT
         BAL   R14,GETINFO         YES - GO COMPLETE INFO            P3
         LTR   R15,R15             WAS INFO COMPLETION SUCCESSFUL    P3
         BNZ   RETURN              NO - EXIT                         P3
         BAL   R14,VERINFO         YES - GO VERIFY INFO              P3
         LTR   R15,R15             WAS INFO VERIFICATION SUCCESSFUL  P3
         BNZ   RETURN              NO - EXIT                         P3
         BAL   R14,UPDINFO         YES - GO UPDATE INFO              P3
         LTR   R15,R15             WAS INFO UPDATE SUCCESSFUL        P3
         BNZ   RETURN              NO - EXIT                         P3
         BAL   R14,UPDDATA         YES - GO UPDATE DATA              P3
         LTR   R15,R15             WAS DATA UPDATE SUCCESSFUL        P3
         BNZ   RETURN              NO - EXIT                         P3
         BAL   R14,BUILDJCL        YES - GO BUILD JCL                P3
         LTR   R15,R15             WAS JCL BUILD SUCCESSFUL          P3
         BNZ   RETURN              NO - EXIT                         P3
         BAL   R14,LISTINFO        YES - GO LIST INFO IF REQUESTED   P3
         LTR   R15,R15             WAS INFO LIST SUCCESSFUL          P3
         BNZ   RETURN              NO - EXIT                         P3
         TITLE 'RETURN TO CALLER'
*********************************************************************P3
*                                                                    P3
* ROUTINE NAME -                                                     P3
*    RETURN                                                          P3
*                                                                    P3
* FUNCTION -                                                         P3
*    PERFORMS CLEANUP AND RETURNS TO CALLER.                         P3
*                                                                    P3
* OPERATION -                                                        P3
*    1) CANCEL STIMER.                                               P3
*    2) CANCEL STAX.                                                 P3
*    3) IF DEBUG REQUESTED, CALL DEBUG ROUTINE.                      P3
*    4) IF UADS WAS OPENED, CLOSE AND FREE BUFFER.                   P3
*    5) IF ANYTHING WAS ENQ'D ON, DEQ IT                             P3
*    6) IF LOGON IS TO BE DENIED, SET DISCONNECT INDICATOR AND WAIT  P3
*       5 SECONDS IF THIS IS A DISPLAY TERMINAL TO GIVE THE USER     P3
*       TIME TO READ THE DIAGNOSTICS.                                P3
*    7) IF ATTENTION WAS HIT, GO CHECK NEW COMMAND.                  P3
*    8) IF RECONNECT WAS SPECIFIED, LEAVE NOW AS PER AZ06786.        P3
*    9) IF LOGON IS TO BE ALLOWED, INDICATE DONT PROMPT AND UPDATE   P3
*       DOPE VECTORS.                                                P3
*   10) CANCEL ESTAE.                                                P3
*   11) RETURN TO CALLER.                                            P3
*                                                                    P3
*                                                                    P3
* ENTRY POINTS -                                                     P3
*    RETURN                                                          P3
*                                                                    P3
* EXTERNAL REFERENCES -                                              P3
*    NONE                                                            P3
*                                                                    P3
* INPUT -                                                            P3
*    NONE                                                            P3
*                                                                    P3
* OUTPUT -                                                           P3
*    NONE                                                            P3
*                                                                    P3
* MESSAGES -                                                         P3
*    NONE                                                            P3
*                                                                    P3
*********************************************************************P3
         EJECT                                                       P3
RETURN   DS    0H                  RETURN TO CALLER
         LR    R2,R15              PROTECT RETURN CODE
         EJECT                                                       P3
*********************************************************************P3
*                                                                    P3
*       CANCEL STIMER.                                               P3
*                                                                    P3
*********************************************************************P3
         TTIMER CANCEL             CANCEL STIMER                     P3
         EJECT                                                       P3
*********************************************************************P3
*                                                                    P3
*       CANCEL STAX.                                                 P3
*                                                                    P3
*********************************************************************P3
         STAX  0                   CANCEL STAX                       P3
         STAX  0                   CANCEL STAX                       P3
         EJECT                                                       P3
*********************************************************************P3
*                                                                    P3
*       IF DEBUG REQUESTED, CALL DEBUG ROUTINE.                      P3
*                                                                    P3
*********************************************************************P3
         TM    GBITS1,GDEBUG       WAS DEBUG REQUESTED               P3
         BZ    RTRNDBUG            NO - CONTINUE
         BAL   R14,DEBUG           YES - GO TO DEBUG ROUTINE
RTRNDBUG DS    0H
         EJECT                                                       P3
*********************************************************************P3
*                                                                    P3
*       IF UADS WAS OPENED, CLOSE AND FREE BUFFER.                   P3
*                                                                    P3
*********************************************************************P3
         TM    GSYSUADS+DCBOFLGS-IHADCB,DCBOFOPN DID UADS OPEN
         BZ    RTRNNOPN            NO - NO NEED TO DO FREEMAIN
         LH    R0,GSYSUADS+DCBBLKSI-IHADCB YES - GET BLKSIZE
         MH    R0,HALF12           MULTIPLY BY 12                  P3.1
         ICM   R1,B'1111',ADDRINFO GET ADDRESS OF BUFFER             P3
         BZ    RTRNNOPN            IF ZERO - SKIP FREEMAIN           P3
         FREEMAIN R,LV=(0),A=(1)   FREE AREA FOR SYS1.UADS BUFFER
         CLOSE MF=(E,GOPENLST)     CLOSE SYS1.UADS
RTRNNOPN DS    0H
         EJECT                                                       P3
*********************************************************************P3
*                                                                    P3
*       IF ANYTHING WAS ENQ'D ON, DEQ IT                             P3
*                                                                    P3
*********************************************************************P3
         TM    GBITS2,GSPFENQ0     WAS MEMBER0 ENQ'D ON              P3
         BZ    RTRNDEQ0            NO - GO CONTINUE                  P3
         MVC   GENQLIST,ENQLIST2   YES - COPY ENQ LIST               P3
         MVI   GENQLIST+1,52       PUT LENGTH OF RNAME IN ENQ LIST   P3
         MVC   GRNAME(44),GJFCB    MOVE DSNAME TO RNAME              P3
         LH    R15,LUSERID         GET LENGTH OF USERID              P3
         LA    R1,GMEMBER(R15)     POINT TO BYTE AFTER USERID        P3
         MVI   0(R1),C'0'          ADD SUFFIX TO MAKE MEMBER0        P3
         MVC   GRNAME+44(8),GMEMBER APPEND MEMBER0 NAME TO DSN       P3
         LA    R15,QNAME2          GET ADDRESS OF QNAME (SPFDSN  )   P3
         ST    R15,GENQLIST+4      PUT IN ENQ LIST                   P3
         LA    R15,GRNAME          GET ADDR OF RNAME (DSN+MEMBER0)   P3
         ST    R15,GENQLIST+8      PUT IN ENQ LIST                   P3
         LA    R1,GENQLIST         GET ADDRESS OF ENQ LIST           P3
         DEQ   MF=(E,(1))          DEQ LIKE SPF DOES                 P3
         NI    GBITS2,255-GSPFENQ0 TURN OFF MEMBER0 ENQ'D ON         P3
RTRNDEQ0 DS    0H                                                    P3
         TM    GBITS2,GSPFENQI     WAS INFO BLOCK ENQ'D ON           P3
         BZ    RTRNDEQI            NO - CONTINUE                     P3
         MVC   GENQLIST,ENQLIST2   YES - COPY ENQ LIST               P3
         MVI   GENQLIST+1,52       PUT LENGTH OF RNAME IN ENQ LIST   P3
         MVC   GRNAME(44),GJFCB    MOVE DSNAME TO RNAME              P3
         MVI   GRNAME+44,C'$'      INSERT '$' AS START OF MEMBER     P3
         MVC   GRNAME+45(7),GUSERID APPEND INFO BLOCK NAME TO DSN    P3
         LA    R15,QNAME2          GET ADDRESS OF QNAME (SPFDSN  )   P3
         ST    R15,GENQLIST+4      PUT IN ENQ LIST                   P3
         LA    R15,GRNAME          GET ADDR OF RNAME (DSN+$USERID)   P3
         ST    R15,GENQLIST+8      PUT IN ENQ LIST                   P3
         LA    R1,GENQLIST         GET ADDRESS OF ENQ LIST           P3
         DEQ   MF=(E,(1))          DEQ LIKE SPF DOES                 P3
         NI    GBITS2,255-GSPFENQI TURN OFF INFO BLOCK ENQ'D ON      P3
RTRNDEQI DS    0H                                                    P3
         TM    GBITS2,GSPFRES      WAS DSN RESERVED                  P3
         BZ    RTRNRES             NO - CONTINUE                     P3
         MVC   GENQLIST,RESLIST    YES - COPY RESERVE LIST           P3
         MVI   GENQLIST+1,44       PUT LENGTH OF RNAME IN RES LIST   P3
         MVC   GRNAME(44),GJFCB    MOVE DSNAME TO RNAME              P3
         LA    R15,QNAME2          GET ADDRESS OF QNAME (SPFDSN  )   P3
         ST    R15,GENQLIST+4      PUT IN RESERVE LIST               P3
         LA    R15,GRNAME          GET ADDR OF RNAME (DSN)           P3
         ST    R15,GENQLIST+8      PUT IN RESERVE LIST               P3
         LA    R15,ADDRUCB         GET ADDRESS OF UCB ADDRESS        P3
         ST    R15,GENQLIST+12     PUT IN RESERVE LIST               P3
         LA    R1,GENQLIST         GET ADDRESS OF RESERVE LIST       P3
         DEQ   MF=(E,(1))          DEQ LIKE SPF DOES                 P3
         NI    GBITS2,255-GSPFRES  TURN OFF DSN RESERVED             P3
RTRNRES  DS    0H                                                    P3
         EJECT                                                       P3
*********************************************************************P3
*                                                                    P3
*       IF LOGON IS TO BE DENIED, SET DISCONNECT INDICATOR AND WAIT  P3
*       5 SECONDS IF THIS IS A DISPLAY TERMINAL TO GIVE THE USER     P3
*       TIME TO READ THE DIAGNOSTICS.                                P3
*                                                                    P3
*********************************************************************P3
         L     R1,ADDRPARM         RESTORE CALLER'S R1
         TM    GBITS1,GDENY        IS LOGON TO BE DENIED             P3
         BNO   RTRNOK              NO - CONTINUE
         L     R15,0(,R1)          YES - GET ADDRESS OF CONTROL SW DESC
         L     R15,0(,R15)         GET ADDRESS OF CONTROL SWITCHES
         OI    0(R15),KBIT3        INDICATE DISCONNECT
         GTSIZE ,                  DETERMINE TERMINAL TYPE           P3
         LTR   R0,R0               WAS THIS A DISPLAY                P3
         BZ    RTRNEND             NO - CONTINUE                     P3
         STIMER WAIT,DINTVL=WAITTIME YES - WAIT A LITTLE WHILE FIRST
         B     RTRNEND             THEN CONTINUE                     P3
RTRNOK   DS    0H
         EJECT                                                       P3
*********************************************************************P3
*                                                                    P3
*       IF ATTENTION WAS HIT, GO CHECK NEW COMMAND.                  P3
*                                                                    P3
*********************************************************************P3
         TM    GBITS1,GATTNHIT     WAS ATTENTION HIT                 P3
         BO    RTRNCHEK            YES - GO CHECK NEW COMMAND        P3
         EJECT                                                       P3
*********************************************************************P3
*                                                                    P3
*       IF RECONNECT WAS SPECIFIED, LEAVE NOW AS PER AZ06786.        P3
*                                                                    P3
*********************************************************************P3
         TM    GBITS1,GRECON       WAS 'RECONNECT' SPECIFIED         P3
         BO    RTRNEND             YES - LEAVE NOW AS PER AZ06786    P3
         EJECT                                                       P3
*********************************************************************P3
*                                                                    P3
*       IF LOGON IS TO BE ALLOWED, INDICATE DONT PROMPT AND UPDATE   P3
*       DOPE VECTORS.                                                P3
*                                                                    P3
*********************************************************************P3
         L     R15,0(,R1)          GET ADDRESS OF CONTROL SW DESC
         L     R15,0(,R15)         GET ADDRESS OF CONTROL SWITCHES
         OI    0(R15),KBIT4        INDICATE DONT PROMPT
         L     R15,8(,R1)          GET ADDRESS OF USERID DESCRIPTOR
         MVC   6(2,R15),LUSERID    SET LENGTH OF USERID
         L     R15,0(,R15)         GET ADDRESS OF USERID
         MVC   0(7,R15),GUSERID    MOVE USERID TO DATA AREA
         L     R15,12(,R1)         GET ADDRESS OF PASSWORD DESCRIPTOR
         MVI   7(R15),8            SET LENGTH OF PASSWORD
         L     R15,0(,R15)         GET ADDRESS OF PASSWORD
         MVC   0(8,R15),GPASS      MOVE PASSWORD TO DATA AREA
         L     R15,16(,R1)         GET ADDRESS OF ACCT NUM DESCRIPTOR
         MVI   7(R15),8            SET LENGTH OF ACCOUNT NUMBER
         L     R15,0(,R15)         GET ADDRESS OF ACCOUNT NUMBER
         MVC   0(8,R15),GACCTNUM   MOVE ACCOUNT NUMBER TO DATA AREA
         L     R15,20(,R1)         GET ADDRESS OF PROC NAME DESCRIPTOR
         MVI   7(R15),8            SET LENGTH OF PROC NAME
         L     R15,0(,R15)         GET ADDRESS OF PROC NAME
         MVC   0(8,R15),GPROC      MOVE PROC NAME TO DATA AREA
         L     R15,52(,R1)         GET ADDRESS OF ECT DESCRIPTOR   P3.2
         L     R15,0(,R15)         GET ADDRESS OF ECT              P3.2
         OC    ECTSWS-ECT(1,R15),GECTSWS SET ECT SWITCHES          P3.2
         EJECT                                                       P3
*********************************************************************P3
*                                                                    P3
*       CANCEL ESTAE.                                                P3
*                                                                    P3
*********************************************************************P3
RTRNEND  DS    0H                                                    P3
         ESTAE 0                   CANCEL ESTAE
         EJECT                                                       P3
*********************************************************************P3
*                                                                    P3
*       RETURN TO CALLER.                                            P3
*                                                                    P3
*********************************************************************P3
         LR    R1,R13              GET POINTER TO DYNAMIC AREA       P3
         L     R13,4(,R13)         UNCHAIN SAVE AREAS
         FREEMAIN R,LV=GOTNLEN,A=(1) FREE DYNAMIC AREA
         LR    R15,R2              RESTORE RETURN CODE
         RETURN (14,12),RC=(15)    RETURN TO CALLER
         EJECT                                                       P3
*********************************************************************P3
*                                                                    P3
*       IF COMMAND WAS NOT 'LOGON' DENY LOGON.  IF COMMAND WAS       P3
*       'LOGON', REENTER LOGON EXIT WITH NEW COMMAND.                P3
*                                                                    P3
*********************************************************************P3
RTRNCHEK DS    0H                                                    P3
         BAL   R14,SCAN            CALL SCAN TO SEE IF 'LOGON'       P3
         LTR   R15,R15             WAS THE NEW COMMAND 'LOGON'       P3
         BZ    RTRNRENT            YES - REENTER                     P3
         L     R1,ADDRPARM         NO - RESTORE CALLER'S R1          P3
         L     R15,0(,R1)          GET ADDRESS OF CONTROL SW DESC    P3
         L     R15,0(,R15)         GET ADDRESS OF CONTROL SWITCHES   P3
         OI    0(R15),KBIT3        INDICATE DISCONNECT               P3
         B     RTRNEND             RETURN                            P3
RTRNRENT DS    0H                                                    P3
         ESTAE 0                   CANCEL ESTAE                      P3
         LR    R1,R13              GET POINTER TO DYNAMIC AREA       P3
         L     R13,4(,R13)         UNCHAIN SAVE AREAS                P3
         FREEMAIN R,LV=GOTNLEN,A=(1) FREE DYNAMIC AREA               P3
         LM    R14,R12,12(R13)     RESTORE CALLER'S REGISTERS        P3
         BR    R15                 REENTER                           P3
         TITLE 'HOUSEKEEPING ROUTINE'
***********************************************************************
*                                                                     *
* ROUTINE NAME -                                                      *
*    HOUSKEEP                                                         *
*                                                                     *
* FUNCTION -                                                          *
*    PERFORMS HOUSEKEEPING FUNCTIONS FOR LOGONXIT.                    *
*                                                                     *
* OPERATION -                                                         *
*    1) ISSUES ESTAE.  IF ESTAE IS UNSUCCESSFUL, TELLS TSO USER AND   *
*       DENIES LOGON.                                                 *
*    2) MOVES STIMER EXIT TO DYNAMIC AREA AND ISSUES STIMER.         P3
*    3) ISSUES STAX.  IF STAX IS UNSUCCESSFUL, TELLS TSO USER AND    P3
*       DENIES LOGON.                                                P3
*    4) CHECKS TO SEE IF THIS IS 'USERID ENQ FAIL'.  IF SO, TELLS TSO *
*       USER AND DENIES LOGON.                                        *
*    5) CHECKS TO SEE IF THIS IS 'RESOURCE FAILURE'.  IF SO, TELLS TSO*
*       USER AND DENIES LOGON.                                        *
*    6) CHECKS TO SEE IF THIS IS 'ABEND'.  IF SO, TELLS TSO USER AND  *
*       DENIES LOGON.                                                 *
*    7) GET APF AUTHORIZED AND GET TERMINAL ID FROM TSB.             P3
*    8) ATTEMPTS TO OPEN SYS1.UADS.  IF OPEN IS UNSUCCESSFUL, TELLS   *
*       TSO USER AND DENIES LOGON.                                    *
*    9) GETS STORAGE FOR UADS DATA BLOCKS.                            *
*   10) READS $SYSTEM$ BLOCK IF PRESENT.                           P3.1
*   11) BUILDS CBUF AND CPPL.                                         *
*   12) CLEAR TERMINAL BUFFERS.                                      P3
*   13) IF DISPLAY TERMINAL, CLEAR THE SCREEN.                       P3
*   14) RETURNS TO THE MAINLINE.                                      *
*                                                                     *
* ENTRY POINTS -                                                      *
*    HOUSKEEP                                                         *
*                                                                     *
* EXTERNAL REFERENCES -                                               *
*    GETAUTH                                                          *
*                                                                     *
* INPUT -                                                             *
*    NONE                                                             *
*                                                                     *
* OUTPUT -                                                            *
*    NONE                                                             *
*                                                                     *
* MESSAGES -                                                          *
*    'ESTAE UNSUCCESSFUL - RC=NNNN'                                   *
*    'STAX UNSUCCESSFUL - RC=NNNN'                                   P3
*    'USERID CURRENTLY LOGGED ON'                                     *
*    'LOGON RESOURCE FAILURE. CONTACT MEMPHIS TECHNICAL SUPPORT'   P3.2
*    'LOGON ABEND. CONTACT MEMPHIS TECHNICAL SUPPORT'                 *
*    'OPEN OF SYS1.UADS UNSUCCESSFUL'                                 *
*    '$SYSTEM$ NOT PRESENT. CONTACT MEMPHIS TECHNICAL SUPPORT'     P3.1
*                                                                     *
***********************************************************************
         EJECT
HOUSKEEP DS    0H
         ST    R14,HSKPSAVE        SAVE RETURN ADDRESS
         ST    R2,ADDRPARM         SAVE ADDRESS OF PARMS
         EJECT
***********************************************************************
*                                                                     *
*       ISSUES ESTAE.  IF ESTAE IS UNSUCCESSFUL, TELLS TSO USER AND   *
*       DENIES LOGON.                                                 *
*                                                                     *
***********************************************************************
         MVC   GESTAE(LGESTAE),LESTAE COPY ESTAE PARM LIST
         LA    R3,ESTAEXIT         GET ADDRESS OF ESTAE EXIT
         LA    R4,ESTAEPRM         GET ADDRESS OF ESTAE PARMS
         ST    R9,ESTAEPRM         SAVE 1ST BASE FOR EXIT ROUTINES P3.1
         ST    R10,ESTAEPRM+4      SAVE 2ND BASE FOR EXIT ROUTINES P3.1
         ST    R11,ESTAEPRM+8      SAVE 3RD BASE FOR EXIT ROUTINES P3.1
         ST    R12,ESTAEPRM+12     SAVE 4TH BASE FOR EXIT ROUTINES P3.1
         ST    R13,ESTAEPRM+16     SAVE DYNAMIC BASE FOR EXIT RTNS P3.1
         ESTAE (3),PARAM=(4),MF=(E,GESTAE) ESTABLISH ESTAE
         LTR   R15,R15             WAS ESTAE SUCCESSFUL
         BNZ   HSKPNSTA            NO - GO GIVE MESSAGE
         EJECT
*********************************************************************P3
*                                                                    P3
*       MOVES STIMER EXIT TO DYNAMIC AREA AND ISSUES STIMER.         P3
*                                                                    P3
*********************************************************************P3
         MVC   GTIMRXIT,TIMERXIT   MOVE STIMER EXIT TO DYNAMIC AREA  P3
         STIMER REAL,GTIMRXIT,DINTVL=LOGTIME ISSUE STIMER            P3
         MVC   GTIMRWTO,TIMERWTO   COPY WTO TEXT TO DYNAMIC AREA     P3
         EJECT                                                       P3
*********************************************************************P3
*                                                                    P3
*       ISSUES STAX.  IF STAX IS UNSUCCESSFUL, TELLS TSO USER AND    P3
*       DENIES LOGON.                                                P3
*                                                                    P3
*********************************************************************P3
         XC    GDSTAX(LSTAX),GDSTAX CLEAR DUMMY STAX PARM LIST       P3
         LA    R1,GDSTAX           GET ADDRESS OF DUMMY STAX PRM LST P3
         USING STX,R1              ESTABLISH ADDRESSABILITY          P3
         LA    R15,STAXDXIT        GET ADDRESS OF DUMMY STAX EXIT    P3
         ST    R15,STXEXIT         PUT IN STAX PARAMETER LIST        P3
         LA    R15,ESTAEPRM        GET ADDRESS OF ESTAE PARAM LIST   P3
         ST    R15,STXOPTS         PUT IN STAX PARAMETER LIST        P3
         DROP  R1                  DROP STAX PRM LIST ADDRESSABILITY P3
         STAX  MF=(E,(1))          ISSUE FIRST STAX                  P3
         LTR   R15,R15             WAS STAX SUCCESSFUL               P3
         BNZ   HSKPNSTX            NO - GO GIVE MESSAGE              P3
         XC    GSTAX(LSTAX),GSTAX  YES - CLEAR STAX PARM LIST        P3
         LA    R1,GSTAX            GET ADDRESS OF STAX PARAM LIST    P3
         USING STX,R1              ESTABLISH ADDRESSABILITY          P3
         LA    R15,STAXEXIT        GET ADDRESS OF STAX EXIT          P3
         ST    R15,STXEXIT         PUT IN STAX PARAMETER LIST        P3
         L     R2,ADDRPARM         GET ADDRESS OF ORIGINAL PARMS     P3
         L     R2,4(,R2)           GET ADDRESS OF INPUT BUFFER DESC  P3
         LH    R15,4(,R2)          GET MAX LEN OF INPUT BUFFER AREA  P3
         STH   R15,STXISIZ         PUT IN STAX PARAMETER LIST        P3
         LA    R15,L'STAXOBUF      GET LENGTH OF STAX MESSAGE        P3
         STH   R15,STXOSIZ         PUT IN STAX PARAMETER LIST        P3
         LA    R15,STAXOBUF        GET ADDRESS OF STAX MESSAGE       P3
         ST    R15,STXOBUF         PUT IN STAX PARAMETER LIST        P3
         L     R15,0(,R2)          GET ADDRESS OF ORIGINAL BUFF AREA P3
         ST    R15,STXIBUF         PUT IN STAX PARAMETER LIST        P3
         LA    R15,ESTAEPRM        GET ADDRESS OF ESTAE PARAM LIST   P3
         ST    R15,STXOPTS         PUT IN STAX PARAMETER LIST        P3
         MVI   GSTAX+STXOPTS-STX,KBIT1 INDICATE REPLACE=NO           P3
         DROP  R1                  DROP STAX PRM LIST ADDRESSABILITY P3
         STAX  MF=(E,(1))          ESTABLISH STAX                    P3
         LTR   R15,R15             WAS STAX SUCCESSFUL               P3
         BNZ   HSKPNSTX            NO - GO GIVE MESSAGE              P3
         EJECT                                                       P3
***********************************************************************
*                                                                     *
*       CHECKS TO SEE IF THIS IS 'USERID ENQ FAIL'.  IF SO, TELLS TSO *
*       USER AND DENIES LOGON.                                        *
*                                                                     *
***********************************************************************
         L     R2,ADDRPARM         GET CALLER'S R1
         L     R15,0(,R2)          GET ADDRESS OF CONTROL SWITCH DESC.
         L     R15,0(,R15)         GET ADDRESS OF CONTROL SWITCH
         TM    0(R15),KBIT0        WAS THIS 'USERID ENQ FAIL'
         BO    HSKPDUPU            YES - GO GIVE MESSAGE
         EJECT
***********************************************************************
*                                                                     *
*       CHECKS TO SEE IF THIS IS 'RESOURCE FAILURE'.  IF SO, TELLS TSO*
*       USER AND DENIES LOGON.                                        *
*                                                                     *
***********************************************************************
         TM    0(R15),KBIT2        WAS THIS 'RESOURCE FAILURE'
         BO    HSKPRESC            YES - GO GIVE MESSAGE
         EJECT
***********************************************************************
*                                                                     *
*       CHECKS TO SEE IF THIS IS 'ABEND'.  IF SO, TELLS TSO USER AND  *
*       DENIES LOGON.                                                 *
*                                                                     *
***********************************************************************
         TM    1(R15),KBIT6        WAS THIS 'ABEND'
         BO    HSKPABND            YES - GO GIVE MESSAGE
         EJECT
*********************************************************************P3
*                                                                    P3
*       GET APF AUTHORIZED AND GET TERMINAL ID FROM TSB.             P3
*                                                                    P3
*********************************************************************P3
         MVC   GTERMID,ABNDWTO     ASSUME 'UNKNOWN'                  P3
         BAL   R14,GETAUTH         GO GET APF AUTHORIZATION          P3
         L     R15,PSATNEW-PSA     GET ADDRESS OF MY TCB             P3
         L     R15,TCBJSCB-TCB(,R15) GET ADDRESS OF MY JSCB          P3
         TM    JSCBOPTS-IEZJSCB(R15),JSCBAUTH CAN I ISSUE MODESET    P3
         BZ    HSKPGTRM            NO - CANT GET TERMINAL FROM TSB   P3
         L     R15,PSAAOLD-PSA     YES - GET ADDRESS OF MY ASCB    P3.2
         L     R3,ASCBTSB-ASCB(,R15) GET ADDRESS OF MY TSB           P3
         MODESET MF=(E,KEYZERO)    GET KEY 0 FOR MOVE FROM TSB       P3
         MVC   GTERMID,TSBTRMID-TSB(R3) COPY TERMINAL TO DYN. AREA   P3
         MODESET MF=(E,NONZERO)    RETURN TO USER KEY                P3
HSKPGTRM DS    0H                                                    P3
         MVC   GTIMRWTO+15(8),GTERMID MOVE TERMID TO TIMER WTO       P3
         EJECT                                                       P3
***********************************************************************
*                                                                     *
*       ATTEMPTS TO OPEN SYS1.UADS.  IF OPEN IS UNSUCCESSFUL, TELLS   *
*       TSO USER AND DENIES LOGON.                                    *
*                                                                     *
***********************************************************************
         MVC   GSYSUADS,SYSUADS    MOVE SYS1.UADS DCB TO DYNAMIC AREA
         MVC   GOPENLST,OPENUPDT   MOVE OPEN LIST TO DYNAMIC AREA    P3
         LA    R15,GJFCB           GET ADDRESS OF JFCB AREA          P3
         ST    R15,EXLST           PUT IN EXLST                      P3
         MVI   EXLST,X'87'         INDICATE JFCB ADDRESS             P3
         LA    R15,EXLST           GET ADDRESS OF EXLST              P3
         LA    R1,GSYSUADS         GET ADDRESS OF SYSUADS DCB
         USING IHADCB,R1           GET DCB ADDRESSABILITY            P3
         STCM  R15,B'0111',DCBEXLSA PUT ADDRRESS OF EXLST IN DCB     P3
         STCM  R1,B'0111',GOPENLST+1 PUT ADDR OF DCB IN OPEN LIST    P3
         DROP  R1                  DROP DCB ADDRESSABILITY           P3
         OPEN  MF=(E,GOPENLST)     ATTEMPT TO OPEN SYS1.UADS
         TM    GSYSUADS+DCBOFLGS-IHADCB,DCBOFOPN DID UADS OPEN
         BZ    HSKPNOPN            NO - GO TO ERROR ROUTINE
         RDJFCB MF=(E,GOPENLST)    YES - READ JFCB FOR LATER ENQ     P3
         L     R15,PSATNEW-PSA     GET ADDRESS OF MY TCB             P3
         L     R15,TCBTIO-TCB(,R15) GET ADDRESS OF MY TIOT           P3
         AH    R15,GSYSUADS+DCBTIOT-IHADCB GET ADDRESS OF UADS TIOT  P3
         MVC   ADDRUCB,16(R15)     SAVE UCB ADDRESS FOR RESERVE      P3
         EJECT
***********************************************************************
*                                                                     *
*       GETS STORAGE FOR UADS DATA BLOCKS.                            *
*                                                                     *
***********************************************************************
         LH    R0,GSYSUADS+DCBBLKSI-IHADCB GET BLKSIZE
         MH    R0,HALF12           MULTIPLY BY 12                  P3.1
         GETMAIN RU,LV=(0),BNDRY=PAGE GET AREA FOR SYS1.UADS BUFFER  P3
         ST    R1,ADDRINFO         SAVE ADDRESS OF INFO BLOCK BUFFER P3
         AH    R1,GSYSUADS+DCBBLKSI-IHADCB BUMP UP ONE BLOCK         P3
         ST    R1,ADDR$SYS         SAVE ADDRESS OF $SYSTEM$ BUFFER P3.1
         AH    R1,GSYSUADS+DCBBLKSI-IHADCB BUMP UP ONE BLOCK       P3.1
         ST    R1,ADDRUBUF         SAVE ADDRESS FOR LATER
         EJECT
*******************************************************************P3.1
*                                                                  P3.1
*       READS $SYSTEM$ BLOCK IF PRESENT.                           P3.1
*                                                                  P3.1
*******************************************************************P3.1
         MVI   GBLL+1,1            SET LL IN BLDL LIST             P3.1
         MVI   GBFF+1,12           SET FF IN BLDL LIST             P3.1
         MVC   GBNAME,$SYSTEM$     MOVE $SYSTEM$ TO BLDL NAME      P3.1
         BLDL  GSYSUADS,GBLDL      SEARCH DIRECTORY FOR $SYSTEM$   P3.1
         LTR   R15,R15             WAS IT FOUND                    P3.1
         BNZ   HSKPNSYS            NO - GO TO ERROR ROUTINE        P3.1
         POINT GSYSUADS,GBTTR      YES - POINT TO $SYSTEM$ TTR     P3.1
         L     R2,ADDR$SYS         GET ADDRESS OF $SYSTEM$ BUFFER  P3.1
         READ  DECB,SF,GSYSUADS,(R2),'S',MF=E READ $SYSTEM$ BLOCK  P3.1
         CHECK DECB                WAIT ON I/O                     P3.1
         EJECT                                                     P3.1
***********************************************************************
*                                                                     *
*       BUILDS CBUF AND CPPL.                                         *
*                                                                     *
***********************************************************************
         L     R2,ADDRPARM         GET CALLER'S R1                 P3.1
         L     R15,4(,R2)          GET ADDRESS OF INPUT BUFFER DESC
         LH    R14,4(,R15)         GET LENGTH OF INPUT BUFFER
         LA    R14,4(,R14)         ADD LENGTH AND OFFSET
         STH   R14,GCBUF+CBUFLEN-CBUF PUT IN CBUF
         XC    GCBUF+CBUFOFFS-CBUF(2),GCBUF+CBUFOFFS-CBUF ZERO OFFSET
         L     R15,0(,R15)         GET ADDRESS OF INPUT BUFFER
         MVC   GCBUF+CBUFTEXT-CBUF(252),0(R15) COPY TO COMMAND BUFFER
         LA    R15,GCBUF           GET ADDRESS OF COMMAND BUFFER
         ST    R15,GCPPL+CPPLCBUF-CPPL PUT IN CPPL
         L     R15,48(,R2)         GET ADDRESS OF UPT DESC
         L     R15,0(,R15)         GET ADDRESS OF UPT
         ST    R15,GCPPL+CPPLUPT-CPPL PUT IN CPPL
         L     R15,PSATNEW-PSA     GET ADDRESS OF MY TCB             P3
         L     R15,TCBJSCB-TCB(R15) GET ADDRESS OF MY JSCB           P3
         L     R15,JSCBPSCB-IEZJSCB(,R15) GET ADDRESS OF MY PSCB     P3
         ST    R15,GCPPL+CPPLPSCB-CPPL PUT IN CPPL
         L     R15,52(,R2)         GET ADDRESS OF ECT DESC
         L     R15,0(,R15)         GET ADDRESS OF ECT
         ST    R15,GCPPL+CPPLECT-CPPL PUT IN CPPL
         EJECT                                                       P3
*********************************************************************P3
*                                                                    P3
*       GET ASID FOR LATER.                                          P3
*                                                                    P3
*********************************************************************P3
         L     R15,PSAAOLD-PSA     GET ADDRESS OF MY ASCB          P3.2
         UNPK  WORKAREA(3),ASCBASID+1-ASCB(2,R15) UNPACK             P3
         TR    WORKAREA(2),TRTABLE-240 CONVERT TO EBCDIC             P3
         MVC   GASID,WORKAREA      COPY TO DYNAMIC AREA              P3
         MVC   GTIMRWTO+76(2),GASID MOVE ASID TO TIMER WTO         P3.1
         EJECT                                                       P3
*********************************************************************P3
*                                                                    P3
*       CLEAR TERMINAL BUFFERS.                                      P3
*                                                                    P3
*********************************************************************P3
         TCLEARQ INPUT             CLEAR TERMINAL BUFFERS            P3
         EJECT                                                       P3
*********************************************************************P3
*                                                                    P3
*       IF DISPLAY TERMINAL, CLEAR THE SCREEN.                       P3
*                                                                    P3
*********************************************************************P3
         GTSIZE ,                  DETERMINE TERMINAL TYPE           P3
         LTR   R0,R0               WAS THIS A DISPLAY                P3
         BZ    HSKPCLR             NO - CONTINUE                     P3
         LA    R0,L'CLRSCRN        YES - GET LENGTH OF MESSAGE       P3
         LA    R1,CLRSCRN          GET ADDRESS OF MESSAGE            P3
         ICM   R1,B'1000',HEX03    INDICATE FULLSCR TPUT             P3
         TPUT  (1),(0),R           ISSUE TPUT TO CLEAR SCREEN        P3
         OI    GBITS2,GDISPLAY     INDICATE DISPLAY TERMINAL         P3
HSKPCLR  DS    0H                                                    P3
         EJECT
***********************************************************************
*                                                                     *
*       RETURNS TO THE MAINLINE.                                      *
*                                                                     *
***********************************************************************
HSKPEXIT DS    0H
         L     R14,HSKPSAVE        RESTORE RETURN ADDRESS
         BR    R14                 RETURN TO CALLER
         EJECT
***********************************************************************
*                                                                     *
*       INFORM USER THAT ESTAE RETURN CODE WAS NOT ZERO AND DENY      *
*       LOGON.                                                        *
*                                                                     *
***********************************************************************
HSKPNSTA DS    0H                  NON-ZERO RETURN CODE FROM ESTAE
         LR    R2,R15              PROTECT RETURN CODE
         MVC   GESTAMSG(L'ESTAEMSG),ESTAEMSG COPY MESSAGE
         CVD   R15,WORKAREA        CONVERT TO DECIMAL
         OI    WORKAREA+7,X'0F'    FIX SIGN
         UNPK  WORKAREA(5),WORKAREA+5(3) UNPACK
         MVC   GESTAMSG+24(4),WORKAREA+1 MOVE INTO MESSAGE
         LA    R0,L'ESTAEMSG       GET LENGTH OF MESSAGE
         LA    R1,GESTAMSG         POINT TO MESSAGE
         TPUT  (1),(0),R           ISSUE MESSAGE
         LR    R15,R2              RESTORE RETURN CODE
         OI    GBITS1,GDENY        DENY LOGON                        P3
         B     HSKPEXIT            EXIT
         EJECT
*********************************************************************P3
*                                                                    P3
*       INFORM USER THAT STAX RETURN CODE WAS NOT ZERO AND DENY      P3
*       LOGON.                                                       P3
*                                                                    P3
*********************************************************************P3
HSKPNSTX DS    0H                  NON-ZERO RETURN CODE FROM STAX    P3
         LR    R2,R15              PROTECT RETURN CODE               P3
         MVC   GSTAXMSG(L'STAXMSG),STAXMSG COPY MESSAGE              P3
         CVD   R15,WORKAREA        CONVERT TO DECIMAL                P3
         OI    WORKAREA+7,X'0F'    FIX SIGN                          P3
         UNPK  WORKAREA(5),WORKAREA+5(3) UNPACK                      P3
         MVC   GSTAXMSG+23(4),WORKAREA+1 MOVE INTO MESSAGE           P3
         LA    R0,L'STAXMSG        GET LENGTH OF MESSAGE             P3
         LA    R1,GSTAXMSG         POINT TO MESSAGE                  P3
         TPUT  (1),(0),R           ISSUE MESSAGE                     P3
         LR    R15,R2              RESTORE RETURN CODE               P3
         OI    GBITS1,GDENY        DENY LOGON                        P3
         B     HSKPEXIT            EXIT                              P3
         EJECT                                                       P3
***********************************************************************
*                                                                     *
*       INFORM USER THAT USERID IN USE AND DENY LOGON.                *
*                                                                     *
***********************************************************************
HSKPDUPU DS    0H
         LA    R0,L'DUPUSER        GET LENGTH OF MESSAGE
         LA    R1,DUPUSER          POINT TO MESSAGE
         TPUT  (1),(0),R           INFORM USER
         OI    GBITS1,GDENY        DENY LOGON                        P3
         LA    R15,4               SET RETURN CODE
         B     HSKPEXIT            AND EXIT
         EJECT
***********************************************************************
*                                                                     *
*       INFORM USER THAT LOGON HAD A RESOURCE FAILURE AND DENY LOGON. *
*                                                                     *
***********************************************************************
HSKPRESC DS    0H
         LA    R0,L'RESCFAIL       GET LENGTH OF MESSAGE
         LA    R1,RESCFAIL         POINT TO MESSAGE
         TPUT  (1),(0),R           INFORM USER
         OI    GBITS1,GDENY        DENY LOGON                        P3
         LA    R15,4               SET RETURN CODE
         B     HSKPEXIT            AND EXIT
         EJECT
***********************************************************************
*                                                                     *
*       INFORM USER THAT LOGON HAD AN ABEND AND DENY LOGON.           *
*                                                                     *
***********************************************************************
HSKPABND DS    0H
         LA    R0,L'LOGABEND       GET LENGTH OF MESSAGE
         LA    R1,LOGABEND         POINT TO MESSAGE
         TPUT  (1),(0),R           INFORM USER
         OI    GBITS1,GDENY        DENY LOGON                        P3
         LA    R15,4               SET RETURN CODE
         B     HSKPEXIT            AND EXIT
         EJECT
***********************************************************************
*                                                                     *
*       INFORM USER THAT OPEN FAILED AND DENY LOGON.                  *
*                                                                     *
***********************************************************************
HSKPNOPN DS    0H                  SYS1.UADS DID NOT OPEN
         LA    R0,L'OPENMSG        GET LENGTH OF MESSAGE
         LA    R1,OPENMSG          POINT TO MESSAGE
         TPUT  (1),(0),R           ISSUE MESSAGE
         LA    R15,4               SET RETURN CODE
         OI    GBITS1,GDENY        DENY LOGON                        P3
         B     HSKPEXIT            EXIT
         EJECT                                                     P3.1
*******************************************************************P3.1
*                                                                  P3.1
*       INFORM USER THAT $SYSTEM$ NOT PRESENT AND DENY LOGON.      P3.1
*                                                                  P3.1
*******************************************************************P3.1
HSKPNSYS DS    0H                  $SYSTEM$ BLOCK NOT PRESENT      P3.1
         LA    R0,L'$SYSMSG        GET LENGTH OF MESSAGE           P3.1
         LA    R1,$SYSMSG          POINT TO MESSAGE                P3.1
         TPUT  (1),(0),R           ISSUE MESSAGE                   P3.1
         LA    R15,4               SET RETURN CODE                 P3.1
         OI    GBITS1,GDENY        DENY LOGON                      P3.1
         B     HSKPEXIT            EXIT                            P3.1
         TITLE 'SCAN COMMAND BUFFER'
***********************************************************************
*                                                                     *
* ROUTINE NAME -                                                      *
*    SCAN                                                             *
*                                                                     *
* FUNCTION -                                                          *
*    SCAN INPUT BUFFER.                                               *
*                                                                     *
* OPERATION -                                                         *
*    1) BUILDS CSPL AND CALLS IKJSCAN TO SCAN INPUT BUFFER.           *
*    2) IF SCAN IS UNSUCCESSFUL, TELLS TSO USER AND DENIES LOGON.     *
*    3) IF COMMAND IS NOT 'LOGON', TELLS TSO USER AND DENIES LOGON.   *
*    4) RETURNS TO THE MAINLINE.                                      *
*                                                                     *
* ENTRY POINTS -                                                      *
*    SCAN                                                             *
*                                                                     *
* EXTERNAL REFERENCES -                                               *
*    IKJSCAN                                                          *
*                                                                     *
* INPUT -                                                             *
*    COMMAND BUFFER.                                                  *
*                                                                     *
* OUTPUT -                                                            *
*    NONE                                                             *
*                                                                     *
* MESSAGES -                                                          *
*    'IKJSCAN UNSUCCESSFUL - RC=NNNN'                                 *
*    'COMMAND IS NOT 'LOGON''                                         *
*    'SYSTEM RESOURCE FAILURE. CONTACT MEMPHIS TECHNICAL SUPPORT'  P3.2
*                                                                     *
***********************************************************************
         EJECT
SCAN     DS    0H
         ST    R14,SCANSAVE        SAVE RETURN ADDRESS
         ST    R2,SCANSAVE+4       SAVE R2
         EJECT
***********************************************************************
*                                                                     *
*       BUILDS CSPL AND CALL IKJSCAN TO SCAN INPUT BUFFER.            *
*                                                                     *
***********************************************************************
         LA    R1,GIKJCSPL         GET ADDRESS OF CSPL
         USING CSPL,R1             ESTABLISH ADDRESSABILITY TO CSPL
         MVC   CSPLUPT,GCPPL+CPPLUPT-CPPL COPY UPT ADDRESS
         MVC   CSPLECT,GCPPL+CPPLECT-CPPL COPY ECT ADDRESS
         LA    R15,GCPECB          GET ADDRESS OF CP ECB             P3
         ST    R15,CSPLECB         STORE CP ECB ADDRESS              P3
         XC    GCPECB,GCPECB       CLEAR CP ECB                      P3
         LA    R15,GCSPLFLG        GET ADDRESS OF FLAGS
         ST    R15,CSPLFLG         STORE FLAG ADDRESS
         XC    GCSPLFLG,GCSPLFLG   CLEAR FLAGS - REQUEST SYNTAX CHECK
         LA    R15,GCSPLOA         GET ADDRESS OF OUTPUT AREA
         ST    R15,CSPLOA          STORE OUTPUT AREA ADDRESS
         XC    GCSPLOA,GCSPLOA     CLEAR OUTPUT AREA
         MVC   CSPLCBUF,GCPPL+CPPLCBUF-CPPL COPY COMMAND BUFFER ADDRESS
         L     R15,CSPLCBUF        GET COMMAND BUFFER ADDRESS
         USING CBUF,R15            GET CBUF ADDRESSABILITY
         XC    CBUFOFFS,CBUFOFFS   RESET OFFSET FOR SCAN
         CALLTSSR EP=IKJSCAN,MF=(E,GIKJCSPL) CALL IKJSCAN
         EJECT
***********************************************************************
*                                                                     *
*       IF SCAN IS UNSUCCESSFUL, TELLS TSO USER AND DENIES LOGON.     *
*                                                                     *
***********************************************************************
         LTR   R15,R15             WAS SCAN SUCCESSFUL
         BNZ   SCANBSCN            NO - GO ISSUE MESSAGE
         LA    R1,GCSPLOA          YES - GET PTR TO SCAN OUTPUT AREA
         USING CSOA,R1             GET ADDRESSABILITY TO OUTPUT AREA
         EJECT
***********************************************************************
*                                                                     *
*       IF COMMAND IS NOT 'LOGON', TELLS TSO USER AND DENIES LOGON.   *
*                                                                     *
***********************************************************************
         TM    CSOAFLG,CSOANOC     IS THIS A BLANK BUFFER          P3.2
         BO    SCANSRES            YES - SYSTEM RESOURCE FAILURE   P3.2
         TM    CSOAFLG,CSOAVWP+CSOAVNP VALID SYNTAX WITH/WITHOUT PARMS
         BZ    SCANBCMD            NO - BAD COMMAND
         CLI   CSOALNM+1,5         YES - IS COMMAND LENGTH 5 BYTES
         BNE   SCANLOGF            NO - MAY BE 'LOGOFF'              P3
         L     R1,CSOACNM          YES - GET ADDRESS OF COMMAND NAME
         CLC   KLOGON,0(R1)        IS COMMAND NAME 'LOGON'
         BNE   SCANBCMD            NO - BAD COMMAND
         SLR   R15,R15             YES - SET RETURN CODE
         EJECT
***********************************************************************
*                                                                     *
*       RETURNS TO THE MAINLINE.                                      *
*                                                                     *
***********************************************************************
SCANEXIT DS    0H
         L     R14,SCANSAVE        RESTORE RETURN ADDRESS
         L     R2,SCANSAVE+4       RESTORE R2
         BR    R14                 RETURN TO CALLER
         EJECT
***********************************************************************
*                                                                     *
*       INFORM USER THAT IKJSCAN RETURN CODE WAS NOT ZERO AND DENY    *
*       LOGON.                                                        *
*                                                                     *
***********************************************************************
SCANBSCN DS    0H                  NON-ZERO RETURN CODE FROM IKJSCAN
         LR    R2,R15              PROTECT RETURN CODE
         MVC   GSCANMSG(L'SCANMSG),SCANMSG YES - COPY MESSAGE
         CVD   R15,WORKAREA        CONVERT TO DECIMAL
         OI    WORKAREA+7,X'0F'    FIX SIGN
         UNPK  WORKAREA(5),WORKAREA+5(3) UNPACK
         MVC   GSCANMSG+26(4),WORKAREA+1 MOVE INTO MESSAGE
         LA    R0,L'SCANMSG        GET LENGTH OF MESSAGE
         LA    R1,GSCANMSG         POINT TO MESSAGE
         TPUT  (1),(0),R           ISSUE MESSAGE
         LR    R15,R2              RESTORE RETURN CODE
         OI    GBITS1,GDENY        DENY LOGON                        P3
         B     SCANEXIT            EXIT
         EJECT
***********************************************************************
*                                                                     *
*       INFORM USER THAT COMMAND WAS NOT 'LOGON' AND DENY LOGON.      *
*                                                                     *
***********************************************************************
SCANBCMD DS    0H                  COMMAND IS NOT 'LOGON'
         LA    R0,L'BCMNDMSG       GET LENGTH OF MESSAGE
         LA    R1,BCMNDMSG         POINT TO MESSAGE
         TPUT  (1),(0),R           ISSUE MESSAGE
SCANBRET DS    0H                                                    P3
         LA    R15,16              SET RETURN CODE
         OI    GBITS1,GDENY        DENY LOGON                        P3
         B     SCANEXIT            EXIT
         EJECT                                                       P3
*********************************************************************P3
*                                                                    P3
*       IF COMMAND WAS 'LOGOFF', DENY LOGON WITHOUT MESSAGE TO USER. P3
*                                                                    P3
*********************************************************************P3
SCANLOGF DS    0H                                                    P3
         TM    GBITS1,GATTNHIT     YES - WAS ATTENTION HIT           P3
         BZ    SCANBCMD            NO - CONTINUE                     P3
         CLI   CSOALNM+1,6         YES - IS COMMAND LENGTH 6 BYTES   P3
         BNE   SCANBCMD            NO - BAD COMMAND                  P3
         L     R1,CSOACNM          YES - GET ADDRESS OF COMMAND NAME P3
         CLC   KLOGOFF,0(R1)       IS COMMAND NAME 'LOGOFF'          P3
         BNE   SCANBCMD            NO - BAD COMMAND                  P3
         B     SCANBRET            YES - DENY LOGON WITHOUT MESSAGE  P3
         EJECT                                                     P3.2
*******************************************************************P3.2
*                                                                  P3.2
*       INFORM USER THAT THERE HAS BEEN A SYSTEM RESOURCE FAILURE  P3.2
*       AND DENY LOGON.                                            P3.2
*                                                                  P3.2
*******************************************************************P3.2
SCANSRES DS    0H                  SYSTEM RESOURCE FAILURE         P3.2
         LA    R0,L'SRESMSG        GET LENGTH OF MESSAGE           P3.2
         LA    R1,SRESMSG          POINT TO MESSAGE                P3.2
         TPUT  (1),(0),R           ISSUE MESSAGE                   P3.2
         B     SCANBRET            DENY LOGON                      P3.2
         TITLE 'PARSE COMMAND BUFFER'
***********************************************************************
*                                                                     *
* ROUTINE NAME -                                                      *
*    PARSE                                                            *
*                                                                     *
* FUNCTION -                                                          *
*    PARSE INPUT BUFFER.                                              *
*                                                                     *
* OPERATION -                                                         *
*    1) BUILDS PPL AND CALLS IKJPARS TO PARSE INPUT BUFFER.           *
*    2) IF PARSE IS UNSUCCESSFUL AND IKJPARS ISSUED MESSAGE, DENIES   *
*       LOGON.  IF IKJPARS DID NOT ISSUE MESSAGE, CALLS GNRLFAIL TO   *
*       ISSUE MESSAGE.  IF GNRLFAIL IS UNSUCCESSFUL, TELLS TSO USER   *
*       THAT GNRLFAIL HAD A NOT-ZERO RETURN CODE. IN ALL OF THE ABOVE *
*       CASES, DENIES LOGON UNLESS ATTENTION HIT.                    P3
*    3) RETURNS TO THE MAINLINE.                                      *
*                                                                     *
* ENTRY POINTS -                                                      *
*    PARSE                                                            *
*                                                                     *
* EXTERNAL REFERENCES -                                               *
*    IKJPARS                                                          *
*    IKJEFF19 (GNRLFAIL)                                              *
*                                                                     *
* INPUT -                                                             *
*    COMMAND BUFFER.                                                  *
*                                                                     *
* OUTPUT -                                                            *
*    NONE                                                             *
*                                                                     *
* MESSAGES -                                                          *
*    'GNRLFAIL UNSUCCESSFUL - RC=NNNN'                                *
*                                                                     *
***********************************************************************
         EJECT
PARSE    DS    0H
         ST    R14,PARSSAVE        SAVE RETURN ADDRESS
         ST    R2,PARSSAVE+4       SAVE R2
         EJECT
***********************************************************************
*                                                                     *
*       BUILDS PPL AND CALLS IKJPARS TO PARSE INPUT BUFFER.           *
*                                                                     *
***********************************************************************
         LA    R1,GIKJPPL          GET ADDRESS OF PPL
         USING PPL,R1              ESTABLISH ADDRESSABILITY TO PPL
         MVC   PPLUPT,GCPPL+CPPLUPT-CPPL COPY UPT ADDRESS
         MVC   PPLECT,GCPPL+CPPLECT-CPPL COPY ECT ADDRESS
         LA    R15,GCPECB          GET ADDRESS OF CP ECB             P3
         ST    R15,PPLECB          STORE CP ECB ADDRESS              P3
         XC    GCPECB,GCPECB       CLEAR CP ECB                      P3
         MVC   PPLPCL,PCLADDR      STORE PCL ADDRESS
         LA    R15,GPPLANS         GET ADDRESS OF PDL
         ST    R15,PPLANS          STORE PDL ADDRESS
         MVC   PPLCBUF,GCPPL+CPPLCBUF-CPPL COPY COMMAND BUFFER ADDRESS
         LA    R15,ESTAEPRM        GET ADDRESS OF BASES FOR VAL CHK RTN
         ST    R15,PPLUWA          SAVE FOR VALIDITY CHK ROUTINES
         CALLTSSR EP=IKJPARS,MF=(E,GIKJPPL) CALL IKJPARS
         LTR   R15,R15             WAS PARSE SUCCESSFUL
         BZ    PARSEXIT            YES - RETURN TO CALLER
         EJECT
***********************************************************************
*                                                                     *
*       IF PARSE IS UNSUCCESSFUL AND IKJPARS ISSUED MESSAGE, DENIES   *
*       LOGON.  IF IKJPARS DID NOT ISSUE MESSAGE, CALLS GNRLFAIL TO   *
*       ISSUE MESSAGE.  IF GNRLFAIL IS UNSUCCESSFUL, TELLS TSO USER   *
*       THAT GNRLFAIL HAD A NOT-ZERO RETURN CODE. IN ALL OF THE ABOVE *
*       CASES, DENIES LOGON UNLESS ATTENTION HIT.                    P3
*                                                                     *
***********************************************************************
         TM    GBITS1,GATTNHIT     WAS ATTENTION HIT                 P3
         BO    PARSATTN            YES - DONT SET DENY BIT           P3
         OI    GBITS1,GDENY        NO - DENY LOGON                   P3
PARSATTN DS    0H                                                    P3
         LA    R14,4               GET 4 IN R14 FOR COMPARISON
         CR    R14,R15             DID PARSE ISSUE MESSAGE
         BE    PARSEXIT            YES - QUIT
         LA    R14,8               NO - GET 8 IN R14                 P3
         CR    R14,R15             DID PARSE GET AN ATTENTION KEY    P3
         BE    PARSEXIT            YES - QUIT                        P3
         LA    R14,20              NO - GET 20 IN R14
         CR    R14,R15             DID VALIDITY CHECKING ISSUE MSG
         BE    PARSEXIT            YES - QUIT
         LR    R2,R15              NO - PROTECT PARSE RETURN CODE    P3
         XC    GGFPARMS,GGFPARMS   CLEAR GNRLFAIL PARAMETER LIST
         LA    R1,GGFPARMS         POINT TO GNRLFAIL PARM LIST ADDR
         ST    R1,GGFPARMP         STORE IN GNRLFAIL PARM LIST POINTER
         USING GFDSECTD,R1         ESTABLISH ADDRESSABILITY TO GF PARMS
         ST    R15,GFRCODE         STORE PARSE RETURN CODE
         LA    R15,GFPARSE         LOAD CALLER ID FOR PARSE
         STH   R15,GFCALLID        STORE CALLER ID
         LA    R15,GCPPL           GET ADDRESS OF CPPL
         ST    R15,GFCPPLP         STORE POINTER TO CPPL
         LA    R15,GCPECB          GET ADDRESS OF CP ECB             P3
         ST    R15,GFECBP          STORE CP ECB POINTER              P3
         XC    GCPECB,GCPECB       CLEAR CP ECB                      P3
         LINK  EP=IKJEFF19,MF=(E,GGFPARMP) LINK TO GNRLFAIL
         LTR   R15,R15             WAS GNRLFAIL SUCCESSFUL
         LR    R15,R2              RESTORE PARSE RETURN CODE         P3
         BZ    PARSEXIT            YES - EXIT
         LR    R2,R15              NO - PROTECT GNRLFAIL RETURN CODE
         MVC   GGFALMSG(L'GFAILMSG),GFAILMSG COPY MESSAGE
         CVD   R15,WORKAREA        CONVERT TO DECIMAL
         OI    WORKAREA+7,X'0F'    FIX SIGN
         UNPK  WORKAREA(5),WORKAREA+5(3) UNPACK
         MVC   GGFALMSG+27(4),WORKAREA+1 MOVE INTO MESSAGE
         LA    R0,L'GFAILMSG       GET LENGTH OF MESSAGE
         LA    R1,GGFALMSG         POINT TO MESSAGE
         TPUT  (1),(0),R           ISSUE MESSAGE
         LR    R15,R2              RESTORE GNRLFAIL RETURN CODE
         EJECT
***********************************************************************
*                                                                     *
*       RETURNS TO THE MAINLINE.                                      *
*                                                                     *
***********************************************************************
PARSEXIT DS    0H
         L     R14,PARSSAVE        RESTORE R14
         L     R2,PARSSAVE+4       RESTORE R2
         BR    R14                 RETURN TO CALLER
         TITLE 'GET DATA FROM PDL ROUTINE'
***********************************************************************
*                                                                     *
* ROUTINE NAME -                                                      *
*    GETDATA                                                          *
*                                                                     *
* FUNCTION -                                                          *
*    RETRIEVE DATA RETURNED BY IKJPARS.                               *
*                                                                     *
* OPERATION -                                                         *
*    1) CHECKS TO SEE IF 'RECONNECT' WAS ENTERED.  IF SO, SETS THE    *
*       RECONNECT BIT.                                                *
*    2) CHECKS TO SEE IF 'NOMAIL' WAS ENTERED.  IF SO, SETS THE       *
*       NOMAIL BIT.                                                   *
*    3) CHECKS TO SEE IF 'NONOTICES' WAS ENTERED.  IF SO, SETS THE    *
*       NONOTICES BIT.                                                *
*    4) CHECKS TO SEE IF 'DEBUG' WAS ENTERED.  IF SO, SETS THE DEBUG  *
*       BIT.                                                          *
*    5) CHECKS TO SEE IF 'LISTINFO' WAS ENTERED.  IF SO, SETS THE    P3
*       LISTINFO BIT.                                                P3
*    6) CHECKS TO SEE IF 'JCL' WAS ENTERED.  IF SO, SETS THE JCL     P3
*       BIT.                                                         P3
*    7) CHECKS TO SEE IF 'NEWPSWD' WAS ENTERED.  IF SO, CALLS        P2
*       GETNPASS TO OBTAIN NEW PASSWORD.                             P2
*    8) CHECKS TO SEE IF 'NAME' WAS ENTERED.  IF SO, MOVES NAME FROM P3
*       PDE TO DYNAMIC AREA.                                         P3
*    9) CHECKS TO SEE IF 'PHONE' WAS ENTERED.  IF SO, MOVES PHONE    P3
*       NUMBER FROM PDE TO DYNAMIC AREA.                             P3
*   10) CHECKS TO SEE IF 'ADDRESS' WAS ENTERED.  IF SO, MOVES        P3
*       ADDRESS FROM PDE TO DYNAMIC AREA.                            P3
*   11) CHECKS TO SEE IF 'PROGNAME' WAS ENTERED.  IF SO, MOVES       P3
*       PROGRAMMER NAME FROM PDE TO DYNAMIC AREA.                    P3
*   12) CHECKS TO SEE IF 'FORMS' WAS ENTERED.  IF SO, MOVES FORMS    P3
*       FROM PDE TO DYNAMIC AREA.                                    P3
*   13) CHECKS TO SEE IF 'ROOM' WAS ENTERED.  IF SO, MOVES ROOM      P3
*       FROM PDE TO DYNAMIC AREA.                                    P3
*   14) CHECKS TO SEE IF 'TIME' WAS ENTERED.  IF SO, MOVES           P3
*       CPU TIME FROM PDE TO DYNAMIC AREA.                           P3
*   15) CHECKS TO SEE IF 'MSGCLASS' WAS ENTERED.  IF SO, MOVES       P3
*       MSGCLASS FROM PDE TO DYNAMIC AREA.                           P3
*   16) CHECKS TO SEE IF 'MSGLEVEL' WAS ENTERED.  IF SO, MOVES       P3
*       MSGLEVEL FROM PDE TO DYNAMIC AREA.                           P3
*   17) CHECKS TO SEE IF 'COPIES' WAS ENTERED.  IF SO, MOVES COPIES  P3
*       FROM PDE TO DYNAMIC AREA.                                    P3
*   18) CHECKS TO SEE IF 'LINECT' WAS ENTERED.   IF SO, MOVES LINECT P3
*       FROM PDE TO DYNAMIC AREA.                                    P3
*   19) CHECKS TO SEE IF 'DEST' WAS ENTERED.  IF SO, MOVES DEST    P3.2
*       FROM PDE TO DYNAMIC AREA.                                  P3.2
*   20) CHECKS TO SEE IF USER IS CURRENTLY LOGGED ON.  IF SO, SETS THE*
*       DUPLICATE USER BIT.                                           *
*   21) CHECKS TO ENSURE THAT THE RECONNECT AND DUPLICATE USER BITS   *
*       AGREE.  IF NOT, ISSUES MESSAGE TO THE TSO USER AND DENIES     *
*       THE LOGON.                                                    *
*   22) RELEASE STORAGE FOR PDL AND RETURN TO THE MAINLINE.           *
*                                                                     *
* ENTRY POINTS -                                                      *
*    GETDATA                                                          *
*                                                                     *
* EXTERNAL REFERENCES -                                               *
*    GETNPASS                                                         *
*                                                                     *
* INPUT -                                                             *
*    PDL RETURNED FROM IKJPARS.                                       *
*                                                                     *
* OUTPUT -                                                            *
*    RECONNECT, NOMAIL, NONOTICES AND JCL BIT(S) SET AS INDICATED.   P3
*    GNEWPSWD UPDATED IF 'NEWPSWD' SPECIFIED.  GNAME, GPHONE,        P3
*    GADDRESS, GPROGNAM, GFORMS, GROOM, GCPUTIME, GMSGCLAS,          P3
*    GMSGLEVL WILL BE UPDATED IF SPECIFIED.                          P3
*                                                                     *
* MESSAGES -                                                          *
*    NONE                                                             *
*                                                                     *
***********************************************************************
         EJECT
GETDATA  DS    0H
         ST    R14,GDATSAVE        SAVE RETURN ADDRESS
         ST    R2,GDATSAVE+4       SAVE R2
         L     R2,GPPLANS          GET ADDRESS OF PDL ADDRESS
         USING IKJPARMD,R2         GET ADDRESSABILITY TO PDL
         EJECT
***********************************************************************
*                                                                     *
*       CHECKS TO SEE IF 'RECONNECT' WAS ENTERED.  IF SO, SETS THE    *
*       RECONNECT BIT.                                                *
*                                                                     *
***********************************************************************
         CLI   PRECON+1,0          WAS 'RECONNECT' SPECIFIED
         BE    GDATMAIL            NO - GO TRY 'MAIL'
         OI    GBITS1,GRECON       YES - SET BIT ON                  P3
         B     GDATCHKU            SKIP DOWN TO CHECK FOR DUPLICATE  P3
         EJECT
***********************************************************************
*                                                                     *
*       CHECKS TO SEE IF 'NOMAIL' WAS ENTERED.  IF SO, SETS THE       *
*       NOMAIL BIT.                                                   *
*                                                                     *
***********************************************************************
GDATMAIL DS    0H
         CLI   PMAIL+1,2           WAS 'NOMAIL' SPCEIFIED
         BNE   GDATNOTS            NO - GO TRY 'NOTICES'
         OI    GECTSWS,ECTNMAL     YES - SET BIT ON                P3.2
         EJECT
***********************************************************************
*                                                                     *
*       CHECKS TO SEE IF 'NONOTICES' WAS ENTERED.  IF SO, SETS THE    *
*       NONOTICES BIT.                                                *
*                                                                     *
***********************************************************************
GDATNOTS DS    0H
         CLI   PNOTICES+1,2        WAS 'NONOTICES' SPECIFIED
         BNE   GDATDBUG            NO - GO TRY 'DEBUG'
         OI    GECTSWS,ECTNNOT     YES - SET BIT ON                P3.2
         EJECT
***********************************************************************
*                                                                     *
*       CHECKS TO SEE IF 'DEBUG' WAS ENTERED.  IF SO, SETS THE DEBUG  *
*       BIT.                                                          *
*                                                                     *
***********************************************************************
GDATDBUG DS    0H
         CLI   PDEBUG+1,0          WAS 'DEBUG' SPECIFIED             P2
         BE    GDATLIST            NO - GO TRY 'LISTINFO'            P3
         OI    GBITS1,GDEBUG       YES - SET BIT ON                  P3
         EJECT                                                       P3
*********************************************************************P3
*                                                                    P3
*       CHECKS TO SEE IF 'LISTINFO' WAS ENTERED.  IF SO, SETS THE    P3
*       LISTINFO BIT.                                                P3
*                                                                    P3
*********************************************************************P3
GDATLIST DS    0H                                                    P3
         CLI   PLIST+1,0           WAS 'LISTINOF' SPECIFIED          P3
         BE    GDATJCL             NO - GO TRY 'JCL'                 P3
         OI    GBITS1,GLIST        YES - SET BIT ON                  P3
         EJECT                                                       P3
*********************************************************************P3
*                                                                    P3
*       CHECKS TO SEE IF 'JCL' WAS ENTERED.  IF SO, SETS THE JCL     P3
*       BIT.                                                         P3
*                                                                    P3
*********************************************************************P3
GDATJCL  DS    0H                                                    P3
         CLI   PJCL+1,0            WAS 'JCL' SPECIFIED               P3
         BE    GDATCHKN            NO - GO TRY 'NEWPSWD'             P3
         OI    GBITS2,GJCL         YES - SET BIT ON                  P3
         EJECT                                                       P2
*********************************************************************P2
*                                                                    P2
*       CHECKS TO SEE IF 'NEWPSWD' WAS ENTERED.  IF SO, CALLS        P2
*       GETNPASS TO OBTAIN NEW PASSWORD.                             P2
*                                                                    P2
*********************************************************************P2
GDATCHKN DS    0H                                                    P2
         CLI   PNEWPSWD+1,0        WAS 'NEWPSWD' SPECIFIED'          P2
         BE    GDATGNPA            NO - CONTINUE                     P3
         BAL   R14,GETNPASS        YES - CALL GETNPASS ROUTINE       P2
         LTR   R15,R15             WAS RETURN CODE ZERO              P3
         BNZ   GDATEXIT            NO - EXIT                         P3
GDATGNPA DS    0H                                                    P3
         EJECT                                                       P3
*********************************************************************P3
*                                                                    P3
*       CHECKS TO SEE IF 'NAME' WAS ENTERED.  IF SO, MOVES NAME FROM P3
*       PDE TO DYNAMIC AREA.                                         P3
*                                                                    P3
*********************************************************************P3
         CLI   PNAME+1,0           WAS 'NAME' SPECIFIED              P3
         BE    GDATGNAM            NO - CONTINUE                     P3
         MVC   GNAME,BLANKS        YES - BLANK NAME                  P3
         L     R1,SNAME            GET ADDRESS OF NAME               P3
         LH    R15,SNAME+4         GET LENGTH OF NAME                P3
         BCTR  R15,0               DECREMENT FOR EXECUTE             P3
         EX    R15,MVCNAME         MOVE NAME TO DYNAMIC AREA         P3
GDATGNAM DS    0H                                                    P3
         EJECT                                                       P3
*********************************************************************P3
*                                                                    P3
*       CHECKS TO SEE IF 'PHONE' WAS ENTERED.  IF SO, MOVES PHONE    P3
*       NUMBER FROM PDE TO DYNAMIC AREA.                             P3
*                                                                    P3
*********************************************************************P3
         CLI   PPHONE+1,0          WAS 'PHONE' SPECIFIED             P3
         BE    GDATGPHO            NO - CONTINUE                     P3
         MVC   GPHONE,BLANKS       YES - BLANK PHONE NUMBER          P3
         L     R1,SPHONE           GET ADDRESS OF PHONE NUMBER       P3
         LH    R15,SPHONE+4        GET LENGTH OF PHONE NUMBER        P3
         BCTR  R15,0               DECREMENT FOR EXECUTE             P3
         EX    R15,MVCPHONE        MOVE PHONE TO DYNAMIC AREA        P3
GDATGPHO DS    0H                                                    P3
         EJECT                                                       P3
*********************************************************************P3
*                                                                    P3
*       CHECKS TO SEE IF 'ADDRESS' WAS ENTERED.  IF SO, MOVES        P3
*       ADDRESS FROM PDE TO DYNAMIC AREA.                            P3
*                                                                    P3
*********************************************************************P3
         CLI   PADDRESS+1,0        WAS 'ADDRESS' SPECIFIED           P3
         BE    GDATGADR            NO - CONTINUE                     P3
         MVC   GADDRESS,BLANKS     YES - BLANK ADDRESS               P3
         L     R1,SADDRESS         GET ADDRESS OF ADDRESS            P3
         LH    R15,SADDRESS+4      GET LENGTH OF ADDRESS             P3
         BCTR  R15,0               DECREMENT FOR EXECUTE             P3
         EX    R15,MVCADDR         MOVE ADDRESS TO DYNAMIC AREA      P3
GDATGADR DS    0H                                                    P3
         EJECT                                                       P3
*********************************************************************P3
*                                                                    P3
*       CHECKS TO SEE IF 'PROGNAME' WAS ENTERED.  IF SO, MOVES       P3
*       PROGRAMMER NAME FROM PDE TO DYNAMIC AREA.                    P3
*                                                                    P3
*********************************************************************P3
         CLI   PPROGNAM+1,0        WAS 'PROGNAME' SPECIFIED          P3
         BE    GDATGPNM            NO - CONTINUE                     P3
         MVC   GPROGNAM,BLANKS     YES - BLANK PROGRAMMER NAME       P3
         L     R1,SPROGNAM         GET ADDRESS OF PROGRAMMER NAME    P3
         LH    R15,SPROGNAM+4      GET LENGTH OF PROGRAMMER NAME     P3
         BCTR  R15,0               DECREMENT FOR EXECUTE             P3
         EX    R15,MVCPNAM         MOVE PROG NAME TO DYNAMIC AREA    P3
         CLC   KNULL,GPROGNAM      WAS PROGRAMMER NAME 'NULL'        P3
         BNE   GDATGPNM            NO - CONTINUE                     P3
         MVC   GPROGNAM,BLANKS     YES - BLANK PROGRAMMER NAME       P3
GDATGPNM DS    0H                                                    P3
         EJECT                                                       P3
*********************************************************************P3
*                                                                    P3
*       CHECKS TO SEE IF 'FORMS' WAS ENTERED.  IF SO, MOVES FORMS    P3
*       FROM PDE TO DYNAMIC AREA.                                    P3
*                                                                    P3
*********************************************************************P3
         CLI   PFORMS+1,0          WAS 'FORMS' SPECIFIED             P3
         BE    GDATGFOR            NO - CONTINUE                     P3
         MVC   GFORMS,BLANKS       YES - BLANK FORMS                 P3
         L     R1,SFORMS           GET ADDRESS OF FORMS              P3
         LH    R15,SFORMS+4        GET LENGTH OF FORMS               P3
         BCTR  R15,0               DECREMENT FOR EXECUTE             P3
         EX    R15,MVCFORMS        MOVE FORMS TO DYNAMIC AREA        P3
         CLC   KNULL,GFORMS        WAS FORMS 'NULL'                  P3
         BNE   GDATGFOR            NO - CONTINUE                     P3
         MVC   GFORMS,BLANKS       YES - BLANK FORMS                 P3
GDATGFOR DS    0H                                                    P3
         EJECT                                                       P3
*********************************************************************P3
*                                                                    P3
*       CHECKS TO SEE IF 'ROOM' WAS ENTERED.  IF SO, MOVES ROOM      P3
*       FROM PDE TO DYNAMIC AREA.                                    P3
*                                                                    P3
*********************************************************************P3
         CLI   PROOM+1,0           WAS 'ROOM' SPECIFIED              P3
         BE    GDATGROM            NO - CONTINUE                     P3
         MVC   GROOM,BLANKS        YES - BLANK ROOM                  P3
         L     R1,SROOM            GET ADDRESS OF ROOM               P3
         LH    R15,SROOM+4         GET LENGTH OF ROOM                P3
         BCTR  R15,0               DECREMENT FOR EXECUTE             P3
         EX    R15,MVCROOM         MOVE ROOM TO DYNAMIC AREA         P3
         CLC   KNULL,GROOM         WAS ROOM 'NULL'                   P3
         BNE   GDATGROM            NO - CONTINUE                     P3
         MVC   GROOM,BLANKS        YES - BLANK ROOM                  P3
GDATGROM DS    0H                                                    P3
         EJECT                                                       P3
*********************************************************************P3
*                                                                    P3
*       CHECKS TO SEE IF 'TIME' WAS ENTERED.  IF SO, MOVES           P3
*       CPU TIME FROM PDE TO DYNAMIC AREA.                           P3
*                                                                    P3
*********************************************************************P3
         CLI   PCPUTIME+1,0        WAS 'TIME' SPECIFIED              P3
         BE    GDATGCPU            NO - CONTINUE                     P3
         MVC   GCPUTIME,BLANKS     YES - BLANK CPUTIME               P3
         L     R1,SCPUTIME         GET ADDRESS OF CPU TIME           P3
         LH    R15,SCPUTIME+4      GET LENGTH OF CPU TIME            P3
         BCTR  R15,0               DECREMENT FOR EXECUTE             P3
         EX    R15,MVCCPUT         MOVE CPU TIME TO DYNAMIC AREA     P3
GDATGCPU DS    0H                                                    P3
         EJECT                                                       P3
*********************************************************************P3
*                                                                    P3
*       CHECKS TO SEE IF 'MSGCLASS' WAS ENTERED.  IF SO, MOVES       P3
*       MSGCLASS FROM PDE TO DYNAMIC AREA.                           P3
*                                                                    P3
*********************************************************************P3
         CLI   PMSGCLAS+1,0        WAS 'MSGCLASS' SPECIFIED          P3
         BE    GDATGMCL            NO - CONTINUE                     P3
         L     R1,SMSGCLAS         YES - GET ADDRESS OF MSGCLASS     P3
         MVC   GMSGCLAS,0(R1)      MOVE MSGCLASS TO DYNAMIC AREA     P3
GDATGMCL DS    0H                                                    P3
         EJECT                                                       P3
*********************************************************************P3
*                                                                    P3
*       CHECKS TO SEE IF 'MSGLEVEL' WAS ENTERED.  IF SO, MOVES       P3
*       MSGLEVEL FROM PDE TO DYNAMIC AREA.                           P3
*                                                                    P3
*********************************************************************P3
         CLI   PMSGLEVL+1,0        WAS 'MSGLEVEL' SPECIFIED          P3
         BE    GDATGMLE            NO - CONTINUE                     P3
         MVC   GMSGLEVL,BLANKS     YES - BLANK MSGLEVEL              P3
         L     R1,SMSGLEVL         GET ADDRESS OF 1ST MSGLEVEL       P3
         MVC   GMSGLEVL(1),0(R1)   MOVE 1ST MSGLEVEL TO DYNAMIC AREA P3
         CLI   SMSGLEVL+8,X'FF'    IS THERE A 2ND MSGLEVEL           P3
         BE    GDATGMLE            NO - CONTINUE                     P3
         L     R15,SMSGLEVL+8      YES - GET ADDRESS OF 2ND PDE      P3
         L     R1,0(,R15)          GET ADDRESS OF 2ND MSGLEVEL       P3
         MVI   GMSGLEVL+1,C','     INSERT ','                        P3
         MVC   GMSGLEVL+2(1),0(R1) MOVE 2ND MSGLEVEL TO DYNAMIC AREA P3
GDATGMLE DS    0H                                                    P3
         EJECT                                                       P3
*********************************************************************P3
*                                                                    P3
*       CHECKS TO SEE IF 'COPIES' WAS ENTERED.  IF SO, MOVES COPIES  P3
*       FROM PDE TO DYNAMIC AREA.                                    P3
*                                                                    P3
*********************************************************************P3
         CLI   PCOPIES+1,0         WAS 'COPIES' SPECIFIED            P3
         BE    GDATGCOP            NO - CONTINUE                     P3
         MVC   GCOPIES,BLANKS      YES - BLANK COPIES                P3
         L     R1,SCOPIES          GET ADDRESS OF COPIES             P3
         LH    R15,SCOPIES+4       GET LENGTH OF COPIES              P3
         BCTR  R15,0               DECREMENT FOR EXECUTE             P3
         EX    R15,MVCCOP          MOVE COPIES TO DYNAMIC AREA       P3
GDATGCOP DS    0H                                                    P3
         EJECT                                                       P3
*********************************************************************P3
*                                                                    P3
*       CHECKS TO SEE IF 'LINECT' WAS ENTERED.   IF SO, MOVES LINECT P3
*       FROM PDE TO DYNAMIC AREA.                                    P3
*                                                                    P3
*********************************************************************P3
         CLI   PLINECT+1,0         WAS 'LINECT' SPECIFIED            P3
         BE    GDATGLIN            NO - CONTINUE                     P3
         MVC   GLINECT,BLANKS      YES - BLANK LINECT                P3
         L     R1,SLINECT          GET ADDRESS OF LINECT             P3
         LH    R15,SLINECT+4       GET LENGTH OF LINECT              P3
         BCTR  R15,0               DECREMENT FOR EXECUTE             P3
         EX    R15,MVCLIN          MOVE LINECT TO DYNAMIC AREA       P3
GDATGLIN DS    0H                                                    P3
         EJECT                                                     P3.2
*******************************************************************P3.2
*                                                                  P3.2
*       CHECKS TO SEE IF 'DEST' WAS ENTERED.  IF SO, MOVES DEST    P3.2
*       FROM PDE TO DYNAMIC AREA.                                  P3.2
*                                                                  P3.2
*******************************************************************P3.2
         CLI   PDEST+1,0           WAS 'DEST' SPECIFIED            P3.2
         BE    GDATGDST            NO - CONTINUE                   P3.2
         MVC   GDEST,BLANKS        YES - BLANK DEST                P3.2
         L     R1,SDEST            GET ADDRESS OF DEST             P3.2
         LH    R15,SDEST+4         GET LENGTH OF DEST              P3.2
         BCTR  R15,0               DECREMENT FOR EXECUTE           P3.2
         EX    R15,MVCDST          MOVE DEST TO DYNAMIC AREA       P3.2
         OI    GBITS2,GREWRITE     INDICATE REWRITE FIRST BLOCK    P3.2
GDATGDST DS    0H                                                  P3.2
         EJECT
***********************************************************************
*                                                                     *
*       CHECKS TO SEE IF USER IS CURRENTLY LOGGED ON.  IF SO, SETS THE*
*       DUPLICATE USER BIT.                                           *
*                                                                     *
***********************************************************************
GDATCHKU DS    0H
         MVC   GENQLIST,ENQLIST    COPY ENQ LIST
         MVC   GENQLIST+1(1),LUSERID+1 PUT LENGTH OF USERID IN LIST  P3
         LA    R15,QNAME           GET ADDRESS OF QNAME (SYSIKJUA)   P3
         ST    R15,GENQLIST+4      PUT IN ENQ LIST                   P3
         LA    R15,GUSERID         GET ADDRESS OF RNAME (USERID)     P3
         ST    R15,GENQLIST+8      PUT IN ENQ LIST                   P3
         LA    R1,GENQLIST         GET ADDRESS OF ENQ LIST           P3
         ENQ   MF=(E,(1))          ATTEMPT ENQ                       P3
         LTR   R15,R15             IS USER LOGGED ON
         BZ    GDATCHKR            NO - CHECK RECONNECT AND DUPLICATE
         OI    GBITS1,GDUPUSER     YES - SET BIT ON                  P3
         EJECT
***********************************************************************
*                                                                     *
*       CHECKS TO ENSURE THAT THE RECONNECT AND DUPLICATE USER BITS   *
*       AGREE.  IF NOT, ISSUES MESSAGE TO THE TSO USER AND DENIES     *
*       THE LOGON.                                                    *
*                                                                     *
***********************************************************************
GDATCHKR DS    0H
         TM    GBITS1,GRECON+GDUPUSER PROPER COMBINATION OF BITS     P3
         BM    GDATERR             NO - GO TO ERROR ROUTINE
         B     GDATRTRN            YES - CONTINUE
GDATERR  DS    0H
         TM    GBITS1,GRECON       WAS RECONNECT SPECIFIED           P3
         BO    GDATRCON            YES - GO ISSUE THAT MESSAGE
         LA    R0,L'DUPUSER        NO - GET LENGTH OF MESSAGE
         LA    R1,DUPUSER          POINT TO MESSAGE
         TPUT  (1),(0),R           ISSUE MESSAGE
         LA    R15,4               SET RETURN CODE
         OI    GBITS1,GDENY        DENY LOGON                        P3
         B     GDATEXIT            AND RETURN TO CALLER
GDATRCON DS    0H                  RECONNECT SPECIFIED BUT USER NOT ON
         LA    R0,L'BADRECON       GET LENGTH OF MESSAGE
         LA    R1,BADRECON         POINT TO MESSAGE
         TPUT  (1),(0),R           ISSUE MESSAGE
         LA    R15,4               SET RETURN CODE
         OI    GBITS1,GDENY        DENY LOGON                        P3
         B     GDATEXIT            AND RETURN TO CALLER
         EJECT
***********************************************************************
*                                                                     *
*       RELEASE STORAGE FOR PDL AND RETURN TO THE MAINLINE.           *
*                                                                     *
***********************************************************************
GDATRTRN DS    0H
         SLR   R15,R15             SET RETURN CODE
GDATEXIT DS    0H
         LR    R2,R15              PROTECT RETURN CODE
         LA    R1,GPPLANS          GET ADDRESS OF PDL
         IKJRLSA (1)               FREE PDL
         LR    R15,R2              RESTORE RETURN CODE
         L     R14,GDATSAVE        RESTORE RETURN ADDRESS
         L     R2,GDATSAVE+4       RESTORE R2
         BR    R14                 RETURN TO CALLER
         EJECT                                                       P3
*********************************************************************P3
*                                                                    P3
*       EXECUTED INSTRUCTIONS FOR GETDATA.                           P3
*                                                                    P3
*********************************************************************P3
MVCNAME  MVC   GNAME(*-*),0(R1)    MOVE NAME TO DYNAMIC AREA         P3
MVCPHONE MVC   GPHONE(*-*),0(R1)   MOVE PHONE TO DYNAMIC AREA        P3
MVCADDR  MVC   GADDRESS(*-*),0(R1) MOVE ADDRESS TO DYNAMIC AREA      P3
MVCPNAM  MVC   GPROGNAM(*-*),0(R1) MOVE PROG NAME TO DYNAMIC AREA    P3
MVCFORMS MVC   GFORMS(*-*),0(R1)   MOVE FORMS TO DYNAMIC AREA        P3
MVCROOM  MVC   GROOM(*-*),0(R1)    MOVE ROOM TO DYNAMIC AREA         P3
MVCCPUT  MVC   GCPUTIME(*-*),0(R1) MOVE CPU TIME TO DYNAMIC AREA     P3
MVCCOP   MVC   GCOPIES(*-*),0(R1)  MOVE COPIES TO DYNAMIC AREA       P3
MVCLIN   MVC   GLINECT(*-*),0(R1)  MOVE LINECT TO DYNAMIC AREA       P3
MVCDST   MVC   GDEST(*-*),0(R1)    MOVE DEST TO DYNAMIC AREA       P3.2
         TITLE 'VERIFY DATA ROUTINE'
***********************************************************************
*                                                                     *
* ROUTINE NAME -                                                      *
*    VERDATA                                                          *
*                                                                     *
* FUNCTION -                                                          *
*    VERIFY DATA RETURNED BY IKJPARS.                                 *
*                                                                     *
* OPERATION -                                                         *
*    1) IF NO ACCOUNT NUMBER WAS ENTERED AND ONLY ONE ACCOUNT NUMBER  *
*       IS DEFINED FOR THIS PASSWORD, ASSUMES THAT IS THE ACCOUNT     *
*       NUMBER FOR THIS LOGON.  IF THERE IS MORE THAN ONE ACCOUNT     *
*       NUMBER DEFINED FOR THIS PASSWORD AND ONE WAS NOT SPECIFIED,   *
*       PROMPTS THE USER FOR AN ACCOUNT NUMBER.  IF AFTER THREE (3)   *
*       PROMPTS THE USER HAS NOT ENTERED A VALID ACCOUNT NUMBER, THE  *
*       USER IS INFORMED, AND THE LOGON IS DENIED.                    *
*    2) IF AN ACCOUNT NUMBER WAS ENTERED ON THE LOGON COMMAND OR IN   *
*       RESPONSE TO THE PROMPT, VERIFIES THAT THIS ACCOUNT NUMBER IS  *
*       DEFINED FOR THIS PASSWORD.  IF NOT, PROMPT AGAIN.             *
*    3) MOVE THE ACCOUNT NUMBER TO GACCTNUM AND THE PROC NAME TO      *
*       GPROC.                                                        *
*    4) RETURN TO THE MAINLINE.                                      P3
*                                                                     *
* ENTRY POINTS -                                                      *
*    VERDATA                                                          *
*                                                                     *
* EXTERNAL REFERENCES -                                               *
*    NONE                                                             *
*                                                                     *
* INPUT -                                                             *
*    GACCTNUM RETURNED BY IKJPARS AND THE UADS DATA BLOCKS READ BY    *
*    VUSERID.                                                         *
*                                                                     *
* OUTPUT -                                                            *
*    REGISTER 15 CONTAINS THE RETURN CODE FOR THE MAINLINE.  THIS WILL*
*    BE AS FOLLOWS -                                                  *
*       --------------------------------------------------------  *
*       RETURN                                                    *
*       CODE  REASON                  ACTION                      *
*       --------------------------------------------------------  *
*          0  ACCOUNT NUMBER GOOD.    CONTINUE WITH LOGON.        *
*       --------------------------------------------------------  *
*          4  USER FAILED TO ENTER    LOGON IS DENIED.            *
*             GOOD ACCOUNT NUMBER IN                              *
*             3 TRIES.                                            *
*       --------------------------------------------------------  *
*                                                                     *
* MESSAGES -                                                          *
*    'ACCOUNT NUMBER NOT AUTHORIZED FOR THIS USER'                    *
*    'ENTER ACCOUNT NUMBER -'                                         *
*                                                                     *
***********************************************************************
         EJECT
VERDATA  DS    0H
         ST    R14,VDATSAVE        SAVE RETURN ADDRESS
         ST    R2,VDATSAVE+4       SAVE R2
         L     R2,ADDRUBUF         GET ADDRESS OF SYS1.UADS BUFFER
         EJECT
***********************************************************************
*                                                                     *
*       IF NO ACCOUNT NUMBER WAS ENTERED AND ONLY ONE ACCOUNT NUMBER  *
*       IS DEFINED FOR THIS PASSWORD, ASSUMES THAT IS THE ACCOUNT     *
*       NUMBER FOR THIS LOGON.  IF THERE IS MORE THAN ONE ACCOUNT     *
*       NUMBER DEFINED FOR THIS PASSWORD AND ONE WAS NOT SPECIFIED,   *
*       PROMPTS THE USER FOR AN ACCOUNT NUMBER.  IF AFTER THREE (3)   *
*       PROMPTS THE USER HAS NOT ENTERED A VALID ACCOUNT NUMBER, THE  *
*       USER IS INFORMED, AND THE LOGON IS DENIED.                    *
*                                                                     *
***********************************************************************
         MVI   PROMPTCT,0          RESET PROMPT COUNTER
         CLI   GACCTNUM,0          WAS ACCOUNT NUMBER PRESENT        P3
         BNE   VDATGACT            YES - GO VERIFY IT
         L     R15,PASSOFF         NO - GET OFFSET TO PSWD OFFSET BLOCK
         L     R15,4(R2,R15)       GET OFFSET OF ACCT # OFFSET BLOCK
         AR    R15,R2              GET ADDRESS OF ACCT # OFFSET BLOCK
         TM    0(R15),KBIT0        MORE THAN ONE ACCOUNT NUMBER
         BO    VDAT1ACT            NO - ASSUME THAT'S IT
         B     VDATPACT            YES - PROMPT FOR ACCOUNT NUMBER
VDATBACT DS    0H
         CLI   PROMPTCT,3          HAS USER BEEN PROMPTED 3 TIMES
         BL    VDATPTCT            NO - CONTINUE
         LA    R0,L'PRMPTMSG       GET LENGTH OF MESSAGE
         LA    R1,PRMPTMSG         POINT TO MESSAGE
         TPUT  (1),(0),R           ISSUE MESSAGE
         OI    GBITS1,GDENY        DENY LOGON                        P3
         LA    R15,4               SET RETURN CODE
         B     VDATEXIT            AND RETURN TO CALLER
VDATPTCT DS    0H
         IC    R15,PROMPTCT        GET PROMPT COUNTER
         LA    R15,1(,R15)         INCREMENT BY 1
         STC   R15,PROMPTCT        AND PUT IT BACK
         LA    R0,L'BADACCT        GET LENGTH OF MESSAGE
         LA    R1,BADACCT          POINT TO MESSAGE
         TPUT  (1),(0),R           ISSUE MESSAGE
         LTR   R15,R15             WAS RETURN CODE ZERO              P3
         BNZ   VDATEXIT            NO - EXIT                         P3
VDATPACT DS    0H
         BAL   R14,GETACCT         GO PROMPT FOR ACCOUNT NUMBER      P3
         LTR   R15,R15             WAS RETURN CODE ZERO              P3
         BNZ   VDATEXIT            NO - EXIT                         P3
         EJECT
***********************************************************************
*                                                                     *
*       IF AN ACCOUNT NUMBER WAS ENTERED ON THE LOGON COMMAND OR IN   *
*       RESPONSE TO THE PROMPT, VERIFIES THAT THIS ACCOUNT NUMBER IS  *
*       DEFINED FOR THIS PASSWORD.  IF NOT, PROMPT AGAIN.             *
*                                                                     *
***********************************************************************
VDATGACT DS    0H
         L     R15,PASSOFF         GET OFFSET TO PASSWORD OFFSET BLOCK
         L     R15,4(R2,R15)       GET ADDRESS OF ACCT # OFFSET BLOCK
VDATALOP DS    0H
         AR    R15,R2              GET ADDRESS OF THIS ACCT # OFF
         L     R14,8(,R15)         GET OFFSET TO ACCT # DATA BLOCK
         AR    R14,R2              GET ADDRESS OF ACCT # DATA BLOCK
         CLC   GACCTNUM,45(R14)    IS THIS MATCHING ACCOUNT NUMBER
         BE    VDATAHIT            YES - CONTINUE
         ICM   R15,B'0111',1(R15)  NO - ANY MORE ACCT # OFF BLOCKS
         BZ    VDATBACT            NO - PROMPT FOR ACCOUNT NUMBER
         B     VDATALOP            YES - LOOP THROUGH THEM
VDAT1ACT DS    0H
         L     R14,8(,R15)         GET OFFSET OF ACCT # DATA BLOCK
         AR    R14,R2              GET ADDRESS OF ACCT # DATA BLOCK
         EJECT
***********************************************************************
*                                                                     *
*       MOVE THE ACCOUNT NUMBER TO GACCTNUM AND THE PROC NAME TO      *
*       GPROC.                                                        *
*                                                                     *
***********************************************************************
VDATAHIT DS    0H
         MVC   GACCTNUM,45(R14)    MOVE TO DYNAMIC AREA
         L     R15,4(,R15)         GET ADDR OF PROC NAME OFFSET BLK  P3
         L     R15,8(R2,R15)       GET OFFSET OF PROC NAME DATA BLOCK
         AR    R15,R2              GET ADDRESS OF PROC NAME DATA BLOCK
         MVC   GPROC,4(R15)        MOVE TO DYNAMIC AREA
VDATGPRC DS    0H
         SLR   R15,R15             SET RETURN CODE
         EJECT                                                       P3
*********************************************************************P3
*                                                                    P3
*       RETURN TO THE MAINLINE.                                      P3
*                                                                    P3
*********************************************************************P3
VDATEXIT DS    0H
         L     R14,VDATSAVE        RESTORE RETURN ADDRESS
         L     R2,VDATSAVE+4       RESTORE R2
         BR    R14                 RETURN TO CALLER
         TITLE 'UPDATE DATA ROUTINE'                                 P3
*********************************************************************P3
*                                                                    P3
* ROUTINE NAME -                                                     P3
*    UPDDATA                                                         P3
*                                                                    P3
* FUNCTION -                                                         P3
*    UPDATE DATA .                                                   P3
*                                                                    P3
* OPERATION -                                                        P3
*    1) IF NECESSARY, REREAD FIRST BLOCK OF SYS1.UADS.             P3.2
*    2) IF NEW PASSWORD WAS SPECIFIED, UPDATE SYS1.UADS BUFFER.    P3.2
*    3) IF NEW DEST WAS SPECIFIED, UPDATE SYS1.UADS BUFFER.        P3.2
*    4) REWRITE FIRST BLOCK IF NECESSARY.                          P3.2
*    5) RETURN TO THE MAINLINE.                                      P3
*                                                                    P3
* ENTRY POINTS -                                                     P3
*    UPDDATA                                                         P3
*                                                                    P3
* EXTERNAL REFERENCES -                                              P3
*    NONE                                                            P3
*                                                                    P3
* INPUT -                                                            P3
*    NONE                                                            P3
*                                                                    P3
* OUTPUT -                                                           P3
*    REGISTER 15 CONTAINS THE RETURN CODE FOR THE MAINLINE.  THIS    P3
*    WILL BE AS FOLLOWS -                                            P3
*       -------------------------------------------------------- P3
*       RETURN                                                   P3
*       CODE  REASON                  ACTION                     P3
*       -------------------------------------------------------- P3
*          0  NO ERRORS ENCOUNTERED.  CONTINUE WITH LOGON.       P3
*       -------------------------------------------------------- P3
*                                                                    P3
* MESSAGES -                                                         P3
*    NONE                                                            P3
*                                                                    P3
*********************************************************************P3
         EJECT                                                       P3
UPDDATA  DS    0H                                                    P3
         ST    R14,UDATSAVE        SAVE RETURN ADDRESS               P3
         ST    R2,UDATSAVE+4       SAVE R2                           P3
         ST    R3,UDATSAVE+8       SAVE R3                           P3
         EJECT                                                       P3
*******************************************************************P3.2
*                                                                  P3.2
*       IF NECESSARY, REREAD FIRST BLOCK OF SYS1.UADS.             P3.2
*                                                                  P3.2
*******************************************************************P3.2
         TM    GBITS2,GREWRITE     DO WE NEED TO REWRITE FIRST BLK P3.2
         BZ    UDATNWRT            NO - CONTINUE                   P3.2
         POINT GSYSUADS,GTTR1ST    YES - POINT TO FIRST BLOCK        P3
         L     R2,ADDRUBUF         GET ADDRESS OF SYS1.UADS BUFFER   P2
         READ  DECB,SF,GSYSUADS,(R2),'S',MF=E REREAD FIRST BLOCK     P2
         CHECK DECB                WAIT ON I/O                       P2
         EJECT                                                     P3.2
*********************************************************************P3
*                                                                    P3
*       IF NEW PASSWORD WAS SPECIFIED, UPDATE SYS1.UADS BUFFER.    P3.2
*                                                                    P3
*********************************************************************P3
         CLI   GNEWPSWD,0          WAS NEW PASSWORD SPECIFIED        P3
         BE    UDATGPAS            NO - CONTINUE                     P3
         L     R1,PASSOFF          GET OFFSET OF PASSWORD OFFSET BLK P2
         L     R1,8(R2,R1)         GET OFFSET OF PASSWORD BLOCK      P2
         AR    R1,R2               ADD ORIGIN                        P2
         MVC   4(8,R1),GNEWPSWD    MOVE NEW PASSWORD TO UADS BUFFER  P2
         MVC   GPASS,GNEWPSWD      CHANGE LOGON PASSWORD             P2
UDATGPAS DS    0H                                                    P2
         EJECT                                                     P3.2
*******************************************************************P3.2
*                                                                  P3.2
*       IF NEW DEST WAS SPECIFIED, UPDATE SYS1.UADS BUFFER.        P3.2
*                                                                  P3.2
*******************************************************************P3.2
         CLI   GDEST,0             WAS NEW DEST SPECIFIED          P3.2
         BE    UDATGDST            NO - CONTINUE                   P3.2
         MVC   32(L'GDEST,R2),GDEST YES - MOVE NEW DEST TO BUFFER  P3.2
         CLC   KLOCAL,GDEST        WAS THIS DEST(LOCAL)            P3.2
         BNE   UDATGDST            NO - CONTINUE                   P3.2
         XC    32(L'GDEST,R2),32(R2) YES - ZERO IT                 P3.2
UDATGDST DS    0H                                                  P3.2
         EJECT                                                     P3.2
*******************************************************************P3.2
*                                                                  P3.2
*       REWRITE FIRST BLOCK IF NECESSARY.                          P3.2
*                                                                  P3.2
*******************************************************************P3.2
         L     R2,ADDRUBUF         GET ADDRESS OF SYS1.UADS BUFFER P3.2
         L     R3,GLEN1ST          GET LENGTH OF FIRST BLOCK       P3.2
         WRITE DECB,SF,GSYSUADS,(R2),(R3),MF=E REWRITE FIRST BLOCK   P3
         CHECK DECB                WAIT ON I/O                       P2
UDATNWRT DS    0H                                                  P3.2
         EJECT                                                       P3
*********************************************************************P3
*                                                                    P3
*       RETURN TO THE MAINLINE.                                      P3
*                                                                    P3
*********************************************************************P3
UDATRTRN DS    0H                                                    P3
         SLR   R15,R15             SET RETURN CODE                   P3
UDATEXIT DS    0H                                                    P3
         L     R14,UDATSAVE        RESTORE RETURN ADDRESS            P3
         L     R2,UDATSAVE+4       RESTORE R2                        P3
         L     R3,UDATSAVE+8       RESTORE R3                        P3
         BR    R14                 RETURN TO CALLER                  P3
         TITLE 'GET INFO ROUTINE'                                    P3
*********************************************************************P3
*                                                                    P3
* ROUTINE NAME -                                                     P3
*    GETINFO                                                         P3
*                                                                    P3
* FUNCTION -                                                         P3
*    COMPLETE INFO BLOCK.                                            P3
*                                                                    P3
* OPERATION -                                                        P3
*    1) READ INFO BLOCK.                                             P3
*    2) IF USERID WAS BLANK, UPDATE IT.                              P3
*    3) IF NAME WAS SPECIFIED ON COMMAND, MOVE IT TO INFO BLOCK.     P3
*    4) IF PHONE WAS SPECIFIED ON COMMAND, MOVE IT TO INFO BLOCK.    P3
*    5) IF ADDRESS WAS SPECIFIED ON COMMAND, MOVE IT TO INFO BLOCK.  P3
*    6) IF PROGRAMMER NAME WAS SPECIFIED ON COMMAND, MOVE IT TO      P3
*       INFO BLOCK.                                                  P3
*    7) IF FORMS WAS SPECIFIED ON COMMAND, MOVE IT TO INFO BLOCK.    P3
*    8) IF ROOM WAS SPECIFIED ON COMMAND, MOVE IT TO INFO BLOCK.     P3
*    9) IF NAME WAS BLANK, ASK FOR IT.                               P3
*   10) IF PHONE WAS BLANK, ASK FOR IT.                              P3
*   11) IF ADDRESS WAS BLANK, ASK FOR IT.                            P3
*   12) IF LASTON WAS BLANK, FILL IT IN.                             P3
*   13) IF CHGPSWD WAS BLANK, FILL IT IN.                            P3
*   14) IF CPUTIME WAS BLANK, SET UP DEFAULT MAXIMUM.                P3
*   15) IF DEFTIME WAS BLANK, SET DEFTIME TO CPUTIME VALUE.          P3
*   16) RETURN TO THE MAINLINE.                                      P3
*                                                                    P3
* ENTRY POINTS -                                                     P3
*    GETINFO                                                         P3
*                                                                    P3
* EXTERNAL REFERENCES -                                              P3
*    FINDINFO                                                        P3
*                                                                    P3
* INPUT -                                                            P3
*    NONE                                                            P3
*                                                                    P3
* OUTPUT -                                                           P3
*    REGISTER 15 CONTAINS THE RETURN CODE FOR THE MAINLINE.  THIS    P3
*    WILL BE AS FOLLOWS -                                            P3
*       -------------------------------------------------------- P3
*       RETURN                                                   P3
*       CODE  REASON                  ACTION                     P3
*       -------------------------------------------------------- P3
*          0  NO ERRORS ENCOUNTERED.  CONTINUE WITH LOGON.       P3
*       -------------------------------------------------------- P3
*       NON-0 TPUT/TGET RETURN CODE.  DENY LOGON.                P3
*       -------------------------------------------------------- P3
*                                                                    P3
* MESSAGES -                                                         P3
*    ENTER YOUR NAME -                                               P3
*    ENTER YOUR PHONE NUMBER -                                       P3
*    ENTER YOUR ADDRESS -                                            P3
*                                                                    P3
*********************************************************************P3
         EJECT                                                       P3
GETINFO  DS    0H                                                    P3
         ST    R14,GINFSAVE        SAVE RETURN ADDRESS               P3
         ST    R2,GINFSAVE+4       SAVE R2                           P3
         ST    R3,GINFSAVE+8       SAVE R3                           P3
         EJECT                                                       P3
*********************************************************************P3
*                                                                    P3
*       READ INFO BLOCK.                                             P3
*                                                                    P3
*********************************************************************P3
         POINT GSYSUADS,GTTRINFO   POINT TO INFO BLOCK               P3
         L     R2,ADDRINFO         GET ADDRESS OF INFO BLOCK BUFFER  P3
         READ  DECB,SF,GSYSUADS,(R2),'S',MF=E READ INFO BLOCK        P3
         CHECK DECB                WAIT ON I/O                       P3
         LH    R1,GSYSUADS+DCBBLKSI-IHADCB GET BLKSIZE FROM DCB      P3
         L     R15,DECB+16         GET IOB ADDRESS FROM DECB         P3
         SH    R1,14(,R15)         SUBTRACT RESIDUAL COUNT           P3
         ST    R1,GLENINFO         SAVE LENGTH OF INFO BLOCK         P3
         EJECT                                                       P3
*********************************************************************P3
*                                                                    P3
*       IF USERID WAS BLANK, UPDATE IT.                              P3
*                                                                    P3
*********************************************************************P3
         LA    R1,MUSERID          POINT TO KEY FOR USERID           P3
         BAL   R14,FINDINFO        FIND USERID                       P3
         CLI   9(R1),C' '          WAS USERID BLANK                  P3
         BNE   GINFGUSR            NO - CONTINUE                     P3
         MVC   9(8,R1),GUSERID     YES - UPDATE USERID               P3
GINFGUSR DS    0H                                                    P3
         EJECT                                                       P3
*********************************************************************P3
*                                                                    P3
*       IF NAME WAS SPECIFIED ON COMMAND, MOVE IT TO INFO BLOCK.     P3
*                                                                    P3
*********************************************************************P3
         CLI   GNAME,0             WAS NAME SPECIFIED                P3
         BE    GINFPNAM            NO - CONTINUE                     P3
         LA    R1,MNAME            YES - POINT TO KEY FOR NAME       P3
         BAL   R14,FINDINFO        FIND NAME                         P3
         MVC   9(63,R1),GNAME      COPY NAME FROM DYNAMIC AREA       P3
GINFPNAM DS    0H                                                    P3
         EJECT                                                       P3
*********************************************************************P3
*                                                                    P3
*       IF PHONE WAS SPECIFIED ON COMMAND, MOVE IT TO INFO BLOCK.    P3
*                                                                    P3
*********************************************************************P3
         CLI   GPHONE,0            WAS PHONE NUMBER SPECIFIED        P3
         BE    GINFPPHO            NO - CONTINUE                     P3
         LA    R1,MPHONE           YES - POINT TO KEY FOR PHONE      P3
         BAL   R14,FINDINFO        FIND PHONE                        P3
         MVC   9(63,R1),GPHONE     COPY PHONE FROM DYNAMIC AREA      P3
GINFPPHO DS    0H                                                    P3
         EJECT                                                       P3
*********************************************************************P3
*                                                                    P3
*       IF ADDRESS WAS SPECIFIED ON COMMAND, MOVE IT TO INFO BLOCK.  P3
*                                                                    P3
*********************************************************************P3
         CLI   GADDRESS,0          WAS ADDRESS SPECIFIED             P3
         BE    GINFPADR            NO - CONTINUE                     P3
         LA    R1,MADDRESS         YES - POINT TO KEY FOR ADDRESS    P3
         BAL   R14,FINDINFO        FIND ADDRESS                      P3
         MVC   9(63,R1),GADDRESS   COPY ADDRESS FROM DYNAMIC AREA    P3
GINFPADR DS    0H                                                    P3
         EJECT                                                       P3
*********************************************************************P3
*                                                                    P3
*       IF PROGRAMMER NAME WAS SPECIFIED ON COMMAND, MOVE IT TO      P3
*       INFO BLOCK.                                                  P3
*                                                                    P3
*********************************************************************P3
         CLI   GPROGNAM,0          WAS PROGRAMMER NAME SPECIFIED     P3
         BE    GINFPPNM            NO - CONTINUE                     P3
         LA    R1,MPROGNAM         YES - POINT TO KEY FOR PROG NAME  P3
         BAL   R14,FINDINFO        FIND PROGRAMMER NAME              P3
         MVC   9(20,R1),GPROGNAM   COPY PROG NAME FROM DYNAMIC AREA  P3
GINFPPNM DS    0H                                                    P3
         EJECT                                                       P3
*********************************************************************P3
*                                                                    P3
*       IF FORMS WAS SPECIFIED ON COMMAND, MOVE IT TO INFO BLOCK.    P3
*                                                                    P3
*********************************************************************P3
         CLI   GFORMS,0            WAS FORMS SPECIFIED               P3
         BE    GINFPFOR            NO - CONTINUE                     P3
         LA    R1,MFORMS           YES - POINT TO KEY FOR FORMS      P3
         BAL   R14,FINDINFO        FIND FORMS                        P3
         MVC   9(4,R1),GFORMS      COPY FORMS FROM DYNAMIC AREA      P3
GINFPFOR DS    0H                                                    P3
         EJECT                                                       P3
*********************************************************************P3
*                                                                    P3
*       IF ROOM WAS SPECIFIED ON COMMAND, MOVE IT TO INFO BLOCK.     P3
*                                                                    P3
*********************************************************************P3
         CLI   GROOM,0             WAS ROOM SPECIFIED                P3
         BE    GINFPROM            NO - CONTINUE                     P3
         LA    R1,MROOM            YES - POINT TO KEY FOR ROOM       P3
         BAL   R14,FINDINFO        FIND ROOM                         P3
         MVC   9(4,R1),GROOM       COPY ROOM FROM DYNAMIC AREA       P3
GINFPROM DS    0H                                                    P3
         EJECT                                                       P3
*********************************************************************P3
*                                                                    P3
*       IF NAME WAS BLANK, ASK FOR IT.                               P3
*                                                                    P3
*********************************************************************P3
         LA    R1,MNAME            POINT TO KEY FOR NAME             P3
         BAL   R14,FINDINFO        FIND NAME                         P3
         CLI   9(R1),C' '          WAS NAME BLANK                    P3
         BNE   GINFGNAM            NO - CONTINUE                     P3
         LR    R2,R1               YES - SAVE POINTER TO NAME ENTRY  P3
GINFANAM DS    0H                                                    P3
         LA    R0,L'NAMEMSG        GET LENGTH OF MESSAGE             P3
         LA    R1,NAMEMSG          POINT TO MESSAGE                  P3
         TPUT  (1),(0),R           ISSUE MESSAGE                     P3
         LTR   R15,R15             WAS RETURN CODE ZERO              P3
         BNZ   GINFEXIT            NO - EXIT                         P3
         LA    R0,63               YES - GET LENGTH OF NAME          P3
         LA    R1,9(,R2)           GET ADDRESS OF NAME               P3
         ICM   R1,B'1000',HEX80    SET OPTIONS                       P3
         MVC   9(63,R2),BLANKS     BLANK INPUT AREA                  P3
         TGET  (1),(0),R           READ NAME                         P3
         LTR   R15,R15             WAS RETURN CODE ZERO              P3
         BZ    GINFCNAM            YES - CHECK NAME                  P3
         BAL   R14,GINFTGER        NO - GO TO ERROR ROUTINE          P3
GINFCNAM DS    0H                                                    P3
         OC    9(63,R2),BLANKS     FORCE TO UPPERCASE                P3
         CLI   9(R2),C' '          WAS NAME BLANK                    P3
         BE    GINFANAM            YES - ASK AGAIN                   P3
GINFGNAM DS    0H                                                    P3
         EJECT                                                       P3
*********************************************************************P3
*                                                                    P3
*       IF PHONE WAS BLANK, ASK FOR IT.                              P3
*                                                                    P3
*********************************************************************P3
         LA    R1,MPHONE           POINT TO KEY FOR PHONE            P3
         BAL   R14,FINDINFO        FIND PHONE                        P3
         CLI   9(R1),C' '          WAS PHONE BLANK                   P3
         BNE   GINFGPHO            NO - CONTINUE                     P3
         LR    R2,R1               YES - SAVE POINTER TO PHONE ENTRY P3
GINFAPHO DS    0H                                                    P3
         LA    R0,L'PHONEMSG       GET LENGTH OF MESSAGE             P3
         LA    R1,PHONEMSG         POINT TO MESSAGE                  P3
         TPUT  (1),(0),R           ISSUE MESSAGE                     P3
         LTR   R15,R15             WAS RETURN CODE ZERO              P3
         BNZ   GINFEXIT            NO - EXIT                         P3
         LA    R0,63               YES - GET LENGTH OF PHONE         P3
         LA    R1,9(,R2)           GET ADDRESS OF PHONE              P3
         ICM   R1,B'1000',HEX80    SET OPTIONS                       P3
         MVC   9(63,R2),BLANKS     BLANK INPUT AREA                  P3
         TGET  (1),(0),R           READ PHONE                        P3
         LTR   R15,R15             WAS RETURN CODE ZERO              P3
         BZ    GINFCPHO            YES - CHECK PHONE                 P3
         BAL   R14,GINFTGER        NO - GO TO ERROR ROUTINE          P3
GINFCPHO DS    0H                                                    P3
         OC    9(63,R2),BLANKS     FORCE TO UPPERCASE                P3
         CLI   9(R2),C' '          WAS PHONE BLANK                   P3
         BE    GINFAPHO            YES - ASK AGAIN                   P3
GINFGPHO DS    0H                                                    P3
         EJECT                                                       P3
*********************************************************************P3
*                                                                    P3
*       IF ADDRESS WAS BLANK, ASK FOR IT.                            P3
*                                                                    P3
*********************************************************************P3
         LA    R1,MADDRESS         POINT TO KEY FOR ADDRESS          P3
         BAL   R14,FINDINFO        FIND ADDRESS                      P3
         CLI   9(R1),C' '          WAS ADDRESS BLANK                 P3
         BNE   GINFGADR            NO - CONTINUE                     P3
         LR    R2,R1               YES - SAVE POINTER TO ADDR ENTRY  P3
GINFAADR DS    0H                                                    P3
         LA    R0,L'ADDRMSG        GET LENGTH OF MESSAGE             P3
         LA    R1,ADDRMSG          POINT TO MESSAGE                  P3
         TPUT  (1),(0),R           ISSUE MESSAGE                     P3
         LTR   R15,R15             WAS RETURN CODE ZERO              P3
         BNZ   GINFEXIT            NO - EXIT                         P3
         LA    R0,63               YES - GET LENGTH OF ADDRESS       P3
         LA    R1,9(,R2)           GET ADDRESS OF ADDRESS            P3
         ICM   R1,B'1000',HEX80    SET OPTIONS                       P3
         MVC   9(63,R2),BLANKS     BLANK INPUT AREA                  P3
         TGET  (1),(0),R           READ ADDRESS                      P3
         LTR   R15,R15             WAS RETURN CODE ZERO              P3
         BZ    GINFCADR            YES - CHECK ADDRESS               P3
         BAL   R14,GINFTGER        NO - GO TO ERROR ROUTINE          P3
GINFCADR DS    0H                                                    P3
         OC    9(63,R2),BLANKS     FORCE TO UPPERCASE                P3
         CLI   9(R2),C' '          WAS ADDRESS BLANK                 P3
         BE    GINFAADR            YES - ASK AGAIN                   P3
GINFGADR DS    0H                                                    P3
         EJECT                                                       P3
*********************************************************************P3
*                                                                    P3
*       IF LASTON WAS BLANK, FILL IT IN.                             P3
*                                                                    P3
*********************************************************************P3
         LA    R1,MLASTON          POINT TO KEY FOR LASTON           P3
         BAL   R14,FINDINFO        FIND LASTON                       P3
         CLI   9(R1),C' '          WAS LASTON BLANK                  P3
         BNE   GINFGLST            NO - CONTINUE                     P3
         L     R15,CVTPTR          YES - GET ADDRESS OF CVT          P3
         MVC   8(7,R1),DATEMASK    MOVE IN EDIT MASK FOR DATE        P3
         ED    8(7,R1),CVTDATE+1-CVTMAP(R15) EDIT IN TODAY'S DATE    P3
         MVC   16(8,R1),GTERMID    COPY TERMINAL TO INFO BLOCK       P3
GINFGLST DS    0H                                                    P3
         EJECT                                                       P3
*********************************************************************P3
*                                                                    P3
*       IF CHGPSWD WAS BLANK, FILL IT IN.                            P3
*                                                                    P3
*********************************************************************P3
         LA    R1,MCHGPSWD         POINT TO KEY FOR CHGPSWD          P3
         BAL   R14,FINDINFO        FIND CHGPSWD                      P3
         CLI   9(R1),C' '          WAS CHGPSWD BLANK                 P3
         BNE   GINFGCPW            NO - CONTINUE                     P3
         L     R15,CVTPTR          YES - GET ADDRESS OF CVT          P3
         MVC   8(7,R1),DATEMASK    MOVE IN EDIT MASK FOR DATE        P3
         ED    8(7,R1),CVTDATE+1-CVTMAP(R15) EDIT IN TODAY'S DATE    P3
GINFGCPW DS    0H                                                    P3
         EJECT                                                       P3
*********************************************************************P3
*                                                                    P3
*       IF CPUTIME WAS BLANK, SET UP DEFAULT MAXIMUM.                P3
*                                                                    P3
*********************************************************************P3
         LA    R1,MCPUTIME         POINT TO KEY FOR CPUTIME          P3
         BAL   R14,FINDINFO        FIND CPUTIME                      P3
         LR    R2,R1               SAVE POINTER TO CPUTIME FOR LATER P3
         CLI   9(R1),C' '          WAS CPUTIME BLANK                 P3
         BNE   GINFGMCP            NO - CONTINUE                     P3
         MVC   9(L'GCPUTIME,R1),DCPUTIME YES - SET UP DEFAULT MAX    P3
GINFGMCP DS    0H                                                    P3
         EJECT                                                       P3
*********************************************************************P3
*                                                                    P3
*       IF DEFTIME WAS BLANK, SET DEFTIME TO CPUTIME VALUE.          P3
*                                                                    P3
*********************************************************************P3
         LA    R1,MDEFTIME         POINT TO KEY FOR DEFTIME          P3
         BAL   R14,FINDINFO        FIND DEFTIME                      P3
         CLI   9(R1),C' '          WAS DEFTIME BLANK                 P3
         BNE   GINFGDCP            NO - CONTINUE                     P3
         MVC   9(L'DCPUTIME,R1),9(R2) YES - SET UP MAX AS DEFAULT    P3
GINFGDCP DS    0H                                                    P3
         EJECT                                                       P3
*********************************************************************P3
*                                                                    P3
*       RETURN TO THE MAINLINE.                                      P3
*                                                                    P3
*********************************************************************P3
GINFRTRN DS    0H                                                    P3
         SLR   R15,R15             SET RETURN CODE                   P3
GINFEXIT DS    0H                                                    P3
         L     R14,GINFSAVE        RESTORE RETURN ADDRESS            P3
         L     R2,GINFSAVE+4       RESTORE R2                        P3
         L     R3,GINFSAVE+8       RESTORE R3                        P3
         BR    R14                 RETURN TO CALLER                  P3
         EJECT                                                       P3
*********************************************************************P3
*                                                                    P3
*       IF RETURN CODE DID NOT INDICATE ATTENTION INTERRUPT, ISSUE   P3
*       MESSAGE TO USER AND DENY LOGON.                              P3
*                                                                    P3
*********************************************************************P3
GINFTGER DS    0H                  NON-ZERO RETURN CODE FROM TGET    P3
         LA    R1,8                GET 8 IN R1 FOR COMPARISON        P3
         CR    R1,R15              WAS THIS AN ATTENTION INTERRUPT   P3
         BNE   GINFTGNA            NO - GO ISSUE MESSAGE             P3
         TM    GBITS1,GATTNHIT     YES - DID I GET ANY INPUT         P3
         BZR   R14                 NO - RETURN AS IF NULL INPUT      P3
         B     GINFEXIT            YES - LET MAINLINE HANDLE INPUT   P3
GINFTGNA DS    0H                  NOT AN ATTENTION INTERRUPT        P3
         LR    R2,R15              PROTECT RETURN CODE               P3
         MVC   GTGETMSG(L'TGETMSG),TGETMSG COPY MESSAGE              P3
         CVD   R2,WORKAREA         CONVERT TO DECIMAL                P3
         OI    WORKAREA+7,X'0F'    FIX SIGN                          P3
         UNPK  WORKAREA(5),WORKAREA+5(3) UNPACK                      P3
         MVC   GTGETMSG+23(4),WORKAREA+1 MOVE INTO MESSAGE           P3
         LA    R0,L'TGETMSG        GET LENGTH OF MESSAGE             P3
         LA    R1,GTGETMSG         POINT TO MESSAGE                  P3
         TPUT  (1),(0),R           ISSUE MESSAGE                     P3
         LR    R15,R2              RESTORE RETURN CODE               P3
         OI    GBITS1,GDENY        DENY LOGON                        P3
         B     GINFEXIT            EXIT                              P3
         TITLE 'VERIFY INFO ROUTINE'                                 P3
*********************************************************************P3
*                                                                    P3
* ROUTINE NAME -                                                     P3
*    VERINFO                                                         P3
*                                                                    P3
* FUNCTION -                                                         P3
*    VERIFY INFO BLOCK.                                              P3
*                                                                    P3
* OPERATION -                                                        P3
*    1) IF USER HAS NOT LOGGED ON IN 45 DAYS, DENY LOGON.            P3
*    2) IF USER HAS NOT CHANGED PASSWORD IN 90 DAYS, DENY LOGON.     P3
*    3) IF USER HAS NOT CHANGED PASSWORD IN 80 DAYS, WARN HIM.       P3
*    4) RETURN TO MAINLINE.                                          P3
*                                                                    P3
* ENTRY POINTS -                                                     P3
*    VERINFO                                                         P3
*                                                                    P3
* EXTERNAL REFERENCES -                                              P3
*    FINDINFO, DAYS, GETNPASS                                        P3
*                                                                    P3
* INPUT -                                                            P3
*    NONE                                                            P3
*                                                                    P3
* OUTPUT -                                                           P3
*    REGISTER 15 CONTAINS THE RETURN CODE FOR THE MAINLINE.  THIS    P3
*    WILL BE AS FOLLOWS -                                            P3
*       -------------------------------------------------------- P3
*       RETURN                                                   P3
*       CODE  REASON                  ACTION                     P3
*       -------------------------------------------------------- P3
*          0  NO ERRORS ENCOUNTERED.  CONTINUE WITH LOGON.       P3
*       -------------------------------------------------------- P3
*          4  USER DID NOT SATISFY    DENY LOGON.                P3
*             INSTALLATION                                       P3
*             REQUIREMENTS.                                      P3
*       -------------------------------------------------------- P3
*                                                                    P3
* MESSAGES -                                                         P3
*    'PASSWORD SHOULD BE CHANGED'                                    P3
*    'USER HAS NOT LOGGED ON IN 45 DAYS'                             P3
*    'USER HAS NOT CHANGED PASSWORD IN 90 DAYS'                      P3
*    'TIME EXCEEDS USER''S LIMIT.  DEFAULT USED'                     P3
*                                                                    P3
*********************************************************************P3
         EJECT                                                       P3
VERINFO  DS    0H                                                    P3
         ST    R14,VINFSAVE        SAVE RETURN ADDRESS               P3
         ST    R2,VINFSAVE+4       SAVE R2                           P3
         ST    R3,VINFSAVE+8       SAVE R3                           P3
         EJECT                                                       P3
*********************************************************************P3
*                                                                    P3
*       IF USER HAS NOT LOGGED ON IN 45 DAYS, DENY LOGON.            P3
*                                                                    P3
*********************************************************************P3
         LA    R1,MLASTON          POINT TO KEY FOR LASTON           P3
         BAL   R14,FINDINFO        FIND LASTON                       P3
         BAL   R14,DAYS            CALCULATE ELAPSED DAYS            P3
         CH    R1,HALF45           MORE THAN 45 DAYS ELAPSED         P3
         BH    VINFLAST            YES - DENY LOGON                  P3
         EJECT                                                       P3
*********************************************************************P3
*                                                                    P3
*       IF USER HAS NOT CHANGED PASSWORD IN 90 DAYS, DENY LOGON.     P3
*                                                                    P3
*********************************************************************P3
         CLI   GNEWPSWD,0          WAS NEW PASSWORD SPECIFIED        P3
         BNE   VINFGPAS            YES - CONTINUE                    P3
         LA    R1,MCHGPSWD         NO - POINT TO KEY FOR CHGPSWD     P3
         BAL   R14,FINDINFO        FIND CHGPSWD                      P3
         BAL   R14,DAYS            CALCULATE ELAPSED DAYS            P3
         CH    R1,HALF90           MORE THAN 90 DAYS ELAPSED         P3
         BH    VINFCPAS            YES - DENY LOGON                  P3
         EJECT                                                       P3
*********************************************************************P3
*                                                                    P3
*       IF USER HAS NOT CHANGED PASSWORD IN 80 DAYS, WARN HIM.       P3
*                                                                    P3
*********************************************************************P3
         CH    R1,HALF80           MORE THAN 80 DAYS ELAPSED         P3
         BNH   VINFGPAS            NO - CONTINUE                     P3
         LA    R0,L'PASSWARN       YES - GET LENGTH OF MESSAGE       P3
         LA    R1,PASSWARN         POINT TO MESSAGE                  P3
         TPUT  (1),(0),R           ISSUE MESSAGE                     P3
VINFGPAS DS    0H                                                    P3
         EJECT                                                       P3
*********************************************************************P3
*                                                                    P3
*       IF USER HAS SPECIFIED 'TIME' CHECK THAT IT IS NOT GREATER    P3
*       THAN CPUTIME ENTRY IN INFO BLOCK.  IF IT IS, IGNORE IT.      P3
*                                                                    P3
*********************************************************************P3
         CLI   GCPUTIME,0          WAS 'TIME' SPECIFIED              P3
         BNE   VINFCTIM            YES - CONTINUE                    P3
         LA    R1,MDEFTIME         NO - POINT TO KEY FOR DEFTIME     P3
         BAL   R14,FINDINFO        FIND DEFTIME                      P3
         MVC   GCPUTIME,9(R1)      DEFAULT 'TIME' TO DEFTIME         P3
VINFCTIM DS    0H                                                    P3
         LA    R1,MCPUTIME         YES - POINT TO KEY FOR CPUTIME    P3
         BAL   R14,FINDINFO        FIND CPUTIME                      P3
         MVI   WORKAREA,C'0'       START TO PREPARE WORKAREA         P3
         MVC   WORKAREA+1(2),WORKAREA CREATE LEFT PAD OF 3 X'F0'S    P3
         MVC   WORKAREA+3(4),9(R1) COPY CPUTIME FROM INFO BLOCK      P3
         MVI   WORKAREA+6,C' '     INSURE SCAN STOPS                 P3
VINFLOP1 DS    0H                                                    P3
         CLI   WORKAREA+4,C' '     SHIFTED ENOUGH                    P3
         BE    VINFEND1            YES - WORKAREA(4) CONTAINS TIME   P3
         MVC   WORKAREA(6),WORKAREA+1 NO - SHIFT ONE MORE            P3
         B     VINFLOP1            AND LOOP                          P3
VINFEND1 DS    0H                                                    P3
         PACK  WORKAREA(8),WORKAREA(4) PACK TIME FROM INFO BLOCK     P3
         CVB   R15,WORKAREA        AND CONVERT TO BINARY             P3
         MVI   WORKAREA,C'0'       START TO PREPARE WORKAREA         P3
         MVC   WORKAREA+1(2),WORKAREA CREATE LEFT PAD OF 3 X'F0'S    P3
         MVC   WORKAREA+3(4),GCPUTIME COPY CPUTIME FROM DYNAMIC AREA P3
         MVI   WORKAREA+6,C' '     INSURE SCAN STOPS                 P3
VINFLOP2 DS    0H                                                    P3
         CLI   WORKAREA+4,C' '     SHIFTED ENOUGH                    P3
         BE    VINFEND2            YES - WORKAREA(4) CONTAINS TIME   P3
         MVC   WORKAREA(6),WORKAREA+1 NO - SHIFT ONE MORE            P3
         B     VINFLOP2            AND LOOP                          P3
VINFEND2 DS    0H                                                    P3
         PACK  WORKAREA(8),WORKAREA(4) PACK TIME FROM PARSE          P3
         CVB   R1,WORKAREA         AND CONVERT TO BINARY             P3
         CR    R1,R15              WAS SPECIFIED TIME > LIMIT        P3
         BH    VINFTIME            YES - GO TO ERROR ROUTINE         P3
VINFGTIM DS    0H                                                    P3
         EJECT                                                       P3
*********************************************************************P3
*                                                                    P3
*       RETURN TO THE MAINLINE.                                      P3
*                                                                    P3
*********************************************************************P3
VINFRTRN DS    0H                                                    P3
         SLR   R15,R15             SET RETURN CODE                   P3
VINFEXIT DS    0H                                                    P3
         L     R14,VINFSAVE        RESTORE RETURN ADDRESS            P3
         L     R2,VINFSAVE+4       RESTORE R2                        P3
         L     R3,VINFSAVE+8       RESTORE R3                        P3
         BR    R14                 RETURN TO CALLER                  P3
         EJECT                                                       P3
*********************************************************************P3
*                                                                    P3
*       USER HAS NOT LOGGED ON IN 45 DAYS.  ISSUE MESSAGE AND DENY   P3
*       LOGON.                                                       P3
*                                                                    P3
*********************************************************************P3
VINFLAST DS    0H                                                    P3
         LA    R0,L'LOG45MSG       GET LENGTH OF MESSAGE             P3
         LA    R1,LOG45MSG         POINT TO MESSAGE                  P3
         TPUT  (1),(0),R           ISSUE MESSAGE                     P3
         OI    GBITS1,GDENY        DENY LOGON                        P3
         LA    R15,4               SET RETURN CODE                   P3
         B     VINFEXIT            EXIT                              P3
         EJECT                                                       P3
*********************************************************************P3
*                                                                    P3
*       USER HAS NOT CHANGED PASSWORD IN 90 DAYS.  ISSUE MESSAGE AND P3
*       DENY LOGON.                                                  P3
*                                                                    P3
*********************************************************************P3
VINFCPAS DS    0H                                                    P3
         LA    R0,L'PAS90MSG       GET LENGTH OF MESSAGE             P3
         LA    R1,PAS90MSG         POINT TO MESSAGE                  P3
         TPUT  (1),(0),R           ISSUE MESSAGE                     P3
         BAL   R14,GETNPASS        GO ASK FOR NEW PASSWORD           P3
         LTR   R15,R15             WAS PROMPT SUCCESSFUL             P3
         BZ    VINFGPAS            YES - CONTINUE                    P3
         OI    GBITS1,GDENY        NO - DENY LOGON                   P3
         LA    R15,4               SET RETURN CODE                   P3
         B     VINFEXIT            EXIT                              P3
         EJECT                                                       P3
*********************************************************************P3
*                                                                    P3
*       USER SPECIFIED 'TIME' GREATER THAN HE IS ALLOWED.  ISSUE     P3
*       MESSAGE AND DEFAULT TO DEFTIME.                              P3
*                                                                    P3
*********************************************************************P3
VINFTIME DS    0H                                                    P3
         LA    R0,L'TIMEMSG        GET LENGTH OF MESSAGE             P3
         LA    R1,TIMEMSG          POINT TO MESSAGE                  P3
         TPUT  (1),(0),R           ISSUE MESSAGE                     P3
         LA    R1,MCPUTIME         POINT TO KEY FOR CPUTIME          P3
         BAL   R14,FINDINFO        FIND DEFTIME                      P3
         MVC   GCPUTIME,9(R1)      DEFAULT CPU TIME TO MAXIMUM       P3
         B     VINFGTIM            EXIT                              P3
         TITLE 'UPDATE INFO ROUTINE'                                 P3
*********************************************************************P3
*                                                                    P3
* ROUTINE NAME -                                                     P3
*    UPDINFO                                                         P3
*                                                                    P3
* FUNCTION -                                                         P3
*    UPDATE INFO BLOCK.                                              P3
*                                                                    P3
* OPERATION -                                                        P3
*    1) IF NEW PASSWORD WAS SPECIFIED, UPDATE CHGPSWD.               P3
*    2) UPDATE LASTON.                                               P3
*    3) IF 'TIME' WAS SPECIFIED, UPDATE DEFTIME.                     P3
*    4) REWRITE INFO BLOCK.                                          P3
*    5) RETURN TO THE MAINLINE.                                      P3
*                                                                    P3
* ENTRY POINTS -                                                     P3
*    UPDINFO                                                         P3
*                                                                    P3
* EXTERNAL REFERENCES -                                              P3
*    FINDINFO                                                        P3
*                                                                    P3
* INPUT -                                                            P3
*    NONE                                                            P3
*                                                                    P3
* OUTPUT -                                                           P3
*    REGISTER 15 CONTAINS THE RETURN CODE FOR THE MAINLINE.  THIS    P3
*    WILL BE AS FOLLOWS -                                            P3
*       -------------------------------------------------------- P3
*       RETURN                                                   P3
*       CODE  REASON                  ACTION                     P3
*       -------------------------------------------------------- P3
*          0  NO ERRORS ENCOUNTERED.  CONTINUE WITH LOGON.       P3
*       -------------------------------------------------------- P3
*       NON-0 TPUT/TGET RETURN CODE.  DENY LOGON.                P3
*       -------------------------------------------------------- P3
*                                                                    P3
* MESSAGES -                                                         P3
*    NONE                                                            P3
*                                                                    P3
*********************************************************************P3
         EJECT                                                       P3
UPDINFO  DS    0H                                                    P3
         ST    R14,UINFSAVE        SAVE RETURN ADDRESS               P3
         ST    R2,UINFSAVE+4       SAVE R2                           P3
         ST    R3,UINFSAVE+8       SAVE R3                           P3
         EJECT                                                       P3
*********************************************************************P3
*                                                                    P3
*       IF NEW PASSWORD WAS SPECIFIED, UPDATE CHGPSWD.               P3
*                                                                    P3
*********************************************************************P3
         CLI   GNEWPSWD,0          WAS NEW PASSWORD SPECIFIED        P3
         BE    UINFGPAS            NO - CONTINUE                     P3
         LA    R1,MCHGPSWD         YES - POINT TO KEY FOR CHGPSWD    P3
         BAL   R14,FINDINFO        FIND CHGPSWD                      P3
         L     R15,CVTPTR          GET ADDRESS OF CVT                P3
         MVC   8(7,R1),DATEMASK    MOVE IN EDIT MASK FOR DATE        P3
         ED    8(7,R1),CVTDATE+1-CVTMAP(R15) EDIT IN TODAY'S DATE    P3
UINFGPAS DS    0H                                                    P3
         EJECT                                                       P3
*********************************************************************P3
*                                                                    P3
*       UPDATE LASTON.                                               P3
*                                                                    P3
*********************************************************************P3
         LA    R1,MLASTON          POINT TO KEY FOR LASTON           P3
         BAL   R14,FINDINFO        FIND LASTON                       P3
         L     R15,CVTPTR          GET ADDRESS OF CVT                P3
         MVC   8(7,R1),DATEMASK    MOVE IN EDIT MASK FOR DATE        P3
         ED    8(7,R1),CVTDATE+1-CVTMAP(R15) EDIT IN TODAY'S DATE    P3
         MVC   16(8,R1),GTERMID    COPY TERMINAL TO INFO BLOCK       P3
         EJECT                                                       P3
*********************************************************************P3
*                                                                    P3
*       IF 'TIME' WAS SPECIFIED, UPDATE DEFTIME.                     P3
*                                                                    P3
*********************************************************************P3
         CLI   GCPUTIME,0          WAS 'TIME' SPECIFIED              P3
         BE    UINFGCPU            NO - CONTINUE                     P3
         LA    R1,MDEFTIME         YES - POINT TO KEY FOR DEFTIME    P3
         BAL   R14,FINDINFO        FIND DEFTIME                      P3
         MVC   9(L'GCPUTIME,R1),GCPUTIME UPDATE DEFTIME              P3
UINFGCPU DS    0H                                                    P3
         EJECT                                                       P3
*********************************************************************P3
*                                                                    P3
*       REWRITE INFO BLOCK.                                          P3
*                                                                    P3
*********************************************************************P3
         L     R2,ADDRINFO         GET ADDRESS OF INFO BLOCK BUFFER  P3
         L     R3,GLENINFO         GET LENGTH OF INFO BLOCK          P3
         WRITE DECB,SF,GSYSUADS,(R2),(R3),MF=E REWRITE INFO BLOCK    P3
         CHECK DECB                WAIT ON I/O                       P3
         EJECT                                                       P3
*********************************************************************P3
*                                                                    P3
*       RETURN TO THE MAINLINE.                                      P3
*                                                                    P3
*********************************************************************P3
UINFRTRN DS    0H                                                    P3
         SLR   R15,R15             SET RETURN CODE                   P3
UINFEXIT DS    0H                                                    P3
         L     R14,UINFSAVE        RESTORE RETURN ADDRESS            P3
         L     R2,UINFSAVE+4       RESTORE R2                        P3
         L     R3,UINFSAVE+8       RESTORE R3                        P3
         BR    R14                 RETURN TO CALLER                  P3
         TITLE 'BUILD JCL ROUTINE'                                   P3
*********************************************************************P3
*                                                                    P3
* ROUTINE NAME -                                                     P3
*    BUILDJCL                                                        P3
*                                                                    P3
* FUNCTION -                                                         P3
*    1) MOVE MODEL JCL TO JCL IMAGES AREA.                           P3
*    2) BUILD JOB CARD.                                              P3
*    3) BUILD EXEC CARD.                                             P3
*    4) BUILD JOBPARM CARD.                                          P3
*    5) CLEAN UP JOBPARM CARD AND SET LENGTH IN DOPE VECTOR          P3
*    6) CHECKS TO SEE IF THE USER IS A CORPORATE USER.  I.E. USERID  P3
*       BEGINS WITH 'B'.  IF NOT, ISSUE MESSAGE AND IGNORE JCL.      P3
*    7) GET JCL FROM USER.                                           P3
*    8) INDICATE JCL AND RETURN TO CALLER.                           P3
*                                                                    P3
* OPERATION -                                                        P3
*                                                                    P3
* ENTRY POINTS -                                                     P3
*    BUILDJCL                                                        P3
*                                                                    P3
* EXTERNAL REFERENCES -                                              P3
*    FINDINFO                                                        P3
*                                                                    P3
* INPUT -                                                            P3
*    NONE                                                            P3
*                                                                    P3
* OUTPUT -                                                           P3
*    REGISTER 15 CONTAINS THE RETURN CODE FOR THE MAINLINE.  THIS    P3
*    WILL BE AS FOLLOWS -                                            P3
*       -------------------------------------------------------- P3
*       RETURN                                                   P3
*       CODE  REASON                  ACTION                     P3
*       -------------------------------------------------------- P3
*          0  NO ERRORS ENCOUNTERED.  CONTINUE WITH LOGON.       P3
*       -------------------------------------------------------- P3
*       NON-0 TPUT/TGET RETURN CODE.  DENY LOGON.                P3
*       -------------------------------------------------------- P3
*                                                                    P3
* MESSAGES -                                                         P3
*    'ENTER NEXT JCL CARD OR 'END''                                  P3
*    'ENTER LAST JCL CARD OR 'END''                                  P3
*    'JCL IGNORED'                                                   P3
*                                                                    P3
*********************************************************************P3
         EJECT                                                       P3
BUILDJCL DS    0H                                                    P3
         ST    R14,BJCLSAVE        SAVE RETURN ADDRESS               P3
         ST    R2,BJCLSAVE+4       SAVE R2                           P3
         ST    R3,BJCLSAVE+8       SAVE R3                           P3
         EJECT                                                       P3
*********************************************************************P3
*                                                                    P3
*       MOVE MODEL JCL TO JCL IMAGES AREA.                           P3
*                                                                    P3
*********************************************************************P3
         L     R15,ADDRPARM        GET ADDRESS OF ORIGINAL PARM      P3
         L     R15,28(,R15)        GET ADDRESS OF JCL DESCRIPTOR     P3
         L     R0,0(,R15)          GET ADDRESS OF JCL IMAGES         P3
         LR    R3,R0               SAVE ADDRESS OF JCL IMAGES        P3
         LH    R1,4(,R15)          GET LENGTH OF JCL IMAGES          P3
         LA    R14,JCLMODEL        GET ADDRESS OF JCL MODEL          P3
         L     R15,LJCLMODL        GET FILL CHARACTER AND LENGTH     P3
         MVCL  R0,R14              MOVE MODEL JCL                    P3
         EJECT                                                       P3
*********************************************************************P3
*                                                                    P3
*       BUILD JOB CARD.                                              P3
*                                                                    P3
*********************************************************************P3
         MVC   2(7,R3),GUSERID     MOVE IN USERID                    P3
         MVC   14(8,R3),GACCTNUM   MOVE IN ACCOUNT NUMBER            P3
         LA    R1,MPROGNAM         POINT TO KEY FOR PROGRAMMER NAME  P3
         BAL   R14,FINDINFO        FIND PROGRAMMER NAME              P3
         MVC   24(20,R3),9(R1)     MOVE IN PROGRAMMER NAME           P3
         CLI   24(R3),C' '         WAS PROGRAMMER NAME BLANK         P3
         BNE   BJCLGPNM            NO - CONTINUE                     P3
         MVC   24(7,R3),GUSERID    YES - MOVE IN USERID              P3
         TRT   24(8,R3),NXTBLANK   FIND NEXT BLANK                   P3
         MVC   0(L'DEFPNAME,R1),DEFPNAME MOVE IN DEFAULT PROG NAME   P3
BJCLGPNM DS    0H                                                    P3
         LA    R15,45(,R3)         GET ADDRESS OF 1ST BLANK          P3
         CLI   GMSGCLAS,0          WAS 'MSGCLASS' SPECIFIED          P3
         BE    BJCLGMCL            NO - CONTINUE                     P3
         MVC   0(L'MSGCLASS,R15),MSGCLASS YES - MOVE IN KEYWORD      P3
         MVC   10(1,R15),GMSGCLAS  MOVE IN MSGCLASS                  P3
         LA    R15,11(,R15)        BUMP OVER MSGCLASS                P3
BJCLGMCL DS    0H                                                    P3
         CLI   GMSGLEVL,0          WAS 'MSGLEVEL' SPECIFIED          P3
         BE    BJCLGMLE            NO - CONTINUE                     P3
         MVC   0(L'MSGLEVEL,R15),MSGLEVEL YES - MOVE IN KEYWORD      P3
         MVC   11(3,R15),GMSGLEVL  MOVE IN MSGLEVEL                  P3
         TRT   11(4,R15),NXTBLANK  FIND NEXT BLANK                   P3
         MVI   0(R1),C')'          MOVE IN ')'                       P3
BJCLGMLE DS    0H                                                    P3
         EJECT                                                       P3
*********************************************************************P3
*                                                                    P3
*       BUILD EXEC CARD.                                             P3
*                                                                    P3
*********************************************************************P3
         LA    R3,80(,R3)          BUMP OVER TO EXEC CARD            P3
         MVC   16(8,R3),GALTPROC   MOVE IN ALTERNATE PROC NAME       P3
         CLI   GALTPROC,0          WAS ALTERNATE PROC AVAILABLE      P3
         BNE   BJCLGPRC            YES - USE IT                      P3
         MVC   16(8,R3),GPROC      NO - USE PROC FROM UADS           P3
BJCLGPRC DS    0H                                                    P3
         MVC   2(8,R3),16(R3)      COPY PROCNAME TO STEPNAME         P3
         CLI   GCPUTIME,0          WAS 'TIME' SPECIFIED              P3
         BE    BJCLGCPU            NO - CONTINUE                     P3
         TRT   16(9,R3),NXTBLANK   YES - FIND NEXT BLANK             P3
         MVC   0(L'TIME,R1),TIME   MOVE IN KEYWORD                   P3
         MVC   L'TIME(L'GCPUTIME,R1),GCPUTIME MOVE IN CPUTIME        P3
BJCLGCPU DS    0H                                                    P3
         EJECT                                                       P3
*********************************************************************P3
*                                                                    P3
*       BUILD JOBPARM CARD.                                          P3
*                                                                    P3
*********************************************************************P3
         LA    R3,89(,R3)          BUMP TO JOBPARM CARD + 9          P3
         LA    R1,MFORMS           POINT TO KEY FOR FORMS            P3
         BAL   R14,FINDINFO        FIND FORMS                        P3
         CLI   9(R1),C' '          WAS FORMS BLANK                   P3
         BE    BJCLGFOR            YES - CONTINUE                    P3
         MVC   0(L'FORMS,R3),FORMS NO - MOVE IN KEYWORD              P3
         MVC   7(4,R3),9(R1)       MOVE IN FORMS                     P3
         TRT   7(5,R3),NXTBLANK    FIND NEXT BLANK                   P3
         LR    R3,R1               UPDATE POINTER                    P3
BJCLGFOR DS    0H                                                    P3
         LA    R1,MROOM            POINT TO KEY FOR ROOM             P3
         BAL   R14,FINDINFO        FIND ROOM                         P3
         CLI   9(R1),C' '          WAS ROOM BLANK                    P3
         BE    BJCLGROM            YES - CONTINUE                    P3
         MVC   0(L'ROOM,R3),ROOM   NO - MOVE IN KEYWORD              P3
         MVC   6(4,R3),9(R1)       MOVE IN ROOM                      P3
         TRT   6(5,R3),NXTBLANK    FIND NEXT BLANK                   P3
         LR    R3,R1               UPDATE POINTER                    P3
BJCLGROM DS    0H                                                    P3
         CLI   GCOPIES,0           WAS 'COPIES' SPECIFIED            P3
         BE    BJCLGCOP            YES - CONTINUE                    P3
         MVC   0(L'COPIES,R3),COPIES NO - MOVE IN KEYWORD            P3
         MVC   8(3,R3),GCOPIES     MOVE IN COPIES                    P3
         TRT   8(4,R3),NXTBLANK    FIND NEXT BLANK                   P3
         LR    R3,R1               UPDATE POINTER                    P3
BJCLGCOP DS    0H                                                    P3
         CLI   GLINECT,0           WAS 'LINECT' SPECIFIED            P3
         BE    BJCLGLIN            YES - CONTINUE                    P3
         MVC   0(L'LINECT,R3),LINECT NO - MOVE IN KEYWORD            P3
         MVC   8(3,R3),GLINECT     MOVE IN LINECT                    P3
         TRT   8(4,R3),NXTBLANK    FIND NEXT BLANK                   P3
         LR    R3,R1               UPDATE POINTER                    P3
BJCLGLIN DS    0H                                                    P3
         EJECT                                                       P3
*********************************************************************P3
*                                                                    P3
*       CLEAN UP JOBPARM CARD AND SET LENGTH IN DOPE VECTOR          P3
*                                                                    P3
*********************************************************************P3
         L     R1,ADDRPARM         GET ADDRESS OF ORIGINAL PARM      P3
         L     R2,28(,R1)          GET ADDRESS OF JCL DESCRIPTOR     P3
         L     R3,0(,R2)           GET ADDRESS OF JCL IMAGES         P3
         MVI   169(R3),C' '        CLEAN UP JOBPARM CARD             P3
         MVI   7(R2),240           ASSUME 3 JCL CARDS                P3
         CLI   170(R3),C' '        WAS JOBPARM USED                  P3
         BNE   BJCLGLEN            YES - ASSUMPTION CORRECT          P3
         MVI   7(R2),160           NO - INDICATE 2 JCL CARDS         P3
BJCLGLEN DS    0H                                                    P3
         TM    GBITS2,GJCL         WAS 'JCL' SPECIFIED               P3
         BZ    BJCLALL             NO - QUIT                         P3
         EJECT                                                       P3
*********************************************************************P3
*                                                                    P3
*       CHECKS TO SEE IF THE USER IS A CORPORATE USER.  I.E. USERID  P3
*       BEGINS WITH 'B'.  IF NOT, ISSUE MESSAGE AND IGNORE JCL.      P3
*                                                                    P3
*********************************************************************P3
         CLI   GUSERID,C'B'        IS THIS A CORPORATE USER          P3
         BE    BJCLLOOP            YES - 'JCL' OK                    P3
         LA    R0,L'BADJCL         NO - GET LENGTH OF MESSAGE        P3
         LA    R1,BADJCL           POINT TO MESSAGE                  P3
         TPUT  (1),(0),R           ISSUE MESSAGE                     P3
         B     BJCLALL             AND RETURN TO CALLER              P3
         EJECT                                                       P3
*********************************************************************P3
*                                                                    P3
*       GET JCL FROM USER.                                           P3
*                                                                    P3
*********************************************************************P3
BJCLLOOP DS    0H                                                    P3
         LH    R14,4(,R2)          GET MAXIMUM LENGTH OF JCL         P3
         SH    R14,6(,R2)          SUBTRACT OFF WHAT HAS BEEN USED   P3
         LTR   R14,R14             IS AREA FULL                      P3
         BZ    BJCLALL             YES - QUIT                        P3
         CH    R14,HALF80          NO - IS THIS LAST CARD            P3
         BNE   BJCLNLST            NO - CONTINUE                     P3
         LA    R0,L'LASTCARD       YES - GET LENGTH OF MESSAGE       P3
         LA    R1,LASTCARD         POINT TO MESSAGE                  P3
         TPUT  (1),(0),R           ISSUE MESSAGE                     P3
         B     BJCLTGET            AND READ RESPONSE                 P3
BJCLNLST DS    0H
         LA    R0,L'NEXTCARD       YES - GET LENGTH OF MESSAGE       P3
         LA    R1,NEXTCARD         POINT TO MESSAGE                  P3
         TPUT  (1),(0),R           ISSUE MESSAGE                     P3
BJCLTGET DS    0H                                                    P3
         LA    R0,80               GET LENGTH FOR TPUT               P3
         L     R3,0(,R2)           GET ADDRESS OF JCL AREA           P3
         AH    R3,6(,R2)           GET ADDRESS OF NEXT JCL CARD      P3
         LR    R1,R3               GET ADDRESS FOR TGET              P3
         ICM   R1,B'1000',HEX80    SET OPTIONS                       P3
         MVC   0(80,R3),BLANKS     BLANK INPUT AREA                  P3
         TGET  (1),(0),R           READ RESPONSE                     P3
         LTR   R15,R15             WAS RETURN CODE ZERO              P3
         BZ    BJCLTGOK            YES - CONTINUE                    P3
         BAL   R14,BJCLTGER        NO - GO TO ERROR ROUTINE          P3
BJCLTGOK DS    0H                                                    P3
         OC    0(80,R3),BLANKS     FORCE TO UPPERCASE                P3
         CLC   KEND,0(R3)          WAS THIS 'END'                    P3
         BE    BJCLALL             YES - QUIT                        P3
         CLI   0(R3),C' '          NO - WAS THIS BLANK LINE          P3
         BE    BJCLLOOP            YES - ASK AGAIN                   P3
         LH    R15,6(,R2)          NO - GET CURRENT LENGTH           P3
         LA    R15,80(,R15)        ADD THIS CARD TO IT               P3
         STH   R15,6(,R2)          UPDATE STRING DOPE VECTOR         P3
         B     BJCLLOOP            AND LOOP                          P3
         EJECT                                                       P3
*********************************************************************P3
*                                                                    P3
*       INDICATE JCL AND RETURN TO CALLER.                           P3
*                                                                    P3
*********************************************************************P3
BJCLALL  DS    0H                                                    P3
         L     R1,ADDRPARM         GET ADDRESS OF ORIGINAL PARM      P3
         L     R15,0(,R1)          GET ADDRESS OF CONTROL SW DESC    P3
         L     R15,0(,R15)         GET ADDRESS OF CONTROL SWITCHES   P3
         OI    0(R15),KBIT6        INDICATE JCL                      P3
BJCLRTRN DS    0H                                                    P3
         SLR   R15,R15             SET RETURN CODE                   P3
BJCLEXIT DS    0H                                                    P3
         L     R14,BJCLSAVE        RESTORE RETURN ADDRESS            P3
         L     R2,BJCLSAVE+4       RESTORE R2                        P3
         L     R3,BJCLSAVE+8       RESTORE R3                        P3
         BR    R14                 RETURN TO CALLER                  P3
         EJECT                                                       P3
*********************************************************************P3
*                                                                    P3
*       IF RETURN CODE DID NOT INDICATE ATTENTION INTERRUPT, ISSUE   P3
*       MESSAGE TO USER AND DENY LOGON.                              P3
*                                                                    P3
*********************************************************************P3
BJCLTGER DS    0H                  NON-ZERO RETURN CODE FROM TGET    P3
         LA    R1,8                GET 8 IN R1 FOR COMPARISON        P3
         CR    R1,R15              WAS THIS AN ATTENTION INTERRUPT   P3
         BNE   BJCLTGNA            NO - GO ISSUE MESSAGE             P3
         TM    GBITS1,GATTNHIT     YES - DID I GET ANY INPUT         P3
         BZR   R14                 NO - RETURN AS IF NULL INPUT      P3
         B     BJCLEXIT            YES - LET MAINLINE HANDLE INPUT   P3
BJCLTGNA DS    0H                  NOT AN ATTENTION INTERRUPT        P3
         LR    R2,R15              PROTECT RETURN CODE               P3
         MVC   GTGETMSG(L'TGETMSG),TGETMSG COPY MESSAGE              P3
         CVD   R2,WORKAREA         CONVERT TO DECIMAL                P3
         OI    WORKAREA+7,X'0F'    FIX SIGN                          P3
         UNPK  WORKAREA(5),WORKAREA+5(3) UNPACK                      P3
         MVC   GTGETMSG+23(4),WORKAREA+1 MOVE INTO MESSAGE           P3
         LA    R0,L'TGETMSG        GET LENGTH OF MESSAGE             P3
         LA    R1,GTGETMSG         POINT TO MESSAGE                  P3
         TPUT  (1),(0),R           ISSUE MESSAGE                     P3
         LR    R15,R2              RESTORE RETURN CODE               P3
         OI    GBITS1,GDENY        DENY LOGON                        P3
         B     BJCLEXIT            EXIT                              P3
         TITLE 'LIST INFO ROUTINE'                                   P3
*********************************************************************P3
*                                                                    P3
* ROUTINE NAME -                                                     P3
*    LISTINFO                                                        P3
*                                                                    P3
* FUNCTION -                                                         P3
*    LIST INFO ENTRIES.                                              P3
*                                                                    P3
* OPERATION -                                                        P3
*                                                                    P3
* ENTRY POINTS -                                                     P3
*    LISTINFO                                                        P3
*                                                                    P3
* EXTERNAL REFERENCES -                                              P3
*    NONE                                                            P3
*                                                                    P3
* INPUT -                                                            P3
*    NONE                                                            P3
*                                                                    P3
* OUTPUT -                                                           P3
*    NONE                                                            P3
*                                                                    P3
* MESSAGES -                                                         P3
*    LISTING OF INFO ENTRIES.                                        P3
*                                                                    P3
* COMMENTS -                                                         P3
*    THIS SUBROUTINE ASSUMES THAT THERE WILL ALWAYS BE AT LEAST ONE  P3
*    BLANK SLOT DEMARKING THE END OF THE INFO BLOCK AND THAT THE     P3
*    APPEARENCE OF THE FIRST EMPTY SLOT IS THE END OF THE VALID      P3
*    ENTRIES.  (MAY CHANGE ALL THIS LATER)                           P3
*                                                                    P3
*********************************************************************P3
         EJECT                                                       P3
LISTINFO DS    0H                                                    P3
         ST    R14,LINFSAVE        SAVE RETURN ADDRESS               P3
         ST    R2,LINFSAVE+4       SAVE R2                           P3
         TM    GBITS1,GLIST        WAS 'LISTINFO' SPECIFIED          P3
         BZ    LINFRTRN            NO - RETURN TO CALLER             P3
         L     R2,ADDRINFO         YES - GET ADDRESS OF INFO BLOCK   P3
LINFLOOP DS    0H                                                    P3
         CLI   0(R2),C' '          IS THIS THE END                   P3
         BE    LINFEXIT            YES - RETURN TO CALLER            P3
         CLI   9(R2),C' '          NO - IS THIS ENTRY EMPTY          P3
         BE    LINFNEXT            YES - SKIP THIS ENTRY             P3
         LA    R0,72               NO - GET LENGTH OF INFO ENTRY     P3
         LR    R1,R2               GET ADDRESS OF INFO ENTRY         P3
         TPUT  (1),(0),R           ISSUE MESSAGE                     P3
LINFNEXT DS    0H                                                    P3
         LA    R2,80(,R2)          BUMP TO NEXT INFO ENTRY           P3
         B     LINFLOOP            AND LOOP                          P3
LINFRTRN DS    0H                                                    P3
         SLR   R15,R15             SET RETURN CODE                   P3
LINFEXIT DS    0H                                                    P3
         L     R14,LINFSAVE        RESTORE RETURN ADDRESS            P3
         L     R2,LINFSAVE+4       RESTORE R2                        P3
         BR    R14                 RETURN TO CALLER                  P3
         TITLE 'FIND INFO SUBROUTINE'                                P3
*********************************************************************P3
*                                                                    P3
* ROUTINE NAME -                                                     P3
*    FINDINFO                                                        P3
*                                                                    P3
* FUNCTION -                                                         P3
*    FIND INFO ENTRY.                                                P3
*                                                                    P3
* OPERATION -                                                        P3
*                                                                    P3
* ENTRY POINTS -                                                     P3
*    FINDINFO                                                        P3
*                                                                    P3
* EXTERNAL REFERENCES -                                              P3
*    NONE                                                            P3
*                                                                    P3
* INPUT -                                                            P3
*    REGISTER 1 POINTS TO THE INFO KEY.                              P3
*                                                                    P3
* OUTPUT -                                                           P3
*    REGISTER 1 POINTS TO EITHER THE REQUESTED KEY OR A BLANK ENTRY  P3
*    WHICH CAN BE USED TO CREATE THE KEY.                            P3
*                                                                    P3
* MESSAGES -                                                         P3
*    NONE                                                            P3
*                                                                    P3
* COMMENTS -                                                         P3
*    THIS SUBROUTINE ASSUMES THAT THERE WILL ALWAYS BE AT LEAST ONE  P3
*    BLANK SLOT DEMARKING THE END OF THE INFO BLOCK AND THAT THE     P3
*    APPEARENCE OF THE FIRST EMPTY SLOT IS THE END OF THE VALID      P3
*    ENTRIES.  (MAY CHANGE ALL THIS LATER)                           P3
*                                                                    P3
*********************************************************************P3
         EJECT                                                       P3
FINDINFO DS    0H                                                    P3
         L     R15,ADDRINFO        GET ADDRESS OF INFO BLOCK         P3
FINFLOOP DS    0H                                                    P3
         CLI   0(R15),C' '         IS THIS THE END                   P3
         BE    FINFBLNK            YES - LET HIM USE THIS SLOT       P3
         CLC   0(8,R1),0(R15)      NO - IS THIS THIS THE REQ'D KEY   P3
         BE    FINFEXIT            YES - GET OUT                     P3
         LA    R15,80(,R15)        NO - POINT TO NEXT SLOT           P3
         B     FINFLOOP            AND LOOP                          P3
FINFBLNK DS    0H                                                    P3
         MVC   0(80,R15),BLANKS    CLEAR OUT SLOT                    P3
         MVC   0(8,R15),0(R1)      INSERT KEY                        P3
FINFEXIT DS    0H                                                    P3
         LR    R1,R15              POINT TO SLOT                     P3
         BR    R14                 RETURN TO CALLER                  P3
         TITLE 'FIND $SYSTEM$ ENTRY SUBROUTINE'                    P3.1
*******************************************************************P3.1
*                                                                  P3.1
* ROUTINE NAME -                                                   P3.1
*    FIND$SYS                                                      P3.1
*                                                                  P3.1
* FUNCTION -                                                       P3.1
*    FIND $SYSTEM$ ENTRY                                           P3.1
*                                                                  P3.1
* OPERATION -                                                      P3.1
*                                                                  P3.1
* ENTRY POINTS -                                                   P3.1
*    FIND$SYS                                                      P3.1
*                                                                  P3.1
* EXTERNAL REFERENCES -                                            P3.1
*    NONE                                                          P3.1
*                                                                  P3.1
* INPUT -                                                          P3.1
*    REGISTER 1 POINTS TO THE $SYSTEM$ KEY.                        P3.1
*                                                                  P3.1
* OUTPUT -                                                         P3.1
*    REGISTER 1 POINTS TO THE REQUESTED KEY.                       P3.1
*                                                                  P3.1
* MESSAGES -                                                       P3.1
*    NONE                                                          P3.1
*                                                                  P3.1
* COMMENTS -                                                       P3.1
*    THERE IS NO PROVISION MADE FOR NOT FINDING THE REQUESTED KEY. P3.1
*                                                                  P3.1
*******************************************************************P3.1
         EJECT                                                     P3.1
FIND$SYS DS    0H                                                  P3.1
         CLC   0(8,R1),0(R15)      IS THIS THIS THE REQ'D KEY      P3.1
         BE    FSYSEXIT            YES - GET OUT                   P3.1
         LA    R15,80(,R15)        NO - POINT TO NEXT SLOT         P3.1
         B     FIND$SYS            AND LOOP                        P3.1
FSYSEXIT DS    0H                                                  P3.1
         LR    R1,R15              POINT TO SLOT                   P3.1
         BR    R14                 RETURN TO CALLER                P3.1
         TITLE 'DAYS ELAPSED SUBROUTINE'                             P3
*********************************************************************P3
*                                                                    P3
* ROUTINE NAME -                                                     P3
*    DAYS                                                            P3
*                                                                    P3
* FUNCTION -                                                         P3
*    CALCULATE NUMBER OF DAYS ELAPSED.                               P3
*                                                                    P3
* OPERATION -                                                        P3
*                                                                    P3
* ENTRY POINTS -                                                     P3
*    DAYS                                                            P3
*                                                                    P3
* EXTERNAL REFERENCES -                                              P3
*    NONE                                                            P3
*                                                                    P3
* INPUT -                                                            P3
*    REGISTER 1 POINTS TO THE INFO RECORD.                           P3
*                                                                    P3
* OUTPUT -                                                           P3
*    REGISTER 1 CONTAINS THE NUMBER OF DAYS ELAPSED SINCE THE ENTRY  P3
*    IN THE INFO LINE.                                               P3
*                                                                    P3
* MESSAGES -                                                         P3
*    NONE                                                            P3
*                                                                    P3
* COMMENTS -                                                         P3
*    NONE                                                            P3
*                                                                    P3
*********************************************************************P3
         EJECT                                                       P3
DAYS     DS    0H                                                    P3
         XC    DAYSWORK,DAYSWORK   CLEAR WORK AREA                  .P3
         CLI   9(R1),C' '          IS DATE BLANK                     P3
         BE    DAYSNONE            YES - NO DAYS ELAPSED             P3
         MVC   WORKAREA(2),9(R1)   NO - MOVE IN YY                   P3
         MVC   WORKAREA+2(3),12(R1) MOVE IN DDD                      P3
         PACK  WORKAREA(8),WORKAREA(5) PACK YYDDD                    P3
         L     R15,CVTPTR          GET CVT POINTER                   P3
         LH    R1,CVTDATE-CVTMAP(,R15) GET CURRENT YY IN DECIMAL    .P3
         SLL   R1,4                SHIFT FOR SIGN                   .P3
         LA    R1,15(,R1)          INSERT SIGN                      .P3
         ST    R1,DAYSWORK+4       SAVE FOR LATER                   .P3
         LH    R1,WORKAREA+4       GET YY IN QUESTION IN DECIMAL    .P3
         SLL   R1,4                SHIFT FOR SIGN                   .P3
         LA    R1,15(,R1)          INSERT SIGN                      .P3
         STH   R1,WORKAREA+4       SAVE FOR LATER                   .P3
         SP    DAYSWORK,WORKAREA+4(2) SUBTRACT OFF YY IN QUESTION   .P3
         CVB   R1,DAYSWORK         CONVERT TO BINARY                .P3
         MH    R1,HALF365          MULTIPLY BY 365 DAYS              P3
         XC    WORKAREA+4(2),WORKAREA+4 ZERO YY                      P3
         SP    WORKAREA+6(2),CVTDATE+2-CVTMAP(2,R15) GET NEG. DIFF   P3
         CVB   R15,WORKAREA        CONVERT TO BINARY                 P3
         SR    R1,R15              COMBINE TO GET ELAPSED DAYS       P3
DAYSEXIT DS    0H                                                    P3
         BR    R14                 RETURN TO CALLER                  P3
         SPACE 3                                                     P3
DAYSNONE DS    0H                                                    P3
         SLR   R1,R1               NO DAYS ELAPSED                   P3
         B     DAYSEXIT            EXIT                              P3
         TITLE 'GET APF AUTHORIZATION SUBROUTINE'                    P3
*********************************************************************P3
*                                                                    P3
* ROUTINE NAME -                                                     P3
*    GETAUTH                                                         P3
*                                                                    P3
* FUNCTION -                                                         P3
*    GET APF AUTHORIZED.                                             P3
*                                                                    P3
* OPERATION -                                                        P3
*    ISSUE SVC 217 WITH R0 SET TO 1.                                 P3
*                                                                    P3
* ENTRY POINTS -                                                     P3
*    GETAUTH                                                         P3
*                                                                    P3
* EXTERNAL REFERENCES -                                              P3
*    NONE                                                            P3
*                                                                    P3
* INPUT -                                                            P3
*    NONE                                                            P3
*                                                                    P3
* OUTPUT -                                                           P3
*    NONE                                                            P3
*                                                                    P3
* MESSAGES -                                                         P3
*    NONE                                                            P3
*                                                                    P3
* COMMENTS -                                                         P3
*    NONE                                                            P3
*                                                                    P3
*********************************************************************P3
         EJECT                                                       P3
GETAUTH  DS    0H                                                    P3
         ST    R14,GAUTSAVE        SAVE RETURN ADDRESS               P3
         LA    R0,1                GET AUTHORIZATION CODE            P3
         SVC   217                 ISSUE AUTHORIZATION SVC           P3
         SLR   R15,R15             SET RETURN CODE                   P3
         L     R14,GAUTSAVE        RESTORE RETURN ADDRESS            P3
         BR    R14                 RETURN TO CALLER                  P3
         TITLE 'DEBUG ROUTINE'                                       P3
***********************************************************************
*                                                                     *
* ROUTINE NAME -                                                      *
*    DEBUG                                                            *
*                                                                     *
* FUNCTION -                                                          *
*    PROVIDE DEBUGGING INFORMATION.                                   *
*                                                                     *
* OPERATION -                                                         *
*    1) DISPLAY DEBUGGING INFORMATION.                                *
*                                                                     *
* ENTRY POINTS -                                                      *
*    DEBUG                                                            *
*                                                                     *
* EXTERNAL REFERENCES -                                               *
*    NONE                                                             *
*                                                                     *
* INPUT -                                                             *
*    NONE                                                             *
*                                                                     *
* OUTPUT -                                                            *
*    NONE                                                             *
*                                                                     *
* MESSAGES -                                                          *
*    DEBUGGING INFORMATION AS REQUIRED                                *
*                                                                     *
***********************************************************************
         EJECT
DEBUG    DS    0H
         ST    R14,DBUGSAVE        SAVE RETURN ADDRESS
         LA    R0,L'GUSERID        GET LENGTH OF MESSAGE
         LA    R1,GUSERID          POINT TO MESSAGE
         TPUT  (1),(0),R           ISSUE MESSAGE
         LA    R0,L'GPASS          GET LENGTH OF MESSAGE
         LA    R1,GPASS            POINT TO MESSAGE
         TPUT  (1),(0),R           ISSUE MESSAGE
         LA    R0,L'GACCTNUM       GET LENGTH OF MESSAGE
         LA    R1,GACCTNUM         POINT TO MESSAGE
         TPUT  (1),(0),R           ISSUE MESSAGE
         LA    R0,L'GPROC          GET LENGTH OF MESSAGE
         LA    R1,GPROC            POINT TO MESSAGE
         TPUT  (1),(0),R           ISSUE MESSAGE
         LA    R0,L'GNEWPSWD       GET LENGTH OF MESSAGE             P2
         LA    R1,GNEWPSWD         POINT TO MESSAGE                  P2
         TPUT  (1),(0),R           ISSUE MESSAGE                     P2
         L     R14,DBUGSAVE        RESTORE RETURN ADDRESS
         BR    R14                 RETURN TO CALLER
         TITLE 'ESTAE EXIT ROUTINE'
***********************************************************************
*                                                                     *
* ROUTINE NAME -                                                      *
*    ESTAEXIT                                                         *
*                                                                     *
* FUNCTION -                                                          *
*    ENTERED AFTER AN ABEND TO PASS CONTROL TO A RETRY ROUTINE.       *
*                                                                     *
* OPERATION -                                                         *
*    1) CHECKS TO SEE IF SDWA WAS OBTAINED.  IF NOT, POINT TO RETRY   *
*       ROUTINE, SAVE ABEND CODE AND RETURN TO ABEND.  IF SDWA WAS    *
*       OBTAINED, SAVE ABEND CODE AND ISSUE SETRP MACRO TO RETURN TO  *
*       ABEND SPECIFING RETRY ROUTINE.  IN EITHER CASE, DO NOT RETRY P3
*       IF THIS IS A RECURSIVE ABEND.                                P3
*                                                                     *
* ENTRY POINTS -                                                      *
*    ESTAEXIT                                                         *
*                                                                     *
* EXTERNAL REFERENCES -                                               *
*    NONE                                                             *
*                                                                     *
* INPUT -                                                             *
*    IF NO SDWA WAS OBTAINED REGISTER 1 CONTAINS THE ABEND CODE.      *
*    IF AN SDWA WAS OBTAINED REGISTER 1 CONTAINS THE ADDRESS OF THE   *
*    SDWA.                                                            *
*                                                                     *
* OUTPUT -                                                            *
*    ADDDRESS OF RETRY ROUTINE.                                       *
*                                                                     *
* MESSAGES -                                                          *
*    NONE                                                             *
*                                                                     *
***********************************************************************
         EJECT
ESTAEXIT DS    0H
         PUSH  USING               SAVE MAIN LINE'S ADDRESSABILITY
         DROP  ,                   DROP MAIN LINE'S ADDRESSABILITY   P3
         USING ESTAEXIT,R12        ESTABLISH EXIT ADDRESSABILITY
         USING GOTNAREA,R13        ESTABLISH DYNAMIC ADDRESSABILITY  P3
         LR    R12,R15             LOAD BASE
         EJECT
***********************************************************************
*                                                                     *
*       CHECKS TO SEE IF SDWA WAS OBTAINED.  IF NOT, POINT TO RETRY   *
*       ROUTINE, SAVE ABEND CODE AND RETURN TO ABEND.  IF SDWA WAS    *
*       OBTAINED, SAVE ABEND CODE AND ISSUE SETRP MACRO TO RETURN TO  *
*       ABEND SPECIFING RETRY ROUTINE.  IN EITHER CASE, DO NOT RETRY P3
*       IF THIS IS A RECURSIVE ABEND.                                P3
*                                                                     *
***********************************************************************
         LA    R15,12              GET 12 IN R15                     P3
         CR    R0,R15              WAS SDWA OBTAINED                 P3
         BNE   HAVESDWA            YES - BRANCH
         LA    R0,ESTAERTY         NO - POINT TO RETRY ROUTINE
         L     R13,16(,R2)         GET DYNAMIC BASE FROM PARM LIST P3.1
         ST    R1,ABENDCDE         SAVE ABEND CODE
         XC    ABENDPSW,ABENDPSW   INDICATE PSW NOT AVAILABLE        P3
         LA    R15,4               INDICATE RETRY ROUTINE SUPPLIED
         TM    GBITS2,GABEND       ABENDED BEFORE                    P3
         BO    NOSDWA2             YES - ABEND RECURSION             P3
         OI    GBITS2,GABEND       NO - SET ABEND INDICATOR          P3
         BR    R14                 RETURN TO CALLER
NOSDWA2  DS    0H                                                    P3
         SLR   R15,R15             INDICATE NO RETRY                 P3
         BR    R14                 RETURN TO CALLER
HAVESDWA DS    0H                  SDWA OBTAINED
         L     R2,0(,R1)           GET ADDRESS OF PARAM LIST FROM SDWA
         L     R13,16(,R2)         GET DYNAMIC BASE FROM PARM LIST P3.1
         MVC   ABENDCDE,4(R1)      SAVE ABEND CODE
         MVC   ABENDPSW,8(R1)      SAVE PSW                          P3
         ST    R14,12(,R13)        SAVE RETURN ADDRESS
         TM    GBITS2,GABEND       ABENDED BEFORE                    P3
         BO    HAVSDWA2            YES - ABEND RECURSION             P3
         OI    GBITS2,GABEND       NO - SET ABEND INDICATOR          P3
         SETRP REGS=(14),RC=4,RETADDR=ESTAERTY,FRESDWA=YES RETURN
HAVSDWA2 DS    0H                                                    P3
         SETRP REGS=(14),RC=0      DO NOT RETRY                      P3
         SPACE 3
         POP   USING               RETURN MAIN LINE'S ADDRESSABILITY
         TITLE 'ESTAE RETRY ROUTINE'
***********************************************************************
*                                                                     *
* ROUTINE NAME -                                                      *
*    ESTAERTY                                                         *
*                                                                     *
* FUNCTION -                                                          *
*    ENTERED AFTER AN ABEND TO RETRY.                                 *
*                                                                     *
* OPERATION -                                                         *
*    1) ISSUES MESSAGE TO TSO USER AND OPERATOR INDICATING TYPE OF   P2
*       ABEND AND DENIES LOGON.  IF ABEND U0522, SKIP MESSAGES AND   P3
*       SDUMP.                                                       P3
*    2) IF APF AUTHORIZED, TAKE SDUMP.                               P3
*                                                                     *
* ENTRY POINTS -                                                      *
*    ESTAERTY                                                         *
*                                                                     *
* EXTERNAL REFERENCES -                                               *
*    NONE                                                             *
*                                                                     *
* INPUT -                                                             *
*    ABENDCDE CONTAINS THE ABEND CODE PLACED THERE BY ESTAEXIT.       *
*                                                                     *
* OUTPUT -                                                            *
*    REGISTER 15 CONTAINS THE RETURN CODE FOR MAINLINE.  THIS WILL BE *
*    AS FOLLOWS -                                                     *
*       --------------------------------------------------------  *
*       RETURN                                                    *
*       CODE  REASON                  ACTION                      *
*       --------------------------------------------------------  *
*         16  LOGONXIT ABENDED.       DENY LOGON SWITCH IS SET.   *
*       --------------------------------------------------------  *
*                                                                     *
* MESSAGES -                                                          *
*    'LOGON USER EXIT ABEND TNNNN'                                    *
*    'USER-ID ON TERMINAL LOGON USER EXIT ABEND TNNNN - ASID X'NN''  P3
*                                                                     *
***********************************************************************
         EJECT
ESTAERTY DS    0H                  ESTAE RETRY ROUTINE
         L     R9,0(,R1)           RESTORE 1ST BASE FROM PARM LIST P3.1
         L     R10,4(,R1)          RESTORE 2ND BASE FROM PARM LIST P3.1
         L     R11,8(,R1)          RESTORE 3RD BASE FROM PARM LIST P3.1
         L     R12,12(,R1)         RESTORE 4TH BASE FROM PARM LIST P3.1
         L     R13,16(,R1)         RESTORE DYN BASE FROM PARM LIST P3.1
         EJECT
***********************************************************************
*                                                                     *
*       ISSUES MESSAGE TO TSO USER AND OPERATOR INDICATING TYPE OF   P2
*       ABEND AND DENIES LOGON.  IF ABEND U0522, SKIP MESSAGES AND   P3
*       SDUMP.                                                       P3
*                                                                     *
***********************************************************************
         CLC   HALF522,ABENDCDE+2  WAS THIS U0522                    P3
         BE    NOSDUMP             YES - SKIP MESSAGES AND SDUMP     P3
         MVC   GABNDMSG,ABNDMSG    NO - COPY ABEND MESSAGE           P3
         L     R15,ABENDCDE        GET COMPLETION CODE
         SLL   R15,8               SHIFT OUT TCBFLGS1
         SRL   R15,20              AND SHIFT OUT USER ABEND CODE
         LTR   R15,R15             WAS IT SYSTEM ABEND CODE
         BZ    USERABND            NO - MUST BE USER ABEND
         MVI   GABNDMSG+22,C'S'    YES - INDICATE SYSTEM ABEND
         STH   R15,WORKAREA        PUT COMPLETION CODE IN WORKAREA
         UNPK  WORKAREA+3(5),WORKAREA(3) UNPACK
         TR    WORKAREA+3(4),TRTABLE-240 CONVERT TO EBCDIC
         MVC   GABNDMSG+23(3),WORKAREA+4 MOVE TO MESSAGE
         B     ABNDTPUT            GO TELL OPERATOR
USERABND DS    0H
         MVI   GABNDMSG+22,C'U'    INDICATE USER ABEND
         L     R15,ABENDCDE        GET COMPLETION CODE               P3
         SLL   R15,20              SHIFT OUT TCBFLGS1
         SRL   R15,20              SHIFT BACK
         CVD   R15,WORKAREA        CONVERT TO DECIMAL
         OI    WORKAREA+7,X'0F'    FIX SIGN
         UNPK  WORKAREA(5),WORKAREA+5(3) UNPACK
         MVC   GABNDMSG+23(4),WORKAREA+1 MOVE TO ABEND MESSAGE
ABNDTPUT DS    0H
         LA    R0,L'ABNDMSG        GET LENGTH FOR TPUT
         LA    R1,GABNDMSG         POINT TO ABEND MESSAGE
         TPUT  (1),(0),R           TELL USER
         MVC   WORKAREA(5),GABNDMSG+22 SAVE ABEND CODE               P3
         MVC   GABNDWTO,ABNDWTO    COPY WTO TEXT TO DYNAMIC AREA     P2
         MVC   GABNDWTO+46(5),WORKAREA COPY ABEND CODE               P3
         CLI   GUSERID,0           HAS USERID BEEN RETRIEVED         P2
         BE    ABENDWTO            NO - LEAVE TEXT WITH 'UNKNOWN'    P2
         MVC   GABNDWTO+4(7),GUSERID YES - COPY TO WTO               P2
ABENDWTO DS    0H                                                    P2
         MVC   GABNDWTO+15(8),GTERMID COPY TERMID TO DYNAMIC AREA    P3
         MVC   GABNDWTO+61(2),GASID COPY ASID TO DYNAMIC AREA        P3
         WTO   MF=(E,GABNDWTO)     TELL OPERATOR                     P2
         EJECT                                                       P3
*********************************************************************P3
*                                                                    P3
*       IF APF AUTHORIZED, TAKE SDUMP.                               P3
*                                                                    P3
*********************************************************************P3
         L     R15,PSATNEW-PSA     GET ADDRESS OF MY TCB             P3
         L     R15,TCBJSCB-TCB(,R15) GET ADDRESS OF MY JSCB          P3
         TM    JSCBOPTS-IEZJSCB(R15),JSCBAUTH CAN I TAKE SDUMP       P3
         BZ    NOSDUMP             NO - SKIP SDUMP                   P3
         LH    R15,GABNDWTO        YES - GET LENGTH OF WTO           P3
         SH    R15,HALF4           GET LENGTH OF TEXT                P3
         STC   R15,GABNDWTO+3      SET UP FOR SDUMP                  P3
         MVC   GSDUMP,SDUMP        COPY SDUMP PARAMETER LIST         P3
         LA    R1,GSDUMP           POINT TO SDUMP PARAMETER LIST     P3
         SDUMP HDRAD=GABNDWTO+3,MF=(E,(1)) TAKE SDUMP                P3
NOSDUMP  DS    0H                                                    P3
         OI    GBITS1,GDENY        DENY LOGON                        P3
         LA    R15,16              SET RETURN CODE
         B     RETURN              RETURN
         TITLE 'STAX EXIT ROUTINE'                                   P3
*********************************************************************P3
*                                                                    P3
* ROUTINE NAME -                                                     P3
*    STAXEXIT                                                        P3
*                                                                    P3
* FUNCTION -                                                         P3
*    ENTERED AFTER AN ATTENTION INTERRUPT HAS OCCURED.               P3
*                                                                    P3
* OPERATION -                                                        P3
*    1) CHECKS LENGTH OF INPUT BUFFER.  IF ZERO, EXIT.               P3
*    2) UPDATES ORIGINAL INPUT BUFFER.                               P3
*    3) REBUILDS CBUF.                                               P3
*    4) POSTS CP ECB.                                                P3
*    5) RETURNS TO CALLER.                                           P3
*                                                                    P3
* ENTRY POINTS -                                                     P3
*    STAXEXIT                                                        P3
*                                                                    P3
* EXTERNAL REFERENCES -                                              P3
*    NONE                                                            P3
*                                                                    P3
* INPUT -                                                            P3
*    ATTENTION EXIT PARAMETER LIST (AEPL)                            P3
*                                                                    P3
* OUTPUT -                                                           P3
*    NONE                                                            P3
*                                                                    P3
* MESSAGES -                                                         P3
*    NONE                                                            P3
*                                                                    P3
*********************************************************************P3
         EJECT                                                       P3
STAXEXIT DS    0H                                                    P3
         SAVE  (14,12),,STAXEXIT   SAVE CALLER'S REGISTERS           P3
         USING AEPL,R1             ESTABLISH ADDRESSABILITY TO AEPL  P3
         L     R2,AEPLUSAD         GET ADDRESS OF USADDR FROM AEPL   P3
         LR    R3,R13              SAVE CALLER'S R13                 P3
         L     R9,0(,R2)           RESTORE 1ST BASE FROM PARM LIST P3.1
         L     R10,4(,R2)          RESTORE 2ND BASE FROM PARM LIST P3.1
         L     R11,8(,R2)          RESTORE 3RD BASE FROM PARM LIST P3.1
         L     R12,12(,R2)         RESTORE 4TH BASE FROM PARM LIST P3.1
         L     R13,16(,R2)         RESTORE DYN BASE FROM PARM LIST P3.1
         EJECT                                                       P3
*********************************************************************P3
*                                                                    P3
*       CHECKS LENGTH OF INPUT BUFFER.  IF ZERO, EXIT.               P3
*                                                                    P3
*********************************************************************P3
         L     R1,AEPLTAIE         GET ADDRESS OF TAIE FROM AEPL     P3
         DROP  R1                  DROP APEL ADDRESSABILITY          P3
         USING TAIE,R1             ESTABLISH ADDRESSABILITY TO TAIE  P3
         ICM   R1,B'0011',TAIEMSGL GET LENGTH OF INPUT BUFFER        P3
         DROP  R1                  DROP TAIE ADDRESSABILITY          P3
         BNZ   STAXGMSG            IF NOT ZERO - THEN GOT MESSAGE    P3
         B     STAXRTRN            ELSE - GET OUT                    P3
         EJECT                                                       P3
*********************************************************************P3
*                                                                    P3
*       UPDATES ORIGINAL INPUT BUFFER.                               P3
*                                                                    P3
*********************************************************************P3
STAXGMSG DS    0H                                                    P3
         L     R2,ADDRPARM         GET ADDRESS OF ORIGINAL PARMS     P3
         L     R2,4(,R2)           GET ADDRESS OF INPUT BUFFER DESC  P3
         L     R15,0(R2)           GET ADDRESS OF ORIGINAL BUFFER    P3
         STH   R1,6(,R2)           UPDATE ACTUAL LENGTH              P3
         EJECT                                                       P3
*********************************************************************P3
*                                                                    P3
*       REBUILDS CBUF.                                               P3
*                                                                    P3
*********************************************************************P3
         LA    R1,4(,R1)           ADD 4 FOR LENGTH AND OFFSET       P3
         STH   R1,GCBUF+CBUFLEN-CBUF UPDATE BUILT CBUF               P3
         XC    GCBUF+CBUFOFFS-CBUF(2),GCBUF+CBUFOFFS-CBUF ZERO OFF   P3
         MVC   GCBUF+CBUFTEXT-CBUF(252),0(R15) COPY STAX RESPONSE    P3
         EJECT                                                       P3
*********************************************************************P3
*                                                                    P3
*       POSTS CP ECB.                                                P3
*                                                                    P3
*********************************************************************P3
         POST  GCPECB              POST CP ECB                       P3
         OI    GBITS1,GATTNHIT     INDICATE ATTENTION INTERRUPT      P3
         EJECT                                                       P3
*********************************************************************P3
*                                                                    P3
*       RETURNS TO CALLER.                                           P3
*                                                                    P3
*********************************************************************P3
STAXRTRN DS    0H                                                    P3
         LR    R13,R3              RESTORE CALLER'S R13              P3
         RETURN (14,12),RC=0       RETURN TO CALLER                  P3
         TITLE 'DUMMY STAX EXIT ROUTINE'                             P3
*********************************************************************P3
*                                                                    P3
* ROUTINE NAME -                                                     P3
*    STAXDXIT                                                        P3
*                                                                    P3
* FUNCTION -                                                         P3
*    ENTERED AFTER AN ATTENTION INTERRUPT HAS OCCURED.               P3
*                                                                    P3
* OPERATION -                                                        P3
*    1) RETURNS TO CALLER.                                           P3
*                                                                    P3
* ENTRY POINTS -                                                     P3
*    STAXDXIT                                                        P3
*                                                                    P3
* EXTERNAL REFERENCES -                                              P3
*    NONE                                                            P3
*                                                                    P3
* INPUT -                                                            P3
*    ATTENTION EXIT PARAMETER LIST (AEPL)                            P3
*                                                                    P3
* OUTPUT -                                                           P3
*    NONE                                                            P3
*                                                                    P3
* MESSAGES -                                                         P3
*    NONE                                                            P3
*                                                                    P3
* NOTES -                                                            P3
*    ADDRESSABILITY IS ESTABLISHED TO REST OF EXIT AND TO DYNAMIC    P3
*    AREA IN CASE THIS MAY BE NEEDED IN THE FUTURE.                  P3
*                                                                    P3
*********************************************************************P3
         EJECT                                                       P3
STAXDXIT DS    0H                                                    P3
         SAVE  (14,12),,STAXDXIT   SAVE CALLER'S REGISTERS           P3
         USING AEPL,R1             ESTABLISH ADDRESSABILITY TO AEPL  P3
         L     R2,AEPLUSAD         GET ADDRESS OF USADDR FROM AEPL   P3
         LR    R3,R13              SAVE CALLER'S R13                 P3
         L     R9,0(,R2)           RESTORE 1ST BASE FROM PARM LIST P3.1
         L     R10,4(,R2)          RESTORE 2ND BASE FROM PARM LIST P3.1
         L     R11,8(,R2)          RESTORE 3RD BASE FROM PARM LIST P3.1
         L     R12,12(,R2)         RESTORE 4TH BASE FROM PARM LIST P3.1
         L     R13,16(,R2)         RESTORE DYN BASE FROM PARM LIST P3.1
         EJECT                                                       P3
*********************************************************************P3
*                                                                    P3
*       RETURNS TO CALLER.                                           P3
*                                                                    P3
*********************************************************************P3
         LR    R13,R3              RESTORE CALLER'S R13              P3
         RETURN (14,12),RC=0       RETURN TO CALLER                  P3
         TITLE 'TIMER EXIT ROUTINE'                                  P3
*********************************************************************P3
*                                                                    P3
* ROUTINE NAME -                                                     P3
*    TIMERXIT                                                        P3
*                                                                    P3
* FUNCTION -                                                         P3
*    ENTERED AFTER A STIMER INTERRUPT HAS OCCURED.                   P3
*                                                                    P3
* OPERATION -                                                        P3
*    1) ISSUE MESSAGE TO OPERATOR AND ABEND WITH U0522.              P3
*                                                                    P3
* ENTRY POINTS -                                                     P3
*    TIMERXIT                                                        P3
*                                                                    P3
* EXTERNAL REFERENCES -                                              P3
*    NONE                                                            P3
*                                                                    P3
* INPUT -                                                            P3
*    NONE                                                            P3
*                                                                    P3
* OUTPUT -                                                           P3
*    NONE                                                            P3
*                                                                    P3
* MESSAGES -                                                         P3
*    'USER-ID ON TERMINAL LOGON TERMINATED DUE TO WAIT TIME EXCEEDED P3
*     - ASID X'NN''                                                  P3
*                                                                    P3
* COMMENTS -                                                         P3
*    THIS ROUTINE IS MOVED TO GTIMRXIT BY THE HOUSEKEEPING ROUTINE.  P3
*    THE LABEL AFTER LTIMRXIT, TTIMRWTO MUST BE KEPT ALIGNED WITH    P3
*    GTIMRWTO IN THE DYNAMIC AREA.                                   P3
*                                                                    P3
*********************************************************************P3
         EJECT                                                       P3
TIMERXIT DS    0F                                                    P3
         PUSH  USING               SAVE MAINLINE'S ADDRESSABILITY    P3
         DROP  ,                   DROP MAINLINE'S ADDRESSABILITY    P3
         SAVE  (14,12),,TIMERXIT   SAVE CALLER'S REGISTERS           P3
         USING TIMERXIT,R12        GET TEMPORARY ADDRESSABILITY      P3
         LR    R12,R15             SET TEMPORARY BASE                P3
         EJECT                                                       P3
*********************************************************************P3
*                                                                    P3
*       ISSUE MESSAGE TO OPERATOR AND ABEND WITH U0522.              P3
*                                                                    P3
*********************************************************************P3
         WTO   MF=(E,TTIMRWTO)     TELL OPERATOR WHY                 P3
         ABEND 522                 AND ABEND WITH U0522              P3
         DS    F                   FILL OUT TO WORD IF NECESSARY     P3
LTIMRXIT EQU   *-TIMERXIT          CALCULATE LENGTH OF STIMER EXIT   P3
TTIMRWTO DS    C                   CORRESPONDS TO GTIMRWTO AFTER MOV P3
         SPACE 3                                                     P3
         POP   USING               RESTORE MAINLINE'S ADDRESSABILITY P3
         TITLE 'VALIDITY CHECK ROUTINE FOR ACCOUNT NUMBER'
***********************************************************************
*                                                                     *
* ROUTINE NAME -                                                      *
*    VACCT                                                            *
*                                                                     *
* FUNCTION -                                                          *
*    ENTERED AS A VALIDITY CHECKING ROUTINE FROM IKJPARS TO VERIFY    *
*    THE KEYWORD PARAMETER 'ACCT'.                                    *
*                                                                     *
* OPERATION -                                                         *
*    1) IF ONLY ONE ACCOUNT NUMBER IS DEFINED FOR THIS PASSWORD,     P3
*       IGNORES ACCOUNT FROM PARSE.                                  P3
*    2) CHECKS THE LENGTH OF THE ACCOUNT NUMBER TO ENSURE THAT IT IS  *
*       EIGHT (8) BYTES LONG.  IF NOT, LETS IKJPARS ISSUE MESSAGE AND *
*       PROMPT FOR NEW ACCOUNT NUMBER.                                *
*    3) RETURN TO IKJPARS.                                            *
*                                                                     *
* ENTRY POINTS -                                                      *
*    VACCT                                                            *
*                                                                     *
* EXTERNAL REFERENCES -                                               *
*    NONE                                                            P3
*                                                                     *
* INPUT -                                                             *
*    REGISTER 1 POINTS TO A THREE WORD PARAMETER LIST OF THE          *
*    FOLLOWING FORMAT -                                               *
*       --------------------------------------------------------  *
*       NUMBER OF                                                 *
*       BYTES     FIELD   CONTENTS OR MEANING                     *
*       --------------------------------------------------------  *
*           4     PDEADR  THE ADDRESS OF THE PDE                  *
*       --------------------------------------------------------  *
*           4     USERWORDTHE ADDRESS OF THE USER WORK AREA.      *
*       --------------------------------------------------------  *
*           4     VALMSG  THE ADDRESS OF A SECOND LEVEL MESSAGE.  *
*       --------------------------------------------------------  *
*                                                                     *
* OUTPUT -                                                            *
*    REGISTER 15 CONTAINS THE RETURN CODE FOR IKJPARS.  THIS WILL BE  *
*    AS FOLLOWS -                                                     *
*       --------------------------------------------------------  *
*       RETURN                                                    *
*       CODE  REASON                  ACTION                      *
*       --------------------------------------------------------  *
*          0  ACCOUNT NUMBER IS VALID.IKJPARS CONTINUE.           *
*       --------------------------------------------------------  *
*          4  ACCOUNT NUMBER IS NOT   IKJPARS WILL ISSUE MESSAGE  *
*             VALID.                  AND PROMPT.                 *
*       --------------------------------------------------------  *
*                                                                     *
* MESSAGES -                                                          *
*    NONE                                                             *
*                                                                    P3
* COMMENTS -                                                         P3
*    THIS ROUTINE OPERATES AS A SUBROUTINE TO IKJPARS.  SINCE        P3
*    THE SAVE AREA POINTED TO BY THE MAINLINE'S R13 IS BEING USED BY P3
*    IKJPARS, THIS ROUTINE MUST USE ANOTHER SAVE AREA.  HOWEVER,     P3
*    THERE IS STILL NEED TO REFERENCE THE MAINLINE'S DYNAMIC AREA.   P3
*    ADDRESSABILITY TO THIS IS PROVIDED BY USING R8.               P3.1
*                                                                     *
***********************************************************************
         EJECT
VACCT    DS    0H
         PUSH  USING               SAVE MAIN LINE'S ADDRESSABILITY
         SAVE  (14,12),,VACCT      SAVE PARSE'S REGISTERS
         L     R2,4(,R1)           GET ADDRESS OF USERWORD
         L     R9,0(,R2)           RESTORE 1ST BASE FROM PARM LIST P3.1
         L     R10,4(,R2)          RESTORE 2ND BASE FROM PARM LIST P3.1
         L     R11,8(,R2)          RESTORE 3RD BASE FROM PARM LIST P3.1
         L     R12,12(,R2)         RESTORE 4TH BASE FROM PARM LIST P3.1
         L     R8,16(,R2)          RESTORE DYN BASE FROM PARM LIST P3.1
         DROP  R13                 DROP MAIN LINE'S ADDRESSABILITY
         USING GOTNAREA,R8         GET ADDRESSABILITY TO DYN AREA  P3.1
         LA    R15,VERSAVEA        GET ADDRESS OF VER ROUTINES SAVEAREA
         ST    R13,4(,R15)         CHAIN
         ST    R15,8(,R13)           SAVE
         LR    R13,R15                 AREAS
         EJECT                                                       P3
*********************************************************************P3
*                                                                    P3
*       IF ONLY ONE ACCOUNT NUMBER IS DEFINED FOR THIS PASSWORD,     P3
*       IGNORES ACCOUNT FROM PARSE.                                  P3
*                                                                    P3
*********************************************************************P3
         L     R2,ADDRUBUF         GET ADDRESS OF SYS1.UADS BUFFER   P3
         L     R15,PASSOFF         NO - GET OFFSET TO PSWD OFF BLOCK P3
         L     R15,4(R2,R15)       GET OFFSET OF ACCT # OFFSET BLOCK P3
         AR    R15,R2              GET ADDRESS OF ACCT # OFFSET BLK  P3
         TM    0(R15),KBIT0        MORE THAN ONE ACCOUNT NUMBER      P3
         BZ    VACTCHEK            YES - GO CHECK THIS ONE           P3
         L     R14,8(,R15)         NO - GET OFFSET OF ACCT # BLOCK   P3
         AR    R14,R2              GET ADDRESS OF ACCT # DATA BLOCK  P3
         MVC   GACCTNUM,45(R14)    MOVE TO DYNAMIC AREA              P3
         SLR   R15,R15             SET RETURN CODE                   P3
         B     VACTEXIT            AND EXIT                          P3
         EJECT
***********************************************************************
*                                                                     *
*       CHECKS THE LENGTH OF THE ACCOUNT NUMBER TO ENSURE THAT IT IS  *
*       EIGHT (8) BYTES LONG.  IF NOT, LETS IKJPARS ISSUE MESSAGE AND *
*       PROMPT FOR NEW ACCOUNT NUMBER.                                *
*                                                                     *
***********************************************************************
VACTCHEK DS    0H
         L     R2,0(,R1)           GET ADDRESS OF PDE
         TM    GBITS2,GDISPLAY     WAS THIS A DISPLAY TERMINAL       P3
         BZ    VACTGOOD            NO - IGNORE FROM HARDCOPY         P3
         CLI   5(R2),8             YES - WAS LEN OF ACCT # 8 BYTES   P3
         BNE   VACTBAD             NO - QUIT NOW
         L     R2,0(,R2)           YES - GET ADDRESS OF ACCOUNT NUMBER
         MVC   GACCTNUM,0(R2)      MOVE TO DYNAMIC AREA
VACTGOOD DS    0H                  ACCTNUM RIGHT LENGTH              P3
         SLR   R15,R15             SET RETURN CODE
         EJECT
***********************************************************************
*                                                                     *
*       RETURN TO IKJPARS.                                            *
*                                                                     *
***********************************************************************
VACTEXIT DS    0H
         L     R13,4(,R13)         UNCHAIN SAVE AREAS
         RETURN (14,12),RC=(15)    RETURN TO PARSE
         EJECT                                                       P3
*********************************************************************P3
*                                                                    P3
*       LET PARSE ISSUE MESSAGE AND PROMPT FOR NEW ACCOUNT NUMBER.   P3
*                                                                    P3
*********************************************************************P3
VACTBAD  DS    0H                                                    P3
         LA    R15,4               LET PARSE ISSUE MESSAGE           P3
         B     VACTEXIT            AND EXIT                          P3
         SPACE 3                                                     P3
         POP   USING               RESTORE MAIN LINE'S ADDRESSABILITY
         TITLE 'VALIDITY CHECK ROUTINE FOR PROCEDURE NAME'
***********************************************************************
*                                                                     *
* ROUTINE NAME -                                                      *
*    VPROC                                                            *
*                                                                     *
* FUNCTION -                                                          *
*    ENTERED AS A VALIDITY CHECKING ROUTINE FROM IKJPARS TO VERIFY    *
*    THE KEYWORD PARAMETER 'PROC'.                                    *
*                                                                     *
* OPERATION -                                                         *
*    1) CHECKS TO SEE IF THE USER IS A CORPORATE USER.  I.E. USERID   *
*       BEGINS WITH 'B'.  ISSUE MESSAGE AND IGNORE PROCEDURE NAME.   P3
*    2) EXTRACTS THE PROC NAME FROM THE PDE AND PLACES IT IN         P3
*       GALTPROC PADDED WITH BLANKS.                                 P3
*    3) RETURN TO IKJPARS.                                            *
*                                                                     *
* ENTRY POINTS -                                                      *
*    VPROC                                                            *
*                                                                     *
* EXTERNAL REFERENCES -                                               *
*    NONE                                                             *
*                                                                     *
* INPUT -                                                             *
*    REGISTER 1 POINTS TO A THREE WORD PARAMETER LIST OF THE          *
*    FOLLOWING FORMAT -                                               *
*       --------------------------------------------------------  *
*       NUMBER OF                                                 *
*       BYTES     FIELD   CONTENTS OR MEANING                     *
*       --------------------------------------------------------  *
*           4     PDEADR  THE ADDRESS OF THE PDE                  *
*       --------------------------------------------------------  *
*           4     USERWORDTHE ADDRESS OF THE USER WORK AREA.      *
*       --------------------------------------------------------  *
*           4     VALMSG  THE ADDRESS OF A SECOND LEVEL MESSAGE.  *
*       --------------------------------------------------------  *
*                                                                     *
* OUTPUT -                                                            *
*    REGISTER 15 CONTAINS THE RETURN CODE FOR IKJPARS.  THIS WILL BE  *
*    AS FOLLOWS -                                                     *
*       --------------------------------------------------------  *
*       RETURN                                                    *
*       CODE  REASON                  ACTION                      *
*       --------------------------------------------------------  *
*          0  PROCEDURE NAME IS VALID.IKJPARS CONTINUE.           *
*       --------------------------------------------------------  *
*                                                                     *
* MESSAGES -                                                          *
*    'PROCEDURE IGNORED'                                             P3
*                                                                    P3
* COMMENTS -                                                         P3
*    THIS ROUTINE OPERATES AS A SUBROUTINE TO IKJPARS.  SINCE        P3
*    THE SAVE AREA POINTED TO BY THE MAINLINE'S R13 IS BEING USED BY P3
*    IKJPARS, THIS ROUTINE MUST USE ANOTHER SAVE AREA.  HOWEVER,     P3
*    THERE IS STILL NEED TO REFERENCE THE MAINLINE'S DYNAMIC AREA.   P3
*    ADDRESSABILITY TO THIS IS PROVIDED BY USING R8.               P3.1
*                                                                     *
***********************************************************************
         EJECT
VPROC    DS    0H
         PUSH  USING               SAVE MAIN LINE'S ADDRESSABILITY
         SAVE  (14,12),,VPROC      SAVE PARSE'S REGISTERS
         L     R2,4(,R1)           GET ADDRESS OF USERWORD
         L     R9,0(,R2)           RESTORE 1ST BASE FROM PARM LIST P3.1
         L     R10,4(,R2)          RESTORE 2ND BASE FROM PARM LIST P3.1
         L     R11,8(,R2)          RESTORE 3RD BASE FROM PARM LIST P3.1
         L     R12,12(,R2)         RESTORE 4TH BASE FROM PARM LIST P3.1
         L     R8,16(,R2)          RESTORE DYN BASE FROM PARM LIST P3.1
         DROP  R13                 DROP MAIN LINE'S ADDRESSABILITY
         USING GOTNAREA,R8         GET ADDRESSABILITY TO DYN AREA  P3.1
         LA    R15,VERSAVEA        GET ADDRESS OF VER ROUTINES SAVEAREA
         ST    R13,4(,R15)         CHAIN
         ST    R15,8(,R13)           SAVE
         LR    R13,R15                 AREAS
         EJECT
***********************************************************************
*                                                                     *
*       CHECKS TO SEE IF THE USER IS A CORPORATE USER.  I.E. USERID   *
*       BEGINS WITH 'B'.  ISSUE MESSAGE AND IGNORE PROCEDURE NAME.   P3
*                                                                     *
***********************************************************************
         CLI   GUSERID,C'B'        IS THIS A CORPORATE USER
         BE    VPRCGOOD            YES - 'PROC' OK
         LA    R0,L'BADPROC        NO - GET LENGTH OF MESSAGE
         LA    R1,BADPROC          POINT TO MESSAGE
         TPUT  (1),(0),R           ISSUE MESSAGE
         SLR   R15,R15             SET RETURN CODE                   P3
         B     VPRCEXIT            AND RETURN TO PARSE
         EJECT
***********************************************************************
*                                                                     *
*       EXTRACTS THE PROC NAME FROM THE PDE AND PLACES IT IN         P3
*       GALTPROC PADDED WITH BLANKS.                                 P3
*                                                                     *
***********************************************************************
VPRCGOOD DS    0H                  USER AUTHORIZED TO USE 'PROC'
         L     R2,0(,R1)           GET ADDRESS OF PDE
         MVC   GALTPROC,BLANKS     BLANK PROC NAME                   P3
         L     R1,0(R2)            GET ADDRESS OF PROC NAME
         IC    R15,5(R2)           GET LENGTH OF PROC NAME
         BCTR  R15,0               DECREMENT FOR EXECUTE
         EX    R15,MVCPROC         MOVE PROC NAME
         SLR   R15,R15             SET RETURN CODE
         EJECT
***********************************************************************
*                                                                     *
*       RETURN TO IKJPARS.                                            *
*                                                                     *
***********************************************************************
VPRCEXIT DS    0H
         L     R13,4(,R13)         UNCHAIN SAVE AREAS
         RETURN (14,12),RC=(15)    RETURN TO PARSE
         EJECT
***********************************************************************
*                                                                     *
*       EXECUTED INSTRUCTIONS FOR VPROC.                              *
*                                                                     *
***********************************************************************
MVCPROC  MVC   GALTPROC(*-*),0(R1) MOVE PROC NAME                    P3
         SPACE 3
         POP   USING               RESTORE MAIN LINE'S ADDRESSABILITY
         TITLE 'VALIDITY CHECK ROUTINE FOR USERID'
***********************************************************************
*                                                                     *
* ROUTINE NAME -                                                      *
*    VUSERID                                                          *
*                                                                     *
* FUNCTION -                                                          *
*    ENTERED AS A VALIDITY CHECKING ROUTINE FROM IKJPARS TO VERIFY    *
*    THE POSITIONAL VALUE OF USERID.                                  *
*                                                                     *
* OPERATION -                                                         *
*    1) EXTRACTS THE USERID FROM THE PDE AND PLACES IT IN GUSERID     *
*       PADDED WITH BLANKS.                                           *
*    2) CHECKS TO SEE IF THE USERID IS IN THE TABLE OF USERIDS TO     *
*       NEVER BE DENIED LOGON.  IF SO, BYPASS DIVISIONAL QUOTA        *
*       CHECKING.                                                     *
*    3) CHECKS THE NUMBER OF USERS ALLOWED TO BE LOGGED ON FOR THIS   *
*       USER'S DIVISION.  IF ZERO, THE LOGON IS DENIED.  OTHERWISE,   *
*       THE ASCBS ARE SEARCHED TO DETERMINE IF THE DIVISIONAL QUOTA   *
*       IS CURRENTLY LOGGED ON.  IF SO, THE LOGON IS DENIED.          *
*    4) CHECKS TO SEE IF THE USERID IS FOUND IN SYS1.UADS. IF NOT,    *
*       THE LOGON IS DENIED.                                         P3
*    5) IF INFO BLOCK IS AVAILABLE, GET TTR.  IF NOT, CREATE ONE     P3
*       PADDED WITH BLANKS.                                          P3
*    6) CREATE INFO BLOCK.                                           P3
*    7) READ DATA BLOCKS.                                            P3
*    8) IF NEW PASSWORD WAS SUPPLIED ON LOGON, MOVE IT TO GNEWPSWD   P2
*       PADDED WITH BLANKS.                                          P2
*    9) BUILD LOG WTO MESSAGE AND ISSUE IT.                          P3
*   10) IF PASSWORD WAS NOT SUPPLIED ON LOGON, PROMPT USER FOR IT NOW.*
*       MOVE PASSWORD TO GPASS PADDED WITH BLANKS.                    *
*   12) SEARCH UADS DATA BLOCKS FOR MATCHING PASSWORD.  IF NOT FOUND, *
*       THE USER IS PROMPTED FOR ANOTHER PASSWORD.                    *
*   12) RETURN TO IKJPARS.                                            *
*                                                                     *
* ENTRY POINTS -                                                      *
*    VUSERID                                                          *
*                                                                     *
* EXTERNAL REFERENCES -                                               *
*    GETPASS                                                          *
*                                                                     *
* INPUT -                                                             *
*    REGISTER 1 POINTS TO A THREE WORD PARAMETER LIST OF THE          *
*    FOLLOWING FORMAT -                                               *
*       --------------------------------------------------------  *
*       NUMBER OF                                                 *
*       BYTES     FIELD   CONTENTS OR MEANING                     *
*       --------------------------------------------------------  *
*           4     PDEADR  THE ADDRESS OF THE PDE                  *
*       --------------------------------------------------------  *
*           4     USERWORDTHE ADDRESS OF THE USER WORK AREA.      *
*       --------------------------------------------------------  *
*           4     VALMSG  THE ADDRESS OF A SECOND LEVEL MESSAGE.  *
*       --------------------------------------------------------  *
*                                                                     *
* OUTPUT -                                                            *
*    REGISTER 15 CONTAINS THE RETURN CODE FOR IKJPARS.  THIS WILL BE  *
*    AS FOLLOWS -                                                     *
*       --------------------------------------------------------  *
*       RETURN                                                    *
*       CODE  REASON                  ACTION                      *
*       --------------------------------------------------------  *
*          0  LOGON IS VALID.         IKJPARS CONTINUE.           *
*       --------------------------------------------------------  *
*          8  USERID NOT IN SYS1.UADS.IKJPARS WILL PROMPT.        *
*       --------------------------------------------------------  *
*         12  DIVISIONAL QUOTA        DENY LOGON SWITCH IS SET.   *
*             CURRENTLY LOGGED ON OR  IKJPARS WILL STOP.          *
*             USER FAILED TO ENTER                                *
*             VALID USERID OR PASSWORD                           P3
*             IN 3 TRIES.                                        P3
*       --------------------------------------------------------  *
*                                                                     *
* MESSAGES -                                                          *
*    'USER-ID LOGGING ON TERMINAL - ASID X'NN''                      P3
*    'USER NOT AUTHORIZED TO USE TIME-SHARING'                        *
*    'PROMPT LIMIT EXCEEDED'                                         P3
*    'DIVISIONAL QUOTA CURRENTLY LOGGED ON. TRY AGAIN LATER'          *
*    'PASSWORD NOT AUTHORIZED FOR THIS USERID'                        *
*                                                                    P3
* COMMENTS -                                                         P3
*    THIS ROUTINE OPERATES AS A SUBROUTINE TO IKJPARS.  SINCE        P3
*    THE SAVE AREA POINTED TO BY THE MAINLINE'S R13 IS BEING USED BY P3
*    IKJPARS, THIS ROUTINE MUST USE ANOTHER SAVE AREA.  HOWEVER,     P3
*    THERE IS STILL NEED TO REFERENCE THE MAINLINE'S DYNAMIC AREA.   P3
*    ADDRESSABILITY TO THIS IS PROVIDED BY USING R8.               P3.1
*                                                                     *
***********************************************************************
         EJECT
VUSERID  DS    0H
         PUSH  USING               SAVE MAIN LINE'S ADDRESSABILITY
         SAVE  (14,12),,VUSERID    SAVE PARSE'S REGISTERS
         L     R2,4(,R1)           GET ADDRESS OF USERWORD
         L     R9,0(,R2)           RESTORE 1ST BASE FROM PARM LIST P3.1
         L     R10,4(,R2)          RESTORE 2ND BASE FROM PARM LIST P3.1
         L     R11,8(,R2)          RESTORE 3RD BASE FROM PARM LIST P3.1
         L     R12,12(,R2)         RESTORE 4TH BASE FROM PARM LIST P3.1
         L     R8,16(,R2)          RESTORE DYN BASE FROM PARM LIST P3.1
         DROP  R13                 DROP MAIN LINE'S ADDRESSABILITY
         USING GOTNAREA,R8         GET ADDRESSABILITY TO DYN AREA  P3.1
         LA    R15,VERSAVEA        GET ADDRESS OF VER ROUTINES SAVEAREA
         ST    R13,4(,R15)         CHAIN
         ST    R15,8(,R13)           SAVE
         LR    R13,R15                 AREAS
         EJECT
***********************************************************************
*                                                                     *
*       EXTRACTS THE USERID FROM THE PDE AND PLACES IT IN GUSERID     *
*       PADDED WITH BLANKS.                                           *
*                                                                     *
***********************************************************************
         L     R2,0(,R1)           GET ADDRESS OF PDE
         MVC   GUSERID,BLANKS      BLANK USERID
         L     R1,0(,R2)           GET ADDRESS OF USERID
         SLR   R15,R15             CLEAR R15 FOR IC - WILL USE AS INDEX
         IC    R15,5(R2)           GET LENGTH OF USERID
         STH   R15,LUSERID         SAVE LENGTH FOR LATER
         BCTR  R15,0               DECREMENT FOR EXECUTE
         EX    R15,MVCUSID         MOVE USERID
         MVC   GTIMRWTO+4(7),GUSERID MOVE USERID TO TIMER WTO        P3
         EJECT                                                       P3
*********************************************************************P3
*                                                                    P3
*       BUILD LOG WTO MESSAGE AND ISSUE IT.                          P3
*                                                                    P3
*********************************************************************P3
         MVC   GLOGWTO,LOGWTO      COPY LOG WTO MESSAGE TO DYN. AREA P3
         MVC   GLOGWTO+4(7),GUSERID MOVE USERID TO LOG WTO MESSAGE   P3
         MVC   GLOGWTO+23(8),GTERMID MOVE TERMID TO LOG WTO MESSAGE  P3
         MVC   GLOGWTO+41(2),GASID MOVE ASID TO LOG WTO MESSAGE      P3
         WTO   MF=(E,GLOGWTO)      ISSUE LOG WTO MESSAGE             P3
         EJECT
***********************************************************************
*                                                                     *
*       CHECKS TO SEE IF THE USERID IS IN THE TABLE OF USERIDS TO     *
*       NEVER BE DENIED LOGON.  IF SO, BYPASS DIVISIONAL QUOTA        *
*       CHECKING.                                                     *
*                                                                     *
***********************************************************************
         LA    R1,NEVERDNY         GET ADDRESS OF NEVERDNY KEY     P3.1
         L     R15,ADDR$SYS        GET ADDRESS OF $SYSTEM$ BLOCK   P3.1
         BAL   R14,FIND$SYS        GO FIND ENTRY IN $SYSTEM$ BLOCK P3.1
VUSENUSR DS    0H                  CHECK NEXT USERID IN TABLE
         CLC   NEVERDNY,0(R1)      IS THIS STILL NEVERDNY ENTRIES  P3.1
         BNE   VUSECHKD            NO - GO CHECK DIVISION          P3.1
         LA    R14,8               YES - GET COUNT OF ENTRIES/LINE P3.1
         LA    R15,9(,R1)          GET ADDRESS OF FIRST ENTRY      P3.1
VUSENLOP DS    0H                  CHECK NEXT USERID IN TABLE      P3.1
         CLC   0(1,R15),GUSERID    IS THIS MATCHING DIVISION       P3.1
         BE    VUSEUSOK            YES - NEVER DENY LOGON
         LA    R15,8(,R15)         NO - BUMP TO NEXT ENTRY ON LINE P3.1
         BCT   R14,VUSENLOP        AND LOOP ACROSS LINE            P3.1
         LA    R1,80(,R1)          AT END - POINT TO NEXT LINE     P3.1
         B     VUSENUSR            AND LOOP ACROSS IT              P3.1
         EJECT
***********************************************************************
*                                                                     *
*       CHECKS THE NUMBER OF USERS ALLOWED TO BE LOGGED ON FOR THIS   *
*       USER'S DIVISION.  IF ZERO, THE LOGON IS DENIED.  OTHERWISE,   *
*       THE ASCBS ARE SEARCHED TO DETERMINE IF THE DIVISIONAL QUOTA   *
*       IS CURRENTLY LOGGED ON.  IF SO, THE LOGON IS DENIED.          *
*                                                                     *
***********************************************************************
VUSECHKD DS    0H                  CHECK FOR DIVISION QUOTA LOGGED ON
         LA    R1,DIVLIMIT         GET ADDRESS OF DIVLIMIT KEY     P3.1
         L     R15,ADDR$SYS        GET ADDRESS OF $SYSTEM$ BLOCK   P3.1
         BAL   R14,FIND$SYS        GO FIND ENTRY IN $SYSTEM$ BLOCK P3.1
VUSENXTD DS    0H                  CHECK NEXT DIVISION IN TABLE
         CLC   DIVLIMIT,0(R1)      IS THIS STILL DIVLIMIT ENTRIES  P3.1
         BNE   VUSEUSOK            NO - NOT FOUND - ALLOW LOGON    P3.1
         LA    R14,6               YES - GET COUNT OF ENTRIES/LINE P3.1
         LA    R15,9(,R1)          GET ADDRESS OF FIRST ENTRY      P3.1
VUSEDLOP DS    0H                                                  P3.1
         CLC   0(1,R15),GUSERID    IS THIS MATCHING DIVISION       P3.1
         BE    VUSECHKC            YES - GET OUT OF LOOP           P3.1
         LA    R15,10(,R15)        NO - BUMP TO NEXT ENTRY ON LINE P3.1
         BCT   R14,VUSEDLOP        AND LOOP ACROSS LINE            P3.1
         LA    R1,80(,R1)          AT END - POINT TO NEXT LINE     P3.1
         B     VUSENXTD            AND LOOP ACROSS IT              P3.1
VUSECHKC DS    0H                  CHECK NUMBER LOGGED ON FOR THIS DIV.
         BAL   R14,GETDVLIM        GO GET DIVISIONAL LIMIT         P3.1
         LTR   R4,R1               CHECK COUNT FOR THIS DIVISION   P3.1
         BZ    VUSEDIVQ            IF ZERO - GO GIVE MESSAGE
         L     R14,CVTPTR          IF NON-ZERO - GET ADDRESS OF CVT
         USING CVTMAP,R14          GET CVT ADDRESSABILITY
         L     R14,CVTASVT         GET ADDRESS OF ASVT
         DROP  R14                 DROP CVT ADDRESSABILITY
         LA    R14,ASVTENTY-ASVT-4(R14) GET ADDR OF 1ST ASVT ENTRY - 4
VUSENASC DS    0H                  CHECK NEXT ASCB FOR THIS DIV. USER
         LA    R14,4(,R14)         GET ADDRESS OF NEXT ASVT ENTRY
         L     R1,0(,R14)          GET ADDRESS OF ASCB
         LA    R1,0(,R1)           CLEAR HIGH ORDER BYTE
         LTR   R1,R1               IS THIS END OF ASVT
         BZ    VUSEUSOK            YES - ALLOW LOGON
         TM    0(R14),KBIT0        NO - IS ASCB AVAILABLE
         BO    VUSENASC            NO - GO TO NEXT ASCB
         CLC   ASCBWORD,ASCBASCB-ASCB(R1) YES - IS ACRONYM PRESENT
         BNE   VUSENASC            NO - GO TO NEXT ASCB
         CLC   ZEROES,ASCBTSB-ASCB(R1) YES - IS TSB ADDRESS ZERO
         BE    VUSENASC            YES - GO TO NEXT ASCB
         L     R1,ASCBJBNS-ASCB(R1) NO - GET ADDRESS OF JOBNAME
         CLC   0(1,R1),GUSERID     IS DIVISION THE SAME
         BNE   VUSENASC            NO - GO TO NEXT ASCB
         BCT   R4,VUSENASC         YES - GO TO NEXT ASCB IF COUNT LEFT
         B     VUSEDIVQ            IF NOT - GO GIVE MESSAGE
VUSEUSOK DS    0H                  USER PASSED DIVISIONAL QUOTA TEST
         EJECT
***********************************************************************
*                                                                     *
*       CHECKS TO SEE IF THE USERID IS FOUND IN SYS1.UADS. IF NOT,    *
*       THE LOGON IS DENIED.                                         P3
*                                                                     *
***********************************************************************
         MVC   GMEMBER,GUSERID     COPY USERID TO MEMBER0 NAME       P3
         LH    R15,LUSERID         GET LENGTH OF USERID
         LA    R3,GMEMBER(R15)     POINT TO BYTE AFTER USERID        P3
         MVI   0(R3),C'0'          ADD SUFFIX (WILL USE R3 LATER     P3
*                                              NEAR LABEL VUSE1ST)   P3
         MVI   GBLL+1,1            SET LL IN BLDL LIST               P3
         MVI   GBFF+1,12           SET FF IN BLDL LIST               P3
         MVC   GBNAME,GMEMBER      MOVE MEMBER0 NAME TO BLDL LIST    P3
         BLDL  GSYSUADS,GBLDL      SEARCH DIRECTORY FOR MEMBER0      P3
         LTR   R15,R15             WAS MEMBER0 FOUND                 P3
         BNZ   VUSENFND            NO - GO TO ERROR ROUTINE
         MVC   GTTR1ST,GBTTR       YES - SAVE MEMBER0 TTR FOR LATER  P3
         EJECT                                                       P3
*********************************************************************P3
*                                                                    P3
*       IF INFO BLOCK IS AVAILABLE, GET TTR.  IF NOT, CREATE ONE     P3
*       PADDED WITH BLANKS.                                          P3
*                                                                    P3
*********************************************************************P3
         MVC   GENQLIST,ENQLIST2   COPY ENQ LIST                     P3
         MVI   GENQLIST+1,52       PUT LENGTH OF RNAME IN ENQ LIST   P3
         MVC   GRNAME(44),GJFCB    MOVE DSNAME TO RNAME              P3
         MVI   GRNAME+44,C'$'      INSERT '$' AS START OF MEMBER     P3
         MVC   GRNAME+45(7),GUSERID APPEND INFO BLOCK NAME TO DSN    P3
         LA    R15,QNAME2          GET ADDRESS OF QNAME (SPFDSN  )   P3
         ST    R15,GENQLIST+4      PUT IN ENQ LIST                   P3
         LA    R15,GRNAME          GET ADDR OF RNAME (DSN+$USERID)   P3
         ST    R15,GENQLIST+8      PUT IN ENQ LIST                   P3
         LA    R1,GENQLIST         GET ADDRESS OF ENQ LIST           P3
         ENQ   MF=(E,(1))          ENQ LIKE SPF DOES                 P3
         OI    GBITS2,GSPFENQI     INDICATE INFO BLOCK ENQ'D ON      P3
         L     R4,ADDRINFO         GET ADDRESS OF INFO BLOCK         P3
         MVI   GBNAME,C'$'         INSERT '$' AS START OF MEMBER     P3
         MVC   GBNAME+1(7),GUSERID MOVE USERID TO BLDL LIST          P3
         BLDL  GSYSUADS,GBLDL      SEARCH DIRECTORY FOR $USERID      P3
         LTR   R15,R15             WAS IT FOUND                      P3
         BNZ   VUSECRET            NO - GO CREATE INFO BLOCK         P3
         MVC   GTTRINFO,GBTTR      YES - SAVE INFO TTR FOR LATER     P3
         B     VUSERED0            GO READ MEMBER0                   P3
         EJECT                                                       P3
*********************************************************************P3
*                                                                    P3
*       CREATE INFO BLOCK.                                           P3
*                                                                    P3
*********************************************************************P3
VUSECRET DS    0H                  CREATE INFO BLOCK                 P3
         LR    R0,R4               GET ADDRESS OF INFO BLOCK         P3
         LH    R1,GSYSUADS+DCBBLKSI-IHADCB GET LENGTH OF INFO BLOCK  P3
         LA    R6,INFOMODL         GET ADDRESS OF MODEL              P3
         L     R7,LINFMODL         GET FILL CHARACTER AND LENGTH     P3
         MVCL  R0,R6               CREATE MODEL INFO BLOCK           P3
         CLOSE MF=(E,GOPENLST)     CLOSE UADS FOR UPDATE             P3
         MVC   GENQLIST,RESLIST    COPY RESERVE LIST                 P3
         MVI   GENQLIST+1,44       PUT LENGTH OF RNAME IN RES LIST   P3
         MVC   GRNAME(44),GJFCB    MOVE DSNAME TO RNAME              P3
         LA    R15,QNAME2          GET ADDRESS OF QNAME (SPFDSN  )   P3
         ST    R15,GENQLIST+4      PUT IN RESERVE LIST               P3
         LA    R15,GRNAME          GET ADDR OF RNAME (DSN)           P3
         ST    R15,GENQLIST+8      PUT IN RESERVE LIST               P3
         LA    R15,ADDRUCB         GET ADDRESS OF UCB ADDRESS        P3
         ST    R15,GENQLIST+12     PUT IN RESERVE LIST               P3
         LA    R1,GENQLIST         GET ADDRESS OF RESERVE LIST       P3
         RESERVE MF=(E,(1))        RESERVE LIKE SPF DOES             P3
         OI    GBITS2,GSPFRES      INDICATE DSN RESERVED             P3
         MVC   GOPENLST(1),OPENOUT CHANGE OPEN LIST                  P3
         OPEN  MF=(E,GOPENLST)     OPEN UADS FOR OUTPUT              P3
         WRITE DECB,SF,GSYSUADS,(R4),'S',MF=E WRITE INFO BLOCK       P3
         CHECK DECB                WAIT ON I/O                       P3
         NOTE  GSYSUADS            GET TTR FOR INFO BLOCK            P3
         ST    R1,GTTRINFO         SAVE FOR LATER                    P3
         MVI   GBNAME,C'$'         INSERT '$' AS START OF MEMBER     P3
         MVC   GBNAME+1(7),GUSERID MOVE USERID TO BLDL LIST          P3
         XC    GBTTR,GBTTR         ZERO TTR FIELD FOR ADD            P3
         STOW  GSYSUADS,GBNAME,A   UPDATE DIRECTORY                  P3
         LR    R4,R15              PROTECT RETURN CODE FROM STOW     P3
         CLOSE MF=(E,GOPENLST)     CLOSE UADS FOR OUTPUT             P3
         LA    R1,GENQLIST         GET ADDRESS OF RESERVE LIST       P3
         DEQ   MF=(E,(1))          DEQ LIKE SPF DOES                 P3
         NI    GBITS2,255-GSPFRES  TURN OFF DSN RESERVED             P3
         LTR   R15,R4              WAS STOW SUCCESSFUL               P3
         BNZ   VUSEBSTO            NO - GO TO ERROR ROUTINE          P3
         MVC   GOPENLST(1),OPENUPDT YES - CHANGE OPEN LIST           P3
         OPEN  MF=(E,GOPENLST)     OPEN UADS FOR UPDATE              P3
         EJECT                                                       P3
*********************************************************************P3
*                                                                    P3
*       READ DATA BLOCKS.                                            P3
*                                                                    P3
*********************************************************************P3
VUSERED0 DS    0H                                                    P3
         MVC   GENQLIST,ENQLIST2   COPY ENQ LIST                     P3
         MVI   GENQLIST+1,52       PUT LENGTH OF RNAME IN ENQ LIST   P3
         MVC   GRNAME(44),GJFCB    MOVE DSNAME TO RNAME              P3
         MVC   GRNAME+44(8),GMEMBER APPEND MEMBER0 NAME TO DSN       P3
         LA    R15,QNAME2          GET ADDRESS OF QNAME (SPFDSN  )   P3
         ST    R15,GENQLIST+4      PUT IN ENQ LIST                   P3
         LA    R15,GRNAME          GET ADDR OF RNAME (DSN+MEMBER0)   P3
         ST    R15,GENQLIST+8      PUT IN ENQ LIST                   P3
         LA    R1,GENQLIST         GET ADDRESS OF ENQ LIST           P3
         ENQ   MF=(E,(1))          ENQ LIKE SPF DOES                 P3
         OI    GBITS2,GSPFENQ0     INDICATE MEMBER0 ENQ'D ON         P3
         L     R4,ADDRUBUF         GET ADDR OF SYS1.UADS BUFFER      P3
         POINT GSYSUADS,GTTR1ST    POINT TO MEMBER0 BLOCK            P3
         READ  DECB,SF,GSYSUADS,(R4),'S',MF=E READ FIRST BLOCK       P2
         CHECK DECB                WAIT ON I/O                       P2
         LH    R1,GSYSUADS+DCBBLKSI-IHADCB GET BLKSIZE FROM DCB      P3
         L     R15,DECB+16         GET IOB ADDRESS FROM DECB         P3
         SH    R1,14(,R15)         SUBTRACT RESIDUAL COUNT           P3
         ST    R1,GLEN1ST          SAVE LENGTH OF FIRST BLOCK        P3
         B     VUSE1ST             AND JUMP RIGHT IN                 P2
VUSELOOP DS    0H                  LOOP READING SYS1.UADS BLOCKS
         READ  DECB,SF,GSYSUADS,(R4),'S',MF=E READ NEXT BLOCK        P2
         CHECK DECB                WAIT ON I/O                       P2
VUSE1ST  DS    0H
         CLI   0(R3),C'9'          WAS THIS LAST BLOCK
         BE    VUSEALL             YES - READ NO MORE
         IC    R15,0(,R3)          NO - GET SUFFIX
         LA    R15,1(,R15)         ADD 1
         STC   R15,0(,R3)          PUT IT BACK
         FIND  GSYSUADS,GMEMBER,D  SEARCH DIRECTORY FOR THIS BLOCK   P3
         LTR   R15,R15             WAS IT FOUND
         BNZ   VUSEALL             NO - READ NO MORE
         LH    R1,GSYSUADS+DCBBLKSI-IHADCB YES - GET BLKSIZE         P3
         L     R15,DECB+16         GET IOB ADDRESS FROM DECB         P3
         SH    R1,14(,R15)         SUBTRACT RESIDUAL COUNT           P3
         AR    R4,R1               BUMP TO NEXT SLOT IN BUFFER       P3
         B     VUSELOOP            AND LOOP
VUSEALL  DS    0H                  READ ALL BLOCKS OF SYS1.UADS
         EJECT                                                       P2
*********************************************************************P2
*                                                                    P2
*       IF NEW PASSWORD WAS SUPPLIED ON LOGON, MOVE IT TO GNEWPSWD   P2
*       PADDED WITH BLANKS.                                          P2
*                                                                    P2
*********************************************************************P2
         TM    GBITS2,GDISPLAY     WAS THIS A DISPLAY TERMINAL       P3
         BZ    VUSENPAS            NO - IGNORE FROM HARDCOPY         P3
         TM    22(R2),KBIT0        YES - WAS NEW PASSWORD SPECIFIED  P3
         BZ    VUSENPAS            NO - CONTINUE                     P2
         MVC   GNEWPSWD,BLANKS     YES - BLANK NEW PASSWORD          P2
         LH    R15,20(R2)          GET LENGTH OF NEW PASSWORD        P2
         L     R1,16(R2)           GET ADDRESS OF NEW PASSWORD       P2
         BCTR  R15,0               DECREMENT FOR EXECUTE             P2
         EX    R15,MVCNPASS        MOVE NEW PASSWORD                 P2
         OI    GBITS2,GREWRITE     INDICATE REWRITE FIRST BLOCK    P3.2
VUSENPAS DS    0H                                                    P2
         EJECT
***********************************************************************
*                                                                     *
*       IF PASSWORD WAS NOT SUPPLIED ON LOGON, PROMPT USER FOR IT NOW.*
*       MOVE PASSWORD TO GPASS PADDED WITH BLANKS.                    *
*                                                                     *
***********************************************************************
         MVI   0(R3),C' '          CLEAR SUFFIX
         MVC   GPASS,BLANKS        BLANK PASSWORD
         MVI   PROMPTCT,0          RESET PROMPT COUNTER
         TM    GBITS2,GDISPLAY     WAS THIS A DISPLAY TERMINAL       P3
         BZ    VUSEPGET            NO - IGNORE FROM HARDCOPY         P3
         TM    14(R2),KBIT0        YES - WAS PASSWORD PRESENT        P3
         BO    VUSEPASS            YES - CONTINUE
VUSEPGET DS    0H                                                    P3
         BAL   R14,GETPASS         GO PROMPT FOR PASSWORD            P3
         LTR   R15,R15             WAS RETURN CODE ZERO              P3
         BZ    VUSEPAS2            YES - SKIP MOVE FROM PDE          P3
         B     VUSEQUIT            NO - EXIT                         P3
VUSEPASS DS    0H                  PASSWORD PRESENT IN COMMAND
         L     R1,8(,R2)           GET ADDRESS OF PASSWORD
         IC    R15,13(,R2)         GET LENGTH OF PASSWORD
         BCTR  R15,0               DECREMENT FOR EXECUTE
         EX    R15,MVCPASS         MOVE PASSWORD
VUSEPAS2 DS    0H
         EJECT
***********************************************************************
*                                                                     *
*       SEARCH UADS DATA BLOCKS FOR MATCHING PASSWORD.  IF NOT FOUND, *
*       THE USER IS PROMPTED FOR ANOTHER PASSWORD.                    *
*                                                                     *
***********************************************************************
         L     R15,ADDRUBUF        GET ADDRESS OF SYS1.UADS BUFFER
         L     R1,24(,R15)         GET OFFSET OF 1ST PASSWORD OFF BLK
VUSEPLOP DS    0H                  SEARCH PASSWORD OFFSET BLOCKS
         L     R2,8(R15,R1)        GET OFFSET OF THIS PASSWORD BLOCK
         LA    R2,0(R15,R2)        ADD ORIGIN
         CLC   GPASS,4(R2)         IS THIS MATCHING PASSWORD
         BE    VUSEPHIT            YES - GET OUT OF LOOP
         LA    R2,0(R15,R1)        NO - GET ADDR OF THIS PSWD OFF BLOCK
         TM    0(R2),KBIT0         MORE PASSWORD OFFSET BLOCKS
         BO    VUSEBPAS            NO - GO TO ERROR ROUTINE
         L     R1,0(,R2)           YES - GET OFF OF NXT PSWD OFF BLK P3
         B     VUSEPLOP            AND LOOP
VUSEPHIT DS    0H                  MATCHING PASSWORD FOUND
         ST    R1,PASSOFF          SAVE OFFSET TO PASSWORD OFFSET BLOCK
         SLR   R15,R15             SET RETURN CODE
         EJECT
***********************************************************************
*                                                                     *
*       RETURN TO IKJPARS.                                            *
*                                                                     *
***********************************************************************
VUSEEXIT DS    0H
         L     R13,4(,R13)         UNCHAIN SAVE AREAS
         RETURN (14,12),RC=(15)    RETURN TO PARSE
         EJECT
***********************************************************************
*                                                                     *
*       INFORM USER THAT USERID WAS NOT IN SYS1.UADS.  IF PROMPT     P3
*       LIMIT NOT EXCEEDED, PROMPT FOR NEW USERID.  ELSE DENY LOGON. P3
*                                                                     *
***********************************************************************
VUSENFND DS    0H                  USERID NOT IN SYS1.UADS
         LA    R0,L'NOTFOUND       GET LENGTH OF MESSAGE
         LA    R1,NOTFOUND         POINT TO MESSAGE
         TPUT  (1),(0),R           ISSUE MESSAGE
         LTR   R15,R15             WAS RETURN CODE ZERO              P3
         BNZ   VUSEQUIT            NO - QUIT                         P3
         CLI   PROMPTCT,3          YES - HAS USER BEEN ASKED 3 TIMES P3
         BL    VUSEPAGN            NO - CONTINUE                     P3
         LA    R0,L'PRMPTMSG       GET LENGTH OF MESSAGE             P3
         LA    R1,PRMPTMSG         POINT TO MESSAGE                  P3
         TPUT  (1),(0),R           ISSUE MESSAGE                     P3
         OI    GBITS1,GDENY        DENY LOGON                        P3
         LA    R15,12              TERMINATE PARSE                   P3
         B     VUSEEXIT            AND RETURN TO CALLER              P3
VUSEPAGN DS    0H                  PROMPT AGAIN                      P3
         IC    R15,PROMPTCT        GET PROMPT COUNTER                P3
         LA    R15,1(,R15)         INCREMENT BY 1                    P3
         STC   R15,PROMPTCT        AND PUT IT BACK                   P3
         LA    R15,8               LET PARSE PROMPT
         B     VUSEEXIT            AND EXIT
         EJECT
***********************************************************************
*                                                                     *
*       INFORM USER THAT DIVISIONAL QUOTA CURRENTLY LOGGED ON, SET BIT*
*       TO DENY LOGON, AND SET RETURN CODE FOR IKJPARS TO STOP.       *
*                                                                     *
***********************************************************************
VUSEDIVQ DS    0H                  DIVISIONAL QUOTA CURRENTLY LOGGED ON
         OI    GBITS1,GDENY        DENY LOGON                        P3
         LA    R0,L'DIVQUOTA       GET LENGTH OF MESSAGE
         LA    R1,DIVQUOTA         POINT TO MESSAGE
         TPUT  (1),(0),R           ISSUE MESSAGE
         B     VUSEQUIT            QUIT                              P3
         EJECT
***********************************************************************
*                                                                     *
*       IF USER HAS NOT BEEN PROMPTED THREE (3) TIMES, INFORM USER    *
*       THAT PASSWORD IS NOT AUTHORIZED FOR THIS USERID AND PROMPT FOR*
*       NEW PASSWORD.  ELSE, INFORM USER, SET BIT TO DENY LOGON AND   *
*       SET RETURN CODE FOR IKJPARS TO STOP.                          *
*                                                                     *
***********************************************************************
VUSEBPAS DS    0H                  PASSWORD NOT AUTHORIZED FOR USERID
         CLI   PROMPTCT,3          HAS USER BEEN PROMPTED 3 TIMES
         BL    VUSEBPGO            NO - CONTINUE
         LA    R0,L'PRMPTMSG       GET LENGTH OF MESSAGE
         LA    R1,PRMPTMSG         POINT TO MESSAGE
         TPUT  (1),(0),R           ISSUE MESSAGE
         OI    GBITS1,GDENY        DENY LOGON                        P3
         B     VUSEQUIT            QUIT                              P3
VUSEBPGO DS    0H
         IC    R15,PROMPTCT        GET PROMPT COUNTER
         LA    R15,1(,R15)         INCREMENT BY 1
         STC   R15,PROMPTCT        AND PUT IT BACK
         LA    R0,L'BADPASS        GET LENGTH OF MESSAGE
         LA    R1,BADPASS          POINT TO MESSAGE
         TPUT  (1),(0),R           ISSUE MESSAGE
         LTR   R15,R15             WAS RETURN CODE ZERO              P3
         BNZ   VUSEQUIT            NO - QUIT                         P3
         BAL   R14,GETPASS         YES - GO PROMPT FOR NEW PASSWORD  P3
         LTR   R15,R15             WAS RETURN CODE ZERO              P3
         BZ    VUSEPAS2            YES - TRY AGAIN                   P3
         B     VUSEQUIT            NO - EXIT                         P3
         EJECT                                                       P3
*********************************************************************P3
*                                                                    P3
*       INFORM USER THAT STOW RETURN CODE WAS NOT ZERO AND DENY      P3
*       LOGON.                                                       P3
*                                                                    P3
*********************************************************************P3
VUSEBSTO DS    0H                  NON-ZERO RETURN CODE FROM STOW    P3
         MVC   GSTOWMSG(L'STOWMSG),STOWMSG COPY MESSAGE              P3
         CVD   R15,WORKAREA        CONVERT TO DECIMAL                P3
         OI    WORKAREA+7,X'0F'    FIX SIGN                          P3
         UNPK  WORKAREA(5),WORKAREA+5(3) UNPACK                      P3
         MVC   GSTOWMSG+23(4),WORKAREA+1 MOVE INTO MESSAGE           P3
         LA    R0,L'STOWMSG        GET LENGTH OF MESSAGE             P3
         LA    R1,GSTOWMSG         POINT TO MESSAGE                  P3
         TPUT  (1),(0),R           ISSUE MESSAGE                     P3
         OI    GBITS1,GDENY        DENY LOGON                        P3
         B     VUSEQUIT            EXIT                              P3
         EJECT                                                       P3
*********************************************************************P3
*                                                                    P3
*       TERMINATE PARSE.                                             P3
*                                                                    P3
*********************************************************************P3
VUSEQUIT DS    0H                                                    P3
         LA    R15,12              TERMINATE PARSE                   P3
         B     VUSEEXIT            AND EXIT                          P3
         EJECT
***********************************************************************
*                                                                     *
*       EXECUTED INSTRUCTIONS FOR VUSERID.                            *
*                                                                     *
***********************************************************************
MVCUSID  MVC   GUSERID(*-*),0(R1)  MOVE USERID
MVCPASS  MVC   GPASS(*-*),0(R1)    MOVE PASSWORD
MVCNPASS MVC   GNEWPSWD(*-*),0(R1) MOVE NEW PASSWORD                 P2
         TITLE 'PROMPT FOR PASSWORD ROUTINE'
***********************************************************************
*                                                                     *
* ROUTINE NAME -                                                      *
*    GETPASS                                                          *
*                                                                     *
* FUNCTION -                                                          *
*    PROMPT TSO USER FOR PASSWORD.                                    *
*                                                                     *
* OPERATION -                                                         *
*    1) CHECKS TO SEE IF TSO USER IS ON A DISPLAY TERMINAL.  IF NOT,  *
*       OVERPRINT BEFORE ASKING FOR PASSWORD.  IF THE USER IS ON A    *
*       DISPLAY TERMINAL, PROMPT FOR PASSWORD WITH PRINT BYPASS.      *
*                                                                     *
* ENTRY POINTS -                                                      *
*    GETPASS                                                          *
*                                                                     *
* EXTERNAL REFERENCES -                                               *
*    IKJPTGT (PUTGET)                                                 *
*                                                                     *
* INPUT -                                                             *
*    NONE                                                             *
*                                                                     *
* OUTPUT -                                                            *
*    PASSWORD IN GPASS.                                               *
*                                                                     *
* MESSAGES -                                                          *
*    'ENTER PASSWORD - '                                              *
*                                                                    P3
* COMMENTS -                                                         P3
*    THIS ROUTINE OPERATES AS A SUBROUTINE OF VUSERID AND MUST USE   P3
*    THE SAME ADDRESSABILITY.                                        P3
*                                                                     *
***********************************************************************
         EJECT
GETPASS  DS    0H                  MUST HAVE SAME USING'S AS VUSERID
         ST    R14,GPASSAVE        SAVE RETURN ADDRESS
         ST    R2,GPASSAVE+4       SAVE R2
         EJECT
***********************************************************************
*                                                                     *
*       CHECKS TO SEE IF TSO USER IS ON A DISPLAY TERMINAL.  IF NOT,  *
*       OVERPRINT BEFORE ASKING FOR PASSWORD.  IF THE USER IS ON A    *
*       DISPLAY TERMINAL, PROMPT FOR PASSWORD WITH PRINT BYPASS.      *
*                                                                     *
***********************************************************************
         TM    GBITS2,GDISPLAY     WAS THIS A DISPLAY TERMINAL       P3
         BO    GPASDISP            YES - USE PUTGET                  P3
GPASNDSP DS    0H                  NO - USE TPUT/TGET                P3
         LA    R0,L'PASSMSG        GET LENGTH OF MESSAGE             P3
         LA    R1,PASSMSG          POINT TO MESSAGE
         TPUT  (1),(0),R           ISSUE MESSAGE
         LTR   R15,R15             WAS RETURN CODE ZERO              P3
         BNZ   GPASEXIT            NO - EXIT                         P3
         LA    R0,LOVRPRNT         YES - GET LENGTH OF MESSAGE       P3
         LA    R1,OVERPRNT         POINT TO MESSAGE
         ICM   R1,B'1000',HEX02    SET OPTIONS FOR CONTROL
         TPUT  (1),(0),R           ISSUE MESSAGE
         LTR   R15,R15             WAS RETURN CODE ZERO              P3
         BNZ   GPASEXIT            NO - EXIT                         P3
         LA    R0,L'GPASS          YES - GET LENGTH OF PASSWORD      P3
         LA    R1,GPASS            GET ADDRESS OF PASSWORD
         ICM   R1,B'1000',HEX80    SET OPTIONS
         MVC   GPASS,BLANKS        BLANK INPUT AREA                  P3
         TGET  (1),(0),R           READ PASSWORD
         LTR   R15,R15             WAS RETURN CODE ZERO              P3
         BZ    GPASTGOK            YES - CONTINUE                    P3
         BAL   R14,GPASTGER        NO - GO TO ERROR ROUTINE          P3
GPASTGOK DS    0H                                                    P3
         OC    GPASS,BLANKS        FORCE TO UPPERCASE                P3
         CLC   GPASS,BLANKS        WAS RESPONSE BLANKS               P3
         BE    GPASNDSP            YES - ASK AGAIN                   P3
         B     GPASEXIT            NO - EXIT                         P3
GPASDISP DS    0H                  TERMINAL WAS DISPLAY
         MVC   GIKJPGPB(LPGPB),LPUTGET MOVE LIST TO WORK AREA
         LA    R1,GIKJIOPL         GET ADDRESS OF IOPL
         USING IOPL,R1             GET ADDRESSABILITY TO IOPL
         MVC   IOPLUPT,GCPPL+CPPLUPT-CPPL COPY UPT ADDRESS TO IOPL
         MVC   IOPLECT,GCPPL+CPPLECT-CPPL COPY ECT ADDRESS TO IOPL
         LA    R15,GCPECB          GET ADDRESS OF CP ECB             P3
         ST    R15,IOPLECB         PUT CP ECB ADDRESS IN IOPL        P3
         XC    GCPECB,GCPECB       ZERO CP ECB                       P3
         LA    R15,GIKJPGPB        GET ADDRESS OF PUTGET PARM LIST
         ST    R15,IOPLIOPB        PUT PUTGET PARM LIST ADDRESS IN IOPL
         XC    GPGOLD(4),GPGOLD    ZERO NUMBER OF SEGMENTS
         MVI   GPGOLD+3,1          SET NUMBER TO 1
         LA    R15,PMSGHDR         GET ADDRESS OF PASSWORD MSG HEADER
         ST    R15,GPGOLD+4        PUT ADDRESS IN OLD
         PUTGET OUTPUT=(GPGOLD,,PTBYPS),MF=(E,GIKJIOPL)
         LTR   R15,R15             WAS PUTGET SUCCESSFUL
         BNZ   GPASEXIT            NO - EXIT                         P3
         LA    R1,GIKJPGPB         YES - GET ADDRESS OF PUTGET PRM LIST
         USING PGPB,R1             GET ADDRESSABILITY TO PARM LIST
         L     R1,PGPBIBUF         GET ADDRESS OF PUTGET INPUT BUFFER
         LR    R2,R1               SAVE ADDRESS FOR FREEMAIN
         CLI   1(R1),12            WAS RESPONSE TOO LONG
         BH    GPASFREE            YES - FREE INPUT BUFF AND ASK AGAIN
         CLI   1(R1),5             NO - WAS RESPONSE TOO SHORT
         BL    GPASFREE            YES - FREE INPUT BUFF AND ASK AGAIN
         MVC   GPASS,BLANKS        NO - BLANK PASSWORD
         SLR   R15,R15             CLEAR R15 FOR IC
         IC    R15,1(,R1)          GET LENGTH IN R15
         SH    R15,HALF5           SUBTRACT OFF LENGTH OF HEADER + 1
         LA    R1,4(,R1)           GET ADDRESS OF PASSWORD IN R1
         EX    R15,MVCPASS         MOVE PASSWORD
         OC    GPASS,BLANKS        FORCE TO UPPERCASE
         LR    R1,R2               RESTORE THE BUFFER ADDRESS
         LA    R0,1                GET THE SUBPOOL NUMBER
         SLL   R0,24               SHIFT INTO HIGH-ORDER BYTE
         ICM   R0,B'0011',0(R1)    GET THE LENGTH
         FREEMAIN R,A=(1),LV=(0)   FREE THE INPUT BUFFER
         SLR   R15,R15             SET RETURN CODE
         B     GPASEXIT            AND EXIT
GPASFREE DS    0H                  FREE INPUT BUFFER AND ASK AGAIN
         LR    R1,R2               RESTORE THE BUFFER ADDRESS
         LA    R0,1                GET THE SUBPOOL NUMBER
         SLL   R0,24               SHIFT INTO HIGH-ORDER BYTE
         ICM   R0,B'0011',0(R1)    GET THE LENGTH
         FREEMAIN R,A=(1),LV=(0)   FREE THE INPUT BUFFER
         B     GPASDISP            AND ASK AGAIN
GPASEXIT DS    0H
         L     R14,GPASSAVE        RESTORE RETURN ADDRESS
         L     R2,GPASSAVE+4       RESTORE R2
         BR    R14                 RETURN TO CALLER
         EJECT                                                       P3
*********************************************************************P3
*                                                                    P3
*       IF RETURN CODE DID NOT INDICATE ATTENTION INTERRUPT, ISSUE   P3
*       MESSAGE TO USER AND DENY LOGON.                              P3
*                                                                    P3
*********************************************************************P3
GPASTGER DS    0H                  NON-ZERO RETURN CODE FROM TGET    P3
         LA    R1,8                GET 8 IN R1 FOR COMPARISON        P3
         CR    R1,R15              WAS THIS AN ATTENTION INTERRUPT   P3
         BNE   GPASTGNA            NO - GO ISSUE MESSAGE             P3
         TM    GBITS1,GATTNHIT     YES - DID I GET ANY INPUT         P3
         BZR   R14                 NO - RETURN AS IF NULL INPUT      P3
         B     GPASEXIT            YES - LET MAINLINE HANDLE INPUT   P3
GPASTGNA DS    0H                  NOT AN ATTENTION INTERRUPT        P3
         LR    R2,R15              PROTECT RETURN CODE               P3
         MVC   GTGETMSG(L'TGETMSG),TGETMSG COPY MESSAGE              P3
         CVD   R2,WORKAREA         CONVERT TO DECIMAL                P3
         OI    WORKAREA+7,X'0F'    FIX SIGN                          P3
         UNPK  WORKAREA(5),WORKAREA+5(3) UNPACK                      P3
         MVC   GTGETMSG+23(4),WORKAREA+1 MOVE INTO MESSAGE           P3
         LA    R0,L'TGETMSG        GET LENGTH OF MESSAGE             P3
         LA    R1,GTGETMSG         POINT TO MESSAGE                  P3
         TPUT  (1),(0),R           ISSUE MESSAGE                     P3
         LR    R15,R2              RESTORE RETURN CODE               P3
         OI    GBITS1,GDENY        DENY LOGON                        P3
         B     GPASEXIT            EXIT                              P3
         TITLE 'GET DIVISIONAL LIMIT SUBROUTINE'                   P3.1
*******************************************************************P3.1
*                                                                  P3.1
* ROUTINE NAME -                                                   P3.1
*    GETDVLIM                                                      P3.1
*                                                                  P3.1
* FUNCTION -                                                       P3.1
*    GET DIVISIONAL LIMIT                                          P3.1
*                                                                  P3.1
* OPERATION -                                                      P3.1
*                                                                  P3.1
* ENTRY POINTS -                                                   P3.1
*    GETDIVLM                                                      P3.1
*                                                                  P3.1
* EXTERNAL REFERENCES -                                            P3.1
*    FIND$SYS                                                      P3.1
*                                                                  P3.1
* INPUT -                                                          P3.1
*    REGISTER 15 POINTS TO THE DIVISIONAL ENTRY FROM $SYSTEM$      P3.1
*                                                                  P3.1
* OUTPUT -                                                         P3.1
*    REGISTER 1 CONTAINS THE DIVISIONAL QUOTA.                     P3.1
*                                                                  P3.1
* MESSAGES -                                                       P3.1
*    NONE                                                          P3.1
*                                                                  P3.1
* COMMENTS -                                                       P3.1
*    THIS ROUTINE OPERATES AS A SUBROUTINE OF VUSERID AND MUST USE P3.1
*    THE SAME ADDRESSABILITY.                                      P3.1
*                                                                  P3.1
*******************************************************************P3.1
         EJECT                                                     P3.1
GETDVLIM DS    0H                                                  P3.1
         ST    R14,GDLMSAVE        SAVE RETURN ADDRESS             P3.1
         ST    R15,GDLMSAVE+4      SAVE R15 (ADDR OF DIV ENTRY)    P3.1
         ST    R2,GDLMSAVE+8       SAVE R2                         P3.1
         ST    R3,GDLMSAVE+12      SAVE R3                         P3.1
         L     R15,CVTPTR          GET ADDRESS OF CVT              P3.1
         UNPK  WORKAREA(5),CVTDATE+1-CVTMAP(3,R15) UNPACK DATE     P3.1
         LA    R1,HOLIDAYS         GET ADDRESS OF HOLIDAYS KEY     P3.1
         L     R15,ADDR$SYS        GET ADDRESS OF $SYSTEM$ BLOCK   P3.1
         BAL   R14,FIND$SYS        GO FIND ENTRY IN $SYSTEM$ BLOCK P3.1
GDLMNXTH DS    0H                  PROCESS NEXT HOLIDAYS LINE      P3.1
         CLC   HOLIDAYS,0(R1)      IS THIS STILL HOLIDAYS ENTRIES  P3.1
         BNE   GDLMNHOL            NO - CANT BE A HOLIDAY          P3.1
         LA    R14,10              YES - GET COUNT OF ENTRIES/LINE P3.1
         LA    R15,9(,R1)          GET ADDRESS OF FIRST ENTRY      P3.1
GDLMLOOP DS    0H                                                  P3.1
         CLC   0(5,R15),WORKAREA   IS THIS MATCHING DATE           P3.1
         BE    GDLMOFFS            YES - GET OUT OF LOOP           P3.1
         LA    R15,6(,R15)         NO - BUMP TO NEXT ENTRY ON LINE P3.1
         BCT   R14,GDLMLOOP        AND LOOP ACROSS LINE            P3.1
         LA    R1,80(,R1)          AT END - POINT TO NEXT LINE     P3.1
         B     GDLMNXTH            AND LOOP ACROSS IT              P3.1
GDLMNHOL DS    0H                  NOT A HOLIDAY                   P3.1
         TIME  DEC                 GET TIME IN R0 (HHMMSSTH)       P3.1
         SRL   R0,12               SHIFT OUT LAST THREE NIBBLES    P3.1
         ST    R0,WORKAREA+4       PUT IN WORKAREA+5 (0HHMMS)      P3.1
         OI    WORKAREA+7,X'0F'    FIX SIGN (0HHMMF)               P3.1
         LA    R1,PRIMTIME         GET ADDRESS OF PRIMTIME KEY     P3.1
         L     R15,ADDR$SYS        GET ADDRESS OF $SYSTEM$ BLOCK   P3.1
         BAL   R14,FIND$SYS        GO FIND ENTRY IN $SYSTEM$ BLOCK P3.1
         PACK  WORKAREA(3),9(4,R1) PACK START OF PRIME SHIFT       P3.1
         CP    WORKAREA(3),WORKAREA+5(3) IS THIS PRIME SHIFT       P3.1
         BH    GDLMOFFS            NO - MUST BE OFF SHIFT          P3.1
         PACK  WORKAREA(3),14(4,R1) PACK END OF PRIME SHIFT        P3.1
         CP    WORKAREA(3),WORKAREA+5(3) IS THIS PRIME SHIFT       P3.1
         BL    GDLMOFFS            NO - MUST BE OFF SHIFT          P3.1
         XC    WORKAREA,WORKAREA   YES - ZERO WORK AREA            P3.1
         L     R15,CVTPTR          GET ADDRESS OF CVT              P3.1
         MVC   WORKAREA+4(4),CVTDATE-CVT(R15) GET PACKED DATE      P3.1
         CVB   R14,WORKAREA        R14 CONTAINS JULIAN DATE IN BIN P3.1
         SRDL  R14,32              GET YEAR AND DAY OF YEAR        P3.1
         D     R14,FULL1000        R14 IS DAY, R15 IS YEAR         P3.1
         LA    R15,1900(,R15)      ADD 1900 TO YEAR                P3.1
         ST    R15,GDLMYEAR        SAVE THE YEAR                   P3.1
         ST    R14,GDLMDAY         SAVE DAY OF YEAR                P3.1
*                                                                  P3.1
* FIND DAY OF WEEK USING ALGORITHM FOUND IN COMPUTERWORLD          P3.1
* JANUARY 14,1980 PAGE 36                                          P3.1
*                                                                  P3.1
* SATURDAY=0, SUNDAY=1, ETC.                                       P3.1
*                                                                  P3.1
         BCTR  R15,0               GET YEAR-1                      P3.1
         LR    R2,R15              SAVE IT                         P3.1
         SLR   R14,R14             CLEAR FOR DIVIDE                P3.1
         D     R14,FULL4           DIVIDE BY 4                     P3.1
         LR    R3,R15              GET INTEGER QUOTIENT            P3.1
         LR    R15,R2              RELOAD YEAR-1                   P3.1
         SLR   R14,R14             CLEAR FOR DIVIDE                P3.1
         D     R14,FULL100         DIVIDE BY 100                   P3.1
         SR    R3,R15              USE INTEGER QUOTIENT            P3.1
         LR    R15,R2              RELOAD YEAR-1                   P3.1
         SLR   R14,R14             CLEAR FOR DIVIDE                P3.1
         D     R14,FULL400         DIVIDE BY 400                   P3.1
         AR    R3,R15              USE INTEGER QUOTIENT            P3.1
         LR    R15,R2              RELOAD YEAR-1                   P3.1
         SLR   R14,R14             CLEAR FOR DIVIDE                P3.1
         D     R14,FULL4000        DIVIDE BY 4000                  P3.1
         SR    R3,R15              USE INTEGER QUOTIENT            P3.1
         LR    R15,R2              RELOAD YEAR-1                   P3.1
         SLR   R14,R14             CLEAR FOR DIVIDE                P3.1
         D     R14,FULL1000        DIVIDE BY 1000                  P3.1
         AR    R3,R15              USE INTEGER QUOTIENT            P3.1
         LR    R15,R2              RELOAD YEAR-1                   P3.1
         SLR   R14,R14             CLEAR FOR DIVIDE                P3.1
         D     R14,FULL2000        DIVIDE BY 2000                  P3.1
         SR    R3,R15              USE INTEGER QUOTIENT            P3.1
*                                  R3=EXTRA DAYS                   P3.1
         BCTR  R3,0                LESS 1                          P3.1
         A     R3,GDLMYEAR         PLUS THE YEAR                   P3.1
         A     R3,GDLMDAY          PLUS NUMBER OF DAYS THIS YEAR   P3.1
         SLR   R2,R2               CLEAR FOR DIVIDE                P3.1
         D     R2,FULL7            DIVIDE BY 7                     P3.1
         LTR   R2,R2               ANY REMAINDER (SATURDAY=0)      P3.1
         BZ    GDLMOFFS            YES - ITS THE WEEKEND           P3.1
         BCTR  R2,0                NO - SUBTRACT 1                 P3.1
         LTR   R2,R2               ANY REMAINDER (SUNDAY=1)        P3.1
         BZ    GDLMOFFS            YES - ITS THE WEEKEND           P3.1
         L     R1,GDLMSAVE+4       NO - GET ADDRESS OF DIV ENTRY   P3.1
         LA    R1,2(,R1)           POINT TO PRIME TIME LIMIT       P3.1
         B     GDLMGO              AND CONTINUE                    P3.1
GDLMOFFS DS    0H                                                  P3.1
         L     R1,GDLMSAVE+4       GET ADDRESS OF DIVISIONAL ENTRY P3.1
         LA    R1,6(,R1)           POINT TO OFF SHIFT LIMIT        P3.1
GDLMGO   DS    0H                  R1 POINTS TO DIVISION LIMIT     P3.1
         PACK  WORKAREA,0(3,R1)    PACK DIVISION LIMIT             P3.1
         CVB   R1,WORKAREA         CONVERT TO BINARY               P3.1
         L     R14,GDLMSAVE        RESTORE RETURN ADDRESS          P3.1
         L     R2,GDLMSAVE+8       RESTORE R2                      P3.1
         L     R3,GDLMSAVE+12      RESTORE R3                      P3.1
         BR    R14                 RETURN TO CALLER                P3.1
         SPACE 3
         POP   USING               RESTORE MAIN LINE'S ADDRESSABILITY
         TITLE 'PROMPT FOR NEW PASSWORD ROUTINE'                     P2
*********************************************************************P2
*                                                                    P2
* ROUTINE NAME -                                                     P2
*    GETNPASS                                                        P2
*                                                                    P2
* FUNCTION -                                                         P2
*    PROMPT TSO USER FOR NEW PASSWORD.                               P2
*                                                                    P2
* OPERATION -                                                        P2
*    1) CHECKS TO SEE IF TSO USER IS ON A DISPLAY TERMINAL.  IF NOT, P2
*       OVERPRINT BEFORE ASKING FOR PASSWORD.  IF THE USER IS ON A   P2
*       DISPLAY TERMINAL, PROMPT FOR PASSWORD WITH PRINT BYPASS.     P2
*                                                                    P2
* ENTRY POINTS -                                                     P2
*    GETNPASS                                                        P2
*                                                                    P2
* EXTERNAL REFERENCES -                                              P2
*    IKJPTGT (PUTGET)                                                P2
*                                                                    P2
* INPUT -                                                            P2
*    NONE                                                            P2
*                                                                    P2
* OUTPUT -                                                           P2
*    NEW PASSWORD IN GNEWPSWD.                                       P2
*                                                                    P2
* MESSAGES -                                                         P2
*    'ENTER NEW PASSWORD - '                                         P2
*                                                                    P2
*********************************************************************P2
         EJECT                                                       P2
GETNPASS DS    0H                                                    P2
         ST    R14,GNPASAVE        SAVE RETURN ADDRESS               P2
         ST    R2,GNPASAVE+4       SAVE R2                           P2
         EJECT                                                       P2
*********************************************************************P2
*                                                                    P2
*       CHECKS TO SEE IF TSO USER IS ON A DISPLAY TERMINAL.  IF NOT, P2
*       OVERPRINT BEFORE ASKING FOR PASSWORD.  IF THE USER IS ON A   P2
*       DISPLAY TERMINAL, PROMPT FOR PASSWORD WITH PRINT BYPASS.     P2
*                                                                    P2
*********************************************************************P2
         TM    GBITS2,GDISPLAY     WAS THIS A DISPLAY TERMINAL       P3
         BO    GNPADISP            YES - USE PUTGET                  P3
GNPANDSP DS    0H                  NO - USE TPUT/TGET                P3
         LA    R0,L'NPASSMSG       GET LENGTH OF MESSAGE             P3
         LA    R1,NPASSMSG         POINT TO MESSAGE                  P2
         TPUT  (1),(0),R           ISSUE MESSAGE                     P2
         LTR   R15,R15             WAS RETURN CODE ZERO              P3
         BNZ   GNPAEXIT            NO - EXIT                         P3
         LA    R0,LOVRPRNT         YES - GET LENGTH OF MESSAGE       P3
         LA    R1,OVERPRNT         POINT TO MESSAGE                  P2
         ICM   R1,B'1000',HEX02    SET OPTIONS FOR CONTROL           P2
         TPUT  (1),(0),R           ISSUE MESSAGE                     P2
         LTR   R15,R15             WAS RETURN CODE ZERO              P3
         BNZ   GNPAEXIT            NO - EXIT                         P3
         LA    R0,L'GNEWPSWD       YES - GET LENGTH OF PASSWORD      P3
         LA    R1,GNEWPSWD         GET ADDRESS OF PASSWORD           P2
         ICM   R1,B'1000',HEX80    SET OPTIONS                       P2
         MVC   GNEWPSWD,BLANKS     BLANK INPUT AREA                  P3
         TGET  (1),(0),R           READ PASSWORD                     P2
         LTR   R15,R15             WAS RETURN CODE ZERO              P3
         BZ    GNPATGOK            YES - CONTINUE                    P3
         BAL   R14,GNPATGER        NO - GO TO ERROR ROUTINE          P3
GNPATGOK DS    0H                                                    P3
         OC    GNEWPSWD,BLANKS     FORCE TO UPPERCASE                P3
         CLC   GNEWPSWD,BLANKS     WAS RESPONSE BLANKS               P3
         BE    GNPANDSP            YES - ASK AGAIN                   P3
         OI    GBITS2,GREWRITE     NO - INDICATE REWRITE FIRST BLK P3.2
         LA    R0,L'GNEWPSWD       GET LENGTH OF MESSAGE             P3
         LA    R1,GNEWPSWD         POINT TO MESSAGE                  P2
         ICM   R1,B'1000',HEX02    SET OPTIONS FOR CONTROL           P2
         TPUT  (1),(0),R           ISSUE MESSAGE                     P2
         LTR   R15,R15             WAS RETURN CODE ZERO              P3
         BNZ   GNPAEXIT            NO - EXIT                         P3
         STIMER WAIT,DINTVL=WAITTIME YES - WAIT FOR USER TO READ IT  P3
         LA    R0,LOVRPRNT+2       GET LENGTH OF MESSAGE             P2
         LA    R1,OVERPRNT-1       POINT TO MESSAGE                  P2
         ICM   R1,B'1000',HEX02    SET OPTIONS FOR CONTROL           P2
         TPUT  (1),(0),R           ISSUE MESSAGE                     P2
         B     GNPAEXIT            AND EXIT WITH RETURN CODE         P3
GNPADISP DS    0H                  TERMINAL WAS DISPLAY              P2
         MVC   GIKJPGPB(LPGPB),LPUTGET MOVE LIST TO WORK AREA        P2
         LA    R1,GIKJIOPL         GET ADDRESS OF IOPL               P2
         USING IOPL,R1             GET ADDRESSABILITY TO IOPL        P2
         MVC   IOPLUPT,GCPPL+CPPLUPT-CPPL COPY UPT ADDRESS TO IOPL   P2
         MVC   IOPLECT,GCPPL+CPPLECT-CPPL COPY ECT ADDRESS TO IOPL   P2
         LA    R15,GCPECB          GET ADDRESS OF CP ECB             P3
         ST    R15,IOPLECB         PUT CP ECB ADDRESS IN IOPL        P3
         XC    GCPECB,GCPECB       ZERO CP ECB                       P3
         LA    R15,GIKJPGPB        GET ADDRESS OF PUTGET PARM LIST   P2
         ST    R15,IOPLIOPB        PUT PUTGET PARM LIST ADDR IN IOPL P2
         XC    GPGOLD(4),GPGOLD    ZERO NUMBER OF SEGMENTS           P2
         MVI   GPGOLD+3,1          SET NUMBER TO 1                   P2
         LA    R15,NPMSGHDR        GET ADDRESS OF NEW PSWD MSG HDR   P2
         ST    R15,GPGOLD+4        PUT ADDRESS IN OLD                P2
         PUTGET OUTPUT=(GPGOLD,,PTBYPS),MF=(E,GIKJIOPL)              P2
         LTR   R15,R15             WAS PUTGET SUCCESSFUL             P2
         BNZ   GNPAEXIT            NO - EXIT                         P3
         LA    R1,GIKJPGPB         YES - GET ADDR OF PUTGET PRM LIST P2
         USING PGPB,R1             GET ADDRESSABILITY TO PARM LIST   P2
         L     R1,PGPBIBUF         GET ADDR OF PUTGET INPUT BUFFER   P2
         LR    R2,R1               SAVE ADDRESS FOR FREEMAIN         P2
         CLI   1(R1),12            WAS RESPONSE TOO LONG             P2
         BH    GNPAFREE            YES - FREE INPUT BUFF & ASK AGAIN P2
         CLI   1(R1),5             NO - WAS RESPONSE TOO SHORT       P2
         BL    GNPAFREE            YES - FREE INPUT BUFF & ASK AGAIN P2
         MVC   GNEWPSWD,BLANKS     NO - BLANK PASSWORD               P2
         SLR   R15,R15             CLEAR R15 FOR IC                  P2
         IC    R15,1(,R1)          GET LENGTH IN R15                 P2
         SH    R15,HALF5           SUBTRACT OFF LENGTH OF HEADER + 1 P2
         LA    R1,4(,R1)           GET ADDRESS OF PASSWORD IN R1     P2
         EX    R15,MVCNPSWD        MOVE PASSWORD                     P2
         OC    GNEWPSWD,BLANKS     FORCE TO UPPERCASE                P2
         OI    GBITS2,GREWRITE     INDICATE REWRITE FIRST BLOCK    P3.2
         LR    R1,R2               RESTORE THE BUFFER ADDRESS        P2
         LA    R0,1                GET THE SUBPOOL NUMBER            P2
         SLL   R0,24               SHIFT INTO HIGH-ORDER BYTE        P2
         ICM   R0,B'0011',0(R1)    GET THE LENGTH                    P2
         FREEMAIN R,A=(1),LV=(0)   FREE THE INPUT BUFFER             P2
         LA    R0,L'GNEWPSWD       GET LENGTH OF MESSAGE             P2
         LA    R1,GNEWPSWD         POINT TO MESSAGE                  P2
         TPUT  (1),(0),R           ISSUE MESSAGE                     P2
         LTR   R15,R15             WAS RETURN CODE ZERO              P3
         BNZ   GNPAEXIT            NO - EXIT                         P3
         STIMER WAIT,DINTVL=WAITTIME YES - WAIT FOR USER TO READ IT  P3
         LA    R0,L'CLRSCRN        GET LENGTH OF MESSAGE             P3
         LA    R1,CLRSCRN          GET ADDRESS OF MESSAGE            P3
         ICM   R1,B'1000',HEX03    INDICATE FULLSCR TPUT             P3
         TPUT  (1),(0),R           ISSUE TPUT TO CLEAR SCREEN        P3
         B     GNPAEXIT            AND EXIT                          P2
GNPAFREE DS    0H                  FREE INPUT BUFFER AND ASK AGAIN   P2
         LR    R1,R2               RESTORE THE BUFFER ADDRESS        P2
         LA    R0,1                GET THE SUBPOOL NUMBER            P2
         SLL   R0,24               SHIFT INTO HIGH-ORDER BYTE        P2
         ICM   R0,B'0011',0(R1)    GET THE LENGTH                    P2
         FREEMAIN R,A=(1),LV=(0)   FREE THE INPUT BUFFER             P2
         B     GNPADISP            AND ASK AGAIN                     P2
GNPAEXIT DS    0H                                                    P2
         L     R14,GNPASAVE        RESTORE RETURN ADDRESS            P2
         L     R2,GNPASAVE+4       RESTORE R2                        P2
         BR    R14                 RETURN TO CALLER                  P2
         EJECT                                                       P3
*********************************************************************P3
*                                                                    P3
*       IF RETURN CODE DID NOT INDICATE ATTENTION INTERRUPT, ISSUE   P3
*       MESSAGE TO USER AND DENY LOGON.                              P3
*                                                                    P3
*********************************************************************P3
GNPATGER DS    0H                  NON-ZERO RETURN CODE FROM TGET    P3
         CR    R1,R15              WAS THIS AN ATTENTION INTERRUPT   P3
         BNE   GNPATGNA            NO - GO ISSUE MESSAGE             P3
         TM    GBITS1,GATTNHIT     YES - DID I GET ANY INPUT         P3
         BZR   R14                 NO - RETURN AS IF NULL INPUT      P3
         B     GNPAEXIT            YES - LET MAINLINE HANDLE INPUT   P3
GNPATGNA DS    0H                  NOT AN ATTENTION INTERRUPT        P3
         LR    R2,R15              PROTECT RETURN CODE               P3
         MVC   GTGETMSG(L'TGETMSG),TGETMSG COPY MESSAGE              P3
         CVD   R2,WORKAREA         CONVERT TO DECIMAL                P3
         OI    WORKAREA+7,X'0F'    FIX SIGN                          P3
         UNPK  WORKAREA(5),WORKAREA+5(3) UNPACK                      P3
         MVC   GTGETMSG+23(4),WORKAREA+1 MOVE INTO MESSAGE           P3
         LA    R0,L'TGETMSG        GET LENGTH OF MESSAGE             P3
         LA    R1,GTGETMSG         POINT TO MESSAGE                  P3
         TPUT  (1),(0),R           ISSUE MESSAGE                     P3
         LR    R15,R2              RESTORE RETURN CODE               P3
         OI    GBITS1,GDENY        DENY LOGON                        P3
         B     GNPAEXIT            EXIT                              P3
         EJECT                                                       P2
*********************************************************************P2
*                                                                    P2
*       EXECUTED INSTRUCTIONS FOR GETNPASS.                          P2
*                                                                    P2
*********************************************************************P2
MVCNPSWD MVC   GNEWPSWD(*-*),0(R1) MOVE NEW PASSWORD                 P2
         TITLE 'PROMPT FOR ACCOUNT NUMBER ROUTINE'                   P3
*********************************************************************P3
*                                                                    P3
* ROUTINE NAME -                                                     P3
*    GETACCT                                                         P3
*                                                                    P3
* FUNCTION -                                                         P3
*    PROMPT TSO USER FOR ACCOUNT NUMBER.                             P3
*                                                                    P3
* OPERATION -                                                        P3
*    1) CHECKS TO SEE IF TSO USER IS ON A DISPLAY TERMINAL.  IF NOT, P3
*       OVERPRINT BEFORE ASKING FOR ACCOUNT NUMBER.  IF THE USER IS  P3
*       ON A DISPLAY TERMINAL, PROMPT FOR PASSWORD WITH PRINT        P3
*       BYPASS.                                                      P3
*                                                                    P3
* ENTRY POINTS -                                                     P3
*    GETACCT                                                         P3
*                                                                    P3
* EXTERNAL REFERENCES -                                              P3
*    IKJPTGT (PUTGET)                                                P3
*                                                                    P3
* INPUT -                                                            P3
*    NONE                                                            P3
*                                                                    P3
* OUTPUT -                                                           P3
*    ACCOUNT NUMBER IN GACCTNUM.                                     P3
*                                                                    P3
* MESSAGES -                                                         P3
*    'ENTER ACCOUNT NUMBER -'                                        P3
*                                                                    P3
* COMMENTS -                                                         P3
*    NONE.                                                           P3
*                                                                    P3
*********************************************************************P3
         EJECT                                                       P3
GETACCT  DS    0H                                                    P3
         ST    R14,GACTSAVE        SAVE RETURN ADDRESS               P3
         ST    R2,GACTSAVE+4       SAVE R2                           P3
         EJECT                                                       P3
*********************************************************************P3
*                                                                    P3
*       CHECKS TO SEE IF TSO USER IS ON A DISPLAY TERMINAL.  IF NOT, P3
*       OVERPRINT BEFORE ASKING FOR ACCOUNT NUMBER.  IF THE USER IS  P3
*       ON A DISPLAY TERMINAL, PROMPT FOR PASSWORD WITH PRINT        P3
*       BYPASS.                                                      P3
*                                                                    P3
*********************************************************************P3
         TM    GBITS2,GDISPLAY     WAS THIS A DISPLAY TERMINAL       P3
         BO    GACTDISP            YES - USE PUTGET                  P3
GACTNDSP DS    0H                  NO - USE TPUT/TGET                P3
         LA    R0,L'ACCTMSG        GET LENGTH OF MESSAGE             P3
         LA    R1,ACCTMSG          POINT TO MESSAGE                  P3
         TPUT  (1),(0),R           ISSUE MESSAGE                     P3
         LTR   R15,R15             WAS RETURN CODE ZERO              P3
         BNZ   GACTEXIT            NO - EXIT                         P3
         LA    R0,LOVRPRNT         YES - GET LENGTH OF MESSAGE       P3
         LA    R1,OVERPRNT         POINT TO MESSAGE                  P3
         ICM   R1,B'1000',HEX02    SET OPTIONS FOR CONTROL           P3
         TPUT  (1),(0),R           ISSUE MESSAGE                     P3
         LTR   R15,R15             WAS RETURN CODE ZERO              P3
         BNZ   GACTEXIT            NO - EXIT                         P3
         LA    R0,L'GACCTNUM       YES - GET LENGTH OF ACCOUNT NUM.  P3
         LA    R1,GACCTNUM         GET ADDRESS OF ACCOUNT NUMBER     P3
         ICM   R1,B'1000',HEX80    SET OPTIONS                       P3
         MVC   GACCTNUM,BLANKS     BLANK INPUT AREA                  P3
         TGET  (1),(0),R           READ ACCOUNT NUMBER               P3
         LTR   R15,R15             WAS RETURN CODE ZERO              P3
         BZ    GACTTGOK            YES - CONTINUE                    P3
         BAL   R14,GACTTGER        NO - GO TO ERROR ROUTINE          P3
GACTTGOK DS    0H                                                    P3
         OC    GACCTNUM,BLANKS     FORCE TO UPPERCASE                P3
         CLC   GACCTNUM,BLANKS     WAS RESPONSE BLANKS               P3
         BE    GACTNDSP            YES - ASK AGAIN                   P3
         B     GACTEXIT            NO - EXIT                         P3
GACTDISP DS    0H                  TERMINAL WAS DISPLAY              P3
         MVC   GIKJPGPB(LPGPB),LPUTGET MOVE LIST TO WORK AREA        P3
         LA    R1,GIKJIOPL         GET ADDRESS OF IOPL               P3
         USING IOPL,R1             GET ADDRESSABILITY TO IOPL        P3
         MVC   IOPLUPT,GCPPL+CPPLUPT-CPPL COPY UPT ADDRESS TO IOPL   P3
         MVC   IOPLECT,GCPPL+CPPLECT-CPPL COPY ECT ADDRESS TO IOPL   P3
         LA    R15,GCPECB          GET ADDRESS OF CP ECB             P3
         ST    R15,IOPLECB         PUT CP ECB ADDRESS IN IOPL        P3
         XC    GCPECB,GCPECB       ZERO CP ECB                       P3
         LA    R15,GIKJPGPB        GET ADDRESS OF PUTGET PARM LIST   P3
         ST    R15,IOPLIOPB        PUT PUTGET PARM LIST ADDR IN IOPL P3
         XC    GPGOLD(4),GPGOLD    ZERO NUMBER OF SEGMENTS           P3
         MVI   GPGOLD+3,1          SET NUMBER TO 1                   P3
         LA    R15,AMSGHDR         GET ADDRESS OF ACCOUNT # MSG HDR. P3
         ST    R15,GPGOLD+4        PUT ADDRESS IN OLD                P3
         PUTGET OUTPUT=(GPGOLD,,PTBYPS),MF=(E,GIKJIOPL)              P3
         LTR   R15,R15             WAS PUTGET SUCCESSFUL             P3
         BNZ   GACTEXIT            NO - EXIT                         P3
         LA    R1,GIKJPGPB         YES - GET ADDRESS OF P/G PRM LIST P3
         USING PGPB,R1             GET ADDRESSABILITY TO PARM LIST   P3
         L     R1,PGPBIBUF         GET ADDRESS OF PUTGET INPUT BUFF  P3
         LR    R2,R1               SAVE ADDRESS FOR FREEMAIN         P3
         CLI   1(R1),12            WAS RESPONSE CORRECT LENGTH       P3
         BNE   GACTFREE            NO - FREE INPUT BUFF & ASK AGAIN  P3
         LA    R1,4(,R1)           YES - GET ADDRESS OF ACCOUNT #    P3
         MVC   GACCTNUM,0(R1)      MOVE ACCOUNT NUMBER               P3
         OC    GACCTNUM,BLANKS     FORCE TO UPPERCASE                P3
         LR    R1,R2               RESTORE THE BUFFER ADDRESS        P3
         LA    R0,1                GET THE SUBPOOL NUMBER            P3
         SLL   R0,24               SHIFT INTO HIGH-ORDER BYTE        P3
         ICM   R0,B'0011',0(R1)    GET THE LENGTH                    P3
         FREEMAIN R,A=(1),LV=(0)   FREE THE INPUT BUFFER             P3
         SLR   R15,R15             SET RETURN CODE                   P3
         B     GACTEXIT            AND EXIT                          P3
GACTFREE DS    0H                  FREE INPUT BUFFER AND ASK AGAIN   P3
         LR    R1,R2               RESTORE THE BUFFER ADDRESS        P3
         LA    R0,1                GET THE SUBPOOL NUMBER            P3
         SLL   R0,24               SHIFT INTO HIGH-ORDER BYTE        P3
         ICM   R0,B'0011',0(R1)    GET THE LENGTH                    P3
         FREEMAIN R,A=(1),LV=(0)   FREE THE INPUT BUFFER             P3
         B     GACTDISP            AND ASK AGAIN                     P3
GACTEXIT DS    0H                                                    P3
         L     R14,GACTSAVE        RESTORE RETURN ADDRESS            P3
         L     R2,GACTSAVE+4       RESTORE R2                        P3
         BR    R14                 RETURN TO CALLER                  P3
         EJECT                                                       P3
*********************************************************************P3
*                                                                    P3
*       IF RETURN CODE DID NOT INDICATE ATTENTION INTERRUPT, ISSUE   P3
*       MESSAGE TO USER AND DENY LOGON.                              P3
*                                                                    P3
*********************************************************************P3
GACTTGER DS    0H                  NON-ZERO RETURN CODE FROM TGET    P3
         CR    R1,R15              WAS THIS AN ATTENTION INTERRUPT   P3
         BNE   GACTTGNA            NO - GO ISSUE MESSAGE             P3
         TM    GBITS1,GATTNHIT     YES - DID I GET ANY INPUT         P3
         BZR   R14                 NO - RETURN AS IF NULL INPUT      P3
         B     GACTEXIT            YES - LET MAINLINE HANDLE INPUT   P3
GACTTGNA DS    0H                  NOT AN ATTENTION INTERRUPT        P3
         LR    R2,R15              PROTECT RETURN CODE               P3
         MVC   GTGETMSG(L'TGETMSG),TGETMSG COPY MESSAGE              P3
         CVD   R2,WORKAREA         CONVERT TO DECIMAL                P3
         OI    WORKAREA+7,X'0F'    FIX SIGN                          P3
         UNPK  WORKAREA(5),WORKAREA+5(3) UNPACK                      P3
         MVC   GTGETMSG+23(4),WORKAREA+1 MOVE INTO MESSAGE           P3
         LA    R0,L'TGETMSG        GET LENGTH OF MESSAGE             P3
         LA    R1,GTGETMSG         POINT TO MESSAGE                  P3
         TPUT  (1),(0),R           ISSUE MESSAGE                     P3
         LR    R15,R2              RESTORE RETURN CODE               P3
         OI    GBITS1,GDENY        DENY LOGON                        P3
         B     GACTEXIT            EXIT                              P3
         TITLE 'EQUATES'
R0       EQU   0                   PARAMETER REGISTER
R1       EQU   1                   PARAMETER REGISTER
R2       EQU   2                   WORK REGISTER
R3       EQU   3                   WORK REGISTER
R4       EQU   4                   WORK REGISTER
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8                   USED BY PARSE EXIT FOR DYN AREA P3.1
R9       EQU   9                   FIRST BASE REG - VALUE EP+0     P3.1
R10      EQU   10                  SECOND BASE REG - VALUE EP+4K   P3.1
R11      EQU   11                  THIRD BASE REG - VALUE EP+8K    P3.1
R12      EQU   12                  FOURTH BASE REG - VALUE EP+12K  P3.1
R13      EQU   13                  SAVE AREA & DYNAMIC AREA POINTER
R14      EQU   14                  RETURN ADDRESS
R15      EQU   15                  RETURN CODE
         SPACE 3
KBIT0    EQU   X'80'
KBIT1    EQU   X'40'
KBIT2    EQU   X'20'
KBIT3    EQU   X'10'
KBIT4    EQU   X'08'
KBIT5    EQU   X'04'
KBIT6    EQU   X'02'
KBIT7    EQU   X'01'
         TITLE 'CONSTANTS'
LESTAE   ESTAE ,MF=L               ESTAE PARAMETER LIST
LPUTGET  PUTGET ,MF=L              PUTGET PARAMETER LIST
FULL4    DC    F'4'                FOR DIVISION                    P3.1
FULL7    DC    F'7'                FOR DIVISION                    P3.1
FULL100  DC    F'100'              FOR DIVISION                    P3.1
FULL400  DC    F'400'              FOR DIVISION                    P3.1
FULL1000 DC    F'1000'             FOR DIVISION                    P3.1
FULL2000 DC    F'2000'             FOR DIVISION                    P3.1
FULL4000 DC    F'4000'             FOR DIVISION                    P3.1
HALF4    DC    H'4'                FOR SUBTRACTION                   P3
HALF5    DC    H'5'                FOR SUBTRACTION
HALF12   DC    H'12'               FOR MULTIPLICATION              P3.1
HALF45   DC    H'45'               FOR COMPARISON                    P3
HALF80   DC    H'80'               FOR COMPARISON                    P3
HALF90   DC    H'90'               FOR COMPARISON                    P3
HALF365  DC    H'365'              FOR MULTIPLICATION                P3
HALF522  DC    H'522'              FOR COMPARISON                    P3
*                HHMMSSTH                                            P3
LOGTIME  DC    C'00050000'         WALL TIME FOR LOGON TO COMPLETE   P3
KLOGON   DC    C'LOGON'            VALID COMMAND NAME
KLOGOFF  DC    C'LOGOFF'           VALID COMMAND NAME                P3
KEND     DC    C'END'              INDICATE END OF 'JCL'             P3
KNULL    DC    C'NULL'             USED TO NULL INFO FIELDS          P3
KLOCAL   DC    CL8'LOCAL'          CENTRAL SITE DEFAULT DEST       P3.2
$SYSTEM$ DC    CL8'$SYSTEM$'       MEMBER NAME OF $SYSTEM$ BLOCK   P3.1
NEVERDNY DC    CL8'NEVERDNY'       KEY IN $SYSTEM$                 P3.1
DIVLIMIT DC    CL8'DIVLIMIT'       KEY IN $SYSTEM$                 P3.1
HOLIDAYS DC    CL8'HOLIDAYS'       KEY IN $SYSTEM$                 P3.1
PRIMTIME DC    CL8'PRIMTIME'       KEY IN $SYSTEM$                 P3.1
DCPUTIME DC    C'2'                DEFAULT MAX CPU TIME (2 MINUTES)  P3
BLANKS   DC    CL80' '             BLANKS                            P3
         DC    X'0D'               CARRIAGE RETURN                   P2
OVERPRNT DC    8X'17'              RUBOUT
         DC    8C'X'               X'S
         DC    X'0D'               CARRIAGE RETURN
         DC    8X'17'              RUBOUT
         DC    8C'O'               O'S
         DC    X'0D'               CARRIAGE RETURN
         DC    8X'17'              RUBOUT
         DC    8C'A'               A'S
         DC    X'0D'               CARRIAGE RETURN
LOVRPRNT EQU   *-OVERPRNT          LENGTH FOR TPUT
         DC    X'25'               LINE FEED                         P2
TRTABLE  DC    C'0123456789ABCDEF' TRANSLATE HEX TO EBCDIC
OPENUPDT OPEN  (SYSUADS,UPDAT),MF=L OPEN FOR UPDATE                  P3
OPENOUT  OPEN  (SYSUADS,OUTPUT),MF=L OPEN FOR OUTPUT                 P3
SYSUADS  DCB   DDNAME=SYSUADS,MACRF=(R,W),DSORG=PO
LSYSUADS EQU   *-SYSUADS           CALCULATE LENGTH OF SYSUADS DCB
KEYZERO  MODESET KEY=ZERO,MF=L     GET KEY 0                         P3
NONZERO  MODESET KEY=NZERO,MF=L    RETURN TO USER KEY                P3
ASCBWORD DC    C'ASCB'             ACRONYM FOR ASCB
QNAME    DC    C'SYSIKJUA'         QNAME FOR ENQ
QNAME2   DC    C'SPFDSN  '         QNAME FOR ENQ                     P3
ENQLIST  ENQ   (,,E,,SYSTEM),RET=TEST,MF=L
         DS    F                   ALLOW ROOM FOR RESERVE UCB ADDR   P3
LENQLIST EQU   *-ENQLIST
ENQLIST2 ENQ   (,,E,,SYSTEM),RET=HAVE,MF=L                           P3
RESLIST  RESERVE (,,E,,SYSTEMS),RET=HAVE,UCB=0,MF=L                  P3
SDUMP    SDUMP MF=L                SDUMP PARAMETER LIST              P3
LSDUMP   EQU   *-SDUMP             CALCULATE LENGTH OF SDUMP PARM    P3
ZEROES   DC    F'0'                CONSTANT FOR COMPARISON
HEX02    DC    X'02'               TPUT CONTROL OPTIONS
HEX03    DC    X'03'               TPUT FULLSCR OPTIONS              P3
HEX80    DC    X'80'               TGET OPTIONS
*                HHMMSSTH
WAITTIME DC    C'00000500'         5 SEC WAIT FOR TUBES              P2
DATEMASK DC    X'4021204B202020'   EDIT MASK FOR DATE (BYY.DDD)      P3
DEFPNAME DC    C'/TSO SYSOUT'      DEFAULT PROGRAMMER NAME           P3
MSGCLASS DC    C',MSGCLASS='       MSGCLASS KEYWORD                  P3
MSGLEVEL DC    C',MSGLEVEL=('      MSGLEVEL KEYWORD                  P3
TIME     DC    C',TIME='           TIME KEYWORD                      P3
FORMS    DC    C',FORMS='          FORMS KEYWORD                     P3
ROOM     DC    C',ROOM='           ROOM KEYWORD                      P3
COPIES   DC    C',COPIES='         COPIES KEYWORD                    P3
LINECT   DC    C',LINECT='         LINECT KEYWORD                    P3
NXTBLANK DC    64X'00',C' ',191X'00' TRANSLATE TABLE FOR NEXT BLANK  P3
         TITLE 'TABLE OF USERIDS TO NEVER DENY'
USIDTABL DS    0C                  TABLE OF USERIDS TO NEVER DENY
         DC    X'FF'               END OF TABLE
         TITLE 'EXECUTED INSTRUCTIONS'
MVCACCT  MVC   GACCTNUM(*-*),0(R1) MOVE ACCOUNT NUMBER
         TITLE 'MESSAGES'
*                          1         2
*                0123456789012345678901234567
ESTAEMSG DC    C'ESTAE UNSUCCESSFUL - RC=XXXX'
STAXOBUF DC    C'ENTER ''LOGON'' OR ''LOGOFF'' -'                    P3
*                          1         2                               P3
*                012345678901234567890123456                         P3
STAXMSG  DC    C'STAX UNSUCCESSFUL - RC=XXXX'                        P3
*                          1         2                               P3
*                012345678901234567890123456                         P3
TGETMSG  DC    C'TGET UNSUCCESSFUL - RC=XXXX'                        P3
*                          1         2                               P3
*                012345678901234567890123456                         P3
STOWMSG  DC    C'STOW UNSUCCESSFUL - RC=XXXX'                        P3
RESCFAIL DC    C'LOGON RESOURCE FAILURE. CONTACT MEMPHIS TECHNICAL SUPPX
               ORT'                                                P3.2
LOGABEND DC    C'LOGON ABEND. CONTACT MEMPHIS TECHNICAL SUPPORT'
OPENMSG  DC    C'OPEN OF SYS1.UADS UNSUCCESSFUL'
$SYSMSG  DC    C'$SYSTEM$ NOT PRESENT. CONTACT MEMPHIS TECHNICAL SUPPORX
               T'                                                  P3.1
*                          1         2
*                012345678901234567890123456789
SCANMSG  DC    C'IKJSCAN UNSUCCESSFUL - RC=XXXX'
*                          1         2         3
*                0123456789012345678901234567890
GFAILMSG DC    C'GNRLFAIL UNSUCCESSFUL - RC=XXXX'
BCMNDMSG DC    C'COMMAND IS NOT ''LOGON'''
SRESMSG  DC    C'SYSTEM RESOURCE FAILURE. CONTACT MEMPHIS TECHNICAL SUPX
               PORT'                                               P3.2
NOTFOUND DC    C'USER NOT AUTHORIZED TO USE TIME-SHARING'
DIVQUOTA DC    C'DIVISIONAL QUOTA CURRENTLY LOGGED ON. TRY AGAIN LATER'
BADPASS  DC    C'PASSWORD NOT AUTHORIZED FOR THIS USERID'
PMSGHDR  DC    AL2(LPASSMSG,0),C' '
PASSMSG  DC    C'ENTER PASSWORD -'
LPASSMSG EQU   *-PMSGHDR
NPMSGHDR DC    AL2(LNPASMSG,0),C' '                                  P2
NPASSMSG DC    C'ENTER NEW PASSWORD -'                               P2
LNPASMSG EQU   *-NPMSGHDR                                            P2
BADACCT  DC    C'ACCOUNT NUMBER NOT AUTHORIZED FOR THIS USERID'
PRMPTMSG DC    C'PROMPT LIMIT EXCEEDED'
AMSGHDR  DC    AL2(LACCTMSG,0),C' '                                  P3
ACCTMSG  DC    C'ENTER ACCOUNT NUMBER -'
LACCTMSG EQU   *-AMSGHDR                                             P3
BADPROC  DC    C'PROCEDURE IGNORED'                                  P3
*                          1         2
*                012345678901234567890123456
ABNDMSG  DC    C'LOGON USER EXIT ABEND      '
*                     1         2         3         4         5      P3
*               4567890123456789012345678901234567890123456789012345678
ABNDWTO  WTO   'UNKNOWN ON TERMINAL LOGON USER EXIT ABEND       - ASID X
               X''NN''',DESC=2,ROUTCDE=(2,11),MF=L                   P3
*              90 123                                                P3
*               6                                                    P3
LABNDWTO EQU   *-ABNDWTO           CALCULATE LENGTH OF WTO MESSAGE   P2
DUPUSER  DC    C'USERID CURRENTLY LOGGED ON'
BADRECON DC    C'RECONNECT SPECIFIED BUT USERID NOT LOGGED ON'
*                     1         2         3         4         5      P3
*               4567890123456789012345678901234567890123456789012345678
TIMERWTO WTO   'UNKNOWN ON TERMINAL LOGON TERMINATED DUE TO WAIT TIME EX
               XCEEDED - ASID X''NN''',ROUTCDE=(2,11),MF=L         P3.1
*              90123456789012345 678                               P3.1
*               6         7                                          P3
LTIMRWTO EQU   *-TIMERWTO          CALCULATE LENGTH OF WTO MESSAGE   P3
*                     1         2         3         4                P3
*               4567890123456789012345678901234567890 123            P3
LOGWTO   WTO   'UNKNOWN LOGGING ON TERMINAL - ASID X''NN''',         P3X
               MCSFLAG=HRDCPY,MF=L                                   P3
LLOGWTO  EQU   *-LOGWTO            CALCULATE LENGTH OF WTO MESSAGE   P3
CLRSCRN  DC    X'27F5401D40'       CLEAR SCREEN                      P3
NAMEMSG  DC    C'ENTER YOUR NAME - '                                 P3
PHONEMSG DC    C'ENTER YOUR PHONE NUMBER - '                         P3
ADDRMSG  DC    C'ENTER YOUR ADDRESS - '                              P3
PASSWARN DC    C'PASSWORD SHOULD BE CHANGED'                         P3
LOG45MSG DC    C'USER HAS NOT LOGGED ON IN 45 DAYS'                  P3
PAS90MSG DC    C'USER HAS NOT CHANGED PASSWORD IN 90 DAYS'           P3
TIMEMSG  DC    C'TIME EXCEEDS USER''S LIMIT.  MAXIMUM USED'          P3
LASTCARD DC    C'ENTER LAST JCL CARD OR ''END'''                     P3
NEXTCARD DC    C'ENTER NEXT JCL CARD OR ''END'''                     P3
BADJCL   DC    C'JCL IGNORED'                                        P3
         TITLE 'PARS PARAMETER LIST'
         PUSH  PRINT                                                 P3
         PRINT NOGEN                                                 P3
         SPACE 5                                                     P3
PCLADDR  DC    A(PARMTAB)          ADDRESS OF PARS PARAMETER LIST
PARMTAB  IKJPARM DSECT=IKJPARMD    START OF PCL
PUSERID  IKJPOSIT UID2PSWD,PROMPT='USERID', USERID/PASSWORD/NEWPSWD  P2X
               VALIDCK=VUSERID
PACCTNUM IKJKEYWD
         IKJNAME 'ACCT',SUBFLD=ACCTSUB
PPROC    IKJKEYWD
         IKJNAME 'PROC',SUBFLD=PROCSUB
*                                                                    P2
PNEWPSWD IKJKEYWD
         IKJNAME 'NEWPSWD'                                           P2
PRECON   IKJKEYWD
         IKJNAME 'RECONNECT'
PMAIL    IKJKEYWD DEFAULT='MAIL'
         IKJNAME 'MAIL'
         IKJNAME 'NOMAIL'
PNOTICES IKJKEYWD DEFAULT='NOTICES'
         IKJNAME 'NOTICES'
         IKJNAME 'NONOTICES'
*                                                                    P2
PDEBUG   IKJKEYWD
         IKJNAME 'DEBUG',INSERT='LISTINFO'                           P3
*                                                                    P3
PNAME    IKJKEYWD
         IKJNAME 'NAME',SUBFLD=NAMESUB                               P3
*                                                                    P3
PPHONE   IKJKEYWD
         IKJNAME 'PHONE',SUBFLD=PHONSUB                              P3
*                                                                    P3
PADDRESS IKJKEYWD
         IKJNAME 'ADDRESS',SUBFLD=ADDRSUB                            P3
*                                                                    P3
PPROGNAM IKJKEYWD
         IKJNAME 'PROGNAME',SUBFLD=PROGSUB                           P3
*                                                                    P3
PCPUTIME IKJKEYWD
         IKJNAME 'TIME',SUBFLD=CPUTSUB                               P3
*                                                                    P3
PMSGCLAS IKJKEYWD
         IKJNAME 'MSGCLASS',SUBFLD=MSGCSUB                           P3
*                                                                    P3
PMSGLEVL IKJKEYWD
         IKJNAME 'MSGLEVEL',SUBFLD=MSGLSUB                           P3
*                                                                    P3
PFORMS   IKJKEYWD
         IKJNAME 'FORMS',SUBFLD=FORMSUB                              P3
*                                                                    P3
PROOM    IKJKEYWD
         IKJNAME 'ROOM',SUBFLD=ROOMSUB                               P3
*                                                                    P3
PLIST    IKJKEYWD
         IKJNAME 'LISTINFO'                                          P3
*                                                                    P3
PJCL     IKJKEYWD
         IKJNAME 'JCL'                                               P3
*                                                                    P3
PCOPIES  IKJKEYWD
         IKJNAME 'COPIES',SUBFLD=COPSUB                              P3
*                                                                    P3
PLINECT  IKJKEYWD
         IKJNAME 'LINECT',SUBFLD=LINSUB,ALIAS=('K') LIKE /*JOBPARM   P3
*                                                                  P3.2
PDEST    IKJKEYWD
         IKJNAME 'DEST',SUBFLD=DESTSUB                             P3.2
         SPACE 3
*        BEGIN SUBFIELDS
ACCTSUB  IKJSUBF
SACCT    IKJIDENT 'ACCOUNT NUMBER',FIRST=NONATNUM,MAXLNTH=8,           X
               OTHER=NONATNUM,PROMPT='ACCOUNT NUMBER',VALIDCK=VACCT
         SPACE 3
PROCSUB  IKJSUBF
SPROC    IKJIDENT 'PROCEDURE NAME',FIRST=ALPHA,MAXLNTH=8,              X
               OTHER=ALPHANUM,PROMPT='PROCEDURE NAME',VALIDCK=VPROC
         SPACE 3                                                     P3
*                                                                    P3
NAMESUB  IKJSUBF
SNAME    IKJIDENT 'YOUR NAME',MAXLNTH=63,CHAR,                       P3X
               PROMPT='YOUR NAME IN APOSTROPHES'                     P3
         SPACE 3                                                     P3
*                                                                    P3
PHONSUB  IKJSUBF
SPHONE   IKJIDENT 'YOUR PHONE NUMBER',MAXLNTH=63,CHAR,               P3X
               PROMPT='YOUR PHONE NUMBER IN APOSTROPHES'             P3
         SPACE 3                                                     P3
*                                                                    P3
ADDRSUB  IKJSUBF
SADDRESS IKJIDENT 'YOUR ADDRESS',MAXLNTH=63,CHAR,                    P3X
               PROMPT='YOUR ADDRESS IN APOSTROPHES'                  P3
         SPACE 3                                                     P3
*                                                                    P3
PROGSUB  IKJSUBF
SPROGNAM IKJIDENT 'PROGRAMMER NAME',MAXLNTH=20,CHAR,                 P3X
               PROMPT='PROGRAMMER NAME IN ASPOSTOPHES'               P3
         SPACE 3                                                     P3
*                                                                    P3
CPUTSUB  IKJSUBF
SCPUTIME IKJIDENT 'CPU TIME',MAXLNTH=4,FIRST=NUMERIC,                P3X
               OTHER=NUMERIC,PROMPT='CPU TIME'                       P3
         SPACE 3                                                     P3
*                                                                    P3
MSGCSUB  IKJSUBF
SMSGCLAS IKJIDENT 'MSGCLASS',MAXLNTH=1,FIRST=NONATNUM,               P3X
               PROMPT='MSGCLASS'                                     P3
         SPACE 3                                                     P3
*                                                                    P3
MSGLSUB  IKJSUBF
SMSGLEVL IKJIDENT 'MSGLEVEL',LIST,MAXLNTH=1,FIRST=NUMERIC,           P3X
               PROMPT='MSGLEVEL'                                     P3
         SPACE 3                                                     P3
*                                                                    P3
FORMSUB  IKJSUBF
SFORMS   IKJIDENT 'JOB FORMS',MAXLNTH=4,FIRST=NONATNUM,              P3X
               OTHER=NONATNUM,PROMPT='JOB FORMS'                     P3
         SPACE 3                                                     P3
*                                                                    P3
ROOMSUB  IKJSUBF
SROOM    IKJIDENT 'ROOM NUMBER',MAXLNTH=4,FIRST=NONATNUM,            P3X
               OTHER=NONATNUM,PROMPT='ROOM NUMBER'                   P3
         SPACE 3                                                     P3
*                                                                    P3
COPSUB   IKJSUBF
SCOPIES  IKJIDENT 'COPIES',MAXLNTH=3,FIRST=NUMERIC,                  P3X
               OTHER=NUMERIC,PROMPT='COPY COUNT'                     P3
         SPACE 3                                                     P3
*                                                                    P3
LINSUB   IKJSUBF
SLINECT  IKJIDENT 'LINECT',MAXLNTH=3,FIRST=NUMERIC,                  P3X
               OTHER=NUMERIC,PROMPT='LINE COUNT'                     P3
         SPACE 3                                                     P3
*                                                                  P3.2
DESTSUB  IKJSUBF
SDEST    IKJPOSIT USERID,PROMPT='SYSOUT DESTINATION'               P3.2
         SPACE 3                                                   P3.2
         IKJENDP
         SPACE 5                                                     P3
         POP   PRINT                                                 P3
         TITLE 'PATCH AREA'
         ENTRY PATCH               PUT PATCH ADDRESS IN LKED MAP
PATCH    DC    50S(*)              GENERATE SCONS FOR PATCH AREA
         TITLE 'MODEL JCL CARDS'                                     P3
LJCLMODL DC    0F'0',C' ',AL3(LENJMODL) FILL CHARACTER AND LENGTH    P3
JCLMODEL DS    0C                  START OF MODEL FOR JCL            P3
*                             1         2          3         4       P3
*                   012345678901234567890123 456789012345678901234 5 P3
MJOBCARD DC    CL80'//USER-ID JOB ACCT-NUM,''                    ''' P3
MEXECARD DC    CL80'//PROCNAME EXEC '                                P3
MJOBPARM DC    CL80'/*JOBPARM '                                      P3
LENJMODL EQU   *-JCLMODEL          CALCULATE LENGTH OF JCL MODEL     P3
         TITLE 'MODEL INFO BLOCK'                                    P3
LINFMODL DC    0F'0',C' ',AL3(LENIMODL) FILL CHARACTER AND LENGTH    P3
INFOMODL DS    0C                  START OF MODEL FOR INFO BLOCK     P3
MUSERID  DC    CL80'USERID  '      USERID                            P3
MNAME    DC    CL80'NAME    '      USER'S NAME                       P3
MPHONE   DC    CL80'PHONE   '      BUSINESS PHONE                    P3
MADDRESS DC    CL80'ADDRESS '      INTERNAL MAILING ADDRESS          P3
MLASTON  DC    CL80'LASTON  '      YY.DDD TERMINAL LAST LOGGED ON    P3
MCHGPSWD DC    CL80'CHGPSWD '      YY.DDD PASSWORD LAST CHANGED      P3
MCPUTIME DC    CL80'CPUTIME '      MAXIMUM CPU TIME                  P3
MDEFTIME DC    CL80'DEFTIME '      DEFAULT CPU TIME                  P3
MPROGNAM DC    CL80'PROGNAME'      PROGRAMMER'S NAME FOR JOBCARD     P3
MFORMS   DC    CL80'FORMS   '      FORMS FOR /*JOBPARM               P3
MROOM    DC    CL80'ROOM    '      ROOM FOR /*JOBPARM                P3
LENIMODL EQU   *-INFOMODL          CALCULATE LENGTH OF INFO MODEL    P3
         TITLE 'CPPL DSECT'
         IKJCPPL ,                 GENERATE CPPL DSECT
         DS    0F
LCPPL    EQU   *-CPPL              CALCULATE LENGTH OF CPPL
         TITLE 'CBUF DSECT'
CBUF     DSECT                     COMMAND BUFFER DSECT
CBUFLEN  DS    H                   LENGTH
CBUFOFFS DS    H                   OFFSET
CBUFTEXT DS    C                   TEXT
         TITLE 'STAX PARAMETER LIST DSECT'                           P3
STX      DSECT                                                       P3
STXEXIT  DS    F                   ADDRESS OF STAX EXIT              P3
STXISIZ  DS    H                   LENGTH OF IBUF                    P3
STXOSIZ  DS    H                   LENGTH OF OBUF                    P3
STXOBUF  DS    F                   ADDRESS OF OBUF                   P3
STXIBUF  DS    F                   ADDRESS OF IBUF                   P3
STXOPTS  DS    X                   OPTION FLAGS                      P3
STXUSER  DS    XL3                 ADDRESS OF USER AREA              P3
         TITLE 'ATTENTION EXIT PARAMETER LIST DSECT'                 P3
AEPL     DSECT                                                       P3
AEPLTAIE DS    F                   ADDRESS OF THE TAIE               P3
AEPLIBUF DS    F                   ADDRESS OF IBUF                   P3
AEPLUSAD DS    F                   ADDRESS OF USER AREA              P3
         TITLE 'TERMINAL ATTENTION INTERRUPT ELEMENT DSECT'          P3
         IKJTAIE ,                 GENERATE TAIE DSECT               P3
         TITLE 'CSPL DSECT'
         IKJCSPL ,                 GENERATE CSPL DSECT
         DS    0F
LCSPL    EQU   *-CSPL              CALCULATE LENGTH OF CSPL
         TITLE 'CSOA DSECT'
         IKJCSOA ,                 GENERATE CSOA DSECT
         DS    0F
LCSOA    EQU   *-CSOA              CALCULATE LENGTH OF CSOA
         TITLE 'PPL DSECT'
         IKJPPL ,                  GENERATE PPL DSECT
         DS    0F
LPPL     EQU   *-PPL               CALCULATE LENGTH OF PPL
         TITLE 'GNRLFAIL DSECT'
         IKJEFFGF GFDSECT=YES      GENERATE GNRLFAIL DSECT
         TITLE 'PGPB DSECT'
         IKJPGPB ,                 GENERATE PGPB DSECT
         DS    0F
LPGPB    EQU   *-PGPB              CALCULATE LENGTH OF PGPB
         TITLE 'IOPL DSECT'
         IKJIOPL ,                 GENERATE IOPL DSECT
         DS    0F
LIOPL    EQU   *-IOPL              CALCULATE LENGTH OF IOPL
         TITLE 'ASVT DSECT'
         IHAASVT ,                 GENERATE ASVT DSECT
         TITLE 'ASCB DSECT'
         PUSH  PRINT                                                 P3
         PRINT NOGEN               SUPPRESS PRINTING OF DSECT        P3
         IHAASCB ,                 GENERATE ASCB DSECT
         POP   PRINT                                                 P3
         TITLE 'ASXB DSECT'                                          P3
         PUSH  PRINT                                                 P3
         PRINT NOGEN               SUPPRESS PRINTING OF DSECT        P3
         IHAASXB ,                 GENERATE ASXB DSECT               P3
         POP   PRINT                                                 P3
         TITLE 'LWA DSECT'                                           P3
LWA      DSECT                                                       P3
         ORG   LWA+X'18'                                             P3
LWAPSCB  DS    F                   ADDRESS OF PSCB                   P3
         ORG   LWA+X'3B'                                             P3
LWAFLGS4 DS    X                                                     P3
LWAILGN  EQU   KBIT7               INDICATES INITIAL LOGON           P3
         TITLE 'UPT DSECT'                                           P3
UPT      DSECT                                                       P3
         DS    H                   RESERVED                          P3
UPTUSER  DS    CL10                RESERVED FOR INSTALLATION USE     P3
UPTSWS   DS    X                   USERS ENVIRONMENT SWITCHES        P3
UPTMID   EQU   KBIT2               PRINT MESSAGE IDENTIFICATION      P3
UPTCDEL  DS    X                   CHAR DELETE CHARACTER             P3
UPTLDEL  DS    X                   LINE DELETE CHARACTER             P3
         DS    X                   RESERVED                          P3
UPTPREFX DS    CL7                 DSNAME PREFIX                     P3
UPTPREFL DS    X                   LENGTH OF DSNAME PREFIX           P3
         TITLE 'TSB DSECT'
         IKJTSB ,                  GENERATE TSB DSECT
         TITLE 'CVT DSECT'
         PUSH  PRINT
         PRINT NOGEN               SUPPRESS PRINTING OF DSECT
         CVT   DSECT=YES           GENERATE CVT DSECT               .P3
         SPACE 5
         POP   PRINT
         TITLE 'DCB DSECT'
         PUSH  PRINT
         PRINT NOGEN               SUPPRESS PRINTING OF DSECT
         DCBD  DSORG=PS,DEVD=DA    GENERATE DCB DSECT
         SPACE 5
         POP   PRINT
LOGONXIT CSECT                     PICK UP LOGONXIT CSECT
         TITLE 'SDWA DSECT'
         PUSH  PRINT
         PRINT NOGEN               SUPPRESS PRINTING OF DSECT
         IHASDWA ,                 GENERATE SDWA DSECT
         SPACE 5
         POP   PRINT
LOGONXIT CSECT                     PICK UP LOGONXIT CSECT
         TITLE 'PSA DSECT'                                           P3
         PUSH PRINT                                                  P3
         PRINT NOGEN               SUPPRESS PRINTING OF DSECT        P3
         IHAPSA ,                  GENERATE PSA DSECT                P3
         SPACE 5                                                     P3
         POP   PRINT                                                 P3
LOGONXIT CSECT                     PICK UP LOGONXIT CSECT            P3
         TITLE 'TCB DSECT'                                           P3
         PUSH PRINT                                                  P3
         PRINT NOGEN               SUPPRESS PRINTING OF DSECT        P3
         IKJTCB ,                  GENERATE TCB DSECT                P3
         SPACE 5                                                     P3
         POP   PRINT                                                 P3
LOGONXIT CSECT                     PICK UP LOGONXIT CSECT            P3
         TITLE 'JSCB DSECT'                                          P3
         PUSH PRINT                                                  P3
         PRINT NOGEN               SUPPRESS PRINTING OF DSECT        P3
         IEZJSCB ,                 GENERATE JSCB DSECT               P3
         SPACE 5                                                     P3
         POP   PRINT                                                 P3
LOGONXIT CSECT                     PICK UP LOGONXIT CSECT            P3
         TITLE 'PSCB DSECT'                                          P3
         IKJPSCB ,                 GENERATE PSCB DSECT               P3
         TITLE 'ECT DSECT'                                         P3.2
         IKJECT ,                  GENERATE ECT DSECT              P3.2
LOGONXIT CSECT                     PICK UP LOGONXIT CSECT            P3
         TITLE 'DYNAMIC AREA'
GOTNAREA DSECT
SAVEAREA DS    9D                  MAIN LINE SAVE AREA
VERSAVEA DS    9D                  VERIFY ROUTINES SAVE AREA
WORKAREA DS    D                   DOUBLE WORD WORK AREA
DAYSWORK DS    D                   WORK AREA FOR DAYS SUBROUTINE     P3
GDLMSAVE DS    4F                  SAVE AREA FOR GETDVLIM SUBR     P3.1
GDLMYEAR DS    F                   GETDVLIM YEAR                   P3.1
GDLMDAY  DS    F                   GETDVLIM DAY                    P3.1
ADDRPARM DS    F                   ADDRESS OF PARMS
ADDRUBUF DS    F                   ADDRESS OF SYS1.UADS BUFFER
ADDRINFO DS    F                   ADDRESS OF INFO BLOCK             P3
ADDR$SYS DS    F                   ADDRESS OF $SYSTEM$ BLOCK       P3.1
ADDRUCB  DS    F                   ADDRESS OF SYS1.UADS UCB          P3
PASSOFF  DS    F                   OFFSET TO MATCHING PSWD OFFSET BLOCK
EXLST    DS    F                   DCB EXIT LIST FOR RDJFCB          P3
GJFCB    DS    XL176               JFCB AREA                         P3
GMSGAREA DS    CL100               COMMON MESSAGE AREA
         ORG   GMSGAREA            OVERLAY COMMON MESSAGE AREA
GESTAMSG DS    CL(L'ESTAEMSG)      ESTAE MESSAGE
         ORG   GMSGAREA            OVERLAY COMMON MESSAGE AREA
GSTAXMSG DS    CL(L'STAXMSG)       STAX MESSAGE                      P3
         ORG   GMSGAREA            OVERLAY COMMON MESSAGE AREA       P3
GSTOWMSG DS    CL(L'STOWMSG)       STOW MESSAGE                      P3
         ORG   GMSGAREA            OVERLAY COMMON MESSAGE AREA       P3
GTGETMSG DS    CL(L'TGETMSG)       TGET MESSAGE                      P3
         ORG   GMSGAREA            OVERLAY COMMON MESSAGE AREA       P3
GLOGWTO  DS    CL(LLOGWTO)         LOGON WTO MESSAGE                 P3
         ORG   GMSGAREA            OVERLAY COMMON MESSAGE AREA       P3
GSCANMSG DS    CL(L'SCANMSG)       IKJSCAN MESSAGE
         ORG   GMSGAREA            OVERLAY COMMON MESSAGE AREA
GGFALMSG DS    CL(L'GFAILMSG)      GNRLFAIL MESSAGE
         ORG   GMSGAREA            OVERLAY COMMON MESSAGE AREA
GABNDMSG DS    CL(L'ABNDMSG)       ABEND MESSAGE
         ORG   GMSGAREA            OVERLAY COMMON MESSAGE AREA
GABNDWTO DS    CL(LABNDWTO)        ABEND WTO MESSAGE                 P2
         ORG   ,                   GET BACK IN SYNC                  P2
PROMPTCT DS    X                   PROMPT COUNTER
         SPACE 5
*        THE FOLLOWING STORAGE IS FOR THE CPPL
         DS    0F                  ALIGN FOR CPPL
GCPPL    DS    XL(LCPPL)           CPPL
         SPACE 5
*        THE FOLLOWING STORAGE IS FOR THE CBUF
GCBUF    DS    XL256               CBUF
         SPACE 5
*        THE FOLLOWING STORAGE IS FOR ESTAE
GESTAE   ESTAE ,MF=L               ESTAE PARAMETER LIST
LGESTAE  EQU   *-GESTAE            CALCULATE LENGTH OF ESTAE PARM LIST
ESTAEPRM DS    5F                  ESTAE EXIT PARAMETER LIST       P3.1
ABENDCDE DS    F                   ABEND CODE FROM ESTAE EXIT
ABENDPSW DS    CL8                 ABEND PSW FROM ESTAE EXIT         P3
         SPACE 5
*        THE FOLLOWING STORAGE IS FOR STAX                           P3
GDSTAX   STAX  ,MF=L               DUMMY STAX PARAMETER LIST         P3
GSTAX    STAX  ,MF=L               STAX PARAMETER LIST               P3
LSTAX    EQU   *-GSTAX             CALCULATE LENGTH OF STAX PRM LIST P3
         SPACE 5                                                     P3
*        THE FOLLOWING STORAGE IS FOR SAVE AREAS FOR INTERNAL ROUTINES
HSKPSAVE DS    F                   HOUSKEEPING ROUTINE
SCANSAVE DS    2F                  SCAN COMMAND BUFFER ROUTINE
PARSSAVE DS    2F                  PARSE COMMAND BUFFER ROUTINE
GDATSAVE DS    2F                  GET DATA FROM PDL ROUTINE
GPASSAVE DS    F                   PROMPT FOR PASSWORD ROUTINE
GACTSAVE DS    F                   PROMPT FOR ACCOUNT NUMBER ROUTINE P3
GNPASAVE DS    F                   PROMPT FOR NEW PASSWORD ROUTINE   P2
VDATSAVE DS    2F                  VERIFY DATA ROUTINE
UDATSAVE DS    3F                  UPDATE DATA ROUTINE               P3
GINFSAVE DS    3F                  GET INFO ROUTINE                  P3
VINFSAVE DS    3F                  VERIFY INFO ROUTINE               P3
UINFSAVE DS    3F                  UPDATE INFO ROUTINE               P3
BJCLSAVE DS    3F                  BUILD JCL SUBROUTINE              P3
LINFSAVE DS    2F                  LIST INFO ROUTINE                 P3
GAUTSAVE DS    F                   GET AUTHORIZATON SUBROUTINE       P3
DBUGSAVE DS    F                   DEBUG ROUTINE
         SPACE 5
*        THE FOLLOWING STORAGE IS FOR STAX AND STIMER EXITS          P3
         DS    0F                  FORCE ALIGNMENT                   P3
GTIMRXIT DS    XL(LTIMRXIT)        STIMER EXIT ROUTINE IS MOVED HERE P3
GTIMRWTO DS    CL(LTIMRWTO)        TIMER WTO MESSAGE                 P3
GCPECB   DS    F                   CP ECB                            P3
         SPACE 5                                                   P3.2
*        THE FOLLOWING STORAGE IS FOR SWITCHES                     P3.2
GBITS    DS    0F                  SWITCHES                          P3
GBITS1   DS    X                   SWITCHES                          P3
GDENY    EQU   KBIT0               LOGON DENIED
GRECON   EQU   KBIT1               'RECON' SPECIFIED
GBITS1B2 EQU   KBIT2               NOT USED                        P3.2
GBITS1B3 EQU   KBIT3               NOT USED                        P3.2
GDEBUG   EQU   KBIT4               'DEBUG' SPECIFIED
GDUPUSER EQU   KBIT5               DUPLICATE USERID LOGGED ON
GLIST    EQU   KBIT6               'LISTINFO' SPECIFIED              P3
GATTNHIT EQU   KBIT7               ATTENTION HIT                     P3
GBITS2   DS    X                   SWITCHES                          P3
GSPFRES  EQU   KBIT0               DSN RESERVED                      P3
GSPFENQ0 EQU   KBIT1               MEMBER0 ENQ'D ON                  P3
GSPFENQI EQU   KBIT2               INFO BLOCK ENQ'D ON               P3
GJCL     EQU   KBIT3               'JCL' SPECIFIED                   P3
GABEND   EQU   KBIT4               ABEND HAS OCCURED                 P3
GDISPLAY EQU   KBIT5               DISPLAY TERMINAL                  P3
GREWRITE EQU   KBIT6               REWRITE FIRST UADS BLOCK        P3.2
GBITS3   DS    X                   SWITCHES                          P3
GBITS4   DS    X                   SWITCHES                          P3
GECTSWS  DS    X                   COPY OF ECTSWS                  P3.2
         SPACE 5                                                     P3
*        THE FOLLOWING STORAGE IS FOR SCAN
GIKJCSPL DS    XL(LCSPL)           CSPL
GCSPLFLG DS    F                   CSPL FLAG
GCSPLOA  DS    XL(LCSOA)           CSPL OUTPUT AREA
         SPACE 5
*        THE FOLLOWING STORAGE IS FOR PARSE
GIKJPPL  DS    XL(LPPL)            PPL
GPPLANS  DS    F                   PPL PDL
         SPACE 5
*        THE FOLLOWING STORAGE IS FOR GNRLFAIL
GGFPARMS DS    XL(GFLENGF)         GNRLFAIL PARAMETER LIST
GGFPARMP DS    F                   GNRLFAIL PARM LIST POINTER
         SPACE 5
*        THE FOLLOWING STORAGE IS FOR PUTGET
GIKJPGPB DS    XL(LPGPB)           PGPB
GIKJIOPL DS    XL(LIOPL)           IOPL
GPGOLD   DS    2F                  OUTPUT LINE DESCRIPTOR (OLD)
         SPACE 5
*        THE FOLLOWING STORAGE IS FOR INFO RETRIEVED FROM PDL & UADS
GUSERID  DS    CL8                 USERID
GMEMBER  DS    CL8                 MEMBER NAME                       P3
GPASS    DS    CL8                 PASSWORD
GACCTNUM DS    CL8                 ACCOUNT NUMBER
GPROC    DS    CL8                 PROCEDURE NAME
GNEWPSWD DS    CL8                 NEW PASSWORD                      P2
GALTPROC DS    CL8                 ALTERNATE PROCEDURE NAME          P3
GTERMID  DS    CL8                 TERMINAL ID FROM TSB              P3
GASID    DS    CL2                 ASID FROM ASCB                    P3
LUSERID  DS    H                   LENGTH OF USERID
GNAME    DS    CL63                NAME                              P3
GPHONE   DS    CL63                PHONE NUMBER                      P3
GADDRESS DS    CL63                ADDRESS                           P3
GPROGNAM DS    CL20                PROGRAMMER NAME                   P3
GFORMS   DS    CL4                 FORMS                             P3
GROOM    DS    CL4                 ROOM                              P3
GCPUTIME DS    CL4                 CPU TIME                          P3
GMSGCLAS DS    C                   MSGCLASS                          P3
GMSGLEVL DS    CL3                 MSGLEVEL                          P3
GCOPIES  DS    CL3                 COPIES                            P3
GLINECT  DS    CL3                 LINECT                            P3
GDEST    DS    CL8                 DEST                            P3.2
         SPACE 5
* THE FOLLOWING STORAGE IS FOR ACCESS OF SYS1.UADS
         DS    0F                  ALIGN FOR OPEN LIST
GOPENLST DS    F                   OPEN LIST
GTTR1ST  DS    F                   TTR OF FIRST BLOCK                P3
GTTRINFO DS    F                   TTR OF INFO BLOCK                 P3
GLEN1ST  DS    F                   LENGTH OF FIRST BLOCK             P3
GLENINFO DS    F                   LENGTH OF INFO BLOCK              P3
GSYSUADS DS    XL(LSYSUADS)        DCB FOR SYS1.UADS
         READ  DECB,SF,MF=L        READ/WRITE DECB LIST              P2
         SPACE 5
* THE FOLLOWING STORAGE IS FOR ENQ
         DS    0F                  ALIGNMENT                         P3
GENQLIST DS    XL(LENQLIST)        ENQ LIST
GRNAME   DS    CL52                RNAME FOR SPF TYPE ENQ            P3
         SPACE 5                                                     P3
* THE FOLLOWING STORAGE IS FOR SDUMP                                 P3
         DS    0F                  ALIGNMENT                         P3
GSDUMP   DS    XL(LSDUMP)          SDUMP PARAMETER LIST              P3
         SPACE 5                                                     P3
* THE FOLLOWING STORAGE IS FOR BLDL                                  P3
GBLDL    DS    0F                  BLDL LIST                         P3
GBLL     DS    H                   LL                                P3
GBFF     DS    H                   FF                                P3
GBNAME   DS    CL8                 NAME                              P3
GBTTR    DS    XL4                 TTRC                              P3
         DS    0D                  FILL OUT DSECT
GOTNLEN  EQU   *-GOTNAREA          LENGTH OF DYNAMIC AREA
LOGONXIT CSECT
         SPACE 10
         END
