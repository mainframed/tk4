IKJEFLD  TITLE 'TSO PRE-PROMPT LOGON EXIT'
IKJEFLD  CSECT
         SAVE  (14,12),,IKJEFLD-PS00031-&SYSDATE SAVE CALLER'S REGS
         USING IKJEFLD,R12         ESTABLISH ADDRESSABILITY
         LR    R12,R15             LOAD BASE
         LR    R2,R1               SAVE PARM LIST ADDRESS IN R2
         GETMAIN R,LV=GOTNLEN      GETMAIN DYNAMIC AREA
         LR    R15,R1              GET ADDRESS OF DYNAMIC AREA
         USING GOTNAREA,R13        ESTABLISH ADDRESSABILITY
         ST    R13,4(,R15)         CHAIN
         ST    R15,8(,R13)           SAVEAREAS
         LR    R13,R15             POINT TO MY SAVE AREA
         LA    R15,1               GET FF FOR BLDL
         STH   R15,BLIST           PUT FF IN LIST
         LA    R15,58              GET LL FOR BLDL
         STH   R15,BLIST+2         PUT LL IN LIST
         MVC   BNAME,LOGONXIT      PUT NAME IN LIST
         L     R15,4(,R2)          GET ADDRESS OF INPUT BUFFER DESC  P3
         L     R15,0(,R15)         GET ADDRESS OF INPUT BUFFER       P3
         CLI   6(R15),C'#'         IS THIS A TEST (LOGON#)         P3.2
         BNE   DOBLDL              NO - CONTINUE                     P3
         MVI   6(R15),C' '         YES - BLANK IT                  P3.2
         MVC   BNAME,LOGONTST      CHANGE NAME OF LOGONXIT           P3
DOBLDL   DS    0H                                                    P3
         BLDL  0,BLIST             CHECK TO SEE IF LOGONXIT IS AVAIL.
         LTR   R15,R15             WAS RETURN CODE ZERO
         BNZ   RETURN              NO - EXIT
         LOAD  DE=BNAME            YES - LOAD LOGONXIT
         LR    R15,R0              GET ENTRY POINT ADDRESS
         LR    R1,R2               RESTORE PARM POINTER
         BALR  R14,R15             CALL LOGONXIT
         LR    R2,R15              SAVE RETURN CODE FROM LOGONXIT
         DELETE DE=BNAME           DELETE LOGONXIT MODULE
RETURN   DS    0H
         LR    R1,R13              GET POINTER TO DYNAMIC AREA
         L     R13,4(,R13)         UNCHAIN SAVE AREAS
         FREEMAIN R,LV=GOTNLEN,A=(1) FREE DYNAMIC AREA
         LR    R15,R2              RESTORE RETURN CODE
         RETURN (14,12),RC=(15)    RETURN TO CALLER
         TITLE 'EQUATES'
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12                  BASE REGISTER - VALUE EP + 0
R13      EQU   13                  SAVE AREA POINTER
R14      EQU   14                  RETURN ADDRESS
R15      EQU   15                  RETURN CODE
         TITLE 'DYNAMIC AREA'
GOTNAREA DSECT
SAVEAREA DS    9D
BLIST    DS    F                   FF & LL
BNAME    DS    CL8                 MODULE NAME
         DS    50X                 DIRECTORY INFO
         DS    0D                  FILL OUT DSECT
GOTNLEN  EQU   *-GOTNAREA          LENGTH OF DYNAMIC AREA
IKJEFLD  CSECT
         TITLE 'CONSTANTS'
LOGONXIT DC    CL8'LOGONXIT'
LOGONTST DC    CL8'LOGONTST'                                         P3
         TITLE 'PATCH AREA'
PATCH    DC    50S(*)              GENERATE SCONS FOR PATCH AREA
         SPACE 5
         END
