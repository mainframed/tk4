         TITLE '* * *  TSOACRPT  * * *'
***********************************************************************
*                                                                     *
* PROGRAM NAME - TSOACRPT                                             *
*                                                                     *
* PURPOSE - TO PROVIDE LISTING OF TSO ACCOUNT INFORMATION.  (AS       *
*           OPPOSSED TO TSO ACCOUNT LIST COMMAND.)                    *
*                                                                     *
* OPERATION - THE PROGRAM READS A TSO ACCOUNT LIST COMMAND'S OUTPUT   *
*             AND SORTS ACCOUNT NUMBERS BY ACCOUNT NUMBERS AND        *
*             PRODUCES THE REPORT.  THE SORT PROGRAM IS USED WITH     *
*             EXITS E15 & E35.                                        *
*                                                                     *
* WRITTEN BY BEN MOORE JULY 1979                                      *
*    MOST OF THIS CODE WAS STOLEN FROM TSOUSRPT WRITTEN BY DOUG MOORE.*
*                                                                     *
* MODIFIED BY BEN MOORE JULY 1980                                     *
*    ADDED DIVISIONS K AND F.                                         *
*                                                                     *
* MODIFIED BY BEN MOORE AUGUST 1980                                   *
*    REMOVED LRECL, BLKSIZE, RECFM FROM ACCTLIST DCB.                 *
*                                                                     *
***********************************************************************
         EJECT
TSOACRPT CSECT
         SAVE  (14,12),,TSOACRPT-&SYSDATE-&SYSTIME  SAVE REGS
         LR    R12,R15             LOAD BASE REGISTER
         USING TSOACRPT,R12
         ST    R13,RSA+4           CHAIN SAVE AREAS
         LR    R2,R13              SAVE CALLERS REG 13
         LA    R13,RSA             POINT TO MY RSA
         ST    R13,8(R2)           CHAIN SAVE AREAS
         SPACE 3
SORT     DS    0H
         LA    R1,PARMPTR          LOAD ADDR OF PARM LIST POINTER
         LINK  EP=SORT
         LTR   R2,R15              Q. SORT SUCCESSFUL
         BZ    MAINRTRN            ...YES, ALL DONE
         SPACE
         WTO   '*** TSOACRPT - NONZERO RETURN CODE FROM SORT ***',     *
               ROUTCDE=(11)
         LA    R1,1                SET UP ABEND
         SLL   R1,31
         AR    R1,R2
         ABEND (1)                 ABEND WITH U00XX
         SPACE 3
MAINRTRN DS    0H
         L     R13,4(R13)          RESTORE CALLERS RSA
         RETURN (14,12),RC=0       RETURN
         TITLE '***  SORT EXIT E15  ***'
***********************************************************************
* THIS EXIT READS THE ACCOUNT LIST COMMAND LISTING AND EXTRACTS THE   *
* USERID ACCOUNT NUMBERS AND PASSES THE RECORD TO SORT TO BE SORTED BY*
* USERID AND ACCOUNT NUMBER.                                          *
* NOTE: THE LOGON PREPROMPT EXIT MAINTAINS A USER INFORMATION BLOCK.  *
* THE MEMBER NAME OF THE BLOCK IS $USERID.  THE ACCOUNT LIST          *
* SUBCOMMAND WHEN USED IN THE FORM 'L (*)' SCREWS UP BECAUSE OF THE   *
* $ MEMBERS.  ONE DUMMY MEMBER NAMED $$ HAS BEEN ADDED TO FIX THE     *
* PROBLEM.  THIS USERID ($$) SHOULD BE SKIPPED.                       *
***********************************************************************
         SPACE
E15      DS    0H
         SAVE  (14,12),,E15        SAVE REGS
         USING E15,R15             SET UP TEMPORARY BASE
         LR    R2,R13              CHAIN SAVE AREAS
         LA    R13,EXITRSA         POINT TO E15 RSA
         ST    R2,4(R13)
         ST    R13,8(R2)
         L     R12,MAINBASE        GET ORIGINAL BASE REG
         USING TSOACRPT,R12
         DROP  R15
         SPACE
         TM    UADSLIST+DCBOFLGS-IHADCB,DCBOFOPN Q. IS UADS LIST OPEN
         BO    ACCTLP              ...YES, GO LOOK FOR ACCT #
         OPEN  (UADSLIST,INPUT)    OPEN FILE
         TM    UADSLIST+DCBOFLGS-IHADCB,DCBOFOPN Q. OPEN SUCCESSFUL
         BNO   E15QUIT             ...NO, GIVE UP
         SPACE
READLOOP DS    0H
         GET   UADSLIST
         CLC   0(2,R1),=AL2(32)    Q. IS THIS RECORD LONG ENOUGH
         BL    READLOOP            ...NO, TRY ANOTHER
         CLC   17(15,R1),=CL15'USER ATTRIBUTES' Q. IS THIS USERID LINE
         BNE   READLOOP            ...NO, KEEP LOOKING
         SPACE
* FOUND USER ID
NEXTUID  DS    0H
         CLI   8(R1),C'$'          Q. IS THIS THE DUMMY USER
         BE    READLOOP            ...YES, GO GET NEXT USERID
         MVC   SORTUID,8(R1)       SAVE USER ID FOR SORT RECORD
         SPACE
INATLOOP DS    0H
         GET   UADSLIST            READ INSTALLATION ATTRIBUTES
         CLC   0(2,R1),=AL2(32)    Q. IS THIS RECORD LONG ENOUGH
         BL    INATLOOP            ...NO, TRY NEXT RECORD
         CLC   17(15,R1),=CL15'INSTALLATION AT' Q. IS THIS INSTALL ATTR
         BNE   INATLOOP            ...NO, TRY NEXT LINE
         SPACE
PWLOOP   DS    0H                  SEARCH FOR PASSWORD
         GET   UADSLIST            READ A LINE
         CLC   0(2,R1),=AL2(11)    Q. IS THIS RECORD LONG ENOUGH
         BL    PWLOOP              ...NO, TRY ANOTHER
         CLI   10(R1),C' '         Q. IS THIS THE PASSWORD LINE
         BE    PWLOOP              ...NO, KEEP LOOKING
         SPACE
* FOUND THE PASSWORD
ACCTLP   DS    0H                  OBTAIN THE ACCOUNT NUMBERS
         GET   UADSLIST            READ A LINE
         CLC   0(2,R1),=AL2(32)    Q. IS THIS RECORD LONG ENOUGH
         BL    NOTUID              ...NO, THIS IS NOT A NEW USER ID
         CLC   17(15,R1),=CL15'USER ATTRIBUTES' Q. NEW USERID FOUND
         BE    NEXTUID             ...YES, GO PROCESS IT
NOTUID   DS    0H
         CLC   0(2,R1),=AL2(13)    Q. IS THIS RECORD LONG ENOUGH
         BL    ACCTLP              ...NO, TRY ANOTHER
         CLI   12(R1),C' '         Q. ACCOUNT NUMBER
         BE    ACCTLP              ...NO, KEEP LOOKING
         SPACE
* HAVE AN ACCOUNT NUMBER
         MVC   SORTACCT,12(R1)     MOVE ACCT NUMBER TO RECORD
         L     R13,4(R13)          POINT TO LAST SAVE AREA
         MVC   24(4,R13),=A(SORTRCD)   ADDRESS OF SORT RCD FOR REG 1
         LM    R14,R12,12(R13)     RESTORE REGS
         LA    R15,12              GIVE THIS RECORD TO SORT
         BR    R14                 RETURN TO SORT
         SPACE 2
E15QUIT  DS    0H
         L     R13,4(R13)          POINT TO LAST SAVE AREA
         RETURN (14,12),RC=16      TERMINATE SORT
         SPACE 2
UADSLEOF DS    0H
         CLOSE (UADSLIST)          CLOSE FILE
         L     R13,4(R13)          POINT TO LAST SAVE AREA
         RETURN (14,12),RC=8       ALL DONE, DONT COME BACK
         TITLE '***  SORT EXIT E35  ***'
***********************************************************************
* THIS EXIT PRODUCES THE REPORT FROM THE SORTED RECORDS.              *
***********************************************************************
         SPACE
E35      DS    0H
         SAVE  (14,12),,E35        SAVE REGS
         USING E35,R15             SET UP TEMPORARY BASE
         LR    R2,R13              CHAIN SAVE AREAS
         LA    R13,EXITRSA
         ST    R2,4(R13)
         ST    R13,8(R2)
         L     R12,MAINBASE        GET ORIGINAL BASE REG
         USING TSOACRPT,R12
         DROP  R15
         SPACE
         L     R11,0(0,R1)         GET POINTER TO SORT RECORD
         TM    ACCTLIST+DCBOFLGS-IHADCB,DCBOFOPN   Q. REPORT FILE OPEN
         BO    OUTLOOP             ...YES, DO IT
         OPEN  (ACCTLIST,OUTPUT)   OPEN FILE
         TM    ACCTLIST+DCBOFLGS-IHADCB,DCBOFOPN   Q. OPEN SUCCESSFUL
         BNO   E35QUIT             ...NO, GIVE UP
         TIME  DEC                 GET DATE AND TIME
         ST    R1,DBLWRK           STORE DATE FOR EDIT
         ED    DATEOUT,DBLWRK+1    EDIT DATE
         ST    R0,DBLWRK           STORE TIME FOR EDIT
         ED    TIMEOUT,DBLWRK      EDIT TIME
         MVC   DATALINE,BLNKLINE   CLEAR THE LINE
         EJECT
OUTLOOP  DS    0H
         LTR   R11,R11             Q. ANY MORE RECORDS
         BZ    E35END              ...NO, ALL DONE
         CLC   SAVEACT,0(R11)      Q. IS THIS A NEW ACCOUNT
         BE    NOTNEW              ...NO, GO GET USERID
         LA    R15,DATALINE        POINT TO OUTPUT LINE
         BAL   R14,WRITE           WRITE OUT LAST LINE OF PREVIOUS ACT#
         CLC   SAVEACT(1),0(R11)   Q. NEW DIVISION
         BE    NOTNEWDV            ...NO, CONTINUE
         ZAP   LINECNT,=P'999'     INDICATE NEW PAGE NEEDED
NOTNEWDV DS    0H
         MVC   SAVEACT,0(R11)      SAVE THE NEW ACCOUNT
         MVC   DATALINE,BLNKLINE   CLEAR THE LINE
         MVI   DATALINE,C'0'       DOUBLE SPACE
         MVC   ACCTNUM,0(R11)      MOVE IT TO OUTPUT LINE
         MVC   LINEPTR,STARTPTR    RESTART LINE
NOTNEW   DS    0H
         L     R3,LINEPTR          GET LINE POINTER
         CL    R3,ENDPTR           Q. WILL IT FIT IN THIS LINE
         BL    MOVEUSER            ...YES, GO MOVE IT
         LA    R15,DATALINE        POINT TO LINE
         BAL   R14,WRITE           WRITE OUT PREVIOUS LINE
         MVC   DATALINE,BLNKLINE   BLANK LINE
         L     R3,STARTPTR         GET INITIAL POINTER
         SPACE
MOVEUSER DS    0H
         MVC   0(7,R3),8(R11)      MOVE THE USERID
         LA    R3,10(0,R3)         BUMP TO NEXT POSITION
         ST    R3,LINEPTR          SAVE IT
         L     R13,4(0,R13)        GET LAST R13
         RETURN (14,12),RC=4
         SPACE
E35END   DS    0H
         LA    R15,DATALINE        POINT TO LINE
         BAL   R14,WRITE           WRITE OUT LAST LINE
         LA    R15,ENDLINE         POINT TO LINE
         BAL   R14,WRITE           WRITE OUT ENDING LINE
         CLOSE (ACCTLIST)          CLOSE FILE
         L     R13,4(0,R13)
         RETURN (14,12),RC=8       TELL SORT WE ARE DONE
         SPACE
E35QUIT  DS    0H
         L     R13,4(0,R13)
         RETURN (14,12),RC=16      TELL SORT TO STUFF IT
         TITLE '* * *  WRITE ROUTINE  * * *'
***********************************************************************
* WRITE - WRITE A LINE AFTER CHECKING FOR NEW PAGE AND DIVISION       *
*         BREAK                                                       *
*                                                                     *
* ON ENTRY:                                                           *
*    R15 - POINTER TO OUTPUT LINE                                     *
*    R14 - RETURN REGISTER                                            *
*                                                                     *
* ON EXIT:                                                            *
*    R15 - DESTROYED                                                  *
*                                                                     *
***********************************************************************
         SPACE
WRITE    DS    0H
         ST    R14,WRITER14        SAVE RETURN REG
         CP    LINECNT,LINEMAX     Q. NEED HEADINGS
         BNH   NOPAGE              ...NO, CONTINUE
         ZAP   LINECNT,=P'5'       INIT LINE COUNT
         AP    PAGECNT,=P'1'       ADD 1 TO PAGE COUNT
         MVC   DBLWRK(4),=X'40202021'
         ED    DBLWRK(4),PAGECNT
         MVC   PAGEOUT,DBLWRK
         ST    R15,WRITER15        SAVE R15
         PUT   ACCTLIST,HEADING1   PUT OUT HEADING
         PUT   ACCTLIST,HEADING2
         LA    R15,DIVLIST         POINT TO LIST OF DIVISIONS
         LA    R14,DIVNENT         NUMBER OF ENTRIES
DIVLOOP  DS    0H
         CLC   0(1,R15),SAVEACT    Q. IS THIS THE DIVISION
         BE    GOTDIV              ...YES, GO PRINT IT
         LA    R15,DIVELTH(0,R15)  BUMP TO NEXT ENTRY
         BCT   R14,DIVLOOP
         SPACE
GOTDIV   DS    0H
         MVC   DIVOUT,1(R15)       MOVE IN DIVISION
         PUT   ACCTLIST,HEADING3
         PUT   ACCTLIST,BLNKLINE
         L     R15,WRITER15        RESTORE R15
NOPAGE   DS    0H
         AP    LINECNT,=P'1'       ADD 1 TO LINECOUNT
         CLI   0(R15),C'0'         Q. TWO LINES
         BNE   NOTTWO              ...NO, CONTINUE
         AP    LINECNT,=P'1'       ADD 1 TO LINECOUNT
NOTTWO   DS    0H
         PUT   ACCTLIST,(R15)      WRITE THE LINE
         L     R14,WRITER14        RESTORE R14
         BR    R14                 RETURN
         SPACE 3
LINECNT  DC    PL2'0'              LINE COUNT
LINEMAX  DC    PL2'50'             NUMBER OF LINES/PAGE
PAGECNT  DC    PL2'0'              PAGE COUNT
WRITER14 DS    A                   R14 SAVE
WRITER15 DS    A                   R15 SAVE
         TITLE '******* REGISTER EQUATES *******'
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11                  PTR TO E35 SORT RECORD
R12      EQU   12                  BASE REG
R13      EQU   13
R14      EQU   14
R15      EQU   15
         TITLE '*******  DATA AREA  *******'
DBLWRK   DS    D                   DOUBLE WORD WORKAREA
RSA      DS    18D                 REGISTER SAVE AREA
EXITRSA  DS    18D                 SORT EXITS RSA
MAINBASE DC    A(TSOACRPT)         BASE ADDRESS
STARTPTR DC    A(USERNO1)          FIRST USERID POSITION
ENDPTR   DC    A(USEREND)          ENDING POINTER
LINEPTR  DC    A(USERNO1)          CURRENT POSITION POINTER
         SPACE
FINDUID  DS    0CL8                USER ID FOR FIND MACRO
         DC    CL1'$'              1ST CHAR FOR INFO BLOCK
SAVEACT  DC    CL8' '              CURRENT ACCOUNT #
         SPACE
PARMPTR  DS    0F                  SORT PARAMETER LIST POINTER
         DC    X'80'               END OF LIST
         DC    AL3(SORTPARM+2)     LIST POINTER
         SPACE
SORTPARM DS    0F                  SORT PARAMETER LIST
         DC    H'0'                UNUSED
         DC    AL2(SORTLE-SORTLB)  LENGTH
SORTLB   DS    0F                  BEGINNING OF LIST
         DC    A(SORTSTB)          START OF SORT STMT
         DC    A(SORTSTE)          END OF SORT STMT
         DC    A(RCDSTB)           START OF RECORD STMT
         DC    A(RCDSTE)           END OF RECORD STMT
         DC    A(E15)              E15 EXIT ROUTINE
         DC    A(E35)              E35 EXIT ROUTINE
         DC    X'FF00',C'CC'       CRITICAL MESSAGES TO CONSOLE
SORTLE   EQU   *                   END OF LIST
         SPACE
SORTSTB  EQU   *                   SORT STATEMENT
         DC    C' SORT FIELDS=(1,8,CH,A,9,7,CH,A)'
SORTSTE  DC    C' '
         SPACE
RCDSTB   EQU   *                   RECORD STATEMENT
         DC    C' RECORD TYPE=F,LENGTH=(19)'
RCDSTE   DC    C' '
         SPACE
SORTRCD  DS    0CL19               SORT RECORD
SORTACCT DS    CL8                 ACCOUNT NUMBER
SORTUID  DS    CL7                 USER ID
         SPACE
DIVLIST  EQU   *
         DC    C'B'
         DC    CL32'CORPORATE INFORMATION PROCESSING'
DIVELTH  EQU   *-DIVLIST           LENGTH OF ONE ENTRY
         DC    C'C'
         DC    CL32'  CORPORATE SYSTEMS DEVELOPMENT '
         DC    C'H'
         DC    CL32'          DAP DIVISION          '
         DC    C'I'
         DC    CL32'     INTERNATIONAL DIVISION     '
         DC    C'M'
         DC    CL32'       MAYBELLINE DIVISION      '
         DC    C'P'
         DC    CL32'         PLOUGH DIVISION        '
         DC    C'R'
         DC    CL32'        RESEARCH DIVISION       '
         DC    C'S'
         DC    CL32'        SCHERING DIVISION       '
         DC    C'K'
         DC    CL32'INTERNATIONAL CONSUMER DIVISION '
         DC    C'F'
         DC    CL32'        SCHOLL''S DIVISION       '
DIVNENT  EQU   (*-DIVLIST)/DIVELTH   NUMBER OF ENTRIES
         DC    C' '                 DUMMY ENTRY FOR UNKNOWN DIVISION
         DC    CL32'         UNKNOWN DIVISION       '
         SPACE 3
HEADING1 DS    0CL81               REPORT HEADING
         DC    CL1'1'
DATEOUT  DC    XL7'4021204B202020' YEAR EDIT PATTERN - YY.DDD
         DC    CL7' '
         DC    CL52'S C H E R I N G - P L O U G H  C O R P O R A T I O *
               N'
         DC    CL6' '
         DC    CL4'PAGE'
PAGEOUT  DS    CL4                 PAGE NUMBER
         SPACE
HEADING2 DS    0CL81               REPORT HEADING
         DC    CL1' '
TIMEOUT  DC    XL9'4021204B20204B2020' TIME EDIT PATTERN - HH.MM.SS
         DC    CL20' '
         DC    CL21'T S O  A C C O U N T S'
         DC    CL30' '
         SPACE
HEADING3 DS    0CL81               REPORT HEADING
         DC    CL1'0'
         DC    CL24' '
DIVOUT   DS    CL32                DIVISION NAME
         DC    CL24' '
         SPACE
DATALINE DS    CL81                DATA LINE
         ORG   DATALINE
         DS    CL1                 CARRIAGE CONTROL
ACCTNUM  DS    CL8                 ACCOUNT NUMBER
         DS    CL1
DATASTRT EQU   *
         ORG   DATASTRT
USERLINE DS    0CL72               USERIDS
USERHEAD DS    CL10
         ORG   USERHEAD+2
USERNO1  DS    CL8                 USERID GOES HERE
         DS    CL2                 SPACE
         DS    CL60                ROOM FOR  6 MORE
USEREND  EQU   *                   END OF USER LINE
         ORG
         SPACE
ENDLINE  DS    0CL81
         DC    CL1'1'
         DC    CL80'***  END OF TSO ACCOUNT REPORT  ***'
         SPACE
BLNKLINE DC    CL81' '             BLANK LINE
         SPACE 5
         LTORG
         TITLE '******* DCBS *******'
UADSLIST DCB   DDNAME=UADSLIST,DSORG=PS,EODAD=UADSLEOF,MACRF=GL
         EJECT
ACCTLIST DCB   DDNAME=ACCTLIST,DSORG=PS,MACRF=PM
         EJECT
         DCBD  DSORG=(PS),DEVD=DA
         EJECT
         END   TSOACRPT
