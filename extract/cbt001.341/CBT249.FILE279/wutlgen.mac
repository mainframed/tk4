         MACRO
&LABEL   WUTLGEN &JOBNAME=,            JOBNAME TO EXTEND               X
               &JOBTIME=0,             JOB  EXTENTION IN SECONDS       X
               &STPTIME=0,             STEP EXTENTION IN SECONDS       X
               &WAITIME=0,             WAIT EXTENTION IN SECONDS       X
               &EXTEND=,               TASK TYPES EXTENDABLE           X
               &PROMPT=NO,             DO WE PROMPT FOR THE EXTEND     X
               &OPERWTO=YES            DO WE ISSUE WTO OR JUST WTP
.*
         LCLC  &DLABEL,&FLAG1,&WUTLBL,&JOBPART
         LCLB  &TSO,&STC,&JOB,&JOBF,&STEPF,&WAITF
         LCLB  &PROMPTF,&WTOF,&BADFLG
         LCLA  &LENGTH,&LOOP
.*
&WUTLBL  SETC  'WUTL'
.*
         AIF   ('&JOBNAME'(1,1) NE '''').XX
&LENGTH  SETA  K'&JOBNAME-2
&JOBPART SETC  '&JOBNAME'(2,&LENGTH)
         AGO   .YY
.*
.XX      ANOP
&JOBPART SETC  '&JOBNAME'
.YY      AIF   ('&JOBPART' NE '').GOTJBN
         MNOTE 8,'WGEN001 - "JOBNAME" WAS NOT SPECIFIED ON THE MACRO'
&BADFLG  SETB  1
         AGO   .CHKLBL
.*
.GOTJBN  AIF   (K'&JOBPART LE 8).DOJOB
         MNOTE 8,'WGEN002 - "JOBNAME"  WAS GREATER THAN 8 CHARS.'
&BADFLG  SETB  1
         AGO   .CHKLBL
.*
.DOJOB   ANOP
&LENGTH  SETA  K'&JOBPART
.*
.CHKLBL  AIF   ('&LABEL' NE '').GOTLBL
&DLABEL  SETC  '&WUTLBL'.'&SYSNDX'
         AGO   .CHKJOBT
.*
.GOTLBL  ANOP
&DLABEL  SETC  '&LABEL'
.*
.CHKJOBT AIF   (&JOBTIME EQ 0).CHKSTPT
         AIF   (&JOBTIME LE 86400).JTOK
         MNOTE 8,'WUTL003 - "JOBTIME"  INVALID MUST BE =< 86400 SECS.'
&BADFLG  SETB  1
         AGO   .CHKSTPT
.*
.JTOK    ANOP
&JOBF    SETB  1
.*
.CHKSTPT AIF   (&STPTIME EQ 0).CHKWAIT
         AIF   (&STPTIME LE 86400).STOK
         MNOTE 8,'WUTL004 - "STPTIME"  INVALID MUST BE =< 86400 SECS.'
&BADFLG  SETB  1
         AGO   .CHKWAIT
.*
.STOK    ANOP
&STEPF   SETB  1
.*
.CHKWAIT AIF   (&WAITIME EQ 0).CHKWTO
         AIF   (&WAITIME LE 86400).WTOK
         MNOTE 8,'WUTL005 - "WAITIME"  INVALID MUST BE =< 86400 SECS.'
&BADFLG  SETB  1
         AGO   .CHKWTO
.*
.WTOK    ANOP
&WAITF   SETB  1
.*
.CHKWTO  AIF   ('&OPERWTO' EQ 'NO').CHKWTOR
         AIF   ('&OPERWTO' EQ 'YES').DOWTO
         MNOTE 8,'WUTL006 - "OPERWTO"  INVALID -- NOT "YES" OR "NO"'
&BADFLG  SETB  1
         AGO   .CHKWTOR
.*
.DOWTO   ANOP
&WTOF    SETB  1
.*
.CHKWTOR AIF   ('&PROMPT' EQ 'NO').CHKXTND
         AIF   ('&PROMPT' EQ 'YES').DOWTOR
         MNOTE 8,'WUTL007 - "PROMPT" INVALID -- NOT "YES" OR "NO"'
&BADFLG  SETB  1
         AGO   .CHKXTND
.*
.DOWTOR  ANOP
&PROMPTF SETB  1
.*
.CHKXTND ANOP
&LOOP    SETA  N'&EXTEND
         AIF   (&LOOP GT 0).CHKL3
         MNOTE 8,'WUTL008 - "EXTEND" NOT SPECIFIED - INVALID'
&BADFLG  SETB  1
         AGO   .ENDLOOP
.*
.CHKL3   AIF   (&LOOP LE 3).GOLOOP
         MNOTE 8,'WUTL009 - "EXTEND" MAY NOT HAVE MORE THAN 3 ITEMS'
&BADFLG  SETB  1
         AGO   .ENDLOOP
.*
.GOLOOP  AIF   (&LOOP LE 0).ENDLOOP
         AIF   ('&EXTEND(&LOOP)' NE 'TSO').CKSTC
&TSO     SETB  1
         AGO   .NXTLOOP
.CKSTC   AIF   ('&EXTEND(&LOOP)' NE 'STC').CKJOB
&STC     SETB  1
         AGO   .NXTLOOP
.CKJOB   AIF   ('&EXTEND(&LOOP)' NE 'JOB').ERRXTND
&JOB     SETB  1
         AGO   .NXTLOOP
.ERRXTND ANOP
         MNOTE 8,'WUTL010 - "EXTEND" NOT "TSO", "STC" OR "JOB"'
&BADFLG  SETB  1
         AGO   .ENDLOOP
.*
.NXTLOOP ANOP
&LOOP    SETA  &LOOP-1
         AGO   .GOLOOP
.*
.ENDLOOP ANOP
         AIF   (NOT &BADFLG).GOOD001
         MNOTE 16,'WUTL099 - TERMINATING DUE TO ERRORS'
         MEXIT
.*
.GOOD001 ANOP
&DLABEL  DC    F'&JOBTIME'             JOB  TIME EXTENTION IN SECONDS
         DC    F'&STPTIME'             STEP TIME EXTENTION IN SECONDS
         DC    F'&WAITIME'             WAIT TIME EXTENTION IN SECONDS
         DC    BL1'&JOBF&STEPF&WAITF&PROMPTF&WTOF&TSO&STC&JOB'
         DC    XL1'00'                 FLAG 2 -- UNUSED
         DC    H'&LENGTH'              LENGTH OF JOBNAME
         DC    CL8'&JOBPART'           JOBNAME FOR EXTENTION
         MEND
