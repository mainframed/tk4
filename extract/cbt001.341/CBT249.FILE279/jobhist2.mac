//JOBHIST EXEC SAS
//*
//* JOB LOADING HISTOGRAM
//* TOM FOTH, WSRCC, 03/09/82
//*
//FILTER DD DUMMY
//*
//* FILTER CARDS HAVE THE FORMAT:
//* JOB=JOBNAME
//* OR
//* CLASS=CLASS
//* PREVENTS THE JOBS OR JOBS IN CERTAIN CLASSES FROM SHOWING UP
//* ON HISTOGRAM
//*
//DATECTL DD *
STARTDTE=YESTERDAY STARTTME=00:00:00
ENDDTE=TODAY       ENDTME=23:59:59
SCALE=60
/*
//* THE ABOVE IS FAIRLY SELF EXPLAINATORY.
//* STARTDTE AND ENDDTE CAN BE YESTERDAY, TODAY, OR MM/DD/YY.
//* STARTTME AND ENDTME CAN BE HH:MM:SS OR NOW.
//* SCALE IS THE NUMBER OF SECONDS REPRESENTED BY THE PLOTTING
//* SYMBOL (*).
//*
//SMFTPE DD DSN=SOURCE OF SMF TYPE 5 RECORDS
//HISTPLOT DD SYSOUT=*
//REPORT   DD SYSOUT=*
//SYSIN DD *
DATA HISTGRAM;
FILE REPORT PRINT LL=RLL;
TITLE W S R C C;
TITLE2 J O B   L O A D I N G   H I S T O G R A M;
TITLE3 F I L T E R   R E P O R T;
KEEP JOBNAME JOBCL STRTTMDT TERMTMDT
     DATESTRT DATEEND SCALE STARSTRT STARLEN
     SCALTMDT ENDSTMDT REASON;
IF _N_ = 1 THEN
 DO;
  INFILE DATECTL;
  INPUT   STARTDTE=$9.
          STARTTME=$8.@;
  INPUT   @1 CDIMGE $80.;
  INPUT   ENDDTE=$9.
          ENDTME=$8.@;
  INPUT   @1 CDIMGE1 $80.;
  INPUT   SCALE=5.0@;
  INPUT   @1 CDIMGE2 $80.;
  IF STARTTME = 'NOW'
   THEN STRTTME = INT(TIME());
   ELSE STRTTME = INPUT(STARTTME,TIME.);
  IF ENDTME = 'NOW'
   THEN ENDTIME = INT(TIME());
   ELSE ENDTIME = INPUT(ENDTME,TIME.);
  IF SCALE = . THEN
    ERROR 'SCALE VALUE IS MISSING OR INVALID';
  IF STARTDTE = 'YESTERDAY'
   THEN DATESTRT = TODAY() - 1;
   ELSE IF STARTDTE = 'TODAY'
         THEN DATESTRT = TODAY();
         ELSE DATESTRT = INPUT(STARTDTE,MMDDYY8.);
  IF ENDDTE = 'YESTERDAY'
   THEN DATEEND  = TODAY() - 1;
   ELSE IF ENDDTE   = 'TODAY'
         THEN DATEEND  = TODAY();
         ELSE DATEEND  = INPUT(ENDDTE,MMDDYY8.);
  IF STRTTME = .
   THEN DO;
         ERROR 'REPORT START TIME SPECIFIED IN IMPROPER FORMAT.';
         ERROR 'USE HH:MM:SS:';
        END;
  IF ENDTIME  = .
   THEN DO;
         ERROR 'REPORT END TIME SPECIFIED IN IMPROPER FORMAT.';
         ERROR 'USE HH:MM:SS:';
        END;
  IF DATESTRT = .
   THEN DO;
         ERROR 'REPORT START DATE SPECIFIED IN IMPROPER FORMAT.';
         ERROR 'USE MM/DD/YY, YESTERDAY, OR TODAY ';
        END;
  IF DATEEND = .
   THEN DO;
         ERROR 'REPORT END DATE SPECIFIED IN IMPROPER FORMAT.';
         ERROR 'USE MM/DD/YY, YESTERDAY, OR TODAY ';
        END;
  IF NOT _ERROR_ AND DATESTRT > DATEEND
   THEN  ERROR 'REPORT START DATE GREATER THAN END DATE';
 DATESTRT = STRTTME + (DATESTRT*24*60*60);
 DATEEND  = ENDTIME  + (DATEEND*24*60*60);
  IF NOT _ERROR_ AND DATESTRT => DATEEND
   THEN  DO;
          ERROR 'REPORT START DATE & TIME GREATER';
          ERROR 'THAN OR EQUAL TO END DATE & TIME';
         END;
 IF _ERROR_
  THEN DO;
        PUT @1 'DATECTL CARDS:';
        PUT @1 CDIMGE;
        PUT @1 CDIMGE1;
        PUT @1 CDIMGE2;
        ABORT ABEND 16;
       END;
 PUTSDATE = DATEPART(DATESTRT);
 PUTEDATE = DATEPART(DATEEND);
 PUT @1 'START DATE:' PUTSDATE WEEKDATE. ' ' DATESTRT TOD.;
 PUT @1 'END DATE:  ' PUTEDATE WEEKDATE. ' ' DATEEND  TOD.;
 PUT @1 'ONE * = ' SCALE 5.0 ' SECONDS.';
 PUT @1 'FILTER CARDS:';
 ARRAY  SJOB(CARDNO) $8 SJOB1-SJOB20;
 INFILE FILTER END=FILTREOF;
 CARDNO = 0;
 DO WHILE(NOT FILTREOF);
  CARDNO = CARDNO + 1;
  IF CARDNO > 20 THEN
   DO;
    ERROR 'PROGRAM ACCOMODATES 20 FILTER CARDS, MAXIMUM';
    ABORT ABEND 24;
   END;
  LENGTH JOB $ 8;
  LENGTH CLASS $ 1;
  JOB = '        ';
  CLASS = ' ';
  INPUT JOB=$8.
        CLASS=$1.@;
  INPUT @1 CDIMGE $80.;
  IF JOB ^= ' ' AND CLASS ^= ' '
   THEN DO;
         ERROR 'JOB AND CLASS CANNOT BOTH BE SPECIFIED ON SAME CARD:';
         PUT @1 CDIMGE;
        END;
  IF JOB = ' ' AND CLASS = ' '
   THEN DO;
         ERROR 'FILTER CARD ENCOUNTERED WITHOUT JOB OR CLASS:';
         PUT @1 CDIMGE;
        END;
  IF JOB ^= ' '
   THEN SJOB=JOB;
   ELSE SJOB='CLASS-' ×× CLASS;
   IF NOT _ERROR_ THEN PUT   @1 SJOB ;
 END;
 FILTRNO = CARDNO;
IF _ERROR_ THEN ABORT ABEND 16;
PUT _PAGE_;
PUT 'JOBS THAT WERE FILTERED FROM PLOT:';
END;
RETAIN SJOB1-SJOB20
       FILTRNO
       DATESTRT
       DATEEND
       SCALE;

INFILE SMFTPE;
INPUT @2  SMFTYPE PIB1.
      @3  TERMTMDT SMFSTAMP8.
      @15 JOBNAME $8.
      @40 STRTTMDT SMFSTAMP8.
      @72 JOBCL   $1.;
IF SMFTYPE = 5;
TERMTMDT = INT(TERMTMDT);
STRTTMDT = INT(STRTTMDT);
IF (STRTTMDT >= DATESTRT AND
    STRTTMDT <= DATEEND) OR
   (TERMTMDT >= DATESTRT AND
    TERMTMDT <= DATEEND) OR
   (STRTTMDT <= DATESTRT AND
    TERMTMDT >= DATEEND);
IF FILTRNO > 0 THEN
  DO;
   IF RLL = 5 THEN
   DO;
    PUT _PAGE_;
    PUT 'JOBS THAT WERE FILTERED FROM PLOT:';
   END;
   DO CARDNO = 1 TO FILTRNO;
    IF SJOB =: 'CLASS-'
     THEN DO;
           COMPCL = SUBSTR(SJOB,7,1);
           IF COMPCL = JOBCL THEN DO;
                                   PUT @1 JOBNAME $8.
                                       @10 JOBCL  $1.
                                       @15 STRTTMDT DATETIME.
                                       @35 TERMTMDT DATETIME.
                                       @55 'BECAUSE OF CLASS';
                                   DELETE;
                                  END;
          END;
     ELSE IF SJOB = JOBNAME THEN DO;
                                   PUT @1 JOBNAME $8.
                                       @10 JOBCL  $1.
                                       @15 STRTTMDT DATETIME.
                                       @35 TERMTMDT DATETIME.
                                       @55 'BECAUSE OF JOBNAME';
                                   DELETE;
                                  END;
   END;
  END;
TIMESPAN = INTCK('SECOND',DATESTRT,DATEEND);
INTVLS = (TIMESPAN/SCALE)/120;
INTVREC = SCALE*120;
DATESTTM = DATESTRT;
DO I = 0 TO INTVLS-1 BY 1;
 SCALTMDT = (I*INTVREC)+DATESTTM;
 ENDSTMDT = SCALTMDT + (INTVREC-1);
 IF STRTTMDT <= SCALTMDT AND
    TERMTMDT >= ENDSTMDT
    THEN DO;
          STARSTRT = 0;
          STARLEN  = 120;
          REASON = 1;
          OUTPUT;
         END;
    ELSE IF STRTTMDT <= SCALTMDT AND
            TERMTMDT <  ENDSTMDT AND
            TERMTMDT >= SCALTMDT
            THEN DO;
                  STARSTRT = 0;
                  STARLEN = INT(((TERMTMDT - SCALTMDT)/SCALE)+.5);
                  REASON = 2;
                  OUTPUT;
                 END;
    ELSE IF STRTTMDT >  SCALTMDT AND
            TERMTMDT >= ENDSTMDT AND
            STRTTMDT <= ENDSTMDT
            THEN DO;
                  STARSTRT = INT(((STRTTMDT - SCALTMDT)/SCALE)+.5);
                  STARLEN = 120-STARSTRT;
                  REASON = 3;
                  OUTPUT;
                 END;
    ELSE IF STRTTMDT >  SCALTMDT AND
            TERMTMDT <  ENDSTMDT
            THEN DO;
                  STARSTRT= INT(((STRTTMDT - SCALTMDT)/SCALE)+.5);
                  STARLEN = INT(((TERMTMDT - STRTTMDT)/SCALE)+.5);
                  REASON = 4;
                  OUTPUT;
                 END;
END;
DELETE;
PROC SORT DATA=HISTGRAM;
 BY SCALTMDT STRTTMDT JOBNAME;
DATA _NULL_;
SET HISTGRAM END=EOF;
FILE HISTPLOT LL=LISTLL PRINT NOTITLES PS=60;
IF LAG(SCALTMDT) ^= SCALTMDT THEN LINK TOF;
IF LISTLL = 5 THEN LINK TOF;
LENGTH STARZ $ 120;
STARCOL = 10 + STARSTRT;
IF STARLEN = 0 THEN STARLEN = 1;
STARLEN = STARLEN - 1;
STARZ = REPEAT('*',STARLEN);
PUT @10 '×'
    @25 '×'
    @40 '×'
    @55 '×'
    @70 '×'
    @85 '×'
    @100 '×'
    @115 '×'
    @130 '×';
PUT @1 JOBNAME
    @10 '×'
    @25 '×'
    @40 '×'
    @55 '×'
    @70 '×'
    @85 '×'
    @100 '×'
    @115 '×'
    @130 '×'
    @STARCOL STARZ;
IF EOF THEN LINK BOF;
RETURN;
TOF:
IF _N_ ^= 1 THEN LINK BOF;
PUT _PAGE_;
PUT @61 'W S R C C';
PUT @45 'J O B   L O A D I N G   H I S T O G R A M';
PUT;
GSDATE = DATEPART(SCALTMDT);
GSTIME = TIMEPART(SCALTMDT);
GEDATE = DATEPART(ENDSTMDT);
GETIME = TIMEPART(ENDSTMDT);
PUT @1  'GRAPH START DATE: ' @19  GSDATE WEEKDATE.
    @82 'GRAPH END DATE: '   @99  GEDATE WEEKDATE.;
PUT @1  'GRAPH START TIME: ' @23  SCALTMDT TOD.
    @82 'GRAPH END TIME: '   @103 ENDSTMDT TOD.;
PUT;
PUT @1 'SECS/STAR:  ' @23 SCALE  5.0;
PUT;
DO I = 25 TO 115 BY 15;
 GTIME = SCALTMDT + ((I-10)*SCALE);
 GCOL  = I - 4;
 PUT @GCOL GTIME TOD. @;
END;
PUT;
PUT @1 'JOB NAME'
    @10 120*'-'
    @10 '+'
    @25 '+'
    @40 '+'
    @55 '+'
    @70 '+'
    @85 '+'
    @100 '+'
    @115 '+'
    @130 '+';
RETURN;
BOF:
  PUT @10 '×'
      @25 '×'
      @40 '×'
      @55 '×'
      @70 '×'
      @85 '×'
      @100 '×'
      @115 '×'
      @130 '×';
  PUT @1 'JOB NAME'
      @10 120*'-'
      @10 '+'
      @25 '+'
      @40 '+'
      @55 '+'
      @70 '+'
      @85 '+'
      @100 '+'
      @115 '+'
      @130 '+';
  DO I = 25 TO 115 BY 15;
   GTIME = SCALTMDT + ((I-10)*SCALE);
   GCOL  = I - 4;
   PUT @GCOL GTIME TOD. @;
  END;
  PUT;
 RETURN;
