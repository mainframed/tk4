*          DATA SET DYNAUTIL   AT LEVEL 002 AS OF 07/09/81
*          DATA SET DYNAUTIL   AT LEVEL 001 AS OF 06/22/81
*
* DYNAUTIL - DYNAMIC FRONT END FOR UTILITIES
*
*            TOM FOTH, WSRCC, 03/09/82
* SEE HELP FOR MORE DETAILS
*
DYNAUTIL BEGIN REQUS=YES
         LR    R7,R12
         LA    R7,4095(R7)
         LA    R7,1(R7)
         USING DYNAUTIL+4096,R7
         LR    R2,R1
         GETMAIN R,LV=WORKL
         LR    R11,R1
         USING WORKD,R11
         SR    R8,R8
         MVI   PARMTBL,C' '     BLANK UTILITY NAME, VERBS
         MVC   PARMTBL+1(L'UTILNAME+L'VERBFLDS-1),PARMTBL
         XC    DDNMFLDS(L'DDNMFLDS),DDNMFLDS ZERO  DDNAMES
         MVI   FLAGS,0
         L     R1,0(R2)         GET ADDR OF PARM IN R1
         ST    R1,PARMADDR      SAVE IT
         LA    R1,0(R1)         PURIFY R1
         LH    R2,0(R1)         GET THE LENGTH
         LTR   R2,R2            DOES THE PARAMETER EXIST?
         BZ    ENDPARM          NOPE
         LA    R1,2(R1)         POINT TO THE STRING
         LA    R6,4             NUMBER OF POSSIBLE FIELDS- 1
         LA    R5,1             NUMBER OF POSSIBLE PARMS FOR FIELD 1
         LA    R3,UTILNAME      POINT TO THE FIRST PARMSAVE FOR FIELD 1
PARMBGN  LA    R4,8(R3)         R4 PTR TO FIRST CHAR AFTER FIELD
ELMNTCK  ALPHANUM 0(R1)         IS IT AN ALPHANUMERIC?
         BE    ELMNTOK          YEP
         CLI   0(R1),C','       NOPE-IS IT A COMMA?
         BE    NEXTPARM         YEP, GO TO NEXT PARM
         CLI   0(R1),C'/'       NOPE-IS IT A SEMICOLON
         BE    NEXTFLD          YEP, GO TO NEXT VERB
         WTO  'DYN002E NONALPHANUMERIC IN PARM',ROUTCDE=(11)
         LA    R8,2            SET THE ERROR CODE
         B     DIENODMP         EXIT
ELMNTOK  CR    R4,R3            ARE WE POINTING PAST OUR FIELD
         BE    ELMNTLEN         NOPE
         MVC   0(1,R3),0(R1)    MOVE THE CHAR TO THE FIELD
         LA    R1,1(R1)         POINT TO NEXT CHARACTER IN PARM
         LA    R3,1(R3)         POINT TO NEXT CHARACTER IN FIELD
         BCT   R2,ELMNTCK       GO GET ANOTHER CHARACTER IF ANY
         B     ENDPARM          NO MORE-END PARM PROCESSING
ELMNTLEN WTO  'DYN003E PARM ELEMENT EXCEEDS 8 CHARACTERS',ROUTCDE=(11)
         LA    R8,3            SET THE RETURN CODE
         B     DIENODMP         EXIT
NEXTPARM BCT   R2,NXTPARM1      DECREMENT FROM LENGTH COUNT
BADPARM  WTO  'DYN004E INVALID PARM STRUCTURE',ROUTCDE=(11)
         LA    R8,4            SET THE ERROR CODE
         B     DIENODMP         EXIT
NXTPARM1 BCT   R5,NXTPARM2      DECREMENT FROM PARM COUNT
         B     BADPARM          BAD PARM PROCESSING
NXTPARM2 LA    R1,1(R1)         POINT TO NEXT CHAR
         LR    R3,R4            POINT TO THE FIELD
         B     PARMBGN          GO SCAN
NEXTFLD  BCT   R2,NXTFLD1       DECREMENT PARM COUNT
         B     BADPARM          BAD PARM PROCESSING
NXTFLD1  BCT   R6,NXTFLD2       DECREMENT FIELD COUNT
         B     ENDPARM          END PARM PROCESSING
NXTFLD2  LA    R1,1(R1)         POINT TO NEXT CHAR
         C     R6,=F'3'         PROCESSING VERBS?
         BNE   NXTFLD3          NOPE
         LA    R3,VERBFLDS      POINT TO DDNAME FIELD
         LA    R5,8             NUMBER OF POSSIBLE DDNAMES
         B     PARMBGN          GO SCAN
NXTFLD3  DS    0H               POINT TO NEXT CHAR
         C     R6,=F'2'         PROCESSING VERBS?
         BNE   NXTFLD4          NOPE
         LA    R3,DDNMFLDS      POINT TO DDNAME FIELD
         LA    R5,2             NUMBER OF POSSIBLE DDNAMES
         B     PARMBGN          GO SCAN
NXTFLD4  LA    R3,SYSDDS        POINT TO SYS DD OVERRIDE FIELD
         LA    R5,12            NUMBER OF POSSIBLE VERBS
         B     PARMBGN          GO SCAN
ENDPARM  STH   R2,PARMLEN       SAVE THE PARM LENGTH
         LTR   R2,R2            DO WE HAVE A PARM TO SAVE
         BZ    NOPARM           NO SIR
         CH    R2,=H'100'       PARM TO BIG
         BNH   PARMOK           NOPE
         WTO  'DYN005E PARM LENGTH EXCEEDS 100 CHARACTERS',ROUTCDE=(11)
         LA    R8,5            SET THE RETURN CODE
         B     DIENODMP         AND EXIT
PARMOK   LA    R1,1(R1)         POINT TO NEXT CHAR IN PARM
         BCTR  R2,0             DECREMENT FO EX
         MVC   PARMFLD(*-*),0(R1) EXECUTED BELOW
         EX    R2,*-6           EXECUTE THE MOVE INSTRUCTION
NOPARM   DS    0H
         LA    R1,8             EIGHT VERBS TO LOOK AT
         LA    R2,VERBFLDS      ADDRESS OF VERBTBL
VERBTST  CLI   0(R2),C' '
         BE    VERBTSTA
         CLC   0(8,R2),=CL8'ICODE'
         BNE   VERBTST1
         TM    FLAGS,ICODE
         BO    DUPVERB
         OI    FLAGS,ICODE
         B     VERBTSTA
VERBTST1 CLC   0(8,R2),=CL8'SCODE'
         BNE   VERBTST2
         TM    FLAGS,SCODE
         BO    DUPVERB
         TM    FLAGS,SIGNORE
         BO    CONFVERB
         OI    FLAGS,SCODE
         B     VERBTSTA
VERBTST2 CLC   0(8,R2),=CL8'SIGNORE'
         BNE   VERBTST3
         TM    FLAGS,SIGNORE
         BO    DUPVERB
         TM    FLAGS,SCODE
         BO    CONFVERB
         OI    FLAGS,SIGNORE
         B     VERBTSTA
VERBTST3 CLC   0(8,R2),=CL8'XCODE'
         BNE   VERBTST4
         TM    FLAGS,XCODE
         BO    DUPVERB
         TM    FLAGS,XIGNORE
         BO    CONFVERB
         OI    FLAGS,XCODE
         B     VERBTSTA
VERBTST4 CLC   0(8,R2),=CL8'XIGNORE'
         BNE   VERBTSTE
         TM    FLAGS,XIGNORE
         BO    DUPVERB
         TM    FLAGS,XCODE
         BO    DUPVERB
         OI    FLAGS,XIGNORE
         B     VERBTSTA
VERBTSTE WTO  'DYN025E PARAMETER CONTAINS ILLEGAL VERBS',ROUTCDE=(11)
         LA    R8,25
         MVI   FLAGS,0
         B     DIENODMP
CONFVERB WTO  'DYN027E CONFLICTING VERBS FOUND IN PARM',ROUTCDE=(11)
         LA    R8,27
         MVI   FLAGS,0
         B     DIENODMP
DUPVERB  WTO  'DYN026I DUPLICATE VERBS FOUND IN PARM - IGNORED',       X
               ROUTCDE=(11)
VERBTSTA LA    R2,8(R2)
         BCT   R1,VERBTST
         LA    R2,DDNMFLDS      ADDRESS OF DDNAMES
         LA    R3,14            THERE ARE FOURTEEN OF THEM
NULDDTST CLI   0(R2),X'00'      HAS THIS FILED BEEN MODIFIED?
         BE    BLNXTDD          NOPE
         LA    R4,8             EIGHT CHARACTERS / DDNAME
BLTSTCHR CLI   0(R2),X'00'      IS THIS CHARACTER A NULL
         BNE   BLNXTCHR         NOPE
         MVI   0(R2),C' '       YEP, BLANK IT
BLNXTCHR LA    R2,1(R2)         POINT TO NEXT CHARACTER
         BCT   R4,BLTSTCHR      GO SEE IT WE NEED TO BLANK
         BCT   R3,NULDDTST      GO DO ANOTHER DDNAME
         B     MOVEDCBS         NO MORE, MOVE DCBS
BLNXTDD  LA    R2,8(R2)         POINT TO NEXT DDNAME
         BCT   R3,NULDDTST      AND BLANK IT
MOVEDCBS MVC   RPTDCB(LRPTDCB),MODELRPT  MOVE THE DCB'S
         MVC   CNTLIN(LCNTLIN),MODELCTL  TO WORKING STORAGE
         MVC   REMOPN(LREMOPN),MODELOPN  TO WORKING STORAGE
         MVC   REMLNK(LREMLNK),MODELLNK  TO WORKING STORAGE
         LA    R10,RPTDCB
         LA    R9,CNTLIN
         CLI   RPTDD,X'00'      DID USER OVERRIDE RPT DDNAME
         BE    OPENRPT          NOPE
         MVC   X'28'(8,R10),RPTDD MOVE IN OVERRIDE DDNAME
OPENRPT  OPEN  ((R10),(OUTPUT)),MF=(E,REMOPN) OPEN THE REPORT FILE
         TM    X'30'(R10),X'10' OPEN REPORT OKAY
         BO    CNTLTST          YEP
         WTO  'DYN001E COULD NOT OPEN REPORT DATASET',ROUTCDE=(11)
         LA    R8,1
         B     DIEWDMP
CNTLTST  PUT   (R10),TTL001
         PUT   (R10),TTL002
         L     R3,PARMADDR      ADDRESS OF PARM
         LH    R2,0(R3)         LENGTH OF PARM
         LTR   R2,R2            WAS THERE ONE
         BNZ   MOVEPARM         YEP
         MVC   INFODATA,INF022  NOPE
         B     PUTPARM          PUT INFO MESSAGE
MOVEPARM PUT   (R10),TTL005     PUT HEADER
         MVI   INFOREC,C' '     CLEAR THE BUFFER
         MVC   INFOREC+1(L'INFOREC-1),INFOREC
         BCTR  R2,0             LESS ON FOR EX
         MVC   INFODATA(*-*),2(R3) TO BE EX'ED BELOW
         EX    R2,*-6           EX THE ABOVE
PUTPARM  PUT   (R10),INFOREC    PUT THE RECORD
         CLI   UTILNAME,C' '
         BNE   NOUTLWRN
         PUT   (R10),INF020
NOUTLWRN DS    0H
         PUT   (R10),TTL003
         MVC   BLDLFF,=H'1'
         MVC   BLDLLL,=H'14'
         MVC   BLDLNAME,=CL8'DYNAM'
         BLDL  0,BLDLFF
         LTR   R15,R15
         BZ    TSTUTIL
         C     R15,=F'8'
         BNE   DYNAMISS
BLDLFAIL WTO  'DYN019E BLDL FAILED IN MODULE SEARCH',ROUTCDE=(11)
         PUT  (R10),ERR019
         LA    R8,19
         B     DIEWDMP
DYNAMISS WTO  'DYN017E DYNAM MODULE NOT IN JOB LINKLIST',ROUTCDE=(11)
         PUT   (R10),ERR017
         LA    R8,17
         B     DIENODMP
TSTUTIL  CLI   UTILNAME,C' '
         BE    TSTCTLDD
         MVC   BLDLNAME,UTILNAME
         BLDL  0,BLDLFF
         LTR   R15,R15
         BZ    TSTCTLDD
         C     R15,=F'8'
         BE    BLDLFAIL
       WTO 'DYN018E SPECIFIED MODULE NOT IN JOB LINKLIST',ROUTCDE=(11)
         PUT   (R10),ERR018
         LA    R8,18
         B     DIENODMP
TSTCTLDD CLI   CTLDD,X'00'      DID USER OVERRIDE CTL DDNAME
         BE    OPENCTL          NOPE
         MVC   X'28'(8,R9),CTLDD MOVE IN OVERRIDE DDNAME
OPENCTL  OPEN  ((R9),(INPUT)),MF=(E,REMOPN) OPEN THE CONTROL FILE
         TM    X'30'(R9),X'10'  OPEN CONTROL OKAY
         BO    SETUPPRM         YEP
         WTO  'DYN006E COULD NOT OPEN CONTROL DATASET',ROUTCDE=(11)
         PUT   (R10),ERR006
         LA    R8,6
         B     DIEWDMP
SETUPPRM DS    0H
         CLC   X'52'(2,R9),=H'80'         IS LRECL 80
         BE    LRECLOK
         WTO  'DYN012E LRECL OF CONTROL FILE NOT 80',ROUTCDE=(11)
         PUT   (R10),ERR012
         LA    R8,12
         B     DIENODMP
LRECLOK  TM    X'24'(R9),X'80'  IS IT FIXED
         B     RECFMOK
         WTO  'DYN013E RECFM OF CONTROL FILE NOT F OR FB',ROUTCDE=(11)
         PUT   (R10),ERR013
         LA    R8,13
         B     DIENODMP
RECFMOK  TM    X'1A'(R9),X'40' IS IT DSORG PS
         B     DSORGOK
         WTO  'DYN014E DSORG OF CONTROL FILE NOT PS',ROUTCDE=(11)
         PUT   (R10),ERR014
         LA    R8,14
         B     DIENODMP
DSORGOK  DS    0H
         MVC   DDNTBL(96),SYSDDS MOVE IN DDNAME TO CALL TABLE
         LA    R0,PARMLEN       ADDRESS OF PARM
         ST    R0,PARMLST       SAVE IN ADDR LIST
         MVC   DDNTBLLN,=H'96'  LENGTH OF DDNAME TABLE
         LA    R0,DDNTBLLN      ADDRESS OF DDN TABLE
         ST    R0,PARMLST+4     SAVE IN ADDR LIST
         MVI   PARMLST+4,X'80'  END OF LIST
         XC    DYNAM,DYNAM      CLEAR WORK
         LA    R0,DYNAM         ADDR OF WORK VARIABLE
         ST    R0,PARMLST2      SAVED IN PARM LIST
         LA    R0,=CL4'INIT'    ADDR OF 'INIT'
         ST    R0,PARMLST2+4    SAVE IN PARM LIST
         MVI   PARMLST2+4,X'80' END OF PARM LIST
         LA    R1,PARMLST2      POINT TO PARM LIST
         LINK  EP=DYNAM         CALL DYNAM FOR INITIALIZATION
         LTR   R15,R15          OKAY?
         BZ    GETCMD           YEP
         PUT   (R10),ERR007
         WTO  'DYN007E DYNAM INITIALIZATION FAILED',ROUTCDE=(11)
         LA    R8,7
         B     DIEWDMP
GETCMD   LA    R2,CARDS
         LA    R4,4
GETCMD1  GET   (R9)
         MVI   INFOREC,C' '
         MVC   INFOREC+1(L'INFOREC-1),INFOREC
         MVC   INFODATA(80),0(R1)
         C     R2,=F'4'
         BNE   GETCMD2
         MVI   INFOCNTL,C'0'
GETCMD2  LR    R6,R1
         PUT   (R10),INFOREC
         LR    R1,R6
         MVC   0(72,R2),0(R1)
         LA    R3,72
         LR    R5,R2
FINDSEMI CLI   0(R5),C';'
         BE    GOTSEMI
         LA    R5,1(R5)
         BCT   R3,FINDSEMI
         LA    R2,72(R2)
         BCT   R4,GETCMD1
         PUT   (R10),ERR021
         WTO  'DYN021E CONTINUATION EXCEEDS FOUR CARDS',ROUTCDE=(11)
         LA    R8,21
         B     SXERRTST
GOTSEMI  CLC   CARDS(4),=CL4'END;'
         BE    XEQUTIL
         CLC   CARDS(4),=CL4'END '
         BE    XEQUTIL
         CLC   CARDS(5),=CL5'INFO  '
         BE    NOSUPORT
         CLC   CARDS(6),=CL6'ALLOC '
         BE    GOODCMD
         CLC   CARDS(7),=CL7'ALLOCR '
         BE    NOSUPORT
         CLC   CARDS(8),=CL8'UNALLOC '
         BE    GOODCMD
         CLC   CARDS(7),=CL7'CONCAT '
         BE    GOODCMD
         CLC   CARDS(9),=CL9'UNCONCAT '
         BE    GOODCMD
         CLC   CARDS(7),=CL7'REMOVE '
         BE    GOODCMD
         CLC   CARDS(8),=CL8'DDALLOC '
         BE    GOODCMD
         PUT   (R10),ERR011
         WTO  'DYN011E COMMAND IS INVALID',ROUTCDE=(11)
         LA    R8,11
         B     SXERRTST
NOSUPORT PUT   (R10),ERR015
         WTO  'DYN015E DYNAM COMMAND NOT YET SUPPORTED',ROUTCDE=(11)
         LA    R8,15
         B     SXERRTST
GOODCMD  LA    R1,CARDS
FINDSPC  CLI   0(R1),C' '       IS THIS THE SPACE AFTER THE CMD?
         BE    GOTSPACE         YEP
         LA    R1,1(R1)         NOPE-POINT TO NEXT CHAR
         B     FINDSPC          AND TRY AGAIN
GOTSPACE LA    R1,1(R1)         POINT PAST THE SPACE
         LA    R0,CARDS
         ST    R0,PARMLST2+4    SAVE THE ADDR OF THE CMD
         ST    R1,PARMLST2+8    AND THE ADDR OF THE KEYWORDS
         MVI   PARMLST2+8,X'80' MARK THE END OF THE LIST
         LA    R1,PARMLST2      POINT TO THE LIST
         LINK  EP=DYNAM         CALL DYNAM
         LTR   R15,R15          WAS IT SUCCESSFUL
         BNZ   BADCMD           NOPE
         PUT   (R10),INF016
         B     GETCMD
BADCMD   C     R15,=F'20'       IS IT A KEYWORD ERROR?
         BE    KEYERR           YEP
         C     R15,=F'28'       NOPE-IS IT A VALUE ERROR?
         BE    VALERR           YEP
         C     R15,=F'4'        NOPE-IS IT A ENV/SYS ERROR?
         BE    TYPE4ERR         YEP
         PUT   (R10),ERR008    PUT OUT ERROR MESSAGE
CMDFAIL  WTO  'DYN008E SYSTEM ERROR IN CALL TO DYNAM',ROUTCDE=(11)
         LA    R8,8
         B     DIEWDMP
TYPE4ERR DS    0H
         LA    R1,DYNAM
         TM    10(R1),B'00000111'
         BO    CMDFAIL
         TM    10(R1),B'00000100'
         BNO   TYPE4ER1
      WTO 'DYN023E ENVIRONMENT ERROR FOUND DURING COMMAND',ROUTCDE=(11)
         PUT   (R10),ERR023
         LA    R8,23
         B     EXERRTST
TYPE4ER1 DS    0H
      WTO 'DYN024E UNAVAILABLE SYSTEM RESOURCE REQUESTED',ROUTCDE=(11)
         PUT   (R10),ERR024
         LA    R8,23
         B     EXERRTST
KEYERR   PUT   (R10),ERR009
         WTO  'DYN009E KEYWORD IS INVALID',ROUTCDE=(11)
         LA    R8,9
         B     SXERRTST
VALERR   PUT   (R10),ERR010
         WTO  'DYN010E KEYWORD ASSIGN INVALID VALUE',ROUTCDE=(11)
         LA    R8,10
SXERRTST NI    FLAGS,X'FF'-RCCODE
         TM    FLAGS,SCODE
         BNO   SXERR1
         OI    FLAGS,RCCODE
         B     EXIT03
SXERR1   TM    FLAGS,SIGNORE
         BNO   EXIT03
         SR    R8,R8
         B     GETCMD
EXERRTST NI    FLAGS,X'FF'-RCCODE
         TM    FLAGS,XCODE
         BNO   EXERR1
         OI    FLAGS,RCCODE
         B     EXIT03
EXERR1   TM    FLAGS,XIGNORE
         BNO   EXIT03
         SR    R8,R8
         B     GETCMD
DIEWDMP  ABEND (R8),DUMP,,USER
DIENODMP DS    0H
         NI    FLAGS,X'FF'-RCCODE
         TM    FLAGS,ICODE
         BNO   DYNODMP1
         OI    FLAGS,ICODE
DYNODMP1 DS    0H
         C     R8,=F'12'
         BL    EXIT01
         C     R8,=F'16'
         BH    EXIT02A
         B     EXIT02
XEQUTIL  DS    0H
         CLI   UTILNAME,C' '    DID USER SPECIFY UTILTIY
         BNE   XEQUTIL1         YES
         PUT   (R10),INF030     NO, TELL USER
         B     EXIT03           EXIT
XEQUTIL1 MVC   INFOREC,INF031
         MVC   INFOREC+9(8),UTILNAME
         PUT   (R10),INFOREC
         LA    R2,UTILNAME
         LA    R1,PARMLST
         LINK  EPLOC=(R2),SF=(E,REMLNK)
         LTR   R8,R15           DID WE GET A RETURN CODE
         BNZ   UTILRET          YES
         PUT   (R10),INF029     NO, TELL USER
         B     EXIT03           EXIT
UTILRET  OI    FLAGS,RCCODE     YES, FLAG AS RETURN CODE
         WTO  'DYN028I SEE REPORT LISTING-INVOKED PROGRAM COMPLETED WITX
               H NONZERO RETURN CODE.',ROUTCDE=(11)
         CVD   R8,DOUBLE
         UNPK  UNPKAREA,DOUBLE
         OI    UNPKAREA+9,X'F0'
         LA    R1,UNPKAREA
FINDNZER CLI   0(R1),C'0'
         BNE   PUTINF28
         LA    R1,1(R1)
         B     FINDNZER
PUTINF28 LA    R2,UNPKAREA+9
         SR    R2,R1
         MVI   INFOREC,C' '
         MVC   INFOREC+1(L'INFOREC-1),INFOREC
         MVC   INFOREC(L'INF28PRE),INF28PRE
         MVC   INFOREC+L'INF28PRE(*-*),0(R1)
         EX    R2,*-6
         LA    R1,INFOREC+L'INF28PRE
         LA    R1,1(R1,R2)
         MVC   0(L'INF28END,R1),INF28END
         PUT   (R10),INFOREC
EXIT03   DS    0H
         LA    R0,=CL3'END'
         ST    R0,PARMLST2+4
         MVI   PARMLST2+4,X'80'
         LA    R1,PARMLST2
         LINK  EP=DYNAM
EXIT02   CLOSE ((R9)),MF=(E,REMOPN)
EXIT02A  CLOSE ((R10)),MF=(E,REMOPN)
EXIT01   FREEMAIN R,LV=WORKL,A=(R11)
         TM    FLAGS,RCCODE
         BO    FINI
         LTR   R8,R8
         BZ    FINI
         ABEND (R8),,,USER
FINI     DS    0H
         FINISH RC=(R8)
MODELRPT DCB   DDNAME=DYNARPT,LRECL=133,BLKSIZE=133,RECFM=FBA,         X
               DSORG=PS,MACRF=(PM)
MODELCTL DCB   DDNAME=DYNAIN,MACRF=(GL),DSORG=PS,EODAD=XEQUTIL
MODELOPN OPEN  (,),MF=L
MODELLNK LINK EPLOC=0,SF=L
ERR006 DC CL133' DYN006E COULD NOT OPEN CONTROL DATASET'
ERR007 DC CL133' DYN007E DYNAM INITIALIZATION FAILED'
ERR008 DC CL133' DYN008E SYSTEM ERROR IN CALL TO DYNAM'
ERR009 DC CL133' DYN009E KEYWORD IS INVALID'
ERR010 DC CL133' DYN010E KEYWORD ASSIGN INVALID VALUE'
ERR011 DC CL133' DYN011E COMMAND IS INVALID'
ERR012 DC CL133' DYN012E LRECL OF CONTROL FILE NOT 80'
ERR013 DC CL133' DYN013E RECFM OF CONTROL FILE NOT F OR FB'
ERR014 DC CL133' DYN014E DSORG OF CONTROL FILE NOT PS'
ERR015 DC CL133' DYN015E DYNAM COMMAND NOT YET SUPPORTED'
INF016 DC CL133' DYN016I ABOVE REQUEST SUCCESSFULLY COMPLETED'
ERR017 DC CL133' DYN017E DYNAM MODULE IN NOT IN JOB LINKLIST'
ERR018 DC CL133' DYN018E SPECIFIED MODULE NOT IN JOB LINKLIST'
ERR019 DC CL133' DYN019E BLDL FAILED IN MODULE SEARCH'
INF020 DC CL133' DYN020I NO PROGRAM SPECIFIED - IEFBR14 ASSUMED'
ERR021 DC CL133' DYN021E CONTINUATION EXCEEDS FOUR CARDS'
INF022 DC CL133' DYN017I NO PARAMETER SPECIFIED'
ERR023 DC CL133' DYN023E ENVIRONMENT ERROR FOUND DURING COMMAND'
ERR024 DC CL133' DYN024E UNAVAILABLE SYSTEM RESOURCE REQUESTED'
INF28PRE DC CL52'0DYN028I INVOKED PROGRAM COMPLETED WITH RETURN CODE '
INF28END DC CL11' (DECIMAL).'
INF029 DC CL133'0DYN029I INVOKED PROGRAM COMPLETED WITH A RETURN CODE OX
               F ZERO.'
INF030 DC CL133'0DYN030I IEFBR14 SIMULATION COMPLETES WITH A RETURN CODX
               E OF ZERO.'
INF031 DC CL133'0DYN031I XXXXXXXX HAS BEEN CALLED BY DYNAUTIL.'
TTL001 DS 0CL133
       DC CL48'1 '
       DC CL38'W. S. R. C. C.   ---   D Y N A U T I L'
       DC CL47' '
TTL002 DS 0CL133
       DC CL47'0 '
       DC CL39'DYNAMIC ALLOCATION UTILITY PREPROCESSOR'
       DC CL47' '
TTL003 DC CL133'-CONTROL CARDS:'
TTL005 DC CL133'-PARAMETER VALUE:'
WORKD    DSECT
DOUBLE   DS    D
PARMADDR DS    F
PARML    DS    H
INFOREC  DS    0CL133
INFOCNTL DS    C
INFODATA DS    CL132
FLAGS    DS    X
ICODE    EQU   B'10000000'
SCODE    EQU   B'01000000'
SIGNORE  EQU   B'00100000'
XCODE    EQU   B'00010000'
XIGNORE  EQU   B'00001000'
RCCODE   EQU   B'00000100'
RESVD1   EQU   B'00000010'
UNPKAREA DS   CL10
PARMTBL  DS    0CL184           SAVE PARMS FROM EXEC CARD
UTILNAME DS    CL8              UTILITY TO CALL
VERBFLDS DS    0CL64            SPACE FOR VERBS FROM PARM
VERB     DS    8CL8
DDNMFLDS DS    0CL112           DDNAMES SPECIFIED
RPTDD    DS    CL8              REPORT DDNAME FOR THIS PROGRAM
CTLDD    DS    CL8              CONTROL CARDS FOR THIS PROGRAM
SYSDDS   DS    12CL8            SYSIN FOR UTILITY
DDNTBLLN DS    H
DDNTBL   DS    12CL8            DDNAMES FOR UTILITY
PARMLST  DS    2F               PARMLST FOR UTILITY CALL
PARMLST2 DS    3F               PARMLST FOR DYNAM CALLS
DYNAM    DS    F                WORK FOR DYNAM
PARMLEN  DS    H                LENGTH OF PARM FOR UTILTIY CALL
PARMFLD  DS    CL100            PARMS FOR UTILITY CALL
BLDLFF   DS    H
BLDLLL   DS    H
BLDLNAME DS    CL8
BLDLTTR  DS    CL3
BLDLK    DS    X
BLDLZ    DS    X
BLDLC    DS    X
RPTDCB   DCB   DDNAME=DYNARPT,LRECL=133,BLKSIZE=133,RECFM=FBA,         X
               DSORG=PS,MACRF=(PM)
LRPTDCB  EQU   *-RPTDCB
CNTLIN   DCB   DDNAME=DYNAIN,MACRF=(GL),DSORG=PS,EODAD=XEQUTIL
LCNTLIN  EQU   *-CNTLIN
REMOPN   OPEN  (,),MF=L
LREMOPN  EQU   *-REMOPN
REMLNK   LINK EPLOC=0,SF=L
LREMLNK  EQU   *-REMLNK
CARDS    DS    4CL72
WORKL    EQU   *-WORKD          LENGTH OF DSECT
         END   DYNAUTIL
