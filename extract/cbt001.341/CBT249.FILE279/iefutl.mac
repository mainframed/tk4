         TITLE 'WSRCC SMF TIME LIMIT EXIT '
*
* WSRCC IEFUTL WITH LOAD MODULE SUPPORT
* BY JOSEPH A MARTIN
*
         PRINT ON,NOGEN
IEFUTL   BEGIN SAVE=(SAVEAREA,SALENGTH,0),AUTHOR=JAM, (SP=252 IN KEY0) X
               TITLE 'WSRCC SMF TIME LIMIT EXIT '
         SPACE 2
         ST    R0,SAVEREG0             SAVE R0 FOR LATER
         ST    R1,SAVEREG1             SAVE R1 FOR LATER
         MVC   WTOMSG01(WL01),MSGWTO01 MOVE WTO1 TO GOTTEN AREA.
         MVC   WTOMSG02(WL02),MSGWTO02 MOVE WTO2 TO GOTTEN AREA.
         MVC   WTOMSG03(WL03),MSGWTO03 MOVE WTOR TO GOTTEN AREA.
         MVC   WTOMSG04(WL04),MSGWTO04 MOVE WTOR TO GOTTEN AREA.
         LR    R2,R0                   SAVE R0 FOR BRANCH
         B     *+4(R2)                 BRANCH TO SETUP ENTRY TYPE
         B     JOBMSG                  ENTRY WAS JOB        X'00'
         B     STEPMSG                 ENTRY WAS STEP       X'04'
         B     WAITMSG                 ENTRY WAS WAIT       X'08'
         B     ERRMSG01                ENTRY WAS WRONG      X'0C'
         B     ERRMSG01                ENTRY WAS WRONG      X'10'
JOBMSG   MVC   WTOTYP01(4),LTJOB       MOVE "JOB" TO EXTEND WTO
         MVC   WTOTYP02(4),LTJOB       MOVE "JOB" TO CANCEL WTO
         MVC   WTOTYP03(4),LTJOB       MOVE "JOB" TO PROMPT WTOR
         B     CLRCODE                 CONTINUE WITH PROCESSING
STEPMSG  MVC   WTOTYP01(4),LTSTEP      MOVE "STEP" TO EXTEND WTO
         MVC   WTOTYP02(4),LTSTEP      MOVE "STEP" TO CANCEL WTO
         MVC   WTOTYP03(4),LTSTEP      MOVE "STEP" TO PROMPT WTOR
         B     CLRCODE                 CONTINUE WITH PROCESSING
WAITMSG  MVC   WTOTYP01(4),LTWAIT      MOVE "WAIT" TO EXTEND WTO
         MVC   WTOTYP02(4),LTWAIT      MOVE "WAIT" TO CANCEL WTO
         MVC   WTOTYP03(4),LTWAIT      MOVE "WAIT" TO PROMPT WTOR
         B     CLRCODE                 CONTINUE WITH PROCESSING
CLRCODE  SR    R15,R15                 SET RET CODE TO 0
         ST    R15,RETURNCD            SAVE RETURN CODE
         LA    R8,WTORECB              ADDRESS OF THE ECB
         ST    R8,WTOECB03             STORE ECB IN THE WTOR
         LA    R8,WTORANSR             ADDRESS OF THE ANSWER AREA
         STCM  R8,B'0111',WTOMSG03+1   STORE ANSWER IN WTOR
         LOAD  EP=WSRCCUTL             GET LIST OF JOBS
         ST    R0,WSRCCUTL             KEEP POINTER AROUND
         LR    R2,R0                   R2 NOW POINTS AT TABLE
         USING WUTLTBL,R2              ADDRESS THE TABLE
         L     R3,SAVEREG1             R3 NOW POINTS COMMON EXIT PARMS
         L     R4,0(R3)                R4 NOW POINTS AT JOBNAME
         MVC   WTOJBN01(8),0(R4)       MOVE THE JOBNAME TO EXTEND WTO
         MVC   WTOJBN02(8),0(R4)       MOVE THE JOBNAME TO CANCEL WTO
         MVC   WTOJBN03(8),0(R4)       MOVE THE JOBNAME TO PROMPT WTOR
         L     R7,X'220'               GET CURRENT ASCB
         L     R6,172(R7)              ADDRESS OF JOBNAME FOR "JOB"
         LTR   R6,R6                   IF NOT ZERO THEN "JOB"
         BZ    NOTJOB                  BRANCH IF NOT JOB
         MVC   TASKTYPE(3),WTJOB       INDICATE "JOB"
         MVI   TSTFLG+3,TSTJOB         SETUP "JOB" MASK FOR LATER TM
         B     MOVETASK                GO CHECK FOR OTHER THINGS
NOTJOB   L     R6,60(R7)               ADDRESS OF TSB
         LTR   R6,R6                   IF NOT ZERO THEN "TSO"
         BZ    NOTTSO                  BRANCH IF NO TSO
         MVC   TASKTYPE(3),WTTSO       INDICATE "TSO"
         MVI   TSTFLG+3,TSTTSO         SETUP "TSO" MASK FOR LATER TM
         B     MOVETASK                GO CHECK FOR OTHER THINGS
NOTTSO   MVC   TASKTYPE(3),WTSTC       INDICATE "STC"
         MVI   TSTFLG+3,TSTSTC         SETUP "STC" MASK FOR LATER TM
         B     MOVETASK                GO CHECK FOR OTHER THINGS
         EJECT
MOVETASK MVC   WTOIND01(3),TASKTYPE    MOVE TASK TYPE TO WTO
         MVC   WTOIND02(3),TASKTYPE    MOVE TASK TYPE TO WTO
         MVC   WTOIND03(3),TASKTYPE    MOVE TASK TYPE TO WTOR
         SPACE
LOOP     CLI   WLENGTH,X'FF'           ARE WE AT END OF TABLE??
         BE    CANCEL                  YES WE ARE -- GO FINISH UP
         SR    R5,R5                   CLEAR LENGTH FOR CLC
         LH    R5,WLENGTH              LOAD LENGTH FOR COMPARE
         BCTR  R5,0                    GET READY FOR CLC
         EX    R5,CLCJOBN              IS JOBNAME IN TABLE
         BE    GOTJOB                  BR IF EQUAL
         LA    R2,WUTLLEN(R2)          POINT AT NEXT ENTRY
         B     LOOP                    GO TRY NEXT NAME
         SPACE 2
GOTJOB   ST    R2,WSRCCJOB             SAVE ADDRESS OF JOBENTRY
         L     R3,SAVEREG0             GET ENTRY CODE FOR BRANCH
         L     R6,TSTFLG               LOAD THE MASK FOR TEST
         EX    R6,CHKFLG               EXECUTE THE FLAG TEST
         BZ    CANCEL                  BRANCH IF TEST FAILED
         TM    WUTLFLG1,WOPERWTO       SHOULD WE TELL THE OPERATOR?
         BO    GOTBRNCH                BRANCH AROUND IF YES.
         NI    WTORTC01,WTO$OPER       DELETE ROUTCDE FOR OPERATOR
         NI    WTORTC02,WTO$OPER       DELETE ROUTCDE FOR OPERATOR
GOTBRNCH B     *+4(R3)                 BRANCH TO CORRECT PLACE
         B     JOBTIME                 ENTRY WAS JOB TIME EXCEEDED
         B     STEPTIME                ENTRY WAS STEP TIME EXCEEDED
         B     WAITTIME                ENTRY WAS WAIT TIME EXCEEDED
         EJECT
JOBTIME  EQU   *
         TM    WUTLFLG1,WJOBEXT        TEST IF JOB EXTENTION OK
         BZ    CANCEL                  IF NOT CANCEL THE JOB
         L     R6,WEXTJOB              LOAD EXTENTION TIME
         LTR   R6,R6                   IS TIME ZERO??
         BZ    CANCEL                  BRANCH IF YES.
         ST    R6,TIMEXTND             STORE THE TIME EXTND AMT.
         CVD   R6,PACKTIME             MAKE TIME PACKED
         ED    WTOSEC01(6),PACKTIME+5  EDIT THE TIME INTO WTO
         ED    WTOSEC03(6),PACKTIME+5  EDIT THE TIME INTO WTOR
         LA    R15,8                   INDICATE EXTENTION
         ST    R15,RETURNCD            SAVE RETURN CODE
         TM    WUTLFLG1,WASKOPER       DO WE NEED TO PROMPT???
         BZ    JOBWTO                  NO - GO DO EXTEND WTO
JOBWTOR  WTOR  MF=(E,WTOMSG03)         ISSUE PROMPT FOR YES×NO
         WAIT  ECB=WTORECB             WAIT ON RESPONSE
         CLI   WTORANSR,C'Y'           IS THE ANSWER YES?
         BE    JOBWTO                  GO DO EXTEND
         CLI   WTORANSR,C'N'           IS THE ANSWER NO?
         BE    CANCEL                  GO CANCEL THE JOB
         B     JOBWTOR                 TRY AGAIN
JOBWTO   WTO   MF=(E,WTOMSG01)         ISSUE THE EXTEND WTO
         LR    R1,R6                   SET UP EXTENTION TIME
         B     FINISH                  WE ARE ALL DONE
         EJECT
STEPTIME EQU   *
         TM    WUTLFLG1,WSTEPEXT       TEST IF STEP EXTENTION OK
         BZ    CANCEL                  IF NOT CANCEL THE JOB
         L     R6,WEXTSTEP             LOAD EXTENTION TIME
         LTR   R6,R6                   IS TIME ZERO??
         BZ    CANCEL                  BRANCH IF YES.
         ST    R6,TIMEXTND             STORE THE TIME EXTND AMT.
         CVD   R6,PACKTIME             MAKE TIME PACKED
         ED    WTOSEC01(6),PACKTIME+5  EDIT THE TIME INTO WTO
         ED    WTOSEC03(6),PACKTIME+5  EDIT THE TIME INTO WTOR
         LA    R15,8                   INDICATE EXTENTION
         ST    R15,RETURNCD            SAVE RETURN CODE
         TM    WUTLFLG1,WASKOPER       DO WE NEED TO PROMPT???
         BZ    STEPWTO                 NO - GO DO EXTEND WTO
STEPWTOR WTOR  MF=(E,WTOMSG03)         ISSUE PROMPT FOR YES×NO
         WAIT  ECB=WTORECB             WAIT ON RESPONSE
         CLI   WTORANSR,C'Y'           IS THE ANSWER YES?
         BE    STEPWTO                 GO DO EXTEND
         CLI   WTORANSR,C'N'           IS THE ANSWER NO?
         BE    CANCEL                  GO CANCEL THE JOB
         B     STEPWTOR                TRY AGAIN
STEPWTO  WTO   MF=(E,WTOMSG01)         ISSUE THE EXTEND WTO
         LR    R1,R6                   SET UP EXTENTION TIME
         B     FINISH                  WE ARE ALL DONE
         EJECT
WAITTIME EQU   *
         TM    WUTLFLG1,WWAITEXT       TEST IF WAIT EXTENTION OK
         BZ    CANCEL                  IF NOT CANCEL THE JOB
         L     R6,WEXTWAIT             LOAD EXTENTION TIME
         LTR   R6,R6                   IS TIME ZERO??
         BZ    CANCEL                  BRANCH IF YES.
         ST    R6,TIMEXTND             STORE THE TIME EXTND AMT.
         CVD   R6,PACKTIME             MAKE TIME PACKED
         ED    WTOSEC01(6),PACKTIME+5  EDIT THE TIME INTO WTO
         ED    WTOSEC03(6),PACKTIME+5  EDIT THE TIME INTO WTOR
         LA    R15,8                   INDICATE EXTENTION
         ST    R15,RETURNCD            SAVE RETURN CODE
         TM    WUTLFLG1,WASKOPER       DO WE NEED TO PROMPT???
         BZ    WAITWTO                 NO - GO DO EXTEND WTO
WAITWTOR WTOR  MF=(E,WTOMSG03)         ISSUE PROMPT FOR YES×NO
         WAIT  ECB=WTORECB             WAIT ON RESPONSE
         CLI   WTORANSR,C'Y'           IS THE ANSWER YES?
         BE    WAITWTO                 GO DO EXTEND
         CLI   WTORANSR,C'N'           IS THE ANSWER NO?
         BE    CANCEL                  GO CANCEL THE JOB
         B     WAITWTOR                TRY AGAIN
WAITWTO  WTO   MF=(E,WTOMSG01)         ISSUE THE EXTEND WTO
         LR    R1,R6                   SET UP EXTENTION TIME
         B     FINISH                  WE ARE ALL DONE
         EJECT
ERRMSG01 EQU   *
         L     R15,SAVEREG1            GET ADDRESS EXIT PARM AREA
         L     R15,0(R15)              GET ADDRESS OF JOBNAME
         MVC   WTOJBN02(8),0(R15)      MOVE JOBNAME TO CANCEL MSG
         MVC   WTOJBN04(8),0(R15)      MOVE JOBNAME TO ERROR  MSG
         WTO   MF=(E,WTOMSG04)         ISSUE ERROR WTO
         SPACE 3
CANCEL   EQU   *
         LA    R15,0                   INDICATE CANCEL
         ST    R15,RETURNCD            SAVE RETURN CODE
         SR    R1,R1                   CLEAR TIME EXTENTION REGISTER
         WTO   MF=(E,WTOMSG02)         ISSUE CANCEL WTO
         SPACE 3
FINISH   EQU   *                       GET RID OF WSRCCUTL MODULE
         DELETE EP=WSRCCUTL            GET RID OF WSRCCUTL MODULE
         L     R15,RETURNCD            GET RETURN CODE BACK IN R15
         L     R1,TIMEXTND             GET EXTEND TIME INTO R1
         FINISH RC=(R15),RC1=(R1)      GO BACK TO SMF
         EJECT
CLCJOBN  CLC   0(*-*,R4),WJOBNAME      COMPARE JOBNAME TO TABLE
CHKFLG   TM    WUTLFLG1,X'00'          USED TO TEST FOR EXTEND OK
         SPACE
MSGWTO01 DS    0D                      ALLIGNMENT
         DC    AL2(WL01-4)             LENGTH OF WTO
         DC    B'1000000000000000'     MCS FLAGS
         DC    CL10'SYS005I - '        MESSAGE FOR WTO
MSGIND01 DC    CL04'    '              TASK INDICATOR
MSGJBN01 DC    CL08'        '          TASK NAME
         DC    CL13' HAS HAD ITS '
MSGTYP01 DC    CL04'    '              EXCEEDED TYPE
         DC    CL17' TIME EXTENDED BY'
MSGSEC01 DC    XL06'402020202120'      EDIT MASK FOR EXTENTION
         DC    CL09' SECONDS.'         END OF MESSAGE
MSGDSC01 DC    B'0000010000000000'     DESCRIPTOR CODES
MSGRTC01 DC    B'1000000000100000'     ROUTE CODES - WTP
WL01     EQU   *-MSGWTO01              LENGTH OF WTO1
         SPACE
MSGWTO02 DS    0D                      ALLIGNMENT
         DC    AL2(WL02-4)             LENGTH OF WTO
         DC    B'1000000000000000'     MCS FLAGS
         DC    CL10'SYS006I - '        MESSAGE FOR WTO
MSGIND02 DC    CL04'    '              TASK INDICATOR
MSGJBN02 DC    CL08'        '          TASK NAME
         DC    CL19' HAS EXCEEDED IT''S '
MSGTYP02 DC    CL04'    '              EXCEEDED TYPE
         DC    CL30' TIME AND HAS BEEN CANCELLED. '
MSGDSC02 DC    B'0000010000000000'     DESCRIPTOR CODES
MSGRTC02 DC    B'1000000000100000'     ROUTE CODES - WTP
WL02     EQU   *-MSGWTO02              LENGTH OF WTO2
         SPACE
MSGWTO03 DS    0D                      ALLIGNMENT
         DC    AL1(1)                  REPLY LENGTH
         DC    AL3(0)                  REPLY ADDRESS
MSGECB03 DC    A(0)                    ECB ADDRESS
         DC    AL2(WL03-12)            LENGTH OF WTO
         DC    B'1000000000000000'     MCS FLAGS
         DC    CL10'SYS007I - '        MESSAGE FOR WTO
MSGIND03 DC    CL04'    '              TASK INDICATOR
MSGJBN03 DC    CL08'        '          TASK NAME
         DC    CL12' REQUESTS A '
MSGTYP03 DC    CL04'    '              EXCEEDED TYPE
         DC    CL19' TIME EXTENTION OF '
MSTSEC03 DC    XL06'402020202120'      EDIT MASK FOR EXTENTION
         DC    CL10' SECONDS. '
         DC    CL37'- REPLY "Y" TO ALLOW OR "N" TO CANCEL'
MSGDSC03 DC    B'0000010000000000'     DESCRIPTOR CODES
MSGRTC03 DC    B'1000000000100000'     ROUTE CODES - WTP
WL03     EQU   *-MSGWTO03              LENGTH OF WTO3
         SPACE
MSGWTO04 DS    0D                      ALLIGNMENT
         DC    AL2(WL04-4)             LENGTH OF WTO
         DC    B'1000000000000000'     MCS FLAGS
         DC    CL55'SYS008E - IEFUTL WAS ENTERED WITH AN INVALID CODE '
MSGJBN04 DC    CL10'          '        JOBNAME
MSGDSC04 DC    B'0000010000000000'     DESCRIPTOR CODES
MSGRTC04 DC    B'1000000000100000'     ROUTE CODES - WTP
WL04     EQU   *-MSGWTO04              LENGTH OF WTO4
         SPACE
WTTSO    DC    CL3'TSU'                TSO USER FLAG
WTSTC    DC    CL3'STC'                STC TASK FLAG
WTJOB    DC    CL3'JOB'                JOB INIT FLAG
LTJOB    DC    CL4'JOB '               ENTRY TYPE FOR MESSAGE
LTSTEP   DC    CL4'STEP'               ENTYR TYPE FOR MESSAGE
LTWAIT   DC    CL4'WAIT'               ENTRY TYPE FOR MESSAGE
TSTTSO   EQU   X'04'                   USED TO TEST TSO EXTEND FLAG
TSTSTC   EQU   X'02'                   USED TO TEST STC EXTEND FLAG
TSTJOB   EQU   X'01'                   USED TO TEST JOB EXTEND FLAG
WTO$OPER EQU   X'FF'-X'80'             USED TO DELETE OPER ROUTECODE
         EJECT
*--------------------------------------*
* WORKAREA -                           *
*     DSECT TO DESCRIBE THE GOTTEN     *
*     STORAGE USED IN IEFUTL.  THIS    *
*     AREA IS GOTTEN IN THE "BEGIN"    *
*     MACRO AND FREED IN "FINISH".     *
*--------------------------------------*
         SPACE 1
WORKAREA DSECT
SAVEAREA DS    18F                     SETUP OS SAVEAREA
SAVEREG0 DS    F                       EXTRA SAVE OF REG0
SAVEREG1 DS    F                       EXTRA SAVE OF REG1
WSRCCUTL DS    F                       ADDRESS OF LOADED MODULE
WSRCCJOB DS    F                       ADDRESS OF JOBENTRY IN WSRCCUTL
RETURNCD DS    F                       RETURN CODE SAVEAREA (R15)
TIMEXTND DS    F                       AMOUNT OF TIME EXTENTION
PACKTIME DS    D                       WORK AREA FOR TIME CVD
TSTFLG   DS    F                       SAVE THE EXTEND FLAG FOR TEST
TASKTYPE DS    CL3                     TYPE OF TASK RUNNING
WTORANSR DS    CL1                     ANSWER AREA FROM WTOR
WTORECB  DS    F                       ECB FOR USE IN WTOR
         SPACE
WTOMSG01 DS    0D                      ALLIGNMENT
         DS    AL2(WL01-4)             LENGTH OF WTO
         DS    B'1000000000000000'     MCS FLAGS
         DS    CL10'SYS005I - '        MESSAGE FOR WTO
WTOIND01 DS    CL04'    '              TASK INDICATOR
WTOJBN01 DS    CL08'        '          TASK NAME
         DS    CL13' HAS HAD ITS '
WTOTYP01 DS    CL04'    '              EXCEEDED TYPE
         DS    CL17' TIME EXTENDED BY'
WTOSEC01 DS    XL06'402020202120'      EDIT MASK FOR EXTENTION
         DS    CL09' SECONDS.'         END OF MESSAGE
WTODSC01 DS    B'0000010000000000'     DESCRIPTOR CODES
WTORTC01 DS    B'1000000000100000'     ROUTE CODES - WTP
         SPACE
WTOMSG02 DS    0D                      ALLIGNMENT
         DS    AL2(WL02-4)             LENGTH OF WTO
         DS    B'1000000000000000'     MCS FLAGS
         DS    CL10'SYS006I - '        MESSAGE FOR WTO
WTOIND02 DS    CL04'    '              TASK INDICATOR
WTOJBN02 DS    CL08'        '          TASK NAME
         DS    CL19' HAS EXCEEDED IT''S '
WTOTYP02 DS    CL04'    '              EXCEEDED TYPE
         DS    CL30' TIME AND HAS BEEN CANCELLED. '
WTODSC02 DS    B'0000010000000000'     DESCRIPTOR CODES
WTORTC02 DS    B'1000000000100000'     ROUTE CODES - WTP
         SPACE
WTOMSG03 DS    0D                      ALLIGNMENT
         DS    AL1(1)                  REPLY LENGTH
         DS    AL3(0)                  REPLY ADDRESS
WTOECB03 DS    A(0)                    ECB ADDRESS
         DS    AL2(WL03-12)            LENGTH OF WTO
         DS    B'1000000000000000'     MCS FLAGS
         DS    CL10'SYS007I - '        MESSAGE FOR WTO
WTOIND03 DS    CL04'    '              TASK INDICATOR
WTOJBN03 DS    CL08'        '          TASK NAME
         DS    CL12' REQUESTS A '
WTOTYP03 DS    CL04'    '              EXCEEDED TYPE
         DS    CL19' TIME EXTENTION OF '
WTOSEC03 DS    XL06'402020202120'      EDIT MASK FOR EXTENTION
         DC    CL10' SECONDS. '
         DC    CL37'- REPLY "Y" TO ALLOW OR "N" TO CANCEL'
WTODSC03 DS    B'0000010000000000'     DESCRIPTOR CODES
WTORTC03 DS    B'1000000000100000'     ROUTE CODES - WTP
         SPACE
WTOMSG04 DS    0D                      ALLIGNMENT
         DS    B'1000000000000000'     MCS FLAGS
         DS    CL55'SYS008E - IEFUTL WAS ENTERED WITH AN INVALID CODE '
WTOJBN04 DS    CL10'        '          JOBNAME
WTODSC04 DS    B'0000010000000000'     DESCRIPTOR CODES
WTORTC04 DS    B'1000000000100000'     ROUTE CODES - WTP
         SPACE
SALENGTH EQU   ((*-SAVEAREA+16)/16)*16
         EJECT
*--------------------------------------*
* WUTLDSCT -                           *
*     DSECT TO DESCRIBE THE ENTRIES    *
*     IN THE WSRCCUTL MODULE.          *
*     EACH ENTRY CONTAINS A JOBNAME    *
*     AND ASSOCIATED EXTENTION TIMES   *
*     AND FLAGS.  THE JOBNAME MAY BE   *
*     THE FIRST PORTION OF A GROUP OF  *
*     LIKE NAMED JOBS BASED ON THE     *
*     LENGTH OF THE COMPARE USED.      *
*--------------------------------------*
         SPACE 1
WUTLDSCT DSECT
WUTLTBL  DS    0CL24                   TIME LIMIT EXIT TABLE ENTRY
WEXTJOB  DS    F                       TIME EXTENTION FOR JOB TIME
WEXTSTEP DS    F                       TIME EXTENTION FOR STEP TIME
WEXTWAIT DS    F                       TIME EXTENTION FOR WAIT TIME
WUTLFLG1 DS    XL1                     JOB FLAG BYTE 1
WJOBEXT  EQU   X'80'                       1... .... EXTEND JOB TIME
WSTEPEXT EQU   X'40'                       .1.. .... EXTEND STEP "
WWAITEXT EQU   X'20'                       ..1. .... EXTEND WAIT "
WASKOPER EQU   X'10'                       ...1 .... PROMPT FOR OK
WOPERWTO EQU   X'08'                       .... 1... MSG TO OPER TOO
WXTNDTSU EQU   X'04'                       .... .1.. EXTEND IF TSU
WXTNDSTC EQU   X'02'                       .... ..1. EXTEND IF STC
WXTNDJOB EQU   X'01'                       .... ...1 EXTEND IF JOB
WUTLFLG2 DS    XL1                     JOB FLAG BYTE 2
WRESVD01 EQU   X'80'                       1... .... RESERVED
WRESVD02 EQU   X'40'                       .1.. .... RESERVED
WRESVD03 EQU   X'20'                       ..1. .... RESERVED
WRESVD04 EQU   X'10'                       ...1 .... RESERVED
WRESVD05 EQU   X'08'                       .... 1... RESERVED
WRESVD06 EQU   X'04'                       .... .1.. RESERVED
WRESVD07 EQU   X'02'                       .... ..1. RESERVED
WRESVD08 EQU   X'01'                       .... ...1 RESERVED
WLENGTH  DS    H                       LENGTH FOR COMPARE OF JOBNAME
WJOBNAME DS    CL8                     JOBNAME TO COMPARE
WUTLLEN  EQU   *-WUTLTBL
         SPACE 1
         END
