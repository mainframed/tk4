*          DATA SET LISTFILE   AT LEVEL 001 AS OF 06/22/81
*
* LISTFILE -- REPLACEMENT FOR THE LISTA TSO COMMAND
*             PROVIDES NICER OUTPUT AND MORE OPTIONS
*             TOM FOTH, WSRCC, 03/09/82
*
         MACRO
&LABEL   DSKEY &FIELD,&LENFLD
         DS    0H
         DS    H
         DS    H
&LENFLD  DS    H
&LABEL   DS    &FIELD
         MEND
         MACRO
&LABEL   BLANK &FIELD
&LABEL   MVI   &FIELD,C' '
         MVC   &FIELD+1(L'&FIELD-1),&FIELD
         MEND
SHOWDD   BEGIN REQUS=YES
         LR    R8,R1
         GETMAIN R,LV=1024
         LR    R11,R1
         USING WORKMAP,R11
         LA    R1,PPLAREA
         USING PPL,R1
         USING CPPL,R8
         MVC   PPLCBUF,CPPLCBUF
         MVC   PPLUPT,CPPLUPT
         MVC   PPLECT,CPPLECT
         DROP  R8
         XC    MYECB(L'MYECB),MYECB
         LA    R0,MYECB
         ST    R0,PPLECB
         L     R0,ADDRPTBL
         ST    R0,PPLPCL
         LA    R0,ANSADDR
         ST    R0,PPLANS
         DROP  R1
         LINK  EP=IKJPARS
         CLI   ANSADDR,X'FF'
         BNE   PARSEOK
         TPUT  ERROR0,33
         SR    R15,R15
         LA    R15,R12
         B     EXIT
PARSEOK  DS    0H
         L     R8,ANSADDR
         USING MAPPARMS,R8
         LA    R1,S99RBBLK         AREA FOR RB POINTER
         XC    S99RBBLK(RBBLEN),S99RBBLK  ZERO OUT RB AREA
         USING S99RBP,R1
         LA    R4,S99RBPTR+4       GET ADDR TO AREA AFTER PTR FOR RB
         USING S99RB,R4
         ST    R4,S99RBPTR         POINT TO THE RB
         OI    S99RBPTR,S99RBPND   ONLY 1 REQUEST-TERMINATE LIST
         MVI   S99RBLN,RBLEN       PUT LENGTH IN RB
         MVI   S99VERB,S99VRBIN    VERB CODE OF THIS FUNCTION IS INFO
         LA    R5,TULIST           POINT TO FIRST TEXT UNIT
         USING S99TUPL,R5
         ST    R5,S99TXTPP         POINT TO TEXT POINTERS
         LA    R6,TXTUNITS         POINT TO TEXT UNITS (11 WORDS)
         USING S99TUNIT,R6
         ST    R6,S99TUPTR         SAVE THE ADDR OF THIS TXT UNIT
         LA    R7,DINRTDDN         REQUEST DDNAME
         STH   R7,S99TUKEY         SAVE THE KEY
         LA    R7,1
         STH   R7,S99TUNUM
         LA    R6,14(R6)
         LA    R5,4(R5)
*
         ST    R6,S99TUPTR
         LA    R7,DINRTDSN     REQUEST DSN
         STH   R7,S99TUKEY
         LA    R7,1
         STH   R7,S99TUNUM
         LA    R6,50(R6)
         LA    R5,4(R5)
*
         ST    R6,S99TUPTR
         LA    R7,DINRTMEM    REQUEST MEMBER
         STH   R7,S99TUKEY
         LA    R7,1
         STH   R7,S99TUNUM
         LA    R6,14(R6)
         LA    R5,4(R5)
*
         ST    R6,S99TUPTR
         LA    R7,DINRTSTA     REQUEST STATUS
         STH   R7,S99TUKEY
         LA    R7,1
         STH   R7,S99TUNUM
         LA    R7,1
         STH   R7,S99TULNG
         LA    R6,8(R6)
         LA    R5,4(R5)
*
         ST    R6,S99TUPTR
         LA    R7,DINRTNDP    REQUEST NORMAL DISPOSITION
         STH   R7,S99TUKEY
         LA    R7,1
         STH   R7,S99TUNUM
         LA    R7,1
         STH   R7,S99TULNG
         LA    R6,8(R6)
         LA    R5,4(R5)
*
         ST    R6,S99TUPTR
         LA    R7,DINRTCDP     REQUEST CONDITIONAL DISPOSITION
         STH   R7,S99TUKEY
         LA    R7,1
         STH   R7,S99TUNUM
         LA    R7,1
         STH   R7,S99TULNG
         LA    R6,8(R6)
         LA    R5,4(R5)
*
         ST    R6,S99TUPTR
         LA    R7,DINRTORG    REQUEST DSORG
         STH   R7,S99TUKEY
         LA    R7,1
         STH   R7,S99TUNUM
         LA    R7,2
         STH   R7,S99TULNG
         LA    R6,8(R6)
         LA    R5,4(R5)
*
         ST    R6,S99TUPTR
         LA    R7,DINRTATT     REQUEST ATTRIBUTES
         STH   R7,S99TUKEY
         LA    R7,1
         STH   R7,S99TUNUM
         LA    R7,1
         STH   R7,S99TULNG
         LA    R6,8(R6)
         LA    R5,4(R5)
*
         ST    R6,S99TUPTR
         LA    R7,DINRTLST    REQUEST FLAG FOR LAST ALLOCATION
         STH   R7,S99TUKEY
         LA    R7,1
         STH   R7,S99TUNUM
         LA    R7,1
         STH   R7,S99TULNG
         LA    R6,8(R6)
         LA    R5,4(R5)
*
         ST    R6,S99TUPTR
         LA    R7,DINRTTYP     REQUEST TYPE OF ALLOCATION
         STH   R7,S99TUKEY
         LA    R7,1
         STH   R7,S99TUNUM
         LA    R7,1
         STH   R7,S99TULNG
         LA    R6,8(R6)
         LA    R5,4(R5)
*
         ST    R6,S99TUPTR
         MVI   S99TUPTR,S99TUPLN
         LA    R7,DINRELNO     SPECIFY RELATIVE NUMBER
         STH   R7,S99TUKEY
         LA    R7,1
         STH   R7,S99TUNUM
         LA    R7,2
         STH   R7,S99TULNG
         DROP  R1
         DROP  R4
         DROP  R5
         DROP  R6
         LA    R9,1
NEXTDDN  STH   R9,INFOREL#
         LA    R1,S99RBBLK
         BLANK INFODDN
         BLANK INFODSN
         BLANK INFOMEM
         LA    R6,TXTUNITS         POINT TO TEXT UNITS
         USING S99TUNIT,R6
         LA    R7,8                RESTORE LENGTH OF DDNAME
         STH   R7,S99TULNG
         LA    R6,14(R6)
*
         LA    R7,X'2C'
         STH   R7,S99TULNG         RESTORE LENGTH OF DSNAME
         LA    R6,50(R6)
*
         LA    R7,8
         STH   R7,S99TULNG         RESTORE LENGTH OF MEMBER NAME
         LA    R6,14(R6)
         DROP  R6
*
         DYNALLOC
         LTR   R2,R15
         BZ    GOTINFO
         TPUT  ERROR2,39
         LR    R15,R2
         B     EXIT
GOTINFO  DS    0H
         MVC   PUTDDN,INFODDN
         MVC   PUTDSN(44),INFODSN
         CLI   INFOMEM,C' '
         BE    NOMEM
         LA    R1,PUTDSN
         SR    R0,R0
         LH    R0,IDSNLEN
         AR    R1,R0
         MVI   0(R1),C'('
         MVC   1(8,R1),INFOMEM
         LH    R0,IMEMLEN
         AR    R1,R0
         MVI   1(R1),C')'
NOMEM    DS    0H
         SR    R1,R1
         LA    R2,STATTBL
         IC    R1,INFOSTAT
         LTR   R1,R1
         BNZ   NXTSTAT
         MVC   PUTSTAT,=CL3'UNK'
         B     GETDISP
NXTSTAT  SRL   R1,1
         LTR   R1,R1
         BZ    MOVESTAT
         LA    R2,4(R2)
         B     NXTSTAT
MOVESTAT DS    0H
         MVC   PUTSTAT,0(R2)
GETDISP  IC    R1,INFODISP
         LA    R2,DISPTBL
NXTDISP  SRL   R1,1
         LTR   R1,R1
         BZ    MOVEDISP
         LA    R2,4(R2)
         B     NXTDISP
MOVEDISP DS    0H
         MVC   PUTDISP,0(R2)
         ICM   R1,B'0011',INFODSRG
         BNZ   FINDDSRG
         IC    R1,INFOSTAT
         LTR   R1,R1
         BNZ   FINDDSRG
         MVC   PUTSTAT,=C'CAT'
         MVC   PUTDSORG,=C'VSAM'
         B     GETTYPE
FINDDSRG LA    R2,DSORGTBL
DSORGTST CH    R1,0(R2)
         BE    GOTDSORG
         CLC   0(2,R2),=H'0'
         BE    GOTDSORG
         LA    R2,6(R2)
         B     DSORGTST
GOTDSORG DS    0H
         MVC   PUTDSORG,2(R2)
GETTYPE  CLI   INFOTYPE,X'80'
         BNE   ATTR4
         MVC   PUTDSN(8),=CL8'NULLFILE'
         B     ATTR5
ATTR4    DS    0H
         CLI   INFOTYPE,X'40'
         BNE   ATTR5
         MVC   PUTDSN(8),=CL8'TERMFILE'
ATTR5    DS    0H
         TM    INFOATTR,X'60'
         BM    ATTR6
         BO    ATTR6
         MVI   PUTINUSE,C'*'
ATTR6    DS    0H
         MVI   PUTPROC,C'P'
         TM    INFOATTR,X'08'
         BNO   ATTR7
         MVI   PUTPROC,C'S'
ATTR7    DS    0H
         CLI   ALLOPT+1,X'01'
         BE    SHOW
         CLI   SYSOPT+1,X'01'
         BE    SHOWCK1
         CLC   PUTDSN(4),=C'SYS7'
         BL    SHOWCK1
         CLC   PUTDSN(4),=C'SYS9'
         BH    SHOWCK1
         B     INCRELNO
SHOWCK1  DS    0H
         CLI   LOGONOPT+1,X'01'
         BE    SHOWCK2
         CLI   PUTPROC,C'S'
         BNE   INCRELNO
SHOWCK2  DS    0H
         CLI   FILEOPT+1,X'00'
         BE    SHOWCK3
         CLI   FILEOPT+1,X'01'
         BNE   SHOWCK2A
         CLI   INFOTYPE,X'40'
         BE    SHOWCK3
         B     INCRELNO
SHOWCK2A CLI   FILEOPT+1,X'02'
         BNE   SHOWCK2B
         CLI   INFOTYPE,X'80'
         BE    SHOWCK3
         B     INCRELNO
SHOWCK2B CLI   INFOTYPE,X'00'
         BE    SHOWCK3
         B     INCRELNO
SHOWCK3  CLI   STATOPT+1,X'00'
         BE    SHOW
         CLC   INFOSTAT,STATOPT+1
         BE    SHOW
         CLI   STATOPT+1,X'03'
         BNE   SHOWCK3A
         CLI   INFOSTAT,X'08'
         BE    SHOW
         B     INCRELNO
SHOWCK3A CLI   STATOPT+1,X'05'
         BNE   SHOWCK3B
         CLI   INFOSTAT,X'00'
         BNE   INCRELNO
         ICM   R1,B'0011',INFODSRG
         BNZ   SHOW
         B     INCRELNO
SHOWCK3B CLI   STATOPT+1,X'06'
         BNE   INCRELNO
         CLI   INFOSTAT,X'00'
         BNE   INCRELNO
         ICM   R1,B'0011',INFODSRG
         BZ    SHOW
         C     R1,=F'8'
         BNE   INCRELNO
SHOW     LA    R2,PUTREC
         TPUT  (R2),PUTLEN
         OI    FLAGS,X'80'
INCRELNO CLI   INFOEND,X'80'
         BE    FINI
         MVI   PUTREC,C' '
         MVC   PUTREC+1(PUTLEN-1),PUTREC
         LA    R9,1(R9)
         B     NEXTDDN
FINI     SR    R15,R15
EXIT     FREEMAIN R,LV=1024,A=(R11)
         TM    FLAGS,X'80'
         BO    EXIT1
         TPUT  NUNLSTED,29
EXIT1    LA    R2,ANSADDR
         IKJRLSA (R2)
         FINISH RC=(R15)
ADDRPTBL DC    A(PARMTBL)
ERROR0   DC    CL35'ERROR IN LISTFILE - TSO PARSE ERROR'
ERROR2   DC    CL42'ERROR IN LISTFILE - BAD RETURN FROM SVC 99'
NUNLSTED DC    CL29'LISTFILE - NO DDNAMES TO LIST'
DSORGTBL DS    0H
         DC    X'0004',C'TR  '
         DC    X'0008',C'VSAM'
         DC    X'0020',C'TQ  '
         DC    X'0040',C'TX  '
         DC    X'0080',C'GS  '
         DC    X'0200',C'PO  '
         DC    X'0300',C'POU '
         DC    X'0400',C'MQ  '
         DC    X'0800',C'CQ  '
         DC    X'1000',C'CX  '
         DC    X'2000',C'DA  '
         DC    X'2100',C'DAU '
         DC    X'4000',C'PS  '
         DC    X'4100',C'PSU '
         DC    X'8000',C'IS  '
         DC    X'8100',C'ISU '
         DC    X'0000',C'??  '
DISPTBL  DS    0F
         DC    C'UNC '
         DC    C'CAT '
         DC    C'DEL '
         DC    C'KEEP'
         DC    C'PASS'
STATTBL  DS    0F
         DC    C'OLD '
         DC    C'MOD '
         DC    C'NEW '
         DC    C'SHR '
         LTORG
PARMTBL  IKJPARM  DSECT=MAPPARMS
ALLOPT   IKJKEYWD
         IKJNAME  'ALL'
SYSOPT   IKJKEYWD
         IKJNAME  'SYS'
LOGONOPT IKJKEYWD
         IKJNAME  'LOGON'
FILEOPT  IKJKEYWD
         IKJNAME  'TERMFILE'
         IKJNAME  'NULLFILE'
         IKJNAME  'DATAFILE'
STATOPT  IKJKEYWD
         IKJNAME  'OLD'
         IKJNAME  'MOD'
         IKJNAME  'SHR'
         IKJNAME  'NEW'
         IKJNAME  'UNK'
         IKJNAME  'CAT'
         IKJENDP
WORKMAP  DSECT
FLAGS    DS    F
MYECB    DS    F
ANSADDR  DS    F
PPLAREA  DS    7F
S99RBBLK DS    0F
         DS    F               ROOM PTR
         DS    XL20            ROOM FOR RB
TULIST   DS    11F             ROOM FOR TEXT PTRS
TXTUNITS DS    0H              HEAD OF TEXT UNIT AREA
INFODDN  DSKEY CL8
INFODSN  DSKEY CL44,IDSNLEN
INFOMEM  DSKEY CL8,IMEMLEN
INFOSTAT DSKEY X
INFODISP DSKEY X
INFOCOND DSKEY X
INFODSRG DSKEY XL2
INFOATTR DSKEY X
INFOEND  DSKEY X
INFOTYPE DSKEY X
INFOREL# DSKEY H
RBBLEN   EQU   (*-S99RBBLK)
PUTREC   DS    0F
         DS    C
PUTPROC  DS    C
PUTINUSE DS    C
         DS    C
PUTSTAT  DS    CL3
         DS    C
PUTDSORG DS    CL3
         DS    C
PUTDDN   DS    CL8
         DS    C
PUTDSN   DS    CL54
PUTLEN   EQU   *-PUTREC
PUTDISP  DS    CL3
         DS    C
WORKLEN  EQU   *-WORKMAP
         IKJPPL
         IKJCPPL
         IEFZB4D0
         IEFZB4D2
RBLEN    EQU   (S99RBEND-S99RB)
         END   SHOWDD
