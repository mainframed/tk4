*
*  THIS PROGRAM IS DESIGNED TO FIX MOD FILE IF THE SYSTEM CRASHES
*
*        AN OBTAIN FOR SYNCFILE IS DONE
*              IF THE DATA SET IS NOT FOUND,THE ADABAS LOG
*            IS ASSUMED TO HAVE CLOSED OK
*              IF IT IS,A CRASH IS ASSUMED
*
*  IF SYSTEM CLOSE OK:
*         NO COPY DONE
*
* NOTE: A PARM DESIGNATING THE VOL SER OF THE SYNCFILE IS REQUIRED
*       ALSO THE DATASET NAME IS PASSED VIA THE PARM
*       (E.G.   PARM='VVVVVV,DSNAME.....'
*
*  IF SYSTEM CRASH:
*         A. FILEIN COPIED TO FILEOUT UNTILL
*            (1) EOF
*            (2) SYNAD
*            (3) RECORD CONTAING FILL CHARACTERS IS FOUND(SEE FORMATPG)
*
*       THE SYNC DATASET IS DELETED AT THE END OF THE COPYS
*
MODCOPY  ENTER (14,12),3,(,72)
         L     R9,0(R1)            GET ADDR OF PARM IF THERE IS ONE
         CLC   0(2,R9),=H'51'      PARM LENGTH <= 51
         BH    BADPARM             YES PARM OK
         CLC   0(2,R9),=H'8'       PARM LENGTH >= 8
         BL    BADPARM             YES PARM OK
         CLI   8(R9),C','          IS THERE THE REQUIRED COMMA?
         BE    MOVEVOL             YUP....
BADPARM  WTO   'INVALID PARM SPECIFIED'
         B     *-1  BLOW UP AT THIS POINT
MOVEVOL  EQU *
         MVC   VOLFMT1,2(R9)       MOVE VOL SER TO CAMLIST TABLE
         MVC   SCRVOL,VOLFMT1      AND MOVE IT INTO SCRATCH VOLUME
         LH    R2,0(R9)            PICK UP FULL LENGTH OF PARM
         SH    R2,=H'8'            MINUS LENGTH FOR VOL PORTION
         EX    R2,MOVEDSN          MOVE NAME INTO DSNAME FIELD
         OBTAIN FMT1               SEE IF SYNC DATA SET PRESENT
         LTR   R15,R15        IS IT HERE?
         BZ    DOCOPY         YES DO THE FIX-UP ON LOG FILE
         CH    R15,=H'08'     DATASET NOT FOUND?
         BE    GOODEND        THATS GOOD
         LR    R2,R15
         WTO   'ERROR OCCURED DURING OBTAIN CHECK R15'
         WTO   'R2 = 4   VOL SER SPECIFIED IN PARM NOT MOUNTED'
         WTO   'R2 = 12  I/O ERROR'
         WTO   'R2 = 16  INVALID WORK AREA POINTER'
         B     *-1      BLOW UP
GOODEND  EQU   *
         WTO   'SYNC DATASET NOT FOUND - ASSUMED NORMAL END'
         LA    R15,0       SET  RETURN CODE
         B     EOJ
DOCOPY   EQU   *
         WTO   'SYNC DATASET FOUND - COPY INVOKED'
         OPEN  (FILEIN,INPUT)
         LH    R5,FILEIN+82    GET LRECL
         CH    R5,=H'260'      RECORD LONG ENOUGH FOR COMPARE?
         BNL   AROUND
         SH    R5,=H'4'        SUB 4 INCASE IT'S VARIBLE LENGTH
         BCTR  R5,0            SUB 1 FOR INSTRUCTION
         STC   R5,COMP+1       PUT LENGTH INTO INSTRUCTION
AROUND   EQU   *
         OPEN  (FILEOUT,OUTPUT)
         STM   R0,R15,SVEREGS    SAVE OUR REGS INCASE OF SYSNAD EXIT
READ1    GET   FILEIN,INAREA
         B     COMP
SVEREGS  DS    16F
COMP     CLC   INAREA+4(256),FILLCHR
         BE    PART2B
         PUT   FILEOUT,INAREA
         B     READ1
PART2    EQU   *
         LM    R0,R15,4(R14)      GET OUR REGS BACK AFTER SYNAD EXIT
PART2B   EQU   *
         CLOSE (FILEIN)
         CLOSE (FILEOUT)
         OPEN  (INPUT,INPUT)
         OPEN  (OUTPUT,OUTPUT)
READ2    EQU   *
         GET   INPUT,INAREA
         PUT   OUTPUT,INAREA
         B     READ2
EOF      EQU   *
         CLOSE (INPUT)
         CLOSE (OUTPUT)
         SCRATCH SCRLST
         LTR   R15,R15           TEST FOR SCRATCH OKAY
         BZ    UNCATRY           EVERYTHING A.O.K.
         MVC   VOLDEV,=X'3010200E' CHANGE TYPE TO 3380
         SCRATCH SCRLST          .. TRY AGAIN
         LTR   R15,R15           TEST FOR SCRATCH OKAY
         BZ    UNCATRY           EVERYTHING A.O.K.
         WTO   '*********************************************'
         WTO   'SYNC DATASET DELETE FAILED -- NOTIFY SOFTWARE'
         WTO   '*********************************************'
         B     *-1
UNCATRY  CATALOG UNCAT             ATTEMPT AN UNCATLG
         LA    R15,4               SHOW COPY DONE AND SYNC DEL'D
EOJ      EQU   *
         LEAVE EQ
MOVEDSN  MVC   DSNAME(0),9(R9)      **** EXECUTED INSTRUCTION **
FILLCHR  DC    256C'$'
ADDR     DS    F
UNCAT    CAMLST UNCAT,DSNAME      ATTEMPT AN UNCATLG
SCRLST  CAMLST  SCRATCH,DSNAME,,VOLIST
VOLIST  DC      H'1'
VOLDEV  DC      X'3050200B'           TRY 3350 FOR VOLUME FIRST
SCRVOL  DC      CL6'XXXXXX'           VOLUME TO SCRATCH ON
FMT1     CAMLST SEARCH,DSNAME,VOLFMT1,VTOCIN2
VOLFMT1  DC    CL6' '
         DS    0D
VTOCIN   DS    0CL140
DSNAME   DC    CL44' '
VTOCIN2  DC    CL96' '
FILEIN   DCB   DDNAME=FILEIN,MACRF=(GM),DSORG=PS,EODAD=PART2,         XX
               SYNAD=PART2
FILEOUT  DCB   DDNAME=FILEOUT,MACRF=(PM),DSORG=PS
INPUT    DCB   DDNAME=FILEOUT,MACRF=(GM),DSORG=PS,EODAD=EOF
OUTPUT   DCB   DDNAME=FILEIN,MACRF=(PM),DSORG=PS
         LTORG
INAREA   DS    CL19069
         END
