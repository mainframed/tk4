VSAMHIST ENTER (14,12),12,*
*
*    VSAMHIST - VSAM DATASET ANALYSIS
*
*    WRITTEN:
*       RICHARD G. NIKULA
*       WASHINGTON STATE DEPARTMENT OF TRANSPORTAION.
*
*
         L     R2,0(R1)
         CLC   0(2,R2),=H'0'
         BE    NOPSWD
         LH    R1,0(R2)
         BCTR  R1,0
         EX    R1,MVEPSWD
NOPSWD   EQU   *
         OPEN  (SORTIN,(OUTPUT),KEYOUT,(OUTPUT))
         OPEN  (VSAMACB)
         TESTCB ACB=VSAMACB,OFLAGS=OPEN
         BNZ   OPENERR
         SHOWCB ACB=VSAMACB,AREA=CINV,LENGTH=16,                       X
               FIELDS=(CINV,LRECL,KEYLEN,RKP)
         L     R3,CINV                  CONTROL INT. SIZE
         SH    R3,=H'4'                 ADJUST FOR CIDF
         ST    R3,MAXPOS                CI LENGTH
         L     R2,KEYLEN
         BCTR  R2,0
         CH    R2,=H'61'                MAX TO WRITE
         BNH   R2OK
         LH    R2,=H'61'
R2OK     EQU   *
         ST    R2,KEYLEN
         POINT RPL=RPL1                 POINT TO INIT RECORD
         LTR   R15,R15                  DID IT WORK
         BNZ   POINTERR                 NO.....
         XR    R7,R7
         XR    R5,R5
         L     R6,MAXPOS                PREPARE TO GET REC LEN
GETLOOP  EQU   *
         GET   RPL=RPL1                 GET FIRST RECORD
         LTR   R15,R15
         BNZ   GETERR
         AP    ALLREC,=PL1'1'
         SHOWCB RPL=RPL1,AREA=RBA,LENGTH=8,FIELDS=(RBA,RECLEN)
         L     R3,RBA                   RBA OF RECORD READ
         XR    R2,R2
         D     R2,CINV                  DETERMINE WHICH CI
         C     R3,LASTCI                SAME??
         BNE   CLEAR                    NO
         L     R1,RECAREA               ADDRESS FOR RECORD
         A     R1,RKP
         L     R2,KEYLEN                ADDRESS FOR KEY
         EX    R2,MOVEKEY               MOVE HIGHKEY
         CLI   INIT,X'00'               FIRST RECORD
         BNE   CONT                     NO
         MVC   LOWKEY,HIGHKEY           COPY THE RECORD
         MVI   INIT,X'FF'               SET FLAG
CONT     LA    R7,1(R7)                 ONE MORE RECORD IN THIS CI
         A     R5,RECLEN                ADD LENGTH
         CLC   RECLEN,PREVLEN           SAME AS LAST RECORD?
         BNE   NEWSIZE
         CLI   HADONE,X'FF'             IS THIS FIRST RECORD?
         BNE   FIRST                    YES
         B     ENDLEN
FIRST    EQU   *
         MVI   HADONE,X'FF'             SHOW GREATER THAN 2
         SH    R6,=H'3'                 STILL HAS ONE RDF
         B     ENDLEN
NEWSIZE  EQU   *
         MVI   HADONE,X'00'             RESET COUNT FLAG
         MVC   PREVLEN,RECLEN           NEW RECORD LENGHT
         SH    R6,=H'3'                 HAS ONE RDF
         B     ENDLEN
ENDLEN   EQU   *
         B     GETLOOP
CLEAR    BAL   R9,RESET
         B     GETLOOP
EODAD    BAL   R9,RESET
         CLOSE (VSAMACB)
         CLOSE (SORTIN,,KEYOUT)
         LA    R1,SORTCON
         LINK  EP=SORT
         LTR   R15,R15
         BNZ   SORTERR
         OPEN  (SORTOUT,(INPUT),PRINT,(OUTPUT))
         PUT   PRINT,HDR1
         PUT   PRINT,HDR2
GETSORT  GET   SORTOUT,LINE
         PACK  PACKFD,NUMCI+1(6)
         MVC   NUMCI,=X'4020206B202120'
         ED    NUMCI(7),PACKFD+1
         MVC   SAVEFD,PACKFD
         SP    PACKFD,=PL1'1'
         CP    PACKFD,PREVFD
         BE    NOSKIP
         SP    PACKFD,PREVFD
         MVC   SKIPPED,=X'402020202120'
         ED    SKIPPED(6),PACKFD+1
         PUT   PRINT,SKIPLINE
         AP    COUNTER,=PL1'1'
NOSKIP   MVC   PREVFD,SAVEFD
         PUT   PRINT,LINE
         AP    COUNTER,=PL1'1'
         CP    COUNTER,=PL2'60'
         BL    SAMEPG
         PUT   PRINT,HDR1
         PUT   PRINT,HDR2
         MVI   LINE,C'0'
         ZAP   COUNTER,=PL1'0'
SAMEPG   B     GETSORT
EODAD2   EQU   *
         MVC   TOTRECS,=X'40206B2020206B202120'
         ED    TOTRECS(10),ALLREC
         PUT   PRINT,FINAL
         CLOSE (PRINT,,SORTOUT)
         LEAVE EQ
RESET    EQU   *
         ST    R7,TOTREC
         L     R1,LASTCI                PREVIOUS CI
         ST    R3,LASTCI                AND UPDATE
         CVD   R1,CVDAREA
         MVC   NUMCI+1(6),=X'402020202120'
         ED    NUMCI+1(6),CVDAREA+5
         MVC   KEYREC(6),NUMCI+1
         PUT   KEYOUT,KEYREC
         L     R1,RECAREA               ADDRESS OF RECORD
         LTR   R1,R1                    IS THIS EOF
         BZ    SKIPNEXT
         A     R1,RKP
         L     R2,KEYLEN                ADDRESS FOR KEY
         EX    R2,MOVEKEY               MOVE HIGHKEY
         MVC   LOWKEY,HIGHKEY
SKIPNEXT XR    R4,R4                    (R5 HAS TOTAL USED BYTES)
         M     R4,=F'100'
         DR    R4,R6                    % USED
         LTR   R5,R5
         BZ    ENDLOOP
         LA    R1,PERCENT
LOOPER   MVI   0(R1),C'*'
         LA    R1,1(R1)
         BCT   R5,LOOPER
ENDLOOP  L     R1,TOTREC                TOTAL USED
         CVD   R1,CVDAREA
         MVC   TOTAL,=X'4020206B202120'
         ED    TOTAL(7),CVDAREA+5
         PUT   SORTIN,LINE
         MVI   LINE,C' '
         MVC   LINE+1(L'LINE-1),LINE
         LA    R7,1                     RESET RECORDS IN THIS CI
         MVC   PREVLEN,RECLEN
         L     R5,RECLEN
         MVI   HADONE,X'00'            RESET HAD ONE FLAG
         L     R6,MAXPOS
         SH    R6,=H'3'                 GOT FIRST RECORD
         BR    R9
OPENERR  EQU   *
         SHOWCB ACB=VSAMACB,LENGTH=4,AREA=ERROR,FIELDS=(ERROR)
WRITERR  OPEN  (PRINT,OUTPUT)
         L     R1,ERROR
         CVD   R1,CVDAREA
         MVC   ERRMSG(4),=X'40202120'
         ED    ERRMSG(4),CVDAREA+6
         PUT   PRINT,PROCMSG
         CLOSE (PRINT)
         LEAVE CC=8
POINTERR EQU   *
         SHOWCB RPL=RPL1,LENGTH=4,AREA=ERROR,FIELDS=(FDBK)
         MVC   TYPERR,=CL26' POINT FAILED, FEEDBACK='
         B     WRITERR
GETERR   EQU   *
         SHOWCB RPL=RPL1,LENGTH=4,AREA=ERROR,FIELDS=(FDBK)
         MVC   TYPERR,=CL26' GET FAILED,   FEEDBACK='
         B     WRITERR
SORTERR  EQU   *
         ST    R15,ERROR
         MVC   TYPERR,=CL26' SORT FAILED, REGISTER 15='
         B     WRITERR
MVEPSWD  MVC   PASSWORD+1(0),2(R2)     **** EXECUTED ONLY ***
MOVEKEY  MVC   HIGHKEY(0),0(R1)         **** EXECUTED ONLY ***
BYTE0    DC    X'00'
ERROR    DS    F
CVDAREA  DS    D
RECAREA  DS    F
ALLREC   DC    PL4'0'
TOTREC   DS    F
MAXPOS   DS    F
LASTCI   DC    F'0'
INIT     DS    X
CINV     DS    F
LRECL    DS    F
KEYLEN   DS    F
RKP      DS    F
*
RBA      DS    F
RECLEN   DS    F
*
PREVLEN  DC    F'-1'               ASSURE NO MATCH INITIALLY
HADONE   DS    X
PREVFD   DC    PL4'-1'
SAVEFD   DS    CL4
PACKFD   DS    PL4
COUNTER  DC    PL2'0'
PROCMSG  DS    0CL133
TYPERR   DC    CL26' OPEN ERROR RETURN CODE ='
ERRMSG   DS    CL4
         DC    CL103' '
HDR1     DS    0CL133
         DC    CL2'1'
         DC    CL7'CONTROL'
         DC    CL5' '
         DC    CL100'++++0++++1++++1++++2++++2++++3++++3++++4++++4++++5X
               ++++5++++6++++6++++7++++7++++8++++8++++9++++9++++*'
         DC    CL5' '
         DC    CL7'RECORDS'
         DC    CL7' '
HDR2     DS    0CL133
         DC    CL1' '
         DC    CL8'INTERVAL'
         DC    CL5' '
         DC    CL100'++++5++++0++++5++++0++++5++++0++++5++++0++++5++++0X
               ++++5++++0++++5++++0++++5++++0++++5++++0++++5++++*'
         DC    CL5' '
         DC    CL7' '
         DC    CL7' '
FINAL    DS    0CL133
         DC    CL30' TOTAL RECORDS IN FILE'
TOTRECS  DS    CL10
         DC    CL93' '
LINE     DS    0CL133
         DC    CL2' '
NUMCI    DC    CL7' '
         DC    CL5' '
PERCENT  DC    CL100' '
         DC    CL5' '
TOTAL    DS    CL7
         DC    CL7' '
SKIPLINE DS    0CL133
         DC    CL20' <<<<<<<<<<<<<<<<<<<<<'
SKIPPED  DS    CL6
         DC    CL107' CONTROL INTERVAL(S) WITH NO RECORDS >>>>>>>>>>>>'
KEYREC   DS    0CL133
         DC    CL7' '
LOWKEY   DC    CL62' '
         DC    CL1' '
HIGHKEY  DC    CL62' '
         DC    CL1' '
         DS    0F
SORTCON  DC    X'80',AL3(SORTPARM)
         DS    CL2
SORTPARM DC    AL2(SORTLEN)
         DC    A(SORTSTAT)
         DC    A(SORTSTND)
         DC    A(RECSTAT)
         DC    A(RECSTAND)
         DC    A(0)
         DC    A(0)
SORTLEN  EQU   *-SORTPARM-2
SORTSTAT DC    C' SORT FIELDS=(1,9,CH,A)'
SORTSTND DC    C' '
RECSTAT  DC    C' RECORD TYPE=F,LENGTH=133'
RECSTAND DC    C' '
PASSWORD DC    X'08',CL8' '
         DS    0F
EXLST    EXLST  EODAD=EODAD
VSAMACB  ACB   AM=VSAM,DDNAME=VSAMDS,EXLST=EXLST,PASSWD=PASSWORD
RPL1     RPL   ACB=VSAMACB,AREA=RECAREA,AREALEN=4,ARG=BYTE0,KEYLEN=1,  X
               OPTCD=(LOC,GEN,KGE)
SORTOUT  DCB   DDNAME=SORTOUT,LRECL=133,MACRF=GM,DSORG=PS,RECFM=FB,    X
               EODAD=EODAD2
SORTIN   DCB   DDNAME=SORTIN,LRECL=133,MACRF=PM,DSORG=PS,RECFM=FB
PRINT    DCB   DDNAME=PRINT,LRECL=133,MACRF=PM,DSORG=PS
KEYOUT   DCB   DDNAME=KEYOUT,LRECL=133,MACRF=PM,DSORG=PS
         END
