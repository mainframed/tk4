*                                                                    *
*       THIS COMMAND PROCESSOR REQUEST A DATASET AND THEN SEARCHES   *
*       THE QCB CHAIN TO FIND ALL ASIDS THAT ARE ALLOCATED TO IT.    *
*       IT THEN SEARCHES THE ASIDS TO FIND THE ACTUAL JOBNAME OR     *
*       USERID ASSOCIATED IT.                                        *
*                                                                    *
*       WRITTEN BY: RICHARD G NIKULA                                 *
*                   WASHINGTON STATE DEPT. OF TRANSPORTATION         *
*                                                                    *
DSNFIND  ENTER (14,12),12,(,72)
         LR    R10,R1
         USING CPPL,R10            CPPL ADDRESSABILITY
         USING IKJPARMD,R11        PCL  ADDRESSABILITY
*     STUFF FOR PARSE ****
         MVC   PPLUPT,CPPLUPT
         MVC   PPLECT,CPPLECT
         MVC   PPLCBUF,CPPLCBUF
         LA    R1,PPLUPT
         LINK  EP=IKJPARS           GO DO IT
         LTR   R15,R15               ALL GO OK???
         BNZ   PARSERR               NOPE....
         L     R11,ANS              WHERE IS PARSE DSECT??
         L     R3,DSN                ADDRESS OF DATASET NAME
         LH    R4,DSN+4              AND THE LENGTH
      GQSCAN AREA=(WORKAREA,12288),SCOPE=SYSTEM,RESNAME=(SYSDSN,(3),(4)C
               ),REQLIM=1000
         C     R15,=F'4'
         BE    NOTALC
         LTR   R15,R15
         BNZ   SYSERR
         LA    R2,WORKAREA
         USING RIB,R2
         CLC   RIBTRIBE,RIBNRIBE    WAS AREA BIG ENOUGH TO GET ALL ?
         BE    ALLFOUND             YES
         TPUT  OVERFLOW,L'OVERFLOW
ALLFOUND EQU   *
         LA    R10,LINE             RESET OUTPUT LINE POINTER
         LH    R1,RIBVLEN           LENGTH OF VARIABLE SECTION
         LA    R2,RIBEND(R1)        TO GET TO RIBE
         DROP  R2
         USING RIBE,R2
         TM    RIBERFLG,RIBETYPE    TEST FOR TYPE OF REQUEST
         BO    SHARED                 X'80' --> SHARED
         MVC   HEADER+18(3),=C'OLD' SHOW MESSAGE AS HAS IT OLD
SHARED   EQU   *
         TPUT  HEADER,L'HEADER      AND PUT HEADING LINE
ASLOOP   MVC   5(8,R10),RIBEJBNM    COPY JOBNAME TO PRINT LINE
         CLI   RIBEJBNM+3,C' '      JOB COULDN'T HAVE BLANK HERE
         BE    TSOSES               BUT TSO SESSION WOULD
         MVC   0(3,R10),=C'JOB'
         B     DISPLAY
TSOSES   MVC   0(4,R10),=C'USER'
DISPLAY  MVI   4(R10),C'-'           ISN'T THIS PRETTY
         LA    R10,18(R10)          INCREMENT POINTER REGISTER
         AP    COUNT,=PL1'1'        FOUR PER LINES FOR ALLOCATIONS
         CP    COUNT,=PL1'4'        IS PRETTIER
         BNE   NEXTASID
         TPUT  LINE,L'LINE
         LA    R10,LINE             RESET POINTER
         ZAP   COUNT,=PL1'0'
         MVI   LINE,C' '
         MVC   LINE+1(L'LINE-1),LINE         CLEAR THE LIN
NEXTASID LA    R2,RIBEEND-RIBE(R2)    NEXT ENTRY
         CLC   RIBEJBNM,=8X'FF'       FOXESS MEANS END
         BE    LASTLN
         CLI   WFLAG,C'Y'            HAS WAIT MESSAGE BEEN WRITTEN
         BE    ASLOOP
         TM    RIBESFLG,RIBESTAT     IS THIS GUY WAITING?
         BZ    PUTWTG               YES, THEN ALL LOWER TASKS WAITING
         B     ASLOOP                NO, SO FAR EVERYTHING SHARING IT
PUTWTG   TPUT  LINE,L'LINE
         MVI   WFLAG,C'Y'            SHOW THAT NO NEED TO CHECK WAITING
         LA    R10,LINE             RESET POINTER
         ZAP   COUNT,=PL1'0'
         MVI   LINE,C' '
         MVC   LINE+1(L'LINE-1),LINE         CLEAR THE LIN
         TPUT  WAITING,L'WAITING    TELL THEM THE REST DON'T HAVE IT
         B     ASLOOP
LASTLN   TPUT  LINE,L'LINE
BYEBYE   LEAVE EQ,CC=0
SYSERR   TPUT  MESSAGE,L'MESSAGE
         B     RET04
NOTALC   TPUT  MESS2,L'MESS2
         B     BYEBYE
PARSERR  TPUT  MESS3,L'MESS3
         B     RET04
RET04    LEAVE CC=4
         LTORG
WFLAG    DC    C'N'
LINE     DC    CL76' '
COUNT    DC    PL2'0'
WAITING  DC    C'FOLLOWING ARE WAITING FOR DATASET:'
HEADER   DC    C'DATASET ALLOCATED SHR BY:'
MESSAGE  DC    C'SYSTEM ERROR, REENTER COMMAND'
MESS2    DC    C'DATA SET NOT ALLOCATED'
MESS3    DC    C'PARSE ERROR, REENTER COMMAND'
OVERFLOW DC    C'TOO MANY ALLOCATIONS ON DATASET - TABLE OVERFLOW'
PPLUPT   DC    A(0)
PPLECT   DC    A(0)
PPLECB   DC    A(PPLECBS)
PPLPCL   DC    A(PARM)
PPLANS   DC    A(ANS)
PPLCBUF  DC    A(0)
PPLUWA   DC    A(0)
PPLECBS  DC    A(0)
ANS      DC    A(0)
SYSDSN   DC    CL8'SYSDSN'
WORKAREA DC    12288X'FF'         ENOUGH FOR LOTS OF ALLOCATIONS
PARM     IKJPARM
DSN      IKJPOSIT DSNAME,PROMPT='DATA SET NAME',USID
         IKJENDP
         IKJCPPL
         ISGRIB
         END
