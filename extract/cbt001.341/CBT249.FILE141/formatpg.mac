FORMATPG ENTER (14,12),(3,4,5),(,72)
*
*  THIS PROGRAM WILL DETERMINE THE SIZE OF A FILE POINTED TO BY THE
*  FILE1 DD STATEMENT AND FILL IT WITH RECORDS CONTAINING ONLY DOLLAR
*  SIGNS. (VARIABLE RECORDS WILL HAVE REC DISCRIPTOR WORD FILLED IN)
*
         OPEN  (FILE1,OUTPUT)
         RDJFCB (FILE1)
         OBTAIN FORMAT4
         LTR   R15,R15
         BZ    AA
         BAL   R14,BLOWUP
AA       EQU   *
         LA    R15,TBL3350         DEFAULT TO A 3350
         MVC   TRKSPER,VTOCIN+20   # OF TRKS PER CYL
         CLC   TRKSPER,=H'15'      IS IT A  3380?
         BH    GETATR              NO, WE'RE ALREADY SET
         LA    R15,TBL3380         GET ADDR OF 3380 TABLE
GETATR   EQU   *
         ST    R15,TABLADDR        STORE CORRECT TABLE ADDR
         OBTAIN FORMAT1            GET DSCB 1 FOR OUR DATA-SET
         LTR   R15,R15             DID WE GET IT
         BZ    AB
         BAL   R14,BLOWUP
AB       EQU   *
         SR    R11,R11             CLEAR A COUNTER
         LA    R6,3                3 EXTENTS DISCRIBED IN FMT1
         LA    R14,VTOCIN+105      GET ADDR OF 1ST ONE
*
*  THIS SECTION WILL FIND THE NUMBER OF TRACKS ALLOCATED
*  TO THIS DATA SET
*
EXTLOOP  EQU   *
         CLI   0(R14),X'00'        DOES THIS ONE DISCRIBE AN EXTENT
         BE    EXTDONE             NO, WE'RE DONE
         CLI   0(R14),X'F3'        IS THIS THAT ODD BYTE IN FMT3?
         BNE   AROUND              NO, SKIP NEXT INSTRUCTION
         LA    R14,1(R14)          YES, SKIP OVER IT
AROUND   EQU   *
         LH    R1,6(R14)           GET CYL PART OF END ADDR
         MH    R1,TRKSPER          MULT BY # TRKS PER CYL
         AH    R1,8(R14)           ADD TRK PORTION OF END ADDR
         LH    R2,2(R14)           GET CYL PART OF START ADDR
         MH    R2,TRKSPER          MULT BY # TRKS PER CYL
         AH    R2,4(R14)           ADD TRK PORTION OF START ADDR
         SR    R1,R2               GET # OF TRKS IN EXT MINUS 1
         LA    R1,1(,R1)           ADD 1
         AR    R11,R1              ADD TO OVER-ALL TOTAL
         LA    R14,10(R14)         GET ADDR OF NEXT EXT
         BCT   R6,EXTLOOP          DO DESIRED NUMBER OF TIMES
         CLI   VTOCIN,X'F3'        DID WE JUST DO THE FMT3?
         BE    EXTDONE             YES, WE'RE DONE
         CLC   VTOCIN+135(5),=XL5'00' IS THERE ONE?
         BE    EXTDONE             NO, WE'RE DONE
         MVC   TRKADDR,VTOCIN+135  MOVE IT'S ADDRESS
         OBTAIN FORMAT3            GET IT
         LTR   R15,R15             GET IT OR?
         BZ    AC
         BAL   R14,BLOWUP
AC       EQU   *
         LA    R14,VTOCIN+4        YES, GET ADDR OF 1ST DISC
         LA    R6,13               THERE ARE 13 OF THEM
         B     EXTLOOP             GO DO THEM
EXTDONE  EQU   *
         LH    R8,JFCB+102         PICK UP BLOCK SIZE
         TM    JFCB+100,X'C0'      IS THIS RECFM=U
         BNO   AD
         BAL   R14,BLOWUP
AD       EQU   *
         TM    JFCB+100,X'40'      VARIABLE ?
         BNO   GETCOUNT            NO, MUST BE FIXED
         STH   R8,BLKSIZE          YES, SET UP VARIABLE
         MVC   BLKSIZE+2(2),=H'0'
         SH    R8,=H'4'            SUBTRACT FOR RECORD LENGTH
         STH   R8,RECSIZE          RECORD
         MVC   RECSIZE+2(2),=H'0'
         LA    R8,4(,R8)          ADD 4 BACK IN FOR LATER
GETCOUNT EQU   *
         L     R10,TABLADDR        YES GET ADDR OF TABLE
         B     TBLSRCH
*
*  THIS CODE WILL COMPARE BLKSIZE TO DISK TABLE TO DETERMINE
*  HOW MANY OF THESE BLOCKS WILL FIT ON A TRACK. THIS NUMBER
*  IS THEN MULTIPLIED BY NUMBER OF TRACKS TO GET NUMBER OF RECORDS
*  THIS PROGRAM HAS TO WRITE
*
TBLSRCH  EQU   *
         CH    R8,4(R10)           BLKSIZE LARGER THAN NEXT ENTRY
         BH    GOTCNT              YES, WE'RE DONE
         CLC   4(4,R10),=X'FFFFFFFF' END OF TABLE?
         BE    GOTCNT              YES, WE'RE DONE
         LA    R10,4(R10)          NO, GET NEXT ENTRY
         B     TBLSRCH             GO CHECK IT
GOTCNT   EQU   *
         LH    R14,2(R10)          GET # OF RECORD THAT WILL FIT
         SR    R10,R10             CLEAR EVEN REG
         MR    R10,R14             MULTIPLY # TRKS X BLKS PER TRK
*                                  GIVING # OF BLKS TO BE WRITTEN
DOWRITE  EQU   *
         WRITE FECB,SF,FILE1,RECORD,'S'
         CHECK FECB
         BCT   R11,DOWRITE
         CLOSE (FILE1)
         OPEN  (FILE1,OUTPUT)
         CLOSE (FILE1)      NULL THE FILE
         LEAVE EQ
BLOWUP   DC    F'0'
ABSADR   DS    F
GETASAVE DS    F
FORMAT1  CAMLST SEARCH,DSNAME,VOLSER,VTOC2
VTOCIN   DS    0CL140
OFOURS   DC    44XL1'04'
VTOC2    DS    CL96
FORMAT3  CAMLST SEEK,TRKADDR,VOLSER,VTOCIN
TRKADDR  DC    CL5' '
TRKSPER  DC    H'00'
FORMAT4  CAMLST SEARCH,OFOURS,VOLSER,VTOCIN
FILE1    DCB   DDNAME=FILE1,MACRF=(W),DSORG=PS,EXLST=AJ
AJ       DC    X'87',AL3(JFCB)
JFCB     DS    0CL176
DSNAME   DS    CL44
         DS    CL74
VOLSER   DS    CL6
         DS    CL52
TABLADDR DS    F
TBL3350  DS    0CL412  NUMBER OF RECORDS PER TRK BY BLKSIZE
         DC    H'19069',H'1'
         DC    H'09442',H'2'
         DC    H'06233',H'3'
         DC    H'04628',H'4'
         DC    H'03665',H'5'
         DC    H'03024',H'6'
         DC    H'02565',H'7'
         DC    H'02221',H'8'
         DC    H'01954',H'9'
         DC    H'01740',H'10'
         DC    H'01565',H'11'
         DC    H'01419',H'12'
         DC    H'01296',H'13'
         DC    H'01190',H'14'
         DC    H'01098',H'15'
         DC    H'01018',H'16'
         DC    H'00947',H'17'
         DC    H'00884',H'18'
         DC    H'00828',H'19'
         DC    H'00777',H'20'
         DC    H'00731',H'21'
         DC    H'00690',H'22'
         DC    H'00652',H'23'
         DC    H'00617',H'24'
         DC    H'00585',H'25'
         DC    H'00555',H'26'
         DC    H'00528',H'27'
         DC    H'00502',H'28'
         DC    H'00478',H'29'
         DC    H'00456',H'30'
         DC    H'00436',H'31'
         DC    H'00416',H'32'
         DC    H'00398',H'33'
         DC    H'00381',H'34'
         DC    H'00365',H'35'
         DC    H'00349',H'36'
         DC    H'00335',H'37'
         DC    H'00321',H'38'
         DC    H'00308',H'39'
         DC    H'00296',H'40'
         DC    H'00284',H'41'
         DC    H'00273',H'42'
         DC    H'00262',H'43'
         DC    H'00252',H'44'
         DC    H'00242',H'45'
         DC    H'00233',H'46'
         DC    H'00224',H'47'
         DC    H'00216',H'48'
         DC    H'00207',H'49'
         DC    H'00200',H'50'
         DC    H'00192',H'51'
         DC    H'00185',H'52'
         DC    H'00178',H'53'
         DC    H'00171',H'54'
         DC    H'00165',H'55'
         DC    H'00158',H'56'
         DC    H'00152',H'57'
         DC    H'00146',H'58'
         DC    H'00141',H'59'
         DC    H'00135',H'60'
         DC    H'00130',H'61'
         DC    H'00125',H'62'
         DC    H'00120',H'63'
         DC    H'00115',H'64'
         DC    H'00111',H'65'
         DC    H'00106',H'66'
         DC    H'00102',H'67'
         DC    H'00098',H'68'
         DC    H'00094',H'69'
         DC    H'00090',H'70'
         DC    H'00086',H'71'
         DC    H'00082',H'72'
         DC    H'00078',H'73'
         DC    H'00075',H'74'
         DC    H'00071',H'75'
         DC    H'00068',H'76'
         DC    H'00065',H'77'
         DC    H'00061',H'78'
         DC    H'00058',H'79'
         DC    H'00055',H'80'
         DC    H'00052',H'81'
         DC    H'00049',H'82'
         DC    H'00046',H'83'
         DC    H'00044',H'84'
         DC    H'00041',H'85'
         DC    H'00038',H'86'
         DC    H'00036',H'87'
         DC    H'00033',H'88'
         DC    H'00031',H'89'
         DC    H'00028',H'90'
         DC    H'00026',H'91'
         DC    H'00024',H'92'
         DC    H'00022',H'93'
         DC    H'00019',H'94'
         DC    H'00017',H'95'
         DC    H'00015',H'96'
         DC    H'00013',H'97'
         DC    H'00011',H'98'
         DC    H'00009',H'99'
         DC    H'00007',H'100'
         DC    H'00005',H'101'
         DC    H'00003',H'102'
         DC    H'00001',H'103'
         DC    X'FFFFFFFF'         END OF TABLE
TBL3380  DS    0CL412  NUMBER OF RECORDS PER TRK BY BLKSIZE
         DC    H'32767',H'1'
         DC    H'23476',H'2'
         DC    H'15476',H'3'
         DC    H'11476',H'4'
         DC    H'09076',H'5'
         DC    H'07476',H'6'
         DC    H'06356',H'7'
         DC    H'05492',H'8'
         DC    H'04820',H'9'
         DC    H'04276',H'10'
         DC    H'03860',H'11'
         DC    H'03476',H'12'
         DC    H'03188',H'13'
         DC    H'02932',H'14'
         DC    H'02676',H'15'
         DC    H'02484',H'16'
         DC    H'02324',H'17'
         DC    H'02164',H'18'
         DC    H'02004',H'19'
         DC    H'01876',H'20'
         DC    H'01780',H'21'
         DC    H'01684',H'22'
         DC    H'01588',H'23'
         DC    H'01492',H'24'
         DC    H'01396',H'25'
         DC    H'01332',H'26'
         DC    H'01268',H'27'
         DC    H'01204',H'28'
         DC    H'01140',H'29'
         DC    H'01076',H'30'
         DC    H'01044',H'31'
         DC    H'00980',H'32'
         DC    H'00948',H'33'
         DC    H'00916',H'34'
         DC    H'00852',H'35'
         DC    H'00820',H'36'
         DC    H'00788',H'37'
         DC    H'00756',H'38'
         DC    H'00724',H'39'
         DC    H'00692',H'40'
         DC    H'00660',H'41'
         DC    H'00628',H'42'
         DC    H'00596',H'44'
         DC    H'00564',H'45'
         DC    H'00532',H'46'
         DC    H'00500',H'48'
         DC    H'00468',H'49'
         DC    H'00436',H'51'
         DC    H'00404',H'53'
         DC    H'00372',H'55'
         DC    H'00340',H'57'
         DC    H'00308',H'59'
         DC    H'00276',H'62'
         DC    H'00244',H'65'
         DC    H'00212',H'68'
         DC    H'00180',H'71'
         DC    H'00148',H'74'
         DC    H'00116',H'78'
         DC    H'00084',H'83'
         DC    H'00052',H'88'
         DC    H'00020',H'93'
         DC    X'FFFFFFFF'         END OF TABLE
         LTORG
         DS    0H
RECORD   DS    0CL47476
BLKSIZE  DC    CL2'$$$$'
         DC    CL2'$$$$'
RECSIZE  DC    CL2'$$$$'
         DC    47470C'$'
         END
