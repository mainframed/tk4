         TITLE 'PRINTDSY  RELEASE 2.0  10/09/79  M.J.SPRINZEN'
*
PRINTDSY CSECT
*
* FUNCTIONS:
*  1-THIS ROUTINE IS CALLED BY PRINTDS TO CHECK FOR VALID UCS OR FCB
*    SPECIFICATIONS. IT DYNAMICALLY ALLOCATES 'SYS1.IMAGELIB' AND DOES
*    A FIND FOR THE SPECIFIED UCS OR FCB. A TPUT IS DONE IF MEMBER
*    NOT FOUND AND RETURN WITH CC=12 IS PERFORMED.
*  2-IF FORMS,UCS, OR FCB IS SPECIFIED, THIS ROUTINE WILL ASSURE THE
*    SYSOUT CLASS WAS ENTERED. IF IT WAS NOT, THE USER WILL BE PROMPTD
*    FOR A VALID SYSOUT CLASS. THIS IS DONE SINCE IT IS ASSUMMED THAT
*    SPECIAL OPTIONS REQUIRE SPECIAL CLASSES. (NOTE: IF JUST FORMS IS
*    SPECIFIED, 'SYS1.IMAGELIB' IS STILL ALLOC BUT NOT USED - BIG
*    DEAL.)
*
* PARAMETERS PASSED:
*  REGISTER 1 UPON ENTRY POINTS TO ADDRESSES OF:
*    1- IOPL
*    2- FCBPDE  (PARSE PDE FOR FCB)
*    3- CHAPDE  (  "    "   "  CHARS)
*    4- CLAPDE  (  "    "   "  CLASS)
*    5- DESPDE  (  "    "   "  DEST)
*
         SPACE 1
SP1      EQU   7              SUBPOOL FOR GETMAINS
         SPACE 1
         SAVE  (14,12),,*     SAVE REGS
         LR    12,15          ESTABLISH ADDRESSABILITY
         USING PRINTDSY,12
* SAVE PARM PTR IN R9
         LR    9,1
         B     AROUND
         SPACE 2
* DEFAULT FIRST 4 CHARS FOR FCB OR UCS >>>> CAN CHANGE AT ANYTIME <<<<
U3800    DC    CL8'XTB1'      3800 UCS
F3800    DC    CL8'FCB3'      3800 FCB
U3211    DC    CL8'UCS2'      3211 UCS
F3211    DC    CL8'FCB2'      3211 FCB
         SPACE 2
* GETMAIN FOR REENT
AROUND   GETMAIN R,LV=DLENGTH,SP=SP1
         ST    13,4(1)        SAVE R13 INTO NEW SAVEAREA
         ST    1,8(0,13)      STORE NEW SAVEAREA ADDR INTO OLD
         LR    13,1           R1 POINTS TO NEW SAVE (BEG DSECT1)
         USING DSECT1,13      ADDRES. FOR DSECT1 (CONTAINS SAVEAREA)
         EJECT
*
*  INITIALIZE GETMAINED AREA
*
         SPACE 1
* MOVE IN DCB
         MVC   DCB(DCBL),DCB2
         SPACE 1
* MOVE IN OPEN & CLOSE MACROS AND INIT.
         MVC   OPENIT(4),OPENIT2
         MVC   CLOSEIT(4),CLOSEIT2
         LA    3,DCB
         STCM  3,B'0111',OPENIT+1
         STCM  3,B'0111',CLOSEIT+1
         SPACE 1
* MOVE IN PUTMSG
         MVC   PUTMSG(4),PUTMSG2
         SPACE 1
* INIT ECBS
         SR    3,3
         ST    3,ECBPUT       PUTLINE ECB
         ST    3,ECBPG        PUTGET ECB
         STC   3,FLAGS        ZERO OUT FLAGS BYTE
         SPACE 1
* INIT IOPL AND PUTLINE STUFF
         MVC   PUTLIST(PUTLISTL),PUTLIST2
         LA    3,PUTMSG
         ST    3,PUTLIST+4
         LA    3,PUTLIST
         ST    3,IOPL+12     IOPL NEEDS ADDR OF LIST FORM
         LA    3,ECBPUT
         ST    3,IOPL+8      IOPL ALSO NEEDS ECB ADDRESS
         SPACE 1
* INIT IOPLX AND PUTGET STUFF
         MVC   PGLIST(PGLISTL),PGLIST2
         LA    3,PGLIST
         ST    3,IOPLX+12    IOPL MUST POINT TO PGPB (PUTGET PARM LIST)
         LA    3,ECBPG
         ST    3,IOPLX+8     IOPL ALSO NEEDS ECB ADDRESS
*        SETUP FIRST OLD FOR MULTILEVEL MESSAGE
         LA    3,1
         ST    3,OLD1+4     INDICATE ONLY 1 MESSAGE IN OLD
         LA    3,PUTMSG
         ST    3,OLD1+8     POINT OLD TO MESSAGE
         LA    3,OLD1
         ST    3,PGLIST+4
*        SETUP SECOND OLD
         LA    3,OLD2
         ST    3,OLD1        CHAIN OLDS
         SR    3,3
         ST    3,OLD2        INDICATE LAST OLD
         LA    3,1
         ST    3,OLD2+4      ONLY 1 MESSAGE
         LA    3,MULTIMSG
         ST    3,OLD2+8      POINT TO MESSAGE
         SPACE 1
* INIT FIND MEMBER NAMES
         MVC   FINDCHA(8),U3800  ONLY 3800 UCS (CHARS) ALLOWED
         MVC   FINDFCB(8),F3800  ONLY 3800 FCB ALLOWED
         EJECT
*
*        GET PARMS
*
         L     2,0(9)
         MVC   IOPL(8),0(2)  MOVE IN FIRST 8 BYTES
         MVC   IOPLX(8),0(2) MOVE IN FIRST 8 BYTES
         L     2,4(9)        ADDRESS OF 2ND   PARM
         ST    2,AFCBPDE
         L     2,8(9)        ADDRESS OF 3RD   PARM
         ST    2,ACHAPDE
         L     2,12(9)       ADDRESS OF 4TH   PARM
         ST    2,ACLAPDE
         L     2,16(9)       ADDRESS OF 5TH   PARM
         ST    2,ADESPDE
         EJECT
*
*       NOW DYNAMICALLY ALLOCATE THE INPUT DATASET
*
         LA    8,107       4+20+12 + 50+7+14
         GETMAIN R,LV=(8),SP=SP1   GET 107 BYTES
         LR    8,1
         USING S99RBP,8       ADDRESSABILITY FOR 'RBPTR' DSECT
         LA    4,S99RBPTR+4   4 BYTES BEYOND 'RBPTR'
         USING S99RB,4        ADDRESSABILITY FOR 'RB' DSECT
         ST    4,S99RBPTR     MAKE 'RBPTR' POINT TO 'RB'
         OI    S99RBPTR,S99RBPND  TURN ON HIGH ORDER BIT IN 'RBPTR'
         XC    S99RB(RBLEN),S99RB ZERO OUT 'RB' ENTIRELY
         MVI   S99RBLN,RBLEN   PUT IN LENGTH OF 'RB' IN ITS LENGTH FLD
         MVI   S99VERB,S99VRBAL  SET VERB CODE FIELD TO ALLOC FUNCTION
         LA    5,S99RB+RBLEN   POINT 20 BYTES BEYOND START OF 'RB'
         USING S99TUPL,5       ESTAB. ADDRESSABILITY FOR TEXT UNIT PTRS
         ST    5,S99TXTPP      INIT. THE TEXT PTRS. ADDRESS IN 'RB'
*
         LA    6,S99TUPL+20    POINT JUST PAST 5 TEXT UNIT PTR'S
         USING S99TUNIT,6      SET ADDRESSABILITY FOR 1ST TEXT UNIT
         ST    6,S99TUPTR      POINT 1ST TEXT UNIT PTR TO 1ST TEXT UNIT
*
         LA    7,DALDSNAM      GET KEY FOR DSNAME
         STH   7,S99TUKEY      PUT KEY IN TEXT UNIT KEY FIELD
         LA    7,1             # OF PARMS
         STH   7,S99TUNUM      PUT # OF PARMS
         LA    7,L'DSN         LOAD LENGTH OF DSNAME FOR MVC
         STH   7,S99TULNG      PUT LENGTH OF PARM
         BCTR  7,0             DEC LENGTH BY 1 FOR MVC
         EX    7,MVCDSN        MOVE DSNAME INTO PARM FIELD
         B     CONT1
MVCDSN   MVC   S99TUPAR(0),DSN MVC FOR EX INSTRUCTION
CONT1    EQU   *
*
         LA    6,S99TUNIT+50   POINT JUST PAST 1ST TEXT UNIT(ASSUME 50)
         LA    5,S99TUPL+4     POINT TO 2ND TUP IN LIST
         ST    6,S99TUPTR      POINT 2ND TUP TO 2ND TEXT UNIT
*
         LA    7,DALSTATS      GET KEY FOR DATASET STATUS
         STH   7,S99TUKEY      PUT KEY IN TEXT UNIT KEY FIELD
         LA    7,1             # OF PARMS
         STH   7,S99TUNUM      PUT # OF PARMS
         STH   7,S99TULNG      SET PARM LENGTH ALSO TO 1
         MVI   S99TUPAR,X'08'  INDICATE SHARE OPTION
*
         LA    6,S99TUNIT+7    POINT JUST PAST 2ND TEXT UNIT
         LA    5,S99TUPL+4     POINT TO 3RD TUP IN LIST
         ST    6,S99TUPTR      POINT 3RD TUP TO 3RD TEXT UNIT
         OI    S99TUPTR,S99TUPLN INDICATE LAST PTR
*
         LA    7,DALRTDDN      GET KEY FOR RETURN DDNAME
         STH   7,S99TUKEY      PUT KEY IN TEXT UNIT KEY FIELD
         LA    7,1             # OF PARMS
         STH   7,S99TUNUM      PUT KEY
         LA    7,8             LENGTH OF PARM - FIX TO 8 BYTES
         STH   7,S99TULNG
*
         LA    7,S99TULNG      LOAD ADDR. OF LENGTH OF PARM
         ST    7,LNGADDR        TO USE LATER - FOR MVC FOR DDN
*
         LA    7,S99TUPAR      LOAD ADDR. OF PARM FIELD
         ST    7,DDNADDR        TO USE LATER - DDNAME FIELD.
*
         LR    1,8       PUT RB PTR ADR IN R1 FOR DYNALLOC
         DYNALLOC        INVOKE DYNALLOC ROUTINE
         LTR   15,15     CHK DYNALLOC RETURN CODE
         BNZ   ERRDYN
*
         DROP  4,5,6,8   NO LONGER NEED REGS FOR THIS DYNALLOC
         EJECT
*
*        MOVE DDNAME RETRND BY DYNALLOC INTO DDNAME FOR INPUT
*
         MVC   DDNAME(8),=CL8' ' FIRST INIT FIELD
         L     2,LNGADDR  LOAD ADDRESS OF LENGTH OF DDN FOR MVC
         LH    7,0(2)     LOAD LENGTH
         BCTR  7,0        SUBTRACT 1 FOR EX MVC
         L     3,DDNADDR  LOAD ADDRESS OF DDNAME FIELD
         EX    7,MVCDD
         B     CONT2
MVCDD    MVC   DDNAME(0),0(3) MOVE DDNAME INTO DDNAME FIELD IN DCB
CONT2    EQU   *
         EJECT
*        ALLOCATION COMPLETED SUCCESSFULLY.
*        NOW MOVE DDNAME INTO DCB AND OPEN.
         SPACE 1
         USING IHADCB,4
         LA    4,DCB
         MVC   DCBDDNAM(8),DDNAME      MOVE  DDNAME
         DROP  4
         SPACE 2
         OPEN  ,MF=(E,OPENIT)
         LTR   15,15      CHK RETURN CODE
         BNZ   ERROPEN
         EJECT
*        CHECK IF EITHER CHARS OF FCB SPECIFIED.
*        NOTE: IF FORMS SPEC, ROUTINE ONLY ASSURES VALID SYSOUT CLASS
         SPACE 1
*        SEE IF CHARS SPEC
         L     7,ACHAPDE  GET ADDR OF PDE
         TM    6(7),X'80' PRESENT
         BZ    CONTF2     NOPE
         LH    6,4(7)     GET LENGTH
         BCTR  6,0        DECR BY 1 FOR MVC
         L     7,0(7)     GET ADDRESS OF ENTRY
         EX    6,MVCCHA   MOVE
         B     CONTF1
MVCCHA   MVC   FINDCHA+4(0),0(7)
CONTF1   EQU   *
*        DO FIND
         LA   7,FINDCHA
         FIND DCB,(7),D
CHECKC1  B     CHECKC1+4(15)
         B     CONTF2
         B     NOTF1
         B     IOERR
         B     NOVS
CONTF2   EQU   *
         SPACE 2
*        SEE IF FCB   SPEC
         L     7,AFCBPDE  GET ADDR OF PDE
         TM    6(7),X'80' PRESENT
         BZ    CONTF5     NOPE
         LH    6,4(7)     GET LENGTH
         BCTR  6,0        DECR BY 1 FOR MVC
         L     7,0(7)     GET ADDRESS OF ENTRY
         EX    6,MVCFCB   MOVE
         B     CONTF4
MVCFCB   MVC   FINDFCB+4(0),0(7)
CONTF4   EQU   *
*        DO FIND
         LA   7,FINDFCB
         FIND DCB,(7),D
CHECKC2  B     CHECKC2+4(15)
         B     CONTF5
         B     NOTF2
         B     IOERR
         B     NOVS
CONTF5   EQU   *
         EJECT
*        ASSURE VALID SYSOUT CLASS AND DEST
         SPACE 2
*        CHECK CLASS FIRST
         L     7,ACLAPDE      SEE IF CLASS ENTERED
         TM    6(7),X'80'     ENTERED?
         BO    CONT6          YES
*        OTHERWISE DO PUTGET FOR CLASS
         MVC   PUTTEXT(70),CLASSMSG
         PUTGET MF=(E,IOPLX)
         LA    4,PGLIST       FOR PUTGET
         USING PGPB,4
         L     9,PGPBIBUF     GET ADDRESS OF INPUT BUFFER
         DROP  4
         CLC   0(2,9),=H'5'   ONLY 1 CHAR ENTERED ?
         BNE   ILLCLASS       NO.
         OI    4(9),X'40'     TRANSLATE TO UPPER CASE
         SPACE 1
*        SETUP PDE AS IF ENTERED  (R9 POINTS TO PGPBIBUF)
         L     7,ACLAPDE      GET CLASS PDE
         OI    6(7),X'80'     TURN ON PRESENT BIT
         LA    8,1
         STH   8,4(7)         STORE LENGTH OF 1
         LA    8,4(9)         LOAD ADDRESS OF CHAR ENTERED BY PUTGET
         ST    8,0(7)         STORE POINTER TO CLASS IN PDE
CONT6    EQU   *
         SPACE 2
*        CHECK DESTINATION
         L     7,ADESPDE
         TM    6(7),X'80'     ENTERED ?
         BZ    CONT7          NO - O.K.
*        OTHERWISE SEE IF IT IS "LOCAL"
*        LH    8,4(7)         LOAD LENGTH                  SCL 01/30/80
*        CH    8,=H'5'        5 BYTES LONG ?               SCL 01/30/80
*        BNE   ERRDEST                                     SCL 01/30/80
         L     9,0(7)         PTR TO FIELD                 SCL 01/30/80
         CLC   0(5,9),=CL5'LOCAL' EQUAL                    SCL 01/30/80
         BE    CONT7                                       SCL 01/30/80
         CLC   0(5,9),=CL5'FICHE' EQUAL                    SCL 01/30/80
         BE    CONT7                                       SCL 01/30/80
         CLC   0(3,9),=CL3'COM'   EQUAL                    SCL 01/30/80
         BE    CONT7                                       SCL 01/30/80
         CLC   0(2,9),=CL5'U4'    EQUAL                    SCL 01/30/80
         BNE   ERRDEST                                     SCL 01/30/80
CONT7    EQU   *
         SR    15,15          INDICATE RC=0
         SPACE 2
CLOSEND  LR    8,15           SAVE RC IN R8
*        SPECIFY FREE SINCE THIS IS A SYSTEM DATASET
         CLOSE (,FREE),MF=(E,CLOSEIT)
         LTR   15,15      CHK RETURN CODE
         BNZ   ERRCLOSE
         SPACE 2
         LR    15,8           RESTORE RC
*        ENTRY HERE FOR ERRORS ALSO
         SPACE 1
END      L     13,SAVEAREA+4
         RETURN (14,12),RC=(15)
         EJECT
*        ERROR MESSAGES
         SPACE 1
NOTF1    TM    FLAGS,X'40'    3800 & 3211 TRIED ?
         BO    CONTN1         YES
*  OTHERWISE, MOVE IN 3211 CHARS
         OI    FLAGS,X'40'    TURN ON SECOND TRY INDICATOR
         MVC   FINDCHA(4),U3211 FOR NEXT TRY
         B     CONTF1         RETURN
CONTN1   MVC   PUTTEXT,ERRMSG01
         MVC   PUTTEXT+33(4),FINDCHA+4
         B     CLOSERR
         SPACE 1
NOTF2    TM    FLAGS,X'20'    3800 & 3211 TRIED ?
         BO    CONTN2         YES
*  OTHERWISE, MOVE IN 3211 FCB
         OI    FLAGS,X'20'    TURN ON SECOND TRY INDICATOR
         MVC   FINDFCB(4),F3211 FOR NEXT TRY
         B     CONTF4         RETURN
CONTN2   MVC   PUTTEXT,ERRMSG02
         MVC   PUTTEXT+33(4),FINDFCB+4
         B     CLOSERR
         SPACE 1
IOERR    MVC   PUTTEXT,ERRMSG9
         B     CLOSERR
NOVS     MVC   PUTTEXT,ERRMSG10
         B     CLOSERR
ILLCLASS MVC   PUTTEXT,ERRMSG12
         B     CLOSERR
ERROPEN  MVC   PUTTEXT,ERRMSG1
         B     TPUTIT
ERRDEST  MVC   PUTTEXT,ERRMSG11
         B     CLOSERR
ERRCLOSE MVC   PUTTEXT,ERRMSG3
         B     TPUTIT
ERRDYN   MVC   PUTTEXT,ERRMSG5
         USING S99RB,4 WAS DROPPED BUT IS STILL VALID ADDRESS.
*BELOW WILL GIVE EXACT RETURN CODE FROM DYNALLOC
         LH    7,S99ERROR LOAD R7 WITH ANY POSS. ERROR CODE
         CH    7,=X'1708' LOCATE ERROR
         BNE   NOLOCERR
         MVC   PUTTEXT,ERRMSG6
NOLOCERR CH    7,=X'0210' DATASET ALLOCTED ELSEWHERE
         BNE   NOTALLOC
         MVC   PUTTEXT,ERRMSG7
NOTALLOC CH    7,=X'0218' SPECIFIED VOLUME NOT MOUNTED
         BNE   TPUTIT
         MVC   PUTTEXT,ERRMSG8
         B     TPUTIT
         SPACE 2
CLOSERR  OI    FLAGS,X'80'    INDICATE CLOSE MUST BE DONE
         SPACE 2
TPUTIT   PUTLINE MF=(E,IOPL)
         SPACE 1
         LA    15,12          SET RETURN CODE
         TM    FLAGS,X'80'    MUST DO CLOSE ?
         BO    CLOSEND
         B     END
         EJECT
*        STORAGE AREAS        1         2         3         4         5
*                   012345678901234567890123456789012345678901234567890
ERRMSG01 DC    CL70'ERROR - INVALID CHARS SPECIFIED (XXXX) - REQUEST CAX
               NCELLED'
ERRMSG02 DC    CL70'ERROR - INVALID  FCB  SPECIFIED (XXXX) - REQUEST CAX
               NCELLED'
ERRMSG1  DC    CL70' OPEN ERROR - IMAGELIB DATASET'
ERRMSG3  DC    CL70' CLOSE ERROR - IMAGELIB DATASET'
ERRMSG5  DC    CL70'ERROR WITH DYNAMIC ALLOC OF IMAGELIB DATASET'
ERRMSG6  DC    CL70'ERROR - IMAGELIB DATASET NOT IN CATALOG'
ERRMSG7  DC    CL70'ERROR - IMAGELIB DATASET ALLOCATED ELSEWHERE'
ERRMSG8  DC    CL70'VOLUME NOT MOUNTED FOR IMAGELIB DATASET - PRINTDSY'
ERRMSG9  DC    CL70'I/O ERROR READING DIRECTORY - IMAGELIB DATASET'
ERRMSG10 DC    CL70'ERROR - NOT ENOUGH V.S. FOR FIND - PRINTDSY'
ERRMSG11 DC    CL70'ERROR - ONLY DEST=LOCAL OR COM ALLOWED WITH FCB×UCSX
               ×FORMS'
*              1234567890123456789
ERRMSG12 DC    CL70'ERROR - ILLEGAL SPECIFICATION OF SYSOUT CLASS - REQX
               UEST CANCELLED'
         SPACE 2
         LTORG
         EJECT
* DEFINE CONSTANTS THAT MUST BE INIT.
         SPACE 1
DSN      DC    C'SYS1.IMAGELIB'
PUTMSG2  DC    H'74' LENGTH OF OUTPUT LINE FOR PUTLINE
         DC    H'0'  RESERVED
CLASSMSG DC    CL70'ENTER VALID SYSOUT CLASS: +'
MULTIMSG DC    H'59',H'0',CL50'THIS IS REQUIRED WHEN FCB, CHARS, OR FORX
               MS IS SPECIFIED'
*              123456789012345 1234567890123456789012345678901234567890
         SPACE 3
* LIST FORM OF OPEN & CLOSE MACROS
         SPACE 1
OPENIT2  OPEN  (,INPUT),MF=L
         SPACE 1
CLOSEIT2 CLOSE (,),MF=L
         SPACE 3
*   PUTLINE PARAMETER LIST
         SPACE 1
PUTLIST2 PUTLINE OUTPUT=(,TERM,SINGLE,DATA),                           X
               TERMPUT=(EDIT,WAIT,NOHOLD,BREAKIN),MF=L
PUTLISTL EQU   *-PUTLIST2
         SPACE 3
*   PUTGET  PARAMETER LIST
         SPACE 1
PGLIST2  PUTGET  OUTPUT=(,MULTLVL,PROMPT),                             X
               TERMPUT=(EDIT,WAIT,NOHOLD,BREAKIN),                     X
               TERMGET=(EDIT,WAIT),MF=L
PGLISTL  EQU   *-PGLIST2
         SPACE 3
*   PDS DCB TO DO FIND
DCB2     DCB   MACRF=R,DSORG=PO,DDNAME=IN,KEYLEN=0
DCBL     EQU   *-DCB2
         EJECT
DSECT1   DSECT
         SPACE 1
*   DSECT FOR REENTERABILITY
         SPACE 1
SAVEAREA DS    18F
         SPACE 1
FLAGS    DS    X '00'         FLAGS BYTE
*                 1000 0000   MUST DO CLOSE AFTER ERROR PROCESSED
*                 0100 0000   INDICATE DO 2ND FIND (CHARS)
*                 0010 0000     "       "  "   "   (FCB)
*                 0001 0000   RESERVED
*                 0000 1000   "
*                 0000 0100   "
*                 0000 0010   "
*                 0000 0001   "
ECBPG    DS    F '0'          ECB FOR PUTGET
ECBPUT   DS    F '0'          ECB FOR PUTLINE
AFCBPDE  DS    A
ACHAPDE  DS    A
ACLAPDE  DS    A
ADESPDE  DS    A
FINDCHA  DS    CL8            FOR FIND MACRO
FINDFCB  DS    CL8             "   "    "
IOPL     DS    A          UPT=      FOR
         DS    A          ECT=       PUTLINE
         DS    A (ECBPUT)  ECB=
         DS    A (PUTLIST) PARM=
IOPLX    DS    A          UPT=      FOR
         DS    A          ECT=       PUTGET
         DS    A (ECBPUT)  ECB=
         DS    A (PUTLIST) PARM=
DDNAME   DS    CL8 ' '
PUTMSG   DS    H     LENGTH OF OUTPUT LINE FOR PUTLINE
         DS    H
PUTTEXT  DS    CL70
DDNADDR  DS    A
LNGADDR  DS    A
DCB      DS    0D,(DCBL)CL1  FOR INPUT DCB
PUTLIST  DS    0F,(PUTLISTL)CL1 FOR PUTLIST - LIST FORM
PGLIST   DS    0F,(PGLISTL)CL1  FOR PGLIST  - LIST FORM
OLD1     DS    3F        1ST  OLD CONTROL BLOCK FOR PUTGET
OLD2     DS    3F        2ND  "     "      "     "    "    (MULTILEVEL)
OPENIT   DS    F              FOR LIST FOR OPEN MACRO
CLOSEIT  DS    F               "   "    "  CLOSE "
         SPACE 2
DLENGTH  EQU   *-DSECT1
         SPACE 3
         EJECT
         IKJPGPB
         SPACE 3
         PRINT NOGEN
         DCBD DEVD=DA,DSORG=PO
         SPACE 2
         IEFZB4D0
RBLEN    EQU   (S99RBEND-S99RB)
         SPACE 2
         IEFZB4D2
         END   PRINTDSY
