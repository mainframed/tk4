*          DATA SET CBT346M    AT LEVEL 002 AS OF 12/12/76
  TITLE 'IEESD03D  -  #D  -  CORE DUMP - ALTER COMMAND.            '
IEESD03D CSECT
         LR    R12,R15
         USING IEESD03D,R12
         L     R0,SIZE             LOAD GETMAIN AREA SIZE AND SUBPOOL
         GETMAIN R,LV=(0)
         LR    R13,R1
         USING WORK,R13
         ST    R14,SAVRET          SAVE RETURN ADDR
         ST    R2,XSASAVE          SAVE XSA ADDR
         USING XSA,R2
         MVC   PLINES(4),=F'4'     DEFAULT PRINT LINES
         L     R3,XAL              LOAD LIST POINTER
         LA    R3,0(0,R3)          CLEAR HIGH ORDER
         LTR   R3,R3               PARM EXIST
         BZ    NOPARM              NO, MVS MSG
         LR    R7,R3               SAVE PARM START
         LA    R4,7                MAX LENGTH
         CLI   0(R3),C'U'          UCB DISPLAY
         BE    UCBLOOP             YES, GO PROCESS
         SPACE 3
PARM1    CLI   0(R3),C' '          END OF PARM
         BE    PARM2               YES, USE IT
         CLI   0(R3),C','          PRINT LINES SPECIFIED
         BE    PARM3               YES, GO PROCESS
         CLI   0(R3),C'/'          END OF ADDR
         BE    PARM2               YES, USE IT
         LA    R3,1(0,R3)          INCR INTO PARM
         BCT   R4,PARM1            CHECK NEXT BYTE
         B     ERROR               PARM TOO LONG
         SPACE
PARM2    LA    R6,7                MAX LENGTH
         SR    R6,R4               ACTUAL LENGTH
         BZ    ERROR               ZERO LENGTH
         STAE  STAEXIT,CT          ISSUE STAE
         BAL   R11,PARMCONV        VALIDITY CHECK AND CONVERT PARM
         CLI   0(R3),C'/'          ALTER FORMAT
         BE    ALTPARM             YES, CONTINUE PARM PROCESSING
         SRL   R5,4                ROUND BACK
         SLL   R5,4                   DUMP ADDR
         BAL   R11,FORMAT          GO FORMAT DUMP
         B     RETURN              EXIT
         EJECT
PARM3    TM    1(R3),X'F0'         NUMERIC
         BNO   ERROR               NO
         TM    2(R3),X'F0'         NUMERIC
         BNO   ERROR               NO
         PACK  DOUBLE(8),1(2,R3)
         CVB   R1,DOUBLE
         CH    R1,=H'64'           MAX LINES=64  (1K)
         BNH   *+8                 OK
         LA    R1,64               FORCE 64 LINE MAX
         ST    R1,PLINES           NO. OF PRINT LINES
         LA    R3,3(0,R3)          NEXT PARM FIELD
         B     PARM2
         EJECT
PARMCONV LR    R10,R6              SAVE LENGTH
         SR    R4,R4               CLEAR RESULT
         SPACE
PARMCNV0 CLI   0(R7),C'0'          VALID CHAR
         BL    PARMCNV1            NO
         CLI   0(R7),C'9'          VALID CHAR
         BH    ERROR               NO, PARM ERROR
         B     PARMCNV2            O.K., VALID PARM
         SPACE
PARMCNV1 CLI   0(R7),C'A'          VALID CHAR
         BL    ERROR               NO, PARM ERROR
         CLI   0(R7),C'F'          VALID CHAR
         BH    ERROR               NO, PARM ERROR
         SPACE
PARMCNV2 SR    R8,R8               INSERT
         IC    R8,0(0,R7)             DIGIT
         SRDL  R8,4                SHIFT OUT NUMERIC
         SRL   R9,28               ADJUST NUMERIC
         AH    R9,=H'9'               VALUE
         SH    R8,=H'12'           MAKE SIGN 0 OR 3
         MH    R8,=H'3'            MAKE SIGN 0 OR 9
         SR    R9,R8               CALCULATE HEX VALUE
         LR    R5,R9               LOAD HEX VALUE AND
         SLL   R5,28                  SHIFT INTO
         SLDL  R4,4                      RESULT
         LA    R7,1(0,R7)          NEXT BYTE
         BCT   R6,PARMCNV0
         LR    R5,R4               SAVE RESULT
         BR    R11
         EJECT
FORMAT   L     R4,PLINES           LOAD PRINT LINES
         SPACE
FORMAT1  LA    R6,PRINT            PRINT AREA ADDR
         ST    R5,PADDR            SAVE DUMP ADDR
         UNPK  0(7,R6),PADDR+1(4) UNPACK ADDR
         MVC   6(4,R6),=CL4' '     BLANK 4 BYTES
         NC    0(6,R6),=8X'0F'     TRANSLATE TO PRINT
         TR    0(6,R6),=C'0123456789ABCDEF'
         LA    R6,10(0,R6)         INCR INTO PRINT AREA
         LA    R7,4                NO. OF FULLWORDS PER LINE
         SPACE
FORMAT2  UNPK  0(9,R6),0(5,R5) UNPACK DATA
         MVC   8(2,R6),=CL4' '     BLANK 2 BYTES
         NC    0(8,R6),=8X'0F'     TRANSLATE TO PRINT
         TR    0(8,R6),=C'0123456789ABCDEF'
         LA    R6,10(0,R6)         INCR INTO PRINT AREA
         LA    R5,4(0,R5)          INCR INTO DATA
         BCT   R7,FORMAT2          GO FORMAT
         SR    R0,R0               LOAD
         IC    R0,XAU                 MCS ID
         MVC   WTOLIST(4),=X'00360100'  FILL IN WTO LIST
         LA    R1,WTOLIST          LOAD WTOLIST ADDRESS.
         L     R15,XSW             FIND THE IO ROUTINE.
         BALR  R14,R15             ISSUE TPUT/WTO SVC.
         BCT   R4,FORMAT1          NEXT LINE
         BR    R11
         EJECT
ALTPARM  ST    R5,ALTADDR          SAVE ALTER ADDR
         CLI   0(R7),C'/'          VALID ALTER
         BNE   ERROR
         CLI   9(R7),C'/'          VALID ALTER
         BNE   ERROR
         CLI   18(R7),C' '         VALID ALTER
         BNE   ERROR
         LA    R7,1(0,R7)          FIRST BYTE OF DATA
         LA    R6,8                MAX LENGTH
         BAL   R11,PARMCONV        GET VERIFY VALUE
         ST    R5,ALTVER
         LA    R7,1(0,R7)
         LA    R6,8
         BAL   R11,PARMCONV        GET REPLACE VALUE
         ST    R5,ALTREP
         L     R5,ALTADDR          LOAD VER/REP ADDR
         CLC   0(4,R5),ALTVER      VERIFY DATA
         BNE   NOTVER              VERIFY REJECT
         MVC   0(4,R5),ALTREP      REPLACE DATA
         SRL   R5,4                ROUND BACK
         SLL   R5,4                   DUMP ADDR
         BAL   R11,FORMAT          GO DUMP
         B     RETURN              EXIT
         EJECT
UCBLOOP  L     R4,16               CVT ADDR
         L     R4,40(0,R4)         UCB LOOKUP TABLE ADDR
UCBLOOP1 SR    R5,R5               CLEAR ADDR
         IC    R5,0(0,R4)          INSERT 1ST BYTE OF UCB ADDR
         SLL   R5,8                SHIFT 1ST BYTE
         IC    R5,1(0,R4)          INSERT 2ND BYTE OF UCB ADDR
         LTR   R5,R5               GOOD ADDR
         BZ    UCBNEXT             NO, GET NEXT
         C     R5,=X'0000FFFF'     END OF TABLE
         BE    UCBNFND             YES, UCB NOT FOUND
         CLC   1(3,R3),13(R5)      UNIT MATCH
         BE    UCBFND              YES, UCB FOUND
UCBNEXT  LA    R4,2(0,R4)          LOAD NEXT SLOT ADDR
         B     UCBLOOP1            GO CHECK NEXT UCB
UCBFND   BAL   R11,FORMAT          GO FORMAT 64 BYTES
         B     RETURN              EXIT
         EJECT
         USING *,R15
STAEXIT  L     R0,=A(STAERTRY)     LOAD RETRY ADDRESS
         LA    R15,4
         BR    R14
         DROP  R15
         USING *,R15
STAERTRY L     R12,=A(IEESD03D)    LOAD ENTRY POINT ADDR
         DROP  R15
         LA    R1,STAEMSG          ADDRESSING ERROR
         L     R2,XSASAVE          RELOAD XSA ADDR
MSGRTN   SR    R0,R0
         IC    R0,XAU              INSERT MCS ID
         LA    R1,0(,R1)           ADDRESS WTO FORMAT MESSAGE.
         L     R15,XSW             FIND THE WRITE CODE.
         BALR  R14,R15             ISSUE THE TPUT/WTO.
         B     RETURN
         SPACE 3
NOTVER   LA    R1,VERMSG           NOT VERIFIED
         B     MSGRTN              GO ISSUE WTO
         SPACE 3
ERROR    LA    R1,ERRMSG           PARM ERROR
         B     MSGRTN              GO ISSUE WTO
         SPACE 3
NOPARM   LA    R1,MVSMSG           MVS
         B     MSGRTN              GO ISSUE WTO
         SPACE 3
UCBNFND  LA    R1,UCBNFMSG         UCB NOT FOUND
         B     MSGRTN              GO ISSUE WTO
         SPACE 3
RETURN   L     R0,SIZE             LOAD GETMAIN SIZE AND SUBPOOL
         LR    R1,R13              LOAD GETMAIN AREA ADDR
         L     R14,SAVRET          RETURN ADDR
         FREEMAIN R,LV=(0),A=(1)
         SR    R15,R15
         BR    R14
         EJECT
         LTORG
         DS    0F
SIZE     DC    X'F1',AL3(WORKEND-WORKSTRT)
         SPACE 3
         DS    0F
STAEMSG  DC    X'00360100'
         DC    CL50'ADDRESS SPECIFIED UNAVAILABLE'
         SPACE 3
         DS    0F
VERMSG   DC    X'00360100'
         DC    CL50'DATA NOT VERIFIED'
         SPACE 3
         DS    0F
ERRMSG   DC    X'00360100'
         DC    CL50'PARAMETER ERROR'
         SPACE 3
         DS    0F
MVSMSG   DC    X'00360100'
         DC    CL50'I AM THE ALL-POWERFUL WIZARD OF MVS, YOUR MASTER '
         SPACE 3
         DS    0F
UCBNFMSG DC    X'00360100'
         DC    CL50'UNIT SPECIFIED NOT FOUND'
         EJECT
WORK     DSECT
WORKSTRT DS    0D
SAVRET   DS    18F
         DS    0D
DOUBLE   DS    2F
PLINES   DS    F
XSASAVE  DS    F
SHIFT    DS    F
WTOLIST  DS    F
PRINT    DS    52C
PADDR    DS    F
ALTADDR  DS    F
ALTVER   DS    F
ALTREP   DS    F
WORKEND  DS    0F
         SPACE 5
         DSXSA
         REGS
         END
