*          DATA SET CBT456     AT LEVEL 005 AS OF 04/07/77
         TITLE 'IEESJ03D  -   #I  -  LIST CURRENT IPS AND SWAP COUNTS'
IEESJ03D CSECT
         LR    R12,R15
         USING IEESJ03D,R12
         L     R0,SIZE             GETMAIN SIZE
         GETMAIN R,LV=(0)
         LR    R13,R1
         USING WORK,R13
         ST    R14,SAVRET          SAVE RETURN ADDRESS
         ST    R2,XSASAVE
         USING XSA,R2
         L     R3,XAL              LOAD PARM
         LA    R3,0(0,R3)
         LTR   R3,R3               ANY SUB-PARMS
         BNZ   PARM                YES, GO PROCESS
         MVC   WTOLIST(4),=X'00280100'
         MVC   WTOLIST+4(36),LINE1
         L     R4,16               CVT
         L     R4,604(0,R4)        RMCT
         L     R5,20(0,R4)         RMCA
         L     R4,24(0,R4)         WMST
         LA    R5,124(0,R5)        SWAP COUNTS IN RMCA
         LA    R6,LINES            LINE FORMATS
         LA    R7,11               NUMBER OF LINES
         MVC   WTOLIST+26(2),4(R4) IPS ID
         EJECT
PRINT    LA    R1,WTOLIST          MESSAGE IN WTO FORMAT.
         L     R15,XSW             FIND THE IO ROUTINE.
         BALR  R14,R15             ISSUE THE TPUT/WTO.
         BCT   R7,PRINT1
         B     EXIT
         SPACE 3
PRINT1   MVC   WTOLIST+4(36),0(R6) SET A LINE
         L     R1,0(0,R5)          LOAD SWAP COUNT
         CVD   R1,DOUBLE
         MVC   WTOLIST+32(8),=X'4020206B20212040'
         ED    WTOLIST+32(7),DOUBLE+5
         LA    R5,4(0,R5)          NEXT COUNT
         LA    R6,36(0,R6)         NEXT LINE
         B     PRINT
         SPACE 3
EXIT     L     R0,SIZE             GETMAIN SIZE
         LR    R1,R13              AREA ADDRESS
         L     R14,SAVRET          LOAD RETURN ADDR
         FREEMAIN R,LV=(0),A=(1)
         SR    R15,R15
         BR    R14
         SPACE 3
PARM     CLI   0(R3),C'P'          PAGE DS DISPLAY
         BE    PART                YES, GO PROCESS
         CLI   0(R3),C'A'          ASM DISPLAY
         BE    ASM                 YES, GO PROCESS
         CLI   0(R3),C'S'          SWAP DS DISPLAY
         BE    SART                YES, GO PROCESS
         LA    R1,PARMERR          MESSAGE IN WTO FORMAT.
         L     R15,XSW             FIND THE IO ROUTINE.
         BALR  R14,R15             ISSUE THE TPUT/WTO.
         B     EXIT
         EJECT
PART     L     R3,16               CVT
         L     R3,704(0,R3)        ASMVT
         L     R3,8(0,R3)          PART
         L     R4,24(0,R3)         DSNAME TABLE
         LA    R3,80(0,R3)         PART ENTRIES
         SPACE 3
PART1    MVI   PARTDSP1,C' '
         MVC   PARTDSP1+1(41),PARTDSP1
         MVC   PARTDSP2(42),PARTOVLY
         MVC   PARTNAME(18),0(R4)  DSNAME
         MVC   PARTTYPE(4),=C'PLPA'
         TM    8(R3),X'80'
         BO    PART2
         MVC   PARTTYPE(4),=C'COMN'
         TM    8(R3),X'40'
         BO    PART2
         MVC   PARTTYPE(4),=C'DPLX'
         TM    8(R3),X'20'
         BO    PART2
         MVC   PARTTYPE(4),=C'LOCL'
         EJECT
PART2    L     R1,44(0,R3)         UCB ADDR
         MVC   PARTVOL(6),28(R1)   VOLSER
         L     R10,16(0,R3)        SIZE IN SLOTS
         CVD   R10,DOUBLE
         UNPK  PARTSIZE(4),DOUBLE+5(3)
         OI    PARTSIZE+3,X'F0'
         L     R9,20(0,R3)         AVAILABLE SLOTS
         CVD   R9,DOUBLE
         UNPK  PARTAVAL(4),DOUBLE+5(3)
         OI    PARTAVAL+3,X'F0'
         SR    R8,R8
         M     R8,=F'100'
         DR    R8,R10              CALCULATE PERCENT AVAILABLE
         L     R8,=F'100'
         SR    R8,R9               PERCENT USED
         CVD   R8,DOUBLE
         OI    DOUBLE+7,X'0F'      FORCE + SIGN
         UNPK  PARTPCNT(2),DOUBLE+6(2)
         CLC   DOUBLE+6(2),=X'100F'   100 PERCENT
         BNE   *+10                NO, LEAVE ALONE
         MVC   PARTPCNT(2),=C'99'  MAKE IT 99 PERCENT
         L     R1,56(0,R3)         PARERQTM -   BURST COMPUTATION VALUE
         SRL   R1,8                DIVIDE BY 256
         CVD   R1,DOUBLE
         UNPK  PARTBRST(3),DOUBLE+6(2)
         OI    PARTBRST+2,X'F0'
         MVC   PARTERR(8),=CL8'ERR(000)'
         L     R1,24(0,R3)         LOAD PART ENTRY ERROR COUNT
         CVD   R1,DOUBLE
         UNPK  PARTERR+4(3),DOUBLE+6(2)
         OI    PARTERR+6,X'F0'
         MVC   PARTLST1(4),=X'002E0100'
         LA    R1,PARTLST1         MESSAGE IN WTO FORMAT.
         L     R15,XSW             FIND THE IO ROUTINE.
         BALR  R14,R15             ISSUE THE TPUT/WTO.
         MVC   PARTLST2(4),=X'002E0100'
         LA    R1,PARTLST2         MESSAGE IN WTO FORMAT.
         L     R15,XSW             FIND THE IO ROUTINE.
         BALR  R14,R15             ISSUE THE TPUT/WTO.
         SPACE 3
PART3    LA    R4,44(0,R4)         NEXT DSNAME
         LA    R3,64(0,R3)         NEXT PART ENTRY
         CLI   8(R3),X'00'         END OF PART
         BE    EXIT                YES, EXIT
         TM    9(R3),X'80'         NOT IN USE
         BO    PART3               YES, GET NEXT
         B     PART1
         EJECT
SART     L     R3,16               CVT
         L     R3,704(0,R3)        ASMVT
         L     R3,4(0,R3)          SART
         L     R4,24(0,R3)         DSNAME TABLE
         SPACE 3
SART0    MVC   SARTDSP0(42),SARTOVL0
         MVC   SARTNO(4),=XL4'40202120'
         MVC   SARTTAVL(6),=XL6'402020202120'
         MVC   SARTPGST(6),=XL6'402020202120'
         MVC   SARTLST0(4),=X'002E0100'
         L     R1,8(0,R3)          NO. OF SWAP DS IN USE
         CVD   R1,DOUBLE
         ED    SARTNO(4),DOUBLE+6
         LR    R10,R1              SAVE NO. OF SWAP DS
         L     R1,36(0,R3)         AVAILABLE SWAP SETS TOTAL
         CVD   R1,DOUBLE
         ED    SARTTAVL(6),DOUBLE+5
         L     R1,56(0,R3)         PAGES/SWAPSET
         CVD   R1,DOUBLE
         ED    SARTPGST(6),DOUBLE+5
         LA    R1,SARTLST0         MESSAGE IN WTO FORMAT.
         L     R15,XSW             FIND THE IO ROUTINE.
         BALR  R14,R15             ISSUE THE TPUT/WTO.
         LA    R3,80(0,R3)         SART ENTRIES
         LTR   R10,R10             ANY SWAP DS IN USE
         BZ    SARTNMSG            NO, ISSUE MSG
         SPACE 3
SART1    MVI   SARTDSP1,C' '
         MVC   SARTDSP1+1(41),SARTDSP1
         MVC   SARTDSP2(42),SARTOVL2
         MVC   SARTNAME(18),0(R4)  DSNAME
         MVC   SARTLST1(4),=X'002E0100'
         MVC   SARTLST2(4),=X'002E0100'
         L     R1,44(0,R3)         UCB ADDR
         MVC   SARTVOL(6),28(R1)   VOLSER
         EJECT
SART2    L     R10,16(0,R3)        SIZE IN SWAP SETS
         CVD   R10,DOUBLE
         UNPK  SARTSIZE(4),DOUBLE+5(3)
         OI    SARTSIZE+3,X'F0'
         L     R9,20(0,R3)         AVAILABLE SWAP SETS
         CVD   R9,DOUBLE
         UNPK  SARTAVAL(4),DOUBLE+5(3)
         OI    SARTAVAL+3,X'F0'
         SR    R8,R8
         M     R8,=F'100'
         DR    R8,R10              CALCULATE PERCENT AVAILABLE
         L     R8,=F'100'
         SR    R8,R9               PERCENT USED
         CVD   R8,DOUBLE
         OI    DOUBLE+7,X'0F'      FORCE + SIGN
         UNPK  SARTPCNT(2),DOUBLE+6(2)
         CLC   DOUBLE+6(2),=X'100F'   100 PERCENT
         BNE   *+10                NO, LEAVE ALONE
         MVC   SARTPCNT(2),=C'99'  MAKE IT 99 PERCENT
         L     R1,24(0,R3)         SWAP SETS IN ERROR
         CVD   R1,DOUBLE
         UNPK  SARTERR(4),DOUBLE+5(3)
         OI    SARTERR+3,X'F0'
         LA    R1,SARTLST1         MESSAGE IN WTO FORMAT.
         L     R15,XSW             FIND THE IO ROUTINE.
         BALR  R14,R15             ISSUE THE TPUT/WTO.
         LA    R1,SARTLST2         MESSAGE IN WTO FORMAT.
         L     R15,XSW             FIND THE IO ROUTINE.
         BALR  R14,R15             ISSUE THE TPUT/WTO.
         SPACE 3
SART3    LA    R4,44(0,R4)         NEXT DSNAME
         LA    R3,48(0,R3)         NEXT SART ENTRY
         CLI   0(R3),X'00'         END OF SART
         BE    EXIT                YES, EXIT
         TM    9(R3),X'80'         NOT IN USE
         BO    SART3               YES, GET NEXT
         B     SART1
         SPACE 3
SARTNMSG LA    R1,SARTMSG          MESSAGE IN WTO FORAMT.
         L     R15,XSW             FIND THE IO ROUTINE.
         BALR  R14,R15             ISSUE THE TPUT/WTO.
         B     EXIT
         EJECT
ASM      MVC   WTOLIST(4),=X'001A0100'
         L     R4,16               CVT
         L     R4,704(0,R4)        ASMVT
         MVC   WTOLIST+4(12),=CL12'ILRSLOTC'
         MVC   WTOLIST+16(10),=X'40206B2020206B202120'
         L     R1,128(0,R4)        LOAD
         LA    R1,0(0,R1)             SLOTC
         SH    R1,=H'8'                  VALUE
         L     R1,0(0,R1)                   AND
         CVD   R1,DOUBLE                       CONVERT
         ED    WTOLIST+16(10),DOUBLE+4
         BAL   R5,APRINT           GO PRINT RESULTS
         MVC   WTOLIST+4(12),=CL12'ILRSLOTV'
         MVC   WTOLIST+16(10),=X'40206B2020206B202120'
         L     R1,128(0,R4)        LOAD
         L     R1,0(0,R1)             SLOTV VALUE
         CVD   R1,DOUBLE                 AND CONVERT
         ED    WTOLIST+16(10),DOUBLE+4
         BAL   R5,APRINT           GO PRINT RESULTS
         LA    R4,108(0,R4)        ASM COUNTS
         MVC   WTOLIST+4(12),=CL12'AVAIL SLOTS'
         BAL   R5,AFORM            GO FORMAT
         MVC   WTOLIST+4(12),=CL12'TOTAL SLOTS'
         BAL   R5,AFORM            GO FORMAT
         MVC   WTOLIST+4(12),=CL12'VIO SLOTS'
         BAL   R5,AFORM            GO FORMAT
         MVC   WTOLIST+4(12),=CL12'ADSPC SLOTS'
         BAL   R5,AFORM            GO FORMAT
         MVC   WTOLIST+4(12),=CL12'BAD SLOTS'
         BAL   R5,AFORM            GO FORMAT
         B     EXIT                EXIT
         SPACE 3
AFORM    L     R1,0(0,R4)          LOAD COUNT
         CVD   R1,DOUBLE              AND CONVERT
         MVC   WTOLIST+16(10),=X'40206B2020206B202120'
         ED    WTOLIST+16(10),DOUBLE+4
         LA    R4,4(0,R4)          NEXT COUNT
         SPACE 3
APRINT   LA    R1,WTOLIST          MESSAGE IN WTO FORMAT.
         L     R15,XSW             FIND THE IO ROUTINE.
         BALR  R14,R15             ISSUE THE TPUT/WTO.
         BR    R5                  RETURN
         EJECT
         LTORG
         SPACE 3
         DS    0F
SIZE     DC    X'F1',AL3(WORKEND-WORKSTRT)
         SPACE 3
PARMERR  DC    X'001A0100',C'SUB-PARM NOT SUPPORTED'
         SPACE 3
SARTMSG  DC    X'001C0100',CL24'NO SWAP DATA SETS IN USE'
         SPACE 3
LINE1    DC    CL22'CURRENT IPS -   IEAIPS'
         DC    CL2' ',CL12' '
         SPACE 3
LINES    DC    CL20'TERM WAIT - IN',C'(01)',CL12' '
         DC    CL20'TERM WAIT - OUT',C'(02)',CL12' '
         DC    CL20'LONG WAIT',C'(03)',CL12' '
         DC    CL20'AUX STOR SHORTAGE',C'(04)',CL12' '
         DC    CL20'REAL STOR SHORTAGE',C'(05)',CL12' '
         DC    CL20'DETECTED WAIT',C'(06)',CL12' '
         DC    CL20'REQUEST SWAP',C'(07)',CL12' '
         DC    CL20'ENQ EXCHANGE',C'(08)',CL12' '
         DC    CL20'WORKLOAD EXCHANGE',C'(09)',CL12' '
         DC    CL20'UNILATERAL SWAP',C'(0A)',CL12' '
         EJECT
PARTOVLY DS    0CL42
         DC    CL5'SIZE(',CL4' '
         DC    CL8') AVAIL(',CL4' '
         DC    CL7') USED(',CL2' '
         DC    CL8'%) BRST(',CL3' ',C')'
         SPACE 3
SARTOVL0 DS    0CL42
         DC    CL9'SWAP  DS-'
         DC    CL4' '
         DC    CL7'  SETS-'
         DC    CL6' '
         DC    CL10'  PGS/SET-'
         DC    CL6' '
         SPACE 3
SARTOVL2 DS    0CL42
         DC    CL5'SIZE(',CL4' '
         DC    CL8') AVAIL(',CL4' '
         DC    CL7') USED(',CL2' '
         DC    CL7'%) ERR(',CL4' ',C')'
         EJECT
WORK     DSECT
WORKSTRT DS    0D
DOUBLE   DS    D
SAVRET   DS    F
XSASAVE  DS    F
WTOLIST  DS    CL44
         SPACE 3
         DS    0F
PARTLST1 DS    CL4
PARTDSP1 DS    0CL42
PARTTYPE DS    CL4
         DS    CL2
PARTVOL  DS    CL6
         DS    CL2
PARTNAME DS    CL18
         DS    CL2
PARTERR  DS    CL8
         SPACE 3
         DS    0F
PARTLST2 DS    CL4
PARTDSP2 DS    0CL42
         DS    CL5
PARTSIZE DS    CL4
         DS    CL8
PARTAVAL DS    CL4
         DS    CL7
PARTPCNT DS    CL2
         DS    CL8
PARTBRST DS    CL3
         DS    C
         EJECT
         DS    0F
SARTLST0 DS    CL4
SARTDSP0 DS    0CL42
         DS    CL9
SARTNO   DS    CL4
         DS    CL7
SARTTAVL DS    CL6
         DS    CL10
SARTPGST DS    CL6
         SPACE 3
         DS    0F
SARTLST1 DS    CL4
SARTDSP1 DS    0CL42
SARTVOL  DS    CL6
         DS    CL4
SARTNAME DS    CL32
         SPACE 3
         DS    0F
SARTLST2 DS    CL4
SARTDSP2 DS    0CL42
         DS    CL5
SARTSIZE DS    CL4
         DS    CL8
SARTAVAL DS    CL4
         DS    CL7
SARTPCNT DS    CL2
         DS    CL7
SARTERR  DS    CL4
         DS    C
WORKEND  DS    0D
         EJECT
         DSXSA
         REGS
         END
