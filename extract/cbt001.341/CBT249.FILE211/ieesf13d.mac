*          DATA SET CBT341     AT LEVEL 009 AS OF 01/23/77
         TITLE 'IEESF03D  -  #F  -   DISPLAY PAGE FRAME ASSIGNMENTS'
IEESF03D CSECT
         LR    R12,R15
         USING IEESF03D,R12
         L     R0,SIZE             LOAD GETMAIN AREA SIZE AND SUBPOOL
         GETMAIN R,LV=(0)
         LR    R13,R1
         USING WORK,R13
         ST    R14,SAVRET          SAVE RETURN ADDR
         ST    R2,XSASAVE          SAVE XSA ADDR
         USING XSA,R2
         MVI   VOSWIT,X'00'        CLEAR OFFLINE SWITCH
         L     R3,XAL              LOAD LIST POINTER
         LA    R3,0(0,R3)          CLEAR HIGH ORDER
         LTR   R3,R3               PARM EXIST
         BZ    PFTLIST             NO, GO PROCESS PFT LIST
         CLI   0(R3),C'R'          V=R DISPLAY REQUEST
         BE    VRDISP              YES, GO PROCESS
         CLI   0(R3),C'M'          MEGABYTE DISPLAY REQUEST
         BE    VVDISP              YES, GO PROCESS
         CLI   0(R3),C'O'          OFFLINE INTERCEPT REQUEST
         BE    VODISP              YES, GO PROCESS
         LA    R11,RETURN          LOAD RET ADDR
         LA    R1,ERRMSG           PARM ERROR
         B     PRINT               GO ISSUE WTO
         EJECT
PFTLIST  MVC   OUTMSG(4),=X'00140100'
         L     R14,16              LOAD CVT ADDR
         L     R6,556(0,R14)       LOAD ASVT ADDR
         L     R7,516(0,R6)        MAX NO. OF ASCBS
         LA    R6,524(0,R6)        1ST ASCB SLOT
         L     R3,356(0,R14)       LOAD PVT ADDR
         L     R4,12(0,R3)         PFT APPARENT ADDR
         AH    R4,16(0,R3)         PFT ACTUAL ADDR
         SR    R5,R5               INDEX
         IC    R5,18(0,R3)            TO
         SLL   R5,8                      END OF
         IC    R5,19(0,R3)                  PFT
         SH    R5,16(0,R3)
         SRL   R5,4                DIVIDE BY 16
         STM   R4,R5,PFTREGS       SAVE PFT REGS
         LH    R14,16(0,R3)        NUCLEUS FRAMES
         SRL   R14,4               DIVIDE BY 16
         CVD   R14,DOUBLE          CONVERT TO DECIMAL
         MVC   OUTMSG+4(12),=CL12'NUCLEUS'
         MVC   OUTMSG+15(5),=X'2020202120'
         ED    OUTMSG+14(6),DOUBLE+5
         BAL   R11,PGPRINT         GO PRINT
         LH    R14,1830(0,R3)      TOTAL FIXED
         CVD   R14,DOUBLE
         MVC   OUTMSG+4(12),=CL12'TOT FIXED'
         MVC   OUTMSG+15(5),=X'2020202120'
         ED    OUTMSG+14(6),DOUBLE+5
         BAL   R11,PGPRINT
         LH    R14,1832(0,R3)      TOTAL SQA FIXED
         CVD   R14,DOUBLE
         MVC   OUTMSG+4(12),=CL12'SQA FIXED'
         MVC   OUTMSG+15(5),=X'2020202120'
         ED    OUTMSG+14(6),DOUBLE+5
         BAL   R11,PGPRINT
         LH    R14,1834(0,R3)      COMMON FIXED
         CVD   R14,DOUBLE
         MVC   OUTMSG+4(12),=CL12'CMN FIXED'
         MVC   OUTMSG+15(5),=X'2020202120'
         ED    OUTMSG+14(6),DOUBLE+5
         BAL   R11,PGPRINT
         L     R10,=X'0000FFFF'
         BAL   R11,PAGEPRNT        GO COUNT AND FORMAT
         EJECT
GETASCB  LA    R6,4(0,R6)          NEXT SLOT
         BCT   R7,ASCBFND          GO PROCESS
         SPACE 3
RETURN   L     R0,SIZE
         LR    R1,R13              WORK AREA ADDR
         L     R14,SAVRET          LOAD RETURN ADDR
         FREEMAIN R,LV=(0),A=(1)
         SR    R15,R15
         BR    R14
         SPACE 3
ASCBFND  TM    0(R6),X'80'         AVAILABLE SLOT
         BO    GETASCB             YES, GET NEXT SLOT
         L     R8,0(0,R6)          LOAD ASCB ADDR
         SR    R10,R10             LOAD
         IC    R10,36(0,R8)
         SLL   R10,8                  ASID
         IC    R10,37(0,R8)
         BAL   R11,PAGEPRNT        GO COUNT AND FORMAT
         B     GETASCB
         EJECT
PAGEPRNT LM    R4,R5,PFTREGS       LOAD PFT REGS
         SR    R14,R14             CLEAR FRAME COUNT
         SR    R9,R9               CLEAR AVAILABLE FRAME COUNT
         SPACE 3
PAGEPR1  SR    R15,R15             LOAD
         IC    R15,0(0,R4)            PFT
         SLL   R15,8                     ASID
         IC    R15,1(0,R4)
         CR    R10,R15             ASID MATCH
         BNE   PAGEPR2             NO, DONT INCR FRAME COUNT
         C     R10,=X'0000FFFF'    SYS PAGES
         BNE   *+12
         TM    12(R4),X'80'        AVAILABLE PAGE
         BO    AVAILCNT            GO ADD TO AVAILABLE COUNT
         LA    R14,1(0,R14)        ADD 1 TO FRAME COUNT
         SPACE 3
PAGEPR2  LA    R4,16(0,R4)         NEXT PFT
         BCT   R5,PAGEPR1          PROCESS NEXT
         MVC   OUTMSG+4(12),=CL12'ASID'
         C     R10,=X'00000001'    MASTER
         BE    ASIDMSTR            YES, GO PRINT MASTER
         C     R10,=X'0000FFFF'    SYS PAGES
         BNE   PAGEPR3             NO, ASID
         MVC   OUTMSG+4(12),=CL12'SYS PAGES'
         B     PAGEPR4
         SPACE 3
PAGEPR3  LTR   R14,R14             ANY FRAMES
         BCR   8,R11               NO, BYPASS PRINT
         STH   R10,DOUBLE+6        SAVE ASID
         UNPK  DOUBLE(5),DOUBLE+6(3)
         NC    DOUBLE(4),=4X'0F'
         TR    DOUBLE(4),=C'0123456789ABCDEF'
         MVC   OUTMSG+9(4),DOUBLE  ASID TO MSG
         SPACE 3
PAGEPR4  CVD   R14,DOUBLE
         MVC   OUTMSG+15(5),=X'2020202120'
         ED    OUTMSG+14(6),DOUBLE+5
         CLC   OUTMSG+4(4),=C'ASID'
         BE    JOBFIND
         EJECT
PGPRINT  LA    R1,OUTMSG           LOAD OUTPUT MSG ADDR
         SPACE 3
PRINT    LA    R1,0(,R1)           MESSAGE IN WTO FORMAT.
         L     R15,XSW             FIND THE IO ROUTINE.
         LR    R0,R14              KEEP IT KLUGE.
         BALR  R14,R15             ISSUE TPUT/WTO
         LR    R14,R0              RESTORE KLUGE.
         CLC   OUTMSG+4(12),=CL12'SYS PAGES'
         BCR   7,R11               NOT SYS PAGES, NOT AVAILABLE
         MVC   OUTMSG+4(12),=CL12'AVAILABLE'
         LR    R14,R9              AVAILABLE COUNT
         B     PAGEPR4             GO FORMAT
         SPACE 3
AVAILCNT LA    R9,1(0,R9)          ADD 1 TO COUNT
         B     PAGEPR2
         SPACE 3
ASIDMSTR MVC   OUTMSG+4(12),=CL12'MASTER'
         B     PAGEPR4
         SPACE 3
VONONE   LA    R1,VOMSG            NO OFFLINE INTERCEPT MSG
         LA    R11,RETURN
         B     PRINT
         EJECT
JOBFIND  EQU   *
         L     R9,56(0,R8)         CSCB ADDR
         CLI   28(R9),X'03'        INIT
         BE    JOBFIND1            YES, GO SEARCH
         SPACE
JOBFIND0 MVC   OUTMSG+4(8),8(R9)   JOBNAME
         MVI   OUTMSG+12,C' '
         B     PGPRINT
         SPACE
JOBFIND1 LA    R1,0(0,R9)          SAVE ADDR
         SR    R14,R14             LOAD
         IC    R14,30(0,R9)
         SLL   R14,8                  ASID
         IC    R14,31(0,R9)
         SPACE
JOBFIND2 L     R9,0(0,R9)          NEXT CSCB
         LA    R9,0(0,R9)
         LTR   R9,R9               END OF CHAIN
         BNZ   JOBFIND3            NO, CONTINUE
         LR    R9,R1               USE ORIG CSCB
         B     JOBFIND0
         SPACE
JOBFIND3 CR    R9,R1               END OF CHAIN
         BE    JOBFIND0            YES, USE ORIG CSCB
         CH    R14,30(0,R9)        ASID EQUAL
         BE    JOBFIND0            YES, USE THIS CSCB
         B     JOBFIND2
         EJECT
VRDISP   MVI   VRSWIT,X'F0'        SET HDR SWITCH
         MVC   OUTMSG(20),VRHDR1   SET 1ST HDR
         BAL   R11,VRDISP5D        GO WTO IT
         MVC   OUTMSG(20),VRHDR2   SET 2ND HDR
         BAL   R11,VRDISP5D        GO WTO IT
         MVI   VRSWIT,X'00'        CLEAR HDR SWITCH
         L     R14,16              CVT ADDR
         L     R3,356(0,R14)       PVT ADDR
         L     R4,12(0,R3)         PFT APPARENT ADDR
         AH    R4,20(0,R3)         PFT V=R ACTUAL ADDR
         LH    R5,22(0,R3)         INDEX TO V=R END
         SH    R5,20(0,R3)         SUBTRACT V=R START
         SRL   R5,4                NO. OF V=R PAGES
         LA    R5,1(0,R5)          ADJUST NO OF FRAMES
         MVI   VRSWIT,X'00'        CLEAR V=R END SWITCH
         MVC   OUTMSG(20),VRMSG    SET UP V=R MSG BASE
         B     VRDISP3             GO PROCESS
         SPACE 3
VRDISP1  CLI   VOSWIT,X'00'        OFFLINE INTERCEPT REQUEST
         BE    VRDISP1O            NO
         TM    12(R4),X'01'        OFFLINE INTERCEPT FLAG BIT ON
         BNO   VRDISP2O            NO, NEXT PFTE
         SPACE 3
VRDISP1O CLC   0(2,R4),VRASID      ASID MATCH
         BNE   VRDISP4             NO, GO FORMAT AND PRINT
         CLC   12(0,R4),VRFLAGS    FLAGS MATCH
         BNE   VRDISP4             NO, GO FORMAT AND PRINT
         SPACE 3
VRDISP2  L     R14,VRNUM           ADD 1
         LA    R14,1(0,R14)           TO EQUAL
         ST    R14,VRNUM                 ENTRY COUNT
         SPACE 3
VRDISP2O LA    R4,16(0,R4)         NEXT PFTE
         BCT   R5,VRDISP1
         MVI   VRSWIT,X'FF'        SET END OF V=R SWITCH
         B     VRDISP4             GO FORMAT AND PRINT LAST
         SPACE 3
VRDISP3  XC    VRNUM(4),VRNUM      CLEAR COUNT
         MVC   VRASID(2),0(R4)     SET ASID
         MVC   VRFLAGS(3),12(R4)   SET FLAGS
         B     VRDISP2
         EJECT
VRDISP4  L     R14,VRNUM           NO. OF FRAMES
         CVD   R14,DOUBLE          DECIMAL
         UNPK  OUTMSG+4(3),DOUBLE+6(2)
         OI    OUTMSG+6,X'F0'
         UNPK  DOUBLE(5),VRASID(3) TRANSLATE ASID
         NC    DOUBLE(4),=4X'0F'
         TR    DOUBLE(4),=C'0123456789ABCDEF'
         MVC   OUTMSG+10(4),DOUBLE PUT ASID IN MSG
         SPACE 3
VRDISP5A MVI   OUTMSG+17,C'O'      OFFLINE
         TM    VRFLAGS+1,X'40'
         BO    VRDISP5B
         MVI   OUTMSG+17,C'C'      OFFLINE INTERCEPT
         TM    VRFLAGS,X'01'
         BO    VRDISP5B
         MVI   OUTMSG+17,C'R'      V=R ALLOCATED
         TM    VRFLAGS,X'02'
         BO    VRDISP5B
         MVI   OUTMSG+17,C'I'      V=R INTERCEPT
         TM    VRFLAGS,X'40'
         BO    VRDISP5B
         MVI   OUTMSG+17,C'A'      AVAILABLE
         TM    VRFLAGS,X'80'
         BO    VRDISP5B
         MVI   OUTMSG+17,C'-'      NO STATUS
         SPACE 3
VRDISP5B MVI   OUTMSG+18,C'B'      BAD PAGE
         TM    VRFLAGS,X'04'
         BO    VRDISP5C
         MVI   OUTMSG+18,C'F'      LONG TERM FIXED
         TM    VRFLAGS,X'10'
         BO    VRDISP5C
         MVI   OUTMSG+18,C'S'      LSQA/SQA PAGE
         TM    VRFLAGS,X'20'
         BO    VRDISP5C
         MVI   OUTMSG+18,C'V'      VIO PAGE
         TM    VRFLAGS+1,X'10'
         BO    VRDISP5C
         MVI   OUTMSG+18,C'-'      NO STATUS
         EJECT
VRDISP5C MVI   OUTMSG+19,C'A'      AVAILABLE
         TM    VRFLAGS+2,X'FF'
         BZ    VRDISP5D
         MVI   OUTMSG+19,C'N'      NOT QUEUED
         BO    VRDISP5D
         MVI   OUTMSG+19,C'S'      SQA
         TM    VRFLAGS+2,X'C0'
         BO    VRDISP5D
         MVI   OUTMSG+19,C'L'      LSQA
         TM    VRFLAGS+2,X'84'
         BO    VRDISP5D
         MVI   OUTMSG+19,C'R'      SQA RESERVED
         TM    VRFLAGS+2,X'04'
         BO    VRDISP5D
         MVI   OUTMSG+19,C'C'      COMMON
         TM    VRFLAGS+2,X'08'
         BO    VRDISP5D
         MVI   OUTMSG+19,C'U'      LOCAL
         TM    VRFLAGS+2,X'80'
         BO    VRDISP5D
         MVI   OUTMSG+19,C'-'      NO STATUS
         SPACE 3
VRDISP5D LA    R1,OUTMSG           MESSAGE IN WTO FORMAT.
         L     R15,XSW             FIND THE IO ROUTINE.
         LR    R0,R14              KEEP FOURTEEN KLUGE.
         BALR  R14,R15             ISSUE TPUT/WTO.
         LR    R14,R0              RESTORE COUNTER KLUGE.
         TM    VRSWIT,X'FF'        TEST HDR/END SWITCH
         BO    RETURN              END
         BCR   4,R11               HDR
         B     VRDISP3
         EJECT
VODISP   MVI   VOSWIT,X'FF'        SET OFFLINE INTERCEPT ONLY SW
         SPACE 3
VVDISP   MVI   VRSWIT,X'F0'        SET HDR SWITCH
         MVI   DOUBLE,X'F0'        TEST
         MVZ   DOUBLE(1),1(R3)        NUMERIC
         CLI   DOUBLE,X'F0'              MEGABYTE NUMBER
         BNE   VVDISP1             NOT NUMERIC, ERROR
         MVC   OUTMSG(20),VRHDR1   1ST HDR
         BAL   R11,VRDISP5D
         MVC   OUTMSG(20),VRHDR2   2ND HDR
         BAL   R11,VRDISP5D
         MVI   VRSWIT,X'00'        CLEAR HDR SWITCH
         MVC   OUTMSG(20),VRMSG    BASE MSG
         B     VVDISP2
         SPACE 3
VVDISP1  LA    R11,RETURN          PARM
         LA    R1,ERRMSG              ERROR
         B     PRINT
         SPACE 3
VVDISP2  IC    R5,1(0,R3)          CALCULATE
         SLL   R5,28                  PFTE INDEX
         SRL   R5,16                     TO THIS MEG
         LTR   R5,R5               ZERO MEG
         BZ    VVDISP1             YES, ERROR
         L     R14,16              CVT ADDR
         L     R3,356(0,R14)       PVT ADDR
         L     R4,12(0,R3)         PFT APPARENT ADDR
         AR    R4,R5               PFT MEG START ADDR
         CH    R5,18(0,R3)         PAST END OF PFT
         BH    VVDISP1             YES, ERROR
         LA    R5,256              NO. OF FRAMES IN × MEG
         CLI   VOSWIT,X'00'        OFFLINE INTERCEPT REQUEST
         BNE   VODISP1             YES, CONTINUE PROCESS
         B     VRDISP3             GO PROCESS
         SPACE 3
VODISP1  TM    12(R4),X'01'        OFFLINE INTERCEPT FLAG BIT ON
         BO    VRDISP3             YES, GO CONTINUE PROCESS
         LA    R4,16(0,R4)         NEXT PFTE
         BCT   R5,VODISP1          GO CHECK NEXT
         B     VONONE              NO INTERCEPT OFFLINE, GO MSG
         EJECT
         LTORG
         SPACE 3
         DS    0F
SIZE     DC    X'F1',AL3(WORKEND-WORKSTRT)
         SPACE 3
         DS    0F
ERRMSG   DC    X'001C0100'
         DC    CL24'PARAMETER ERROR'
         SPACE 3
         DS    0F
VRHDR1   DC    X'00140100'
         DC    CL16'PAGES.ASID.FLAGS'
         SPACE 3
         DS    0F
VRHDR2   DC    X'00140100'
         DC    16C'-'
         SPACE 3
         DS    0F
VRMSG    DC    X'00140100'
         DC    CL16'   ...    ...   '
         SPACE 3
         DS    0F
VOMSG    DC    X'00220100'
         DC    CL30'NO FRAMES INTERCEPTED OFFLINE'
         EJECT
WORK     DSECT
WORKSTRT DS    0D
SAVRET   DS    18F
DOUBLE   DS    2F
XSASAVE  DS    F
PFTREGS  DS    2F
VRNUM    DS    F
VRFLAGS  DS    CL3
VRSWIT   DS    C
VOSWIT   DS    C
VRASID   DS    H
         DS    0F
OUTMSG   DS    20C
         SPACE
WORKEND  DS    0F
         EJECT
         DSXSA
         REGS
         END
