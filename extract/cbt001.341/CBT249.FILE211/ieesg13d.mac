*          DATA SET CBT823     AT LEVEL 001 AS OF 11/06/77
  TITLE 'IEESG03D  -  # R  -  LIST DEVICES REQUIRING OPERATOR ACTION'
IEESG03D CSECT
         LR    R12,R15
         USING IEESG03D,R12
         L     R0,SIZE             GETMAIN SIZE
         GETMAIN R,LV=(0)
         LR    R13,R1
         USING WORK,R13
         ST    R14,SAVRET          SAVE RETURN ADDRESS
         ST    R2,XSASAVE
         USING XSA,R2
         LA    R4,UCBTAB
         LA    R5,4000
         LA    R6,=CL8' '
         L     R7,CLEAR
         MVCL  R4,R6               CLEAR UCBTAB TO BLANKS
         L     R4,16               CVT
         L     R4,40(0,R4)         UCB LOOKUP TABLE
         SR    R5,R5
         MVI   TYPE,C'M'           SET DEFAULT OF TAPE MOUNTS
         L     R3,XAL              LOAD PARM ADDRESS
         LTR   R3,R3               ANY PARM SPECIFIED
         BZ    UCBLOOP             NO, USE DEFAULT
         MVC   TYPE(10),0(R3)      SET TYPE FROM PARM
         CLI   TYPE,C'B'           BOTH
         BE    UCBLOOP
         CLI   TYPE,C'D'           DISK
         BE    UCBLOOP
         CLI   TYPE,C'T'           TAPE
         BE    UCBLOOP
         CLI   TYPE,C'P'           UNIT RECORD
         BE    UCBLOOP
         CLI   TYPE,C'U'           UNIT=
         BE    UCBLOOP
         CLI   TYPE,C'S'           VOLSER=
         BE    UCBLOOP
         CLI   TYPE,C'V'           VARIED
         BE    UCBLOOP
         CLI   TYPE,C'R'           RESERVE SEARCH
         BE    RESERVE
         MVI   TYPE,C'M'           TAPE MOUNTS
         EJECT
UCBLOOP  ICM   R5,3,0(R4)          LOAD UCB ADDR
         LTR   R5,R5               TEST ADDR
         BNZ   UCBLOOP2            NOT ZERO, CONTINUE
         SPACE 3
UCBLOOP1 LA    R4,2(0,R4)          NEXT ADDR
         B     UCBLOOP
         SPACE 3
UCBLOOP2 CLC   0(2,R4),=X'FFFF'    END OF TABLE
         BE    PRINT               YES, GO PRINT RESUTLS
         MVI   ASIDSW,X'00'        CLEAR ASID SEARCH SW
         CLI   TYPE,C'V'
         BE    UCBLOOPV
         TM    3(R5),X'88'         ONLINE AND ALLOCATED
         BNO   UCBLOOP1            NO, GET NEXT
         CLI   TYPE,C'D'           DISK
         BE    UCBLOOPD
         CLI   TYPE,C'T'           TAPE
         BE    UCBLOOPT
         CLI   TYPE,C'P'           UNIT RECORD
         BE    UCBLOOPP
         CLI   TYPE,C'U'           UNIT=
         BE    UCBLOOPU
         CLI   TYPE,C'S'           VOLSER=
         BE    UCBLOOPS
         CLI   TYPE,C'M'           MOUNTS
         BE    UCBLOOPM
         B     UCBLOOP3            ASSUME BOTH
         EJECT
UCBLOOPD TM    18(R5),X'20'        DISK
         BZ    UCBLOOP1            NO, GET NEXT
         B     UCBLOOP3
         SPACE 3
UCBLOOPT TM    18(R5),X'80'        TAPE
         BZ    UCBLOOP1            NO, GET NEXT
         B     UCBLOOP3
         SPACE 3
UCBLOOPP TM    18(R5),X'08'        UNIT RECORD
         BZ    UCBLOOP1
         B     UCBLOOP3
         SPACE 3
UCBLOOPU MVI   ASIDSW,X'F0'        FORCE PRINT
         CLC   TYPE+2(3),13(R5)    UNIT MATCH
         BE    UCBLOOP4
         CLI   TYPE+4,C' '         UNIT COMPARE
         BNE   UCBLOOP1            YES, NO MATCH
         CLC   TYPE+2(2),13(R5)    CONTROL UNIT MATCH
         BE    UCBLOOP4
         CLI   TYPE+3,C' '         CONTROL UNIT COMPARE
         BNE   UCBLOOP1            YES, NO MATCH
         CLC   TYPE+2(1),13(R5)    CHANNEL MATCH
         BE    UCBLOOP4
         B     UCBLOOP1
         SPACE 3
UCBLOOPS MVI   ASIDSW,X'F0'        FORCE PRINT
         CLC   TYPE+2(6),28(R5)    VOLSER MATCH
         BNE   UCBLOOP1
         B     UCBLOOP4
         SPACE 3
UCBLOOPM TM    18(R5),X'80'        TAPE
         BZ    UCBLOOP1
         TM    6(R5),X'40'         NOT READY
         BZ    UCBLOOP1
         TM    34(R5),X'01'        MOUNT MSG ISSUED
         BZ    UCBLOOP1
         EJECT
UCBLOOPV TM    3(R5),X'50'         VARIED OR UNLOADED
         BZ    UCBLOOP1            NO, GO GET NEXT
         MVI   ASIDSW,X'F0'        FORCE PRINT
         B     UCBLOOP4
         SPACE 3
UCBLOOP3 MVI   ASIDSW,X'FF'        SET ASID SEARCH SW
         CLI   TYPE+1,C'='         ASID DELIMITED SEARCH
         BE    UCBLOOP4
         MVI   ASIDSW,X'00'        CLEAR ASID SEARCH SW
         CLI   6(R5),X'00'         FLAGS ZERO
         BNE   UCBLOOP4            NO, GOT ONE
         TM    18(R5),X'20'        DISK
         BZ    UCBLOOP1            NO, NO RESERVE CHECK
         CLI   40(R5),X'00'        RESERVE
         BZ    UCBLOOP1            NO, GET NEXT
         SPACE 3
UCBLOOP4 LA    R6,UCBTAB           UCB TABLE
         LA    R7,100              MAX ENTRIES
         SPACE 3
UCBLOOP5 CLI   0(R6),X'40'         EMPTY SLOT
         BE    UCBLOOP7            YES, GO FILL IT
         CLC   0(3,R6),13(R5)      UCB ALREADY IN LIST
         BE    UCBLOOP1            YES, GET NEXT
         SPACE 3
UCBLOOP6 LA    R6,40(0,R6)         NEXT SLOT
         BCT   R7,UCBLOOP5
         B     PRINT
         EJECT
UCBLOOP7 L     R1,20(0,R5)         UCB EXTENSION ADDR
         UNPK  25(5,R6),14(3,R1)   ASID
         NC    25(4,R6),=8X'0F'
         TR    25(4,R6),=C'0123456789ABCDEF'
         MVI   29(R6),C' '
         MVC   22(3,R6),=C'AS='
         BAL   R11,JOBFIND         GO GET JOBNAME
         MVC   0(3,R6),13(R5)      UNIT NAME
         TM    18(R5),X'A0'        TAPE OR DISK
         BZ    *+10
         MVC   5(6,R6),28(R5)      VOLSER
         CLI   5(R6),X'FF'         PRIVAT MOUNT
         BNE   *+10                NO, BYPASS MOVE
         MVC   5(6,R6),=C'PRIVAT'  SET PRIVAT IN MSG
         CLI   5(R6),X'00'         ZERO VOLSER
         BNE   *+10                NO
         MVC   5(6,R6),=CL6'*MTP*'
         MVC   13(7,R6),=CL7'INT REQ'
         TM    6(R5),X'40'         NOT READY
         BO    UCBLOOP1            YES, GET NEXT
         MVC   13(7,R6),=CL7'BUSY'
         TM    6(R5),X'80'         BUSY
         BO    UCBLOOP1
         MVC   13(7,R6),=CL7'CU BUSY'
         TM    6(R5),X'08'         CONTROL UNIT BUSY
         BO    UCBLOOP1
         TM    18(R5),X'20'        DISK
         BZ    *+12
         CLI   40(R5),X'00'        RESERVE COUNT EQUAL ZERO
         BNE   UCBLOOP8
         MVC   13(7,R6),=CL7'ALLOC'
         TM    ASIDSW,X'F0'        PRINT FORCED
         BO    UCBLOOP1            YES
         MVI   0(R6),C' '          FORCE NO PRINT
         B     UCBLOOP1
         SPACE 3
UCBLOOP8 MVC   13(7,R6),=CL7'RSVD-'
         SR    R8,R8
         IC    R8,40(0,R5)         RESERVE COUNT
         CVD   R8,DOUBLE           SAVE RESERVE COUNT
         UNPK  18(2,R6),DOUBLE+6(2)
         OI    19(R6),X'F0'
         B     UCBLOOP1
         EJECT
RESERVE  MVI   RESVFLAG,X'00'
         MVC   RESVMSG(4),=X'004E0100'
         L     R4,16               CVT
         L     R4,640(0,R4)        QCB HEAD
         LTR   R4,R4               ANY QCBS
         BZ    NORESV              NO
         SPACE 1
RESVMAJ  L     R5,8(0,R4)          1ST MINOR QCB
         LTR   R5,R5               ANY QCBS
         BZ    RESVERR
         SPACE 1
RESVMIN  L     R8,8(0,R5)          1ST QEL
         LTR   R8,R8               ANY QELS
         BZ    RESVERR             NO, ERROR IN CHAIN
         SPACE 1
RESVQEL  TM    12(R8),X'10'        RESERVE QEL
         BO    RESVFORM            YES, FORMAT
         SPACE 1
RESVNEXT L     R8,0(0,R8)          NEXT QEL
         LTR   R8,R8               ANY MORE
         BNZ   RESVQEL             YES
         L     R5,0(0,R5)          NEXT MINOR QCB
         LTR   R5,R5               ANY MORE
         BNZ   RESVMIN             YES
         L     R4,0(0,R4)          NEXT MAJOR QCB
         LTR   R4,R4               ANY MORE
         BNZ   RESVMAJ             YES
         SPACE 3
NORESV   CLI   RESVFLAG,X'00'      ANY FOUND
         BNE   EXIT                YES
         LA    R1,NORESMSG         MESSAGE IN WTO FORMAT.
         L     R15,XSW             FIND THE IO ROUTINE.
         BALR  R14,R15             ISSUE THE TPUT/WTO.
         B     EXIT
         SPACE 3
RESVERR  LA    R1,RESERMSG         MESSAGE IN WTO FORMAT.
         L     R15,XSW             FIND THE IO ROUTINE.
         BALR  R14,R15             ISSUE THE TPUT/WTO.
         B     EXIT
         EJECT
RESVFORM MVI   RESVFLAG,X'FF'      FOUND ONE
         MVI   RESVMSG+4,C' '
         MVC   RESVMSG+5(73),RESVMSG+4
         MVC   RESVMSG+24(8),16(R4) MAJOR NAME
         SR    R9,R9
         IC    R9,16(0,R5)         MINOR NAME LENGTH
         CH    R9,=H'44'           MAX LENGTH TO PRINT
         BNH   *+8
         LA    R9,44               FORCE MAX
         BCTR  R9,0
         EX    R9,RESVMOVE         MOVE MINOR NAME
         L     R9,24(0,R8)         UCB ADDR
         MVC   RESVMSG+4(3),13(R9) UNIT NAME
         MVC   RESVMSG+8(6),28(R9) VOLSER
         LR    R1,R8               LOAD QEL ADDR FOR JOBFIND
         LA    R6,RESVMSG-17       FAKE OUT JOBFIND
         MVI   ASIDSW,X'00'
         BAL   R11,JOBFIND         FILL IN JOBNAME
         LA    R1,RESVMSG          MESSAGE IN WTO FORMAT.
         L     R15,XSW             FIND THE IO ROUTINE.
         BALR  R14,R15             ISSUE THE TPUT/WTO.
         B     RESVNEXT
         SPACE 3
RESVMOVE MVC   RESVMSG+33(0),20(R5)
         EJECT
JOBFIND  SR    R14,R14
         ICM   R14,3,14(R1)        ASID
         SLL   R14,2               MULTIPLY ASID BY 4 FOR ASVT ADDR INC
         L     R15,16              CVT
         L     R15,556(0,R15)      ASVT
         L     R15,524(R14,R15)    ASCB
         L     R14,56(0,R15)       CSCB
         CLI   28(R14),X'03'       INIT/JOB
         BNE   JOBFIND2            NO, GO USE THIS CSCB
         LR    R15,R14             SAVE CSCB
         LA    R15,0(0,R15)
         SPACE 3
JOBFIND1 L     R14,0(0,R14)        NEXT CSCB
         LA    R14,0(0,R14)
         LTR   R14,R14             END OF CSCBS
         BNZ   *+10                NO, CONTINUE
         LR    R14,R15             LOAD ORIG CSCB ADDR
         B     JOBFIND2
         CR    R14,R15             SAME CSCB
         BE    JOBFIND2            YES, INIT
         CLC   14(2,R1),30(R14)    ASID MATCH
         BNE   JOBFIND1            NO, GET NEXT
         SPACE 3
JOBFIND2 MVC   32(8,R6),=CL8' '
         LTR   R14,R14             CSCB ZERO
         BZ    JOBFIND3
         MVC   32(8,R6),8(R14)     JOBNAME
         SPACE 3
JOBFIND3 CLI   ASIDSW,X'FF'        ASID SEARCH
         BCR   7,R11               NO, CONTINUE
         CLC   32(8,R6),TYPE+2     JOBNAME MATCH
         BCR   8,R11               YES, CONTINUE
         CLC   25(4,R6),TYPE+2     ASID MATCH
         BCR   8,R11               YES, EXIT
         B     UCBLOOP1
         EJECT
PRINT    MVC   WTOLIST(4),=X'002C0100' SET MSG LEN AND UCM FLAGS
         LA    R6,UCBTAB           UCB TABLE ADDR
         LA    R7,100              MAX ENTRIES
         CLI   0(R6),C' '          ANY TO PRINT
         BE    NOPRINT             NO, GO TELL OPERATOR
         SPACE 3
PRINT1   CLI   0(R6),C' '          END OF PRINT
         BE    EXIT                YES, EXIT
         MVC   WTOLIST+4(40),0(R6)  SET MSG IN AREA
         LA    R1,WTOLIST          MESSAGE IN WTO FORMAT.
         L     R15,XSW             FIND THE IO ROUTINE.
         BALR  R14,R15             ISSUE THE TPUT/WTO.
         LA    R6,40(0,R6)         NEXT TABLE ENTRY
         BCT   R7,PRINT1           GET NEXT
         B     EXIT
         SPACE 3
NOPRINT  MVC   WTOLIST(4),=X'00200100'  SET LENGTH AND UCM FLAGS
         MVC   WTOLIST+4(28),=CL28'NO SELECTED DEVICES FOUND'
         CLI   TYPE,C'M'
         BNE   NOPRINT1
         MVC   WTOLIST+4(28),=CL28'NO MOUNTS PENDING'
         SPACE 3
NOPRINT1 LA    R1,WTOLIST          MESSAGE IN WTO FORMAT.
         L     R15,XSW             FIND THE IO ROUTINE.
         BALR  R14,R15             ISSUE THE TPUT/WTO.
         SPACE 3
EXIT     L     R0,SIZE             GETMAIN SIZE
         LR    R1,R13              AREA ADDRESS
         L     R14,SAVRET          LOAD RETURN ADDR
         FREEMAIN R,LV=(0),A=(1)
         SR    R15,R15
         BR    R14
         EJECT
         LTORG
         SPACE 3
         DS    0F
SIZE     DC    X'F1',AL3(WORKEND-WORKSTRT)
CLEAR    DC    X'40000001'
         DS    0F
NORESMSG DC    X'00200100',CL28'NO RESERVES OUTSTANDING'
RESERMSG DC    X'00200100',CL28'ERROR -  INVALID QCB CHAIN'
         EJECT
WORK     DSECT
WORKSTRT DS    0D
DOUBLE   DS    D
SAVRET   DS    F
XSASAVE  DS    F
WTOLIST  DS    CL44
RESVMSG  DS    CL78
TYPE     DS    CL10
ASIDSW   DS    C
RESVFLAG DS    X
UCBTAB   DS    100CL40
WORKEND  DS    0D
         EJECT
         DSXSA
         REGS
         END
