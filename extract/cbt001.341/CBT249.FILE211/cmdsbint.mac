*          DATA SET CBT561     AT LEVEL 002 AS OF 04/03/77
         TITLE 'COMMAND SUB-SYSTEM INITIALIZATION'
CMDSBINT CSECT
         SAVE  (14,12)
         LR    R3,R15
         USING CMDSBINT,R3
         ST    R13,SAVE+4
         LA    R13,SAVE
         SPACE 1
         MODESET KEY=ZERO,MODE=SUP
         SPACE 1
         L     R0,SIZE             LOAD SIZE AND SUBPOOL
         SPACE 1
         GETMAIN R,LV=(0)
         SPACE 1
         LR    R4,R1               SAVE ADDR OR SSVT AND WORK AREA
         USING AREA,R4
         XC    SSVT(4),SSVT        CLEAR SSVT 1ST 4 BYTES
         MVI   SSVTFNUM+1,X'03'    SET NO. OF SUPPORTED FUNCTIONS
         XC    SSVTFCOD(256),SSVTFCOD  CLEAR FUNCTION MATRIX
         XC    USERWORK(228),USERWORK  CLEAR WORK AREA
         XC    XSA(XSAEND-XSA),XSA CLEAR XSA
         XC    JOBTABLE(240),JOBTABLE CLEAR
         XC    JOBTABLE+240(124),JOBTABLE+240   JOB TABLE
         MVI   JOBEND,X'FF'        SET END OF JOB TABLE
         MVI   SSVTFCOD+8,X'01'    SUPPORT WTO ENTRY
         MVI   SSVTFCOD+9,X'02'    SUPPORT CMD ENTRY
         MVI   SSVTFCOD+255,X'03'  SUPPORT USER MSG ENTRY
         EJECT
         LOAD  EP=CMDSBSYS         LOAD CMD SUB-SYSTEM MODULE
         SPACE 1
         LR    R10,R0              LOAD FROM ADDR
         LA    R11,0(0,R1)         LOAD LENGTH
         SLL   R11,3               MULT NO. OF DOUBLEWORDS BY 8
         SPACE 1
         LOAD  EP=CMDDUMMY         LOAD DUMMY FLPA MODULE ADDR
         SPACE 1
         LR    R8,R0               SAVE
         A     R8,0(0,R10)            FUNCTION 1
         ST    R8,SSVTFUN1               ADDR IN SSVT
         LR    R8,R0               SAVE
         A     R8,4(0,R10)            FUNCTION 2
         ST    R8,SSVTFUN2               ADDR IN SSVT
         LR    R8,R0               SAVE
         A     R8,8(0,R10)            FUNCTION 3
         ST    R8,SSVTFUN3               ADDR IN SSVT
         LR    R8,R0               LOAD TO ADDR
         LA    R9,1024             LOAD LENGTH
         MVCL  R8,R10              MOVE IN SUB-SYS MODULE
         L     R5,16               CVT
         L     R6,0(0,R5)          TCB WORDS
         L     R6,4(0,R6)          CURRENT TCB
         L     R6,12(0,R6)         TIOT
         L     R5,296(0,R5)        JESCT
         L     R5,24(0,R5)         1ST SSCVT ADDR
         EJECT
SSCVT1   CLC   8(4,R5),8(R6)       NAME MATCH
         BE    SSCVT2              YES, CONTINUE
         L     R5,4(0,R5)          NEXT SSCVT
         LTR   R5,R5               ZERO SSCVT
         BNZ   SSCVT1              NO, CONTINUE
         SPACE 1
         ABEND 77,DUMP             INIT ERROR
         SPACE 3
SSCVT2   LA    R7,TABLE            INPUT TABLE
         LA    R12,CMDTABLE        OUTPUT TABLE
         XC    0(84,R12),0(R12)    CLEAR OUTPUT TABLE
         SPACE 3
SSCVT3   MVC   CMDNAME+3(2),1(R7)  FINISH NAME
         SPACE 1
         LOAD  EPLOC=CMDNAME       LOAD IT
         SPACE 1
         CLI   3(R7),C'0'          TEST VERSION
         BNE   SSCVT5              YES, GET DUMMY TO OVERLAY
         ST    R0,0(0,R12)         SAVE ADDRESS
         MVC   0(1,R12),0(R7)      SAVE COMMAND ID
         EJECT
SSCVT4   LA    R7,4(0,R7)          INCR INPUT TABLE
         LA    R12,4(0,R12)        INCR OUTPUT TABLE
         CLI   0(R7),X'00'         END OF INPUT
         BE    SSCVT6              YES, EXIT
         B     SSCVT3              CONTINUE
         SPACE 3
SSCVT5   LR    R10,R0              SAVE TEST MODULE ADDR
         LA    R11,0(0,R1)         SAVE FROM LENGTH
         SLL   R11,3               MULT NO. OF DOUBLEWORDS BY 8
         MVC   TESTNAME+7(1),3(R7) FINISH NAME
         SPACE 1
         LOAD  EPLOC=TESTNAME
         SPACE 1
         ST    R0,0(0,R12)         SAVE ADDRESS
         MVC   0(1,R12),0(R7)      SAVE COMMAND ID
         LR    R8,R0               SAVE DUMMY MODULE ADDR
         LA    R9,2048             SAVE TO LENGTH
         MVCL  R8,R10              MOVE IN TEST MODULE
         B     SSCVT4              NEXT
SSCVT6   ST    R0,XSW              KEEP IO ROUTINE ADDRESS.
         ST    R4,16(0,R5)         SAVE SSVT ADDR IN SSCVT
         L     R13,SAVE+4
         SPACE 1
         RETURN (14,12),RC=0
         EJECT
         LTORG
         SPACE 5
SAVE     DS    18F
SIZE     DC    X'F1',AL3(AREAEND-AREA)
         DS    0D
CMDNAME  DC    CL8'IEEXX13D'
TESTNAME DC    CL8'CMDTESTX'
         EJECT
TABLE    DC    C'N',C'SA',C'0'
         DC    C'L',C'SC',C'0'
         DC    C'D',C'SD',C'0'
         DC    C'A',C'SE',C'0'
         DC    C'F',C'SF',C'0'
         DC    C'R',C'SG',C'0'
         DC    C'S',C'SH',C'0'
         DC    C'V',C'SI',C'0'
         DC    C'I',C'SJ',C'0'
         DC    C'C',C'SK',C'0'
         DC    C'J',C'SL',C'0'
         DC    C'H',C'SM',C'0'
         DC    C'T',C'SN',C'0'
         DC    X'FF',C'SW',C'0'     THE IO ROUTINE MUST BE LAST.
         DC    X'00'
         EJECT
AREA     DSECT
SSVT     DS    H
SSVTFNUM DS    H
SSVTFCOD DS    XL256
SSVTFRTN DS    0F
SSVTFUNC DS    0F
SSVTFUN1 DS    F
SSVTFUN2 DS    F
SSVTFUN3 DS    F
         DS    7F
         SPACE 3
CMDTABLE DS    20F
CMDEND   DS    F
         SPACE 3
         DS    0D
USERWORK DS    0CL228
         DS    CL228
         SPACE 3
XSA      DS    0CL48
         DS    5F
XAL      DS    F
         DS    4F
XAU      DS    X
         DS    XL7
XSW      DS    18F               STANDARDS IO DRIVER AREA.
XSAEND   EQU   *
JOBTABLE DS    30CL12
JOBEND   DS    F
         SPACE 3
AREAEND  DS    0D
         SPACE 3
         REGS
         END
