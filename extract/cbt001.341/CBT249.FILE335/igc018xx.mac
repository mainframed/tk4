DYN      TITLE 'D Y N A M I C    B L D L    S V C    H O O K '
IGC018XX CSECT
*---------------------------------------------------------------------*
*                                                                     *
*                        DYNAMIC BLDL                                 *
*                                                                     *
*   DYNAMIC BLDL IS A LOCAL MOD TO IGC018 (SVC 18 - BLDL)             *
*  WHICH INTERCEPTS ALL BLDL'S DONE TO THE LNKLST DATASETS AND        *
*  STORES THE INFORMATION IN A TABLE.  THIS REDUCES BY GREATER THAN   *
*  95% THE NUMBER OF I/O 'S DONE TO THE THESE DATASETS.               *
*                                                                     *
*                                                                     *
*   DYNAMIC BLDL IS ACTIVIATED AT IPL TIME VIA THE BLDLINIT *HMD 11/82*
*   PROGRAM AND IT MAY BE STOPPED AND RESTARTED AT ANY TIME.*HMD 11/82*
*                                                                     *
*   TO START OR STOP DYNAMIC BLDL, USE THE 'BLDLSW' PROGRAM:          *
*                                                                     *
*     PARMS:  S = START DYNAMIC BLDL (ERROR IF ALREADY STARTED)       *
*             P = STOP  DYNAMIC BLDL (ERROR IF NOT ACTIVE)            *
*                                                                     *
*   THE TABLE IS CLEARED EACH TIME IT IS STOPPED.                     *
*                                                                     *
*   NOTE: THE BLDLSW PROGRAM ISSUES MODESETS AND MUST APF AUTHORIZED. *
*                                                                     *
*   EXAMPLE: USE THE 'BLDLSW' CATALOGED PROCEDURE AS A STARTED TASK:  *
*                                                                     *
*            S BLDLSW,BLDL=P     (STOP DYNAMIC BLDL)                  *
*            S BLDLSW,BLDL=S     (START DYNAMIC BLDL)                 *
*                                                                     *
*                                                                     *
*   CAUTION - IF A LOAD MODULE IS RE-LINKED INTO ANY OF THE LNKLST    *
*             LIBRARIES AND THAT MODULE IS IN THE CURRENT TABLE THEN  *
*             THE OLD MODULE WILL BE USED UNTIL THE TABLE ENTRY FOR   *
*             THAT MODULE IS OVERLAID (THIS COULD BE NEVER IF THE     *
*             MODULE IS FREQUENTLY USED) OR DYNAMIC BLDL IS STOPED    *
*             AND RESTARTED.                                          *
*                                                                     *
*                                                                     *
*   TO LIST THE CURRENT TABLE USE THE 'BLDLST' PROGRAM:               *
*         PARMS:                                                      *
*                                                                     *
*             (NO PARM) = FULL LIST                                   *
*              S        = SUMMARY (HITS, MISSES, SORTS)               *
*                                                                     *
*   EXAMPLE: USE THE CATALOGED PROCEDURE 'BLDLST' AS A STARTED TASK:  *
*                                                                     *
*              S BLDLST            PRODUCES FULL REPORT               *
*              S BLDLST,BLDL=S     PRODUCES SUMMARY REPORT            *
*                                                                     *
*   NOTE: BLDLST DOES NOT ISSUE MODESET MACRO INSTRUCTIONS  *HMD 11/82*
*         AND THEREFOR NEED NOT BE APF AUTHORIZED.          *HMD 11/82*
*---------------------------------------------------------------------*
         EJECT
*---------------------------------------------------------------------*
*       D Y N A M I C    B L D L    P R O G R A M    L O G I C        *
*                                                                     *
*                                                                     *
*                    DYNAMIC BLDL                                     *
*                                                                     *
*     TABLE -      NUMBER OF SORTS                                    *
*                  NUMBER OF HITS                                     *
*                  NUMBER OF MISSES                                   *
*                  200  64 BYTE SLOTS  ( 74  ARE SEMI-FIXED)          *
*                                                                     *
*     EACH SLOT -  RECENT NUMBER OF USES (SINCE LAST SORT)            *
*                  TOTAL NUMBER OF USES (BEFORE LAST SORT)            *
*                  DATA                                               *
*                                                                     *
*                                                                     *
*                                                                     *
* THE TABLE STARTS WITH 200 EMPTY SLOTS AND IS FILLED FROM THE BOTTOM.*
* ONE IS ADDED TO THE RECENT USE COUNT OF AN ENTRY FOR EACH USE OF AN *
* ENTRY.  WHEN 126 SLOTS ARE FILLED THEY ARE SORTED IN ACCENDING SEQU-*
* ENCE FROM THE TOP DOWN AND RECENT IS MOVED TO TOTAL AND ZEROED.     *
* THE LAST  74 EMPTY SOLTS ARE AGAIN FILLED FROM THE BOTTOM, THEN ANY *
* SLOT BETWEEN  74 AND 126 WITH ZERO RECENT IS USED.                  *
*                                                                     *
* SORT-LOOP;  WHEN ALL ENTRIES BETWEEN 0 AND 126 HAVE NON ZERO RECENT *
*             THE ENTRIES ARE SORTED, RECENT IS ADDED TO TOTAL AND    *
*             ZEROED. NEW ENTRIES ARE GOTTEN FROM 0-126 WHEN RECENT   *
*             IS ZERO.    GO TO SORT-LOOP.                            *
*---------------------------------------------------------------------*
         EJECT
*---------------------------------------------------------------------*
*              /* LEVEL=0014 */                             *HMD 05/81*
*                                                           *HMD 01/82*
*                 BASE=JDM1134  (DF/DS PROG PRODUCT)        *HMD 01/82*
*                 CPTF=UZ56064  (CURRENT PTF LEVEL)         *HMD 11/82*
*                                                           *HMD 01/82*
*             ##################################                      *
*             # FOR CHANGES REQUIRED.          #                      *
*             # LATEST MAINTAINCE ACTIVITY:    #            *HMD 05/80*
*             #                                #            *HMD 01/82*
*             # JDM1134     DF/DS PROG PROD    #            *HMD 01/82*
*             #           (THIS IS BASE LEVEL) #            *HMD 01/82*
*             #                                #            *HMD 01/82*
*             # SEE LABELS ST02,RET1, AND RET2 #                      *
*             #                                *            *HMD 01/82*
*             # OFFSETS ARE NOW EQUATED. SEE   #            *HMD 01/82*
*             # THE FOLLOWING SYMBOLS IF YOU   #            *HMD 01/82*
*             # HAVE TO REFIT THIS MODULE:     #            *HMD 01/82*
*             #                                #            *HMD 01/82*
*             # LTEST1, LTEST2, LRET1, LRET2,  #            *HMD 01/82*
*             # LRET3, CONCAT#, LIB, EXSFLG,   #            *HMD 01/82*
*             # EXSSAVE, EXSUN                 #            *HMD 01/82*
*             #                                #            *HMD 01/82*
*             # THEIR VALUES MUST BE UPDATED   #            *HMD 01/82*
*             # WITH THE CORRECT OFFSETS IN    #            *HMD 01/82*
*             # IGC018 CODE                    #            *HMD 01/82*
*             #                                #            *HMD 01/82*
*             # ALSO SEE LABELS:               #            *HMD 05/80*
*             #                                #            *HMD 01/82*
*             #   $CHG01   $CHG06              #            *HMD 01/82*
*             #   $CHG02   $CHG07              #            *HMD 01/82*
*             #   $CHG03                       #            *HMD 01/82*
*             #   $CHG04                       #            *HMD 01/82*
*             #   $CHG05                       #            *HMD 01/82*
*             #                                #            *HMD 01/82*
*             # FOR CHANGES THAT MUST BE MADE  #            *HMD 05/80*
*             # IF THE LEVEL OF 'IGC018' OR    #            *HMD 05/81*
*             # THIS MODULE CHANGES.           #            *HMD 05/81*
*             ##################################                      *
*---------------------------------------------------------------------*
         EJECT                                              *HMD 11/82*
*---------------------------------------------------------------------*
*      H I S T O R Y    L O G:                              *HMD 01/82*
*---------------------------------------------------------------------*
*                                                           *HMD 05/81*
*
*  LAST MODIFIED ON 06/11/80 BY:                            *HMD 05/80*
*                                                           *HMD 05/80*
*       HOWARD M. DEAN                                      *HMD 05/80*
*       GTE DATA SERVICES                                   *HMD 05/80*
*       4750 LINCOLN BLVD.                                  *HMD 05/80*
*       MARINA DEL REY, CA                                  *HMD 05/80*
*                                                           *HMD 05/80*
*  ACTION: INCREASE BLDL TABLE TO 200 ENTRIES 74            *HMD 05/80*
*          ARE SEMI-FIXED.                                  *HMD 05/80*
*                                                           *HMD 05/80*
*  MODIFIED ON 05/04/81 - HOWARD M. DEAN                    *HMD 05/81*
*  ACTION: RETROFIT FOR PTF UZ32516 ON TAPE 8009            *HMD 05/81*
*                                                           *HMD 05/81*
*  LAST MODIFIED ON 01/24/81 BY:
*                                                           *HMD 01/82*
*       HOWARD M. DEAN                                      *HMD 01/82*
*       GTE DATA SERVICES                                   *HMD 01/82*
*       4750 LINCOLN BLVD.                                  *HMD 01/82*
*       MARINA DEL REY, CA                                  *HMD 01/82*
*                                                           *HMD 01/82*
*  ACTION: RETROFIT FOR JDM1134 (DF/DS) PRODUCT             *HMD 01/82*
*                                                           *HMD 01/82*
*       1. CLEAN UP DOCUMENTATION AT THE BEGINNING OF THIS  *HMD 01/82*
*          MODULE.                                          *HMD 01/82*
*                                                           *HMD 01/82*
*       2. MAKE OFFSETS IN REAL SVC (IGC018) INTO           *HMD 01/82*
*          EQUATES FOR EASY MAINTAINCE.                     *HMD 01/82*
*                                                           *HMD 01/82*
*       3. UPDATE MODULE ID TO CURRENT DATE.                *HMD 01/82*
*                                                           *HMD 01/82*
*       4. UPDATE MAINTENCE PROCEDURES BELOW                *HMD 01/82*
*                                                           *HMD 01/82*
*       5. REMOVE OFFSETS FOR ALL OTHER VERSIONS OF         *HMD 01/82*
*          DYNAMIC BLDL.                                    *HMD 01/82*
*                                                           *HMD 01/82*
*  NOTE: THIS RETROFIT (DF/DS BASE - JDM1134) IS TO BE USED *HMD 01/82*
*        AS A BASE OF IGC018XX. ALL PREVIOUS OFFSETS HAVE   *HMD 01/82*
*        BEEN DELETED. IN FUTURE MAINTAINCE, THE OLD VALUES *HMD 01/82*
*        OF THE VARIOUS EQUATES WILL BE COMMENTED OUT       *HMD 01/82*
*        SO THAT A HISTORY OF OFFSETS WILL BE MAINTAINED.   *HMD 01/82*
*                                                           *HMD 01/82*
*  LAST MODIFIED ON 01/24/81 BY:
*                                                           *HMD 01/82*
*       HOWARD M. DEAN                                      *HMD 01/82*
*       GTE DATA SERVICES                                   *HMD 01/82*
*       4750 LINCOLN BLVD.                                  *HMD 01/82*
*       MARINA DEL REY, CA                                  *HMD 01/82*
*                                                           *HMD 01/82*
*  ACTION: RETROFIT FOR UZ37480 TAPE 8107                   *HMD 01/82*
*                                                           *HMD 01/82*
*  LAST MODIFIED ON 11/09/82 BY:                            *HMD 11/82*
*                                                           *HMD 11/82*
*       HOWARD M. DEAN                                      *HMD 11/82*
*       GTE DATA SERVICES                                   *HMD 11/82*
*       4750 LINCOLN BLVD.                                  *HMD 11/82*
*       MARINA DEL REY, CA                                  *HMD 11/82*
*                                                           *HMD 11/82*
*  ACTION:                                                  *HMD 11/82*
*                                                           *HMD 11/82*
*  1. RETROFIT FOR UZ56064 ON TAPE 8206                     *HMD 11/82*
*                                                           *HMD 11/82*
*  2. ADD TWO MORE EQUATES TO ELIMINATE HARD CODED OFFSETS  *HMD 11/82*
*     LABELS (IOBSTDRD AND ECB) IN MODULE IGC018            *HMD 11/82*
*                                                           *HMD 11/82*
*  LAST MODIFIED ON 11/24/82 BY:                            *HMD 11/82*
*                                                           *HMD 11/82*
*       HOWARD M. DEAN                                      *HMD 11/82*
*       GTE DATA SERVICES                                   *HMD 11/82*
*       4750 LINCOLN BLVD.                                  *HMD 11/82*
*       MARINA DEL REY, CA                                  *HMD 11/82*
*                                                           *HMD 11/82*
*  ACTION:                                                  *HMD 11/82*
*                                                           *HMD 11/82*
*  1. ELIMINATE NUCLEUS LINK AND LOCAL MOD TO SYSGEN        *HMD 11/82*
*     MACRO.                                                *HMD 11/82*
*                                                           *HMD 11/82*
*  2. DYNAMIC BLDL IS NOW STARTED VIA PROC BLDLINIT FROM    *HMD 11/82*
*     COMMND00 AT IPL TIME.                                 *HMD 11/82*
*                                                           *HMD 11/82*
*  3. COMPLETE RE-WORK OF FRONT OF MODULE TO ADD            *HMD 11/82*
*     RELOCATION DICTIONARY SO THAT MODULE AN BE            *HMD 11/82*
*     DYNAMICALLY RELOCATED AT INITIALIZATION TIME.         *HMD 11/82*
*                                                           *HMD 11/82*
*---------------------------------------------------------------------*
         EJECT                                              *HMD 05/80*
*
*  IGC018 ENTERED IN SYSTEM KEY
*
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
*************************************************************HMD 01/82*
**  EQUATES FOR OFFSETS IN IGC018 CODE                      *HMD 01/82*
**  CURRENT LEVEL IS UZ56064 (TAPE 8206) WITH 5740-AM7      *HMD 01/82*
**  ALL 'LABELS' REFERENCED BELONG TO IGC018 CODE           *HMD 01/82*
*************************************************************HMD 01/82*
LTEST1   EQU   X'758' LOCATION OF 'READIT' LABEL    UZ56064 *HMD 11/82*
*******  EQU   X'74C' LOCATION OF 'READIT' LABEL    UZ37480 *HMD 01/82*
*******  EQU   X'68C' LOCATION OF 'READIT' LABEL    JDM1134 *HMD 01/82*
LTEST2   EQU   X'7C4' LOCATION OF 'DFOUND' LABEL    UZ56064 *HMD 11/82*
*******  EQU   X'7B8' LOCATION OF 'DFOUND' LABEL    UZ37480 *HMD 01/82*
*******  EQU   X'6F8' LOCATION OF 'DFOUND' LABEL    JDM1134 *HMD 01/82*
LRET1    EQU   X'770' LOCATION OF 'BYWAIT' LABEL    UZ56064 *HMD 11/82*
*******  EQU   X'762' LOCATION OF 'BYWAIT' LABEL    UZ37480 *HMD 01/82*
*******  EQU   X'6A4' LOCATION OF 'BYWAIT' LABEL    JDM1134 *HMD 01/82*
LRET2    EQU   X'766' LOCATION OF WAIT MACRO        UZ56064 *HMD 11/82*
*******  EQU   X'75A' LOCATION OF WAIT MACRO        UZ37480 *HMD 01/82*
*******  EQU   X'69C' LOCATION OF WAIT MACRO        JDM1134 *HMD 01/82*
LRET3    EQU   X'7D0' LOCATION OF RETURN - DFOUND   UZ56064 *HMD 11/82*
*                     SEE $CHG05 FOR MORE INFO      JDM1134 *HMD 11/82*
*******  EQU   X'7C4' LOCATION OF RETURN - DFOUND   UZ37480 *HMD 01/82*
*******  EQU   X'706' LOCATION OF RETURN - DFOUND   JDM1134 *HMD 01/82*
CONCAT#  EQU   X'141' LOC OF 'CONCAT#' IN SAVE AREA JDM1134 *HMD 01/82*
LIB      EQU   X'142' LOC OF 'LIB'     IN SAVE AREA JDM1134 *HMD 01/82*
ECB      EQU   X'144' LOC OF 'ECB'     IN SAVE AREA JDM1134 *HMD 11/82*
IOBSTDRD EQU   X'148' LOC OF 'IOBSTDRD' - SAVE AREA JDM1134 *HMD 11/82*
EXSFLG   EQU   X'61'  LOC OF 'EXSFLG'  IN XSA       JDM1134 *HMD 01/82*
EXSUN    EQU   X'62'  LOC OF UNUSED AREA IN XSA     JDM1134 *HMD 01/82*
EXSSAVE  EQU   X'74'  LOC OF 'EXSSAVE' IN XSA       JDM1134 *HMD 01/82*
         EJECT                                              *HMD 05/80*
* USE UNEQUATED REGISTER NOTATION TO DENOTE SVC-REG VALUES
         USING *,R6
ST01     B     ST02
$LEVEL   EQU   *                    LEVEL EQUATE FOR BLDL   *HMD 11/82*
         DC    AL1(L'$CHG01)        LENGTH OF LEVEL STAMP   *HMD 11/82*
$CHG01   DC    C'IGC018XX11-24-82'  CODE ID (CURRENT LEVEL) *HMD 01/82*
COPYR    DC    C' COPYRIGHT (C) HOWARD M. DEAN 1982,1983'   *HMD 11/82*
**                                                          *HMD 11/82*
* DYNAMIC RELOCATION DICTIONARY - OFFSET HARDCODED IN BLDLINIT
**                                                          *HMD 11/82*
$DYNRLD  DC    A(IGC018AD-IGC018XX)    REAL IGC018 EP       *HMD 11/82*
         DC    A(LINKADDR-IGC018XX)    LINKTAB ADCON        *HMD 11/82*
         DC    A(LINKTAB-IGC018XX)     LINKTAB ADDRESS      *HMD 11/82*
         DC    A(ENTRY1-IGC018XX)      ENTRY1 OFFSET        *HMD 11/82*
         DC    A(NWB1-IGC018XX)        NWB1 ADCON           *HMD 11/82*
         DC    A(ENTRY2-IGC018XX)      ENTRY2 ADDRESS       *HMD 11/82*
         DC    A(NWB2-IGC018XX)        NWB2 ADCON           *HMD 11/82*
         DC    A(TABSTAT1-IGC018XX)    TABSTAT1 ADDRESS     *HMD 11/82*
         DC    A(START1-IGC018XX)      START1   OFFSET      *HMD 11/82*
         DC    A(LASTENT1-IGC018XX)    LASTENT1 OFFSET      *HMD 11/82*
         DC    A(TABEND1-IGC018XX)     TABEND1  OFFSET      *HMD 11/82*
         DC    A(LASTSLT1-IGC018XX)    LASTSLT1 OFFSET      *HMD 11/82*
         DC    A(TABSLOT1-IGC018XX)    TABSLOT1 OFFSET      *HMD 11/82*
         DC    A((START1+ENT1X*64)-IGC018XX)                *HMD 11/82*
         DC    A($$$DIS01-IGC018XX)    $$$DIS01 OFFSET      *HMD 11/82*
         DC    A($$$DIS02-IGC018XX)    $$$DIS02 OFFSET      *HMD 11/82*
         DC    A($$$DIS04-IGC018XX)    $$$DIS04 OFFSET      *HMD 11/82*
ST02     MVI   ST01+3,ST03-ST01        BYPASS INISH HEREAFTER
         L     R12,IGC018AD            ADDR BLDL MODULE
*                                                           *HMD 01/82*
* OVERLAY CODE AT LABEL 'READIT' IN IGC018                  *HMD 01/82*
* HOWEVER VERIFY THE CODE FIRST                             *HMD 01/82*
*                                                           *HMD 01/82*
         CLC   LTEST1(12,R12),READIT   ** DF/DS JDM1134  01/23/82/HMD
         BNE   ST03                    IF VERIFY FAILS      *HMD 01/82*
         CLC   LTEST2(12,R12),DFOUND   ** DF/DS JDM1134  01/23/82/HMD
         BNE   ST03                    IF VERIFY FAILS      *HMD 01/82*
*  PRE-EXCP  TRAP - LABEL 'READIT' IN IGC018
         MVC   LTEST1(12,R12),PATCH1   ** DF/DS JDM1134  01/23/82/HMD
*  POST-EXCP TRAP - LABEL 'DFOUND' IN IGC018
         MVC   LTEST2(12,R12),PATCH2   ** DF/DS JDM1134  01/23/82/HMD
ST03     L     R6,IGC018AD             TRUE BLDL ADDR.
         BR    R6                      GO TO BLDL
         DROP  R6
         EJECT
*
* 'BLDL' TO ISSUE EXCP, R6 ADDR SVRB EXT S.A. , WE USE +X'62'
*
*
*   OVER WRITE BLDL CODE FROM LABEL 'READIT'                *HMD 01/82*
*   NOTE WE ENTER IN USER PROTECT KEY
*        UNLESS REQUEST IS FOR CVT-LINKLIB-DCB,THEN PK ZERO
*
         DS    0H
         USING *,R9
NWB1     TM    EXSFLG(6),X'A0'   Q.SYSTEM LINKLIB AND BLDL  *HMD 01/82*
$CHG02   DS    0H             FLAG FOR CHANGES TO IGC018XX  *HMD 01/82*
*
* NOTE: BELOW DISPLACEMENT ($$$DIS01) IS USED BY BLDLSW PROGRAM
*       TO START AND STOP DYNAMIC BLDL. ITS OFFSET IS STORED IN
*       THE RELOCATION DICTIONARY AT THE BEGINNING OF THIS MODULE.
*                                                           *HMD 11/82*
*       AS LONG AS THE 'RLDSECT' DSECT IS KEPT CURRENT,     *HMD 11/82*
*       THEN ALL WILL WORK FINE.                            *HMD 11/82*
*
$$$DIS01 BNO   RET1           NO ,NOT INTERESTED
         TM    EXSUN(6),X'C0'   Q.FIRST TIME                *HMD 01/82*
         BNZ   RET1               NO ,LIST OR NTH CONCATENATION
         CLI   LIB(13),1        Q.CHECK LINKLIB             *HMD 01/82*
         BNE   RET1                NO ,PLAY SAFE                75035
         CLC   =H'1',0(10)      Q.MUTIPLE BLDL
         BNE   NOTOURS            YES,FLAG AS NOT OURS AND RETN
         TM    3(7),X'40'       Q.NORMAL SORT OF MODULE NAME
         BNO   NOTOURS            NO ,FLAG AS NOT FOR US
NWB1COMM STM   2,7,80(13)         SAVE WORKREGS IN BLDL SAVEAEA
         L     R0,0(7)            1ST WORD OF MODULE NAME
         L     R5,4(7)            2ND
         L     R4,LINKADDR        ADDR.LINKLIB LIBRARY TABLE
         USING TABHEAD,R4
RETRY1   LM    R1,R3,TABSTART     REGS FOR  'BXLE'
         USING ENTRY,R1
FINDMOD  C     R0,DATA          Q.1ST WORD MATCH
         BNE   BXLE               NO TRY NEXT ENTRY
         C     R5,DATA+4        Q.2ND WORD MATCH
         BE    MATCH              YES FOUND IT IN A SLOT
BXLE     BXLE  R1,R2,FINDMOD      NO ,TRY ALL THE SLOTS
         L     R3,MISSES          KEEP STATS ON             *HMD 05/80*
         LA    R3,1(,R3)          HOW MANY LOOKUPS
         ST    R3,MISSES          FAIL PER TABLE            *HMD 05/80*
         OI    EXSUN(6),X'40'     FLAG SVRB AS NOT FOUND    *HMD 01/82*
         B     RESTORE1           EXIT
MATCH    LR    R3,R0              1ST WORD OF NAME
         X     R3,HEX40           SET TO 'LOCKED' FORMAT
SER1     CS    R0,R3,DATA       Q.SET LOCK FOR ENTRY
         BNE   RETRY1             NO ,LOST IT RE-LOOKUP
         C     R5,DATA+4        Q.CHECK REALLY OUR ENTRY
         BE    UPDATE1            YES ,USE IT
UNSERIA  OI    DATA+3,X'40'       NO ,LOST IT, RESET LOCK
         B     RETRY1             LOOKUP AGAIN
UPDATE1  LH    R3,RECENT          MAINTAIN
         LA    R3,1(,R3)          STATISTICS
         STH   R3,RECENT          ON SLOT USE
         SH    R2,H5              CALC. OFFSET FOR CONCATENATION
         IC    R3,DATA(R2)        CONCAT. NO.
         STC   R3,CONCAT#(13)     SAVE FOR 'BLDL'           *HMD 01/82*
         MVC   2(64,13),DATA      MOVE PDS DATA TO WORKAREA
UNSERIB  OI    DATA+3,X'40'       RESET LOCK FOR SLOT
         OI    5(13),X'40'        RESET NAME IN WORKAREA
         L     R3,HITS            MAINTAIN                  *HMD 05/80*
         LA    R3,1(,R3)          TABLE
         ST    R3,HITS            STATISTICS                *HMD 05/80*
         LA    8,2(,13)           **BLDL** ADDR DATA IN WORKAREA
         LA    14,31              **BLDL** SET MASK FOR CONCATN.
         B     RESTORE2           RESUME AFTER EXCP DONE IN BLDL
NOTOURS  OI    EXSUN(6),X'80' FLAG AS NOT OURS              *HMD 01/82*
         B     RET1
RESTORE1 LM    2,7,80(13)     RESTORE WORK REGISTERS
RET1     L     R9,EXSSAVE(6)  RESTORE BASE REG              *HMD 01/82*
         SPACE 2                                            *HMD 01/82*
$CHG03   DS    0H                                           *HMD 01/82*
***********************************************************************
*  THIS CODE IS FROM BLDL (IGC018)                          *HMD 01/82*
*  IF THE LEVEL OF IGC018 CHANGES, READ THE MICROFICHE      *HMD 01/82*
*  AND APPROPRIATELY CHANGE THIS CODE                       *HMD 01/82*
*                                                           *HMD 01/82*
*  SYMBOLIC CODE FRAGMENT IS:                               *HMD 01/82*
*                                                           *HMD 01/82*
*  READIT EQU *               CONTINUE                      *HMD 01/82*
*         EXCP  IOBSTDRD      READ IN THE CORRECT BLOCK     *HMD 01/82*
* +       LA    1,IOBSTDRD           LOAD PARAMETER REG 1   *HMD 01/82*
* +       SVC   0                    ISSUE SVC FOR EXCP     *HMD 01/82*
*         TM    ECB,COMPLETE  DID ECB GET POSTED?           *HMD 01/82*
*                                                           *HMD 01/82*
***********************************************************************
READIT   LA    1,IOBSTDRD(,13)    **BLDL**  JDM1134         *HMD 11/82*
         SVC   0                  **BLDL**  JDM1134         *HMD 11/82*
         TM    ECB(13),X'40'      **BLDL**  JDM1134         *HMD 11/82*
         SPACE 2                                            *HMD 01/82*
         BC    1,LRET1-2(,12)     **BLDL**  JDM1134         *HMD 11/82*
* END OF BLDL CODE                                          *HMD 01/82*
         B     LRET2-2(,12)       JDM1134 (DF/DS)           *HMD 11/82*
         DROP  R9
         EJECT
*
* IGC018 DIRECTORY SEARCH FOUND MODULE
*
NWB2     DS    0H
         USING *,R9
         TM    EXSUN(6),X'40'  Q.DID WE SHOW INTEREST(NOMATCH)
$CHG04   DS    0H            FLAG FOR CHANGES TO IGC018XX   *HMD 01/82*
*
* NOTE: BELOW DISPLACEMENT ($$$DIS02) IS USED BY BLDLSW PROGRAM
*       TO START AND STOP DYNAMIC BLDL. IT IS CONTAINED IN THE
*       $DYNRLD BLOCK AT THE BEGINNING OF THIS MODULE. AS LONG
*       AT THE 'RLDSECT' DSECT REFLECTS THE $DYNRLD BLOCK AT
*       THE BEGINNING OF THIS MODULE THEN ALL WILL BE FINE. *HMD 11/82*
*
$$$DIS02 BNO   RET2               NO
         TM    EXSFLG(6),X'A0' Q.BLDL FOR MANAGED LIBRARY   *HMD 01/82*
         BNO   RET2                NO ,NO INTEREST              75035
         CLI   LIB(13),1           Q.DOUBLE CHECK           *HMD 01/82*
         BNE   RET2                NO ,PLAY SAFE                75035
SETSAVE  MVC   2(64,13),0(8)      PUT DATA TO WORKAREA START
         LA    8,2(,13)           TELL IGC018
         STM   2,7,80(13)         WORK REGS
         L     R4,LINKADDR    ADDRESS OUR TABLE
         LA    R4,0(,R4)      DENOTE FIRST PASS
GETSLOT  L     R3,TABSLOTF    SCAN LIMIT ,LOW ADDR
         L     R2,TABSTART+4  ENTRY SIZE
         LCR   R2,R2          COMPLEMENT FOR   'BXH'
GETSLOTX L     R0,LASTSLOT    SCAN LIMIT ,HIGH ADDR
GETSLOTY SR    R5,R5          ZERO FOR    'CH'
         LR    R1,R0          INISH FOR
TESTSLOT CH    R5,RECENT    Q.SLOT AVAIL
         BE    MAYBE2         YES, PERHAPS
BXH      BXH   R1,R2,TESTSLOT NO, TRY NEXT SLOT
         LTR   R4,R4        Q.IST OR 2ND PASS
         BP    SORTCNTL       1ST
         B     SORTDO         2ND
MAYBE2   L     R3,DATA        1ST WORD OF MODULE NAME
         LTR   R3,R3        Q.VIRGIN ENTRY
         BNZ   MAYBE2A        NO, CONTINUE
         LR    R5,R3          YES, SET UP VALUE TO GRAB IT
         BCTR  R5,0
         CS    R3,R5,DATA   Q.CAN WE GRAB IT
         BNE   GETSLOT        NO ,GET ANOTHER SLOT
         B     MAYBE2         YES , LOOP BACK TO USE IT
MAYBE2A  O     R3,HEX40       SETUP 'UNLOCKED' FORM
         LR    R5,R3          COPY
         X     R5,HEX40       'MANAGED' FORM
SER2     CS    R3,R5,DATA   Q.TRY TO MANAGE SLOT
         BE    MANAGED2       YES SUCCEEDED
         L     R3,TABSLOTF    NO ,FAILED , CONTINUE SEARCH
         SR    R5,R5          RESET REGS FIRST
         B     BXH            TREAT AS NOT FOUND
MANAGED2 AR    R2,R1              ADDR. PREDECESSOR ENTRY
         C     R2,TABSLOTF    Q.IS THIS IN UPDATE AREA
         BL    OUTRANGE       NO ,SO DONT UPDATE POINTER
         CS    R0,R2,LASTSLOT     UPDATE SCAN LIMIT
OUTRANGE SR    R5,R5              NEW USER COUNT FORMAT
         CH    R5,RECENT        Q.CHECK WE REALLY HAVE SLOT
         BE    USESLOT            YES, USE IT
UNSER2A  OI    DATA+3,X'40'       NO, RELEASE 'UNLOCK' SLOT
         B     GETSLOT            TRY AGAIN
USESLOT  L     R5,=X'00010000'
         ST    R5,RECENT          SET NEW USER COUNT
         L     R3,TABSIZE         SIZE OF SLOT
         SH    R3,H5              SUBTRACT HEADER
         IC    R2,CONCAT#(13)     CONCAT. NO.               *HMD 01/82*
         LA    R5,DATA(R3)        ADDR. LOCN. TO SAVE IT
         SH    R3,H4              DONT MOVE FIRST (LOCK) WORD
         EX    R3,DATAMOVE        MOVE DATA EXCEPT 1ST WORD
         STC   R2,0(,R5)          SAVE CONCAT. NO.
UNSER2B  MVC   DATA(4),0(8)       1ST WORD OF NAME,RELEASE LOCK
         B     RESTORE2           EXIT
DATAMOVE MVC   DATA+4(1),4(8) MOVE TTR DATA TO OUR ENTRY
SORTCNTL ICM   R4,8,=X'FF'        SET R4 NEG. FOR 2ND PASS
         MVC   LASTSLOT,TABEND    RESET SCAN LIMIT
         B     GETSLOT            TRY AGAIN
SORTDO   LA    R3,0(,R4)          CLEAR TOP BYTE
         ICM   R4,8,=X'40'        SET FOR 'LOCK'    'CS'
         LA    R1,LINKADDR        POINT TO CTL WORD
SER3     CS    R3,R4,0(R1)      Q.OURS AND 'UNLOCKED'
         BE    SORTIT             YES,AND LOCKED SO SORT IT
*                                 NO,GIVE UP QUIETLY
RESTORE2 LM    2,7,80(13)      WORKREGS
RET2     L     R9,EXSSAVE(6)   RESTORE OUR BASE REGISTER    *HMD 01/82*
         EJECT                                              *HMD 11/82*
$CHG05   DS    0H              FLAG FOR BLDL CHANGES        *HMD 01/82*
***********************************************************************
*  THIS CODE IS FROM BLDL (IGC018)                          *HMD 01/82*
*  IF THE LEVEL OF IGC018 CHANGES, READ THE MICROFICHE      *HMD 01/82*
*  AND APPROPRIATELY CHANGE THIS CODE                       *HMD 01/82*
*                                                           *HMD 01/82*
*  SYMBOLIC CODE FRAGMENT IS:                               *HMD 01/82*
*                                                           *HMD 01/82*
*  DFOUND MVC   PDS2TTRP,PDS1TTRP     MOVE TTR              *HMD 01/82*
*         IC    RX,PDS1INDC          GET C FROM DIRECTORY   *HMD 01/82*
*         NR    RX,RZ                MASK OFF FLAG BITS     *HMD 01/82*
*                                                           *HMD 01/82*
*  RETURN INLINE TO STATEMENT:  (EQUATED TO LRET3)          *HMD 01/82*
*         LA    RY,PDS2USRD-PDS2                            *HMD 01/82*
*                                                           *HMD 01/82*
***********************************************************************
DFOUND   MVC   8(3,7),8(8)        **BLDL** OVERWRITTEN INSTR.
         IC    4,11(,8)           **BLDL**                  *HMD 01/82*
         NR    4,14               **BLDL**
* END OF BLDL CODE                                          *HMD 01/82*
         BC    15,LRET3-2(,12)    JDM1134                   *HMD 11/82*
* ABOVE BRANCH GOES TO 'LA RY,PDS2USED-PDS2' IN BLDL CODE   *HMD 01/82*
         EJECT
SORTIT   LH    R1,SORTS           MAINTAIN
         LA    R1,1(,R1)          TABLE SORT
         STH   R1,SORTS           STATISTICS
         LA    R7,X'40'           BIT PATTERN FOR LOCKING
SORTLOCK LM    R1,R3,TABSTART     LOCK UNLOCKED SLOTS TO US
SORTX    L     R5,DATA            PICKUP LOCK WORD
         LTR   R5,R5               Q.ENTRY A VIRGIN             75035
         BZ    SORTFLGL            YES,ALREADY MANAGED FMT      75035
         OR    R5,R7              ENSURE 'UNLOCKED' FORM
         LR    R6,R5              SAVE IT
         XR    R6,R7              SET 'LOCKED' FORM
         CS    R5,R6,DATA       Q.TRY TO LOCK SLOT
         BNE   LOCKBXLE           NO, TRY NEXT
SORTFLGL DS    0H                                               75035
         OI    RECENT,X'80'       YES,SPECIAL FLAG FOR WE OWN
LOCKBXLE BXLE  R1,R2,SORTX        ENSURE ALL SLOTS TRIED FOR
         LH    R7,ENTRIES         NO. OF PAIRS IS NO. OF SLOTS-1
         LR    R6,R2              SIZE
         BCTR  R6,0               SIZE-1 FOR 'EX'
         L     R3,TABEND          LAST SLOT
OUTER    L     R1,TABSTART        SET START RANGE
         SR    R3,R2              DO 1 LESS PAIR EACH LOOP
INNER    L     R0,RECENT          COUNT -VE IF OURS             77082
         C     R0,RECENT(2)     Q.SHOULD WE SWAP                77082
         BH    LOOPIN             NO, DO NOT                    75035
         LTR   R0,R0            Q.YES, IS IT 'LOCKED' TO US
         BNM   LOOPIN             NO, CAN NOT
         LA    R5,0(R2,R1)        YES,ADDR 2ND SLOT
         EX    R6,SWAP1           JUXTAPOSE
         EX    R6,SWAP2           OUT OF ORDER
         EX    R6,SWAP1           SLOTS
LOOPIN   BXLE  R1,R2,INNER        DO FOR ALL PAIRS IN SCAN
HOUSEKEP TM    RECENT,X'80'     Q.IS THIS OUR SLOT, AS FINISHED
         BNO   RESETUSE           NO, SKIP UNLOCK CODE
         NI    RECENT,X'7F'       YES ,REMOVE OURBIT
         OI    DATA+3,X'40'       RELEASE LOCK
RESETUSE LH    R0,TOTAL           PREV. COUNT
         AH    R0,RECENT          NEW COUNT
         ST    R0,RECENT          UPDATE PREV. ,ZEROISE RECENT
         LTR   R7,R7            Q.SPECIAL CASE ,TOP SLOT
         BE    SORTDONE           YES, SPECIAL HOUSEKEEPING ENTRY
         BCT   R7,OUTER         Q.NO ,CARRY ON SCANS IF ANY
         L     R1,TABSTART        NONE LEFT, RESET TO ENTRY
         B     HOUSEKEP           UPDATE 1ST ENTRY IN TABLE
SWAP1    XC    0(1,R5),0(R1)
SWAP2    XC    0(1,R1),0(R5)
SORTDONE MVC   LASTSLOT,TABEND    RESET SCAN LIMIT
LINKFREE MVI   LINKADDR,0         YES UNSERIALISE SORT WISE
         LA    R4,0(,R4)          DENOTE FIRST SCAN
TABFREE  B     GETSLOT            GO GET SLOT FOR OUR APPLICATION
         DROP  R9
         EJECT
LINKADDR DC    A(0)               A(LINKTAB)                *HMD 11/82*
         SPACE 2                                            *HMD 11/82*
PATCH1   BALR  9,0                                          *HMD 11/82*
         L     9,6(9)                                       *HMD 11/82*
         BR    9                                            *HMD 11/82*
ENTRY1   DC    A(0)               A(NWB1)                   *HMD 11/82*
         SPACE 2                                            *HMD 11/82*
PATCH2   BALR  9,0                                          *HMD 11/82*
         L     9,6(9)                                       *HMD 11/82*
         BR    9                                            *HMD 11/82*
ENTRY2   DC    A(0)               A(NWB2)                   *HMD 11/82*
         SPACE 4
$CHG06   EQU   *                                            *HMD 01/82*
$$$DISAD DS    0F
IGC018AD DC    A(0)                 FILLED IN BY BLDLINIT   *HMD 11/82*
H5       DC    H'5'
H4       DC    H'4'
HEX40    DC    X'00000040'
         LTORG
         EJECT
**                                                          *HMD 05/80*
** DYNAMIC BLDL TABLE                                       *HMD 05/80*
**                                                          *HMD 05/80*
ENT1     EQU   200  100 10           SLOTS (CURRENT OLD OLD)    75035
ENT1X    EQU    74   37  3           FIXED PART(ENT1/2.71828)   75035
*                                                           *HMD 06/80*
LINKTAB  DS    0D
$CHG07   EQU   *                                            *HMD 01/82*
* NOTE: BELOW DISPLACEMENT ($$$DIS04) IS USED BY BLDLSW PROGRAM
*       TO START AND STOP DYNAMIC BLDL. IT IS ALSO USED BY THE
*       BLDLST PROGRAM TO LOCATE THE DYNAMIC BLDL TABLE.
*                                                           *HMD 11/82*
*       THE OFFSET OF $$$DIS04 IS CONTAINED IN THE RLD      *HMD 11/82*
*       BLOCK AT THE BEGINNING OF THIS MODULE. AS LONG      *HMD 11/82*
*       AS THE 'RLDSECT' DSECT IS KEPT UP TO DATE, THEN     *HMD 11/82*
*       ALL SHOULD WORK FINE.                               *HMD 11/82*
*                                                           *HMD 11/82*
$$$DIS04 EQU   *
*                                                           *HMD 11/82*
* DYNAMICALLY RELOCATED BLDL TABLE                          *HMD 11/82*
*                                                           *HMD 11/82*
TABSTAT1 DC    A(0)                   A(START1)             *HMD 11/82*
TABSIZE1 DC    F'64'                                        *HMD 11/82*
TABEND1  DC    A(0)                   A(LASTENT1)           *HMD 11/82*
LASTSLT1 DC    A(0)                   A(LASTENT1)           *HMD 11/82*
TABSLOT1 DC    A(0)                   A(START1+ENT1X*64)    *HMD 11/82*
SORT1    DC    H'0'                   LEAVE AT HALFWORD     *HMD 05/80*
ENTRIES1 DC    AL2(ENT1)
HITS1    DC    F'0'                   INCREASE TO FULLWORD  *HMD 05/80*
MISSES1  DC    F'0'                   INCREASE TO FULLWORD  *HMD 05/80*
         DS    0F                      ENSURE ALIGNED           77082
START1   DC    ((ENT1-1)*64)X'0'
LASTENT1 DC    64X'0'
LINKEND  DS    0F
         EJECT
TABHEAD  DSECT
TABSTART DS    F
TABSIZE  DS    F
TABEND   DS    F
LASTSLOT DS    F
TABSLOTF DS    F
SORTS    DS    H
ENTRIES  DS    H
HITS     DS    F                     INCREASE TO FULLWORD   *HMD 05/80*
MISSES   DS    F                     INCREASE TO FULLWORD   *HMD 05/80*
         SPACE 2
ENTRY    DSECT
RECENT   DS    H
TOTAL    DS    H
DATA     DS     0F
         END
