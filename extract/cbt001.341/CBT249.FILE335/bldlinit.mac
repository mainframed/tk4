BLDLINIT TITLE 'BLDLINIT - PROGRAM TO INITIALIZE DYNAMIC BLDL'
***********************************************************************
* MODULE                                                              *
*   NAME:   BLDLINIT                                                  *
*                                                           *HMD 11/82*
* DECRIPTIIVE                                               *HMD 11/82*
*    NAME:   INITIALIZE DYNAMIC BLDL                        *HMD 11/82*
*                                                                     *
* FUNCTION: BLDLINIT IS STARTED AT IPL TIME FROM COMMND00 TO          *
*           INITIALIZE DYNAMIC BLDL FOR THE LIFE OF AN IPL.           *
*                                                           *HMD 11/82*
*           BLDLINIT LOADS THE DYNAMIC BLDL SVC HOOK (IGC018XX)       *
*           INTO FIXED STORAGE IN SQA AND PLACES THE ADDRESS IN       *
*           THE MVS SVC TABLE. THE ADDRESS OF IGC018 IS PLACED        *
*           IN IGC018XX AT OFFSET IGC018AD. ALL ADCONS ARE RELOCATED  *
*           DYNAMICALLY IN IGC018XX VIA AN ADDRESS VECTOR TABLE       *
*           LOCATED AT THE BEGINNING OF IGC018XX. AVT IS MAPPED VIA   *
*           THE 'RLDSECT' DSECT AT THE END OF THIS MODULE.            *
*                                                                     *
* PURPOSE:  THE PURPOSE OF THIS PROGRAM IS TO AVOID A       *HMD 11/82*
*           RE-LINK OF THE MVS NUCLEUS AND ASSOCIATED       *HMD 11/82*
*           LOCAL MODIFICATION TO A SYSGEN MACRO.           *HMD 11/82*
*           HOWEVER, SMP STILL HAS TO BE AWARE OF A CHANGE  *HMD 11/82*
*           TO IGC018 SO THAT THE HOOK (IGC018XX) CAN       *HMD 11/82*
*           BE ALTERED WHENEVER BLDL CHANGES. A SIMPLE      *HMD 11/82*
*           MODIFICATION THAT REPLACES THE FIRST TWO BYTES  *HMD 11/82*
*           OF IGC018 WITH ITSELF WILL BE SUFFICIENT.       *HMD 11/82*
*                                                           *HMD 11/82*
* RESTRICTIONS:                                             *HMD 11/82*
*                                                           *HMD 11/82*
*           MODULE IGC018XX MUST BE AT THE SAME LEVEL       *HMD 11/82*
*           AS THIS PROGRAM (A CHECK IS MADE BEFORE         *HMD 11/82*
*           RELOCATING IGC018XX TO SQA                      *HMD 11/82*
*                                                           *HMD 11/82*
*           THOSE SOURCE LINES MARKED WITH $$$$$$ MUST      *HMD 11/82*
*           BE CHANGED WHENEVER CERTAIN CHANGES ARE MADE    *HMD 11/82*
*           TO IGC018XX.                                    *HMD 11/82*
*                                                           *HMD 11/82*
* EXITS                                                     *HMD 11/82*
*   ERROR:  UPON ABEND AN SVC DUMP IS TAKEN AND A RECORD    *HMD 11/82*
*           IS WRITTEN TO SYS1.LOGREC                       *HMD 11/82*
*                                                           *HMD 11/82*
*           A RETRY IS MADE AT ADDRESS 'FREEMAIN' TO FREE   *HMD 11/82*
*           THE SQA STORAGE OBTAINED.                       *HMD 11/82*
*                                                           *HMD 11/82*
*           THE RETURN CODE IS SET TO 4 UPON EXIT.          *HMD 11/82*
*                                                           *HMD 11/82*
* EXITS                                                     *HMD 11/82*
*   NORMAL: THE RETURN CODE IS SET TO 0 UPON EXIT.          *HMD 11/82*
*                                                           *HMD 11/82*
* ATTRIBUTES:                                               *HMD 11/82*
*                                                           *HMD 11/82*
*           SYSTEM KEY                                      *HMD 11/82*
*           APF AUTHORIZED                                  *HMD 11/82*
*           SUPERVISOR STATE                                *HMD 11/82*
*                                                           *HMD 11/82*
* MACROS                                                              *
*  USED:       SETLOCK                                                *
*              SAVE                                                   *
*              RETURN                                                 *
*              GETMAIN                                                *
*              LOAD                                                   *
*              MODESET                                                *
*              WTO                                                    *
*                                                                     *
*  REGISTERS:  R15 - ENTRY ADDRESS & RETURN CODE                      *
*              R14 - RETURN ADDRESS                                   *
*              R13 - SAVE AREA ADDRESS + WORK AREA PTR                *
*              R12 - CSECT BASE                                       *
*               R6 - WORK REG                                         *
*               R5 - WORK REG                                         *
*               R4 - WORK REG                                         *
*               R3 - WORK REG                                         *
*               R2 - WORK REG                                         *
*               R1 - PARAM ADDRESS                                    *
*                                                           *HMD 11/82*
*  AUTHOR:  HOWARD M. DEAN                                  *HMD 11/82*
*           GTE DATA SERVICES                               *HMD 11/82*
*           4750 LINCOLN BLVD.                              *HMD 11/82*
*           MARINA DEL REY, CA 90291                        *HMD 11/82*
*           (213) 821-0511 X330                             *HMD 11/82*
*                                                           *HMD 11/82*
***********************************************************************
         EJECT
BLDLINIT CSECT                                              *HMD 11/82*
**                                                          *HMD 11/82*
** NOTE: ALL STATMENTS WITH $$$$$$ MUST BE CHANGED IF       *HMD 11/82*
**       IGC018XX CODE CHANGES                              *HMD 11/82*
**                                                          *HMD 11/82*
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
*                                                           *HMD 11/82*
SVCBLDL  EQU   18                    BLDL SVC NUMBER        *HMD 11/82*
$LEVEL   EQU   5        $$$$$$       START OF LEVEL STAMP   *HMD 11/82*
$DYNRLD  EQU   60       $$$$$$       START OF DYNAMIC RLD   *HMD 11/82*
HSA      EQU   4                     HIGH SAVE AREA         *HMD 11/82*
LSA      EQU   8                     LOW SAVE AREA          *HMD 11/82*
*                                                           *HMD 11/82*
         EJECT                                              *HMD 11/82*
         SAVE  (14,12),,BLDLINIT_(C)_HOWARD_M_DEAN_&SYSDATE_&SYSTIME
         LR    R12,R15                                      *HMD 11/82*
         USING BLDLINIT,R12          ESTABLISH PGM BASE     *HMD 11/82*
         USING STORAGE,R13           ADDRESS GOTTEN STORAGE *HMD 11/82*
         GETMAIN R,LV=WMLEN          GET SAVE AREA STORAGE  *HMD 11/82*
         ST    R1,LSA(R13)           CHAIN                  *HMD 11/82*
         ST    R13,HSA(R1)              SAVE                *HMD 11/82*
         LR    R13,R1                      AREAS            *HMD 11/82*
*                                                           *HMD 11/82*
* CLEAR STORAGE                                             *HMD 11/82*
*                                                           *HMD 11/82*
         LA    R2,CLEARHD            START OF CLEAR AREA    *HMD 11/82*
         LA    R3,CLRLEN             LENGTH TO CLEAR        *HMD 11/82*
         SLR   R4,R4                 CLEAR                  *HMD 11/82*
         SLR   R5,R5                       REGISTERS        *HMD 11/82*
         MVCL  R2,R4                 M O V E    L O N G     *HMD 11/82*
*                                                           *HMD 11/82*
         MODESET MODE=SUP          GET SUPERVISOR STATE     *HMD 11/82*
*                                                           *HMD 11/82*
         EJECT                                              *HMD 11/82*
*---------------------------------------------------------------------*
*        CHECK TO SEE IF ALREADY INITIALIZED                          *
*---------------------------------------------------------------------*
         L     R3,CVTPTR                CVT ADDRESS
         L     R3,CVTABEND-CVT(,R3)     POINT TO THE SCVT   *HMD 11/82*
         L     R3,SCVTSVCT-SCVTSECT(,R3)                    *HMD 11/82*
*                                       POINT TO SVC TABLE  *HMD 11/82*
         L     R3,SVCBLDL*8(,R3)        POINT TO SVCT ENTRY *HMD 11/82*
         CLC   $LEVEL(8,R3),LEVEL       CHECK CONSTANT      *HMD 11/82*
         BNE   DOESTAE                                      *HMD 11/82*
*                                                           *HMD 11/82*
         WTO   'HMD452I DYNAMIC BLDL IS ALREADY INITIALIZED',          X
               ROUTCDE=(1,2,10),DESC=3                      *HMD 11/82*
*                                                           *HMD 11/82*
         B     BLDLFAIL                                     *HMD 11/82*
         EJECT
*---------------------------------------------------------------------*
*        ESTABLISH ESTAE ENVIORNMENT IN CASE OF ABEND                 *
*---------------------------------------------------------------------*
DOESTAE  STM   R12,R13,SAVER12            BASE REGISTERS    *HMD 11/82*
         LA    R1,RETRY                   GET RETRY ADDR    *HMD 11/82*
         ST    R1,RETRADDR                SAVE IN PARMLIST  *HMD 11/82*
         SPACE 2                                            *HMD 11/82*
*                                                           *HMD 11/82*
         ESTAE STAEX,CT,PARAM=PARMLIST,                     *HMD 11/82*X
               PURGE=HALT,                                  *HMD 11/82*X
               ASYNCH=YES,                                  *HMD 11/82*X
               TERM=YES,                                    *HMD 11/82*X
               MF=(E,ESTAEL)                                *HMD 11/82*
*                                                           *HMD 11/82*
         LTR   R15,R15                    CHECK RC          *HMD 11/82*
         BZ    SETFLAG                    OK..SET FLAG      *HMD 11/82*
*                                                           *HMD 11/82*
         WTO   'HMD786I BLDLINIT ESTAE PROCESSING FAILED',  *HMD 11/82*X
               ROUTCDE=(1,2,10),DESC=3                      *HMD 11/82*
*                                                           *HMD 11/82*
         B     BLDLFAIL                                     *HMD 11/82*
SETFLAG  OI    FLAGS,BLDLSTAE             INDICATE ESATE    *HMD 11/82*
         EJECT                                              *HMD 11/82*
*---------------------------------------------------------------------*
*        LOAD IGC018XX AND CALCULATE SIZE TO GETMAIN IN SQA           *
*---------------------------------------------------------------------*
*                                                           *HMD 11/82*
LOAD018  DS    0H                                           *HMD 11/82*
         OI    FLAGS,BLDLLOAD         ASSUME LOAD WORKED    *HMD 11/82*
*                                                           *HMD 11/82*
         LOAD  EPLOC=LOADMBR,ERRET=NOLOAD
*                                                           *HMD 11/82*
         SLL   R1,3                   GET NUMBER OF BYTES   *HMD 11/82*
         LA    R1,7(,R1)              ROUND SIZE TO         *HMD 11/82*
         N     R1,ROUND                NEXT DOUBLEWORD      *HMD 11/82*
         ST    R1,DBLDSIZE            SAVE SIZE FOR LATER   *HMD 11/82*
         ST    R0,DBLDADDR                                  *HMD 11/82*
         LR    R2,R0                  GET ENTRY ADDRESS     *HMD 11/82*
         CLC   $LEVEL(L'LEVEL,R2),LEVEL                     *HMD 11/82*
         BE    LEVELOK                                      *HMD 11/82*
*                                                           *HMD 11/82*
         WTO   'HMD377I IGC018XX IS WRONG LEVEL FOR BLDLINIT',         X
               ROUTCDE=(1,2,10),DESC=3                      *HMD 11/82*
*                                                           *HMD 11/82*
         B     BLDLFAIL                                     *HMD 11/82*
         EJECT
*---------------------------------------------------------------------*
*        GET AREA IN SQA - SP=245, FIXED, KEY=0, FETCH-OK             *
*---------------------------------------------------------------------*
*
LEVELOK  DS    0H                                           *HMD 11/82*
         MODESET EXTKEY=ZERO,SAVEKEY=KEYSAVE,WORKREG=2      *HMD 11/82*
*                                                           *HMD 11/82*
         SETLOCK OBTAIN,TYPE=SALLOC,REGS=USE,MODE=UNCOND,   *HMD 11/82*X
               RELATED=('SALLOC LOCK FOR GETMAIN')          *HMD 11/82*
*                                                           *HMD 11/82*
         L     R0,DBLDSIZE         GET AREA LENGTH          *HMD 11/82*
*                                                           *HMD 11/82*
         GETMAIN RC,LV=(0),SP=245,BRANCH=(YES,GLOBAL)       *HMD 11/82*
*                                                           *HMD 11/82*
         ST    R1,GMAREA           SAVE GETMAINED AREA      *HMD 11/82*
         ST    R15,GMRC            SAVE RETURN CODE         *HMD 11/82*
*                                                           *HMD 11/82*
         SETLOCK RELEASE,TYPE=SALLOC,REGS=USE,              *HMD 11/82*X
               RELATED=('SALLOC LOCK FOR GETMAIN')          *HMD 11/82*
*                                                           *HMD 11/82*
         MODESET KEYADDR=KEYSAVE,WORKREG=2                  *HMD 11/82*
*                                                           *HMD 11/82*
*---------------------------------------------------------------------,
*        CHECK GETMAIN RETURN CODE                                    ,
*---------------------------------------------------------------------,
         L     R15,GMRC            RESTORE RETURN CODE      *HMD 11/82*
         LTR   R15,R15             CHECK FOR ZERO           *HMD 11/82*
         BZ    FETCH               ZERO, FETCH TO SQA AREA  *HMD 11/82*
*                                                           *HMD 11/82*
         WTO   'HMD342I GETMAIN IN FIXED COMMON FAILED',    *HMD 11/82*X
               ROUTCDE=(1,2,10),DESC=3                      *HMD 11/82*
*                                                           *HMD 11/82*
BLDLFAIL DS    0H                                           *HMD 11/82*
         TM    FLAGS,BLDLSTAE      STAE EXIT ESTABLISHED?   *HMD 11/82*
         BZ    BFNOSTAE            NO, FORGET CANCEL        *HMD 11/82*
*                                                           *HMD 11/82*
         ESTAE  0                  CANCEL ESTAE EXIT        *HMD 11/82*
*                                                           *HMD 11/82*
         NI    FLAGS,255-BLDLSTAE  TURN OF FLAG             *HMD 11/82*
BFNOSTAE DS     0H                                          *HMD 11/82*
         WTO   'HMD343I DYNAMIC BLDL INITIALIZATION FAILED',           X
               ROUTCDE=(1,2,10),DESC=3                      *HMD 11/82*
         LA    R15,4               RETURN CODE OF 4         *HMD 11/82*
         B     EXIT                RETURN TO MVS            *HMD 11/82*
         EJECT                                              *HMD 11/82*
*---------------------------------------------------------------------,
*        LOAD IGC018XX INTO FIXED SYSTEM QUEUE AREA                   ,
*---------------------------------------------------------------------,
FETCH    DS    0H                                           *HMD 11/82*
         OI    FLAGS,BLDLGM        INDICATE GETMAIN WORKED  *HMD 11/82*
*                                                           *HMD 11/82*
         MODESET EXTKEY=ZERO,SAVEKEY=KEYSAVE,WORKREG=2      *HMD 11/82*
*                                                           *HMD 11/82*
         L     R2,GMAREA           LOAD SQA AREA            *HMD 11/82*
         L     R3,DBLDSIZE         GET MODULE LENGTH        *HMD 11/82*
         L     R4,DBLDADDR         GET MODULE ADDRESS       *HMD 11/82*
         L     R5,DBLDSIZE         SECOND OPERAND LENGTH    *HMD 11/82*
         ICM   R5,B'1000',=X'FF'   PADDING BYTE (EYECATCH)  *HMD 11/82*
         MVCL  R2,R4               M O V E    L O N G       *HMD 11/82*
         SPACE 2                                            *HMD 11/82*
*---------------------------------------------------------------------*
*        GET CURRENT BLDL ENTRY IN SVC TABLE FOR RELOCATION           *
*---------------------------------------------------------------------*
         L     R3,CVTPTR                CVT ADDRESS
         L     R3,CVTABEND-CVT(,R3)     POINT TO THE SCVT   *HMD 11/82*
         L     R3,SCVTSVCT-SCVTSECT(,R3)                    *HMD 11/82*
*                                       POINT TO SVC TABLE  *HMD 11/82*
         LA    R3,SVCBLDL*8(,R3)        POINT TO SVCT ENTRY *HMD 11/82*
         ST    R3,SVCTABL               SAVE TABLE ADDRESS  *HMD 11/82*
         EJECT                                              *HMD 11/82*
*---------------------------------------------------------------------*
*        RELOCATE ENTRIES IN THE COPIED MODULE AND PREPARE FOR USE    *
*---------------------------------------------------------------------*
         USING RLDSECT,R5               ADDRESS THE SECTION *HMD 11/82*
         L     R4,GMAREA                POINT TO MODULE     *HMD 11/82*
         LA    R5,$DYNRLD(,R4)          POINT TO $DYNRLD    *HMD 11/82*
         L     R6,IGC018AD              GET IGC018 ADDRESS  *HMD 11/82*
         AR    R6,R4                    RELOCATE            *HMD 11/82*
         L     R3,SVCTABL               GET ENTRY ADDRESS   *HMD 11/82*
         L     R3,0(,R3)                GET IGC018 ADDRESS  *HMD 11/82*
         ST    R3,0(,R6)                SAVE INTO IGC018XX  *HMD 11/82*
         L     R6,LINKTAB               LINKTAB ADDRESS     *HMD 11/82*
         L     R7,LINKADDR              LINKADDR ADDRESS    *HMD 11/82*
         AR    R6,R4                    RELOCATE            *HMD 11/82*
         AR    R7,R4                    RELOCATE            *HMD 11/82*
         ST    R6,0(,R7)                SAVE IN CONSTANT    *HMD 11/82*
         L     R7,ENTRY1                ENTRY1 ADDRESS      *HMD 11/82*
         L     R6,NWB1                  NWB1 ADDRESS        *HMD 11/82*
         AR    R6,R4                    RELOCATE            *HMD 11/82*
         AR    R7,R4                    RELOCATE            *HMD 11/82*
         ST    R6,0(,R7)                SAVE IN CONSTANT    *HMD 11/82*
         L     R7,ENTRY2                ENTRY2 ADDRESS      *HMD 11/82*
         L     R6,NWB2                  NWB2 ADDRESS        *HMD 11/82*
         AR    R6,R4                    RELOCATE            *HMD 11/82*
         AR    R7,R4                    RELOCATE            *HMD 11/82*
         ST    R6,0(,R7)                SAVE IN CONSTANT    *HMD 11/82*
         L     R7,TABSTAT1              TABSTAT1 ADDRESS    *HMD 11/82*
         L     R6,START1                START1   ADDRESS    *HMD 11/82*
         AR    R6,R4                    RELOCATE            *HMD 11/82*
         AR    R7,R4                    RELOCATE            *HMD 11/82*
         ST    R6,0(,R7)                SAVE IN CONSTANT    *HMD 11/82*
         L     R7,TABEND1               TABEND1 ADDRESS     *HMD 11/82*
         L     R8,LASTSLT1              LASTSLT1 ADDRESS    *HMD 11/82*
         L     R6,LASTENT1              LASTENT1 ADDRESS    *HMD 11/82*
         AR    R6,R4                    RELOCATE            *HMD 11/82*
         AR    R7,R4                    RELOCATE            *HMD 11/82*
         AR    R8,R4                    RELOCATE            *HMD 11/82*
         ST    R6,0(,R7)                SAVE IN ADCON       *HMD 11/82*
         ST    R6,0(,R8)                SAVE IN ADCON       *HMD 11/82*
         L     R6,CALC                  LOAD CALCULATION    *HMD 11/82*
         L     R7,TABSLOT1              SLOT1 ADDRESS       *HMD 11/82*
         AR    R6,R4                    RELOCATE            *HMD 11/82*
         AR    R7,R4                    RELOCATE            *HMD 11/82*
         ST    R6,0(R7)                 SAVE IN ADCON       *HMD 11/82*
         SPACE 2                                            *HMD 11/82*
*
* GET LOCAL LOCK WHILE UPDATING SVC TABLE                   *HMD 11/82*
*
         SETLOCK OBTAIN,TYPE=LOCAL,MODE=UNCOND,REGS=USE,    *HMD 11/82*X
               RELATED=('SERIALIZED UPDATES TO BLDL')       *HMD 11/82*
*
         L     R3,SVCTABL               POINT TO BLDL ENTRY *HMD 11/82*
         L     R4,GMAREA                GET AREA ADDRESS    *HMD 11/82*
         ST    R4,0(,R3)                SAVE IN SVCTABLE    *HMD 11/82*
*                                                           *HMD 11/82*
         SETLOCK RELEASE,TYPE=LOCAL,REGS=USE,               *HMD 11/82*X
               RELATED=('SERIALIZED UPDATES TO BLDL')       *HMD 11/82*
*                                                           *HMD 11/82*
         MODESET KEYADDR=KEYSAVE,WORKREG=2                  *HMD 11/82*
*                                                           *HMD 11/82*
FINISH   WTO   'HMD355I DYNAMIC BLDL INITIALIZATION COMPLETE',         X
               ROUTCDE=(1,2,10),DESC=(4,6)                  *HMD 11/82*
*                                                           *HMD 11/82*
         MVC   WTOENTRY(WTOLISTL),WTOLIST                   *HMD 11/82*
*                                    MOVE IN LIST FORM WTO  *HMD 11/82*
         UNPK  UNPACK,GMAREA(5)      UNPACK ENTRY ADDRESS   *HMD 11/82*
         TR    UNPACK(9),HEXTABL     TRANSLATE TO HEX       *HMD 11/82*
         MVC   WTOADDR,UNPACK        MOVE INTO WTO AREA     *HMD 11/82*
*                                                           *HMD 11/82*
         WTO   MF=(E,WTOENTRY)       WRITE TO OPERATOR      *HMD 11/82*
*                                                           *HMD 11/82*
         L     R15,DBLDSIZE          LOAD BLDLSIZE          *HMD 11/82*
         CVD   R15,DBLWORK           CONVERT TO DECIMAL     *HMD 11/82*
         MVC   EDMKWK,PATTERN        MOVE PATTERN           *HMD 11/82*
         LA    R1,EDMKWK+PATRNL-1    IN CASE OF SIG.        *HMD 11/82*
         EDMK  EDMKWK,DBLWORK+4      EDIT THE VALUE         *HMD 11/82*
         LA    R2,EDMKWK+PATRNL      LENGTH TO MOVE         *HMD 11/82*
         SR    R2,R1                 CALCULATE LENBTH       *HMD 11/82*
         BCTR  R2,0                  -1 FOR EXECUTE         *HMD 11/82*
         MVC   WTOLGTH(WTOLENL),WTOLEN  MOVE IN WTO LIST    *HMD 11/82*
         EX    R2,MOVWTO             MOVE NUMBER TO MSG     *HMD 11/82*
*                                                           *HMD 11/82*
         LA    R3,WTODEC             POINT TO POSITION      *HMD 11/82*
         LA    R2,1(R2,R3)           BUMP POINTER TO END    *HMD 11/82*
         MVC   1(L'BYTE,R2),BYTE     MOVE IN 'BYTE'         *HMD 11/82*
         C     R15,=F'1'             GREATER THAN ONE?      *HMD 11/82*
         BNH   SIZEWTO               NOPE, FORGET THE 'S'   *HMD 11/82*
         MVI   L'BYTE+1(R2),C'S'     MOVE IN THE 'S'        *HMD 11/82*
*                                                           *HMD 11/82*
SIZEWTO  WTO   MF=(E,WTOLGTH)        PUT OUT LENGTH MSG     *HMD 11/82*
*                                                           *HMD 11/82*
         SLR   R15,R15        CLEAR RETURN CODE             *HMD 11/82*
         B     EXIT                                         *HMD 11/82*
*                                                           *HMD 11/82*
MOVWTO   MVC   WTODEC(0),0(R1)      << EXECUTED >>          *HMD 11/82*
         EJECT                                              *HMD 11/82*
***********************************************************************
*                                                           *HMD 11/82*
*  ERROR HANDLING ROUTINES AND ESTAE EXIT ROUTINE           *HMD 11/82*
*                                                           *HMD 11/82*
***********************************************************************
*                                                           *HMD 11/82*
*  LOAD ERROR FOR IGC018XX                                  *HMD 11/82*
*                                                           *HMD 11/82*
NOLOAD   DS     0H                                          *HMD 11/82*
         NI     FLAGS,255-BLDLLOAD      TURN OFF FLAG       *HMD 11/82*
*                                                           *HMD 11/82*
         WTO    'HMD375I LOAD FAILED FOR IGC018XX',         *HMD 11/82*X
               ROUTCDE=(1,2,10),DESC=3                      *HMD 11/82*
*                                                           *HMD 11/82*
         B      BLDLFAIL                NO..DO NOT FREE     *HMD 11/82*
         SPACE  2                                           *HMD 11/82*
***********************************************************************
*                                                           *HMD 11/82*
*  RETRY FROM ESTAE EXIT                                    *HMD 11/82*
*                                                           *HMD 11/82*
***********************************************************************
RETRY    DS     0H                                          *HMD 11/82*
*                                                           *HMD 11/82*
         ESTAE  0        CANCEL ESTAE EXITS                 *HMD 11/82*
*                                                           *HMD 11/82*
         MODESET KEYADDR=KEYSAVE,WORKREG=2    NORMAL KEY    *HMD 11/82*
         NI     FLAGS,255-BLDLSTAE            TURN OFF FLAG *HMD 11/82*
*                                                           *HMD 11/82*
         WTO    'HMD376I BLDLINIT HAS ABENDED, DUMPED, AND RECORDED',  X
               ROUTCDE=(1,2,10),DESC=3                      *HMD 11/82*
         B      FREEMAIN                                    *HMD 11/82*
*                                                           *HMD 11/82*
*  FREEMAIN IN CASE OF ERROR                                *HMD 11/82*
*                                                           *HMD 11/82*
         SPACE  2                                           *HMD 11/82*
FREEMAIN DS     0H                      FREE SQA STORAGE    *HMD 11/82*
         SPACE  1                                           *HMD 11/82*
         TM     FLAGS,BLDLGM            DID GETMAIN WORK?   *HMD 11/82*
         BZ     BLDLFAIL                NO, DO NOT FREEMAIN *HMD 11/82*
         OC     GMAREA,GMAREA           DOUBLE CHECK        *HMD 11/82*
         BZ     BLDLFAIL                NO, DO NOT FREEMAIN *HMD 11/82*
*                                                           *HMD 11/82*
         WTO    'HMD377I FREEMAIN OF SQA STORAGE WILL BE ATTEMPTED',   X
               ROUTCDE=(1,2,10),DESC=3                      *HMD 11/82*
*                                                           *HMD 11/82*
         MODESET EXTKEY=ZERO,SAVEKEY=KEYSAVE,WORKREG=2      *HMD 11/82*
*                                                           *HMD 11/82*
         SETLOCK OBTAIN,MODE=UNCOND,TYPE=SALLOC,REGS=USE,   *HMD 11/82*X
               RELATED=('FREE SQA GETMAINED STORAGE')       *HMD 11/82*
*                                                           *HMD 11/82*
         L      R0,DBLDSIZE             GET GETMAIN SIZE    *HMD 11/82*
         L      R1,GMAREA               GET GETMAIN AREA    *HMD 11/82*
*                                                           *HMD 11/82*
         FREEMAIN RC,A=(1),LV=(0),SP=245,BRANCH=(YES,GLOBAL)
*                                                           *HMD 11/82*
         ST     R15,FMRC                SAVE RETURN CODE    *HMD 11/82*
*                                                           *HMD 11/82*
         SETLOCK RELEASE,TYPE=SALLOC,REGS=USE,              *HMD 11/82*X
               RELATED=('FREE SQA GETMAINED STORAGE')       *HMD 11/82*
*                                                           *HMD 11/82*
         MODESET KEYADDR=KEYSAVE,WORKREG=2                  *HMD 11/82*
*                                                           *HMD 11/82*
         L      R15,FMRC                RESTORE RETURN CODE *HMD 11/82*
         LTR    R15,R15                 RETURN CODE OK?     *HMD 11/82*
         BZ     FSUCCESS                YES..CONTINUE       *HMD 11/82*
*                                                           *HMD 11/82*
         WTO    'HMD573I FREEMAIN OF SQA STORAGE FAILED',   *HMD 11/82*X
               ROUTCDE=(1,2,10),DESC=3                      *HMD 11/82*
*                                                           *HMD 11/82*
         B      BLDLFAIL                USE FAIL RETURN     *HMD 11/82*
*                                                           *HMD 11/82*
FSUCCESS DS     0H                                          *HMD 11/82*
*                                                           *HMD 11/82*
         WTO    'HMD574I FREEMAIN OF SQA STORAGE SUCCESSFUL',          X
               ROUTCDE=(1,2,10),DESC=3                      *HMD 11/82*
*                                                           *HMD 11/82*
         B      BLDLFAIL                USE FAIL RETURN     *HMD 11/82*
         EJECT
***********************************************************************
*                                                           *HMD 11/82*
*        NORMAL/ABNORMAL EXIT PROCESSING                    *HMD 11/82*
*                                                           *HMD 11/82*
***********************************************************************
EXIT     DS     0H                                          *HMD 11/82*
         LR     R2,R15         SAVE RETURN CODE             *HMD 11/82*
*                                                           *HMD 11/82*
         TM     FLAGS,BLDLLOAD IGC018XX LOADED?             *HMD 11/82*
         BZ     EXIT1          NO, DO NOT DELETE            *HMD 11/82*
*                                                           *HMD 11/82*
         DELETE EPLOC=LOADMBR  DELETE MODULE (NOT NEEDED)   *HMD 11/82*
*                                                           *HMD 11/82*
EXIT1    DS     0H                                          *HMD 11/82*
         MODESET MODE=PROB     RETURN TO PROBLEM STATE      *HMD 11/82*
*                                                           *HMD 11/82*
         LR     R1,R13         POINT TO SAVE AREA           *HMD 11/82*
         L      R13,4(,R1)     POINT TO PREVIOUS SA         *HMD 11/82*
*                                                           *HMD 11/82*
         FREEMAIN R,LV=WMLEN,A=(1)   FREE STORAGE           *HMD 11/82*
*                                                           *HMD 11/82*
         LR     R15,R2         RESTORE RETURN CODE          *HMD 11/82*
*                                                           *HMD 11/82*
         RETURN (14,12),T,RC=(15)    RETURN TO CALLER       *HMD 11/82*
         SPACE  2                                           *HMD 11/82*
IRVING   LTORG ,
         EJECT                                              *HMD 11/82*
***********************************************************************
*                                                                     *
*        E S A T E    E X I T                                         *
*                                                                     *
***********************************************************************
STAEX    DS     0H                                          *HMD 11/82*
         USING  STAEX,R15                                   *HMD 11/82*
         C     R0,SDWACODE           SDWA OBTAINED          *HMD 11/82*
         BNE   GOTSDWA               YES, GOT IT            *HMD 11/82*
         LM    R12,R13,0(R2)         LOAD REGISTERS         *HMD 11/82*
         L     R4,8(,R2)             LOAD RETRY ADDRESS     *HMD 11/82*
         LR    R5,R14                                       *HMD 11/82*
         DROP  R15                   NO NEED FOR BASE NOW   *HMD 11/82*
*                                                           *HMD 11/82*
         WTO   'HMD988I BLDLINIT ESTAE EXIT ENTERED - NO SDWA',        X
               ROUTCDE=(1,2,10),DESC=3                      *HMD 11/82*
*                                                           *HMD 11/82*
         SDUMP HDR='BLDLINIT,IGC018XX,ABEND,SDWA=NO,RETRY', *HMD 11/82*X
               ASID=ASIDRNGE,MF=(E,SDUMPL)                  *HMD 11/82*
*                                                           *HMD 11/82*
         LA    R15,4                 RETRY AT FREEMAIN      *HMD 11/82*
         LR    R0,R4                 RESTORE RETRY ADDR     *HMD 11/82*
         LR    R14,R5                RESTORE RETURN ADDR    *HMD 11/82*
         BR    R14                   RETURN TO RTM          *HMD 11/82*
         SPACE 2                                            *HMD 11/82*
GOTSDWA  SAVE  (14,12)               SAVE REGISTERS         *HMD 11/82*
         LR    R7,R13                SAVE SA ADDRESS        *HMD 11/82*
         L     R2,0(,R1)             LOAD SDWAPARM          *HMD 11/82*
         LM    R12,R13,0(R2)         LOAD REGISTERS         *HMD 11/82*
         L     R5,8(,R2)             LOAD RETRY ADDRESS     *HMD 11/82*
         LR    R6,R1                 SAVE SDWA ADDRESS      *HMD 11/82*
*                                                           *HMD 11/82*
         WTO   'HMD988I BLDLINIT ESTAE EXIT ENTERED',                  X
               ROUTCDE=(1,2,10),DESC=3                      *HMD 11/82*
*                                                           *HMD 11/82*
         SDUMP  HDR='BLDLINIT,IGC018XX,ABEND,SDWA=YES,RETRY',          X
               ASID=ASIDRNGE,MF=(E,SDUMPL)                  *HMD 11/82*
*                                                           *HMD 11/82*
         SPACE 2                                            *HMD 11/82*
         LR    R13,R7                RESTORE SA ADDR        *HMD 11/82*
*                                                           *HMD 11/82*
         SETRP WKAREA=(6),REGS=(14,12),                     *HMD 11/82*X
               DUMP=NO,RETREGS=YES,                         *HMD 11/82*X
               RECORD=YES,RECPARM=RECORD,                   *HMD 11/82*X
               RETADDR=(5),RC=4                             *HMD 11/82*
*                                                           *HMD 11/82*
         EJECT                                              *HMD 11/82*
**                                                          *HMD 11/82*
**  CONSTANTS                                               *HMD 11/82*
***                                                         *HMD 11/82*
LEVEL    DC     C'IGC018XX11-24-82'  $$$$$$ IGC018XX LEVEL  *HMD 11/82*
RECORD   DC     CL8'BLDLINIT'                               *HMD 11/82*
LOADMBR  DC     CL8'IGC018XX'                               *HMD 11/82*
DATER    DC     CL8'&SYSDATE'                               *HMD 11/82*
BYTE     DC     C'BYTE'      BYTE OR BYTES                  *HMD 11/82*
S        DC     C'S'              ONE OR MORE               *HMD 11/82*
PATTERN  DC     X'206B2020206B202120'                       *HMD 11/82*
PATRNL   EQU    *-PATTERN                                   *HMD 11/82*
ROUND    DC     F'16777208'              ROUNDING FACTOR    *HMD 11/82*
SDWACODE DC     F'12'                    SDWA CODE          *HMD 11/82*
ASIDRNGE DC     H'0'                     ASID RANGE         *HMD 11/82*
*                                                           *HMD 11/82*
WTOLIST  WTO   'HMD945I IGC018XX ENTRY ADDRESS IS XXXXXXXX',           X
               ROUTCDE=(1,2,10),DESC=(4,6),MF=L             *HMD 11/82*
WTOLISTL EQU    *-WTOLIST      LIST FORM OF WTO MACRO       *HMD 11/82*
*                                                           *HMD 11/82*
WTOLEN   WTO   'HMD946I IGC018XX LENGTH IS X                        ', X
               ROUTCDE=(1,2,10),DESC=(4,6),MF=L             *HMD 11/82*
WTOLENL  EQU    *-WTOLEN                                    *HMD 11/82*
*                                                           *HMD 11/82*
HEXTABL  EQU    *-240          HEX TRANSLATE TABLE          *HMD 11/82*
         DC    C'0123456789ABCDEF'                          *HMD 11/82*
*                                                           *HMD 11/82*
         EJECT
STORAGE  DSECT                                              *HMD 11/82*
SAVEAREA DS    18F             REGISTER SAVE AREA           *HMD 11/82*
CLEARHD  DS    0C              START OF AREA TO CLEAR       *HMD 11/82*
PARMLIST DS    3A                                           *HMD 11/82*
         ORG   PARMLIST                                     *HMD 11/82*
SAVER12  DS    A               E S T A E                    *HMD 11/82*
SAVER13  DS    A                     P A R M                *HMD 11/82*
RETRADDR DS    A                           L I S T          *HMD 11/82*
DBLDSIZE DS    F               SIZE OF IGC018XX             *HMD 11/82*
DBLDADDR DS    F               IGC018XX STARTING ADDRESS    *HMD 11/82*
SVCTABL  DS    F               BLDL ADDRESS IN SVC TABLE    *HMD 11/82*
GMAREA   DS    F               SP 245 GETMAINED AREA        *HMD 11/82*
GMRC     DS    F               GETMAIN RETURN CODE          *HMD 11/82*
FMRC     DS    F               FREEMAIN RETURN CODE         *HMD 11/82*
DBLWORK  DS    D               DOUBLE-WORD WORK AREA        *HMD 11/82*
EDMKWK   DS    XL(PATRNL)      EDIT/MARK WORK AREA          *HMD 11/82*
UNPACK   DS    XL9             AREA TO UNPACK INTO          *HMD 11/82*
KEYSAVE  DS    X               AREA TO SAVE PSW KEY         *HMD 11/82*
*                                                           *HMD 11/82*
FLAGS    DS    X               VARIOUS STATUS FLAGS         *HMD 11/82*
BLDLSTAE EQU   X'80' 1... ....   BLDLINIT ESATE ACTIVATED   *HMD 11/82*
BLDLGM   EQU   X'40' .1.. ....   GETMAIN WAS SUCCESSFUL     *HMD 11/82*
BLDLLOAD EQU   X'20' ..1. ....   IGC018XX WAS LOADED        *HMD 11/82*
*                                                           *HMD 11/82*
WTOENTRY WTO   'HMD945I IGC018XX ENTRY ADDRESS IS XXXXXXXX',           X
               ROUTCDE=(1,2,10),DESC=(4,6),MF=L             *HMD 11/82*
WTOADDR  EQU   WTOENTRY+38,8                                *HMD 11/82*
*                                                           *HMD 11/82*
WTOLGTH  WTO   'HMD946I IGC018XX LENGTH IS X,XXX,XXX                ', X
               ROUTCDE=(1,2,10),DESC=(4,6),MF=L             *HMD 11/82*
WTODEC   EQU    WTOLGTH+31,12                               *HMD 11/82*
*                                                           *HMD 11/82*
ESTAEL   ESTAE MF=L            ESTAE LIST FORM              *HMD 11/82*
ESTAELL  EQU   *-ESTAEL        LENGTH TO CLEAR              *HMD 11/82*
*                                                           *HMD 11/82*
SDUMPL   SDUMP MF=L            SDUMP PARM LIST              *HMD 11/82*
SDUMPLL  EQU   *-SDUMPL        LENGTH TO CLEAR              *HMD 11/82*
*                                                           *HMD 11/82*
*                                                           *HMD 11/82*
STOREND  DS    0C                                           *HMD 11/82*
CLRLEN   EQU   STOREND-CLEARHD          LENGTH TO CLEAR     *HMD 11/82*
WMLEN    EQU   STOREND-SAVEAREA         LENGTH TO GET       *HMD 11/82*
         EJECT                                              *HMD 11/82*
RLDSECT  DSECT                 IGC018XX RELOCATION DSECT    *HMD 11/82*
IGC018AD DS    A               REAL IGC018 EP               *HMD 11/82*
LINKADDR DS    A               LINKTAB ADCON                *HMD 11/82*
LINKTAB  DS    A               LINKTAB ADDRESS              *HMD 11/82*
ENTRY1   DS    A               ENTRY1 OFFSET                *HMD 11/82*
NWB1     DS    A               NWB1 ADCON                   *HMD 11/82*
ENTRY2   DS    A               ENTRY2 ADDRESS               *HMD 11/82*
NWB2     DS    A               NWB2 ADCON                   *HMD 11/82*
TABSTAT1 DS    A               TABSTAT1 ADDRESS             *HMD 11/82*
START1   DS    A               START1   OFFSET              *HMD 11/82*
LASTENT1 DS    A               LASTENT1 OFFSET              *HMD 11/82*
TABEND1  DS    A               TABEND1  OFFSET              *HMD 11/82*
LASTSLT1 DS    A               LASTSLT1 OFFSET              *HMD 11/82*
TABSLOT1 DS    A               TABSLOT1 OFFSET              *HMD 11/82*
CALC     DS    A               CALCULATION                  *HMD 11/82*
DIS01    DS    A               $$$DIS01 OFFSET              *HMD 11/82*
DIS02    DS    A               $$$DIS02 OFFSET              *HMD 11/82*
DIS04    DS    A               $$$DIS03 OFFSET              *HMD 11/82*
         EJECT                                              *HMD 11/82*
         CVT   LIST=YES,DSECT=YES                           *HMD 11/82*
         EJECT                                              *HMD 11/82*
         IHAWSAVT CLASS=GLOBAL                              *HMD 11/82*
         EJECT                                              *HMD 11/82*
         IHASCVT  LIST=YES,DSECT=YES                        *HMD 11/82*
         EJECT                                              *HMD 11/82*
         IHAPSA   ,                                         *HMD 11/82*
         EJECT                                              *HMD 11/82*
         IHASDWA  ,                                         *HMD 11/82*
         END   BLDLINIT
