***********************************************************************
*                                                                     *
*              'PRTARG' INTERNAL ROUTINE --- GET PRINT TARGET         *
*                                                                     *
***********************************************************************
         SPACE
*        ON ENTRY --  (R0) -> RDW OF OUTPUT LINE
*                     (R1) CONTAINS DESIRED OFFSET
*
*        ON EXIT  --  (R1) ADDR OF POTENTIAL TARGET
         SPACE
PRTARG  $ENTER ,
         LR    X1,R1               DESIRED COLUMN
         BCTR  X1,0                CHANGE TO AN OFFSET
         LR    R1,R0               RECORD START ADDR
         LA    R1,4(,R1)           SKIP THE RDW
        $IFON  TTYPE,TONLINE,PRTARG01   BRANCH IF ONLINE
         LA    R1,1(X1,R1)         SKIP CC AND USER AMOUNT
         B     PRTARG05            AND GO TO RETURN
         SPACE
PRTARG01 DS    0H
         LTR   X1,X1               ALL DONE .Q
         BZ    PRTARG05            BRANCH IF YES
PRTARG02 DS    0H
         CLI   0(R1),CBACKSP       BACKSPACE ON ONLINE OUTPUT .Q
         BE    PRTARG03            BRANCH IF YES
         CLI   0(R1),CESCAPE       ESCAPE ON ONLINE OUTPUT .Q
         BNE   PRTARG04            BRANCH IF NOT
         L     TEMP,VDEVICEA       TERMINAL DESCRIPTOR BLOCK
         LTR   TEMP,TEMP           DOES IT EXIST .Q
         BZ    PRTARG03            BRANCH IF NOT
         LA    TEMP,TDEVAESC-TDEVICEA(,TEMP) AFTER ESCAPE TABLE
         SR    R0,R0               CLEAR WORK REG
         IC    R0,1(,R1)           FUNCTION CHARACTER
         AR    TEMP,R0             POINT TO LENGTH-1
         IC    R0,0(,TEMP)         LENGTH-1 AFTER ESCAPE
         AR    R1,R0               BUMP BY LENGTH-1
PRTARG03 DS    0H
         LA    R1,2(,R1)           SKIP BACKSPACE AND NEXT CHAR
         B     PRTARG02            AND TRY AGAIN
         SPACE
PRTARG04 DS    0H
         LA    R1,1(,R1)           NEXT CHAR
         BCTR  X1,0                DECREMENT LENGTH REMAINING
         B     PRTARG01            AND TRY ONCE MORE
         SPACE
PRTARG05 DS    0H
        $EXIT  KEEP=(R1),RC=0      RETURN TO CALLER
