***********************************************************************
*                                                                     *
*        INTERNAL ROUTINE 'PRTERM' -- PRINT LINE ON ERROR FILE        *
*                                                                     *
***********************************************************************
         SPACE
*        TO ENTER --     LA    R0,AREA PREFACED BY RDW/CC
*                       $CALL  PRTERM
         SPACE
PRTERM  $ENTER ,
         L     R1,IOCBTERM         SYSTERM IOCB ADDRESS
         LA    R1,0(,R1)           CLEAR HIGH BYTE
         LTR   R1,R1               IS FILE OPEN .Q
         BZ    PRTERM01            BRANCH IF NOT
        $CALL  PUTTERM             OUTPUT THE DATA
         B     PRTERM02            GO TO RETURN
         SPACE
PRTERM01 DS    0H
         L     X1,WTOCOUNT         CURRENT RECORDS TO OPERATOR
         A     X1,F1               PLUS ONE
         C     X1,F1               TOO MANY .Q
         ST    X1,WTOCOUNT         UPDATE ATTEMPTS
         BH    PRTERM02            BRANCH IF TOO MANY
         LR    R1,R0               POINT TO RDW
         LH    R0,0(,R1)           LENGTH INCL RDW/CC
         S     R0,F5               GET DATA LENGTH
         BNP   PRTERM02            BRANCH IF NOTHING
         LA    R1,5(,R1)           POINT TO DATA
        $CALL  SYWTO               DISPLAY THE RESULT
         SPACE
PRTERM02 DS    0H
        $EXIT  RC=0                RETURN TO CALLER
