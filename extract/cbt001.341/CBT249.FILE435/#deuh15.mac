*                             ********
* *            SOURCE BOOK *** DEUH15 ***  WORD PREPARATION ROUTINES
*                             ********
         SPACE 3
*
*              WORD PREP & HYPHENFIELD SCAN   ************
*        ***** WORD PREPARATION  FOR SUFFIX ANALYSIS OR EXIT FROM
*        *****  HYPHENATION
*
WRDPRP   LH    REGWK3,MINONE       MINUS ONE TO BEGIN WORD SCAN
         BAL   14,NEGFWD            DO NEGATE FORWARD SCAN
         BC    BNEG,HREJ           BRANCH IF WORD REJECTED
         STH   REGWK3,FIRSTVOW     SAVE ADDRESS OF FIRST VOWEL
         LH    REGWK3,NUMCHR       INITIALIZE FOR REVERSE SCAN
         BAL   14,NEGREV            DO NEGATE REVERSE SCAN
         BC    BNEG,HREJ           BRANCH IF WORD REJECTED
         STH   REGWK3,LASTVOW      SAVE ADDRESS OF LAST VOWEL
         CLC   FIRSTVOW(2),LASTVOW
         BE    NOHYP
WORDSCN  NI    SCNDSW,X'3F'        CLEAR YES & POSS.INDICATORS  H-FIELD
         MVC   HFLDST,FIRSTVOW     GET ADDR.OF FIRST VOWEL
NXTFLD   LH    REGWK1,HFLDST
         CH    REGWK1,LASTVOW       IS THIS VOWEL = LAST VOWEL
         BE    LSTVWL               BRANCH IF YES
         LA    REGWK3,ASTATUS(REGWK1)
HFLDSCN  NI    SCNDSW,X'C7'        TURN OFF YES POSS & CONSONANT INDIC.
         XC    POSADR,POSADR       SET POSS.ADDRESS TO ZERO
         B     TSTATS
NXTATS   LA    REGWK1,1(REGWK1)    GET NEXT STATUS POINT
         LA    REGWK3,ASTATUS(REGWK1)
         TM    0(REGWK3),X'01'     TEST FOR VOWEL AS NEXT CHAR
         BO    ENDHFLD              & BRANCH IF YES
TSTATS   TM    0(REGWK3),X'02'     TEST CHAR FOR CONSONANT
         BZ    BYCON                IF NOT BYPASS 1 INST.
         OI    SCNDSW,X'08'        INDICATE CONSONANT FOR HYPH/FIELD
BYCON    TM    0(REGWK3),X'10'     *TEST STATUS OF THIS POINT
         BZ    NXTATS             * STATUS NO OR VARIABLE
         TM    0(REGWK3),X'30'    *
         BM    TSTPOS             * STATUS POSSIBLE
         LR    REGWK3,REGWK1            * STATUS YES
         STH   REGWK3,POSADR
         B     NGREV                    TERMINATE FIELD SCAN ON 1ST YES
PTOY     LH    REGWK3,MINONE
         AH    REGWK3,POSADR
         STH   REGWK3,POSADR
         LA    REGWK2,ASTATUS(REGWK3)   * POINT IN FIELD & GENERAT FULL
         OI    0(REGWK2),X'30'          * ADDR. THEN SET POINT TO YES
NGREV    BAL   REGRET,NEGREV            SET ALL OTHER POINTS IN FIELD
         BC    BNEG,HREJ           BRANCH IF WORD REJECTED
         LH    REGWK3,POSADR            *TO NO
         BAL   REGRET,NEGFWD            *
         BC    BNEG,HREJ           BRANCH IF WORD REJECTED
         OI    SCNDSW,X'20'             INDICATE A YES FIELD FOUND
         LR    REGWK1,REGWK3
         B     SVOWAD
TSTPOS   LH    REGWK3,POSADR       TEST POSSIBLE STATUS
         LTR   REGWK3,REGWK3
         BM    NXTATS              IF NEGATIVE MORE THAN ONE POSS.POINT
         BZ    FSTPOS              IF ZERO - FIRST POSS.POINT
         OI    POSADR,X'80'        ELSE INDICATE NEG.ADDRESS FOR MORE
         B     NXTATS              * THAN ONE POSS.POINT IN FIELD
FSTPOS   LA    REGWK1,1(REGWK1)
         STH   REGWK1,POSADR
         B     NXTATS+4
         B     NXTATS
ENDHFLD  LA    REGWK3,0
         CH    REGWK3,POSADR
         BE    SVOWAD
         TM    SCNDSW,X'08'        TEST FOR CONSONANT
         BZ    SETNO                & SET HFLDST VOWEL NO IF NOT
         TM    POSADR,X'80'
         BZ    PTOY
         OI    SCNDSW,X'10'        INDICATE POSSIBLE FIELD
SVOWAD   STH   REGWK1,HFLDST       RECORD ADDR.OF FIELD TERM.VOWEL
         TM    SCNDSW,X'30'        TEST FOR YES OR POSS.POINTS IN FIELD
         BZ    NXTFLD               IF NONE RETURN FOR NEXT FIELD
         BM    INDPOS              BRANCH IF POSS FIELD
         OI    SCNDSW,X'80'        INDICATE ONE YES FIELD
         B     NXTFLD
INDPOS   OI    SCNDSW,X'40'        INDICATE ONE POSS FIELD
         B     NXTFLD
SETNO    LH    REGWK3,HFLDST           *THIS ROUTINE INSURES THAT
         LA    REGWK3,ASTATUS(REGWK3)  * CHARACTER SEQUENCES WHICH DO
         NI    0(REGWK3),X'CF'         * NOT CONSTITUTE HYPHEN FIELDS
         OI    0(REGWK3),X'20'     SET STATUS VARIABLE
         OI    SCNDSW,X'40'         & INDICATE UNRESOLVED WORD
         B     SVOWAD                  * WILL NOT REFLECT POSS.POINTS
LSTVWL   DS    0H
        $CALL  EXCEPT              LOOKUP THE WORD IN EXCEPTION DICT
         BC    BPOS,FOUND          BRANCH IF WORD FOUND
         SPACE
*  FOLLOW RETURN FROM EXCEPTION WORD SEARCH WITH TEST OF HIT IN
*  DICTIONARY AND SUBSEQUENT ISOLATION OF YES POINTS AND LINKAGE
*  TO FOUND ROUTINE
         BAL   REGRET,SCNLFT
         BC    BPOS,FOUND          BRANCH IF FOUND BREAK
         TM    SCNDSW,X'40'        TEST FOR UNRESOLVED POSS.POIMTS
         BZ    FOUND                & IF NONE GO TO SUFFIX ANALYSIS
        $CALL  SUFFIX              SUFFIX SCAN ROUTINE
         BC    BPOS,FOUND          BRANCH IF WORD FOUND
         B     HREJ                ELSE REJECT IT
MINONE   DC    H'-1'
HFLDST   DC    H'0'
POSADR   DC    H'0'
         LTORG
WREG     EQU   6
REG3     EQU   3
REG4     EQU   4
REG5     EQU   5
