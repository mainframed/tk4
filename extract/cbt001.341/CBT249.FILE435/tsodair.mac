         MACRO
&NAME    TSODAIR &BLOCK=,&PL=(1),&ENTRY=,&BUILD=NO
         GBLC  &R
         LCLC  &N
&N       SETC  '&NAME'
         AIF   ('&PL' NE '').P1
         MNOTE 12,'PL= OMITTED, MACRO END'
         MEXIT
.P1      AIF   (('&PL(1)' EQ '1')OR('&PL(1)' EQ 'R1')).B1A
         AIF   ('&PL'(1,1) EQ '(').P1R
&N       LA    &R.1,&PL(1) .               GET PARM LIST ADDR
&N       SETC  ''
         AGO   .B1A
.P1R     ANOP
&N       LR    &R.1,&PL(1) .               GET PARM LIST ADDR
&N       SETC  ''
.B1A     AIF   ('&BLOCK' NE '').B1
         MNOTE 4,'** WARNING ** BLOCK= ASSUMED STORED IN PARM LIST'
         AGO   .U1
.B1      AIF   ('&BLOCK'(1,1) EQ '(').B1R
&N       LA    &R.0,&BLOCK(1) .            GET DAPB ADDR
&N       SETC  ''
         ST    &R.0,DAPLDAPB-DAPL(,1) .    SAVE IN PARM LIST
         AGO   .U1
.B1R     ANOP
&N       ST    &BLOCK(1),DAPLDAPB-DAPL(,&R.1) . SAVE IN PARM LIST
&N       SETC  ''
.U1      AIF   ('&BUILD' EQ 'NO').UUX
&N       MVC   DAPLUPT-DAPL(4,&R.1),CPPLUPT .   COPY IN PARMS
         MVC   DAPLECT-DAPL(4,&R.1),CPPLECT
         MVC   DAPLPSCB-DAPL(4,&R.1),CPPLPSCB
         LA    &R.0,TSOECB .            ECB ADDRESS
         ST    &R.0,DAPLECB-DAPL(,&R.1) .          STORE IT
&N       SETC  ''
.UUX     ANOP
&N       MVI   TSOECB,0 .               CLEAR ATTN ECB
         AIF   ('&ENTRY' NE '').SPCALL
         CALLTSSR EP=IKJDAIR                                *HMD 10/79*
         MEXIT
.SPCALL  AIF   ('&ENTRY'(1,1) EQ '(').RENTRY
         L     &R.15,&ENTRY(1) .        GET EPA OF IKJDAIR
         AGO   .DAIRCAL
.RENTRY  AIF   (('&ENTRY(1)' EQ '15')OR('&ENTRY(1)' EQ 'R15')).DAIRCAL
         LR    &R.15,&ENTRY(1) .        GET EPA OF IKJDAIR
.DAIRCAL BALR  &R.14,&R.15 .            CALL DAIR
         MEND
