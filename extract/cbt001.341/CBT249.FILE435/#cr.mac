***********************************************************************
*                                                                     *
*              .CR (CONTROL WORD REPLACEMENT) --- CONTROL             *
*                                                                     *
***********************************************************************
         SPACE
CR      $ENTER ,
        $CALL  GETARG              GET FIRST ARGUMENT
         BC    BPOS,CR02           BRANCH IF ARG ONE PRESENT
         LM    X1,X3,INDEXS        SET INDEXS FOR TABLE
CR01     DS    0H
         MVC   SPCTLW(2,X1),SPCTLI(X1)  WORKING EQUAL INITIAL
         BXLE  X1,X2,CR01          RESET TILL DONE
         B     CR10                AND RETURN
         SPACE
CR02     DS    0H
         LM    X1,X3,INDEXS        SET INDEXS FOR LOOKUP
CR03     DS    0H
         CLC   SPCTLI(2,X1),0(COUNT)  IS THIS IT .Q
         BE    CR04                BRANCH IF YES
         BXLE  X1,X2,CR03          CHECK TILL DONE
         MVI   EFLAG005,ERRSET     ERROR IF NOT FOUND
         B     CR10                AND GO TO RETURN
         SPACE
CR04     DS    0H
        $CALL  GETARG              GET SECOND ARGUMENT
         LR    TEMP,X1             RESTORE POINTER TO FIRST IN TABLE
         BC    BPOS,CR07           BRANCH IF ARG TWO PRESENT
         LM    X1,X3,INDEXS        SET INDEXS FOR LOOKUP
CR05     DS    0H
         CLC   SPCTLW(2,X1),SPCTLI(TEMP)  WORKING VALUE EQUAL .Q
         BNE   CR06                BRANCH IF NOT
         MVC   SPCTLW(2,X1),SPCTLI(X1)  RESTORE TO ORIGINAL
CR06     DS    0H
         BXLE  X1,X2,CR05          CHECK THEM ALL
         MVC   SPCTLW(2,TEMP),SPCTLI(TEMP)  RESTORE ARG1 ENTRY
         B     CR10                AND RETURN
         SPACE
CR07     DS    0H
         LM    X1,X3,INDEXS        SET INDEXS FOR LOOKUP
CR08     DS    0H
         CLC   SPCTLW(2,X1),0(COUNT)  WORKING VALUES EQUAL .Q
         BNE   CR09                BRANCH IF NOT
         MVC   SPCTLW(2,X1),BLANKS    REMOVE THAT WORKING ENTRY
CR09     DS    0H
         BXLE  X1,X2,CR08          GO TILL DONE
         MVC   SPCTLW(2,TEMP),0(COUNT)  REDEFINE WORKING VALUE
CR10     DS    0H
        $EXIT  RC=0                AND RETURN
