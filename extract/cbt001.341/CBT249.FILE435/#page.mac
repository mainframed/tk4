***********************************************************************
*                                                                     *
*              INTERNAL FUNCTION 'PAGE' --- GENERATE PAGE EJECT       *
*                                                                     *
***********************************************************************
         SPACE
PAGE    $ENTER ,
         CLI   ICOLFLG,ICOLMC      IN MULTI-COLUMN MODE .Q
         BNE   PAEJ00A             BRANCH IF NOT
        $CALL  COLPAGE             EJECT A COLUMN IF YES
         B     PAEJ17              GO TO RETURN
         SPACE
PAEJ00A  DS    0H
         L     COUNT,PAGECNT       REAL PAGE EJECT COUNT
         LA    COUNT,1(,COUNT)     PLUS ONE
         ST    COUNT,PAGECNT       UPDATE REAL PAGE EJECT COUNT
PAEJ00   DS    0H
        $IFOFF SWITCHS,FFIRST,PAEJ01    BRANCH IF NOT FIRST PAGE
        $IFOFF TTYPE,TONLINE,PAEJ02     BRANCH IF NOT ONLINE
PAEJ01   DS    0H
        $IFOFF SWITCHS,(FEMPTY,FNOEMPTY),PAEJ02 EMPTY PAGE AND .EM NO
         OC    FNQCNT,FNQCNT       ANY FOOTNOTES QUEUED .Q
         BNZ   PAEJ02              NOT EMPTY IF YES
         OC    FKQCNT,FKQCNT       ANY FLOATING KEEPS QUEUED .Q
         BNZ   PAEJ02              NOT EMPTY IF YES
         OC    CPQCNT,CPQCNT       ANY CONDITIONAL KEEPS QUEUED .Q
         BNZ   PAEJ02              NOT EMPTY IF YES
         OC    RMTRIG,RMTRIG       NO, ANY REMOTES WAITING .Q
         BNZ   PAEJ02              IF YES, ALSO NOT NULL
        $IFON  SWITCHS,FNOEMBMP,PAEJ16  BRANCH IF NO PAGE NUMBER BUMP
         B     PAEJ13              REALLY NULL PAGE
         SPACE
PAEJ02   DS    0H
        $IFOFF SWITCHS,FEMPTY,PAEJ03    BRANCH IF PAGE NOT EMPTY
        $CALL  PAGETOP             YES, PRINT HEADERS FIRST
        $CALL  RMNEXT              AND SET UP FOR ANY USER REMOTE
         SPACE
PAEJ03   DS    0H
         LH    R1,BMMRG            BOTTOM MARGIN
         AH    R1,FNSEP            PLUS FOOTNOTE MARGIN
         CH    R1,RMTRIG           REMOTE BEFORE BOTTOM MARGIN .Q
         BNL   PAEJ04              IF NOT, SKIP
         LH    COUNT,PLCT          YES, SPACE DOWN TO IT
         SH    COUNT,RMTRIG        ..
        $CALL  SPACER              ..
         SPACE
         L     COUNT,PAGECNT       SAVE REAL PAGE EJECT COUNT
         LH    R1,RMTRIG           REMOTE TO IMBED
        $CALL  RMIMBED             IMBED A REMOTE SEQUENCE
         SPACE
         C     COUNT,PAGECNT       DID A PAGE EJECT OCCUR .Q
         BE    PAEJ00              IF NOT, TRY AGAIN
         B     PAEJ17              YES, DON'T DO ANOTHER
         SPACE
PAEJ04   DS    0H
         LH    COUNT,PLCT          NUMBER OF LINES LEFT ON THIS PAGE
         SH    COUNT,BMMRG         EXCLUDING BOTTOM MARGIN
         SH    COUNT,FNSEP         AND FOOTNOTE MARGIN
        $ON    SWITCHS,OOSWS       DISABLE OUTPUT OVERLAY
        $CALL  SPACER              SKIP TO BOTTOM OF PAGE
         SPACE
        $CALL  FNPRT               PRINT ANY FOOTNOTES NOW
         SPACE
         LA    R1,SYSBMRM          BOTTOM MARGIN REMOTE
        $CALL  RMCOND              TRY IT
         SPACE
         L     X1,ADEVHEAD         ASSUME EVEN FOOTING
        $CALL  ODDEVN              EVEN OR ODD PAGE .Q
         BC    BPOS,PAEJ06         BRANCH IF EVEN
         L     X1,ADODHEAD         ODD PAGE FOOTING
PAEJ06   DS    0H
         L     R1,DEFHSFS+TAB3DEF  TT/BT OVERLAP COUNT
         BCTR  R1,0                LESS ONE
         LA    R0,L'EVNHEAD        LENGTH FOR ONE POINTER
         MR    R0,R0               OFFSET TO LAST POINTER
         AR    X1,R1               POINT TO LAST ENTRY
         CLC   FSCNT,H1            ONE ENTRY ONLY .Q
         BL    PAEJ07              BRANCH IF ZERO
         BNE   PAEJ08              BRANCH IF NOT ONE
         OC    0(L'EVNHEAD,X1),0(X1)    ANY FOOTING PRESENT .Q
         BNZ   PAEJ08              BRANCH IF NOT NULL
PAEJ07   DS    0H
         LH    COUNT,BMMRG         GET TOTAL BOTTOM MARGIN
         B     PAEJ11              DO SPACING ALL AT ONCE
         SPACE
PAEJ08   DS    0H
         LH    COUNT,FTMRG         SKIP FOOTING MARGIN
        $ON    SWITCHS,OOSWS       DISABLE OUTPUT OVERLAY
        $CALL  SPACER              SKIP DOWN TO FOOTING START
         LH    COUNT,FSCNT         GET FOOTING SPACE
PAEJ09   DS    0H
         S     COUNT,F1            LESS ONE
         BM    PAEJ10              BRANCH IF DONE
        $CALL  HEADPRT             PRINT ONE FOOTING LINE
         LA    R0,L'EVNHEAD        LENGTH ONE FOOTING ENTRY
         SR    X1,R0               BACK UP TABLE PTR
         B     PAEJ09              DO REMAINDER
         SPACE
PAEJ10   DS    0H
         LH    COUNT,BMMRG         GET BOTTOM MARGIN
         SH    COUNT,FTMRG         LESS FOOTING MARGIN
         SH    COUNT,FSCNT         LESS FOOTING SPACING
PAEJ11   DS    0H
         LTR   COUNT,COUNT         GO SKIP TO BOTTOM OF PAGE
         BNP   PAEJ12              BRANCH IF NOTHING TO DO
        $ON    SWITCHS,OOSWS       DISABLE OUTPUT OVERLAY
        $CALL  SPACER              SKIP DOWN TO PAGE BOTTOM
PAEJ12   DS    0H
         LA    R1,SYSBOTRM         BOTTOM OF PAGE REMOTE
        $CALL  RMCOND              TRY IT
         SPACE
PAEJ13   DS    0H
        $IFON  SWITCHS,PINCRNO,PAEJ16   SKIP IF NO PAGE INCREMENT
         L     COUNT,PACNTD        DECIMAL PART OF PAGE NUMBER
         C     COUNT,PACNTDI       ARE WE USING IT .Q
         BE    PAEJ15              BRANCH IF NOT
         LTR   COUNT,COUNT         ARE WE ALREADY USING IT .Q
         BNM   PAEJ14              BRANCH IF YES
         TM    PACNTI+L'PACNTI-1,X'01'  ODD INTEGER PAGE .Q
         BO    PAEJ15              YES, DON'T INCREMENT YET
         SR    COUNT,COUNT         START AT THE BEGINNING
PAEJ14   DS    0H
         LA    COUNT,1(,COUNT)     BUMP DECIMAL PART
         ST    COUNT,PACNTD        UPDATE DECIMAL COUNT
         B     PAEJ16              AND CONTINUE
PAEJ15   DS    0H
         L     COUNT,PACNTI        INTEGER PART OF PAGE NUMBER
         LA    COUNT,1(,COUNT)     PLUS ONE
         ST    COUNT,PACNTI        AND UPDATE WITH NEW
         SPACE
PAEJ16   DS    0H
         MVC   PLCT,PLZ            SET PAGE COUNT FOR NEXT PAGE
         MVC   CDPLCT,PLZ          SET COLUMN COUNT FOR NEXT PAGE
        $ON    SWITCHS,FEMPTY      NOTE THAT NEXT PAGE IS EMPTY
        $OFF   SWITCHS,(AFIRST,FFIRST)  IT IS NO LONGER THE FIRST PAGE
        $IFON  SWITCHS,NPSWS,PAEJ17     BRANCH IF NOPRINT MODE
         CLC   PASSNO,DEFPASOF+TAB3DEF  IS THIS THE LAST PASS .Q
         BNE   PAEJ17              BRANCH IF NOT
         L     COUNT,PACNTI        GET NEXT INTEGER PAGE NUMBER
         C     COUNT,DEFPAGL+TAB3DEF    MORE THAN LAST TO DO .Q
         BNH   PAEJ17              BRANCH IF NOT
         LA    R1,WRETPA           FINAL PAGE IS COMPLETE
        $CALL  QUITS               CALL TERMINATION ROUTINE
         SPACE
PAEJ17   DS    0H
         MVC   HYSUPWRK,HYSUPCNT   RESET NUMBERED HYPHENATION SUPPRESS
         XC    SKSP,SKSP           CLEAR ANY OUTSTANDING COND SK/SP
        $CALL  RMNEXT              SETUP FOR NEXT REMOTE
        $RESTOR ,                  RESTORE CALLER'S REGISTERS
         BR    RETURN              AND RETURN TO CALLER
         SPACE
SYSBMRM  DC    CL8'SYSBM'          ABOVE BOTTOM MARGIN REMOTE
SYSBOTRM DC    CL8'SYSBOT'         BOTTOM OF PAGE REMOTE
