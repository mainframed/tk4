***********************************************************************
*                                                                     *
*              INTERNAL FUNCTION 'SPACEIM' --- SKIP ONLINE IMMEDIATE  *
*                                                                     *
***********************************************************************
         SPACE
*        ON ENTRY --  (COUNT) IS NUMBER OF LINES TO SKIP
         SPACE
SPACEIM $ENTER ,
         LTR   COUNT,COUNT         TEST FOR VALID ARGUMENT
         BM    ABSPIM              BRANCH IF NOT VALID
         BZ    SPIM07              BRANCH IF NOTHING TO DO
        $IFOFF TTYPE,TONLINE,SPIM07     FORGET IMMEDIATE IF NOT ONLINE
         CLC   PASSNO,DEFPASOF+TAB3DEF  IS THIS LAST PASS .Q
         BNZ   SPIM07              NO, DO NOT SPACE THEN
         SPACE
SPIM01   DS    0H
         LA    TEMP,LINEFC         MAXIMUM LINEFEED COUNT
         CR    COUNT,TEMP          MORE THAN THAT TO DO .Q
         BH    SPIM02              BRANCH IF YES
         LR    TEMP,COUNT          SET FEWER LINES
SPIM02   DS    0H
         SR    COUNT,TEMP          LESS LINES WE DO NOW
        $IFOFF TTYPE,TFILE,SPIM03  BRANCH IF OUTPUT NOT TO FILE
         L     R1,BUFF3AD          POINT TO WORK AREA
         XC    0(4,R1),0(R1)       CLEAR A RDW
         MVC   4(L'LINEF*LINEFC,R1),LINEF    ENTER LINE FEED STRING
         LA    TEMP,3(TEMP,TEMP)   TOTAL LENGTH LESS ONE LINEFEED
         STH   TEMP,0(,R1)         AND SET THE LENGTH
         LR    R0,R1               POINT TO RESULT
        $CALL  PUTPRT              OUTPUT THE RESULT
         B     SPIM06              GO TEST FOR DONE
         SPACE
SPIM03   DS    0H
        $IFOFF SWITCHS,TSOWS,SPIM05     BRANCH IF NOT TSO
        $IFON  TTYPE,TSCREEN,SPIM04     BRANCH IF TSO CRT
         AR    TEMP,TEMP           DOUBLE NUMBER FOR LENGTH
         LA    R1,LINEF            POINT TO LINEFEED STRING
         TPUT  (1),(TEMP),ASIS     IDLE-LINEFEED SOME TSO SPACES
         B     SPIM06              GO TEST FOR DONE
         SPACE
SPIM04   DS    0H
         LA    R1,BLANKS           POINT TO A BLANK
         TPUT  (1),1,EDIT          ONE TSO CRT LINE AT A TIME
         BCT   TEMP,SPIM04         DO THEM ALL
         B     SPIM06              GO TEST FOR DONE
         SPACE
SPIM05   DS    0H
        $IFOFF SWITCHS,CMSWS,SPIM06     BRANCH IF NOT CMS
         AR    TEMP,TEMP           DOUBLE NUMBER FOR LENGTH
         LA    R1,LINEF            POINT TO LINEFEED STRING
         WRTERM (R1),(TEMP),EDIT=NO     LINEFEED SOME CMS SPACES
         SPACE
SPIM06   DS    0H
         LTR   COUNT,COUNT         ANY LINES LEFT .Q
         BP    SPIM01              BRANCH BACK IF YES
         SPACE
SPIM07   DS    0H
        $RESTOR ,                  RESTORE CALLER'S REGISTERS
         BR    RETURN              AND RETURN TO CALLER
         SPACE
ABSPIM   DS    0H
        $LOGERR 1010               NEGATIVE IMMEDIATE SPACING ERROR
