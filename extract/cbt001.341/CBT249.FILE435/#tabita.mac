***********************************************************************
*                                                                     *
*              TABITA ---  ALIGN PHRASE ON TAB ALIGNMENT CHARACTER    *
*                                                                     *
***********************************************************************
         SPACE
TABITA  $ENTER ,
         USING LINKELEM,P
         USING TBAREA,TABS
         LR    R0,X1               SAVE CURRENT PTR
         L     X1,LINKFOR          POINT TO NEXT CHAR BLOCK
         USING LNEWELEM,X1         INFORM ASSEMBLER
         LR    R1,TEMP             GET INTER TAB COUNT
         LA    X2,1                INITIALIZE CHAR COUNT
TBA1     DS    0H
         CLC   LNEWCHAR,TBCHAR     FOUND ALIGNMENT CHAR .Q
         BE    TBA2                BR IF YES
         CLI   LNEWCHAR,X'05'      DID WE HIT NEXT TAB .Q
         BE    TBA15               EXIT IF YES
         LA    X2,1(,X2)           BUMP CHAR COUNT
         L     X1,LNEWFOR          CHAIN TO NEXT BLOCK
         LTR   X1,X1               ANY MORE CHARS .Q
         BZ    TBA15               EXIT IF NO
         BCT   R1,TBA1             LOOP TILL TAB COLUMN
TBA15    DS    0H
         LR    C,R0                RETURN PTR IN C
        $CC    NEG                 INDICATE NO ALIGNMENT
         B     TBA4                RETURN
TBA2     DS    0H
         LR    X1,R0               RESTOR START PTR
        $CALL  TABCHK              ADJUST FOR HYPHEN
         SR    TEMP,X2             CALC NUMBER OF FILL CHAR
         BP    TBA3                BR IF POSITIVE
         LA    TEMP,1              ELSE SET TO 1
TBA3     DS    0H
         STC   TEMP,LINKMULT       UPDATE MULTIPLIER FIELD
        $CC    POS                 INDICATE ALIGNMENT OK
TBA4     DS    0H
        $EXIT  KEEP=(C),RC=0
         DROP  P,X1,TABS
