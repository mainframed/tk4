***********************************************************************
*                                                                     *
*        INTERNAL FUNCTION 'RMIMBED' --- IMBED A REMOTE SEQUENCE.     *
*                                                                     *
***********************************************************************
         SPACE
*        TO ENTER:   $CALL  RMIMBED                                   *
*                  (R1) = -1 --  APIMBLOCK MOSTLY BUILT BY .SI        *
*                  (R1) = -4 --  (SAME AS ABOVE SOME DAY)             *
*                  (R1) = -8 --  AUTOMATIC NAMED REMOTE               *
*                  (R1) = 0  --  SHOULD NEVER HAPPEN                  *
*                  (R1) > 0  --  AUTO REMOTE NUMBER TO IMBED          *
         SPACE
RMIMBED $ENTER ,
         MVC   RMIMSAV(4*16),0(BASESAVE)     SAVE REGS FOR PUSHDOWN
         LTR   X1,R1               TEST CALL TYPE
         BP    RMIM01              BRANCH IF NUMBERED AUTOMATIC
         BZ    ABRMIM              BRANCH IF DON'T KNOW
         C     X1,FM8              AUTOMATIC NAMED REMOTE .Q
         BE    RMIM02              BRANCH IF YES
         B     RMIM03              ELSE IDENT FILLED IN
         SPACE
RMIM01   DS    0H
         XC    APIMNAME,APIMNAME   CLEAR IDENTIFIER
         SH    R1,PLZ              LESS PAGE LENGTH
         LCR   R1,R1               MAKE IT POSITIVE
         A     R1,F1               PLUS ONE
         ST    R1,APIMNAME+4       ENTER REMOTE NUMBER
         XC    APIMFLAG,APIMFLAG   CLEAR FLAGS
RMIM02   DS    0H
         MVC   APIMMIN,F1          DEFAULT MIN RECORD NUMBER
         MVC   APIMMAX,F16MEG      DEFAULT MAX RECORD NUMBER
         MVC   APIMGOTO,BLANKS     CLEAR GOTO TARGET
         SPACE
        $ON    APIMFLAG,(IO$RM,IO$UEOF)  THIS IS A REMOTE
        $ON    APIMFLAG,IO$AURM    REMOTE TRIGGERED AUTOMATICALLY
         XC    RMTRIG,RMTRIG       PREVENT ANY MORE AUTOMATIC REMOTES
         B     RMIM04              AND CONTINUE
         SPACE
RMIM03   DS    0H
         XC    RMIMSAV(16*4),RMIMSAV    CLEAR RETURN REGS FOR SIGNAL
         SPACE
RMIM04   DS    0H
         LA    R1,APIMBLOK         IN ORDER TO IMBED THIS NEW FILE
        $CALL  RDFILE              INPUT COORDINATOR WILL DO IT
         C     X1,FM8              AUTOMATIC NAMED REMOTE .Q
         BE    RMIM06              BRANCH IF YES TO DRIVER
         LTR   X1,X1               A SIGNAL REQUEST .Q
         BM    RMIM05              BRANCH IF YES
        $IFOFF SWITCHS,FRMKP,RMIM06  BRANCH IF DON'T KEEP ENVIRONMENT
RMIM05   DS    0H
        $OFF    SWITCHS,FRMKP      RESET KEEP FLAG
        $RESTOR ,                  RESTORE CALLER'S REGISTERS
         BR    RETURN              RETURN TO CALLER
         SPACE
RMIM06   DS    0H
        $OFF   SWITCHS,FRMKP       RESET FLAG
        $RESTOR ,                  RESTORE CALLER'S REGISTERS
         L     RETURN,VMAIN        TOP OF SCRIPTW DRIVER
         L     BASE,ADBASE         SCRIPT BASE
         BR    RETURN              RETURN TO SCRIPT
         SPACE
ABRMIM   DS    0H
        $LOGERR 1006               REMOTE IMBED CALL ERROR
