***********************************************************************
*                                                                     *
*              .SA (SAVE STATUS) --- CONTROL                          *
*                                                                     *
***********************************************************************
         SPACE
SA      $ENTER ,
         L     X3,STAKCTR          SAVE NEST COUNTER
         LA    X3,1(,X3)           PLUS ONE
         LA    R0,STAKMAX          MAXIMUM NEST ALLOWED
         CR    X3,R0               EXCEEDED THE MAXIMUM .Q
         BH    SAE011              ERROR IF YES
         L     X1,STAKANCH         POINTER TO FIRST SAVE ELEMENT
         LA    R1,STAKSIZE         SPACE FOR ONE MORE LEVEL
        $CALL  GETCELL             GET THE AREA
         BC    BNEG,SAE024         FORGET IF NO SPACE
         SPACE
         USING STAKSAVE,TEMP       AREA LOOKS LIKE THIS
         ST    TEMP,STAKANCH       SET NEW FIRST SAVE ELEMENT
         ST    X1,STAKCHN          POINT TO NEXT ELEMENT
         ST    X3,STAKCTR          SET NEW NEST COUNTER
         MVC   STAKTLD1(SWCTLZ),SWCTL      SAVE SWITCHES
         MVC   STAKTLD2(LFMTOLD2),FMTOLD2  SAVE PAGE DESCRIPTORS
         MVC   STAKICOL,ICOLFLG         SAVE TYPE OF COLUMN FORMAT
         MVC   STAKOC,OCCNT        SAVE OVERLAY CHARACTER STATUS
         XC    OCCNT,OCCNT         CLEAR COUNT LEFT
         MVC   OCCHAR,OCCHAR+1     AND RESET CHARACTER
         L     R1,TRANADO          OUTPUT TRANSLATE TABLE ADDRESS
         MVC   STAKTRNO,0(R1)      SAVE OUTPUT TRANSLATE TABLE
         L     R1,TRANADI          INPUT TRANSLATE TABLE ADDRESS
         MVC   STAKTRNI,0(R1)      SAVE INPUT TRANSLATE TABLE
         MVC   STAKESTA,ESTABAD    SAVE ESCAPE CHAR/TABLE ADDRESS
        $CALL  TBCOPY              COPY USER'S CURRENT TABS
         MVC   STAKTABI,TBIND      SAVE USER TAB CHARACTER
         MVC   STAKTABA,TBANCHR    SAVE CURRENT TAB
         ST    R1,TBANCHR          SAVE COPY ANCHOR
         DROP  TEMP
         SPACE
SA01     DS    0H
        $EXIT  RC=0                AND RETURN TO CALLER
         SPACE
SAE011   DS    0H
         MVI   EFLAG011,ERRSET     SAVE STACK EXCEEDED
         B     SA01                GO TO RETURN
SAE024   DS    0H
         MVI   EFLAG024,ERRSET     NO STORAGE TO SAVE
         B     SA01                GO TO RETURN
