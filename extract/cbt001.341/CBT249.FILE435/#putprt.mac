***********************************************************************
*                                                                     *
*              'PUTPRT' --- I/O ROUTINE TO PRINT FILE                 *
*                                                                     *
***********************************************************************
         SPACE
*        ON ENTRY  --  (R0)=0 AND IOCBPRT=0 TO OPEN FILE
*                      (R0)>0 ADDRESS OF DATA TO PUT DATA
*                      (R0)<0 TO CLOSE FILE
*
*        ON EXIT   --  CC = BPOS  IF OK
*                         = BNEG  IF ERROR
         SPACE
PUTPRT  $ENTER ,
         L     X1,IOCBPRT          SYSPRINT IOCB ADDRESS
         LTR   R0,R0               TEST CALL TYPE
         BP    PPRT04              BRANCH IF PUT CALL
         BM    PPRT02              BRANCH IF CLOSE CALL
         LTR   X1,X1               FILE TRIED BEFORE .Q
         BNZ   PPRT06              YES, DON'T DO IT AGAIN
         SPACE
         MVI   IOCBPRT,X'80'       MARK ADDR TRIED
         LA    TEMP,SAVED0         POINT TO SHORT WORK AREA
         USING IOCIODEF,TEMP       IODEF LOOKS LIKE THIS
         MVC   IOCIODEF(PPIODEFL),PPIODEF  FRESH IODEF
         LA    R1,IOCDDNAM         POINTER TO FILENAME
        $CALL  RDDDREP             REPLACEMENT FILENAME .Q
         MVC   IOCDDNAM,0(R1)      ENTER REQUIRED NAME
         SPACE
         LA    R1,IOCDDNAM         POINT AGAIN
        $CALL  SYTEST              CAN OPEN SUCCEED .Q
         BC    BNEG,PPRT01         BRANCH IF NOT
         LA    R1,IOCIODEF         POINT TO IODEF
        $CALL  SYPUTOP             OPEN OUTPUT FILE
         ST    R1,IOCBPRT          SAVE IOCB ADDRESS
         BC    BPOS,PPRT01         CONTINUE IF SUCCESSFUL
         MVI   IOCBPRT,X'80'       TRIED AND FAILED
PPRT01   DS    0H
         DROP  TEMP
         TM    IOCBPRT,X'80'       TRIED AND SUCCEEDED .Q
         BZ    PPRT05              BRANCH IF YES
         B     PPRT06              ELSE SET FOR FAILURE
         SPACE
         USING IOCB,X1             FILE CONTROL BLOCK
PPRT02   DS    0H
         LA    X1,0(,X1)           CLEAR HIGH BYTE
         LTR   X1,X1               DOES FILE EXIST .Q
         BZ    PPRT03              BRANCH IF NOT
         LA    R1,IOCB             IOCB ADDRESS
        $CALL  SYPUTCL             CLOSE OUTPUT FILE
PPRT03   DS    0H
         XC    IOCBPRT,IOCBPRT     CLEAR IOCB POINTER
         B     PPRT05              GO TO SUCCESSFUL EXIT
         SPACE
PPRT04   DS    0H
         LA    X1,0(,X1)           CLEAR HIGH BYTE
         LTR   X1,X1               DOES FILE EXIST .Q
         BZ    PPRT05              BRANCH IF NOT
         LA    R1,IOCB             IO CONTROL BLOCK
        $CALL  SYPUT               OUTPUT THE RECORD
         DROP  X1
         SPACE
PPRT05   DS    0H
        $CC    POS                 ALL IS SUCCESSFUL
         B     PPRT07              GO TO RETURN
         SPACE
PPRT06   DS    0H
        $CC    NEG                 UNSUCCESSFUL
         SPACE
PPRT07   DS    0H
        $EXIT  RC=0                RETURN TO CALLER
         SPACE
PPIODEF  SYSIODEF FILETYP=IOC$QSAM,DDNAME=SYSPRINT,                    *
               RECFM=IOCRECV+IOCRECCA,                                 *
               RECMERG=IOCRECF+IOCRECV+IOCRECBR+IOCRECCA,              *
               LRECL=137,MAXLREC=240
PPIODEFL EQU   *-PPIODEF           LENGTH OF IODEF
