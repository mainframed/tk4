*                             ********
* *            SOURCE BOOK *** DEUH03 *** NON-ALPHA ANALYSIS       1-1
*                             ********
         SPACE 3
*
* NON-ALPHA SCAN BEGINS BY SEARCHING HYPFIELD FOR VARIOUS CHARACTERS
* AND BRANCHING TO PROPER SUBROUTINES
NASCN    L     REG3,HFLDAD        ESTABLISH REG3 AS POINTER TO HYPFIELD
         LA    REG1,0              ZERO IN REG1 TO CLEAR SWITCHES
         ST    REG1,ATEOW             TEMP. EOW ADDRESS
         STH   REG1,SCNDSW  &HYFSW   NON-ALPHA & ALPHA SCAN INDICATORS
         IC    REG1,0(REG3)        GET VALUE IN LTF BYTE
         LTR   REG1,REG1           TEST FOR L-T-F OR ALL POINTS
         BZ    NOLTF               BRANCH IF  ALL POINTS
         AR    REG1,REG3            ADD TO ADDRESS OF USERS WORD AREA
NOLTF    ST    REG1,ALTF           STORE ZERO AS ADDRESS OF L-T-F CHAR
*                            OR SAVE ACTUAL ADDRESS OF L-T-F CHAR.
         LA    REG3,1(REG3)
         LA    REGWK2,0            INITIALIZE GSTATUS POINTER
         STH   REGWK2,NUMFIT       CLEAR ASCAN CONTROL COUNTERS
         STH   REGWK2,NUMCHR       *
         STH   REGWK2,LSTCHROF     SET LENGTH-1 OF INPUT
         LA    REGWK0,30           INITIALIZE TO LIMIT OF WORD SEGMENT
         LA    REGWK3,0
         LA    REG4,64           INITIALIZE FOR FULL SEARCH OF HYPFIELD
NXTCHR   LA    REG2,0              CLEAR REG2
         EX    REG4,NASC          DO TRT FOR INDIVIDUAL CHARACTER TEST
         CH    REG2,=H'8'           INVALID CHARACTER CONFIGURATION
         BH    HREJ                  REJECT THE WORD
         C     REG3,ALTF           TEST FOR LAST TO FIT CHAR.
         BE    MKLTF                & IF YES MARK L-T-F
RENAS    SLL   REG2,24             ISOLATE TABLE FUNCTION
         SRL   REG2,22             AND MULT.BY 4 FOR PROPER BRANCH EXIT
         B     NAEXT(REG2)        EXECUTE LINKAGE TO PROPER EXIT
NAEXT    B     HREJ               ** DUMMY BRANCH FOR IMPOSSIBLE CHAR.
         B     ALPH               *  THIS SERIES OF BRANCHES
         B     NUMB               *   CONSTITUTES THE LINKAGE TO
         B     CMMA               *** EACH SPECIALIZED ROUTINE
         B     HYPN               *   CONNECTED WITH THE NON-ALPHA
         B     SHFT               *   SCAN (IE. ALPHA,COMMA,ETC.)
         B     APOS               *
         B     EOWC
         B     NULL
MKLTF    STH   REGWK3,NUMFIT       SAVE NUMBER OF CHAR. TO FIT
         B     RENAS               RESUME NON ALPHA SCAN
REJWDSG  NI    HYFSW,B'11111100'   CLEAR ALPHA + NUMBER
REJWDSG1 LA    REG3,1(REG3)        GET NEXT PRIMARY CHARACTER
         LA    REGWK2,1(REGWK2)     AND NEXT STATUS BYTE
         CLI   0(REG3),X'1C'       IS CHAR A TEXT HYPHEN
         BE    HYPN                IF YES GO TO HYPN TO END SEGMENT
         CLI   0(REG3),X'20'       IS CHAR AN EOW
         BE    EOWC                YES GO TO E-O-W CHAR. ROUTINE
         B     REJWDSG1            IF NO GET NEXT PRIMARY CHARACTER
*
* TEMPORARY END OF WORD PROCESSOR
*
TEOW     L     REG1,HFLDAD
         CLI   0(REG1),X'00'       TEST L-T-F REQD.
         BNE   TLTFMK              IF YES GO TEST L-T-F MARKED
         ST    REG3,ATEOW          SAVE ADDR OF TEOW CHAR.
         TM    HYFSW,X'02'              TEST FOR ALPHA CHAR. IN WORD
         BO    STRCNTS              & CONTINUE ALPHA PROCESSING IF YES
BYSGMT   NI    HYFSW,X'00'         ELSE SET UP TO BYPASS WORD SEGMENT
         LA    REGWK3,0            *
         B     NULL                *RETURN FOR NEXT CHARACTER
TLTFMK   LH    REG1,NUMFIT         IF L-T-F REQ'D.
         LTR   REG1,REG1           *  TEST FOR L-T-F MARKED
         BH    EOWC+4                & TREAT CHAR. AS FINAL EOW IF YES
         B     BYSGMT              ELSE BYPASS WORD SEGMENT
         EJECT
* THIS ROUTINE TESTSCND IS DESIGNED TO INTERROGATE A CHARACTER OR
*  SERIES OF CHARACTERS IN CONTIGUOUS BYTES TO DETERMINE SIGNIFICANT
*  CHARACTERISTICS OF THE CHARACTER(S) AND RELAY THAT INFORMATION TO
*  THE CALLING ROUTINE IN THE FORM OF CERTAIN SWITCH SETTINGS.
* THE ROUTINE ASSUMES THAT REGISTER 3 CONTAINS THE ADDRESS OF A PRIMARY
*  CHARACTER (CHAR. PRECEEDING CHAR.(S) OF INTEREST AND THAT THE CODES
*  USED TO REPRESENT THE CHARACTERS ARE THE PRATT CODES USED THROUGHOUT
*  THE HYPHENATION 360 PROGRAM.
TESTSCND LA    WREG,1(REG3)        INITIALIZE FOR SECONDARY SCAN
LOOP     CLI   0(WREG),X'19'       TEST SECONDARY FOR ALPHA/NON-ALPHA
         BH    NASCR               BRANCH FOR NON-ALPHA SECONDARY CHAR.
         OI    SCNDSW,X'02'        TURN ON SECONDARY ALPHA INDICATOR
         BR    REGRET               AND RETURN TO PRIMARY ROUTINE
NASCR    CLI   0(WREG),X'1A'       TEST SECONDARY CHAR. FOR NUMERIC
         BH    NNSCR                BRANCH FOR NON-NUMERIC SECONDARY
         OI    SCNDSW,X'01'        TURN ON SECONDARY NUMERIC INDICATOR
         BR    REGRET               AND RETURN TO PRIMARY ROUTINE
NNSCR    CLI   0(WREG),X'1D'       TEST SECONDARY CHAR FOR SHIFT CODE
         BE    BYSCND               AND BYPASS TO NEXT CHARACTER
         CLI   0(WREG),X'1F'       TEST SECONDARY CHAR. FOR NULL
         BNE   CLRSC                AND BRANCH TO CLEAR SCNDSW IF NOT
* THE FOLLOWING PERMITS THE BYPASSING OF SHIFT & NULL SECONDARIES
BYSCND   LA    WREG,1(WREG)        BUMP WORK INDEX TO NEXT HYPFIELD
         B     LOOP                 CHAR. & CONTINUE SECONDARY SCAN
CLRSC    NI    SCNDSW,X'FC'        CLEAR SECONDARY ALPHA/NUMERIC IND.
         BR    REGRET               AND RETURN TO PRIMARY ROUTINE
ALTF     DC    F'0'                ADDR.OF ACTUAL L-T-F CHARACTER
AEOW     DC    F'0'
LSIG     DC    F'0'
RJCTAD   DC    F'0'                  ADDRESS OF REJECT RETURN
HYFLDCCT DC    H'0'                  COUNT OF HYPFIELD NUM.OF CHARS.
SGSTS    MVC   GSTATUS(0),0(REG3)
TGSTS    TR    GSTATUS(0),VOWTBL     TRANS.TO SET INDICATORS
NASC     TRT   0(0,REG3),NASCTBL     FOR NON-ALPHA SCAN SEARCH
ITESTBL  DC    32X'00'
         DC    X'2021'
NASCTBL  DC    26X'01'             ** NON-ALPHA TABLE FOR LINKAGE ALPHA
         DC    X'02'                * TO ANALYSIS ROUTINES        NUMBR
         DC    X'030405060807'    **SPECIAL FROM COMMA TO EOW
         LTORG
