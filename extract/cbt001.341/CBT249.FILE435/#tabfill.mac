***********************************************************************
*                                                                     *
*              TABFILL---  SET UP FILL CHARACTERS                     *
*                                                                     *
***********************************************************************
         SPACE
TABFILL $ENTER ,
         USING LINKELEM,P
         USING TBAREA,TABS
         LR    R0,COUNT            SAVE PTR TO CHAR AFTER TAB
         LR    R14,R1              SAVE PTR TO FOPMLIST
         SR    COUNT,COUNT         ASSUME NO NEW BLOCKS ADDED
         USING FOPMLIST,R1
        $IFOFF SWITCHS,TCSWS,TABF05     BRANCH IF NOT IN TC MODE
         STH   COUNT,FOINDR        CLEAR RIGHT INDENT VALUE
TABF05   DS    0H
         DROP  R1
         LTR   TABS,TABS           USER TABS DEFINED .Q
         BZ    TABF60              BR IF NO
         CLI   LINKMULT,X'01'      WAS TAB FILL LENGTH SET TO 1 .Q
         BE    TABF10              BR IF YES
         BH    TABF07              BR IF GREATER THAN 1
         MVI   LINKMULT,X'01'      SET TO 1
         B     TABF20              AND CONTINUE
TABF07   DS    0H
         LH    TEMP,TBLEN          LENGTH OF FILL STRING
         C     TEMP,F1             LENGTH = 1 .Q
         BNE   TABF30              BR IF NO
TABF10   DS    0H
        $IFOFF SWITCHS,THSWS,TABF20     ADJUSTED FOR HYPHEN .Q
         MVI   LINKMULT,X'02'      CHANGE HYPHEN TO BLANK
TABF20   DS    0H
         MVC   LINKCHAR,TBFILL     MOVE IN FILL CHAR
        $IFOFF SWITCHS,TCSWS,TABF60     ARE WE IN TC MODE .Q
         MVI   LINKCHAR,C' '       YES -> BLANK INITIAL FILL CHAR
         B     TABF60              SINGLE CHAR PROCESSED
TABF30   DS    0H
         LR    R1,C                SAVE CURRENT COLUMN
         L     X1,BUFF3AD          FETCH ADDRESS OF BUFFER
         LA    X1,4(,X1)           SKIP OVER RDW
         LR    X2,TEMP             SAVE FILL LENGTH
         AR    X2,X1               INDEX INTO BUFFER
         BCTR  TEMP,0              SETUP FOR INITIAL MOVE
         EX    TEMP,TBFMVE1        MOVE IN FILL STRING TO BUFFER
         LA    COUNT,B2LG          LENGTH OF INPUT BUFFER
         SR    COUNT,TEMP          MINUS FIELD ALREADY MOVED
         BCTR  COUNT,0             ADJUST FOR MOVE
         EX    COUNT,TBFMVE2       PROPOGATE FILL STRING
         AR    R1,X1               POINT TO START OF ACTUAL FILL
         MVC   LINKCHAR(1),0(R1)   SET 1ST CHAR TO REPLACE TAB
        $IFOFF SWITCHS,TCSWS,TABF40     ARE WE IN TC MODE .Q
         MVI   LINKCHAR,C' '       YES -> BLANK INITIAL FILL CHAR
TABF40   DS    0H
         LA    R1,1(,R1)           SKIP OVER 1ST CHAR
         SR    COUNT,COUNT         CLEAR FOR EXECUTE
         IC    COUNT,LINKMULT      SET LENGTH OF FILL MOVE
        $IFON  SWITCHS,TCSWS,TABF42     BRANCH IF IN TC MODE
        $IFON  SWITCHS,THSWS,TABF45     BRANCH IF TAB HYPHEN SWITCH
TABF42   DS    0H
         BCTR  COUNT,0             DECREMENT FOR MVC
TABF45   DS    0H
        $IFOFF SWITCHS,TCSWS,TABF50  BRANCH IF NOT IN TC MODE
         LR    TEMP,R1             START OF FILL STRING
         AR    TEMP,COUNT          POINT TO LAST CHAR
         MVI   0(TEMP),C' '        BLANK IT
TABF50   DS    0H
         EX    COUNT,TBFMVE3       SET FILL STRING
         MVI   LINKMULT,X'01'      CHANGE MULTIPLIER BACK TO 1
         L     TEMP,BUFF3AD        FETCH ADDRESS OF BUFFER
         LR    X1,R0               RESTORE PTR TO CHAR AFTER TAB
         LR    R1,R14              RESTORE FOPMLIST PTR
         USING FOPMLIST,R1         SET UP BASE
         L     R0,FOLL             CURRENT LINE LENGTH
         AH    R0,FOINDR           PLUS RIGHT INDENT LINE VALUE
         CH    R0,TBVALUE          TAB EQUAL TO LINE LENGTH .Q
         BNE   TABF55              BR IF NO
         USING LNEWELEM,X1         SET UP BASE
         CLI   LNEWCHAR,C' '       NEXT CHAR A BLANK .Q
         BE    TABF55              BR IF YES
         LA    COUNT,1(,COUNT)     BUMP LENGTH BY 1 DUE TO BCTR
TABF55   DS    0H
         ST    COUNT,0(TEMP)       UPDATE LENGTH OF FIELD
         LA    TEMP,2(,TEMP)       POINT TO BUFFER START
         LR    R0,TEMP             POINT AT STRING TO INSERT
        $CALL  FOADD               ADD THE FILL STRING
         L     POLD,WRKFLN         ADDRESS OF FILL(1) LINK
         L     R15,WRKLLN          ADDRESS OF FILL(N) LINK
         SPACE 1
         MVC   LINKFOR-LINKELEM(4,R15),LINKFOR    FILL(N) -> B
         ST    POLD,LINKFOR        A -> FILL(1)
         SPACE
         USING LOLDELEM,POLD       LOOK AT FILL(1) LINK
         ST    P,LOLDBACK          A <- FILL(1)
         LR    P,POLD              FILL(1) NOW END OF LINE
         SPACE
         L     POLD,LINKFOR-LINKELEM(R15)    ADDR OF B LINK
         LTR   POLD,POLD           ANY CHARS FOLLOWING TAB .Q
         BZ    TABF60              BRANCH IF NONE
         ST    R15,LOLDBACK        FILL(N) <- B
         DROP  POLD                BACK TO EXPECTED USAGE
         SPACE 1
TABF60   DS    0H
        $EXIT  KEEP=(COUNT),RC=0
TBFMVE1  MVC   0(*-*,X1),TBFILL
TBFMVE2  MVC   0(*-*,X2),0(X1)
TBFMVE3  MVC   0(*-*,X1),0(R1)
         DROP  P,TABS,X1,R1
