***********************************************************************
*                                                                     *
*              DHCLR (DEFINE HEADER INITIALIZATION)                   *
*                                                                     *
***********************************************************************
         SPACE
DHCLR   $ENTER ,
         L     X1,DHNUMTC          GET TABLE OF CONTENTS NUMBER
         SLL   X1,2                TIMES 4
         L     TEMP,DHANCH(X1)     OBTAIN CORRRECT ANCHOR POINTER
         LTR   TEMP,TEMP           HAS AREA BEEN ASSIGNED .Q
         BNZ   DHCDONE             BRANCH IF YES
         LA    R1,DHMAX*DHDLEN     LENGTH OF HEAD LEVEL TABLE
        $CALL  GETCELL             OBTAIN CORE
         BC    BNEG,DHC010         BRANCH IF NOT AVAILABLE
         USING DHDSECT,TEMP        INFORM ASSEMBLER
         MVC   DHDSECT(DHDLEN*DHMAX),DHDEFALT  SET DEFAULTS
         ST    TEMP,DHANCH(X1)     SAVE ADDRESS OF TABLE
DHCDONE  DS    0H
        $EXIT  RC=0                RETURN TO CALLER
         DROP  TEMP
         SPACE
DHC010   DS    0H
         MVI   EFLAG024,ERRSET     WORKING STORAGE EXHAUSTED
         B     DHCDONE             ALL DONE
         SPACE
DHDEFALT DS    0H
DHLEV0   DC    AL1(0,0,0,1)        SKBF,SPBF,TCIN,TCOF
         DC    AL1(DHTO+DHTC,0)
DHLEV1   DC    AL1(0,5,0,1)
         DC    AL1(DHTC+DHTS+DHUS+DHUP+DHOJ+DHPA+DHBR,0)
DHLEV2   DC    AL1(3,2,0,1)
         DC    AL1(DHTC+DHUS+DHUP+DHBR,0)
DHLEV3   DC    AL1(3,2,2,1)
         DC    AL1(DHTC+DHUP+DHBR,0)
DHLEV4   DC    AL1(3,2,4,1)
         DC    AL1(DHUS+DHBR,0)
DHLEV5   DC    AL1(1,0,6,1)
         DC    AL1(DHUS+DHUP,0)
DHLEV6   DC    AL1(1,0,8,1)
         DC    AL1(DHUS,0)
DHLEV7   DC    AL1(0,0,0,0)
         DC    AL1(0,0)
DHLEV8   DC    AL1(0,0,0,0)
         DC    AL1(0,0)
DHLEV9   DC    AL1(0,0,0,0)
         DC    AL1(0,0)
