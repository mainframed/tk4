***********************************************************************
*                                                                     *
*              'PUWORK'  --  WORKFILE SUBROUTINE                      *
*                                                                     *
***********************************************************************
         SPACE
*        ON ENTRY  --  (R1) = FILE NUMBER
*                      (R0) = ZERO MEANS CLOSE REQUEST
*                           > ZERO MEANS RDW POINTER
         SPACE
PUWORK  $ENTER ,
         LR    X1,R1               GET FILE NUMBER
         CVD   X1,DECM             SAVE FILE NUMBER AGAIN
         SLL   X1,2                FILE NUMBER TIMES FOUR FOR OFFSET
         A     X1,PUIOCBPT         IOCB ADDRESS POINTER
         L     TEMP,0(,X1)         IOCB ADDRESS
         SPACE
         LTR   X3,R0               IS THIS A CLOSE REQUEST .Q
         BZ    PUWK03              BRANCH IF YES
         LTR   TEMP,TEMP           IOCB PRESENT FOR PUT .Q
         BNZ   PUWK02              BRANCH IF YES
         SPACE
         LA    R1,PUIODEFL         WORK LENGTH REQUIRED FOR IDOEF
        $CALL  GETCELL             GET AN IODEF WORK AREA
         BC    BNEG,PUWKE024       FORGET IT IF NO SPACE
         USING IOCIODEF,TEMP       IODEF LOOKS LIKE THIS
         MVC   IOCIODEF(PUIODEFL),PUIODEF  ENTER A FRESH IODEF
         MVC   IOCDDNAM(8),PUDDNAME     SET BASIC DDNAME
         OI    DECM+7,X'0F'        FIX THE FILE NUMBER SIGN
         UNPK  IOCDDNAM+6(2),DECM+6(2)  ENTER THE DDNAME SUFFIX
         SPACE
         MVI   0(X1),X'80'         MARK FILE TRIED BUT UNSUCCESSFUL
         LA    R1,IOCDDNAM         POINT TO DDNAME
        $CALL  SYTEST              CAN THE OPEN SUCCEED .Q
         BC    BNEG,PUWKE058       BRANCH IF NOT
         SPACE
         LA    R1,IOCIODEF         POINT TO THE IODEF
        $CALL  SYPUTOP             OPEN OUTPUT FILE
         BC    BNEG,PUWKE058       BRANCH IF OPEN FAILED
         ST    R1,0(,X1)           SAVE IOCB ADDRESS
PUWK01   DS    0H
         LA    R0,IOCIODEF         PATTERN IODEF
         LA    R1,PUIODEFL         LENGTH OF SAME
        $CALL  FREECELL            RELEASE WORK AREA
         DROP  TEMP
         SPACE
PUWK02   DS    0H
         L     TEMP,0(,X1)         IOCB ADDRESS
         LA    TEMP,0(,TEMP)       CLEAR HIGH BYTE
         LTR   TEMP,TEMP           ANY FILE PRESENT .Q
         BZ    PUWK05              BRANCH IF NOT
         SPACE
         USING IOCB,TEMP           IO CONTROL BLOCK DESCRIPTOR
         LA    R1,IOCB             IO CONTROL BLOCK
         LR    R0,X3               OIUBT TI DATA
        $CALL  SYPUT               OUTPUT THE RECORD
         B     PUWK05              GO TO RETURN
         SPACE
PUWK03   DS    0H
         LA    TEMP,0(,TEMP)       CLEAR HIGH BYTE
         LTR   TEMP,TEMP           IOCB PRESENT .Q
         BZ    PUWK04              BRANCH IF NOT
         LA    R1,IOCB             IOCB ADDRESS
        $CALL  SYPUTCL             CLOSE OUTPUT FILE
PUWK04   DS    0H
         XC    0(4,X1),0(X1)       CLEAR IOCB POINTER
         DROP  TEMP
PUWK05   DS    0H
        $EXIT  RC=0                RETURN TO CALLER
         SPACE
PUWKE024 DS    0H
         MVI   EFLAG024,ERRSET     NO CORE FOR IODEF
         B     PUWK05              GO TO RETURN
PUWKE058 DS    0H
         MVI   EFLAG058,ERRSET     CANNOT OPEN WORK FILE
         B     PUWK01              FREE WORK BLOCK
         SPACE
PUIODEF  SYSIODEF FILETYP=IOC$QSAM,DDNAME=**,                          *
               RECFM=IOCRECV,RECMERG=IOCRECF+IOCRECV+IOCRECBR,         *
               LRECL=136,MAXLREC=240
PUIODEFL EQU   *-PUIODEF           LENGTH!OF IODEF
