***********************************************************************
*                                                                     *
*              INTERNAL FUNCTION 'PRNUM' --- ADD NUMBER/FILE          *
*                                                                     *
***********************************************************************
         SPACE
*        ON ENTRY  --  (R0)  PTR TO RDW TO INCREASE LENGTH
*                      (R1)  OFFSET IN DATA FOR RESULT
         SPACE
PRNUM   $ENTER ,
        $IFON  SWITCHS,FOVLY,PRNUM03    BRANCH IF OVERLAY LINE
        $CALL  PRTARG              GET TARGET FOR DATA
         CLC   0(28,R1),BLANKS     A CLEAN WORK AREA .Q
         BNE   PRNUM03             NO, FORGET NUM FOR THIS LINE
         SPACE
         MVC   0(8,R1),COMFILNO    FILE RECORD NUMBER
         LA    R1,9(,R1)           FILE NAME GOES HERE
         CLI   COMMEMNM,C' '       BLANK MEMBER NAME .Q
         BNE   PRNUM01             BRANCH IF NOT
         CLC   COMFILNM,PRNUMIN    INSIGNIFICANT FILE NAME .Q
         BE    PRNUM02             BRANCH IF YES
PRNUM01  DS    0H
         MVC   0(8,R1),COMFILNM    ADD CURRENT FILE NAME
         BAL   RETURN,PRNUMSK      SKIP TO 1ST BLANK
         CLI   COMMEMNM,C' '       IS MEMBER NAME BLANK .Q
         BE    PRNUM02             YES, SET NEW LENGTH
         MVI   0(R1),C'('          INITIAL PAREN
         MVC   1(8,R1),COMMEMNM    ADD MEMBER NAME
         BAL   RETURN,PRNUMSK      SKIP TO 1ST BLANK AGAIN
         MVI   0(R1),C')'          CLOSING PAREN
         LA    R1,1(,R1)           FIRST BLANK AFTER
PRNUM02  DS    0H
         LTR   X1,R0               THIS IS THE RECORD
         BZ    PRNUM03             BRANCH IF NO RECORD
         SR    R1,X1               LENGTH WHERE WE LEFT OFF
         SR    TEMP,TEMP           ZERO WORK REGISTER
         IC    TEMP,1(,X1)         CURRENT RECORD LENGTH
         CR    R1,TEMP             IS THIS NOW LONGER .Q
         BNH   PRNUM03             BRANCH IF NOT
         STC   R1,1(,X1)           SET LONGER LENGTH
PRNUM03  DS    0H
        $EXIT  RC=0                AND RETURN TO CALLER
         SPACE
PRNUMSK  DS    0H
         LA    R1,1(,R1)           BUMP OUTPUT POINTER
         CLI   0(R1),C' '          IS IT BLANK .Q
         BNE   PRNUMSK             BUMP ALONG IF NOT
         BR    RETURN              GO BACK TO CALLER
         SPACE
PRNUMIN  DC    CL8'SYSIN'          INSIGNIFICANT FILE NAME
