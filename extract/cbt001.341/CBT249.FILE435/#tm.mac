***********************************************************************
*                                                                     *
*              .TOP MARGIN (.TM) --- CONTROL                          *
*                                                                     *
***********************************************************************
         SPACE
TM      $ENTER ,
        $CALL  PRINTNB             CLEAR PRINT BUFFER
        $CALL  GETNUM              GET NUMBER
         BC    BDNO,TM03           ERROR IF INVALID
         BC    BOMIT,TM01          ENTER DEFAULT IF INVALID
         CLI   NUMSIGN,C' '        WAS ARGUMENT SIGNED .Q
         BE    TM02                BRANCH IF NOT
         AH    COUNT,TOPMRG        ALTER TOP MARGIN VALUE
         B     TM02                GO CHECK AND ENTER
TM01     DS    0H
         L     COUNT,DEFTM+TAB3DEF DEFAULT TOP MARGIN VALUE
TM02     DS    0H
         LTR   COUNT,COUNT         IS TOP MARGIN NEGATIVE .Q
         BM    TM05                ERROR IF YES
         LH    TEMP,HDMRG          HEADING MARGIN VALUE
         AH    TEMP,HSCNT          PLUS HEADING SPACING
         CR    COUNT,TEMP          COMPARE VALUES
         BL    TM06                ERROR IF LESS
         LH    TEMP,PLZ            GET CURRENT PAGE LENGTH
         SH    TEMP,BMMRG          LESS BOTTOM MARGIN
         CR    COUNT,TEMP          TOO BIG .Q
         BNL   TM04                ERROR IF TOO BIG
         STH   COUNT,TOPMRG        SET TOP MARGIN SIZE
         B     TM07                BRANCH TO RETURN
         SPACE
TM03     DS    0H
         MVI   EFLAG005,ERRSET     INVALID OPERAND
         B     TM07                BRANCH TO RETURN
TM04     DS    0H
         MVI   EFLAG045,ERRSET     OPERAND TOO BIG
         B     TM07                BRANCH TO RETURN
TM05     DS    0H
         MVI   EFLAG014,ERRSET     OPERAND TOO SMALL
         B     TM07                BRANCH TO RETURN
TM06     DS    0H
         MVI   EFLAG041,ERRSET     OPERAND TOO BIG
         B     TM07                BRANCH TO RETURN
         SPACE
TM07     DS    0H
        $EXIT  RC=0                RETURN TO MAIN CONTROLLER
