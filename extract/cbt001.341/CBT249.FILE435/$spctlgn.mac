         MACRO
        $SPCTLGN   &DOC=NOGEN
         GBLA  &G,&GFR(255)
         GBLC  &GOP(255),&GAD(255)
         LCLA  &I,&J,&W
         LCLC  &WC
         ACTR  25000
         PUSH  PRINT
         PRINT &DOC
         MNOTE *,'$SPCTLGN -- NUMBER OF CONTROL WORDS IS &G..'
.*                 BUBBLE SORT OP AND AD BY FREQUENCY
.LOOP1   ANOP
&I       SETA  &I+1
&J       SETA  &I
.LOOP2   ANOP
&J       SETA  &J+1
         AIF   (&GFR(&I) GE &GFR(&J)).LOOP3
&W       SETA  &GFR(&I)
&GFR(&I) SETA  &GFR(&J)
&GFR(&J) SETA  &W
&WC      SETC  '&GOP(&I)'
&GOP(&I) SETC  '&GOP(&J)'
&GOP(&J) SETC  '&WC'
&WC      SETC  '&GAD(&I)'
&GAD(&I) SETC  '&GAD(&J)'
&GAD(&J) SETC  '&WC'
.LOOP3   AIF   (&J LT &G).LOOP2
         AIF   (&I LT &G-1).LOOP1
.*                 OUTPUT THE RESULTS
&I       SETA  0
.OUTLP1  ANOP
&I       SETA  &I+1
         AIF   (&I NE &G).OUTLP2
CTLTABN  EQU   *
         ENTRY CTLTABN             END OF SCRIPT CONTROL WORD TABLE
.OUTLP2  ANOP
&GOP(&I).#### DC CL2'&GOP(&I)',CL2'&GOP(&I)',AL2(V&GAD(&I).-SCRICVT,0)
         AIF   (&I LT &G).OUTLP1
         POP PRINT
         MEND
