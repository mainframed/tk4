***********************************************************************
*                                                                     *
*              .LN (IMMEDIATE LINE) --- CONTROL                       *
*                                                                     *
***********************************************************************
         SPACE
LN      $ENTER ,
         CLI   ICORFLG,ICORNONE    SAVING FORMATTED TEXT INCORE .Q
         BNE   LNE012              ERROR IF YES
        $CALL  PRINTNB             CLEAR OUT PRINT BUFFER
        $CALL  GETNUM              GET NUMBER OF TARGET LINE
         BC    BOMIT,LNE003        ERROR IF ARG OMITTED
         BC    BDNO,LNE005         ERROR IF INVALID
         SPACE
        $IFOFF SWITCHS,FEMPTY,LN01 BRANCH IF PAGE NOT EMPTY
        $CALL  PAGETOP             START THIS PAGE GOING
LN01     DS    0H
         SR    X1,X1               ZERO TARGET LINE
         CLI   NUMSIGN,C' '        OPERAND UNSIGNED .Q
         BE    LN02                BRANCH IF YES
         LH    X1,PLZ              PAGE LENGTH LESS REMAINDER
         SH    X1,PLCT             IS LINES ALREADY PRINTED
         SH    X1,TOPMRG           RELATIVE OFFSET FROM TOP MARGIN
         A     X1,F1               PLUS CURRENT LINE
LN02     DS    0H
         AR    X1,COUNT            TARGET LINE NUMBER AFTER TOP MARGIN
         S     X1,F1               LESS CURRENT LINE
         BM    LNE014              ERROR IF TOO SMALL
         LH    R0,PLZ              PAGE LENGTH
         SH    R0,TOPMRG           LESS TOP MARGIN
         SH    R0,BMMRG            LESS BOTTOM MARGIN
         CR    X1,R0               TOO BIG .Q
         BNL   LNE013              ERROR IF YES
         SPACE
         AH    X1,TOPMRG           ABSOLUTE LINE NUMBER
         SH    X1,PLZ              LINE NUMBER LESS PAGE LENGTH
         LPR   X1,X1               IS LINES TO BE LEFT AFTER
         SPACE
LN03     DS    0H
         CH    X1,PLCT             FURTHER DOWN PAGE TO GO .Q
         BNH   LN04                BRANCH IF YES
        $CALL  PAGE                FINISH CURRENT PAGE
        $IFOFF SWITCHS,FEMPTY,LN03 BRANCH IF PAGE STARTED
        $CALL  PAGETOP             AND START THE NEXT
         B     LN03                ALLOW FOR FLOATING KEEPS
         SPACE
LN04     DS    0H
         LH    COUNT,PLCT          LINES LEFT ON THIS PAGE
         SR    COUNT,X1            LESS WHERE WE WANT TO BE
        $CALL  SPACER              SPACE DOWN, IGNORING REMOTES
        $CALL  RMNEXT              SET UP FOR NEXT REMOTE
LN05     DS    0H
        $EXIT  RC=0                RETURN TO CALLER
         SPACE
LNE003   DS    0H
         MVI   EFLAG003,ERRSET     OMITTED OPERAND
         B     LN05                GO TO RETURN
LNE005   DS    0H
         MVI   EFLAG005,ERRSET     INVALID OPERAND
         B     LN05                GO TO RETURN
LNE012   DS    0H
         MVI   EFLAG012,ERRSET     CONTROL WORD ILLEGAL HERE
         B     LN05                GO TO RETURN
LNE013   DS    0H
         MVI   EFLAG013,ERRSET     OPERAND TOO BIG
         B     LN05                GO TO RETURN
LNE014   DS    0H
         MVI   EFLAG014,ERRSET     OPERAND TOO SMALL
         B     LN05                GO TO RETURN
