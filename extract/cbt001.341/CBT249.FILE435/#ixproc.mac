***********************************************************************
*                                                                     *
*              INTERNAL FUNCTION 'IXPROC' -- PROCESS INDEX            *
*                                                                     *
***********************************************************************
         SPACE
*        TO ENTER --  LA    R1,INDEX ANCHOR POINTER
*                    $CALL  IXPROC
*
*        WHERE -- IXPTRS  IS A VECTOR OF
*                       AL1(LEN-1),AL3(PTR TO STRING)
*              -- IXPARM  IS THE REFERENCE LEN-1/ADDR
*              -- ZERO PTR MEANS POSITION HOLDER ONLY
         SPACE
IXPROC  $ENTER ,
         LR    X3,R1               RETAIN HEADER BLK CHAIN START
         L     R15,BUFF3AD         POINT TO UPPER CASE WORK AREA
         LA    R1,IXPTRS           POINT TO PARM LIST
         ST    R1,IXPCURR          SAVE IT IN WORK
         SPACE
IX010    DS    0H
         L     R1,IXPCURR          POINT TO CURRENT PARM WORD
         L     X1,0(,R1)           LOAD CURRENT PARM
         SR    COUNT,COUNT         ZERO LENGTH-1 REGISTER
         IC    COUNT,0(,R1)        GET LENGTH-1 OF PARM
         LA    R1,L'IXPTRS(,R1)    BUMP PARMLIST CURSOR
         ST    R1,IXPCURR          SAVE UPDATED POINTER
         LA    X1,0(,X1)           CLEAR LENGTH-1
         LTR   X1,X1               MORE INDEX LEVELS .Q
         BZ    IX110               BRANCH IF NOT
         EX    COUNT,IXHMVC1       SHIFT PARM STRING TO SCRATCH
         EX    COUNT,IXHOC         AND CONVERT TO UPPER CASE
         SR    TEMP,TEMP           FOR WORKING WITH LENGTH-1
IX020    DS    0H
         ST    X3,IXHPREV          SAVE PREVIOUS HEADER LINK FIELD ADDR
IX030    DS    0H
         L     X3,0(,X3)           NEXT HEADER (IXHBNEXT/IXHBLOWR)
         LTR   X3,X3               MORE TO GO .Q
         BZ    IX060               NO, CHAIN NEW BLK ON END
         USING IXHBLK,X3           TELL THE ASSEMBLER
         IC    TEMP,IXHBTXTL       LENGTH-1 OF TEXT IN BLOCK
         LR    R0,TEMP             REMEMBER EXISTING TEXT LENGTH-1
         CR    COUNT,TEMP          COMPARE NEW TO EXISTING TEXT LEN-1
         BE    IX050               IF SAME, DIRECT MATCH POSSIBLE
         BH    IX040               IF LONGER, USE SHORTER FOR MATCH
         LR    TEMP,COUNT          ELSE SET TO TEST SHORTER
IX040    DS    0H
         EX    TEMP,IXHCLC         TEST FOR COLLATING SEQUENCE
         BH    IX020               LOOP IF NOT WANTED HERE
         BL    IX060               BRANCH IF INSERTION POINT
         CR    COUNT,R0            IS NEW LONGER .Q
         BH    IX020               YES, WAIT FOR NEXT CHANCE
         B     IX060               ELSE INSERT NEW HERE
IX050    DS    0H
         EX    TEMP,IXHCLC         TEST FOR COLLATING SEQUENCE
         BH    IX020               LOOP IF NOT WANTED HERE
         BE    IX070               ENTRY ALREADY EXISTS, SKIP
IX060    DS    0H
         LA    R1,IXHBLKL+2(COUNT,COUNT)     LENGTH OF INDEX HEADER BLK
        $CALL  GETCELL             GET SOME SPACE
         L     R15,BUFF3AD         RESTORE POINTER TO UPPER CASE WORK
         BC    BNEG,IX200          BRANCH IF FAILED
         XC    0(IXHBLKL,TEMP),0(TEMP)  CLEAR NEW BLOCK PREFIX
         ST    X3,IXHBNEXT-IXHBLK(,TEMP)     SET NEXT HEADER BLK ADDR
         L     X3,IXHPREV          PTR PREVIOUS LINK FIELD LOCATION
         ST    TEMP,0(,X3)         COMPLETE THE ENQ (IXHBNEXT/IXHBLOWR)
         LR    X3,TEMP             SET WORKING BASE FOR NEW IXHBLK
         STC   COUNT,IXHBTXTL      SAVE TEXT LENGTH-1
         EX    COUNT,IXHMVC2       SHIFT UPPER CASE TEXT TO BLOCK
         L     R1,IXPCURR          PTR CURRENT PARM ENTRY ADDR
         L     R1,0(,R1)           PTR TEXT STRING FOR THIS PARM
         LA    X2,IXHBTXTU+1(COUNT)     PTR END OF UPPER CASE STRING
         EX    COUNT,IXHMVC3       SHIFT ACTUAL TEXT TO BLK ALSO
IX070    DS    0H
         LA    X3,IXHBLOWR         PTR LINK FIELD TO DROP TO NEXT LEVEL
         B     IX010               PROCESS OTHER LEVELS
         SPACE
IXHMVC1  MVC   4(*-*,R15),0(X1)    TO SHIFT HEADER TEXT TO SCRATCH
IXHOC    OC    4(*-*,R15),BLANKS   TO CONVERT HEADER TO UPPER CASE
IXHCLC   CLC   4(*-*,R15),IXHBTXTU-IXHBLK(X3)     TEST HEADER COLLATION
IXHMVC2  MVC   IXHBTXTU-IXHBLK(*-*,X3),4(R15)     ADD UPPER CASE TEXT
         SPACE
****************  PROCESS INDEX REFERENCE ENTRY ***********************
IX110    DS    0H
         LA    R0,IXHBLOWR-IXHBLK  WE'RE OFFSET THIS FAR
         SR    X3,R0               BACKUP TO BLOCK START
         SR    COUNT,COUNT         LOAD ENTRY REFERENCE STRING
         IC    COUNT,IXPARM             LENGTH-1
         L     X2,IXPARM           POINTER TO REFERENCE ENTRY
         LA    X2,0(,X2)           CLEAR HIGH BYTE
         LTR   X2,X2               ANY REFERENCE ENTRY .Q
         BZ    IX210               BRANCH IF NOT
         LA    X1,IXHBENTA         PTR LINK FIELD IN CURRENT HEADER BLK
         L     X3,IXHBENTA         PTR FIRST ENTRY BLOCK (IF ANY YET)
         LTR   X3,X3               HAVE ONE .Q
         BZ    IX130               NO, SKIP TO GET IT
         USING IXEBLK,X3
IX120    DS    0H
         LR    X1,X3               SAVE CURRENT ENTRY BLK ADDRESS
         L     X3,IXEBNEXT         PTR TO NEXT ENTRY BLOCK
         LTR   X3,X3               CHAIN END YET .Q
         BNZ   IX120               NO, CONTINUE SEARCH
         DROP  X3
         USING IXEBLK,X1
         SR    R1,R1               PREPARE LENGTH REG
         IC    R1,IXEBTXTL         LENGTH OF TEST ALREADY IN BLK
         S     R1,F3               BACK OVER ', ' AND LENGTH-1
         SR    R1,COUNT            LENGTH SANS PREVIOUS ENTRY
         LA    TEMP,IXEBTXT(R1)    PTR TO PREVIOUS ENTRY
         L     X2,IXPARM           PTR TO ENTRY STRING TEXT
         EX    COUNT,IXECLC        IS NEW ENTRY ALREADY PRESENT .Q
         BE    IX210               YES, NO ADD NEEDED
         LA    R1,3(COUNT,R1)      RESTORE CURRENT TEXT LENGTH
         LA    TEMP,IXEBTXTA-2-1   AVAILABLE TEXT LENGTH PER BLK
         SR    TEMP,R1             SPACE LEFT IN BLK
         CR    COUNT,TEMP          TEST FREE SPACE
         BNH   IX140               SKIP IF ENTRY CAN FIT WITH ', '
IX130    DS    0H
         LA    R1,IXEBLKL          LENGTH OF ENTRY BLOCK
        $CALL  GETCELL             GET SOME SPACE
         BC    BNEG,IX200          BRANCH IF ERROR
         ST    TEMP,0(,X1)         SET TO PREV LINK (IXHBENTA/IXEBNEXT)
         LR    X1,TEMP             SET WORKING BASE
         XC    IXEBLK(IXEBLKL),IXEBLK   CLEAR ENTIRE AREA
         SR    R1,R1               CURRENT TEXT LENGTH IN BLK IS ZERO
IX140    DS    0H
         L     TEMP,IXPARM         PTR TO ENTRY TEXT STRING
         LA    X2,IXEBTXT(R1)      PTR TO FREE LOC IN BLOCK
         EX    COUNT,IXEMVC        SHIFT ENTRY TEXT TO BLOCK
         LA    R1,2+1(COUNT,R1)    TOTAL TEXT LEN IN BLK (WITH ', ')
         STC   R1,IXEBTXTL         SAVE LENGTH
         LA    TEMP,IXEBTXT-2(R1)  PTR TO ENTRY STRING END
         MVI   0(TEMP),C','        ADD A COMMA
         MVI   1(TEMP),C' '             AND A BLANK
         B     IX210               GO TO RETURN
         DROP  X1
         SPACE
**************** RETURN ***********************************************
IX200    DS    0H
         MVI   EFLAG024,ERRSET     THERE'S NO CORE
IX210    DS    0H
        $EXIT  RC=0                AND RETURN TO CALLER
         SPACE
IXHMVC3  MVC   0(*-*,X2),0(X1)     SHIFT ACTUAL HEADER TO BLOCK
IXECLC   CLC   0(*-*,TEMP),0(X2)   TEST PREVIOUS ENTRY FOR MATCH
IXEMVC   MVC   0(*-*,X2),0(TEMP)   SHIFT ENTRY TEXT TO BLOCK
         SPACE
IXPCURR  DC    A(*-*)              PTR CURRENT HEADER PARM WORD
IXHPREV  DC    A(*-*)              PTR PREVIOUS HEADER BLK LINK FIELD
