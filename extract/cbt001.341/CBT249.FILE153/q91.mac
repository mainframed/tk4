SCREEN QSTART 'QUEUE COMMAND - JQE AND JOE SEARCH AND FORMAT - Q20' RGR
         GBLB  &QSP           MVS/SP OPTION                       UF020
         USING QDISPLAY,R15   BASE REG FOR DISPLAY WORK AREA
         L     R15,QVDSPL     LOAD BASE REG
*********************************************************************
*
*        NUMBER OF LINES PER SCREEN
*
*********************************************************************
         LH    R5,QLNG1
         CH    R5,=H'1'
         BL    LNECHAR
         CH    R5,=H'2'
         BH    TILT1
         BCTR  R5,0
         MVZ   QFZONES,QFZONE
ZON1     MVZ   QFZONES(1),QPARM1
         EX    R5,ZON1
         CLC   QFZONES,QFZONE
         BNE   TILT2
PACK1    PACK  QDWORK,QPARM1(1)
         EX    R5,PACK1
         CP    QDWORK,=PL2'21'      MAX LINES FOR 3278.
         BH    TILT3                TO LONG
         CP    QDWORK,=PL1'1'       AT LEAST 1 LINE.
         BL    LNECHAR              NO GO CKECK COLUMNS.
         ZAP   QDLNES,QDWORK        SAVE NUMBER OF LINES.
*********************************************************************
*
*        NUMBER OF CHAR PER LINE
*
*********************************************************************
LNECHAR  DS    0H
         LH    R7,QLNG2
         CH    R7,=H'1'
         BL    COMPUTE
         CH    R7,=H'2'
         BH    TILT1
         BCTR  R7,0
         MVZ   QFZONES,QFZONE
ZON2     MVZ   QFZONES(1),QPARM2
         EX    R7,ZON2
         CLC   QFZONES,QFZONE
         BNE   TILT2
PACK2    PACK  QDWORK,QPARM2(1)
         EX    R7,PACK2
         CVB   R7,QDWORK
         CH    R7,=H'80'      MAX CHAR PER LINE.
         BH    TILT4          TO LONG
         CH    R7,=H'1'
         BL    COMPUTE
         STH   R7,QDLNELEN    STORE IT.
*********************************************************************
*
*        COMPUTE NEW SCREEN SIZE.
*
*********************************************************************
COMPUTE  DS    0H
         ZAP   QDWORK,QDLNES
         CVB   R5,QDWORK
         MH    R5,QDLNELEN    MUTLIPLY BY LINE LENGTH.
         STH   R5,QDSCRLEN    STORE IT
         MVC   QDSCRPLN,=AL2(QDLINE1-QDSCREEN)
         AH    R5,QDSCRPLN
         STH   R5,QDSCRPLN
TILT0    QTILT '**** SCREEN SIZE UPDATED ****'
TILT1    QTILT '**** MAX LENGTH OF 2 CHAR ****'
TILT2    QTILT '**** MUST BE A NUMERIC ****'
TILT3    QTILT '**** SCREEN LENGTH TO LONG (21 LINES MAX) ****'
TILT4    QTILT '**** LINE LENGTH TO LONG ( 80 CHAR MAX) ****'
***********************************************************************
*                                                                     *
*   GO HOME                                                           *
*                                                                     *
***********************************************************************
STOP     QSTOP
         LTORG
         QCOMMON
         END
