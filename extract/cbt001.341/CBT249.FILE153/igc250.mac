SVC250   TITLE 'SVC 250 FOR MVS SUPERVISOR FUNCTIONS '
         MACRO
         DICVT &DSECT=
*
* COMMUNICATIONS VECTOR TABLE
*
         AIF   ('&DSECT' EQ 'NO').Z1
DICVT    DSECT CREATE A DSECT FOR ADDRESSIBILITY
         AGO   .Z3
.Z1      ANOP
DICVT    DS    0D                  CVT ORIGIN MVS/3
         AIF   ('&DSECT' EQ 'YES').Z3
DIDFLAGS DC    XL4'00'             DISASTER/CRITICAL FLAGS
POINTR1  DC    F'0'                POINTER NO 1 FOR DICVT
POINTR2  DC    F'0'                POINTER NO 2 FOR DICVT
POINTR3  DC    F'0'                POINTER NO 3 FOR DICVT
POINTR4  DC    F'0'                POINTER NO 4 FOR DICVT
POINTR5  DC    F'0'                POINTER NO 5 FOR DICVT
         AGO   .Z4
.Z3      ANOP
DIDFLAGS DS    XL4                 DISASTER/CRITICAL FLAGS
POINTR1  DS    F'0'                POINTER NO 1 FOR DICVT
POINTR2  DS    F'0'                POINTER NO 2 FOR DICVT
POINTR3  DS    F'0'                POINTER NO 3 FOR DICVT
POINTR4  DS    F'0'                POINTER NO 4 FOR DICVT
POINTR5  DS    F'0'                POINTER NO 5 FOR DICVT
.Z4      ANOP
         MEND
IGC250   CSECT
*
*        UPDATES - 06-31-82                                         RGR
*           BIT THAT WAS BEING SET IN THE "TCBID" FIELD TO INDICATE RGR
*           THAT THE USER WAS REALLY STILL AUTHORIZED WAS CAUSING   RGR
*           ABENDS IN MVS SP1.3.0.  THE BIT WAS MOVED TO "TCBUSER". RGR
*                                                                   RGR
*                                                                   RGR
*        NOTE :: SVC ISSUES ABEND MACRO; THEREFORE IS TYPE 2.
*
*        THIS SVC IS FOR MULTIPLE USES. INPUT IS IN R0,R1,AND R15.
*              INPUT REGISTER 0 HAS A ONE BYTE FUNCTION CODE.
*               FUNCTION CODE AND SERVICE
*                         00     PAGE FIX R1 TO R15
*                         01     PAGE FREE R1 TO R15
*                         02     AUTH USER JSCB
*                         03     UN AUTH USER JSCB
*                         04     SET UP DICVT IN SYSTEM CVT
*                         05     TURN AUTH ON AND ISSUE DONTSWAP
*                         06     TURN AUTH OFF AND ISSUE OKSWAP
*
*
*        TO FIX PAGES, R0 MUST HAVE EBCDIC INQ IN OTHER THREE BYTES.
*              IF NOT THE USER IS ABENDED. PAGE FREE MUST ALSO HAVE THE
*              SAME PASSWORD. IN THE PAGE FREE, THE PAGES WILL NOT
*              BE PAGED BACK TO EPS. WHILE FIXING/FREEING PAGES THE
*              CALLER WILL BE TEMP AUTH FOR THE FUNCTION.
*
*
*
*        THE AUTHORIZATION PART OF THIS SVC REQUIRES THE THREE
*              LOW BYTES OF R0 TO HAVE EBCDIC SMF. IF NOT THE CALLER
*              WILL BE ABENDED. THE JSCB AUTH BIT X 01 WILL BE TURNED
*              ON. IF THE PGM IS AUTH ON ENTRY, AND THE SVC IS TOLD TO
*              UNAUTH A CALLER, AUTH IS KEPT, BY SETING FLAG IN TCB
*
*
*              SMF2 ISSUES THE ENTRY TO UPDATE DICVT IN THE SYSTEM
*              CVT. THIS ALLOWS US TO ENTER AS MANY ENTRIES AS
*                   WE WANT INTO THE SYSTEM AND NOT EXCEED ONE
*                   FULLWORD. SVC IS LINKEDIT INTO NUC..
*
*
*
*        REGISTERS ON ENTRY ARE AS FOLLOWS.........
*                       REGISTER        INPUT
*
*                          R6           ENTRY POINT
*                          R0           FUNCTION CODE AND PASSWORD
*                          R1           DATA AREA TO FIX BYTE 1
*                          R15          DATA AREA TO FIX END BYTE
*                          R4           TCB ADDRESS OF CALLER
*                          R7           ASCB ADDRESS OF ASID
*                          R5           LOADED WITH JSCB
*                          R3           ADDRESS OF CVT
*
*
*
*                ********    CAUTION       **********
*
*
*                  ANY FUNCTION BYTE GT 6 WILL ABEND USER
*                               ABEND CODE 999
*
*
*
*                         NORMAL ABEND CODE IS 555.
*
*
*
         EJECT
         ENTRY DICVT              USED FOR CVTUSER FIELD         77075
         USING *,R6           ESTABLISH ADDRESSIBILITY
         CLM   R0,8,HIFUNC    IS FUNCTION GT HIGHEST ALLOWED
         BH    ABEND3         YES ABEND USER
         LR    R2,R0          LOAD R2 WITH FUNC
         SRL   R2,24          SHIFT RIGHT TO LOW BYTE
         SLL   R2,2           AND MULTIPLY BY FOUR **ERR MAY 20,1976
         B     BRTAB(R2)      BRANCH TO FUNCTION
BRTAB    B     PAGEFIX        FIX PAGES....
         B     PAGEFREE       FREE PAGES ...
         B     AUTHON         TURN AUTH ON FOR TCB
         B     AUTHOFF        TURN AUTH OFF FOR TCB
         B     DICVTPUT       SETUP DICVT IN SYS CVT USER FIELD
         B     DONTSWAP       MAKE ASID NONSWAPPABLE FOR FUNCTION
         B     OKSWAP         MAKE ASID SWAPPABLE
         EJECT
*
*
*        ENTRY HERE IS TO FIX PAGES IN MEMORY.
*
*              TEMP AUTH IS NECESSARY TO EXEC THIS FUNCTION AND
*              A BAL R14 IS USED TO ACCOMPLISH FUNCTION
*
         SPACE 2
PAGEFIX  CLM   R0,7,INQ            IS USER CALLING INQ....
         BNE   ABEND               NO.  ABEND THE TASK CALLING US
         LR    R9,R1              SAVE INPUT REGISTER 1
         LA    R0,WORKAREA        SET LENGTH TO DSECT
         ICM   R0,8,SUBPOOL       SET CORRECT SUBPOOL
         GETMAIN   R,LV=(0)       GET STORAGE FOR SAVE AREAS
         USING WORK,R10           SET ADDRESS IBILITY
         LR    R10,R1             LOAD R 10 WITH THE UPDATE
         XC    ECB,ECB             CLEAR ECB FOR ALL CASES
         ST    R14,S14        SAVE R14 FOR BAL
         LA    R11,ECB       LOAD R11 WITH ECB ADDR
         BAL   R14,AUTHONE    AUTH TCB FOR PAGEFIX/FREE
         SPACE
         PGFIX R,A=(R9),ECB=(R11),EA=(R15),LONG=Y    FIX PAGES...
         SPACE
         SLR   R0,R0               SUBTRACT FOR RETURN
         LTR   R15,R15             CHECK RETURN CODE
         BZ    ALLOK               PAGES FIXED LETS GO
         LA    R1,8                LOAD R1 WITH 8 FOR COMPARE
         CR    R15,R1              COMPARE FOR 8 RET CODE
         BE    WAIT                IF 8 WAIT FOR FIXING....
         B     ALLOK               RETURN IF NOT 8 LET USER DECIDE
WAIT     WAIT  ECB=ECB             WAIT FOR FIX TO END
         LA    R0,1                SET R0 TO 1 WAIT ISS...
         SR    R15,R15             CLEAR R15
         CLI   ECB+3,0             IS IT OKAY
         BE    ALLOK               YES GO THROUGH OKAY
         ICM   R15,1,ECB+3         SET ECB RETURN CODE IN R15
         SPACE
ALLOK    XC    ECB,ECB             CLEAR ECB
         BAL   R14,AUTHOFFE        UNAUTH BEFOR RETURN
         L     R14,S14        RELOAD R14 FOR RETURN
         LA    R0,WORKAREA        SET LENGTH FOR FREEMAIN
         ICM   R0,8,SUBPOOL       SET SUBPOOL
         LR    R1,R10             SET ADDRESS OF AREA
         FREEMAIN  R,LV=(0),A=(1) FREE AREA
         BR    R14                 RETURN TO CALLER
         EJECT
*
*
*        ENTRY HERE TO FREE PAGES DONE WITH A PAGEFIX.
*              NOTE.. PAGES ARE NOT PUT BACK ON EPS....
*
*        A PAGEFIX NEED NOT BE DONE BEFORE ISSUSING A
*              PAGEFREE SVC250.
*
*        TEMP AUTH ACCOMPLISHED FOR THIS FUNCTION BY A
*              BAL R14 TO SERVICE ROUTINES.
*
*
         SPACE 2
PAGEFREE EQU   *                   COME HERE TO FREE PAGES
         CLM   R0,7,INQ       IS IT INQ CALLING ...
         BNE   ABEND          NO ABEND CALLER
         LR    R9,R1              SAVE INPUT REGISTER 1
         LA    R0,WORKAREA        SET LENGTH TO DSECT
         ICM   R0,8,SUBPOOL       SET CORRECT SUBPOOL
         GETMAIN   R,LV=(0)       GET STORAGE FOR SAVE AREAS
         USING WORK,R10           SET ADDRESS IBILITY
         LR    R10,R1             LOAD R 10 WITH THE UPDATE
         ST    R14,S14        SAVE R 14 FOR BAL
         BAL   R14,AUTHONE    AUTH TCB FOR PAGEFIX/FREE
         SPACE
*        NOTE PAGES WILL NOT BE PAGED BACK TO EPS......
         SPACE
         PGFREE  R,A=(R9),EA=(R15)     RELEASE THE PAGES
         SPACE
         B     ALLOK               RETURN RO CALLER FREE DONE
         SPACE 5
ABEND    EQU   *                   COME HER TO ABEND BAD USERS
         ABEND 555,DUMP,STEP       ABEND HIM
         SPACE
ABEND3   ABEND 999,DUMP,STEP        FUNCTION CODE GT HIFUNC
         EJECT
DICVTPUT EQU   *
*
*        THIS ROUTINE IS ENTERED BY SMF2 BY ISSUING A SVC 250
*              AT INIT TIME. THE ADDRESS OF THE DICVT IS PLACED
*              IN THE SYSTEM CVT FOR ACCESS BY ALL SYSTEM RTNS.
*
*
*
         CLM   R0,7,SMF            IS IT JES WHO IS COMING
         BNE   TELLUSR             NO. DONT ABEND HIM BUT ISSUE MSG
         LA    R0,DICVT            LOAD ADDR OF DICVT
         LH    R1,36(R7)           LOAD ASID INTO REG 1
*        CLM   R1,1,THREE          IS IT SMF2 FOR SU7 MUST BE 3 6-24
*        BNE   TELLUSR             NOT HIM. REPORT TO OPERATOR
         USING CVTMAP,R3                                           RGR
         ST    R0,CVTUSER          ST DICVT ADDR INTO CVTUSER      RGR
GOBACKVT SR    R0,R0               SUBTRACT FOR RETURN
         LR    R1,R0               HIM TOO
         BR    R14                 RETURN BACK TO SCP/JES/CALLER
*
TELLUSR  EQU   *                   COME HERE TO TELL USER BAD DEAL
         WTO   'UNAUTHORIZED CALL TO SVC250. REPORT JOB TO S.P.',      X
               ROUTCDE=(1),DESC=2
         B     GOBACKVT            RETURN
         EJECT
AUTHON   EQU   *
*
*        ENTRY HERE IS TO AUTH A JSCB FOR A PARTICULAR FUNCTION.
*
*              IF THE USER IS AUTH , ONLY A BIT IS SET IN THE TCB,
*              TO TELL UNAUTH THAT USER REALLY IS AUTH..
*
*        REG14 CAN BE EITHER BR ENTRY FROM SERVICE FUNCTIONS
*              OR SCP SVC ENTRY.
*
         CLM   R0,7,SMF            DOES HE HAVE THE PASSWORD
         BNE   ABEND               NO. GET RID OF HIM
         SPACE 2
AUTHONE  EQU   *
         L     R5,180(R4)          LOAD JSCB FROM TCB
         TM    236(R5),ONE         TEST FOR ALREADY AUTH USER
         BO    TCBAUTH             YES. ONLY SET TCB FLAG
         SPACE 4
         OI    236(R5),ONE         OTHERWISE SET AUTH BIT ON
         BR    R14                 RETURN TO CALLER.
         SPACE 4
TCBAUTH  EQU   *
         OI    X'A8'(R4),ONE       TURN ON .AUTH. SETTING           RGR
         BR    R14                 RETURN TO CALLER.
         SPACE 5
AUTHOFF  EQU   *
*
*        ENTRY INTO HERE TO UNAUTH A JSCB..
*              IF THE USER IS AUTH AT INIT TIME AND HE WENT TO
*              AUTHON TO GAIN AUTH, THIS RTN WILL ENSURE THAT HE KEEPS
*              AUTH. IF THE USER ISSUES ONLY AUTHOFF AND IS REALLY
*              AUTH, HE WILL LOSE IT.
*
*        THE TCBID FIELD IS USED TO SET .AUTH. BIT ON
*
*        REG 14 CAN BE FROM ANOTHER SERVICE RTN OR SCP.
*
         SPACE 2
         CLM   R0,7,SMF            DOES USER HAVE PASSWORD
         BNE   ABEND               NO.ABEND THE JOB
         SPACE
*        TEST TCB AUTH BIT FIRST BEFORE TURNING OFF JSCB AUTH
         SPACE
AUTHOFFE EQU   *
         TM    X'A8'(R4),ONE       DID AUTHON SET .AUTH.            RGR
         BO    QUICKOFF            YES. JUST TURN OFF TCB BIT
         SPACE
         L     R5,180(R4)          LOAD JSCB FROM TCB
         NI    236(R5),ALLONES-ONE TURN OFF AUTH BIT
         BR    R14                 RETURN TO CALLER
         SPACE
QUICKOFF EQU   *
         NI    X'A8'(R4),ALLONES-ONE TURN OFF .AUTH. BIT            RGR
         BR    R14                 RETURN TO CALLER
         EJECT
DONTSWAP EQU   *
*
*        A SYSEVENT DONTSWAP IS ISSUSED HERE FOR THE ADDRESS SPACE
*
*              AUTH IS TEMP ON/OFF FOR SYSEVENT PROCESSING
*
*        R2  IS SAVED IN R15 FOR BAL TO AUTH RTNS.
*
         SPACE 2
         CLM   R0,7,SMF            DOES USER HAVE PASSWORD
         BNE   ABEND               NO. ABEND USER JOBSTEP
         SPACE
         LR    R2,R14              LOAD RETURN REG INTO SAVEREG
         BAL   R14,AUTHONE         AUTH USER FOR SYSEVENT
         SPACE
         LH    R1,36(R7)           LOAD ASID INTO R1 FOR SYSEVENT
         SYSEVENT DONTSWAP,ASID=(1)              MAKE NON SWAPPABLE
         SPACE
         BAL   R14,AUTHOFFE        UNAUTH FOR RETURN
         LR    R14,R2              RELOAD RETURN REGISTER
         BR    R14                 RETURN
         SPACE 3
OKSWAP   EQU   *
*
*        A SYSEVENT OKSWAP   IS ISSUSED HERE FOR THE ADDRESS SPACE
*
*              AUTH IS TEMP ON/OFF FOR SYSEVENT PROCESSING
*
*        R2  IS SAVED IN R15 FOR BAL TO AUTH RTNS.
*
         SPACE 2
         CLM   R0,7,SMF            DOES USER HAVE PASSWORD
         BNE   ABEND               NO. ABEND USER JOBSTEP
         SPACE
         LR    R2,R14              LOAD RETURN REG INTO SAVEREG
         BAL   R14,AUTHONE         AUTH USER FOR SYSEVENT
         SPACE
         LH    R1,36(R7)           LOAD ASID INTO R1 FOR SYSEVENT
         SYSEVENT OKSWAP,ASID=(1)  MAKE SWAPABLE...
         SPACE
         BAL   R14,AUTHOFFE        UNAUTH FOR RETURN
         LR    R14,R2              RELOAD RETURN REGISTER
         BR    R14                 RETURN
         EJECT
*        CONSTANTS
INQ      DC    C'INQ'              PASSWORD FOR INQ
SUBPOOL  DC    AL1(250) TWO-FIFTY  SUBPOOL FOR GETMAIN
THREE    DC    AL1(3)              ASID FOR SMF2
ONE      EQU   1
ALLONES  EQU   255
HIFUNC   DC    AL1(6)              HIGHEST FUNCTION ALLOWED
SMF      DC    C'SMF'         SMF PASSWORD
         DS    0F
PATCH    DC    S(*)           PATCH AREA FOR MISTAKES
         DC    10H'0'             PATCH AREA
         SPACE 5
DICVT    DICVT DSECT=NO            MAKE DICVT AREA
         SPACE 5
WORK     DSECT DSECT FOR GETMAINED AREA
ECB      DS    F                  ECB FOR FIXING
S14      DS    F                  SAVE R 14 FROM EVERYONE
WORKAREA EQU   *-WORK             COMPUT LENGTH
         SPACE 5
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         CVT   DSECT=YES,LIST=YES
         END
