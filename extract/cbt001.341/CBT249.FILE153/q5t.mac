DISPLAY  QSTART 'QUEUE COMMAND - 3270 DISPLAY ROUTINES'
***********************************************************************
* RNB CHANGES:                                                        *
*     (1) RNB13 - MODIFICATIONS TO FIX PROBLEMS WITH TCAM FULL-SCREEN *
*                 PROCESSING OF TEST-REQUEST, SYSTEM REQUEST, AND THE *
*                 PA2/PA3 KEYS.                                       *
*     (2) RNB14 - MODIFICATIONS TO FIX PROBLEMS FULL-SCREEN           *
*                 PROCESSING. THIS ONE ALLOWS THE USER TO TYPE IN THE *
*                 TOP COMMAND LINE WITHOUT QUEUE MISINTERPRETING WHAT *
*                 WAS ENTERED. (TRY THE DO COMMAND FROM BOTH PLACES   *
*                 WITHOUT THE MOD TO SEE THE EFFECT.)                 *
*     (3) RNB15 - RESTORE PFK DEFINITIONS FOR PF7 AND PF8 TO ORIGINAL *
*                 ICBC VALUES OF -27 AND +27. WE DON'T HAVE THE OTHER *
*                 3278 MODELS, AND PARTIAL SCROLLING IS EASIER AND    *
*                 MORE SPF CONSISTENT WITH THE ORIGINAL VALUES. ONLY  *
*                 IF QRNB=1.                                          *
***********************************************************************
         GBLB  &QPFK          PF-KEY OPTION. DEFINED BY QSTART     ICBC
         GBLB  &QRNB                                              RNB13
         USING QDISPLAY,R10   BASE REG FOR DISPLAY WORK AREA
         L     R10,QVDSPL     ADDR OF DISPLAY WORK AREA
         USING QCPRINT,R9     BASE REG FOR PRINT   WORK AREA       FCI*
         L     R9,QVPRINT     ADDR OF PRINT   WORK AREA            FCI*
         USING QDQTTY,R8
         L     R8,QVDQTTY
***********************************************************************
*                                                                     *
*   CHECK FOR ROOM ON SCREEN                                          *
*                                                                     *
***********************************************************************
         MVI   QDOVER,0       ZERO OUT THE PAGE OVERFLOW INDICATOR
         MVI   QDPLUS,C' '    BLANK THE OVERFLOW INDICATOR
         LH    R4,QDMLNG      LOAD MSG LENGTH
         MVC   QPRSAVE,QDTLINE        SAVE SUBTITLE LINE ON ENTRY  FCI*
         LTR   R4,R4          IS MSG LENGTH ZERO?
         BZ    WRTSCR
NOTMSG   DS    0H
         MVI   QDPLUS,C'+'    INDICATE SCREEN OVERFLOW
         LH    R1,QDNEXT      GET NUMBER OF LINES.                EAM
         LA    R1,1(R1)       CURRENT LINE NUMBER ON SCREEN       EAM
         ZAP   QDWORK,QDLNES  GET NUMBER OF LINES                 EAM
         CVB   R2,QDWORK      FIX IT SO WE CAN WORK WITH IT.      EAM
         CR    R1,R2          IS THERE ROOM ON THE SCREEN?        EAM
         BH    WRTSCR         NO. WRITE SCREEN.
***********************************************************************
*                                                                     *
*   MOVE THE MESSAGE TO THE SCREEN                                    *
*                                                                     *
***********************************************************************
DSP2     LH    R7,QDMLNG      LOAD MESSAGE LENGTH
         LTR   R7,R7          IS MESSAGE LENGTH ZERO?
         BZ    RETURN         YES. RETURN.
         BCTR  R7,0           SUB FOR EX.                         EAM
         LH    R4,QDNEXT      LOAD CURRENT SCREEN LINE NUMBER     EAM
         LR    R1,R4          SAVE LINE NUMBER                    EAM
         MH    R4,=H'80'      MULT. BY BUFFER LENGTH.             EAM
         LA    R5,QDLINE1     LOAD ADDRESS                        EAM
         AR    R4,R5                      OF NEXT LINE.           EAM
         L     R6,QDMSGA      LOAD ADDR OF MESSAGE                EAM
DSP4     LA    R1,1(R1)       UPDATE LINE NUMBER                  EAM
         STH   R1,QDNEXT      STORE LINE NUMBER                   EAM
MVCMSG   MVC   0(1,R4),0(R6)  MOVE THE MESSAGE TO THE SCREEN      EAM
         EX    R7,MVCMSG                                          EAM
***********************************************************************
*                                                                     *
*   RETURN TO CALLER                                                  *
*                                                                     *
***********************************************************************
RETURN   QSTOP
***********************************************************************
*                                                                     *
*   WRITE A FULL SCREEN, WAIT FOR REPLY                               *
*                                                                     *
***********************************************************************
WRTSCRTR TR    0(*-*,R1),TABLE  FIX UNPRINTABLES                  UF003
WRTSCR   DS    0H                                                 EAM
         LA    R1,QDLINE1                                         EAM
         ZAP   QDWORK,QDLNES  GET NUMBER OF LINES                 EAM
         CVB   R2,QDWORK      FIX IT SO WE CAN WORK WITH IT.      EAM
         LH    R14,QDLNELEN                                       EAM
         BCTR  R14,0                                              EAM
WRTSCR1  DS    0H                                                 EAM
         EX    R14,WRTSCRTR                                       EAM
         LA    R1,80(R1)                                          EAM
         BCT   R2,WRTSCR1                                         EAM
TPUTSCRN DS    0H                                                 UF003
         CLI   QTTY,X'01'
         BNE   TPUTFULL
         MVC   QPLINE,QBLANK                   RECONSTRUCT IMAGE   FCI*
         MVC   QPLINE(15),=C'QUEUE COMMAND -'    OF HEADER LINE    FCI*
         MVC   QPLINE+16(L'QPRSAVE),QPRSAVE        WITHOUT 3270    FCI*
         PUT   SYSPRINT,QPDETAIL                      CONTROL CHARS
         EJECT ,                                                   FCI*
*******************************************************************FCI*
*  PUT OUT ENTIRE SCREEN OF DETAIL LINES (BLANK OR NOT)            FCI*
*******************************************************************FCI*
         ZAP   QDWORK,QDLNES
         CVB   R3,QDWORK         GET NUMBER OF LINES              EAM
         LH    R7,QDLNELEN       LINE LENGTH                      UF003
         BCTR  R7,0
         MVI   QPDETAIL,X'40'    BLANK
         MVC   QPLINE,QPDETAIL         PUTLINE.
MVCHEAD  MVC   QPLINE(1),QDHLINE MOVE HEADING LINE TO BUFFER      UF003
         EX    R7,MVCHEAD                                         EAM
         PUT   SYSPRINT,QPDETAIL PRINT THE LINE                   UF003
         LH    R1,QDNEXT                                          EAM
         LTR   R1,R1                                              EAM
         BZ    REPLNE                                             EAM
         LA    R2,QDLINE1        LOAD ADDRESS OF FIRST LINE       UF003
PUTLOOP  DS    0H                                                 EAM
         EX    R7,CLCMVC         COMPARE FOR BLANKS.              EAM
         BE    BYPASS                                             EAM
         EX    R7,PUTMVC         MOVE LINE TO BUFFER              UF003
         PUT   SYSPRINT,QPDETAIL PUT DETAIL LINE OUT               FCI*
BYPASS   DS    0H                                                 EAM
         LA    R2,80(R2)         POINT TO NEXT LINE               EAM
         BCT   R3,PUTLOOP        AND GRIND THE SCREEN THROUGH      FCI*
REPLNE   DS    0H                                                  FCI*
         MVC   QPLINE,QBLANK                   RECONSTRUCT IMAGE   FCI*
         MVC   QPLINE(07),=C'REPLY -'            OF COMMAND LINE   FCI*
         MVC   QPLINE+08(L'QDREPLY),QDREPLY        WITHOUT 3270    FCI*
         MVC   QPLINE+72(1),QDPLUS                   CONTROL CHARS FCI*
         PUT   SYSPRINT,QPDETAIL                       AND PRINT IT
         GET   SYSIN,QDREPLY
         LA    R1,QDREPLY+62
COMBLK   CLC   0(,R1),=C' '
         BE    REPLOOP
         LA    R3,QDREPLY-1
         SR    R1,R3
         B     NOTXT2
REPLOOP  BCT   R1,COMBLK
         SR    R1,R1
         B     NOTXT2
PUTMVC   MVC   QPLINE(1),0(R2)  EXECUTED MOVE                     UF003
CLCMVC   CLC   0(1,R2),QBLANK                                     EAM
TPUTFULL LH    R0,QDSCRPLN    LOAD LENGTH FOR TPUT                UF003
         TPUT  QDSCREEN,(0),FULLSCR,MF=(E,QTPUT)                  UF003
         MVC   QDTLINE,QPRSAVE        RESTORE SUBTITLE INFO        FCI*
         AIF  (&QPFK).PFK1    SKIP NON-PFK CODE                    ICBC
         TGET  QDREPLY,63,EDIT,MF=(E,QTGET)
         CH    R15,=H'12'     IS INPUT LONGER THAN BUFFER?
         BNE   NOCLEAR        NO. CONTINUE.
         TCLEARQ INPUT        CLEAR THE QUEUE
NOCLEAR  STH   R1,QDRLNG      STORE LENGTH OF REPLY
         AGO   .PFK2                                               ICBC
.PFK1    ANOP                                                      ICBC
         LA    R6,QDREPLY                                          ICBC
         XC    PFREPLY,PFREPLY                                     ICBC
         XC    QDREPLY,QDREPLY                                     ICBC
         TGET  PFREPLY,69,ASIS,MF=(E,QTGET)
         CH    R15,=H'12'     IS INPUT LONGER THAN BUFFER?         ICBC
         BNE   NOCLEAR        NO. CONTINUE.                        ICBC
         TCLEARQ INPUT        CLEAR THE QUEUE                      ICBC
NOCLEAR  IC    R4,PFCODE                                           ICBC
         XR    R5,R5                                               ICBC
         CLI   PFCODE,X'F0'      TEST-REQ/SYS-REQ?                RNB13
         BE    ENTKEY            /YES - TREAT AS ENTER            RNB13
         CLI   PFCODE,X'01'      OTHER KIND OF SYS-REQ?           RNB13
         BE    ENTKEY            /YES - TREAT AS ENTER            RNB13
         CLI   PFCODE,X'6E'      PA2?                             RNB13
         BE    TPUTSCRN          /YES - GO RESHOW SCREEN          RNB13
         CLI   PFCODE,X'6B'      PA3?  (TCAM GENERATED)           RNB13
         BE    TPUTSCRN          /YES - GO RESHOW SCREEN          RNB13
         N     R4,=X'0000000F'   EXTRACT PF-KEY NUMBER             ICBC
         CH    R4,=H'12'                                           ICBC
         BH    ENTKEY           "ENTER" KEY                        ICBC
         BCTR  R4,0                                                ICBC
         MH    R4,=H'5'                                            ICBC
         LA    R5,PFKTAB                                           ICBC
         LA    R5,0(R4,R5)                                         ICBC
         MVC   QDREPLY(5),0(R5)  MOVE PF-KEY VALUE                 ICBC
         LA    R5,5                                                ICBC
         LA    R6,3(,R6)                                           ICBC
         SH    R1,=H'3'          ALLOW USER TO INPUT ON EITHER    RNB14
         BZ    NOTXT             THE TOP OR BOTTOM COMMAND LINE   RNB14
         MVC   0(60,R6),PFTXT                                     RNB14
         LA    R5,3                                               RNB14
         SH    R1,=H'3'                                           RNB14
         B     NOTXT                                              RNB14
ENTKEY   EQU   *                                                   ICBC
         SH    R1,=H'3'                                            ICBC
         BZ    NOTXT                                               ICBC
         MVC   0(63,R6),PFTXT                                     RNB14
         SH    R1,=H'3'                                            ICBC
NOTXT    LA    R1,0(R5,R1)                                         ICBC
NOTXT2   STH   R1,QDRLNG      STORE LENGTH OF REPLY                ICBC
.PFK2    ANOP                                                      ICBC
         OC    QDREPLY,QBLANK     UPPERCASE THE COMMAND            FCI*
         CLC   QDREPLY(2),=CL2'PR' POSSIBLE PRINT COMMAND      PWF FCI*
         BNE   CLSCRN             NOPE..SPLIT NORMALLY             FCI*
*                                                                  FCI*
         L     R15,=V(PRINT)  FETCH PRINT ENTRY ADDRESS            FCI*
         BALR  R14,R15        AND CALL HIM                         FCI*
         LTR   R15,R15        HOW IS HIS RETURN CODE               FCI*
         BNZ   CLSCRN         NOTHING TO REPORT                    FCI*
*                                                                  FCI*
         B     TPUTSCRN       GO REPOST SCREEN                     FCI*
CLSCRN   LA    R4,QDLINE1     LOAD ADDRESS OF FIRST LINE           FCI*
         LH    R5,QDSCRLEN    LOAD LENGTH OF SCREEN               UF003
         SR    R6,R6          NO SENDING FIELD NEEDED              FCI*
         STH   R6,QDNEXT      STORE ZERO IN LINE NUMBER            FCI*
         SR    R7,R7          FILL SCREEN WITH NULLS               FCI*
         MVCL  R4,R6          CLEAR THE SCREEN                     FCI*
*
         LH    R1,QDRLNG      STORE LENGTH OF REPLY
         LTR   R1,R1          WAS THERE A RESPONSE FROM USER?
         BNZ   INTER          YES. INTERRUPT PROCESSING.
         MVI   QDOVER,1       INDICATE PAGE OVERFLOW
         B     DSP2           CONTINUE PROCESSING
INTER    L     R13,QFRSTSA    GO BACK TO MAIN MODULE
         LM    R14,R12,12(R13) RESTORE REGISTERS FROM FIRST SAVEAREA
         BR    R10            ADDRESS OF INTERRUPT HANDLER IN QUEUE
***********************************************************************
*                                                                     *
*   MISCELLANY                                                        *
*                                                                     *
***********************************************************************
         LTORG
         AIF  (NOT &QPFK).PFK3                                     ICBC
* PF-KEY DEFINITIONS                                               ICBC
PFKTAB   DS    0CL60                                               ICBC
PF1      DC    CL5'H'                                              ICBC
PF2      DC    CL5'DA'                                       FCI*  ICBC
PF3      DC    CL5'E'                                              ICBC
PF4      DC    CL5'PRINT'                                    FCI*  ICBC
PF5      DC    CL5'F'                                              ICBC
PF6      DC    CL5'DI'                                             ICBC
         AIF   (&QRNB).RNB15A                                     RNB15
PF7      DC    CL5'PB'                                            UF003
PF8      DC    CL5'PF'                                            UF003
         AGO   .RNB15B                                            RNB15
.RNB15A  ANOP                                                     RNB15
PF7      DC    CL5'-  21'                                         RNB15
PF8      DC    CL5'+  21'                                         RNB15
.RNB15B  ANOP                                                     RNB15
PF9      DC    CL5'DO'                                             ICBC
PF10     DC    CL5'CO 1'                                           ICBC
PF11     DC    CL5'CO 41'                                          ICBC
PF12     DC    CL5'ST'                                             ICBC
.PFK3    ANOP                                                      ICBC
* TABLE OF PRINTABLE CHARACTERS
TABLE    DC    CL64' '
         DC    191AL1(*-TABLE)  REST OF TABLE IS OK               UF003
         DC    C' '              EXCEPT FOR X'FF'                 UF031
SYMDEL   DSECT ,                   KILL SYM CARD GENERATION       UF023
         QCOMMON
SYMNODEL DSECT ,                   RESTORE SYM CARD GENERATION    UF023
         END
