         TITLE 'S W A P - SYSTEM WORKLOAD ANALYSIS PROGRAM - LEVEL 4.2'
***********************************************************************
*                                                                     *
*                              S W A P                                *
*                                                                     *
*                 SYSTEM  WORKLOAD  ANALYSIS  PROGRAM                 *
*                                                                     *
*                  MARTY SPRINZEN AND STEVE LANGLEY                   *
*                     SOUTHERN CALIFORNIA EDISON                      *
*                       ROSEMEAD, CALIF. 91770                        *
*                            213-572-3521                             *
*                                                                     *
*  COMMAND         DESCRIPTION                                        *
*                                                                     *
*     A     DISPLAY TSO, BATCH AND STARTED TASK JOBS                  *
*     B     DISPLAY BATCH AND STARTED TASK JOBS ONLY                  *
*     E     EXIT FROM SWAP                                            *
*     G     THIS MODE WILL DISPLAY GENERAL ADDRESS SPACE INFO.        *
*           (CPU, PROCSTEP, STEPNAME, M/S/L, TRS. COUNT, TCBS)        *
*     I     THIS MODE WILL DISPLAY ONLY THOSE MEMORIES WHICH ARE      *
*           EITHER SWAPPED IN AND RUNNING OR SWAPPED OUT BUT READY    *
*           TO RUN.                                                   *
*     L     ATTEMPT TO XCTL TO OPERATOR SPY PROGRAM                   *
*     O     THIS MODE WILL DISPLAY ALL ACTIVE MEMORIES IN THE SYSTEM, *
*           REGARDLESS OF LOCATION.                                   *
*     S     THIS MODE WILL DISPLAY SRM INFO. (MEM,WSS,DP,DQ,G,P,D,RM, *
*           WR,RAT,SRV,I/O)                                           *
*     T     DISPLAY TSO JOBS ONLY                                     *
*     DXX   SET DELAY TIME TO XX TENTHS OF A SECOND                   *
*     WXX   ENTER AUTOMATIC REFRESH MODE. SWAP WILL AUTOMATICALLY     *
*     W     REFRESH AND UPDATE THE SCREEN ONCE A SECOND FOR XX        *
*           SECONDS. IF XX IS OMITTED, A DEFAULT OF 30 SECONDS IS     *
*           ASSUMED. IF XX = 0, THE TIMER WILL COUNT UP INSTEAD OF    *
*           DOWN, AND WILL COUNT UNTIL 'INTERRUPT' IS PRESSED.        *
*     ?     DISPLAY SHORT HELP FOR SWAP COMMANDS AND OUTPUT           *
*                                                                     *
*  GLOSSARY OF TERMS FOR OUTPUT DISPLAY:                              *
*                                                                     *
*    HH:MM:SS  TRANSACTION TIME                                       *
*    R         REASON CODE FOR SWAP OUT FROM OUCBEFL.                 *
*              THE FOLLOWING ARE THE REASON CODES DISPLAYED:          *
*                   NOT SWAPPED - IN CORE                             *
*                O   SWAP CODE 1 : TERMINAL OUTPUT WAIT               *
*                I   SWAP CODE 2 : TERMINAL INPUT WAIT                *
*                W   SWAP CODE 3 : LONG WAIT                          *
*                A   SWAP CODE 4 : AUXILIARY STORAGE SHORTAGE         *
*                R   SWAP CODE 5 : REAL STORAGE SHORTAGE              *
*                V   SWAP CODE 6 : MS0 DETECTED WAIT                  *
*                S   SWAP CODE 7 : REQSWAP SYSEVENT ISSUED            *
*                E   SWAP CODE 8 : ENQHOLD EXCHG BY SWAP ANALYSIS     *
*                X   SWAP CODE 9 : EXCHG RECOMMENDED BY SWAP ANALYSIS *
*                $   SWAP CODE A : UNILATERAL SWAPOUT                 *
*                ?   NON OF ABOVE: PROBABLE ERROR OR SRM MODIFICATION *
*    L         CURRENT LOCATION OF THIS MEMORY:                       *
*                I  SWAPPED IN AND ELIGIBLE TO RUN.                   *
*                O  SWAPPED OUT BUT READY TO RUN.                     *
*                W  SWAPPED OUT AND NOT READY TO RUN.                 *
*                $  SWAPPED IN AND V=R OR NON-SWAPPABLE STATUS.       *
*                ?  TRANSITIONING BETWEEN STATES.                     *
*    MEM       CURRENT AMOUNT OF MEMORY ALLOCATED TO THIS MEMORY.     *
*    WSS       SRM'S VIEW OF THE WORKING SET SIZE FOR THIS MEMORY.    *
*    DP        DISPATCHING PRIORITY OF MEMORY (IN HEX).               *
*    DQ        RELATIVE POSITION OF MEMORY ON DISPATCHING QUEUE.      *
*    G         PERFORMANCE GROUP                                      *
*    P         PERFORMANCE GROUP PERIOD                               *
*    D         DOMAIN                                                 *
*    RM        RESOURCE MANAGER RECOMMENDATION                        *
*    WR        WORK LOAD MANAGER RECOMMENDATION                       *
*    SU/S      SERVICE RATE (S.U.'S PER SECOND)                       *
*    S.U.      SERVICE UNITS (IN INTERVAL IF MODE=S )                 *
*    I/O       EXCP'S IN INTERVAL (MAX=65K)                           *
*    CPU       TASK CPU TIME                                          *
*    TCPU      THE TOTAL CPU TIME USED BY THIS MEMORY (TASK+SRB).     *
*    TRS       SERVICE ACCUMULATED IN THE TRANSACTION.                *
*    SC        NUMBER OF TIMES THE MEMORY HAS BEEN SWAPPED            *
*    MSL       MOUNT/START/LOGON INITIATED TASK                       *
*    TCB       NUMBER OF ACTIVE TCBS                                  *
*    PROCNAME  PROCSTEPNAME FOR THIS STEP                             *
*    STEPNAME  STEPNAME                                               *
*    TSLS      TIME SINCE LAST SWAP ACTION IN SECONDS                 *
*                                                                     *
*     SWAP REQUIRES SYS1.AMODGEN AS A MACLIB                          *
*                                                                     *
*  REGISTERS USED:                                                    *
*    R1 - BUFFER ADDRESS         R6 - PTR TO OUCB                     *
*    R2 - PTR TO ASVT ENTRY      R7 - R11  WORKING REGS               *
*    R3 - COUNT OF ASCBS         R12 - BASE REGISTER                  *
*    R4 - PTR TO OUXB                                                 *
*    R5 - PTR TO ASCB                                                 *
*                                                                     *
***********************************************************************
         SPACE 4
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         EJECT
SWAP     CSECT
         B     100(R15)
         DC    CL9'SWAP'          IDENTIFIER
         DC    CL9'&SYSDATE'
         DC    CL6'&SYSTIME'
SAVE     DC    18F'0'             SAVE AREA
         STM   R14,R12,12(R13)    SAVE REGISTERS
         LR    R12,R15            R12 = ADDR OF ENTRY POINT
         USING SWAP,R12            ADDRESABILITY TO CSECT
         LA    R11,SAVE           R11 = ADDR OF OUR SAVE AREA
         ST    R13,SAVE+4         SAVE POINTER TO CALLERS SAVE AREA
         ST    R11,8(R13)         SAVE PTR TO OUR SAVE AREA IN CALLER'S
         LR    R13,R11            R13 = ADDR OF OUR SAVE AREA
         C     R2,SPY             DID WE COME FROM SPY?      RGR
         BE    SPY1               YES, SKIP OPER CHECK       RGR
         L     R1,8(R1)           GET PSCB @ FROM CPPL       RGR
         TM    X'10'(R1),X'80'    IS HE OPER PRIVILEGED?     RGR
         BNO   STOP                                          RGR
         B     SPY1                                          RGR
SPY      DC    0F'0',C'SPY '                                 RGR
SPY1     DS    0H                                            RGR
         EJECT
         LA    3,STAXLIST
         STAX  ATTNEXIT,MF=(E,(3))     ATTN EXIT TRAP
         GTSIZE
         LTR   R0,R0
         BZ    HARDCOPY
         LR    R2,R0              R2 = NUMBER OF LINES PER SCREEN
         SH    R2,=H'2'           R2 = R2 - 2
         MH    R2,=H'81'          R2 = NUMBER OF BYTES IN SCREEN BUFR
         L     R3,BUFAD           R3 = ADDR OF START OF BUFFER
         LA    R2,0(R2,R3)        R2 = ADDR OF END OF BUFFER
         ST    R2,ENDBUF          SAVE R2
         STFSMODE ON,INITIAL=YES  TURN ON FULL SCREEN MODE FOR VTAM
         B     BOTHTYPE
HARDCOPY STSIZE SIZE=80           SET LINE SIZE TO 80
         MVI   CRTFLAG,X'00'      WE ARE USING A HARDCOPY
         MVC   CMDCTRL1(2),BLANKS
BOTHTYPE MVC   HEADINGA(80),HEADING2   LOAD GENERAL HEADING INITIALLY.
***********************************************************************
*                                                                     *
*                        LOCATE CVT AND ASVT POINTERS                 *
*                                                                     *
***********************************************************************
FINDCVT  L     R2,16              CVT POINTER
         USING CVT,R2
         L     R2,CVTASVT         ASVT POINTER
         DROP  R2
         USING ASVT,R2
         L     R3,ASVTMAXU        MAXIMUM NUMBER OF ENTRIES
         LA    R2,ASVTFRST        FIRST ENTRY MINUS 4
         L     R1,BUFAD           LOAD ADDRESS OF OUTPUT BUFFER.    SP3
         ST    R2,ASCBADDR        SAVE ACSB ADDRESS
         CLC   JOBMASK(8),BLANKS  IS THE JOBMASK BLANK?
         BNE   ASCBLOOP           NO, SO DON'T SET PAGE TO 0
         MVI   PAGE,C'0'          RESET PAGE TO PAGE 0
         EJECT
***********************************************************************
*                                                                     *
*            MAJOR LOOP (THROUGH ALL ASCB'S)                          *
*                                                                     *
***********************************************************************
ASCBLOOP L     R2,ASCBADDR        R2 = ADDR OF LAST ASCB ENTRY
         LA    R2,4(R2)           INCREMENT TO NEXT ASCB ENTRY
         ST    R2,ASCBADDR        SAVE ASCB ADDR
         ICM   R5,B'1111',0(R2)   ASCB POINTER
         USING ASCB,R5
         BM    NOGOOD             BRANCH IF BAD PTR
         TM    102(R5),X'04'      TEST FOR SWAPPED OUT
INOROUT  BC    0,NOGOOD           BRANCH IF OUT AND NOT READY TO RUN
         L     R6,ASCBOUCB        OUCB POINTER
         USING OUCB,R6
         L     R4,ASCBOUXB        OUXB POINTER
         USING OUXB,R4
         TM    OUCBYFL,OUCBLOG    LOGON CREATED USER?               SP3
         BO    TSOTEST            BRANCH TO TEST FOR TSO            SP3
         ICM   R7,B'1111',ASCBJBNI JOBNAME POINTER                  SP3
         BZ    STCTEST            JOBNAME NOT AVAILABLE -> STC      SP3
BATCHTST DS    0H                                                   SP3
         TM    TYPEREQ,BATCHREQ   DO WE WANT BATCH?                 SP3
         BNO   NOGOOD                NO, SKIP IT                    SP3
         B     GOGETIT                                              SP3
TSOTEST  DS    0H                                                   SP3
         TM    TYPEREQ,TSOREQ     DO WE WANT TSO?                   SP3
         BNO   NOGOOD                NO, SKIP IT                    SP3
         B     GOGETIT                                              SP3
STCTEST  DS    0H                                                   SP3
         TM    TYPEREQ,STCREQ     DO WE WANT STARTED TASKS?         SP3
         BNO   NOGOOD                NO, SKIP IT                    SP3
GOGETIT  DS    0H                                                   SP3
         MVI   LINE+2,C' '        CLEAR OUTPUT LINE TO BLANKS.
         MVC   LINE+3(77),LINE+2
***********************************************************************
*                                                                     *
*                              JOBNAME                                *
*                                                                     *
***********************************************************************
         MVC   JOB(8),START       INITIALIZE 'STARTING' JOBNAME
         ICM   R7,B'1111',ASCBJBNI JOBNAME POINTER
         BZ    NOTJOB             JOBNAME NOT AVAILABLE
         MVC   JOB,0(R7)          MOVE IN JOBNAME
         B     CONT1
NOTJOB   ICM   R7,B'1111',ASCBJBNS S/M/L POINTER
         BZ    CONT1              S/M/L NOT AVAILABLE
         MVC   JOB,0(R7)          MOVE IN JOBNAME FOR S/M/L
CONT1    CLC   JOBMASK(8),BLANKS  IS THE JOB MASK ALL BLANKS?
         BE    CONT1A             YES, SO GO ON
         LA    R8,JOBMASK         R8 = ADDR OF JOBMASK
         LA    R7,JOBMASK+7       R7 = ADDR OF LAST CHAR IN MASK
MASKLOOP CLI   0(R8),C' '         IS THE CHAR A BLANK?
         BE    GOTLEN             YES
         CR    R8,R7              IS R8 = R7?
         BE    GOTLEN             YES - END OF MASK
         LA    R8,1(R8)           POINT TO NEXT CHAR
         B     MASKLOOP           CHECK NEXT CHAR
GOTLEN   LA    R7,JOBMASK+1       R7 = ADDR OF FIRST CHAR IN MASK + 1
         SR    R8,R7              R8 = LENGTH OF MASK - 1
         EX    R8,VARCLC1         IS THE JOB MASK = JOBNAME?
         BNE   NOGOOD             NO, SO FLUSH THIS ASCB
***********************************************************************
*                                                                     *
*                             SWAP COUNT                              *
*                                                                     *
***********************************************************************
CONT1A   LH    R7,OUCBSWC         R7 = SWAP COUNT
         CVD   R7,WORK            CONVERT IT TO DECIMAL.
         MVO   WORK,WORK
         MVI   WORK+7,X'0C'
         MVC   $UCBSWC(2),PTRN1+5
         ED    $UCBSWC-1(3),WORK+6
***********************************************************************
*                                                                     *
*                          SWAP REASON CODE - OUCBSRC                 *
*                                                                     *
***********************************************************************
SWAPCODE DS    0H
         MVI   LINE+1,X'E8'       ASSUME IN STORAGE                RGR
         TM    OUCBQFL,OUCBOUT    ARE WE SWAPPED OUT?
         BZ    CHKSWAP            NO, SWAP CODE MEANS NOTHING      RGR
         CLI   OUCBSRC,X'0A'      CHECK FOR VALID SWAP REASON
         BH    BADCODE            NO.
         CLI   OUCBSRC,X'00'
         BE    BADCODE            NO.
         SR    R8,R8
         IC    R8,OUCBSRC         USE CODE NUMBER AS OFFSET INTO TABLE
         SH    R8,=H'1'           DECREASE BY 1
         LA    R7,CODETABL
         LA    R7,0(R8,R7)        LOAD ADDR OF CORRECT ALPHA CODE
         MVC   STATUS(1),0(R7)    MOVE CODE TO OUTPUT
         B     CHKSWAP                                            RGR
BADCODE  MVI   STATUS,C'?'        ERROR IN CODE OR PROGRAM LOGIC
CHKSWAP  DS    0H                                                 RGR
         CLI   STATUS,C' '        ARE WE IN?                      RGR
         BE    FORGET             YES, HIGHLIGHT                  RGR
         MVI   LINE+1,X'60'       NO, LOWLIGHT                    RGR
***********************************************************************
*                                                                     *
*                    ADDRESS SPACE QUEUE LOCATION                     *
*                                                                     *
***********************************************************************
FORGET   MVI   LOC,C'I'           ASSUME ADDRESS SPACE ON 'IN' QUEUE
*        MVI   LINE+1,X'E8'       ATTR BYTE - PROTECTED, HI INTENSITY
         TM    OUCBSFL,OUCBNSW    ADDRESS SPACE - NON-SWAP
         BZ    WAITQ              NO
         MVI   LOC,C'$'           MOVE IN NON-SWAP INDICATION.
         B     DONE
WAITQ    TM    OUCBQFL,OUCBOFF    'WAIT' QUEUE
         BZ    OUTQ               NO
         MVI   LOC,C'W'           'WAIT' QUEUE INDICATION
*        MVI   LINE+1,X'60'       ATTR BYTE - LOW INTENSITY
         B     DONE
OUTQ     TM    OUCBQFL,OUCBOUT    'OUT' QUEUE
         BZ    TRANS              NO
         MVI   LOC,C'O'           'OUT' QUEUE INDICATION
*        MVI   LINE+1,X'60'       ATTR BYTE - LOW INTENSITY
         B     DONE
TRANS    TM    OUCBQFL,OUCBGOO+OUCBGOI+OUCBGOB    TRANSITIONING' STATUS
         BZ    DONE               NO
         MVI   LOC,C'?'           'TRANSITIONING' INDICATION
***********************************************************************
*                                                                     *
*                    TOTAL CPU TIME (TASK + SRB)                      *
*                                                                     *
***********************************************************************
DONE     LM    R8,R9,ASCBEJST     EJST (CPU TIME - TASK TYPE)
         SRDL  R8,12              CONVERT TO MICROSEC
         LM    R10,R11,ASCBSRBT   SRB TIME
         SRDL  R10,12             CONVERT TO MICROSEC
         AR    R9,R11             TOTAL (TASK + SRB) CPU TIME
         BNO   NOOVFLO            BRANCH IF NO OVERFLOW.
         A     R8,=F'1'           OTHERWISE ADD ONE TO HIGH ORDER.
NOOVFLO  AR    R8,R10             HIGH ORDER.
         D     R8,=F'1000'        FOR DIVIDE.
         CVD   R9,WORK            CONVERT TO DECIMAL.
         MVC   TCPU(8),PTRN2
         ED    TCPU-1(9),WORK+4
***********************************************************************
*                                                                     *
*                 FRAMES ALLOCATED TO ADDRESS SPACE                   *
*                                                                     *
***********************************************************************
         LH    R7,ASCBFMCT        ALLOCATED PAGE FRAME COUNT
         CVD   R7,WORK
         MVC   TEMP+1(5),PTRN1+2                                    RGR
         ED    TEMP(6),WORK+5                                       RGR
         MVC   MEM-1(4),TEMP+2                                      RGR
*        MVC   MEM(3),PTRN1+4                                       RGR
*        ED    MEM-1(4),WORK+6                                      RGR
         EJECT
***********************************************************************
*                                                                     *
*     'GENERAL' OR 'SYSTEM' MODE?                                     *
*                                                                     *
***********************************************************************
         CLI   SRMSW,X'00'
         BE    GENERAL
***********************************************************************
*  SRM MODE:                                                          *
***********************************************************************
*                                                                     *
*                     WORKING SET SIZE AT SWAPIN                      *
*                                                                     *
***********************************************************************
         LH    R7,OUCBWSS         SWAPIN FRAME COUNT
         CVD   R7,WORK
         MVC   $UCBWSS(3),PTRN1+4
         ED    $UCBWSS-1(4),WORK+6
***********************************************************************
*                                                                     *
*                        DISPATCHING PRIORITY                         *
*                                                                     *
***********************************************************************
         XR    R7,R7              R7 = 0
         IC    R7,ASCBDP          R7 = DISPATCHING PRIORITY
         SRL   R7,4               CONVERT FIRST HEX CHARACTER
         IC    R8,HEX(R7)
         STC   R8,DP
         IC    R7,ASCBDP          R7 = DISPATCHING PRIORITY
         N     R7,=F'15'          CONVERT SECOND HEX CHARACTER
         IC    R8,HEX(R7)
         STC   R8,DP+1
***********************************************************************
*                                                                     *
*                   POSITION ON DISPATCHING QUEUE                     *
*                                                                     *
***********************************************************************
         CLI   LOC,C'O'           MEMORY SWAPPED OUT ?
         BE    NOPOS              IF YES, FORGET ABOUT POSITION.
         CLI   LOC,C'W'           WAIT STATUS ?
         BE    NOPOS              IF YES, FORGET ABOUT POSITION.
         LH    R7,ASCBSEQN        LOAD DP QUEUE POSITION.
         CVD   R7,WORK            PREPARE FOR CONVERSION TO EBCDIC.
         UNPK  DQ,WORK+6(2)       CONVERT TO DECIMAL.
         OI    DQ+1,X'F0'
***********************************************************************
*                                                                     *
*                  RESOURCE MANAGER RECOMMENDATION                    *
*                                                                     *
***********************************************************************
NOPOS    L     R7,OUCBCMRV        COMPOSITE WLM RECOMMENDATION VALUE
         LTR   R7,R7              HI-ORDER BIT IS ON ?
         BNM   RMROK              YES, WMR INVALID
         MVC   $UCBWMR,=C'**'     'NOT AVAIL' INDICATION
         MVI   $UCBWMS+4,C'-'     BLANK OUT THE SERVICE COUNTER.
         B     CONT3
RMROK    SRL   R7,8               DIVIDE BY 256
         CVD   R7,WORK            CONVERT TO DECIMAL .
         UNPK  $UCBRMR-1(3),WORK+6(2)                               RGR
         OI    $UCBRMR+1,X'F0'                                      RGR
         MVI   $UCBRMR-1,C' '                                       RGR
         CP    WORK+6(2),=PL2'0'  RMR GREATER THAN 99 ?
         BNE   CONT3              YES, DOUBLE ZERO FIELD.
         MVI   $UCBRMR,C' '       WMR LESS THAN 1.
***********************************************************************
*                                                                     *
*                         PERFORMANCE GROUP                           *
*                                                                     *
***********************************************************************
CONT3    XR    R7,R7              R7 = 0
         LH    R7,OUCBNPG         R7 = PERFORMANCE GROUP            SP3
         CVD   R7,WORK            CONVERT TO DECIMAL.
         UNPK  $UCBPGN-1(3),WORK+6(2)                               RGR
         OI    $UCBPGN+1,X'F0'                                      RGR
*        MVI   $UCBPGN-1,C' '                                       RGR
         CLI   $UCBPGN-1,X'F0'                                      RGR
         BNE   NOPGNED                                              RGR
         MVI   $UCBPGN-1,C' '                                       RGR
NOPGNED  EQU   *                                                    RGR
***********************************************************************
*                                                                     *
*                      PERFORMANCE GROUP PERIOD                       *
*                                                                     *
***********************************************************************
         XR    R7,R7         R7 = 0
         IC    R7,OUCBPGP         PERFORMANCE GROUP PERIOD OFFSET
         SH    R7,=H'12'
         SRL   R7,3                    LENGTH IS NOW 8 @Z40BPCH     RGR
         LA    R7,1(R7)
         STC   R7,$UCBPGP
         OI    $UCBPGP,X'F0'
***********************************************************************
*                                                                     *
*                         DOMAIN                                      *
*                                                                     *
***********************************************************************
         XR    R7,R7         R7 = 0
         IC    R7,OUCBDMN         R7 = DOMAIN
         CVD   R7,WORK            CONVERT TO DECIMAL.
         UNPK  $UCBDOM-1(3),WORK+6(2)                               RGR
         OI    $UCBDOM+1,X'F0'                                      RGR
         MVI   $UCBDOM-1,C' '                                       RGR
***********************************************************************
*                                                                     *
*                      WLM RECOMMENDATION VALUE                       *
*                                                                     *
***********************************************************************
         L     R7,OUCBWMR         WORK LOAD MANAGER REC. VALUE
         LTR   R7,R7              HI-ORDER BIT IS ON ?
         BNM   WMROK              YES, WMR INVALID
         MVC   $UCBWMR,=C'NA'     'NOT AVAIL' INDICATION
         MVI   $UCBWMS+4,C'-'     BLANK OUT THE SERVICE COUNTER.
         B     NORECOM
WMROK    SRL   R7,8               DIVIDE BY 256
         CVD   R7,WORK            CONVERT TO DECIMAL .
         UNPK  $UCBWMR-1(3),WORK+6(2)                               RGR
         OI    $UCBWMR+1,X'F0'                                      RGR
         MVI   $UCBWMR-1,C' '                                       RGR
         CP    WORK+6(2),=PL2'0'  WMR GREATER THAN 99 ?
         BNE   NORECOM            YES, DOUBLE ZERO FIELD.
         MVI   $UCBWMR,C' '       WMR LESS THAN 1.
***********************************************************************
*                                                                     *
*                      IN-STORAGE SERVICE RATE                        *
*                                                                     *
***********************************************************************
NORECOM  L     R10,16             R10 = ADDR OF CVT
         USING CVT,R10
         L     R10,CVTOPCTP       R10 = ADDR OF RMCT
         DROP  R10
         L     R10,X'7C'(R10)     R10 = CURRENT TOD
         S     R10,OUCBTMW        WLM INTERVAL START TIME
         BP    PLUSRATE
         MVC   SERATE(3),BLANKS
         B     NOSRV
PLUSRATE L     R8,OUCBWMS         INTERVAL SERVICE UNITS
         SRDL  R8,32              CALCULATE SEVICE RATE
         M     R8,=F'976'
         DR    R8,R10
         CVD   R9,WORK            CONVERT TO DECIMAL
         MVC   SERATE(5),PTRN1+2                                    RGR
         ED    SERATE-1(6),WORK+5                                   RGR
***********************************************************************
*                                                                     *
*                   INTERVAL SERVICE ACCUMULATION                     *
*                                                                     *
***********************************************************************
NOSRV    L     R7,OUCBWMS         SERVICE UNITS - XACTION
         CVD   R7,WORK            CONVERT TO DECIMAL.
         MVC   $UCBWMS(8),PTRN3                                     RGR
         MVO   WORK,WORK          SHIFT LEFT WORK?                  RGR
         MVI   WORK+7,X'0C'                                         RGR
         ED    $UCBWMS-1(9),WORK+3                                  RGR
***********************************************************************
*                                                                     *
*               IO COUNT USED FOR RECOMMENDATION VALUE                *
*                                                                     *
***********************************************************************
         LH    R9,ASCBIOSM        R9 =  I/0 SERVICE MEASURE
         LH    R10,OUXBIOS        R10 = WLM BASE I/O MEASUREMENT
         SLR   R9,10              IO COUNT FOR THIS IN CORE INTERVAL
         N     R9,=X'0000FFFF'    CLEAR HIGH ORDER BYTES
         CVD   R9,WORK
         MVC   IOC(5),PTRN1+2
         ED    IOC-1(6),WORK+5
***********************************************************************
*                                                                     *
*              TSLS - TIME SINCE LAST SWAP (SECONDS)                  *
*                                                                     *
***********************************************************************
         L     R8,16              R8 = ADDR OF CVT
         USING CVT,R8
         L     R8,CVTOPCTP        R8 = ADDR OF RMCT
         DROP  R8
         L     R8,X'7C'(R8)       R8 = TIME-OF-DAY
         S     R8,OUCBTMS         SUBTRACT TIME OF LAST SWAP ACTION
         SRDL  R8,32              CONVERT TO H.MM.SS
         D     R8,=F'1024'        CONVERT TO SECONDS
         CVD   R9,WORK
         MVC   TSLS(5),PTRN1+2
         ED    TSLS-1(6),WORK+5
*
*
         B     LINEDONE
         EJECT
***********************************************************************
*                                                                     *
*                      ELAPSED TRANSACTION TIME                       *
*                                                                     *
***********************************************************************
GENERAL  L     R8,16              R8 = ADDR OF CVT
         USING CVT,R8
         L     R8,CVTOPCTP        R8 = ADDR OF RMCT
         DROP  R8
         L     R8,X'7C'(R8)       R8 = CURRENT TOD
         S     R8,OUCBTMO         SUBTRACT TRANSACTION START TIME
         SRDL  R8,32              CONVERT TO H.MM.SS
         D     R8,=F'1024'        CONVERT TO SECONDS
         ST    R9,TRANSSEC        STORE SECONDS FOR LATER
         SR    R8,R8              GET HOURS
         D     R8,=F'3600'
         CVD   R9,PACK
         UNPK  HH,PACK+6(R2)
         OI    HH+1,X'F0'
         MVI   HH+2,C':'
         SRDL  R8,32              GET MINUTES
         D     R8,=F'60'
         CVD   R9,PACK
         UNPK  MM(2),PACK+6(2)
         OI    MM+1,X'F0'
         MVI   MM+2,C':'          GET SECONDS
         CVD   R8,PACK
         UNPK  SS(2),PACK+6(2)
         OI    SS+1,X'F0'
***********************************************************************
*                                                                     *
*                           TASK CPU TIME                             *
*                                                                     *
***********************************************************************
         LM    R8,R9,ASCBEJST     EJST (CPU TIME - TASK TYPE)
         SRDL  R8,12              CONVERT TO MICROSEC
         D     R8,=F'1000'        CONVERT TO SECONDS X 10-3
         CVD   R9,WORK            CONVERT TO DECIMAL.
         MVC   CPU(8),PTRN2
         ED    CPU-1(9),WORK+4
***********************************************************************
*                                                                     *
*                            STEPNAME                                 *
*                                                                     *
***********************************************************************
         ICM   R7,B'1111',ASCBJBNI JOBNAME POINTER
         BZ    SML                JOBNAME NOT AVAILABLE
         MVC   PROCSTEP(8),24(R7) MOVE IN PROCSTEPNAME
         MVC   STEPNAME,56(R7)    MOVE IN STEPNAME
         B     CONT
SML      ICM   R7,B'1111',ASCBJBNS S/M/L POINTER
         BZ    CONT               S/M/L NOT AVAILABLE
         CLI   JOB,C'*'           IS THIS *MASTER* ?
         BE    TSO                YES, TREAT IT LIKE A USERID
         TM    OUCBYFL,OUCBSTT    IS THIS A STARTED TASK?
         BNO   TSO                NO, SO USE TSO STEPNAME
         B     CONT
TSO      MVC   STEPNAME,8(R7)     MOVE IN STEPNAME FOR LOGON
         TM    OUCBYFL,OUCBLOG         LOGON CREATED TASK
         BNO   CONT
         LR    R9,R1              SAVE REGISTER                     RGR
         XAUTHON ,                SET UP TO MOVE IN TERMID          RGR
         MODESET KEY=ZERO                                           RGR
         L     R8,ASCBTSB         ADDRESS TERMINAL STATUS BLOCK     RGR
         USING TSB,R8                                               RGR
         MVC   PROCSTEP(8),TSBTRMID MOVE IN TERMINAL ID             RGR
         DROP  R8                                                   RGR
         MODESET KEY=NZERO                                          RGR
         XAUTHOFF ,                                                 RGR
         LR    R1,R9              RESTORE REGISTER                  RGR
CONT     EQU    *
***********************************************************************
*                                                                     *
*                    M/S/L                                            *
*                                                                     *
***********************************************************************
         TM    OUCBYFL,OUCBSTT    START CREATED TASK?
         BZ    TEST2              NO
         MVI   MSL,C'S'
         B     CONT2
TEST2    TM    OUCBYFL,OUCBLOG    LOGON CREATED TASK?
         BZ    TEST3              NO
         MVI   MSL,C'L'
         B     CONT2
TEST3    TM    OUCBYFL,OUCBMNT    MOUNT CREATED TASK?
         BZ    CONT2              NO
         MVI   MSL,C'M'
***********************************************************************
*                                                                     *
*                  # TCBS ACTIVE                                      *
*                                                                     *
***********************************************************************
CONT2    L     R7,ASCBTCBS         R7 = NO. OF ACTIVE TCBS
         CVD   R7,WORK
         UNPK  TCBA,WORK+7(1)
         OI    TCBA,X'F0'
         MVI   TCBA+1,C' '                                          RGR
***********************************************************************
*                                                                     *
*                  TRANSACTION SERVICE ACCUMULATION                   *
*                                                                     *
***********************************************************************
         TM    OUCBQFL,OUCBOUT    ADDRESS SPACE SWAPPED IN
         BO    OUTRANS            YES
         L     R7,OUXBTRS         ACCUMULATED SERVICE
         CVD   R7,WORK            CONVERT TO DECIMAL.
         MVC   $UXBTRS(7),PTRN1
         ED    $UXBTRS-1(8),WORK+4
         B     LINEDONE
OUTRANS  MVC   $UXBTRS(7),BLANKS  OUXB NOT AVAILABLE
*
******** MOVE LINE TO BUFFER
*
LINEDONE MVC   0(81,R1),LINE      MOVE THE LINE TO THE 3270 BUFFER.
         LH    R7,SCRSIZE         INCREMENT 'TPUT' SIZE
         LA    R7,81(R7)          ADD 81 TO CURRENT BUFFER LENGTH
         STH   R7,SCRSIZE         STORE AWAY AGAIN
         LA    R1,81(R1)          R1 = ADDR OF BOTTOM OF BUFFER
         C     R1,ENDBUF          HAVE WE REACHED THE BOTTOM?
         BE    PUTIT              YES, GO PUT THIS SCREEN.
NOGOOD   BCT   R3,ASCBLOOP        PROCESS NEXT ADDRESS SPACE
         DROP  R4,R5,R6
         EJECT
***********************************************************************
*                                                                     *
*              PUT THE SWAP BUFFER TO THE 3270 SCREEN                 *
*                                                                     *
***********************************************************************
PUTIT    CLC   JOBMASK(8),BLANKS  IS THE JOBMASK BLANK?
         BNE   CHKTYPE            NO, SO SKIP
         IC    R8,PAGE            LOAD CURRENT PAGE CHARACTER
         LA    R8,1(R8)           ADD ONE TO IT
         STC   R8,PAGE            STORE IT BACK
         CLC   SCRSIZE(2),=H'0'   IS THE SCREEN EMPTY?
*        BNE   CHKATTN            YES - GO DO THE SCREEN OVER     RGR
CHKTYPE  CLI   CRTFLAG,X'FF'      IS THIS A CRT?
         BE    CRT1               YES
         LA    R1,ERROR           R1 = ADDR OF FIRST LINE
         LA    R0,79              R0 = LENGTH OF HEADING
         ICM   R1,8,EDITFLG       EDIT MODE
         TPUT  (1),(0),R          WRITE LINE
         LA    R1,HEADINGA+1      R1 = ADDR OF FIRST LINE
         LA    R0,79              R0 = LENGTH OF HEADING
         ICM   R1,8,EDITFLG       EDIT MODE
         TPUT  (1),(0),R          WRITE LINE
         LH    R7,SCRSIZE         R7 = TOTAL SCREENSIZE
         LA    R8,81              R8 = ONE LINE
         L     R1,BUFAD           SET POINTER                       SP3
         LA    R1,2(,R1)              TO FIRST LINE
         ICM   R1,8,EDITFLG       EDIT MODE
         LA    R0,79              R0 LENGTH OF OUTPUT LINE
NEXTL    LR    R2,R1              SAVE R1 SINCE TPUT ZAPS IT
         TPUT  (1),(0),R          PRINT ONE LINE
         LA    R8,81(R8)          ADD 81 BYTES
         CR    R7,R8              HAVE WE PRINTED LAST LINE?
         BL    CONTA              YES, CONTINUE
         LA    R1,81(R2)          NOPE, POINT TO NEXT LINE
         LA    R0,79              LOAD LENGTH
         ICM   R1,8,EDITFLG       EDIT MODE
         B     NEXTL              PRINT NEXT LINE
CRT1     LA    R1,CLEAR           R1 = ADDR OF BUFFER
         LA    R0,LENGTH          R0 = LENGTH OF BUFFER
         AH    R0,SCRSIZE         R0 = FULL LENGTH OF BUFFER
         CLC   SCRSIZE,=H'0'      IS THE SCREEN EMPTY?              SP3
         BNE   CRT2                  NO, CONTINUE                   SP3
         L     R14,=A(NODATA)                                       SP3
         L     R15,BUFAD                                            SP3
         MVC   0(NODATAL,R15),0(R14)                                SP3
         AH    R0,=AL2(NODATAL)                                     SP3
CRT2     DS    0H                                                   SP3
         ICM   R1,8,FULLSCR       FULL SCREEN MODE
         TPUT  (1),(0),R          PUT SCREEN
CONTA    MVC   ERROR(27),BLANKS   BLANK OUT ERROR FIELD
         CLI   WAITSW,X'FF'       ARE WE IN WAIT MODE?
         BNE   GETCHAR            NO, READ FROM TERMINAL
***********************************************************************
*                                                                     *
*                             WAIT MODE                               *
*                                                                     *
***********************************************************************
         STIMER WAIT,BINTVL=DELAY YES, WAIT 1 SECOND
         L     R10,COUNTER        LOAD COUNTER
         BCTR  R10,0              COUNTER = COUNTER - 1
         ST    R10,COUNTER        STORE BACK
         CVD   R10,WORK           CONVERT TO DECIMAL.
         MVC   SCRATCH(4),PATTERN MOVE IN EDIT PATTERN
         ED    SCRATCH(4),WORK+6  EDIT IN TIME
         MVC   TIME(3),SCRATCH+1  MOVE IN TIME
         LTR   R10,R10            COUNTER = 0 ?
         BNZ   CONTINUE           REFRESH IF STILL COUNTING
         XI    WAITSW,X'FF'       COUNTER UP, DO READS AGAIN
         XOKSWAP                  SET UP AS SWAPPABLE              RGR
         MVC   TIME(3),BLANKS     CLEAR COUNTER FIELD
         B     CONTINUE           REFRESH ONE LAST TIME
GETCHAR  DS    0H                 'TGET' - WHAT NEXT FROM USER ?
         XC    REPLY0(06),REPLY0  CLEAR THE INPUT LINE              RGR
         MVC   REPLY(32),BLANKS   CLEAR THE INPUT LINE              RGR
         TGET  REPLY0,38,ASIS     GET 79 CHARACTERS FROM TERMINAL   RGR
         CH    R15,=H'12'              MORE INPUT?                  RGR
         BNE   NOCLEAR                    NO, SKIP CLEARQ           RGR
         TCLEARQ INPUT                                              RGR
NOCLEAR  DS    0H                                                   RGR
         MVC   PHEADINA(2),=X'1D40'    AND RESET MDT FLAG           RGR
         MVC   USERLINE(33),BLANKS     CLEAR USER LINE              RGR
         XR    R1,R1                   CLEAR REG.                   RGR
         IC    R1,REPLY0               SET UP FOR PFK               RGR
         N     R1,=XL4'0000000F'       OBTAIN NUMBER ONLY           RGR
         CH    R1,=XL2'000C'           CHECK FOR ENTER              RGR
         BH    ENTER                                                RGR
         MVC   REPLY(32),BLANKS        CLEAR THE REPLY AREA         RGR
         BCTR  R1,0                    KNOCK IT DOWN BY ONE         RGR
         SLL   R1,5                    TABLE OF 32 BYTE ENTRIES     RGR
         AL    R1,REPLYSA              POINT TO ENTRY               RGR
         MVC   REPLY(32),0(R1)         AND MOVE IT IN               RGR
ENTER    DS    0H                                                   RGR
*        CLI   REPLY,X'6E'        TEST FOR RESHOW (VTAM ONLY)
*        BE    CONTINUE
         CLI   REPLY,C' '         TEST FOR NO ENTRY
         BE    CONTINUE
         OC    REPLY(33),BLANKS   CONVERT TO UPPER CASE
         SPACE 3
***********************************************************************
*                                                                     *
*                       X  --  RESHOW PREVIOUS COMMAND                *
*                                                                     *
***********************************************************************
RESHOW   DS    0H                                                   RGR
         CLI   REPLY,C'X'         IS IT RESHOW?                     RGR
         BE    RESHOW0            YES, SET UP THE COMMAND           RGR
         MVC   REPLYSAV(32),REPLY NO, SAVE FOR NEXT TIME.           RGR
         B     WAITCHK            AND CONTINUE                      RGR
RESHOW0  DS    0H                                                   RGR
         MVC   USERLINE(32),REPLYSAV   YES, RESET                   RGR
         MVC   PHEADINA(2),=X'1DC1'    AND RESET MDT FLAG           RGR
         B     CONTINUE           AND GO ON                         RGR
***********************************************************************
*                                                                     *
*                       W  --  ENTER WAIT MODE                        *
*                                                                     *
***********************************************************************
WAITCHK  CLI   REPLY,C'W'         GO INTO WAIT MODE?
         BNE   CDELAY             NO
         XI    WAITSW,X'FF'       YES, TOGGLE WAIT SWITCH
         XNOSWAP                  SET UP NON-SWAPPABLE             RGR
         LA    R2,30              SET DEFAULT VALUE = 30
         LA    R15,CONVBIN        BRANCH TO CONVERSION RTN
         BALR  R14,R15            EBCDIC TO BINARY
         MVC   SCRATCH(4),PATTERN MOVE IN EDIT PATTERN
         ED    SCRATCH(4),WORK+6  EDIT IN TIME LEFT
         MVC   TIME(3),SCRATCH+1  MOVE TIME LEFT INTO PLACE
         ST    R2,COUNTER         STORE STARTING TIMER VALUE
         B     CONTINUE           ALL SET - GO DISPLAY NEXT PAGE
         SPACE 3
***********************************************************************
*                                                                     *
*            D  --  SET TIMER DELAY IN TENTHS OF A SECOND             *
*                                                                     *
***********************************************************************
CDELAY   CLI   REPLY,C'D'         ARE WE CHANGING THE TIME DELAY?
         BNE   ENDCHK             NO, SO CONTINUE
         LA    R2,10              SET DEFAULT VALUE = 10 TENTHS SECOND
         LA    R15,CONVBIN        BRANCH TO CONVERSION RTN
         BALR  R14,R15            EBCDIC TO BINARY
         C     R2,=F'10'          IS IT LESS THAN 10?               RGR
         BH    NO10               OK ALLOW IT                       RGR
         L     R2,=F'10'          MAKE IT 10                        RGR
         MVC   WORK+6(2),=X'010C'      AND TELL THEM ABOUT IT       RGR
NO10     MVC   SCRATCH(5),DPATTRN MOVE IN EDIT PATTERN
         ED    SCRATCH(5),WORK+6  EDIT IN DELAY TIME
         MVC   PAUSE(3),SCRATCH+2 MOVE TIME LEFT INTO PLACE
         MH    R2,=H'10'          CONVERT TO 100THS OF A SECOND
         ST    R2,DELAY           STORE WAIT DELAY VALUE
         B     CONTINUE           ALL SET - GO DISPLAY NEXT PAGE
         SPACE 3
***********************************************************************
*                                                                     *
*                          E  --  END SWAP                            *
*                                                                     *
***********************************************************************
ENDCHK   CLI   REPLY,C'E'         TERMINATION REQUESTED ?
         BE    STOP               ALL DONE
         SPACE 3
***********************************************************************
*                                                                     *
*                   T  --  TSO ADDRESS SPACES ONLY                    *
*                                                                     *
***********************************************************************
TSOCHK   CLI   REPLY,C'T'         TSO ONLY ?
         BNE   BATCHCHK           NO
         MVI   MODESW,X'FF'       INDICATE CHANGE OF MODES.
         MVI   TYPEREQ,TSOREQ     RESET THE BRANCHES                SP3
         MVI   MODETBA,C'T'       SET MODE INDICATOR
         B     CONTINUE
         SPACE 3
***********************************************************************
*                                                                     *
*                  B  --  BATCH ADDRESS SPACES ONLY                   *
*                                                                     *
***********************************************************************
BATCHCHK CLI   REPLY,C'B'         BATCH ONLY ?
         BNE   STCCHK             NO
         MVI   MODESW,X'FF'       INDICATE CHANGE OF MODES.
         MVI   TYPEREQ,BATCHREQ   RESET THE BRANCHES                SP3
         MVI   MODETBA,C'B'       SET MODE INDICATOR
         B     CONTINUE
         SPACE 3
***********************************************************************
*                                                                     *
*                  C  --  STARTED TASKS SPACES ONLY                   *
*                                                                     *
***********************************************************************
STCCHK   CLI   REPLY,C'C'         STC ONLY ?                        SP3
         BNE   ALLCHK             NO                                SP3
         MVI   MODESW,X'FF'       INDICATE CHANGE OF MODES.         SP3
         MVI   TYPEREQ,STCREQ     RESET THE BRANCHES                SP3
         MVI   MODETBA,C'C'       SET MODE INDICATOR                SP3
         B     CONTINUE                                             SP3
         SPACE 3
***********************************************************************
*                                                                     *
*             A  --  ALL ADDRESS SPACES (BATCH, TSO, AND STC)         *
*                                                                     *
***********************************************************************
ALLCHK   CLI   REPLY,C'A'         BATCH, TSO, AND STC?              SP3
         BNE   OUTCHK             NO                                SP3
         MVI   MODESW,X'FF'       INDICATE CHANGE OF MODES.         SP3
         MVI   TYPEREQ,0          CLEAR IT OUT                      SP3
         OI    TYPEREQ,BATCHREQ   RESET THE BRANCHES                SP3
         OI    TYPEREQ,STCREQ     RESET THE BRANCHES                SP3
         OI    TYPEREQ,TSOREQ     RESET THE BRANCHES                SP3
         MVI   MODETBA,C'A'       SET MODE INDICATOR                SP3
         B     CONTINUE                                             SP3
         SPACE 3
***********************************************************************
*                                                                     *
*        O  --  ADDRESS SPACES WHICH ARE 'OUT' AND NOT READY          *
*                                                                     *
***********************************************************************
OUTCHK   CLI   REPLY,C'O'         OUT MODE ?
         BNE   INCHK              NO
         MVI   MODESW,X'FF'       INDICATE CHANGE OF MODES.
         MVI   INOROUT+1,X'00'    NOP THE BRANCH
         MVI   MODEIO,C'O'        SET MODE INDICATOR
         B     CONTINUE
         SPACE 3
***********************************************************************
*                                                                     *
*             I  --  ADDRESS SPACES WHICH ARE SWAPPED IN              *
*                                                                     *
***********************************************************************
INCHK    CLI   REPLY,C'I'         IN OR READY MODE ?
         BNE   FINDCHK            NO
         MVI   INOROUT+1,X'10'    BRANCH
         MVI   MODESW,X'FF'       INDICATE MODE SWITCH.
         MVI   MODEIO,C'I'        SET MODE INDICATOR
         B     CONTINUE
         SPACE 3
***********************************************************************
*                                                                     *
*                F  --  DISPLAY ONLY CERTAIN JOBNAMES                 *
*                                                                     *
***********************************************************************
FINDCHK  CLI   REPLY,C'F'         SHOULD WE SET THE JOBMASK?
         BNE   SRMCHK             NO
         MVI   MODESW,X'FF'       INDICATE MODE SWITCH.
         MVC   JOBMASK(8),REPLY+1 MOVE MASK INTO PLACE
         MVC   MASKNAME(8),JOBMASK MOVE MASK INDICATOR INTO PLACE
         CLC   JOBMASK(8),BLANKS  IS JOBMASK BLANK?
         BNE   CONTINUE           NO, GO ON
         MVC   MASKNAME(8),=C'  PAGE 1'
         B     CONTINUE
         SPACE 3
***********************************************************************
*                                                                     *
*                      S  --  SRM DISPLAY MODE                        *
*                                                                     *
***********************************************************************
SRMCHK   CLI   REPLY,C'S'         SRM MODE ?
         BNE   GENCHK             NO
         MVI   SRMSW,X'FF'        INDICATE SRM MODE
         MVC   HEADINGA(80),HEADING1    MOVE IN SRM HEADING
         MVC   MODEGS(7),=C'S.R.M. '   SET MODE INDICATOR
         B     CONTINUE
         SPACE 3
***********************************************************************
*                                                                     *
*                    G  --  GENERAL DISPLAY MODE                      *
*                                                                     *
***********************************************************************
GENCHK   CLI   REPLY,C'G'         GENERAL MODE ?
         BNE   LINKSPY            NO
         MVI   SRMSW,X'00'        INDICATE GENERAL MODE
         MVC   HEADINGA(80),HEADING2    MOVE IN GENERAL HEADING
*        MVC   MODEGS(7),=C'GENERAL'  SET MODE INDICATOR            RGR
         MVC   MODEGS(7),=X'C7859585998193'                         RGR
         B     CONTINUE
         SPACE 3
***********************************************************************
*                                                                     *
*                         L  --  LINK TO SPY                          *
*                                                                     *
***********************************************************************
LINKSPY  CLI   REPLY,C'L'         XCTL TO SPY?
         BNE   NEEDHELP           NO
         L     R13,SAVE+4         CALLERS SAVE AREA POINTER.
         L     R2,SWAPD           INDICATE WE CAME FROM SWAP   RGR
         B     SWAPD1                                          RGR
SWAPD0   DS    0F                                              RGR
SWAPD    DC    CL4'SWAP'                                       RGR
SWAPD1   DS    0H                                              RGR
         XCTL  (3,12),EP=SPY      XCTL TO SPY
         SPACE 3
***********************************************************************
*                                                                     *
*               ?  --  DISPLAY HELP FOR SWAP COMMANDS                 *
*                                                                     *
***********************************************************************
NEEDHELP CLI   REPLY,C'?'         USER NEEDS HELP PAGE DISPLAYED?
         BNE   BADCMD
         L     R1,HLP1ADDR        R1 = ADDR OF HELP
         LA    R0,HELPLEN1        R0 = LENGTH OF HELP
         ICM   R1,8,FULLSCR       FULL SCREEN MODE
         TPUT  (1),(0),R          WRITE HELP TO SCREEN
         TGET  REPLY0,6           GO GET A COMMAND
         CH    R15,=H'12'                                           RGR
         BNE   NEED0                                                RGR
         TCLEARQ INPUT                                              RGR
NEED0    DS    0H                                                   RGR
         L     R1,HLP2ADDR        R1 = ADDR OF HELP
         LA    R0,HELPLEN2        R0 = LENGTH OF HELP
         ICM   R1,8,FULLSCR       FULL SCREEN MODE
         TPUT  (1),(0),R          WRITE HELP TO SCREEN
         TGET  REPLY0,6           GO GET A COMMAND
         CH    R15,=H'12'                                           RGR
         BNE   NEED1                                                RGR
         TCLEARQ INPUT                                              RGR
NEED1    DS    0H                                                   RGR
         L     R1,HLP3ADDR        R1 = ADDR OF HELP
         LA    R0,HELPLEN3        R0 = LENGTH OF HELP
         ICM   R1,8,FULLSCR       FULL SCREEN MODE
         TPUT  (1),(0),R          WRITE HELP TO SCREEN
         B     GETCHAR            GO GET A COMMAND
         SPACE 3
***********************************************************************
*                                                                     *
*                     BLANK OR INVALID COMMANDS                       *
*                                                                     *
***********************************************************************
BADCMD   CLI   REPLY,C' '         WAS A BLANK JUST ENTERED?
         BE    CONTINUE
         MVC   ERROR(27),ERRMSG2  MOVE IN BAD COMMAND MSG
CONTINUE MVC   SCRSIZE,=H'0'      RESET SCREEN SIZE.
         L     R1,BUFAD           ---+                              SP3
         L     R6,BUFAD                                            SP3
         ICM   R6,8,C' '              MOVE CHARACTER LONG TO FILL
         LA    R7,22*81               SCREEN BUFFER WITH BLANKS
         L     R8,BUFAD                                            SP3
         SR    R9,R9                 
         MVCL  R6,R8              ---+
         CLI   MODESW,X'FF'       CHANGE OF MODES ?
         BNE   MODEOK             BRANCH IF SAME MODE.
         MVI   MODESW,X'00'       RESET MODE CHANGE.
         B     FINDCVT            START OVER FROM BEGINNING.
MODEOK   DS    0H
*        IGNORES ATTN ON AN EXACTLY FULL SCREEN W/O THIS FIX.
*                     RGR  09/24/81
*        LTR   R3,R3         *** OLD TEST ***
         LR    R0,R3         R3 = # MEMORIES TO PROCESS + 1      RGR
         BCTR  R0,R0         R0 = # MEMORIES TO PROCESS          RGR
         LTR   R0,R0              ANY MORE MEMORIES TO PROCESS?  RGR
         BP    NOGOOD             YES.                           RGR
CHKATTN  DS    0H                 TEST FOR AN ATTENTION          RGR
         CLI   ATTNFLG,X'00'      WAS ATTN HIT?
         BE    FINDCVT            NO
         MVI   ATTNFLG,X'00'      YES, RESET FLAG
         XOKSWAP                  CHANGE TO SWAPPABLE            RGR
         MVC   TIME(3),BLANKS     BLANK OUT TIMER FIELD
         MVI   WAITSW,X'00'       TURN OFF WAIT FLAG
         LA    R3,0               R3 = 0
         ST    R3,COUNTER         SET TIMER TO 0
         B     FINDCVT            OTHERWISE, START OVER FROM THE TOP.
STOP     CLI   CRTFLAG,X'00'      IS THIS A CRT?
         BE    STOP1              NO; DON'T CLEAR
         LA    R1,CLEAR           R1 = ADDR OF CLEAR STRING
         LA    R0,CLEARLEN        R0 = LENGTH OF CLEAR STRING
         ICM   R1,8,FULLSCR       FULL SCREEN MODE
         TPUT  (1),(0),R          CLEAR THE SCREEN BEFORE WE LEAVE
         STFSMODE OFF             TURN OFF FULLSCREEN MODE
STOP1    L     R13,SAVE+4
         RETURN (14,12),RC=0
VARCLC1  CLC   JOBMASK(0),JOB
         EJECT
***********************************************************************
*                                                                     *
*            CONVERT EBCDIC NUMBERS FROM USER INTO BINARY             *
*                                                                     *
***********************************************************************
CONVBIN  CVD   R2,WORK            CONVERT TO DECIMAL.
         CLI   REPLY+1,C' '       DID HE ENTER A NUMBER?
         BE    RTRN               NO, USE THE DEFAULT
         CLI   REPLY+1,C'0'       IS THE HEX CODE < 'F0' ?
         BL    BADCHAR            YES, ERROR
         CLI   REPLY+1,C'9'       IS THE HEX CODE > 'F9' ?
         BH    BADCHAR            YES, ERROR
         PACK  WORK(8),REPLY+1(1) PACK EBCDIC (ASSUME 1 DIGIT)
         CLI   REPLY+2,C' '       DID HE ENTER 2 DIGITS?
         BE    CVB                NO, DONT DO THE 2 DIGIT PACK
         CLI   REPLY+2,C'0'       IS THE HEX CODE < 'F0' ?
         BL    BADCHAR            YES, ERROR
         CLI   REPLY+2,C'9'       IS THE HEX CODE > 'F9' ?
         BH    BADCHAR            YES, ERROR
         PACK  WORK(8),REPLY+1(2) PACK AGAIN, WITH 2 DIGITS THIS TIME
CVB      CVB   R2,WORK            GET BINARY
RTRN     BR    R14                RETURN TO MAINLINE
BADCHAR  MVC   ERROR(27),ERRMSG1  NUMERIC ERROR
         B     RTRN
         DROP  R12
         EJECT
***********************************************************************
*                                                                     *
*                          A T T N E X I T                            *
*                                                                     *
*         TRAP USERS ATTENTION INTERRUPTS AND FLAG FOR RESET          *
*                                                                     *
***********************************************************************
ATTNEXIT SAVE  (14,12)            SAVE THE CALLERS REGISTERS
         LR    R7,R15             ESTABLISH
         USING ATTNEXIT,R7        ADDRESSABILITY.
         LA    R11,SAVEA          SET-UP
         ST    R13,SAVEA+4         SAVE
         ST    R11,8(,R13)          AREA
         LR    R13,R11               CHAINING
         MVI   ATTNFLG,X'FF'      SET ATTN FLAG
         L     R13,SAVEA+4        CALLERS SAVE AREA POINTER.
         RETURN (14,12),RC=0      AND AWAY WE GO...
SAVEA    DS    18F
         DROP  R7
         EJECT
***********************************************************************
*                                                                     *
*                             CONSTANTS                               *
*                                                                     *
***********************************************************************
STAXLIST STAX  ATTNEXIT,MF=L
PACK     DS    D
WORK     DS    D
SCRATCH  DS    D                  SCRATCH AREA
ASCBADDR DS    F                  ASCB ADDR SAVE AREA
COUNTER  DC    F'0'               WAIT COUNTER
TRANSSEC DS    F                  TRANSACTION TIME IN SECONDS
DELAY    DC    F'100'             2 SECOND DELAY FOR REFRESH
REPLYSA  DC    A(REPLYS)
HLP1ADDR DC    A(HELP1)
HLP2ADDR DC    A(HELP2)
HLP3ADDR DC    A(HELP3)
BUFAD    DC    A(BUFFER)                                            SP3
ENDBUF   DC    A(0)
SCRSIZE  DC    H'0'
TYPEREQ  DC    AL1(BATCHREQ)      START WITH BATCH                  SP3
BATCHREQ EQU   B'10000000'                                          SP3
TSOREQ   EQU   B'01000000'                                          SP3
STCREQ   EQU   B'00100000'                                          SP3
ATTNFLG  DC    X'00'              ATTENTION FLAG
WAITSW   DC    X'00'              WAIT MODE
MODESW   DC    X'00'              MODE CHANGE.
SRMSW    DC    X'00'              SRM  MODE.
PTRN1    DC    X'20202020202120'  EDIT PATTERN
PTRN2    DC    X'202021204B202020'
PTRN3    DC    X'2020202020202120'
PATTERN  DC    X'40202020'        EDIT PATTERN
DPATTRN  DC    X'4021204B20'      EDIT PATTERN
TEMP     DC    CL6'      '        FOR MEM EDIT
REPLYSAV DC    CL32' '                                              RGR
BLANKS   DC    CL40' '
REPLY0   DC    XL06'00'                                             RGR
REPLY    DC    CL33' '
CODETABL DC    C'O'  SWAP CODE 1 : TERMINAL OUTPUT WAIT
         DC    C'I'  SWAP CODE 2 : TERMINAL INPUT WAIT
         DC    C'W'  SWAP CODE 3 : LONG WAIT
         DC    C'A'  SWAP CODE 4 : AUXILIARY STORAGE SHORTAGE
         DC    C'R'  SWAP CODE 5 : REAL STORAGE SHORTAGE
         DC    C'V'  SWAP CODE 6 : MS0 DETECTED WAIT
         DC    C'S'  SWAP CODE 7 : REQSWAP SYSEVENT ISSUED
         DC    C'E'  SWAP CODE 8 : ENQHOLD EXCHG BY SWAP ANALYSIS
         DC    C'X'  SWAP CODE 9 : EXCHG RECOMMENDED BY SWAP ANALYSIS
         DC    C'$'  SWAP CODE A : UNILATERAL SWAPOUT
HEX      DC    C'0123456789ABCDEF'
START    DC    C'Starting'
         EJECT
***********************************************************************
*                                                                     *
*                        FORMAT DISPLAY LINE                          *
*                                                                     *
***********************************************************************
X        DC    CL80' '
         ORG   X                  BYTE
LINE     DC    X'1D60'            0            PROTECTED, LOW INTENSITY
JOB      DC    CL8' '             2
         DC    C' '               10
         ORG   X+11               FOR GENERAL LISTING
PROCSTEP DC    CL8' '             11
         DC    C' '               19
STEPNAME DC    CL8' '             20
         DC    C' '               28
CPU      DC    CL8' '             29
         DC    C' '               37
         ORG   X+11               FOR SRM LISTING
$UCBPGN  DC    CL2' '             11                                RGR
         DC    C' '               13                                RGR
$UCBPGP  DC    C' '               14                                RGR
         DC    C' '               15                                RGR
$UCBDOM  DC    CL2' '             16                                RGR
         DC    C' '               18
DP       DC    CL2' '             19
         DC    C' '               21
DQ       DC    CL2' '             22
         DC    C' '               24                                RGR
SERATE   DC    CL5' '             25                                RGR
         DC    C' '               30
$UCBWMS  DC    CL8' '             31
         DC    C' '               39
         ORG   X+40               FOR GENERAL AND SRM LISTINGS
TCPU     DC    CL8' '             40
         DC    C' '               48
$UCBSWC  DC    CL2' '             49
         DC    C' '               51
STATUS   DC    C' '               52
         DC    C' '               53
LOC      DC    C' '               54
         DC    C' '               55
MEM      DC    CL3' '             56
         DC    C' '               59
         ORG   X+60               FOR SRM LISTING
$UCBWSS  DC    CL3' '             60
         DC    C' '               63
IOC      DC    CL5' '             64
         DC    C' '               69
TSLS     DC    CL5' '             70
         DC    C' '               75                                RGR
$UCBRMR  DC    CL2' '             76                                RGR
         DC    C' '               78                                RGR
$UCBWMR  DC    CL2' '             79                                RGR
         ORG   X+60               FOR GENERAL LISTING
HH       DC    CL2' '             60
         DC    C':'               62
MM       DC    CL2' '             63
         DC    C':'               65
SS       DC    CL2' '             66
         DC    C' '               68
MSL      DC    C' '               69
         DC    C' '               70
$UXBTRS  DC    CL7' '             71
         DC    C' '               78
TCBA     DC    CL1' '             79
         DC    C' '               80
         ORG X+90
FULLSCR  DC    X'03'
EDITFLG  DC    X'00'
CRTFLAG  DC    X'FF'
JOBMASK  DC    CL8'        '
ERRMSG1  DC    CL27' ERROR - Non-numeric value '
ERRMSG2  DC    CL27' ERROR - Invalid command   '
         LTORG
HEADING1 DC CL80' Jobname  PG P DM DP DQ  SU/S    S.U.     TCPU  SC R LX
                MEM WSS   I/O  TSLS RM WS '                         RGR
HEADING2 DC CL80' Jobname  Procstep Stepname    CPU        TCPU  SC R LX
                MEM HH:MM:SS MSL S.U. TCB '
CLEAR    DC    X'27F1C3'           ESC;WRITE;WCC                  RGR
         DC    X'115D7F'           SBA TO ROW 24, COL 80 (FSE 5.0)
         DC    X'114040'           SBA TO ROW 1, COL 1
         DC    X'3C404000'         FILL SCREEN WITH NULLS
PHEADING DC    X'114040'           SBA TO ROW 1, COL 1
PHEADINA DC    X'1D40'             ATTR BYTE - UNPROTECTED, LOW INTENS.
         DC    X'13'               INSERT CURSOR
CLEARLEN EQU   *-CLEAR                                              SP3
USERLINE DC    C'   '
ERROR    DC    CL30' '
CMDCTRL1 DC    X'1DE8'             ATTR BYTE - PROTECTED, HI INTENS.
         DC    CL7'Timer: '
TIME     DC    CL3'   '
SLASH    DC    CL1'/'
PAUSE    DC    CL3'1.0'
         DC    CL5'     '
         DC    CL6'Mode: '
MODETBA  DC    CL1'B'
MODEIO   DC    CL2'O '
MODEGS   DC    CL9'General  '
MASKNAME DC    CL7'  Page '
PAGE     DC    CL1'1'
HEADINGA DS    CL80' '
BUFFER   DC    41CL81' '
LENGTH   EQU   BUFFER-CLEAR
NODATA   DC    X'11C260',C'   NO DATA IS AVAILABLE.'                SP3
NODATAL  EQU   *-NODATA                                             SP3
         EJECT
REPLYS   DC    CL32'?                               '       PFK  1  RGR
         DC    CL32'LINK                            '       PFK  2  RGR
         DC    CL32'END                             '       PFK  3  RGR
         DC    CL32'W0                              '       PFK  4  RGR
         DC    CL32'SRM                             '       PFK  5  RGR
         DC    CL32'GENERAL                         '       PFK  6  RGR
         DC    CL32'IN                              '       PFK  7  RGR
         DC    CL32'OUT                             '       PFK  8  RGR
         DC    CL32'X                               '       PFK  9  RGR
         DC    CL32'ALL                             '       PFK 10  RGR
         DC    CL32'                                '       PFK 11  RGR
         DC    CL32'                                '       PFK 12  RGR
         EJECT
HELP1    DC    X'C1'               WCC
         DC    X'115D7F'           SBA TO ROW 24, COL 80 (FSE 5.0)
         DC    X'114040'           SBA TO ROW 1, COL 1
         DC    X'3C404000'         FILL SCREEN WITH NULLS
         DC    X'114040',X'1DE8',C'Command      Description'
         DC    X'11C150'
         DC    X'11C260',C'   A         Display Started tasks, batch '
         DC    C'and TSO'
         DC    X'11C3F0',C'   B         Display Batch only'
         DC    X'11C540',C'   C         Display Started Tasks only' SP3
         DC    X'11C650',C'   DXX       Set delay to XX tenths seconds'
         DC    X'11C760',C'   E         End SWAP'
         DC    X'11C8F0',C'   F         Display all Jobnames'
         DC    X'114A40',C'   FXXXXXXXX Display only Job ''XXXXXXXX'''
         DC    X'114B50',C'   G         General Information Will be '
         DC    C'displayed'
         DC    X'114C60',C'   I         Display only swapped-in '
         DC    C'memories'
         DC    X'114DF0',C'   O         Display both in and out '
         DC    C'memories'
         DC    X'114F40',C'   L         LINK to program SPY '
         DC    C'(if available)'
         DC    X'115050',C'   S         SRM information displayed'
         DC    X'11D160',C'   T         Display TSO jobs only'
         DC    X'11D2F0',C'   W         Start timer mode for 30 '
         DC    C'seconds'
         DC    X'11D440',C'   WXX       Start timer mode for XX '
         DC    C'seconds'
         DC    X'11D550',C'   W0        Start timer mode until '
         DC    C'interrupt'
         DC    X'11D660',C'   ?         Display this page'
         DC    X'11D7F0'
         DC    X'11D940',C'Hitting INTERRUPT will stop the wait timer'
         DC    X'115A50',C'*WARNING*  INTERRUPT may be ignored if '
         DC    C'timer is set too fast (< .4 sec)'
         DC    X'115B60'
         DC    X'115CF0',C'Hit ENTER to continue'
         DC    X'115DC61D40134040401DE8'
HELPMRK1 EQU   *
HELPLEN1 EQU   HELPMRK1-HELP1
         EJECT
HELP2    DC    X'C1'               WCC
         DC    X'115D7F'           SBA TO ROW 24, COL 80 (FSE 5.0)
         DC    X'114040'           SBA TO ROW 1, COL 1
         DC    X'3C404000'         FILL SCREEN WITH NULLS
         DC    X'114040',X'1DE8',C'Swap Code    Description'
         DC    X'11C150'
         DC    X'11C260',C'   A       Auxilary storage shortage'
         DC    X'11C3F0',C'   E       ENQHOLD exchange'
         DC    X'11C540',C'   I       Terminal input wait'
         DC    X'11C650',C'   O       Terminal output wait'
         DC    X'11C760',C'   R       Real storage shortage'
         DC    X'11C8F0',C'   S       REQSWAP SYSEVENT issued'
         DC    X'114A40',C'   V       MS0 detected wait'
         DC    X'114B50',C'   W       Long wait'
         DC    X'114C60',C'   X       Exchange swap'
         DC    X'114DF0',C'   $       Unilateral swapout'
         DC    X'114F40',C'   ?       Unknown'
         DC    X'115050'
         DC    X'11D160',C'Loc. Code   Description'
         DC    X'11D2F0'
         DC    X'11D440',C'    I      Swapped in and eligible to run'
         DC    X'11D550',C'    O      Swapped out but ready to run'
         DC    X'11D660',C'    W      Swapped out and not ready to run'
         DC    X'11D7F0',C'    $      Swapped in: V=R or non-swappable'
         DC    X'11D940',C'    ?      In transition between states'
         DC    X'115CF0',C'Hit ENTER to continue'
         DC    X'115DC61D40134040401DE8'
HELPMRK2 EQU   *
HELPLEN2 EQU   HELPMRK2-HELP2
HELP3    DC    X'C1'               WCC
         DC    X'115D7F'           SBA TO ROW 24, COL 80 (FSE 5.0)
         DC    X'114040'           SBA TO ROW 1, COL 1
         DC    X'3C404000'         FILL SCREEN WITH NULLS
 DC    X'114040',X'1DE8',C'  PFK #  -   COMMAND'
         DC    X'11C150'
         DC    X'11C260',C'   1     -   ?'
         DC    X'11C3F0',C'   2     -   LINK'
         DC    X'11C540',C'   3     -   END'
         DC    X'11C650',C'   4     -   W0'
         DC    X'11C760',C'   5     -   SRM'
         DC    X'11C8F0',C'   6     -   GENERAL'
         DC    X'114A40',C'   7     -   IN'
         DC    X'114B50',C'   8     -   OUT'
         DC    X'114C60',C'   9     -   **RESHOW**'
         DC    X'114DF0',C'  10     -   ALL'
         DC    X'114F40',C'  11     -   **UNDEFINED**'
         DC    X'115050',C'  12     -   **UNDEFINED**'
         DC    X'115CF0',C'Hit ENTER to continue'
         DC    X'115DC61D40134040401DE8'
HELPMRK3 EQU   *
HELPLEN3 EQU   HELPMRK3-HELP3
         EJECT
         CVT   DSECT=YES
         IRAOUCB
         IHAASCB
         IHAASVT
         IHAOUXB
         IKJTSB                                                     RGR
         END   SWAP
