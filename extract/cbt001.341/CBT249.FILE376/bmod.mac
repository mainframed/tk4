         MACRO
&NAME    BMOD  &REGS,&TYPE,&PARM=(1),&PUSHPOP=
         GBLC  &MODCS,&MODVN,&MODRVA,&MODRV1,&MODRV2,&MODBR,&RNBNM
         GBLB  &MODRENT,&MODTASK,&MODFE,&MODPP
         GBLB  &MODPREN,&MODXREN    2/01/76 RENT=P,RENT=(R)
         GBLA  &MODRVN
         GBLB  &MODLEAF,&MODPMOD   8/26/76  NOTE BMOD .L OR .P
         GBLB  &MODCMOD    12/2/76  NOTE  BMOD ,C
.*   THIS MACRO IS PART OF A  BAL STRUCTURED PROGRAMMING PACKAGE
.*    WRITTEN BY  GARY S. NEMETH  OF THE CLEVELAND TRUST COMPANY
.*     DURING 1976.  ALL RIGHTS RESERVED.  ANY COMMERCIAL USE
.*      MUST BE APPROVED BY THE CLEVELAND TRUST COMPANY.
.* THIS MACRO MODIFIED AT RAINIER NATIONAL BANK
.*       &TYPE TELLS THE SCOPE OF THE BMOD
.*       ,P    PRIMITIVE, NO REG SAVED/RESTORED, R14 NOT SAVED/RESTORED
.*       R,L   LEAF. CONTAINS NO CALLM MACROS. THE DATA AREAS FOR
.*       ,C    CONSTANTS   MUST NOT BE NAMED
.*                ALL LEAF BMODS WILL OVERLAP REG SAVE AREAS
         AIF   (&MODFE).ERR1            BMOD BEFORE FIRST EMOD
         AIF   ('&MODRVA' NE '').ERR2   BMOD BEFORE ANOTHER EMOD
&RNBNM   CSECT
         AIF   ('&TYPE' EQ 'C').CONST
         AIF   ('&TYPE' EQ 'P' AND '&REGS' EQ '').PRIMTIV
         AIF   (&MODPREN OR &MODXREN).ERR4
&MODLEAF SETB  ('&TYPE' EQ 'L' OR '&TYPE' EQ 'P')   LEAF ? NO CALLMS.
         AIF   (&MODLEAF  OR '&TYPE' EQ '').TYPEOK
         MNOTE 8,'BMOD TYPE IS  NEITHER L  NOR P.  ASSUMING OMITTED.'
.TYPEOK  AIF   (&MODLEAF).LEAFRVA   LEAF BMOD ?
&MODRVA  SETC  'V$'.'&SYSNDX'           NAME OF LOCAL REG SAVE AREA
         AGO   .LEAF2               GO STORE REGISTERS
.LEAFRVA ANOP   ,
&MODRVA  SETC  '&MODCS.F'           REG SAVE AREA BUILT BY ENDSECT
.LEAF2   ANOP   ,
&NAME    ST    14,&MODRVA          SAVE THE RETURN ADDRESS
&MODRVN  SETA  1                   ONE FULLWORD, FOR R14
&MODRV1  SETC  ''                  NO WORK REGS
         AIF   ('&REGS' EQ '').NREGS
         AIF   (T'&REGS(1) NE 'N').ERR3      IMPROPER
         AIF   ('&REGS(2)' NE '').REGS2
&MODRVN  SETA  2
&MODRV1  SETC  '&REGS(1)'
&MODRV2  SETC  ''
         ST    &MODRV1,&MODRVA+4
         AGO   .NREGS
.REGS2   AIF   (T'&REGS(2) NE 'N').ERR3
         AIF   (&REGS(2) LT &REGS(1)).ERR3
&MODRVN  SETA  2+&REGS(2)-&REGS(1)
&MODRV1  SETC  '&REGS(1)'
&MODRV2  SETC  '&REGS(2)'
         STM   &MODRV1,&MODRV2,&MODRVA+4
.NREGS   INREG (1),REG=&PARM,R=YES
         AGO   .PP
.PRIMTIV ANOP
&MODPMOD SETB  (1)
&NAME    INREG (1),REG=&PARM,R=YES
.PP      AIF   ('&PUSHPOP' EQ '' OR '&PUSHPOP' EQ 'NO').PPN
         AIF   ('&PUSHPOP' EQ 'YES').PP2
         MNOTE 4,'PUSHPOP=&PUSHPOP IS INVALID. ASSUMING YES'
.PP2     ANOP
&MODPP   SETB  (1)                 TURN ON PUSHPOP=YES TO EMOD
         PUSH  USING               STASH USING STATE
         DROP  ,                   DROP EVERYTHING IN SIGHT
         MODUSNG  MF=GENUSING
.PPN     MEXIT
.*             * * *     CONSTANT BMOD      ,C    T Y P E   EQ  C
.CONST   ANOP
         AIF   ('&NAME' EQ '').CONST2
         MNOTE *,'A NAME IS NOT VALID FOR CONSTANT BMOD, &NAME IGNORED'
.CONST2  AIF   ('&REGS' EQ '' AND '&PARM' EQ '(1)').CONST4
         MNOTE 8,'REGISTER PARAMETERS ARE NOT VALID FOR CONSTANT BMOD'
.CONST4  AIF   ('&PUSHPOP' EQ '').CONST6
         MNOTE *,'PUSHPOP=&PUSHPOP IS IGNORED FOR CONSTANT BMOD'
.CONST6  ANOP  ,
&MODCMOD SETB  (1)            INDICATE  BMOD ,C TO  EMOD MACRO
         MEXIT
.ERR1    ANOP
.ERR2    MNOTE 12,'ERROR- A BMOD MACRO CAN ONLY FOLLOW AN EMOD MACRO'
&NAME    DC   2H'0'
         MEXIT
.ERR3    MNOTE 8,'IMPROPER REGISTER PARAMETER'
         DC   2H'0'                     CRASH
         MEXIT
.ERR4    MNOTE 38,'ERROR- FOR YOUR BGNSECT RENT=, BMODS ARE NO NO S'
         DC    2H'0'        CRASH
         MEND
