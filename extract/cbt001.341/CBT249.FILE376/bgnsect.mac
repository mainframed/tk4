         MACRO
&NAME    BGNSECT  &RENT=,&BASE=(10),&PARM=(1),&USING=,&GENMAC=,&C=
         GBLC  &MODCS,&MODVN,&MODBR,&RNBNM
         GBLB  &MODRENT,&MODTASK,&MODFE
         GBLB  &MODPREN,&MODXREN    2/01/76 RENT=P,RENT=(R)
         GBLB  &MODTRM        INDICATE TRACEMNTR(TRUE) ELSE OS MACROS
         GBLC  &CALLPLA            12/10/76   DEFAULT CALLX PARAM LIST
         GBLA  &CALLPLN            12/10/76   MAX WORD COUNT OF CALLXPL
         LCLA  &RNBA                     ***RNB  TGA
         LCLC  &RNBCOM                   ***RNB  TGA
         AIF   ('&MODCS' NE '').ERR1
.*   THIS MACRO IS PART OF A  BAL STRUCTURED PROGRAMMING PACKAGE
.*    WRITTEN BY  GARY S. NEMETH  OF THE CLEVELAND TRUST COMPANY
.*     DURING 1976.  ALL RIGHTS RESERVED.  ANY COMMERCIAL USE
.*      MUST BE APPROVED BY THE CLEVELAND TRUST COMPANY.
.* THIS MACRO MODIFIED AT RAINIER NATIONAL BANK
.RECERR1 ANOP  ,                           RECOVERY
&MODRENT SETB  ('&RENT' EQ 'YES' OR '&RENT' EQ 'Y')
&MODPREN SETB  ('&RENT' EQ 'PRIMITIVE' OR '&RENT' EQ 'P')
&MODXREN SETB  ('&RENT'(1,1) EQ '(')
         AIF   (NOT(&MODRENT OR &MODPREN OR &MODXREN                   X
               OR '&RENT' EQ 'NO' OR '&RENT' EQ 'N')).ERR2
.RECRENT ANOP  ,
         AIF   ('&GENMAC' NE 'OS').GM2
&MODTRM  SETB  (0)                 NOT TRACEMNTR,  TURN OFF INDICATOR
         AGO   .GMX
.GM2     AIF   ('&GENMAC' EQ '').GMX
         AGO   .ERR3
.GMX     ANOP
&RNBNM   SETC  '&NAME'                             ***RNB  TGA
&MODCS   SETC  '&NAME'
&MODCS   SETC  '&MODCS'(1,7)             MOD       ***RNB  TGA
&MODFE   SETB  (1)
&MODBR   SETC  '&BASE(1)'
         AIF   (&MODPREN OR &MODXREN).NOV
&MODVN   SETC  '&MODCS'.'V'
&CALLPLA SETC  '&MODCS'.'P'        LABEL FOR DEFAULT CALLXPL
&CALLPLN SETA  0                   INITIALLY NOT NEEDED, ZERO WORDS LNG
.NOV     ANOP
.*                                                CSECT ENTRY
         AIF   (NOT &MODTRM).NOR12
         USING TRACEMON,R12
         MODUSNG  TRACEMON,(12),MF=INSERT
.NOR12   ANOP
&RNBCOM  SETC  '&C'                                  ***RNB  TGA
&RNBA    SETA  K'&RNBCOM                             ***RNB  TGA
         AIF   (&RNBA EQ 0).RNB2                     ***RNB  TGA
         AIF   ('&C'(1,1) NE '''').RNB2              ***RNB  TGA
&RNBA    SETA  &RNBA-2                               ***RNB  TGA
&RNBCOM  SETC  '&RNBCOM'(2,&RNBA)                    ***RNB  TGA
.RNB2    ANOP                                        ***RNB  TGA
&RNBNM   CSECT
         USING &RNBNM,15
         B     &MODCS.B
         DC    AL1(&MODCS.B-*-1),C'&RNBNM &SYSDATE &SYSTIME &RNBCOM'
&MODCS.B STM   14,12,12(13)
.*                                                BASE REG
         INREG (15),REG=&BASE
         DROP  15
         USING &RNBNM,&MODBR
         MODUSNG  &RNBNM,(&MODBR),MF=INSERT
         MODUSNG  USING=&USING,MF=SETUSING
.*                                                SAVE AREA
         AIF   (&MODPREN).NSVA
         AIF   (&MODRENT).RENT
         AIF   (&MODXREN).XREN
         LA    15,&MODVN           OUR SVA
         AGO   .XRENN
.XREN    LR    15,&RENT(1)         OUR SVA, PASSED TO US BY CALLER
.XRENN   ANOP
         ST    15,8(,13)
         ST    13,4(,15)           SAVE OUR HSA
         LR    13,15
         AGO   .NSVA
.RENT    AIF   (NOT &MODTRM).RENTOS
         XGM   &MODCS.L,TYPE=SVA   GETMAIN AND CHAIN SAVE AREAS
         AGO   .RENTC
.RENTOS  ANOP
         LR    2,13                TEMP STASH  OF HIGH SAVE AREA
.*       LA    0,&MODCS.L          LENGTH OF STORAGE REQUEST
.*       L     R0,&MODCS.#             **** RNB
         GETMAIN R,LV=&MODCS.L
         LR    13,1                NEW SAVE AREA  + WORK AREA
         ST    13,8(,2)            CHAIN
         ST    2,4(,13)            CHAIN
         LM    0,2,20(2)           RESTORE LINKAGE REGISTERS
.RENTC   ANOP
         USING &MODVN,R13          DYNAMIC WORK AREA ADDRESSABILITY
         MODUSNG  &MODVN,(13),MF=INSERT
.NSVA    INREG (1),REG=&PARM       SET PARM REGISTER
         MEXIT
.ERR1    MNOTE 12,'IMPROPER CSECT STRUCTURING, MAYBE NO ENDSECT MACRO'
         MNOTE *,'TEMPORARY RECOVERY -   INSERTING AN ENDSECT FOR YOU'
         ENDSECT ,            CLOSE OUT THE PREVIOUS  BGNSECT
         AGO   .RECERR1       CONTINUE
.ERR2    MNOTE 8,'RENT=&RENT IS INVALID, FOR YOU I ASSUME  RENT=NO'
         AGO   .RECRENT
.ERR3    MNOTE 8,'GENMAC=&GENMAC IS INVALID,  ASSUMING GENMAC=OS'
&MODTRM  SETB  (0)
         AGO   .GMX
         MEND
