         MACRO
&NAME    IF    &P1,&P2,&P3,&P4,&P5,&P6,&P7,&P8,&P9,&P10,               X
               &RW=,&LABEL=YES
         GBLA  &STRUCQ(9)          SEQUENCE VALIDATION STACK
         GBLA  &STRUCB(9)          BLOCK NUMBER STACK
         GBLA  &STRUCL             LEVEL COUNTER, ALSO STACK INDEX
         GBLA  &STRUCN             LAST ASSIGNED BLOCK NUMBER
         GBLC  &THEN,&ELSE
         LCLC  &LBL
.*   THIS MACRO IS PART OF A  BAL STRUCTURED PROGRAMMING PACKAGE
.*    WRITTEN BY  GARY S. NEMETH  OF THE CLEVELAND TRUST COMPANY
.*     DURING 1976.  ALL RIGHTS RESERVED.  ANY COMMERCIAL USE
.*      MUST BE APPROVED BY THE CLEVELAND TRUST COMPANY.
.* THIS MACRO MODIFIED AT RAINIER NATIONAL BANK
         AIF   (&STRUCL LE 0).OK             BLOCK OPEN IS VALID
         AIF   (&STRUCQ(&STRUCL) LT 20).OK   BLOCK OPEN IS VALID
.ERR     MNOTE 12,'STRUCTURE ERROR - BLK &STRUCB(&STRUCL) NOT ENDED'
         AGO   .RECOVER
.OK      ANOP
&STRUCL  SETA  &STRUCL+1           PUSH
.RECOVER ANOP
&STRUCN  SETA  &STRUCN+1           BUMP BLOCK NUMBERER
&STRUCB(&STRUCL) SETA &STRUCN      ASSIGN BLOCK NUMBER
&STRUCQ(&STRUCL) SETA 21         * SET THEN-REQUIRED
&LBL     SETC  'B'.'&STRUCN'.'&STRUCL'
         AIF   ('&NAME' EQ '').NONAME
&NAME    DS   0H
.NONAME  ANOP  ,
.*       MNOTE *,'BEGIN        LEVEL &STRUCL BLK &STRUCN'
&THEN    SETC  '&LBL'.'T'
&ELSE    SETC  '&LBL'.'F'
         AIF   ('&P1' EQ '').NOCOND
      STRUCOND &P1,&P2,&P3,&P4,&P5,&P6,&P7,&P8,&P9,&P10,               X
               TRUE=&THEN,FALSE=&ELSE,RWRK=&RW,BLKTYPE=IF,DELIM=THEN
.NOCOND  ANOP
         MEXIT
         MEND
