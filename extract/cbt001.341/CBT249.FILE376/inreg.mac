         MACRO
&NAME    INREG &R1,&R0,&R15,&R14,&R13,&R12,&R11,&R10,                  X
               &R9,&R8,&R7,&R6,&R5,&R4,&R3,&R2,&R=NO,&REG=
         LCLA  &X,&Q,&NLIST
         LCLB  &RYES,&RNO
         LCLC  &NM,&QX,&RG
.*   THIS MACRO IS PART OF A  BAL STRUCTURED PROGRAMMING PACKAGE
.*    WRITTEN BY  GARY S. NEMETH  OF THE CLEVELAND TRUST COMPANY
.*     DURING 1976.  ALL RIGHTS RESERVED.  ANY COMMERCIAL USE
.*      MUST BE APPROVED BY THE CLEVELAND TRUST COMPANY.
.* THIS MACRO MODIFIED AT RAINIER NATIONAL BANK
&RYES    SETB  ('&R' EQ 'YES')
&RNO     SETB  ('&R' EQ 'NO')
&NM      SETC  '&NAME'
&NLIST   SETA  N'&SYSLIST
&Q       SETA  2
         AIF   (NOT(&RYES OR &RNO) OR &NLIST GT 16).ERR
         AIF   ('&REG' NE '').REG       GO PROCESS ONE REGISTER
         AIF   (&NLIST LE 0).ENDX
.NEXTQ   AIF   (&Q NE 0).NEXTQ2
&Q       SETA  16                  WRAP 0 TO 15
.NEXTQ2  ANOP
&X       SETA  &X+1                NEXT &SYSLIST
&Q       SETA  &Q-1                NEXT REGISTER
         AIF   ('&SYSLIST(&X)' EQ '').NEXTX       NULL
         AIF   ('&SYSLIST(&X)'(1,1) NE '(').LBL   LABEL, ELSE REG
&QX      SETC  '&SYSLIST(&X)'(2,K'&SYSLIST(&X)-2)           REG NAME
         AIF   ('&QX' EQ '&Q' OR (&RYES AND '&QX' EQ 'R&Q')).NEXTX
&NM      LR    &Q,&QX
         AGO   .GENEND
.LBL     AIF   ('&SYSLIST(&X)' EQ '0').SR         TEST ZERO
&NM      LA    &Q,&SYSLIST(&X)
         AGO   .GENEND
.SR      ANOP
&NM      SR    &Q,&Q
.GENEND  ANOP
&NM      SETC  ''
.NEXTX   AIF   (&X LT &NLIST).NEXTQ     CONTINUE THRU &SYSLIST
         AGO   .ENDX
.REG     AIF   ('&R1' EQ '' OR &NLIST NE 1).ERR2
         AIF   ('&REG'(1,1) NE '(' OR N'&REG NE 1).ERR3
&RG      SETC  '&REG'(2,K'&REG-2)                           REG NAME
         AIF   ('&R1'(1,1) NE '(').RLBL       NOT A REGISTER
&QX      SETC  '&R1'(2,K'&R1-2)                             REG NAME
         AIF   (('&QX' EQ '&RG') OR                                    X
               (&RYES AND '&QX' EQ 'R&RG') OR                          X
               (&RYES AND 'R&QX' EQ '&RG')).ENDX
&NM      LR    &RG,&QX
         AGO   .RGENEND
.RLBL    AIF   ('&R1' EQ '0').RSR        TEST ZERO FOR SR OPCODE
&NM      LA    &RG,&R1
         AGO   .RGENEND
.RSR     ANOP
&NM      SLR   &RG,&RG
.RGENEND ANOP
&NM      SETC  ''
.ENDX    AIF   ('&NM' EQ '').MEXGEN
&NM      DS    0H
.MEXGEN  MEXIT
.ERR     MNOTE 8,'EITHER R=&R UNRECOGNIZED, OR TOO MANY REGS'
         MEXIT
.ERR2    MNOTE 8,'FOR REG=&REG, INVALID INPUT LIST'
         MEXIT
.ERR3    MNOTE 8,'INVALID VALUE OF REG=&REG    MUST BE REG=(R)'
         MEND
