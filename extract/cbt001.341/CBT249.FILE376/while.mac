         MACRO
&NAME    WHILE &P1,&P2,&P3,&P4,&P5,&P6,&P7,&P8,&P9,&P10,               X
               &RW=,&LABEL=YES
         GBLA  &STRUCQ(9)          SEQUENCE VALIDATION STACK
         GBLA  &STRUCB(9)          BLOCK NUMBER STACK
         GBLA  &STRUCL             LEVEL COUNTER, ALSO STACK INDEX
         GBLA  &STRUCN             LAST ASSIGNED BLOCK NUMBER
         GBLC  &DO,&ENDDO
         LCLC  &LBL
.*   THIS MACRO IS PART OF A  BAL STRUCTURED PROGRAMMING PACKAGE
.*    WRITTEN BY  GARY S. NEMETH  OF THE CLEVELAND TRUST COMPANY
.*     DURING 1976.  ALL RIGHTS RESERVED.  ANY COMMERCIAL USE
.*      MUST BE APPROVED BY THE CLEVELAND TRUST COMPANY.
.* THIS MACRO MODIFIED AT RAINIER NATIONAL BANK
         AIF   (&STRUCL LE 0).OK             BLOCK OPEN IS VALID
         AIF   (&STRUCQ(&STRUCL) LT 20).OK   BLOCK OPEN IS VALID
.ERR     MNOTE 12,'STRUCTURE ERROR - BLK &STRUCB(&STRUCL) NOT ENDED'
         AGO   .RECOVER
.OK      ANOP
&STRUCL  SETA  &STRUCL+1           WE WILL PUSH A NEW LEVEL
.RECOVER ANOP
&STRUCN  SETA  &STRUCN+1           BUMP BLOCK NUMBER
&STRUCB(&STRUCL) SETA &STRUCN      ASSIGN BLOCK NUMBER
&STRUCQ(&STRUCL) SETA 22         * SET DO-REQUIRED
&LBL     SETC  'B'.'&STRUCN'.'&STRUCL'
         AIF   ('&NAME' EQ '').NONAME
&NAME    DS    0H
.NONAME  ANOP  ,
.*       MNOTE *,'BEGIN        LEVEL &STRUCL BLK &STRUCN'
&DO      SETC  '&LBL'.'D'
&ENDDO   SETC  '&LBL'.'N'
.*       AIF   ('&LABEL' NE 'YES').NOLABEL
&LBL     SETC  '&LBL'.'W'
&LBL     DS    0H
.NOLABEL ANOP
         AIF   ('&P1' EQ '').NOCOND
      STRUCOND &P1,&P2,&P3,&P4,&P5,&P6,&P7,&P8,&P9,&P10,               X
               TRUE=&DO,FALSE=&ENDDO,RWRK=&RW,BLKTYPE=WHILE,DELIM=DO
.NOCOND  ANOP
         MEND
