         MACRO
         MODUSNG  &SYM,&REG,&MF=,&USING=
         GBLC  &MODUSYM(16),&MODUREG(16)
         GBLA  &MODUNUM
         LCLA  &I
         LCLC  &R
.*   THIS MACRO IS PART OF A  BAL STRUCTURED PROGRAMMING PACKAGE
.*    WRITTEN BY  GARY S. NEMETH  OF THE CLEVELAND TRUST COMPANY
.*     DURING 1976.  ALL RIGHTS RESERVED.  ANY COMMERCIAL USE
.*      MUST BE APPROVED BY THE CLEVELAND TRUST COMPANY.
.* THIS MACRO MODIFIED AT RAINIER NATIONAL BANK
         AIF   ('&MF' EQ 'INSERT').INS
         AIF   ('&MF' EQ 'GENUSING').GEN
         AIF   ('&MF' EQ 'CLOSE').CLOSE
         AIF   ('&MF' EQ 'SETUSING').SUS
         MNOTE 4,'MF=&MF  IS NOT VALID FOR  MODUSNG MACRO'
         MEXIT
.INS     AIF   ('&SYM' EQ '' OR '&REG' EQ '').ERRINS
         AIF   ('&REG'(1,1) NE '(').ERRINS
&R       SETC  '&REG'(2,K'&REG-2)            REMOVE  (REG)  PARENS
.INSL    ANOP       ,
&I       SETA  &I+1
         AIF   (&I LE &MODUNUM  AND '&MODUREG(&I)' NE '&R').INSL
&MODUSYM(&I) SETC '&SYM'
&MODUREG(&I) SETC '&R'
         AIF   (&I LE &MODUNUM).INS2
&MODUNUM SETA  &I
.INS2    ANOP
         MEXIT
.ERRINS  MNOTE 4,'PARAMETERS ARE INVALID FOR MODUSNG   MF=INSERT'
         MEXIT
.GEN     ANOP       ,         GENERATE ALL USINGS
.GENL    AIF   (&I GE &MODUNUM).GENN
&I       SETA  &I+1
         USING &MODUSYM(&I),&MODUREG(&I)
         AGO   .GENL
.GENN    MEXIT
.CLOSE   ANOP  ,              CLOSE/RESET THE TABLE
&MODUNUM SETA  0
         MEXIT
.SUS     AIF   (&I GE N'&USING).SUSN
         MODUSNG &USING(&I+1),&USING(&I+2),MF=INSERT
&I       SETA  &I+2
         AGO   .SUS
.SUSN    MEXIT ,
         MEND
