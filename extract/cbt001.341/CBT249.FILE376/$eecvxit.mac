TEC013   TITLE 'IEECVXIT - WTO EXIT ROUTINE'
***********************************************************************
*$DEL U004013                                                         *
*H U004013 S=MVS C=GX16 D=DEC81 L=050                                 *
*T   TEC013 - IEECVXIT -- WTO EXIT ROUTINE                            *
*K TEC013 IEECVXIT WTO EXIT PZ00045                                   *
*K IEF861I IEF863I IEF099I IEF452I IEF453I IEFTMS0                    *
*                                                                     *
*                          COPYRIGHT: 1981 RAINIER NATIONAL BANK      *
*                                     SEATTLE WASHINGTON              *
*                                     (206) 433-7467                  *
* MEMBER: TEC013                                                      *
* SOURCE LIB: SLICK.PROD.SOURCE                                       *
* SMP USERMOD:  PZ00045                                               *
* LOAD LIB:   LOAD LIBRARY NAME                                       *
* MACRO LIBS NEEDED: SYS1.MACLIB                                      *
*                    SYS1.AMODGEN                                     *
*                                                                     *
* LINKAGE EDITOR PARMS: LET,NCAL,RENT                                 *
*                                                                     *
* PURPOSE:                                                            *
*        THE PURPOSE OF IEECVXIT (WTO EXIT ROUTINE) IS TO INTERCEPT   *
*        AND EXAMINE ALL WTO MESSAGES AND FOR THOSE MESSAGES LISTED   *
*        BELOW INITIATE PROCESSING AS SPECIFIED:                      *
*                                                                     *
*             IEF861I - THESE MESSAGES ARE ALL PUT OUT AS THE RESULT  *
*             IEF863I - OF A JOB WAITING FOR ONE OR MORE DATASETS     *
*             IEF099I - WHICH HAVE BEEN RESERVED BY ANOTHER JOB. THIS *
*                       EXIT ROUTINE WILL ROUTE THE MESSAGES TO THE   *
*                       USERS TSO TERMINAL.                           *
*                                                                     *
*             IEF452I -                                               *
*             IEF453I - THE ROUTING AND DESCRIPTOR CODES WILL BE      *
*                       MODIFIED TO MAKE THESE MESSAGES HANG ON THE   *
*                       MASTER CONSOLE FOR PRODUCTION JOBS ONLY.      *
*                                                                     *
*             IEFTMS0 - ROUTE CODE WILL BE MODIFIED TO MAKE THIS A    *
*                       SECURITY MESSAGE WHICH WILL SUPPRESS THE REPLY*
*                       IN CASE IT'S THE MASTER PASSWORD.             *
*                                                                     *
*
*  SEE NOTE-  IEE361I -                                               *
*             IEE362I - THESE MESSAGES INDICATES THAT THE SMF DATASET *
*                       IS FULL. PROCESSING IN THIS ROUTINE WILL CAUSE*
*                       THE FULL SMF DATASET TO BE DUMPED.            *
*                                                                     *
*  SEE NOTE- CMFSDV93 - THIS MESSAGE SAYS THAT CMF HAS FLIPPED TO AN  *
*                       ALTERNATE DATASET. THIS ROUTINE WILL START A  *
*                       JOB TO DUMP THE FULL ONE.                     *
*                                                                     *
*             ******* - ROUTING CODE 14 WILL BE TURNED OFF ON ALL     *
*                       MESSAGES THAT FIND THEIR WAY TO THIS ROUTINE. *
*                                                                     *
*  NOTE>  THESE MESSAGES NOT CURRENTLY BEING PROCESSED.               *
* RETURN CODES: ZERO                                                  *
*                                                                     *
*                                                                     *
*                                                                     *
* CHANGE LOG: WRITTEN BY JOE L DURAN (4/81).                          *
*    12/31/81 -001-WBF- CHANGED HEADER TO ADD OZ DOCUMENTATION INFO.  *
*$EOM                                                                 *
***********************************************************************
         EJECT
IEECVXIT CSECT
         SAVE  (14,12),,_&SYSDATE_&SYSTIME  SAVE REGISTER CONTENTS
         LR    R12,R15             ENTRY ADDRESS TO R12
         USING IEECVXIT,R12        ESTABLISH ADDRESSABILITY
         L     R8,0(,R1)           SAVE PARM ADDRESS
         USING WPL,R8              ASSOCIATE MESSAGE DSECT W/R8
         GETMAIN RC,LV=MYAREAL,SP=230   GET STORAGE WORK AREA
         LTR   R15,R15             CHECK CONDITION CODE.
         BNZ   RET                 BAD CONDITION CODE.
         ST    R1,8(,R13)          MY SAVE TO CALLER.
         ST    R13,4(,R1)          CALLERS SAVE ADDRESS TO MY SAVE
         LR    R13,R1              ADDRESS OF MY SAVE TO R13
         USING MYAREA,R13          ASSOCIATE GOTTEN AREA W/R13
         SR    R3,R3
         SR    R4,R4
         SR    R5,R5               CLEAR REGS FOR ICM.
         SR    R6,R6
         SR    R7,R7
         EJECT
*      - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
*        ** LOOK IN MESSAGE TABLE FOR MATCHING ENTRY, IF A MATCH
*          IS FOUND AND AN ADDRESS FOR A PROCESSING ROUTINE EXISTS,
*          THE PROCESSING ROUTINE IS BRANCHED TO.
*      - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
         LA    R9,MSGTABLE         GET ADDRESS OF MESSAGE TABLE
         USING TBLENTRY,R9         ASSOCIATE TABLE ENTRY DSECT W/R9
         SR    R15,R15
SRCHTBLE IC    R15,MSGL            GET MESSAGE ID LENGTH
         C     R15,EOT             AM I AT THE END OF TABLE
         BE    OFF14               YES
         BCTR  R15,0               -1 FOR EX
         EX    R15,COMPARE         EXECUTE COMPARISON
         BE    CONTINUE            FOUND MATCH
         BH    OFF14               NOT IN TABLE, GET OUT
         LA    R9,MSGID+1(R15)     BUMP TABLE POINTER
         B     SRCHTBLE            GO TO NEXT ENTRY IN TABLE
         SPACE 2
COMPARE  CLC   MSGID(*-*),TEXT
         SPACE 2
CONTINUE DS    0H
         ICM   R15,7,RTNADDR       GET PROCESSING ROUTINE ADDRESS
         BZ    CODEMOD             ROUTE AND DESC. CODE MOD ONLY
         BR    R15                 GO TO PROCESSING ROUTINE
         EJECT
*      - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
*      PROCESSING FOR IEF861I, IEF099I, AND IEF863I IS DONE HERE. IF
*      THE JOB WAS SUBMITTED FROM A TSO TERMINAL, THE MESSAGE IS SENT
*      TO THE USERS TSO TERMINAL.
*      - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
SENDDSN  EQU   *
         LA    R3,0
         USING PSA,R3              ASSOCIATE PSA TO R3.
         ICM   R4,7,PSAAOLD+1
         USING ASCB,R4             ASSOCIATE ASCB TO R4.
         ICM   R5,7,ASCBASXB+1
         USING ASXB,R5             ASSOCIATE ASXB TO R5
         ICM   R6,7,ASXBSENV+1
         BZ    OFF14               NO ACEE ADDRESS
         USING ACEE,R6             ASSOCIATE ACEE DSECT W/R6
         CLC   ACEEACEE,=C'ACEE'   IS THIS AN ACEE
         BNE   OFF14               NO, GET OUT
         TM    ACEEFLG1,ACEERACF   IS USERID USABLE
         BNO   OFF14               NO, GET OUT
         SR    R15,R15
         CLC   ACEEUSRI+3(4),PROD  IS IT PRODUCTION ID
         BE    OFF14               YES, GET OUT
         IC    R15,ACEEUSRL        GET USERID LENGTH, AGAIN
         BCTR  R15,0               -1 FOR MVC
         MVC   USERID(8),BLANKS    INITIALIZE USERID
         MVC   USERID(*-*),ACEEUSRI
         EX    R15,*-6             MOVE USERID FOR TPUT.
         LA    R0,79               LENGTH OF MESSAGE TEXT TO R0
         LA    R1,TEXT             ADDRESS OF MESSAGE TEXT TO R1
         B     SEND                GO TO TPUT
         SPACE 3
         SPACE 3
*      - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
*        INITIAL PROCESSING FOR MESSAGE IEF452I. ALSO JCLERR
*      - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
INIT1    MVC   JOBNAME(8),TEXT+8
         CLC   JOBNAME(8),JOBFAIL  IS JOBNAME 'JOBFAIL' -
         BE    OFF14               TURN OFF ROUTE CODE 14
         B     JCLERR              CONTINUE PROCESSING MESSAGE
         EJECT
*      - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
*        JCLERR CONTAINS COMMON CODE USED IN PROCESSING MESSAGES
*       IEF452I AND IEF453I.
*      - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
         SPACE 2
JCLERR   EQU   *
         LA    R3,0
         USING PSA,R3              ASSOCIATE PSA TO R3.
         ICM   R4,7,PSAAOLD+1
         USING ASCB,R4             ASSOCIATE ASCB TO R4.
         ICM   R5,7,ASCBASXB+1
         USING ASXB,R5             ASSOCIATE ASXB TO R5
         ICM   R6,7,ASXBSENV+1
         BZ    OFF14               NO ACEE ADDRESS
         CLC   ACEEACEE,=C'ACEE'   IS THIS AN ACEE
         BNE   OFF14               NO, GET OUT
         TM    ACEEFLG1,ACEERACF   IS USERID USABLE
         BNO   OFF14               NO, GET OUT
         SR    R15,R15
         CLC   ACEEUSRI+3(4),PROD  IS IT PRODUCTION ID
         BNE   OFF14               NO, GET OUT
         SPACE 2
*      - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
*         CODE FOR CHECKING TO SEE IF THE ROUTING CODE OR THE
*        DESCRIPTOR CODE IS TO BE MODIFIED.
*      - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
CODEMOD  DS    0H
         CLC   ONROUT(2),BINZEROS
         BE    CHKOFFRC
         OC    RTCODE(2),ONROUT
CHKOFFRC CLC   OFFROUT(2),BINONES
         BE    CHKONDC
         NC    RTCODE(2),OFFROUT
CHKONDC  CLC   ONDESC(2),BINZEROS
         BE    CHKOFFDC
         OC    DESCODE(2),ONDESC
CHKOFFDC CLC   OFFDESC(2),BINONES
         BE    EXITA
         NC    DESCODE(2),OFFDESC
EXITA    B     OFF14
         EJECT
*      - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
*          THIS IS THE TPUT TO SEND MESSAGES TO USER.
*      - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
SEND     TPUT  (1),(0),EDIT,NOWAIT,NOHOLD,BREAKIN,HIGHP,               X
               USERIDL=USERID
         SPACE 2
*      - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
*      THIS IS THE INSTRUCTION TO TURN OFF ROUTE CODE 14 (ALL MESSAGES)
*      AND  THE WAY OUT.
*      - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
OFF14    NC    RTCODE(2),ROUTE14   TURN OFF ROUTE CODE 14
         SPACE 2
         L     R13,4(,R13)         CALLERS SAVE ADDRESS TO R13
         LR    R1,R13              STORAGE AREA ADDRESS TO R1
         FREEMAIN RC,LV=MYAREAL,A=(1),SP=230   FREE GETMAINED STORAGE
*      - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
*      THIS IS THE WAY OUT WHEN GETMAIN FAILED.
*      - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
RET      RETURN (14,12),RC=0       RETURN
         EJECT
*
*      - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
*        FOLLOWING IS THE CONSTANT WORK AREA FOR THIS PROGRAM.
*      - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
         SPACE 2
         REGEQU                    EQUATE REGISTERS
         SPACE 2
JOBFAIL  DC    C'JOBFAIL '
BINZEROS DC    X'0000'
BINONES  DC    X'FFFF'
ROUTE14  DC    X'FFFB'             TO TURN OFF ROUTE CODE 14
EOT      DC    F'255'              X'FF' - END OF TABLE
FOUR     DC    F'4'
PROD     DC    C'PROD'
BLANKS   DC    8C' '
         EJECT
*      - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
*         THE MESSAGE TABLE WITH THE MESSAGE ID AND ALL OF THE INFO
*      NECESSARY FOR MODIFICATIONS TO THE ROUTE CODE OR DESCRIPTOR
*      CODE.
*      - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
*
MSGTABLE DS    0H
         DC    XL1'07'             MESSAGE IEFTMS0
         DC    AL3(0)
         DC    XL2'0080'           ROUTE CODE 9
         DC    XL2'FFFF'
         DC    XL2'0000'
         DC    XL2'FFFF'
         DC    CL7'IEFTMS0'
         SPACE 2
         DC    XL1'07'             MESSAGE IEF099I
         DC    AL3(SENDDSN)
         DC    XL2'0000'
         DC    XL2'FFFF'           NO
         DC    XL2'0000'           MODS
         DC    XL2'FFFF'
         DC    CL7'IEF099I'
         SPACE 2
         DC    XL1'07'             MESSAGE IEF452I
         DC    AL3(INIT1)
         DC    XL2'8000'           ROUTE CODE 1
         DC    XL2'FFFF'
         DC    XL2'2000'           DESCRIPTOR CODE 3
         DC    XL2'FFFF'
         DC    CL7'IEF452I'
         EJECT
         DC    XL1'07'             MESSAGE IEF453I
         DC    AL3(JCLERR)
         DC    XL2'8000'           ROUTE CODE 1
         DC    XL2'FFFF'
         DC    XL2'2000'           DESCRIPTOR CODE 3
         DC    XL2'FFFF'
         DC    CL7'IEF453I'
         SPACE 2
         DC    XL1'07'             MESSAGE IEF861I
         DC    AL3(SENDDSN)
         DC    XL2'0000'
         DC    XL2'FFFF'           NO
         DC    XL2'0000'           MODS
         DC    XL2'FFFF'
         DC    CL7'IEF861I'
         SPACE 2
         DC    XL1'07'             MESSAGE IEF863I
         DC    AL3(SENDDSN)
         DC    XL2'0000'
         DC    XL2'FFFF'           NO
         DC    XL2'0000'           MODS
         DC    XL2'FFFF'
         DC    CL7'IEF863I'
         SPACE 1
         DC    X'FF'               END OF TABLE
         SPACE 1
         LTORG
         EJECT
*      - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
*        MYAREA IS THE GETMAINED WORK AREA.
*      - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
MYAREA   DSECT
SAVE     DC    18F'0'
USERID   DC    CL8' '
JOBNAME  DC    CL8' '
MYAREAL  EQU   *-MYAREA            LENGTH OF GOTTEN AREA
         SPACE 2
*      - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
*        DSECT TO MAP MESSAGE W/RELATED EQUATES
*      - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
WPL      DSECT
TEXT     DS    CL128
RTCODE   DS    CL4
DESCODE  DS    CL4
         SPACE 3
*      - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
*        DSECT TO MAP TABLE ENTRIES
*      - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
TBLENTRY DSECT
MSGL     DS    XL1
RTNADDR  DS    XL3
ONROUT   DS    XL2
OFFROUT  DS    XL2
ONDESC   DS    XL2
OFFDESC  DS    XL2
MSGID    DS    CL7
         SPACE 3
*       - - -- - - - - - - - - - - - - -
*            MAPPING MACROS
*       - - -- - - - - - - - - - - - - -
         PRINT NOGEN
         CVT   DSECT=YES           CVT
         IHAPSA                    PSA
         IHAACEE                   ACEE
         IHAASCB                   ASCB
         IRAOUCB                   OUCB
         IHAASXB                   ASXB
         END   IEECVXIT
