         MACRO
&NAME    SDO   &LABEL=YES
         GBLA  &STRUCQ(9)          SEQUENCE VALIDATION STACK
         GBLA  &STRUCB(9)          BLOCK NUMBER STACK
         GBLA  &STRUCL             LEVEL COUNTER, ALSO STACK INDEX
         GBLA  &STRUCN             LAST ASSIGNED BLOCK NUMBER
         GBLC  &DO,&ENDDO
         LCLC  &LBL
.*   THIS MACRO IS PART OF A  BAL STRUCTURED PROGRAMMING PACKAGE
.*    WRITTEN BY  GARY S. NEMETH  OF THE CLEVELAND TRUST COMPANY
.*     DURING 1976.  ALL RIGHTS RESERVED.  ANY COMMERCIAL USE
.*      MUST BE APPROVED BY THE CLEVELAND TRUST COMPANY.
.* THIS MACRO MODIFIED AT RAINIER NATIONAL BANK
         AIF   (&STRUCL LE 0).OK                  BLOCK OPEN IS VALID
         AIF   (&STRUCQ(&STRUCL) EQ 22).WHILE     WHILE, DO-REQUIRED
         AIF   (&STRUCQ(&STRUCL) LT 20).OK        BLOCK OPEN IS VALID
.ERR     MNOTE 12,'STRUCTURE ERROR - BLK &STRUCB(&STRUCL) NOT ENDED'
         AGO   .RECOVER
.OK      ANOP
&STRUCL  SETA  &STRUCL+1           WE WILL PUSH A NEW LEVEL
.RECOVER ANOP
&STRUCN  SETA  &STRUCN+1           BUMP BLOCK NUMBER
&STRUCB(&STRUCL) SETA &STRUCN      ASSIGN BLOCK NUMBER
&STRUCQ(&STRUCL) SETA 3       DO * SET UNTIL,BLOCKOPEN OK
         AIF   ('&NAME' EQ '').NONAME
&NAME    DS    0H
.NONAME  ANOP  ,
.*       MNOTE *,'BEGIN        LEVEL &STRUCL BLK &STRUCN'
.*       AIF   ('&LABEL' NE 'YES').NOLABEL
&LBL     SETC  'B'.'&STRUCN'.'&STRUCL'.'D'
&LBL     DS    0H
.NOLABEL ANOP
         MEXIT
.WHILE   ANOP
&DO      SETC  ''
&ENDDO   SETC  ''
&STRUCQ(&STRUCL) SETA 4       DO * SET ENDDO, BLOCKOPEN VALID
         AIF   ('&NAME' EQ '').NONAMEW
&NAME    DS    0H
.NONAMEW ANOP
.*       AIF   ('&LABEL' NE 'YES').NOLBLW
&LBL     SETC  'B'.'&STRUCB(&STRUCL)'.'&STRUCL'.'D'
&LBL     DS    0H
.NOLBLW  ANOP
         MEND
