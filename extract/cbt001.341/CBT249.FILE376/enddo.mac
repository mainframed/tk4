         MACRO
&NAME    ENDDO &LABEL=YES
         GBLA  &STRUCQ(9)          SEQUENCE VALIDATION STACK
         GBLA  &STRUCB(9)          BLOCK NUMBER STACK
         GBLA  &STRUCL             LEVEL COUNTER, ALSO STACK INDEX
         GBLC  &DO,&ENDDO
         LCLC  &LBL
.*   THIS MACRO IS PART OF A  BAL STRUCTURED PROGRAMMING PACKAGE
.*    WRITTEN BY  GARY S. NEMETH  OF THE CLEVELAND TRUST COMPANY
.*     DURING 1976.  ALL RIGHTS RESERVED.  ANY COMMERCIAL USE
.*      MUST BE APPROVED BY THE CLEVELAND TRUST COMPANY.
.* THIS MACRO MODIFIED AT RAINIER NATIONAL BANK
         AIF   (&STRUCL LE 0).ERR                 ENDDO IS IMPOSSIBLE
         AIF   (&STRUCQ(&STRUCL) EQ 23).UNTIL     DO UNTIL ENDDO
         AIF   (&STRUCQ(&STRUCL) EQ 4).DO         WHILE DO ENDDO
         MNOTE *,'END          LEVEL &STRUCL BLK &STRUCB(&STRUCL)'
&STRUCL  SETA  &STRUCL-1
.ERR     MNOTE 12,'STRUCTURE ERROR'
&DO      SETC  ''
&ENDDO   SETC  ''
         MEXIT
.DO      ANOP
&LBL     SETC  'B'.'&STRUCB(&STRUCL)'.'&STRUCL'.'W'
         B     &LBL                BACK TO WHILE TEST
.UNTIL   ANOP
.*       MNOTE *,'END          LEVEL &STRUCL BLK &STRUCB(&STRUCL)'
.*       AIF   ('&LABEL' NE 'YES').NOLABEL
&LBL     SETC  'B'.'&STRUCB(&STRUCL)'.'&STRUCL'.'N'
&LBL     DS    0H
.NOLABEL ANOP
&STRUCL  SETA  &STRUCL-1           POP LEVEL STATUS
&DO      SETC  ''
&ENDDO   SETC  ''
         AIF   ('&NAME' EQ '').NONAME
&NAME    DS    0H
.NONAME  MEND
