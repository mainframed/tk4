         MACRO
         STRUCTYP &F,&X            INNER MACRO TO SET MODE, TYPE, NAME
         GBLC  &STRUC$M(3)    SYMTAB-   MODE   A/F,H,=,R    P/P
         GBLC  &STRUC$T(3)    SYMTAB-   /TYPE  F/E,D,R      X/C,X,B
         GBLC  &STRUC$F(3)    SYMTAB-
         LCLC  &TC            RAW TYPE CHARACTER
.*   THIS MACRO IS PART OF A  BAL STRUCTURED PROGRAMMING PACKAGE
.*    WRITTEN BY  GARY S. NEMETH  OF THE CLEVELAND TRUST COMPANY
.*     DURING 1976.  ALL RIGHTS RESERVED.  ANY COMMERCIAL USE
.*      MUST BE APPROVED BY THE CLEVELAND TRUST COMPANY.
.* THIS MACRO MODIFIED AT RAINIER NATIONAL BANK
         AIF   ('&F'(1,1) NE '(').NR
&STRUC$M(&X) SETC 'A'         MODE-A
&STRUC$T(&X) SETC 'R'         TYPE-R REGISTER
&STRUC$F(&X) SETC '&F(1)'
         MEXIT
.NR      AIF   (T'&F NE 'N').NN
&STRUC$M(&X) SETC 'A'         MODE-A
&STRUC$T(&X) SETC '='         TYPE-= SELF-DEFINING TERM
&STRUC$F(&X) SETC '&F'
         MEXIT
.NN      AIF   ('&F'(1,1) EQ '=').LIT
&TC      SETC  T'&F                     SYMBOL TYPE
         AGO   .W
.LIT     ANOP
&TC      SETC  '&F'(2,1)                LITERAL TYPE
.W       ANOP
&STRUC$F(&X) SETC '&F'
         AIF   ('&TC' EQ 'F' OR '&TC' EQ 'A' OR '&TC' EQ 'V').MODEAF
         AIF   ('&TC' EQ 'H' OR '&TC' EQ 'Y').MODEAH
         AIF   ('&TC' EQ 'E' OR '&TC' EQ 'D').MODEF
         AIF   ('&TC' EQ 'P'                ).MODEP
         AIF   ('&TC' EQ 'C' OR '&TC' EQ 'X' OR '&TC' EQ 'B').MODEX
         AIF   ('&TC' EQ 'Z').MODEX
         AGO   .MODEX        ASSUME &TC = C          ****RNB  TGA
         MNOTE 8,'THE TYPE OF FIELD &F CANNOT BE DETERMINED'
         MNOTE *,'THE TYPE OF FIELD &F IS &TC'       ****RNB  TGA
&STRUC$M(&X) SETC '*'
&STRUC$T(&X) SETC '*'
         MEXIT
.MODEAF  ANOP
&STRUC$M(&X) SETC 'A'
&STRUC$T(&X) SETC 'F'
         MEXIT
.MODEAH  ANOP
&STRUC$M(&X) SETC 'A'
&STRUC$T(&X) SETC 'H'
         MEXIT
.MODEF   ANOP
&STRUC$M(&X) SETC 'F'
&STRUC$T(&X) SETC '&TC'
         MEXIT
.MODEP   ANOP
&STRUC$M(&X) SETC 'P'
&STRUC$T(&X) SETC '&TC'
         MEXIT
.MODEX   ANOP
&STRUC$M(&X) SETC 'X'
&STRUC$T(&X) SETC '&TC'
         MEXIT
         MEND
