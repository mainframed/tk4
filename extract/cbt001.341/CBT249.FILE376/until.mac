         MACRO
&NAME    UNTIL &P1,&P2,&P3,&P4,&P5,&P6,&P7,&P8,&P9,&P10,               X
               &RW=,&LABEL=YES
         GBLA  &STRUCQ(9)          SEQUENCE VALIDATION STACK
         GBLA  &STRUCB(9)          BLOCK NUMBER STACK
         GBLA  &STRUCL             LEVEL COUNTER, ALSO STACK INDEX
         GBLC  &DO,&ENDDO
         LCLC  &LBL
.*   THIS MACRO IS PART OF A  BAL STRUCTURED PROGRAMMING PACKAGE
.*    WRITTEN BY  GARY S. NEMETH  OF THE CLEVELAND TRUST COMPANY
.*     DURING 1976.  ALL RIGHTS RESERVED.  ANY COMMERCIAL USE
.*      MUST BE APPROVED BY THE CLEVELAND TRUST COMPANY.
.* THIS MACRO MODIFIED AT RAINIER NATIONAL BANK
         AIF   (&STRUCL LE 0).ERR            UNTIL IS IMPOSSIBLE
         AIF   (&STRUCQ(&STRUCL) EQ 3).OK    UNTIL IS REQUIRED, OK
&STRUCQ(&STRUCL) SETA 23   UNTIL * SET ENDDO REQUIRED
.ERR     MNOTE 12,'STRUCTURE ERROR'
&DO      SETC  ''
&ENDDO   SETC  ''
         MEXIT
.OK      ANOP
&STRUCQ(&STRUCL) SETA 23  UNTIL * SET ENDDO REQUIRED
&LBL     SETC  'B'.'&STRUCB(&STRUCL)'.'&STRUCL'
&DO      SETC  '&LBL'.'D'
&ENDDO   SETC  '&LBL'.'N'
         AIF   ('&NAME' EQ '').NONAME
&NAME    DS   0H
.NONAME  ANOP
         AIF   ('&P1' EQ '').NOCOND
      STRUCOND &P1,&P2,&P3,&P4,&P5,&P6,&P7,&P8,&P9,&P10,               X
               TRUE=&ENDDO,FALSE=&DO,RWRK=&RW,BLKTYPE=UNTIL,DELIM=ENDDO
.NOCOND  ANOP
         MEND
