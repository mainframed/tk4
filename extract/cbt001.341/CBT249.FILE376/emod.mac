         MACRO
&NAME    EMOD  &RC=(15),&ALL=NO
         GBLC  &MODCS,&MODVN,&MODRVA,&MODRV1,&MODRV2,&RNBNM
         GBLB  &MODRENT,&MODTASK,&MODFE,&MODPP
         GBLB  &MODPREN,&MODXREN    2/01/76 RENT=P,RENT=(R)
         GBLB  &MODTRM             INDICATE TRACEMNTR(TRUE) ELSE OS MAC
         GBLA  &MODRVN
         GBLB  &MODLEAF,&MODPMOD   8/26/76  INDICATE  BMOD ,L OR ,P
         GBLA  &MODFRVN            8/26/76  NUMBER OF LEAF RVA WORDS
         GBLB  &MODCMOD      12/2/76  INDICATE  BMOD ,C  CONSTANTS
         GBLA  &STRUCL             IF-MACROS   BLOCK LEVEL INDEX
.*   THIS MACRO IS PART OF A  BAL STRUCTURED PROGRAMMING PACKAGE
.*    WRITTEN BY  GARY S. NEMETH  OF THE CLEVELAND TRUST COMPANY
.*     DURING 1976.  ALL RIGHTS RESERVED.  ANY COMMERCIAL USE
.*      MUST BE APPROVED BY THE CLEVELAND TRUST COMPANY.
.* THIS MACRO MODIFIED AT RAINIER NATIONAL BANK
         AIF   (&STRUCL EQ 0).IFOK
&STRUCL  SETA  0
         MNOTE 12,'STRUCTURE ERROR,  IF/WHILE BLOCKS WERE NOT ENDED'
.IFOK    ANOP  ,
         AIF   (&MODFE).FE                        GO AROUND FOR FIRST
.*                                                EMOD PAIRS A BMOD
         AIF   (&MODCMOD).CONST
         AIF   ('&RC'(1,1) GE '0').RNB1CN        **** RNB 04/78 ****
         AIF   ('&RC'(1,1) EQ '(').RNB1CN        **** RNB 04/78 ****
&NAME    L     15,&RC                            **** RNB 04/78 ****
         AGO   .RNB2CN                           **** RNB 04/78 ****
.RNB1CN  ANOP                                    **** RNB 04/78 ****
&NAME    INREG ,,&RC,R=YES         SET RETURN CODE
.RNB2CN  ANOP                                    **** RNB 04/78 ****
         AIF   (NOT &MODPP).NPP
         DROP  ,
         POP   USING               RESTORE OUTER USING STATE
&MODPP   SETB  (0)                 TURN OFF PUSHPOP=YES
.NPP     ANOP
         AIF   ('&MODRVA' EQ '').NFE6
         L     14,&MODRVA          MODULE RETURN ADDRESS
         AIF   ('&MODRV1' EQ '').NFE6
         AIF   ('&MODRV2' NE '').NFE4
         L     &MODRV1,&MODRVA+4
         AGO   .NFE6
.NFE4    LM    &MODRV1,&MODRV2,&MODRVA+4
.NFE6    BR    14                  RETURN
         AIF   (NOT &MODLEAF).NOTLEAF        BYPASS SPECIAL LEAF CODE
         AIF   (&MODFRVN GE &MODRVN).SMALLF   IS THIS THE LARGEST AREA
&MODFRVN SETA  &MODRVN                        YES, UPDATE TO THIS LENGT
.SMALLF  ANOP  ,
&MODRVN  SETA  0         FOR A LEAF,  GENERATE NO SAVE AREA WORDS HERE
&MODRVA  SETC  ''         ALSO,  DO NOT USE THIS LABEL, ITS IN ENDSECT
.NOTLEAF ANOP  ,
&MODRVA  VMOD  &MODRVN
&MODRVN  SETA  0                   NO VMOD WORDS TO BE GENERATED
&MODRVA  SETC  ''
&MODLEAF SETB  (0)
&MODPMOD SETB  (0)
         MEXIT
.CONST   ANOP  ,              EMOD PAIRS CONSTANT  BMOD ,C
         AIF   ('&NAME' EQ '').CONST2
         MNOTE *,'A NAME IS NOT VALID FOR CONSTANT EMOD'
.CONST2  AIF   ('&RC' EQ '(15)').CONST4
         MNOTE 8,'RC=&RC IS NOT ALLOWED FOR CONSTANT EMOD'
.CONST4  ANOP
         VMOD  0         ALTER ADDRESSABILITY TO  VARIABLE DATA AREA
&MODCMOD SETB  (0)
         MEXIT
.FE      AIF   (&MODTASK).FETASK
.*                                                EMOD PAIRS A BGNSECT
         AIF   (&MODRENT).FERENT
         AIF   ('&RC'(1,1) GE '0').RNB3CN        **** RNB 04/78 ****
         AIF   ('&RC'(1,1) EQ '(').RNB3CN        **** RNB 04/78 ****
&NAME    L     15,&RC                            **** RNB 04/78 ****
         AGO   .RNB4CN                           **** RNB 04/78 ****
.RNB3CN  ANOP                                    **** RNB 04/78 ****
&NAME    INREG ,,&RC,R=YES
.RNB4CN  ANOP                                    **** RNB 04/78 ****
         AIF   (&MODPREN).FEPREN
         AIF   (NOT &MODTRM).NRNTRM
         B     EXIT$               CALL MONITOR NON-RENT EXIT MACRO
         AGO   .FEEXIT
.NRNTRM  ANOP  ,              SAME CODING AS TRACEMON  EXIT$
         L     13,4(,13)           HSA, OUR CALLERS SVA
         MVI   8(13),X'FF'         FLAG MEANINGLESS LSA POINTER
         L     14,12(,13)          RETURN (LEAVE  RC=(15),R0,R1)
         LM    2,12,28(13)         RESTORE REGS
         BR    14                   DONE
         AGO   .FEEXIT
.FERENT  AIF   (NOT &MODTRM).FERNTRM
&NAME    XFM   (13),&MODCS.L,RC=&RC,TYPE=SVA      FREEMAIN, RETURN
         AGO   .FEEXIT
.FERNTRM ANOP
         AIF   ('&RC'(1,1) GE '0').RNB5CN        **** RNB 04/78 ****
         AIF   ('&RC'(1,1) EQ '(').RNB5CN        **** RNB 04/78 ****
&NAME    L     15,&RC                            **** RNB 04/78 ****
         AGO   .RNB6CN                           **** RNB 04/78 ****
.RNB5CN  ANOP                                    **** RNB 04/78 ****
&NAME    INREG ,,&RC,R=YES
.RNB6CN  ANOP                                    **** RNB 04/78 ****
         LR    2,13                STASH THIS SVA ADDRESS
         L     13,4(,13)           HSA, OUR CALLERS SVA
         STM   15,1,16(13)         RETURN LINKAGE  RC=(15),R0,R1
         MVI   8(13),X'FF'         FLAG MEANINGLESS LSA POINTER
.*       LA    0,&MODCS.L          LENGTH OF OUR SAVE AREA
         L     0,&MODCS.#              **** RNB
         FREEMAIN R,A=(2),LV=(0)
         LM    14,12,12(13)
         BR    14
         AGO   .FEEXIT
.FEPREN  ANOP
         L     14,12(,13)          RESTORE R14 DIRECTLY
         AIF ('&ALL' EQ 'YES').RNB1RT             **** RNB 11/78 ****
         LM    2,12,28(13)         AND THUSLY ALL THE OTHERS
         AGO   .RNB3RT                            **** RNB 11/78 ****
.RNB1RT  ANOP                                     **** RNB 11/78 ****
         LM    0,12,20(13)         AND THUSLY ALL **** RNB 11/78 ****
.RNB3RT  ANOP                                     **** RNB 11/78 ****
         BR    14                  RETURN
         AGO   .FEEXIT
.FETASK  ANOP
&NAME    DC   2H'0'      CRASH, A TASK CANNOT EXIT
.FEEXIT  AIF   (&MODXREN OR &MODPREN).FEEXIT2
&MODVN   VMOD  18                                 GEN REG SAVE AREA
.FEEXIT2 ANOP
&MODFE   SETB  (0)
         MEXIT
         MEND
