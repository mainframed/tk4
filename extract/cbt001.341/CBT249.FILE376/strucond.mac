         MACRO
         STRUCOND &OP,&P2,&P3,&P4,&P5,&P6,&P7,&P8,&P9,                 X
               &TRUE=,&FALSE=,&RWRK=,&BLKTYPE=,&DELIM=
         LCLA  &NLIST,&I
         LCLC  &DLM,&RW
         LCLC  &LABEL              FOR READABILITY OF GENERATED CODING
.*   THIS MACRO IS PART OF A  BAL STRUCTURED PROGRAMMING PACKAGE
.*    WRITTEN BY  GARY S. NEMETH  OF THE CLEVELAND TRUST COMPANY
.*     DURING 1976.  ALL RIGHTS RESERVED.  ANY COMMERCIAL USE
.*      MUST BE APPROVED BY THE CLEVELAND TRUST COMPANY.
.* THIS MACRO MODIFIED AT RAINIER NATIONAL BANK
&DLM     SETC  '&DELIM'            THEN,ETC   TERMINATOR OF THE COND
&NLIST   SETA  N'&SYSLIST
&I       SETA  2
&LABEL   SETC  'NO'                MUST BE YES IF BRANCH TO TRUE
&RW      SETC  '0'
         AIF   ('&RWRK(1)' EQ '').RW
&RW      SETC  '&RWRK(1)'
.RW      ANOP
         AIF   ('&OP'(1,1) EQ '(').REL
         AIF   ('&OP' EQ 'AND').AND
         AIF   ('&OP' EQ 'OR').OR
         AIF   ('&OP' EQ 'BCT').BCT          * * B C T * *
         AIF   ('&OP' EQ 'ALWAYS').ALWAYS
         AIF   ('&OP' EQ 'NEVER').NEVER
         MNOTE 08,'&OP IS UNRECOGNIZABLE'
         MEXIT
.BCT     ANOP
         AIF   ('&P2'(1,1) NE '(').BCTE1     MUST BE A REGISTER
         BCT   &P2(1),&FALSE       FALSE
&I       SETA  3                   POINTER TO THE DELIM
         AGO   .DELIM
.ALWAYS  ANOP
         AGO   .DELIM              &I IS STILL 2
.NEVER   ANOP
         B     &FALSE              NEVER
        AGO   .DELIM               &I IS STILL 2
.REL     ANOP
         STRUCREL   &OP,TF=F,TO=&FALSE,RW=&RW
         AGO   .DELIM                        &I IS STILL 2
.AND     ANOP
.ANDL    AIF   ('&SYSLIST(&I)' EQ '').STRUCE
         STRUCREL  &SYSLIST(&I),TF=F,TO=&FALSE,RW=&RW
&I       SETA  &I+1
         AIF   ('&SYSLIST(&I)' NE '&DLM').ANDL
         AGO   .DELIM
.OR      ANOP
.ORL     AIF   ('&SYSLIST(&I)' EQ '').STRUCE
         AIF   ('&SYSLIST(&I+1)' EQ 'CASE').OR2   ***** RNB *****
         AIF   ('&SYSLIST(&I+1)' EQ '&DLM').ORN        FOUND LAST REL
.OR2     STRUCREL  &SYSLIST(&I),TF=T,TO=&TRUE,RW=&RW
         AIF   ('&SYSLIST(&I+1)' EQ 'CASE').DELIM ***** RNB *****
&LABEL   SETC  'YES'               GEN LABEL, WE BRANCH TO IT
&I       SETA  &I+1
         AGO   .ORL
.ORN     STRUCREL &SYSLIST(&I),TF=F,TO=&FALSE,RW=&RW
&I       SETA  &I+1                POINTER TO DELIM
         AGO   .DELIM
.DELIM   AIF   ('&DLM' EQ 'CASE').DELIM9   **** RNB **** FOR CASE
         AIF   ('&SYSLIST(&I)' NE '&DLM').STRUCE
         AIF   ('&DLM' EQ 'THEN').DELIMT
         AIF   ('&DLM' EQ 'ENDDO').DELIMN
         AIF   ('&DLM' EQ 'DO').DELIMD
         AGO   .STRUCE                   * WOW
.DELIMN  ENDDO LABEL=&LABEL
         AGO   .DELIM9
.DELIMD  SDO   LABEL=&LABEL
         AGO   .DELIM9
.DELIMT  THEN  LABEL=&LABEL
.DELIM9  ANOP
         MEXIT
.STRUCE  MNOTE 12,'STRUCTURE ERROR - THE KEYWORD &DLM IS MISSING'
         MEXIT
.BCTE1   MNOTE 08,'BCT ONLY WORKS ON REGISTERS'
         AIF   ('&P3' NE '&DLM').STRUCE
         MEXIT
         MEND
