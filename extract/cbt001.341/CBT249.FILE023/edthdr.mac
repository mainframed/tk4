         TITLE 'EDTHDR  F O R M A T  R E C O R D  P R E F I X'
         PRINT NOGEN
         DMSMACRO
         PRINT ON,GEN
*****************************************
*                                       *
*                                       *
*        E D T H D R                    *
*                                       *
*****************************************
         SPACE
EDTHDR   PGMSTART
         USING DMSCONST,DMSREG
         USING BLKBUFF,R7
         LR    R7,R1                    PARM
         LA    R9,14(R2)                POINT TO ACTUAL TEXT
         MVI   0(R9),C' '               CLEAR
         MVC   1(120,R9),0(R9)          LINE
         MVC   0(4,R9),=CL4'PSW'
         MVC   EDTWORK1(8),BLKPSW       SAVE PSW FOR
         XC    EDTWORK1+3(2),EDTWORK1+3 CLEAR INTERRUPT CODE
         UNPK  4(9,R9),EDTWORK1(5)      MOVE
         UNPK  13(9,R9),EDTWORK1+4(5)   PSW
         TR    4(17,R9),HDRTRTAB        TO
         MVI   12(R9),64                LINE
         MVC   21(2,R9),HDRTRTAB
         CLI   BLKJOBN,0                IS JOBNAME PRESENT
         BE    HDR001                   NO, FORMAT ASCB
         MVC   23(4,R9),=CL4'JOB'       MOVE
         MVC   27(8,R9),BLKJOBN
         MVC   35(16,R9),HDRTRTAB
         B     HDR002
HDR001   MVC   23(4,R9),=CL4'ASCB'      MOVE
         MVC   38(4,R9),=CL4'TCB'       ASCB
         UNPK  27(9,R9),BLKASCB(5)      AND
         UNPK  41(9,R9),BLKTCB(5)       TCB
         TR    27(8,R9),HDRTRTAB        TO
         TR    41(8,R9),HDRTRTAB        LINE
         MVC   27(2,R9),HDRTRTAB
         MVC   41(2,R9),HDRTRTAB
         MVC   49(2,R9),HDRTRTAB        BLANK OUT BAD FIELDS
HDR002   MVC   71(7,R9),=CL7'SERIAL'    MOVE
         UNPK  78(9,R9),BLKCPUID(5)     CPU
         TR    78(8,R9),HDRTRTAB        SERIAL
         MVC   86(2,R9),HDRTRTAB        NUMBER
         MVC   88(5,R9),=CL5'TIME'
         MVC   EDTWORK1(8),BLKCLOCK     MOVE
         LM    R4,R5,EDTWORK1           TIME
         SRDL  R4,12                    TO
         D     R4,=F'60000000'          LINE
         CVD   R4,EDTWORK2
         UNPK  97(8,R9),EDTWORK2(8)
         OI    104(R9),240
         MVC   96(2,R9),97(R9)
         MVI   98(R9),C'.'
         MVC   105(2,R9),HDRTRTAB
         LR    R4,R5
         SRDL  R4,32
         D     R4,=F'60'
         CVD   R4,EDTWORK2
         UNPK  93(2,R9),EDTWORK2+6(2)
         OI    94(R9),240
         MVI   95(R9),C'.'
         MVC   107(4,R9),=CL4'REC '
         UNPK  111(7,R9),BLKSEQ(4)
         TR    111(6,R9),HDRTRTAB
         MVC   117(6,R9),HDRTRTAB
         MVC   59(6,R9),=CL6'MODEL'     MOVE
         UNPK  65(5,R9),BLKCPUID+4(3)   CPU
         TR    65(4,R9),HDRTRTAB        MODEL
         MVI   69(R9),64                TO LINE
         ICM   R4,3,0(R2)               SAVE LINE COUNT
         MVC   0(2,R2),=H'137'          SET NEW LINE COUNT
         PRINTER 0(R2)                  PRINT LINE
         STCM  R4,3,0(R2)               RESET OLD LINE COUNTER
HDRETURN L     R13,4(R13)
         LM    R14,R12,12(R13)
         SR    R15,R15
         BR    R14                      RETURN
         EJECT
EDTWORK1 DC    1D'0'
EDTWORK2 DC    1D'0'
HDRTRTAB DC    256X'40'
         ORG   HDRTRTAB+C'A'
         DC    X'0A0B0C0D0E0F'
         ORG   HDRTRTAB+C'0'
         DC    C'0123456789ABCDEF'
         LTORG
         END
