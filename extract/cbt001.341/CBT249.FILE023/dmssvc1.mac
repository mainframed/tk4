         TITLE 'SVC1  S P E C I A L  S V C  R O U T I N E'
         PRINT NOGEN
         DMSMACRO
         PRINT ON,GEN
         EJECT
DMSSVC1  CSECT
         USING *,R11
         USING DMSCONST,DMSREG
         USING PSA,PSAREG
         LR    R11,R15                  ASSIGN BASE REGISTER
         LM    R2,R3,PREGSAVE           GET CALLERS REGISTER 0 AND 1
         LM    R4,R5,PREGSAVE+56        CALLERS REGISTER 14 AND 15
         STM   R2,R5,PSUSER             SAVE THEM TO WORK
         LA    R0,16                    SAVE 4 REGISTERS
         LA    R1,PSUSER                POINT TO LOCATION
         BALR  R12,R13                  SAVE DATAS
         LH    R15,FLCSVCN              GET SVC NUMBER
         SLL   R15,2                    MULTIPLY BY FOUR
         L     R15,DSVCTAB0(R15)        -> OF SPECIAL RTN
         LTR   R15,R15                  IS THERE A RTN
         BZR   R14                      NO, GO BACK
         LA    R11,0(R11,R15)           YES, POINT TO IT
         L     R3,PREGSAVE+4            GET CALLERS REGISTER 1
         BR    R11                      GO TO SPECIAL SUBROUTINE
         DS    0D
         EJECT
DSVCTAB0 DS    0H
         DC    256AL4(0)
         ORG   DSVCTAB0+(4*126)         SVC 126
         DC    AL4(MSSSVC-DMSSVC1)      OFFSET TO RTN
         ORG   DSVCTAB0+(4*256)         END OF TABLE
         DS    0D
         EJECT
MSSSVC   DS    0H
         DROP  R11                      R3 IS CALLERS R1
         USING *,R11
         SR    R15,R15                  CLEAR FOR RETURN
         LRA   R4,0(R3)                 IS IT GOOD ADDRESS
         BNER  R14                      NO, SO GO BACK
         LR    R1,R3                    POINT TO DATA
         L     R0,0(,R1)                GET LENGTH OF PARM
         BALR  R12,R13                  SAVE INITIAL PARMLIST
         LTR   R15,R15                  COULD IT BE SAVED
         BNZR  R14                      NO, SO RETURN
         IC    R15,4(R3)                GET OP-CODE
         IC    R15,MSSTAB(R15)          IS THERE AN EXTENSION
         LTR   R15,R15
         BZR   R14                      NO, GO BACK
         L     R1,0(R15,R3)             GET EXTENSION ADDRESS
         SR    R15,R15
         LTR   R1,R1                    IS IT VALID ADDRESS
         BNPR  R14                      NO, SO RETURN
         LRA   R4,0(R1)                 IS IT GOOD ADDRESS
         BNER  R14                      NO RETURN
         L     R0,0(R1)                 FIRST WORD CONTAINS LENGTH
         BALR  R12,R13                  SAVE PARMLIST EXTENSION
         SR    R15,R15                  CLEAR RETURN CODE
         BR    R14                      AND GO BACK
         DS    0D
         EJECT
MSSTAB   DC    256AL1(0)
         ORG   MSSTAB+X'0E'
         DC    X'2C'
         ORG   MSSTAB+X'18'
         DC    X'80'
         ORG   MSSTAB+X'2A'
         DC    X'24'
         ORG   MSSTAB+256
         SPACE 3
         CSECTID DMSSVC1
         END
