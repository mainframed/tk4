         PRINT NOGEN
         DMSMACRO TYPE=DSECT
         PRINT ON,GEN
DSP0     TITLE 'D M S D S P 0  DISPATCHER EVENT SAVE ROUTINE'
         SPACE 3
*****************************************
*                                       *
*        D M S D S P 0                  *
*                                       *
*****************************************
         SPACE 3
DMSDSP0  CSECT
         USING *,R11
         USING DMSCONST,DMSREG
         USING PSA,PSAREG
         LR    R11,R15                  SET BASE
         LM    R1,R2,PREGSAVE           GET CALLERS REGISTER 0 AND 1
         LM    R3,R4,PREGSAVE+56        GET CALLERS REGISTER 14 AND 15
         STM   R1,R4,PSUSER             SAVE THEM TO WORKSPACE
         SR    R4,R4                    CLEAR REGISTER
         CLI   PSDSPCDE+3,X'06'         IS IT VALID CODE
         BH    DSPCODE7                 NO, USE CALLEES REGISTER
         ICM   R4,1,PSDSPCDE+3          GET CODE
         BZ    DSPCODE7                 SAVE CALLERS IF NOT VALID
         SLL   R4,2                     MULTIPLY BY FOUR
         B     *(R4)                    USE TABLE
         B     DSPCODE1                 SRB DISPATCH
         B     DSPCODE2                 WAIT TASK DISPATCH
         B     DSPCODE3                 SUSPENDED SRB DISPATCH
         B     DSPCODE4                 LOCAL SVR DISPATCH
         B     DSPCODE5                 TASK DISPATCH
         B     DSPCODE6                 TASK DISPATCH FROM EXIT
DSPCODE1 B     DSPCODE7                 REGISTERS ARE VALID
DSPCODE2 XC    PSUSER(16),PSUSER        REGISTERS ARE ZERO
         B     DSPCODE7
DSPCODE3 L     R5,PREGSAVE+20           GET CALLERS REGISTER 5
         MVC   PSUSER(8),0(R5)          MOVE REGISTER 0 + 1
         MVC   PSUSER+8(8),56(R5)       MOVE REGISTER 14 + 15
         B     DSPCODE7
DSPCODE4 L     R5,PREGSAVE+48           GET CALLERS GEGISTER 12
         MVC   PSUSER(8),56(R5)         REGISTER 0 + 1
         MVC   PSUSER+8(8),112(R5)      REGISTER 14 + 15
         B     DSPCODE7                 AND SAVE
DSPCODE5 L     R5,PREGSAVE+24           GET CALLERS REGISTER 6
         MVC   PSUSER(8),48(R5)         GET REGISTERS 0 + 1
         MVC   PSUSER+8(8),104(R5)      REGISTERS 14 + 15
         B     DSPCODE7
DSPCODE6 L     R5,PREGSAVE+16           GET CALLERS REGISTER 4
         MVC   PSUSER(8),48(R5)         REGISTER 0 + 1
         MVC   PSUSER+8(8),104(R5)      REGISTERS 14 + 15
DSPCODE7 DS    0H                       SAVE REGISTERS CHOOSEN
         LA    R1,PSUSER(R0)            ADDRESS OF DATA
         LA    R0,16                    LENGTH OF DATA
         BALR  R12,R13                  GO TO SAVE RTN
         SR    R15,R15                  INDICATE RECORD TO BE SAVED
         BR    R14                      RETURN
         CSECTID DMSDSP0
         END
