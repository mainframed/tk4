         PRINT NOGEN
*
*        EXAMPLE OF SYSIN INPUT
*
*
*        ./  REP  SEQ1=7940
*
*
*        ./  DEL  SEQ1=1520,SEQ2=1580
*
*
*        ./  INS  SEQ1=1575
*
*
*        ./  LIST
*
*
DMSUPDTE CSECT
         USING *,11
         STM   14,12,12(13)
         LR    11,15
         LA    12,DMSAVE
         ST    13,4(12)
         ST    12,8(13)
         LR    13,12
         OPEN  (SYSIN,(INPUT),SYSPRINT,(OUTPUT),SYSUT1,(INPUT),        C
               SYSUT2,(OUTPUT))
RLOOP    GET   SYSIN,INREC
RCLC     BAL   14,SYNTAX
         CLC   INREC+9(3),=CL3'INS'
         BE    INSERT
         CLC   INREC+9(4),=CL4'LIST'
         BE    LIST
         CLC   INREC+9(3),=CL3'DEL'
         BE    DELETE
         CLC   INREC+9(3),=C'REP'
         BE    REPLACE
ERROR    LA    15,8
         ST    15,RETCODE
RETURN   CLOSE (SYSIN,,SYSPRINT,,SYSUT1,,SYSUT2)
         L     15,RETCODE
         LTR   15,15
         BNZ   ABEND
         L     13,4(13)
         L     14,12(13)
         LM    0,12,20(13)
         BR    14
ABEND    ABEND 1,DUMP
         EJECT
LIST     GET   SYSUT1,UT1REC
         MVC   LDATA(80),UT1REC
         BAL   14,RECSEQ
         BAL   3,PLINE
         B     LIST
         SPACE 3
DELETE   LA    5,INREC+20
         LA    6,SEQ1
         BAL   14,SEQNO
         LA    5,6(5)
         LA    6,SEQ2
         BAL   14,SEQNO
DELLOOP  GET   SYSUT1,UT1REC
         MVC   LDATA(80),UT1REC
         BAL   14,RECSEQ
         CLC   LDATA+81(5),SEQ1
         BL    DEL04
         CLC   LDATA+81(5),SEQ2
         BH    RLOOP
DELCLC   MVC   LDATA+95(7),=CL7'DELETED'
         BAL   3,PLINE
         MVC   LDATA+95(7),=CL8' '
         CLC   LDATA+81(5),SEQ2
         BE    RLOOP
         BH    RLOOP
         B     DELLOOP
DEL04    BAL   14,OUTNUM
         PUT   SYSUT2,LINE
         BAL   3,PLINE
         MVC   LDATA+110(6),=CL8' '
         B     DELLOOP
         EJECT
INSERT   LA    5,INREC+20
         LA    6,INSEQ
         BAL   14,SEQNO
INSLOOP  GET   SYSUT1,UT1REC
         MVC   LDATA(80),UT1REC
         BAL   14,RECSEQ
         CLC   LDATA+81(5),INSEQ
         BE    INS05
         BH    INS03
         BAL   14,OUTNUM
         PUT   SYSUT2,LINE
         BAL   3,PLINE
         MVC   LDATA+110(6),=CL8' '
         MVC   LASTSEQ(5),INSEQ
         MVI   LASTSEQ+4,C'0'
         MVI   FLAG,X'FF'
         CLC   LASTSEQ(5),LDATA+81
         BE    INS03
         MVI   FLAG,X'00'
         B     INSLOOP
INS03    GET   SYSIN,INREC
         CLC   INREC(2),=CL2'./'
         BE    INS04
         MVC   LDATA(80),INREC
         MVC   LINE+95(8),=CL8'INSERTED'
         BAL   14,OUTNUM
         PUT   SYSUT2,LINE
         BAL   3,PLINE
         MVC   LDATA+110(6),=CL8' '
         MVC   LINE+95(8),=CL8' '
         B     INS03
INS04    BAL   14,SYNTAX
         CLC   INREC+9(3),=C'DEL'
         BNE   INS07
         LA    5,INREC+20
         LA    6,DELSEQ
         BAL   14,SEQNO
         CLC   DELSEQ(5),LDATA+81
         BNE   INS07
         MVC   SEQ1(4),DELSEQ
         LA    5,6(5)
         LA    6,SEQ2
         BAL   14,SEQNO
         MVC   LDATA(80),UT1REC
         B     DELCLC
INS07    TM    FLAG,X'FF'
         MVI   FLAG,X'00'
         BO    RCLC
         TM    FLAG+1,X'FF'
         MVI   FLAG+1,X'00'
         BO    RCLC
         BAL   14,OUTNUM
         MVC   LDATA(80),UT1REC
         PUT   SYSUT2,LINE
         BAL   3,PLINE
         MVC   LDATA+110(6),=CL8' '
         B     RCLC
INS05    MVC   LDATA+95(8),=CL8'REPLACED'
         BAL   3,PLINE
         MVC   LDATA+95(8),=CL8' '
         MVI   FLAG+1,X'FF'
         B     INS03
INEOF    GET   SYSUT1,UT1REC
         MVC   LDATA(80),UT1REC
         BAL   14,RECSEQ
         BAL   14,OUTNUM
         PUT   SYSUT2,LINE
         BAL   3,PLINE
         MVC   LDATA+110(6),=CL8' '
         B     INEOF
UT1EOF   B     RETURN
         EJECT
REPLACE  DS    0H
         LA    5,INREC+20
         LA    6,REPSEQ
         BAL   14,SEQNO
REPLOOP  GET   SYSUT1,UT1REC
         MVC   LDATA(80),UT1REC
         BAL   14,RECSEQ
         CLC   LDATA+81(5),REPSEQ
         BE    REP05
         BL    *+8
         BAL   15,ERROR
         BAL   14,OUTNUM
         PUT   SYSUT2,LINE
         BAL   3,PLINE
         MVC   LDATA+110(6),=CL8' '
         B     REPLOOP
REP05    MVC   LDATA+95(8),=CL8'REPLACED'
         BAL   3,PLINE
         MVC   LDATA+95(8),=CL8' '
         GET   SYSIN,INREC
REPCLC   CLC   INREC(2),=CL2'./'
         BNE   *+8
         BAL   15,ERROR
         MVC   LDATA(80),INREC
         MVC   LINE+93(11),=CL11'REPLACEMENT'
         BAL   14,OUTNUM
         PUT   SYSUT2,LINE
         BAL   3,PLINE
         MVC   LINE+93(8),=CL8' '
         MVC   LINE+96(8),=CL8' '
         MVC   LDATA+110(6),=CL8' '
REP06    GET   SYSIN,INREC
         CLC   INREC(2),=CL2'./'
         BE    RCLC
         GET   SYSUT1,UT1REC
         MVC   LDATA(80),UT1REC
         BAL   14,RECSEQ
         MVC   LDATA+95(8),=CL8'REPLACED'
         BAL   3,PLINE
         MVC   LDATA+95(8),=CL8' '
         B     REPCLC
         EJECT
OUTNUM   L     15,OUTSEQ
         LA    15,10(15)
         ST    15,OUTSEQ
         CVD   15,WORK1
         UNPK  LDATA+110(5),WORK1(8)
         OI    LDATA+114,240
         BR    14
         SPACE 3
SEQNO    DS    0H
         L     4,=F'-1'
         LR    7,5
         MVC   0(5,6),=CL5'00000'
         LA    6,5(6)
         TM    0(5),X'F0'
         BO    *+8
         BAL   15,ERROR
SEQ01    CLI   0(5),C','
         BE    SEQ02
         CLI   0(5),C' '
         BE    SEQ02
         LA    4,1(4)
         LA    5,1(5)
         BCTR  6,0
         B     SEQ01
SEQ02    EX    4,MOVE1
         BR    14
         SPACE 3
RECSEQ   DS    0H
         L     15,RECNUM
         LA    15,10(15)
         ST    15,RECNUM
         CVD   15,WORK1
         UNPK  LDATA+81(5),WORK1(8)
         OI    LDATA+85,240
         BR    14
         EJECT
SYNTAX   STM   4,10,SYNSAVE
         MVC   SYNREC(80),INREC
         MVI   INREC,64
         MVC   INREC+1(79),INREC
         MVC   INREC(2),=CL2'./'
         LA    4,SYNREC+2
         LA    5,78
SYNLOOP  CLI   0(4),64
         BNE   SYN00
         LA    4,1(4)
         BCT   5,SYNLOOP
         BAL   15,ERROR
SYN00    CLC   0(5,4),=CL5'LIST '
         BE    SYNLIST
         CLC   0(4,4),=CL4'INS '
         BE    SYNDEL
         CLC   0(4,4),=CL4'REP '
         BE    SYNDEL
         CLC   0(4,4),=CL4'DEL '
         BE    SYNDEL
         BAL   15,ERROR
SYNLIST  MVC   INREC+9(5),=CL5'LIST '
         B     SYNRET
SYNDEL   MVC   INREC+9(4),0(4)
         LA    4,4(4)
         LA    5,SYNREC+73
SYNREPLP CLR   4,5
         BL    *+8
         BAL   15,ERROR
         CLI   0(4),64
         BNE   SYNREP01
         LA    4,1(4)
         B     SYNREPLP
SYNREP01 CLC   0(5,4),=CL5'SEQ1='
         BE    *+8
         BAL   15,ERROR
         MVC   INREC+15(5),=CL5'SEQ1='
         LA    4,5(4)
         LA    7,INREC+20
         TM    0(4),X'F0'
         BO    *+8
         BAL   15,ERROR
         LA    6,6
SYNCLI   CLI   0(4),C' '
         BE    SYNRET
         CLI   0(4),C','
         BE    SYNSEQ2
         TM    0(4),X'F0'
         BO    *+8
         BAL   15,ERROR
         MVC   0(1,7),0(4)
         LA    4,1(4)
         LA    7,1(7)
         BCT   6,SYNCLI
         BAL   15,ERROR
SYNSEQ2  CLC   INREC+9(4),=CL4'DEL '
         BE    *+8
         BAL   15,ERROR
         MVC   0(1,7),0(4)
         LA    4,1(4)
         LA    7,1(7)
         CLC   0(5,4),=CL5'SEQ2='
         BE    *+8
         BAL   15,ERROR
         MVC   0(5,7),0(4)
         LA    4,5(4)
         LA    7,5(7)
         LA    6,6
         TM    0(4),X'F0'
         BO    SYNCLI
         BAL   15,ERROR
SYNRET   LM    4,10,SYNSAVE
         BR    14
         SPACE 3
PLINE    LH    0,LCOUNT
         BCTR  0,0
         STH   0,LCOUNT
         LTR   0,0
         BP    NOSKIP
         MVC   LCOUNT(2),=AL2(64)
         PUT   SYSPRINT,SKIP
NOSKIP   PUT   SYSPRINT,LINE-1
         BR    3
         EJECT
MOVE1    MVC   0(1,6),0(7)
WORK1    DC    1D'0'
DMSAVE   DC    18F'0'
SYNSAVE  DC    8F'0'
SEQ1     DC    CL8'00000000'
SEQ2     DC    CL8'00000000'
INSEQ    DC    CL8'00000000'
DELSEQ   DC    CL8'00000000'
REPSEQ   DC    CL8'00000000'
LASTSEQ  DC    CL8'00000000'
RETCODE  DC    F'0'
RECNUM   DC    F'0'
OUTSEQ   DC    F'0'
FLAG     DC    F'0'
INREC    DC    CL80' '
UT1REC   DC    CL80' '
SYNREC   DC    CL80' '
LCOUNT   DC    H'64'
SKIP     DC    C'1',CL121' '
LDATA    EQU   *
LINE     DC    121C' '
SYSIN    DCB   DSORG=PS,RECFM=FB,LRECL=80,MACRF=(GM),EODAD=INEOF,      C
               DDNAME=SYSIN,BLKSIZE=400
SYSUT1   DCB   DSORG=PS,RECFM=FB,LRECL=80,MACRF=(GM),EODAD=UT1EOF,     C
               DDNAME=SYSUT1
SYSPRINT DCB   DSORG=PS,RECFM=FBA,LRECL=121,MACRF=(PM),                C
               DDNAME=SYSPRINT,BLKSIZE=1210
SYSUT2   DCB   DSORG=PS,RECFM=FB,LRECL=80,MACRF=(PM),                  C
               DDNAME=SYSUT2
         LTORG
         END
