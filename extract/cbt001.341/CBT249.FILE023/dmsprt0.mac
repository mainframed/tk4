         TITLE 'DMSPRT0 / STANDART UTILITY PRINT ROUTINE '
* * * * * * * * * * * * * * * * * * * * *
*                                       *
*              D M S P R T 0            *
*                                       *
* * * * * * * * * * * * * * * * * * * * *
         SPACE
* FUNCTION - OPERATION
*        THIS ROUTINE CONTROLS THE PRINT-OUTPUT AND THE CONSOLE
*        MESSAGES FOR THE DYNAMIC MONITOR SYSTEM
         SPACE
* INPUT
*        R1 POINTS TO THE VARIABLE LENGTH RECORD TO BE PROCESSED
*           BIT0 FROM R1 INDICATES THE REQUIRED FUNCTION
*
*           0  PRINT ONLY
*           1  PRINT AND WTO IF POSSIBLE
         SPACE
* OUTPUT
*        PRINTED RECORD AND IF REQUESTED, WRITTEN OPERATOR MSG OF UP
*        TO 70 CHARACTERS
         SPACE
         PRINT ON,NOGEN
       DMSMACRO
         EJECT
DMSPRT0  PGMSTART
         SPACE
         LR    R2,R1                    SAVE PARAMETER
         TM    PRTFLAG,PRTCONFO         IS FORCE TO CONSOLE ON (LIST)
         BO    PRTFORCE                 YES, FORCE LINE TO CONSOLE
         LTR   R2,R1                    PRINT ONLY ?
         BP    PRT003                   YES, GO
         TM    PRTFLAG,PRTCONS          OPERATOR COMMUNICATION ?
         BZ    PRT003                   PRINT ONLY
PRTFORCE DS    0H
         MVC   PRTWTO+4(70),5(R2)       SET WTO BUFFER
         MVI   PRTWTO+1,74              SET MAX LENGTH
         LH    R3,0(R2)                 GET INPUT LENGTH
         LA    R4,PRTWTO-1(R3)          FIRST BYTE BEHIND
         LA    R3,PRTWTO+74             MAX WTO LENGTH
PRT001   CLR   R4,R3                    IS INPUT > BUFFER
         BNL   PRT002
         MVI   0(R4),64
         LA    R4,1(R4)
         B     PRT001
         SPACE
PRT002   WTO   MF=(E,PRTWTO)
         SPACE
PRT003   L     R3,LINEREST              RESIDUAL LINE COUNT
         IC    R4,4(R2)                 SAVE CONTROL CHARACTER
         LA    R6,PRTDCB
         TM    PRTFLAG,PRTOPEN          IS DCB OPEN
         BO    PRT004                   YES, SKIP OPEN
         ST    R6,PRTDCBP               SAVE IT IN SHARED CONSTANTS
         OPEN  ((R6),OUTPUT)            OPEN SYSPRINT
         LA    R1,PRTDCB                BASE PRINT DCB
         TM    DCBOFLGS,DCBOFOPN        IS IT OPEN
         BO    PRT005                   YES, CONTINUE
         OI    DMSFLAG1,NOGO            SET NOGO SWITCH
         B     PRTRET0                  RETURN
PRT005   OI    PRTFLAG,PRTOPEN          INDICATE PRINT DCB OPEN
         SPACE
PRT004   CLI   4(R2),C' '               SPACE 1
         BE    PRTSKP1
         CLI   4(R2),C'0'               SPACE 2
         BE    PRTSKP2
         CLI   4(R2),C'-'               SPACE 3
         BE    PRTSKP3
         CLI   4(R2),C'1'               NEWPAGE
         BE    PRTPAGE
         CLI   4(R2),C'+'               SPACE 0
         BE    PRTSKP0
         MVI   4(R2),C' '               DEFAULT SPACE 1
         B     PRTSKP1
         SPACE
PRTSKP3  BCTR  3,0                      DECREMENT
PRTSKP2  BCTR  3,0                      LINE
PRTSKP1  BCTR  3,0                      COUNT
PRTSKP0  LTR   3,3
         BM    PRTPAGE                  SET NEW PAGE
         SPACE
         PUT   (R6),(R2)
         ST    R3,LINEREST              SAVE RESIDUAL COUNT
         SPACE
         STC   R4,4(R2)                 RESTORE CONTROL CHARACTER
         B     PRTRET0
         SPACE
PRTPAGE  L     R1,PAGECNT               INCREMENT
         LA    R1,1(R1)                 PAGE
         ST    R1,PAGECNT               COUNT
         CVD   R1,DMSWORK1              PREPARE
         UNPK  PRTTEXT(5),DMSWORK1(8)   TITLE
         OI    PRTTEXT+4,240
         STM   R0,R14,PRT0SAVE          SAVE REGISTERS
         MVC   PRTTITDT-5(30),=CL30' '
         TM    DMSFUNCT,DMSEDT          ARE WE IN EDIT MODE
         BZ    PRT0RET                  NO, GO BACK
         CLI   1(R2),106                IS RECORD MORE THAN 106
         BL    PRT0RET                  NO, GO BACK
         CLC   102(4,R2),=CL4'TIME'     IS TIME TO BE PRINTED
         BE    PRT0PRT                  YES, PRINT
         CLC   14(4,R2),=CL4'GPRS'      IS IR GPRS LINE
         BE    PRT0PRT                  YES, PRINT
         CLC   14(3,R2),=CL3'PSW'       IS IT PSW LINE
         BE    PRT0PRT                  YES, PRINT
         CLC   22(4,R2),=CL4'CPU='      IS IT CPU LINE
         BE    PRT0PRT                  YES, PRINT
         B     PRT0RET                  DO SKIP DATE INSERTION
PRT0PRT  DS    0H
         CLC   DMSEDTSC(8),=XL8'00'     IS CLOCK VALUE IN
         BE    PRT0RET                  NO, SO DO NOT PRINT
         MVC   PRTTITDT-5(4),=CL4'DATE'
         MVC   PRTTITDT+10(11),=CL11'TIME HH.MM.'
         LM    R4,R5,DMSEDTSC           CLOCK VALUE
         SRDL  R4,12
         D     R4,=F'60000000'          NUMBER OF MINUTES (R5)
         LR    R6,R5                    SAVE MINUTES
         SRDL  R4,32
         D     R4,=F'1000000'
         CVD   R5,DMSWORK1
         UNPK  PRTTITTM+6(2),DMSWORK1+6(2) SS
         OI    PRTTITTM+7,240
         SRDL  R6,32
         D     R6,=F'60'
         CVD   R6,DMSWORK1
         UNPK  PRTTITTM+3(2),DMSWORK1+6(2) MM
         OI    PRTTITTM+4,240
         SLR   R6,R6
         D     R6,=F'24'
         CVD   R6,DMSWORK1
         UNPK  PRTTITTM(2),DMSWORK1+6(2)   HH
         OI    PRTTITTM+1,240
*        R7 HAS NUMBER OF DAYS
         S     R7,=F'365'               SUBTR 1900
         SLR   R6,R6
         D     R6,=F'1461'              DAYS PER 4 YEAR CYCLE
         MH    R7,=H'4'                 NR OF YEARS
         LR    R8,R7                    SAVE YEARS
         SRDL  R6,32                    NR OF
         D     R6,=F'365'               NORMAL YEARS
         LA    R7,1(R7,R8)              TOTAL YEARS
         CVD   R7,DMSWORK1
         UNPK  PRTTITDT(2),DMSWORK1+6(2)
         OI    PRTTITDT+1,240
         LA    R6,1(,R6)                ADD ONE FOR FIRST DAY
         CVD   R6,DMSWORK1
         UNPK  PRTTITDT+3(3),DMSWORK1+6(2)
         OI    PRTTITDT+5,240
         MVI   PRTTITDT+2,C'.'
PRT0RET  LM    R0,R14,PRT0SAVE          RELOAD REGISTERS
         PUT   (R6),PRTPTXT             WRITE TITLE
         SPACE
         L     R3,LINECNT               GET ORIGINAL LINE COUNT
         BCTR  R3,0                     LINE COUNT
         MVI   4(R2),C'0'               SET SPACE 2
         B     PRTSKP2                  PROCESS
         SPACE
PRTRET0  L     R13,4(R13)
         LM    R14,R12,12(R13)
         SR    R15,R15
         BR    R14
         SPACE 3
***
*        LOCAL CONSTANTS
***
         SPACE
PRTPTXT  DC    AL2(PRTPTXTE-PRTPTXT),AL2(0),C'1'
         DC    C'PAGE '
PRTTEXT  DC    C'00000'
         DC    77C' '
         DC    C'DATE '
PRTTITDT DC    C'JJ.DDD    '
         DC    C'TIME '
PRTTITTM DC    C'HH.MM.SS  '
PRTPTXTE EQU   *
         SPACE 2
PRTDCB   DCB    DDNAME=SYSPRINT,DSORG=PS,MACRF=(PM),                   X
               RECFM=VBA,LRECL=137,BLKSIZE=2000
         SPACE 2
PRTWTO   WTO   'TEXT - TEXT - TEXT - TEXT - TEXT - TEXT - TEXT - TEXT -X
                TEXT - TEXT- T',ROUTCDE=(2),MF=L
         SPACE
PRT0SAVE DC    16F'0'
         LTORG
         END
