*PROCESS OPT(TIME),LC(60);
 /*     W E E K D A Y   ---   DAY-OF-WEEK ROUTINE FOR RMFRPT          */
 WEEKDAY: PROC (TYPE,JUL,GREG,DAY_OF_WEEK);

    /* INPUT MUST BE DECLARED AS FOLLOWS: */
    DCL TYPE   FIXED BIN(31), /* 1-JUL GIVEN,2-GREG GIVEN,3-USE "DATE"*/
        JUL    FIXED DEC(7), /* 00YYDDDF */
        GREG   CHAR(6),       /* YYMMDD   */
        DAY_OF_WEEK CHAR(9);

    DCL DAYS_PER_MONTH(12) FIXED DEC(2) INIT(
            /* JN FB MA AP MY JU JL AU SP OC NV DC */
               31,28,31,30,31,30,31,31,30,31,30,31 ),
        DAY_NAMES(0:6) CHAR(9)  INIT('THURSDAY','FRIDAY',
            'SATURDAY','SUNDAY','MONDAY','TUESDAY','WEDNESDAY'),
       (YR76,DAYS76,J) FIXED BIN(31),
       (DATE,TRUNC,MOD,SUBSTR) BUILTIN,
       (YEAR,MONTH)          FIXED DEC(2),
       (DAYS,DAYS_JULIAN)    FIXED DEC(3);

    /* IF TYPE 3 GET GREG DATE FROM SYSTEM */
    IF TYPE = 3 THEN GREG = DATE;

    /* IF TYPE 2, SEPARATE INTO MONTH/DAYS/YEAR FROM GREG */
    IF TYPE >= 2 THEN DO;
       GET STRING(GREG ) EDIT(MONTH) (X(2),A(2),X(2));
       GET STRING(GREG ) EDIT(DAYS ) (X(4),A(2));
       GET STRING(GREG ) EDIT(YEAR ) (A(2),X(4));
       END;
    /* NOW IN FORM   MONTH,DAYS,YEAR   */

    /* IF TYPE 1, GET MONTH,DAYS,YEAR FROM JUL */
    IF TYPE = 1 THEN DO;
       YEAR = TRUNC ( JUL /1000 );
       /* SEE IF THIS IS A LEAP YEAR */
       IF MOD (YEAR,4) = 0 THEN  DAYS_PER_MONTH(2) = 29;
       DAYS = MOD ( JUL ,1000 );
       DO MONTH = 1 TO 12 WHILE (DAYS-DAYS_PER_MONTH(MONTH) > 0);
          DAYS = DAYS - DAYS_PER_MONTH(MONTH);
          END;
       /* GIVE BACK DATE IN GREG ALSO */
       PUT STRING(SUBSTR(GREG,5,2)) EDIT(DAYS ) (P'99');
       PUT STRING(SUBSTR(GREG,3,2)) EDIT(MONTH) (P'99');
       PUT STRING(SUBSTR(GREG,1,2)) EDIT(YEAR ) (P'99');
       END;
       /* NOW IN FORM   MONTH/DAYS/YEAR   */

    /* FIND DAY OF THE WEEK */
    YR76 = YEAR - 76;               /* YEARS SINCE 1976 */
    DAYS76 = 365 * YR76 + TRUNC(YR76/4); /* FIND DAYS SINCE 1/1/76 */
    DAYS_JULIAN = DAYS;
    DO J = 1 TO (MONTH-1);
       DAYS_JULIAN = DAYS_PER_MONTH(J) + DAYS_JULIAN;
       END;
    IF ( DAYS_JULIAN < 59 & MOD(YR76,4) = 0 ) THEN
       DAYS76 = DAYS76 - 1;  /* IN A LEAP YEAR BUT PRIOR TO 2/29 */
    DAYS76 = DAYS76 + DAYS_JULIAN;  /* GET TOT DAYS SINCE 1/1/76 */
    DAY_OF_WEEK = DAY_NAMES( MOD(DAYS76,7) );

    /* FOR TYPE 2, ALSO GIVE BACK JULIAN DATE */
    IF TYPE >= 2 THEN
       JUL = DAYS_JULIAN + 1000 * YEAR;

    RETURN;

 END WEEKDAY;
