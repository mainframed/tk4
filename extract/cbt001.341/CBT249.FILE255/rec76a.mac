*PROCESS OPT(TIME),LC(60);
 /*     R E C 7 6 A   ---   REC76 SUBROUTINE                          */
 REC76A: PROC( OPT, LEN, SETB, ENTRY_SET_A); /* VER 1.07 */
0/* REC76A EXTRACTS TRACE VALUES FROM 1 TRACE ENTRY SET.
    WRITTEN BY VERNE TICE, SCE, 1/18/78
    ARGUEMENTS:
      INPUT:
        OPT             TRACE OPTION FIELD (SEE RMF2 MANUAL)
        LEN             LENGTH OF FIELD BEING TRACED (2 OR 4)
                        CHECKED FOR CORRECTNESS BY CALLER
        SETB            CHAR STR (LEN 128) WHICH IS MAPPED ON
                        TRACE ENTRY
      OUTPUT:
        ENTRY_SET_A     MAX, SUM, MIN, SUM SQ, END VALUES FROM TRACE
                        ENTRY SET. (VALUES NOT PRESENT ARE SET TO
                        0.0)
 */
0DCL OPT                BIT(8) ALIGNED;
 DCL LEN                FIXED BIN(15);
 DCL SETB               CHAR(128);
 DCL SETBPTR            POINTER;
 DCL FLD(64)            BIT(16) ALIGNED BASED(SETBPTR);
 DCL HW                 FIXED BIN(15) STATIC;
 DCL J                  FIXED BIN(15) STATIC;
 DCL ENTRY_SET_A(5)     FLOAT DEC(15) CONNECTED;
 DCL FLD_LEN(5,2)     FIXED BIN(15) STATIC INIT(2,4,6,8,2,4,8,12,2,4);
 DCL I                FIXED BIN(15) STATIC;
 DCL FLDS_PRES        BIT(8) STATIC ALIGNED;
 DCL LENF             FIXED BIN(15) STATIC;
 DCL FLEN             FIXED BIN(15) STATIC;
 DCL (ADDR,SUBSTR)    BUILTIN;
0ENTRY_SET_A = 0E0;
 FLDS_PRES = '0'B;
 IF ( OPT & '00000011'B ) = '00000011'B THEN RETURN;
 IF OPT & '00000100'B THEN FLDS_PRES = FLDS_PRES × '11111000'B;
 IF OPT & '10000000'B THEN FLDS_PRES = FLDS_PRES × '10000000'B;
 IF OPT & '01000000'B THEN FLDS_PRES = FLDS_PRES × '00100000'B;
 IF OPT & '00100000'B THEN FLDS_PRES = FLDS_PRES × '01000000'B;
 IF OPT & '00010000'B THEN FLDS_PRES = FLDS_PRES × '01010000'B;
 IF OPT & '00001000'B THEN FLDS_PRES = FLDS_PRES × '00001000'B;
 LENF = LEN / 2;
 HW = 1;
 SETBPTR = ADDR(SETB);
 DO I = 1 TO 5;
    IF SUBSTR(FLDS_PRES,I,1) THEN DO;
       FLEN = FLD_LEN(I,LENF);
       DO J = 2 TO FLEN BY 2;
          ENTRY_SET_A(I) = ENTRY_SET_A(I) * 65536 +
              FLD(HW);
          HW = HW + 1;
          END;
       END;
    END;
0END; /* REC76A */
