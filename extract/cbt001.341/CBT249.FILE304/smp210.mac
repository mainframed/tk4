*          DATA SET SMP210     AT LEVEL 027 AS OF 06/18/82
         TITLE 'IGGPRE00 - DFDS DADSM PRE EXECUTION EXIT'
**********************************************************************
**********************************************************************
**                                                                  **
**      MODULE  -- IGGPRE00                                         **
**      PURPOSE -- THIS EXIT WILL BE USED TO CONTROL ALLOCATION ON  **
**                 VOLUMES.                                         **
**                 IF ALLOCATION IS ALLOWED THEN ALSO INITIALIZE    **
**                 THE DATE-LAST-USED FIELD SO THAT OPEN WILL CON-  **
**                 TINUE TO UPDATE IT.                              **
**                 MODULE ISSUES RACHECK.  IF PROFILE FOR VOLUME    **
**                 EXISTS, THEN VOLUME IS ASSUMED TO BE PROTECTED   **
**                 AND USER MUST HAVE 'READ' AUTHORITY TO ALLOCATE. **
**                                                                  **
**********************************************************************
**********************************************************************
IGGPRE00 CSECT ,                   DFDS DADSM PRE EXECUTION EXIT
         SAVE  (14,12),,*          SAVE REGISTERS
         LR    R12,R15             PROGRAM ADDRESSABILITY
         USING IGGPRE00,R12        ******
         LR    R2,R1               SAVE PARM LIST ADDRESS IN R2
         USING PREPL,R2            ******
         EJECT
**********************************************************************
**********************************************************************
**                                                                  **
**           EXIT TO BE ENTERED ONLY IF ALLOCATE REQUEST            **
**                                                                  **
**********************************************************************
**********************************************************************
         CLI   IEXFUNC,IEXALL      IS THIS AN ALLOCATE REQUEST?
         BE    RUNEXIT             GET OUT QUICKLY IF NOT
EXIT     RETURN (14,12),T,RC=0     RESTORE REGISTERS AND RETURN
         EJECT
**********************************************************************
**********************************************************************
**                                                                  **
**           EXIT TO BE ENTERED ONLY IF NON-VIO DATASET             **
**                                                                  **
**********************************************************************
**********************************************************************
RUNEXIT  TM    IEXFLAG,IEXVIO      ALLOCATE VIO DATASET?
         BO    EXIT                YES - GET OUT
*
****IEXDSN IS POINTER TO DSN
*
         L     R0,DYNSIZE          DYNAMIC AREA SIZE TO R0
         GETMAIN R,                GETMAIN                             X
               LV=(0)              DYNAMIC AREA
         LR    R11,R1              DYNAMIC AREA ADDRESSABILILTY
         USING DATD,R11            ******
         ST    R13,SAVEAREA+4      SAVE CALLER'S SAVEAREA ADDR
         ST    R11,8(,R13)         SAVE OUR SAVEAREA ADDR
         LR    R13,R11             OUR SAVEAREA ADDR TO R13
         SR    R3,R3               PRIME RETURN CODE
         EJECT
**********************************************************************
**********************************************************************
**                                                                  **
**         PASS BACK A DATE FOR DATE-LAST-USED FIELD IN DSCB        **
**                                                                  **
**********************************************************************
**********************************************************************
         TM    IEXFLAG,IEXMF1      ALREADY HAVE A MODEL?
         BO    MODEL               NO - USE DUMMY DSCB
         SPACE 3
**********************************************************************
**********************************************************************
**                                                                  **
**               IF NO MODEL, SET A DUMMY EARLY DATE                **
**                                                                  **
**********************************************************************
**********************************************************************
         LA    R8,DS1FMTID         GET BEGINNING OF DUMMY DSCB
         ST    R8,IEXFMT1          AND PASS BACK TO DADSM
         B     TECHECK             CHECK IF ALLOCATION ALLOWED
         SPACE 3
**********************************************************************
**********************************************************************
**                                                                  **
**            DETERMINE DATE TO SET DATE-LAST-USED FIELD            **
**                                                                  **
**********************************************************************
**********************************************************************
MODEL    DS    0H                  *****
         TIME  DEC                 GET TIME AND DATE - 00YYDDDF
         STCM  R1,3,DOUBLE+6       STORE DATE - DDDF
         CVB   R0,DOUBLE           CONVERT DATE TO BINARY
         STCM  R0,3,CURRDATE+1     STORE BINARY DATE - DDDD
         SRL   R1,12               SHIFT YEAR - 00000YYD
         STCM  R1,3,DOUBLE+6       STORE YEAR - 0YYD
         OI    DOUBLE+7,X'0F'      MAKE VALID PACKED DEC - 00000YYD
         CVB   R0,DOUBLE           CONVERT YEAR TO BINARY
         STC   R0,CURRDATE         STORE BINARY YEAR - YYDDDD
         EJECT
**********************************************************************
**********************************************************************
**                                                                  **
**                  INITIALIZE DATE-LAST-USED FIELD                 **
**                                                                  **
**********************************************************************
**********************************************************************
         L     R8,IEXFMT1          GET DSCB POINTER
         MVC   DS1REFD-DS1FMTID(L'DS1REFD,R8),CURRDATE
*                                  UPDATE FIELD WITH CURRENT DATE
         SPACE 3
**********************************************************************
**********************************************************************
**                                                                  **
**           CHECK WITH RACF TO SEE IF ALLOWED TO ALLOCATE          **
**                                                                  **
**********************************************************************
**********************************************************************
TECHECK  DS    0H
         L     R6,X'10'            ADDRESS OF CVT       (KJD)
         USING CVT,R6                                   (KJD)
         L     R5,CVTTCBP          LOAD TCB POINTER     (KJD)
         L     R5,12(R5)           ADDRESS OF ASCB      (KJD)
         USING ASCB,R5                                  (KJD)
         L     R5,ASCBASXB         ADDRESS OF ASXB      (KJD)
         USING ASXB,R5                                  (KJD)
         CLC   ASXBUSER(3),=C'TEC' IS IT A TEC USER?    (KJD)
         BNE   TECHECK1            NO CONTINUE CHECK    (KJD)
         L     R8,IEXUCB           GET POINTER TO UCB   (KJD)
         USING UCB,R8              GET ADDRESSABILITY   (KJD)
         CLC   UCBVOLI(3),LEE      IS IT A LEE VOLUME?  (KJD)
         BNE   RETURN              NO - OK TO USE VOL   (KJD)
         LA    R15,8               YES - SET BAD RC     (KJD)
         B     CHKRC               GO BACK              (KJD)
TECHECK1 DS    0H
         CLC   ASXBUSER(3),=C'HSM' IS IT AN HSM USER?   (RWS)
         BE    RETURN              YES - OK TO USE VOL  (RWS)
         DROP R6
         EJECT
**********************************************************************
**********************************************************************
**                                                                  **
**           CHECK WITH RACF TO SEE IF ALLOWED TO ALLOCATE          **
**                                                                  **
**********************************************************************
**********************************************************************
RACF     DS    0H
         MVC   AUTHCHK(LRACHECK),RACHECKL  INIT RACHECK MACRO
         L     R8,IEXUCB           GET UCB POINTER
         USING UCB,R8              *****
         L     R6,IEXDSN           GET DSN POINTER             (RWS)
         CLC   0(3,R6),SYS         CHECK FOR TEMPORARY DATASET (RWS)
         BNE   RACSVC                 NO...RACHECK             (RWS)
         CLI   9(R6),C'T'          FURTHER TESTING             (RWS)
         BNE   RACSVC                 NO...RACHECK             (RWS)
         CLI   17(R6),C'R'         FURTHER TESTING             (RWS)
         BNE   RACSVC                 NO...RACHECK             (RWS)
         CLC   UCBVOLI(4),SYSDA    CHECK FOR SYSDA             (RWS)
         BE    RETURN                 YES..OK                  (RWS)
         SPACE
RACSVC   RACHNB CLASS=RACLASS,     CHECK                               X
               ENTITY=UCBVOLI,     AUTHORITY                           X
               MF=(E,AUTHCHK)      TO USE THIS VOLUME
         EJECT
**********************************************************************
**********************************************************************
**                                                                  **
**             CHECK RACF RETURN CODES TO SEE IF PERMITTED          **
**                                                                  **
**********************************************************************
**********************************************************************
CHKRC    DS    0H
         SR    R3,R3               SET DEFAULT RC
         C     R15,=F'0'           RC < OR = 0?   0 = PERMITTED
         BE    RETURN              YES - RETURN   4 = VOL NOT PROTECTED
         SPACE 3
**********************************************************************
**********************************************************************
**                                                                  **
**           CHECK RACF RETURN CODES TO SEE IF ANY DENIED           **
**                                                                  **
**********************************************************************
**********************************************************************
CHKEIGHT DS    0H
         C     R15,=F'8'           EXPLICITLY NOT AUTHORIZED?
         BNE   CHKFOUR                NO..CHECK FOR 4
         MVC   IEXREASN,=X'FFFF'   CREATE A JCL ERROR
         LA    R3,4                SET RETURN CODE TO FAIL REQUEST
         B     RETURN
         SPACE 3
**********************************************************************
**********************************************************************
**                                                                  **
**            DEFAULT PERMISSION IF VOL NOT PROTECTED               **
**                                                                  **
**********************************************************************
**********************************************************************
CHKFOUR  DS    0H
         C     R15,=F'4'           VOLUME NOT PROTECTED?
         BNE   CHKANY                 NOT A 4 DON'T KNOW THE PROBLEM
         MVC   MSGAREA(MSGLEN),MODELMSG  MOVE IN MODEL MSG
         MVC   MSGAREA+22(6),UCBVOLI     MOVE IN VOLSER
         WTO   MF=(E,MSGAREA)            DO WTO
         MVC   IEXREASN,=X'FFFF'   CREATE A JCL ERROR
         LA    R3,4                SET RETURN CODE TO FAIL REQUEST
         B     RETURN              ALLOW ALLOCATE (FOR NOW)
**********************************************************************
**********************************************************************
**                                                                  **
**            DEFAULT PERMISSION IF UNUSUAL RETURN CODE             **
**                                                                  **
**********************************************************************
**********************************************************************
CHKANY   DS    0H
         WTO   'IGGPRE00 - INVALID RETURN FROM RACHECK',               X
               ROUTCDE=(2,9,11),DESC=2
         LA    R3,0                SET RETURN CODE
         B     RETURN              ALLOW ALLOCATE (FOR NOW)
         EJECT
**********************************************************************
**********************************************************************
**                                                                  **
**                        RETURN TO DADSM                           **
**                                                                  **
**********************************************************************
**********************************************************************
RETURN   DS    0H
         L     R13,SAVEAREA+4      RESTORE R13
         L     R0,DYNSIZE          DYNAMIC AREA SIZE TO R0
         LR    R1,R11              DYNAMIC DATA ADDRESS TO R1
         FREEMAIN R,               FREEMAIN                            X
               LV=(0),             DYNAMIC                             X
               A=(1)               AREA
         LR    R15,3               LOAD RETURN CODE
         RETURN (14,12),T,RC=(15)  RESTORE REGISTERS AND RETURN
         EJECT
**********************************************************************
**********************************************************************
**                                                                  **
**                          DATA AREAS                              **
**                                                                  **
**********************************************************************
**********************************************************************
MODELMSG DS    0F
EMSGLEN  DC    AL2(MSGLEN-4)
         DC    X'8000'                 ROUTCDE=(1,2,11)
         DC    C'IGGPRE00 * '
         DC    C'VOLUME VOLSER * '
         DC    C'NOT RACF PROTECTED'
MODRTC   DC    X'0000C020'
MSGLEN   EQU   *-MODELMSG
SYSDA    DC    CL4'SYSD'
SYS      DC    CL3'SYS'
LEE      DC    CL3'LEE'
         DS    0D DOUBLE WORD ALIGN
DYNSIZE  DC    AL1(0)              SUBPOOL
         DC    AL3(SIZEDATD)       DYN AREA SIZE
         DC    C'&SYSDATE &SYSTIME' ASSEMBLY DATE/TIME STAMP
RACLASS  DC    AL1(L'RACLASSN)     CLASS NAME FOR RACCHECK
RACLASSN DC    C'ALLOCAT'          CLASS NAME FOR RACCHECK
*ACHECKL RACHECK MF=L
         SPACE
*        RACHNB IS A LOCAL VERSION OF THE RACHECK MACRO WHICH ISSUES
*        SVC 253, A VERSION OF THE RACF SVC WHICH DOES NOT ISSUE
*        THE ICH408 ACCESS ERROR MESSAGE.  THE SVC IS IN SYS1.LPALIB
*        AND THE MACRO IS IN TEC.MACLIB
         SPACE
RACHECKL RACHNB  MF=L
LRACHECK EQU   *-RACHECKL          LENGTH OF RACHECK MACRO
DSCB     DS    0D                  MODEL FORMAT 1 DSCB
         IECSDSL1 1
SIZEDSCB EQU   *-DSCB              LENGTH OF DSCB
         ORG   DSCB
         DC    XL(SIZEDSCB)'00'    ZERO MODEL DSCB
         ORG   DS1REFD             SET A FIXED
         DC    XL3'46007C'         REFERENCE DATE
         ORG   ,                   IF NO MODEL DSCB
         DS    0D
         LTORG
         EJECT
**********************************************************************
**********************************************************************
**                                                                  **
**              DSECT FOR THIS PROGRAM'S DYNAMIC AREA               **
**                                                                  **
**********************************************************************
**********************************************************************
DATD     DSECT
SAVEAREA DS    18F                 REGISTER SAVE AREA
         DS    0D
CURRDATE DC    XL3'0'              AREA TO HOLD CURRENT DATE
DOUBLE   DC    D'0'                WORK DOUBLE WORD
         DS    0D
AUTHCHK  RACHECK MF=L
MSGAREA  DS    (MSGLEN)C
         DS    CL72
SIZEDATD EQU   *-DATD              LENGTH OF DSECT
         EJECT
**********************************************************************
**********************************************************************
**                                                                  **
**                        DF/DS PARAMETER LIST                      **
**                                                                  **
**********************************************************************
**********************************************************************
         IECIEXPL ,
         EJECT
**********************************************************************
**********************************************************************
**                                                                  **
**                        UCB MAPPING MACRO                         **
**                                                                  **
**********************************************************************
**********************************************************************
UCB      DSECT ,
         IEFUCBOB ,
         EJECT
**********************************************************************
**********************************************************************
**                                                                  **
**                        REGISTER EQUATES                          **
**                                                                  **
**********************************************************************
**********************************************************************
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
RA       EQU   10
R11      EQU   11
RB       EQU   11
R12      EQU   12
RC       EQU   12
R13      EQU   13
RD       EQU   13
R14      EQU   14
RE       EQU   14
R15      EQU   15
RF       EQU   15
         SPACE 3
         CVT   DSECT=YES
         IHAASCB
         IHAASXB
         END   ,
