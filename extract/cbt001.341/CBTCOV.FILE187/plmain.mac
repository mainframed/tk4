*****
* MODULE: PLMAIN
* FUNCTION: AN ALTERNATE ENTRY POINT FOR ANY PLIX LOAD MODULE
*           USING ASSEMBLER TO PLI LINKAGE CONVENTIONS TO ESTABLISH
*           MORE ACCEPTABLE LINKAGE FOR MODULES LOADED OTHER THAN
*           BY THE 'PGM=' ON A BATCH EXEC CARD
* ATTRIBUTES: NONE
* DATA AREAS: THE PLI PROGRAM MUST SPECIFY THE DESIRED ISA SIZE BY
*           DCL ISASIZE FIXED BIN(31) EXT INIT(XXXXX);
*****
         REPRO
 ENTRY PLMAIN
         PRINT NOGEN
PLMAIN  CSECT
         USING *,15 .              FOR TEMPORARY ADDRESSIBILITY
         B     14(,15)  .           BRANCH AROUND ID
         DC    AL1(9),CL9'PLMAIN'
         STM   14,12,12(13) .      SAVE STANDARD REGISTER SET
         GETMAIN R,LV=64
         ST    13,4(1) .           STORE OLD POINTER IN NEW AREA
         ST    1,8(13) .           STORE (EW POINTER IN OLD AREA
         LR    12,1  .             SAVE VALUE OF NEW SAVE POINTER
         LM    0,1,20(13) .        RESTORE PREVIOUS VALUES OF REGS
         LR    13,12  .            POINT 13 TO NEW SAVE AREA
         BALR  12,0  .             SET UP NEW BASE REGISTER
         DROP  15 .                CLEAN UP USING SITUATION
         USING *,12
* FOR REFERENCE SEE PLIX PROGRAMMERS GUIDE CHAPTER 12 FIG 12.5
R1       EQU   1
R2       EQU   2
         ST    R1,PLIARGS
         WXTRN PLIOPTS             DCL PLIOPTS BIT EXT INITIAL( )
         L     R2,=V(PLIOPTS)      ALLOWS SETTING OF THE 'REPORT'
         LTR   R2,R2              MODULE IS REENTRANT IF
         BZ    *+10               PLIOPTS NOT SPECIFIED
         MVC   OPTIONS(1),0(R2)    BIT FOR STORAGE INFO IN PLIDUMP
         LA    R1,PLIARGS
         CALL  PLICALLB
RET      DS    0H .                DEFINE LABEL
         L     13,4(13) .          GET OLD SA POINTER BACK
         STM   15,1,16(13) .       SAVE REGS FROM FREEMAIN CRUNCHING
         L     1,8(13) .           GET ADDRESS OF AREA BACK
         FREEMAIN R,LV=64,A=(1)
         LM    15,1,16(13) .       RESTORE THE REGS
         LM    14,12,12(13) .      STANDARD REGISTER RESTORATION
         BR    14 .                RETURN NORMALLY TO CALLER
PLIARGS  DC    A(0)               ADDRESS OF OS PARM FIELD
         DC    V(ISASIZE)          ADDR OF ISA AMOUNT
         DC    3A(0)               ADDR OF ISA+TASKING OPTIONS
         DC    X'80'
         DC    AL3(OPTIONS)        ADDRESS OF OPTIONS WORD
OPTIONS  DC    X'00000000'         REPORT/STAE/SPIE OPTIONS
         END   PLMAIN
