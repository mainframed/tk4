* PROCESS MACRO,MAR(2,72),OPT(TIME);
 /* OMFPST3: OUTPUT THE RELATIVE ADDRESS IN NAMED MODULE             */
 OMFPST3: PROC OPTIONS(MAIN REENTRANT);
 /********************************************************************/
 /* PROCEDURE: OMFPST3                                               */
 /* FUNCTION:                                                        */
 /*            POST PROCESSOR FOR OMF1 RECORD (30 SECOND SUMMARY)    */
 /*             SUPPORT 'PROGLOOK' OR 'PACKER' TYPE FUNCTION BY      */
 /*             SELECTING THE PSW'S BY MODULE NAME AND WRITING       */
 /*             THE RELATIVE INSTRUCTION ADDRESS TO THE OUTPUT       */
 /*             DATASET.                                             */
 /* ATTRIBUTES: NONE                                                 */
 /* EXTERNAL: NONE                                                   */
 /* INPUT:                                                           */
 /*     FILE SYSIN CONTAINS PARAMETERS IN OURMF FORMAT               */
 /*           START( YYDDD HHMMSS )  /* STARTING DATE AND TIME       */
 /*           END  ( YYCCC HHMMSS )  /* ENDING DATE AND TIME         */
 /*           HOURS( HHMMSS HHMMSS)  /* APPLICABLE HOURS DURING      */
 /*                                  /* THIS TIME PERIOD             */
 /*           PGM( PGMNAME)        /* CDENAME SELECTED               */
 /* DEFAULTS:  INCLUDE ALL RECORDS IN SCAN.                          */
 /*            PGM MUST BE SPECIFIED.                                */
 /*                                                                  */
 /* THE OURMF OUTPUT DATA IS READ FROM DDNAME 'IN'                   */
 /*                                                                  */
 /* AUTHOR:  HOWARD GILBERT                                          */
 /*          175 WHITNEY AVE                                         */
 /*          NEW HAVEN, CT 06520                                     */
 /*          203 432-4080                                            */
 /* DISTIRBUTION                                                     */
 /*     THIS PROGRAM IS NOT RESTRICTED IN ANY WAY AND MAY BE         */
 /* MODIFIED OR DISTRIBUTED IN ANY MANNER AND FOR ANY PURPOSE.       */
 /* IT IS SUGGESTED THAT OTHER OURMF POST PROCESSORS CONSIDER        */
 /* COMPATIBLE SYNTAX AND PARAMETER SCANNING.                        */
 /********************************************************************/
 DCL 1 OPTIONS,
         5 STARTDATE FIXED DEC(7) INIT(0),
         5 STARTHOUR FIXED DEC(7) INIT(0),
         5 ENDDATE FIXED DEC(7) INIT(99365),
         5 ENDHOUR FIXED DEC(7) INIT(240000),
         5 EARLIEST FIXED DEC(7) INIT(0),
         5 LATEST FIXED DEC(7) INIT(240000),
         5 PGM CHAR(8) INIT((LOW(8)));
 %PAGE;
 % INCLUDE STATGLBL;
 %INCLUDE YCCSTART;
 % INCLUDE OURMFDEF;
 DCL MAXVALS(4) FIXED BIN INIT(2,2,2,1);
 DCL WORDS(4) CHAR(8) INIT('START','END','HOURS','PGM');
 DCL INSADDR BIT(32) ALIGNED;
 DCL MASK BIT(32) ALIGNED INIT(((8)'0'B××(24)'1'B));
 DCL IN INPUT FILE RECORD ENV(VB TOTAL);
 DCL OUT OUTPUT FILE RECORD ENV(FB RECSIZE(4) BLKSIZE(4240));
 %PAGE;
 ON UNDEFINEDFILE(SYSIN)GO TO PROCESS;
 ON ENDFILE(SYSIN) GO TO PROCESS;
 ON CONVERSION CALL ERR('INVALID NUMBER IN '××WORDS(I));
 KEYWDLOOP: CALL KEYWORD;
 I=INDOX('START END HOURS PGM',KEY);
 IF NVALS>MAXVALS(I) THEN CALL ERR('TOO MANY VALUES FOR '××WORDS(I));
 SELECT (I);
    WHEN (1)
         DO;
         STARTDATE=VAL(1);
         IF NVALS=2 THEN STARTHOUR=VAL(2);
         END;
    WHEN (2)
         DO;
         ENDDATE=VAL(1);
         IF NVALS=2 THEN ENDHOUR=VAL(2);
         END;
    WHEN (3)
         DO;
         EARLIEST=VAL(1);
         IF NVALS=2 THEN LATEST=VAL(2);
         END;
    WHEN (4)
         DO;
         IF LENGTH(VAL(1))>8 THEN
              CALL ERR('INVALID PGM NAME');
         PGM=VAL(1);
         END;
    END;
 GO TO KEYWDLOOP;
 %PAGE;
 PROCESS:
 CLOSE FILE(SYSIN);
 IF PGM=LOW(8) THEN CALL ERR('NO PROGRAM NAME PROVIDED IN INPUT');
 ON ENDFILE(IN) GO TO FINN;
 PRLOOP:
 READ FILE(IN) SET(OMFRECPTR);
 IF OMF1ID^='OMF1' THEN GO TO PRLOOP;
 IF OMF1DATE>ENDDATE ×
    (OMF1DATE=ENDDATE & OMF1TIME>ENDHOUR)
    THEN GO TO FINN;
 IF OMF1DATE<STARTDATE ×
    (OMF1DATE=STARTDATE & OMF1TIME < STARTHOUR) ×
    OMF1TIME<EARLIEST ×
    OMF1TIME>LATEST
         THEN GO TO PRLOOP;
    DO I=1 TO SRBINTVL;
    IF CDENAME(I)=PGM & ADDRESS(I)<0 THEN
        DO;
        INSADDR=UNSPEC(ADDRESS(I))&MASK;
        WRITE FILE(OUT) FROM(INSADDR);
        END;
    END;
 GO TO PRLOOP;
 FINN: RETURN;
 %INCLUDE CNTLCRD;
 END;
