* PROCESS NOOPTIMIZE;
 /* PAGES: DISPLAY STATUS OF PAGE AND SWAP DATASETS */
 PAGES: PROC OPTIONS(MAIN REENTRANT);
  /*     VERSION DATE 10/4/76
         MVT R3.7 + SU 7
         YALE UNIVERSITY COMPUTER CENTER
         175 WHITNEY AVE
         NEW HAVEN, CT.
         203 432-4080
         HOWARD GILBERT

         RECOMMENDED AS A TSO COMMAND,
         LINKEDIT WITH PLMAIN AS THE ENTRY POINT
         USE PLI OPTIMIZING COMPILER            */
  DCL SIXTEEN PTR;
  UNSPEC(SIXTEEN)=(24)'0'B××'00010000'B;
  DCL CVTPTR PTR BASED(SIXTEEN);
  DCL ISASIZE FIXED BIN(31) EXT INIT(5000);
  DCL 1 CVT BASED(CVTPTR),
          2 F1 CHAR(704),
          2 CVTASMVT PTR;
  DCL 1 ASMVT BASED(CVTASMVT),
          2 F1 CHAR(4),
          2 ASMSART PTR,
          2 ASMPART PTR,
          2 F2 CHAR(96),
          2 (UNRESERVED,POOL,VIO,ADSPC) FIXED BIN(31);
  DCL 1 PART BASED(ASMPART),
          2 ID CHAR(4),
          2 PARTSIZE FIXED BIN(31),
          2 F2 CHAR(16),
          2 PARTDSNL PTR,
          2 F3 CHAR(52),
          2 PARTE (N REFER(PARTSIZE)),
                3 F4 CHAR(8),
                3 TYPE BIT(4) ALIGNED,
                3 NOTINUSE BIT ALIGNED,
                3 F5 CHAR(6),
                3 (SLOTS,AVAIL,ERRS) FIXED BIN(31),
                3 (IORB,PAT,PCT,EDP) PTR,
                3 UCBP PTR,
                3 F6 CHAR(16);
  DCL 1 SART BASED(ASMSART),
          2 ID CHAR(4),
          2 SARTSIZE FIXED BIN(31),
          2 F2 CHAR(16),
          2 SARTDSNL PTR,
          2 F3 CHAR(52),
          2 SARTE (N REFER(SARTSIZE)),
                3 F4 CHAR(9),
                3 NOTINUSE BIT ALIGNED,
                3 F5 CHAR(6),
                3 (SETS,AVAILABLE,ERRORS) FIXED BIN(31),
          3 (IORB,SAT,SDCT,EDB) PTR,
                3 UCBPTR PTR;
  DCL DSN(20) CHAR(44) BASED(P);
  DCL N FIXED BIN;
  DCL P PTR;
  DCL TYPES(4) CHAR(10) INIT('PLPA','COMMON','DUPLEX','LOCAL');
  DCL 1 UCB BASED(P),
          2 F1 CHAR(28),
          2 SER CHAR(6);
  PUT EDIT('PAGE DATASETS:',
          'TYPE','SER','SLOTS',' % USED ','ERRORS','NAME')
          (COL(1),A,COL(1),6 A(10));
  DO I=1 TO PARTSIZE;
          IF ^PARTE.NOTINUSE(I) & SLOTS(I)^=0 THEN
          PUT EDIT(TYPES(INDEX(TYPE(I),'1'B)),
                UCBP(I)->SER,
                SLOTS(I),(SLOTS(I)-AVAIL(I))*100/SLOTS(I),ERRS(I),
           SUBSTR(PARTDSNL->DSN(I),1,INDEX(PARTDSNL->DSN(I),' ')-1))
                (COL(1), 2 A(10), 3 (F(5),X(5)),A);
          END;
          PUT EDIT('PAGE SLOTS ARE ',(POOL-UNRESERVED)*100/POOL,
               ' % RESERVED')
          (SKIP(2),A,F(3),A);
          PUT EDIT('SWAP DATASETS:',
                'SER','SETS',' % USED','ERRORS','NAME')
                (SKIP(2),A,COL(1),X(10),5 A(10));

          DO I=1 TO SARTSIZE;
          IF ^SARTE.NOTINUSE(I) &SETS(I)^=0 THEN
          PUT EDIT(UCBPTR(I)->SER,
                SETS(I),(SETS(I)-AVAILABLE(I))*100/SETS(I),ERRORS(I),
           SUBSTR(SARTDSNL->DSN(I),1,INDEX(SARTDSNL->DSN(I),' ')-1))
                (COL(11),A(10),3 (F(5),X(5)),A);
          END;
  END;
