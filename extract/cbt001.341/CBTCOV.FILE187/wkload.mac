* PROCESS MAR(2,72),INCLUDE,OPT(TIME);
  /* WKLOAD: TSO COMMAND TO DISPLAY MF1 WORKLOAD DATA */
 WKLOAD: PROC OPTIONS(MAIN );
 /* PROCEDURE:    WKLOAD                                             */
 /* FUNCTION:     DISPLAY WORKLOAD ACTIVITY MEASUREMENT TABLE FROM   */
 /*               THE LAST MF1 REPORTING INTERVAL                    */
 /* SYSTEM:       MVS R3.7 + SUPERVISOR 2 SU                         */
 /* VERSION:      MAY, 1977                                          */
 /* AUTHOR:       HOWARD GILBERT                                     */
 /*               YALE COMPUTER CENTER                               */
 /*               NEW HAVEN, CT. 06520                               */
 /*               203 432-4080                                       */
 /* EXTERNAL:     REQUIRES MF1 BE RUNNING FOR AT LEAST ONE REPORTING */
 /*               PERIOD.                                            */
 /*               REQUIRES STATDIE BE INITIALIZED.                   */
 /*               REQUIRES ZAP TO IRBMFDWP TO SAVE WAMT (PTF HG00101) */
 /********************************************************************/
 /*                                                                  */
 /* INSTALLATION INFORMATION                                         */
 /* TARGET LIBRARY: LINKLIB OR LPALIB                                */
 /* LINKEDIT PARMS: RENT,REUS                                        */
 /* LINKEDIT CONTROL CARDS:                                          */
 /*      ENTRY PLICALLA                                              */
 /* SUBROUTINES: PLIBASE, TODCNVT, PLIMISC ENTRY POINTS              */
 /*                                                                  */
 /* DISTRIBUTION :                                                   */
 /*     THIS MATERIAL IS NOT RESTRICTED IN ANY MANNER AND MAY        */
 /* BE FREELY REPRODUCED AND DISTRIBUTED FOR ANY PURPOSE.            */
 /* THERE ARE TWO LITTLE RULES:                                      */
 /*     1) YOU MUST MARK ANY MODIFICATIONS PRIOR TO GIVING           */
 /*        ANYONE COPIES OF ALTERED CODE.                            */
 /*     2)SEND THE AUTHOR A NOTE IF YOU HAVE INSTALLED               */
 /*        AND ARE USING THIS PROGRAM SO HE CAN KEEP                 */
 /*        TABS ON THE USER COMMUNITY.                               */
 %PAGE;
 /*    WHATSA WAMT??                                                 */
 /* THE WORKLOAD ACTIVITY MEASUREMENT TABLE IS CREATED AND           */
 /* INITIALIZED BY RMF OR MF1. THERE IS ONLY ONE WAMT, ONLY ONE      */
 /* WAMT FORMAT, AND IT IS MAINTAINED BY THE SRM IDENTICALLY         */
 /* FOR BOTH MF1 AND RMF. IT CONTAINS ALL THE WORKLOAD INFORMATION   */
 /* REPORTED BY RMF FOR PERFORMANCE GROUPS AND PERIODS. MF1 IS       */
 /* SMART ENOUGH TO IGNORE MUCH OF THE WAMT INFORMATION, THEREBY     */
 /* INSURING A MARKET FOR RMF.                                       */
 /* THIS PROGRAM IS A USABLE TSO COMMAND TO SUMMARIZE THE            */
 /* INFORMATION IN THE LAST WAMT COLLECTED BY MF1. THIS              */
 /* SATISFIES THE OCCASIONAL DESIRE TO LOOK AT THE SYSTEM            */
 /* WITHOUT THE GENERATION OF BOXES OF OLD MF1/RMF REPORTS.          */
 /* ALL THE USUAL NUMBERS ARE THERE, BUT A FEW EDITORIAL             */
 /* DECISIONS HAVE BEEN MADE FOR THE TSO ENVIRONMENT:                */
 /*  1) ONLY THE BREAKDOWN BY PGP IS PROVIDED. IT IS                 */
 /*     EASY TO SUM THE COLUMNS IF YOU WANT, BUT THE                 */
 /*     VARIOUS OTHER REPORTS DO NOT SEEM AS IMPORTANT               */
 /*     AS THE BREAKDOWN BY PGP.                                     */
 /*  2) ALL THE SERVICE UNITS ARE REPORTED 'PER SECOND',             */
 /*     BECAUSE THE LENGTH OF THE REPORTING INTERVAL IS              */
 /*     NOT AT THE USERS CONTROL AND ABSOLUTE NUMBERS                */
 /*     SEEM LESS USEFUL.                                            */
 /*  FIELDS REPORTED:                                                */
 /*      PG    PERFORMANCE GROUP                                     */
 /*      PGP   PERFORMANCE GROUP PERIOD                              */
 /*      OBJ   SERVICE OBJECTIVE                                     */
 /*      DMN   DOMAIN                                                */
 /*      SRV   SERVICE UNITS PER SECOND CONSUMED BY PGP              */
 /*      CPU   CPU COMPONENT OF SRV                                  */
 /*      IO    IO COMPONENT OF SRV                                   */
 /*      MSO   STORAGE COMPONENT OF SRV                              */
 /*      ABS   ABSORPTION RATE (SERVICE/TRANS-RESIDENT TIME)         */
 /*            THIS IS THE RATE AT WHICH A USER IN THIS PGP          */
 /*            CONSUMES SERVICE WHILE IN CORE. IT IS TOTAL           */
 /*            SERVICE PER SECOND OF SWAPPED-IN TIME.                */
 /*      TSR   TRANS SERVICE RATE (SERVICE/TRANS-ACTIVE TIME)        */
 /*            THIS IS THE RATE AT WHICH A USER IN THIS PGP          */
 /*            CONSUMES SERVICE INCLUDING BOTH SWAPPED IN            */
 /*            AND SWAPPED OUT TIME. 'TSR'<='ABS' WITH               */
 /*            EQUALITY ONLY IF THE USER IS NOT SWAPPED OUT          */
 /*            DURING THIS PGP.                                      */
 /*      WLL   WORKLOAD LEVEL                                        */
 /*      SWP   SWAPS PER SECOND                                      */
 /*      ACT   ACTIVITY (TRANS-RESIDENT/ELAPSED TIME)                */
 /*            ESSENTIALLY THIS IS THE AVERAGE NUMBER                */
 /*            OF INCORE TRANSACTIONS IN THIS PGP OVER               */
 /*            THIS REPORTING PERIOD. IT IS A MEASURE                */
 /*            OF MULTIPROGRAMMING LEVEL FOR THE PGP.                */
 /*      TRLEN AVERAGE TRANSACTION ELAPSED TIME                      */
 /*            THIS IS THE AVERAGE ELAPSED TIME FOR                  */
 /*            ENDED TRANSATIONS. IT IS A MEASURE OF                 */
 /*            RESPONSE TIME FOR TSO OR TURNAROUND FOR               */
 /*            BATCH, BUT EXCEPT FOR TSO TRIVIAL PERIODS             */
 /*            THIS VALUE IS DANGEROUSLY UNSTABLE AS A               */
 /*            TUNING CRITERION                                      */
 %PAGE;
 DCL 1 WAMT BASED(WAMTPTR),
 % INCLUDE IRAWAMT;
 DCL TIMES FIXED BIN;
 DCL STATADDR PTR;
 DCL STATWAMT PTR BASED(STATADDR);
 DCL WAMTPTR PTR;
 DCL PTRIN ENTRY(PTR) RETURNS(FIXED BIN(31));
 DCL PTROUT ENTRY(FIXED BIN(31)) RETURNS(PTR);
 DCL LOADPTR ENTRY(CHAR(8),PTR) OPTIONS(ASM INTER RETCODE);
 DCL TODCNVT ENTRY(*) RETURNS(CHAR(24));
 DCL PLIRETV BUILTIN;
 CALL LOADPTR('STATWAMT',STATADDR);
 IF PLIRETV >0 THEN
     DO;
     PUT EDIT('STATDIE NOT INSTALLED')(COL(1),A);
     STOP;
     END;
 IF UNSPEC(STATWAMT)=(32)'0'B THEN
     DO;
     PUT EDIT('STATDIE NOT INITIALIZED')(COL(1),A);
     STOP;
     END;
 WAMTPTR=STATWAMT;
 IF SIZ=0 THEN
     DO;
     PUT EDIT('NO MF1 DATA AVAILABLE YET')(COL(1),A);
     STOP;
     END;
 TIMES=TOC-TMA;
 PUT EDIT('WORKLOAD DURING ',TIMES,' SECONDS ENDING AT ',
     SUBSTR(TODCNVT(ADDR(TOC)),8,5),'IPS=',IPS,
         ' CPU=',IPC,' IOC=',IPI,' MSO=',IPM)
         (COL(1),A,F(4),2 A,COL(1), 8 A);
 PUT EDIT('PG PGP OBJ DMN SRV CPU IO  MSO ABS TSR WLL SWP  ACT   TRLEN'
          )(COL(1),A);
    DO I=0 TO HPG;
    K=1;
      DO J=NDX(I) TO NDX(I+1)-SPD BY SPD;
      WAMPNEXT=PTROUT(PTRIN(WAMTPTR)+J);
      IF TRR<=0 THEN
           PUT EDIT(I,K,OBJ,DMN,' ZEROS')(COL(1),F(2),3 F(4),A);
          ELSE DO;
           PUT EDIT(I,K,OBJ,DMN,SRV/TIMES,CPU/TIMES,IOC/TIMES,MSO/TIMES,
            (SRV*1000)/TRR,(SRV*1000)/TAT,NWL/000256,FLOAT(SWC)/TIMES,
            FLOAT(TAT)/(TIMES*1000))
            (COL(1),F(2),10 F(4),2 F(5,2));
           IF TRN=0 THEN PUT EDIT('  0.00')(A);
                    ELSE PUT EDIT(TET/(TRN*10))(P'ZZZ9.99');
            END;
      K=K+1;
      END;
    END;
 END;
