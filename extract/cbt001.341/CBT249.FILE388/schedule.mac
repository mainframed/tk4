SCHED    TITLE 'SCHEDULE COMMAND'
*************************************************************
*                                                           *
* MODULE NAME = SCHEDULE.                                   *
*                                                           *
* DESCRIPTIVE NAME = AUTOMATIC OPERATOR COMMANDS SCHEDULE   *
*                    MAINTENANCE PROGRAM.                   *
*                                                           *
* COPYRIGHT = DAVID B. COLE - 1980.                         *
*                                                           *
* STATUS = RELEASE 2, MODIFICATION LEVEL 1.                 * 02/84 DBC
*                                                           *
* FUNCTION = ADDS, DELETES, AND DISPLAYS ENTRIES IN THE     *
*            SCHEDULE OF AUTOMATICALLY ISSUED SYSTEM        *
*            OPERATOR COMMANDS. SUCH ENTRIES IDENTIFY A     *
*            SYSTEM OPERATOR COMMAND AND DEFINE A SET OF    *
*            DATES AND A TIME-OF-DAY WHEN (AS WELL AS       *
*            VARIOUS OTHER CIRCUMSTANCES UNDER WHICH) THAT  *
*            COMMAND IS TO BE EXECUTED BY THE SYSTEM. THE   *
*            COMMAND MAY BE ANY COMMAND (OS, JES2, ETC.)    *
*            THAT CAN BE ISSUED FROM AN OPERATOR'S CONSOLE. *
*                                                           *
* PROCESSOR = OS/VS ASSEMBLER.                              *
*                                                           *
* TYPE = TSO COMMAND PROCESSOR.                             *
*                                                           *
* ATTRIBUTES = TASK MODE, AUTHORIZED, REENTRANT.            *
*                                                           *
* DEPENDANCIES = REQUIRES THE GSAM IUP.                     *
*                                                           *
* AUTHOR = DAVID B. COLE.                                   *
*                                                           *
*************************************************************
         TITLE 'SCHEDULE COMMAND -- CHANGE HISTORY'
************************************************************* 02/84 DBC
*                                                           * 02/84 DBC
* RELEASE 2.1                                               * 02/84 DBC
*                                                           * 02/84 DBC
* LAST CHANGE DATE - FEBRUARY 15, 1984                      * 02/84 DBC
*                                                           * 02/84 DBC
*                  - "TIME OF DAY" RECOGNITION HAS BEEN     * 02/84 DBC
*                    CHANGED. SCHEDULE NOW RECOGNIZES AND   * 02/84 DBC
*                    USES LOCAL TIME INSTEAD OF GMT.        * 02/84 DBC
*                                                           * 02/84 DBC
************************************************************* 02/84 DBC
         EJECT ,                                              02/84 DBC
*************************************************************
*                                                           *
* RELEASE 2.0                                               *
*                                                           *
* LAST CHANGE DATE - MAY 1, 1983                            *
*                                                           *
*                  - THE DEPENDANCY UPON THE AVAILABILITY   *
*                    OF THE GSAM IUP (WRITTEN AT YALE) HAS  *
*                    BEEN REMOVED.                          *
*                                                           *
*                  - SCHEDULE WILL NOW AUTOMATICALLY        *
*                    INITIALIZE AN EMPTY SCHEDULE FILE.     *
*                                                           *
*                  - THE APPLICATION NAME HAS BEEN EXPANDED *
*                    FROM 4 CHARACTERS TO 8 CHARACTERS.     *
*                                                           *
*                  - THE DELETE COMMAND HAS BEEN REWRITTEN  *
*                    TO BE CONGRUENT WITH THE DISPLAY       *
*                    COMMAND. IT NOW HAS SELECTION          *
*                    CAPABILITIES THAT ARE IDENTICAL TO THE *
*                    DISPLAY COMMAND'S. IN ADDITION, ALL    *
*                    RECORDS TO BE DELETED ARE DISPLAYED    *
*                    BEFORE BEING DELETED.                  *
*                                                           *
*                  - FOR THE "DISPLAY" AND "DELETE"         *
*                    SUBCOMMANDS, "ALL" IS NOW AN ALIAS OF  *
*                    "ANYTIME".                             *
*                                                           *
*                  - FOR THE "DISPLAY" AND "DELETE"         *
*                    SUBCOMMANDS, "OBSOLETE" LIMITS THE     *
*                    SELECTION TO THOSE SCHEDULED COMMANDS  *
*                    THAT ARE OBSOLETE - I.E., WILL NEVER   *
*                    EXECUTE AGAIN. "OBSOLETE" IS MUTUALLY  *
*                    EXCLUSIVE WITH "YESTERDAY", "TODAY",   *
*                    "TOMMOROW", "DATE", "ANYTIME", AND     *
*                    "ID".                                  *
*                                                           *
*                  - THE "DELETE" AND "DISPLAY" SUBCOMMANDS *
*                    NOW SUPPORT THE FOLLOWING ADDITIONAL   *
*                    MUTUALLY EXCLUSIVE OPERANDS:           *
*                                                           *
*                    - "SHORT": THIS SUPPRESSES THE DISPLAY *
*                      OF INDIVIDUAL RECORDS. FOR           *
*                      "DISPLAY", ONLY A SUMMARY MESSAGE IS *
*                      DISPLAYED SHOWING HOW MANY RECORDS   *
*                      WERE SELECTED.                       *
*                                                           *
*                    - "LONG": THIS DISPLAYS ALL SELECTED   *
*                      RECORDS. FOR "DISPLAY", THIS IS THE  *
*                      DEFAULT.                             *
*                                                           *
*                    - "NOPROMPT": THIS IS AN ALIAS OF      *
*                      "LONG".                              *
*                                                           *
*                    - "PROMPT": FOR "DELETE", THIS IS THE  *
*                      DEFAULT. IT DISPLAYS EACH SELECTED   *
*                      RECORD AND THEN PROMPTS THE USER FOR *
*                      A FINAL DELETION DECISION. FOR       *
*                      "DISPLAY", "PROMPT" IS AN ALIAS OF   *
*                      "LONG".                              *
*                                                           *
*                  - THE "DELETE" AND "DISPLAY" SUBCOMMANDS *
*                    NOW SUPPORT THE FOLLOWING ADDITIONAL   *
*                    MUTUALLY EXCLUSIVE OPERANDS:           *
*                                                           *
*                    - "OVERRIDE": THIS SELECTS ONLY THE    *
*                      SCHEDULED COMMANDS THAT ARE          *
*                      OVERRIDES.                           *
*                                                           *
*                    - "NOOVERRIDE": THIS SELECTS ONLY      *
*                      THOSE SCHEDULED COMMANDS THAT ARE    *
*                      NOT OVERRIDES.                       *
*                                                           *
*                    - "BOTH: THIS SELECTS SCHEDULED        *
*                      COMMANDS WITHOUT REGUARD TO WHETHER  *
*                      OR NOT THEY ARE OVERRIDES.           *
*                                                           *
*                    - "EITHER": THIS IS THE DEFAULT. IT    *
*                      FUNCTIONS EXACTLY LIKE "BOTH" UNLESS *
*                      THE SELECTION IS ALSO LIMITED BY A   *
*                      SPECIFIC DATE (I.E., "DATE",         *
*                      "YESTERDAY", "TODAY", OR "TOMORROW"  *
*                      IS ALSO GIVEN). IN THIS CASE IF      *
*                      OVERRIDES FOR A GIVEN APPLICATION    *
*                      NAME EXIST FOR THIS DATE, THEN ONLY  *
*                      THE OVERRIDES ARE DISPLAYED FOR THAT *
*                      APPLICATION; OTHERWISE, ONLY THE     *
*                      NON-OVERRIDES ARE DISPLAYED FOR THAT *
*                      APPLICATION.                         *
*                                                           *
*                  - IF A "SCHEDULE" DDNAME IS NOT          *
*                    AVAILABLE, THEN AN ATTEMPT IS MADE TO  *
*                    DYNAMICALLY ALLOCATE                   *
*                    "SYSVSAM.SCHEDULE".                    *
*                                                           *
*                  - A "CHANGE" COMMAND HAS BEEN            *
*                    IMPLEMENTED (WITH AN OPTIONAL "COPY"   *
*                    OPERAND TOO).                          *
*                                                           *
*                  - NEW SCHEDULED COMMANDS WILL NO LONGER  *
*                    BE SCHEDULED IMMEDIATELY WHEN THE      *
*                    CURRENT TIME IS WITHIN THEIR WINDOWS.  *
*                    THEY WILL WAIT UNTIL THEIR NEXT START  *
*                    TIMES.                                 *
*                                                           *
*                  - WEEKDAYS SCOPE LIMITATION: IF THE      *
*                    SCHEDULED DATE SPECIFIES A SPECIFIC    *
*                    MONTH OR YEAR, THEN WEEKDAY            *
*                    MODIFICATION CANNOT CHANGE THAT MONTH  *
*                    OR YEAR. (IT CAN STILL CHANGE A        *
*                    SPECIFIC DAY, THOUGH).                 *
*                                                           *
*                  - NEGATIVE WEEKDAYS SUPPORT IS           *
*                    IMPLEMENTED. E.G., "-WED" MEANS THE    *
*                    FIRST WEDNESDAY AT OR PRECEEDING THE   *
*                    SCHEDULED DATE.                        *
*                                                           *
*                  - "WEEKDAY(ANY)" IS NOW AN ALIAS FOR     *
*                    "WEEKDAY(-)".                          *
*                                                           *
*                  - DEFAULT WINDOW VALUE CHANGED TO BE     *
*                    "UNTIL MIDNIGHT".                      *
*                                                           *
*                  - EXPLICIT SYSTEM SELECTION HAS BEEN     *
*                    IMPLEMENTED FOR USE IN MULTI-SYSTEM    *
*                    CONFIGURATIONS:                        *
*                                                           *
*                    - THE SCHEDULE FILE RECORD HAS BEEN    *
*                      EXPANDED TO INCLUDE A "SFRSYSID"     *
*                      FIELD WHICH IDENTIFIES ON WHICH      *
*                      SYSTEM(S) THE SCHEDULE COMMAND MAY   *
*                      BE EXECUTED.                         *
*                                                           *
*                    - THE "SYSID" OPERAND HAS BEEN ADDED   *
*                      TO THE RELEVENT SUBCOMMANDS TO       *
*                      CONTROL SELECTION VIA SYSTEM-ID.     *
*                                                           *
* THINGS STILL TO DO:                                       *
*                                                           *
*                  - INTERVAL SCHEDULING.                   *
*                                                           *
*************************************************************
         TITLE 'SCHEDULE COMMAND -- IBM CONTROL BLOCK MAPS'
         PUNCH '         SETCODE AC(1)'
         SPACE 3
SCHEDULE CSECT ,
         PRINT NOGEN
         SPACE 3
*************************************************************
*        CPPL = COMMAND PROCESSOR PARAMETER LIST (TSO       *
*               COMMANDS)                                   *
*        CPPL = DSECT AND BASE                              *
*************************************************************
         SPACE 1
         IKJCPPL ,
CPPLLEN  EQU   *-CPPL              L'CPPL
         SPACE 3
*************************************************************
*        CSOA = COMMAND SCAN OUTPUT AREA (TSO COMMAND SCAN) *
*        CSOA = DSECT AND BASE                              *
*************************************************************
         SPACE 1
         IKJCSOA ,
CSOALEN  EQU   *-CSOA              L'CSOA
         SPACE 3
*************************************************************
*        CSPL = COMMAND SCAN PARAMETER LIST (TSO COMMAND    *
*               SCAN)                                       *
*        CSPL = DSECT AND BASE                              *
*************************************************************
         SPACE 1
         IKJCSPL ,
CSPLLEN  EQU   *-CSPL              L'CSPL
         SPACE 3
*************************************************************
*        CVT = COMMUNICATIONS VECTOR TABLE                  *
*        CVTFIX = PREFIX AND BASIC SECTIONS DSECT           *
*        CVTFIX = PREFIX SECTION BASE                       *
*        CVTMAP = BASIC SECTION BASE                        *
*        CVTXTNT1 = OS COMMON EXTENSION DSECT AND BASE      *
*        CVTXTNT2 = VS1/VS2 COMMON EXTENSION DSECT AND BASE *
*************************************************************
         SPACE 1
         CVT   DSECT=YES,PREFIX=YES,LIST=YES
         SPACE 3
*************************************************************
*        ECB = EVENT CONTROL BLOCK                          *
*        ECB = BASIC SECTION DSECT AND BASE                 *
*        ECBE = EXTENSION DSECT AND BASE                    *
*************************************************************
         SPACE 1
         IHAECB EXT=YES
         SPACE 3
*************************************************************
*        ECT = ENVIRONMENT CONTROL TABLE (TSO)              *
*        ECT = DSECT AND BASE                               *
*************************************************************
         SPACE 1
         IKJECT ,
         SPACE 3
*************************************************************
*        IOPL = INPUT/OUTPUT SERVICE ROUTINE PARAMETER LIST *
*               (TSO)                                       *
*        IOPL = DSECT AND BASE                              *
*************************************************************
         SPACE 1
         IKJIOPL ,
IOPLLEN  EQU   *-IOPL              L'IOPL
         SPACE 3
*************************************************************
*        JSCB = JOB STEP CONTROL BLOCK                      *
*        IEZJSCB = DSECT AND BASE                           *
*************************************************************
         SPACE 1
         IEZJSCB ,
         SPACE 3
*************************************************************
*        PGPB = PUTGET PARAMETER BLOCK (TSO)                *
*        PGPB = DSECT AND BASE                              *
*************************************************************
         SPACE 1
         IKJPGPB ,
PGPBLEN  EQU   *-PGPB              L'PGPB
         SPACE 3
*************************************************************
*        PPL = PARSE PARAMETER LIST (TSO)                   *
*        PPL = DSECT AND BASE                               *
*************************************************************
         SPACE 1
         IKJPPL ,
PPLLEN   EQU   *-PPL               L'PPL
         SPACE 3
*************************************************************
*        PSA = PREFIXED STORAGE AREA                        *
*        PSA = DSECT AND BASE                               *
*************************************************************
         SPACE 1
         IHAPSA ,
         SPACE 3
*************************************************************
*        PSCB = PROTECTED STEP CONTROL BLOCK (TSO)          *
*        PSCB = DSECT AND BASE                              *
*************************************************************
         SPACE 1
         IKJPSCB ,
         SPACE 3
*************************************************************
*        PTPB = PUTLINE PARAMETER BLOCK (TSO)               *
*        PTPB = DSECT AND BASE                              *
*************************************************************
         SPACE 1
         IKJPTPB ,
PTPBLEN  EQU   *-PTPB              L'PTPB
         SPACE 3
*************************************************************
*        SDWA = SYSTEM DIAGNOSTIC WORK AREA                 *
*        SDWA = DSECT AND BASE                              *
*************************************************************
         SPACE 1
         IHASDWA ,
         SPACE 3
*************************************************************
*        SMCA = SMF (SYSTEM MANAGEMENT FACILITIES) CONTROL  *
*               AREA                                        *
*        SMCABASE = DSECT AND BASE                          *
*************************************************************
         SPACE 1
         IEESMCA ,
         SPACE 3
*************************************************************
*        STPL = STACK PARAMETER LIST (TSO)                  *
*        STPL = DSECT AND BASE                              *
*************************************************************
         SPACE 1
         IKJSTPL ,
STPLLEN  EQU   *-STPL              L'STPL
         SPACE 3
*************************************************************
*        S99 = SVC 99 PARAMETERS (DYNAMIC ALLOCATION)       *
*        S99RBP = SVC 99 INPUT REQUEST BLOCK POINTER DSECT  *
*                 AND BASE                                  *
*        S99RB = SVC 99 INPUT REQUEST BLOCK DSECT AND BASE  *
*        S99TUPL = SVC 99 TEXT UNITS POINTER LIST DSECT AND *
*                  BASE                                     *
*        S99TUNIT = SVC 99 TEXT UNIT DSECT AND BASE         *
*        S99TUFLD = SVC 99 TEXT UNIT FIELDS DSECT AND BASE  *
*************************************************************
         SPACE 1
         IEFZB4D0 ,
         SPACE 3
*************************************************************
*        TEXT KEY NAMES FOR SVC 99 (DYNAMIC ALLOCATION)     *
*************************************************************
         SPACE 1
         IEFZB4D2 ,
         SPACE 3
*************************************************************
*        TCB = TASK CONTROL BLOCK                           *
*        TCBFIX = PREFIX AND BASIC SECTIONS DSECT           *
*        TCBFIX = PREFIX SECTION BASE                       *
*        TCB = BASIC SECTION BASE                           *
*        TCBXTNT2 = COMMON EXTENSION DSECT AND BASE         *
*************************************************************
         SPACE 1
         IKJTCB LIST=YES
         SPACE 3
*************************************************************
*        TIOT = TASK INPUT/OUTPUT TABLE                     *
*        DSECT CARD NOT GENERATED                           *
*        TIOCNJOB = BASIC SECTION BASE                      *
*        TIOENTRY = DD ENTRY BASE                           *
*        TIOESTTB = DEVICE ENTRY BASE                       *
*************************************************************
         SPACE 1
TIOT     DSECT ,
         IEFTIOT1 ,
         SPACE 3
*************************************************************
*        STANDARD REGISTER NAMES                            *
*************************************************************
         SPACE 1
         #REGS R,                  STANDARD REGISTER NAMES             *
               (BASE1REG,R12),     PROGRAM BASE                        *
               (BASE2REG,R11),     PROGRAM BASE                        *
               (BASE3REG,R7)       PROGRAM BASE
         SPACE 3
         PRINT ON,GEN,NODATA
         TITLE 'SCHEDULE COMMAND -- ATTENTION EXIT PARAMETER LIST'
*************************************************************
*                                                           *
*        AEPI -- ATTENTION EXIT PARAMETER LIST              *
*                                                           *
*************************************************************
         SPACE 1
AEPL     DSECT ,
AEPLTAIE DS    A                   --> TAIE
AEPLIBUF DS    A                   --> IBUF (IF ANY)
AEPLUSER DS    A                   --> USER DATA (FROM STXUSER)
AEPLLEN  EQU   *-AEPL              L'AEPL
         TITLE 'SCHEDULE COMMAND -- VALIDITY CHECK PARAMETER LIST MAP'
*************************************************************
*                                                           *
*        VCPL -- VALIDITY CHECK PARAMETER LIST.             *
*                                                           *
*************************************************************
         SPACE 1
VCPL     DSECT ,
PDEADR   DS    A                   --> PDE BUILT BY IKJPARS
USERWORD DS    A                   USER SUPPLIED VALUE (FROM PPLUWA)
VALMSG   DC    X'FF000000'         2ND-LVL MSG PTR (FEEDBACK FIELD)
VCPLLEN  EQU   *-VCPL              L'VCPL
         TITLE 'SCHEDULE COMMAND -- PARAMETER DESCRIPTER ENTRY MAP'
*************************************************************
*                                                           *
*        PDE -- PARAMETER DESCRIPTER ENTRY.                 *
*                                                           *
*************************************************************
         SPACE 1
PDE      DSECT ,
PDEPTR   DS    A                   --> THE PARSED PARAMETER
PDELNGTH DS    Y                   L'PARSED PARAMETER
PDEFLAGS DS    B                   FLAG BYTE
PDEFEXST EQU   B'10000000'         THE PARAMETER IS PRESENT
PDECHAIN DS    A                   CHAIN FIELD FOR LISTS
         SPACE 1
         ORG   PDECHAIN            RELOCATE
PDEPTR2  DS    A                   --> 2ND PARM IN A RANGE
PDELNGT2 DS    Y                   L'2ND PARM OF A RANGE
PDEFLAG2 DS    B                   FLAG BYTE FOR 2ND PARM OF A RANGE
PDECHAI2 DS    A                   CHAIN FIELD FOR LISTS OF RANGES
         TITLE 'SCHEDULE COMMAND -- OUTPUT LINE DESCRIPTER MAP'
*************************************************************
*                                                           *
*        OLD -- OUTPUT LINE DESCRIPTER                      *
*                                                           *
*************************************************************
         SPACE 1
OLD      DSECT ,
OLDCHAIN DS    A                   --> NEXT OLD OR ZERO
OLDSEGCT DS    F                   MESSAGE SEGMENT COUNT
OLDTEXTP DS    A                   --> MESSAGE SEGMENT TEXT (-4)
         SPACE 1
OLDEND   DS    0F                  EO-OLD
OLDLEN   EQU   OLDEND-OLD          L'OLD
         TITLE 'SCHEDULE COMMAND -- COMNET "ALLOW" FLAGS'
*************************************************************
*                                                           *
*        COMNET "ALLOW" FLAGS -- THESE FLAGS ARE COPIED TO  *
*        THE PSCBATR2 FIELD DURING TSO-USER-LOGON.          *
*                                                           *
*************************************************************
         SPACE 1
         ALWFLAGS ,
         TITLE 'SCHEDULE COMMAND -- TIME - TIMESTAMP MAP'
*************************************************************
*                                                           *
*        TIME -- TIMESTAMP MAP                              *
*                                                           *
*************************************************************
         SPACE 1
TIME     DSECT ,
TIMEYR   DS    H                   YEAR
TIMEMO   DS    H                   MONTH
TIMEDY   DS    H                   DAY-OF-MONTH
TIMEYMD  EQU   TIMEYR,*-TIMEYR     DATE SEGMENT
TIMEHR   DS    H                   HOUR
TIMEMN   DS    H                   MINUTE
TIMEHM   EQU   TIMEHR,*-TIMEHR     TIME-OF-DAY SEGMENT
TIMETIME EQU   TIMEYR,*-TIMEYR
         TITLE 'SCHEDULE COMMAND -- LOCAL DATA STORAGE AREA'
*************************************************************
*                                                           *
*        #DSA - LOCAL DYNAMIC STORAGE AREA                  *
*                                                           *
*************************************************************
         SPACE 1
         #DSA  ,
         SPACE 3
*************************************************************
*        START OF DATA AREA. MISCELLANIOUS DATA.            *
*************************************************************
         SPACE 1
SAVDATSC DS    5A                  DATESCRN
SAVDISPL DS    4A                  DISPLAY
SAVFTIME DS    3A                  FINDTIME/MINSTIME
SAVFMINS DS    3A                  FINDMINS
SAVFWKDY DS    A                   FINDWKDY
SAVNXTID DS    3A                  NEXTID
SAVPMSG  DS    8A                  PUTMSG
         SPACE 1
DSAPUTL  DS    A                   --> IKJPUTL
DSAPTGT  DS    A                   --> IKJPTGT
         SPACE 1
DSAECB   DS    A                   PUTLINE/PUTGET ECB
SUBTTCBA DS    A                   --> SUBTASK TCB
STARMINS DS    F
LTOFFGMT DS    F                   THE DIFFERENCE BETWEEN     02/84 DBC
*                                  LOCAL TIME AND GMT         02/84 DBC
         SPACE 1
NOWYR    DS    H                   CURRENT YEAR
NOWMO    DS    H                   CURRENT MONTH
NOWDY    DS    H                   CURRENT DAY
NOWYMD   EQU   NOWYR,*-NOWYR       DATE SEGMENT
NOWHR    DS    H                   CURRENT HOUR
NOWMN    DS    H                   CURRENT MINUTE
NOWHM    EQU   NOWHR,*-NOWHR       TIME-OF-DAY SEGMENT
NOWTIME  EQU   NOWYR,*-NOWYR
NOWWK    DS    H                   CURRENT WEEKDAY
         SPACE 1
SCHDYR   DS    H                   SCHEDULED YEAR (IF ANY)
SCHDMO   DS    H                   SCHEDULED MONTH (IF ANY)
SCHDDY   DS    H                   SCHEDULED DAY (IF ANY)
SCHDYMD  EQU   SCHDYR,*-SCHDYR     DATE SEGMENT
SCHDHR   DS    H                   SCHEDULED HOUR
SCHDMN   DS    H                   SCHEDULED MINUTE
SCHDHM   EQU   SCHDHR,*-SCHDHR     TIME-OF-DAY SEGMENT
SCHDTIME EQU   SCHDYR,*-SCHDYR
SCHDWK   DS    H                   SCHEDULED WEEKDAY (IF ANY)
         SPACE 1
WRKPYR   DS    H
WRKPMO   DS    H
WRKPDY   DS    H
WRKPYMD  EQU   WRKPYR,*-WRKPYR
WRKPHR   DS    H
WRKPMN   DS    H
WRKPHM   EQU   WRKPHR,*-WRKPHR
WRKPTIME EQU   WRKPYR,*-WRKPYR
         SPACE 1
WRKNYR   DS    H
WRKNMO   DS    H
WRKNDY   DS    H
WRKNYMD  EQU   WRKNYR,*-WRKNYR
WRKNHR   DS    H
WRKNMN   DS    H
WRKNHM   EQU   WRKNHR,*-WRKNHR
WRKNTIME EQU   WRKNYR,*-WRKNYR
         SPACE 1
WNDOHR   DS    H                   SCHEDULED WINDOW SIZE (HHHH)
WNDOMN   DS    H                   SCHEDULED WINDOW SIZE (MM)
         SPACE 1
MYSYSID  DS    CL(L'SMCASID)       LOCAL SYSTEM IDENTIFIER
         SPACE 1
OFFSSAVE DS    H                   OFFSET SAVE AREA
NEXTID#  DS    H                   NEXT SFRID
         SPACE 1
         DS    0D
DSAWORK  DS    XL18                MISC VOLITILE WORK AREA
         SPACE 1
DSAFLAGS DS    B                   FLAG BYTE
DSAFSUBC EQU   B'10000000'         SUBCOMMAND PROCESSING HAS BEEN
*                                  INVOKED
DSAFNTLD EQU   B'01000000'         ESSENTIAL INITIALIZATION IS COMPLETE
DSAFADD2 EQU   B'00100000'         THE SCHEDULE FILE HAS BEEN ADDED TO
DSAFWNDX EQU   B'00010000'         THE WINDOW VALUE GIVEN WITH THE
*                                  SCHEDULE COMMAND HAS BEEN REDUCED TO
*                                  END AT MIDNIGHT.
DSAFOVRD EQU   B'00001000'         THE DISPLAY COMMAND IS PERFORMING A
*                                  PRELIMINARY PASS OF THE SCHEDULE
*                                  FILE TO CHECK FOR OVERRIDES.
DSAFTITL EQU   B'00000100'         A DISPLAY TITLE LINE HAS BEEN
*                                  WRITTEN
DSAFHIT  EQU   B'00000010'         A "HIT" HAS BEEN MADE ON THE
*                                  SCHEDULE FILE
         SPACE 1
CMDID    DS    X                   CURRENT SUBCOMMAND ID
CMDFGDSH EQU   B'10000000'         FLAG: THE "DATE" OPERAND FOR THIS
*                                  SUBCOMMAND IS PERMITTED TO CONTAIN
*                                  DASHES (SIGNIFYING NON-SPECIFIC
*                                  VALUES)
CMDFGSID EQU   B'01000000'         FLAG: THE "SYSID" OPERAND FOR THIS
*                                  SUBCOMMAND IS PERMITTED TO SPECIFY
*                                  "ALL".
         SPACE 1                                              CNW87 RWO
CMDIDINV EQU   0                   INVALID
CMDIDCHA EQU   1+CMDFGDSH          CHANGE
CMDIDDEL EQU   2+CMDFGSID          DELETE
CMDIDDIS EQU   3+CMDFGSID          DISPLAY
CMDIDEND EQU   4                   END
CMDIDHEL EQU   5                   HELP
CMDIDSCH EQU   6+CMDFGDSH          SCHEDULE
         EJECT ,
*************************************************************
*        GSAM REQUEST BLOCK                                 *
*************************************************************
         SPACE 1
         GSB   DSECT=NO
         EJECT ,
         ORG   GSBBUFX             LOCATE TO THE START OF THE BUFFER
         SFR   MF=(D,N)            GEN THE SFR MAP
         SPACE 1
         ORG   ,
GSBBUFLN EQU   *-GSBBUFX           L'GSAM BUFFER
         SPACE 3
SAVESFR  DS    XL(SFRLEN)          SFR SAVE AREA (USED BY CHANGE)
         EJECT ,
*************************************************************
*        VARIOUS TSO CONTROL/PARAMETER BLOCKS               *
*************************************************************
         SPACE 1
         DS    0F
DSACPPL  DS    XL(CPPLLEN)         COMMAND PROCESSOR PARAMETER LIST
         SPACE 1
         DS    0F
DSAIOPL  DS    XL(IOPLLEN)         INPUT/OUTPUT PARAMETER LIST
         SPACE 3
*************************************************************
*        COMMAND SCAN PARAMETER BLOCKS                      *
*************************************************************
         SPACE 1
         DS    0F
DSACSPL  DS    XL(CSPLLEN)         COMMAND SCAN PARAMETER LIST
         SPACE 1
         DS    0F
DSACSOA  DS    XL(CSOALEN)         COMMAND SCAN OUTPUT AREA
         SPACE 1
CSFLGWRD DS    XL4                 COMMAND SCAN FLAG WORD
         SPACE 3
*************************************************************
*        PARSE PARAMETER BLOCKS                             *
*************************************************************
         SPACE 1
         DS    0F
DSAPPL   DS    XL(PPLLEN)          PARSE PARAMETER LIST
         SPACE 1
DSAPDLAD DS    A                   PARSE ANSWER AREA (--> PDL)
         SPACE 3
*************************************************************
*        PUTLINE PARAMETER BLOCKS                           *
*************************************************************
         SPACE 1
DSAPTPB  PUTLINE OUTPUT=(DSAOLDS,TERM,MULTLVL,INFOR),                  *
               TERMPUT=(EDIT,WAIT,NOHOLD,NOBREAK),                     *
               MF=L
DSAIBUF  DS    A                   --> SUBCOMMAND'S IBUF OR ZERO
*        NOTE                      DSAIBUF ALWAYS POINTS TO THE IBUF
*                                  GOTTEN BY THE SUBCOMMAND-PUTGET CALL
*                                  OR IT CONTAINS A ZERO. PGPBIBUF
*                                  (ABOVE) ALWAYS POINTS TO THE CURRENT
*                                  WORKING IBUF WHICH MAY BE FROM
*                                  EITHER THE SUBCOMMAND-PUTGET OR THE
*                                  TMP (CPPLCBUF) OR A
*                                  PROMPTING-PUTGET.
         SPACE 1
         DS    0F,XL(OLDLEN)       GARBAGE AREA
DSAOLDS  DS    2XL(OLDLEN)         TWO OLDS
DSAOLD#  EQU   (*-DSAOLDS)/L'DSAOLDS NUMBER OF OLDS
DSAOLDZ  DC    X'FF'               EO-OLDS SIGNAL
         SPACE 1
DSAMSGS  DS    CL256               MSG BLOCKS CONSTRUCTION AREA FOR
*                                  PUTMSG
         SPACE 1
         DS    FL1
DYNAMSGS DS    CL256               DYNAMIC MESSAGES CONSTRUCTION AREA
         DS    FL1'0'              TRAILER
         SPACE 1
         ORG   DYNAMSGS
DMSG     DS    0C
         DC    C' '
DMID     DC    C'9999',C' '        ID
DMSYSID  DC    CL(L'SFRSYSID)'SID',C'  ' SYSID
DMDATE   DC    C'--/--/--',C' '    DATE
         DC    C'-'
DMDAY    DC    C'SUN',C' '         DAY
DMTIME   DC    C'23:59',C' '       TIME
DMWINDOW DC    C'+9999:59',C' '    WINDOW
DMFLAGS  DC    C'I O N',C' '       FLAGS
DMNAME   DC    CL(L'SFRNAME)'APPLNAME',C'  ' NAME
DMCMD    DS    CL(L'DYNAMSGS-(*-DMSG)) COMMAND
DML      EQU   *-DMSG
         SPACE 1
         ORG   ,
         SPACE 3
*************************************************************
*        PUTGET PARAMETER BLOCKS                            *
*************************************************************
         SPACE 1
DSAPGPB  PUTGET TERMPUT=(EDIT,WAIT,NOHOLD,NOBREAK),                    *
               TERMGET=(EDIT,WAIT),                                    *
               MF=L
         SPACE 3
*************************************************************
*        STAX PARAMETER BLOCKS                              *
*************************************************************
         SPACE 1
DSASTPL  STAX  STAXEXIT,USADDR=DSA,MF=L
         SPACE 3
*************************************************************
*        ATTACH PARAMETER LIST FOR THE HELP SUB-COMMAND     *
*************************************************************
         SPACE 1
HELPATTA ATTACH EP=HELP,ECB=DSAECB,SHSPV=78,SZERO=NO,ESTAI=ESTAHELP,   *
               LPMOD=1,SF=L
         SPACE 3
*************************************************************
*        DYNALLOC PARAMETERS                                *
*************************************************************
         SPACE 1
         DS    0F                  PLIST
DALPLIST DS    AL1(S99RBPND),AL3(DALRB)
         SPACE 1
         DS    0F                  REQUEST BLOCK
DALRB    DS    5A(0)
DALRBLEN EQU   *-DALRB
         SPACE 1
         DS    0F                  TEXT KEYS AND VECTOR
DALINFO  DS    XL256
         SPACE 3
*************************************************************
*        LISTS AREA. THIS MUST COME AT THE END OF THE DSA.  *
*************************************************************
         SPACE 1
OVRDEOL  DS    A                   --> EO-LIST
OVRDLIST DS    0X
         SPACE 1
         DS    1000XL(L'SFRNAME)   OVERRIDE LIST (USED BY "DISPLAY" AND
*                                  "DELETE")
         SPACE 1
         ORG   OVRDLIST            OVERLAY
         DS    1000XL(L'SFRID)     IDS LIST (USED BY "CHANGE")
         SPACE 1
         ORG   ,                   RELOCATE HIGH
OVRDLEND EQU   *                   END OF LIST AREA
         SPACE 3
*************************************************************
*        END OF DSA                                         *
*************************************************************
         SPACE 1
DSAEND   EQU   *                   EO-DSA
DSALEN   EQU   DSAEND-DSA          L'DSA
         TITLE 'SCHEDULE COMMAND -- INITIALIZATION'
*************************************************************
*        REENTRANT ENTRY LINKAGE                            *
*************************************************************
         SPACE 1
SCHEDULE #ENTER SCHEDULE
               BASES=(BASE1REG,BASE2REG,BASE3REG),                     *
               SAVTYPE=(RENT,DSALEN)
         USING DSA,R13             DCL DSA BASE
         SPACE 3
*************************************************************
*        INITIALIZE MISC. DSA FIELDS                        *
*************************************************************
         SPACE 1
         MVC   DSACPPL,0(R1)       SAVE THE CPPL
         SPACE 1
         L     R1,CVTPTR           --> CVT
         ICM   R1,15,CVTSMCA-CVTMAP(R1) --> SMCA; EXIST?
         BZ    NOSMCA              NO, SKIP
         MVC   MYSYSID,SMCASID-SMCABASE(R1) YES, SAVE LOCALLY
NOSMCA   DS    0H
         SPACE 1                                              02/84 DBC
         TIME  MIC,DSAWORK,        GET "ELAPSED" TIME         02/84 DBC*
               ZONE=LT             LOCAL TIME                 02/84 DBC
         L     R2,DSAWORK          SAVE THE SIGNIFICANT PART  02/84 DBC
         TIME  MIC,DSAWORK,        GET "ELAPSED" TIME         02/84 DBC*
               ZONE=GMT            GMT                        02/84 DBC
         SL    R2,DSAWORK          GET THE DIFFERENCE         02/84 DBC
         ST    R2,LTOFFGMT         SAVE IT                    02/84 DBC
         SPACE 3
*************************************************************
*        INITIALIZE VARIOUS TSO SERVICE ROUTINE RELATED     *
*        PARAMETER BLOCKS.                                  *
*************************************************************
         SPACE 1
         L     R2,CPPLUPT-CPPL+DSACPPL --> UPT
         L     R3,CPPLECT-CPPL+DSACPPL --> ECT
         LA    R4,DSAECB           --> ECB
         LA    R5,CSFLGWRD         --> CMD SCAN FLAG WORD
         LA    R6,DSACSOA          --> CSOA
         STM   R2,R6,DSACSPL       INIT CSPL
         SPACE 1
         SR    R5,R5               CLEAR
         LA    R6,DSAPDLAD         --> IKJPARS ANSWER AREA
         STM   R2,R6,DSAPPL        BUILD MOST OF THE PPL
         ST    R13,PPLUWA-PPL+DSAPPL BUILD REST OF THE PPL
         SPACE 1
         PUTLINE PARM=DSAPTPB,     INITIALIZE THE IOPL AND PTPB        *
               UPT=(R2),ECT=(R3),ECB=(R4),                             *
               OUTPUT=(DSAOLDS,TERM,MULTLVL,INFOR),                    *
               TERMPUT=(EDIT,WAIT,NOHOLD,NOBREAK),                     *
               ENTRY=PLINITED,MF=(E,DSAIOPL)
PLINITED DS    0H
         SPACE 1
         LA    R1,DSAOLDS          --> THE OLDS
         USING OLD,R1              DCL OLD BASE
         LA    R0,DSAOLD#          GET # OLDS ASSEMBLED
         LA    R15,1               SETUP TO INITIALIZE SEGMENT COUNTS
         BALR  R14,0               LOAD LOOP HEAD
         ST    R15,OLDSEGCT        SET SEGMENT COUNT=1
         LA    R1,OLD+OLDLEN       --> NEXT OLD
         BCTR  R0,R14              LOOP TO INITIALIZE IT
         DROP  R1                  RELEASEE OLD BASE
         MVI   0(R1),X'FF'         SET EO-OLDS SIGNAL
         SPACE 1
         #PUT  SUBAD=PUTMSG,MF=INIT INITIALIZE #PUT MACRO
         SPACE 1
         L     R15,CVTPTR          --> CVT
         ICM   R0,15,CVTPUTL-CVTMAP(R15) --> IKJPUTL; LOADED BY NIP?
         BM    GOTPUTL             YES, PROCEED
         LOAD  EP=IKJPUTL          NO, LOAD IT NOW
GOTPUTL  ST    R0,DSAPUTL          STORE IKJPUTL ADDRESS LOCALLY
         SPACE 1
         PUTGET PARM=DSAPGPB,                                          *
               TERMPUT=(EDIT,WAIT,NOHOLD,NOBREAK),                     *
               TERMGET=(EDIT,WAIT),                                    *
               ENTRY=PGINITED,MF=(E,DSAIOPL) INIT PGPB
PGINITED DS    0H
         SPACE 1
         L     R15,CVTPTR          --> CVT
         ICM   R0,15,CVTPTGT-CVTMAP(R15) --> IKJPTGT; LOADED BY NIP?
         BM    GOTPTGT             YES, PROCEED
         LOAD  EP=IKJPTGT          NO, LOAD IT NOW
GOTPTGT  ST    R0,DSAPTGT          STORE IKJPTGT ADDRESS LOCALLY
         SPACE 3
*************************************************************
*        ESTABLISH A STAX EXIT.                             *
*************************************************************
         SPACE 1
         STAX  STAXEXIT,USADDR=DSA,MF=(E,DSASTPL)
         SPACE 3
*************************************************************
*        CHECK TO SEE IF THE USER IS PERMITTED TO USE THIS  *
*        COMMAND.                                           *
*************************************************************
         SPACE 1
         L     R1,PSATOLD-PSA      --> OUR TCB
         L     R1,TCBJSCB-TCB(,R1) --> JSCB
         L     R1,JSCBACT-IEZJSCB(,R1) --> ACTIVE JSCB
         ICM   R1,15,JSCBPSCB-IEZJSCB(R1) --> PSCB; EXIST?
         BZ    CALLFAIL            NO, NOT TSO; FAIL THE CALLER
*************************************************************
* INSERT HERE YOUR OWN AUTHORITY CHECK                      *
*************************************************************
         TM    PSCBATR2-PSCB(R1),ALWDEVEL+ALWSUPRT+ALWOPER Y, GOODGUY?
         BNZ   CALLEROK            YES, PROCEED
         SPACE 3
*************************************************************
*        THE CALLER IS NOT AUTHORIZED TO USE THIS COMMAND.  *
*        BUILD A MISSLEADING MESSAGE AND THEN EXIT.         *
*************************************************************
         SPACE 1
CALLFAIL MVC   DYNAMSGS(18),=C'IKJ56500I COMMAND '
         L     R1,CPPLECT-CPPL+DSACPPL --> ECT
         MVC   DYNAMSGS+18(L'ECTPCMD),ECTPCMD-ECT(R1) GET CMD NAME
         LA    R1,DYNAMSGS+L'ECTPCMD+18 LOAD SCANNER
CMDNMLP  BCTR  R1,0                BACK UP
         TM    0(R1),B'10111111'   STILL BLANK?
         BZ    CMDNMLP             YES, KEEP BACKING UP
         MVC   1(11,R1),=C' NOT FOUND
         LA    R1,11(,R1)          --> PAST TEXT
         LA    R0,DYNAMSGS         --> SO-MSG BLOCK
         SR    R1,R0               GET L'MSG BLOCK
         STC   R1,DYNAMSGS-1       STORE
         LA    R0,DYNAMSGS-1
         B     MSGGET              ISSUE MSG AND EXIT
CALLEROK DS    0H
         SPACE 3
*************************************************************
*        PARTIAL INITIALIZATION OF DYNALLOC PARAMETERS      *
*************************************************************
         SPACE 1
         LA    R0,DALRB            --> REQUEST BLOCK
         ST    R0,DALPLIST         STORE INTO PLIST
         MVI   DALPLIST,S99RBPND   SET "LAST PLIST ENTRY" FLAG
         SPACE 1
         LA    R0,DALINFO          --> TEXT KEYS STUFF AREA
         ST    R0,S99TXTPP-S99RB+DALRB STORE INTO REQUEST BLOCK
         SPACE 3
*************************************************************
*        THE SCHEDULE FILE IS ABOUT TO BE "OPENED". IF      *
*        "FAKEGSAM" IS BEING USED, THEN A REAL OPEN IS      *
*        ABOUT TO BE PERFORMED IN WHICH CASE, IF THE        *
*        SCHEDULE FILE IS PASSWORD PROTECTED FOR UPDATE,    *
*        THEN THE USER WILL BE PROMPTED FOR THE PASSWORD.   *
*        HOWEVER, I AM SECURE; I AM SPECIAL; AND IN GENERAL *
*        I AM AN ALL AROUND CERTIFIED GOOD-GUY. SO, IT IS   *
*        NOT NECESSARY THAT THE USER BE PROMPTED.           *
*        ACCORDINGLY, I WILL NOW SET THE "SUPPRESS PASSWORD *
*        CHECKING" FLAG; OPEN THE SCHEDULE FILE; AND THEN   *
*        RESET THE FLAG AFTER OPEN. I WILL ALSO, HOWEVER,   *
*        SET UP ESTAE PROTECTION SO THAT AN INADVERTANT     *
*        ABEND DOESN'T LEAVE THAT FLAG ON FOREVER.          *
*************************************************************
         SPACE 1
GSAMRTRY L     R1,PSATOLD-PSA      --> MY TCB
         L     R1,TCBJSCB-TCB(,R1) --> MY JSCB
         L     R1,JSCBACT-IEZJSCB(,R1) (HO-HUM) --> "ACTIVE" JSCB
         SR    R2,R2               SET "PASSWORD SUPPRESSION
*                                  ALREADY SET" SIGNAL
         TM    JSCBSWT1-IEZJSCB(R1),JSCBPASS TRUE?
         BNZ   PASSOK              YES, NOTHING TO DO HERE
         SPACE 1
         LR    R2,R1               NO, COPY JSCB PTR. ALSO
*                                  SERVES AS A "PASSWORD
*                                  SUPPRESSION BEING SET
*                                  LOCALLY" SIGNAL
         ESTAE PASSGARD,CT,TERM=YES SET UP TEMP ESTAE COVERAGE
         MODESET KEY=ZERO          SET KEY-0
         OI    JSCBSWT1-IEZJSCB(R2),JSCBPASS SUPPRESS PASSWORD
*                                  CHECKING
         MODESET KEY=NZERO         RESTORE P/P-KEY
PASSOK   DS    0H
         SPACE 3
*************************************************************
*        INITIALIZE THE GSAM REQUEST BLOCK. SEE IF A MASTER *
*        RECORD EXISTS IN THE SCHEDULE FILE.                *
*************************************************************
         SPACE 1
         MVC   SFMRKEY,SFMRKEY-SFMR+MASTER GET MASTER'S KEY
         GSAMCALL READ,            READ THE MASTER RECORD              *
               FILE=DDNAME,        DDNAME                              *
               POOL=3,             (VALUE INTERNAL TO GSAM)            *
               BUFFER=-1,          BUFFER FOLLOWS THE GSB              *
               BUFFLEN=GSBBUFLN,   L'BUFFER                            *
               OPTIONS=(INITIAL,   INITIALIZE THE GSB -                *
               WAIT,SYNC,FWD,ABTERM, WITH MISC "STATIC" FLAGS          *
               DIR),               THIS IS A DIRECT REQUEST            *
               MF=(E,GSB)
         LR    R3,R15              SAVE THE GSAM RETURN CODE
         SPACE 3
*************************************************************
*        REGARDLESS OF WHETHER OR NOT THE SCHEDULE FILE     *
*        "OPEN" OCCURED OR SUCCEEDED, IF I SET THE          *
*        "SUPPRESS PASSWORD CHECKING" FLAG LOCALLY, THEN    *
*        RESET IT NOW.                                      *
*************************************************************
         SPACE 1
         LTR   R2,R2               WAS THE FLAG SET LOCALLY?
         BZ    PASSOK2             NO, SKIP
         MODESET KEY=ZERO          YES, LOAD KEY-0
         NI    JSCBSWT1-IEZJSCB(R2),255-JSCBPASS RESET THE FLAG
         MODESET KEY=NZERO         RESUME P/P-KEY
         ESTAE 0                   CANCEL THE TEMP ESTAE
PASSOK2  DS    0H
         SPACE 3
*************************************************************
*        TEST THE RESULTS OF THE INITIAL GSAM CALL.         *
*************************************************************
         SPACE 1
         LTR   R15,R3              WAS THE READ OK?
         BNZ   FILEPROB            NO, GO SEE WHAT HAPPENED
         CLC   SFMRKEY(L'SFMRKEY+L'SFMRFID),SFMRKEY-SFMR+MASTER YES,
*                                  RIGHT RECORD READ?
         BE    FILEOK              YES, PROCEED
         LA    R14,=C'THE MASTER RECORD DOES NOT EXIST' NO, --> TEXT
         B     GSAMERR             TERMINAL ERROR
FILEPROB DS    0H
         SPACE 3
*************************************************************
*        AN ERROR HAS OCCURED WHILE ATTEMPTING TO READ THE  *
*        MASTER RECORD. IF THE ERROR IS BECAUSE THE         *
*        SCHEDULE FILE IS NOT ALLOCATED, THEN ATTEMPT TO    *
*        ALLOCATE IT NOW. NOTE, THIS WILL BE EFFECTIVE ONLY *
*        IF FAKEGSAM IS RUNNING. IF REAL GSAM IS RUNNING,   *
*        THEN THIS WILL HAVE NO EFFEFT.                     *
*************************************************************
         SPACE 1
         CH    R15,=Y(GSAMEFIL)    GSAM FILE ERROR?
         BNE   NOTEFIL             NO, SKIP
         CLI   GSBVSAM+3,0         YES, FILE NOT DEFINED?
         BNE   NOTEFIL             NO, SKIP
         SPACE 1
         L     R1,PSATOLD-PSA      YES, --> MY TCB
         L     R1,TCBTIO-TCB(,R1)  --> MY TIOT
         LA    R0,TIOENTRY-TIOT    GET 1ST DD-ENTRY OFFSET
         USING TIOENTRY,R1         DCL DD-ENTRY BASE
         BALR  R14,0               LOAD LOOP HEAD
         AR    R1,R0               --> NEXT DD ENTRY
         ICM   R0,1,TIOELNGH       GET ITS LENGTH; EO-TIOT?
         BZ    ALOCSCHD            YES, "SCHEDULE" NOT ALREADY
*                                  ALLOCATED; GO ATTEMPT ALLOCATION
         CLC   TIOEDDNM,DDNAME     NO, "SCHEDULE" DD-ENTRY?
         BNER  R14                 NO, KEEP SEARCHING
         DROP  R1                  YES, RELEASE DD-ENTRY BASE
         SPACE 1
IMRRFAIL LA    R14,=C'INITIAL MASTER RECORD READ' --> FAILURE MSG
         LA    R15,GSAMEFIL        RESTORE GSAM'S RC
         B     GSAMERR             ISSUE ERROR MSG AND TERMINATE
         SPACE 1
ALOCSCHD MVC   S99RBLN-S99RB+DALRB(L'S99RBLN+L'S99VERB+L'S99FLAG1),=AL1*
               (DALRBLEN,S99VRBAL,S99NOMNT,0) BUILD DAL RB
         LA    R14,DSADDNAM-DSADATA+DALINFO+4*DSACOUNT --> DDNAME KEY
         LA    R15,DSADSNAM-DSADATA+DALINFO+4*DSACOUNT --> DSNAME KEY
         LA    R0,DSASTATS-DSADATA+DALINFO+4*DSACOUNT --> 1ST DISP KEY
         STM   R14,R0,DALINFO      BUILT TEXT KEYS VECTOR
         MVI   DALINFO+4*DSACOUNT-4,S99TUPLN FLAG EO-VECTOR
         MVC   DALINFO+4*DSACOUNT(DSADATAL),DSADATA GET TEXT KEYS
         #TEST SIZE=(L'DALINFO,GE,4*DSACOUNT+DSADATAL)
         LA    R1,DALPLIST         --> PLIST
         DYNALLOC ,                ATTEMPT ALLOCATION
         LTR   R15,R15             SCHEDULE FILE ALLOCATED?
         BZ    GSAMRTRY            YES, RETRY THE MASTER RECORD READ
         B     IMRRFAIL            NO, ISSUE ERROR MSG AND QUIT
NOTEFIL  DS    0H
         SPACE 3
*************************************************************
*        SEE IF THE MASTER FILE READ FAILURE IS BECAUSE THE *
*        SCHEDULE FILE IS ALLOCATED BY SOMEONE ELSE (AND IT *
*        IS NOT SHARABLE).                                  *
*************************************************************
         SPACE 1
         CH    R15,=Y(GSAMEFIL)    FILE OPEN ERROR?
         BNE   NOTRESOU            NO, SKIP
         CLI   GSBVSAM+3,168       YES, RESOURCE(S) UNAVAILABLE ERR?
         BNE   NOTRESOU            NO, SKIP
         MVC   GSBMSGX(77),=C' THE SCHEDULE FILE IS IN USE BY ANOTHER J*
               OB OR USER. PLEASE TRY AGAIN LATER. ' YES, MSG
         MVC   GSBMSGX+77(L'GSBMSGX-77),GSBMSGX+76 CLEAR REST OF BUF
         B     IMRRFAIL            GO TAKE ERROR EXIT
NOTRESOU DS    0H
         SPACE 3
*************************************************************
*        SEE IF THE MASTER FILE READ FAILURE IS BECAUSE THE *
*        SCHEDULE FILE IS EMPTY.                            *
*************************************************************
         SPACE 1
         CH    R15,=Y(GSAMEDS)     GSAM DATASET ERROR?
         BNE   IMRRFAIL            NO, NOT RECOVERABLE
         CLI   GSBVSAM+3,116       YES, EMPTY DATASET?
         BNE   IMRRFAIL            NO, TERMINATE
         SPACE 3
*************************************************************
*        THE SCHEDULE FILE IS EMPTY. INITIALIZE IT BY       *
*        WRITING A NEW MASTER RECORD TO IT.                 *
*************************************************************
         SPACE 1
         MVC   SFMR(SFMRLEN),MASTER GET A NEW MASTER RECORD
         GSAMCALL WRITE,           WRITE THE NEW MASTER RECORD         *
               OPTIONS=SEQ,                                            *
               RECLEN=SFMRLEN,                                         *
               MF=(E,GSB)
         SPACE 1
         LA    R14,=C'
MASTER RECORD CREATION'
         LTR   R15,R15             AOK?
         BNZ   GSAMERR             NO, TERMINAL ERROR
         SPACE 3
*************************************************************
*        NOW RESET THE DATASET AND VALIDATE THE NEW MASTER  *
*        RECORD BY RE-READING IT.                           *
*************************************************************
         SPACE 1
         GSAMCALL READ,            RE-READ THE NEW MASTER RECORD       *
               OPTIONS=(DIR,NOUPDATE),                                 *
               MF=(E,GSB)
         LA    R14,=C'INITIAL MASTER RECORD REREAD'
         LTR   R15,R15             AOK?
         BNZ   GSAMERR             NO, TERMINAL ERROR
FILEOK   DS    0H
         SPACE 3
*************************************************************
*        ESSENTIAL INITIALIZATION IS NOW COMPLETE.          * 02/84 DBC
*************************************************************
         SPACE 1
         OI    DSAFLAGS,DSAFNTLD   ESSENTIAL INITIALIZATION IS COMPLETE
         EJECT ,
*************************************************************
*        CALL COMMAND-SCAN TO DETERMINE IF THE INVOCATION   *
*        COMMAND CONTAINS A SUBCOMMAND.                     *
*************************************************************
         SPACE 1
         L     R1,CPPLCBUF-CPPL+DSACPPL --> INVOCATION CMD
         ST    R1,PGPBIBUF-PGPB+DSAPGPB (PRETENT IT'S PUTGET INPUT)
         ST    R1,CSPLCBUF-CSPL+DSACSPL STORE FOR IKJSCAN
         MVC   OFFSSAVE,2(R1)      SAVE CURRENT OFFSET VALUE
         MVI   DSAECB,0            CLEAR ATTENTION SIGNAL
         CALLTSSR EP=IKJSCAN,MF=(E,DSACSPL) CALL IKJSCAN
         LTR   R15,R15             AOK?
         #DIE  NZ                  NO, LOGIC ERROR
         TM    CSOAFLG-CSOA+DSACSOA,CSOAQM+CSOABAD+CSOAEXEC IS AN
*                                  INVALID COMMAND NAME PRESENT?
         BNZ   JUSTSCHD            YES, IMPLIED SCHEDULE COMMAND
         TM    CSOAFLG-CSOA+DSACSOA,CSOAVWP+CSOAVNP NO, IS A VALID
*                                  COMMAND NAME PRESENT?
         BNZ   CMDDCODE            YES, GO IDENTIFY IT
         TM    CSOAFLG-CSOA+DSACSOA,CSOANOC NO, IS "NO OPERANDS" SIGNAL
*                                  SET?
         #DIE  Z                   NO, LOGIC ERROR
         OI    DSAFLAGS,DSAFSUBC   YES, REMEMBER SUBCOMMAND MODE
         B     PUTGET              GO GET 1ST SUBCOMMND
         TITLE 'SCHEDULE COMMAND -- SUBCOMMAND AQUISITION LOOP'
*************************************************************
*        DISPLAY AN (ERROR) MESSAGE AND THEN FALL THROUGH   *
*        TO GET THE NEXT SUBCOMMAND.                        *
*************************************************************
         SPACE 1
MSGGET   #PUT  (R0)                SEND THE MESSAGE
         NOP   0                   +0 ATTENTION; IGNORE IT
*        FALL THROUGH              +4 AOK; PROCEED
         SPACE 3
*************************************************************
*        MAIN PUTGET LOOP. FIRST CLEAN UP ANY TRASH THAT    *
*        MIGHT BE LYING AROUND.                             *
*************************************************************
         SPACE 1
PUTGET   ICM   R1,15,DSAIBUF       DOES A PUTGET INPUT BUFFER EXIST?
         BZ    PUTGET1             NO, PROCEED
         LH    R0,0(,R1)           YES, GET CBUF'S LENGTH
         ICM   R0,8,=AL1(1)        SET SUBPOOL=1
         FREEMAIN R,LV=(0),A=(1)   RELEASE THE BUFFER
         XC    DSAIBUF,DSAIBUF     CLEAR THE IBUF PTR
PUTGET1  DS    0H
         SPACE 1
         IKJRLSA DSAPDLAD          IF PDL EXISTS, THEN RELEASE IT TOO
         XC    DSAPDLAD,DSAPDLAD   CLEAR PDL PTR
         SPACE 1
         TM    DSAFLAGS,DSAFNTLD   HAS ESSENTIAL INITIALIZATION
*                                  COMPLETED?
         BZ    EXIT16              NO, ERROR
         SPACE 3
*************************************************************
*        IF THIS IS SUBCOMMAND MODE, THEN PUT OUT THE MODE  *
*        MESSAGE AND GET THE NEXT SUBCOMMAND.               *
*************************************************************
         SPACE 1
         TM    DSAFLAGS,DSAFSUBC   SUBCOMMAND MODE?
         BZ    EXIT0               NO, RETURN TO TMP
         LA    R1,=X'4016'         YES, --> BLANK, BACKSPACE
         LA    R0,2                GET L'MSG
         ICM   R1,8,=B'00001001'   TPUT ASIS, HOLD
         TPUT  (1),(0),R           WAIT FOR ALL OUTPUT TO COMPLETE     *
                                   BEFORE CANCELLING THE STAX
         STAX  ,                   CANCEL THE STAXEXIT
         MVI   DSAECB,0            CLEAR THE ATTENTION ECB
         XC    PGPBIBUF-PGPB+DSAPGPB,PGPBIBUF-PGPB+DSAPGPB ZAP IBUF PTR
         L     R15,DSAPTGT         --> IKJPTGT
         PUTGET PARM=DSAPGPB,      GET NEXT COMMAND                    *
               OUTPUT=(MODEMSG-12,SINGLE,MODE),                        *
               ENTRY=(15),MF=(E,DSAIOPL)
         MVC   DSAIBUF,PGPBIBUF-PGPB+DSAPGPB GET IBUF PTR, IF ANY
         SPACE 1
         CL    R15,=F'32'          IS THE RC VALID?
         #DIE  H                   NO, LOGIC ERROR
         B     *+4(R15)            YES, ACT ON THE RC
         B     CMDSCAN             +0 CMD RECEIVED FROM TERMINAL
         B     CMDSCAN             +4 CMD RECEIVED FROM ^TERMINAL
         B     EXIT4               +8 ATTENTION INTERRUPT
         B     PUTGET              +12 SUPPRESSED DUE TO 2ND-LVL MSGS
         #DIE  C,15                +16 TPUT-NOWAIT FAILURE
         #DIE  C,15                +20 TGET-NOWAIT FAILURE
         #DIE  C,15                +24 INVALID PARAMETERS
         B     EXIT12              +28 GETMAIN FAILURE
         B     EXIT8               +32 TERMINAL DISCONNECTED
         SPACE 3
*************************************************************
*        USE IKJSCAN TO EXTRACT AND SYNTAX CHECK THE        *
*        SUBCOMMAND.                                        *
*************************************************************
         SPACE 1
CMDSCAN  STAX  STAXEXIT,USADDR=DSA,MF=(E,DSASTPL) RESTORE STAX EXIT
         MVC   CSPLCBUF-CSPL+DSACSPL,PGPBIBUF-PGPB+DSAPGPB --> CBUF
         MVI   DSAECB,0            CLEAR ATTENTION ECB
         CALLTSSR EP=IKJSCAN,MF=(E,DSACSPL) CALL IKJSCAN
         LTR   R15,R15             AOK?
         #DIE  NZ                  NO, LOGIC ERROR
         TM    CSOAFLG-CSOA+DSACSOA,CSOANOC ANYTHING PRESENT?
         BNZ   PUTGET              NO, IGNORE NULL LINE
         L     R0,=A(MSYNTERR-1)   YES, ASSUME INVALID
         TM    CSOAFLG-CSOA+DSACSOA,CSOAQM+CSOABAD+CSOAEXEC INVALID
*                                  COMMAND PRESENT?
         BNZ   MSGGET              YES, ERROR
         SPACE 3
*************************************************************
*        IDENTIFY THE GIVEN SUBCOMMAND. CHECK FOR AMBIGUOUS *
*        OR UNRECOGNIZED NAMES.                             *
*************************************************************
         SPACE 1
CMDDCODE ICM   R1,15,CSOACNM-CSOA+DSACSOA --> CBUF; EXIST?
         #DIE  Z                   NO, LOGIC ERROR
         LH    R15,CSOALNM-CSOA+DSACSOA YES, GET ITS LENGTH
         LTR   R15,R15             NULL?
         #DIE  NP                  YES, LOGIC ERROR
         CH    R15,=H'8'           NO, TOO LONG?
         #DIE  H                   YES, LOGIC ERROR
         BCTR  R15,0               NO, ADJUST FOR 'EX'
         SR    R3,R3               CLEAR A(COMMAND HANDLER)
         MVI   CMDID,CMDIDINV      BE PESSIMISTIC
         LA    R2,COMMANDS-CMDROOTL-1 LOAD VALID CMDS LIST SCANNER
         SR    R14,R14             CLEAR L'CMD
         SPACE 1
CMDLOOP  LA    R2,CMDROOTL+1(R14,R2) --> NEXT LIST ENTRY
         IC    R14,CMDLNGTH(,R2)   GET LENGTH VALUE
         CLR   R15,R14             IS THIS CMD LONG ENOUGH?
         BH    CMDLOOP             NO, TRY NEXT ENTRY
         CLC   0(*-*,R1),CMDNAME(R2) (EXECUTED)
         EX    R15,*-6             YES, RIGHT ENTRY?
         BH    CMDLOOP             NO, TRY NEXT ENTRY
         BL    CMDCHK              NO, UNRECOGNIZED; GO HANDLE
         L     R0,=A(MAMBGERR-1)   YES, ASSUME AMBIGUOUS
         LTR   R3,R3               AMBIGUOUS?
         BNZ   MSGGET              YES, ERROR
         LR    R3,R2               NO, SAVE A(ENTRY)
         B     CMDLOOP             LOOP TO CHECK FOR AMBIGUITY
         SPACE 1
CMDCHK   LTR   R3,R3               WAS A HANDLER FOUND?
         BNZ   GOTCMD              YES, PROCEED
         L     R0,=A(MUNREERR-1)   NO, --> 'UNRECOGNIZED COMMAND'
         TM    DSAFLAGS,DSAFSUBC   SUBCOMMAND MODE?
         BNZ   MSGGET              YES, ERROR
         SPACE 3
*************************************************************
*        NOT SUBCOMMAND MODE. POSSIBLE IMPLIED SCHEDULE     *
*        COMMAND GIVEN.                                     *
*************************************************************
         SPACE 1
JUSTSCHD L     R1,PGPBIBUF-PGPB+DSAPGPB --> TMP'S CBUF
         MVC   2(2,R1),OFFSSAVE    RESTORE ORIGINAL OFFSET VALUE
         LA    R3,CMDSCHED         --> SCHEDULE'S TABLE ENTRY
         SPACE 3
*************************************************************
*        GOT A COMMAND. SET UP SOME STANDARD STUFF AND THEN *
*        GO TO IT.                                          *
*************************************************************
         SPACE 1
GOTCMD   MVC   CMDID,CMDIDENT(R3) SAVE A COMMAND ID
         L     R1,CPPLECT-CPPL+DSACPPL --> ECT
         LA    R0,ECTSCMD-ECT(,R1) --> SUBCOMMAND NAME AREA (COPY SINK)
         LA    R1,L'ECTSCMD        GET L'SINK
         SR    R15,R15
         IC    R15,CMDLNGTH(,R3)   GET L'CMD NAME - 1
         LA    R15,1(,R15)         GET L'CMD NAME
         ICM   R15,8,=C' '         GET PAD CHARACTER
         LA    R14,CMDNAME(,R3)    --> COMMAND NAME
         MVCL  R0,R14              COPY TO ECT
         NI    DSAFLAGS,255-DSAFTITL CLEAR FLAGS
         ICM   R3,7,CMDHNDLR(R3)   --> COMMAND HANDLER
         BR    R3                  GO TO IT
         SPACE 1
INVALCMD #DIE  ,                   (LOGIC ERROR)
         TITLE 'SCHEDULE COMMAND -- SCHEDULE SUBCOMMAND'
*************************************************************
*        PRIME SOME VALUE-LIMITING DATA USED BY THE PARSE   *
*        VALIDITY CHECK ROUTINES.                           *
*************************************************************
         SPACE 1
SCHEDCMD NI    DSAFLAGS,255-DSAFWNDX CLEAR FLAGS
         TIME  STCK,DSAWORK        GET TODC (GMT)             02/84 DBC
         L     R0,DSAWORK          LOAD SIGNIFICANT PART      02/84 DBC
         AL    R0,LTOFFGMT         CNVRT TO LOCAL TIME        02/84 DBC
         ST    R0,DSAWORK          STORE BACK                 02/84 DBC
         LA    R1,NOWTIME          --> OUTPUT AREA
         BAL   R14,FINDTIME        CNVRT TO YYMMDDHHMM
         SPACE 3
*************************************************************
*        PARSE THE SCHEDULE COMMAND OPERANDS.               *
*************************************************************
         SPACE 1
         MVC   PPLCBUF-PPL+DSAPPL,PGPBIBUF-PGPB+DSAPGPB --> CBUF
         MVC   PPLPCL-PPL+DSAPPL,=A(PCLSCHED) --> SCHEDULE'S PCL
         MVI   DSAECB,0            CLEAR ATTENTION ECB
         CALLTSSR EP=IKJPARS,MF=(E,DSAPPL) CALL IKJPARS
         CL    R15,=F'28'          VALID RC?
         #DIE  H                   NO, LOGIC ERROR
         B     *+4(R15)            ACT UPON THE RC
         B     SCHEDOK             +0 PARSE COMPLETED OK
         B     PUTGET              +4 PROMPT FAILURE
         B     PUTGET              +8 ATTENTION INTERRUPT
         #DIE  C,15                +12 INVALID PARAMETERS
         B     EXIT12              +16 GETMAIN FAILURE
         B     PUTGET              +20 TERMINATED BY VALIDCK ROUTINE
         #DIE  C,15                +24 CONFLICTING PARAMETERS
         B     EXIT8               +28 TERMINAL DISCONNECTED
SCHEDOK  DS    0H
         SPACE 3
*************************************************************
*        BUILD AND WRITE THE NEW COMMAND RECORD. START BY   *
*        SETTING A UNIQUE ID NUMBER FOR IT.                 *
*************************************************************
         SPACE 1
         BAL   R14,NEXTID          GET THE NEXT RECORD ID
         LR    R2,R1               SAVE IT FOR A MOMENT
         SPACE 1
         LA    R0,SFR              --> RECORD BUFFER
         LA    R1,SFRLEN           GET ITS LENGTH
         SR    R15,R15             SETUP TO CLEAR
         MVCL  R0,R14              CLEAR THE RECORD BUFFER
         STH   R2,SFRID            STORE UNIQUE ID NUMBER
         SPACE 3
*************************************************************
*        SET THE SCHEDULED DATE AND TIME.                   *
*************************************************************
         SPACE 1
         MVC   SFRTIME,SCHDTIME    COPY YYMMDDHHMM
         MVC   SFRWK,SCHDWK
         MVC   SFRXTIME,NOWTIME    PREVENT IMMEDIATE WINDOW TRIGGERING
         SPACE 1
         LH    R14,SCHDHR          GET THE SCHEDULED HOUR
         MH    R14,=H'60'          CONVERT TO MINUTES
         AH    R14,SCHDMN          ADD IN SCHEDULED MINUTE
         SPACE 3
*************************************************************
*        SET THE COMMAND'S EXECUTION WINDOW.                *
*************************************************************
         SPACE 1
         LA    R15,24*60           ASSUME "UNTIL MIDNIGHT" WINDOW
*                                  DEFAULT REQUESTED
         SPACE 1
         LH    R1,WNDOHR           GET THE WINDOW HOURS
         MH    R1,=H'60'           CNVRT TO MINUTES
         AH    R1,WNDOMN           ADD IN WINDOW MINUTES; DEFAULT
*                                  REQUESTED?
         BNP   SWNOOFLW            YES, USE THE MIDNIGHT VALUE
         LA    R15,0(R1,R14)       NO, GENERATE THE END OF THE WINDOW
         CH    R15,=Y(24*60)       PAST MIDNIGHT?
         BNH   SWNOOFLW            NO, PROCEED
         OI    DSAFLAGS,DSAFWNDX   YES, REMEMBER "WINDOW TOO LARGE"
*                                  ERROR
         LA    R15,24*60           RESET EO-WINDOW TO MIDNIGHT
         SPACE 1
SWNOOFLW SR    R15,R14             GET WINDOW LENGTH
         SR    R14,R14             CLEAR FOR DIVIDE
         D     R14,=F'60'          CNVRT TO HOURS (R15) & MINUTES (R14)
         STH   R15,SFRWHR          STORE WINDOW LENGTH (HOURS)
         STH   R14,SFRWMN          STORE WINDOW LENGTH (MINUTES)
         SPACE 3
*************************************************************
*        SET THE COMMAND'S APPLICATION NAME.                *
*************************************************************
         SPACE 1
         L     R2,DSAPDLAD         --> PARSED DATA
         USING PDLSCHED,R2         DCL BASE
         LA    R14,SFRNAME         --> COPY SINK
         LA    R15,L'SFRNAME       GET L'SINK
         L     R0,PDEPTR-PDE+PDLNAME --> COPY SOURCE
         LH    R1,PDELNGTH-PDE+PDLNAME GET L'SOURCE
         ICM   R1,8,=C' '          PAD CHARACTER
         MVCL  R14,R0              COPY APPLICATION NAME
         #DIE  L                   (LOGIC ERROR)
         SPACE 3
*************************************************************
*        SET THE COMMAND'S SYSTEM AFFINITY.                 *
*************************************************************
         SPACE 1
         LA    R14,SFRSYSID        --> COPY SINK
         LA    R15,L'SFRSYSID      GET L'COPY SINK
         L     R0,PDEPTR-PDE+PDLSYSID --> COPY SOURCE
         LH    R1,PDELNGTH-PDE+PDLSYSID GET L'COPY SOURCE
         ICM   R1,8,=C' '          LOAD PAD CHARACTER
         MVCL  R14,R0              COPY SYSID TO SFR
         SPACE 1
         CLC   =CL(L'SFRSYSID)'ANY',SFRSYSID "ANY" SPECIFIED?
         BNE   SCSIDANY            NO, SKIP
         XC    SFRSYSID,SFRSYSID   YES, CLEAR TO SIGNAL "ANY"
SCSIDANY DS    0H
         SPACE 1
         CLC   =CL(L'SFRSYSID)'CRNT',SFRSYSID "CRNT" SPECIFIED?
         BNE   SCSIDCRN            NO, SKIP
         MVC   SFRSYSID,MYSYSID    YES, SET TO CURRENT SYSID
         #TEST SIZE=(L'SFRSYSID,EQ,L'MYSYSID)
SCSIDCRN DS    0H
         SPACE 3
*************************************************************
*        MAYBE SET THE "IPLFORCED" FLAG.                    *
*************************************************************
         SPACE 1
         CLI   KEYIPLFO+1,IPLFORCE IPLFORCED?
         BNE   SNIPLF              NO, SKIP
         CLI   SUBIPLFO+1,YES      MAYBE, IPLFORCED(YES)?
         BNE   SNIPLF              NO, SKIP
         OI    SFRFLAG,SFRFIPLF    YES, REMEMBER
SNIPLF   DS    0H
         SPACE 3
*************************************************************
*        MAYBE SET THE "OPERRIDE" FLAG.                     *
*************************************************************
         SPACE 1
         CLI   KEYOVERR+1,OVERRIDE OVERRIDE?
         BNE   SNOVRR              NO, SKIP
         CLI   SUBOVERR+1,YES      MAYBE, OVERRIDE(YES)?
         BNE   SNOVRR              NO, SKIP
         OI    SFRFLAG,SFRFOVRD    YES, REMEMBER
SNOVRR   DS    0H
         SPACE 3
*************************************************************
*        COPY THE COMMAND TEXT.                             *
*************************************************************
         SPACE 1
         LA    R14,SFRCMD          --> COPY SINK
         L     R0,PDEPTR-PDE+PDLCMD --> COPY SOURCE
         LH    R1,PDELNGTH-PDE+PDLCMD L'SOURCE
         LA    R15,4(,R1)          GET L'CMD BLOCK
         STH   R15,SFRCMDL         STORE
         LR    R15,R1              SET L'SINK = L'SOURCE
         MVCL  R14,R0              COPY THE COMMAND TEXT
         DROP  R2                  RELEASE PDL BASE
         SPACE 3
*************************************************************
*        WRITE THE NEW SCHEDULE RECORD.                     *
*************************************************************
         SPACE 1
         LA    R0,SFR              --> SO-RECORD
         SR    R14,R0              GET L'RECORD
         GSAMCALL WRITE,RECLEN=(R14),MF=(E,GSB) WRITE THE NEW COMMAND R
         LA    R14,=C'WRITE'      --> REQUEST DESCRIPTION
         LTR   R15,R15             AOK?
         BNZ   GSAMERR             NO, ERROR
         OI    DSAFLAGS,DSAFADD2   YES, REMEMBER FILE HAS BEEN ADDED TO
         SPACE 3
*************************************************************
*        DISPLAY THE SCHEDULED COMMAND JUST CREATED         *
*************************************************************
         SPACE 1
         BAL   R14,DISPLAY         GO TO DISPLAY ROUTINE
         NOP   0                   +0 ATTN RECEIVED; IGNORE
*        FALL THROUGH              +4 AOK; PROCEED
         SPACE 3
*************************************************************
*        DONE. RETURN OR RETURN WITH AN INFO MSG            *
*************************************************************
         SPACE 1
         TM    DSAFLAGS,DSAFWNDX   WAS WINDOW ADJUSTED?
         BZ    PUTGET              NO, DONE HERE
         L     R0,=A(MWNDOADJ-1)   YES, --> WARNING MESSAGE
         B     MSGGET              DISPLAY IT
         TITLE 'SCHEDULE COMMAND -- CHANGE SUBCOMMAND'
*************************************************************
*        PRIME SOME VALUE-LIMITING DATA USED BY THE PARSE   *
*        VALIDITY CHECK ROUTINES.                           *
*************************************************************
         SPACE 1
CHANGCMD NI    DSAFLAGS,255-DSAFWNDX-DSAFHIT CLEAR FLAGS
         TIME  STCK,DSAWORK        GET TODC (GMT)             02/84 DBC
         L     R0,DSAWORK          LOAD SIGNIFICANT PART      02/84 DBC
         AL    R0,LTOFFGMT         CNVRT TO LOCAL TIME        02/84 DBC
         ST    R0,DSAWORK          STORE BACK                 02/84 DBC
         LA    R1,NOWTIME          --> OUTPUT AREA
         BAL   R14,FINDTIME        CNVRT TO YYMMDDHHMM
         SPACE 3
*************************************************************
*        PARSE THE CHANGE COMMAND OPERANDS.                 *
*************************************************************
         SPACE 1
         MVC   PPLCBUF-PPL+DSAPPL,PGPBIBUF-PGPB+DSAPGPB --> CBUF
         MVC   PPLPCL-PPL+DSAPPL,=A(PCLCHANG) --> SCHEDULE'S PCL
         MVI   DSAECB,0            CLEAR ATTENTION ECB
         CALLTSSR EP=IKJPARS,MF=(E,DSAPPL) CALL IKJPARS
         CL    R15,=F'28'          VALID RC?
         #DIE  H                   NO, LOGIC ERROR
         B     *+4(R15)            ACT UPON THE RC
         B     CHANGEOK            +0 PARSE COMPLETED OK
         B     PUTGET              +4 PROMPT FAILURE
         B     PUTGET              +8 ATTENTION INTERRUPT
         #DIE  C,15                +12 INVALID PARAMETERS
         B     EXIT12              +16 GETMAIN FAILURE
         B     PUTGET              +20 TERMINATED BY VALIDCK ROUTINE
         #DIE  C,15                +24 CONFLICTING PARAMETERS
         B     EXIT8               +28 TERMINAL DISCONNECTED
         SPACE 1
CHANGEOK L     R9,DSAPDLAD         --> PDL
         USING PDLCHANG,R9         DCL THE PDL BASE
         SPACE 3
*************************************************************
*        SEARCH THE SCHEDULE FILE FOR THE SCHEDULED COMMAND *
*        TO BE CHANGED.                                     *
*************************************************************
         SPACE 1
         LA    R1,OVRDLIST         --> SO-LISTS AREA
         ST    R1,OVRDEOL          RESET THE AREA EMPTY
         SPACE 1
         XC    SFRKEY,SFRKEY       CLEAR THE SEARCH KEY
CHNGSRCH GSAMCALL READ,            READ NEXT RECORD                    *
               OPTIONS=(SEQ,NOUPDATE),                                 *
               MF=(E,GSB)
         LTR   R15,R15             GOT IT?
         BZ    CSRCNEOF            YES, PROCEED
         LA    R14,=C'SEQUENTIAL-READ' NO, --> REQUEST DESCRIPTION
         CH    R15,=Y(GSAMEKEY)    KEY ERROR?
         BNE   GSAMERR             NO, ERROR
         CLI   GSBVSAM+3,4         YES, EOD ERROR?
         BNE   GSAMERR             NO, ERROR
         SPACE 1
CSRCEOF  L     R0,=A(MDISPERR-1)   YES, --> MSG
         TM    DSAFLAGS,DSAFHIT    WERE THERE ANY HITS?
         BZ    MSGGET              NO, DISPLAY "MISS" MSG
         TM    DSAFLAGS,DSAFWNDX   YES, WAS A WINDOW ADJUSTED?
         BZ    PUTGET              NO, DONE HERE
         L     R0,=A(MWNDOAD2-1)   YES, --> WARNING MESSAGE
         B     MSGGET              DISPLAY IT
         SPACE 1
CSRCNEOF CLI   SFRKEY,X'FF'        MASTER RECORD?
         BE    CSRCEOF             YES, JUST LIKE EOF
         SPACE 3
*************************************************************
*        GOT THE NEXT RECORD. SEE IF IT HAS ALREADY BEEN    *
*        CHANGED. (THIS IS POSSIBLE BECAUSE THE "CHANGE"    *
*        COMMAND CAN CHANGE A RECORD'S KEY THUS CAUSING IT  *
*        TO BE MOVED AROUND).                               *
*************************************************************
         SPACE 1
         LH    R3,SFRID            GET THIS RECORD'S ID
         LA    R1,OVRDLIST-L'SFRID LOAD BXH SCANNER
         LA    R14,L'SFRID         LOAD BXH INCRIMENT
         L     R15,OVRDEOL         LOAD BXH -
         SR    R15,R14              LIMIT
         SPACE 1
CHRDUNCK BXH   R1,R14,CHRDUNZ      SCAN DO NEXT LIST ENTRY
         CH    R3,0(,R1)           CRNT RECORD ALREADY CHANGED?
         #TEST SIZE=(L'SFRID,EQ,2)
         BNE   CHRDUNCK            DON'T KNOW YET; KEEP SCANNING
         B     CHNGSRCH            YES, IGNORE THE CURRENT RECORD
CHRDUNZ  DS    0H
         SPACE 3
*************************************************************
*        THE CURRENT RECORD HAS NOT ALREADY BEEN CHANGED.   *
*        NOW SEE IF IT IS SELECTED FOR CHANGE.              *
*************************************************************
         SPACE 1
         LA    R2,PDLCHID          --> PDE FOR "ID"
         USING PDE,R2              DCL PDE BASE
CHIDLP   TM    PDEFLAGS,PDEFEXST   THIS LIST ENTRY EXIST?
         BZ    CHIDNEXT            NO, GO CHECK NEXT
         ICM   R1,7,PDEPTR+1       YES, --> 1ST VALUE; EXIST?
         BZ    CHIDNEXT            NO, GO CHECK NEXT
         C     R3,0(,R1)           YES, SFR ACCEPTED BY THIS LIST ITEM?
         BL    CHIDNEXT            NO, TRY NEXT ENTRY
         BE    CHIDOK              YES, PROCEED
         ICM   R1,7,PDEPTR2+1      MAYBE, --> EO-RANGE VALUE; EXIST?
         BZ    CHIDNEXT            NO, TRY NEXT LIST ENTRY
         C     R3,0(,R1)           YES, THIS SFR SELECTED?
         BNH   CHIDOK              YES, PROCEED
CHIDNEXT ICM   R2,7,PDECHAI2+1     NO, --> NEXT LIST ENTRY; ANY MORE?
         BNZ   CHIDLP              YES, LOOP TO TEST IT
         DROP  R2                  NO, RELEASE PDE BASE
         B     CHNGSRCH            REJECT THIS RECORD
         SPACE 1
CHIDOK   OI    DSAFLAGS,DSAFHIT    SIGNAL A "HIT"
         #XXC  MVC,SAVESFR,SFR,(257,L'SAVESFR) SAVE THE RECORD
         SPACE 3
*************************************************************
*        IF "COPY" HAS BEEN SPECIFIED, THEN GET A NEW       *
*        RECORD ID NUMBER FOR THE NEW RECORD.               *
*************************************************************
         SPACE 1
         CLI   CHCOPY+1,COPY       "COPY" SPECIFIED?
         BNE   CHREPLAC            NO, THE RECORD WILL BE
*                                  REPLACED
         BAL   R14,NEXTID          YES, GET THE NEXT AVAILABLE
*                                  RECORD ID (NOTE, SFR IS
*                                  DESTROYED)
         #XXC  MVC,SFR,SAVESFR,(257,SFRLEN) RESTORE THE RECORD TO BE
*                                  CHANGED
         STH   R1,SFRID            STORE A NEW SFR ID NUMBER
         B     CHANGEIT            PROCEED WITH THE CHANGE
         SPACE 3
*************************************************************
*        "COPY" HAS NOT BEEN SPECIFIED. DELETE THE ORIGINAL *
*        RECORD.                                            *
*************************************************************
         SPACE 1
CHREPLAC GSAMCALL READ,            GET EXCLUSIVE CONTROL OF THE RECORD *
               OPTIONS=(DIR,UPDATE),                                   *
               MF=(E,GSB)
         LA    R14,=C'UPDATE-READ' --> REQUEST DESCRIPTION
         LTR   R15,R15             AOK?
         BNZ   GSAMERR             NO, ERROR
         GSAMCALL DELETE,MF=(E,GSB) YES, DELETE THE RECORD
         LA    R14,=C'	DELETE'     --> REQUEST DESCRIPTION
         LTR   R15,R15             AOK?
         BNZ   GSAMERR             NO, ERROR
         SPACE 3
*************************************************************
*        IF THE COMMAND TEXT HAS BEEN CHANGED, THEN UPDATE  *
*        IT.                                                *
*************************************************************
         SPACE 1
CHANGEIT CLI   CHCOMMAN+1,0        'COMMAND' OMITTED?
         BE    CHCMDZ              YES, SKIP
         LA    R14,SFRCMD          NO, --> COPY SINK
         L     R0,PDEPTR-PDE+PDLCHCOM --> COPY SOURCE
         LH    R1,PDELNGTH-PDE+PDLCHCOM L'SOURCE
         LA    R15,4(,R1)          GET L'CMD BLOCK
         STH   R15,SFRCMDL         STORE
         LR    R15,R1              SET L'SINK = L'SOURCE
         MVCL  R14,R0              COPY THE COMMAND TEXT
CHCMDZ   DS    0H
         SPACE 3
*************************************************************
*        IF THE APPLICATION NAME HAS BEEN CHANGED, THEN     *
*        UPDATE IT.                                         *
*************************************************************
         SPACE 1
         CLI   CHNAME+1,0          'NAME' OMITTED?
         BE    CHNAMEZ             YES, SKIP
         LA    R14,SFRNAME         NO, --> COPY SINK
         LA    R15,L'SFRNAME       GET L'SINK
         L     R0,PDEPTR-PDE+PDLCHNAM --> COPY SOURCE
         LH    R1,PDELNGTH-PDE+PDLCHNAM GET L'SOURCE
         ICM   R1,8,=C' '          PAD CHARACTER
         MVCL  R14,R0              COPY APPLICATION NAME
         #DIE  L                   (LOGIC ERROR)
CHNAMEZ  DS    0H
         SPACE 3
*************************************************************
*        IF THE SYSTEM AFFINITY HAS BEEN CHANGED, THEN      *
*        UPDATE IT.                                         *
*************************************************************
         SPACE 1
         CLI   CHSYSID+1,0         "SYSID" SPECIFIED?
         BE    CHSIDZ              NO, SKIP
         LA    R14,SFRSYSID        YES, --> COPY SINK
         LA    R15,L'SFRSYSID      GET L'COPY SINK
         L     R0,PDEPTR-PDE+PDLCHSYS --> COPY SOURCE
         LH    R1,PDELNGTH-PDE+PDLCHSYS GET L'COPY SOURCE
         ICM   R1,8,=C' '          LOAD PAD CHANACTER
         MVCL  R14,R0              COPY THE SYSID TO THE SFR
         SPACE 1
         CLC   =CL(L'SFRSYSID)'ANY',SFRSYSID "ANY" SPECIFIED?
         BNE   CHSIDANY            NO, SKIP
         XC    SFRSYSID,SFRSYSID   YES, RESET
CHSIDANY DS    0H
         SPACE 1
         CLC   =CL(L'SFRSYSID)'CRNT',SFRSYSID "CRNT" SPECIFIED?
         BNE   CHSIDZ              NO, SKIP
         MVC   SFRSYSID,MYSYSID    YES, SET TO CURRENT SYSID
         #TEST SIZE=(L'SFRSYSID,EQ,L'MYSYSID)
CHSIDZ   DS    0H
         SPACE 3
*************************************************************
*        IF A NEW DATE HAS BEEN GIVEN, THEN UPDATE IT.      *
*************************************************************
         SPACE 1
         CLI   CHDATE+1,0          'DATE' OMITTED?
         BE    CHDATEZ             YES, SKIP
         MVC   SFRYR,SCHDYR        NO, COPY YEAR
         MVC   SFRMO,SCHDMO        COPY MONTH
         MVC   SFRDY,SCHDDY        COPY DAY
         MVC   SFRXTIME,NOWTIME    PREVENT IMMEDIATE WINDOW TRIGGERING
         NI    SFRFLAG,255-SFRFOBSO MIGHT NOT BE OBSOLETE NOW
CHDATEZ  DS    0H
         SPACE 3
*************************************************************
*        IF A NEW TIME HAS BEEN GIVEN, THEN UPDATE IT.      *
*************************************************************
         SPACE 1
         CLI   CHTIME+1,0          'TIME' OMITTED?
         BE    CHTIMEZ             YES, SKIP
         MVC   SFRHR,SCHDHR        NO, COPY HOUR
         MVC   SFRMN,SCHDMN        COPY MINUTE
         MVC   SFRXTIME,NOWTIME    PREVENT IMMEDIATE WINDOW TRIGGERING
         NI    SFRFLAG,255-SFRFOBSO MIGHT NOT BE OBSOLETE NOW
CHTIMEZ  DS    0H
         SPACE 3
*************************************************************
*        IF A NEW WEEDKAY HAS BEEN GIVEN, THEN UPDATE IT.   *
*************************************************************
         SPACE 1
         CLI   CHWEEKDA+1,0        'WEEKDAY' OMITTED?
         BE    CHWEEKDZ            YES, SKIP
         MVC   SFRWK,SCHDWK        NO, COPY WEEKDAY
         MVC   SFRXTIME,NOWTIME    PREVENT IMMEDIATE WINDOW TRIGGERING
         NI    SFRFLAG,255-SFRFOBSO MIGHT NOT BE OBSOLETE NOW
CHWEEKDZ DS    0H
         SPACE 3
*************************************************************
*        IF A NEW WINDOW HAS BEEN GIVEN, THEN UPDATE IT.    *
*************************************************************
         SPACE 1
         CLI   CHWINDOW+1,0        'WINDOW' OMITTED?
         BE    CHWINDOZ            YES, SKIP
         MVC   SFRWHR,WNDOHR       NO, STORE WINDOW HOURS
         MVC   SFRWMN,WNDOMN       STORE WINDOW MINUTES
CHWINDOZ DS    0H
         SPACE 3
*************************************************************
*        IF EITHER "WINDOW" OR "TIME" HAS BEEN GIVEN, THEN  *
*        VALIDITY CHECK THE WINDOW TO INSURE THAT IT DOES   *
*        NOT CROSS MIDNIGHT.                                *
*************************************************************
         SPACE 1
         CLI   CHWINDOW+1,0        "WINDOW" OMITTED?
         BNE   CHWNDCHK            NO, CHECK THE WINDOW
         CLI   CHTIME+1,0          YES, "TIME" ALSO OMITTED?
         BE    CHWNKOK             YES, NO CHECK IS NECESSARY
         SPACE 1
CHWNDCHK LH    R14,SFRHR           GET THE SCHEDULED HOUR
         MH    R14,=H'60'          CONVERT TO MINUTES
         AH    R14,SFRMN           ADD IN SCHEDULED MINUTE
         SPACE 1
         LA    R15,24*60           ASSUME "UNTIL MIDNIGHT" WINDOW
*                                  DEFAULT REQUESTED
         SPACE 1
         LH    R1,SFRWHR           GET THE WINDOW HOURS
         MH    R1,=H'60'           CNVRT TO MINUTES
         AH    R1,SFRWMN           ADD IN WINDOW MINUTES; DEFAULT
*                                  REQUESTED?
         BNP   CWNOOFLW            YES, USE THE MIDNIGHT VALUE
         LA    R15,0(R1,R14)       NO, GENERATE THE END OF THE WINDOW
         CH    R15,=Y(24*60)       PAST MIDNIGHT?
         BNH   CWNOOFLW            NO, PROCEED
         OI    DSAFLAGS,DSAFWNDX   YES, REMEMBER "WINDOW TOO LARGE"
*                                  ERROR
         LA    R15,24*60           RESET EO-WINDOW TO MIDNIGHT
         SPACE 1
CWNOOFLW SR    R15,R14             GET WINDOW LENGTH
         SR    R14,R14             CLEAR FOR DIVIDE
         D     R14,=F'60'          CNVRT TO HOURS (R15) & MINUTES (R14)
         STH   R15,SFRWHR          STORE WINDOW LENGTH (HOURS)
         STH   R14,SFRWMN          STORE WINDOW LENGTH (MINUTES)
CHWNKOK  DS    0H
         SPACE 3
*************************************************************
*        IF IPLFORCED HAS BEEN GIVEN, THEN UPDATE IT.       *
*************************************************************
         SPACE 1
         CLI   CHIPLFOR+1,0        'IPLFORCED'/'NOTIPLFORCED' OMITTED?
         BE    CHIPLFOZ            YES, SKIP
         OI    SFRFLAG,SFRFIPLF    NO, ASSUME 'IPLFORCED'
         CLI   CHIPLFOR+1,IPLFORCE RIGHT?
         BE    CHIPLFOZ            YES, DONE HERE
         NI    SFRFLAG,255-SFRFIPLF NO, 'NOTIPLFORCED'; RESET
CHIPLFOZ DS    0H
         SPACE 3
*************************************************************
*        IF OVERRIDE HAS BEEN GIVEN, THEN UPDATE IT.        *
*************************************************************
         SPACE 1
         CLI   CHOVERRI+1,0        'OVERRIDE'/'NOOVERRIDE' OMITTED?
         BE    CHOVERRZ            YES, SKIP
         NI    SFRFLAG,255-SFRFOVRD NO, ASSUME 'NOOVERRIDE'
         CLI   CHOVERRI+1,NOOVERRI RIGHT?
         BE    CHOVERRZ            YES, DONE HERE
         OI    SFRFLAG,SFRFOVRD    NO, 'OVERRIDE'; RESET
CHOVERRZ DS    0H
         SPACE 3
*************************************************************
*        NOW WRITE THE NEW CHANGED RECORD.                  *
*************************************************************
         SPACE 1
         LH    R14,SFRCMDL         GET L'COMMAND BLOCK
         LA    R14,SFRCMDL-SFR(,R14) GET L'SFR
         OI    DSAFLAGS,DSAFADD2   REMEMBER TO RERUN SCHEDRUN
         GSAMCALL WRITE,RECLEN=(R14),MF=(E,GSB) WRITE THE NEW COMMAND R
         LA    R14,=C'WRITE'      --> REQUEST DESCRIPTION
         LTR   R15,R15             AOK?
         BNZ   GSAMERR             NO, ERROR
         SPACE 3
*************************************************************
*        DISPLAY THE SCHEDULED COMMAND JUST CREATED         *
*************************************************************
         SPACE 1
         BAL   R14,DISPLAY         GO TO DISPLAY ROUTINE
         NOP   0                   +0 ATTN RECEIVED; IGNORE
*        FALL THROUGH              +4 AOK; PROCEED
         SPACE 3
*************************************************************
*        IF THE "CHANGE" COMMAND GAVE THIS RECORD A HIGHER  *
*        KEY (AND, THUS, WAS MOVED FORWARD IN THE FILE),    *
*        THEN THIS RECORD'S ID NEEDS TO BE SAVED IN A LIST  *
*        TO PREVENT IT FROM BEING CHANGED AGAIN.            *
*************************************************************
         SPACE 1
         CLC   SFRKEY,SFRKEY-SFR+SAVESFR WAS THE KEY CHANGED?
         BE    CHNGSRCH            NO, LOOP FOR NECT RECORD
         MVC   SFRKEY,SFRKEY-SFR+SAVESFR (YES, RESTORE THE ORIGINAL KEY
*                                  SO THAT GSAM WILL PROCEED
*                                  SEQUENTIALLY).
         BL    CHNGSRCH            YES, THE RECORD WAS MOVED BACK. NO
*                                  SPECIAL ACTION IS NECESSARY. LOOP
*                                  FOR THE NEXT RECORD
         SPACE 1
         L     R1,OVRDEOL          YES, BUT THE RECORD WAS MOVED
*                                  FORWARD. --> LIST SAVE AREA
         LA    R14,L'SFRID(,R1)    GET NEW EOL
         LA    R0,OVRDLIST         --> MAX -
         AH    R0,=Y(OVRDLEND-OVRDLIST) POSSIBLE EOL
         CR    R0,R14              WILL THE LIST OVERFLOW?
         BL    CHNGSRCH            YES, FORGET IT (NO CATASTROPHIE
*                                  ANYWAY)
         ST    R14,OVRDEOL         NO, STORE NEW EOL PTR
         MVC   0(L'SFRID,R1),SFRID SAVE THIS RECORD'S ID
         B     CHNGSRCH            LOOP FOR NEXT RECORD
         DROP  R9                  RELEASE THE CHANGE PDL BASE
         TITLE 'SCHEDULE COMMAND -- DISPLAY AND DELETE SUBCOMMANDS'
*************************************************************
*        PARSE THE COMMAND OPERANDS.                        *
*************************************************************
         SPACE 1
DISPDELE NI    DSAFLAGS,255-DSAFOVRD CLEAR FLAGS
         MVC   PPLCBUF-PPL+DSAPPL,PGPBIBUF-PGPB+DSAPGPB --> CBUF
         MVC   PPLPCL-PPL+DSAPPL,=A(PCLDISPL) --> DISPLAY'S PCL
         MVI   DSAECB,0            CLEAR THE ATTENTION ECB
         CALLTSSR EP=IKJPARS,MF=(E,DSAPPL) CALL IKJPARS
         CL    R15,=F'28'          VALID RC?
         #DIE  H                   NO, LOGIC ERROR
         B     *+4(R15)            ACT UPON THE RC
         B     DISPLOK             +0 PARSE COMPLETED OK
         B     PUTGET              +4 PROMPT FAILURE
         B     PUTGET              +8 ATTENTION INTERRUPT
         #DIE  C,15                +12 INVALID PARAMETERS
         B     EXIT12              +16 GETMAIN FAILURE
         B     PUTGET              +20 TERMINATED BY VALIDCK ROUTINE
         #DIE  C,15                +24 CONFLICTING PARAMETERS
         B     EXIT8               +28 TERMINAL DISCONNECTED
         SPACE 1
DISPLOK  L     R9,DSAPDLAD         --> PDL
         USING PDLDISPL,R9         DCL THE BASE FOR IT
         SPACE 3
*************************************************************
*        IF THIS IS THE DELETE COMMAND, THEN INSURE THAT AT *
*        LEAST SOME SELECTION CRITERIA OPERANDS HAVE BEEN   *
*        GIVEN.                                             *
*************************************************************
         SPACE 1
         CLI   CMDID,CMDIDDEL      DELETE COMMAND?
         BNE   DDFLTDIS            NO, DISPLAY; GO POSSIBLY SET
*                                  DEFAULTS
         CLI   DISANAME+1,0        YES, "NAME" OMITTED?
         BNE   DDFLTDIS            NO, GIVEN; GO POSSIBLY SET OTHER
*                                  DEFAULTS
         CLI   DDSYSID+1,0         YES, "SYSID" OMITTED?
         BNE   DDFLTDIS            NO, GIVEN; GO POSSIBLY SET OTHER
*                                  DEFAULTS
         CLI   DISWHEN+1,0         YES, OTHER SELECTION ALSO OMITTED?
         BNE   DDFLTOK             NO, GIVEN; AOK
         L     R0,=A(MDELOPRN-1)   YES, --> MSG
         B     MSGGET              SEND MSG AND FORGET THE COMMAND
         SPACE 1
DDFLTDIS CLI   DISWHEN+1,0         MISC SELECTION OMITTED?
         BNE   DDFLTOK             NO, GIVEN; AOK
         MVI   DISWHEN+1,ANYTIME   YES, DEFAULT TO "ANYTIME"
DDFLTOK  DS    0H
         SPACE 3
*************************************************************
*        IF 'YESTERDAY', 'TODAY', OR 'TOMORROW' IS GIVEN,   *
*        THEN DEVELOP A SUITABLE YYMMDD VALUE.              *
*************************************************************
         SPACE 1
         CLI   DISWHEN+1,TOMORROW  'YESTERDAY', 'TODAY', OR
*                                  'TOMORROW'?
         BH    DIGOTDTE            NO, SKIP
         TIME  STCK,DSAWORK        YES, GET TODC (GMT)        02/84 DBC
         LM    R14,R15,DSAWORK     LOAD IT
         AL    R0,LTOFFGMT         CNVRT TO LOCAL TIME        02/84 DBC
         SRDL  R14,12              CNVRT TO MICRO-SECONDS
         D     R14,=F'60000000'    CONVERT TO MINUTES
         LH    R1,DISWHEN          'YESTERDAY' ==> -1
         BCTR  R1,0                 'TODAY' ==> 0
         BCTR  R1,0                  'TOMORROW' ==> +1
         MH    R1,=Y(24*60)        CNVRT TO +-MINUTES
         AR    R15,R1              ADJUST TOD VALUE
         LA    R1,SCHDTIME         --> CONVERSION SINK
         BAL   R14,MINSTIME        CNVRT TO YYMMDD
DIGOTDTE DS    0H
         SPACE 3
*************************************************************
*        RESET THE OVERRIDE LIST AND SEE IF AN OVERRIDE     *
*        PASS IS NECESSARY. ONE IS NECESSARY IF AN EXPLICIT *
*        DATE IS TO BE DISPLAYED, AND "EITHER" HAS BEEN     *
*        GIVEN.                                             *
*************************************************************
         SPACE 1
         LA    R1,OVRDLIST         --> LIST
         ST    R1,OVRDEOL          EMPTY IT
         CLI   DISWHEN+1,DATE      EXPLICIT DATE BEING DISPLAYED?
         BH    DIDOVNO             NO, SKIP
         CLI   DDOVERRI+1,EITHER   YES, "EITHER" GIVEN?
         BNE   DIDOVNO             NO, SKIP
         OI    DSAFLAGS,DSAFOVRD   YES, SIGNAL "OVERRIDE SCAN NEEDED"
DIDOVNO  DS    0H
         EJECT ,
*************************************************************
*        SEARCH THE SCHEDULE FILE FOR COMMANDS THAT MATCH   *
*        THE GIVEN CRITERIA. DISPLAY THOSE COMMANDS.        *
*************************************************************
         SPACE 1
DIDPASS2 XC    SFRKEY,SFRKEY       CLEAR THE SEARCH KEY
         SR    R8,R8               CLEAR THE HIT SIGNAL/COUNTER
         SR    R5,R5               CLEAR THE (SURVIVING) RECORDS CNTR
         SPACE 1
DSRCHLP  GSAMCALL READ,            READ NEXT RECORD                    *
               OPTIONS=(SEQ,NOUPDATE),                                 *
               MF=(E,GSB)
         LTR   R15,R15             GOT IT?
         BZ    DSRCNEOF            YES, PROCEED
         LA    R14,=C'SEQUENTIAL-READ' NO, --> REQUEST DESCRIPTION
         CH    R15,=Y(GSAMEKEY)    KEY ERROR?
         BNE   GSAMERR             NO, ERROR
         CLI   GSBVSAM+3,4         YES, EOD ERROR?
         BNE   GSAMERR             NO, ERROR
         SPACE 3
*************************************************************
*        EOF PROCESSING. IF THIS IS THE END OF AN OVERRIDE  *
*        SCAN, THEN RESET AND REPASS THE FILE; OTHERWISE,   *
*        EXIT APROPRIATELY.                                 *
*************************************************************
         SPACE 1
DSRCEOF  LTR   R8,R8               ANY HITS?
         BNZ   DDSOME              YES, GO EXAMINE FURTHER
         L     R0,=A(MDISPERR-1)   NO, ASSUME NOT DUE TO EMPTY FILE
         LTR   R5,R5               IS THE FILE EMPTY?
         BNZ   MSGGET              NO, SIMPLY ALL MISSES
         L     R0,=A(MFILEMPT-1)   YES, SAY SO
         B     MSGGET              SAY SO
         SPACE 1
DDSOME   TM    DSAFLAGS,DSAFOVRD   THERE WERE HITS; OVERRIDE SCAN?
         BZ    DDSOME2             NO, SKIP
         NI    DSAFLAGS,255-DSAFOVRD YES, RESET FLAG
         B     DIDPASS2            RE-PASS THE FILE TO ACTUALLY
*                                  PERFORM THE DISPLAY OR DELETE
         SPACE 1
DDSOME2  CLI   CMDID,CMDIDDEL      "DELETE" COMMAND?
         BE    DDSMYMSG            YES, GO SETUP A SUMMARY MSG
         CLI   DDPROMPT+1,SHORT    NO, "DISPLAY"; "SHORT" GIVEN?
         BNE   PUTGET              NO, DONE HERE
         SPACE 1
DDSMYMSG CVD   R8,DSAWORK          YES, CNVRT DELETION/DISPLAY COUNT TO
*                                  DECIMAL
         MVC   DYNAMSGS(6),=X'402020202120' EDIT MASK
         ED    DYNAMSGS(6),DSAWORK+5 CNVRT TO EBCDIC
         MVC   DYNAMSGS+6(19),=C' COMMANDS DELETED 
         CLI   CMDID,CMDIDDEL      "DELETE" COMMAND?
         BE    DDSMYDEL            YES, PROCEED
         MVC   DYNAMSGS+16(8),=C'SELECTED' NO, "DISPLAY"; ADJUST TEXT
DDSMYDEL LA    R1,24               L'MSG
         STC   R1,DYNAMSGS-1       STORE FOR PUTLINE
         #PUT  DYNAMSGS            DISPLAY THE MESSAGE
         NOP   0                   +0 ATTENTION; IGNORE
*        FALL THROUGH              +4 AOK; PROCEED
         SPACE 1
         CLI   CMDID,CMDIDDEL      "DELETE" COMMAND?
         BNE   PUTGET              NO, "DISPLAY"; DONE HERE
         CR    R5,R8               YES, WERE ALL RECORDS DELETED?
         BNE   PUTGET              NO, DONE HERE
         L     R0,=A(MFILEMPT-1)   YES, --> INFO MSG
         B     MSGGET              DISPLAY THE MESSAGE AND EXIT
         SPACE 1
DSRCNEOF CLI   SFRKEY,X'FF'        MASTER RECORD?
         BE    DSRCEOF             YES, JUST LIKE EOF
         BCTR  R5,0                NO, INCRIMENT ACTUAL RECORD COUNT
         SPACE 3
*************************************************************
*        CHECK APPLICATION NAME SELECTION.                  *
*************************************************************
         SPACE 1
         ICM   R1,3,DISANAME       'NAME' KEYWORD GIVEN?
         BZ    DINMOK              NO, ACCEPT
         LA    R2,DISNAME          YES, --> ITS PDE
         USING PDE,R2              DCL PDE BASE
DINMLP   TM    PDEFLAGS,PDEFEXST   THIS VALUE EXIST?
         BZ    DINMNEXT            NO, TRY NEXT
         ICM   R14,7,PDEPTR+1      MAYBE; --> IT; EXIST?
         BZ    DINMNEXT            NO, TRY NEXT
         LH    R15,PDELNGTH        YES, GET L'VALUE
         LTR   R15,R15             NULL VALUE?
         BNP   DINMNEXT            YES, TRY NEXT
         LA    R0,DSAWORK          NO, --> COPY SINK
         LA    R1,L'SFRNAME        GET L'COPY SINK
         ICM   R15,8,=C' '         SET PAD VALUE
         MVCL  R0,R14              COPY VALUE TO SINK; TOO LONG?
         #TEST SIZE=(L'SFRNAME,LE,L'DSAWORK)
         BL    DINMNEXT            YES, TRY NEXT VALUE
         CLC   SFRNAME,DSAWORK     NO, SELECTED NAME?
         BE    DINMOK              YES, PROCEED
DINMNEXT ICM   R2,7,PDECHAIN+1     NOT YET, --> NEWXT PDE IN LIST; XST?
         BNZ   DINMLP              YES, GO CHECK IT OUT
         DROP  R2                  NO, RELEASE PDE BASE
         B     DSRCHLP             SKIP THIS SFR
DINMOK   DS    0H
         SPACE 3
*************************************************************
*        CHECK FOR SELECTION VIA SYSTEM AFFINITY.           *
*************************************************************
         SPACE 1
         CLI   DDSYSID+1,0         "SYSID" SPECIFIED?
         BE    DDSIDOK             NO, PERMIT
         LA    R2,PDLDDSYS         YES, --> PDE
         USING PDE,R2              DCL PDE BASE
         SPACE 1
DDSIDLP  TM    PDEFLAGS,PDEFEXST   THIS ENTRY EXIST?
         BZ    DDSIDNXT            NO, SKIP TO NEXT PDE IN THE LIST
         ICM   R14,7,PDEPTR+1      MAYBE, --> TEXT; EXIST?
         BZ    DDSIDNXT            NO, SKIP TO NEXT PDE IN THE LIST
         LH    R15,PDELNGTH        GET L'SYSID
         LTR   R15,R15             NULL?
         BNP   DDSIDNXT            YES, SKIP TO NEXT PDE IN THE LIST
         LA    R0,DSAWORK          NO, --> COPY SINK
         LA    R1,L'SFRSYSID       GET L'COP SINK
         #TEST SIZE=(L'DSAWORK,GE,L'SFRSYSID)
         ICM   R15,8,=C' '         LOAD PAD CHARACTER
         MVCL  R0,R14              COPY AND PAD THE SYSID
         BL    DDSIDNXT            FORGET IT IF TOO LONG
         SPACE 1
         CLC   =CL(L'SFRSYSID)'ALL',DSAWORK "ALL" SPECIFIED?
         BE    DDSIDOK             YES, ACCEPT THIS SFR
         SPACE 1
         CLC   =CL(L'SFRSYSID)'ANY',DSAWORK NO, "ANY" SPECIFIED?
         BNE   DDSIDANY            NO, SKIP
         XC    DSAWORK(L'SFRSYSID),DSAWORK YES, CLEAR TO SIGNAL "ANY"
DDSIDANY DS    0H
         SPACE 1
         CLC   =CL(L'SFRSYSID)'CRNT',DSAWORK "CRNT" SPECIFIED?
         BNE   DDSIDCRN            NO, SKPI
         MVC   DSAWORK(L'SFRSYSID),MYSYSID YES, SET CURRENT SYSID
         #TEST SIZE=(L'SFRSYSID,EQ,L'MYSYSID)
DDSIDCRN DS    0H
         SPACE 1
         CLC   SFRSYSID,DSAWORK    IS THE CURRENT RECORD SELECTED?
         BE    DDSIDOK             YES, PROCEED
DDSIDNXT ICM   R2,7,PDECHAIN+1     NO, --> NEXT PDE IN THE LIST; EXIST?
         BNZ   DDSIDLP             YES, LOOP TO TEST IT
         DROP  R2                  NO, RELEASE PDE BASE
         B     DSRCHLP             REJECT THIS RECORD
DDSIDOK  DS    0H
         SPACE 3
*************************************************************
*        CHECK FOR "OVERRIDE" VS. "NOOVERRIDE" VS.          *
*        "BOTH"-OR-"EITHER" SELECTION.                      *
*************************************************************
         SPACE 1
         CLI   DDOVERRI+1,OVERRIDE "OVERRIDE" GIVEN?
         BE    DDOVRCHK            YES, GO SEE IF CRNT RCD IS OVRIDE
         CLI   DDOVERRI+1,NOOVERRI NO, "NOOVERRIDE"?
         BNE   DDNOVRCK            NO, "BOTH" OR "EITHER"; SKIP OUT
         TM    SFRFLAG,SFRFOVRD    YES, AN OVERRIDE RECORD?
         BNZ   DSRCHLP             YES, SKIP IT
         B     DDNOVRCK            NO, ACCEPT IT
         SPACE 1
DDOVRCHK TM    SFRFLAG,SFRFOVRD    "OVERRIDE" REQUESTED; AN OVERRIDE
*                                  RECORD?
         BZ    DSRCHLP             NO, SKIP IT
DDNOVRCK DS    0H                  YES, ACCEPT IT (SO FAR)
         SPACE 3
*************************************************************
*        CHECK FOR OBSOLETE COMMANDS SELECTION              *
*************************************************************
         SPACE 1
         CLI   DISWHEN+1,OBSOLETE  OBSOLETE CMDS WANTED?
         BNE   DDNOBSOL            NO, PROCEED
         TM    SFRFLAG,SFRFOBSO    YES, IS THIS AN OBSOLETE CMD?
         BZ    DSRCHLP             NO, SKIP IT
DDNOBSOL DS    0H
         SPACE 3
*************************************************************
*        CHECK FOR "ID" VS. DATE SELECTION.                 *
*************************************************************
         SPACE 1
         CLI   DISWHEN+1,ID        WHICH KEYWORD(S) GIVEN?
         BH    DIGO                'ANYTIME' OR 'OBSOLETE', PROCEED
         BL    DIDATE              'DATE', 'YESTERDAY', 'TODAY', OR
*                                  'TOMORROW'; GO PROCESS
         LH    R3,SFRID            'ID', GET THIS SFR'S ID
         LA    R2,DISID            --> 'ID' PDE
         USING PDE,R2              DCL PDE BASE
DIIDLP   TM    PDEFLAGS,PDEFEXST   THIS LIST ENTRY EXIST?
         BZ    DIIDNEXT            NO, GO CHECK NEXT
         ICM   R1,7,PDEPTR+1       YES, --> 1ST VALUE; EXIST?
         BZ    DIIDNEXT            NO, GO CHECK NEXT
         C     R3,0(,R1)           YES, SFR ACCEPTED BY THIS LIST ITEM?
         BL    DIIDNEXT            NO, TRY NEXT ENTRY
         BE    DIGO                YES, PROCEED
         ICM   R1,7,PDEPTR2+1      MAYBE, --> EO-RANGE VALUE; EXIST?
         BZ    DIIDNEXT            NO, TRY NEXT LIST ENTRY
         C     R3,0(,R1)           YES, THIS SFR SELECTED?
         BNH   DIGO                YES, PROCEED
DIIDNEXT ICM   R2,7,PDECHAI2+1     NO, --> NEXT LIST ENTRY; ANY MORE?
         BNZ   DIIDLP              YES, LOOP TO TEST IT
         DROP  R2                  NO, RELEASE PDE BASE
         B     DSRCHLP             REJECT THIS SFR
         SPACE 3
*************************************************************
*        CHECK FOR 'DATE', 'YESTERDAY', 'TODAY', OR         *
*        'TOMORROW' SELECTION.                              *
*************************************************************
         SPACE 1
DIDATE   LA    R1,SCHDTIME         --> SELECTION DATE
         BAL   R14,DATESCRN        IS THIS SFR SELECTED?
         B     DSRCHLP             +0 NO, LOOP FOR NEXT SFR
*        FALL THROUGH              +4 YES, PROCEED
         SPACE 3
*************************************************************
*        THE CURRENT SFR HAS BEEN ACCEPTED. DISTINGUISH     *
*        BETWEEN THE OVERRIDE PASS AND THE DISPLAY PASS.    *
*************************************************************
         SPACE 1
         TM    DSAFLAGS,DSAFOVRD   OVERRIDE PASS?
         BZ    DIDOVCHK            NO, DISPLAY PASS
         SPACE 3
*************************************************************
*        OVERRIDE PASS. SEE IF THIS SFR ADDS TO THE         *
*        OVERRIDE LIST.                                     *
*************************************************************
         SPACE 1
         BCTR  R8,0                SIGNAL THAT A SECOND PASS IS
*                                  NECESSARY
         TM    SFRFLAG,SFRFOVRD    IS THIS AN OVERRIDE CMD?
         BZ    DSRCHLP             NO, READ NEXT SFR
         LA    R1,OVRDLIST-L'SFRNAME YES, --> SO-LIST
         LA    R14,L'SFRNAME       GET L'LIST ENTRY
         L     R15,OVRDEOL         --> EO-LIST
         SR    R15,R14             GET BXH LIMIT
DIDOVLP1 BXH   R1,R14,DIDOVNEW     SCAN THE LIST
         CLC   0(L'SFRNAME,R1),SFRNAME NAME ALREADY IN THE LIST?
         BNE   DIDOVLP1            NOT YET, CONTINUE SCAN
         B     DSRCHLP             YES, READ NEXT SFR
         SPACE 1
DIDOVNEW AR    R14,R1              --> NEW EOL
         LA    R0,OVRDLIST         --> MAX -
         AH    R0,=Y(OVRDLEND-OVRDLIST) POSSIBLE EOL
         CR    R14,R0              WILL THE LIST OVERFLOW?
         BH    DSRCEOF             YES, HANDLE LIKE EOF
         ST    R14,OVRDEOL         NO, STORE NEW EOL PTR
         MVC   0(L'SFRNAME,R1),SFRNAME SAVE THIS APPL NAME
         B     DSRCHLP             LOOP FOR NEXT RECORD
         SPACE 3
*************************************************************
*        DISPLAY PASS. SEE IF THIS SFR IS OVERRIDDEN.       *
*************************************************************
         SPACE 1
DIDOVCHK TM    SFRFLAG,SFRFOVRD    IS THIS AN OVERRIDE?
         BNZ   DIGO                YES, DISPLAY IT
         LA    R1,OVRDLIST-L'SFRNAME NO, --> EO-LIST
         LA    R14,L'SFRNAME       GET L'LIST ENTRY
         L     R15,OVRDEOL         --> EO-LIST
         SR    R15,R14             GET BXH LIMIT
DIDOVLP2 BXH   R1,R14,DIGO         SCAN THE LIST
         CLC   0(L'SFRNAME,R1),SFRNAME IS THIS SFR OVERRIDDEN?
         BNE   DIDOVLP2            NOT YET, CONTINUE SCAN
         B     DSRCHLP             YES, READ NEXT SFR
         SPACE 3
*************************************************************
*        SEE IF THE SFR IS TO BE DISPLAYED.                 *
*************************************************************
         SPACE 1
DIGO     CLI   DDPROMPT+1,SHORT    "SHORT" GIVEN?
         BE    DDDISKIP            YES, SKIP THE DISPLAY
DRESHOW  BAL   R14,DISPLAY         DISPLAY THE SCHEDULED COMMAND
         B     DSRCEOF             +0 ATTENTION. PROCESS LIKE EOF
*        FALL THROUGH              +4 AOK. PROCEED
         SPACE 3
*************************************************************
*        IF THIS IS A "DELETE" COMMAND, AND IF "PROMPT" HAS *
*        BEEN GIVEN, THEN PROMPT THE USER TO SEE IF HE      *
*        REALLY WANTS TO DELETE THIS RECORD.                *
*************************************************************
         SPACE 1
         CLI   CMDID,CMDIDDEL      "DELETE" COMMAND?
         BNE   DDNOPROM            NO, "DISPLAY"; SKIP PROMTING
         CLI   DDPROMPT+1,PROMPT   YES, "PROMPT" REQUESTED?
         BNE   DDNOPROM            NO, SKIP; GO DIRECTLY TO DELETE
         SPACE 1
         MVI   DSAECB,0            CLEAR THE PUTGET ECB
         XC    PGPBIBUF-PGPB+DSAPGPB,PGPBIBUF-PGPB+DSAPGPB ZAP IBUF PTR
         L     R15,DSAPTGT         --> IKJPTGT
         PUTGET PARM=DSAPGPB,      YES, PROMPT THE USER FOR A DECISION *
               OUTPUT=(DELASKO1,MULTLVL,PROMPT),                       *
               ENTRY=(15),MF=(E,DSAIOPL)
         B     *+4(R15)            PROCESS ACCORDING TO THE RC
         B     DASKCHK             +0 RESPONSE RECEIVED FROM TERMINAL
         #DIE  C,15                +4 RESPONSE RECEIVED FROM ^TERMINAL
         B     DSRCEOF             +8 ATTENTION RECEIVED
         B     DSRCEOF             +12 "PROFILE NOPAUSE" IN EFFECT
         #DIE  C,15                +16 NOWAIT ON TPUT
         #DIE  C,15                +20 NOWAIT ON TGET
         #DIE  C,15                +24 INVALID PARAMETERS
         B     EXIT12              +28 GETMAIN FAILURE
         B     EXIT8               +32 TERMINAL DISCONNECT
         SPACE 3
*************************************************************
*        THE USER HAS RESPONDED TO THE PROMPT. EXAMINE IT.  *
*        IF "Y", THEN DELETE THE CURRENT RECORD. IF "N",    *
*        THEN KEEP THE CURRENT RECORD. IF ANYTHING ELSE,    *
*        THEN REDISPLAY THE CURRENT RECORD AND REPROMPT.    *
*************************************************************
         SPACE 1
DASKCHK  MVI   DSAWORK,C' '        PRIME THE EXTRACTION SINK
         L     R15,PGPBIBUF-PGPB+DSAPGPB --> IBUF
         LH    R1,0(,R15)          GET ITS LENGTH
         AR    R1,R15              --> PAST IT
         LA    R0,1                GET BXH INCRIMENT
         SR    R1,R0               --> IBUF'S LAST BYTE
         LA    R15,3(,R15)         --> SO-TEXT-1
         SPACE 1
         BALR  R14,0               LOAD LOOP HEAD
         BXH   R15,R0,DPROMFRE     SCAN IBUF
         TM    0(R15),255-C' '     STILL A LEADING BLANK?
         BZR   R14                 YES, CONTINUE THE SCAN
         OC    DSAWORK(1),0(R15)   NO, COPY 1ST CHAR W/ UPCASING
         SPACE 1
DPROMFRE L     R1,PGPBIBUF-PGPB+DSAPGPB --> PROMPT'S IBUF
         LH    R0,0(,R1)           GET ITS LENGTH
         ICM   R0,8,=AL1(1)        GET ITS SUBPOOL
         FREEMAIN R,LV=(0),A=(1)   FREE IT
         SPACE 1
         CLI   DSAWORK,C'N'        KEEP?
         BE    DSRCHLP             YES, LOOP FOR NEXT RECORD
         CLI   DSAWORK,C'Y'        NO, DELETE?
         BNE   DRESHOW             NO, GO REDISPLAY AND REPROMPT
DDNOPROM DS    0H                  YES, FALL THROUGH TO DELETION
         SPACE 3
*************************************************************
*        IF THIS IS "DELETE" COMMAND PROCESSING, THEN       *
*        DELETE THE RECORD JUST DISPLAYED.                  *
*************************************************************
         SPACE 1
DDDISKIP BCTR  R8,0                COUNT THE SELECTED RECORD
         CLI   CMDID,CMDIDDEL      "DELETE" COMMAND?
         BNE   DSRCHLP             NO, LOOP TO DISPLAY NEXT RECORD
         SPACE 1
         OI    DSAFLAGS,DSAFADD2   YES, MUST RERUN SCHEDRUN
         GSAMCALL READ,            GET EXCLUSIVE CONTROL OF THE RECORD *
               OPTIONS=(DIR,UPDATE),                                   *
               MF=(E,GSB)
         LA    R14,=C'UPDATE-READ' --> REQUEST DESCRIPTION
         LTR   R15,R15             AOK?
         BNZ   GSAMERR             NO, ERROR
         GSAMCALL DELETE,MF=(E,GSB) YES, DELETE THE RECORD
         LA    R14,=C'	DELETE'     --> REQUEST DESCRIPTION
         LTR   R15,R15             AOK?
         BNZ   GSAMERR             NO, ERROR
         B     DSRCHLP             YES, LOOP TO SELECT NEXT RECORD
         DROP  R9                  RELEASE THE PDL BASE
         TITLE 'SCHEDULE COMMAND -- HELP SUBCOMMAND'
*************************************************************
*        ATTACH THE HELP COMMAND PROCESSOR.                 *
*************************************************************
         SPACE 1
HELPCMD  MVI   DSAECB,0            CLEAR ECB FOR ATTACH
         MVC   CPPLCBUF-CPPL+DSACPPL,PGPBIBUF-PGPB+DSAPGPB --> CBUF
         LA    R1,DSACPPL          --> CPPL
         ATTACH EP=HELP,ECB=DSAECB,SHSPV=78,SZERO=NO,ESTAI=ESTAHELP,   *
               LPMOD=1,SF=(E,HELPATTA)
         ST    R1,SUBTTCBA         SAVE A(SUBTASK TCB)
         WAIT  ECB=DSAECB          WAIT FOR COMPLETION
         DETACH SUBTTCBA,STAE=YES  DETACH THE HELP COMMAND
         XC    SUBTTCBA,SUBTTCBA   CLEAR TCB PTR
         SPACE 3
*************************************************************
*        MARK NOT-IN-USE ANY DATA SETS THAT HELP MIGHT HAVE *
*        USED.                                              *
*************************************************************
         SPACE 1
         MVC   S99RBLN-S99RB+DALRB(L'S99RBLN+L'S99VERB+L'S99FLAG1),=AL1*
               (DALRBLEN,S99VRBRI,S99NOMNT,0) BUILD DAL RB
         LA    R14,MNUCURNT-MNUDATA+DALINFO+4*MNUCOUNT --> "MARK NOT-IN
*                                  -USE" KEY
         ST    R14,DALINFO         BUILD TEXT KEY VECTOR
         MVI   DALINFO+4*MNUCOUNT-4,S99TUPLN SET EO-VECTOR SIGNAL
         MVC   DALINFO+4*MNUCOUNT(MNUDATAL),MNUDATA GET TEXT KEYS
         #TEST SIZE=(L'DALINFO,GE,4*MNUCOUNT+MNUDATAL)
         SPACE 1
         LA    R1,DALPLIST         --> PLIST
         DYNALLOC ,                GO TO DAL
         B     PUTGET              DONE HERE
         TITLE 'SCHEDULE COMMAND -- TERMINATION'
*************************************************************
*        RETURNS TO CALLER                                  *
*************************************************************
         SPACE 1
EXIT16   BAL   R0,EXIT             RC=16 AUTHORIZATION OR GSAM FAILURE
EXIT12   BAL   R0,EXIT             RC=12 GETMAIN FAILURE
EXIT8    BAL   R0,EXIT             RC=8 TERMINAL DISCONNECT
EXIT4    BAL   R0,EXIT             RC=4 ATTENTION INTERRUPT
EXIT0    BAL   R0,EXIT             RC=0 NORMAL COMPLETION
EXIT     LA    R2,EXIT             LOAD RC BASE
         SR    R2,R0               GET RC VALUE
         LA    R2,0(,R2)           PURIFY
         SPACE 3
*************************************************************
*        RELEASE GSAM CONTROL BLOCKS.                       *
*************************************************************
         SPACE 1
         GSAMCALL FINISH,MF=(E,GSB) CLEAR GSAM CONTROL
         SPACE 3
*************************************************************
*        IF THE SCHEDULE FILE HAS BEEN ALTERED, THEN I MUST *
*        NOW START THE SCHEDRUN PROGRAM TO ACCOMPLISH THE   *
*        ALTERED SCHEDULE.                                  *
*************************************************************
         SPACE 1
         TM    DSAFLAGS,DSAFADD2   HAS THE FILE BEEN ADDED TO?
         BZ    NOADDED2            NO, SKIP
         MODESET KEY=ZERO          YES, SET KEY=0
         SR    R0,R0               CLEAR THE UCM ID VALUE
         MGCR  SCHEDRUN-4          EXECUTE THE SCHEDRUN PROGRAM
         MODESET KEY=NZERO         RESTORE P/P KEY
NOADDED2 DS    0H
         SPACE 3
*************************************************************
*        RELEASE THE DSA AND RETURN TO THE CALLER.          *
*************************************************************
         SPACE 1
         #EXIT ((R14,R12)),RC=(R2) RETURN TO CALLER
         TITLE 'SCHEDULE COMMAND -- DATESCRN - DATE SCREENING ROUTINE'
*************************************************************
*                                                           *
*        DATESCRN -- THIS ROUTINE SCREENS THE CURRENT       *
*        SCHEDULE FILE RECORD AGAINST A GIVEN DATE TO SEE   *
*        IF THE RECORD IS SELECTED BY THAT DATE.            *
*                                                           *
*        NOTE, IN THE COMMENTARY FOR THIS ROUTINE ALL       *
*        REFERENCES TO THE "CURRENT DATE" SHOULD BE TAKEN   *
*        TO MEAN THE SELECTION DATE.                        *
*                                                           *
*        INPUTS:                                            *
*              - R1 POINTS TO THE SELECTION DATE (TIMESTAMP *
*                FORM).                                     *
*              - R14 POINTS TO A RETURN ADDRESS.            *
*              - SFR CONTAINS THE CURRENT SCHEDULE FILE     *
*                RECORD.                                    *
*                                                           *
*        RETURN 0(,R14):                                    *
*              - THE CURRENT SFR IS REJECTED. IT IS NOT     *
*                SELECTED BY THE GIVEN DATE.                *
*              - R14: THE HIGH-ORDER BIT IS SET ON IF THE   *
*                SFR IS OBSOLETE WITH RESPECT TO THE GIVEN  *
*                DATE; OTHERWISE, IT IS SET OFF.            *
*              - ALL OTHER REGISTERS ARE RESTORED.          *
*              - WRKPTIME AND WRKNTIME ARE ALTERED IN AN    *
*                UNUSEFUL WAY.                              *
*                                                           *
*        RETURN 4(,R14):                                    *
*              - THE CURRENT SFR IS ACCEPTED. IT IS         *
*                SELECTED BY THE GIVEN DATE.                *
*              - R14: THE HIGH-ORDER BIT IS SET OFF         *
*                SIGNIFYING THAT THE SFR IS NOT OBSOLETE    *
*                WITH RESPECT TO THE GIVEN DATE.            *
*              - R15 CONTAINS THE SCHEDULED EXECUTION TIME  *
*                IN MINUTES (SINCE THE START OF THE         *
*                CENTURY).                                  *
*              - ALL OTHER REGISTERS ARE RESTORED.          *
*              - WRKPTIME CONTAINS THE SCHEDULED EXECUTION  *
*                TIMESTAMP.                                 *
*              - WRKNTIME HAS BEEN ALTERED IN AN UNUSEFUL   *
*                WAY.                                       *
*                                                           *
*************************************************************
         SPACE 1
DATESCRN LA    R14,0(,R14)         CLEAR THE HIGH-BIT
         STM   R14,R2,SAVDATSC     SAVE REGISTERS
         SPACE 1
         LR    R2,R1               COPY THE SELECTION TIMESTAMP PTR
         USING TIME,R2             DCL THE TIMESTAMP BASE
         SPACE 3
*************************************************************
*        GENERATE A START DATE BY SUBSTITUTING CURRENT DATE *
*        VALUES INTO THOSE FIELDS OF THE SFR ASSIGNED DATE  *
*        THAT CONTAIN ZEROS. IF THE RESULTING DATE FALLS    *
*        PAST THE VALID END OF THE MONTH, THEN ADJUST THE   *
*        DAY TO BE THE LAST DAY OF THAT MONTH.              *
*************************************************************
         SPACE 1
         MVC   WRKPTIME,SFRTIME    COPY THE SFR ASSIGNED TIME TO A WORK
*                                  AREA
         SPACE 1
         ICM   R1,3,WRKPYR         IS A YEAR ASSIGNED?
         BNZ   GOTYR               YES, SKIP
         MVC   WRKPYR,TIMEYR       NO, SET TO THE CURRENT YEAR
GOTYR    DS    0H
         SPACE 1
         ICM   R1,3,WRKPMO         IS A MONTH ASSIGNED?
         BNZ   GOTMO               YES, SKIP
         MVC   WRKPMO,TIMEMO       NO, SET TO THE CURRENT MONTH
GOTMO    DS    0H
         SPACE 1
         ICM   R1,3,WRKPDY         IS A DAY ASSIGNED?
         BNZ   GOTDY               YES, SKIP
         MVC   WRKPDY,TIMEDY       NO, SET TO THE CURRENT DAY
GOTDY    DS    0H
         SPACE 1
         LH    R1,WRKPMO           GET THE MONTH VALUE
         AR    R1,R1               CNVRT TO A HWORD OFFSET
         LA    R0,=Y(0,31,28,31,30,31,30,31,31,30,31,30,31) MO LENS
         AR    R1,R0               --> THIS MONTH'S LENGTH
         LH    R0,WRKPDY           GET THE DAY VALUE
         CH    R0,0(,R1)           VALID FOR THIS MONTH?
         BNH   GOTDYOK             YES, NO ADJUSTMENT NEEDED
         LH    R0,0(,R1)           NO, RESET TO THE MONTH'S LAST DAY
         TM    WRKPYR+1,B'00000011' IS THIS A LEAP YEAR?
         BNZ   GOTDYSET            NO, SKIP
         CLI   WRKPMO+1,2          YES, IS THIS FEBRUARY?
         BNE   GOTDYSET            NO, SKIP
         LA    R0,29               YES, RESET TO LEAP-FEB'S LAST DAY
GOTDYSET STH   R0,WRKPDY           RESET DAY TO EO-MONTH
         SPACE 1
GOTDYOK  MVC   WRKNTIME,WRKPTIME   PRIME "WRKN" = "WRKP"
         SPACE 3
*************************************************************
*        ADJUST THE GENERATED START DATE SO THAT "WRKPTIME" *
*        CONTAINS EITHER TODAY OR THE MOST RECENT PRIOR     *
*        EXECUTION DATE, AND "WRKNTIME" CONTAINS EITHER     *
*        TODAY OR THE EARLIEST NEXT EXECUTION DATE FOR THIS *
*        SCHEDULED COMMAND (UNLESS THE RANGE OF POSSIBLE    *
*        EXECUTION DATES DOES NOT SPAN THE CURRENT DATE IN  *
*        WHICH CASE ONE OR THE OTHER OF "WRKPTIME" OR       *
*        "WRKNTIME" WILL CONTAIN AN INCORRECT VALUE BUT THE *
*        ERROR WILL BE SUCH THAT THE VALIDITY OF SUBSEQUENT *
*        TESTS WILL NOT BE AFFECTED).                       *
*************************************************************
         SPACE 1
         CLC   WRKPYMD,TIMETIME    WHICH WAY DO I HAVE TO
*                                  TIME-TRAVEL?
         BE    CHKWDY              NIETHER; GO PERFORM WEEKDAY
*                                  MODIFICATION
         BH    ROLLBACK            BACKWARDS; GO DO IT
*        FALL THROUGH              FORWARDS; GO DO IT
         SPACE 3
*************************************************************
*        THE GENERATED DATE FALLS PRIOR TO THE CURRENT      *
*        DATE. ENTER A LOOP TO ADJUST THE DATE FORWARD IN   *
*        TIME EITHER UNTIL A FUTURE DATE IS ACHIEVED OR     *
*        UNTIL THE DATE OVERFLOWS BACK TO EITHER THE SAME   *
*        DATE OR A PRIOR DATE (IN WHICH CASE THIS SCHEDULED *
*        COMMAND WILL NEVER BE EXECUTED AGAIN).             *
*************************************************************
         SPACE 1
ROLL4WRD MVC   WRKPTIME,WRKNTIME   NEXT IS STILL IN THE PAST. COPY
*                                  IT IN CASE IT TURNS OUT TO BE THE
*                                  MOST RECENT PAST DATE FOR THIS SFR.
         SPACE 1
         LA    R0,1                LOAD AN ARITHMATIC "CARRY" SIGNAL.
*                                  ASSUME "CARRY"
         ICM   R1,3,SFRDY          IS THE DAY SPECIFICALLY DEFINED?
         BNZ   GOTDYF              YES, GO STORE IT
         SR    R0,R0               NO, ASSUME "NO CARRY"
         LH    R1,WRKNDY           GET DAY VALUE
         LA    R1,1(,R1)           ADVANCE A DAY
         CH    R1,=H'31'           OVERFLOW? (NOTE, THE "FINDMINS"
*                                  ROUTINE COMPENSATES FOR SHORTER
*                                  MONTHS)
         BNH   GOTDYF              NO, GO STORE NEW DAY
         BCTR  R0,0                YES, SIGNAL "CARRY"
         LA    R1,1                RESET TO START OF NEXT MONTH
GOTDYF   STH   R1,WRKNDY           STORE NEW DAY VALUE
         SPACE 1
         LTR   R0,R0               WAS THERE A CARRY TO THE MONTHS
*                                  VALUE?
         BZ    VCHKINCR            NO, GO TEST THE NEW DATE
         ICM   R1,3,SFRMO          YES, IS THE MONTH SPECIFICALLY
*                                  ASSIGNED?
         BNZ   GOTMOF              YES, GO STORE IT ("CARRY" IS SET)
         SR    R0,R0               NO, SET "NO CARRY"
         LH    R1,WRKNMO           GET MONTH VALUE
         LA    R1,1(,R1)           INCRIMENT IT
         CH    R1,=H'12'           OVERFLOW?
         BNH   GOTMOF              NO, GO STORE NEW VALUE
         BCTR  R0,0                YES, RESTORE "CARRY" SIGNAL
         LA    R1,1                RESET TO 1ST MONTH OF NEXT YEAR
GOTMOF   STH   R1,WRKNMO           STORE NEW MONTH VALUE
         SPACE 1
         LTR   R0,R0               WAS THERE A CARRY TO THE YEAR VALUE?
         BZ    VCHKINCR            NO, GO TEST THE NEW DATE
         ICM   R1,3,SFRYR          YES, IS THE YEAR SPECIFICALLY
*                                  ASSIGNED?
         BNZ   GOTYRF              YES, GO STORE IT
         LH    R1,WRKNYR           NO, GET OLD YEAR VALUE
         LA    R1,1(,R1)           INCRIMENT IT
GOTYRF   STH   R1,WRKNYR           STORE NEW YEAR VALUE
         SPACE 1
VCHKINCR CLC   WRKPYMD,WRKNYMD     DID THE DATE ADVANCE?
         BL    VCHKSCHD            YES, GO SEE HOW FAR
         OI    SAVDATSC,B'10000000' NO (BUT IT STILL MIGHT DUE TO
*                                  WEEKDAY MODIFICATION). PROBABLY THIS
*                                  SFR IS OBSOLETE. (SAY SO).
         B     CHKWDY              GO CHECK THE WEEKDAY MODIFIER. NOTE,
*                                  "WRKPTIME" IS GOOD; "WRKNTIME" IS
*                                  TRASH.
         SPACE 1
VCHKSCHD CLC   WRKNYMD,TIMETIME    YES, DID THE DATE REACH THE PRESENT
*                                  OR FUTURE?
         BL    ROLL4WRD            NO, LOOP TO TRY AGAIN
         B     CHKWDY              YES, GO EXAMINE WEEKDAY MODIFICATION
         SPACE 3
*************************************************************
*        THE GENERATED DATE FALLS AFTER THE CURRENT DATE.   *
*        ENTER A LOOP TO ADJUST THE DATE BACKWARDS IN TIME  *
*        EITHER UNTIL A PAST DATE IS ACHIEVED OR UNTIL THE  *
*        DATE UNDERFLOWS BACK TO EITHER THE SAME DATE OR A  *
*        MORE FUTURE DATE (IN WHICH CASE THIS SCHEDULED     *
*        COMMAND HAS NEVER YET BEEN EXECUTED).              *
*************************************************************
         SPACE 1
ROLLBACK MVC   WRKNTIME,WRKPTIME   WRKP IS STILL IN THE FUTURE. COPY
*                                  IT IN CASE IT TURNS OUT TO BE THE
*                                  EARLIEST FUTURE DATE FOR THIS SFR.
         SPACE 1
         LA    R0,1                LOAD AN ARITHMATIC "CARRY" SIGNAL.
*                                  ASSUME "CARRY"
         ICM   R1,3,SFRDY          IS THE DAY SPECIFICALLY DEFINED?
         BNZ   GOTDYB              YES, GO STORE IT
         SR    R0,R0               NO, ASSUME "NO CARRY"
         LH    R1,WRKPDY           GET THE DAY VALUE
         BCTR  R1,0                RETREAT A DAY
         LTR   R1,R1               UNDERFLOW?
         BNZ   GOTDYB              NO, GO STORE NEW VALUE
         BCTR  R0,0                YES, SIGNAL "CARRY"
         LA    R1,31               RESET TO END OF PRIOR MONTH (NOTE,
*                                  THE "FINDMINS" ROUTINE COMPENSATES
*                                  FOR SHORTER MONTHS)
GOTDYB   STH   R1,WRKPDY           STORE NEW DAY VALUE
         SPACE 1
         LTR   R0,R0               WAS THERE A CARRY TO THE MONTHS
*                                  VALUE?
         BZ    VCHKDECR            NO, GO TEST THE NEW DATE
         ICM   R1,3,SFRMO          YES, IS THE MONTH SPECIFICALLY
*                                  ASSIGNED?
         BNZ   GOTMOB              YES, GO STORE IT ("CARRY" IS SET)
         SR    R0,R0               NO, SET "NO CARRY"
         LH    R1,WRKPMO           GET MONTH VALUE
         BCTR  R1,0                DECRIMENT IT
         LTR   R1,R1               UNDERFLOW?
         BNZ   GOTMOB              NO, GO STORE NEW VALUE
         BCTR  R0,0                YES, RESTORE "CARRY" SIGNAL
         LA    R1,12               RESET TO LAST MONTH OF PRIOR YEAR
GOTMOB   STH   R1,WRKPMO           STORE NEW MONTH VALUE
         SPACE 1
         LTR   R0,R0               WAS THERE A CARRY TO THE YEAR VALUE?
         BZ    VCHKDECR            NO, GO TEST THE NEW DATE
         ICM   R1,3,SFRYR          YES, IS THE YEAR SPECIFICALLY
*                                  ASSIGNED?
         BNZ   GOTYRB              YES, GO STORE IT
         LH    R1,WRKPYR           NO, GET OLD YEAR VALUE
         BCTR  R1,0                DECRIMENT IT
GOTYRB   STH   R1,WRKPYR           STORE NEW YEAR VALUE
         SPACE 1
VCHKDECR CLC   WRKPYMD,WRKNYMD     DID THE DATE RETREAT?
         BNL   CHKWDY              NO, BUT IT STILL MIGHT DUE TO
*                                  WEEKDAY MODIFICATION. NOTE,
*                                  "WRKNTIME" IS GOOD. "WRKPTIME" IS
*                                  TRASH.
         CLC   WRKPYMD,TIMETIME    YES, DID IT REACH THE PRESENT OR
*                                  PAST?
         BH    ROLLBACK            NO, LOOP TO TRY AGAIN
*        FALL THROUGH              YES, EXAMINE WEEKDAY MODIFICATION
         SPACE 3
*************************************************************
*        PROBABLY VALID BUT CERTAINLY USABLE VALUES FOR     *
*        "WRKPTIME" AND "WRKNTIME" HAVE BEEN DEVELOPED. NOW *
*        MODIFY EITHER THE PREVIOUS DATE FORWARD OR THE     *
*        NEXT DATE BACKWARD ACCORDING TO WHETHER THE        *
*        WEEKDAY MODIFICATION IS FORWARDS OR BACKWARDS. IF  *
*        THE MODIFICATION RESULTS IN THE GENERATION OF THE  *
*        CURRENT DATE, THEN I HAVE A HIT; OTHERWISE I       *
*        DON'T.                                             *
*************************************************************
         SPACE 1
CHKWDY   ICM   R1,3,SFRWK          IS THERE A WEEKDAY MODIFIER?
         BP    WDY4WRD             YES, FORWARDS; GO PROCESS IT
         BM    WDYBACK             YES, BACKWARDS; GO PROCESS IT
         SPACE 1
         CLC   WRKPYMD,TIMETIME    NO, IS THE EXECUTION DATE TODAY?
*                                  (NOTE, IF IT IS, THEN PRECEEDING
*                                  LOGIC FORCES WRKPTIME=WRKNTIME).
         BE    DSCRRET4            YES, ACCEPT THIS SFR; RETURN +4
         B     DSCRRET0            NO, REJECT THIS SFR; RETURN +0
         SPACE 3
*************************************************************
*        THE WEEKDAY MODIFICATION IS FORWARDS               *
*************************************************************
         SPACE 1
WDY4WRD  LA    R1,WRKPTIME         --> MOST RECENT PRIOR DATE
         BAL   R14,FINDMINS        CNVRT TO MINUTES SINCE SO-CENTURY
         LR    R0,R15              SAVE
         BAL   R14,FINDWKDY        CNVRT TO WEEKDAY ID NUMBER
         SH    R15,SFRWK           DISTANCE FROM THE SCHEDULED
*                                  WEEKDAY. IS THE DATE THAT DAY?
         BZ    WDYFCHK             YES, GO SEE IF THE DATE IS TODAY
         LCR   R15,R15             NO, RESET THE DIRECTION; IS THE
*                                  SCHEDULED WEEKDAY AFTER THE DATE'S
*                                  WEEKDAY?
         BP    WDYFAVNC            YES, SKIP
         LA    R15,7(,R15)         NO, RESET THE DATE'S WEEKDAY TO
*                                  "LAST WEEK"
WDYFAVNC MH    R15,=Y(60*24)       CNVRT THE DISTANCE TO MINUTES
         AR    R15,R0              ADVANCE THE GENERATED DATE
         LA    R1,WRKPTIME         --> CONVERSION SINK
         BAL   R14,MINSTIME        CNVRT MINUTES BACK TO TIMESTAMP
         SPACE 1
WDYFCHK  CLC   WRKPYMD,TIMETIME    IS THE GENERATED DATE TODAY?
         BL    DSCRRET0            NO, REJECT THE SFR; RETURN +0 (IT
*                                  MAY OR MAY NOT BE OBSOLETE. R14 IS
*                                  ALREADY SET).
         L     R14,SAVDATSC        (MAYBE, BUT IT DEFINITELY IS NOT -
         LA    R14,0(,R14)          OBSOLETE. INSURE THAT THE -
         ST    R14,SAVDATSC          "OBSOLETE" FLAG IS CLEAR)
         BH    DSCRRET0            NO, REJECT THE SFR; RETURN +0
         B     WDYFRAME            YES, GO MAKE FINAL TESTS
         SPACE 3
*************************************************************
*        THE WEEKDAY MODIFICATION IS BACKWARDS              *
*************************************************************
         SPACE 1
WDYBACK  LA    R1,WRKNTIME         --> EARLIEST FUTURE DATE
         BAL   R14,FINDMINS        CNVRT TO MINUTES SINCE SO-CENTURY
         LR    R0,R15              SAVE
         BAL   R14,FINDWKDY        CNVRT TO WEEKDAY ID NUMBER
         AH    R15,SFRWK           DISTANCE FROM THE SCHEDULED
*                                  WEEKDAY; IS THE DATE'S WEEKDAY THAT
*                                  DAY?
         BZ    WDYBCHK             YES, GO SEE IF THE DATE IS TODAY
         BP    WDYBRTRT            NO, THE SCHEDULED WEEKDAY FALLS
*                                  EARLIER IN THE WEEK THAN THE DATE'S
*                                  WEEKDAY; PROCEED
         LA    R15,7(,R15)         NO, THE SCHEDULED WEEKDAY FALLS
*                                  LATER IN THE WEEK THAN THE DATE'S
*                                  WEEKDAY. ADJUST THE DATE'S WEEKDAY
*                                  TO THE PRECEEDING WEEK
WDYBRTRT MH    R15,=Y(-60*24)      CNVRT THE DISTANCE TO MINUTES AND
*                                  COMPLEMENT
         AR    R15,R0              ADJUST THE DATE BACK IN TIME
         LA    R1,WRKNTIME         --> CONVERSION SINK
         BAL   R14,MINSTIME        CNVRT DATE BACK TO TIMESTAMP
         SPACE 1
WDYBCHK  CLC   WRKNYMD,TIMETIME    IS THE GENERATED DATE TODAY?
         BNE   DSCRRET0            NO, REJECT THIS SFR
         SPACE 3
*************************************************************
*        THE GENERATED DATE AS MODIFIED BY THE WEEKDAY      *
*        VALUE IS TODAY. BUT THAT IS NOT ENOUGH. IF THE     *
*        SFR'S DATE CONTAINS SPECIFIC VALUES FOR MONTH AND  *
*        YEAR, THEN THE WEEKDAY MODIFICATION MUST NOT       *
*        CHANGE THOSE VALUES.                               *
*************************************************************
         SPACE 1
WDYFRAME SR    R1,R1               CLEAR FOR INSERTS
         ICM   R1,3,SFRYR          IS A SPECIFIC YEAR ASSIGNED?
         BZ    WDYYROK             NO, SKIP
         CH    R1,TIMEYR           YES, CHANGED?
         BNE   DSCRRET0            YES, REJECT THIS SFR
         SPACE 1
WDYYROK  ICM   R1,3,SFRMO          NO, IS A SPECIFIC MONTH ASSIGNED?
         BZ    DSCRRET4            NO, ACCEPT THIS SFR
         CH    R1,TIMEMO           YES, CHANGED?
         BNE   DSCRRET0            YES, REJECT THIS SFR
*        FALL THROUGH              NO, ACCEPT THIS SFR
         SPACE 3
*************************************************************
*        ACCEPT THE SFR. CONSTRUCT THE SCHEDULED TIMESTAMP  *
*        IN WRKPTIME. GET THE SCHEDULED TIME, IN MINUTES,   *
*        INTO R15. RETURN +4 TO THE CALLER.                 *
*************************************************************
         SPACE 1
DSCRRET4 MVC   WRKPYMD,TIMEYMD     GET THE SCHEDULED DATE
         MVC   WRKPHM,SFRHM        APPEND THE SCHEDULED TIME
         SPACE 1
         LA    R1,WRKPTIME         --> TIMESTAMP
         BAL   R14,FINDMINS        CONVERT TO MINUTES IN R15
         SPACE 1
         L     R14,SAVDATSC        RESTORE R14 WITH OBSOLETE=0
         LM    R0,R2,SAVDATSC+8    RESTORE REGISTERS
         DROP  R2                  RELEASE TIMESTAMP BASE
         B     4(,R14)             RETURN +4 TO CALLER
         SPACE 3
*************************************************************
*        REJECT THE SFR. RETURN +0                          *
*************************************************************
         SPACE 1
DSCRRET0 LM    R14,R2,SAVDATSC     RESTORE REGISTERS
         BR    R14                 RETURN +0 TO CALLER
         TITLE 'SCHEDULE COMMAND -- DISPLAY - DISPLAY A SCHEDULED COMMA*
               ND TO THE USER'
*************************************************************
*                                                           *
*        DISPLAY -- DISPLAY A SCHEDULED COMMAND TO THE      *
*        USER.                                              *
*                                                           *
*        INPUTS:                                            *
*              - R14 POINTS TO A RETURN ADDRESS.            *
*              - SFR CONTAINS THE SCHEDULED COMMAND TO BE   *
*                DISPLAYED.                                 *
*              - DSAFLAGS(DSAFTITL) GATES WHETHER OR NOT A  *
*                DISPLAY LISTING TITLE LINE HAS BEEN        *
*                GENERATED ALREADY.                         *
*                                                           *
*        RETURN 0(,R14):                                    *
*              - THE DISPLAY HAS BEEN INTERRUPTED BY AN     *
*                ATTENTION SIGNAL FROM THE USER.            *
*              - ALL REGISTERS ARE RESTORE.                 *
*                                                           *
*        RETURN 4(,R14):                                    *
*              - THE DISPLAY LINE HAS BEEN PRINTED WITHOUT  *
*                INTERRUPTION.                              *
*              - ALL REGISTERS ARE RESTORED.                *
*                                                           *
*************************************************************
         SPACE 1
DISPLAY  STM   R14,R1,SAVDISPL     SAVE REGISTERS
         SPACE 3
*************************************************************
*        SEE IF A TITLE LINE HAS BEEN WRITTEN YET.          *
*************************************************************
         SPACE 1
         TM    DSAFLAGS,DSAFTITL   TITLE ALREADY PRINTED?
         BNZ   DSTITLED            YES, DON'T REPRINT IT
         OI    DSAFLAGS,DSAFTITL   NO, CLOSE THE GATE
         #PUT  MDISPTIT%           DISPLAY THE TITLE LINE
         B     DISPRET0            +0 ATTENTION; RETURN +0 TO CALLER
DSTITLED DS    0H                  +4 AOK; PROCEED
         SPACE 3
*************************************************************
*        GENERATE THE SFR DISPLAY LINE.                     *
*************************************************************
         SPACE 1
         MVI   DMSG,C' '           CLEAR THE -
         MVC   DMSG+1(DML-1),DMSG   MSG BUFFER
         SPACE 1
         LH    R1,SFRID            GET THE ID NUMBER
         CVD   R1,DSAWORK          CNVRT TO DECIMAL
         MVC   DSAWORK+8(9),=X'402020202120404040' EDIT MASK
         #TEST SIZE=(17,LE,L'DSAWORK)
         LA    R1,DSAWORK+13       PRIME ICO SIGNIFICANCE IS FORCED
         EDMK  DSAWORK+8(6),DSAWORK+5 CNVRT TO EBCDIC
         MVC   DMID(4),0(R1)       COPY LEFT-JUSTIFIED TO MSG
         SPACE 1
         MVC   DMSYSID,SFRSYSID    GET THE SYSTEM-ID
         TM    DMSYSID,255-C' '    "ANY"?
         BNZ   DSTSYSID            NO, DONE HERE
         MVC   DMSYSID,=CL(L'SFRSYSID)'ANY' YES, SAY SO
DSTSYSID DS    0H
         SPACE 1
         MVC   DMDATE(8),=C'--/--/--' JUNK
         SR    R1,R1
         ICM   R1,3,SFRMO          EXPLICIT MONTH?
         BZ    DSTNOMO             NO, SKIP
         CVD   R1,DSAWORK          YES, CNVRT TO DECIMAL
         UNPK  DMDATE(2),DSAWORK+6(2) CNVRT TO EBCDIC
         OI    DMDATE+1,B'11110000' FIX THE FUCKING SIGN
DSTNOMO  ICM   R1,3,SFRDY          EXPLICIT DAY?
         BZ    DSTNODY             NO, SKIP
         CVD   R1,DSAWORK          YES, CNVRT TO DECIMAL
         UNPK  DMDATE+3(2),DSAWORK+6(2) CNVRT TO EBCDIC
         OI    DMDATE+4,B'11110000' FIX THE FUCKING SIGN
DSTNODY  ICM   R1,3,SFRYR          EXPLICIT YEAR?
         BZ    DSTNOYR             NO, SKIP
         CVD   R1,DSAWORK          YES, CNVRT TO DECIMAL
         UNPK  DMDATE+6(2),DSAWORK+6(2) CNVRT TO EBCDIC
         OI    DMDATE+7,B'11110000' FIX THE FUCKING SIGN
DSTNOYR  DS    0H
         SPACE 1
         ICM   R1,3,SFRWK          EXPLICIT WEEK?
         BZ    DSTNOWK             NO, SKIP
         LA    R15,WEEKDAYS-WDYROOTL-1 YES, --> LOOKUP TABLE
         SR    R14,R14             CLEAR FOR LENGTH INSERTS
DSTWKLP  LA    R15,WDYROOTL+1(R14,R15) --> NEXT ENTRY
         ICM   R14,1,WDYLNGTH(R15) GET L'ENTRY; EOL?
         #DIE  M                   YES, LOGIC ERROR
         CLM   R1,1,WDYNUMBR(R15)  NO, RIGHT DAY?
         BNE   DSTWKLP             NO, KEEP LOOKING
         MVC   DMDAY,WDYNAME(R15)  YES, COPY TO MSG
         CLI   DMDAY,C'-'          NEG. WEEKDAY?
         BNE   DSTNOWK             NO, DONE HERE
         MVC   DMDAY-1(L'DMDAY+1),WDYNAME(R15) YES, ADJUST THE TEXT
DSTNOWK  DS    0H
         SPACE 1
         LH    R1,SFRWHR           GET WINDOW HOURS
         MH    R1,=H'100'          DECIMAL SHIFT 2 PLACES
         AH    R1,SFRWMN           INCLUDE WINDOW MINUTES
         CVD   R1,DSAWORK          CNVRT TO DECIMAL
         MVC   DMWINDOW-1(9),=X'4020202021207A2020' EDIT MASK
         LA    R1,DMWINDOW+4       PRIME ICO SIGNIFICANCE FORCED
         EDMK  DMWINDOW-1(9),DSAWORK+4 CNVRT TO EBCDIC
         BCTR  R1,0                BACK OFF
         MVI   0(R1),C'+'          DECORATION
         SPACE 1
         LH    R1,SFRHR            GET HOUR
         MH    R1,=H'100'          DECIMAL SHIFT 2 PLACES
         AH    R1,SFRMN            INCLUDE MINUTES
         CVD   R1,DSAWORK          CNVRT TO DECIMAL
         UNPK  DMTIME+1(4),DSAWORK+5(3) CNVRT TO EBCDIC
         OI    DMTIME+4,B'11110000' FIX THE FUCKING SIGN
         MVC   DMTIME(2),DMTIME+1  CORRECT POSITION
         MVI   DMTIME+2,C':'       DECORATION
         SPACE 1
         LA    R1,DMFLAGS          --> FLAGS FIELD
         TM    SFRFLAG,SFRFIPLF    IF?
         BZ    DSTNOIF             NO, SKIP
         MVI   0(R1),C'I'          YES, SAY SO
         LA    R1,2(,R1)           ADVANCE
DSTNOIF  TM    SFRFLAG,SFRFOVRD    OVERRIDE?
         BZ    DSTNOOV             NO, SKIP
         MVI   0(R1),C'O'          YES, SAY SO
         LA    R1,2(,R1)           ADVANCE
DSTNOOV  TM    SFRFLAG,SFRFOBSO    OBSOLETE COMMAND?
         BZ    DSTNOOBS            NO, SKIP
         MVI   0(R1),C'N'          YES, SAY SO
DSTNOOBS DS    0H
         SPACE 1
         MVC   DMNAME,SFRNAME      GET THE APPLICATION NAME
         SPACE 1
         LA    R0,DMCMD-2          EO-MSG IF NULL COMMAND
         LH    R15,SFRCMDL         GET L'CMD + 4
         LA    R15,SFRCMDL(R15)    --> EO-CMD
DSTCLN   BCTR  R15,0               BACK UP
         TM    0(R15),B'10111111'  C' ' OR X'00'?
         BZ    DSTCLN              YES, KEEP SCANNING
         LA    R14,SFRCMD-1        NO, --> SO-FIELD
         SR    R15,R14             GET L'COMMAND TEXT; NULL?
         BNP   DSTPUT              YES, DONE HERE
         LA    R14,SFRCMD          NO, --> COPY SOURCE
         LA    R0,DMCMD            --> COPY SINK
         LA    R1,L'DMCMD          L'SINK
         CR    R1,R15              IS SINK TOO LONG?
         BNH   DSTCMOVE            NO, PROCEED
         LR    R1,R15              YES, USE L'SOURCE
DSTCMOVE MVCL  R0,R14              COPY CMD TEXT TO MSG
         SPACE 1
DSTPUT   LA    R1,DMSG             --> MSG
         SR    R0,R1               GET L'MSG
         STC   R0,DMSG-1           STORE FOR "PUTMSG"
         AR    R1,R0               --> EO-MSG
         MVI   0(R1),0             SET TRAILER
         #PUT  DMSG                DISPLAY THE MESSAGE
         B     DISPRET0            +0 ATTENTION; RETURN +4 TO THE CALLR
*        FALL THROUGH              +4 AOK; RETURN +4 TO THE CALLER
         SPACE 3
*************************************************************
*        AOK. RETURN +4                                     *
*************************************************************
         SPACE 1
         LM    R14,R1,SAVDISPL     RESTORE REGISTERS
         B     4(,R14)             RETURN TO CALLER
         SPACE 3
*************************************************************
*        ATTENTION RECEIVED. RETURN +0                      *
*************************************************************
         SPACE 1
DISPRET0 LM    R14,R1,SAVDISPL     RESTORE REGISTERS
         BR    R14                 RETURN +0
         TITLE 'SCHEDULE COMMAND -- FINDTIME/MINSTIME - TODC TO TIMESTA*
               MP CONVERSION'
*************************************************************
*                                                           *
*        FINDTIME -- CONVERTS A TIME-OF-DAY CLOCK VALUE TO  *
*        A TIMESTAMP OF THE FORM YY/MM/DD/HH/MM.            *
*                                                           *
*        MINSTIME - CONVERTS A TIME VALUE GIVEN IN MINUTES  *
*        (SINCE THE START OF THE CENTURY) TO A TIMESTAMP.   *
*                                                           *
*        INPUTS (FINDTIME):                                 *
*              - R1 POINTS TO A TIMESTAMP OUTPUT AREA WHICH *
*                CONSISTS OF FIVE CONSECUATIVE HALFWORDS.   *
*              - DSAWORK CONTAINS A TOD CLOCK VALUE.        *
*              - R14 POINTS TO THE RETURN ADDRESS.          *
*                                                           *
*        INPUTS (MINSTIME):                                 *
*              - R1 POINTS TO A TIMESTAMP OUTPUT AREA.      *
*              - R14 POINTS TO A RETURN ADDRESS.            *
*              - R15 CONTAINS THE MINUTES VALUE TO BE       *
*                CONVERTED.                                 *
*                                                           *
*        RETURN 0(,R14):                                    *
*              - THE CLOCK VALUE IS CONVERTED AND STORED    *
*                INTO THE AREA POINTED TO BY R1.            *
*              - ALL REGISTERS ARE RESTORED.                *
*                                                           *
*************************************************************
         SPACE 1
         USING TIME,R1             DCL TIME BASE
FINDTIME STM   R14,R0,SAVFTIME     SAVE REGISTERS
         LM    R14,R15,DSAWORK     LOAD TODC VALUE
         SRDL  R14,12              CNVRT TO MICROSECONDS
         D     R14,=F'60000000'    CNVRT TO MINUTES
         B     GOTMINUT            SKIP ALTERNATE ENTRY
         SPACE 3
MINSTIME STM   R14,R0,SAVFTIME     SAVE REGISTERS
GOTMINUT SR    R14,R14
         D     R14,=F'60'          GET +MM, HOURS
         STH   R14,TIMEMN          SAVE +MM
         SR    R14,R14
         D     R14,=F'24'          GET +HH, DAYS
         STH   R14,TIMEHR          SAVE +HH
         SPACE 1
         LA    R15,1(,R15)         PRETEND 1900 WAS A LEAPYEAR
         SR    R14,R14
         D     R14,=A(365*4+1)     GET +DAYS, QUADYEARS
         SLL   R15,2               GET YEARS (ALMOST)
         CH    R14,=H'366'         ANY RESIDUAL YEARS?
         BL    GOTYYDDD            NO, SKIP
         BCTR  R14,0               YES, DECR FOR LEAPDAY
         LR    R0,R15              SAVE QUADYEARS*4
         SRDL  R14,32              SHIFT +DAYS
         D     R14,=F'365'         GET +DAYS, +YEARS
         AR    R15,R0              GET +YEARS
GOTYYDDD LA    R15,1900(,R15)      GET YY
         STH   R15,TIMEYR          SAVE
         SPACE 1
         LA    R14,1(,R14)         CNVRT +DAYS TO 1-ORIGIN
         TM    TIMEYR+1,B'00000011' IN A LEAPYEAR?
         BNZ   NRMLYEAR            NO, PROCEED
         CH    R14,=Y(31+29)       YES, PAST THE LEAPDAY?
         BL    NRMLYEAR            NO, PROCEED
         BH    LEAPYEAR            YES, MUST ADJUST
         LA    R15,2               NO, ON THE LEAP DAY; GET MM
         LA    R14,29              GET DD
         B     GOTMMDD             DONE HERE
LEAPYEAR BCTR  R14,0               ADJUST FOR THE EXTRA (LEAP) DAY
NRMLYEAR LA    R15,=Y(0,31,28,31,30,31,30,31,31,30,31,30,31)
         LR    R0,R15              (SAVE)
MONSIZLP LA    R15,2(,R15)         --> NEXT MONTH SIZE
         SH    R14,0(,R15)         DECR +DAYS; TOO FAR YET?
         BP    MONSIZLP            NO, KEEP LOOPING
         AH    R14,0(,R15)         YES, RESTORE CORRECT DD
         SR    R15,R0              GET MM*2
         SRL   R15,1               GET MM
GOTMMDD  STH   R15,TIMEMO          STORE MM
         STH   R14,TIMEDY          STORE DD
         SPACE 1
         LM    R14,R0,SAVFTIME     RESTORE REGISTERS
         BR    R14                 RETURN TO CALLER
         DROP  R1                  RELEASE TIME BASE
         TITLE 'SCHEDULE COMMAND -- FINDMINS - TIMESTAMP TO MINUTES CON*
               VERSION'
*************************************************************
*                                                           *
*        FINDMINS -- CONVERTS A TIMESTAMP VALUE TO MINUTES  *
*        SINCE THE START OF THE CENTURY.                    *
*                                                           *
*        INPUTS:                                            *
*              - R1 POINTS TO A TIMESTAMP.                  *
*              - R14 POINTS TO THE RETURN ADDRESS.          *
*                                                           *
*        RETURN 0(,R14):                                    *
*              - R15 CONTAINS THE MINUTES VALUE.            *
*              - ALL OTHER REGISTERS ARE RESTORED.          *
*                                                           *
*************************************************************
         SPACE 1
         USING TIME,R1             DCL TIME BASE
FINDMINS STM   R14,R0,SAVFMINS     SAVE REGISTERS
         LH    R15,TIMEYR          GET YY
         SH    R15,=H'1900'        CNVRT TO +YEARS
         SR    R14,R14
         D     R14,=F'4'           CNVRT TO +YEARS, QUADYEARS
         MH    R15,=Y(365*4+1)     CNVRT QUADYEARS TO DAYS
         BCTR  R15,0               ADJUST FOR THE FACT THAT 1900 WAS
*                                  NOT A LEAPYEAR.
         LTR   R14,R14             ANY +YEARS?
         BZ    NORSDUYR            NO, SKIP
         MH    R14,=H'365'         YES, CNVRT TO +DAYS - 1 (LEAPDAY)
         LA    R15,1(R14,R15)      GET +DAYS (INCL LEAPDAY)
NORSDUYR DS    0H                  GOT YY CONVERTED TO DAYS
         SPACE 1
         LH    R14,TIMEMO          GET MM
         AR    R14,R14             CNVRT TO HWORD OFFSET
         LA    R0,=Y(0,31,28,31,30,31,30,31,31,30,31,30,31)
         AR    R14,R0
         LH    R0,TIMEDY           GET THE DD
         CH    R0,0(,R14)          IS IT TOO LARGE FOR THIS MM?
         BNH   FMDYOK              NO, PROCEED
         LH    R0,0(,R14)          YES, USE MAX DD
         TM    TIMEYR+1,B'00000011' IS THIS A LEAPYEAR?
         BNZ   FMDYOK              NO, GOT DD
         CLI   TIMEMO+1,2          YES, IS THIS FEBRUARY?
         BNE   FMDYOK              NO, GOT DD
         LA    R0,29               YES, (JESUS CHRIST.)
FMDYOK   BCTR  R0,0                CNVRT FROM 1-ORIGIN TO 0-ORIGIN
         AR    R15,R0              ADD DD TO DAYS
         SPACE 1
FMMSZLP  BCTR  R14,0               --> PREVIOUS -
         BCTR  R14,0                MONTH'S SIZE
         AH    R15,0(,R14)         ADD ITS SIZE TO DAYS
         CLI   1(R14),0            BACK PAST JANUARY YET?
         BNE   FMMSZLP             NO, KEEP LOOPING
         TM    TIMEYR+1,B'00000011' YES, IS THIS A LEAP YEAR?
         BNZ   FMNOLPYR            NO, GAT DAYS (FINALLY)
         CLI   TIMEMO+1,2          YES, IS MM PAST FEBRUARY?
         BNH   FMNOLPYR            NO, GOT DAYS
         LA    R15,1(,R15)         YES, ADJUST DAYS
FMNOLPYR DS    0H                  GOT DAYS SINCE START OF CENTURY
         SPACE 1
         MH    R15,=H'24'          CNVRT TO HOURS
         AH    R15,TIMEHR          INCLUDE HH
         MH    R15,=H'60'          CNVRT TO MINUTES
         AH    R15,TIMEMN          INCLUDE MM
         SPACE 1
         L     R14,SAVFMINS        RESTORE REGISTER
         L     R0,SAVFMINS+8       RESTORE REGISTERS
         BR    R14                 RETURN TO CALLER
         DROP  R1                  RELEASE TIME BASE
         TITLE 'SCHEDULE COMMAND -- FINDWKDY - FROM GIVEN MINUTES'
*************************************************************
*                                                           *
*        FINDWKDY -- FINDS THE DAY OF THE WEEK ON WHICH A   *
*        TIME VALUE, GIVEN IN MINUTES, FALLS.               *
*                                                           *
*        INPUTS:                                            *
*              - R14 POINTS TO THE RETURN ADDRESS.          *
*              - R15 CONTAINS THE GIVEN TIME VALUE IN       *
*                MINUTES SINCE THE START OF THE CENTURY.    *
*                                                           *
*        RETURN 0(,R14):                                    *
*              - R15 CONTAINS THE DAY OF THE WEEK.          *
*                (SUNDAY = 1).                              *
*              - ALL OTHER REGISTERS ARE RESTORED.          *
*                                                           *
*************************************************************
         SPACE 1
FINDWKDY ST    R14,SAVFWKDY        SAVE REGISTER
         SR    R14,R14
         D     R14,=A(60*24)       CNVRT MINUTES TO DAYS
         LA    R15,1(,R15)         ADJUST THE ORIGIN
         SR    R14,R14
         D     R14,=F'7'           GET THE WEEKDAY
         LA    R15,1(,R14)         COPY AND CNVRT TO 1-ORIGIN
         L     R14,SAVFWKDY        RESTORE
         BR    R14                 RETURN TO CALLER
         TITLE 'SCHEDULE COMMAND -- GSAMERR - MESSAGE GENERATER'
*************************************************************
*                                                           *
*        GSAMERR -- A GSAM ERROR HAS OCCURED. ISSUE THE     *
*        GSAM GENERATED ERROR MESSAGE. STACK BEHIND IT A    *
*        MESSAGE GIVING THE EXPLICIT ERROR CODES RECIEVED   *
*        FROM GSAM. THEN LOOP TO MSGGET.                    *
*                                                           *
*        INPUTS:                                            *
*              - R14 POINTS TO THE GSAM REQUEST             *
*                DESCRIPTION.                               *
*              - R15 CONTAINS THE GSAM ERROR CODE.          *
*              - GSBVSAM CONTAINS THE GSAM REASON CODE.     *
*              - GSBMSGX CONTAINS THE GSAM GENERATED        *
*                MESSAGE.                                   *
*                                                           *
*        EXIT MSGGET:                                       *
*              - MESSAGES ARE ISSUED/STACKED VIA PUTLINE.   *
*                                                           *
*************************************************************
         SPACE 3
*************************************************************
*        GET THE PRIMARY MESSAGE FROM GSAM. IF NONE IS      *
*        PROVIDED, THEN USE A DEFAULT MESSAGE.              *
*************************************************************
         SPACE 1
GSAMERR  MVC   DYNAMSGS(L'GSBMSGX),GSBMSGX GET GSAM'S MESSAGE
         #TEST SIZE=(L'DYNAMSGS,GT,L'GSBMSGX)
         LA    R2,DYNAMSGS+L'GSBMSGX LOAD SCANNER
GEBLP0   BCTR  R2,0                BACK UP
         TM    0(R2),B'10111111'   STILL C' ' OR X'00'?
         BZ    GEBLP0              YES, KEEP SCANNING
         LA    R0,DYNAMSGS-1       NO, LOAD BASE
         SR    R2,R0               GET L'MSG; NULL?
         BP    GOTGMSG1            NO, PROCEED
         MVC   DYNAMSGS(27),=C' SCHEDULE FILE ACCESS ERROR' YES, USE DF
         LA    R2,27               GET L'DEFAULT
GOTGMSG1 LA    R2,1(,R2)           GET L'MSG
         STC   R2,DYNAMSGS-1       STORE
         LA    R2,DYNAMSGS-1(R2)   --> EO-TEXT (SO FAR)
         MVI   0(R2),C'+'          INSERT STACKED MESSAGES SIGNAL
         LA    R3,1(,R2)           --> ENTIRELY PAST 1ST MSG
         SPACE 3
*************************************************************
*        CONSTRUCT A 2ND-LEVEL INFORMATION MESSAGE.         *
*************************************************************
         SPACE 1
         MVC   1(14,R3),=C' GSAM ERROR - '
         MVC   15(8,R3),GSBFILE
         LA    R2,23(,R3)
GEBLP1   BCTR  R2,0
         TM    0(R2),B'10111111'  C' ' OR X'00'?
         BZ    GEBLP1              YES, KEEP SCANNING
         MVC   1(7,R2),=C' FILE, '
         SR    R1,R1
         IC    R1,0(,R14)          GET L'REQUEST DESCRIPTER
         MVC   8(*-*,R2),1(R14)    (EXECUTED)
         EX    R1,*-6              COPY TO MSG
         LA    R2,9(R1,R2)         ADVANCE
         MVC   0(13,R2),=C', ERROR CODE '
         CVD   R15,DSAWORK
         MVC   DSAWORK+8(10),=X'40202020212040404040'
         #TEST SIZE=(18,LE,L'DSAWORK)
         LA    R1,DSAWORK+13       PRIME ICO SIGNIFICANCE FORCED
         EDMK  DSAWORK+8(6),DSAWORK+5 CNVRT TO EBCDIC
         MVC   13(5,R2),0(R1)      COPY LEFT JUSTIFIED
         LA    R15,DSAWORK+14
         SR    R15,R1              L'DIGITS
         LA    R2,13(R15,R2)       --> EO-MSG SO FAR
         MVC   0(14,R2),=C', REASON CODE ' TEXT
         L     R15,GSBVSAM         GET REASON CODE
         CVD   R15,DSAWORK         CNVRT TO DECIMAL
         MVC   DSAWORK+8(10),=X'40202020212040404040' EDIT MASG & PAD
         #TEST SIZE=(18,LE,L'DSAWORK)
         LA    R1,DSAWORK+13       PRIME ICO SIGNIFICANCE FORCED
         EDMK  DSAWORK+8(6),DSAWORK+5 CNVRT TO EBCDIC; SET R1
         MVC   14(5,R2),0(R1)      COPY LEFT JUSTIFIED
         LA    R15,DSAWORK+14
         SR    R15,R1              L'DIGITS
         LA    R2,13(R15,R2)       --> LAST CHARACTER OF THE MSG
         SR    R2,R3               L'MSG
         STC   R2,0(,R3)           STORE FOR PUTLINE
         SPACE 1
         AR    R3,R2               --> LAST CHARACTER OF 2ND MSG
         MVI   1(R3),0             SET "NO 3RD MSG" SIGNAL
         SPACE 1
         LA    R0,DYNAMSGS+L'DYNAMSGS-1 --> EO-MSGS BUFFER
         CR    R3,R0               OVERFLOW?
         #DIE  H                   YES, DESIGN ERROR
         SPACE 3
*************************************************************
*        CALL PUTLINE TO DISPLAY THE MESSAGE(S).            *
*************************************************************
         SPACE 1
         #PUT  DYNAMSGS            DISPLAY THE MESSAGES
         #DIE  C,15                +0 ATTENTION; TERMINATE
         B     PUTGET              +4 AOK; LOOP FOR NEXT SUB-CMD
         TITLE 'SCHEDULE COMMAND -- NEXTID - READ AND UPDATE "SFMRNXID"*
               '
*************************************************************
*                                                           *
*        NEXTID -- THIS ROUTINE READS THE SCHEDULE FILE'S   *
*        MASTER RECORD; EXTRACTS, INCRIMENTS, AND UPDATES   *
*        THE SFMRNXID FIELD; REWRITES THE UPDATED MASTER    *
*        RECORD; AND RETURNS THE UPDATED VALUE TO THE       *
*        CALLER.                                            *
*                                                           *
*        INPUTS:                                            *
*              - R14 POINTS TO THE RETURN ADDRESS.          *
*                                                           *
*        RETURN 0(,R14):                                    *
*              - R1 CONTAINS THE NEXT ID VALUE.             *
*              - ALL OTHER REGISTERS ARE RESTORED.          *
*              - NEXTID# CONTAINS A COPY OF THE UPDATED ID  *
*                VALUE.                                     *
*              - THE SFMRNXID FIELD OF THE SCHEDULE FILE'S  *
*                MASTER RECORD IS INCRIMENTED, AND THE      *
*                ENTIRE MASTER RECORD IS REWRITTEN TO THE   *
*                SCHEDULE FILE.                             *
*              - THE SFR BUFFER CONTAINS A COPY OF THE      *
*                UPDATED MASTER RECORD. (THE BUFFER'S PRIOR *
*                CONTENTS ARE LOST).                        *
*                                                           *
*        EXIT GSAMERR:                                      *
*              - AN ERROR HAS OCCURED DURING THE PROCESSING *
*                OF THE SCHEDULE FILE.                      *
*              - R14 POINTS TO A MESSAGE SEGMENT.           *
*              - R15 CONTAINS THE GSAM RETURN CODE.         *
*              - R0 AND R1 ARE NOT RESTORED.                *
*              - ALL OTHER REGISTERS ARE RESTORED.          *
*                                                           *
*************************************************************
         SPACE 1
NEXTID   STM   R14,R0,SAVNXTID     SAVE REGISTERS
         MVI   SFMRKEY,X'FF'       SET THE MASTER -
         MVC   SFMRKEY+1(L'SFMRKEY-1),SFMRKEY RECORD'S KEY
         GSAMCALL READ,OPTIONS=(DIR,UPDATE),MF=(E,GSB) EXCLUSIVE READ
         LA    R14,=C'UPDATE-READ' --> REQUEST DESCRIPTION
         LTR   R15,R15             AOK?
         BNZ   GSAMERR             NO, ERROR
         LH    R1,SFMRNXID         YES, GET THE MASTER ID
         LA    R1,1(,R1)           INCREMENT IT
         CH    R1,=H'9999'         OVERFLOW?
         BNH   NIDGOTID            NO, PROCEED
         SR    R1,R1               YES, RESET
NIDGOTID STH   R1,SFMRNXID         STORE NEW VALUE
         STH   R1,NEXTID#          SAVE LOCALLY TOO
         GSAMCALL REWRITE,MF=(E,GSB) REWRITE THE MASTER RECORD
         LA    R14,=C'REWRITE'    --> REQUEST DESCRIPTION
         LTR   R15,R15             AOK?
         BNZ   GSAMERR             NO, ERROR
         SPACE 1
         LH    R1,NEXTID#          YES, RESTORE UPDATED ID#
         LM    R14,R0,SAVNXTID     RESTORE REGISTERS
         BR    R14                 RETURN TO CALLER
         TITLE 'SCHEDULE COMMAND -- PUTMSG - INTERFACE TIO PUTLINE'
*************************************************************
*                                                           *
*        PUTMSG -- THIS ROUTINE INTERFACES TO THE TSO       *
*        "PUTLINE" SERVICE ROUTINE TO DISPLAY MESSAGES AT   *
*        THE USER'S TERMINAL.                               *
*                                                           *
*        INPUTS:                                            *
*              - R1 POINTS TO THE MESSAGES TO BE DISPLAYED. *
*                EACH MESSAGE IS PRECEEDED BY A SINGLE BYTE *
*                CONTAINING THAT MESSAGE'S TRUE LENGTH. THE *
*                END OF THE MESSAGES LIST IS SIGNALLED BY A *
*                LENGTH BYTE CONTAINING X'00'.              *
*              - R14 POINTS TO A RETURN ADDRESS.            *
*                                                           *
*        RETURN 0(,R14):                                    *
*              - THE MESSAGE DISPLAY HAS FAILED, LIKELY DUE *
*                TO AN ATTENTION SIGNAL.                    *
*              - ALL REGISTERS ARE RESTORED.                *
*                                                           *
*        RETURN 4(,R14):                                    *
*              - THE MESSAGES HAVE BEEN DISPLAYED WITHOUT   *
*                PROBLEMS.                                  *
*              - ALL REGISTERS ARE RESTORED.                *
*                                                           *
*************************************************************
         SPACE 1
PUTMSG   STM   R14,R5,SAVPMSG      SAVE REGISTERS
         CLI   0(R1),0             NULL CALL?
         BE    PMSGRET4            YES, RETURN +4
         SPACE 3
*************************************************************
*        CONVERT THE MESSAGES BLOCK TO TSO "OLD" BLOCKS.    *
*************************************************************
         SPACE 1
         LA    R2,DSAOLDS-OLDLEN PRIME THE OLDS SCANNER
         USING OLD,R2              DCL OLD BASE
         LA    R14,DSAMSGS         --> MSG BLOCKS CONSTRUCTION AREA
         LA    R3,L'DSAMSGS        GET THE AREA'S LENGTH
         SR    R5,R5               CLEAR FOR INSERTS
         SPACE 1
PMSGBUIL ICM   R5,1,0(R1)          GET NEXT MSG'S LENGTH; EO-LIST?
         BZ    PMSGPUT             YES, DONE HERE
         LA    R0,OLD+OLDLEN       NO, --> NEXT OLD
         ST    R0,OLDCHAIN         LINK TO CURRENT OLD (MAY BE THE
*                                  GARBAGE AREA)
         LR    R2,R0               LOAD NEXT OLD POINTER
         CLI   0(R2),X'FF'         OLD OVERFLOW?
         #DIE  E                   YES, CAPACITY ERROR
         SPACE 1
         LA    R0,1(,R14)          NO, ROUND -
         SRL   R0,1                 UP -
         AR    R0,R0                 THE -
         SR    R0,R14                 NEXT -
         AR    R14,R0                  TEXT POINTER
         SR    R3,R0               ADJUST THE AREA RESIDUE
         ST    R14,OLDTEXTP        STORE TEXT PTR INTO THE OLD
         SPACE 1
         LA    R0,4(,R5)           GET L'TEXT BLOCK
         SR    R3,R0               WILL IT FIT IN THE AREA?
         #DIE  M                   NO, CAPICITY ERROR
         STH   R0,0(,R14)          YES, STORE L'TEXT BLOCK+4
         SR    R0,R0               GET A ZERO
         STH   R0,2(,R14)          CLEAR
         ST    R0,OLDCHAIN         ASSUME THIS WILL BE THE LAST OLD
         SPACE 1
         LA    R14,4(,R14)         --> TEXT SINK
         LR    R15,R5              GET L'SINK
         LA    R4,1(,R1)           --> TEXT SOURCE
         MVCL  R14,R4              COPY TEXT TO TSO TEXT BLOCK
         LR    R1,R4               ADVANCE THE SOURCE POINTER
         B     PMSGBUIL            LOOP FOR NEXT MESSAGE (IF ANY)
         DROP  R2                  RELEASE OLD BASE
PMSGPUT  DS    0H
         SPACE 3
*************************************************************
*        CALL PUTLINE TO DISPLAY THE MESSAGE(S)             *
*************************************************************
         SPACE 1
         MVI   DSAECB,0            CLEAR PUTLINE'S ECB
         L     R15,DSAPUTL         --> PUTLINE
         PUTLINE PARM=DSAPTPB,                                         *
               ENTRY=(15),MF=(E,DSAIOPL)
         LTR   R15,R15             AOK?
         BNZ   PMSGRET0            NO, RETURN +0
         SPACE 3
*************************************************************
*        AOK: RETURN +4                                     *
*************************************************************
         SPACE 1
PMSGRET4 LM    R14,R5,SAVPMSG      RESTORE REGISTERS
         B     4(,R14)             RETURN +4
         SPACE 3
*************************************************************
*        ERROR (LIKELY ATTENTION): RETURN +0                *
*************************************************************
         SPACE 1
PMSGRET0 LM    R14,R5,SAVPMSG      RESTORE REGISTERS
         BR    R14                 RETURN +0
         DROP  ,                   RELEASE ALL BASES
         TITLE 'SCHEDULE COMMAND -- STAXEXIT - ATTENTION ROUTINE'
*************************************************************
*                                                           *
*        STAXEXIT -- POSTS DSAECB TO SIGNAL MAINLINE THAT   *
*        AN ATTENTION INTERRUPT HAS BEEN RECEIVED.          *
*                                                           *
*        INPUTS:                                            *
*              - R1 POINTS TO AN AEPL.                      *
*              - R13 POINTS TO A STANDARD REGISTER SAVE     *
*                AREA.                                      *
*              - R14 POINTS TO THE RETURN ADDRESS.          *
*              - R15 POINTS TO THE ENTRY ADDRESS.           *
*                                                           *
*        RETURN 0(,R14):                                    *
*              - DSAECB HAS BEEN POSTED.                    *
*              - R15 IS SET TO RC=0.                        *
*              - ALL OTHER REGISTERS ARE RESTORED.          *
*                                                           *
*************************************************************
         SPACE 1
         USING AEPL,R1             DCL AEPL BASE
STAXEXIT STM   R14,R12,DSAR14-DSA(R13) SAVE REGISTERS
         LR    R12,R15             COPY BASE REG
         USING STAXEXIT,R12        DCL LOCAL BASE
         SPACE 1
         L     R2,AEPLUSER         --> DSA
         USING DSA,R2              DCL DSA BASE
         DROP  R1                  RELEASE AEPL BASE
         ICM   R1,15,SUBTTCBA      DOES A SUBTASK EXIST?
         BZ    NOSUBTCB            NO, SKIP
         STATUS STOP,TCB=SUBTTCBA  YES, HALT IT
NOSUBTCB DS    0H
         SPACE 1
         LA    R0,ECBPOST          GET POST CODE
         SLL   R0,24               SHIFT TO HI-BYTE
         POST  DSAECB,(0)          POST THE ECB
         LM    R14,R12,DSAR14-DSA(R13) RESTORE REGISTERS
         DROP  R2                  RELEASE DSA BASE
         DROP  R12                 RELEASE LOCAL BASE
         SR    R15,R15             SET RC=0
         BR    R14                 RETURN TO CALLER
         DROP  ,                   RELEASE ALL BASES
         TITLE 'SCHEDULE COMMAND -- ESTAHELP - PROTECTION ROUTINE'
*************************************************************
*                                                           *
*        ESTAHELP -- THIS ROUTINE PROTECTS AGAINST ABENDS   *
*        IN THE  ATTACHED HELP COMMAND PROCESSOR.           *
*                                                           *
*************************************************************
         SPACE 1
ESTAHELP LA    R15,12
         CR    R0,R15              SDWA EXIST?
         LA    R15,16              (ASSUME NOT; LOAD RC)
         BER   R14                 NO, RETURN TO RTM
         USING SDWA,R1             YES, DCL SDWA BASE
         SETRP RC=16               PREVENT PERCOLATION
         BR    R14                 RETURN TO RTM
         DROP  ,                   RELEASE ALL BASES
         TITLE 'SCHEDULE COMMAND -- PARSE VALIDITY CHECK EXITS'
*************************************************************
*                                                           *
*        CHKDATE -- VALIDITY CHECKS THE DATE OPERAND. THE   *
*        DATE MUST BE OF THE FORM MM/DD/YY. IF THIS OPERAND *
*        IS BEING PARSED FOR THE SCHEDULE COMMAND, THEN     *
*        DASHES MAY BE SUBSTITUTED FOR MM, DD, AND/OR       *
*        YY. ZERO VALUES ARE STORED WHEN DASHES ARE         *
*        ENCOUNTERED.                                       *
*                                                           *
*************************************************************
         SPACE 1
CHKDATE  BAL   R15,CHKENTRY-CHKDATE(,R15) GOTO ENTRY LINKAGE
         #USING ,                  DCL LOCAL BASES
         USING DSA,R10             DCL DSA BASE
         USING PDE,R9              DCL PDE BASE
         SPACE 3
*************************************************************
*        INSURE THAT A NON-NULL PARM EXISTS.                *
*************************************************************
         SPACE 1
         TM    PDEFLAGS,PDEFEXST   VALUE EXIST?
         BZ    CHKRET4             NO, ERROR
         LH    R15,PDELNGTH        YES, GET ITS LENGTH
         LTR   R15,R15             NON-NULL?
         BNP   CHKRET4             NO, ERROR
         SPACE 3
*************************************************************
*        SCAN THE GIVEN OPERAND. EXTRACT THE MM, DD, AND YY *
*        VALUES.                                            *
*************************************************************
         SPACE 1
         L     R1,PDEPTR           --> OPERAND
         AR    R15,R1              --> EO-VALUE
         BCTR  R15,0               GET BXH LIMIT
         LA    R14,1               GET BXH INCRIMENT
         SPACE 1
         TM    CMDID,CMDFGDSH      DASHES PERMITTED?
         BZ    CDTMO#              NO, DASHES NOT ALLOWED
         CLI   0(R1),C'-'          YES, DASH GIVEN?
         BNE   CDTMO#              NO, GO CHECK FOR #
         SR    R2,R2               YES, GET A ZERO
         BXH   R1,R14,CHKRET4      ADVANCE SCANNER
         CLI   0(R1),C'-'          2ND DASH GIVEN?
         BNE   CDTMODLM            NO, GO CHECK FOR /
         BXH   R1,R14,CHKRET4      YES, ADVANCE SCANNER
         B     CDTMODLM            GO CHECK FOR /
CDTMO#   TM    0(R1),B'11110000'   DIGIT?
         BNO   CHKRET4             NO, ERROR
         IC    R2,0(,R1)           YES, GET IT
         N     R2,=X'0000000F'     ISSOLATE IT
         BXH   R1,R14,CHKRET4      ADVANCE SCANNER
         TM    0(R1),B'11110000'   2ND DIGIT?
         BNO   CDTMOCK#            NO, GO VALIDATE THE MM VALUE
         IC    R0,0(,R1)           YES, GET IT
         N     R0,=X'0000000F'     ISSOLATE IT
         MH    R2,=H'10'           DECIMAL SHIFT THE ACCUMULATER
         AR    R2,R0               ACCUMULATE THE 2ND DIGIT
         BXH   R1,R14,CHKRET4      ADVANCE THE SCANNER
CDTMOCK# LTR   R2,R2               VALID MM GIVEN?
         BNP   CHKRET4             NO, ERROR
         CH    R2,=H'12'           MAYBE, CHECK AGAIN
         BH    CHKRET4             NO, ERROR
CDTMODLM CLI   0(R1),C'/'          YES, DLM OK?
         BNE   CHKRET4             NO, ERROR
         STH   R2,SCHDMO           YES, STORE MM
         SPACE 1
         BXH   R1,R14,CHKRET4      ADVANCE THE SCANNER
         TM    CMDID,CMDFGDSH      DASHES ALLOWED?
         BZ    CDTDY#              NO, DASHES NOT PERMITTED
         CLI   0(R1),C'-'          YES, DASH GIVEN?
         BNE   CDTDY#              NO, GO GET DD VALUE
         SR    R2,R2               YES, GET A ZERO
         BXH   R1,R14,CHKRET4      ADVANCE THE SCANNER
         CLI   0(R1),C'-'          2ND DASH PRESENT?
         BNE   CDTDYDLM            NO, GO CHK FOR /
         BXH   R1,R14,CHKRET4      YES, ADVANCE THE SCANNER
         B     CDTDYDLM            GO CHECK FOR /
CDTDY#   TM    0(R1),B'11110000'   DIGIT PRESENT?
         BNO   CHKRET4             NO, ERROR
         IC    R2,0(,R1)           YES, GET IT
         N     R2,=X'0000000F'     ISSOLATE IT
         BXH   R1,R14,CHKRET4      ADVANCE THE SCANNER
         TM    0(R1),B'11110000'   2ND DIGIT PRESENT?
         BNO   CDTDYCK#            NO, GO VALIDATE THE DD VALUE
         IC    R0,0(,R1)           YES, GET IT
         N     R0,=X'0000000F'     ISSOLATE IT
         MH    R2,=H'10'           DECIMAL SHIFT THE ACCUMULATER
         AR    R2,R0               ACCUMULATE THE 2ND DIGIT
         BXH   R1,R14,CHKRET4      ADVANCE THE SCANNER
CDTDYCK# LTR   R2,R2               VALID DAY VALUE?
         BNP   CHKRET4             NO, ERROR
CDTDYDLM CLI   0(R1),C'/'          MAYBE, VALID DELIMITER?
         BNE   CHKRET4             NO, ERROR
         STH   R2,SCHDDY           YES, STORE THE DD VALUE
         SPACE 1
         BXH   R1,R14,CHKRET4      ADVANCE THE SCANNER
         TM    CMDID,CMDFGDSH      DASHES ALLOWED?
         BZ    CDTYR#              NO, DASH NOT PERMITTED
         CLI   0(R1),C'-'          YES, DASH GIVEN?
         BNE   CDTYR#              NO, GO CHECK FOR YY VALUE
         SR    R2,R2               YES, GET A ZERO
         BXH   R1,R14,CDTYRSTO     ADVANCE THE SCANNER
         CLI   0(R1),C'-'          2ND DASH PRESENT
         BNE   CHKRET4             NO, ERROR
         BXLE  R1,R14,CHKRET4      INSURE EO-VALUE
         B     CDTYRSTO            GO STORE A ZERO YY
CDTYR#   TM    0(R1),B'11110000'   DIGIT PRESENT?
         BNO   CHKRET4             NO, ERROR
         IC    R2,0(,R1)           YES, GET IT
         N     R2,=X'0000000F'     ISSOLATE IT
         BXH   R1,R14,CDTYRCK#     ADVANCE THE SCANNER
         TM    0(R1),B'11110000'   IS 2ND CHAR A DIGIT?
         BNO   CHKRET4             NO, ERROR
         IC    R0,0(,R1)           YES, GET IT
         N     R0,=X'0000000F'     ISSOLATE IT
         MH    R2,=H'10'           DECIMAL SHIFT THE ACCUMULATER
         AR    R2,R0               ACCUMULATE THE 2ND DIGIT
         BXLE  R1,R14,CHKRET4      INSURE EO-VALUE
CDTYRCK# LA    R2,1900(,R2)        ASSUME 20TH CENTURY
         CH    R2,=H'1950'         LOW HALF OF CENTURY?
         BH    CDTYRSTO            NO, PROCEED
         LA    R2,100(,R2)         YES, ASSUME 21ST CENTURY WANTED
CDTYRSTO STH   R2,SCHDYR           STORE YEAR VALUE
         SPACE 3
*************************************************************
*        INSURE THAT THE GIVEN DD VALUE IS NOT TOO LARGE.   *
*************************************************************
         SPACE 1
         LH    R2,SCHDMO           GET MM
         AR    R2,R2               CNVRT TO HWORD OFFSET
         LA    R1,=Y(31,31,28,31,30,31,30,31,31,30,31,30,31)
         LH    R1,0(R2,R1)         GET SIZE OF THE GIVEN MM
         CLI   SCHDMO+1,2          FEBRUARY?
         BNE   CDTNLPMO            NO, PROCEED
         TM    SCHDYR+1,B'00000011' YES, LEAPYEAR?
         BNZ   CDTNLPMO            NO, PROCEED
         LA    R1,29               YES, ADJUST MONTH SIZE
CDTNLPMO CH    R1,SCHDDY           IS DD VALUE VALID?
         BL    CHKRET4             NO, ERROR
         SPACE 3
*************************************************************
*        IF THIS OPERAND IS FOR THE "SCHEDULE" OR "CHANGE"  *
*        SUBCOMMAND, THEN THE GIVEN DATE CANNOT BE IN THE   *
*        PAST.                                              *
*************************************************************
         SPACE 1
         TM    CMDID,CMDFGDSH      DASHES PERMITTED?
         BZ    CHKRET0             NO, DONE HERE
         SR    R1,R1               YES, CLEAR
         ICM   R1,3,SCHDYR         GET YY; EXPLICIT?
         BZ    CHKRET0             NO, DONE HERE
         CH    R1,NOWYR            YES, WHEN?
         BL    CHKRET4             PAST, ERROR
         BH    CHKRET0             FUTURE, DONE HERE
         ICM   R1,3,SCHDMO         PRESENT; GET MM; EXPLICIT?
         BZ    CHKRET0             NO, DONE HERE
         CH    R1,NOWMO            YES, WHEN?
         BL    CHKRET4             PAST, ERROR
         BH    CHKRET0             FUTURE, DONE HERE
         ICM   R1,3,SCHDDY         PRESENT, GET DD; EXPLICIT?
         BZ    CHKRET0             NO, DONE HERE
         CH    R1,NOWDY            YES, WHEN?
         BL    CHKRET4             PAST, ERROR
         B     CHKRET0             PRESENT OR FUTURE; DONE HERE
         DROP  ,                   RELEASE ALL BASES
         EJECT ,
*************************************************************
*                                                           *
*        CHKSYSID -- VALIDITY CHECKS THE "SYSID" OPERAND.   *
*                                                           *
*        INPUTS:                                            *
*              - R9 POINTS TO A PDE.                        *
*              - R10 POINTS TO THE DSA.                     *
*                                                           *
*************************************************************
         SPACE 1
CHKSYSID BAL   R15,CHKENTRY-CHKSYSID(,R15) GOTO ENTRY LINKAGE
         #USING ,                  DCL BASE REGS
         USING DSA,R10             DCL DSA BASE
         USING PDE,R9              DCL PDE BASE
         SPACE 1
         TM    PDEFLAGS,PDEFEXST   IS PARAM PRESENT?
         BZ    CHKRET4             NO, REJECT
         LH    R15,PDELNGTH        YES, GET ITS LENGTH
         LTR   R15,R15             NULL?
         BNP   CHKRET4             YES, REJECT
         SPACE 3
*************************************************************
*        RESTRICT THE VALUE "ALL" TO ONLY THE "DISPLAY" AND *
*        "DELETE" COMMANDS.                                 *
*************************************************************
         SPACE 1
         TM    CMDID,CMDFGSID      IS "ALL" PERMITTED?
         BNZ   CHKRET0             YES, RETURN 0
         SPACE 1
         L     R14,PDEPTR          NO, --> TEXT
         ICM   R15,8,=C' '         LOAD PAD CHARACTER
         LA    R0,DSAWORK          --> COPY SINK
         LA    R1,L'SFRSYSID       GET L'COPY SINK
         #TEST SIZE=(L'DSAWORK,GE,L'SFRSYSID)
         MVCL  R0,R14              COPY AND PADD THE TEXT
         SPACE 1
         CLC   =CL(L'SFRSYSID)'ALL',DSAWORK "ALL" SPECIFIED?
         BE    CHKRET4             YES, RETURN +4
         B     CHKRET0             NO, RETURN +0
         DROP  ,                   RELEASE ALL BASES
         EJECT ,
*************************************************************
*                                                           *
*        CHKWEEKD -- VALIDITY CHECKS THE WEEKDAY OPERAND.   *
*        NOTE, IF A * IS GIVEN, THEN 0 IS SAVED.            *
*                                                           *
*        INPUTS:                                            *
*              - R9 POINTS TO A PDE.                        *
*              - R10 POINTS TO THE DSA.                     *
*                                                           *
*************************************************************
         SPACE 1
CHKWEEKD BAL   R15,CHKENTRY-CHKWEEKD(,R15) GOTO ENTRY LINKAGE
         #USING ,                  DCL BASE REGS
         USING DSA,R10             DCL DSA BASE
         USING PDE,R9              DCL PDE BASE
         SPACE 1
         TM    PDEFLAGS,PDEFEXST   IS PARAM PRESENT?
         BZ    CHKRET4             NO, REJECT
         LH    R15,PDELNGTH        YES, GET ITS LENGTH
         LTR   R15,R15             NULL?
         BNP   CHKRET4             YES, REJECT
         L     R1,PDEPTR           NO, --> PARAM
         BCTR  R15,0               ADJUST LENGTH
         LA    R14,WEEKDAYS-WDYROOTL-1 --> WEEKDAYS LIST
         SR    R2,R2               CLEAR L'NAME
         SR    R3,R3               CLEAR HIT SIGNAL
CKWLP    LA    R14,WDYROOTL+1(R2,R14) --> NEXT ENTRY
         ICM   R2,1,WDYLNGTH(R14)  GET L'NAME; EOL?
         BM    CKWLPEND            YES, SEE IF SOMETHING FOUND
         CR    R15,R2              NO, IS ENTRY LONG ENOUGH?
         BH    CKWLP               NO, TRY NEXT ENTRY
         CLC   0(*-*,R1),WDYNAME(R14) (EXECUTED)
         EX    R15,*-6             YES, MATCH?
         BH    CKWLP               NO, TRY NEXT ENTRY
         BL    CKWLPEND            NO, SEE IF SOMETHING FOUND
         CR    R15,R2              YES, EXACT MATCH?
         BNE   CKWGNRIC            NO, PROCEED
         LR    R3,R14              YES, COPY WDY ENTRY POINTER
         B     CKWLPEND            TERMINATE THE LOOP
CKWGNRIC LTR   R3,R3               SOMETHING FOUND PREVIOUSLY?
         BNZ   CHKRET4             YES, ERROR
         LR    R3,R14              NO, REMEMBER HIT
         B     CKWLP               CONTINUE
CKWLPEND LTR   R3,R3               ANYTHING FOUND?
         BZ    CHKRET4             NO, ERROR
         ICM   R2,8,WDYNUMBR(R3)   YES, GET WEEKDAY'S NUMBER
         SRA   R2,24               SHIFT AND PROPAGATE THE SIGN
         STH   R2,SCHDWK           SAVE
         B     CHKRET0             DONE HERE
         DROP  ,                   RELEASE ALL BASES
         EJECT ,
*************************************************************
*                                                           *
*        CHKTIME -- VALIDITY CHECK THE TIME OPERAND. IT IS  *
*        IN THE FORM HH:MM. MM MUST FALL WITHIN THE RANGE   *
*        OF 00 - 59. HH MUST FALL WITH THE RANGE OF 00 -    *
*        23.                                                *
*                                                           *
*************************************************************
         SPACE 1
CHKTIME  BAL   R15,CHKENTRY-CHKTIME(,R15) GOTO ENTRY LINKAGE
         #USING ,                  DCL BASE REGS
         USING DSA,R10             DCL DSA BASE
         USING PDE,R9              DCL PDE BASE
         SPACE 1
         TM    PDEFLAGS,PDEFEXST   IS PARAM PRESENT?
         BZ    CHKRET4             NO, REJECT
         LH    R15,PDELNGTH        YES, GET ITS LENGTH
         LTR   R15,R15             NULL?
         BNP   CHKRET4             YES, REJECT
         SPACE 3
*************************************************************
*        SCAN THE GIVEN OPERAND. EXTRACT THE HH AND MM      *
*        VALUES.                                            *
*************************************************************
         SPACE 1
         L     R1,PDEPTR           --> OPERAND
         AR    R15,R1              --> EO-OPERAND
         BCTR  R15,0               GET MXH LIMIT
         LA    R14,1               GET BXH INCRIMENT
         SPACE 1
         TM    0(R1),B'11110000'   DIGIT PRESENT?
         BNO   CHKRET4             NO, ERROR
         IC    R2,0(,R1)           YES, GET IT
         N     R2,=X'0000000F'     ISSOLATE IT
         BXH   R1,R14,CHKRET4      ADVANCE THE SCANNER
         TM    0(R1),B'11110000'   2ND DIGIT?
         BNO   CTMDLM              NO, GO CHECK FOR A :
         IC    R0,0(,R1)           YES, GET IT
         N     R0,=X'0000000F'     ISSOLATE IT
         MH    R2,=H'10'           DECIMAL SHIFT THE ACCUMULATER
         AR    R2,R0               ACCUMULATE THE 2ND DIGIT
         BXH   R1,R14,CHKRET4      ADVANCE THE SCANNER
CTMDLM   CLI   0(R1),C':'          DELIMITER OK?
         BNE   CHKRET4             NO, ERROR
         CH    R2,=H'23'           YES, VALUE OK?
         BH    CHKRET4             NO, ERROR
         STH   R2,SCHDHR           YES, STORE HH
         SPACE 1
         BXH   R1,R14,CHKRET4      ADVANCE THE SCANNER
         TM    0(R1),B'11110000'   1ST MM DIGIT PRESENT?
         BNO   CHKRET4             NO, ERROR
         IC    R2,0(,R1)           YES, GET IT
         N     R2,=X'0000000F'     ISSOLATE IT
         MH    R2,=H'10'           DECIMAL SHIFT IT
         BXH   R1,R14,CHKRET4      ADVANCE THE SCANNER
         TM    0(R1),B'11110000'   2ND MM DIGIT PRESENT?
         BNO   CHKRET4             NO, ERROR
         IC    R0,0(,R1)           YES, GET IT
         N     R0,=X'0000000F'     ISSOLATE IT
         AR    R2,R0               ACCUMULATE IT
         BXLE  R1,R14,CHKRET4      INSURE EO-VALUE
         CH    R2,=H'59'           IS MM VALUE VALID?
         BH    CHKRET4             NO, ERROR
         STH   R2,SCHDMN           YES, SAVE IT
         B     CHKRET0             NO, DONE HERE
         DROP  ,                   RELEASE ALL BASES
         EJECT ,
*************************************************************
*                                                           *
*        CHKWINDO -- VALITITY CHECK THE WINDOW OPERAND. IT  *
*        IS IN THE FORM HH:MM. MM MUST FALL WITHIN THE      *
*        RANGE OF 00 - 59. HH MUST FALL WITHIN THE RANGE    *
*        OF 0 - 24. IF HH IS 24, THEN MM MUST BE 00.        *
*                                                           *
*************************************************************
         SPACE 1
CHKWINDO BAL   R15,CHKENTRY-CHKWINDO(,R15) GOTO ENTRY LINKAGE
         #USING ,                  DCL BASE REGS
         USING DSA,R10             DCL DSA BASE
         USING PDE,R9              DCL PDE BASE
         SPACE 1
         TM    PDEFLAGS,PDEFEXST   IS PARAM PRESENT?
         BZ    CHKRET4             NO, REJECT
         LH    R15,PDELNGTH        YES, GET ITS LENGTH
         LTR   R15,R15             NULL?
         BNP   CHKRET4             YES, REJECT
         SPACE 3
*************************************************************
*        SCAN THE GIVEN OPERAND. EXTRACT THE HHHH AND MM    *
*        VALUES.                                            *
*************************************************************
         SPACE 1
         L     R1,PDEPTR           NO, --> PARAM
         TM    0(R1),B'11110000'   IS 1ST CHAR A DIGIT?
         BNO   CHKRET4             NO, ERROR
         BCTR  R1,0                YES, BACK UP
         AR    R15,R1              GET BXH LIMIT
         LA    R14,1               GET BXH INCRIMENT
         SR    R2,R2               CLEAR THE HHHH ACCUMULATER
CWIHHLP  BXH   R1,R14,CHKRET4      ADVANCE SCANNER
         TM    0(R1),B'11110000'   DIGIT?
         BNO   CWIDLM              NO, GO CHECK FOR :
         IC    R0,0(,R1)           YES, GET IT
         N     R0,=X'0000000F'     ISSOLATE IT
         MH    R2,=H'10'           DECIMAL SHIFT THE ACCUMULATER
         AR    R2,R0               ACCUMULATE THE NEW DIGIT
         CH    R2,=H'24'           HH TOO LARGE?
         BH    CHKRET4             YES, ERROR
         B     CWIHHLP             NO, LOOP FOR NEXT DIGIT
CWIDLM   CLI   0(R1),C':'          IS DLM VALID?
         BNE   CHKRET4             NO, ERROR
         STH   R2,WNDOHR           YES, SAVE HHHH
         SPACE 1
         BXH   R1,R14,CHKRET4      ADVANCE THE SCANNER
         TM    0(R1),B'11110000'   DIGIT PRESENT?
         BNO   CHKRET4             NO, ERROR
         IC    R2,0(,R1)           YES, GET IT
         N     R2,=X'0000000F'     ISSOLATE IT
         MH    R2,=H'10'           DECIMAL SHIFT IT
         BXH   R1,R14,CHKRET4      ADVANCE THE SCANNER
         TM    0(R1),B'11110000'   2ND MM DIGIT PRESENT?
         BNO   CHKRET4             NO, ERROR
         IC    R0,0(,R1)           YES, GET IT
         N     R0,=X'0000000F'     ISSOLATE IT
         AR    R2,R0               ACCUMULATE IT
         BXLE  R1,R14,CHKRET4      INSURE EO-VALUE
         CH    R2,=H'59'           IS THE VALUE VALID?
         BH    CHKRET4             NO, ERROR
         STH   R2,WNDOMN           YES, SAVE
         LA    R1,24               GET MAX HH TEST VALUE
         CH    R1,WNDOHR           WINDOW AT MAX VALUE?
         BL    CHKRET4             OVER; ERROR RETURN
         BH    CHKRET0             NO, UNDER; RETURN AOK
         LTR   R2,R2               YES, IS MM ZERO?
         BNZ   CHKRET4             NO, ERROR
         B     CHKRET0             YES, RETURN AOK
         DROP  ,                   RELEASE ALL BASES
         EJECT ,
*************************************************************
*                                                           *
*        CHKID -- VALIDITY CHECK THE ID OPERANDS. THEY ARE  *
*        IN THE FORM OF ASCENDING PAIRS OF DECIMAL NUMBERS  *
*        THAT MUST FALL WITHIN THE RANGE OF 0 TO 65535.     *
*                                                           *
*************************************************************
         SPACE 1
CHKID    BAL   R15,CHKENTRY-CHKID(,R15) GOTO ENTRY LINKAGE
         #USING ,                  DCL BASE REGS
         USING DSA,R10             DCL DSA BASE
         USING PDE,R9              DCL PDE BASE
         SPACE 1
         TM    PDEFLAGS,PDEFEXST   IS PARAM PRESENT?
         BZ    CHKRET4             NO, REJECT
         ICM   R1,7,PDEPTR+1       --> 1ST NUMBER; EXIST?
         BZ    CHKRET4             NO, ERROR
         CLC   0(4,R1),=X'0000FFFF' YES, VALID VALUE?
         BH    CHKRET4             NO, ERROR
         ICM   R15,7,PDEPTR2+1     YES, --> 2ND NUMBER; EXIST?
         BZ    CHKRET0             NO, DONE HERE
         CLC   0(4,R15),=X'0000FFFF' YES, VALID VALUE?
         BH    CHKRET4             NO, ERROR
         CLC   0(4,R1),0(R15)      YES, ASCENDING RANGE?
         BH    CHKRET4             NO, ERROR
         B     CHKRET0             OK; DONE HERE
         DROP  ,                   RELEASE ALL BASES
         TITLE 'SCHEDULE COMMAND -- PARSE VALIDITY CHECK EXITS - COMMON*
                ENTRY CODE'
*************************************************************
*                                                           *
*        CHKENTRY -- PROVIDE ENTRY LINKAGE COMMON TO ALL    *
*        PARSE VALIDITY CHECK ROUTINES.                     *
*                                                           *
*        INPUTS:                                            *
*              - R1 POINTS TO THE VALIDITY CHECK PARAMETER  *
*                LIST.                                      *
*              - R1%+8 CONTAINS X'FF000000'.                *
*              - R13 POINTS TO A STANDARD REGISTER SAVE     *
*                AREA.                                      *
*              - R14 POINTS TO A RETURN ADDRESS IN IKJPARS. *
*              - R15 POINTS TO THE PARTICULER VALIDITY      *
*                CHECK ROUTINE THAT WAS CALLED BY IKJPARS.  *
*                                                           *
*        EXIT 0(,R15):                                      *
*              - A STANDARD REGISTER SAVE HAS BEEN OBTAINED *
*                FOR LOCAL USE AND IS CHAINED TO THE HIGHER *
*                SAVE AREA.                                 *
*              - ALL OF IKJPARS'S REGISTERS HAVE BEEN SAVED *
*                IN THE HIGHER SAVE AREA.                   *
*                                                           *
*************************************************************
         SPACE 1
         USING VCPL,R1             VCPL BASE
CHKENTRY ST    R15,VALMSG          TEMP SAVE
         BALR  R15,0               TEMP LOCAL BASE
TEMPBASE #ENTER ESDTYPE=NONE,BASES=*,SAVTYPE=RENT ENTRY LINKAGE
         LM    R9,R10,PDEADR       --> PDE AND DSA
         USING DSA,R10             DCL DSA BASE
         L     R15,VALMSG          RESTORE A(VALIDCK ROUTINE)
         MVC   VALMSG,=X'FF000000' RESTORE PREVIOUS CONTENTS
         BR    R15                 ENTER THE VALIDITY CHECK ROUTINE
         DROP  ,                   RELEASE ALL BASES
         TITLE 'SCHEDULE COMMAND -- PARSE VALIDITY CHECK EXITS - COMMON*
                EXIT CODE'
*************************************************************
*                                                           *
*        CHKRET# -- COMMON EXIT CODE FOR THE VARIOUS        *
*        VALIDITY CHECK ROUTINES CALLED BY IKJPARS.         *
*                                                           *
*        INPUTS:                                            *
*              - R13 POINTS TO THE LOCAL STANDARD REGISTER  *
*                SAVE AREA.                                 *
*                                                           *
*        EXIT R13%+4%+C% (THE CALLER'S R14):                *
*              - R15 IS LOADED WITH A RETURN CODE.          *
*              - ALL OTHER REGISTERS ARE RESTORED FROM THE  *
*                HIGHER SAVE AREA.                          *
*              - THE LOCAL SAVE AREA IS DECHAINED AND       *
*                RELEASED.                                  *
*              - CONTROL IS RETURNED TO IKJPARS.            *
*                                                           *
*************************************************************
         SPACE 1
         #USING ,                  REDECLARE LOCAL BASES
CHKRET8  BAL   R0,CHKRET           RC=8
CHKRET4  BAL   R0,CHKRET           RC=4
CHKRET0  BAL   R0,CHKRET           RC=0
CHKRET   LA    R15,CHKRET          LOAD RC BASE
         SR    R15,R0              GET RC
         LA    R15,0(,R15)         PURIFY
         #EXIT ((R14,R12)),RC=(R15) RETURN TO IKJPARS
         DROP  ,                   RELREASE ALL BASES
         TITLE 'SCHEDULE COMMAND -- STATIC DATA'
DSADATA  DS    0F                  DSNAME ALLOCATION TEXT KEYS
DSADDNAM DC    Y(DALDDNAM,1,L'DDNAME)
DDNAME   DC    CL8'SCHEDULE'
DSADSNAM DC    Y(DALDSNAM,1,L'DSNAME)
DSNAME   DC    C'SYSVSAM.SCHEDULE'
DSASTATS DC    Y(DALSTATS,1,1),X'08'
DSADATAL EQU   *-DSADATA
DSACOUNT EQU   3
         SPACE 3
MNUDATA  DS    0F                  MARK-NOT-IN-USE TEXT KEYS
MNUCURNT DC    Y(DRICURNT,0)
MNUDATAL EQU   *-MNUDATA
MNUCOUNT EQU   1
         SPACE 3
COMMANDS DS    0C
         DC    AL3(CHANGCMD),AL1(CMDIDCHA,5),C'CHANGE'
         DC    AL3(DISPDELE),AL1(CMDIDDEL,5),C'DELETE'
         DC    AL3(DISPDELE),AL1(CMDIDDIS,6),C'DISPLAY'
         DC    AL3(EXIT0),AL1(CMDIDEND,2),C'END'
         DC    AL3(HELPCMD),AL1(CMDIDHEL,3),C'HELP'
CMDSCHED DC    AL3(SCHEDCMD),AL1(CMDIDSCH,7),C'SCHEDULE'
         DC    AL3(INVALCMD),AL1(CMDIDINV,7),8X'FF' DELIMITER
         SPACE 1
CMDHNDLR EQU   0,3                 --> COMMAND HANDLER
CMDIDENT EQU   CMDHNDLR+L'CMDHNDLR,1 COMMAND ID NUMBER
CMDLNGTH EQU   CMDIDENT+L'CMDIDENT,1 L'COMMAND NAME - 1
CMDNAME  EQU   CMDLNGTH+L'CMDLNGTH COMMAND NAME
CMDROOTL EQU   CMDNAME             LIST ENTRY ROOT LENGTH
         SPACE 3
WEEKDAYS DS    0C
         DC    AL1(0,0),C'-'
         DC    AL1(6,-6),C'-FRIDAY'
         DC    AL1(6,-2),C'-MONDAY'
         DC    AL1(8,-7),C'-SATURDAY'
         DC    AL1(6,-1),C'-SUNDAY'
         DC    AL1(8,-5),C'-THURSDAY'
         DC    AL1(7,-3),C'-TUESDAY'
         DC    AL1(9,-4),C'-WEDNESDAY'
         DC    AL1(2,0),C'ANY'
         DC    AL1(5,6),C'FRIDAY'
         DC    AL1(5,2),C'MONDAY'
         DC    AL1(7,7),C'SATURDAY'
         DC    AL1(5,1),C'SUNDAY'
         DC    AL1(7,5),C'THURSDAY'
         DC    AL1(6,3),C'TUESDAY'
         DC    AL1(8,4),C'WEDNESDAY'
         DC    B'10000000'         DELIMITER
         SPACE 1
WDYLNGTH EQU   0,1                 L'NAME - 1
WDYNUMBR EQU   WDYLNGTH+L'WDYLNGTH,1 WEEKDAY'S SEQUENCE # (1-ORIGIN)
WDYNAME  EQU   WDYNUMBR+L'WDYNUMBR WEEKDAY'S NAME
WDYROOTL EQU   WDYNAME             LIST ENTRY ROOT LENGTH
         SPACE 3
MASTER   SFR   MF=(L,M)            SCHEDULE FILE'S MASTER RECORD
         SPACE 3
         DC    Y(L'SCHEDRUN+4,0)
SCHEDRUN DC    C'S SCHEDRUN '
         SPACE 3
         DC    A(1,MODEMSG-4)
         DC    Y(L'MODEMSG+4,0)
MODEMSG  DC    C' SCHED'
         SPACE 3
DELASKO1 DC    A(DELASKO2,1,DELASKM1-4)
         DC    Y(L'DELASKM1+4,0)
DELASKM1 DC    C' DELETE?+'
         SPACE 1
DELASKO2 DC    A(0,1,DELASKM2-4)
         DC    Y(L'DELASKM2+4,0)
DELASKM2 DC    C' REPLY "Y" TO DELETE. REPLY "N" TO KEEP. REPLY ANYTHIN*
               G ELSE TO REDISPLAY.'
         SPACE 3
         SPACE 3
         LTORG ,
         TITLE 'SCHEDULE COMMAND -- STATIC DATA - MESSAGES'
         DC    AL1(L'MDELOPRN)
MDELOPRN DC    C' AT LEAST SOME SELECTION CRITERIA OPERANDS ARE REQUIRE*
               D'
         DC    FL1'0'
         SPACE 3
         DC    AL1(L'MFILEMPT)
MFILEMPT DC    C' THE SCHEDULE FILE IS EMPTY'
         DC    FL1'0'
         SPACE 3
         DC    AL1(L'MSYNTERR)
MSYNTERR DC    C' INVALID COMMAND NAME SYNTAX - TYPE HELP IF YOU NEED H*
               ELP'
         DC    FL1'0'
         SPACE 3
         DC    AL1(L'MWNDOADJ)
MWNDOADJ DC    C' WARNING - WINDOW VALUE ADJUSTED TO END AT MIDNIGHT'
         DC    FL1'0'
         SPACE 3
         DC    AL1(L'MWNDOAD2)
MWNDOAD2 DC    C' WARNING - ONE OR MORE WINDOW VALUES WERE ADJUSTED TO *
               END AT MIDIGHT'
         DC    FL1'0'
         SPACE 3
         DC    AL1(L'MAMBGERR)
MAMBGERR DC    C' AMBIGUOUS COMMAND NAME'
         DC    FL1'0'
         SPACE 3
         DC    AL1(L'MUNREERR)
MUNREERR DC    C' UNRECOGNIZED COMMAND NAME - TYPE HELP IF YOU NEED HEL*
               P'
         DC    FL1'0'
         SPACE 3
         DC    AL1(L'MDISPERR)
MDISPERR DC    C' NO SCHEDULED COMMANDS FIT THE GIVEN SELECTION CRITERI*
               A'
         DC    FL1'0'
         SPACE 3
         DC    AL1(MDISPTIL)
MDISPTIT DC    C' ID   SYSID DATE      DAY TIME    WINDOW FLAGS '
         DC    CL(L'SFRNAME)'NAME',C'  COMMAND'
MDISPTIL EQU   *-MDISPTIT
         DC    FL1'0'
         TITLE 'SCHEDULE COMMAND -- PASSGARD - SECURITY CONTROL ESTAE'
*************************************************************
*                                                           *
*        PASSGARD -- IF "SCHEDULE" ABENDS, THEN THIS ESTAE  *
*        ROUTINE TURNS OFF THE "JSCBPASS" FLAG SO AS TO     *
*        CLOSE THE POSSIBILITY OF THE CALLER BEING GIVEN    *
*        "BYPASS PASSWORDS" AUTHORITY.                      *
*                                                           *
*        THIS ESTAE IS IN EFFECT ONLY WHEN NEEDED.          *
*                                                           *
*        INPUTS:                                            *
*              - R15 POINTS TO THE ENTRY LOCATION.          *
*              - R14 POINTS TO A RETURN ADDRESS.            *
*                                                           *
*        RETURN 0(,R14):                                    *
*              - THE "JSCBPASS" FLAG IS TURNED OFF.         *
*              - R15 IS SET TO 0 (SIGNALLING                *
*                "PERCOLATION").                            *
*              - NO OTHER REGISTERS ARE RESTORED.           *
*                                                           *
*************************************************************
         SPACE 1
PASSGARD LR    R12,R15             LOAD LOCAL BASE
         USING PASSGARD,R12        DECLARE IT
         SPACE 1
         L     R2,PSATOLD-PSA      --> MY TCB
         L     R2,TCBJSCB-TCB(,R2) --> MY JSCB
         L     R2,JSCBACT-IEZJSCB(,R2) --> THE ACTIVE JSCB
         MODESET KEY=ZERO          LOAD KEY-ZERO
         NI    JSCBSWT1-IEZJSCB(R2),255-JSCBPASS CLEAR THE FLAG
         SPACE 1
         SR    R15,R15             LOAD THE RETURN CODE
         BR    R14                 RETURN TO THE RTM
         DROP  ,                   RELEASE ALL BASES
         TITLE 'SCHEDULE COMMAND -- PARSE CONTROL LISTS'
*************************************************************
*                                                           *
*        PCL AND PDL FOR SCHEDULE SUBCOMMAND                *
*                                                           *
*************************************************************
         SPACE 1
         PRINT NOGEN
PCLSCHED IKJPARM DSECT=PDLSCHED
         SPACE 1
PDLCMD   IKJPOSIT QSTRING,UPPERCASE,                                   *
               PROMPT='THE COMMAND TO BE SCHEDULED, ENCLOSED WITHIN SIN*
               GLE QUOTES.'
         SPACE 1
KEYNAME  IKJKEYWD DEFAULT='NAME'
         IKJNAME 'NAME',SUBFLD=PCLNAME
         SPACE 1
KEYDATE  IKJKEYWD DEFAULT='DATE'
         IKJNAME 'DATE',SUBFLD=PCLDATE
         SPACE 1
KEYTIME  IKJKEYWD DEFAULT='TIME'
         IKJNAME 'TIME',SUBFLD=PCLTIME
         SPACE 1
KEYWEEKD IKJKEYWD DEFAULT='WEEKDAY(-)'
         IKJNAME 'WEEKDAY',SUBFLD=PCLWEEKD
         SPACE 1
KEYWINDO IKJKEYWD DEFAULT='WINDOW(0:00)'
         IKJNAME 'WINDOW',SUBFLD=PCLWINDO
         SPACE 1
KEYSYSID IKJKEYWD DEFAULT='SYSID(CRNT)'
         IKJNAME 'SYSID',                                              *
               ALIAS='SYSTEM',                                         *
               SUBFLD=PCLSYSID
         SPACE 1
KEYIPLFO IKJKEYWD DEFAULT='NOTIPLFORCED'
         IKJNAME 'IPLFORCED',SUBFLD=PCLIPLFO
         IKJNAME 'NOTIPLFORCED'
IPLFORCE EQU   1
NOTIPLFO EQU   2
         SPACE 1
KEYOVERR IKJKEYWD DEFAULT='NOOVERRIDE'
         IKJNAME 'OVERRIDE',SUBFLD=PCLOVERR
         IKJNAME 'NOOVERRIDE'
OVERRIDE EQU   1
NOOVERRI EQU   2
         SPACE 1
PCLNAME  IKJSUBF
PDLNAME  IKJIDENT 'APPLICATION NAME',                                  *
               UPPERCASE,MAXLNTH=8,FIRST=ALPHA,OTHER=ALPHANUM,         *
               PROMPT='THE NAME OF THE APPLICATION TO WHICH THE COMMAND*
                IS ASSOCIATED.'
         #TEST SIZE=(L'SFRNAME,EQ,8)
         SPACE 1
PCLDATE  IKJSUBF
PDLDATE  IKJIDENT 'DATE OF EXECUTION',                                 *
               MAXLNTH=8,FIRST=ANY,OTHER=ANY,VALIDCK=CHKDATE,          *
               PROMPT='THE MONTH, DAY, AND YEAR (MM/DD/YY) OF EXECUTION*
               .+',                                                    *
               HELP='- FOR MM, DD, AND/OR YY IF THE COMMAND IS TO BE EX*
               ECUTED ON EVERY MONTH, DAY, AND/OR YEAR.'
         SPACE 1
PCLTIME  IKJSUBF
PDLTIME  IKJIDENT 'TIME OF EXECUTION',                                 *
               MAXLNTH=5,FIRST=ANY,OTHER=ANY,VALIDCK=CHKTIME,          *
               PROMPT='THE HOUR AND MINUTE (HH:MM) OF EXECUTION.'
         SPACE 1
PCLWEEKD IKJSUBF
PDLWEEKD IKJIDENT 'WEEKDAY OF EXECUTION',                              *
               MAXLNTH=9,FIRST=ANY,OTHER=ANY,                          *
               VALIDCK=CHKWEEKD,                                       *
               PROMPT='THE NAME OF THE WEEKDAY ON WHICH THE COMMAND IS *
               TO BE EXECUTED OR -.+',                                 *
               HELP='"ANY" IF THE COMMAND CAN BE EXECUTED ON ANY WEEKDA*
               Y.'
         SPACE 1
PCLWINDO IKJSUBF
PDLWINDO IKJIDENT 'EXECUTION WINDOW',                                  *
               MAXLNTH=5,FIRST=ANY,OTHER=ANY,VALIDCK=CHKWINDO,         *
               PROMPT='THE SIZE OF THE EXECUTION WINDOW FOR THIS COMMAN*
               D.+',                                                   *
               HELP='THE LENGTH OF TIME (HH:MM) FOLLOWING THE SCHEDULED*
                EXECUTION TIME DURING WHICH THE COMMAND MIGHT STILL BE *
               EXECUTED IF THE SCHEDULED TIME WERE MISSED. THE WINDOW C*
               ANNOT EXTEND PAST MIDNIGHT. "0:00" MEANS "UNTIL MIDNIGHT*
               ".'
         SPACE 1
PCLSYSID IKJSUBF
PDLSYSID IKJIDENT 'SYSTEM IDENTIFICATION',                             *
               MAXLNTH=4,FIRST=ALPHANUM,OTHER=ALPHANUM,                *
               VALIDCK=CHKSYSID,                                       *
               PROMPT='THE NAME OF THE SYSTEM ON WHICH THE COMMAND MAY *
               BE EXECUTED.+',                                         *
               HELP='"CRNT" FOR THE SYSTEM WHICH YOU ARE LOGGED ON TO, *
               AND "ANY" IF THE COMMAND MAY BE EXECUTED ON ANY AVAILABL*
               E SYSTEM.'
         #TEST SIZE=(L'SFRSYSID,EQ,4)
         SPACE 1
PCLIPLFO IKJSUBF
SUBIPLFO IKJKEYWD DEFAULT='YES'
         IKJNAME 'YES'
         IKJNAME 'NO'
YES      EQU   1
NO       EQU   2
         SPACE 1
PCLOVERR IKJSUBF
SUBOVERR IKJKEYWD DEFAULT='YES'
         IKJNAME 'YES'
         IKJNAME 'NO'
*YES     EQU   1
*NO      EQU   2
         SPACE 1
         IKJENDP
         PRINT ON,GEN,NODATA
         EJECT ,
*************************************************************
*                                                           *
*        PCL AND PDL FOR THE CHANGE COMMAND                 *
*                                                           *
*************************************************************
         SPACE 1
         PRINT NOGEN
PCLCHANG IKJPARM DSECT=PDLCHANG
         SPACE 1
CHID     IKJKEYWD DEFAULT='ID'
         IKJNAME 'ID',SUBFLD=PCLCHID
         SPACE 1
CHCOMMAN IKJKEYWD
         IKJNAME 'COMMAND',SUBFLD=PCLCHCOM,                            *
               ALIAS='CMD'
         SPACE 1
CHNAME   IKJKEYWD
         IKJNAME 'NAME',SUBFLD=PCLCHNAM
         SPACE 1
CHDATE   IKJKEYWD
         IKJNAME 'DATE',SUBFLD=PCLCHDAT
         SPACE 1
CHTIME   IKJKEYWD
         IKJNAME 'TIME',SUBFLD=PCLCHTIM
         SPACE 1
CHWEEKDA IKJKEYWD
         IKJNAME 'WEEKDAY',SUBFLD=PCLCHWEE
         SPACE 1
CHWINDOW IKJKEYWD
         IKJNAME 'WINDOW',SUBFLD=PCLCHWIN
         SPACE 1
CHSYSID  IKJKEYWD
         IKJNAME 'SYSID',                                              *
               ALIAS='SYSTEM',                                         *
               SUBFLD=PCLCHSYS
         SPACE 1
CHIPLFOR IKJKEYWD
         IKJNAME 'IPLFORCED',SUBFLD=PCLCHIPL
         IKJNAME 'NOTIPLFORCED'
*IPLFORCE EQU  1
*NOTIPLFO EQU  2
         SPACE 1
CHOVERRI IKJKEYWD
         IKJNAME 'OVERRIDE',SUBFLD=PCLCHOVE
         IKJNAME 'NOOVERRIDE'
*OVERRIDE EQU  1
*NOOVERRI EQU  2
         SPACE 1
CHCOPY   IKJKEYWD DEFAULT='NOCOPY'
         IKJNAME 'COPY'
         IKJNAME 'NOCOPY'
COPY     EQU   1
NOCOPY   EQU   2
         SPACE 1
PCLCHID  IKJSUBF
PDLCHID  IKJIDENT 'IDENTIFICATION NUMBER',                             *
               LIST,RANGE,INTEG,VALIDCK=CHKID,                         *
               PROMPT='THE ID NUMBER OF THE COMMAND TO BE CHANGED.'
         SPACE 1
PCLCHCOM IKJSUBF
PDLCHCOM IKJPOSIT QSTRING,UPPERCASE,                                   *
               PROMPT='THE NEW COMMAND TO BE SCHEDULED, ENCLOSED WITHIN*
                SINGLE QUOTES.'
         SPACE 1
PCLCHNAM IKJSUBF
PDLCHNAM IKJIDENT 'APPLICATION NAME',                                  *
               UPPERCASE,MAXLNTH=8,FIRST=ALPHA,OTHER=ALPHANUM,         *
               PROMPT='THE NAME OF THE NEW APPLICATION TO WHICH THE COM*
               MAND IS TO BE ASSOCIATED.'
         #TEST SIZE=(L'SFRNAME,EQ,8)
         SPACE 1
PCLCHDAT IKJSUBF
PDLCHDAT IKJIDENT 'DATE OF EXECUTION',                                 *
               MAXLNTH=8,FIRST=ANY,OTHER=ANY,VALIDCK=CHKDATE,          *
               PROMPT='THE NEW MONTH, DAY, AND YEAR (MM/DD/YY) OF EXECU*
               TION.+',                                                *
               HELP='- FOR MM, DD, AND/OR YY IF THE COMMAND IS TO BE EX*
               ECUTED ON EVERY MONTH, DAY, AND/OR YEAR.'
         SPACE 1
PCLCHTIM IKJSUBF
PDLCHTIM IKJIDENT 'TIME OF EXECUTION',                                 *
               MAXLNTH=5,FIRST=ANY,OTHER=ANY,VALIDCK=CHKTIME,          *
               PROMPT='THE NEW HOUR AND MINUTE (HH:MM) OF EXECUTION.'
         SPACE 1
PCLCHWEE IKJSUBF
PDLCHWEE IKJIDENT 'WEEKDAY OF EXECUTION',                              *
               MAXLNTH=9,FIRST=ANY,OTHER=ANY,                          *
               VALIDCK=CHKWEEKD,                                       *
               PROMPT='THE NAME OF THE NEW WEEKDAY ON WHICH THE COMMAND*
               TO BE EXECUTED OR -.+',                                 *
               HELP='"ANY" IF THE COMMAND CAN BE EXECUTED ON ANY WEEKDA*
               Y.'
         SPACE 1
PCLCHWIN IKJSUBF
PDLCHWIN IKJIDENT 'EXECUTION WINDOW',                                  *
               MAXLNTH=5,FIRST=ANY,OTHER=ANY,VALIDCK=CHKWINDO,         *
               PROMPT='THE NEW SIZE OF THE EXECUTION WINDOW FOR THIS CO*
               MMAND.+',                                               *
               HELP='THE LENGTH OF TIME (HH:MM) FOLLOWING THE SCHEDULED*
                EXECUTION TIME DURING WHICH THE COMMAND MIGHT STILL BE *
               EXECUTED IF THE SCHEDULED TIME WERE MISSED. THE WINDOW C*
               ANNOT EXTEND PAST MIDNIGHT. "0:00" MEANS "UNTIL MIDNIGHT*
               ".'
         SPACE 1
PCLCHSYS IKJSUBF
PDLCHSYS IKJIDENT 'SYSTEM IDENTIFICATION',                             *
               MAXLNTH=4,FIRST=ALPHANUM,OTHER=ALPHANUM,                *
               VALIDCK=CHKSYSID,                                       *
               PROMPT='THE NAME OF THE SYSTEM ON WHICH THE COMMAND MAY *
               BE EXECUTED.+',                                         *
               HELP='"CRNT" FOR THE SYSTEM WHICH YOU ARE LOGGED ON TO, *
               AND "ANY" IF THE COMMAND MAY BE EXECUTED ON ANY AVAILABL*
               E SYSTEM.'
         #TEST SIZE=(L'SFRSYSID,EQ,4)
         SPACE 1
PCLCHIPL IKJSUBF
PDLCHIPL IKJKEYWD DEFAULT='YES'
         IKJNAME 'YES'
         IKJNAME 'NO'
*YES     EQU   1
*NO      EQU   2
         SPACE 1
PCLCHOVE IKJSUBF
PDLCHOVE IKJKEYWD DEFAULT='YES'
         IKJNAME 'YES'
         IKJNAME 'NO'
*YES     EQU   1
*NO      EQU   2
         SPACE 1
         IKJENDP
         PRINT ON,GEN,NODATA
         EJECT ,
*************************************************************
*                                                           *
*        PCL AND PDL FOR THE DISPLAY AND DELETE SUBCOMMANDS *
*                                                           *
*************************************************************
         SPACE 1
         PRINT NOGEN
PCLDISPL IKJPARM DSECT=PDLDISPL
         SPACE 1
DISWHEN  IKJKEYWD
         IKJNAME 'YESTERDAY'             (1)
         IKJNAME 'TODAY'                 (2)
         IKJNAME 'TOMORROW'              (3)
         IKJNAME 'DATE',SUBFLD=PCLDISDT  (4)
         IKJNAME 'ID',SUBFLD=PCLDISID    (5)
         IKJNAME 'ANYTIME',              (6)                          *
               ALIAS='ALL'               (6)
         IKJNAME 'OBSOLETE'             V (7)
YESTERDA EQU   1
TODAY    EQU   2
TOMORROW EQU   3
DATE     EQU   4
ID       EQU   5
ANYTIME  EQU   6
OBSOLETE EQU   7
         SPACE 1
DISANAME IKJKEYWD
         IKJNAME 'NAME',SUBFLD=PCLDISNM
         SPACE 1
DDSYSID  IKJKEYWD
         IKJNAME 'SYSID',                                              *
               ALIAS='SYSTEM',                                         *
               SUBFLD=PCLDDSYS
         SPACE 1
DDPROMPT IKJKEYWD DEFAULT='PROMPT'
         IKJNAME 'SHORT'           (1)
         IKJNAME 'LONG',           (2)                                 *
               ALIAS='NOPROMPT'    (2)
         IKJNAME 'PROMPT'          (3)
SHORT    EQU   1
LONG     EQU   2
PROMPT   EQU   3
         SPACE 1
DDOVERRI IKJKEYWD DEFAULT='EITHER'
         IKJNAME 'OVERRIDE'        (1)
         IKJNAME 'NOOVERRIDE'      (2)
         IKJNAME 'BOTH'            (3)
         IKJNAME 'EITHER'          (4)
*OVERRIDE EQU  1
*NOOVERRI EQU  2
BOTH     EQU   3
EITHER   EQU   4
         SPACE 1
PCLDISDT IKJSUBF
DISDATE  IKJIDENT 'EXECUTION DATE',                                    *
               MAXLNTH=8,FIRST=ANY,OTHER=ANY,VALIDCK=CHKDATE,          *
               PROMPT='THE MONTH, DAY, AND YEAR (MM/DD/YY) OF EXECUTION*
               .'
         SPACE 1
PCLDISID IKJSUBF
DISID    IKJIDENT 'IDENTIFICATION NUMBERS',                            *
               LIST,RANGE,INTEG,VALIDCK=CHKID,                         *
               PROMPT='THE ID NUMBERS OF THE COMMANDS TO BE SELECTED.'
         SPACE 1
PCLDISNM IKJSUBF
DISNAME  IKJIDENT 'APPLICATION NAMES',                                 *
               LIST,UPPERCASE,MAXLNTH=8,FIRST=ALPHA,                   *
               OTHER=ALPHANUM,                                         *
               PROMPT='THE APPLICATION NAMES TO BE SELECTED.'
         #TEST SIZE=(L'SFRNAME,EQ,8)
         SPACE 1
PCLDDSYS IKJSUBF
PDLDDSYS IKJIDENT 'SYSTEM IDENTIFICATIONS',                            *
               LIST,MAXLNTH=4,FIRST=ALPHANUM,OTHER=ALPHANUM,           *
               VALIDCK=CHKSYSID,                                       *
               PROMPT='THE NAMES OF THE SYSTEMS ON WHICH THE COMMANDS T*
               O BE SELECTED MUST BE ELIGIBLE TO EXECUTE.+',           *
               HELP='"CRNT" FOR THE SYSTEM WHICH YOU ARE LOGGED ON TO; *
               "ANY" FOR THOSE COMMANDS THAT CAN EXECUTE ON ANY SYSTEM;*
               AND "ALL" FOR ALL COMMANDS (THE DEFAULT).'
         #TEST SIZE=(L'SFRSYSID,EQ,4)
         SPACE 1
         IKJENDP
         PRINT ON,GEN,NODATA
         SPACE 3
         END   SCHEDULE
