PERF     TITLE 'RESET PREFORMANCE GROUP FROM TSO'
*
*        WRITTEN BY BOB JUCH
*        POSTAL DATA CENTER
*        800 CHERRY AVE.
*        SAN BRUNO, CA.
*
*        ENHANCED BY BOB WENZEL
*        MEMOREX CORPORATION
*        SAN TOMAS AT CENTRAL EXPRESSWAY
*        SANTA CLARA, CA. 95052
*
PERFORM  CSECT
         SAVE (14,12),,PERFORM_&SYSDATE_&SYSTIME
         REGS
         LR    12,15
         USING PERFORM,12
         LR    2,13
         LA    13,SAVEAREA
         ST    13,8(2)
         ST    2,4(13)
         LR    2,1
         USING CPPL,2
         L     3,CPPLPSCB
         CLC   0(2,3),=C'$U'
         BNE   ABEND
         LA    4,PARSELST
         USING PPL,4
         MVC   PPLUPT,CPPLUPT
         MVC   PPLECT,CPPLECT
         MVC   PPLCBUF,CPPLCBUF
         CALLTSSR EP=IKJPARS,MF=(E,(4))
         L     5,PARSEANS
         USING PDL,5
         L     R6,16          CVT ->
         L     R6,556(R6)     ASVT ->
         LA    R6,516(R6)     ADDR OF ASVTMAXU
         L     R7,0(R6)       ASVTMAXU
         ST    R7,ASCBMAX     SAVE MAX ASCB'S
PRSEASID L     R7,ASIDNO      ADDR OF ASIDNO
         CLI   0(R7),C'*'     WANT ONLY CURRENT USER?
         BNE   PRSEASLN       NO
         L     R7,16          CVT ->
         L     R7,0(R7)       TCB CURRENT - OLD POINTER
         L     R7,8(R7)       ASCB CURRENT - OLD POINTER
         LH    R7,36(R7)      ASCBASID
         STH   R7,ASIDNMBR    SAVE ASID NUMBER
         B     RESET          GO RESET
PRSEASLN LH    R7,ASIDNO+4    LENGTH OF ASIDNO
         BCTR  R7,0           DECREMENT BY 1
         LA    R8,X'70'       LENGTH OF WORK - 1
         OR    R7,R8          LENGTHS FOR PACK INSTR.
         L     R8,ASIDNO      ADDR OF ASIDNO
         EX    R7,PACK        PACK ASIDNO
         CVB   R7,WORK        CONVERT ASID TO BINARY
         C     R7,ASCBMAX     DOES ASIDNO EXISTS?
         BH    PRSEMXER       NO SAY SO AND QUIT
         STH   R7,ASIDNMBR    SAVE ASID NUMBER
         B     RESET          RESET PERFORMANCE GROUP
PRSEMXER L     R7,ASCBMAX     MAX ASCB
         MVC   LINE,PRSEMXTP
         CVD   R7,WORK        CONVERT TO DECIMAL
         MVC   CHAR,EDMSK1    MOVE EDIT MASK TO CHAR
         ED    CHAR,WORK+4    EDIT TO EBCDIC
         MVC   LINE+27(4),CHAR+4 MOVE FOUR CHARS TO LINE
         LA    R7,LINE        SET UP FOR TPUT
         TPUT  (R7),80
         B     RETURN
PACK     PACK  WORK,0(0,R8)
PRSEMXTP DC    CL80'MAXIMUM NUMBER OF ASCBS IS'
RESET    AUTH  ON
         LH    0,ASIDNMBR
         L     7,PGN
         L     1,0(7)
         SYSEVENT RESETPG,ASID=(0)    DO NOT USE PGN=(1)
         SRL   1,8
         B     TABLE(1)
TABLE    B     HONORED
         B     INVALID
         B     NOTASGND
HONORED  LA    1,MSG1
         LA    0,L'MSG1
         B     TPUT
INVALID  LA    1,MSG2
         LA    0,L'MSG2
         B     TPUT
NOTASGND LA    1,MSG3
         LA    0,L'MSG3
TPUT     TPUT  (1),(0)
         AUTH  OFF
RETURN   IKJRLSA (5)
         L     13,SAVEAREA+4
         LM    14,12,12(13)
         SLR   15,15
         BR    14
         SPACE 3
ABEND    ABEND 2989,,STEP,SYSTEM
         SPACE 3
SAVEAREA DC    18F'0'
PARSELST DC    A(0),A(0),A(ECB),A(PCL),A(PARSEANS),A(0),A(0)
PARSEANS DC    A(0)
ECB      DC    F'0'
MSG1     DC    C'RESET REQUEST HONORED'
MSG2     DC    C'PERFORMANCE GROUP NUMBER INVALID'
MSG3     DC    C'ASID NOT CURRENTLY ASSIGNED'
EDMSK1   DC    XL8'4020202020202120'
LINE     DC    CL80' '
WORK     DC    D'0'
CHAR     DC    D'0'
ASCBMAX  DC    F'0'
ASIDNMBR DC    H'0'
         LTORG
         SPACE 3
PCL      IKJPARM DSECT=PDL
ASIDNO   IKJIDENT 'ASID NUMBER',ASTERISK,                              C
               MAXLNTH=3,FIRST=NUMERIC,OTHER=NUMERIC,                  C
               DEFAULT='0'
PGN      IKJIDENT 'PGN',INTEG,PROMPT='PERF. GROUP'
         IKJENDP
         IKJCPPL
         IKJPPL
         CVT
         END
