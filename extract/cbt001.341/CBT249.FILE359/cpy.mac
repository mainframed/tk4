CPY      TITLE 'INVOKE SPFCOPY TO COPY MEMBERS OF A PDS'
*  LAST MODIFIED 08/01/84 - HOWARD M. DEAN
CPY      CSECT
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
BASEREG  EQU   R12
PPLBAR   EQU   R11
PDLBAR   EQU   R9
UPTBAR   EQU   R8
         TSOENTER REGS=NO,PL=(PARSE,IO)
         USING UPT,UPTBAR
         USING IKJPARMD,PDLBAR
         MVC   PPLPCL,APARMTAB     PARAMETER TABLE POINTER
         MVC   PPLANS,APDLPT       POINTER TO THE POINTER TO THE PDL
         CALLTSSR EP=IKJPARS,MF=(E,PPL)
         CLI   PDLPT,X'FF'         PARSE UNNCESSFUL
         BE    ABEND
         L     PDLBAR,PDLPT        POINTER TO THE PDL
         L     UPTBAR,CPPLUPT      POINTER TO THE UPD
         MVI   S99RBLN-S99RB+RB99,RBLEN LENGTH OF RB
         MVI   S99VERB-S99RB+RB99,S99VRBAL  INDICAT ALLOCATE FUNCTION
CKFROM   DS    0H
         L     R2,DSNIN1           ADDRESS OF DSNAME
         LH    R1,DSNIN1+4         LENGTH
         LA    R3,5(R1)
         STH   R3,PRFXM2S2         STORE FOR PUTLINE
         STH   R1,DSNLNTH          SAVE LENGTH
         BCTR  R1,0                DECREMENT
         EX    R1,CDSN             MOVE DSN TO COMMON AREA
         EX    R1,CDSNF            MOVE DSN TO PUTLINE AREA
         TM    DSNIN1+22,X'80'     PASSOWRD PRESENT
         BZ    CKFROM1
         L     R2,DSNIN1+16        POINTER TO PASSWORD
         LH    R1,DSNIN1+20        LENGTH
         STH   R1,PSWLNTH          *
         BCTR  R1,0                DECREMENT
         EX    R1,CPSWD            MOVE PASSWORD
         LA    R1,TUNTPSWD         ADD VOL SER TO LIST
         ST    R1,SPSWD            PUT IN PARM LIST
CKFROM1  DS    0H
         L     R2,CPYFRM1          ADDRESS OF DDNAME
         LH    R1,CPYFRM1+4        LENGTH
         STH   R1,DDNLNTH          SAVE LENGTH
         BCTR  R1,0                DECREMENT
         EX    R1,CDDN             MOVE DDNAME TO COMMON AREA
         TM    VOLIN1+6,X'80'      PARM ENTERED
         BZ    CKFROM2
         L     R2,VOLIN1           ADDRESS OF VOLUME PARM
         LH    R1,VOLIN1+4         LENGTH
         STH   R1,VOLLNTH
         BCTR  R1,0                DECREMENT
         EX    R1,CVOL             MOVE VOLUME
         LA    R1,TUNTVLSR         ADD VOL SER TO LIST
         ST    R1,SVOL             PUT IN PARM LIST
CKFROM2  DS    0H
         LA    R1,TUPLSHR          TEXT PARM LIST
         ST    R1,S99TXTPP-S99RB+RB99 STORE PARM LIST ADDRESS
         BAL   R14,ALLOCATE
         XC    SMBR(12),SMBR       CLEAR
CKTO     DS    0H
         L     R2,DSNOT1           ADDRESS OF DSNAME
         LH    R1,DSNOT1+4         LENGTH
         LA    R3,4(R1)            LENGTH FOR PUTLINE
         STH   R3,PRFXM2S3
         STH   R1,DSNLNTH          SAVE LENGTH
         BCTR  R1,0                DECREMENT
         EX    R1,CDSN             MOVE DSN TO COMMON AREA
         EX    R1,CDSNT            MOVE DSN TO PUTLINE AREA
         TM    DSNOT1+22,X'80'     PASSOWRD PRESENT
         BZ    CKTO1
         L     R2,DSNOT1+16        POINTER TO PASSWORD
         LH    R1,DSNOT1+20        LENGTH
         STH   R1,PSWLNTH          *
         BCTR  R1,0                DECREMENT
         EX    R1,CPSWD            MOVE PASSWORD
         LA    R1,TUNTPSWD         ADD VOL SER TO LIST
         ST    R1,SPSWD            PUT IN PARM LIST
CKTO1    DS    0H
         L     R2,CPYTO1           ADDRESS OF DDNAME
         LH    R1,CPYTO1+4         LENGTH
         STH   R1,DDNLNTH          SAVE LENGTH
         BCTR  R1,0                DECREMENT
         EX    R1,CDDN             MOVE DDNAME TO COMMON AREA
         TM    VOLOT1+6,X'80'      PARM ENTERED
         BZ    CKIN2
         L     R2,VOLOT1           ADDRESS OF VOLUME PARM
         LH    R1,VOLOT1+4         LENGTH
         STH   R1,VOLLNTH
         BCTR  R1,0                DECREMENT
         EX    R1,CVOL             MOVE VOLUME
         LA    R1,TUNTVLSR         ADD VOL SER TO LIST
         ST    R1,SVOL             PUT IN PARM LIST
CKIN2    DS    0H
         BAL   R14,ALLOCATE
         XC    SMBR(12),SMBR       CLEAR
CKCPYIN  DS    0H
         LA    R1,TUPLNEW          ALLOCATE NEW WORK FILE
         ST    R1,S99TXTPP-S99RB+RB99
         LA    R1,TUNTRTDD         RETURN DD
         ST    R1,SDDN             STORE IN SHR DDNAME
         TM    CPYIN1+6,X'80'      PARM PRESENT
         BZ    CKCPYIN2
         LA    R1,TUPLSHR          ALLOCATE NEW WORK FILE
         ST    R1,S99TXTPP-S99RB+RB99
         L     R2,CPYIN1           INPUT DATASET
         LH    R1,CPYIN1+4         LENGTH
         STH   R1,DSNLNTH
         BCTR  R1,0                DECREMENT
         EX    R1,CDSN
         TM    CPYIN1+14,X'80'     MEMBER NAME PRESENT
         BZ    CKCPYIN1
         L     R2,CPYIN1+8
         LH    R1,CPYIN1+12        LENGTH
         STH   R1,MBRLNTH
         BCTR  R1,0                DECREMENT
         EX    R1,CMBR             MOVE MENBER
         LA    R1,TUNTMBR          ADD MEMBER TO LIST
         ST    R1,SMBR
CKCPYIN1 DS    0H
         TM    CPYIN+22,X'80'      PARM PRESENT
         BZ    CKCPYIN2
         L     R2,CPYIN1+16        PASSWORD POINTER
         LH    R1,CPYIN1+20        LENGTH
         STH   R1,PSWLNTH
         BCTR  R1,0                DECREMENT
         EX    R1,CPSWD            MOVE PASSWORD
         LA    R1,TUNTPSWD         ADD PASSWORD TO TUPL
         ST    R1,SPSWD
CKCPYIN2 DS    0H
         BAL   R14,ALLOCATE
         MVC   SYSIN,DDNRTN        SAVE DDNAME
         XC    SMBR(12),SMBR       CLEAR
ASYSUT3  DS    0H
         LA    R1,TUPLNEW          ALLOCATE TEMP DATASET
         ST    R1,S99TXTPP-S99RB+RB99
         BAL   R14,ALLOCATE        ALLOCATE SYSUT3
         MVC   SYSUT3,DDNRTN       SAVE DDNAME
         BAL   R14,ALLOCATE        ALLOCATE SYSUT4
         MVC   SYSUT4,DDNRTN       SAVE DDNAME
         TM    CPYIN1+6,X'80'      WAS IT ENTERD
         BO    XCTL
         MVC   CPYINDCB+X'28'(L'SYSIN),SYSIN MOVE IN SYSIN NAME
         OPEN  (CPYINDCB,OUTPUT)
         LA    R2,OUTAREA          FIRST BYTE OF DATA
         LA    R3,L'COPY           C O=
         LA    R4,COPY             POINT TO COPY
         LR    R5,R3               LENGTH EQUAL
         MVCL  R2,R4               MOVE TO OUTPUT
         L     R4,CPYTO1           POINT TO TO DDNAME
         LH    R5,CPYTO1+4         LENGTH
         LR    R3,R5               SET LENGTH EQUAL
         MVCL  R2,R4
         LA    R4,COPYTO           ,I=
         LA    R5,L'COPYTO         LENGTH
         LR    R3,R5               LENGTH EQUAL
         MVCL  R2,R4
         CLI   REPLACE+1,X'02'     REPLACE OPERAND
         BE    NOREP1
         LA    R4,LPARN            RIGHT PARENTHES
         LA    R5,L'LPARN          LENGTH OF RPARN
         LR    R3,R5               SET LENGTH EQUAL
         MVCL  R2,R4
NOREP1   DS    0H
         L     R4,CPYFRM1         FROM DDNAME
         LH    R5,CPYFRM1+4       LENGTH
         LR    R3,R5              SET LENGTH EQUAL
         MVCL  R2,R4
         CLI   REPLACE+1,X'02'    NO REPLACE OPERAND
         BE    NOREP2
         LA    R4,RPARN           ADDRESS OF LEFT PAREN
         LA    R5,L'RPARN         LENGTH
         LR    R3,R5              SET LENGTH EQUAL
         MVCL  R2,R4
NOREP2   DS    0H
         BAL   R2,PUTIT
         CLI   ALL+1,X'02'         PARM ENTERD
         BZ    CLOSE
         LA    R6,MEMBER           BASE FOR LOOP
         MVC   OUTAREA(L'SELECT),SELECT
MBRLOOP  DS    0H
         L     R4,0(R6)            MEMBER POINTER
         LH    R5,4(R6)            LENGTH
         LA    R2,OUTAREA+L'SELECT START OF MEMBER
         LA    R3,L'OUTAREA-L'SELECT
         ICM   R5,8,OUTAREAB       PAD CHARACTER
         MVCL  R2,R4               MOVE MEMBER NAME
         BAL   R2,PUTIT
         CLI   08(R6),X'FF'        END OF CHAIN
         L     R6,08(R6)           NEXT MEMBER
         BNE   MBRLOOP
CLOSE    DS    0H
         CLOSE (CPYINDCB)
XCTL     DS    0H
         MVC   OLD(4*4),MSG        MOVE MSG TO OUTPUT DIS LIST
         XC    TSOECB,TSOECB       CLEAR ECB
         PUTLINE OUTPUT=OLD,MF=(E,IOPL),PARM=PUTBLOCK
         GETMAIN R,LV=11*8+8+2+2   STORAGE FORM DDPARM LIST
         LR    R5,R1               SAVE ADDRESS OF AREA     *HMD 08/81*
         LA    R2,11*8+8+2(R1)     ADDRESS OF PARM
         ST    R2,0(R1)            POINTER TO PARM
         LA    R2,8(R1)            POINT TO DDNAME LIST
         ST    R2,4(R1)            PUT IN PARM LIST
         OI    4(R1),X'80'         INDICATE END OF LIST
         LA    R3,11*8             LENGTH OF DDNAME LIST
         USING IEBDDNS,R2
         STH   R3,IEBLNTH          STORE LENGTH
         MVC   IEBIN,SYSIN         SYSIN DDNAME
         MVC   IEBUT3,SYSUT3       SYSUT3 DDNAME
         MVC   IEBUT4,SYSUT4       SYSUT4 DDNAME
         LINK  EP=SPFCOPY          LINK TO SPFCOPY          *HMD 08/81*
         LR    R6,R15              SAVE RETURN CODE         *HMD 08/81*
         FREEMAIN R,LV=11*8+8+2+2,A=(R5)    FREE STORAGE    *HMD 08/81*
         B     RETURN
PUTIT    DS    0H
         PUT   CPYINDCB,OUTAREAB
         BR    R2
         SPACE 2
RETURN   TSOLEAVE RETCODE=(6)      RETURN TO CALLER         *HMD 08/84*
         SPACE 2
ABEND    DS    0H
         ABEND 1,DUMP
ALLOCATE DS    0H
         LA    R1,RB99PTR          POINT TO PARM LIST
         DYNALLOC
         LR    R6,R15              SAVE RETURN CODE         *HMD 08/81*
         LTR   R15,R15             REQUEST SUCCESSFUL
         BZR   R14                 ..YES
         ST    R15,S99RC           STORE RETURN CODE
         LA    R1,CPPLCBUF         GET CPPL ADDRESS
         ST    R1,S99CPPL          POINTER TO THE CPPL
         LA    R1,RB99PTR          POINT TO DAIR FAIL BLOCK
         LINK  EP=IKJEFF18         DAIR FAIL ROUTINE
         B     RETURN              TERMINATE PROGRAM
         EJECT
         DS    0F                  ALLIGNMENT
CDDN     MVC   DDNAME(0),0(R2)     MOVE DDNAME
CDSN     MVC   DSNAME(0),0(R2)     MOVE DSNAME
CDSNT    MVC   TODSN(0),0(R2)      MOVE DSNAME
CDSNF    MVC   FROMDSN(0),0(R2)    MOVE DSNAME
CVOL     MVC   VOLSER(0),0(R2)     VOLUME SERIAL NO
CPSWD    MVC   PASSWORD(0),0(R2)   PASSWORD
CMBR     MVC   MEMBR(0),0(R2)      MEMBER NAME
         DS    0F
RB99PTR  DC    XL1'80',AL3(RB99)   POINT TO THE REQUEST BLOCK
         DC    A(S99RC)            RETURN CODE POINTER
         DC    A(S99EFF02)         POINTER TO IKJEFF02
         DC    A(S99FAIL)          ID NUMBER       PTR
S99CPPL  DC    A(0)                POINTER TO THE CPPL
         DC    A(0)
S99RC    DC    A(0)                RETURN CODE
S99EFF02 DC    A(0)                POINTER TO IKJEFF02
S99FAIL  DC    Y(50)               SVC 99 RETURN CODE
RB99     DC    5A(0)               SVC 99 REQUEST BLOCK
TUPLNEW  DS    0F
NTRDD    DC    A(TUNTRTDD)         DDNAME
NDEL     DC    A(TUNTDEL)          DELETE WHEN FREEDED
NNEW     DC    A(TUNTNEW)          ALLOCATE NEW
NCYL     DC    A(TUNTCYL)          CYL ALLOCATION
NPRIM    DC    A(TUNTPRIM)         PRIMARY ALLOCATION
NSECND   DC    XL1'80',AL3(TUNTSEC) SECONDARY ALLOCATION
TUPLSHR  DS    0F
SDDN     DC    A(TUNTDDN)          DDNAME
SNDSP    DC    A(TUNTNDSP)         KEEP WHEN FREEDED
SSHR     DC    A(TUNTSHR)          ALLOCATE SHARED
SMBR     DC    A(0)                MEMBER NAME
SPSWD    DC    A(0)                PASSWORD
SVOL     DC    A(0)                VOLUME SERIAL
SDSN     DC    XL1'80',AL3(TUNTDSN) SECONDARY ALLOCATION
TUNTDDN  DC    Y(DALDDNAM),H'1'
DDNLNTH  DC    H'8'
DDNAME   DC    CL8' '
TUNTDSN  DC    Y(DALDSNAM),H'1'
DSNLNTH  DC    H'44'
DSNAME   DC    CL44' '
TUNTVLSR DC    Y(DALVLSER),H'1'
VOLLNTH  DC    H'6'
VOLSER   DC    CL6' '
TUNTMBR  DC    Y(DALMEMBR),H'1'
MBRLNTH  DC    H'8'
MEMBR    DC    CL8' '
TUNTNDSP DC    Y(DALNDISP),H'1',H'1',XL1'08'     KEEP
TUNTDEL  DC    Y(DALNDISP),H'1',H'1',XL1'04'     DELETE
TUNTSHR  DC    Y(DALSTATS),H'1',H'1',XL1'08'     SHR
TUNTNEW  DC    Y(DALSTATS),H'1',H'1',XL1'04'     NEW
TUNTCYL  DC    Y(DALCYL),H'0'
TUNTPRIM DC    Y(DALPRIME),H'1',H'3',XL3'00000A' TEN  CYL PRIMARY
TUNTSEC  DC    Y(DALSECND),H'1',H'3',XL3'000005' FIVE CYL SECONDARY
TUNTPSWD DC    Y(DALPASSW),H'1'
PSWLNTH  DC    H'8'
PASSWORD DC    CL8' '
TUNTRTDD DC    Y(DALRTDDN),H'1'
DDNRLTH  DC    H'8'
DDNRTN   DC    CL8' '
SYSIN    DC    CL8' '
SYSUT3   DC    CL8' '
SYSUT4   DC    CL8' '
PUTBLOCK PUTLINE MF=L
MSG      DC    A(3),A(PRFXMSG2),A(PRFXM2S2),A(PRFXM2S3)
OLD      DS    F                   NUMBER OF ENTRIES
         DS    F
         DS    F
         DS    F
         DS    F
COPY     DC    C'C O='             START COPY AND OUTPUT DDNAME
COPYTO   DC    C',I='              INPUT DDNAME
LPARN    DC    CL2'(('             REPLACE
RPARN    DC    CL4',R))'           *
SELECT   DC    CL4'S M='           SELECT MEMBER
OUTAREAB DC    CL1' '              START OF OUTPUT AREA
OUTAREA  DC    CL79' '             START OF OUTPUT AREA
PRFXMSG2 DC    H'40'               LENGTH OF SEGMENT
         DC    H'0'                SEGMENT POSITION
         DC    CL36' INPUT DATASET IS OUTPUT DATASET IS '
PRFXM2S2 DC    H'49'               LENGTH OF SEGMENT
         DC    H'18'               SEGMENT POSITION
FROMDSN  DC    CL45' '
PRFXM2S3 DC    H'49'               LENGTH OF SEGMENT
         DC    H'36'               SEGMENT POSITION
TODSN    DC    CL45' '
APDLPT   DC    A(PDLPT)            POINTER TO THE PDS
APARMTAB DC    V(PARMTAB)          POINTER TO THE PARM TABLE
PDLPT    DS    F
OPREFX   DS    CL7                 OLD PROFILE CPY
OPREFXL  DS    XL1                 LENGTH OF OLD CPY
CPYINDCB DCB   DDNAME=X,LRECL=80,BLKSIZE=2480,RECFM=FB,MACRF=(PM),     *
               DSORG=PS
         EJECT
PARMTAB  IKJPARM
         SPACE 3
MEMBER   IKJIDENT 'MEMBER',LIST,MAXLNTH=8,                             *
               FIRST=ALPHA,OTHER=ALPHANUM,CHAR
         SPACE 3
FROM     IKJKEYWD DEFAULT='FROM(CNTL)'
         SPACE 3
         IKJNAME 'FROM',SUBFLD=DSNIN
         SPACE 3
FVOL     IKJKEYWD
         SPACE 3
         IKJNAME 'FVOL',SUBFLD=VOLIN
         SPACE 3
TO       IKJKEYWD DEFAULT='TO(''TODSN      '')'
         SPACE 3
         IKJNAME 'TO',SUBFLD=DSNOT
         SPACE 3
TOVOL    IKJKEYWD
         SPACE 3
         IKJNAME 'TVOL',SUBFLD=VOLOT
         SPACE 3
ALL      IKJKEYWD DEFAULT='MBR'
         SPACE 3
         IKJNAME 'MBR',ALIAS='MEMBER'
         SPACE 3
         IKJNAME 'ALL',ALIAS='COPYALL'
         SPACE 3
REPLACE  IKJKEYWD DEFAULT='REPLACE'
         SPACE 3
         IKJNAME 'REPLACE',ALIAS='REP'
         SPACE 3
         IKJNAME 'NOREPLACE',ALIAS='NOREP'
         SPACE 3
CPYIN    IKJKEYWD
         SPACE 3
         IKJNAME 'CPYIN',SUBFLD=CPYINS
         SPACE 3
CPYFRM   IKJKEYWD DEFAULT='CPYFRM(CPYFRM)'
         SPACE 3
         IKJNAME  CPYFRM,SUBFLD=CPYFRMS
         SPACE 3
CPYTO    IKJKEYWD DEFAULT='CPYTO(CPYTO)'
         SPACE 3
         IKJNAME  CPYTO,SUBFLD=CPYTOS
         SPACE 3
DSNIN    IKJSUBF
         SPACE 3
DSNIN1   IKJPOSIT DSNAME,USID
         SPACE 3
VOLIN    IKJSUBF
         SPACE 3
VOLIN1   IKJIDENT 'FVOL',FIRST=ALPHANUM,OTHER=ALPHANUM
         SPACE 3
DSNOT    IKJSUBF
         SPACE 3
DSNOT1   IKJPOSIT DSNAME,USID
         SPACE 3
VOLOT    IKJSUBF
         SPACE 3
VOLOT1   IKJIDENT 'TVOL',FIRST=ALPHANUM,OTHER=ALPHANUM
         SPACE 3
CPYINS   IKJSUBF
         SPACE 3
CPYIN1   IKJPOSIT DSNAME,USID
         SPACE 3
CPYFRMS  IKJSUBF
         SPACE 3
CPYFRM1  IKJPOSIT DSNAME,DDNAM
         SPACE 3
CPYTOS   IKJSUBF
         SPACE 3
CPYTO1   IKJPOSIT DSNAME,DDNAM
         EJECT
         IKJENDP
         IKJUPT
         CVT   DSECT=YES
         IEFZB4D0
         IEFZB4D2
RBLEN    EQU   (S99RBEND-S99RB)    LENGTH OF SVC 99 REQ BLOCK
IEBDDNS  DSECT
IEBLNTH  DS    H                   LENGHT OF PARM
         DS    CL8                 FILLER
         DS    CL8                 FILLER
         DS    CL8                 FILLER
         DS    CL8                 FILLER
IEBIN    DS    CL8                 SYSIN
IEBSYSPT DS    CL8                 SYSIN
         DS    CL8                 FILLER
IEBUT1   DS    CL8                 SYSUT1
IEBUT2   DS    CL8                 SYSUT2
IEBUT3   DS    CL8                 SYSUT3
IEBUT4   DS    CL8                 SYSUT4
          END  CPY
