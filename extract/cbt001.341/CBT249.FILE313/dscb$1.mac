$1       TITLE 'DSCB$1,CNNX - MACRO DEFINITION'
         MACRO -- SPECIFY CONTROL NUMBER FOR CONDITIONAL ASSEMBLY
         CONTROL &NUMBER=3330
         GBLC  &SYSCODE
         LCLA  &#
.*
.*  THIS MACRO EDITS THE CONTROL NUMBER
.*
         AIF   (T'&NUMBER NE 'N').#ERROR
&#       SETA  &NUMBER
         AIF   (&# GT 8768 OR &# LT 0).#ERROR
.*
&SYSCODE SETC  '&NUMBER'
.*
.#SET    ANOP
         SPACE 2
         MNOTE *,'CONTROL NUMBER IS SET TO &SYSCODE'
         SPACE 2
         MEXIT
.*
.*  ERROR MESSAGES
.*
.#ERROR  ANOP
         SPACE 2
         MNOTE 8,'CONTROL NUMBER = &NUMBER IS INVALID.'
&SYSCODE SETC  '2314'
         AGO   .#SET
         MEND
         SPACE 2
         GBLC  &SYSCODE      NUMERIC CODE FOR CONTROL NUMBER
 TITLE 'DSCB$1,CNNX - DOCUMENTATION'
         SPACE 2
***********************************************************************
**  THIS ROUTINE CHECKS THAT THE PARM FIELD PROVIDED CONTAINS THE    **
**  CORRECT PASSWORD AND THE CORRECT SECURITY TYPE.                  **
**                                                                   **
**  THE PARM FIELD HAS THE GENERAL FORMAT - NNNNS.                   **
**                                                                   **
**    NNNN     A FOUR DIGIT PASSWORD CODE.  THIS CODE IS MADE UP OF  **
**             A CONTROL NUMBER (ASSIGNED BY THE SYSTEMS PROGRAMMER  **
**             WHEN THIS MODULE IS ASSEMBLED) ADDED TO THE CURRENT   **
**             DATE (IN THE FORMAT MMDD).  THIS ARRANGEMENT CAUSES   **
**             THE PASSWORD TO CHANGE DAILY.                         **
**                                                                   **
**    S        ONE CHARACTER CODE FOR THE TYPE OF SECURITY TO APPLY  **
**             TO ALL FILES PROCESSED BY THIS PROGRAM.  THE 'S' CODE **
**             MAY BE 'N', 'W', OR 'R' INDICATING RESPECTIVELY NO    **
**             SECURITY, WRITE-PROTECTED SECURITY, OR READ-PROTECTED **
**             SECURITY.                                             **
**                                                                   **
**  EXAMPLE -- YOU WISH TO PROTECT SYS1.PROCLIB AGAINST ATTEMPTS TO  **
**             WRITE INTO IT.  ASSUME YOU HAVE CHOSEN 2314 AS THE    **
**             CONTROL NUMBER.  IT IS SEPTEMBER 8, 1975.             **
**                                                                   **
**             TO CALCULATE THE PASSWORD  ADD  2314  CONTROL NUMBER  **
**                                         TO  0908  DATE - MMDD     **
**                                             ----                  **
**                                     GIVING  3222  THE PASSWORD    **
**                                                                   **
**             THE PARM FIELD WOULD BE - PARM=3222W                  **
***********************************************************************
         SPACE 2
         CONTROL NUMBER=3330
 TITLE 'DSCB$1,CNNX - GENERAL EQUATES'
DSCB$1   CSECT
         SPACE 2
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         SPACE 4
**       WORK REGISTER NAMES
         SPACE 1
WA       EQU   R2
WB       EQU   R3
WC       EQU   R4
WD       EQU   R5
WE       EQU   R6
WF       EQU   R7
         SPACE 4
**       MISCELLANEOUS EQUATES
         SPACE 1
NSEC     EQU   C'N'                    NO SECURITY
WSEC     EQU   C'W'                    NO WRITE ALLOWED
RSEC     EQU   C'R'                    NO READ OR WRITE ALLOWED
 TITLE 'DSCB$1,CNNX - PARM EDIT ROUTINE'
         USING DSCB$1,R12
         SAVE  (14,12)                 SAVE REGISTERS
         LR    R12,R15                 RETAIN EPA AS BASE
         ST    R13,PSAVE+4             STORE HSA
         LA    R14,PSAVE               PICK UP SA ADDRESS
         ST    R14,8(,R13)             STORE LSA
         BAL   R13,PSAVEND             LOAD R13 WITH @ PSAVE
PSAVE    DS    18F                     SAVE AREA
         SPACE 2
PSAVEND  DS   0H                       BEGIN PROCESSING
         L     WB,0(R1)                GET POINTER TO PASSWORD NUMBER
         LH    WC,0(WB)                AND SECURITY TYPE
         CH    WC,PARMLGTH             IS PARM LENGTH O.K.?
         BE    PLGTHOK                 YES, BRANCH
         B     PBADLGTH                GO ISSUE ERROR MESSAGE
         SPACE 1
PLGTHOK  DS   0H                        LENGTH IS CORRECT.
         LA    WB,2(WB)                POINT WB AT CHARACTERS
         CLI   4(WB),NSEC              INSURE THAT
         BE    PSECOK                   SECURITY
         CLI   4(WB),WSEC                REQUEST IS
         BE    PSECOK                     A VALID ONE
         CLI   4(WB),RSEC
         BE    PSECOK
         B     PBADSEC                 ERROR IF NOT.
         SPACE 1
PSECOK   DS   0H
         SLR   WE,WE                   ZERO A WORK REGISTER
         IC    WE,4(WB)                PICK UP SECURITY TYPE
         LR    WD,WB                   SAVE PARM POINTER
         BCTR  WC,R0                   LESS 1 FOR SEC TYPE
         SPACE 1
PNUMCHK  DS   0H
         CLI   0(WB),C'0'              INSURE THAT
         BL    PNOTNUM                  NUMERICS ARE
         CLI   0(WB),C'9'                ACTUALLY
         BH    PNOTNUM                    NUMBERS...
         LA    WB,1(WB)
         BCT   WC,PNUMCHK              LOOP THRU ALL POSITIONS
         PACK  PWORK1,0(4,WD)          PACK NUMERIC PORTION
         CVB   WC,PWORK1               BINARY CODE IN WC
PXC      DS   0H
         S     WC,PSYSCODE             SUBTRACT SYSTEMS CODE
         BNP   PBADCODE                RESULT MUST BE POSITIVE
         TIME  DEC                     PUT DATE IN R1 00YYDDDF
         XC    PWORK1,PWORK1           CLEAR WORKAREA
         STH   R1,PWORK1+6             STORE DDDF INTO WORKAREA
         CVB   WD,PWORK1                AND CONVERT RADIX
         SRL   R1,12                   SHIFT OUT DAYS
         STH   R1,PWORK1+6             STORE AS 0YYD
         OI    PWORK1+7,X'0F'          FIX SIGN
         CVB   WB,PWORK1                AND MAKE BINARY
         SLR   WA,WA                   SETUP FOR DIVISION
         LTR   WB,WB                   ABSURD TEST FOR CENTURY BORDER
         BZ    PNOTLEAP                NOT LEAP YEAR THEN
         D     WA,PFOUR                DIVIDE BY FOUR
         LTR   WA,WA                   IF REMAINDER,
         BP    PNOTLEAP                 THEN NOT LEAP YEAR
         MVI   PFEB,29                 SET FEBRUARY TO 29 IF LEAP YR
         SPACE 1
PNOTLEAP DS   0H
         LA    WF,1                    INITIALIZE MONTH COUNTER
         LA    WA,PMONTHS              GET DAY-LIST ADDRESS
         SPACE 1
PDAYLOOP DS   0H
         CH    WD,0(WA)                COMPARE JUL DAYS TO MONTH
         BNH   PGOTIT                  BRANCH IF FOUND
         SH    WD,0(WA)                ELSE SUBTRACT THESE DAYS
         LA    WF,1(WF)                BUMP MONTH COUNT
         LA    WA,2(WA)                POINT AT NEXT MONTH
         B     PDAYLOOP                AND TRY AGAIN
PGOTIT   DS   0H
         MH    WF,PH100                MULTIPLY MONTHS BY 100
         AR    WD,WF                   NOW HAVE MMDD
         CLR   WC,WD                   COMPARE TO USER'S DATE
         BNE   PBADCODE                NOT EQUAL, ERROR
         L     R13,PSAVE+4             NORMAL RETURN
         ST    WE,20(R13)              PUT SEC TYPE IN CALLER'S R0
         RETURN (14,12),RC=0
 TITLE 'DSCB$1,CNNX - DATA AND WORK AREAS'
PARMLGTH DC    H'5'                    LENGTH OF PARM - NNNNS
PWORK1   DC    D'0'                    WORK AREA
PSYSCODE DC    F'&SYSCODE'             ENCODING VALUE
PSAFE    DC    X'C0AFE000'             ABEND CODE
PFOUR    DC    F'4'                    CONSTANT
PH100    DC    H'100'                  CONSTANT
PMONTHS  DC    H'31,28,31,30,31,30,31,31,30,31,30,31' MONTHS TABLE
PFEB     EQU   PMONTHS+3               FEBRUARY'S ADDRESS
PMEND    DS   0H
 TITLE 'DSCB$1,CNNX - ERROR HANDLING ROUTINES'
***********************************************************************
**  NOTE - NO ERROR ROUTINES ARE PROVIDED EXCEPT ABEND 'SAFE'.  ANY  **
**  ADDITIONAL ERROR PROCESSING IS LEFT TO THE DISCRETION OF THE     **
**  SYSTEMS PROGRAMMERS.                                             **
***********************************************************************
         SPACE 2
PBADLGTH DS   0H                       PARM FIELD HAS INCORRECT LENGTH
         B     PERROR                   GO TO ABEND
         SPACE 2
PNOTNUM  DS   0H                       PASSWORD IS NOT ALL NUMERIC
         B     PERROR                   GO TO ABEND
         SPACE 2
PBADCODE DS   0H                       INCORRECT PASSWORD
         B     PERROR                   GO TO ABEND
         SPACE 2
PBADSEC  DS   0H                       INVALID SECURITY CODE
         B     PERROR                   GO TO ABEND
         SPACE 2
PERROR   DS   0H                       TERMINAL ERROR HAS BEEN FOUND
         L     R1,PSAFE                LOAD UP ABEND CODE
         XC    PXC(PMEND-PXC),PXC      CLOBBER CORE DUMP
         ABEND (1)                     TERMINATE PROCESSING
         SPACE 1
         END
