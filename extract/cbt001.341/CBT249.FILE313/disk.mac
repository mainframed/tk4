*)F FUNCTION -
*   THE DISK COMMAND WILL LIST ALL DASD DEVICES THAT ARE ONLINE.
*)X SYNTAX -
*         DISK    ADDRESS/VOLSER/BOTH
*  REQUIRED - NONE
*  DEFAULTS - BOTH
*  ALIAS    - DI
*
*   OUTPUT FROM THE SPACE COMMAND HAS THE FOLLOWING FORMAT:
*
*   VOL SER     ADDR      UNIT    ALOC
*
*          VOL SER  - IS THE VOLUME SERIAL.
*
*             ADDR  - IS THE ADDRESS OF THE DEVICE.
*
*             UNIT  - IS THE DEVICE TYPE.
*
*             ALOC  - IS AN INDICATOR.
*                     A MEANS THE DEVICE IS ALLOCATED.
*                     U MEANS THE DEVICE IS UNALLOCATED.
*
*)O OPERANDS -
*))ADDRESS      - DISPLAY ONLINE DASD VOLUMES BY ASCENDING ADDRESSES.
*))VOLSER       - DISPLAY ONLINE DASD VOLUMES BY SORTED VOLUME NAMES.
*))BOTH         - DISPLAY ONLINE DASD VOLUMES FIRST BY ASCENDING
*                 ADDRESSES AND THEN BY SORTED VOLUME NAMES.
         EJECT
DISKLIST CSECT
         SAVE       (14,12),,DISKLIST
         LR         R12,R15        USE R12 AS PROG BASE
         USING      DISKLIST,R12   TELL ASM ABOUT BASE
         USING      CPPL,R1        TELL ASM ABOUT IT
         ST         R13,SAVE+4     O. S. SAVE AREA
         LA         R3,SAVE
         ST         R3,8(,R13)
         LR         R13,R3
         EJECT
* THE FOLLOWING 7 LINES SUPPORT OPERATOR PRIVILEDGE
*         L     R2,CPPLPSCB
*         USING PSCB,R2
*         TM    PSCBATR1,PSCBCTRL USER HAVE OPERATOR PRIV.
*         BO    BEGIN         TPUT MESSAGE AND QUIT
*         TPUT  NOOPER,40
*         B     RETURN
*NOOPER   DC    CL40'COMMAND NOT AUTHORIZED FOR YOUR USE'
BEGIN    LR    R2,R1         SAVE R1
         USING CPPL,R2
         LA    R3,PPLADDR         ADDRESSABILITY FOR PPL
         USING PPL,R3
         MVC   PPLUPT,CPPLUPT           UPT
         MVC   PPLECT,CPPLECT           ECT
         LA    R4,ECB
         ST    R4,PPLECB               ECB
         L     R4,PCLADDR
         ST    R4,PPLPCL               PCL
         LA    R4,PDLADDR              PDL ADDRESS
         ST    R4,PPLANS               ANSWER
         MVC   PPLCBUF,CPPLCBUF         CBUF
         LA    R1,PPLADDR
         LINK  EP=IKJPARS               GO PARSE
         LTR   R15,R15
         BZ    PARSE
         TPUT  PRSERM,40
         B     RETURN
         DROP  R2
         DROP  R3
PRSERM   DC    CL40'PARSE ERROR'
 EJECT
PARSE    L     R10,PDLADDR
         USING IKJPARMD,R10
PRSEBRTB LH    R3,TYPE        PICK UP TYPE
         SLL   R3,2           MULT. BY 4
         B     *+0(R3)        BRANCH TABLE
         B     PRSEBOTH       TYPE = 1
         B     PRSEADDR       TYPE = 2
         B     PRSEVOL        TYPE = 3
PRSEBOTH BAL   R14,ADDR
         BAL   R14,VOL
         B     RETURN
PRSEVOL  BAL   R14,VOL0
         BAL   R14,VOL
         B     RETURN
PRSEADDR BAL   R14,ADDR
         B     RETURN
ADDR     L     R6,16
         L     R6,X'C4'(R6)
         MVC   HDR1+50(4),16(R6)
         TPUT  HDR1,80
         L     R6,16           A(CVT)
         L     R6,40(R6)        POINTER TO UCB ADDR TABLE
NXTUCB   LH    R5,0(R6)         GET UCB ADDRESS
         LTR   R5,R5            IS THIS A HOLE IN THE UCB TABLE
         BZ    UCBHOLE        YES
         CH    R5,=X'FFFF'     ANY MORE UCB'S
         BE    FINIS          NO - THAT'S ALL FOLKS
         CLI   18(R5),X'20'    IS IT DIRECT ACCESS STORAGE
         BNE   UCBHOLE        NO
         TM    3(R5),X'08'     IS UCB REALLY ALLOCATED
         MVI   PRALOC,C'A'    SET DEFAULT OF ALLOCATED
         BO    GETTYPE        CONTINUE
         MVI   PRALOC,C'U'    NO, SAY IT IS UNALLOCATED
GETTYPE  SR    R4,R4           ZERO THE REGISTER
         IC    R4,19(R5)       GET THE UNIT TYPE
         SLA   R4,3            * 8
         LA    R4,UNITTYPE(R4) ADDRESS IN TABLE
         MVC   PRUNIT,0(R4)    MOVE UNIT TYPE TO PRINT LINE
         CLC   PRADDR(2),13(R5)
         BE    *+8
         MVI   LINE,C' '
         MVC   PRADDR,13(R5)   MOVE UNIT ADDRESS TO PRINT LINE
         MVC   PRSER,28(R5)    MOVE VOLUME SERIAL TO PRINT LINE
         CLC   PRSER,ZEROES
         BE    UCBHOLE
         SR    R7,R7
COMPLIST CH    R7,LCT
         BNL   ADDLIST
         CH    R5,LIST(R7)
         BE    UCBHOLE
         LA    R7,2(R7)
         B     COMPLIST
ADDLIST  LH    R7,LCT
         CH    R7,LMAX
         BNL   FULL
         STH   R5,LIST(R7)
         LA    R7,2(R7)
         STH   R7,LCT
FULL     EQU   *
         TPUT  LINE,80     OUTPUT
         MVI   LINE,C' '
UCBHOLE  LA    R6,2(R6)         INCREMENT POINTER
         B     NXTUCB         GET NEXT UCB
FINIS    EQU   *
         BR    R14
VOL0     L     R6,16
         L     R6,X'C4'(R6)
         MVC   HDR1+50(4),16(R6)
         L     R6,16           A(CVT)
         L     R6,40(R6)        POINTER TO UCB ADDR TABLE
NXTUCB0  LH    R5,0(R6)         GET UCB ADDRESS
         LTR   R5,R5            IS THIS A HOLE IN THE UCB TABLE
         BZ    UCBHOLE0       YES
         CH    R5,=X'FFFF'     ANY MORE UCB'S
         BE    FINIS0         NO - THAT'S ALL FOLKS
         CLI   18(R5),X'20'    IS IT DIRECT ACCESS STORAGE
         BNE   UCBHOLE0       NO
         TM    3(R5),X'08'     IS UCB REALLY ALLOCATED
         MVI   PRALOC,C'A'    SET DEFAULT OF ALLOCATED
         BO    GETTYPE0       CONTINUE
         MVI   PRALOC,C'U'    NO, SAY IT IS UNALLOCATED
GETTYPE0 SR    R4,R4           ZERO THE REGISTER
         IC    R4,19(R5)       GET THE UNIT TYPE
         SLA   R4,3            * 8
         LA    R4,UNITTYPE(R4) ADDRESS IN TABLE
         MVC   PRUNIT,0(R4)    MOVE UNIT TYPE TO PRINT LINE
         CLC   PRADDR(2),13(R5)
         BE    *+8
         MVI   LINE,C' '
         MVC   PRADDR,13(R5)   MOVE UNIT ADDRESS TO PRINT LINE
         MVC   PRSER,28(R5)    MOVE VOLUME SERIAL TO PRINT LINE
         CLC   PRSER,ZEROES
         BE    UCBHOLE0
         SR    R7,R7
COMPLIS0 CH    R7,LCT
         BNL   ADDLIST0
         CH    R5,LIST(R7)
         BE    UCBHOLE0
         LA    R7,2(R7)
         B     COMPLIS0
ADDLIST0 LH    R7,LCT
         CH    R7,LMAX
         BNL   FULL0
         STH   R5,LIST(R7)
         LA    R7,2(R7)
         STH   R7,LCT
FULL0    EQU   *
         MVI   LINE,C' '
UCBHOLE0 LA    R6,2(R6)         INCREMENT POINTER
         B     NXTUCB0        GET NEXT UCB
FINIS0   EQU   *
         BR    R14
VOL      MVI   LINE,C' '
*        SORT THE DISKS BY VOLUME SERIAL AND PRINT THEM
         SR    R3,R3
SORTLOOP LH    R4,LIST(R3)
         LR    R5,R3
INLOOP   LH    R6,LIST(R5)
         CLC   28(6,R4),28(R6)   COMPARE VOL SER
         BNP   NOSWITCH
         STH   R4,LIST(R5)
         STH   R6,LIST(R3)
         LR    R4,R6
NOSWITCH LA    R5,2(R5)
         CH    R5,LCT
         BL    INLOOP
         LA    R3,2(R3)
         CH    R3,LCT
         BL    SORTLOOP
         TPUT  HDR1,80
         SR    R3,R3
PR2LOOP  SR    R4,R4
         LH    R5,LIST(R3)
         IC    R4,19(R5)
         SLA   R4,3
         LA    R4,UNITTYPE(R4)
         MVC   PRUNIT,0(R4)   MOVE UNIT TYPE TO PRINT LINE
         MVC   PRADDR,13(R5)   MOVE UNIT ADDRESS TO PRINT LINE
         MVC   PRSER,28(R5)    MOVE VOLUME SERIAL TO PRINT LINE
         TM    3(R5),X'08'     IS UCB REALLY ALLOCATED
         MVI   PRALOC,C'A'    SET DEFAULT OF ALLOCATED
         BO    TPUT           CONTINUE
         MVI   PRALOC,C'U'    NO, SAY IT IS UNALLOCATED
TPUT     TPUT  LINE,80
         LA    R3,2(R3)
         CH    R3,LCT
         BL    PR2LOOP
         BR    R14
RETURN   DS         0H
         L          R13,SAVE+4     RESTORE SAVE AREA POINTER
         RETURN     (14,12),T,RC=0 RETURN
SAVE     DS         18F
LINE     DC    CL4' '
PRSER    DC    CL6' '
         DC    CL6' '
PRADDR   DC    CL3' '
         DC    CL6' '
PRUNIT   DC    CL8' '
         DC    CL2' '
PRALOC   DC    CL1' '
BLANK    DC    CL44' '
UNITTYPE DC    CL8'    '      00
         DC    CL8'2311'      01
         DC    CL8'2301'      02
         DC    CL8'2303'      03
         DC    CL8'2302'      04
         DC    CL8'2321'      05
         DC    CL8'2305'      06
         DC    CL8'2305'      07
         DC    CL8'2314'      08
         DC    CL8'3330'      09
         DC    CL8'    '      0A
         DC    CL8'3350'      0B
         DC    CL8'    '      0C
         DC    CL8'3330-1'    0D
HDR1     DC CL80'   VOL SER     ADDR      UNIT    ALOC   MACHINE - '
         DS    0F
PCLADDR  DC    A(DSPPCL)
ECB      DS    F
PDLADDR  DS    F
PPLADDR  DS    7F
ZEROES   DC    2F'0'
LCT      DC    H'0'
LMAX     DC    H'2000'
LIST     DS    1000H
         DS    H
         EJECT
DSPPCL   IKJPARM
TYPE     IKJKEYWD DEFAULT='BOTH'
         IKJNAME 'BOTH'
         IKJNAME 'ADDRESS'
         IKJNAME 'VOLSER'
         IKJENDP
         EJECT
         IKJCPPL
         IKJPPL
         IKJPSCB
         REGS
         END   DISKLIST
