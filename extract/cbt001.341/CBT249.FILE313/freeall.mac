*
*   FREEALL WAS WRITTEN BY
*                             R P WENZEL
*                             OPTIMUM SYSTEMS INCORPORATED
*                             5615 FISHERS LANE
*                             ROCKVILLE, MARYLAND 20852
*                             (301) 468-1000 X249
*
*                             MARCH 24, 1976.
*
         EJECT
*)F FUNCTION -
*  THE FREEALL COMMAND WILL FREE ALL DYNAMICALLY ALLOCATED DATA SETS
*  AND ATTRIBUTE LISTS.  IT WILL NOT FREE DYNAMICALLY CONCATENATED
*  DATA SETS.
*)X SYNTAX -
*         FREEALL
*
*  ALIAS   - FRA
*)O OPERANDS -
*  THERE ARE NO OPERANDS ON THE FREEALL COMMAND.
         EJECT
         MACRO
         REGS
         LCLA  &R
.R       AIF   (&R EQ 16).END
R&R      EQU   &R
&R       SETA  &R+1
         AGO   .R
.END     MEND
         EJECT
FREEALL  CSECT
         REGS
         STM   R14,R12,12(R13)          STORE REGS
         LR    R12,R15                    SET UP PROGRAM BASE
         USING FREEALL,R12
         LR    R8,R1                    SAVE PARM ADDR
         USING CPPL,R8
         GETMAIN R,LV=LDYNAMIC          GET CORE
         LR    R11,R1
         USING WORKAREA,R11             SET UP ADDRESSABILITY
         LA    R9,SAVEAREA              CHAIN
         ST    R13,SAVEAREA+4             SAVE
         ST    R9,8(R13)                    AREAS
         LR    R13,R9
         L     R1,CPPLUPT               GET
         L     R2,CPPLECT                 PARMS
         LA    R3,ECB                      FROM
         L     R4,CPPLPSCB                  CPPL
         LA    R5,DAPB
         DROP  R8
         STM   R1,R5,DAPL               STORE PARMS FOR DAPL
         SR    R7,R7                    ZERO
         ST    R7,ECB                     THE ECB
         EJECT
         USING DSAB,R2
         L     R2,16          CVT ->
         L     R2,0(R2)       NEXT CURRENT ->
         L     R2,4(R2)       CURRENT TCB ->
         L     R2,180(R2)     JSCB ->
         L     R2,320(R2)     QDB -> (ALIAS DSAB)
         L     R3,8(R2)       NUMBER OF DSAB
         L     R2,16(R2)      LAST DSAB
LOOP     TM    DSABFLG1,X'80' DYNAMICALLY ALLOCATED?
         BZ    SKIP           NO SKIP
         TM    DSABFLG1,X'08' DYNAMICALLY CONCATENATED
         BO    SKIP           YES SKIP
         TM    DSABFLG2,X'80' OPENED?
         BO    SKIP           YES SKIP
         TM    DSABORG2,X'08' VSAM?
         BO    SKIP           YES SKIP
         TM    DSABFLG1,X'01' IN-USE ATTRIBUTE
         BO    SKIP           YES SKIP
         L     R4,DSABTIOT    TIOT DD ENTRY PTR
TESTATTR CLI   DSABORG1,X'00' ANY DSORG?
         BNE   TESTDD         MUST BE A DDNAME
         BAL   R10,FREEATTR   GO FREE ATTR NAME
         B     SKIP
TESTDD   BAL   R10,FREEDD     GO FREE DDNAME
SKIP     L     R2,DSABBCHN    BACK UP ANOTHER DSAB
         BCT   R3,LOOP
RETURN   LR    R1,R13                   DYNAMIC AREA ADDRESS IN R1
         LA    R0,LDYNAMIC              SIZE OF AREA
         L     R13,4(R13)               PREVIOUS SAVE AREA
         LA    R1,0(R1)                 CLEAR TOP BYTE
         SVC   10                       FREEMAIN
         LM    R14,R12,12(R13)
         SR    R15,R15
         MVI   12(R13),X'FF'
         BR    R14
         EJECT
FREEATTR EQU   *
         MVC   DAPB(DAPB34L),DAPB34C INITIALIZE DAPB
         MVC   DAPB+8(8),4(R4) ATTRIBUTE NAME
         LA    R1,DAPL        SET UP FOR DAIR
         LINK  EP=IKJDAIR     GO TO DAIR
         B     *+4(R15)       TEST RC
         BR    R10            WAS 0
         DC    20F'0'         PROGRAM CHECK IF NOT 0
FREEDD   EQU   *
         MVC   DAPB(DAPB34L),DAPB34C INITIALIZE DAPB
         MVC   DAPB+8(8),4(R4) DDNAME
         LA    R1,DAPL        SET UP FOR DAIR
         LINK  EP=IKJDAIR     GO TO DAIR
         B     *+4(R15)       TEST RC
         BR    R10            WAS 0
         DC    20F'0'         PROGRAM CHECK IF NOT 0
DAPB18C  DS    0F
         DC    X'0018'                  DA18CD
         DC    5H'0'                    DA18FLG, DARC, CTRC, PDSN
         DC    CL18' '                  DDN, MNM, SCLS
         DC    X'0010'                  DSP2 , CTL -RLSE PERM ALLOC
         DC    CL8' '                   JBNM
DAPB18L  EQU   *-DAPB18C                LENGTH
DAPB34C  DS    0F
         DC    X'0034'                  DA34CD
         DC    2H'0'                    DA34FLG, DARC
         DC    X'2000'                  DLTE AN ATRCB FRM CHAIN
         DC    CL8' '                   DA34NAME
         DC    2H'0'                    DA34ADDR
DAPB34L  EQU   *-DAPB34C                LENGTH
         EJECT
WORKAREA DSECT
SAVEAREA DS    18F
DAPB     DS    0F
         ORG   DAPB+DAPB34L
         ORG   DAPB+DAPB18L
         DS    0D
DAPL     DS    5F
ECB      DS    F
         DS    0D
LDYNAMIC EQU   *-WORKAREA
         EJECT
         IKJDAP18
         IKJCPPL
         IHADSAB
         END   FREEALL
