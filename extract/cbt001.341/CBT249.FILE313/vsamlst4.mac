         PRINT OFF
         MACRO
&NAME    INIT &BASE=3,&REGS=Y,&PATCH=3,&RENT=N,&SAVE=Y
         AIF   ((&BASE LT 13) AND (&BASE GT 1)).N020
         MNOTE 12,'INVALID BASE REGISTER'
         MEXIT
.N020    ANOP
         PUSH  PRINT
         PRINT ON,GEN
         EJECT
&SYSECT  CSECT
         USING *,15
         B     BEGIN
         DC    AL1(24)
         DC    CL8'&SYSECT'
         DC    CL16'-&SYSDATE-&SYSTIME'
         AIF   ('&RENT' EQ 'Y').N004
MYSAVE   DC    18F'-1'
.N004    ANOP
         AIF   ('&PATCH' EQ '0').N005
PATCH    DC    &PATCH.CL8'*PATCH*'
.N005    ANOP
         AIF   ('&REGS' EQ 'N').N030
         AIF   ('&REGS' EQ 'Y').N010
         MNOTE 4,'REGS OPERAND INVALID. Y SUBSTITUTED'
.N010    ANOP
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
.N030    ANOP
BEGIN    DS   0H
         STM   14,12,12(13)
         LR    &BASE,15
         DROP  15
         USING &SYSECT,&BASE
         AIF   ('&SAVE' EQ 'N').N003
         AIF   ('&RENT' EQ 'Y').N002
         AIF   ('&RENT' EQ 'N').N001
         MNOTE 4,'RENT OPERAND INVALID. N SUBSTITUTED'
.N001    ANOP
         ST    13,MYSAVE+4
         LR    15,13
         LA    13,MYSAVE
         ST    13,8(15)
         AGO   .N003
.N002    ANOP
         GETMAIN R,LV=72
         ST    13,4(1)
         ST    1,8(13)
         LR    13,1
.N003    ANOP
         POP   PRINT
         EJECT
         MEND
         PRINT ON
*  MODULE NAME:         VSAMLST4     (REL. 1.1  08/10/79)
*
*  MODULE DESCRIPTION:  VSAM CATALOG LIST UTILITY - DETAILS REPORT
*
*  RETURN LINKAGE:      RETURN (14,12),RC=0
*
*  LINKAGE TABLE:       NONE - SINGLE ENTRY
*
*  PARAMETERS:          R1 POINTS TO A PARAMETER LIST ALIGNED ON A
*                       FULL WORD BOUNDARY.
*                        WORD 1 - ADDRESS OF VSAM CATALOG NAME
*                        WORD 2 - ADDRESS OF CTLGPL-B
*                        WORD 3 - ADDRESS OF MSGFILE DCB
*                        WORD 4 - ADDRESS OF SORTIN DCB
*                        WORD 5 - ADDRESS OF WORK AREA 'B'
*
*  EXIT:                RC=00 - SUCCESSFUL
*
*  MODULE FUNCTION:     FOR EACH VSAM ENTRY SPECIFIED IN THE PARAMETER
*                       LIST, THIS MODULE FORMATS IT FOR THE VSAM
*                       VOLUME CONTENTS REPORT.  THE FORMATTED DATA
*                       WILL BE SORTED IN ASCENDING ORDER OF VOLUME
*                       SERIAL NUMBER AND THEN PRINTED VIA AN E15 SORT
*                       EXIT.  ONLY TYPES 'C', 'G' AND 'V' ARE
*                       PROCESSED BY THIS MODULE.
*
*  CALLER:              VSAMLST2
*
*  CALLS:               NONE
*
*  SYSTEMS SERVICES:    PUT (QSAM), SVC 26
*
*  MODULE ENVIRONMENT:  OS/VS1, OS/VS2
*                       NOTE - SEE DSECT UCBOB AT THE END OF SOURCE
*                       UCBVTOC HAS DIFFERENT OFFSETS IN VS1 AND MVS
         TITLE 'VSAMLST4 --- VSAM CATALOG LIST UTILITY DETAILS REPORTS'X
               ' DATA GENERATOR (2.2)'
*        *-----------------------------------*
*        *   R E G I S T E R   U S A G E S   *
*        *-----------------------------------*
*
*  R0  -  STANDARD LINKAGE
*  R1  -  STANDARD LINKAGE (ADDR OF PARM LIST) & TEMP WORK SPACE
*  R2  -  TEMP WORK SPACE
*  R3  -  TEMP WORK SPACE
*  R4  -  NOT USED
*  R5  -  NOT USED
*  R6  -  BASE REG FOR DSECT ETXDSCRP
*  R7  -  BASE REG FOR DSECT ETVOL
*  R8  -  BASE REG FOR DSECT NMDSECT, DSPDSCRP, DSCB4, DSCB5
*  R9  -  BASE REG FOR DSECT CTLGPLB
*  R10 -  BASE REG FOR DSECT TXVLCHR, TXDTAIL, TXEXTNT
*  R11 -  RETURN ADDR FOR SUBROUTINES
*  R12 -  BASE REG FOR CSECT VSAMLST4
*  R13 -  STANDARD LINKAGE (ADDR OF SAVE AREA)
*  R14 -  STANDARD LINKAGE (ADDR TO RETURN)
*  R15 -  STANDARD LINKAGE (ADDR OF ENTRY POINT & RETURN CODE)
         SPACE 3
VSAMLST4 CSECT
         INIT  BASE=12
***********************************************************************
*                                                                     *
*  2.2    DETAILS REPORT GENERATOR                                    *
*                                                                     *
*     FUNCTION:  IF TYPE 'V', SET VOLUME INFO'S FOR TITLES AND FREE   *
*                EXTENT INFO'S FOR TRACK ALLOCATION MAP.              *
*                IF TYPE 'C' OR 'G', FORMAT DETAIL INFO'S AND WRITE   *
*                IT OUT TO THE SORT INPUT DATA SET.                   *
*                                                                     *
*     ERRORS:  NONE                                                   *
*                                                                     *
***********************************************************************
         SPACE 3
VCL22    EQU   *
         MVC   PARM9422(20),0(R1)       SAVE 5 FULL WORDS PARM LIST
         LA    R10,LINE                 SET BASE REG FOR OUTPUT
         L     R9,ACTPL22               ADDR OF CTLGPL-B
         USING CTLGPLB,R9
         SPACE
         L     R1,CTGBAFL1              ADDR OF CTGFL-B1
         L     R1,20(,R1)               ADDR OF ENTRY TYPE
         MVC   ENTYPE,0(R1)             SAVE IT FOR CHECKING
         SR    R2,R2                    INDEX FOR BRANCH TABLE
         TRT   ENTYPE,RECTBL            SEE WHICH TYPE IT IS
         BZ    TYPE0                    TYPE 0 - UNDEFINED
         SPACE
         SLL   R2,2                     DISPLACEMENT OF BRANCH TABLE
RECTYP   B     RECTYP(R2)
         B     TYPE2                    TYPE 'C'
         B     TYPE2                    TYPE 'G'
         B     TYPE1                    TYPE 'V'
         EJECT
*        *******************************************************
*        *                                                     *
*        *  TYPE 1 - SET VOLUME CHAR'S AND FREE EXTENT INFO'S  *
*        *                                                     *
*        *******************************************************
         SPACE
TYPE1    EQU   *
         USING TXEXTNT,R10
         L     R1,CTGBAFL2              ADDR OF CTLGFL-B2
         L     R1,20(,R1)               ADDR OF ENTRY NAME
         ST    R1,CTGDENT
         MVC   CTGDCAT,CTGBCAT          ADDR OF CATACB
         MVC   CTGDWKB,AWORK22          ADDR OF WORK AREA 'B'
         MVC   CTGDPSWD,CTGBPSWD        ADDR OF PASSWORD
         SPACE
         LA    R1,CTLGPLD
         SVC   26
         SPACE
         LTR   R15,R15                  IS SVC SUCCESSFUL?
         BZ    SVCOK221                 YES - CONTINUE
         ABEND 221,DUMP
         SPACE
SVCOK221 EQU   *
         L     R1,CTLGFLD2+20           ADDR OF ENTNAME
         MVC   TXEVOLNO,0(R1)
         SPACE
         USING VOLDVCHR,R1
         L     R1,CTLGFLD3+20           ADDR OF VOLDVCHR
         MVC   TRKCYL,VOLDNTRK          SAVE TRK'S PER CYL
         DROP  R1
         SPACE
         MVC   VSFSNUM(6),ZERO          AND VSFSTRK & VSFSLGE
         MVC   OSFSNUM(8),ZERO          AND OSFSTRK & OSFSCYL & OSFSLGE
         EJECT
*        *------------------------------*
*        *  SET VSAM FREE SPACE INFO'S  *
*        *------------------------------*
         SPACE
SETVSFS  EQU   *
         USING DSPDSCRP,R8
         L     R0,CTLGFLD7+16           LENGTH OF DSPDSCRP(S)
         L     R8,CTLGFLD7+20           ADDR OF DSPDSCRP(S)
         AR    R0,R8                    ADDR OF END OF DSPDSCRP(S)
         ST    R0,EODSPX                UPPER LIMIT
         SPACE
NEXTDSPX EQU   *
         MVC   BEGINRCH,DSPXSTRT        GET THE BEGINNING ADDR IN CCHH
         LH    R1,BEGINCC               CYL OF BEGINNING ADDR
         MH    R1,TRKCYL                IN TRK'S
         AH    R1,BEGINHH               PLUS TRK OF BEGINNING ADDR
         STH   R1,BEGINTT               SAVE ITS RELATIVE TRACK ADDR
         SPACE
         MVC   TXELOTCH,DSPXSTRT        SET BEGINNING CCHH OF DSP EXT
         STH   R1,TXELOTRK              SET BEGINNING TT OF DSP EXT
         LH    R15,DSPTKEXT             LTH OF DSP EXT
         STH   R15,TXEXTLTH
         AR    R15,R1
         BCTR  R15,0                    UPPER = LOWER + LTH - 1
         STH   R15,TXEHITRK             SET ENDING TT OF DSP EXT
         SPACE
         LH    R0,TRKCYL                NO. OF TRK'S PER CYL
         SR    R14,R14
         DR    R14,R0                   CONVERT TT TO CCHH
         STH   R15,TXEHICC              CYL
         STH   R14,TXEHIHH              TRK
         MVC   TXERCKEY,ZERO            MAKE SURE THIS COMES FIRST
         MVI   TXEXTNM,X'00'            CLEAR EXT NAME
         MVC   TXEXTNM+1(L'TXEXTNM-1),TXEXTNM
         MVC   TXEXTNO,ZERO             EXT SEQ NO. (NOT USED)
         OI    TXERPTNO,X'80'           FOR REPORT 2
         SPACE
         L     R1,ASORT22               WRITE TO FILE SORTIN
         PUT   (1),LINE                 REPORT 2
         EJECT
*        *--------------------------------------*
*        *  SET VSAM FREE SPACE INFO'S - CONT.  *
*        *--------------------------------------*
         SPACE
         MVC   TXELOTCH,TXEHITCH        SET DUMMY EXT FOR END OF DSP
         MVC   TXERCKEY,FFFF            MAKE SURE THIS COMES LAST
         MVI   TXEXTNM,X'FF'            SET EXT NAME
         MVC   TXEXTNM+1(L'TXEXTNM-1),TXEXTNM
         MVC   TXELOTRK,TXEHITRK
         OI    TXERPTNO,X'80'           FOR REPORT 2
         SPACE
         L     R1,ASORT22               WRITE TO FILE SORTIN
         PUT   (1),LINE                 REPORT 2
         SPACE 2
         SR    R2,R2
         ICM   R2,3,DSPSMLTH            LTH OF RLC (RUN LENGTH CODE)
         LTR   R2,R2                    IS THERE A SPACE MAP?
         BZ    BUMPDSPX                 NO - BUMP TO NEXT DSPDSCRP
         CLC   DSPTUSED,DSPTKEXT        IS DATA SPACE ALL USED?
         BE    BUMPDSPX                 YES - NO FREE SPACE HERE
         SPACE
         LA    R7,DSPSMRLC              BEGINNING OF RLC
         MVC   TXEXTNM,VSFSNAM          * * *  VSAM FREE SPACE  * * *
         MVI   TXERCKEY+1,X'01'         ONLY DSP EXT HAS KEY X'0000'
         EJECT
*        *------------------------------*
*        *  DECODE THE SPACE MAP'S RLC  *
*        *------------------------------*
         SPACE
NEXTVSE  EQU   *
         SR    R3,R3
         CLI   0(R7),X'FA'              IS IT AN 1-BYTE RLC?
         BL    RLC1B1                   YES
RLC2B1   EQU   *
         ICM   R3,3,1(R7)               NO - IT'S A 2-BYTES RLC
         LA    R7,3(,R7)
         SH    R2,THREE
         B     USEDVSE                  USED VSAM EXT
RLC1B1   EQU   *
         IC    R3,0(,R7)                ONLY AN 1-BYTE RLC
         LA    R7,1(,R7)
         BCTR  R2,0
         SPACE
USEDVSE  EQU   *
         LH    R15,BEGINTT              BEGINNING OF EXTENT
         AR    R15,R3                   PLUS ITS LENGTH
         STH   R15,BEGINTT              FOR NEXT EXTENT
         LTR   R2,R2                    ARE THERE ANY MORE RLC'S?
         BZ    BUMPDSPX                 NO - PROCESS NEXT DSPDSCRP
         SPACE 2
         SR    R3,R3                    YES - IT MUST BE FOR FREE EXT
         CLI   0(R7),X'FA'              IS IT AN 1-BYTE RLC?
         BL    RLC1B2                   YES
RLC2B2   EQU   *
         ICM   R3,3,1(R7)               NO - IT'S A 2-BYTES RLC
         LA    R7,3(,R7)
         SH    R2,THREE
         B     FREEVSE                  FREE VSAM EXT
RLC1B2   EQU   *
         IC    R3,0(,R7)                ONLY AN 1-BYTE RLC
         LA    R7,1(,R7)
         BCTR  R2,0
         EJECT
*        *--------------------------------------*
*        *  FORMAT THE VSAM FREE SPACE EXTENTS  *
*        *--------------------------------------*
         SPACE
FREEVSE  EQU   *
         LH    R0,TRKCYL                NO. OF TRK'S PER CYL
         LH    R15,BEGINTT              BEGINNING ADDR IN RELATIVE TRK
         STH   R15,TXELOTRK
         SR    R14,R14
         DR    R14,R0                   CONVERT TT TO CCHH
         STH   R15,TXELOCC              CYL
         STH   R14,TXELOHH              TRK
         SPACE
         LH    R15,BEGINTT              BEGINNING OF EXTENT
         AR    R15,R3                   PLUS ITS LENGTH
         STH   R15,BEGINTT              FOR NEXT EXTENT
         BCTR  R15,0                    LESS ONE FOR THIS EXTENT'S END
         STH   R15,TXEHITRK
         SR    R14,R14
         DR    R14,R0                   CONVERT TT TO CCHH
         STH   R15,TXEHICC              CYL
         STH   R14,TXEHIHH              TRK
         SPACE
         LH    R1,VSFSNUM               SEQ NO. OF VSAM FREE EXT
         STH   R1,TXEXTNO
         LA    R1,1(,R1)                BUMP NO. OF VSAM FREE EXT'S
         STH   R1,VSFSNUM
         LH    R1,VSFSTRK               TOTAL NO. OF VSAM FREE TRK'S
         AR    R1,R3                    PLUS LENGTH OF THIS EXT
         STH   R1,VSFSTRK               BUMP TOTAL
         CH    R3,VSFSLGE               IS THIS THE LARGEST EXT SO FAR?
         BNH   NOTVSLGE                 NO - SKIP IT THEN
         STH   R3,VSFSLGE               YES - SWAP IT
         SPACE
NOTVSLGE EQU   *
         STH   R3,TXEXTLTH              LTH OF VSAM FREE EXT
         OI    TXERPTNO,X'80'           FOR REPORT 2
         EJECT
*        *-----------------------------------------*
*        *  WRITE OUT THE VSAM FREE SPACE EXTENTS  *
*        *-----------------------------------------*
         SPACE
         L     R1,ASORT22               WRITE TO FILE SORTIN
         PUT   (1),LINE                 REPORT 2
         SPACE
         LTR   R2,R2                    ARE THERE ANY MORE RLC'S?
         BNZ   NEXTVSE                  YES - PROCESS THEM
         SPACE 2
BUMPDSPX EQU   *
         SR    R0,R0
         ICM   R0,3,DSPSMLTH            BUMP PAST THE SPACE MAP
         LA    R8,DSPSMRLC              TO THE NEXT DSPDSCRP
         AR    R8,R0
         C     R8,EODSPX                IS IT STILL WITHIN LIMIT?
         BL    NEXTDSPX                 YES - KEEP ON GOING
         SPACE
         B     SETOSFS                  NO - ALL DONE WITH VSAM FREE SP
         DROP  R8
         SPACE 2
EODSPX   DC    F'-1'
         EJECT
*        *----------------------------*
*        *  SET OS FREE SPACE INFO'S  *
*        *----------------------------*
         SPACE
SETOSFS  EQU   *
         MVI   TXERCKEY+1,X'01'         ONLY DSP EXT HAS KEY '0000'
         SPACE
*        *-----------------------------------*
*        *  SEARCH THE UCB'S FOR THE VOLUME  *
*        *-----------------------------------*
         SPACE
         L     R2,16                    R2 = ADDR OF CVT
         L     R2,40(,R2)               R2 = ADDR OF UCB ADDR LIST
         USING UCBOB,R1
         SPACE
ADDRUCB  EQU   *
         LH    R1,0(,R2)                R1 = ADDR OF UCB
         CH    R1,FFFF                  IS THIS THE LAST ENTRY?
         BE    NOOSFS                   YES - NOT FOUND
         SPACE
         LTR   R1,R1                    IS THIS A NULL ENTRY?
         BZ    NEXTUCBP                 YES - NEXT UCB POINTER, PLEASE
         CLI   UCBTYP+2,X'20'           IS DEVICE TYPE DASD?
         BNE   NEXTUCBP                 NO - NEXT UCB POINTER, PLEASE
         TM    UCBSTAT,X'80'            IS DEVICE STATUS ON-LINE?
         BZ    NEXTUCBP                 NO - NEXT UCB POINTER, PLEASE
         CLC   UCBVOLI,TXEVOLNO         IS THIS THE RIGHT VOLUME?
         BE    UCBFOUND                 YES - STOP SEARCHING
         SPACE
NEXTUCBP EQU   *
         LA    R2,2(,R2)                R2 = ADDR OF NEXT UCB POINTER
         B     ADDRUCB                  LET'S DO IT AGAIN
         SPACE 2
UCBFOUND EQU   *
         MVC   VOLSTAB,UCBSTAB          SAVE VOLUME USE
         LH    R0,TRKCYL                NO. OF TRK'S PER CYL
         SR    R14,R14                  CONVERT TTRZ OF VTOC TO CCHHR
         LH    R15,UCBVTOC              R15 = TT OF TTRZ
         DR    R14,R0                   R14 = HH & R15 = CC
         STH   R15,F45CC
         STH   R14,F45HH
         MVC   F45R,UCBVTOC+2           R1 = R OF TTRZ
         ST    R10,F45VOLNO             SET VOLSER IN CAMLST
         SPACE
         DROP  R1
         EJECT
*        *---------------------------------------*
*        *  OBTAIN FMT 4 DSCB - SET VTOC EXTENT  *
*        *---------------------------------------*
         SPACE
FMT4     EQU   *
         USING DSCB4,R8
         LA    R8,F45DSCB               SET BASE REG FOR DSCB4 & DSCB5
         OBTAIN F45CMLST
         SPACE
         LTR   R15,R15                  IS OBTAIN SUCCESSFUL?
         BNZ   NOOSFS                   NO - SKIP ALL OS EXT'S
         CLI   DS4DSNAM,X'04'           MAKE SURE
         BNE   NOOSFS                   THIS IS A
         CLI   DS4IDFMT,X'F4'           VALID
         BNE   NOOSFS                   FMT 4 DSCB AND
         TM    DS4VTOCI,X'80'           IS THERE A VALID FMT 5 DSCB?
         BO    NOOSFS                   NO - EITHER NONE OR INVALID
         SPACE
         MVC   TXEXTNM,VTOCNAM          * * *  VTOC  * * *
         MVC   TXELOTCH,DS4VTLOR        LOW CCHH OF VTOC
         MVC   TXEHITCH,DS4VTUPR        HIGH CCHH OF VTOC
         SPACE
         DROP  R8
         EJECT
*        *----------------------------------------*
*        *  FORMAT AND WRITE OUT THE VTOC EXTENT  *
*        *----------------------------------------*
         SPACE
         LH    R2,TXELOCC               LOW CYL
         MH    R2,TRKCYL                TIMES TRK'S/CYL
         AH    R2,TXELOHH               PLUS LOW TRK
         STH   R2,TXELOTRK              LOWER LIMIT IN TRACKS
         SPACE
         LH    R3,TXEHICC               HIGH CYL
         MH    R3,TRKCYL                TIMES TRK'S/CYL
         AH    R3,TXEHIHH               PLUS HIGH TRK
         STH   R3,TXEHITRK              UPPER LIMIT IN TRACKS
         SPACE
         SR    R3,R2
         LA    R3,1(,R3)
         STH   R3,TXEXTLTH              TOTAL TRK'S = UPPER - LOWER + 1
         MVC   TXEXTNO,ZERO             EXT SEQ NO. = 0
         OI    TXERPTNO,X'80'           FOR REPORT 2
         L     R1,ASORT22               WRITE TO FILE SORTIN
         PUT   (1),LINE
         EJECT
*        *------------------------------------------*
*        *  OBTAIN FMT 5 DSCB - SET OS FREE EXTENT  *
*        *------------------------------------------*
         SPACE
         IC    R1,F45R                  1ST DSCB OF VTOC IS FMT 4 DSCB
         LA    R1,1(,R1)                2ND DSCB OF VTOC IS FMT 5 DSCB
         STC   R1,F45R                  GET READY TO OBTAIN
         SPACE
FMT5     EQU   *
         USING DSCB5,R8
         OBTAIN F45CMLST
         SPACE
         LTR   R15,R15                  IS OBTAIN SUCCESSFUL?
         BNZ   NOOSFS                   NO - SKIP ALL OS EXT'S
         CLI   DS5KEYID,X'05'           MAKE SURE
         BNE   NOOSFS                   THIS IS A
         CLI   DS5FMTID,X'F5'           VALID
         BNE   NOOSFS                   FMT 5 DSCB
         SPACE
         MVC   TXEXTNM,OSFSNAM          * * *  OS FREE SPACE  * * *
         LA    R7,DS5AVEXT              SKIP KEY IDENTIFICATION
         LA    R2,8                     PROCESS THE FIRST 8 EXTENTS
NXTF5EX1 EQU   *
         CLC   0(5,R7),ZERO             IS THIS A GOOD EXTENT?
         BE    LASTF5EX                 NO - WRAP IT UP THEN
         BAL   R11,FREEEXT              YES - PROCESS THIS FREE EXTENT
         LA    R7,5(,R7)                NEXT FREE EXTENT, PLEASE.
         BCT   R2,NXTF5EX1
         SPACE
         LA    R7,DS5MAVET              SKIP FMT 5 IDENTIFIER
         LA    R2,18                    PROCESS THE NEXT 18 EXTENTS
NXTF5EX2 EQU   *
         CLC   0(5,R7),ZERO             IS THIS A GOOD EXTENT?
         BE    LASTF5EX                 NO - WRAP IT UP THEN
         BAL   R11,FREEEXT              YES - PROCESS THIS FREE EXTENT
         LA    R7,5(,R7)                NEXT FREE EXTENT, PLEASE.
         BCT   R2,NXTF5EX2
         SPACE
LASTF5EX EQU   *
         CLC   DS5PTRDS,ZERO            IS THERE ANOTHER FMT 5 DSCB?
         BZ    SETVCHR                  NO - ALL DONE WITH OS FREE EXT
         SPACE
         MVC   F45CCHHR,DS5PTRDS        ADDR OF TNE NEXT FMT 5 DSCB
         B     FMT5                     DO IT AGAIN
         DROP  R8
         EJECT
*        *--------------------------------------*
*        *  OS FREE SPACE INFO'S NOT AVAILABLE  *
*        *--------------------------------------*
         SPACE
NOOSFS   EQU   *
         MVC   OSFSNUM,FFFF             OS SPACE INFO'S NOT AVAILABLE
         MVC   MSG01VL(L'TXEVOLNO),TXEVOLNO
         SPACE
         L     R1,AMSGF22               WRITE ERROR MESSAGE
         PUT   (1),MSG01
         SPACE
         B     SETVCHR                  SET VOL CHAR
         EJECT
*        *---------------------------------------*
*        *  PROCESS FREE EXTENT                  *
*        *  IN:   R7 = ADDR OF EXTENT            *
*        *  OUT:  WRITE OS FREE EXT TO REPORT 2  *
*        *---------------------------------------*
         SPACE
FREEEXT  EQU   *
         MVC   DFXEXT,0(R7)             ALIGN FREE EXTENT DESCRIPTOR
         LH    R1,OSFSNUM               SEQ NO. OF OS FREE EXT
         STH   R1,TXEXTNO
         LA    R1,1(,R1)                BUMP NO. OF OS FREE EXT'S
         STH   R1,OSFSNUM
         SPACE
         LH    R1,DFXCYL                NO. OF FREE CYL'S IN THIS EXT
         LR    R0,R1                    SAVE IT TO CALC LTH IN TRK'S
         AH    R1,OSFSCYL
         STH   R1,OSFSCYL               TOTAL NO. OF UNUSED CYL'S
         SPACE
         MH    R0,TRKCYL                UNUSED CYL'S IN TRK'S
         SR    R1,R1
         IC    R1,DFXTRK                ADDITIONAL TRK'S IF ANY
         AR    R1,R0
         STH   R1,TXEXTLTH              TOTAL NO. OF TRK'S IN THIS EXT
         CH    R1,OSFSLGE               IS THIS THE LARGEST EXT SO FAR?
         BNH   NOTOSLGE                 NO - SKIP IT THEN
         STH   R1,OSFSLGE               YES - SWAP IT
         SPACE
NOTOSLGE EQU   *
         AH    R1,OSFSTRK
         STH   R1,OSFSTRK               TOTAL NO. OF UNUSED TRK'S
         EJECT
*        *------------------------------------*
*        *  FORMAT THE OS FREE SPACE EXTENTS  *
*        *------------------------------------*
         SPACE
         LH    R0,TRKCYL                NO. OF TRK'S PER CYL
         LH    R15,DFXRTA               BEGINNING ADDR IN RELATIVE TRK
         STH   R15,TXELOTRK
         SR    R14,R14
         DR    R14,R0                   CONVERT TT TO CCHH
         STH   R15,TXELOCC              CYL
         STH   R14,TXELOHH              TRK
         SPACE
         LH    R15,DFXRTA               BEGINNING OF EXTENT
         AH    R15,TXEXTLTH             PLUS ITS LENGTH
         BCTR  R15,0                    LESS ONE FOR THIS EXTENT'S END
         STH   R15,TXEHITRK
         SR    R14,R14
         DR    R14,R0                   CONVERT TT TO CCHH
         STH   R15,TXEHICC              CYL
         STH   R14,TXEHIHH              TRK
         SPACE
         OI    TXERPTNO,X'80'           FOR REPORT 2
         L     R1,ASORT22               WRITE TO FILE SORTIN
         PUT   (1),LINE
         SPACE
         BR    R11                      RETURN
         EJECT
*        *-------------------------*
*        *  SET VOL CHAR'S INFO'S  *
*        *-------------------------*
         SPACE
SETVCHR  EQU   *
         DROP  R10
         USING TXVLCHR,R10
         MVC   TXVRCKEY,ZERO            CLEAR SORT KEY
         SR    R1,R1
         IC    R1,VOLSTAB               VOLUME USE (UCBSTAB)
         STH   R1,TXVOLUSE              FIRST BYTE NOT USED
         SPACE
         USING VOLDVCHR,R1
         L     R1,CTLGFLD3+20           ADDR OF VOLDVCHR
         MVC   TXVDVTYP,VOLDVTYP
         MVC   TXVCYLVL,VOLDNCYL
         MVC   TXVTRKCL,VOLDNTRK
         MVC   TXVBYTTK,VOLDNBYT
         DROP  R1
         SPACE
         L     R1,CTLGFLD6+20           ADDR OF NODSET
         MVC   TXVNODST,0(R1)
         L     R1,CTLGFLD5+20           ADDR OF NODSPACE
         MVC   TXVNODSP,0(R1)
         L     R1,CTLGFLD4+20           ADDR OF SYSEXTDS
         MVI   TXVSYSXT,X'00'
         MVC   TXVSYSXT+1(1),0(R1)
         SPACE
         L     R1,ACATN22               ADDR OF CATLG NAME
         MVC   TXVCATNM,0(R1)
         SPACE
         MVC   TXVVSEXT,VSFSNUM         NO. OF VSAM FREE EXT'S
         MVC   TXVVSTRK,VSFSTRK         NO. OF VSAM FREE TRK'S
         MVC   TXVVSLGE,VSFSLGE         LTH OF LARGEST VSAM FREE EXT
         SPACE
         MVC   TXVOSEXT,OSFSNUM         NO. OF OS FREE EXT'S
         MVC   TXVOSTRK,OSFSTRK         NO. OF OS FREE TRK'S
         MVC   TXVOSCYL,OSFSCYL         NO. OF OS FREE CYL'S
         MVC   TXVOSLGE,OSFSLGE         LTH OF LARGEST OS FREE EXT
         EJECT
*        *---------------------------------------------------*
*        *  WRITE OUT DETAIL VOL INFO'S & REPORT SEPARATORS  *
*        *---------------------------------------------------*
         SPACE
         L     R1,ASORT22
         PUT   (1),LINE                 FOR REPORT 1
         SPACE
         OI    TXVRPTNO,X'80'
         L     R1,ASORT22
         PUT   (1),LINE                 REPORT 2
         SPACE
         MVI   TXVRCKEY,X'FF'           LAST RECORD FOR REPORT 2
         MVC   TXVRCKEY+1(L'LINE-L'TXVVOLNO-1),TXVRCKEY
         L     R1,ASORT22
         PUT   (1),LINE                 REPORT 2
         SPACE
         NI    TXVRPTNO,X'7F'           LAST RECORD FOR REPORT 1
         L     R1,ASORT22
         PUT   (1),LINE                 REPORT 1
         SPACE
         MVI   LINE,X'00'               CLEAR LINE
         MVC   LINE+1(L'LINE-1),LINE
         B     GOHOME                   AND RETURN
         EJECT
*        *************************************************************
*        *                                                           *
*        *  TYPE 2 - SET DETAIL INFO'S FOR TYPE 'C' & 'G' & ASSOC'S  *
*        *                                                           *
*        *************************************************************
         SPACE
TYPE2    EQU  *
         DROP  R10
         USING TXDTAIL,R10
         MVC   CTGECAT,CTGBCAT          ADDR OF CATACB
         MVC   CTGEWKB,AWORK22          ADDR OF WORK AREA 'B'
         MVC   CTGEPSWD,CTGBPSWD        ADDR OF PASSWORD
         L     R1,RPT0KEY               SET KEY FOR FLD TXDRCKEY
         LA    R1,1(,R1)
         ST    R1,RPT0KEY
         SPACE
         SR    R1,R1
         STH   R1,RPT0VLN               RESET NO. OF VOL'S ALLOCATED
         MVI   RPT0VLS,X'40'            CLEAR ALL 16 VOL SERIAL NUMBERS
         MVC   RPT0VLS+1(16*L'RPT0VLS-1),RPT0VLS
         SPACE 2
         USING NMDSECT,R8
         L     R1,CTGBAFL4              ADDR OF CTLGFL-B4
         L     R0,16(,R1)               LENGTH OF NAMEDS(ES)
         L     R8,20(,R1)               ADDR OF NAMEDS(ES)
         AR    R0,R8                    ADDR OF END OF NAMEDS(ES)
         ST    R0,EONMDS                UPPER LIMIT
         SPACE
NEXTNMDS EQU   *
         LH    R1,RPT0SEQ               INCREMENT RPT0SEQ
         LA    R1,1(,R1)
         STH   R1,RPT0SEQ
         SPACE
         LA    R1,NMDSCIN               ADDR OF CI NUMBER
         ST    R1,CTGEENT               SET CTLGPL-E
         MVC   CTGERCTP,NMDSTYP         ENTRY TYPE
         SPACE
         CLI   NMDSTYP,C'D'             IS IT FOR A TYPE 'D' ENTRY?
         BE    SETVLXT                  YES - SET VOL'S & EXT'S
         CLI   NMDSTYP,C'I'             IS IT FOR A TYPE 'I' ENTRY?
         BE    SETVLXT                  YES - SET VOL'S & EXT'S
         SPACE
*        *--------------------------------------------------------*
*        *  ASSUMING THE ORDER OF NAMEDS AS FOLLOWS, D, I, G, R.  *
*        *  THE ENTVOL FLD'S OF TYPES 'D' AND 'I' DETERMINE THE   *
*        *  VOL'S W/ ALLOC FOR THE OTHER ENTRIES.                 *
*        *--------------------------------------------------------*
         EJECT
*        *----------------------------------------------------------*
*        *  NAMEDS OF ASSOCIATED ENTRY - NEITHER TYPE 'D' NOR 'I'   *
*        *  WRITE THIS ENTRY'S NAME AND TYPE FOR EACH VOL W/ ALLOC  *
*        *----------------------------------------------------------*
         SPACE
         LH    R2,RPT0VLN               NO. OF VOL'S W/ ALLOC
         LTR   R2,R2                    ARE THERE ANY VOL'S?
         BZ    GOHOME                   NO - QUIT FOR THIS 'C' OR 'G'
         SPACE
         LA    R3,RPT0VLS               1ST VOL W/ ALLOC
         MVI   CTGEFLDN,X'02'           ENTYPE & ENTNAME ONLY
         LA    R1,CTLGPLE
         SVC   26
         SPACE
         LTR   R15,R15                  IS SVC SUCCESSFUL?
         BZ    SVCOK222                 YES - CONTINUE
         ABEND 222,DUMP
         SPACE
SVCOK222 EQU   *
         MVI   TXDDSORG,X'FF'           INDICATE AN ASSOCIATED ENTRY
         L     R1,CTLGFLE1+20           ENTYPE
         MVC   TXDENTYP,0(R1)
         L     R1,CTLGFLE2+20           ENTNAME
         MVC   TXDENTNM,0(R1)
         MVC   TXDRPTNO(L'TXDRCKEY+L'TXDRCSEQ),RPT0KEY
         SPACE
NEXTNCG  EQU   *
         MVC   TXDVOLNO,0(R3)           VOLUME SERIAL NUMBER
         L     R1,ASORT22
         PUT   (1),LINE
         SPACE
         LA    R3,6(,R3)                NEXT VOLUME PLEASE
         BCT   R2,NEXTNCG               FOR THIS NON-'C' OR NON-'G' ENT
         SPACE
         B     BUMPNMDS                 NEXT NAMEDS FLD
         EJECT
*        *----------------------------------------------------*
*        *  NAMEDS OF TYPE 'D' & 'I' - NEED MORE THAN A NAME  *
*        *  SET INFO'S OF EXTENTS AND VOLUMES                 *
*        *----------------------------------------------------*
         SPACE
SETVLXT  EQU   *
         MVI   CTGEFLDN,X'09'           ALL THE INFO'S
         LA    R1,CTLGPLE
         SVC   26
         SPACE
         LTR   R15,R15                  IS SVC SUCCESSFUL?
         BZ    SVCOK223                 YES - CONTINUE
         ABEND 223,DUMP
         SPACE
SVCOK223 EQU   *
         USING ETVOL,R7
         L     R0,CTLGFLE7+16           LENGTH OF ENTVOL(S)
         L     R7,CTLGFLE7+20           ADDR OF ENTVOL(S)
         AR    R0,R7                    ADDR OF END OF ENTVOL(S)
         ST    R0,EOENTV                UPPER LIMIT
         SPACE
NEXTENTV EQU   *
         SR    R1,R1
         LA    R6,ETVLOKEY              SET BASE REG FOR DSECT ETXDSCRP
         ICM   R1,3,0(R6)
         LA    R6,2(R1,R6)              BUMP LO KEY
         ICM   R1,3,0(R6)
         LA    R6,2(R1,R6)              BUMP HI KEY
         ICM   R1,3,0(R6)               R1 = ETVXTLTH
         LA    R6,2(,R6)                R6 = A(ETVXTENT)
         LTR   R1,R1                    ARE THERE ANY EXTENTS?
         BNZ   HAVEXTS                  YES - PROCESS THEM
         ST    R6,NXENTV                NO - R6 HAS ADDR OF NEXT ENTVOL
         B     BUMPENTV                 PROCESS NEXT ENTVOL
         SPACE
HAVEXTS  EQU   *
         AR    R1,R6                    R1 HAS ADDR OF NEXT ENTVOL
         ST    R1,NXENTV
         TM    ETVOLFLG,X'80'           IS THIS A PRIME VOLUME?
         BZ    BUMPENTV                 NO - SKIP IT
         TM    ETVITYPE,X'80'           IS INDEX'ES SEQ SET W/ DATA?
         BO    BUMPENTV                 YES - SKIP IT
         EJECT
*        *-----------------------------------------------------------*
*        *  THIS VOL'S LOOKIN' GOOD - ADD IT TO THE LIST OF VOLUMES  *
*        *-----------------------------------------------------------*
         SPACE
         LH    R2,RPT0VLN               GET NUMBER OF VOLUMES ON LIST
         LA    R3,RPT0VLS               GET ADDR OF LIST OF VOLUMES
         SPACE
CHKRPT0V EQU   *
         CLI   0(R3),X'40'              IS THIS THE END OF LIST?
         BE    ADDRPT0V                 YES - ADD A NEW ONE
         CLC   ETVOLSER,0(R3)           IS THIS VOLUME ON LIST ALREADY?
         BE    DONTADDV                 YES - DON'T ADD TO THE LIST
         LA    R3,L'RPT0VLS(,R3)        BUMP TO NEXT VOLUME ON LIST
         BCT   R2,CHKRPT0V              AND KEEP CHECKING
         SPACE
ADDRPT0V EQU   *
         MVC   0(L'ETVOLSER,R3),ETVOLSER
         LH    R2,RPT0VLN
         LA    R2,1(,R2)
         STH   R2,RPT0VLN               NEW VOLUME ADDED TO LIST
DONTADDV EQU   *
         SPACE 2
*        *-----------------------------*
*        *  NOW CHECK OUT ITS EXTENTS  *
*        *-----------------------------*
         SPACE
         USING ETXDSCRP,R6
         MVC   TXDVOLNO,ETVOLSER        SAME AS SETTING TXEVOLNO
         MVC   TXDRCSEQ,ONE             SAME AS SETTING TXERCKEY
         L     R1,CTLGFLE2+20           ADDR OF ENTNAME
         MVC   TXDENTNM,0(R1)           SAME AS SETTING TXENTNM
         MVC   EXTXNUM(4),ZERO          CLEAR EXTXNUM & EXTXTRK
         MVC   EXTXLOR,ETXLORBA         LOW RBA OF THE 1ST EXTENT
         SPACE
NEXTETXD EQU   *
         BAL   R11,SETEXTS
         LA    R6,L'ETVXTENT(,R6)
         C     R6,NXENTV
         BL    NEXTETXD
         SPACE
         BAL   R11,SETDTLS              ALL EXT'S DONE, SET UP THE REST
         SPACE 2
BUMPENTV EQU   *
         L     R7,NXENTV                ADDR OF NEXT ENTVOL
         C     R7,EOENTV                IS IT STILL WITHIN LIMIT?
         BL    NEXTENTV                 YES - KEEP ON KEEPING ON
         B     BUMPNMDS                 NO - ALL DONE WITH THIS ENTRY
         SPACE 2
EOENTV   DC    F'-1'
NXENTV   DC    F'-1'
         EJECT
*        *------------------------------------------------------*
*        *  BUMP TO NEXT NAMEDS FIELD AND CONTINUE PROCESSING.  *
*        *  IF ALL DONE, DO THE TYPE 'C' OR 'G' AND QUIT THEN.  *
*        *------------------------------------------------------*
         SPACE
BUMPNMDS EQU   *
         LA    R8,4(,R8)                ADD LENGTH OF NAMEDS
         C     R8,EONMDS                IS IT STILL WITHIN LIMIT?
         BL    NEXTNMDS                 YES - KEEP ON KEEPING ON
         SPACE
         LH    R2,RPT0VLN               NO. OF VOL'S W/ ALLOC
         LTR   R2,R2                    ARE THERE ANY VOL'S?
         BZ    GOHOME                   NO - QUIT FOR THIS 'C' OR 'G'
         SPACE
         LA    R3,RPT0VLS               1ST VOL W/ ALLOC
         MVC   TXDDSORG,ENTORG          AMDATTR
         L     R1,CTGBAFL5
         L     R1,20(,R1)
         MVC   TXDEXPDT,0(R1)           DSETEXDT
         L     R1,CTGBAFL6
         L     R1,20(,R1)
         MVC   TXDCREDT,0(R1)           DSETCRDT
         SPACE
         L     R1,CTGBAFL1
         L     R1,20(,R1)
         MVC   TXDENTYP,0(R1)           ENTYPE
         L     R1,CTGBAFL2
         L     R1,20(,R1)
         MVC   TXDENTNM,0(R1)           ENTNAME
         MVC   RPT0SEQ,ZERO             RESET SEQ NO. TO ZERO
         MVC   TXDRPTNO(L'TXDRCKEY+L'TXDRCSEQ),RPT0KEY
         SPACE
NEXTCG   EQU   *
         MVC   TXDVOLNO,0(R3)           VOLUME SERIAL NUMBER
         L     R1,ASORT22
         PUT   (1),LINE
         SPACE
         LA    R3,6(,R3)                NEXT VOLUME PLEASE
         BCT   R2,NEXTCG                FOR THIS 'C' OR 'G' ENT
         SPACE
         MVI   LINE,X'00'               CLEAR LINE
         MVC   LINE+1(L'LINE-1),LINE
         B     GOHOME
         SPACE 2
EONMDS   DC    F'-1'
         EJECT
*        *-----------------------------------------*
*        *  SET EXTENTS' INFO'S                    *
*        *  IN:    R6 POINTS TO EXTENT DESCRIPTOR  *
*        *  FUNC:  FORMAT, ACCUMULATE AND WRITE    *
*        *  OUT:   NONE                            *
*        *-----------------------------------------*
         SPACE
SETEXTS  EQU   *
         DROP  R10
         USING TXEXTNT,R10
         MVC   TXELOTCH,ETXLOTRK        SET SORT KEY (LO TRK ADDR)
         OI    TXERPTNO,X'80'           FOR REPORT 2
         MVC   TXEHITCH,ETXHITRK        HI TRK ADDR OF EXT
         LH    R1,ETXNOTRK
         STH   R1,TXEXTLTH              LENGTH OF EXT
         AH    R1,EXTXTRK               TOTAL TRK'S ON THIS VOL
         STH   R1,EXTXTRK               ALLOCATED
         SPACE
         LH    R1,EXTXNUM               NUMBER OF EXT
         STH   R1,TXEXTNO               SEQ NO OF EXT
         LA    R1,1(,R1)                BUMP IT BY ONE
         STH   R1,EXTXNUM
         SPACE
         L     R1,ASORT22
         PUT   (1),LINE
         SPACE
         BR    R11                      RETURN
         EJECT
*        *-----------------------------------------*
*        *  SET DETAILS' INFO'S                    *
*        *  IN:    R7 POINTS TO ENTVOL STILL       *
*        *  FUNC:  FORMAT THE REMAINING AND WRITE  *
*        *  OUT:   NONE                            *
*        *-----------------------------------------*
         SPACE
SETDTLS  EQU   *
         DROP  R10
         USING TXDTAIL,R10
         MVC   TXDRPTNO(L'TXDRCKEY+L'TXDRCSEQ),RPT0KEY
         L     R1,CTLGFLE1+20           ADDR OF ENTYPE
         MVC   TXDENTYP,0(R1)
         L     R1,CTLGFLE4+20           ADDR OF DSETCRDT
         MVC   TXDCREDT,0(R1)
         L     R1,CTLGFLE3+20           ADDR OF DSETEXDT
         MVC   TXDEXPDT,0(R1)
         L     R1,CTLGFLE5+20           ADDR OF CRAVOL
         MVC   TXDCRAVL,0(R1)
         SPACE
         USING SPACPR,R1
         L     R1,CTLGFLE6+20           ADDR OF SPACPARM
         MVC   TXDSCALC,SPACSCON
         MVC   TXDSCTYP,SPACOPTN
         DROP  R1
         SPACE
         USING AMDSB,R1
         L     R1,CTLGFLE8+20           ADDR OF AMDSBCAT
         CLI   NMDSTYP,C'D'             IS THIS TYPE A 'D' ENTRY?
         BNE   SETAMDS                  NO - MUST BE A TYPE 'I'
         MVC   ENTORG,AMDATTR           YES - SAVE IT FOR 'CL' OR 'AIX'
SETAMDS  EQU   *
         MVC   TXDDSORG,AMDATTR
         MVC   TXDCISIZ,AMDCINV
         MVC   TXDMAXRC,AMDLRECL
         DROP  R1
         SPACE
         L     R1,CTLGFLE9+20           ADDR OF LRECL
         MVC   TXDAVGRC,0(R1)
         EJECT
*        *---------------------------------------*
*        *   A L L O C A T I O N   I N F O ' S   *
*        *---------------------------------------*
         SPACE
         SR    R2,R2
         ICM   R3,15,ETVHURBA           HIGH-USED RBA IN ENTVOL
         BNP   NOTUSED                  S/B POSITIVE
         ICM   R1,15,ETVHARBA           HIGH-ALLOCATED RBA IN ENTVOL
         BNP   NOTUSED                  S/B POSITIVE
         CLR   R1,R3                    ARE THEY THE SAME?
         BNH   ALLUSED                  YES - ALL ALLOC'D ARE USED
         SPACE
         S     R1,EXTXLOR               ADJUST HARBA
         BNP   NOTUSED                  S/B POSITIVE
         S     R3,EXTXLOR               ADJUST HURBA
         BNP   NOTUSED                  S/B POSITIVE
         SPACE
         LR    R0,R3                    SAVE HURBA FOR A WHILE
         M     R2,HUNDRED
         DR    R2,R1                    R3 = ( HURBA * 100 ) / HARBA
         STC   R3,TXDPCTUS              PERCENTAGE OF TRACKS USED
         SPACE
         SR    R2,R2
         LR    R3,R0                    RESTORE HURBA
         LH    R0,EXTXTRK               TOTAL NO. OF TRK'S ALLOCATED
         MR    R2,R0
         DR    R2,R1                    R3 = ALLOC * ( HURBA / HARBA )
         LTR   R2,R2                    IS THERE A FRACTION OF A TRACK?
         BZ    NOEXTRA                  NO - NO NEED TO ROUND IT UP
         LA    R3,1(,R3)                YES - ONE MORE
NOEXTRA  EQU   *
         STH   R3,TXDTRKUS              TOTAL NO. OF TRK'S USED
         B     SETALLC
         EJECT
*        *-------------------------------------------------------*
*        *   A L L O C A T I O N   I N F O ' S   -   C O N T .   *
*        *-------------------------------------------------------*
         SPACE
NOTUSED  EQU   *
         MVI   TXDPCTUS,0               ZERO PERCENTAGE USED
         STH   R2,TXDTRKUS              R2 S/B ZERO
         B     SETALLC
         SPACE
ALLUSED  EQU   *
         MVI   TXDPCTUS,100             A HUNDRED PERCENTS USED
         MVC   TXDTRKUS,EXTXTRK         TRK'S USED SAME AS ALLOC'D
         SPACE
SETALLC  EQU   *
         MVC   TXDTRKAL,EXTXTRK         NO. OF TRK'S ALLOC'D ON VOL
         MVC   TXDNOEXT,EXTXNUM+1       NO. OF EXT'S FOR ENT ON VOL
         L     R1,ASORT22
         PUT   (1),LINE
         SPACE
         MVI   LINE,X'00'               CLEAR LINE
         MVC   LINE+1(L'LINE-1),LINE
         BR    R11                      RETURN FROM SETDTLS
         EJECT
*        ***************
*        *             *
*        *   E X I T   *
*        *             *
*        ***************
         SPACE
TYPE0    EQU   *
GOHOME   EQU   *
         L     R13,MYSAVE+4
         RETURN (14,12),RC=0
         SPACE 2
         LTORG
         SPACE
         DS    0H
         EJECT
*        *-----------------------*
*        *   V A R I A B L E S   *
*        *-----------------------*
         SPACE
RPT0KEY  DC    F'0'                     COUNTER FOR TYPE 'C' & 'G' ENT
RPT0SEQ  DC    H'0'                     SEQ NO. FOR NAMEDS FLD'S
RPT0VLN  DC    H'0'                     NO. OF VOL'S W/ ALLOC
RPT0VLS  DC    16CL6'VOLSER'            MAX OF 16 VOL'S
         SPACE
EXTXLOR  DC    F'0'                     LOWEST RBA IN ENTVOL
EXTXNUM  DC    H'0'                     NO. OF EXT'S FOR ENT ON VOL
EXTXTRK  DC    H'0'                     NO. OF TRK'S ALLOC'D FOR ENT/V
         SPACE
VSFSNUM  DC    H'0'                     NO. OF VSAM FREE SPACE EXT'S
VSFSTRK  DC    H'0'                     NO. OF VSAM FREE SPACE TRK'S
VSFSLGE  DC    H'0'                     NO. OF TRK'S IN LARGEST VSFS
         SPACE
OSFSNUM  DC    H'0'                     NO. OF OS FREE SPACE EXT'S
OSFSTRK  DC    H'0'                     NO. OF OS FREE SPACE TRK'S
OSFSCYL  DC    H'0'                     NO. OF OS FREE SPACE CYL'S
OSFSLGE  DC    H'0'                     NO. OF TRK'S IN LARGEST VSFS
         SPACE
TRKCYL   DC    H'0'                     NO. OF TRK'S PER CYL
BEGINTT  DC    H'0'                     BEGINNING IN RELATIVE TRK ADDR
BEGINRCH DS    0CL4                     BEGINNING ADDR IN CCHH
BEGINCC  DC    H'0'                     BEGINNING CYL
BEGINHH  DS    H'0'                     BEGINNING TRK
         SPACE
ENTYPE   DC    X'40'                    ENTRY TYPE
ENTORG   DC    X'00'                    ENTRY ORGANIZATION
VOLSTAB  DC    X'00'                    VOLUME USE (UCBSTAB)
         DS    0H
LINE     DC    XL92'00'
         EJECT
*        *---------------------------------------------------------*
*        *   T O   O B T A I N   F M T   4   &   5   D S C B ' S   *
*        *---------------------------------------------------------*
         SPACE
F45CMLST CAMLST SEEK,F45CCHHR,0,F45DSCB
         SPACE
         ORG   F45CMLST+8
F45VOLNO DS    A
         ORG
         SPACE
F45CCHHR DS    0CL5
F45CC    DC    H'0'
F45HH    DC    H'0'
F45R     DC    X'00'
         DS    0H
         SPACE
DFXEXT   DS    0CL5
DFXRTA   DC    H'0'                     RELATIVE TRK ADDR OF FREE EXT
DFXCYL   DC    H'0'                     NO. OF UNUSED CYL'S IN FREE EXT
DFXTRK   DC    X'00'                    NO. OF UNUSED TRK'S BESIDES CYL
         SPACE
F45DSCB  DS    0D
         DC    CL148' '
         EJECT
*        *-----------------------*
*        *   C O N S T A N T S   *
*        *-----------------------*
         SPACE
RECTBL   DC    256X'00'                 TYPE 0 - UNDEFINED
         ORG   RECTBL+C'C'
         DC    X'01'                    TYPE 2 - CLUSTER
         ORG   RECTBL+C'G'
         DC    X'02'                    TYPE 2 - ALERNATE INDEX
         ORG   RECTBL+C'V'
         DC    X'03'                    TYPE 1 - SPACE
         ORG
         SPACE 2
HEXNUM   DC    CL16'0123456789ABCDEF'
HEXTBL   EQU   HEXNUM-240
         SPACE
ZERO     DC    D'0'
ONE      DC    H'1'
THREE    DC    H'3'
FFFF     DC    H'-1'
HUNDRED  DC    F'100'
VSFSNAM  DC    CL44'* * *  VSAM FREE SPACE  * * *'
OSFSNAM  DC    CL44'* * *  OS FREE SPACE  * * *'
VTOCNAM  DC    CL44'* * *  VTOC  * * *'
         SPACE
MSG01    DC    X'40',CL132'VSAMLST4---01 OS FREE SPACE INFORMATION NOT X
               AVAILABLE FOR VOLUME XXXXXX'
MSG01VL  EQU   MSG01+65
         SPACE 2
*        *-------------------------*
*        *   P A R M   L I S T S   *
*        *-------------------------*
         SPACE
PARM9422 DS    0F
ACATN22  DC    A(0)
ACTPL22  DC    A(0)
AMSGF22  DC    A(0)
ASORT22  DC    A(0)
AWORK22  DC    A(0)
         EJECT
*        *---------------------------------------------*
*        *   C T L G P L   A N D   C T G F L   -   D   *
*        *---------------------------------------------*
         SPACE
CTLGPLD  DS    0D
CTGDOPT1 DC    X'44008108'
*  CHECK THE MASTER PASSWORD
*  CTGENT CONTAINS ADDR OF ENTRY NAME
*  A CATALOG MANAGEMENT SERVICES FUNCTION
*  THE CALL IS A VSAM CATALOG MANAGEMENT REQUEST
*  BYPASS SECURITY PROMPTING TO SYSTEM OPERATOR
*
CTGDENT  DC    A(0)
CTGDCAT  DC    A(0)                     ADDR OF CATACB
CTGDWKB  DC    A(0)                     ADDR OF WORK AREA 'B'
CTGDOPT2 DC    X'2000E507'
CTGDDDNM DC    A(0)
CTGDPSWD DC    A(0)                     ADDR OF PASSWORD
CTGDFLDS DS    0F
         DC    A(CTLGFLD1)
         DC    A(CTLGFLD2)
         DC    A(CTLGFLD3)
         DC    A(CTLGFLD4)
         DC    A(CTLGFLD5)
         DC    A(CTLGFLD6)
         DC    A(CTLGFLD7)
         SPACE 2
CTGFLD   DS    0D
         SPACE
CTLGFLD1 DC    X'01',AL3(0),F'0'
         DC    A(REQUES02)              ENTYPE    (LTH = 1)
         DC    3F'0'
CTLGFLD2 DC    X'01',AL3(0),F'0'
         DC    A(REQUES03)              ENTNAME   (LTH = 44)
         DC    3F'0'
CTLGFLD3 DC    X'01',AL3(0),F'0'
         DC    A(REQUES11)              VOLDVCHR  (LTH = 20)
         DC    3F'0'
CTLGFLD4 DC    X'01',AL3(0),F'0'
         DC    A(REQUES18)              SYSEXTDS  (LTH = 1)
         DC    3F'0'
CTLGFLD5 DC    X'01',AL3(0),F'0'
         DC    A(REQUES19)              NODSPACE  (LTH = 2)
         DC    3F'0'
CTLGFLD6 DC    X'01',AL3(0),F'0'
         DC    A(REQUES20)              NODSET    (LTH = 2)
         DC    3F'0'
CTLGFLD7 DC    X'01',AL3(0),F'0'
         DC    A(REQUES23)              DSPDSCRP  (LTH = (15+))
         DC    3F'0'
         EJECT
*        *-------------------------*
*        *   C T L G P L   -   E   *
*        *-------------------------*
         SPACE
CTLGPLE  DS    0D
CTGEOPT1 DC    X'40008108'
*  CHECK THE MASTER PASSWORD
*  A CATALOG MANAGEMENT SERVICES FUNCTION
*  THE CALL IS A VSAM CATALOG MANAGEMENT REQUEST
*  BYPASS SECURITY PROMPTING TO SYSTEM OPERATOR
*
CTGEENT  DC    A(0)                     ADDR OF CI NUMBER
CTGECAT  DC    A(0)                     ADDR OF CATACB
CTGEWKB  DC    A(0)                     ADDR OF WORK AREA 'B'
CTGEOPT2 DC    X'2000C409'
         ORG   CTGEOPT2+2
CTGERCTP DS    CL1
CTGEFLDN DS    CL1
         ORG
CTGEDDNM DC    A(0)
CTGEPSWD DC    A(0)                     ADDR OF PASSWORD
CTGEFLDS DS    0F
         DC    A(CTLGFLE1)
         DC    A(CTLGFLE2)
         DC    A(CTLGFLE3)
         DC    A(CTLGFLE4)
         DC    A(CTLGFLE5)
         DC    A(CTLGFLE6)
         DC    A(CTLGFLE7)
         DC    A(CTLGFLE8)
         DC    A(CTLGFLE9)
         EJECT
*        *-----------------------*
*        *   C T G F L   -   E   *
*        *-----------------------*
         SPACE
CTGFLE   DS    0D
         SPACE
CTLGFLE1 DC    X'01',AL3(0),F'0'
         DC    A(REQUES02)              ENTYPE    (LTH = 1)
         DC    3F'0'
CTLGFLE2 DC    X'01',AL3(0),F'0'
         DC    A(REQUES03)              ENTNAME   (LTH = 44)
         DC    3F'0'
CTLGFLE3 DC    X'01',AL3(0),F'0'
         DC    A(REQUES06)              DSETEXDT  (LTH = 3)
         DC    3F'0'
CTLGFLE4 DC    X'01',AL3(0),F'0'
         DC    A(REQUES07)              DSETCRDT  (LTH = 3)
         DC    3F'0'
CTLGFLE5 DC    X'01',AL3(0),F'0'
         DC    A(REQUES09)              CRAVOL    (LTH = 6)
         DC    3F'0'
CTLGFLE6 DC    X'01',AL3(0),F'0'
         DC    A(REQUES12)              SPACPARM  (LTH = 7)
         DC    3F'0'
CTLGFLE7 DC    X'01',AL3(0),F'0'
         DC    A(REQUES15)              ENTVOL    (LTH = (45+))
         DC    3F'0'
CTLGFLE8 DC    X'01',AL3(0),F'0'
         DC    A(REQUES25)              AMDSBCAT  (LTH = 96)
         DC    3F'0'
CTLGFLE9 DC    X'01',AL3(0),F'0'
         DC    A(REQUES28)              LRECL     (LTH = 4)
         DC    3F'0'
         EJECT
*        *---------------------------*
*        *   F I E L D   N A M E S   *
*        *---------------------------*
         SPACE
REQUES01 DC    CL8'MULTITYP'
REQUES02 DC    CL8'ENTYPE'              LTH = 1
REQUES03 DC    CL8'ENTNAME'             LTH = 44
REQUES04 DC    CL8'CATTR'               LTH = 1
REQUES05 DC    CL8'NAMEDS'              LTH = VL: (4) / ASSOC
REQUES06 DC    CL8'DSETEXDT'            LTH = 3
REQUES07 DC    CL8'DSETCRDT'            LTH = 3
REQUES08 DC    CL8'OWNERID'             LTH = 8
REQUES09 DC    CL8'CRAVOL'              LTH = 6
REQUES10 DC    CL8'CATVOL'              LTH = VL: (15) / VOL
         SPACE
REQUES11 DC    CL8'VOLDVCHR'            LTH = 20
REQUES12 DC    CL8'SPACPARM'            LTH = 7
REQUES13 DC    CL8'HURBADS'             LTH = 4
REQUES14 DC    CL8'HARBADS'             LTH = 4
REQUES15 DC    CL8'ENTVOL'              LTH = VL: (45+) / VOL
REQUES18 DC    CL8'SYSEXTDS'            LTH = 1
REQUES19 DC    CL8'NODSPACE'            LTH = 2
REQUES20 DC    CL8'NODSET'              LTH = 2
REQUES23 DC    CL8'DSPDSCRP'            LTH = VL: (13+SPACE MAP) / EXT
REQUES24 DC    CL8'PASSWORD'            LTH = 32
         SPACE
REQUES25 DC    CL8'AMDSBCAT'            LTH = 96
REQUES28 DC    CL8'LRECL'               LTH = 4
REQUES33 DC    CL8'CATACB'              LTH = 4
         EJECT
*        *-------------------*
*        *   D S E C T ' S   *
*        *-------------------*
         SPACE
CTLGPLB  DSECT
CTGBOPT1 DS    XL4
CTGBENT  DS    F
CTGBCAT  DS    F
CTGBWKA  DS    F
CTGBOPT2 DS    XL4
CTGBDDNM DS    F
CTGBPSWD DS    F
CTGBFLDS DS    0F
CTGBAFL1 DS    F                        ENTYPE
CTGBAFL2 DS    F                        ENTNAME
CTGBAFL3 DS    F                        CATTR
CTGBAFL4 DS    F                        NAMEDS
CTGBAFL5 DS    F                        DSETEXDT
CTGBAFL6 DS    F                        DSETCRDT
CTGBAFL7 DS    F                        OWNERID
CTGBAFL8 DS    F                        CATVOL
         SPACE 2
NMDSECT  DSECT
NMDSTYP  DS    CL1                      ENTRY TYPE
NMDSCIN  DS    XL3                      CI NUMBER
         SPACE 2
VOLDVCHR DSECT
VOLDVTYP DS    CL4            VOL DEVIVE TYPE
VOLDMBLK DS    CL4            MAX DEVICE BLKSIZE
VOLDNCYL DS    CL2            NO. OF CYL'S PER VOL
VOLDNTRK DS    CL2            NO. OF TRK'S PER CYL
VOLDNBYT DS    CL2            NO. OF BYTES PER TRK
VOLDBGAP DS    3CL1           NO. OF BYTES REQUIRED FOR GAPS & CHK BITS
VOLDTFLG DS    CL1            BITS 0-6 RESERVED, BIT 7 TOLERANCE FLAG
VOLDTFAC DS    CL2            TOLERANCE FACTOR FOR EFFECTIVE LTH OF BLK
         SPACE 2
DSPDSCRP DSECT
DSPREPNO DS    CL2            RELATIVE REPETITION NUMBER
DSPTUSED DS    CL2            NO. OF TRACKS ALLOCATED IN EXTENT
DSPXSTRT DS    CL4            BEGINNING ADDR OF EXTENT IN CCHH
DSPTKEXT DS    CL2            NO. OF TRACKS IN EXTENT
DSPSNSPH DS    CL2            SEQ NO. OF SETS-O-FLD'S FOR EXT'S DATA SP
DSPSPMAP DS    0CL3           SPACE MAP OF EXTENT
DSPSMLTH DS    CL2            LTH OF RLC OF SPACE MAP
DSPSMRLC DS    CL1            (VL) RUN LENGTH CODE OF SPACE MAP
         EJECT
SPACPR   DSECT
SPACPRIM DS    CL3                      PRIMARY SPACE ALLOCATION
SPACSCON DS    CL3                      SECONDARY SPACE ALLOCATION
SPACOPTN DS    CL1                      SPACE ALLOCATION OPTION
*                                       B'10.. ....'  IN TRACKS
*                                       B'11.. ....'  IN CYLINDERS
         SPACE 2
AMDSB    DSECT
AMDSBCAT DS    CL96
         ORG   AMDSBCAT+1
AMDATTR  DS    CL1                      ATTRIBUTES OF THE DATA SET
*              BIT 0  B'1... ....'      KEY-SEQUENCED DATA SET (KSDS)
*                     B'0... ....'      ENTRY-SEQUENCED DATA SET (ESDS)
*              BIT 1  B'.1.. ....'      WRITECHECK
*              BIT 2  B'..1. ....'      IMBED
*              BIT 3  B'...1 ....'      REPLICATE
*              BIT 4  B'.... 1...'      ORDERED
*              BIT 5  B'.... .1..'      KEYRANGES
*              BIT 6  B'.... ..1.'      RELATIVE RECORD DATA SET (RRDS)
*              BIT 7  B'.... ...1'      SPANNED
         ORG   AMDSBCAT+20
AMDCINV  DS    CL4                      CONTROL INTERVAL SIZE
AMDLRECL DS    CL4                      MAXIMUM RECORD SIZE
         ORG
         EJECT
ETVOL    DSECT
ETVREPNO DS    CL2                      RELATIVE REPETITION NUMBER
ETVDEVTP DS    CL4                      DEVICE TYPE
ETVOLSER DS    CL6                      VOLUME SERIAL NUMBER
ETVFILSQ DS    CL2                      FILE SEQUENCE NUMBER
ETVOLFLG DS    CL1                      VOLUME FLAGS
*                                       BIT 0    X'80' PRIME
*                                       BIT 1    X'40' CANDIDATE
*                                       BIT 2    X'20' OVERFLOW
*                                       BIT 3-7  X'1F' RESERVED
ETVNOEXT DS    CL1                      NO. OF EXT IN THIS SET OF EXT
ETVHKRBA DS    CL4                      RBA OF DATA CI W/ THE HIGH KEY
ETVHURBA DS    CL4                      HIGH-USED RBA
ETVHARBA DS    CL4                      HIGH-ALLOCATED RBA
ETVPBSIZ DS    CL4                      PHYSICAL BLOCKSIZE
ETVBLKTK DS    CL2                      NO. OF BLOCKS PER TRACK
ETVTRKAU DS    CL2                      NO. OF TRACKS PER ALLOC UNIT
ETVITYPE DS    CL1                      INDEX EXTENT TYPE
*                                       BIT 0    X'80' SEQ SET W/ DATA
*                                       BIT 1    X'40' EXT'S ARE NOT
*                                                      PREFORMATTED
*                                       BIT 2-7  X'3F' RESERVED
ETVDSDSN DS    CL2                      DATA SET DIR SEQ NO. IN 'V' REC
ETVLOKEY DS    0CL3                     LOW KEY ON VOL - 64 BYTES MAX
ETVLKLTH DS    CL2                      LENGTH OF KEY - 0 IF NO KEY
ETVLKKEY DS    0CL64                    KEY - 0 TO 64 BYTES LONG
ETVHIKEY DS    0CL3                     HIGH KEY ON VOL - 64 BYTES MAX
ETVHKLTH DS    CL2                      LENGTH OF KEY - 0 IF NO KEY
ETVHKKEY DS    0CL64                    KEY - 0 TO 64 BYTES LONG
ETVEXTNT DS    0CL22                    EXTENT DESCRIPTION FIELDS
ETVXTLTH DS    CL2                      LTH OF THIS FLD: MULTIPLE OF 20
ETVXTENT DS    0CL20                    ONE EXTENT (SEE DSECT ETXDSCRP)
         SPACE 2
ETXDSCRP DSECT
ETXDSESN DS    CL2                      DATA SPACE EXTENT'S SEQ NO.
ETXLOTRK DS    CL4                      LOW CCHH
ETXHITRK DS    CL4                      HIGH CCHH
ETXNOTRK DS    CL2                      NO. OF TRACKS
ETXLORBA DS    CL4                      LOW RBA OF EXTENT
ETXHIRBA DS    CL4                      HIGH RBA OF EXTENT
         EJECT
TXDTAIL  DSECT
TXDVOLNO DS    CL6                      VOLUME SERIAL NUMBER
TXDRPTNO DS    0CL1                     REPORT NO. B'0... ....' DETAILS
TXDRCKEY DS    CL4                      RECORD KEY - GENERATED SORT KEY
TXDRCSEQ DS    CL2                      RECORD SEQ - SEQ NO IN 'NAMEDS'
TXDENTNM DS    CL44                     ENTRY NAME
TXDENTYP DS    CL1                      ENTRY TYPE
TXDCREDT DS    CL3                      CREATION DATE
TXDEXPDT DS    CL3                      EXPIRATION DATE
TXDCRAVL DS    CL6                      CRA VOLUME SERIAL NUMBER
TXDSCALC DS    CL3                      SECONDARY SPACE ALLOCATION
TXDSCTYP DS    CL1                      SPACE ALLOCATION TYPE
TXDDSORG DS    CL1                      DATA SET ORGANIZATION (AMDATTR)
TXDCISIZ DS    CL4                      CONTROL INTERVAL SIZE
TXDMAXRC DS    CL4                      MAXIMUM RECORD SIZE
TXDAVGRC DS    CL4                      AVERAGE RECORD SIZE
TXDTRKAL DS    H                        TRK'S ALLOC'D FOR DATA SET
TXDTRKUS DS    H                        TRK'S USED FOR DATA SET
TXDPCTUS DS    CL1                      PERCENT OF USAGE
TXDNOEXT DS    CL1                      NO. OF EXTENTS FOR DATA SET
         EJECT
TXVLCHR  DSECT
TXVVOLNO DS    CL6                      VOLUME SERIAL NUMBER
TXVRPTNO DS    0CL1                     REPORT NO. B'X... ....'  X=0,1
TXVRCKEY DS    CL4                      RECORD KEY - ZERO FOR THIS REC
TXVOLUSE DS    CL2                      VOLUME USE (UCBSTAB)
TXVDVTYP DS    CL4                      DEVICE TYPE
TXVCYLVL DS    H                        NO. OF CYLINDERS PER VOLUME
TXVTRKCL DS    H                        NO. OF TRACKS PER CYLINDER
TXVBYTTK DS    H                        NO. OF BYTES PER TRACK
TXVNODST DS    H                        NUMBER OF DATA SETS ON VOLUME
TXVNODSP DS    H                        NUMBER OF DATA SPACES ON VOLUME
TXVSYSXT DS    H                        SYSTEM ALLOWED EXTENTS
TXVCATNM DS    CL44                     VSAM CATALOG NAME
TXVVSEXT DS    H                        NUMBER OF VSAM FREE EXTENTS
TXVVSTRK DS    H                        NUMBER OF VSAM FREE TRACKS
TXVVSLGE DS    H                        TRACKS OF VSAM LARGEST FREE EXT
TXVOSEXT DS    H                        NUMBER OF OS FREE EXTENTS
TXVOSTRK DS    H                        NUMBER OF OS FREE TRACKS
TXVOSCYL DS    H                        NUMBER OF OS FREE CYLINDERS
TXVOSLGE DS    H                        TRACKS OF OS LARGEST FREE EXT
         EJECT
TXEXTNT  DSECT
TXEVOLNO DS    CL6                      VOLUME SERIAL NUMBER
TXERPTNO DS    0CL1                     REPORT NO. B'1... ....' EXTENTS
TXELOTCH DS    0CL4                     LOW CCHH OF EXTENT (SORT KEY)
TXELOCC  DS    H
TXELOHH  DS    H
TXERCKEY DS    H                        H'0' FOR DSP EXT, H'1' OTHERS
TXEXTNM  DS    CL44                     DATA SET NAME (ENTRY NAME)
TXEHITCH DS    0CL4                     HIGH CCHH OF EXTENT
TXEHICC  DS    H
TXEHIHH  DS    H
TXEXTLTH DS    H                        LENGTH OF EXTENT IN TRACKS
TXELOTRK DS    H                        LOW TRACK NUMBER
TXEHITRK DS    H                        HIGH TRACK NUMBER
TXEXTNO  DS    H                        EXTENT NUMBER
         EJECT
DSCB4    DSECT
DS4DSNAM DS    XL44                     X'04' IN EACH BYTE
DS4IDFMT DS    CL1                      FMT 4 ID - X'F4'
DS4HPCHR DS    CL5
DS4DSREC DS    H
DS4HCCHH DS    CL4
DS4NOATK DS    H
DS4VTOCI DS    XL1                      B'1... ....'  NO FMT 5 DSCB
DS4NOEXT DS    XL1                      NO. OF VTOC EXT'S - X'01'
         DS    CL2
DS4DEVSZ DS    2H
DS4DEVTK DS    H
DS4DEVOV DS    0XL2
DS4DEVI  DS    XL1
DS4DEVL  DS    XL1
DS4DEVK  DS    XL1
DS4DEVFG DS    XL1
DS4DEVTL DS    H
DS4DEVDT DS    XL1
DS4DEVDB DS    XL1
DS4AMTIM DS    CL8
DS4AMCAT DS    0XL3
DS4VSIND DS    XL1
DS4VSCRA DS    XL2
DS4R2TIM DS    CL8
         DS    CL5
DS4F6PTR DS    CL5
DS4VTOCE DS    0CL10                    VTOC EXT - SAME AS DS1EXT1
DS4VTIND DS    XL1                      DATA SET EXT TYPE INDICATOR
DS4VTSEQ DS    XL1                      EXT SEQ NO.
DS4VTLOR DS    XL4                      LOWER LIMIT OF EXT (CCHH)
DS4VTUPR DS    XL4                      UPPER LIMIT OF EXT (CCHH)
         DS    CL25
         EJECT
DSCB5    DSECT
DS5KEYID DS    XL4                      X'05' IN EACH BYTE
DS5AVEXT DS    XL5                      1 FREE EXT - SEE DSECT DFXEXT
DS5EXTAV DS    7XL5                     7 MORE FREE EXT'S
DS5FMTID DS    CL1                      FMT 5 ID - X'F5'
DS5MAVET DS    18XL5                    18 MORE FREE EXT'S
DS5PTRDS DS    XL5                      CCHHR OF NEXT FMT 5 DSCB IF ANY
         SPACE 2
UCBOB    DSECT
UCB      DS    CL64
         ORG   UCB+3
UCBSTAT  DS    XL1                      B'1... ....'  DEVICE ON-LINE
         ORG   UCB+16
UCBTYP   DS    XL4                      X'.... 20..'  DASD
         ORG   UCB+28
UCBVOLI  DS    CL6                      VOLUME SERIAL NUMBER
UCBSTAB  DS    XL1                      VOLUME USE
*                                       B'...1 ....'  PRIVATE
*                                       B'.... 1...'  PUBLIC
*                                       B'.... .1..'  STORAGE
*
* NOTE:  THE FOLLOWING FIELD 'UCBVTOC' HAS DIFFERENT OFFSETS IN
*        VS1 AND MVS.
*        ORG   UCB+36                   USED FOR VS1
*        ORG   UCB+24                   USED FOR MVS
*
         ORG   UCB+24                   MVS VERSION
UCBVTOC  DS    XL4                      ADDR OF VTOC IN TTRZ
         ORG
         END
