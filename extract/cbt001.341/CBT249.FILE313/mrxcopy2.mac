*          DATA SET MRXCOPY2   AT LEVEL 003 AS OF 04/26/77
TEST     CSECT
         STM   14,12,12(13)
         BALR  12,0
         USING *,12
         USING IHADCB,9
         ST    13,SAVE+4
         LA    15,SAVE
         ST    15,8(13)
         LR    13,15
         OPEN  (SYSDCB,(OUTPUT))
         LA    9,SYSDCB
         TM    DCBOFLGS,X'10'
         BZ    ERRSYS
         OPEN  (UT1DCB,(INPUT))
         LA    9,UT1DCB
         TM    DCBOFLGS,X'10'
         BZ    DDMIS1
         OPEN  (UT2DCB,(OUTPUT))
         LA    9,UT2DCB
         TM    DCBOFLGS,X'10'
         BZ    DDMIS2
         OPEN  (UT3DCB,(OUTPUT))
         LA    9,UT3DCB
         TM    DCBOFLGS,X'10'
         BZ    DDMIS3
RDSYSUT1 EQU   *
         GET   UT1DCB
         LR    2,1
         PUT   UT2DCB,(2)
         TM    DCBOFLGS,X'10'
         BZ    RDSYSUT1
         PUT   UT3DCB,(2)
         B     RDSYSUT1
ENDUT1   EQU   *
         CLOSE (UT1DCB)
         CLOSE (UT2DCB)
         TM    DCBOFLGS,X'10'
         BZ    CLOSSYS
         CLOSE (UT3DCB)
CLOSSYS  EQU   *
         MVC   TIMEOUT(27),=C'NO ERRORS DURING PROCESSING'
         PUT   SYSDCB,TIMEOUT
         CLOSE (SYSDCB)
         LA    15,0
ERROUT   EQU   *
         L     13,SAVE+4
         L     14,12(13)
         LM    0,12,20(13)
         BR    14
ERR1     EQU   *
         MVC   TIMEOUT(16),=C'I/O ERROR SYSUT1'
         B     PUTERR
ERR2     EQU   *
         MVC   TIMEOUT(16),=C'I/O ERROR SYSUT2'
         B     PUTERR
ERR3     EQU   *
         MVC   TIMEOUT(16),=C'I/O ERROR SYSUT3'
         B     PUTERR
DDMIS1   EQU   *
         MVC   TIMEOUT(22),=C'SYSUT1 DD CARD MISSING'
         B     PUTERR
DDMIS2   EQU   *
         MVC   TIMEOUT(22),=C'SYSUT2 DD CARD MISSING'
         B     PUTERR
DDMIS3   EQU   *
         MVC   TIMEOUT(22),=C'SYSUT3 DD CARD MISSING'
         PUT   SYSDCB,TIMEOUT
         B     RDSYSUT1
PUTERR   EQU   *
         PUT   SYSDCB,TIMEOUT
         LA    15,16
         B     ERROUT
ERRSYS   EQU   *
         LA    15,20
         B     ERROUT
SAVE     DS    18F
TIMEOUT  DC    CL133' '
UT1DCB   DCB   DDNAME=SYSUT1,DSORG=PS,MACRF=(GL),EODAD=ENDUT1,         C
               SYNAD=ERR1,OPTCD=C
UT2DCB   DCB   DDNAME=SYSUT2,DSORG=PS,MACRF=(PM),SYNAD=ERR2,           C
               OPTCD=C
UT3DCB   DCB   DDNAME=SYSUT3,DSORG=PS,MACRF=(PM),SYNAD=ERR3,           C
               OPTCD=C
SYSDCB   DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=(PM),RECFM=F,LRECL=133,  C
               BLKSIZE=133,SYNAD=ERRSYS
         DCBD  DSORG=(QS)
         END   TEST
