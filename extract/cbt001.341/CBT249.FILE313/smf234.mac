SMF234   SAVE  (14,12)
         LR    R12,R15
         USING SMF234,R12
         LR    R3,R1
         USING CPPL,R3
*
*              GETMAIN TO MAKE A REENTRANT PROGRAM
*              AND MOVE IN CONSTANTS
*
         GETMAIN R,LV=56
         LR    R11,R1
         USING GETMAIN,R11
         MVC   GETMAIN(56),CONSTANT
*
*              FIND TIME OF LOGON
*
LOGON    L     R4,CPPLPSCB
         USING PSCB,R4
         TIME  STCK,WORK
         L     R5,WORK
         SL    R5,PSCBLTIM             GET LOGON TIME
         L     R6,=X'00100000'           CONVERSION FACTOR
         MR    R4,R6                   GET TOTAL # OF SECONDS
         L     R6,=F'1000000'          DECIMAL FACTOR
         DR    R4,R6                   SHIFT DECIMAL POINT
         ST    R5,WORK                 STORE TIME
         MVC   LOGONTME(4),WORK        SAVE LOGON TIME
         DROP  R3,R4
*
*              DATE AND NOW TIME
*
TIMEDATE TIME  BIN
         STM   R0,R1,WORK
         MVC   NOWTIME(8),WORK         MOVE TIME AND DATE
*
*              FIND CPU MODEL
*
CPUMODEL L     R3,16                   ADDR OF CVT
         L     R3,X'C4'(,R3)           GET CVTSMCA  ADDR
         MVC   MODEL(4),16(R3)         GET CPU MODEL
*
*              FIND USERID AND STEPNAME
*
USERSTEP L     R3,16                   CVT ->
         L     R3,0(R3)                NEW/OLD TCB ->
         L     R3,8(R3)                CURRENT ASCB ->
         USING ASCB,R3                 ESTABLISH ADDRESSABILITY TO ASCB
         L     R4,ASCBCSCB             ASCBCSCB ->
         USING CSCB,R4
         MVC   USERID(8),CHKEY         MOVE JOBNAME
         MVC   STEPNAME(8),CHCLS       MOVE STEPNAME
*
*              FIND PHYSICAL TERMINAL ADDRESS
*
TERMADDR L     R5,ASCBTSB              LOAD ADDRESS OF TSB
         USING TSB,R5                  ESTABLISH ADDRESSABILITY TO TSB
         MODESET KEY=ZERO              MODESET KEY ZERO
         TM    TSBFLG5,TSBVTAM         SEE IF THIS IS A TSO/VTAM TSB
         BO    GETSYM                  YES....GO GET SYMBOLICS
         LH    R6,TSBLINE              GET BINARY LINE ADDRESS
         STH   R6,WORK                 STORE ADDRESS IN WORKAREA
         UNPK  WORK+3(5),WORK(3)       UNWORK THE ADDRESS
         MVC   LINEADDR(3),WORK+4      MOVE UNWORKED ADDRESS INTO LINE
         TR    LINEADDR,TRT-240        TRANSLATE TO EBCDIC
         B     WRITESMF                GO WRITE SMF RECORD
GETSYM   DS    0H
         MVC   LINEADDR(8),TSBTRMID    MOVE IN TERMINAL SYMBOLIC NAME
         DROP  R3,R4,R5
*
*              WRITE SMF RECORD
*
WRITESMF DS    0H
         SMFWTM SMFRCRD
*
*        CLEAN UP AND GO HOME
*
         MODESET KEY=NZERO             MODESET KEY NONZERO
FREE     FREEMAIN R,LV=56,A=(R11)
*
RETURN   RETURN (14,12)
CONSTANT DS    0D
LEN      DC    H'46'
FIL      DC    H'0'
         DC    X'02'    VS2 INDICATOR
REC      DC    X'EA'    DECIMAL 234
NOW      DC    BL4'0'
DAT      DC    BL4'0'
MOD      DC    CL4' '
LOG      DC    BL4'0'
USE      DC    CL8' '
STE      DC    CL8' '
LIN      DC    CL8' '
PAC      DC    D'0'
         DC    CL2' '
TRT      DC    CL16'0123456789ABCDEF'
GETMAIN  DSECT
SMFRCRD  DS    0CL46
LENGTH   DS    BL2
FILL     DS    BL2
NUMBER   DS    B
RECID    DS    B
NOWTIME  DS    BL4
DATE     DS    BL4
MODEL    DS    CL4
LOGONTME DS    BL4
USERID   DS    CL8
STEPNAME DS    CL8
LINEADDR DS    CL8
WORK     DS    D
         DS    CL2
         EJECT
         REG
         IKJTSB
         IKTTSBX
         IHAASCB
         IKJCPPL
         IKJPSCB
CSCB     DSECT
         IEECHAIN
         END   SMF234
