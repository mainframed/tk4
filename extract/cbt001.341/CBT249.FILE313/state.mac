*)F FUNCTION -
*   THE STATE COMMAND CHECKS THE CATALOG FOR THE GIVEN DATA SET
*   AND GIVES A RETURN CODE FOR OTHER PROCESSORS TO CHECK:
*        RETURN CODE OF 0 INDICATES DATASET EXISTS
*        RETURN CODE OF 4 INDICATES DATASET DOES NOT EXISTS
*)X SYNTAX -
*         STATE DATA-SET-NAME
*     REQUIRED - DATA-SET-NAME
*     DEFAULTS - NONE
*)O OPERANDS -
*   DATA-SET-NAME - THE NAME OF THE DATA SET TO BE CHECKED
         REGS
STATE    SAVE  (14,12)
         LR    R12,R15                    SET UP PROGRAM BASE
         USING STATE,R12        ADDRESSABILITY FOR STATE CSECT
         LR    R5,R1        SAVE R1
         GETMAIN R,LV=LDYNAMIC          GET CORE
         LR    R11,R1
         USING WORKAREA,R11             SET UP ADDRESSABILITY
         LA    R6,SAVEAREA              CHAIN
         ST    R13,SAVEAREA+4             SAVE
         ST    R6,8(R13)                    AREAS
         LR    R13,R6
         LR    R1,R5         RESTORE R1
         LR    CPPLBASE,R1    SAVE ADDRESS OF CPPL
         USING CPPL,CPPLBASE
         L     PCLBASE,=A(MYPCL)   LOAD
         USING MYPCL,PCLBASE
         SR    R0,R0          ZERO REG ZERO
*
*                   BUILD PPL AND DAPL LISTS
*
         LA    PPLBASE,SKIKJPPL    AREA FOR PPL
         USING PPL,PPLBASE
         LA    DAPLBASE,MYDAPL     AREA FOR DAPL
         USING DAPL,DAPLBASE
         L     R2,CPPLUPT
         ST    R2,PPLUPT
         ST    R2,DAPLUPT
         L     R2,CPPLECT
         ST    R2,PPLECT
         ST    R2,DAPLECT
         ST    R0,PPLECB           ZERO THE ECB
         ST    R0,DAPLECB          ZERO THIS ECB
         LA    R2,MYPCL            PUT PCL ADDRESS
         ST    R2,PPLPCL              INTO PPL
         LA    R2,SKANSWER         ADDRESS FOR PDL POINTER
         ST    R2,PPLANS              INTO PPL
         L     R2,CPPLCBUF         ADDRESS OF COMMAND BUFFER
         ST    R2,PPLCBUF             INTO PPL
         LR    R1,PPLBASE          SET UP
*                                   AND LINK TO PARSE
         LINK  EP=IKJPARS
         LTR   R15,R15             CHECK FOR RETURN CODES
         BNZ   ERROR2              - TELL THEM IT'S NOT CATALOGED
         L     PDLBASE,SKANSWER    GET ADDRESS OF PDL
         USING IKJPARMD,PDLBASE
         LA    DAPBBASE,DAPB       SET ADDRESS FOR DAPB
         USING DAPB04,DAPBBASE
         XC    DAPB04(16),DAPB04   ZERO OUT DAPB
         MVI   DA04CD+1,DA04CAT    SET DAPB CODE FOR SEARCH
         LA    R4,DSNAMEL          MOVE ADDRESS OF DSN
         ST    R4,DA04PDSN            TO DAPB
         TM    PDEDSN+6,X'80'      IS THERE A DATA SET FROM PARSE
         BZ    ERROR2                 NO, SET AS NOT CATALOGED
         TM    PDEDSN+6,X'40'      IS THE NAME IN QUOTES?
         BO    *+8                      YES
         OI    DA04CTL,DA04UID          NO - SET FLAG TO ATTACH USERID
         L     R4,CPPLPSCB         MOVE ADDRESS OF PSCB
         ST    R4,DAPLPSCB              TO DAPL
         ST    DAPBBASE,DAPLDAPB   MOVE ADDRESS OF DAPB TO DAPL
*                   BUILD DSNAME
         MVI   DSNAME,C' '         BLANK
         MVC   DSNAME+1(43),DSNAME   THE DSNAME
         LH    R4,PDEDSN+4         LENGTH OF DSN
         STH   R4,DSNAMEL             TO FRONT OF DSN
         L     R3,PDEDSN           ADDRESS OF
         EX    R4,MOVE             MOVE NAME TO DSNAME AREA
*                   CALL DAIR
         LR    R1,DAPLBASE
         LINK  EP=IKJDAIR
         LTR   R15,R15             CHECK THE RETURN CODE
         BZ    DAIROK
         LH    R2,DA04CTRC         TEST DAIR RETURN CODE
         CH    R2,=H'4'
         BE    ERROR1
         CH    R2,=H'16'
         BH    ERROR1
         B     ERROR2
DAIROK   TM    DA04FLG,DA04DSE     IS DATA SET IN DSE
         BO    THEEND                 YES
         TM    DA04FLG,DA04CAT     IS DATA SET IN CATALOG
         BO    THEEND                 YES
         B     ERROR2                 NO
ERROR1   MVC   MESSAGE,EMESSAGE    MOVE ERROR MESSAGE TO WORK AREA
         CVD   R2,PACKED              CONVERT ERROR CODE TO ZONED
         ED    MESSAGE+24(8),PACKED+4
         LA    R0,L'MESSAGE        LOAD PARMS FOR TPUT
         LA    R1,MESSAGE
         TPUT  (1),(0),R
ERROR2   IKJRLSA SKANSWER          RELEASE CORE
         RETURN (14,12),RC=4
THEEND   IKJRLSA SKANSWER          RELEASE CORE
         RETURN (14,12),RC=0
PCLBASE  EQU   5
CPPLBASE EQU   6
DAPBBASE EQU   7
DAPLBASE EQU   8
PDLBASE  EQU   9
PPLBASE  EQU   10
MOVE     MVC   DSNAME(0),0(R3)     MOVE DSNAME
EMESSAGE DC    CL24'CATALOG MANAGEMENT ERROR'
         DC    XL8'402020202020202120'
         LTORG
MYPCL    IKJPARM
PDEDSN   IKJPOSIT DSNAME,PROMPT='DATA SET NAME'
         IKJENDP
         IKJDAPL
         IKJDAP04
         IKJPPL
         IKJCPPL
WORKAREA DSECT
SAVEAREA DS    18F
PACKED   DS    PL8
SKIKJPPL DS    7F
SKANSWER DS    A             ADDRESS OF PDL
DAPB     DS    CL16
MYDAPL   DS    5F
DSNAMEL  DS    H
DSNAME   DS    CL44
MESSAGE  DS    CL32
LDYNAMIC EQU   *-WORKAREA
         END   STATE
