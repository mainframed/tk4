         MACRO
         REGS
         LCLA  &R
.R       AIF   (&R EQ 16).END
R&R      EQU   &R
&R       SETA  &R+1
         AGO   .R
.END     MEND
         EJECT
ENQASID  CSECT
         REGS
         STM   R14,R12,12(R13)          STORE REGS
         LR    R12,R15                    SET UP PROGRAM BASE
         USING ENQASID,R12
*
         BAL   R14,BEGIN
*
         BAL   R2,PRSE
*
         B     QCBSTART
*
RETURN   LR    R1,R13                   DYNAMIC AREA ADDRESS IN R1
         LA    R0,LDYNAMIC              SIZE OF AREA
         L     R13,4(R13)               PREVIOUS SAVE AREA
         LA    R1,0(R1)                 CLEAR TOP BYTE
         SVC   10                       FREEMAIN
         LM    R14,R12,12(R13)
         SR    R15,R15
         BR    R14
         EJECT
*  SUBROUTINE TO GET READY SET AND GO
*
BEGIN    LR    R10,R1        SAVE R1
         GETMAIN R,LV=LDYNAMIC          GET CORE
         LR    R11,R1
         USING WORKAREA,R11             SET UP ADDRESSABILITY
         LA    R9,SAVEAREA              CHAIN
         ST    R13,SAVEAREA+4             SAVE
         ST    R9,8(R13)                    AREAS
         LR    R13,R9
         BR    R14           RETURN
         EJECT
*  SUBROUTINE TO PARSE INPUT AND SET UP TO START
*
PRSE     EQU   *
         USING CPPL,R10
         LA    R9,PPLADDR         ADDRESSABILITY FOR PPL
         USING PPL,R9
         MVC   PPLUPT,CPPLUPT           UPT
         MVC   PPLECT,CPPLECT           ECT
         LA    R3,ECB
         ST    R3,PPLECB                ECB
         L     R3,PCLADDR
         ST    R3,PPLPCL                PCL
         LA    R3,PDLADDR               PDL ADDRESS
         ST    R3,PPLANS                ANSWER
         MVC   PPLCBUF,CPPLCBUF         CBUF
         LA    R1,PPLADDR
         LINK  EP=IKJPARS               GO PARSE
         LTR   R15,R15
         BZ    PRSE1
         TPUT  PRSERM,40
         B     RETURN
         DROP  R10
PRSE1    L     R10,PDLADDR
         USING IKJPARMD,R10
         LH    R3,ASID+4      LENGTH OF ASID
         BCTR  R3,0           DECREMENT BY 1
         LA    R4,X'70'       LENGTH OF WORK-1
         OR    R3,R4          LENGTHS FOR PACK INSTRS.
         L     R4,ASID        ADDR OF ASID
         EX    R3,PACK        PACK ASID
         CVB   R3,WORK        CONVERT ASID TO BINARY
         BR    R2             AND CONTINUE
         DROP  R10
*
PACK     PACK  WORK,0(0,R4)
PRSERM   DC    CL16'PARSE ERROR'
         EJECT
*  SUBROUTINE TO TEST IF APPROPRIATE HEADER HAS BEEN PRINTED
*             AND THEN PRINT CURRENT LINE
*              REG.6 HAS THE ADDR OF MAJOR NAME
*
PUTLINE  TM    MAJORSW,ON     PRINT MAJOR NAME ALREADY
         BO    PUTMINOR       YES
         TPUT  (R6),8
         MVI   MAJORSW,ON     TURN MAJOR NAME SWITCH ON
PUTMINOR TPUT  (7),60         PRINT MINOR NAME
         BR    R10            GO BACK
         EJECT
* SUBROUTINE TO FIND MAJOR QCB OF SYSDSN AND THEN
*            MINOR QCB OF DSN PASSED FROM PARSE
*            FINALLY FIND WHO HAS IT AND TELL.
*
QCBSTART LR    R2,R3          ASID NUMBER
         MVI   MAJORSW,OFF    SET MAJOR NAME HEADER SWITCH
         SR    R7,R7          CLEAR REG. 7
         L     R3,16          CVT ->
         L     R3,644(R3)     LAST MAJOR QCB FOR ENQ
         USING QCB,R3
QCBMAJOR L     R4,MAJLMIN     LAST MINOR QCB
         USING MIN,R4
         LA    R6,MAJNAME
QCBMINOR IC    R7,MINNAMEL    LENGTH OF MINOR NAME
         LA    R8,MINNAME
         L     R5,MINFQEL     FIRST QEL
         USING QEL,R5
QCBQEL   CH    R2,QELASID     DOES QEL HAVE ASID WE WANT
         BNE   QCBNQEL        NO FIND NEXT QEL
         MVI   LINE,C' '      BLANK OUT LINE
         MVC   LINE+1(59),LINE
         BCTR  R7,0           DECREMENT REG 7 FOR MOVE
         EX    R7,MOVE        MOVE MINOR NAME TO LINE
         LA    R7,LINE        ADDR OF LINE IN REG. 7 FOR PUTLINE
         MVC   LINE+1(3),=C'SHR' DEFAULT DISP.
         TM    QELQFLGS,QELSHARE IS DISP. SHR
         BO    QCBPUTL        YES PUTLINE
         MVC   LINE+1(3),=C'EXC' NO SET DISP. TO 'EXC'
QCBPUTL  BAL   R10,PUTLINE    DISPLAY MINOR NAME
         B     QCBNEXT        FIND NEXT QCB
QCBNQEL  L     R5,QELNQEL     ADDR OF NEXT QEL
         LTR   R5,R5          VALID
         BNZ   QCBQEL         YES
QCBNEXT  L     R4,MINPMIN     FIND PREVIOUS MINOR NAME
         LTR   R4,R4          VALID
         BNZ   QCBMINOR       YES
         MVI   MAJORSW,OFF    SET MAJOR NAME HEADER SWITCH
         L     R3,MAJPMAJ     FIND PREVIOUS MAJOR NAME
         LTR   R3,R3          VALID
         BNZ   QCBMAJOR       YES
         B     RETURN
MOVE     MVC   LINE+6,0(R8)   MOVE MINOR NAME TO LINE
         EJECT
PCLADDR  DC    A(DSPPCL)
PADCHAR  DC    XL4'40000000'
         DS    0D
LOCAREA  DS    CL265
         LTORG
         EJECT
DSPPCL   IKJPARM
ASID     IKJIDENT 'ASID NUMBER',PROMPT='ASID NUMBER',                  X
               MAXLNTH=3,FIRST=NUMERIC,OTHER=NUMERIC
         IKJENDP
         EJECT
         IKJCPPL
         EJECT
         IKJPSCB
         EJECT
         IKJPPL
         EJECT
*   CSCB
CSCB     DSECT
         IEECHAIN
*   ASCB
         IHAASCB
         EJECT
*   QCB
         IHAQCB
*   QEL
         IHAQEL
         EJECT
ON       EQU   X'01'
OFF      EQU   X'00'
WORKAREA DSECT
SAVEAREA DS    18F
WORK     DS    D
ECB      DS    F
PDLADDR  DS    F
PPLADDR  DS    7F
MAJORSW  DS    C
LINE     DS    60C
LDYNAMIC EQU   *-WORKAREA
         END   ENQASID
