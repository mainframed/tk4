         $REG
         PRINT NOGEN
EDRATING TITLE   'EDUCATIONAL SERVICES (C) 1980 COPYRIGHT MEMOREX'
***********************************************************************
*                                                                     *
*   MODULE - EDRATING                             WRITTEN: 01/15/80   *
*                                                 UPDATED: 02/08/80   *
*   FUNCTION -                                                        *
*                                                                     *
*            DISPLAYS QUARTER COURSE RATINGS FOR MRX ISD EDUCATIONAL  *
*            SERVICES DATA BASE                                       *
*                                                                     *
*   INPUT REGISTERS -                                                 *
*                                                                     *
*            R1  - ADDRESS OF FCB  LIST (6 WORDS)                     *
*                    INDEX    FCB                                     *
*                    TITLES   FCB                                     *
*                    SESSIONS FCB                                     *
*                    REGISTER FCB                                     *
*                    STUDENTS FCB                                     *
*                    DPTMENTS FCB                                     *
*                                                                     *
*   RETURN REGISTERS -                                                *
*                                                                     *
*            R15 - RETURN CODE                                        *
*                  0  - SUCCESSFULL                                   *
*                  8  - NO SESSIONS IN SESSION FILE                   *
*                                                                     *
*   REGISTERS DURING EXECUTION -                                      *
*                                                                     *
*            R10 - ADDRESS OF SCREEN CONTROL BLOCKS                   *
*            R11 - RECORD NUMBER TO BE DISPLAYED AT TOP OF SCREEN     *
*            R12 - BASE REGISTER                                      *
*            R13 - SAVE AREA ADDRESS                                  *
*                                                                     *
*   MACROS -                                                          *
*                                                                     *
*            $REG                                                     *
*            $LNKGE                                                   *
*            $RETURN                                                  *
*            $FCB                                                     *
*            $DISPLAY                                                 *
*            $ENTER                                                   *
*            @SCR                                                     *
*            @CRS                                                     *
*            @FLD                                                     *
*            $SHOW                                                    *
*            $SET                                                     *
*                                                                     *
*   AUTHOR -                                                          *
*            M. PELT                                                  *
*            MEMOREX CORPORATION                                      *
*            SAN TOMAS AT CENTRAL EXPRESSWAY                          *
*            SANTA CLARA, CA  95052                                   *
*                                                                     *
*            (408) 987-1735                                           *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*                                                                     *
*   SORT SESSIONS AND TITLES                                          *
*                                                                     *
***********************************************************************
EDRATING $LNKGE BASE=12            STANDARD OS/VS LINKAGE
         ST    R1,PARMAD           SAVE PARM LIST ADDRESS
         LR    R9,R1
         USING @PRM,R9
         L     R8,TITLES
         L     R7,SESSIONS
         DROP  R9
         $SET  (R8),KEYLEN=L'CRSNO,OFFSET=CRSNO-@CRS
         $SORT (R8)                SORT TITLES BY COURSE NUMBER
         $SET  (R7),KEYLEN=L'SESTIME,OFFSET=SESTIME-@SES
         $SORT (R7),DESC           SORT SESSIONS BY SESSION TIME
         $SET  (R7),KEYLEN=L'SESDATE,OFFSET=SESDATE-@SES
         $SORT (R7),DESC           SORT SESSIONS BY SESSION DATE
         $SET  (R7),KEYLEN=L'SESSCTNO,OFFSET=SESSCTNO-@SES
         $SORT (R7)                SORT SESSIONS BY SECTION NUMBER
         $SET  (R7),KEYLEN=L'SESCRSNO,OFFSET=SESCRSNO-@SES
         $SORT (R7)                SORT SESSIONS BY COURSE NUMBER
         EJECT
***********************************************************************
*                                                                     *
*   TRICKY CODE COMING UP - WE RESHUFFLE THE SESSIONS FILE SO THAT    *
*                           THE FIRST SESSION OF EACH SECTION APPEARS *
*                           AT THE TOP OF THE FILE. THE FILE IS THEN  *
*                           SUBSET (BY SETTING FCBLAST) SUCH THAT     *
*                           ONLY THIS TOP PORTION IS DISPLAYED,       *
*                           SORTED OR UPDATED. MAKE SURE YOU DON'T    *
*                           ISSUE ANY $RESET MACROS FROM HERE ON      *
*                           EXCEPT FOR PFK03 (SAVE AND END)           *
*                                                                     *
***********************************************************************
         $SHOW (R7),LAST           NUMBER OF SESSIONS IN FILE
         LTR   R6,R1
         BNZ   HAVESESS
         $RETURN RC=8
HAVESESS $SHOW (R7),BASE           ADDRESS OF FIRST SES RECORD
         LR    R8,R1
         LR    R7,R1
         BCT   R6,*+8
         B     ONESESS
NEXTSESS LA    R8,@SESLEN(R8)      NEXT SESSION RECORD
         CLC   SESCRSNO-@SES(L'SESCRSNO,R7),SESCRSNO-@SES(R8)
         BL    SWAPSESS
         CLC   SESSCTNO-@SES(L'SESSCTNO,R7),SESSCTNO-@SES(R8)
         BNL   SKIPSESS
SWAPSESS LA    R7,@SESLEN(R7)      NEXT SLOT FOR SWAP
         CR    R7,R8               CHECK IF THIS IS NEXT RECORD
         BNL   SKIPSESS
         XC    0(@SESLEN,R8),0(R7) SWAP RECORDS
         XC    0(@SESLEN,R7),0(R8)
         XC    0(@SESLEN,R8),0(R7)
SKIPSESS BCT   R6,NEXTSESS
         LR    R6,R7               SAVE RECORD ADDRESS
         L     R9,PARMAD
         USING @PRM,R9
         L     R7,SESSIONS
         DROP  R9
         $SHOW (R7),LINE,(R6)      SHOW RECORD NUMBER
         LR    R2,R1
         $SET  (R7),LAST=(R2)
ONESESS  EQU   *
         EJECT
***********************************************************************
*                                                                     *
*   GET STORAGE FOR SCREEN CONTROL BLOCKS AND INITIALIZE THEM         *
*                                                                     *
***********************************************************************
         $OPEN SCREEN
         $SHOW SCREEN,BASE
         LA    R10,0(R1)           SAVE ADDRESS OF SCREEN CONTROL BLOCK
         MVC   0(CRSNOFLD-$SCR,R10),$SCR MOVE INITIAL FIELDS
         L     R9,PARMAD
         USING @PRM,R9
         L     R8,SESSIONS
         DROP  R9
         LA    R8,FCBMEMBR+3-@FCB(R8) PICK UP YEAR FROM MEMBER NAME
         ST    R8,FLDADDR-@FLD+YEARFLD-$SCR(R10) PUT ADDRESS IN YEARFLD
         LA    R8,4(R8)            PICK UP QUARTER FROM MEMBER NAME
         ST    R8,FLDADDR-@FLD+QTRFLD-$SCR(R10) PUT ADDRESS IN QTRFLD
         LA    R9,CRSNOFLD-$SCR(R10) FIRST COURSE NUMBER FIELD
         LA    R8,0                OFFSET TO FIRST DISPLAYED COURSE
         USING @SES,R8
         LH    R6,FLDROW-@FLD+CRSNOFLD  START AT SCREEN LINE 7
INITFLDS MVC   0(ENDFLDS-CRSNOFLD,R9),CRSNOFLD FIELDS FOR ONE LINE
         USING CRSNOFLD,R9         COURSE NUMBER FIELD
         STH   R6,CRSNOFLD+FLDROW-@FLD SAVE SCREEN LINE NUMBER
         STH   R6,SCTNOFLD+FLDROW-@FLD SAVE SCREEN LINE NUMBER
         STH   R6,TITLEFLD+FLDROW-@FLD SAVE SCREEN LINE NUMBER
         STH   R6,TEACHFLD+FLDROW-@FLD SAVE SCREEN LINE NUMBER
         STH   R6,RATINFLD+FLDROW-@FLD SAVE SCREEN LINE NUMBER
         STH   R6,RESPSFLD+FLDROW-@FLD SAVE SCREEN LINE NUMBER
         STH   R6,STATFLD+FLDROW-@FLD SAVE SCREEN LINE NUMBER
         STH   R6,MRK1FLD+FLDROW-@FLD SAVE SCREEN LINE NUMBER
         STH   R6,MRK2FLD+FLDROW-@FLD SAVE SCREEN LINE NUMBER
         STH   R6,MRK3FLD+FLDROW-@FLD SAVE SCREEN LINE NUMBER
         STH   R6,MRK4FLD+FLDROW-@FLD SAVE SCREEN LINE NUMBER
         LA    R7,SESCRSNO         OFFSET TO CRSNO IN NEXT RECORD
         ST    R7,CRSNOFLD+FLDADDR-@FLD POINT FIELD TO IT
         LA    R7,SESSCTNO         OFFSET TO SCTNO IN NEXT RECORD
         ST    R7,SCTNOFLD+FLDADDR-@FLD POINT FIELD TO IT
         LA    R7,SESTEACH         OFFSET TO SESDATE IN NEXT RECORD
         ST    R7,TEACHFLD+FLDADDR-@FLD POINT FIELD TO IT
         LA    R7,SESRATIN         OFFSET TO SESTIME IN NEXT RECORD
         ST    R7,RATINFLD+FLDADDR-@FLD POINT FIELD TO IT
         LA    R7,SESRESPS         OFFSET TO SESPLACE IN NEXT RECORD
         ST    R7,RESPSFLD+FLDADDR-@FLD POINT FIELD TO IT
         LA    R7,SESSTAT          OFFSET TO SESSTAT IN NEXT RECORD
         ST    R7,STATFLD+FLDADDR-@FLD POINT FIELD TO IT
         LA    R9,ENDFLDS          COURSE NUMBER FIELD NEXT LINE
         LA    R8,@SESLEN(R8)      OFFSET TO NEXT RECORD FROM BASE
         LA    R6,1(R6)            NEXT SCREEN LINE NUMBER
         CH    R6,BTMROW           LINE 24 JUST PROCESSED ?
         BNH   INITFLDS            NO - SOME MORE TO GO THEN
         DROP  R8,R9
         EJECT
***********************************************************************
*                                                                     *
*   SET R11 TO RECORD NUMBER OF TOP LINE DISPLAYED AND DISPLAY SCREEN *
*                                                                     *
***********************************************************************
         LA    R11,1
DISPLAY  L     R9,PARMAD
         USING @PRM,R9
         L     R7,SESSIONS
         DROP  R9
         $SHOW (R7),ADDRESS,(R11) ADDRESS OF RECORD
         LR    R2,R1               SAVE IT
         $SHOW (R7),LAST           LAST RECORD NUMBER TO DISPLAY
         SR    R1,R11              MAX NUMBER REMAIN TO DISPLAY
         LA    R1,1(R1)
         CH    R1,SCROLL           MORE THAN 18 TO DISPLAY ?
         BNH   *+8                 NO - USE LESS THAN 18 RECORDS
         LH    R1,SCROLL           YES - USE 18 RECORDS ONLY
         LR    R3,R1               SAVE NUMBER OF DISPLAYED LINES
         MH    R1,FLDSPRCD         TIMES NUMBER OF FIELDS PER RECORD
         LA    R1,(CRSNOFLD-FIRSTFLD)/@FLDLEN(R1)
         STH   R1,SCR#ENT-@SCR(R10) SET NUMBER OF FIELDS TO DISPLAY
         EJECT
***********************************************************************
*                                                                     *
*   GET TITLES FOR DISPLAYED SESSIONS                                 *
*                                                                     *
***********************************************************************
         LA    R9,CRSNOFLD-$SCR(R10) FIRST CRSNOFLD
         USING CRSNOFLD,R9
         LR    R5,R3               SAVE NUMBER OF LINES FOR LATER
DOTITLE  L     R4,CRSNOFLD+FLDADDR-@FLD OFFSET OF COURSE NUMBER
         AR    R4,R2               ADD BASE ADDRESS
         L     R1,PARMAD
         USING @PRM,R1
         L     R8,TITLES
         DROP  R1
         $FIND (R8),(R4)           GET TITLE
         XC    FLDADDR-@FLD+TITLEFLD,FLDADDR-@FLD+TITLEFLD
         XC    FLDLEN-@FLD+TITLEFLD,FLDLEN-@FLD+TITLEFLD
         LTR   R1,R1               DID WE FIND A TITLE ?
         BNZ   GOTTITLE
         LA    R1,MSG004           USE DUMMY TITLE * NO COURSE TITLE *
         ST    R1,FLDADDR-@FLD+TITLEFLD
         MVC   MESSAGE(L'MSG003),MSG003 TELL USER NO TITLE
         LA    R1,L'CRSSHORT
         STH   R1,FLDLEN-@FLD+TITLEFLD
         B     NXTTITLE
GOTTITLE LA    R1,CRSSHORT-@CRS(R1) ADDRESS OF SHORT TITLE
         ST    R1,FLDADDR-@FLD+TITLEFLD
         LA    R1,L'CRSSHORT
         STH   R1,FLDLEN-@FLD+TITLEFLD
NXTTITLE LA    R9,ENDFLDS          ADDRESS OF NEXT CRSNOFLD
         BCT   R3,DOTITLE
         DROP  R9
         EJECT
***********************************************************************
*                                                                     *
*   DISPLAY SCREEN AND GET A RESPONSE                                 *
*                                                                     *
***********************************************************************
         LR    R1,R5               NUMBER OF LINES
         BAL   R14,NOVUDUP         SET DUPLICATE ENTRIES TO NONVIEWABLE
         CLC   MESSAGE,BLANKS
         BE    *+8
         OI    SCRWCC-@SCR(R10),SCRALARM SOUND ALARM
         $DISPLAY (R10),BASE=(R2)
         $ENTER (R10)
         MVC   MESSAGE,BLANKS
         NI    SCRWCC-@SCR(R10),X'FF'-SCRALARM
         CLI   SCRAID-@SCR(R10),SCRENTER
         BE    DISPLAY
         CLI   SCRAID-@SCR(R10),SCRPFK03
         BE    PFK03
         CLI   SCRAID-@SCR(R10),SCRPFK07
         BE    PFK07
         CLI   SCRAID-@SCR(R10),SCRPFK08
         BE    PFK08
         CLI   SCRAID-@SCR(R10),SCRPFK04
         BE    PFK04
         CLI   SCRAID-@SCR(R10),SCRPFK05
         BE    PFK05
         B     DISPLAY
         EJECT
***********************************************************************
*                                                                     *
*   PFK03 - END                                                       *
*                                                                     *
***********************************************************************
PFK03    L     R9,PARMAD
         USING @PRM,R9
         L     R7,SESSIONS
         DROP  R9
         $SHOW (R7),MAX
         LR    R2,R1
         $SET  (R7),LAST=(R2)
         $SET  (R7),KEYLEN=L'SESTIME,OFFSET=SESTIME-@SES
         $SORT (R7)                SORT (R7) BY SESSION TIME
         $SET  (R7),KEYLEN=L'SESDATE,OFFSET=SESDATE-@SES
         $SORT (R7)                SORT (R7) BY SESSION DATE
         $SET  (R7),KEYLEN=L'SESSCTNO,OFFSET=SESSCTNO-@SES
         $SORT (R7)                SORT SESSIONS BY SECTION NUMBER
         $SET  (R7),KEYLEN=L'SESCRSNO,OFFSET=SESCRSNO-@SES
         $SORT (R7)                SORT SESSIONS BY COURSE NUMBER
         $CLOSE SCREEN
         $RETURN                   RETURN TO CALLER
         EJECT
***********************************************************************
*                                                                     *
*   PFK07 - SCROLL DOWN                                               *
*                                                                     *
***********************************************************************
PFK07    SH    R11,SCROLL
         BP    *+8
         LA    R11,1
         B     DISPLAY
         EJECT
***********************************************************************
*                                                                     *
*   PFK08 - SCROLL UP                                                 *
*                                                                     *
***********************************************************************
PFK08    AH    R11,SCROLL
         L     R9,PARMAD
         USING @PRM,R9
         L     R7,SESSIONS
         DROP  R9
         $SHOW (R7),LAST
         CR    R1,R11
         BNL   *+6
         LR    R11,R1
         B     DISPLAY             NUMBER OF LINES IN FILE REMAINING
         EJECT
***********************************************************************
*                                                                     *
*   PFK04/PFK05 SORT FIELD UNDER CURSOR ASCENDING OR DESCENDING       *
*                                                                     *
***********************************************************************
PFK04    EQU   *
PFK05    LH    R2,SCRROW-@SCR(R10) LOAD ROW NUMBER OF CURSOR
         CH    R2,FLDROW-@FLD+CRSNOFLD IS CURSOR ABOVE 1ST COURSE LINE?
         BL    DISPLAY             YIP - IGNORE SORT REQUEST
         LH    R2,SCRCOL-@SCR(R10) LOAD COLUMN NUMBER OF CURSOR
         LA    R9,CRSNOFLD-$SCR(R10) FIRST FIELD TO CHECK
         USING @FLD,R9
         LA    R8,ENDFLDS-$SCR(R10)
SORTFIND LH    R3,FLDCOL           GET FIELD ATTRIBUTE COLUMN
         CR    R2,R3               CHECK IF CURSOR TO LEFT OR UNDER IT
         BNH   SORTNEXT            YIP - TRY NEXT FIELD
         AH    R3,FLDLEN
         CR    R2,R3
         BNH   SORTHAVE
SORTNEXT LA    R9,@FLDLEN(R9)      NEXT FIELD
         CR    R9,R8
         BL    SORTFIND
         B     CANTSORT
SORTHAVE TM    FLDFLAG,FLDBASED    IS FIELD WITHIN SESSIONS FILE ?
         BZ    CANTSORT            NO - CAN'T SORT ON IT
         L     R3,FLDADDR          LOAD FIELD OFFSET
         LH    R4,FLDLEN           LOAD FIELD LENGTH
         DROP  R9
         L     R9,PARMAD
         USING @PRM,R9
         L     R7,SESSIONS
         DROP  R9
         $SET  (R7),OFFSET=(R3),KEYLEN=(R4)
         CLI   SCRAID-@SCR(R10),SCRPFK05 WAS IT ASCENDING ?
         BNE   SORTDESC            NO - DESCENDING
         $SORT (R7),ASC            SORT ASCENDING
         B     DISPLAY
SORTDESC $SORT (R7),DESC           SORT DESCENDING
         B     DISPLAY
CANTSORT MVC   MESSAGE(L'MSG002),MSG002 TELL USER CURSOR NOT RIGHT
         B     DISPLAY
         EJECT
***********************************************************************
*                                                                     *
*   NOVUDUP - SET DUPLICATE FIELDS NONVIEWABLE                        *
*                                                                     *
***********************************************************************
NOVUDUP  STM   R14,R12,12(R13)
         BCT   R1,*+8
         B     NOVURET
         LA    R9,CRSNOFLD-$SCR(R10)
         USING CRSNOFLD,R9
         LR    R8,R9
         LA    R9,ENDFLDS
NVUNEXT  L     R6,FLDADDR-@FLD(R8)
         L     R7,CRSNOFLD+FLDADDR-@FLD
         AR    R6,R2               ADD BASE
         AR    R7,R2
         NI    CRSNOFLD+FLDATTR-@FLD,X'FF'-FLDNOVU SET CRSNO VIEWABLE
         NI    SCTNOFLD+FLDATTR-@FLD,X'FF'-FLDNOVU SET SCTNO VIEWABLE
         CLC   0(L'SESCRSNO+L'SESSCTNO,R6),0(R7) COMPARE CRSNO & SCTNO
         BNE   VUCRSNO             NOT THE SAME
         CLI   0(R6),0             NULL FIELD ?
         BE    VUCRSNO             YIP - VIEWABLE
         OI    CRSNOFLD+FLDATTR-@FLD,FLDNOVU SET CRSNO NONVIEWABLE
         OI    SCTNOFLD+FLDATTR-@FLD,FLDNOVU SET SCTNO NONVIEWABLE
VUCRSNO  L     R6,TITLEFLD+FLDADDR-@FLD-CRSNOFLD(R8)  SHORT TITLE
         L     R7,TITLEFLD+FLDADDR-@FLD
         NI    TITLEFLD+FLDATTR-@FLD,X'FF'-FLDNOVU SET TITLE VIEWABLE
         OI    TITLEFLD+FLDATTR-@FLD,FLDHIGH BUT HIGH INTENSITY
         CLC   0(L'CRSSHORT,R6),0(R7) DUPLICATE TITLES ?
         BNE   VUTITLE             NO - VIEW THEM
         CLI   0(R6),0             NULL FIELD ?
         BE    VUTITLE             YIP - VIEWABLE
         OI    TITLEFLD+FLDATTR-@FLD,FLDNOVU SET VIEWABLE
VUTITLE  L     R6,TEACHFLD+FLDADDR-@FLD-CRSNOFLD(R8) INSTRUCTOR NAME
         L     R7,TEACHFLD+FLDADDR-@FLD
         AR    R6,R2               ADD BASE
         AR    R7,R2
         NI    TEACHFLD+FLDATTR-@FLD,X'FF'-FLDNOVU SET NAME VIEWABLE
         CLC   0(L'SESTEACH,R6),0(R7)
         BNE   VUTEACH
         CLI   0(R6),0             NULL FIELD ?
         BE    VUTEACH             YIP - VIEWABLE
         OI    TEACHFLD+FLDATTR-@FLD,FLDNOVU SET NAME NOVIEWABLE
VUTEACH  LA    R8,ENDFLDS-CRSNOFLD(R8)
         LA    R9,ENDFLDS
         BCT   R1,NVUNEXT
NOVURET  LM    R14,R12,12(R13)
         BR    R14
         EJECT
ERROR    ABEND 111,DUMP
         EJECT
SCROLL   DC    H'18'
BTMROW   DC    H'24'
FLDSPRCD DC    AL2((ENDFLDS-CRSNOFLD)/@FLDLEN)
PARMAD   DC    A(*-*)
BLANKS   DC    CL60' '
         EJECT
TITLE    DC    C'Memorex ISD - Educational Services'
RPTTITLE DC    C'Course Ratings'
QTRLBL   DC    C'Qtr'
HEADING1 DC    C'Course Title                  Instructor         Ratin*
               g   Responses  Status'
MESSAGE  DC    CL60' '
MSG002   DC    C'Cursor not under sortable column - Reposition please'
MSG003   DC    C'Course title missing for course - Check course number'
MSG004   DC    CL20'* NO COURSE TITLE *'
SCREEN   $FCB  MAX=(CRSNOFLD-$SCR+20*(ENDFLDS-CRSNOFLD))/@FLDLEN,      *
               RECL=@FLDLEN
$SCR     $SCR  ENTRIES=(CRSNOFLD-FIRSTFLD+20*(ENDFLDS-CRSNOFLD))/@FLDLE*
               N,WCC=MDT,CURSOR=(7,2)
FIRSTFLD $FLD  1,1,ATTR=(PROT,NUM,HI)
         $FLD  1,22,ATTR=(PROT,NUM,HI),ADDRESS=TITLE
         $FLD  2,31,ATTR=(PROT,NUM,HI),ADDRESS=RPTTITLE
YEARFLD  $FLD  3,34,ATTR=NOSF,LEN=4
         $FLD  3,39,ATTR=NOSF,ADDRESS=QTRLBL
QTRFLD   $FLD  3,43,ATTR=NOSF,LEN=1
         $FLD  4,10,ATTR=(PROT,NUM,HI),ADDRESS=MESSAGE
HEADFLD  $FLD  4,80,ATTR=(PROT,NUM,HI),ADDRESS=HEADING1
CRSNOFLD $FLD  7,1,ATTR=(PROT,LO),OFFSET=SESCRSNO-@SES,                *
               LEN=L'SESCRSNO
SCTNOFLD $FLD  7,5,ATTR=(PROT,LO),OFFSET=SESSCTNO-@SES,                *
               LEN=L'SESSCTNO
TITLEFLD $FLD  7,7,ATTR=(HI,PROT,NUM),LEN=20,ADDRESS=0
TEACHFLD $FLD  7,28,ATTR=LO,OFFSET=SESTEACH-@SES,LEN=L'SESTEACH
MRK1FLD  $FLD  7,49,ATTR=(HI,PROT,NUM)
RATINFLD $FLD  7,51,ATTR=LO,OFFSET=SESRATIN-@SES,LEN=L'SESRATIN
MRK2FLD  $FLD  7,54,ATTR=(HI,PROT,NUM)
RESPSFLD $FLD  7,61,ATTR=LO,OFFSET=SESRESPS-@SES,LEN=L'SESRESPS
MRK3FLD  $FLD  7,65,ATTR=(HI,PROT,NUM)
STATFLD  $FLD  7,69,ATTR=(LO,PROT),OFFSET=SESSTAT-@SES,                *
               LEN=L'SESSTAT,BLANK=NO
MRK4FLD  $FLD  7,80,ATTR=(HI,PROT,NUM)
ENDFLDS  EQU   *
         SPACE 3
         LTORG
         EJECT
         COPY  @PRM
         EJECT
         COPY  @FCB
         EJECT
         COPY  @SCR
         EJECT
         COPY  @FLD
         EJECT
         COPY  @CRS
         EJECT
         COPY  @SES
         END
