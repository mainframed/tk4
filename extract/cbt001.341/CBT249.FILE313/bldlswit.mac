BLDLSWIT TITLE 'BLDLSWIT - PROGRAM TO START OR STOP DYNAMIC BLDL'
BLDLSWIT CSECT
*
*   THIS IS A SLIGHTLY MODIFIED VERSION
*   OF THE AMDAHL VERSION OF THE CBT CODE.
*   NOTE THAT THE TABLE DISPLACEMENT IS NOW X'28'.
*   --- WJS/20MAR81
*
         B     26(,R15)            BRANCH AROUND ID         WJS/20MAR81
         DC    AL1(21),CL8'BLDLSWIT'    PROGRAM NAME
         DC    CL8'&SYSDATE'       DATE                     WJS/20MAR81
         DC    CL5'&SYSTIME'       TIME                     WJS/20MAR81
*
         STM   R14,R12,12(R13)     SAVE REGS IN HI SAVE AREA
         LR    R12,R15             SET UP BASE
         USING BLDLSWIT,R12        OF R12
         LR    R11,R13             SAVE CALLERS SAVE AREA ADDR
*
         LA    R13,SAVEAREA        SET DSECT BASE
         ST    R11,4(,R13)         SAVE CALLERS SAVE AREA ADDR
         ST    R13,8(,R11)         SAVE OURS IN CALLERS
         USING SAVEAREA,R13        DYNAMIC SAVEAREA BASE + WORK
*
         L     R1,0(,R1)           POINT TO PARM FIELD
         CLI   2(R1),C'S'          IS IT START ?
         BE    START               GO SET START
         CLI   2(R1),C'P'          IS IT STOP ?
         BNE   BADPARM             NO - GO ISSUE MSG
         MVI   SW,X'F0'            YES - SET STOP
         B     GO
*
START    DS    0H
         MVI   SW,X'E0'            SET SWITCH START
*
GO       DS    0H
         L     R2,16               CVT
         L     R2,X'C8'(,R2)       SCVT
         L     R2,X'84'(,R2)       SVCTABLE
         L     R2,X'90'(,R2)       BLDL ENTRY
*
         CLC   4(8,R2),=CL8'DYNBLDL'    IS THIS OK
         BNE   BADBLDL
*
         L     R3,X'24'(,R2)       PICK UP SWITCH ADDRESS   WJS/20MAR81
         L     R4,X'28'(,R2)       PICK UP LINK TABLE ADDRESS ""  ""
         USING TABHEAD,R4          ADDRESSABILITY
*
         TM    SW,X'10'            IS IT STOP
         BZ    VERSTART            NO - GO CHECK OUR CODE
         CLI   0(R3),0             ARE WE ALREADY STOPPED
         BE    DONEBE4             THIS BIT NO GOOD
         B     GODIRTY             GO OVERWRITE
*
VERSTART DS    0H
         CLI   0(R3),0             ARE WE ALREADY STARTED
         BNE   DONEBE4             THIS BIT NO GOOD
*
GODIRTY  DS    0H
         MODESET KEY=ZERO
*
         XI    0(R3),X'FF'         FLIP THE BIRD
*
         CLI   0(R3),0             TURNED OFF?
         BNE   UNZERO              BRANCH IF NOT
*
         LM    R1,R3,TABSTART      PICK UP TABLE POINTERS
         LR    R6,R3               SAVE LAST ENTRY PTR
         AR    R3,R2               POINT TO END OF TABLE
*
         LA    R2,8                SIZE OF INCREMENT
         SR    R3,R2               TABLE END -8
*
         SR    R14,R14             ZERO
         LR    R15,R14             FOR CLEARING
*
         STM   R14,R15,0(R1)       ZEROISE 8 BYTES
         BXLE  R1,R2,*-4           UP TO NEXT 8 BYTES
*
         ST    R6,LASTSLOT         STORE LAST ENTRY AS PRESENT
         STH   R15,SORTS           SORT COUNT
         ST    R15,HITS            TABLE HITS
         ST    R15,MISSES          TABLE MISSES
*
UNZERO   MODESET KEY=NZERO
*
         TM    SW,X'10'            WAS IT STOP ?
         BZ    MSGSTART            NO - JUST GO ISSUE MSG
*
         WTO   'BLDLSWIT - DYNAMBLDL STOPPED.'
         B     RET
*
MSGSTART DS    0H
         WTO   'BLDLSWIT - DYNAMBLDL STARTED.'
         B     RET
*
RET      DS    0H
         L     R13,4(,R13)         HI SAVE AREA ADDR
         LM    R14,R12,12(R13)     RESORE CALLERS REGS
         SLR   R15,R15             SET RETURN CODE
         BR    R14                 RETURN
*
BADPARM  DS    0H
         WTO   'BLDLSWIT - INCORRECT PARM FIELD'
         B     RET
*
BADBLDL  DS    0H
         WTO   'BLDLSWIT - DYNAMIC BLDL MODULE ERROR'
         B     RET
*
DONEBE4  DS    0H
         TM    SW,X'10'            IS IT STOP ?
         BZ    DONES               NO - ITS START
         WTO   'BLDLSWIT - STOP REJECTED - ALREADY STOPPED'
         B     RET                 HOME
*
DONES    DS    0H
         WTO   'BLDLSWIT - START REJECTED - ALREADY STARTED'
         B     RET                 HOME
*
         EJECT
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
*
SAVEAREA DS    18F
SW       DS    X
*
LITERALS LTORG                     ,
*
         TITLE 'DYNAMIC BLDL DSECTS'
TABHEAD  DSECT
TABSTART DS    F
TABSIZE  DS    F
TABEND   DS    F
LASTSLOT DS    F
TABSLOTF DS    F
SORTS    DS    H
ENTRIES  DS    H
HITS     DS    F
MISSES   DS    F
         DS    0D
TABLENG  EQU   *-TABHEAD
*
         END   BLDLSWIT
