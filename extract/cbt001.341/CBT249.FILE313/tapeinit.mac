*          DATA SET TAPEINIT   AT LEVEL 001 AS OF 09/20/73
         START
         STM   14,12,12(13)
         PRINT NOGEN
BEGIN    BALR  4,0
         USING *,4
         ST    13,SAVE+4
         LR    12,13
         LA    13,SAVE
START    OPEN  (SYSIN,(OUTPUT))
         MVC   ANSWR,CLEAR3
REPLY    XC    WECB,WECB
         WTOR  ' GIVE SERIAL NUMBER (AND NUMBTAPE INFORMATION IF NEEDEDC
               )',ANSWR,9,WECB
         WAIT  ECB=WECB
         MVC   SER,CLEAR
         MVC   NUM,CLEAR2
         LA    8,ANSWR
         LA    7,9
GOAGIN   OC    0(1,8),=1X'40'
         CLC   0(1,8),=C','
         BE    FIG1
         LA    8,1(8)
         BCT   7,GOAGIN
         B     FIG2
FIG1     LA    9,9
         SR    9,7
         MVC   NUM(2),1(8)
         LA    8,ANSWR
         EX    9,MVCSER
         B     WRITE
FIG2     MVC   NUM,ONE
         LA    8,ANSWR
         LA    7,9
GOAGIN2  CLC   0(1,8),=C' '
         BE    FIG3
         LA    8,1(8)
         BCT   7,GOAGIN2
FIG3     MVI   0(8),C','
         LA    9,9
         SR    9,7
         LA    8,ANSWR
         EX    9,MVCSER
WRITE    PUT   SYSIN,CNTRLCD1
         PUT   SYSIN,CNTRLCD2
         CLOSE (SYSIN)
         CLC   SER(1),=C'$'
         BE    EOJ
         LINK  EP=IEHINITT,PARAM=(OPT),VL=1
         B     START
EOJ      L     13,SAVE+4
         ST    15,16(13)
         LM    14,12,12(13)
         BR    14
SYSIN    DCB   DDNAME=SYSIN,DSORG=PS,MACRF=PM
SAVE     DS    18F
         DS    0F
OPT      DC    H'0'
CLEAR    DC    CL48' '
CLEAR2   DC    CL2' '
CLEAR3   DC    CL9' '
ONE      DC    CL2'1 '
CNTRLCD1 DS    0CL80
         DC    CL23'TAPEUNIT   INITT   SER='
SER      DS    CL48
         DC    CL1'C'
         DC    CL8' '
CNTRLCD2 DS    0CL80
         DC    CL24'               NUMBTAPE='
NUM      DS    CL2
         DC    CL54' '
ANSWR    DC    CL9' '
WECB     DC    F'0'
MVCSER   MVC   SER(0),0(8)
         END
