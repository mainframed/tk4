         $REG
*        PRINT NOGEN
EDTITLES TITLE  'EDUCATIONAL SERVICES (C) 1980 COPYRIGHT MEMOREX'
***********************************************************************
*                                                                     *
*   MODULE - EDTITLES                             WRITTEN: 01/15/80   *
*                                                 UPDATED: 02/08/80   *
*   FUNCTION -                                                        *
*                                                                     *
*            DISPLAYS COURSE TITLE MENU FOR MRX ISD EDUCATIONAL       *
*            SERVICES DATA BASE                                       *
*                                                                     *
*   INPUT REGISTERS -                                                 *
*                                                                     *
*            NONE                                                     *
*                                                                     *
*   RETURN REGISTERS -                                                *
*                                                                     *
*            R15 - RETURN CODE                                        *
*                  0  - SUCCESSFULL                                   *
*                  4  - TOO MANY TITLES                               *
*                                                                     *
*                                                                     *
*   REGISTERS DURING EXECUTION -                                      *
*                                                                     *
*            R10 - ADDRESS OF SCREEN CONTROL BLOCKS                   *
*            R11 - RECORD NUMBER TO BE DISPLAYED AT TOP OF SCREEN     *
*            R12 - BASE REGISTER                                      *
*            R13 - SAVE AREA ADDRESS                                  *
*                                                                     *
*   MACROS -                                                          *
*                                                                     *
*            $REG                                                     *
*            $LNKGE                                                   *
*            $RETURN                                                  *
*            $LOAD                                                    *
*            $OPEN                                                    *
*            $CLOSE                                                   *
*            $FCB                                                     *
*            $DISPLAY                                                 *
*            $ENTER                                                   *
*            @SCR                                                     *
*            @CRS                                                     *
*            @FLD                                                     *
*            $SHOW                                                    *
*            $SET                                                     *
*                                                                     *
*   AUTHOR -                                                          *
*            M. PELT                                                  *
*            MEMOREX CORPORATION                                      *
*            SAN TOMAS AT CENTRAL EXPRESSWAY                          *
*            SANTA CLARA, CA  95052                                   *
*                                                                     *
*            (408) 987-1735                                           *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*                                                                     *
*   GET STORAGE FOR COURSE TITLES AND READ THEM IN                    *
*                                                                     *
***********************************************************************
EDTITLES $LNKGE BASE=12            STANDARD OS/VS LINKAGE
         ST    R1,PARMAD           SAVE PARM LIST ADDRESS
         LR    R9,R1
         USING @PRM,R9
         L     R8,TITLES
         DROP  R9
         $SET  (R8),KEYLEN=L'CRSNO,OFFSET=CRSNO-@CRS
         $SORT (R8)                SORT TITLES BY COURSE NUMBER
         EJECT
***********************************************************************
*                                                                     *
*   GET STORAGE FOR SCREEN CONTROL BLOCKS AND INITIALIZE THEM         *
*                                                                     *
***********************************************************************
LOADOK   $OPEN SCREEN
         $SHOW SCREEN,BASE
         LA    R10,0(R1)           SAVE ADDRESS OF SCREEN CONTROL BLOCK
         MVC   0(CRSNOFLD-$SCR,R10),$SCR MOVE INITIAL FIELDS
         L     R9,PARMAD
         USING @PRM,R9
         L     R8,TITLES
         DROP  R9
         LA    R8,FCBMEMBR+3-@FCB(R8) PICK UP YEAR FROM MEMBER NAME
         ST    R8,FLDADDR-@FLD+YEARFLD-$SCR(R10) PUT ADDRESS IN YEARFLD
         LA    R8,4(R8)            PICK UP QUARTER FROM MEMBER NAME
         ST    R8,FLDADDR-@FLD+QTRFLD-$SCR(R10) PUT ADDRESS IN QTRFLD
         LA    R9,CRSNOFLD-$SCR(R10) FIRST COURSE NUMBER FIELD
         LA    R8,0                OFFSET TO FIRST DISPLAYED COURSE
         USING @CRS,R8
         LH    R6,FLDROW-@FLD+CRSNOFLD  START AT SCREEN LINE 5
INITFLDS MVC   0(ENDFLDS-CRSNOFLD,R9),CRSNOFLD FIELDS FOR ONE LINE
         USING CRSNOFLD,R9
         STH   R6,CRSNOFLD+FLDROW-@FLD SAVE SCREEN LINE NUMBER
         STH   R6,MARK1FLD+FLDROW-@FLD SAVE SCREEN LINE NUMBER
         STH   R6,TITLEFLD+FLDROW-@FLD SAVE SCREEN LINE NUMBER
         STH   R6,SHORTFLD+FLDROW-@FLD SAVE SCREEN LINE NUMBER
         STH   R6,MARK2FLD+FLDROW-@FLD SAVE SCREEN LINE NUMBER
         LA    R7,CRSNO            OFFSET TO CRSNO IN NEXT RECORD
         ST    R7,CRSNOFLD+FLDADDR-@FLD POINT FIELD TO IT
         LA    R7,CRSTITLE         OFFSET TO CRSTITLE IN NEXT RECORD
         ST    R7,TITLEFLD+FLDADDR-@FLD POINT FIELD TO IT
         LA    R7,CRSSHORT         OFFSET TO CRSTITLE IN NEXT RECORD
         ST    R7,SHORTFLD+FLDADDR-@FLD POINT FIELD TO IT
         LA    R9,ENDFLDS          COURSE NUMBER FIELD NEXT LINE
         LA    R8,@CRSLEN(R8)      OFFSET TO NEXT RECORD FROM BASE
         LA    R6,1(R6)            NEXT SCREEN LINE NUMBER
         CH    R6,BTMROW           LINE 24 JUST PROCESSED ?
         BNH   INITFLDS            NO - SOME MORE TO GO THEN
         DROP  R9
         EJECT
***********************************************************************
*                                                                     *
*   SET R11 TO RECORD NUMBER OF TOP LINE DISPLAYED AND DISPLAY SCREEN *
*                                                                     *
***********************************************************************
         LA    R11,1
DISPLAY  L     R9,PARMAD
         USING @PRM,R9
         L     R8,TITLES
         DROP  R9
         $SHOW (R8),ADDRESS,(R11)  ADDRESS OF RECORD
         LR    R2,R1               SAVE IT
         $SHOW (R8),LAST           MAX NUMBER OF RECORDS
         SR    R1,R11              MAX NUMBER REMAIN TO DISPLAY
         LA    R1,1(R1)
         CH    R1,SCROLL           MORE THAN 20 TO DISPLAY ?
         BNH   *+8                 NO - USE LESS THAN 20 RECORDS
         LH    R1,SCROLL           YES - USE 20 RECORDS ONLY
         MH    R1,FLDSPRCD         5 FIELDS PER RECORD
         LA    R1,(CRSNOFLD-FIRSTFLD)/@FLDLEN(R1)
         STH   R1,SCR#ENT-@SCR(R10) SET NUMBER OF FIELDS TO DISPLAY
         CLC   MESSAGE,BLANKS      ANY ERROR MESSAGE ?
         BE    *+8                 NO - SKIP ALARM
         OI    SCRWCC-@SCR(R10),SCRALARM YIP - SOUND SIREN
         $DISPLAY (R10),BASE=(R2)
         $ENTER (R10)
         MVC   MESSAGE,BLANKS      CLEAR ANY ERROR MESSAGE
         NI    SCRWCC-@SCR(R10),X'FF'-SCRALARM RESET SIREN
         CLI   SCRAID-@SCR(R10),SCRENTER
         BE    DISPLAY
         CLI   SCRAID-@SCR(R10),SCRPFK03
         BE    PFK03
         CLI   SCRAID-@SCR(R10),SCRPFK04
         BE    PFK04
         CLI   SCRAID-@SCR(R10),SCRPFK05
         BE    PFK05
         CLI   SCRAID-@SCR(R10),SCRPFK06
         BE    PFK06
         CLI   SCRAID-@SCR(R10),SCRPFK07
         BE    PFK07
         CLI   SCRAID-@SCR(R10),SCRPFK08
         BE    PFK08
         CLI   SCRAID-@SCR(R10),SCRPFK09
         BE    PFK09
         B     DISPLAY
         EJECT
***********************************************************************
*                                                                     *
*   PFK03 - END                                                       *
*                                                                     *
***********************************************************************
PFK03    L     R9,PARMAD
         USING @PRM,R9
         L     R8,TITLES
         DROP  R8
         $SET  (R8),KEYLEN=L'CRSNO,OFFSET=CRSNO-@CRS
         $RESET (R8)               CLEAR BLANK COURSE NUMBERS
         $CLOSE SCREEN
         $RETURN                   RETURN TO CALLER
         EJECT
***********************************************************************
*                                                                     *
*   PFK04/PFK05 SORT FIELD UNDER CURSOR ASCENDING OR DESCENDING       *
*                                                                     *
***********************************************************************
PFK04    EQU   *
PFK05    LH    R2,SCRROW-@SCR(R10) LOAD ROW NUMBER OF CURSOR
         CH    R2,FLDROW-@FLD+CRSNOFLD IS CURSOR ON COURSE LINE
         BL    CANTSORT            NO - IGNORE
         LH    R2,SCRCOL-@SCR(R10) LOAD COLUMN NUMBER OF CURSOR
         LA    R9,CRSNOFLD-$SCR(R10) FIRST FIELD TO CHECK
         USING @FLD,R9
         LA    R8,ENDFLDS-$SCR(R10)
SORTFIND LH    R3,FLDCOL           GET FIELD ATTRIBUTE COLUMN
         CR    R2,R3               CHECK IF CURSOR TO LEFT OR UNDER IT
         BNH   SORTNEXT            YIP - TRY NEXT FIELD
         AH    R3,FLDLEN
         CR    R2,R3
         BNH   SORTHAVE
SORTNEXT LA    R9,@FLDLEN(R9)      NEXT FIELD
         CR    R9,R8
         BL    SORTFIND
         B     CANTSORT
SORTHAVE TM    FLDFLAG,FLDBASED    IS FIELD WITHIN TITLES FILE ?
         BZ    CANTSORT            NO - CAN'T SORT FIELD
         L     R3,FLDADDR
         LH    R4,FLDLEN
         DROP  R9
         L     R9,PARMAD
         USING @PRM,R9
         L     R8,TITLES
         DROP  R9
         $SET  (R8),KEYLEN=(R4),OFFSET=(R3)
         CLI   SCRAID-@SCR(R10),SCRPFK05 WAS IT ASCENDING ?
         BNE   SORTDESC            NO - DESCENDING
         $SORT (R8),ASC            SORT ASCENDING
         B     DISPLAY
SORTDESC $SORT (R8),DESC           SORT DESCENDING
         B     DISPLAY
CANTSORT MVC   MESSAGE(L'MSG002),MSG002 TELL USER BAD CURSOR POSITION
         B     DISPLAY
         EJECT
***********************************************************************
*                                                                     *
*   PFK06 - SCROLL TO TOP                                             *
*                                                                     *
***********************************************************************
PFK06    LA    R11,1
         B     DISPLAY
         EJECT
***********************************************************************
*                                                                     *
*   PFK07 - SCROLL DOWN                                               *
*                                                                     *
***********************************************************************
PFK07    SH    R11,SCROLL
         BP    *+8
         LA    R11,1
         B     DISPLAY
         EJECT
***********************************************************************
*                                                                     *
*   PFK08 - SCROLL UP                                                 *
*                                                                     *
***********************************************************************
PFK08    AH    R11,SCROLL
         L     R9,PARMAD
         USING @PRM,R9
         L     R8,TITLES
         DROP  R9
         $SHOW (R8),LAST
         CR    R1,R11
         BNL   *+6
         LR    R11,R1
         B     DISPLAY             NUMBER OF LINES IN FILE REMAINING
         EJECT
***********************************************************************
*                                                                     *
*   PFK09 - SCROLL TO BOTTOM                                          *
*                                                                     *
***********************************************************************
PFK09    L     R9,PARMAD
         USING @PRM,R9
         L     R8,TITLES
         DROP  R9
         $SHOW (R8),LAST
         LR    R11,R1
         B     DISPLAY
         EJECT
SCROLL   DC    H'19'
BTMROW   DC    H'24'
PARMAD   DC    A(*-*)
FLDSPRCD DC    AL2((ENDFLDS-CRSNOFLD)/@FLDLEN)
BLANKS   DC    CL60' '
TITLE    DC    C'Memorex ISD - Educational Services'
RPTTITLE DC    C'Course Titles'
QTRLBL   DC    C'Qtr'
MESSAGE  DC    CL60' '
MSG002   DC    C'Cursor not under sortable column - Reposition please'
HEADING  DC    C'Course   Title                                    Shor*
               t Title'
SCREEN   $FCB  MAX=(CRSNOFLD-$SCR+19*(ENDFLDS-CRSNOFLD))/@FLDLEN,      *
               RECL=@FLDLEN
$SCR     $SCR  ENTRIES=(CRSNOFLD-FIRSTFLD+19*(ENDFLDS-CRSNOFLD))/@FLDLE*
               N,WCC=MDT,CURSOR=(6,3)
FIRSTFLD $FLD  1,22,ATTR=(PROT,NUM,HI),ADDRESS=TITLE
         $FLD  2,33,ATTR=(PROT,NUM,HI),ADDRESS=RPTTITLE
YEARFLD  $FLD  3,34,ATTR=NOSF,LEN=4
         $FLD  3,39,ATTR=NOSF,ADDRESS=QTRLBL
QTRFLD   $FLD  3,43,ATTR=NOSF,LEN=1
         $FLD  4,10,ATTR=(PROT,NUM,HI),ADDRESS=MESSAGE
         $FLD  5,1,ATTR=(PROT,NUM,HI),ADDRESS=HEADING
CRSNOFLD $FLD  6,2,ATTR=LO,OFFSET=CRSNO-@CRS,LEN=L'CRSNO
MARK1FLD $FLD  6,6,ATTR=(PROT,NUM)
TITLEFLD $FLD  6,10,ATTR=LO,OFFSET=CRSTITLE-@CRS,LEN=L'CRSTITLE
SHORTFLD $FLD  6,51,ATTR=LO,OFFSET=CRSSHORT-@CRS,LEN=L'CRSSHORT
MARK2FLD $FLD  6,72,ATTR=(PROT,NUM)
ENDFLDS  EQU   *
         SPACE 3
         LTORG
         EJECT
         COPY  @FCB
         EJECT
         COPY  @PRM
         EJECT
         COPY  @SCR
         EJECT
         COPY  @FLD
         EJECT
         COPY  @CRS
         END
