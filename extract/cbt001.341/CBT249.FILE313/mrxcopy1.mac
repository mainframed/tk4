*          DATA SET MRXCOPY1   AT LEVEL 001 AS OF 04/17/72
TEST     CSECT
         STM   14,12,12(13)
         BALR  12,0
         USING *,12
         USING IHADCB,9
         ST    13,SAVE+4
         LA    15,SAVE
         ST    15,8(13)
         LR    13,15
         OPEN  (SYSDCB,(OUTPUT))
         LA    9,SYSDCB
         TM    DCBOFLGS,X'10'
         BZ    ERRSYS
         OPEN  (UT1DCB,(INPUT))
         LA    9,UT1DCB
         TM    DCBOFLGS,X'10'
         BZ    DDMIS1
         OPEN  (UT2DCB,(OUTPUT))
         LA    9,UT2DCB
         TM    DCBOFLGS,X'10'
         BZ    DDMIS2
RDSYSUT1 EQU   *
         GET   UT1DCB
         LR    2,1
         PUT   UT2DCB,(2)
         B     RDSYSUT1
ENDUT1   EQU   *
         CLOSE (UT1DCB)
         CLOSE (UT2DCB)
         MVC   TIMEOUT(27),=C'NO ERRORS DURING PROCESSING'
         PUT   SYSDCB,TIMEOUT
         CLOSE (SYSDCB)
         LA    15,0
ERROUT   EQU   *
         L     13,SAVE+4
         L     14,12(13)
         LM    0,12,20(13)
         BR    14
ERR1     EQU   *
         MVC   TIMEOUT(16),=C'I/O ERROR SYSUT1'
         B     PUTERR
ERR2     EQU   *
         MVC   TIMEOUT(16),=C'I/O ERROR SYSUT2'
         B     PUTERR
DDMIS1   EQU   *
         MVC   TIMEOUT(22),=C'SYSUT1 DD CARD MISSING'
         B     PUTERR
DDMIS2   EQU   *
         MVC   TIMEOUT(22),=C'SYSUT2 DD CARD MISSING'
         B     PUTERR
PUTERR   EQU   *
         PUT   SYSDCB,TIMEOUT
         LA    15,16
         B     ERROUT
ERRSYS   EQU   *
         LA    15,20
         B     ERROUT
SAVE     DS    18F
TIMEOUT  DC    CL133' '
UT1DCB   DCB   DDNAME=SYSUT1,DSORG=PS,MACRF=(GL),EODAD=ENDUT1,         C
               SYNAD=ERR1,OPTCD=C
UT2DCB   DCB   DDNAME=SYSUT2,DSORG=PS,MACRF=(PM),SYNAD=ERR2
SYSDCB   DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=(PM),RECFM=F,LRECL=133,  C
               BLKSIZE=133,SYNAD=ERRSYS
         DCBD  DSORG=(QS)
         END   TEST
