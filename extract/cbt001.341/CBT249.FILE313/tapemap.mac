MPTP     TITLE 'MAPTAPE -- LIST ALL DATA SETS ON A REEL OF TAPE  -WPS2'
         MACRO
&LBL     F     &TO,&OFF,&L
         GBLC  &LBLNUM
         AIF   (T'&L EQ 'O').NOL
&LBLNUM  SETC  '&L'
.NOL     ANOP
&LBL     MVC   &LBLNUM&TO,RECORD+&OFF
         MEND
MAPTAPE  CSECT
         SAVE  (14,12),,*
         LR    12,15
         USING MAPTAPE,12
         LR    15,13
         LA    13,SAVER
         ST    15,4(13)
         ST    13,8(15)
         RDJFCB (SYSUT1)           GET DD CARD FOR THE REEL
         ICM   1,3,JFCB+68        GET THE BLP COUNT
         BNZ   *+8                IF SPECIFIED, USE IT
         LA    1,1                ELSE DEFAULT TO 1
         STH   1,FCNT             SAVE IT FOR LATER
         OPEN  (SYSUT1,INPUT,SYSPRINT,OUTPUT)
         TM    SYSUT1+48,X'10'     DID IT OPEN
         BZ    CLOSEOUT
         TM    SYSPRINT+48,X'10'
         BZ    CLOSEOUT
         SPACE 3
LOOP     EXCP  IOB                 GO DO THE READ
         WAIT  ECB=ECB             WAIT FOR COMPLETION
         CLI   ECB,X'7F'           WERE THERE ANY ERRORS
         BNE   ERROR
         SPACE 2
GOTREC   LA    1,LABELS            POINT TO TABLE OF LABEL TYPES
LCOMP    CLC   0(4,1),RECORD       IS THIS THE ONE
         BE    GOTIT               YES
         LA    1,8(,1)             MOVE TO NEXT ENTRY
         CLI   0(1),X'FF'          IS IT END OF TABLE
         BNE   LCOMP
FUNNYREC LH    1,FCNT             GET THE FILE COUNT
         CVD   1,DBLWRK           MAKE IT DECIMAL
         UNPK  BLPMSG+11(2),DBLWRK MAKE IT PRINTABLE
         OI    BLPMSG+12,C'0'     REMOVE THE PLUS SIGN
         PUT   SYSPRINT,UNKNOWN
         MVI   RECORD+2,C'F'       SIMULATE EOF2
         MVI   IOB,0              CLEAR ERROR FLAG
         MVI   SYSUT1+44,0        RESET DCBOFLGS ERROR INDICATION
         MVI   CCW,BSR                  BACK SPACE OVER THE FUNNY REC
         EXCP  IOB
         WAIT  ECB=ECB
         B     CHKDATA                  GO ANALYZE THE DATA FILE
         SPACE 3
ERROR    CLC   CSW+4(2),=X'0C40'   IS IT JUST WRONG LENGTH RECORD
         BE    FUNNYREC            YES, GO PRINT FIRST 80 BYTES
         SYNADAF ACSMETH=EXCP,PARM1=IOB SET UP A MESSAGE
         MVC   ERRMSG(52),68(1)    MOVE IT TO OUR AREA
         SYNADRLS
         PUT   SYSPRINT,ERRMSGX    TELL THE USER
         B     CLOSEOUT
         SPACE 4
GOTIT    L     1,4(,1)             GET PROCESSING ROUTINE ADDRESS
         BR    1                   GO DO IT
         SPACE 2
LABELS   DS    0F
         DC    CL4'VOL1',A(VOL1)
         DC    CL4'HDR1',A(HDR1)
         DC    CL4'HDR2',A(HDR2)
         DC    CL4'EOF1',A(EOF1)
         DC    CL4'EOF2',A(EOF2)
         DC    CL4'EOV1',A(EOF1)
         DC    CL4'EOV2',A(EOF2)
         DC    X'FF'               END OF TABLE FLAG
         SPACE 4
VOL1     MVC   VOLSER,RECORD+4
         MVC   OWNER,RECORD+41
         PUT   SYSPRINT,VOLMSG
         B     LOOP                GO READ NEXT LABEL
         SPACE 3
HDR1     F     DSN,4,L1
         F     SER,21
         F     VSEQ,27
         F     DSEQ,32
         F     CRDY,42
         F     CRDD,44
         F     EXDY,48
         F     EXDD,50
         MVC   L1GEN,=CL18' '
         PUT   SYSPRINT,L1R1
         B     LOOP                GO GET THE HDR2 LABEL
         SPACE 4
EOF1     F     BLC,54,E1
         MVC   E1EOV,=CL18' '
         CLI   RECORD+2,C'F'       IS IT EOF OR EOV
         BE    NOTEOV
         MVC   E1EOV,=CL13'END OF VOLUME'
NOTEOV   MVC   E1PRO,=CL18' '
         CLI   RECORD+53,C'1'      TEST THE PROTECTION FIELD
         BL    DONEPRO             0 MEANS NO PROTECTION
         MVC   E1PRO,=C'PASSWORD PROTECTED'
         BE    DONEPRO             1 MEANS READ OR WRITE
         MVC   E1PRO,=C'WRITE PROTECTED'
DONEPRO  PUT   SYSPRINT,E1R1
         B     LOOP                GO READ EOF2/EOV2 LABEL
         SPACE 4
HDR2     MVC   L2RCFM,=CL18' '     BLANK OUT THE RECFM FIELD
         MVC   L2RCFM(1),RECORD+4  IS IT F, V OR U
         MVC   L2RCFM+1(1),RECORD+36  SET CARRIAGE CONTROL
         CLI   RECORD+38,C' '      IS IT BLOCKED
         BE    OK2                 NO, RECFM IS COMPLETE
         MVC   L2RCFM+1(1),RECORD+38 SET BLOCKING INDICATOR
         MVC   L2RCFM+2(1),RECORD+36 SET CARRIAGE CONTROL
         CLI   RECORD+38,C'R'      IS IT VBS
         BNE   OK2                 NO, RECFM IS COMPLETE
         MVC   L2RCFM+1(2),=C'BS'
         MVC   L2RCFM+3(1),RECORD+36 SET CARRIAGE CONTROL
OK2      F     BLK,5,L2
         F     LREC,10
         F     DEN,15
         F     STPID,17
         F     TRTCH,34
         MVC   L2DSPOS,=CL18' '    BLANK OUT FIELD
         CLI   RECORD+16,C'0'      CHECK FOR MULTI-VOLUME
         BE    OK3                 NO, CONTINUE
         CLI   RECORD+2,C'V'       IS THIS AN EOV2
         BE    OK3                 YES, IGNORE THE FIELD
         MVC   L2DSPOS,=CL16'NOT FIRST VOLUME'
OK3      PUT   SYSPRINT,L2R1
EOF2     CLI   RECORD+2,C'V'       IS THERE MORE ON THIS VOLUME
         BE    CLOSEOUT            NO, THAT WAS EOV2
         SPACE 3
*
*   NOW WE HAVE TO SKIP TO THE NEXT LABEL GROUP
*
SKIPF    MVI   CCW,FSF             SET OPCODE TO FORWARD SPACE FILE
         EXCP  IOB
         LH    1,FCNT
         LA    1,1(,1)            INCREMENT THE FILE COUNT
         STH   1,FCNT
         WAIT  ECB=ECB
         CLI   ECB,X'7F'           ANY PROBLEMS
         BNE   ERROR               YES, COMMENT AND RAISE ERROR
         CLI   RECORD+2,C'F'       WAS LAST ONE AN EOF2
         BE    EOF                 YES, NEXT MAY BE TAPE MARK
         SPACE 3
CHKDATA  LA    1,LENCCW                POINT AT CCW FOR MEASURING RECS
         ST    1,IOB+16                MAKE IT THE CURRENT ONE
         LA    2,1                     INITIALIZE RECORD COUNTER
CHKLOOP  EXCP  IOB                     GO READ A RECORD
         WAIT  ECB=ECB
         CLI   ECB,X'7F'               DID IT COMPLETE OK
         BNE   CHKEOF                  NO, MAYBE WAS EOF
         LH    5,LENCCW+6              GET ORIGINAL COUNT
         SH    5,CSW+6                 SUBTRACT RESIDUAL
         BCT   2,NOTFIRST              FALL THRU ONLY ON FIRST RECORD,
*                                      SUBSEQUENT ONES DECREMENT AND GO
         LR    6,5                     SET FIRST DIVISOR
         LR    3,5                     SET FIRST BLKSIZE
         B     CHKLOOP                 GO TRY ANOTHER RECORD
         SPACE 2
NOTFIRST CR    5,3                     IS THIS A NEW HIGH
         BNH   *+6                     NO, FORGET IT
         LR    3,5                     YES, SAVE IT
GCDLOOP  SR    4,4                     CLEAR HIGH END OF DIVIDEND
         DR    4,6                     CHECK FOR DIVISABILITY
         LTR   4,4                     IS THERE A REMAINDER
         BZ    CHKLOOP                 NO, GO DO THE NEXT RECORD
         LR    5,6                     YES, USE OLD DIVISOR AS DIVIDEND
         LR    6,4                     AND REMAINDER AS DIVISOR
         B     GCDLOOP                 AND TRY AGAIN
         SPACE 2
CHKEOF   TM    CSW+4,X'01'             WAS IT AN EOF
         BZ    ERROR                   NO, BITCH LIKE HELL
         LA    1,CCW                   YES, RESTORE ORIGINAL CCW
         ST    1,IOB+16
         MVI   CCW,READ                SET UP FOR NORMAL READ AGAIN
         MVI   SYSUT1+44,0             RESET THE ERROR FLAGS
         MVI   IOB,0
         LH    1,FCNT
         LA    1,1(,1)                 INCREMENT THE FILE COUNT
         STH   1,FCNT
         LA    1,1
         SR    1,2                     COMPUTE BLOCK COUNT
         BZ    EMPTY                   NO RECORDS AT ALL
         CVD   1,DBLWRK
         UNPK  DRBLK,DBLWRK            MAKE BLOCK COUNT PRINTABLE
         OI    DRBLK+5,C'0'            DELETE THE SIGN
         CVD   3,DBLWRK                GET MAX BLOCK SIZE
         MVC   DRBSZ,=X'2020202120'    MOVE IN THE EDIT MASK
         ED    DRBSZ-1(6),DBLWRK+5     MAKE IT PRINTABLE
         CVD   6,DBLWRK                GET THE MAX LRECL
         MVC   DRLRL,=X'2020202120'    MOVE IN THE EDIT MASK
         ED    DRLRL-1(6),DBLWRK+5     MAKE THAT PRINTABLE
         PUT   SYSPRINT,DATAREC        PRINT THE PRINTABLE STUFF
         B     LOOP
         SPACE 2
EMPTY    PUT   SYSPRINT,EMPTYMSG       TELL WHAT HAPPENED
         B     LOOP                GO PROCESS NEXT LABEL GROUP
         SPACE 3
EOF      MVI   CCW,READ            SET TO READ A RECORD
         EXCP  IOB
         WAIT  ECB=ECB
         CLI   ECB,X'7F'
         BE    GOTREC              NOT EOF, GOT ANOTHER LABEL GROUP
         TM    CSW+4,X'01'         WAS IT AN EOF
         BO    CLOSEOUT            YES, END OF REEL
         B     ERROR               NO, SOMETHING ELSE WRONG
         SPACE 5
CLOSEOUT CLOSE (SYSUT1,,SYSPRINT)
         L     13,4(13)
         RETURN (14,12),RC=0
         EJECT
READ     EQU   X'02'               READ OPCODE FOR TAPE
FSF      EQU   X'3F'               FORWARD SPACE FILE
BSR      EQU   X'27'                    TAPE COMMAND TO BACKSPACE
SKIP     EQU   X'10'                   SKIP DATA TRANSFER IN CCW
SLI      EQU   X'20'                    CCW SUPPRESS INCORRECT LENGTH
SAVER    DS    18F
SYSUT1   DCB   DDNAME=SYSUT1,MACRF=(E),DEVD=TA,EXLST=EXL,DSORG=PS,     X
               IOBAD=IOB,RECFM=F
EXL      DC    X'87',AL3(JFCB)     EXIT LIST
JFCB     DS    CL176
ECB      DC    A(0)
IOB      DC    A(0,ECB)
CSW      DC    A(0,0)
         DC    A(CCW,SYSUT1,0,0,0)
CCW      CCW   READ,RECORD,0,80   READ A RECORD
LENCCW   CCW   READ,0,SLI+SKIP,32767
DBLWRK   DS    D
FCNT     DS    H
UNKNOWN  DC    C'0'                CARRIAGE CONTROL FOR DUMPING RECORD
RECORD   DC    CL80' '
BLPMSG   DC    CL40' -- LABEL=(NN,BLP) '
EMPTYMSG DC    CL121' FILE CONTAINS NO RECORDS'
DATAREC  DC    CL43' '                 FOR LOOKS
DRBLK    DC    CL6'999999'
         DC    C' PHYSICAL BLOCKS, BLKSIZE '
DRBSZ    DC    CL5'ZZZZ9'
         DC    C', '
DRLRL    DC    CL5'ZZZZ9'
         DC    CL34' IS A MULTIPLE OF LRECL (IF FB).'
ERRMSGX  DC    C'-'
ERRMSG   DC    CL52' '
         DC    CL68' -- UNRECOVERABLE INPUT ERROR'
SYSPRINT DCB   DDNAME=SYSPRINT,MACRF=(PM),DSORG=PS,LRECL=121,          X
               BLKSIZE=121,RECFM=FA
         SPACE 2
VOLMSG   DC    CL34'1     M A P   O F   V O L U M E   '
VOLSER   DC    CL6' '
         DC    CL20'           OWNER IS '
OWNER    DC    CL10' '
         DC    CL51' '
         SPACE 2
L1R1     DC    CL6'-FILE '          6
L1DSEQ   DC    CL4'0000'           10
         DC    CL6'  DSN='         16
L1DSN    DC    CL17'&&DSN'         33
L1GEN    DC    CL9'.G0000V00'      42
         DC    CL10'  CREATED '       52
L1CRDY   DC    CL2'73'             54
         DC    CL1'.'              55
L1CRDD   DC    CL3'004'            58
         DC    CL10'  EXPIRES '    68
L1EXDY   DC    CL2'99'             70
         DC    CL1'.'              71
L1EXDD   DC    CL3'360'            74
         DC    CL13'   STARTS ON ' 87
L1SER    DC    CL6'DLIB01'         93
         DC    CL17', THIS IS VOLUME '  110
L1VSEQ   DC    CL4'0001'           114
         DC    CL7' '              121
         SPACE 2
L2R1     DC    CL11' '             11
L2DSPOS  DC    CL16' '             27
         DC    CL16' '             43
         DC    CL6'RECFM='         49
L2RCFM   DC    CL4'VBSA'           53
         DC    CL9' BLKSIZE='      62
L2BLK    DC    CL5'00080'          67
         DC    CL7' LRECL='        74
L2LREC   DC    CL5'00080'          79
         DC    CL5' DEN='          84
L2DEN    DC    CL1'0'              85
         DC    CL7' TRTCH='        92
L2TRTCH  DC    CL2'ET'             94
         DC    CL10' JOB/STEP='    104
L2STPID  DC    CL17'JOBNAME /STEPNAME'  121
         SPACE 2
E1R1     DC    CL11' '                 11
E1EOV    DC    CL13' '             24
         DC    CL19' '             43
         DC    CL21'LABEL BLOCK COUNT IS ' 64
E1BLC    DC    CL6'999999'             70
         DC    CL3'   '                73
E1PRO    DC    CL18' '             91
         DC    CL30' '             121
         END   MAPTAPE
