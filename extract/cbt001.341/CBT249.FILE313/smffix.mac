SMFFIX   CSECT
         TITLE 'SMF FIX BAD BLOCKS PROGRAM'
*****
*****      THIS PROGRAM READS SYS1.MANX/Y OR ANY SMF TAPE
*****    AND STRIPS BAD BLOCKS OFF OF IT. IT ALSO PRODUCES AN EXTRA
*****    COPY OF THE SMF DATA
*****
*****      JCL REQ.
*****
*****      //SMFFIX   EXEC PGM=SMFFIX,REGION=62K
*****      //SYSPRINT DD  SYSOUT=A
*****      //INPUT    DD  INPUT MANX/Y OR ANY SMF TAPE/DISK DATASET
*****      //OUTPUT1  DD    OUTPUT TAPE OR DISK 1
*****      //OUTPUT2  DD    OUTPUT TAPE OR DISK 2
*****
*******************************************
*                                         *
*          NOTE THAT DCB BLKSIZE          *
*        AND IN/OUT AREAS                 *
*        ARE ISTALLATION DEPENDENT        *
*                                         *
*******************************************
         EJECT
         SAVE  (14,12),,*
         LR    10,15
         USING SMFFIX,10,9,8,7
         LA    6,4095
         LA    9,1(6,10)
         LA    8,1(6,9)
         LA    7,1(6,8)
         LA    11,SAVE
         ST    13,4(11)
         ST    11,8(13)
         LR    13,11
         OPEN  (ADCB,,DDCB,OUTPUT)
L        BAL   6,GETREC
         B     EOF
S        BC    0,S2
         OPEN  (BDCB,OUTPUT,CDCB,OUTPUT)
         OI    S+1,X'F0'
S2       PUT   BDCB,B1
         PUT   CDCB,B1
         SR    15,15
         IC    15,B1+5
         AR    15,15
         LH    1,COUNTS(15)
         LA    1,1(,1)
         STH   1,COUNTS(15)
         B     L
EOF      TM    S+1,X'F0'
         BZ    ERROR
         CLOSE (BDCB,,CDCB)
DONE     CLOSE (ADCB)
         MVC   ELNOTE+13(L'BLANKS),BLANKS
         SR    12,12
REPON    LH    0,COUNTS(12)
         LTR   0,0
         BZ    REPXT
         CVD   0,DWORD
         UNPK  ELNOTE,DWORD+5(3)
         OI    ELNOTE+4,C'0'
         LR    0,12
         SRL   0,1
         CVD   0,DWORD
         UNPK  ELNOTE+8(3),DWORD+6(2)
         OI    ELNOTE+10,C'0'
         PUT   DDCB,ELINE
REPXT    LA    12,2(,12)
         C     12,=F'512'
         BL    REPON
         CLOSE (DDCB)
         L     13,4(13)
         TM    S+1,X'F0'
         BZ    ERC
         RETURN (14,12),RC=0
ERC      RETURN (14,12),RC=16
ERROR    WTO   '* ERROR * ACCOUNTING DATA SET EOF BEFORE BACKUP',      X
               ROUTCDE=(1,2,10,11),DESC=6
         B     DONE
COUNTS   DC    256H'0'
DWORD    DC    D'0'
FULL     DC    F'0'
SAVE     DC    18F'0'
         EJECT
******************************************
*                                         *
*          INPUT, OUTPUT1, OUTPUT2        *
*        BLKSIZE SHOULD BE MODIFIED       *
*        TO REFLECT INSTALLATION          *
*        SMF BUFFER SIZE                  *
*                                         *
******************************************
ADCB     DCB   DSORG=PS,RECFM=U,BLKSIZE=12960,MACRF=GM,                X
               DDNAME=INPUT,EODAD=EA
BDCB     DCB   DSORG=PS,LRECL=12956,BLKSIZE=12960,MACRF=PM,RECFM=VBS,  X
               DDNAME=OUTPUT1
CDCB     DCB   DSORG=PS,LRECL=12956,BLKSIZE=12960,MACRF=PM,RECFM=VBS,  X
               DDNAME=OUTPUT2
DDCB     DCB   DSORG=PS,LRECL=121,BLKSIZE=121,RECFM=FA,                X
               DDNAME=SYSPRINT,MACRF=PM
         EJECT
E        UNPK  ELNOTE,EBLOCK
         OI    ELNOTE+4,C'0'
         PUT   DDCB,ELINE
         BR    5
EBLOCK   DC    PL3'0'
ELINE    DS    0CL121
         DC    C'-******  '
ELNOTE   DC    CL5'00000'
         DC    C'  ****** INPUT BLOCK DROPPED *** BAD RECORD ***'
BLANKS   DC    CL60' '
ANYIN    DC    X'00'
ANYOUT   DC    X'00'
GETREC   CLI   ANYIN,X'00'
         BNE   YESIN
         GET   ADCB,A1
         AP    EBLOCK,=P'1'
         MVI   ANYIN,X'10'
         LA    4,A1
         LH    3,A1
         AR    3,4
         LA    4,4(,4)
YESIN    CLI   ANYOUT,X'00'
         BNE   YESOUT
         MVI   ANYOUT,X'10'
         LA    2,4
         STH   2,B1
         LA    2,B1+4
         XC    B1+2(2),B1+2
YESOUT   CLI   2(4),X'00'
         BE    ANYLEFT0
         CLI   2(4),X'01'
         BE    ANYLEFT1
         CLI   ANYOUT,X'11'
         BNE   DROPIT
         CLI   2(4),X'03'
         BH    SPANERR
YESMOVE  MVC   FULL+2(2),0(4)
         L     1,FULL
         C     1,INBUFMAX      IS THE INPUT SEQMENT LARGER THAN BUFFER?
         BH    ABEND            IF SO-ABEND NOW
         S     1,=F'5'
         BM    SPANERR
         LA    0,1
         AH    0,B1
         C     0,OUTBFMAX       IS THE ASSEMBLED OUTPUT REC TOO LARGE?
         BH    ABEND             IF SO ABEND NOW
         AR    0,1
         STH   0,B1
         CLI   2(4),X'02'
         BNE   CFOR255
         MVI   ANYOUT,X'12'
CFOR255  C     1,=F'255'
         BH    MOVE256
         STC   1,*+5
         MVC   0(*-*,2),4(4)
         LA    2,1(1,2)
         LA    4,5(1,4)
TRY4MORE CR    4,3
         BL    *+8
         MVI   ANYIN,X'00'
         CLI   ANYOUT,X'12'
         BE    GIVEBAC
         B     GETREC
MOVE256  MVC   0(256,2),4(4)
         LA    2,256(,2)
         LA    4,256(,4)
         S     1,=F'256'
         B     CFOR255
GIVEBAC  MVI   ANYOUT,X'00'
         B     4(,6)
DROPIT   BAL   5,E
         MVC   FULL+2(2),0(4)
         A     4,FULL
         B     TRY4MORE
SPANERR  BAL   5,E
         MVI   ANYIN,X'00'
         MVI   ANYOUT,X'00'
         B     GETREC
ANYLEFT0 CLI   ANYOUT,X'11'
         BE    SPANINC
         MVI   ANYOUT,X'12'
         B     YESMOVE
ANYLEFT1 CLI   ANYOUT,X'11'
         BE    SPANINC
         MVI   ANYOUT,X'11'
         B     YESMOVE
SPANINC  BAL   5,E
         MVI   ANYOUT,X'00'
         B     YESIN
EA       CLI   ANYOUT,X'00'
         BE    0(,6)
         BAL   5,E
         B     0(,6)
INBUFMAX   DC    A(12960)     INPUT BUFF MAX SIZE
OUTBFMAX    DC    A(12960)     OUTPUT BUFF MAX SIZE
ABEND       ABEND 001,DUMP,STEP
            EJECT
            LTORG
         DS    0D
******************************************
*                                         *
*          A1 AND B1 SHOULD BE MODIFED    *
*        TO REFLECT INSTALLATION          *
*        SMF BUFFER SIZE                  *
*B1 MUST BE AS LARGE AS THE LARGEST COMPLETELY ASSEMBLED RECORD*
******************************************
A1       DS    12960C                  INPUT READ IN AREA
         DS    0D
B1       DS    12960C                   OUTPUT 1 AND 2 OUT AREA
         END
