BLOCK    CSECT
         SPACE 2
         SETUP
         SPACE 2
         OPEN  (INPUT,(INPUT),OUTPUT,(OUTPUT),PRINT,(OUTPUT))
         EJECT
         MVI   CONTROL,C'1'            START PRINTOUT ON NEW PAGE
         LH    WKREG1,OUTPUT+82        GET 'LRECL' FROM OUTPUT DCB
         LA    WKREG2,80               INPUT CARD LENGTH
         SR    WKREG0,WKREG0           ZERO LEFT OF DIVIDEND
         DR    WKREG0,WKREG2           NUMBER OF FULL IN RECS / OUT REC
         LTR   WKREG0,WKREG0           IS THERE ANY REMAINDER
         BZ    NEXT                    IF NOT, BEGIN PROCESSING
         LA    WKREG1,1(WKREG1)        OTHERWISE, ADD 1 TO INPUT NEEDS
NEXT     LR    WKREG2,WKREG1           PICK UP NR OF RECS TO BE READ
         LA    WKREG0,OUTAREA          AND ADDRESS OF OUTPUT AREA
         SPACE 2
GET      GET   INPUT,INAREA            READ A CARD
         SPACE 2
         PUT   PRINT,CONTROL           LIST INPUT RECORD
         SPACE 2
         MVI   CONTROL,C' '            NOW GO TO SINGLE SPACING
         MVC   0(80,WKREG0),INAREA     MOVE RECORD TO OUTPUT AREA
         LA    WKREG0,80(WKREG0)       POINT TO NEXT AREA
         BCT   WKREG2,GET              FILL OUTPUT AREA
         SPACE 2
         PUT   OUTPUT,OUTAREA          THEN WRITE OUT A RECORD
         SPACE 2
         MVI   CONTROL,C'0'            DOUBLE SPACE BETWEEN INPUT RECS
         B     NEXT                    CONTINUE THROUGH FILE
         EJECT
EOF      CLOSE (INPUT,,OUTPUT,,PRINT)
         SPACE 2
         RESTORE
         EJECT
INPUT    DCB   DSORG=PS,MACRF=GM,DDNAME=SYSUT1,EODAD=EOF
         EJECT
PRINT    DCB   DSORG=PS,MACRF=PM,DDNAME=SYSPRINT,                      ,
               RECFM=FBA,LRECL=81,BLKSIZE=891
         EJECT
OUTPUT   DCB   DSORG=PS,MACRF=PM,DDNAME=SYSUT2
         EJECT
WKREG0   EQU   6
WKREG1   EQU   7
WKREG2   EQU   8
CONTROL  DS    C
INAREA   DS    CL80
OUTAREA  DS    CL4000
         END
