000010 IDENTIFICATION DIVISION.
000020 PROGRAM-ID.
000030     PRG-2-FLOBOL.
000040 AUTHOR.
000050     NAVY PROGRAMMING LANGUAGES DIVISION.
000060 INSTALLATION.
000070     NPLD OFFICE OF INFORMATION SYSTEMS PLANNING AND DEVELOPMENT.
000080 DATE-WRITTEN.
000090     VERSION 1.0 01-01-69 VERSION 2.0 09-01-69.
000100     UPDATED AS OF  1 APRIL 1970.
000110 DATE-COMPILED.
000120     COMMENT-ENTRY.
000130 SECURITY.
000140     FREEDOM OF INFORMATION ACT.
000150 REMARKS.
000160     *        ANY COMMENTS ABOUT THIS PROGRAM SHOULD BE SENT TO  *
000170     *                                                           *
000180     *        NAVY PROGRAMMING LANGUAGES DIVISION                *
000190     *        OFFICE OF INFORMATION SYSTEMS PLANNING AND         *
000200     *             DEVELOPMENT                                   *
000210     *        ROOM 5D840 PENTAGON                                *
000220     *        WASHINGTON D. C.  20350 USA                        *
000230     *                                                           *
000240     *     ATTN  FLOBOL.                                         *
000250     *                                                           *
000260     *                                   (202) OX5-4750          *
000270     *                                                           *
000280     *        *         *         *         *         *          *
000290          THIS PROGRAM CREATES THE CROSS REFERENCE LISTING FROM
000300     SUPPLEMENTAL--PART2-IN AND CREATES TWO FILES THAT ARE PASSED
000310     TO FLOBOL03. EACH DATA-NAME AND PROCEDURE-NAME IS LISTED IN
000320     ALPHABETICAL ORDER SHOWING THE LINE WHERE IT APPEARED
000330     FOLLOWED BY THE LINE NUMBER OF EACH REFERENCE AND THE TYPE
000340     OF REFERENCE. ONLY THE 'A' RECORDS ARE USED FOR THE CROSS
000350     REFERENCE LISTING.
000360
000370          DATA-NAME-1   0029  01
000380                        0130  MOVE
000390                        0137  MOVE
000400                        0296  IF
000410                        0300  EXAMINE
000420          PROCEDURE-1   0100  PARAGRAPH
000430                        0150  GO TO
000440                        0175  PERFORM
000450                        0200  ALTER
000460
000470          THE 'C' TYPE RECORDS ARE USED TO CREATE THE TWO OUTPUT
000480     FILES. ONLY THE PARAGRAPH AND SECTION RECORDS ARE WRITTEN TO
000490     PARFILEOUT. THE REST OF THE FILE IS WRITTEN TO
000500     INPUT FILES  PRG-2.
000510        SUPPLEMENTAL-PART2-IN.
000520            1 PASSED TO SORT 1 THEN TO FLOBOL 02.
000530                 MAJOR FIELD  1  -  15 ASCENDING,
000540                 MINOR FIELD  17 -  64 ASCENDING
000550           2 RECORD LENGTH IS 120 CHARACTERS.
000560           3 BLOCKING FACTOR IS 1 RECORD PER BLOCK.
000570           4 LABEL RECORDS HAVE BEEN OMITTED BUT SHOULD BE USED
000580             IF NECESSARY.
000590           5 RECORD AND/OR BLOCK LENGTH MAY BE INCREASED FOR
000600             EFFICIENCY REASONS BUT SHOULD BE CONSISTENT WITH
000610             OTHER DESCRIPTIONS FOR THIS FILE IN THE FLOBOL
000620             SYSTEM.
000630           6 A DESCRIPTION OF THE RECORDS FOUND IN THIS FILE
000640             FOLLOW.
000650          DATA DIVISION RECORDS
000660               A A  (TYPE A RECORD)
000670               B PROGRAM ID
000680               C DATA NAME
000690               D LEVEL NUMBER, FD SD RD
000700               E DIVISION A2 FILE SECTION
000710                          A3 WORKING STORAGE SECTION
000720                          A4 REPORT SECTION
000730                          A6 PROCEDURE DIVISION
000740               F LINE NUMBER
000750          PARAGRAPH RECORDS
000760               A A (TYPE A RECORD)
000770               B PROGRAM ID
000780               C            BLANK
000790               D PROCEDURE NAME
000800               E   PRGRAPH
000810               F A6 FOR PROCEDURE DIVISION
000820               G LINE NUMBER
000830               H            BLANK
000840          DATA NAME RECORDS - PROCEDURE DIVISION
000850               A A (TYPE A RECORDS)
000860               B PROGRAM ID
000870               C            BLANK
000880               D DATA NAME
000890               E VERB WHICH REFERENCES DATA NAME
000900               F A6 - FOR PROCEDURE DIVISION
000910               G LINE NUMBER
000920               H            BLANK
000930          DATA NAME SUBSCRIPTED
000940               A A (TYPE A RECORD)
000950               B PROGRAM ID
000960               C S
000970               D DATA NAME
000980               E VERB WHICH REFERENCES DATA NAME
000990               F A6
001000               G LINE NUMBER
001010               H            BLANK
001020          DATA NAME QUALIFIED
001030               A A (TYPE A RECORD)
001040               B PROGRAM ID
001050               C            BLANK
001060               D DATA NAME
001070               E VERB WHICH REFERENCES DATA NAME
001080               F A6
001090               G LINE NUMBER
001100               H QQ
001110               I QUALIFIER
001120          PARAGRAPH AND SECTION RECORDS
001130              A C (TYPE C RECORD)
001140              B PROGRAM NUMBER
001150              C PROGRAM ID
001160          D PARAGRAPH OR SECTION NAME
001170              E COBOL REFERENCE NUMBER
001180              F 0000
001190              G            NOT USED
001200              H PARAGRAPH OR SECTION
001210              I            NOT USED
001220              J            NOT USED
001230              K            NOT USED
001240              L RECORD OCCURRENCE NUMBER
001250          CONNECTOR RECORDS
001260              A C (TYPE C RECORDS)
001270              B PROGRAM NUMBER
001280              C PROGRAM ID
001290              D PARAGRAPH THAT GO TO ADDRESSED
001300              E 0000
001310              F COBOL REFERENCE OF GO TO
001320              G COBOL STATEMENT OF GO TO
001330              H CONNECTOR
001340              I PARAGRAPH THAT GO TO WAS WITHIN
001350              J            NOT USED
001360              K            NOT USED
001370              L RECORD OCCURRENCE NUMBER
001380          SWITCH / ALTER RECORD
001390              A C (TYPE C RECORD)
001400              B PROGRAM NUMBER
001410              C PROGRAM ID
001420              D ALTER NAME FIRST ONE
001430              E ----
001440              F COBOL REFERENCE NUMBER OF ALTER
001450              G COBOL STATEMENT NUMBER OF ALTER
001460              H SWITCH
001470              I ALTER NAME SECOND ONE
001480              J            NOT USED
001490              K            NOT USED
001500              L RECORD OCCURRENCE NUMBER
001510          CONNECTOR FROM SWITCH / ALTER SETTING RECORD
001520              A C (TYPE C RECORD)
001530              B PROGRAM NUMBER
001540              C PROGRAM ID
001550              D ALTER NAME SECOND ONE
001560              E ----
001570              F 0000
001580              G 0000
001590              H SWITCHENT
001600              I ALTER NAME FIRST ONE
001610              J            NOT USED
001620              L RECORD OCCURRENCE NUMBER
001630          PERFORM ENTRANCE RECORDS
001640              A C (TYPE C RECORD)
001650              B PROGRAM NUMBER
001660              C PROGRAM ID
001670              D PERFORM NAME FIRST ONE
001680              E 0000
001690              F COBOL REFERENCE NUMBER OF PERFORM
001700              G COBOL STATEMENT NUMBER OF PERFORM
001710              H PER ENT
001720              I PARAGRAPH THAT PERFORM WAS WITHIN
001730              J            NOT USED
001740              K            NOT USED
001750              L RECORD OCCURRENCE NUMBER
001760          PERFORM EXIT RECORD
001770              A C (TYPE C RECORDS)
001780              B PROGRAM NUMBER
001790              C PROGRAM ID
001800              D PERFORM NAME SECOND IF PRESENT
001810              E ****
001820              F COBOL REFERENCE NUMBER OF PERFORM
001830              G COBOL STATEMENT NUMBER OF PERFORM
001840              H PER EXT
001850              I            NOT USED
001860              J            NOT USED
001870              K            NOT USED
001880              L RECORD OCCURRENCE NUMBER
001890     OUTPUT FILES  PRG-2.
001900        PARFILEOUT
001910           1. PASSED TO FLOBOL03.
001920           2. RECORD LENGTH IS 60 CHARACTERS.
001930           3. BLOCKING FACTOR IS 1 RECORD PER BLOCK.
001940           4. LABEL RECORDS HAVE BEEN OMITTED BUT SHOULD BE USED
001950              IF NECESSARY.
001960           5. RECORD LENGTH AND/OR BLOCKING FACTOR MAY BE
001970              INCREASED FOR EFFICIENCY REASONS BUT SHOULD BE
001980              CONSISTENT WITH THE OTHER DESCRIPTIONS FOR THIS
001990              FILE IN THE FLOBOL SYSTEM.
002000
002010          PARAGRAPH / SECTION RECORD
002020              A PROGRAM NUMBER
002030              B PROCEDURE NAME
002040              C COBOL REFERENCE NUMBER OF PROCEDURE NAME
002050              D COBOL STATEMENT NUMBER OF PROCEDURE NAME
002060              E RECORD OCCURENCE NUMBER
002070        SORTEDSOURCE
002080           1. PASSED TO FLOBOL03.
002090           2. RECORD LENGTH IS 120 CHARACTERS.
002100           3. BLOCKING FACTOR IS 1 RECORD PER BLOCK.
002110           4. LABEL RECORDS HAVE BEEN OMITTED BUT SHOULD BE USED
002120              IF NECESSARY.
002130           5. RECORD LENGTH AND/OR BLOCKING FACTOR MAY BE
002140              INCREASED FOR EFFICIENCY REASONS BUT SHOULD BE
002150              CONSISTENT WITH THE OTHER DESCRIPTIONS FOR THIS
002160              FILE IN THE FLOBOL SYSTEM.
002170           6. A DESCRIPTION OF THE RECORDS IN THIS FILE
002180              FOLLOWS.
002190          PARAGRAPH AND SECTION RECORDS
002200              A C (TYPE C RECORD)
002210              B PROGRAM NUMBER
002220              C PROGRAM ID
002230              D PARAGRAPH OR SECTION NAME
002240              E COBOL REFERENCE NUMBER
002250              F 0000
002260              G            NOT USED
002270              H PARAGRAPH OR SECTION
002280              I            NOT USED
002290              J            NOT USED
002300              K            NOT USED
002310              L RECORD OCCURRENCE NUMBER
002320          CONNECTOR RECORDS
002330              A C (TYPE C RECORDS)
002340              B PROGRAM NUMBER
002350              C PROGRAM ID
002360              D PARAGRAPH THAT GO TO ADDRESSED
002370              E 0000
002380              F COBOL REFERENCE OF GO TO
002390              G COBOL STATEMENT OF GO TO
002400              H CONNECTOR
002410              I PARAGRAPH THAT GO TO WAS WITHIN
002420              J            NOT USED
002430              K            NOT USED
002440              L RECORD OCCURRENCE NUMBER
002450          SWITCH / ALTER RECORD
002460              A C (TYPE C RECORD)
002470              B PROGRAM NUMBER
002480              C PROGRAM ID
002490              D ALTER NAME FIRST ONE
002500              E ----
002510              F COBOL REFERENCE NUMBER OF ALTER
002520              G COBOL STATEMENT NUMBER OF ALTER
002530              H SWITCH
002540              I ALTER NAME SECOND ONE
002550              J            NOT USED
002560              K            NOT USED
002570              L RECORD OCCURRENCE NUMBER
002580          CONNECTOR FROM SWITCH / ALTER SETTING RECORD
002590              A C (TYPE C RECORD)
002600              B PROGRAM NUMBER
002610              C PROGRAM ID
002620              D ALTER NAME SECOND ONE
002630              E ----
002640              F 0000
002650              G 0000
002660              H SWITCHENT
002670              I ALTER NAME FIRST ONE
002680              J            NOT USED
002690              L RECORD OCCURRENCE NUMBER
002700          PERFORM ENTRANCE RECORDS
002710              A C (TYPE C RECORD)
002720              B PROGRAM NUMBER
002730              C PROGRAM ID
002740              D PERFORM NAME FIRST ONE
002750              E 0000
002760              F COBOL REFERENCE NUMBER OF PERFORM
002770              G COBOL STATEMENT NUMBER OF PERFORM
002780              H PER ENT
002790              I PARAGRAPH THAT PERFORM WAS WITHIN
002800              J            NOT USED
002810              K            NOT USED
002820              L RECORD OCCURRENCE NUMBER
002830          PERFORM EXIT RECORD
002840              A C (TYPE C RECORDS)
002850              B PROGRAM NUMBER
002860              C PROGRAM ID
002870              D PERFORM NAME SECOND IF PRESENT
002880              E ****
002890              F COBOL REFERENCE NUMBER OF PERFORM
002900              G COBOL STATEMENT NUMBER OF PERFORM
002910              H PER EXT
002920              I            NOT USED
002930              J            NOT USED
002940              K            NOT USED
002950              L RECORD OCCURRENCE NUMBER
002960        ANALYZER.
002970           1. THIS IS THE CROSS REFERENCE LISTING PRODUCED BY
002980              FLOBOL. THIS IS A PRINTER DESTINED FILE.
002990           2. RECORD LENGTH IS 132 CHARACTERS.
003000           3. BLOCKING FACTOR IS 1 RECORD PER BLOCK.
003010           4. LABEL RECORDS HAVE BEEN OMITTED BUT SHOULD BE USED I
003020              IF NECESSARY.
003030 ENVIRONMENT DIVISION.
003040 CONFIGURATION SECTION.
003050 SPECIAL-NAMES.
003060     C01
003070     IS TOP-PAGE.
003080 INPUT-OUTPUT SECTION.
003090 FILE-CONTROL.
003100     SELECT SUPPLEMENTAL-PART2-IN ASSIGN TO
003110     UT-S-XXXXX02.
003120     SELECT ANALYZER ASSIGN TO
003130     UT-S-XXXXX36.
003140     SELECT PARFILEOUT ASSIGN TO
003150     UT-S-XXXXX03.
003160     SELECT SORTEDSOURCE ASSIGN TO
003170     UT-S-XXXXX04.
003180 DATA DIVISION.
003190 FILE SECTION.
003200 FD  SUPPLEMENTAL-PART2-IN
003210     LABEL RECORDS ARE STANDARD
003220     BLOCK CONTAINS 0 RECORDS RECORDING MODE IS F
003230     DATA RECORDS ARE SORTLOOK SORTRECORD.
003240 01  SORTLOOK.
003250     02 SORTKEY                         PICTURE IS X(64).
003260     02 SORTKEYANALYZER REDEFINES SORTKEY.
003270     03 SKA                             PICTURE IS X.
003280     03 FILLER                          PICTURE IS XX.
003290     03 SKAPROGRAMID                    PICTURE IS X(12).
003300     03 SKASUB                          PICTURE IS X.
003310     03 SKADATANAME                     PICTURE IS X(32).
003320     03  SKAWSORPD   PICTURE   XX.
003330     03 SKAVERBREF                      PICTURE IS X(8).
003340     03 SKAREFNO                        PICTURE IS 9999.
003350     03 SKAQ                            PICTURE IS XX.
003360     02 SORTKEYCOBOL REDEFINES SORTKEYANALYZER.
003370     03 SKEY                            PICTURE IS X.
003380     03 FILLER                          PICTURE IS XXX.
003390     03 SPROGNO                         PICTURE IS 9999.
003400     03 SPROGRAMID                      PICTURE IS X(12).
003410     03 SPARNAME                        PICTURE IS X(32).
003420     03 SHOLD1                          PICTURE IS XXXX.
003430     03 SHOLD2                          PICTURE IS XXXX.
003440     03 SHOLD3                          PICTURE IS XXXX.
003450     02 SITSWHAT                        PICTURE IS X(9).
003460     02 SINFO                           PICTURE IS X(32).
003470     02 FILLER                          PICTURE IS X(7).
003480     02 SLOCATE                         PICTURE IS 9(8).
003490 01  SORTRECORD                         PICTURE IS X(120).
003500 FD  PARFILEOUT
003510     LABEL RECORDS ARE STANDARD
003520     BLOCK CONTAINS 0 RECORDS RECORDING MODE IS F
003530     DATA RECORDS ARE PARRECOUT.
003540 01  PARRECOUT.
003550     02 PROGNOPAR                       PICTURE IS 9999.
003560     02 PARNAMEPAR                      PICTURE IS X(32).
003570     02 LINEPAR                         PICTURE IS 9999.
003580     02  LINEPAR2 REDEFINES LINEPAR PICTURE XXXX.
003590     02 STATEPAR                        PICTURE IS 9999.
003600     02 LOCATIONPAR                     PICTURE IS 9(8).
003610     02 FILLER                          PICTURE IS X(8).
003620 FD  SORTEDSOURCE
003630     LABEL RECORDS ARE STANDARD
003640     BLOCK CONTAINS 0 RECORDS RECORDING MODE IS F
003650     DATA RECORDS ARE SORTEDRECORD.
003660 01  SORTEDRECORD                       PICTURE IS X(120).
003670 FD  ANALYZER
003680     LABEL RECORDS ARE STANDARD
003690     BLOCK CONTAINS 0 RECORDS RECORDING MODE IS F
003700     DATA RECORDS ARE PRINTHEADER PRINTBODY PRINTCLEAR.
003710 01  PRINTHEADER                 PICTURE X(132).
003720 01  PRINTBODY.
003730     02 PBCONTROL                       PICTURE IS X.
003740     02 FILLER                          PICTURE IS X(4).
003750     02 PBQUALIFY                       PICTURE IS XX.
003760     02 FILLER                          PICTURE IS X.
003770     02 PBDATANAME                      PICTURE IS X(32).
003780     02 FILLER                          PICTURE IS X(6).
003790     02 PBREFLINE                       PICTURE IS 9999.
003800     02 FILLER                          PICTURE IS X(6).
003810     02 PBREFVERB                       PICTURE IS X(12).
003820     02 FILLER                          PICTURE IS X(5).
003830     02 PBDD                            PICTURE IS XXX.
003840     02 FILLER                          PICTURE IS X(6).
003850     02 PBCOMS                          PICTURE IS XXX.
003860     02 FILLER                          PICTURE IS X(6).
003870     02 PBWS                            PICTURE IS XXX.
003880     02 FILLER                          PICTURE IS X(6).
003890     02 PBCONS                          PICTURE IS XXX.
003900     02 FILLER                          PICTURE IS X(8).
003910     02 PBPD                            PICTURE IS XXX.
003920     02 FILLER                          PICTURE IS X(6).
003930     02 PBNOTE                          PICTURE IS X(12).
003940 01  PRINTCLEAR                  PICTURE X(132).
003950 WORKING-STORAGE SECTION.
003960 77  PAR-HOLD2 PICTURE X(32) VALUE SPACE.
003970 77  SW9  PICTURE 9   VALUE 0.
003980 77  HOLDID                             PICTURE IS X(12)
003990     VALUE IS SPACES.
004000 77  SAVESKADATANAME                    PICTURE IS X(32).
004010 77  PGNO                               PICTURE IS S999 VALUE IS
004020     ZEROS.
004030 77  LCON                               PICTURE IS 99.
004040 01  HDR1.
004050     02 H1CONTROL                       PICTURE IS X VALUE IS '1'.
004060     02 FILLER                          PICTURE IS X(3) VALUE IS
004070     SPACES.
004080     02 H1FILL1                         PICTURE IS X(10)
004090     VALUE IS '     PAGE '.
004100     02 PAGE-X                          PICTURE IS 999.
004110     02 PGNOS                           PICTURE IS X(27)
004120     VALUE IS '     VERSION 3.0 7-01-71   '.
004130     02 H1FILL2                         PICTURE X(25)
004140     VALUE IS '    PROGRAM ANALYZER FOR '.
004150     02 H1PROGRAMID                     PICTURE IS X(12).
004160     02 H1FILL3                         PICTURE IS X(30)
004170     VALUE IS '                GENERATED ON  '.
004180     02 H1DATE                          PICTURE IS X(9) VALUE IS
004190     SPACES.
004200     02 H1FILL4                         PICTURE IS X(12)
004210     VALUE IS SPACES.
004220     02 FILLER                          PICTURE IS X VALUE IS '*'.
004230 01  HDR1R REDEFINES HDR1               PICTURE IS X(133).
004240 01  HDR2.
004250     02 H2CONTROL                       PICTURE IS X VALUE IS
004260     SPACE.
004270     02 FILLER                          PICTURE IS X(3) VALUE IS
004280     SPACES.
004290     02 H2FILL1                         PICTURE IS X(40)
004300     VALUE IS '              DATA NAME                R'.
004310     02 H2FILL2                         PICTURE IS X(40)
004320     VALUE IS 'EFERENCE     REFERENCE      FILE    LINK'.
004330     02 H2FILL3                         PICTURE IS X(48)
004340     VALUE IS 'AGE  WORKING  REPORT    PROCEDURE      SPECIAL  '.
004350     02 FILLER                          PICTURE IS X VALUE IS
004360     SPACES.
004370 01  HDR2R REDEFINES HDR2               PICTURE IS X(133).
004380 01  HDR3.
004390     02 H3CONTROL                       PICTURE IS X VALUE IS
004400     SPACE.
004410     02 FILLER                          PICTURE IS X(3) VALUE IS
004420     SPACES.
004430     02 H3FILL1                         PICTURE IS X(40)
004440     VALUE IS SPACES.
004450     02 H3FILL2                         PICTURE IS X(40)
004460     VALUE IS '  LINE        OR LEVEL    SECTION   SECT'.
004470     02 H3FILL3                         PICTURE IS X(48)
004480     VALUE IS 'ION  STORAGE  SECTION   DIVISION         NOTE   '.
004490     02 FILLER                          PICTURE IS X VALUE IS
004500     SPACES.
004510 01  HDR3R REDEFINES HDR3               PICTURE IS X(133).
004520 PROCEDURE DIVISION.
004530 LPOC         SECTION.
004540 BEGINLASTPASS.
004550     OPEN     INPUT SUPPLEMENTAL-PART2-IN.
004560     OPEN     OUTPUT SORTEDSOURCE.
004570     OPEN     OUTPUT ANALYZER.
004580     OPEN     OUTPUT PARFILEOUT.
004590 DLP.
004600     READ     SUPPLEMENTAL-PART2-IN AT END
004610     DISPLAY ' FLOBOL02, NO SUPPLEMENTAL-PART2-IN RECORDS PRESENT,
004620-    ' EOJ ' GO TO WRAPUP.
004630      IF SKADATANAME EQUAL TO SPACE GO TO DLP.
004640 WRITEHDRSS.
004650     MOVE     000 TO PGNO.
004660 WRITEHDRS.
004670     MOVE     SKAPROGRAMID TO HOLDID H1PROGRAMID.
004680     ADD      001 TO PGNO
004690              MOVE PGNO TO PAGE-X.
004700     WRITE  PRINTHEADER   FROM HDR1 AFTER ADVANCING TOP-PAGE.
004710     WRITE PRINTHEADER FROM HDR2R AFTER ADVANCING 1 LINES.
004720     WRITE PRINTHEADER FROM HDR3R AFTER ADVANCING 1 LINES.
004730     MOVE     SPACES TO PRINTCLEAR.
004740     MOVE ' ' TO PBCONTROL.
004750     WRITE PRINTHEADER AFTER ADVANCING 1 LINES.
004760     MOVE     05 TO LCON.
004770 WROTEHDRS.
004780     GO       TO TESTAC.
004790 READSORTER.
004800     IF SITSWHAT EQUAL TO 'PARAGRAPH' OR 'SECTION  '
004810     MOVE SPARNAME TO PAR-HOLD2.
004820 RSORT-2.
004830     READ     SUPPLEMENTAL-PART2-IN AT END
004840              GO TO WRAPUP.
004850     IF SPARNAME EQUAL TO PAR-HOLD2 AND (SITSWHAT EQUAL TO
004860     'PARAGRAPH' OR 'SECTION  ')     AND SKEY EQUAL TO 'C'
004870     AND SPARNAME NOT EQUAL TO SPACE
004880     DISPLAY  'DUPLICATE PARAGRAPH NAME, FIRST USED ' SPARNAME
004890     GO TO RSORT-2.
004900     IF SW9 EQUAL TO 1 MOVE 0 TO SW9
004910     GO TO READSORTER.
004920 TESTAC.
004930     IF       SKEY IS EQUAL TO 'C'
004940              GO TO ISC.
004950      IF SKADATANAME EQUAL TO SPACE GO TO READSORTER.
004960 ISA.
004970     IF       SKAPROGRAMID IS NOT EQUAL TO HOLDID
004980              PERFORM WRITEHDRSS THRU WRITEHDRS.
004990     IF       LCON IS GREATER THAN 55
005000              PERFORM WRITEHDRS.
005010     IF       SKADATANAME IS NOT EQUAL TO SAVESKADATANAME
005020              MOVE SKADATANAME TO SAVESKADATANAME
005030              MOVE SPACES TO PRINTCLEAR
005040              MOVE ' ' TO PBCONTROL
005050     WRITE PRINTCLEAR AFTER ADVANCING 1 LINES
005060              ADD 01 TO LCON
005070              MOVE SPACES TO PRINTCLEAR
005080              MOVE SKADATANAME TO PBDATANAME
005090              GO TO CONNECTA.
005100     MOVE     SPACES TO PRINTCLEAR.
005110 CONNECTA.
005120     MOVE ' ' TO PBCONTROL.
005130     IF SKASUB IS EQUAL TO 'S' ADD 2 TO SKAREFNO
005140     MOVE 1 TO SW9.
005150     MOVE     SKAREFNO TO PBREFLINE.
005160     IF       SKAQ IS EQUAL TO 'QQ'
005170              MOVE 'IN' TO PBQUALIFY
005180              MOVE SINFO TO PBDATANAME
005190              MOVE '*QUALIFIED**' TO PBNOTE
005200              MOVE 'YES' TO PBPD
005210     MOVE SKAVERBREF TO PBREFVERB
005220              GO TO CONNECTB.
005230     MOVE     SKAVERBREF TO PBREFVERB.
005240     IF       PBREFVERB IS EQUAL TO ' PRGRAPH    '
005250              MOVE 'PARAGRAPH   ' TO PBREFVERB.
005260     IF       SKAWSORPD IS EQUAL TO 'A2'
005270              MOVE 'YES' TO PBDD
005280              GO TO CONNECTB.
005290     IF       SKAWSORPD IS EQUAL TO 'A3'
005300              MOVE 'YES' TO PBWS
005310              GO TO CONNECTB.
005320     IF       SKAWSORPD IS EQUAL TO 'A4'
005330              MOVE 'YES' TO PBCOMS
005340              GO TO CONNECTB.
005350     IF       SKAWSORPD IS EQUAL TO 'A5'
005360              MOVE 'YES' TO PBCONS
005370              GO TO CONNECTB.
005380     MOVE 'YES' TO PBPD.
005390 CONNECTB.
005400     EXAMINE PBDATANAME TALLYING ALL QUOTE REPLACING BY SPACE.
005410     IF TALLY IS GREATER THAN ZERO
005420     MOVE '*LITERAL' TO PBNOTE.
005430     IF SKASUB EQUAL TO 'U'
005440     MOVE '*NUM LIT' TO PBNOTE.
005450     IF SKASUB EQUAL TO 'T'
005460      MOVE '*NUMERIC SUB' TO PBNOTE.
005470     IF       SKASUB IS EQUAL TO 'S'
005480     MOVE '*SUBSCRIPTED' TO PBNOTE.
005490     IF SKASUB IS EQUAL TO 'Y'
005500     MOVE '*QUALIFIER**' TO PBNOTE.
005510     IF       SKASUB IS EQUAL TO 'Z'
005520              MOVE '*SECTION****' TO PBNOTE.
005530     IF SKASUB EQUAL TO 'W'
005540     MOVE '*SUBSCRIPT *' TO PBNOTE.
005550     WRITE PRINTBODY AFTER ADVANCING 1 LINES.
005560     ADD      01 TO LCON.
005570     GO       TO READSORTER.
005580 ISC.
005590     MOVE     SORTRECORD TO SORTEDRECORD.
005600     WRITE    SORTEDRECORD.
005610     IF       SITSWHAT IS EQUAL TO 'PARAGRAPH' OR
005620              SITSWHAT IS EQUAL TO 'SECTION  '
005630              MOVE SPROGNO TO PROGNOPAR
005640              MOVE SPARNAME TO PARNAMEPAR
005650     MOVE SHOLD1 TO LINEPAR2
005660              ADD 0001 TO LINEPAR
005670              MOVE 0001 TO STATEPAR
005680              MOVE SLOCATE TO LOCATIONPAR
005690              WRITE PARRECOUT.
005700     GO       TO READSORTER.
005710 WRAPUP.
005720     MOVE     SPACES TO PRINTCLEAR.
005730     MOVE ' ' TO PBCONTROL.
005740     MOVE 'END PRINTING' TO SKAPROGRAMID.
005750      WRITE PRINTBODY AFTER ADVANCING 1 LINES.
005760     CLOSE    SUPPLEMENTAL-PART2-IN.
005770     CLOSE    SORTEDSOURCE.
005780     CLOSE    PARFILEOUT.
005790     CLOSE    ANALYZER
005800              STOP RUN.
