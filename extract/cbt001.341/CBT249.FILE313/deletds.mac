*          DATA SET DELETDS    AT LEVEL 005 AS OF 08/13/76
DELETDS  CSECT
         ENTRY1 SAVEA
         SPACE 2
*
GETPARM  DS    0H
         L     R11,0(R1)
         LH    R10,0(R11)
         LA    R9,2(R11)
         STM   R9,R10,APARM
         LTR   R10,R10
         BNZ   GPARM100
*
         OPEN  (SYSIN,(INPUT))
*
         TM    SYSIN+48,X'10'
         BZ    EXIT
         MVI   $SYSIN,C'O'
         SPACE 2
*
GPARM100 OPEN  (SYSPRINT,(OUTPUT))
*
         TM    SYSPRINT+48,X'10'
         BNZ   GETDSN              BRANCH IF DD CARD SUPPLIED
         MVI   WTP$+1,X'F0'
*
**
GETDSN   DS    0H
*
         CLI   $SYSIN,C'O'
         BNE   GDSN008
         GET   SYSIN,CARD
         MVC   DELDSN(44),CARD
         B     FINDDS
*
GDSN008  LM    R2,R4,APARM
         LTR   R3,R3
         BNP   EXIT
         MVC   DELDSN(44),=CL44' '
*
GDSN010  CLI   0(R2),C','
         BE    GDSN020
         MVC   0(1,R4),0(R2)
         LA    R2,1(R2)
         LA    R4,1(R4)
         BCT   R3,GDSN010
*
GDSN020  LA    R2,1(R2)
         S     R3,=F'1'
         STM   R2,R3,APARM
         CLI   DELDSN,C' '
         BE    EXIT
         EJECT
*
**
FINDDS   DS    0H
         MVC   LINE1+13(44),DELDSN
         LOCATE CATLGPTR
*
         LTR   R15,R15
         BNZ   WTPNODS
*      FIND VOLUME INFO
         LH    R2,VOLLIST
         LA    R3,VOLLIST+2
         MVC   LINE1P(57),LINE1
FDS020   MVC   VOLUME+2(10),0(R3)  SETUP DEVTYPE & VOLUME FOR SCRATCH
         MVC   LINE1A+13(6),VOLUME+6
         SPACE 2
*
** SCRATCH DS FROM A VOLUME
SCRHDS   DS    0H
         SR    R0,R0
         SCRATCH DATASET
*                                    ** SETUP SCRATCH RETURN CODE
         CVD   R15,DWORD
         UNPK  LINE1A+20(2),DWORD+6(2)
         OI    LINE1A+21,X'F0'
*                                    ** SETUP SCRATCH REASON CODE
         LH    R6,VOLUME+12
         CVD   R6,DWORD
         UNPK  LINE1A+23(2),DWORD+6(2)
         OI    LINE1A+24,X'F0'
*
         MVC   LINE1P+57(44),LINE1A
*
         LA    R3,12(R3)
         BCT   R2,FDS030      BRANCH IF MORE VOLUMES SAME DS,
         B     UNCATDS        OTHERWIZE GO UNCATALOG THE DATASET.
*
FDS030   LA    R0,LINE1P
         BAL   R10,WTP
         MVC   LINE1P+1(56),LINE1P
         B     FDS020
         SPACE 2
*
**
UNCATDS  DS    0H
         CATALOG DELETE
*                          ** SETUP UNCAT RETURN CODE
         ST    R15,CATCODE
         OC    RCODE(4),CATCODE
         CVD   R15,DWORD
         UNPK  LINE1B+12(2),DWORD+6(2)
         OI    LINE1B+13,X'F0'
         MVC   LINE1P+83(18),LINE1B
*
** PRINT-DATASET DELETED AND UNCATALOGED.
WTPDELDS DS    0H
         LA    R0,LINE1P
         BAL   R10,WTP
         B     GETDSN
         SPACE 2
*
** PRINT-DATASET NOT FOUND IN CATALOG.
WTPNODS  DS    0H
         MVC   LINE2+13(44),DELDSN
         LA    R0,LINE2
         BAL   R10,WTP
         B     GETDSN
         SPACE 2
*
**
WTP      DS    0H
WTP$     NOP   WTPX
*
         PUT   SYSPRINT,(0)
*
WTPX     BR    R10
         EJECT
*
**
CLOSE    DS    0H
         CLOSE SYSIN
EXIT     DS    0H
         CLI   WTP$+1,X'00'
         BE    EXIT04
         CLOSE SYSPRINT
*
EXIT04   L     R15,RCODE
         LTR   R1,R15
         BNZ   EXIT10              BRANCH IF UNCAT PROBLEM
         EXITRC
*                               ** ABEND WITH CAT RETURN CODE
EXIT10   N     R1,=XL4'000000FF'
         O     R1,=XL4'40000000'
         SVC   13
         EJECT
*
*
SYSIN    DCB   DDNAME=SYSIN,DSORG=PS,MACRF=GM,RECFM=FB,LRECL=80,       X
               BFTEK=S,EODAD=CLOSE
*
*
*
SYSPRINT DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=PM,RECFM=FA,             X
               LRECL=101,BLKSIZE=101,BFTEK=S,EROPT=ACC
*
SAVEA    DC    18A(0)
         EJECT
*
**
DATASET  CAMLST SCRATCH,DELDSN,,VOLUME,,OVRD
VOLUME   DC    H'1',X'30C02008',C'VOLUME',H'0'
*
CATLGPTR CAMLST NAME,DELDSN,,VOLLIST
VOLLIST  DC    0D'0',265C' '
*
DELETE   CAMLST UCATDX,DELDSN
DELDSN   DC    CL44' '
*
APARM    DC    A(0)
JPARM    DC    F'0'
ADELDSN  DC    A(DELDSN)
*
*
         EJECT
LINE1    DC    CL57' *** DATASET'
LINE1A   DC    CL44'SCRATCH FROM XXXXXX 00-00'
LINE1B   DC    CL18', UNCATALOG 00'
LINE1P   DC    CL104' '
*
LINE2    DC    CL57' *** DATASET'
         DC    CL44' NOT FOUND IN CATALOG.'
DWORD    DC    D'0'
RCODE    DC    F'0'
CATCODE  DC    F'0'
$SYSIN   DC    F'0'
CARD     DC    CL80' '
*
         DC    0D'0'
         LTORG
*
         END
