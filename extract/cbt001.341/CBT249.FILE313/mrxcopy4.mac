*          DATA SET MRXCOPY4   AT LEVEL 001 AS OF 04/17/72
MRXCOPY4 CSECT
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         STM   14,12,12(13)
         BALR  12,0
         USING *,12
         USING IHADCB,9
         ST    13,SAVE+4
         LA    15,SAVE
         ST    15,8(13)
         LR    13,15
         EXTRACT TIOTADDR,FIELDS=(TIOT)
         L     R10,TIOTADDR
         MVC   JOBNAME,0(R10)
         MVC   STEPNAME,8(R10)
         MVC   WTOR1+16(8),JOBNAME
         MVC   WTOR1+25(8),STEPNAME
         MVC   WTOR1+34(7),=C'MRXCOPY'
         MVC   WTOR2+16(41),WTOR1+16
         OPEN  (SYSDCB,(OUTPUT))
         LA    9,SYSDCB
         TM    DCBOFLGS,X'10'
         BZ    ERRSYS
         LA    R3,0
         OPEN  (UT1DCB,(INPUT))
         LA    9,UT1DCB
         TM    DCBOFLGS,X'10'
         BZ    DDMIS1
         OPEN  (UT2DCB,(OUTPUT))
         LA    9,UT2DCB
         TM    DCBOFLGS,X'10'
         BZ    DDMIS2
         MVC   FILL2+3(8),=C'REPLY = '
RDSYSUT1 EQU   *
         GET   UT1DCB
         LA    R3,1(,R3)
         LR    2,1
WTSYSUT2 EQU   *
         PUT   UT2DCB,(2)
         B     RDSYSUT1
ENDUT1   EQU   *
         CLOSE (UT1DCB)
         CLOSE (UT2DCB)
         MVC   MSGTYP,EOFMSG
         MVC   FILL2+1(20),FILL2
         PUT   SYSDCB,TIMEOUT
         BAL   R4,PUTCOUNT
         CLOSE (SYSDCB)
ERROUT   EQU   *
         L     R15,RTRNCODE
         L     13,SAVE+4
         L     14,12(13)
         LM    0,12,20(13)
         BR    14
ERR1     EQU   *
         MVC   MSGTYP,IOERRUT1
         MVC   WTOR1+42(16),IOERRUT1
         MVI   ECBAD,X'00'
         CNOP  0,4
WTOR1    EQU   *
         WTOR  '                                 RETRY? Y OR N',       C
               REPLY,1,ECBAD
WAIT1    EQU   *
         WAIT  ECB=ECBAD
         MVC   FILL2+11(1),REPLY
         CLI   REPLY,C'Y'
         BE    REPLY1Y
         CLI   REPLY,C'N'
         BE    REPLY1N
         MVI   ECBAD,X'00'
         MVC   WTO1+8(25),WTOR1+8
         CNOP  0,4
WTO1     EQU   *
         WTO   '                          INVALID REPLY. REPLY Y OR N'
         B     WAIT1
REPLY1Y  EQU   *
         LA    R4,RDSYSUT1
         LA    R15,4
         ST    R15,RTRNCODE
         B     PUTCOUNT
REPLY1N  EQU   *
         LA    R4,ERROUT
         LA    R15,8
         ST    R15,RTRNCODE
         B     PUTCOUNT
ERR2     EQU   *
         MVC   MSGTYP,IOERRUT2
         MVC   WTOR1+42(16),IOERRUT2
         MVI   ECBAD,X'00'
         CNOP  0,4
WTOR2    EQU   *
         WTOR  '                                 RETRY? Y OR N',       C
               REPLY,1,ECBAD
WAIT2    EQU   *
         WAIT  ECB=ECBAD
         MVC   FILL2+11(1),REPLY
         CLI   REPLY,C'Y'
         BE    REPLY2Y
         CLI   REPLY,C'N'
         BE    REPLY2N
         MVI   ECBAD,X'00'
         MVC   WTO2+8(25),WTOR2+8
         CNOP  0,4
WTO2     EQU   *
         WTO   '                          INVALID REPLY. REPLY Y OR N'
         B     WAIT2
REPLY2Y  EQU   *
         LA    R4,WTSYSUT2
         LA    R15,4
         ST    R15,RTRNCODE
         B     PUTCOUNT
REPLY2N  EQU   *
         LA    R4,ERROUT
         LA    R15,8
         ST    R15,RTRNCODE
         B     PUTCOUNT
DDMIS1   EQU   *
         MVC   TIMEOUT(22),=C'SYSUT1 DD CARD MISSING'
         B     PUTERR
DDMIS2   EQU   *
         MVC   TIMEOUT(22),=C'SYSUT2 DD CARD MISSING'
         B     PUTERR
PUTERR   EQU   *
         PUT   SYSDCB,TIMEOUT
         LA    15,16
         ST    R15,RTRNCODE
         BAL   R4,PUTCOUNT
         B     ERROUT
ERRSYS   EQU   *
         LA    15,20
         ST    R15,RTRNCODE
         B     ERROUT
PUTCOUNT EQU   *
         CVD   R3,PCOUNT
         ED    DCOUNT,PCOUNT
         PUT   SYSDCB,COUNTMSG
         BR    R4
PCOUNT   DS    D
SAVE     DS    18F
ECBAD    DS    F
TIOTADDR DS    F
RTRNCODE DC    F'0'
JOBNAME  DS    CL8
STEPNAME DS    CL8
REPLY    DS    CL1
IOERRUT1 DC    CL19'I/O ERROR SYSUT1'
IOERRUT2 DC    CL19'I/O ERROR SYSUT2'
EOFMSG   DC    CL19'EOF SYSUT1.'
TIMEOUT  DC    CL133' '
COUNTMSG DS    0CL133
MSGTYP   DS    CL19
CON1     DC    CL25'NUMBER OF RECORDS READ ='
DCOUNT   DS    0CL20
FILL1    DC    C' '
EDIT1    DC    4X'2020206B'
EDIT2    DC    X'202021'
FILL2    DC    CL90' '
UT1DCB   DCB   DDNAME=SYSUT1,DSORG=PS,MACRF=(GL),EODAD=ENDUT1,         C
               SYNAD=ERR1,OPTCD=C
UT2DCB   DCB   DDNAME=SYSUT2,DSORG=PS,MACRF=(PM),SYNAD=ERR2
SYSDCB   DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=(PM),RECFM=F,LRECL=133,  C
               BLKSIZE=133,SYNAD=ERRSYS
         DCBD  DSORG=(QS)
         END   MRXCOPY4
