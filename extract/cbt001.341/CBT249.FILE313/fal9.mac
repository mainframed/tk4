FREEALL  TITLE 'FREEALL--- UNALLOCATES ALL DATASETS (WITH EXCEPTIONS)'
*
*   FREEALL WAS WRITTEN BY        BOB JUCH
*                                 POSTAL DATA CENTER
*                                 850 CHERRY AVE.
*                                 SAN BRUNO CA. 94097
*                                 415 876-9176
*
      EJECT
*)F FUNCTION:
*      THE FREEAX COMMAND FREES ALL ALLOCATED DATASETS (WITH OPTIONAL
*      EXCEPTIONS) AND OPTIONALLY LISTS THE FREED DD AND DATASET NAME.
*      SINCE ATTRIBUTE LISTS ARE SET UP AS DUMMY DD ENTRIES THEY ALSO
*      WILL BE FREED.
*
*      THE DDNAMES SYSIN, SYSPRINT, SYSPROC, SYSEDIT, SYSEDIT2,
*      AND ANY DDNAME STARTING WITH ARCH ARE NEVER FREED.
*
*)X SYNTAX:
*         FREEAX LIST/NOLIST EXCEPT(DDNAME1,DDNAME2...DDNAMEN)
*
*   DEFAULTS:  NOLIST NOEXCEPT
*
*)O OPERANDS:
*))LIST   SPECIFIES THAT FREED DATASET NAMES WILL BE LISTED
*))NOLIST FREED DATASET NAMES WILL NOT BE LISTED
*))EXCEPT DDNAMES LISTED WILL NOT BE FREED. NOTE THAT DDNAMES ARE
*         GENERIC I.E. IF A DDNAME STARTS WITH THE SPECIFIED DDNAME
*         IT WILL NOT BE FREED.
         EJECT
FREEAX   CSECT
         SAVE  (14,12),,*
         LR    10,15          LOAD BASE
         USING FREEAX,10
         ST    13,SAVEAREA+4  SAVE PTR TO CALLER'S SAVE AREA
         LR    3,13           LOAD ADDR OF CALLING PGM'S SAVE AREA
         LA    13,SAVEAREA    POINT TO NEW SAVE AREA
         ST    13,8(3)        PUT POINTER IN CALLER'S SAVE AREA
         LA    3,WORKPPL
         USING PPL,3
         LR    2,1            SAVE PARM POINTER
         USING CPPL,2
         L     1,CPPLUPT      SET UP FOR PARSE
         ST    1,PPLUPT
         L     1,CPPLECT
         ST    1,PPLECT
         L     1,CPPLCBUF
         ST    1,PPLCBUF
         CALLTSSR EP=IKJPARS,MF=(E,PPL)  CALL PARSE
         LTR   15,15          CHECK RETURN CODE
         BZ    PARSEOK
PARSERR  EQU   *
         ST    15,GFRCODE     PUT RETURN CODE IN PARM LIST
         LA    1,GFPARSE      GET CALLER ID NUMBER
         STH   1,GFCALLID     SAVE IN PARM LIST
         MVI   GFBITS,GFKEYN08  SAY THAT NOT IN KEY 0 OR 8
         ST    2,GFCPPLP      PUT CPPL POINTER IN PARM LIST
         LA    1,GFPRMPTR     POINT TO PARM LIST POINTER
         LINK  EP=IKJEFF19,MF=(E,(1))
         B     RETURN
         SPACE
PARSEOK  L     4,ANSWER       POINT TO PARM DESC LIST
         USING PDL,4
         LA    3,DFRETCOD     POINT TO SVC99 RC WORD
         ST    3,DFRCP        PUT IN PARM LIST
         LA    3,DFZEROES     POINT TO ZERO ADDRESS WORD
         ST    3,DFJEFF02     PUT IN PARM LIST
         LA    3,DFCALLID     POINT TO CALLER ID NUMBER
         ST    3,DFIDP        PUT IN PARM LIST
         ST    2,DFCPPLP      PUT CPPL ADDR IN PARM LIST
         LA    3,1            INIT COUNTER
TOP      LA    1,S99RB        POINT TO REQUEST BLOCK
         ST    1,S99RBPTR     SAVE IT
         MVI   S99RBPTR,X'80' SET LAST PARM BIT
         LA    1,S99RBPTR     POINT TO REQUEST BLOCK POINTER
         MVC   DDNAME(10),FILLDDN
         MVC   DSNAME(46),FILLDSN
SVC99IR  DYNALLOC             GO DO IT
         LTR   15,15          ERROR ?
         BNZ   SVC99ERR
         TM    ATTRIB,X'C0'   CONCAT OR IN USE ?
         BNZ   BUMPCNTR       FORGET IT
         CLC   DSNAME+2(8),=CL8'SYSCTLG.'  PRIVATE CATALOG ?
         BE    BUMPCNTR
         CLC   DDNAME+2(8),=CL3'SYSPRINT'  TEST FOR NEVER FREES
         BE    BUMPCNTR
         CLC   DDNAME+2(5),=CL5'SYSIN'
         BE    BUMPCNTR
         CLC   DDNAME+2(7),=CL7'SYSPROC'
         BE    BUMPCNTR
         CLC   DDNAME+2(7),=CL7'SYSEDIT'
         BE    BUMPCNTR
         CLC   DDNAME+2(3),=CL3'SPF'
         BE    BUMPCNTR
         CLC   DDNAME+2(4),=CL4'SORT'
         BE    BUMPCNTR
         CLC   DDNAME+2(3),=CL3'PAN'
         BE    BUMPCNTR
         CLC   DDNAME+2(3),=CL3'PDS'
         BE    BUMPCNTR
         CLC   DDNAME+2(3),=CL3'PWD'
         BE    BUMPCNTR
         CLC   DDNAME+2(4),=C'ARCH'   ARCH PREFIX TOO
         BE    BUMPCNTR
         CLI   EXCEPT+1,X'01'     WAS EXCEPT ENTERED ?
         BNE   MOVEDDN
         LA    5,NAMES        POINT TO PDE
TESTDDN  L     6,0(5)         GET DDNAME POINTER
         LH    7,4(5)         GET DDNAME LENGTH
         BCTR  7,0            SUBTRACT ONE
         EX    7,COMPDDN      COMPARE
         BE    BUMPCNTR       FORGET IT IF MATCH
         CLI   24(5),X'FF'    SEE IF VALID
         BE    MOVEDDN        END OF LIST
         L     5,24(5)        POINT TO NEXT PDE
         B     TESTDDN        LOOP BACK
COMPDDN  CLC   DDNAME+2(1),0(6)
MOVEDDN  MVC   FREDDN(10),DDNAME  MOVE IN DDNAME TO BE FREED
         LA    1,FRERB        POINT TO RB FOR FREE
         ST    1,S99RBPTR     PUT IN RB PTR
         MVI   S99RBPTR,X'80' SET LAST PARM BIT
         LA    1,S99RBPTR     POINT TO REQUEST BLOCK POINTER
SVC99FR  DYNALLOC             FREE IT
         LTR   15,15          ZERO RC ?
         BNZ   SVC99ERR
         LH    1,FREINFO      SECONDARY ERROR ?
         LTR   1,1
         BNZ   SVC99ERR
         CLI   LIST+1,X'01'   LIST NAMES ?
         BNE   TESTLAST       DO NEXT ONE
MOVES    MVC   OUTDDN,DDNAME+2   MOVE DDNAME TO OUTPUT LINE
         CLI   DSTYPE,X'00'   NORMAL DATASET ?
         BE    MOVEDSN
         CLI   DSTYPE,X'40'   ALLOC TO TERM ?
         BNE   *+16
         MVI   OUTDSN,C'*'
         LA    0,23           SET LINE LENGTH TO 23
         B     PUTLINE
         MVC   OUTDSN(8),=C'NULLFILE'
         LA    0,30           SET LINE LENGTH TO 30
         B     PUTLINE
MOVEDSN  MVC   OUTDSN,DSNAME+2   MOVE DSNAME
         LA    0,22           SET LINE LENGTH TO 22
         AH    0,DSNAME       ADD LENGTH OF DSNAME
PUTLINE  LA    1,OUTLINE
         TPUT  (1),(0),R
         B     TESTLAST
         SPACE
SVC99ERR EQU   *
         MVC   DFS99RBP,S99RBPTR  PUT RB PTR IN PARM LIST
         ST    15,DFRETCOD    PUT RETURN CODE IN PARM LIST
         LA    1,DFPARMS      POINT TO PARM LIST
         LINK  EP=IKJEFF18,MF=(E,(1))
         B     TESTLAST
         SPACE
BUMPCNTR LH    3,RELNO        ADD FOR REQUEST NUMBER
         LA    3,1(3)
         STH   3,RELNO        PUT IT IN TEXT UNIT
TESTLAST TM    LASTENT,X'80'  LAST ENTRY ?
         BZ    TOP            IF NOT, TRY NEXT
         SPACE
RETURN   EQU   *
         L     13,4(13)       LOAD ADDRESS OF PREVIOUS SAVE AREA
         L     14,12(13)      LOAD RETURN ADDRESS
         RETURN (14,12)       RELOAD REGISTERS AND RETURN
         EJECT
SAVEAREA DC     18F'0'
WORKPPL  DS     0F
WORKUPT  DC     F'0'
WORKECT  DC     F'0'
WORKECB  DC     A(ECB)
WORKPCL  DC     A(PCL)
WORKANS  DC     A(ANSWER)
WORKCBUF DC     F'0'
WORKUWA  DC     F'0'
ANSWER   DC     F'0'
ECB      DC     F'0'
         SPACE
FILLDDN  DC    H'8',CL8' '
FILLDSN  DC    H'44',CL44' '
         SPACE
         DS    0F
S99RBPTR DC    X'80',AL3(S99RB)
         SPACE
S99RB    DS    0F
S99RBLN  DC    X'14'
S99VERB  DC    X'07'
S99FLAG1 DC    H'0'
S99ERROR DC    X'0000'
S99INFO  DC    X'0000'
S99TXTPP DC    A(S99TUPL)
S99RSVD  DC    F'0'
S99FLAG2 DC    F'0'
         SPACE
S99TUPL  DS    0F
S99TUP04 DC    A(S99TUN04)
S99TUP05 DC    A(S99TUN05)
S99TUP12 DC    A(S99TUN12)
S99TUP13 DC    A(S99TUN13)
S99TUP14 DC    A(S99TUN14)
S99TUP15 DC    X'80',AL3(S99TUN15)
         SPACE
S99TUN04 DC    H'4',H'1'
DDNAME   DC    H'8',CL8' '
S99TUN05 DC    H'5',H'1'
DSNAME   DC    H'44',CL44' '
S99TUN12 DC    H'12',H'1',H'1'
ATTRIB   DC    B'0000'
S99TUN13 DC    H'13',H'1',H'1'
LASTENT  DC    X'00'
S99TUN14 DC    H'14',H'1',H'1'
DSTYPE   DC    X'00'
S99TUN15 DC    H'15',H'1',H'2'
RELNO    DC    H'1'
         SPACE
FRERB    DS    0F
FRERBLN  DC    X'14'
FREVERB  DC    X'02'
FREFLAG1 DC    H'0'
FREERROR DC    X'0000'
FREINFO  DC    X'0000'
FRETXTPP DC    A(FRETUPL)
FRERSVD  DC    F'0'
FREFLAG2 DC    F'0'
         SPACE
FRETUPL  DS    0F
FRETUP01 DC    A(FRETUN01)
FRETUP07 DC    X'80',AL3(FRETUN07)
         SPACE
FRETUN01 DC    H'1',H'1'
FREDDN   DC    H'8',CL8' '
FRETUN07 DC    H'7',H'0'
         SPACE
OUTLINE  DS    0CL66
         DC    C'FILE '
OUTDDN   DC    CL8' '
         DC    C' FREED'
         DC    C' - '
OUTDSN   DC    CL44' '
         EJECT
GFPRMPTR DC    A(GFPARMS)
         SPACE
         IKJEFFGF
         EJECT
DFCALLID DC    H'50'
DFRETCOD DC    F'0'
DFZEROES DC    F'0'
         SPACE
         IKJEFFDF DFDSEC2=YES
         SPACE
FREEAX   CSECT
         LTORG
         EJECT
PCL      IKJPARM  DSECT=PDL
LIST     IKJKEYWD DEFAULT='NOLIST'
         IKJNAME  'LIST'
         IKJNAME  'NOLIST'
EXCEPT   IKJKEYWD
         IKJNAME  'EXCEPT',SUBFLD=DDNS
DDNS     IKJSUBF
NAMES    IKJPOSIT DSNAME,DDNAM,LIST,PROMPT='DDNAME(S)',                *
               HELP=('DDNAME(S) NOT TO BE FREED')
         IKJENDP
         EJECT
         IKJCPPL
         IKJPPL
         EJECT
         CVT   DSECT=YES
         END   FREEAX
