WRITELOG TITLE 'WRITELOG COMMAND PROCESSOR'
         MACRO
         REGS
         LCLA  &R
.R       AIF   (&R EQ 16).END
R&R      EQU   &R
&R       SETA  &R+1
         AGO   .R
.END     MEND
 SPACE 4
  EJECT
WRITELOG CSECT
         REGS
         SAVE  (14,12)
         LR    R12,R15                    SET UP PROGRAM BASE
         USING WRITELOG,R12
*
BEGIN    LR    R11,R1        SAVE R1
         LA    R10,SAVEAREA              CHAIN
         ST    R13,SAVEAREA+4             SAVE
         ST    R10,8(R13)                    AREAS
         LR    R13,R10
         MVI   LINE,C' '      BLANK LINE
         MVC   LINE+1(79),LINE
         SR    R15,R15
         ST    R15,RC
*
         USING CPPL,R11
         LA    R10,PPLADDR         ADDRESSABILITY FOR PPL
         USING PPL,R10
         MVC   PPLUPT,CPPLUPT           UPT
         MVC   PPLECT,CPPLECT           ECT
         LA    R3,ECB
         ST    R3,PPLECB                ECB
         L     R3,PCLADDR
         ST    R3,PPLPCL                PCL
         LA    R3,PDLADDR               PDL ADDRESS
         ST    R3,PPLANS                ANSWER
         MVC   PPLCBUF,CPPLCBUF         CBUF
         LA    R1,PPLADDR
         CALLTSSR EP=IKJPARS,MF=(E,(R1)) GO PARSE
         LTR   R15,R15
         BNZ   PARSEERR
         L     R11,PDLADDR
         USING IKJPARMD,R11
*
         LA    R9,OUT
         USING IHADCB,R9
         OPEN  (OUT,(OUTPUT))   OPEN DSN
         TM    DCBOFLGS,X'10' TEST FOR ERROR
         BZ    OPENERR        JUMP ON ERROR
*
         L     R2,RECORD      ADDR OF RECORD
         LH    R3,RECORD+4   LENGTH OF RECORD
TEST0    LTR   R3,R3
         BZ    RETURN
TEST80   CH    R3,=H'81'
         BL    SETMOVE
         LA    R3,80
         LA    R4,ERRMG3
         LA    R5,L'ERRMG3
         TPUT  (R4),(R5)
SETMOVE  BCTR  R3,0          DECREMENT ONE FOR MOVE
         EX    R3,MOVEDATA
         PUT   OUT,LINE       MOVE QSTING TO LINE
         CLOSE (OUT)
*
RETURN   L     R13,4(R13)               PREVIOUS SAVE AREA
         LM    R14,R12,12(R13)
         L     R15,RC
         BR    R14
  EJECT
PARSEERR EQU   *              ERROR IN PARSE
         LA    R2,ERRMG1
         LA    R3,L'ERRMG1
         B     PUTERRM
*
OPENERR  EQU   *              ERROR ON OPEN OF DATASET
         LA    R2,ERRMG2
         LA    R3,L'ERRMG2
*
COVERR   EQU   *
         CVD   R15,DBLWRD     CONVERT ERROR CODE TO CHAR
         MVC   31(6,R2),DMASK
         ED    31(6,R2),DBLWRD+5
*
PUTERRM  TPUT  (R2),(R3)
RETURN0  LA    R15,8
         ST    R15,RC
         B     RETURN
*
MOVEDATA MVC   LINE(0),0(R2) MOVE RECORD TO LINE
ERRMG1   DC    CL40'PARSE ERROR'
ERRMG2   DC    CL40'OPEN ERROR RETURN CODE   ='
ERRMG3   DC    CL40'RECORD TRUNCATED TO 80 CHARACTERS'
  EJECT
SAVEAREA DS    18F
LINE     DS    CL80
RC       DS    1F
ECB      DS    F
PCLADDR  DC    A(DSPPCL)
PDLADDR  DS    F
PPLADDR  DS    7F
DBLWRD   DS    D
DMASK    DC    X'402020202020'
  EJECT
OUT      DCB   RECFM=FB,MACRF=(PM),DSORG=PS,LRECL=80,DDNAME=WRITELOG
DSPPCL   IKJPARM
RECORD   IKJPOSIT QSTRING,PROMPT='ENTER LOG RECORD'
         IKJENDP
         IKJCPPL
         IKJPPL
         CVT
         DCBD  DSORG=PS
         END   WRITELOG
