TABENTRY DSECT
TSID     DS    CL2       SECTION ID
TPID     DS    CL2       PROMPT ID
TRDISP   DS    CL1       DISPOSITION OF RECORD
TBRANCH  DS    CL4       NEXT PROMPT POINTER
TDEFAULT DS    CL1       DEFAULT INDICATOR, QUESTION INDICATOR
TOUTLOC  DS    CL2       OUTPUT FIELD LOCATION
TLENGTH  DS    CL2       OUTPUT FIELD LENGTH
TEXIT    DS    CL2       SPECIAL PROCESSING EXIT ID
TVALUE   DS    CL50      DEFAULT VALUE
TPROMPT  DS    CL60      PROMPT MESSAGE
TFILL    DS    CL4       FILLER
         TITLE 'INTERACTIVE DATA GENERATOR - VERSION 1.0'
*
*
* CONTROL CARD.  (FIRST CARD IN INPUT TABLE FILE)
*
* CARD COLUMN
* START  STOP
* ----- -----
*     1     1  A Y IN THE FIRST POSITION OF THIS CARD WILL CAUSE THE
*              PROGRAM TO TAKE DEFAULTS, WITH NO PROMPTING, FOR ALL
*              FIELDS WHICH HAVE DEFAULT DATA.
*              DO NOT USE LETTERS C,M,N,Q IN THIS FIELD.
*
*     2    80  BLANK OR COMMENTS.
*
*
***********************************************************************
*
*
* TABLE CARD 1
*
* CARD COLUMN  INPUT FLD  TABLE FLD  COMMENTS
* ----- -----  ---------  ---------  ----------------------------------
*     1     2  TC1SID     TSID       SECTION ID.
*     3     4  TC1PID     TPID       PROMPT ID.
*     5     5                        ALWAYS '1'.
*     6     6  TC1DISP    TRDISP     RECORD DISPOSITION AFTER PROCESS
*                                    ING OF TABLE ENTRY IS COMPLETE.
*                                    R = RETAIN FOR FUTHER PROCESSING.
*                                    O = OUTPUT THIS RECORD.
*     7    10  TC1BRNCH   TBRANCH    NEXT SECTION/PROMPT ID TO PROCESS
*                                    AFTER PROCESSING OF THIS ENTRY IS
*                                    COMPLETE.
*    11    11  TC1DFALT   TDEFAULT   TABLE ENTRY TYPE INDICATOR.
*                                    N = NO DEFAULT DATA PRESENT.
*                                    Y = DEFAULT DATA PRESENT.
*                                    M = MESSAGE ONLY.
*                                    Q = QUESTION.
*                                    C = USE DEFAULT DATA, NO PROMPT.
*    12    13  TC1LOC     TOUTLOC    BEGINING LOCATION OF FIELD IN THE
*                                    OUTPUT RECORD.
*    14    15  TC1LEN     TLENGTH    LENGTH OF FIELD IN THE OUTPUT
*                                    RECORD.
*    16    17  TC1EXIT    TEXIT      2 CHARACTER ID OF SPECIAL
*                                    PROCESSING EXIT TO CALL AFTER DATA
*                                    HAS BEEN MOVED TO OUTPUT RECORD.
*    18    67  TC1VALUE   TVALUE     DEFAULT DATA TO BE USED IF NEEDED.
*    68    80                        NOT USED.
*
***********************************************************************
         EJECT
*
*
* TABLE CARD 2
*
*
* CARD COLUMN  INPUT FLD  TABLE FLD  COMMENTS
* ----- -----  ---------  ---------  ----------------------------------
*     1     2  TC2SID     N/A        SECTION ID.  (SAME AS CARD 1)
*     3     4  TC2PID     N/A        PROMPT ID.  (SAME AS CARD 1)
*     5     5                        ALWAYS '2'.
*     6    60  TC2PROMT   TPROMPT    PROMPT MESSAGE.
*    61    80                        NOT USED.
         EJECT
IDG      CSECT
         SPACE 1
R0       EQU   0                   E
R1       EQU   1                   Q
R2       EQU   2                   U
R3       EQU   3                   A
R4       EQU   4                   T
R5       EQU   5                   E
R6       EQU   6
R7       EQU   7                   R
R8       EQU   8                   E
R9       EQU   9                   G
R10      EQU   10                  I
R11      EQU   11                  S
R12      EQU   12                  T
R13      EQU   13                  E
R14      EQU   14                  R
R15      EQU   15                  S
         SPACE 2
BEGIN    STM   14,12,12(13)        SAVE REGISTERS
         LR    R12,R15             SET UP R12 FOR BASE REGISTER
         USING BEGIN,12            SET R12 TO THE BASE
         ST    R13,SAVEAREA+4      SET UP THE SAVE AREA
         LR    R4,R13
         LA    R13,SAVEAREA        SET UP POINTER TO SAVE AREA
         ST    R13,8(R4)           SET UP HIGH POINTER
         SPACE 1
         B     START
         SPACE 1
SAVEAREA DC    18F'0'              SAVEAREA
         SPACE 2
START    EQU   *
         SPACE 1
         USING TABENTRY,R4
         EJECT
         OPEN  (TABLES,(INPUT))
         SPACE 1
         GET   TABLES,CONTROL
         SPACE 1
         LA    R4,TABLE            GET ADDRESS OF TABLE TO BUILD
         SR    R5,R5               SET UP COUNT REG
         SPACE 1
GETABLES GET   TABLES,TCARD1
         GET   TABLES,TCARD2
         SPACE 1
         MVC   TSID(2),TC1SID        SETUP SECTION ID
         MVC   TPID(2),TC1PID        SETUP PROMPT ID
         MVC   TRDISP(1),TC1RDISP    SETUP RECORD DISPOSITION
         MVC   TBRANCH(4),TC1BRNCH   SETUP GO TO NEXT PROMPT LOC
         MVC   TDEFAULT(1),TC1DFALT  SETUP UP DEFAULT SW INDICATOR
         MVC   TOUTLOC(2),TC1LOC     SETUP OUTPUT FIELD LOCATION
         MVC   TLENGTH(2),TC1LEN     SETUP OUTPUT FIELD LENGTH
         MVC   TEXIT(2),TC1EXIT      SETUP SPECIAL PROCESS EXIT ID
         MVC   TVALUE(50),TC1VALUE   SETUP DEFAULT VALUE
         MVC   TPROMPT(2),TSID       STORE SECTION WITH PROMPT
         MVC   TPROMPT+2(2),TPID     STORE PROMPT ID WITH PROMPT
         MVC   TPROMPT+4(1),SPACES   STORE A SPACE
         MVC   TPROMPT+5(55),TC2PROMT  SETUP THE PROMPT MESSAGE
         LA    R4,130(R4)            BUMP THE TABLE POINTRER
         LA    R5,1(R5)              BUMP THE ENTRY COUNT
         B     GETABLES              GO READ A NEW SET OF CARDS
         EJECT
OPENALL  STC   R5,TCOUNT             STORE TABLE ENTRY COUNT
         SPACE 2
         CLOSE (TABLES)
         SPACE 1
         OPEN  (KEYBOARD,(INPUT))
         SPACE 1
         OPEN  (SCREEN,(OUTPUT))
         SPACE 1
         OPEN  (DATAFILE,(OUTPUT))
         SPACE 1
         PUT   SCREEN,SPACES
         SPACE 1
         PUT   SCREEN,BEGINMSG
         SPACE 1
         PUT   SCREEN,SPACES
         SPACE 1
         LA    R4,TABLE
         EJECT
STARTLUP MVC   PROMPT(60),TPROMPT     SETUP THE PROMPT MESSAGE
         SPACE 1
         CLI   TDEFAULT,C'Q'          IS THIS A QUESTION?
         BE    QUESTION               YES IT IS, BRANCH
         SPACE 1
         CLI   TDEFAULT,C'M'          MESSAGE ONLY?
         BE    MESSAGE                YES IT IS, BRANCH
         SPACE 1
         CLI   TDEFAULT,C'C'          CANNED DATA, NO PROMPT?
         BE    DEFAULT                YES IT IS,BRANCH
         SPACE 1
         CLC   TDEFAULT(1),CDSW       USE DEFAULT, NO PROMPT
         BE    DEFAULT                YES IT IS, BRANCH
         SPACE 1
REPROMPT PUT   SCREEN,PROMPT
         MVC   REPLY(50),SPACES
         GET   KEYBOARD,REPLY
         EJECT
CHKSPACE CLC   REPLY(4),=CL4'$EOJ'    END OF JOB??
         BE    ENDOFJOB
         SPACE 1
         CLC   REPLY(50),SPACES
         BNE   RECEIVED               NO, BRANCH
         SPACE 1
         CLI   TDEFAULT,C'Y'          IS THERE A DEFAULT
         BE    DEFAULT                YES, BRANCH
         SPACE 1
         CLC   TOUTLOC(2),=CL2'00'    IS THERE AN OUTPUT FIELD TO BUILD
         BE    NEXTONE                NO, BRANCH
         SPACE 1
         B     REPROMPT               NULL REPLY OR BLANKS NOT VALID
         SPACE 1
MESSAGE  PUT   SCREEN,PROMPT
         B     NEXTONE
         SPACE 1
RECEIVED BAL   R11,MOVEDATA           GO STORE THE INPUT
         B     NEXTONE                PROCESS NEXT PROMPT
         SPACE 1
DEFAULT  MVC   REPLY(50),SPACES       CLEAR THE REPLY AREA
         MVC   REPLY(50),TVALUE       MOVE IN THE DEFAULT VALUE
         BAL   R11,MOVEDATA           GO STORE THE DEFAULT
         B     NEXTONE                PROCESS NEXT PROMPT
         SPACE 1
QUESTION PUT   SCREEN,PROMPT
         MVC   REPLY(50),SPACES
         GET   KEYBOARD,REPLY
         SPACE 1
         CLI   REPLY,C'Y'             YES ANSWER TO QUESTION?
         BE    YESREPLY
         CLI   REPLY,C'N'             NO  ANSWER TO QUESTION?
         BE    NOREPLY
         B     QUESTION               ACCEPT Y ON N ONLY
         SPACE 1
YESREPLY MVC   TBRANCH(4),TVALUE      FETCH YES BRANCH
         B     NEXTONE                PROCESS NEXT PROMPT
NOREPLY  MVC   TBRANCH(4),TVALUE+4    FETCH NO  BRANCH
         EJECT
NEXTONE  CLC   TEXIT(2),=CL2'  '      SPECIAL PROCESSING REQUIRED?
         BE    CHKDISP                NO, CHECK RECORD DISPOSITION
         BAL   R11,SPECIAL            GO DO SPECIAL PROCESSING
         SPACE 1
CHKDISP  CLI   TRDISP,C'R'            RETAIN THIS RECORD?
         BE    SEARCH                 YES, NOT DONE YET
         SPACE 1
         PUT   DATAFILE,RECORD
         MVC   RECORD(80),SPACES
         SPACE 1
SEARCH   MVC   KEY(4),TBRANCH         SAVE OFF THE BRANCH KEY
         CLC   KEY(3),=CL3'EOJ'       END OF JOB
         BE    ENDOFJOB
         LA    R4,TABLE               LOAD ADDRESS OF TABLE
         SR    R5,R5                  CLEAR R5
         IC    R5,TCOUNT              GET NUMBER OF ENTRIES IN TABLE
         SPACE 1
LOOP     CLC   0(4,R4),KEY            THIS THE NEW ENTRY TO USE?
         BE    STARTLUP               YES, GO PROCESS
         LA    R4,130(R4)             POINT TO NEXT ENTRY
         BCT   R5,LOOP                DECREMENT THE ENTRY COUNT
         SPACE 1
         ABEND 1000                   NO ENTRY IN TABLE
         EJECT
MOVEDATA PACK  DOUBLE(8),TOUTLOC(2)   PACK THE OUTPUT LOCATION
         CVB   R6,DOUBLE              CONVERT TH BINARY
         BCTR  R6,0                   SUBTRACT 1
         LA    R5,RECORD              GET THE ADDRESS OF THE RECORD
         AR    R5,R6                  POINT TO THE OUTPUT LOCATION
         SPACE 1
         PACK  DOUBLE(8),TLENGTH(2)   PACK THE OUTPUT LENGTH
         CVB   R6,DOUBLE              CONVERT TO BINARY
         BCTR  R6,0                   SUBTRACT 1
         STC   R6,MVCD+1              STORE LENGTH IN MVC INSTR
         SPACE 1
MVCD     MVC   0(0,R5),REPLY          MOVE DATA TO RECORD
         SPACE 2
         BR    R11                    RETURN TO CALLER
         SPACE 3
ERROR    CLI   TDEFAULT,C'Q'
         BE    QUESTION
         B     CHKSPACE
         SPACE 3
ENDOFJOB PUT   SCREEN,EOJMSG
         SPACE 1
         CLOSE (DATAFILE)
         SPACE 1
         SR    R15,R15
         L     R13,4(R13)
         RETURN (14,12),T,RC=(15)
         EJECT
SPECIAL  MVC   EXITNAME+6(2),TEXIT     SETUP EXIT NAME
         SPACE 2
         LOAD  EPLOC=EXITNAME
         SPACE 2
         LA    R1,RECORD
         LR    R15,R0
         SPACE 2
         CALL  (15)
         SPACE 2
         BR    R11
         EJECT
TABLES   DCB   DDNAME=TABLES,DSORG=PS,MACRF=GM,EODAD=OPENALL,          X
               RECFM=F,LRECL=80,BLKSIZE=80
         EJECT
KEYBOARD DCB   DDNAME=KEYBOARD,DSORG=PS,MACRF=GM,SYNAD=ERROR,          X
               RECFM=F,LRECL=80,BLKSIZE=80
         EJECT
SCREEN   DCB   DDNAME=SCREEN,DSORG=PS,MACRF=PM,                        X
               RECFM=F,LRECL=80,BLKSIZE=80
         EJECT
DATAFILE DCB   DDNAME=DATAFILE,DSORG=PS,MACRF=PM,                      X
               RECFM=F,LRECL=80,BLKSIZE=80
         EJECT
TCARD1   DS    0CL80
TC1SID   DS    CL2
TC1PID   DS    CL2
         DS    CL1
TC1RDISP DS    CL1
TC1BRNCH DS    CL4
TC1DFALT DS    CL1
TC1LOC   DS    CL2
TC1LEN   DS    CL2
TC1EXIT  DS    CL2
TC1VALUE DS    CL50
         DS    CL13
         SPACE 1
TCARD2   DS    0CL80
TC2SID   DS    CL2
TC2PID   DS    CL2
         DS    CL1
TC2PROMT DS    CL55
         DS    CL20
         SPACE 1
CONTROL  DS    0CL80
CDSW     DS    CL1
         DS    CL79
         SPACE 1
RECORD   DC    CL80' '
SPACES   DC    CL80' '
PROMPT   DC    CL80' '
REPLY    DC    CL80' '
         SPACE 1
BEGINMSG DC    CL80'            INTERACTIVE DATA GENERATOR - VER. 1.0'
EOJMSG   DC    CL80'EOJ -- IDG   VER 1.0'
         SPACE 1
DOUBLE   DS    D
EXITNAME DC    CL8'IDGEXT  '
TCOUNT   DS    CL1
KEY      DC    CL4' '
         SPACE 1
         LTORG
TABLE    DS    300CL130
         END
