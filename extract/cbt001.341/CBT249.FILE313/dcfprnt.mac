//$SYS7DCF JOB (8205,0802),'6 JACKSON G W',CLASS=Z,MSGCLASS=A,
//         NOTIFY=$SYS7
/*JOBPARM O=JACKSON,Q=F
//STEP01   EXEC PLIXCL,
//             PARM.PLI='MACRO,NAG,NGN,NOMAP,STMT,NSTG'
//PLI.SYSIN DD *
 /* PRINT DCF FORMATTED TEXT - TRANSLATE FBM TO FBA                  */
 DCFPRNT: PROC (IPARM) OPTIONS (MAIN) REORDER;

 /*                                                                  */
 /* PROGRAM:       DCFPRNT                                           */
 /*                                                                  */
 /* AUTHOR:        GREGG W. JACKSON                                  */
 /*                                                                  */
 /* DATE:          05 JANUARY 1981                                   */
 /*                                                                  */
 /* INSTALLATION:  MEMOREX CORPORATION                               */
 /*                SAN TOMAS AT CENTRAL EXPRESSWAY                   */
 /*                SANTA CLARA, CALIFORNIA 95052                     */
 /*                                                                  */
 /*  ABSTRACT:     TRANSLATE MACHINE TO ASCII CARRIAGE CONTROL       */
 /*                CHARACTERS IN TEXT SO THAT THE HONEYWELL PPS1     */
 /*                CAN PRINT THE DOCUMENT CORRECTLY.                 */
 /*                PARAMETER CONTROLS SUPPRESSION OF OVERPRINTED     */
 /*                LINES (PARM='/NOV') OR PRINTING (PARM='/OV').     */
 /*                DEFAULT IS TO SUPPRESS.                           */
 /*                                                                  */
 %PAGE;
 DCL INFILE        FILE RECORD INPUT;
 DCL OUTFILE       FILE RECORD OUTPUT;
 DCL SYSPRINT      FILE STREAM OUTPUT PRINT;

 DCL IPARM         CHAR (100) VAR;

 DCL SUBJECT       CHAR (255) VAR INIT ((255)' ');
 DCL OBJECT        CHAR (255) VAR INIT ((255)' ');

 DCL MACHCTL       CHAR (32) INIT
         ('°ijr~zœ¹AIJRá¸¯lòt¨ž³C“L–T');
 DCL ASACTL        CHAR (32) INIT
         ('1 0-123456789ABC 0-123456789ABC+');
 DCL OVERPRINT_CHAR     CHAR (1) INIT ('+');
 DCL NO_OVERPRINT       BIT (1) INIT ('1'B);
 DCL OVERPRINT_SW       BIT (1) INIT ('0'B);

 DCL INDEX         BUILTIN;
 DCL SUBSTR        BUILTIN;
 DCL TRANSLATE     BUILTIN;

 ON ENDFILE (INFILE) GOTO DONE;

 %PAGE;
 /* DECODE PARAMETER                                                 */

    IF INDEX(IPARM,'NOV') = 1
    THEN NO_OVERPRINT = '1'B;
    ELSE IF INDEX(IPARM,'OV') = 1
         THEN NO_OVERPRINT = '0'B;
         ELSE NO_OVERPRINT = '1'B;

    IF NO_OVERPRINT
    THEN PUT SKIP LIST('DCFPRNT - OVERPRINTING WILL BE SUPPRESSED.');
    ELSE PUT SKIP
              LIST('DCFPRNT - OVERPRINTING WILL NOT BE SUPPRESSED.');

    OPEN FILE (INFILE),
         FILE (OUTFILE);

 DOIT:;

    READ FILE (INFILE) INTO (SUBJECT);
    OBJECT = SUBJECT;
    SUBSTR(OBJECT,1,1) =
      TRANSLATE (SUBSTR(SUBJECT,1,1),ASACTL,MACHCTL);

    IF NO_OVERPRINT = '1'B
                                       /* DO NOT OVERPRINT -         */
                                       /*   PRINT THE FIRST LINE AND */
                                       /*   THROW OUT THE REST.      */
    THEN IF SUBSTR(OBJECT,1,1) = OVERPRINT_CHAR
                                       /* THIS IS AN OVERPRINT LINE. */
         THEN IF OVERPRINT_SW = '0'B
                                       /* IT'S THE FIRST OF A GROUP  */
                                       /*   TO BE OVERPRINTED, SO    */
                                       /*   REMOVE THE OVERPRINT     */
                                       /*   CONTROL, PRINT IT, AND   */
                                       /*   SET A SWITCH.            */
              THEN DO;
                      SUBSTR(OBJECT,1,1) = ' ';
                      OVERPRINT_SW = '1'B;
                   END;
                                       /* IT'S NOT THE FIRST, DROP   */
                                       /*   THE LINE.                */
              ELSE GOTO DOIT;
                                       /* IT'S NOT AN OVERPRINT LINE.*/
         ELSE IF OVERPRINT_SW = '1'B
              THEN DO;
                                       /* IF THE LINES BEFORE WERE   */
                                       /*   OVERPRINT LINES, SET THE */
                                       /*   SW OFF AND DROP THE LINE.*/
                      OVERPRINT_SW = '0'B;
                      GOTO DOIT;
                   END;
              ELSE;
    ELSE;

    WRITE FILE (OUTFILE) FROM (OBJECT);
    GOTO DOIT;

 DONE:;

 END; /* DCFPRNT */
/*
//LKED.SYSLMOD DD DSN=$SYS7.WORK.LOAD,DISP=SHR
//LKED.SYSIN DD *
  NAME DCFPRNT(R)
/*
//DCFPRNT  PROC P='NOV',L=,M=
//DCFPRNT  EXEC PGM=DCFPRNT,PARM='/&P'
//STEPLIB  DD  DSN=$SYS7.WORK.LOAD,DISP=SHR
//SYSPRINT DD  SYSOUT=*
//INFILE   DD  DSN=&L(&M),DISP=SHR
//OUTFILE  DD  SYSOUT=*,DCB=(RECFM=VBA,LRECL=255,BLKSIZE=3156)
//         PEND
//LAST01   EXEC DCFPRNT,L='MRX.DOCLIB.LIST',M='DCFINTRM'
//
