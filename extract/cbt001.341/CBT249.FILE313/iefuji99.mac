IEFUJI   CSECT
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
* FUNCTION:                                                           *
*        THIS MODULE PERFORMS THE FOLLOWING:                          *
*         1. OBTAINS 36 BYTES FOR MEG ACCOUNTING DATA.  THE ADRS IS   *
*         PLACED IN THE COMMON EXIT USER DATA FIELD OF THE SMF COMMON *
*         EXIT TABLE.                                                 *
* ENTRY POINT:                                                        *
*         IEFUJI                                                      *
* INPUT:                                                              *
*        REG 1 POINTS TO A LIST OF 4 ADDRESSES;                       *
*         1. SMF COMMON EXIT TABEL                                    *
*         2. PROGRAMMERS NAME                                         *
*         3. JOB PRIORITY                                             *
*         4. JOB ACCOUNTING FIELDS                                    *
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
         REGS
         EJECT
         STM   14,12,12(13)  SAVE REGS
         BALR  R11,0
         USING *,R11
         L     R2,0(,R1)     GET SMF COMMON EXIT TABLE
         USING SMFCOMON,R2
         SPACE 2
*
** TEST IF JOB IS TO BE FAILED BECAUSE OF STANDARDS VIOLATION BACK IN
*  IEFUJV (INTERPRETER), IF SO, PUT MSG TO OPERATOR & PROGRAMMER AND
*  FAIL THE JOB.
*
WTOPARM  EQU   X'3C'                                                JMS
WLEN     EQU   X'B4'                                                JMS
CLASS    EQU   X'1C'
*
         SR    R15,R15
         ST    R15,16(R13)
         TM    SMFACDAT,X'FF'
         BO    RETURN         BRANCH IF SYSTEMS TASK.
         BM    DFH010         BRANCH IF JOB MEETS STANDARDS.
*      PUT MSG TO OPERATOR & PROGRAMMER.
         L     R7,SMFUDATA
         LA    R1,WTOPARM(R7)
         SVC   35
*
         MVC   SMFACDAT(4),CLASS(R7)    SETUP JOB CLASS & HASP NO.
         LR    R1,R7
         FREEMAIN R,A=(1),LV=WLEN,SP=255
         LA    R15,4
         ST    R15,16(R13)
         EJECT
DFH010   GETMAIN R,LV=LEN,SP=255
         LA    R1,0(,R1)     CLEAR HI BYTE
         USING ACCTDATA,R1
         XC    COREMIN(DATALEN-3),COREMIN
         MVC   JCLASS(4),SMFACDAT  STORE JOB  CLASS & HASP #
         ST    R1,SMFACDAT   SAVE ADRS
         MVC   ACCTNUMB(8),SMFUSER GET ACCT NOS
RETURN   DS    0H
         LM    R14,R12,12(R13) RESTORE REGS
         BR    R14
         LTORG
*
*     DSECTS FOR DATA RECORDS
*
*
*     SMF COMMON DATA EXIT
*
         COMMON
*
*     MEG ACCOUNT DATA
*
         ACCOUNT
         END
