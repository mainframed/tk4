         TITLE 'IKJEFLD - TSO LOGON EXIT'
         SPACE 3
***********************************************************************
*                                                                  *
*                                                                  *
*  IKJEFLD IS AN EXIT INVOKED BY THE LOGON MONITOR AFTER IT HAS    *
*  RECOGNIZED A LOGON COMMAND, BUT BEFORE IT SCANS THE LOGON       *
*  COMMAND BUFFER. THE EXIT, BY SETTING CERTAIN FLAGS, CAN INDICATE*
*  THAT IT WISHES TO SUPPLY CERTAIN CONTOL BLOCKS, SUCH AS THE     *
*  PSCB, UPT, LOGON JCL ETC AND OTHER OPTIONS. IT CAN ALSO SET A   *
*  FLAG TO INDICATE THAT A LOGON SHOULD BE DISCONNECTED.  THIS     *
*  FEATURE IS USED IN CONNECTION WITH TSHANGUP. IKJEFLD LOOKS      *
*  THROUGH THE ENQ QCBS FOR A MAJOR NAME OF BOCTSTOP. IF IT DOES   *
*  NOT FIND A MAJOR NAME IT ALLOWS THE LOGON TO CONTINUE. IF IT    *
*  LOCATES THE MAJOR NAME, IT TAKES THE FIRST RNAME AND TPUTS      *
*  THIS TO THE USER, THEN TELLS LOGON MONITOR TO DISCONNECT        *
*  THE USER. IKJEFLD IS AN INDEPENDENT LOAD MODULE IN SYS1.LINKLIB *
*  ATTRIBUTES.  READ-ONLY REENTRANT AND REUSABLE.                  *
*                                                                  *
*  IKJEFLD DOES ALL ITS WORK IN REGISTERS. THE ONLY SVCS ISSUED    *
*  ARE SPIE, TO TRAP 0C4 ABENDS IN CASE OF ERRORS WHEN LOOKING     *
*  THROUGH THE ENQ CONTROL BLOCKS, AND TPUT, TO ISSUE A MESSAGE TO *
*  THE TERMINAL.                                                   *
*                                                                  *
***********************************************************************
         EJECT
*
***  ABSOLUTE REGISTER EQUATES
*
R0       EQU   0
R1       EQU   1
R2       EQU   2             SAVES PARAMETER ADDRESS
R3       EQU   3             USED TO POINT TO STRING DOPE VECTOR
R4       EQU   4              HOLDS CVT ADDRESS
R5       EQU   5              BASE FOR MAJOR QCB
R6       EQU   6              BASE FOR MINOR QCB
R8       EQU   8              SPIE EXIT MAXIMUM ALLOWABLE RETRIES
R9       EQU   9              SPIE EXIT RETRY COUNTER
R10      EQU   10             HOLDS ADDRESS OF PREVIOUS SPIE ENVIR
R14      EQU   14
R12      EQU   12            BASE REGISTER
R15      EQU   15
         SPACE 3
IKJEFLD  CSECT
         SAVE  (14,12),,*     SAVE REGISTERS AND PROVIDE ID
         LR    R12,R15        LOAD BASE REGISTER
         USING IKJEFLD,R12    ESATBLISH ADDRESSABILTY
         LR    R2,R1          SAVE PARAMETER REGISTER
         LA    R8,4           SET R8 TO ALLOW 4 SPIE RETRIES
         SLR   R9,R9          ZERO SPIE RETRY COUNTER
*
**     A SPIE EXIT IS SET UP TO HANDLE 0C4 INTERRUPTS.
***    IKJEFLD CHAINS THROUGH ENQ CONTROL BLOCKS WHICH MIGHT
***    BE ALTERED WHILE IKJEFLD LOOKS AT THEM. LOOKING AT AN INVALID
**     QCB CAN CAUSE AN OC4, SO UP TO 4 RETRIES ARE ALLOWED
*
         SPIE  FIXUP,(4)      SETUP EXIT FOR 0C4 INTERRUPTS
         LR    R10,R1         SAVE PREVIOUS SPIE ENVIRONMENT
START    L     R4,16          LOAD CVT ADDRESS
         USING CVTMAP,R4      SETUP ADDR FOR CVT DSECT
         L     R5,CVTLQCB     LOAD ADDRESS OF FIRST MAJOR
         B     TESTMAJ        JUMP TO TEST THIS MAJOR
         USING MAJ,R5         SETUP ADDRESSABILTY FOR QCB DSECT
PREVMAJ  L     R5,MAJPMAJ     LOAD ADDRESS OF PREVIOUS MAJOR
         LTR   R5,R5          IS THIS LAST MAJOR?
         BZ    CONTINUE       IF SO, SKIP TO END
TESTMAJ  CLC   MAJNAME,QNAME  IS THIS REQUIRED NAME?
         BNE   PREVMAJ        IF NOT, GO GET PREVIOUS MAJOR
         L     R6,MAJFMIN     OTHERWISE LOAD ADDRESS OF FIRST MINOR
         USING MIN,R6         SETUP ADDRESSABILTY FOR QCB DSECT
         SLR   R0,R0          ZERO REG 0
         IC    R0,MINNAMEL    INSERT MINOR LENGTH
         LA    R1,MINNAME     LOAD ADDRESS OF MINOR NAME
         TPUT  (1),(0)        ISSUE TPUT TO DISPLAY MESSAGE
         L     R3,4(R2)       LOAD ADDRESS OF INPUT BUFFER DOPE VECTOR
         L     R3,0(R3)       LOAD ADDRESS OF INPUT BUFFER
         CLC   0(10,R3),=C'LOGON $SYS'  IS USER VALID TO LOG ON NOW?
         BE    OK             YES, GO OK
         CLC   0(10,R3),=C'LOGON $OPS'  IS USER VALID TO LOG ON NOW?
         BNE   DISCON         IF NOT, GO DISCONNECT
OK       TPUT  CNSMSG,CNSMSGL
         B     CONTINUE
DISCON   EQU   *
         L     R3,0(R2)       R3 -> STRING DOPE VECTOR FOR
*                                  CONTROL SWITCHES
         L     R3,0(R3)       R3 -> CONTROL SWITCHES
         OI    0(R3),X'10'    SET DISCONNECT SWITCH
         STIMER WAIT,BINTVL=SECONDS  LET THE VTAM TURKEYS SEE
CONTINUE SPIE  MF=(E,(10))    RESTORE PREVIOUS SPIE ENV
         RETURN (14,12),RC=0  RETURN TO LOGON MONITOR
         SPACE 3
SECONDS  DC    F'500'
QNAME    DC    C'BOCTSTOP'   QNAME FOR ENQ
*
** THIS IS THE SPIE FIXUP ROUTINE
*
FIXUP    LA    R9,1(R9)       INCREMENT RETRY COUNTER
         CLR   R9,R8          MORE THAN 4 RETRIES?
         BH    NORETRY        IF SO, DON'T RETRY
         LA    R6,START       LOAD ADDRESS OF RETRY ADDRESS
         ST    R6,8(R1)       STORE IN RESTART PS
         BR    14             RETURN TO OPERATING SYSTEM
NORETRY  LA    R6,CONTINUE    LOAD ADDRESS OF CLEANUP
         ST    R6,8(R1)       STORE IN RESTART PSW
         BR    R14            RETURN TO OPERATING SYSTEM
         EJECT
CNSMSG   DC    C'BUT FOR YOU.. ANYTHING'
CNSMSGL  EQU   *-CNSMSG
         LTORG
*
***
*****      DSECTS FOR CVT AND QUEUE CONTROL BLOCKS
***
*
CVTMAP   DSECT
         ORG   *+644
CVTLQCB  DS    F
         SPACE 3
         IHAQCB
         END
