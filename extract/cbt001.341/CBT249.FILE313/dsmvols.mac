* SYSTEM NAME:        DSM - DASD SPACE MANAGEMENT SYSTEM
*
* PROGRAM NAME:       DSMSVOL
*
* PROGRAM TITLE:      DSM DISK VOLUME CONTENT REPORTS
*
* AUTHOR:             G. W. JACKSON - MEMOREX CORPORATION
*
* ABSTRACT:           THIS PROGRAM REPORTS THE CONTENTS OF ONE OR MORE
*                     DISK PACKS.  THE FIRST REPORT IS AN ALPHABETICAL
*                     LISTING OF DATASETS FOR EACH PACK, THE SECOND IS
*                     A GRAPH SHOWING SPACE ALLOCATED AND USED ON EACH
*                     PACK REPORTED.
*
* METHOD:             THE INPUT TO THIS PROGRAM IS THE OUTPUT OF THE
*                     C. S. G. ASM2 SYSTEM $MONVOL PROGRAM, SORTED BY
*                     VOLSER AND DATASET NAME.  THE FIRST REPORT SIMPLY
*                     CONSISTS OF THESE LINES AND HEADINGS, WITH A
*                     SUMMARY OF TRACKS ALOOCATED AND USED AND
*                     PERCENTAGE OF USAGE.  THE SECOND REPORT PRESENTS
*                     THE SUMMARY DATA IN HISTOGRAM FORMAT.
* PARMS:              NONE
*
* JCL REQUIRED:       //STEP  EXEC  PGM=DSMVOLS
*                     //SYSPRINT DD  SYSOUT=A
*                     //DSMMONV  DD  << SORTED OUTPUT OF $MONVOLS >>
*                     //DSMPACK  DD  << DSM PACK LIST >>
*                     //DSMOUT1  DD  SYSOUT=A
*                     //DSMOUT2  DD  SYSOUT=A
*
* HISTORY:   7 JUL 78 - FIRST VERSION.
*
*           25 SEP 78 - MODIFICATIONS: CHANGED TO BYPASS COMMENTS IN
*                       PACK FILE, THESE COMMENTS ADDED.
*
DSMVOLS  START 0
***********************************************************************
*                                                                     *
*                  REGISTER ASSIGNMENT AND USAGE                      *
*                                                                     *
***********************************************************************
R0       EQU   0                       O/S.
R1       EQU   1                       O/S.
R2       EQU   2                       (NOT USED).
R3       EQU   3                       (NOT USED).
R4       EQU   4                       (NOT USED).
R5       EQU   5                       (NOT USED).
R6       EQU   6                       (NOT USED).
R7       EQU   7                       (NOT USED).
R8       EQU   8                       (NOT USED).
R9       EQU   9                       (NOT USED).
R10      EQU   10                      (NOT USED).
R11      EQU   11                      RETURN FROM SUBROUTINE.
R12      EQU   12                      BASE REGISTER.
R13      EQU   13                      O/S.
R14      EQU   14                      O/S.
R15      EQU   15                      O/S.
***********************************************************************
*                                                                     *
*                  INITIALIZATION                                     *
*                                                                     *
***********************************************************************
         PRINT NOGEN
BEGIN    SAVE  (14,12)
         BALR  R12,0
         USING *,R12
         ST    R13,SAVE+4
         LA    R13,SAVE
         OPEN  (MONVFILE,INPUT,PACKFILE,INPUT,REP1FILE,OUTPUT,         C
               REP2FILE,OUTPUT)
         DAY   DATE8=TODAY
         MVC   TITLE1A+12(8),TODAY
         MVC   TITLE2A+12(8),TODAY
***********************************************************************
*                                                                     *
*                  MAIN FLOW                                          *
*                                                                     *
***********************************************************************
READMONV GET   MONVFILE,MONVREC        READ INPUT FILE.
         CLC   SAVEVOL,=C'*FIRST'      IS THIS THE FIRST TIME?
         BNE   TESTVOL                 NO - CHECK SAVE VS. NEW VOL.
         MVC   SAVEVOL,MONVVOL         YES - SET SAVE TO NEW VOLUME.
         B     ADDALLC                 BYPASS VOL CHECK.
TESTVOL  CLC   SAVEVOL,MONVVOL
         BE    ADDALLC                 NO
         BAL   R10,VOLCHNG             YES - HANDLE VOLUME CHANGE.
ADDALLC  LA    R3,MONVALLC             PULL TRKS ALLC FROM RECORD.
         CONVB (R4),(R3),L=4
         LTR   R15,R15
         BNZ   ADDUSED
         L     R5,TRKSALLC
         AR    R5,R4
         ST    R5,TRKSALLC
ADDUSED  LA    R3,MONVUSED             PULL TRKS USED FROM RECORD.
         CONVB (R4),(R3),L=4
         LTR   R15,R15
         BNZ   ADDCOUNT
         L     R5,TRKSUSED
         AR    R5,R4
         ST    R5,TRKSUSED
ADDCOUNT L     R5,DSCOUNT
         LA    R5,1(R5)
         ST    R5,DSCOUNT
         MVC   WORKLINE,MONVREC
         BAL   R10,PRINTDET            PRINT DETAIL LINE.
         B     READMONV
***********************************************************************
*                                                                     *
*                  VOLUME CHANGE                                      *
*                                                                     *
***********************************************************************
VOLCHNG  STM   R2,R10,SUBSAVE1         SAVE REGISTERS.
         MVI   WORKLINE,C'0'           SKIP LINE.
         MVI   WORKLINE+1,C' '         SET LINE TO BLANKS.
         MVC   WORKLINE+2(131),WORKLINE+1
         MVC   WORKLINE+4(6),SAVEVOL   MOVE VOLUME NAME TO LINE.
         CONVD WORKLINE+115,TRKSALLC,L=5
         CONVD WORKLINE+121,TRKSUSED,L=5
         PUT   REP1FILE,WORKLINE       PRINT TOTALS LINE.
         MVC   LINECTR1,=H'100'        FORCE NEW PAGE.
READPACK GET   PACKFILE,PACKREC        READ PACK FILE.
         CLI   PACKREC+0,C'*'          IS THIS A COMMENT RECORD?
         BE    READPACK                YES - READ ANOTHER
         CLC   PACKNAME,SAVEVOL        DOES RECORD MATCH VOLUME?
         BL    READPACK                LOWER - READ AGAIN.
         BH    DDTRACKS                HIGHER - APPLY DEFAULT.
LOADTRKS LA    R5,PACKTRKS
         CONVB (R6),(R5),L=6
         LTR   R15,R15                 CONVERSION SUCCESSFUL?
         BZ    STORETOT                YES - STORE TOTAL TO W/A.
DDTRACKS L     R6,=F'15351'            ASSUME DOUBLE DENSITY.
STORETOT ST    R6,TOTTRKS
         BAL   R10,GRAPHRTN            PRODUCE GRAPH OF DATA.
         MVC   SAVEVOL,MONVVOL         SAVE NEW VOLUME NAME.
         L     R4,TOTTRKS              ADD TO TOTALS.
         L     R5,GRANDTOT
         AR    R5,R4                   ADD TRKS THIS PACK TO TOTAL.
         ST    R5,GRANDTOT
         XC    TOTTRKS,TOTTRKS         ZERO TOTAL TRACKS.
         L     R4,TRKSALLC
         L     R5,TOTALLC
         AR    R5,R4                   ADD TRKS ALLC TO TOTAL ALLC.
         ST    R5,TOTALLC
         XC    TRKSALLC,TRKSALLC       ZERO TRACKS ALLOCATED.
         L     R4,TRKSUSED
         L     R5,TOTUSED
         AR    R5,R4                   ADD TRKS USED TO TOTAL USED.
         ST    R5,TOTUSED
         XC    TRKSUSED,TRKSUSED       ZERO TRACKS USED.
         L     R4,DSCOUNT
         L     R5,TOTDSCNT
         AR    R5,R4                   ADD DSCOUNT TO TOTAL COUNT.
         ST    R5,TOTDSCNT
         XC    DSCOUNT,DSCOUNT         ZERO TRACKS CNT.
ENDVOLCH LM    R2,R10,SUBSAVE1         RESTORE REGISTERS.
         BR    R10                     RETURN.
***********************************************************************
*                                                                     *
*                  GRAPH ROUTINE                                      *
*                                                                     *
***********************************************************************
GRAPHRTN STM   R2,R10,SUBSAVE2         SAVE REGISTERS.
         L     R5,TRKSALLC
         SR    R4,R4
         MH    R5,=H'100'              ADJUST FOR PERCENT CALULATION.
         D     R4,TOTTRKS              CALCULATE PERCENT ALLOCATED.
         ST    R5,PCTALLC
         L     R5,TRKSUSED
         SR    R4,R4
         MH    R5,=H'100'              ADJUST FOR PERCENT CALCULATION.
         D     R4,TOTTRKS              CALCULATE PERCENT ALLOCATED.
         ST    R5,PCTUSED
         MVC   WORKLINE,SKELETON
         MVC   WORKLINE+1(6),SAVEVOL
         CONVD WORKLINE+9,TRKSALLC,L=6
         CONVD WORKLINE+16,TRKSUSED,L=6
         CONVD WORKLINE+23,DSCOUNT,L=6
         LA    R9,WORKLINE+32          POINT TO START OF GRAPH.
         L     R8,PCTUSED              SET R8 TO PCT USED.
         LTR   R8,R8
         BZ    SETLOOP2                BYPASS IF ZERO.
LOOP1    LA    R9,1(R9)                POINT R9 TO NEXT POS IN GRAPH.
         MVI   0(R9),C'*'              MOVE '*' TO GRAPH.
         BCT   R8,LOOP1                BR ON COUNT TO TOP OF LOOP.
SETLOOP2 L     R8,PCTALLC              SET R8 TO PCT ALLOCATED.
         L     R7,PCTUSED
         SR    R8,R7                   SUBTRACT USED FROM ALLOCATED.
         BNP   CHKLNCTR                BYPASS IF NOT POSITIVE.
LOOP2    LA    R9,1(R9)                POINT R9 TO NEXT POS IN GRAPH.
         MVI   0(R9),C'.'              MOVE '.' TO GRAPH.
         BCT   R8,LOOP2                BR ON COUNT TO TOP OF LOOP.
CHKLNCTR L     R5,LINECTR2
         LA    R5,2(R5)                ADD 2 TO LINE COUNTER.
         ST    R5,LINECTR2
         C     R5,PAGELIM              IS LINE COUNTER > PAGE SIZE?
         BNH   PUTLINE2                NO
         SR    R5,R5                   YES - RESET LINE COUNTER.
         ST    R5,LINECTR2
         L     R5,PAGECTR2
         LTR   R5,R5                   IS THIS FIRST PAGE?
         BZ    PUTTITLE                YES - BYPASS FOOTINGS.
         PUT   REP2FILE,TITLE2C        PAGE FOOTINGS.
         MVI   TITLE2B,C' '
         PUT   REP2FILE,TITLE2B
PUTTITLE LA    R5,1(R5)                ADD 1 TO PAGE COUNTER.
         ST    R5,PAGECTR2
         CONVD TITLE2A+131,PAGECTR2,L=2
         PUT   REP2FILE,TITLE2A        NEW PAGE HEADINGS.
         MVI   TITLE2B,C'0'
         PUT   REP2FILE,TITLE2B
         PUT   REP2FILE,TITLE2C
PUTLINE2 PUT   REP2FILE,WORKLINE       PRINT DETAIL LINE.
         PUT   REP2FILE,SKELETON
         LM    R2,R10,SUBSAVE2         RESTORE REGISTERS.
         BR    R10                     RETURN.
***********************************************************************
*                                                                     *
*                  PRINT DETAIL LINE                                  *
*                                                                     *
***********************************************************************
PRINTDET STM   R2,R10,SUBSAVE2         SAVE REGISTERS.
         L     R5,LINECTR1
         LA    R5,1(R5)                ADD 1 TO LINE COUNTER.
         ST    R5,LINECTR1
         C     R5,PAGELIM              IS LINE COUNTER > PAGE SIZE?
         BNH   PUTLINE1                NO
         SR    R5,R5                   YES - RESET LINE COUNTER.
         ST    R5,LINECTR1
         L     R5,PAGECTR1
         LA    R5,1(R5)                ADD 1 TO PAGE COUNTER.
         ST    R5,PAGECTR1
         CONVD TITLE1A+131,PAGECTR1,L=2
         PUT   REP1FILE,TITLE1A        NEW PAGE HEADINGS.
         MVI   TITLE1B,C'0'
         PUT   REP1FILE,TITLE1B
         PUT   REP1FILE,TITLE1C
PUTLINE1 PUT   REP1FILE,WORKLINE       PRINT DETAIL LINE.
         LM    R2,R10,SUBSAVE2         RESTORE REGISTERS.
         BR    R10                     RETURN.
***********************************************************************
*                                                                     *
*                  END OF JOB PROCESSING                              *
*                                                                     *
***********************************************************************
EOFMONV  MVC   MONVVOL,=C'TOTAL '
         BAL   R10,VOLCHNG             DO LAST VOLUME.
         MVC   TOTTRKS,GRANDTOT
         MVC   TRKSALLC,TOTALLC
         MVC   TRKSUSED,TOTUSED
         MVC   DSCOUNT,TOTDSCNT
         BAL   R10,GRAPHRTN
         PUT   REP2FILE,TITLE2C
         MVI   TITLE2B,C' '
         PUT   REP2FILE,TITLE2B
EOJRTN   CLOSE (MONVFILE,,PACKFILE,,REP1FILE,,REP2FILE)
         L     R13,SAVE+4
         RETURN (14,12)
***********************************************************************
*                                                                     *
*                  DCB'S                                              *
*                                                                     *
***********************************************************************
MONVFILE DCB   DDNAME=DSMMONV,EODAD=EOFMONV,MACRF=GM,DSORG=PS
PACKFILE DCB   DDNAME=DSMPACK,EODAD=DDTRACKS,MACRF=GM,DSORG=PS
REP1FILE DCB   DDNAME=DSMOUT1,MACRF=PM,DSORG=PS
REP2FILE DCB   DDNAME=DSMOUT2,MACRF=PM,DSORG=PS
***********************************************************************
*                                                                     *
*                  REGISTER SAVE AREAS                                *
*                                                                     *
***********************************************************************
SAVE     DS    18F                     SAVE REGISTERS.
SUBSAVE1 DS    9F
SUBSAVE2 DS    9F
***********************************************************************
*                                                                     *
*                  FILE DEFINITIONS                                   *
*                                                                     *
***********************************************************************
MONVREC  DS    0CL133
         DS    CL109
MONVVOL  DS    CL6
         DS    CL1
MONVALLC DS    CL4
         DS    CL1
MONVUSED DS    CL4
         DS    CL8
PACKREC  DS    0CL80
PACKNAME DS    CL6
         DS    CL10
PACKTRKS DS    CL6
         DS    CL58
***********************************************************************
*                                                                     *
*                  DATA DEFINITIONS                                   *
*                                                                     *
***********************************************************************
TRKSALLC DC    F'0'
TRKSUSED DC    F'0'
DSCOUNT  DC    F'0'
TOTTRKS  DC    F'0'
TOTALLC  DC    F'0'
TOTUSED  DC    F'0'
TOTDSCNT DC    F'0'
GRANDTOT DC    F'0'
PCTALLC  DC    F'0'
PCTUSED  DC    F'0'
LINECTR1 DC    F'51'
LINECTR2 DC    F'51'
PAGECTR1 DC    F'0'
PAGECTR2 DC    F'0'
PAGELIM  DC    F'50'
TODAY    DS    CL8
SAVEVOL  DC    C'*FIRST'
WORKLINE DS    CL133
SKELETON DS    0CL133
         DC    CL32' '
         DC    10CL10'+'
         DC    CL1'+'
TITLE1A  DS    0CL133
         DC    CL30'1RUN DATE ='
         DC    CL96'*** M E M O R E X ***  D A T A  S E T  U T I L I Z *
               A T I O N  R E P O R T'
         DC    CL7'PAGE'
TITLE1B  DS    0CL133
         DC    CL50'0   --------- D A T A  S E T  N A M E ------  DSOR'
         DC    CL50'G  CREATION  EXPIRATION LAST USE  LAST MOD    USE '
         DC    CL33' AVG USE VOLUME TRKS TRKS  %   #'
TITLE1C  DS    0CL133
         DC    CL50' '
         DC    CL50'     DATE       DATE      DATE      DATE     COUNT'
         DC    CL33' PER DAY       ALLC USED ALLC XTN'
TITLE2A  DS    0CL133
         DC    CL30'1RUN DATE ='
         DC    CL96'SPACE ALLOCATION IN PERCENTAGE OF TOTAL SPACE BY VO*
               LUME'
         DC    CL7'PAGE'
TITLE2B  DS    0CL133
         DC    CL41' VOLUME   ALLC   USED   DSCNT   0'
         DC    CL10'10'
         DC    CL10'20'
         DC    CL10'30'
         DC    CL10'40'
         DC    CL10'50'
         DC    CL10'60'
         DC    CL10'70'
         DC    CL10'80'
         DC    CL9'90'
         DC    CL3'100'
TITLE2C  DS    0CL133
         DC    CL32' '
         DC    10C'+---------'
         DC    CL1'+'
         END   BEGIN
