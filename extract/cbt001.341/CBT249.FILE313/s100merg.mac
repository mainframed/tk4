GETDSNS  CSECT
* MUST BE LINKED INTO AUTHORIZED LIBRARY WITH "SETCODE AC(1)"
         ENTRY1 SAVE
         SPACE 1
         L     R2,0(R1)
         LH    R3,0(R2)
         CH    R3,=H'0'
         BE    NOPARM
         SPACE 1
         STC   R3,COUNT
         LA    R4,PARM
         ST    R4,START
         BCTR  R3,0
         STC   R3,MOVE1+1
MOVE1    MVC   PARM(0),2(R2)
         SPACE 1
         MVI   OPTBR1+1,X'F0'
         MVI   OPTBR2+1,X'F0'
         B     OPTBR1
         SPACE 1
NOPARM   MVI   OPTBR1+1,X'00'
         MVI   OPTBR2+1,X'00'
         SPACE 2
OPTBR1   B     NXTOPEN
         SPACE 1
         OPEN  (SYSIN,(INPUT))
         SPACE 1
         TM    SYSIN+48,X'10'
         BNZ   NXTOPEN
         MVC   RC,=F'8'
         B     EXIT
         SPACE 1
NXTOPEN  OPEN  (SYSUT2,(OUTPUT))
         OPEN  (SYSPRINT,(OUTPUT))
         SPACE 1
         TM    SYSUT2+48,X'10'
         BZ    SETRC12
         TM    SYSPRINT+48,X'10'
         BZ    SETRC12
         B     SETUP
         SPACE 1
SETRC12  MVC   RC,=F'12'
         B     EXIT
         EJECT
SETUP    PUT   SYSPRINT,HEADING1
         PUT   SYSPRINT,HEADING2
         SPACE 1
NEXTDSN  BAL   R11,GETADSN
         BAL   R11,LOCATE
         BAL   R11,READJFCB
         BAL   R11,MODTIOT
         BAL   R11,PROCESS
         B     NEXTDSN
         SPACE 2
EODCDIN  MVI   LINE,X'F0'
         MVC   DSNAME,=CL44'TOTAL RECORDS PROCESSED'
         MVC   VOLSER,=CL6' '
         UNPK  READCNT(6),TCNT
         OI    READCNT+5,X'F0'
         SPACE 1
         PUT   SYSPRINT,LINE
         SPACE 2
         CLOSE (SYSIN,,SYSPRINT,,SYSUT2)
         SPACE 2
         EJECT
EXIT     L     R15,RC
         SPACE 2
         EXITRC
         EJECT
SYSPRINT DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=PM,                      C
               RECFM=FBA,LRECL=80,BLKSIZE=800
         EJECT
SYSIN    DCB   DDNAME=SYSIN,DSORG=PS,MACRF=GM,EODAD=EODCDIN
         EJECT
SYSUT1   DCB   DDNAME=SYSUT1,DSORG=PS,MACRF=GL,BUFL=13030,             C
               EODAD=EODUT1,EXLST=JFCBEXIT
         EJECT
SYSUT2   DCB   DDNAME=SYSUT2,DSORG=PS,MACRF=PM
         EJECT
GETADSN  EQU   *
OPTBR2   B     SCANPARM
         SPACE 1
         GET   SYSIN,CARD
         SPACE 1
         MVC   DSN(44),CARD
         BR    R11
         SPACE 1
SCANPARM CLI   COUNT,X'00'
         BE    EODCDIN
         SPACE 1
         MVC   DSN(44),CARD
         SR    R2,R2
         IC    R2,COUNT
         L     R3,START
         LA    R4,DSN
SCANLOOP CLI   0(R3),C','
         BE    FOUND
         MVC   0(1,R4),0(R3)
         LA    R3,1(R3)
         LA    R4,1(R4)
         BCT   R2,SCANLOOP
         LA    R2,1
FOUND    BCTR  R2,0
         STC   R2,COUNT
         LA    R3,1(R3)
         ST    R3,START
         BR    R11
         EJECT
LOCATE   LOCATE DATASET
         SPACE 1
         CH    R15,=H'0'
         BNE   CATERROR
         BR    R11
         SPACE 1
CATERROR MVC   DSNAME,DSN
         MVC   VOLSER,VOLUMES+6
         MVC   READCNT,=CL6'000000'
         MVC   ERROR,=CL20'CATALOG ERROR'
         SPACE 1
         PUT   SYSPRINT,LINE
         SPACE 1
         MVC   ERROR,=CL20' '
         MVC   RC,=F'4'
         B     NEXTDSN
         SPACE 2
DATASET  CAMLST NAME,DSN,,VOLUMES
DSN      DS    CL44
VOLUMES  DS    0D
         DS    265C
         DS    0F
         EJECT
READJFCB EQU   *
         CLI   VOLUMES+4,X'80'
         BE    TAPE
         MVC   SYSUT1+40(8),=CL8'SYSUT1D '
         B     BYTAPE
TAPE     MVC   SYSUT1+40(8),=CL8'SYSUT1T '
         SPACE 1
BYTAPE   RDJFCB (SYSUT1)
         SPACE 1
*        MVI   JFCBTSDM,X'08'          REMOVED FOR MVS CONVERSION RLD
         MVI   JFCBTSDM,X'40'          MVS CONVERSION       RLD
         MVC   JFCBDSNM(44),DSN
         MVI   JFCBMASK+4,X'80'      MVS CONVERSION       RLD
         SPACE 1
         SR    R2,R2
         LH    R2,VOLUMES
         LA    R3,VOLUMES+6
         LA    R4,JFCBVOLS
VOLMOVES MVC   0(6,R4),0(R3)
         LA    R4,6(R4)
         LA    R3,12(R3)
         BCT   R2,VOLMOVES
         EJECT
MODTIOT  EQU   *
         CLI   VOLUMES+4,X'80'
         BNE   DISK
         BR    R11
         SPACE 1
DISK     NOP   UCBSCAN
         L     R2,16              CVT
         L     R3,0(R2)           TCB WORDS
         L     R4,4(R3)           CURRENT TCB
         L     R5,12(R4)          TIOT
         LA    R6,24(R5)          1ST DD ENTRY
         MVC   UCBLUTA(4),40(R2)  ADDR UCB LOOK UP TABLE
         SPACE 1
         SR    R2,R2
         LR    R3,R6
TEST1    IC    R2,0(R3)
         LTR   R2,R2
         BZ    SETRC12
TEST2    CLC   4(7,R3),=CL7'SYSUT1D'
         BE    DDFOUND
         AR    R3,R2
         SR    R2,R2
         B     TEST1
         SPACE 1
DDFOUND  ST    R3,DDADDR
         MVI   DISK+1,X'F0'
         SPACE 2
UCBSCAN  XC    UCBA,UCBA
         L     R9,UCBLUTA
DOAGAIN  LH    R7,0(R9)
         N     R7,MASK
         BZ    BUMP
         CL    R7,MASK
         BE    NOMORUCB
         CLI   2(R7),X'FF'
         BE    GOTAUCB
BUMP     LA    R9,2(R9)
         B     DOAGAIN
GOTAUCB  C     R7,UCBA
         BNH   BUMP
         ST    R7,UCBA
         CLI   18(R7),X'20'
         BNE   BUMP
         CLC   28(6,R7),JFCBVOLS
         BNE   BUMP
         TM    6(R7),X'40'
         BO    BUMP
         B     GOTADDR
         SPACE 1
NOMORUCB MVC   DSNAME,DSN
         MVC   VOLSER(6),JFCBVOLS
         MVC   READCNT,=CL6'000000'
         MVC   ERROR,=CL20'VOLUME NOT MOUNTED'
         SPACE 1
         PUT   SYSPRINT,LINE
         SPACE 1
         MVC   ERROR,=CL20' '
         MVC   RC,=F'4'
         B     NEXTDSN
         SPACE 2
GOTADDR  L     R5,DDADDR
         LA    R6,16(R5)
         SPACE 1
         LA    R1,C'S'
* MUST BE LINKED INTO AUTHORIZED LIBRARY WITH "SETCODE AC(1)"
         MODESET KEY=ZERO,MODE=SUP             FSH/031280
         SPACE 1
         STM   R0,R1,PROGPSW1
         MVC   1(3,R6),UCBA+1
         MVI   PROGPSW1+7,C'P'
         LM    R0,R1,PROGPSW1
         SPACE 1
         MODESET KEY=NZERO,MODE=PROB           FSH/031280
         SPACE 1
         BR    R11
         EJECT
PROCESS  EQU   *
         ZAP   WCNT,=P'0'
         SPACE 1
         OPEN  (SYSUT1,(INPUT)),TYPE=J
         SPACE 1
         TM    SYSUT1+48,X'10'
         BZ    SETRC12
         SPACE 1
GETREC   GET   SYSUT1
         SPACE 1
         LR    R10,R1
         SPACE 1
         PUT   SYSUT2,(10)
         SPACE 1
         AP    WCNT,=P'1'
         B     GETREC
         SPACE 2
EODUT1   CLOSE (SYSUT1)
         SPACE 1
         AP    TCNT,WCNT
         MVC   DSNAME,DSN
         MVC   VOLSER(6),JFCBVOLS
         UNPK  READCNT(6),WCNT
         OI    READCNT+5,X'F0'
         SPACE 1
         PUT   SYSPRINT,LINE
         SPACE 1
         BR    R11
         EJECT
SAVE     DS    18F
RC       DC    F'0'
PARM     DC    CL100' '
COUNT    DC    XL1'00'
START    DC    F'0'
         SPACE 1
UCBA     DC    A(0)
UCBLUTA  DC    A(0)
DDADDR   DC    A(0)
         SPACE 1
MASK     DC    XL4'0000FFFF'
         SPACE 1
PROGPSW1 DS    D
         SPACE 1
         DS    0F
JFCBEXIT DC    X'87',AL3(JFCB)
JFCB     DC    44F'0'
         SPACE 1
JFCBDSNM EQU   JFCB
JFCBTSDM EQU   JFCB+52
JFCBMASK EQU   JFCB+72              MVS CONVERSION      RLD
JFCBVOLS EQU   JFCB+118
JFCBFLSQ EQU   JFCB+68
         SPACE 1
TCNT     DC    PL4'0'
WCNT     DC    PL4'0'
         SPACE 1
CARD     DC    CL80' '
         SPACE 1
LINE     DS    0CL80
         DC    CL1' '
DSNAME   DS    CL44
         DC    CL1' '
VOLSER   DS    CL6
         DC    CL1' '
READCNT  DS    CL6
         DC    CL1' '
ERROR    DC    CL20' '
         SPACE 1
HEADING1 DS    0CL80
         DC    CL1'1'
         DC    CL79' '
         SPACE 1
HEADING2 DS    0CL80
         DC    CL1'0'
         DC    CL44'-------- D A T A   S E T   N A M E ---------'
         DC    CL35' VOLSER   READ  ERRORS              '
         EJECT
         END
