//$SYS17IT JOB (8220,0000),'6-CPM',CLASS=Z,MSGCLASS=X,NOTIFY=$SYS17
/*JOBPARM Q=F
//       EXEC  ASMFCL
//ASM.SYSLIB DD DSN=SYS1.MACLIB,VOL=SER=SYS999,DISP=SHR,UNIT=DISK
//ASM.SYSIN DD *
INTTAB   TITLE 'MRX INTERPRET TABLE FOR VTAM LOGONS'
***********************************************************************
*             MODULE NAME = INTTAB                                    *
*                                                                     *
*             DESCRIPTIVE NAME =  MRX INTERPRET TABLE                 *
*                                                                     *
*             STATUS =                                                *
*                                                                     *
*             FUNCTION =                                              *
*                CONTAINS THE UPPER AND LOWER CASE LOGONS FOR LOGON   *
*                (STDLOGON), TMONLINE, TMDATAEN, TMTEST01, TMTEST02   *
*                                                                     *
*                OPERATION =                                          *
*                   NO EXECUTABLE CODE                                *
*                                                                     *
*              NOTES =                                                *
*                                                                     *
*                 DEPENDENCIES = CHARACTER IS EBCDIC.  REASSEMBLE     *
*                    IF A DIFFERENT CHARACTER SET IS NEEDED.          *
*                                                                     *
*                 RESTRICTIONS = NONE                                 *
*                                                                     *
*                 REGISTER CONVENTIONS = UNUSED                       *
*                                                                     *
*                PATCH LABEL = NONE                                   *
*                                                                     *
*             MODULE TYPE = TABLE                                     *
*                                                                     *
*                PROCESSOR = ASM                                      *
*                                                                     *
*                ATTRIBUTES = NONE                                    *
*                                                                     *
*             ENTRY POINTS = NONE                                     *
*                                                                     *
*             INPUT = NONE                                            *
*                                                                     *
*             OUTPUT = NONE                                           *
*                                                                     *
*             EXITS = NONE                                            *
*                                                                     *
*             EXTERNAL REFERENCES = NONE                              *
*                                                                     *
*             TABLES = NONE                                           *
*                                                                     *
*             MACROS = NONE                                           *
*                                                                     *
*             CHANGE ACTIVITY = NONE                                  *
*                                                                     *
*             MESSAGES = NONE                                         *
*                                                                     *
*             ABEND CODES = NONE                                      *
*                                                                     *
***********************************************************************
         EJECT
INTBL    INTAB
         LOGCHAR APPLID=(ROUTINE,STDLOGON),SEQNCE='LOGON'
         LOGCHAR APPLID=(ROUTINE,STDLOGON),SEQNCE='logon'
         LOGCHAR APPLID=(APPLICID,TMONLINE),SEQNCE='TMONLINE'
         LOGCHAR APPLID=(APPLICID,TMONLINE),SEQNCE='tmonline'
         LOGCHAR APPLID=(APPLICID,TMDATAEN),SEQNCE='TMDATAEN'
         LOGCHAR APPLID=(APPLICID,TMDATAEN),SEQNCE='tmdataen'
         LOGCHAR APPLID=(APPLICID,TMTEST01),SEQNCE='TMTEST01'
         LOGCHAR APPLID=(APPLICID,TMTEST01),SEQNCE='tmtest01'
         LOGCHAR APPLID=(APPLICID,TMTEST02),SEQNCE='TMTEST02'
         LOGCHAR APPLID=(APPLICID,TMTEST02),SEQNCE='tmtest02'
         ENDINTAB
***************************************************************
*
*    SCAN INPUT FOR STANDARD LOGON SEQUENCE
*
*    STANDARD LOGON SEQUENCE IS AS FOLLOWS:
*      ANY NUMBER OF BLANKS
*      'LOGON ' OR 'LOGON,'
*      OPTIONAL OTHER DATA ENDING IN ' ' OR ','
*      'APPLID('
*      ONE TO EIGHT ALAPHANUMERIC CHARACTERS SIGNIFYING
*       THE APPLICATION NAME
*      ')'
*      AN OPTIONAL ' ' OR ',' FOLLOWED BY OTHER DATA
*
*    NOTE:
*      'LOGON' OR 'LOGON ' OR 'LOGON,'
*      IS SUFFICIENT TO LOGON TO TSO.
*
*    NOTE:
*      IF 'APPLID(NAME)' IS NOT FOUND,
*      THEN TSO IS ASSUMED.
*
***************************************************************
STDLOGON CSECT
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         STM   R14,R12,12(R13)
         BALR  R12,0
         USING *,R12
*
         LR    R2,R0                   LENGTH OF LOGON MESSAGE
         BCTR  R2,0                    DECREMENT TO
         EX    R2,TRANS                 EXECUTE TRANSLATE
         LR    R2,R0                   LENGTH OF LOGON MESSAGE
         LR    R3,R1                   POINT TO LOGON MESSAGE
         LR    R4,R3                   POINT BEYOND END OF
         AR    R4,R2                    LOGON MESSAGE
         BCTR  R3,0                    BACKSPACE 1 FOR LOOP
*                                       ACTION
*                                       BELOW
         SPACE 2
NEXTCHAR EQU   *
         SPACE 2
         LA    R3,1(,R3)               NEXT CHARACTER
         CLI   0(R3),C' '              IGNORE LEADING BLANKS
         BNE   FOUNDLOG                NON BLANK CONTINUE
         BCT   R2,NEXTCHAR             ALL BLANKS?
         B     VS2NTSO                 YES. MUST BE TSO
         SPACE 2
FOUNDLOG EQU   *
         LA    R5,L'CLOGON+L'CAPPLID+3(,R3) MINIMUM END
         CR    R5,R4                   CAN STANDARD SEQUENCE
*                                       FIT IN DATA AREA?
         BH    VS2NTSO                 NO. MUST BE TSO
         SPACE 2
*       'LOGON' HAS BEEN FOUND.SEARCH FOR APPLID(NAME)
         SPACE 2
         LA    R3,L'CLOGON(,R3)        NEXT CHAR BEYOND LOGON
         CLI   0(R3),C' '              NEXT CHAR MUST BE
         BE    APPLSCAN                  EITHER BLANK OR A
         CLI   0(R3),C','                COMMA
         BNE   VS2NTSO                 NO. MUST BE TSO
         SPACE 2
APPLSCAN EQU   *
         SPACE 2
         LA    R3,1(,R3)               NEXT CHAR
         LA    R5,L'CAPPLID+2(,R3)     MINIMUM END
         CR    R5,R4                   CAN STANDARD SEQUENCE
*                                       FIT IN DATA AREA?
         BH    VS2NTSO                 NO. MUST BE TSO
         SPACE 2
         CLC   CAPPLID,0(R3)           IS THIS APPLID(?
         BNE   APPLSCAN
         SPACE 2
*       APPLID( FOUND--ENSURE THAT BLANK OR COMMA PRECEDES IT
*       AND THAT ITS WITHIN BOUNDS OF MESSAGE
         SPACE 2
         BCTR  R3,0                    BACKSPACE ONE
         CLI   0(R3),C' '              IS IT A BLANK?
         BE STARTSOK                   CONTINUE IF SO
         SPACE 2
         CLI 0(R3),C','                IS IT A COMMA?
         BE    STARTSOK                CONTINUE IF SO
         SPACE 2
         LA    R3,L'CAPPLID+1(,R3) NEXT CHAR PAST APPLID(
         B     APPLSCAN                CONTINUE SCAN FOR APPLID(
         SPACE 2
STARTSOK EQU   *
         SPACE 2
         LA    R3,L'CAPPLID+1(,R3) GO PAST APPLID(
NEXT     CLI   0(R3),C' '              IS IT A BLANK?
         BNE   RIGHT                   NO.CHECK FOR RIGHT PAREN
         LA    R3,1(,R3)               POINT TO NEXT CHAR
         CR    R3,R4                   END REACHED?
         BNL   VS2NTSO                 YES. MUST BE TSO
         B     NEXT                    NO. GO TO NEXT CHAR
RIGHT    CLI   0(R3),C')'              NULL APPL NAME?
         BE    APPLSCAN                YES. LOOK FOR VALID
*                                       APPLID(
         SPACE 2
*       FIND RIGHT PARENTHESIS
         SPACE 2
         LR    R6,R3                   ADDRESS POTENTIAL APPLID NAME
         SPACE 2
FNDPAREN EQU   *
         SPACE 2
         LA    R3,1(,R3)               POINT TO NEXT CHAR
         CR    R3,R4                   END REACHED?
         BNL VS2NTSO                   YES. MUST BE TSO
         SPACE 2
         CLI   0(R3),C')'              IS THIS RIGHT PAREN.?
         BNE   FNDPAREN                NO,CONTINUE SCAN
         LA    R5,1(,R3)               MINIMUM END
         CR    R5,R4                   END OF DATA
         BE    NAMELENG                YES. CAN CHECK FOR
*                                       APPL NAME
         SPACE 2
         CLI   1(R3),C' '              NO. NEXT CHARACTER MUST
         BE    NAMELENG                 BE BLANK OR COMMA,
         CLI   1(R3),C','               ELSE CONTINUE SCAN FOR
         BNE   APPLSCAN                 VALID APPLID
         SPACE 2
NAMELENG EQU *
         SPACE 2
*       IF THIS POINT REACHED APPLID WITH LEFT AND RT. PAREN FOUND
*        AND IT IS FOLLOWED BY A BLANK OR A COMMA OR NO MORE DATA
         SPACE 2
         SR    R3,R6                   LENGTH OF APPL NAME IN R3
         CH    R3,LAPPLID              IS NAME TOO BIG?
         BNH   EXTRAPPL                NO. EXTRACT APPL NAME
         LR    R3,R6                   YES. CONTINUE SCAN FOR
         B     APPLSCAN                 VALID APPLID(
EXTRAPPL EQU   *
         SPACE 2
         BCTR  R3,0                    DECREMENT FOR EXECUTE
         MVC   20(8,R13),BLANK8        CLEAR OUT TO MOVE NAME
         EX    R3,MVCNAME              EXEC MVC WITH CORRECT LENGTH
         B     VS2ISOK                 GO SET GOOD CODE
         SPACE 2
*  LOGON IS FOR TSO
VS2NTSO  MVC   20(8,R13),CTSO          MOVE TSO NAME INTO SAVE AREA
         SPACE 2
*  VS2 LOGON SEQUENCE O.K.
VS2ISOK  SR    R15,R15                 INDICATE THAT LOGON SEQ
         LM    R0,R12,20(R13)           WAS INTERPRETED OK
         BR    R14                     RETURN TO CALLER
         SPACE 3
BLANK8   DC    CL8' '                  BLANKS FOR NAME
CLOGON   DC    C'LOGON'
CTSO     DC    CL8'TSO'
CAPPLID  DC    C'APPLID('
LAPPLID  DC    H'8'                    LENGTH OF APPLID NAME
MVCNAME  MVC   20(0,R13),0(R6)
TRANS    TR    0(0,R1),TRTAB2
         SPACE 3
TRTAB2  EQU   *
         DC    74C' '         00-49    BLANK OUT CONTROL CHARS
         DC    X'4A'                   CENT OR LEFT ARROW
         DC    C'.<(+×'
         DC    X'50'                   AMPERSAND
         DC    X'515253545556575859'
         DC    X'5A'                   EXCLAMATION POINT
         DC    C'$*);^-/'
         DC    X'6263646566676869'
         DC    X'6A'                   VERTICLE LINE (NOT ×)
         DC    C',%_>?'
         DC    X'707172737475767778'
         DC    X'79'                   GRAVE ACCENT
         DC    C':#@'
         DC    X'7D'                   SINGLE QUOTE
         DC    C'="'
         DC    X'80'
         DC    C'ABCDEFGHI'            SMALL LETTERS
         DC    X'8A8B8C8D8E8F90'
         DC    C'JKLMNOPQR'            SMALL LETTERS
         DC    X'9A9B9C9D9E9FA0'
         DC    X'A1'                   TILDE
         DC    C'STUVWXYZ'             SMALL LETTERS
         DC    X'AAABACADAEAF'
         DC    X'B0B1B2B3B4B5B6B7B8B9'
         DC    X'BABBBCBDBEBF'
         DC    X'C0'                   LEFT BRACE
         DC    C'ABCDEFGHI'            CAPITAL LETTERS
         DC    X'CACB'
         DC    X'CC'                   HOOK
         DC    X'CD'
         DC    X'CE'                   FORK
         DC    X'CF'
         DC    X'D0'                   RIGHT BRACE
         DC    C'JKLMNOPQR'            CAPITAL LETTERS
         DC    X'DADBDCDDDEDF'
         DC    X'E0'                   REVERSE SLANT
         DC    X'E1'
         DC    C'STUVWXYZ'             CAPITAL LETTERS
         DC    X'EAEB'
         DC    X'EC'                   CHAIR
         DC    X'EDEEEF'
         DC    C'0123456789'
         DC    X'FA'                   LONG VERTICLE LINE (NOT
*                                       × OR VERTICLE LINE)
         DC    X'FBFCFDFEFF'
         END
/*
//LKED.SYSLMOD DD DSN=SYS1.VTAMLIB(INTTAB),DISP=SHR,VOL=SER=SYS999,
//             UNIT=DISK
//
