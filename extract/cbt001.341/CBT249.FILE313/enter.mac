         MACRO
&NAME    ENTER &BASE=12,&CSECT=YES,&REENT=NO,&SA=
         LCLA  &NUM,&DISPL
         LCLB  &SW(3)
         LCLC  &START,&SAVE
         ACTR  200
         AIF   ('&NAME' EQ '' AND '&CSECT' EQ 'YES').ERR1
         AIF   ('&CSECT' NE 'YES' AND '&CSECT' NE 'NO').ERR2
         AIF   ('&REENT' NE 'YES' AND '&REENT' NE 'NO').ERR3
         AIF   ('&SA' NE '' AND '&REENT' EQ 'YES').ERR4
&SW(1)   SETB  ('&NAME' NE '')
&SW(2)   SETB  ('&CSECT' EQ 'YES')
&SW(3)   SETB  ('&REENT' EQ 'YES')
         AIF   (NOT &SW(1)).EQU
&START   SETC  '&NAME'
         AIF   (NOT &SW(2)).UN
&NAME    CSECT
         SAVE  (14,12),T,*
         AGO   .LB
.EQU     ANOP
ENTR&SYSNDX EQU *
&START   SETC  'ENTR&SYSNDX'
         SAVE  (14,12),T
         AGO   .LB
.UN      ANOP
&NAME    SAVE  (14,12),T,&NAME
.LB      LR    &BASE(1),15
         USING &START,&BASE(1)
.RETST   AIF   (NOT &SW(3)).SATST
         LR    14,1
         GETMAIN R,LV=72
         ST    13,4(1)
         LR    15,13
         LR    13,1
         LR    1,14
         ST    13,8(15)
         AGO   .MBASE
.SATST   AIF   ('&SA' EQ '').MSA
&SAVE    SETC  '&SA'
         AGO   .CONTS
.MSA     ANOP
&SAVE    SETC  'SAVE&SYSNDX'
.CONTS   ST    13,&SAVE+4
         LR    15,13
         LA    13,&SAVE
         ST    13,8(15)
.MBASE   AIF   (N'&BASE EQ 1).SA2T
&NUM     SETA  2
.L1      LA    &BASE(&NUM),4095(&BASE(&NUM-1))
&DISPL   SETA  4095*(&NUM-1)
         USING &START+&DISPL,&BASE(&NUM)
         AIF   (&NUM EQ N'&BASE).SA2T
&NUM     SETA  &NUM+1
         AGO   .L1
.SA2T    AIF   (&SW(3) OR '&SA' NE '').EXIT
         B     ENTD&SYSNDX
SAVE&SYSNDX DC 18F'0'
ENTD&SYSNDX EQU *
         MEXIT
.ERR1    MNOTE 12,'SYMBOL FIELD CAN NOT BE BLANK WHEN CSECT=YES'
         MEXIT
.ERR2    MNOTE 12,'CSECT MUST BE YES OR NO'
         MEXIT
.ERR3    MNOTE 12,'REENT MUST BE YES OR NO'
         MEXIT
.ERR4    MNOTE 12,'SA AND REENT ARE MUTUALLY EXCLUSIVE'
.EXIT    MEND
