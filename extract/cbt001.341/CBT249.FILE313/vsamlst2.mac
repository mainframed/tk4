         PRINT OFF
         MACRO
&NAME    INIT &BASE=3,&REGS=Y,&PATCH=3,&RENT=N,&SAVE=Y
         AIF   ((&BASE LT 13) AND (&BASE GT 1)).N020
         MNOTE 12,'INVALID BASE REGISTER'
         MEXIT
.N020    ANOP
         PUSH  PRINT
         PRINT ON,GEN
         EJECT
&SYSECT  CSECT
         USING *,15
         B     BEGIN
         DC    AL1(24)
         DC    CL8'&SYSECT'
         DC    CL16'-&SYSDATE-&SYSTIME'
         AIF   ('&RENT' EQ 'Y').N004
MYSAVE   DC    18F'-1'
.N004    ANOP
         AIF   ('&PATCH' EQ '0').N005
PATCH    DC    &PATCH.CL8'*PATCH*'
.N005    ANOP
         AIF   ('&REGS' EQ 'N').N030
         AIF   ('&REGS' EQ 'Y').N010
         MNOTE 4,'REGS OPERAND INVALID. Y SUBSTITUTED'
.N010    ANOP
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
.N030    ANOP
BEGIN    DS   0H
         STM   14,12,12(13)
         LR    &BASE,15
         DROP  15
         USING &SYSECT,&BASE
         AIF   ('&SAVE' EQ 'N').N003
         AIF   ('&RENT' EQ 'Y').N002
         AIF   ('&RENT' EQ 'N').N001
         MNOTE 4,'RENT OPERAND INVALID. N SUBSTITUTED'
.N001    ANOP
         ST    13,MYSAVE+4
         LR    15,13
         LA    13,MYSAVE
         ST    13,8(15)
         AGO   .N003
.N002    ANOP
         GETMAIN R,LV=72
         ST    13,4(1)
         ST    1,8(13)
         LR    13,1
.N003    ANOP
         POP   PRINT
         EJECT
         MEND
         PRINT ON
*  MODULE NAME:         VSAMLST2     (REL. 1.1  08/10/79)
*
*  MODULE DESCRIPTION:  VSAM CATALOG LIST UTILITY - PROCESS MODULE
*
*  RETURN LINKAGE:      RETURN (14,12),RC=(15)
*
*  LINKAGE TABLE:       NONE - SINGLE ENTRY
*
*  PARAMETERS:          R1 POINTS TO A PARAMETER LIST ALIGNED ON A
*                       FULL WORD BOUNDARY.
*                        WORD 1 - ADDRESS OF VSAM CATALOG NAME
*                        WORD 2 - ADDRESS OF PASSWORD & ITS FLAG
*                        WORD 3 - ADDRESS OF REPORT SWITCH
*                        WORD 4 - ADDRESS OF VCL MODULES' ENTRY POINTS
*                        WORD 5 - ADDRESS OF MSGFILE DCB
*                        WORD 6 - ADDRESS OF RPTFIL1 DCB
*                        WORD 7 - ADDRESS OF SORTIN DCB
*                        WORD 8 - ADDRESS OF WORK AREA 'A'
*                        WORD 9 - ADDRESS OF WORK AREA 'B'
*
*  EXIT:                RC=00 - SUCCESSFUL
*                       RC=04 - WRONG MASTER PASSWORD
*                       RC=08 - INVALID VSAM CATALOG NAME
*
*  MODULE FUNCTION:     FOR THE VSAM CATALOG SPECIFIED IN THE PARAMETER
*                       LIST, THIS MODULE CREATES ONE TO THREE REPORTS
*                       ACCORDING TO THE REPORT SWITCH (RPTSW).
*
*  CALLER:              VSAMLIST
*
*  CALLS:               VSAMLST3, VSAMLST4
*
*  SYSTEMS SERVICES:    CALL, PUT (QSAM), SVC 26
*
*  MODULE ENVIRONMENT:  OS/VS1, OS/VS2
         TITLE 'VSAMLST2 --- VSAM CATALOG LIST UTILITY PROCESS MODULE (X
               2.0)'
*        *-----------------------------------*
*        *   R E G I S T E R   U S A G E S   *
*        *-----------------------------------*
*
*  R0  -  STANDARD LINKAGE
*  R1  -  STANDARD LINKAGE (ADDR OF PARM LIST) & TEMP WORK SPACE
*  R2  -  NOT USED
*  R3  -  NOT USED
*  R4  -  NOT USED
*  R5  -  NOT USED
*  R6  -  NOT USED
*  R7  -  NOT USED
*  R8  -  NOT USED
*  R9  -  NOT USED
*  R10 -  NOT USED
*  R11 -  NOT USED
*  R12 -  BASE REG FOR CSECT VSAMLST2
*  R13 -  STANDARD LINKAGE (ADDR OF SAVE AREA)
*  R14 -  STANDARD LINKAGE (ADDR TO RETURN)
*  R15 -  STANDARD LINKAGE (ADDR OF ENTRY POINT & RETURN CODE)
         SPACE 3
VSAMLST2 CSECT
         INIT  BASE=12
***********************************************************************
*                                                                     *
*  2.0    PROCESS MODULE                                              *
*                                                                     *
*     FUNCTION:  CHECK THE MASTER PASSWORD & GET THE ADDR OF CATACB.  *
*                SET THE MASTER CTGPL & READ THRU THE VSAM CATLG      *
*                SEQUENTIALLY.                                        *
*                FOR EACH ENTRY CALL THE SUMMARY REPORT GENERATOR.    *
*                IF 'PARM=ALL' IS SPECIFIED, CALL THE DETAIL REPORTS  *
*                GENERATOR ALSO.                                      *
*                                                                     *
*     ERRORS:  IF INVALID VSAM CATALOG, PRINT ERROR MESSAGE, SET      *
*              RETURN CODE AND RETURN.                                *
*              IF WRONG MASTER PASSWORD, PRINT ERROR MESSAGE, SET     *
*              RETURN CODE AND RETURN.                                *
*                                                                     *
***********************************************************************
         SPACE 3
VCL20    EQU   *
         MVI   RC20,X'00'               RESET RETURN CODE
         MVC   PARM9420(36),0(R1)       SAVE 9 FULL WORDS PARM LIST
         L     R1,ACATNAME              SET CATALOG NAME
         MVC   CATNAME,0(R1)
         MVI   CATENTRY,X'40'           SET CATALOG ENTRY TO BLANKS
         MVC   CATENTRY+1(L'CATENTRY-1),CATENTRY
         L     R1,APASSWRD              SET MASTER PASSWORD & ITS FLAG
         MVC   MSTRPSWD,0(R1)
         MVC   MPSWDFLG,8(R1)           X'FF', X'00' OR X'01'
         SPACE
         L     R1,AWORKA                SET ADDR OF WORK AREA 'A'
         ST    R1,CTGZWKA               IN CTLGPL-Z
         ST    R1,CTGAWKA               IN CTLGPL-A
         ST    R1,CTGBWKA               IN CTLGPL-B
         SPACE
CHKCATNM EQU   *
         LA    R1,CTLGPLZ               SET PARM LIST FOR SVC 26
         SVC   26
         LTR   R15,R15                  IS THE SUPPLIED CATNAME FOUND?
         BZ    CHKMPSWD                 YES - CHECK THE MASTER PASSWORD
         SPACE
         MVI   RC20,X'08'               SET FOR WRONG CATLG NAME
         L     R1,AMSGFILE
         PUT   (1),MSG01                WRITE ERROR MESSAGE
         SPACE
         B     GOHOME                   AND RETURN
         EJECT
*        *-------------------------*
*        *  CHECK MASTER PASSWORD  *
*        *-------------------------*
         SPACE
CHKMPSWD EQU   *
         LA    R1,CTLGPLA               SET PARM LIST FOR SVC 26
         SVC   26
         LTR   R15,R15                  IS IT THE MASTER PASSWORD?
         BZ    INIT20                   YES - SAVE THE ADDR OF CATACB
         SPACE
         MVI   RC20,X'04'               SET FOR WRONG MASTER PASSWORD
         CLI   MPSWDFLG,X'00'           IS PASSWORD PRE-DEFINED?
         BE    DEFAULT                  YES - MUST HAVE BEEN CHANGED
         CLI   MPSWDFLG,X'01'           IS PASSWORD SUPPLIED BY USER?
         BE    NOTSUPPL                 NO - DON'T GIVE OUT PASSWORD
         SPACE
         MVC   MSG02PSW,MSTRPSWD        YES - SET ERROR MESSAGE
         LA    R0,MSG02                 ERROR MESSAGE TO BE PRINTED
         B     PRTMSG
         SPACE
DEFAULT  EQU   *
         LA    R0,MSG03
         B     PRTMSG
         SPACE
NOTSUPPL EQU   *
         LA    R0,MSG04
         SPACE
PRTMSG   EQU   *
         L     R1,AMSGFILE
         PUT   (1),(0)                  WRITE ERROR MESSAGE
         SPACE
         B     GOHOME                   AND RETURN
         EJECT
*        *------------------*
*        *  SET PARM LISTS  *
*        *------------------*
         SPACE
INIT20   EQU   *
         L     R1,CTLGFLA4+20
         MVC   CATACB,0(R1)             ADDR OF CATLG ACB
         SPACE
         L     R1,ARPTSW                ADDR OF REPORT SWITCH
         MVC   RPTSW,0(R1)              SAVE IT
         L     R1,AEPVCL                ADDR OF VCL ENTRY POINTS
         MVC   EPVCL,4(R1)              SAVE ONLY VCL 2.1 & VCL 2.2
         L     R1,AMSGFILE              ADDR OF DCB MSGFILE
         ST    R1,AMSGF21
         ST    R1,AMSGF22
         SPACE
         L     R1,ARPTFIL1              ADDR OF DCB RPTFIL1
         ST    R1,ARPTF21
         L     R1,ASORTIN               ADDR OF DCB SORTIN
         ST    R1,ASORT22
         L     R1,AWORKB                ADDR OF WORK AREA 'B'
         ST    R1,AWORK21
         ST    R1,AWORK22
         SPACE
         LA    R0,4                     INIT CALL TO SET TITLES
         LA    R1,PARM9421
         L     R15,EPVCL21
         CALL  (15)                     CALL VCL 2.1
         EJECT
*        *------------------------------------*
*        *  PROCESS - CALL VCL 2.1 & VCL 2.2  *
*        *------------------------------------*
         SPACE
NEXTENT  EQU   *
         LA    R1,CTLGPLB
         SVC   26
         LTR   R15,R15
         BNZ   GOHOME
         SPACE
CALL9421 EQU   *
         SR    R0,R0                    REGULAR CALL
         LA    R1,PARM9421
         L     R15,EPVCL21
         CALL  (15)                     CALL VCL 2.1
         SPACE
         TM    RPTSW,X'60'              ARE THE DETAIL REPORTS NEEDED?
         BZ    BUMPENT                  NO - BUMP TO NEXT ENTRY
         SPACE
CALL9422 EQU   *
         LA    R1,PARM9422
         L     R15,EPVCL22
         CALL  (15)                     CALL VCL 2.2
         SPACE
BUMPENT  EQU   *
         L     R1,CTLGFLB2+20
         MVC   CATENTRY,0(R1)           ENTRY NAME
         SR    R1,R1
         IC    R1,CATENTRY+43
         LA    R1,1(,R1)
         STC   R1,CATENTRY+43
         B     NEXTENT                  NEXT ENTRY
         EJECT
*        *-------------*
*        *   E X I T   *
*        *-------------*
         SPACE
GOHOME   EQU   *
         SR    R15,R15
         IC    R15,RC20
         L     R13,MYSAVE+4
         RETURN (14,12),RC=(15)
         SPACE 2
RC20     DC    X'00'
MPSWDFLG DC    X'00'
RPTSW    DC    X'00'
         SPACE
MSG01    DS    0CL133
         DC    C'0'                     SKIP 2 LINES
         DC    CL132'VSAMLST2---01 CATALOG NAME SPECIFIED ON THE CONTROX
               L CARD IS NOT FOUND'
         SPACE
MSG02    DS    0CL133
         DC    C'0'                     SKIP 2 LINES
         DC    CL132'VSAMLST2---02 XXXXXXXX IS AN INVALID MASTER PASSWOX
               RD FOR ABOVE VSAM CATALOG'
         ORG   MSG02+14
MSG02PSW DS    CL8
         ORG
         SPACE
MSG03    DS    0CL133
         DC    C'0'                     SKIP 2 LINES
         DC    CL132'VSAMLST2---03 DEFAULT MASTER PASSWORD IS INVALID FX
               OR ABOVE VSAM CATALOG'
         SPACE
MSG04    DS    0CL133
         DC    C'0'                     SKIP 2 LINES
         DC    CL132'VSAMLST2---04 PASSWORD MUST BE SUPPLIED FOR THIS VX
               SAM CATALOG'
         SPACE
CATACB   DC    A(0)
CATNAME  DC    CL44'VSAM.CATALOG.NAME'
CATENTRY DC    CL44'VSAM.CATALOG.ENTRY.BLANKS'
PASSWORD DS    0CL32
MSTRPSWD DC    CL8'MSTRPSWD'
CNTLPSWD DC    CL8' '
UPDTPSWD DC    CL8' '
READPSWD DC    CL8' '
         SPACE
EPVCL    DS    0CL8
EPVCL21  DC    A(0)
EPVCL22  DC    A(0)
         EJECT
*        *-------------------------*
*        *   P A R M   L I S T S   *
*        *-------------------------*
         SPACE
PARM9420 DS    0F
ACATNAME DC    A(0)
APASSWRD DC    A(0)
ARPTSW   DC    A(0)
AEPVCL   DC    A(0)
AMSGFILE DC    A(0)
ARPTFIL1 DC    A(0)
ASORTIN  DC    A(0)
AWORKA   DC    A(0)
AWORKB   DC    A(0)
         SPACE 2
PARM9421 DS    0F
ACATN21  DC    A(CATNAME)
ACTPL21  DC    A(CTLGPLB)
AMSGF21  DC    A(0)
ARPTF21  DC    A(0)
AWORK21  DC    A(0)
         SPACE 2
PARM9422 DS    0F
ACATN22  DC    A(CATNAME)
ACTPL22  DC    A(CTLGPLB)
AMSGF22  DC    A(0)
ASORT22  DC    A(0)
AWORK22  DC    A(0)
         EJECT
*        *---------------------------------------------*
*        *   C T L G P L   A N D   C T G F L   -   Z   *
*        *---------------------------------------------*
         SPACE
CTLGPLZ  DS    0D
CTGZOPT1 DC    X'06008108'
*  CTGENT CONTAINS ADDR OF ENTRY NAME
*  CTGCAT CONTAINS ADDR OF CATLG NAME
*  A CATALOG MANAGEMENT SERVICES FUNCTION
*  THE CALL IS A VSAM CATALOG MANAGEMENT REQUEST
*  BYPASS SECURITY PROMPTING TO SYSTEM OPERATOR
*
CTGZENT  DC    A(CATNAME)
CTGZCAT  DC    A(CATNAME)
CTGZWKA  DC    A(0)
CTGZOPT2 DC    X'2000C302'
CTGZDDNM DC    A(0)
CTGZPSWD DC    A(0)
CTGZFLDS DS    0F
         DC    A(CTLGFLZ1)
         DC    A(CTLGFLZ2)
         SPACE 2
CTGFLZ   DS    0D
         SPACE
CTLGFLZ1 DC    X'01',AL3(0),F'0'
         DC    A(REQUES02)              ENTYPE    (LTH = 1)
         DC    3F'0'
         SPACE
CTLGFLZ2 DC    X'01',AL3(0),F'0'
         DC    A(REQUES03)              ENTNAME   (LTH = 44)
         DC    3F'0'
         EJECT
*        *---------------------------------------------*
*        *   C T L G P L   A N D   C T G F L   -   A   *
*        *---------------------------------------------*
         SPACE
CTLGPLA  DS    0D
CTGAOPT1 DC    X'46008108'
*  CHECK THE MASTER PASSWORD
*  CTGENT CONTAINS ADDR OF ENTRY NAME
*  CTGCAT CONTAINS ADDR OF CATLG NAME
*  A CATALOG MANAGEMENT SERVICES FUNCTION
*  THE CALL IS A VSAM CATALOG MANAGEMENT REQUEST
*  BYPASS SECURITY PROMPTING TO SYSTEM OPERATOR
*
CTGAENT  DC    A(CATNAME)
CTGACAT  DC    A(CATNAME)
CTGAWKA  DC    A(0)
CTGAOPT2 DC    X'2000C304'
CTGADDNM DC    A(0)
CTGAPSWD DC    A(PASSWORD)
CTGAFLDS DS    0F
         DC    A(CTLGFLA1)
         DC    A(CTLGFLA2)
         DC    A(CTLGFLA3)
         DC    A(CTLGFLA4)
         SPACE 2
CTGFLA   DS    0D
         SPACE
CTLGFLA1 DC    X'01',AL3(0),F'0'
         DC    A(REQUES02)              ENTYPE    (LTH = 1)
         DC    3F'0'
         SPACE
CTLGFLA2 DC    X'01',AL3(0),F'0'
         DC    A(REQUES03)              ENTNAME   (LTH = 44)
         DC    3F'0'
         SPACE
CTLGFLA3 DC    X'01',AL3(0),F'0'
         DC    A(REQUES24)              PASSWORD  (LTH = 32)
         DC    3F'0'
         SPACE
CTLGFLA4 DC    X'01',AL3(0),F'0'
         DC    A(REQUES33)              CATACB    (LTH = 4)
         DC    3F'0'
         EJECT
*        *---------------------------------------------*
*        *   C T L G P L   A N D   C T G F L   -   B   *
*        *---------------------------------------------*
         SPACE
CTLGPLB  DS    0D
CTGBOPT1 DC    X'44108108'
*  CHECK THE MASTER PASSWORD
*  CTGENT CONTAINS ADDR OF ENTRY NAME
*  GET NEXT OPTION WITH LISTCAT
*  A CATALOG MANAGEMENT SERVICES FUNCTION
*  THE CALL IS A VSAM CATALOG MANAGEMENT REQUEST
*  BYPASS SECURITY PROMPTING TO SYSTEM OPERATOR
*
CTGBENT  DC    A(CATENTRY)
CTGBCAT  DC    A(CATACB)
CTGBWKA  DC    A(0)
CTGBOPT2 DC    X'20000008'
CTGBDDNM DC    A(0)
CTGBPSWD DC    A(PASSWORD)
CTGBFLDS DS    0F
         DC    A(CTLGFLB1)
         DC    A(CTLGFLB2)
         DC    A(CTLGFLB3)
         DC    A(CTLGFLB4)
         DC    A(CTLGFLB5)
         DC    A(CTLGFLB6)
         DC    A(CTLGFLB7)
         DC    A(CTLGFLB8)
         SPACE
CTGFLB   DS    0D
CTLGFLB1 DC    X'01',AL3(0),F'0'
         DC    A(REQUES02)              ENTYPE    (LTH = 1)
         DC    3F'0'
CTLGFLB2 DC    X'01',AL3(0),F'0'
         DC    A(REQUES03)              ENTNAME   (LTH = 44)
         DC    3F'0'
CTLGFLB3 DC    X'01',AL3(0),F'0'
         DC    A(REQUES04)              CATTR     (LTH = 1)
         DC    3F'0'
CTLGFLB4 DC    X'01',AL3(0),F'0'
         DC    A(REQUES05)              NAMEDS    (LTH = (4))
         DC    3F'0'
CTLGFLB5 DC    X'01',AL3(0),F'0'
         DC    A(REQUES06)              DSETEXDT  (LTH = 3)
         DC    3F'0'
CTLGFLB6 DC    X'01',AL3(0),F'0'
         DC    A(REQUES07)              DSETCRDT  (LTH = 3)
         DC    3F'0'
CTLGFLB7 DC    X'01',AL3(0),F'0'
         DC    A(REQUES08)              OWNERID   (LTH = 8)
         DC    3F'0'
CTLGFLB8 DC    X'01',AL3(0),F'0'
         DC    A(REQUES10)              CATVOL    (LTH = (15))
         DC    3F'0'
         EJECT
*        *---------------------------*
*        *   F I E L D   N A M E S   *
*        *---------------------------*
         SPACE
REQUES01 DC    CL8'MULTITYP'
REQUES02 DC    CL8'ENTYPE'              LTH = 1
REQUES03 DC    CL8'ENTNAME'             LTH = 44
REQUES04 DC    CL8'CATTR'               LTH = 1
REQUES05 DC    CL8'NAMEDS'              LTH = VL: (4) / ASSOC
REQUES06 DC    CL8'DSETEXDT'            LTH = 3
REQUES07 DC    CL8'DSETCRDT'            LTH = 3
REQUES08 DC    CL8'OWNERID'             LTH = 8
REQUES09 DC    CL8'CRAVOL'              LTH = 6
REQUES10 DC    CL8'CATVOL'              LTH = VL: (15) / VOL
         SPACE
REQUES11 DC    CL8'VOLDVCHR'            LTH = 20
REQUES12 DC    CL8'SPACPARM'            LTH = 7
REQUES13 DC    CL8'HURBADS'             LTH = 4
REQUES14 DC    CL8'HARBADS'             LTH = 4
REQUES15 DC    CL8'ENTVOL'              LTH = VL: (45+) / VOL
REQUES18 DC    CL8'SYSEXTDS'            LTH = 1
REQUES19 DC    CL8'NODSPACE'            LTH = 2
REQUES20 DC    CL8'NODSET'              LTH = 2
REQUES23 DC    CL8'DSPDSCRP'            LTH = VL: (13+SPACE MAP) / EXT
REQUES24 DC    CL8'PASSWORD'            LTH = 32
         SPACE
REQUES25 DC    CL8'AMDSBCAT'            LTH = 96
REQUES28 DC    CL8'LRECL'               LTH = 4
REQUES33 DC    CL8'CATACB'              LTH = 4
         END
