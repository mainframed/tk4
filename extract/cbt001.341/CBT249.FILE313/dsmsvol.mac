* SYSTEM NAME:        DSM - DASD SPACE MANAGEMENT SYSTEM
*
* PROGRAM NAME:       DSMSVOL
*
* PROGRAM TITLE:      DSM VOLUME SELECTION
*
* AUTHOR:             G. W. JACKSON - MEMOREX CORPORATION
*
* ABSTRACT:           THIS PROGRAM GENERATES THE SYSIN TO THE
*                     C.S.G.  $MONVOL PROGRAM, BASED ON THE
*                     CATEGORY OF PACK USE REQUESTED BY THE INPUT
*                     PARAMETER, THE DEFAULT BEING ALL PACKS.
*
* METHOD:             THE INPUTS TO THIS PROGRAM ARE A PARAMETER
*                     SPECIFYING THE TYPE OR TYPES OF PACK TO
*                     SELECT AND A FILE WHICH CONTAINS THE
*                     FOLLOWING DATA: PACK NAME POS 1-6, PACK
*                     CATEGORY POS 24-26.  THE PACK FILE CAN BE
*                     USED AS A GENERAL PACK INVENTORY LIST BY
*                     INCLUDING: DEVICE TYPE POS 8-15, PACK
*                     DESCRIPTION POS 27-68, AND RESPONSIBLE
*                     MANAGER'S INITIALS POS 69-71.  AN '*' IN
*                     POS 1 OF THE FILE DENOTES A COMMENT RECORD.
*
* PARMS:              PACK CATEGORY (ONE OR MORE)
*
* JCL REQUIRED:       //STEP  EXEC  PGM=DSMSVOL,PARM='ALL'
*                     //SYSPRINT DD  SYSOUT=A
*                     //DSMPACK  DD  << PACK FILE >>
*                     //DSMOUT1  DD  << OUTPUT SYSIN TO $MONVOL >>
*
* HISTORY:  13 MAY 78 - FIRST VERSION.
*
*            6 SEP 78 - MODIFICATIONS: CHANGE TO BYPASS COMMENT
*                       RECORDS ON PACK FILE; THIS COMMENT PAGE
*                       ADDED; LISTING CONTROL.
*
DSMSVOL  START 0
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
*        PRINT NOGEN
BEGIN    SAVE  (14,12)
         BALR  R12,0
         USING *,R12
         ST    R13,SAVE+4
         LA    R13,SAVE
         EJECT
***********************************************************************
*                                                                     *
*                  INITIALIZATION                                     *
*                                                                     *
***********************************************************************
         LR    R2,R1                   LOAD R1 TO R2
         OC    0(4,R2),0(R2)           TEST FOR MISSING PARM
         BNZ   LOADPARM                OK - LOAD PARAMETERS
         MVC   PARMLIST(3),=C'ALL'     NO PARM - DEFAULT TO ALL
         B     OPENFILE                BR TO OPEN FILES
LOADPARM L     R3,0(R2)                LOAD PTR TO PARM TO R3
         LH    R4,0(R3)                LOAD PARM LENGTH TO R4
         LA    R3,2(R3)                POINT R3 TO PARM TEXT
         LA    R9,PARMLIST             POINT R9 TO PARMLIST
         SR    R7,R7                   ZERO OUT R7
COMPPARM MVC   0(3,R9),0(R3)           MOVE FIRST PARM TO PARMLIST
         LA    R7,1(R7)                ADD 1 TO R7
         LA    R9,3(R9)                POINT TO NEXT PARM IN LIST
         LA    R3,4(R3)                BUMP UP PTR TO PARM
         SH    R4,=H'4'                SUBTRACT 4 FROM PARM LENGTH
         BM    ENDCOUNT                IF MINUS END MOVES
         B     COMPPARM                BR BACK FOR NEXT PARM
ENDCOUNT ST    R7,PARMCNT              SAVE COUNT OF PARMS
OPENFILE OPEN  (PACKFILE,INPUT,OUT1FILE,OUTPUT)
         EJECT
***********************************************************************
*                                                                     *
*                  MAIN FLOW                                          *
*                                                                     *
***********************************************************************
SELECT   GET   PACKFILE,PACKREC        READ INPUT FILE
         CLI   PACKREC+0,C'*'          COMMENT RECORD?
         BE    SELECT                  YES - BYPASS IT
         CLC   PARMLIST(3),=C'ALL'     SELECT ALL?
         BE    WRITEREC                YES - BYPASS SELECTION
         L     R8,PARMCNT              LOAD PARM COUNT INTO R8
         LA    R9,PARMLIST             POINT R9 AT HEAD OF PARMLIST
TESTPACK CLC   PACKTYPE,0(R9)          IS THIS PACK A SELECTED TYPE?
         BE    WRITEREC                YES - WRITE OUTPUT RECORD
         LA    R9,3(R9)                NO - TRY NEXT PARM IN LIST
         BCT   R8,TESTPACK             BR ON COUNT TO TEST PACK AGAIN
         B     SELECT                  NO HIT - READ NEXT PACK RECORD
WRITEREC MVC   OUTRECRD+1(6),PACKNAME  MOVE PACK NAME TO OUTPUT RECORD
         PUT   OUT1FILE,OUTRECRD       WRITE OUTPUT RECORD TO FILE
         B     SELECT                  READ NEXT PACK RECORD
         EJECT
***********************************************************************
*                                                                     *
*                  END OF JOB PROCESSING                              *
*                                                                     *
***********************************************************************
EOJRTN   CLOSE (PACKFILE,,OUT1FILE)
         L     R13,SAVE+4
         RETURN (14,12)
         EJECT
***********************************************************************
*                                                                     *
*                  DCB'S                                              *
*                                                                     *
***********************************************************************
PACKFILE DCB   DDNAME=DSMPACK,EODAD=EOJRTN,MACRF=GM,DSORG=PS
OUT1FILE DCB   DDNAME=DSMOUT1,MACRF=PM,DSORG=PS
         EJECT
***********************************************************************
*                                                                     *
*                  FILE DEFINITIONS                                   *
*                                                                     *
***********************************************************************
PACKREC  DS    0CL80
PACKNAME DS    CL6
         DS    CL10
PACKTRKS DS    CL6
         DS    CL1
PACKTYPE DS    CL3
         DS    CL54
         EJECT
***********************************************************************
*                                                                     *
*                  DATA DEFINITIONS                                   *
*                                                                     *
***********************************************************************
SAVE     DS    18F                     SAVE REGISTERS
PARMCNT  DC    F'0'
PARMLIST DC    CL100' '
OUTRECRD DC    CL80' '
         END   BEGIN
