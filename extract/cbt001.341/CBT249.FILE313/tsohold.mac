         TITLE 'TSOHOLD - TEMPORARILY SUSPENDS TSO LOGONS'
         SPACE 3
***********************************************************************
*                                                                     *
*                                                                     *
*    TSOHOLD  IS A PROGRAM STARTED BY THE OPERATORS TO TEMMPORARILY   *
*    SUSPEND TSO LOGONS. IT ISSUES AN ENQ FOR EXCLUSIVE USE OF A      *
*    RESOURCE WITH A QNAME BOCTSTOP WITH AN RNAME CONSISTING OF A     *
*    MESSAGE TO BE DISPLAYED TO A USER ATTEMPTING TO LOG ON.          *
*    THIS MESSAGE CAN BE SUPPLIED BY THE OPERATOR ON THE START        *
*    COMMAND. IF NO MESSAGE IS SUPPLIED, A DEFAULT MESSAGE IS         *
*    USED. AFTER ISSUING THE ENQ, TSOHOLD  NOTIFIES THE OPERATOR      *
*    AND WAITS FOR A REPLY. AS SOON AS THE OPERATOR RESPONDS,         *
*    TSOHOLD  ISSUES A DEQ AND TERMINATES.                            *
*    TSOHOLD  OPERATES IN CONJUNCTION WITH THE INSTALLATION LOGON     *
*    EXIT, IKJEFLD. IT IS IKJEFLD WHICH ACTUALLY INTERCEPTS THE       *
*    LOGONS AND DISPLAYS THE MESSAGE. ENQ/DEQ IS USED AS A MEANS OF   *
*    CROSS-MEMORY COMMUNICATION.                                      *
*                                                                     *
*                                                                     *
***********************************************************************
         EJECT
*
*** ABSOLUTE REGISTER DESIGNATION
*
R1       EQU   1
R2       EQU   2
R3       EQU   3
R12      EQU   12
R14      EQU   14
R15      EQU   15
         SPACE 3
TSOHOLD  CSECT
         SAVE  (14,12),,*     SAVE REGISTERS AND PROVIDE AN ID
         LR    R12,R15        LOAD BASE REGISTER WITH EP
         USING TSOHOLD,R12   AND SET UP ADDRESSABILTY
         L     R1,0(R1)       LOAD PARM ADDRESS
         LH    R2,0(R1)       LOAD LENGTH OF PARM
         LTR   R2,R2          AND TEST FOR ZERO
         BZ    ENQ            IF ZERO, USE DEFAULT ENQ PLIST
         STC   R2,RNAME       ELSE STORE RNAME LENGTH IN ENQ PL
         BCTR  R2,0           GET MACHINE LEN
         EX    R2,MOVE
ENQ      ENQ   MF=(E,ENQPLIST) ISSUE E FORM ENQ
         WTO   '*** TSOHOLD  STARTED. TSO LOGONS NOW SUSPENDED ***'
         WTOR  '*** TSOHOLD  .. ENTER ANY CHARACTER TO TERMINATE ***', X
               REPLYBUF,10,ECB
         WAIT  ECB=ECB,LONG=YES
         DEQ   MF=(E,ENQPLIST) ISSUE E FORM DEQ
         WTO   '*** TSOHOLD  TERMINATING. LOGONS ARE NOW ENABLED ***'
         RETURN (14,12),RC=0  RETURN
         SPACE 3
ENQPLIST ENQ   (QNAME,RNAME,E,0,SYSTEM),MF=L LIST FORM OF ENQ/DEQ
QNAME    DC    C'BOCTSTOP'    ENQ QNAME
RNAME    DS    CL256
         ORG   RNAME
         DC    AL1(RNAMEL)
         DC    C'TSO HAS BEEN TEMPORARILY HALTED. '
         DC    C'WOULD YOU PLEASE TRY AGAIN LATER'
RNAMEL   EQU   *-RNAME
         ORG
ECB      DC    A(0)
REPLYBUF DS    CL10
MOVE     MVC   RNAME+1(0),2(1)
         END
