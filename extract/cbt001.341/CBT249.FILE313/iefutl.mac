IEFUTL TITLE 'IEFUTL -- USER CPU AND WAIT TIME LIMIT EXIT'
***********************************************************************
*                                                                     *
*             MODULE NAME = IEFUTL                                    *
*                                                                     *
*             DESCRIPTIVE NAME =  USER TIME LIMIT EXIT                *
*                                                                     *
*             STATUS = 01/04/81 JFL                                   *
*                                                                     *
*             FUNCTION =                                              *
*                ALLOW JOBS TO BE CANCELLED IF JOB (STEP) TIME        *
*                   LIMIT EXCEEDED.                                   *
*                ALLOW JOBS TO HAVE 30 MORE MINUTES OF WAIT TIME      *
*                   IF WAIT TIME LIMIT EXCEEDED.                      *
*                ALLOW TSO SESSIONS TO BE CANCELLED IF WAIT TIME      *
*                   LIMIT EXCEEDED.                                   *
*                                                                     *
*                OPERATION =                                          *
*                   ANALYZE SMF COMMON EXIT PARAMETER AREA.           *
*                   IF WAIT TIME LIMIT EXCEEDED SEE IF JOB.  NO,      *
*                      CANCEL THE TSO SESSION.  IF JOB NOTIFY         *
*                      OPERATOR THAT A 30 MINUTE EXTENSION WILL       *
*                      BE GRANTED.  RETURN TO CALLER WITH A RETURN    *
*                      CODE OF 8 IN REGISTER 15 AND THE NUMBER        *
*                      OF SECONDS (1800) OF WAIT TIME EXTENSION IN    *
*                      REGISTER 1.                                    *
*                                                                     *
*                   IF JOB OR STEP TIME LIMIT IS EXCEEDED CANCEL      *
*                      THE JOB WITH RETURN CODE 0 IN R15.         .   *
*                                                                     *
*             NOTES =                                                 *
*                                                                     *
*                REGISTER CONVENTIONS = STANDARD CONVENTIONS.         *
*                   REGISTERS 0 TO 3  = WORK REGISTERS                *
*                   REGISTERS 4 TO 9  = UNUSED                        *
*                   REGISTER  10      = ADDRESS OF GETMAINED AREA     *
*                   REGISTER  11      = ADDRESSABILITY TO COMMON      *
*                                       EXIT PARAMETER AREA           *
*                   REGISTER  12      = ADDRESSABILITY TO IEFUTL      *
*                                       CSECT                         *
*                   REGISTER  13      = SAVE AREA REGISTER            *
*                   REGISTER  14      = RETURN ADDRESS                *
*                   REGISTER  15      = RETURN CODE                   *
*                                                                     *
*                PATCH LABEL = PATCH (DC 8F'0')                       *
*                                                                     *
*                ATTRIBUTES = SCHEDULER KEY 0, REENTRANT,             *
*                   SUPERVISOR STATE, ENABLED                         *
*                                                                     *
       EJECT
*             ENTRY POINTS = IEFUTL (ONLY ENTRY POINT)                *
*                                                                     *
*                LINKAGE =                                            *
*                   FROM INITIATOR VIA MODULE IEATLEXT                *
*                                                                     *
*             INPUT = REGISTER 1 POINTS TO A LIST OF FULL WORDS       *
*                THE FIRST OF WHICH POINTS TO THE SMF COMMON          *
*                EXIT PARAMETER AREA MAPPED BY SMFCEPA DSECT          *
*                                                                     *
*             OUTPUT = REGISTER 15 - RETURN CODE                      *
*                REGISTER 1 - TIME LIMIT EXTENSION                    *
*                                                                     *
*             EXIT - NORMAL = AT PROGRAM END VIA BRANCH REGISTER 14   *
*                                                                     *
*                OUTPUT = WTO IF WAIT TIME EXTENDED                   *
*                         TPUT IF TSO SESSION CANCELLED               *
*                                                                     *
*                RETURN CODE = SEE FUNCTION ABOVE                     *
*                                                                     *
*                                                                     *
*             MACROS = SAVE, TPUT                                     *
*                                                                     *
*             CHANGE ACTIVITY = NONE                                  *
*                                                                     *
*             MESSAGES =                                              *
*                                                                     *
*                THE MESSAGES ISSUED BY THE PROGRAM ARE FOUND AT      *
*                   THE LABELS MESSAGEC, MESSAGEW, AND WTOC.          *
*                                                                     *
***********************************************************************
IEFUTL   CSECT
         SAVE  (14,12),,IEFUTL.&SYSTIME_&SYSDATE SAVE REGISTERS
         LR    R12,R15                 LOAD REGISTER 12 FROM 15
         USING IEFUTL,R12              ESTABLISH ADDRESSABILITY TO
*                                      PROGRAM CSECT
         TITLE 'EVAL ENTRY CODE ... HANDLE WAIT TIME EXCEEDED'
***********************************************************************
*                                                                     *
*        ESTABLISH ADDRESSABILITY TO SMF PARAMETER AREA DSECT         *
*        EVALUATE REGISTER 0 = 0 - JOB CPU TIME EXCEEDED              *
*                            = 4 - STEP CPU TIME EXCEEDED             *
*                            = 8 - JOB WAIT TIME EXCEEDED             *
*                                                                     *
***********************************************************************
*
         L     R11,D0(R1)              LOAD POINTER TO PARAMETER AREA
         USING SMFEPLA,R11             ESTABLISH ADDRESSABILITY TO SMF
*                                      PARAMETER AREA DSECT
         C     R0,FULL8                SEE IF WAIT TIME EXCEEDED
         BNE   CANCEL                  NO.....GO CANCEL
*
*
***********************************************************************
*                                                                     *
*        WAIT TIME EXCEEDED                                           *
*           - ALLOW TSO SESSION TO BE CANCELLED                       *
*           - ALLOW JOB A 30 MINUTE EXTENSION AND NOTIFY OPERATOR     *
*                                                                     *
***********************************************************************
*
WAITTIME TM    SMFOPT,SMFTSO           IS IT TSO BIT OFF ?
         BNO   BATCHJOB                YIP - THEN BATCH JOB
         CLC   SMFJOBN(4),=C'$SYS'     SYSTEMS PROGRAMMER?
         BE    EXTEND30                BIF YES - EXTEND THE TIME
         CLC   SMFJOBN(4),=C'$OPS'     OPERATIONS?
         BE    EXTEND30                BIF YES - EXTEND THE TIME
         CLC   SMFJOBN(4),=C'$USS'     USER ASSISTENCE?
         BE    EXTEND30                BIF YES - EXTEND THE TIME
         L     R1,16                   GET CVT ADDRESS
         L     R1,0(R1)                GET ADDRESS OF 4 FULLWORDS
         L     R1,12(R1)               GET ASCB ADDRESS
         L     R1,60(R1)               GET TSB ADDRESS
         CLI   12(R1),X'00'            CHECK NUMBER OF OUTPUT BUFFERS
         BNZ   EXTEND30                IF OUTPUT WAITING - EXTEND
         TPUT  MESSAGEC,L'MESSAGEC     NO - TELL USER BEING CANCELLED
         B     CANCEL                  AND GO CANCEL
         TITLE 'EXTEND 30 MINUTES'
***********************************************************************
*                                                                     *
*        ALLOW JOB A 30 MINUTE EXTENSION AND NOTIFY OPERATOR          *
*                                                                     *
***********************************************************************
*                                                                     *
*        ROUTCDE=2 WAS USED SO THAT ENQ RET=TEST WAS UNNECESSARY      *
*        ENQ RET=TEST IS NECESSARY FOR ROUTCDE=11 (WTP) WHETHER       *
*           OR NOT THAT ROUTING OCCURS BY DEFAULT                     *
*        WTP CAUSES AN ENQ OF THE FOLLOWING FORM:                     *
*           MAJORNAME: SYSSJWTP                                       *
*           MINORNAME: RPL + ASID (LENGTH OF 6)                       *
*              RPL IS THE 4 BYTE REQUEST PARAMETER LIST POINTER       *
*                 LOCATED AT OFFSET JSCBSMLR (X'128') IN THE JSCB     *
*              ASID IS THE 2 BYTE ADDRESS SPACE IDENTIFIER            *
*                 LOCATED AT OFFSET ASCBASID (X'24') IN THE ASCB      *
*                                                                     *
***********************************************************************
*
BATCHJOB GETMAIN R,LV=WTOL,SP=241      GETMAIN AREA FOR WTO DATA
         LR    R10,R1                  SAVE ADDRESS OF GETMAINED AREA
         MVC   D0(WTOL,R1),WTOC        MOVE WTOC CONSTANT INTO WORKAREA
         MVC   D13+4(L'SMFJOBN,R1),SMFJOBN  MOVE JOBNAME INTO WTO
         SVC   WTOSVC                  ISSUE WTO SVC
         FREEMAIN R,LV=WTOL,A=(R10),SP=241
*                                      FREEMAIN AREA FOR WTO DATA
EXTEND30 L     R2,FULL8                INDICATE RETURN CODE OF 8 -
*                                      EXTEND CPU TIME IN SECONDS
         L     R1,WAIT1800             LOAD TIME EXTENSION IN SECONDS
*                                      INTO REGISTER 1
         B     RETURN                  GO RETURN TO CALLER
         TITLE 'SET CANCEL RETCODE FOR CPU OR TSO WAIT'
***********************************************************************
*                                                                     *
*        ZERO REGISTER 2 TO INDICATE CANCEL                           *
*                                                                     *
***********************************************************************
*
CANCEL   DS    0H
         SLR   R2,R2                   INDICATE RETURN CODE OF ZERO -
*                                      CONTINUE (CANCEL)
*
***********************************************************************
*                                                                     *
*        NORMAL END PROCESSING                                        *
*                                                                     *
***********************************************************************
*
RETURN   DS    0H
         L     R14,D12(,R13)           LOAD ADDRESS FOR RETURN
         LR    R15,R2                  LOAD RETURN CODE FROM REGISTER 2
         LM    R2,R12,D28(R13)         RESTORE REGISTERS 2 TO 12
         BR    R14                     RETURN TO CALLER
         TITLE 'CONSTANTS, MESSAGES, AND MACROS'
**************************** CONSTANTS ********************************
FULL4    DC    F'4'
FULL8    DC    F'8'
WAIT1800 DC    F'1800'
         SPACE 2
****************************** MESSAGES *******************************
MESSAGEC DC    C'IEFUTL - SESSION CANCELLED DUE TO TERMINAL INACTIVITY'
*
WTOC     WTO   'IEFUTL - JOB XXXXXXXX TIMED OUT - EXTENDED 30 MINUTES',X
               ROUTCDE=(2),DESC=(6),MF=L
WTOL     EQU   *-WTOC
*
         SPACE 2
***************************** PATCH AREA ******************************
         DS    0F
PATCH    DC    8F'0'
         EJECT
************************ DSECTS (MAPPING MACROS) **********************
SMFEPLA  DSECT
SMFJOBN  DS    CL8
SMFTIME  DS    CL4
SMFDATE  DS    CL4
SMFSID   DS    CL4
SMFUSID  DS    CL8
SMFSTEP  DS    CL1
SMFOPT   DS    CL1
SMFTSO   EQU   X'01'
SMFVS1   DS    CL1
SMFRESV  DS    CL1
SMFUSER  DS    CL4
         ORG   SMFUSER
SMFUSERC DS    CL1
SMFUSERW DS    CL1
         ORG
         SPACE 5
********************************* EQUATES *****************************
D0       EQU   0
D12      EQU   12
D13      EQU   13
D28      EQU   28
ONE      EQU   1
WTOSVC   EQU   35
ZERO     EQU   0
         SPACE 5
***************************** REGISTER EQUATES ************************
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END   IEFUTL
