IEFUJI   CSECT
*-------------------------------------------------------------*
*                                                             *
*  CSECT NAME : IEFUJI            MVS/SP1                     *
*  MOD LEVEL  : 12/01/80          JFL                         *
*                                                             *
*  FUNCTION   : ENFORCE THE JOBCLASS/TIME STANDARDS :         *
*                JOBCLASS B GETS UP TO 7 MINS CPU TIME        *
*                JOBCLASS C GETS UP TO 3 MINS CPU TIME        *
*                JOBCLASS D GETS UP TO 1 MIN  CPU TIME        *
*               FOR ANY INVALID JOBCLASS/TIME PAIRS, THE      *
*               TIME FIELD IS MODIFIED TO CONFORM WITH THE    *
*               ABOVE STANDARDS.                              *
*               ***********************************************
*               *** N O T E ***   THIS FUNCTION IS DEPENDENT  *
*                UPON THE CALLING MODULE (IEFSMFIE) LEAVING   *
*                THE ADDRESS OF THE JCT IN R7 (SE1) OR        *
*                R9 (SP1).  FURTHERMORE, SINCE THE JCT IS     *
*                MAINTAINED ON DISK, IF THE JCT IS NOT WRITTEN*
*                TO DISK BY A LATER ROUTINE, THE TIME FIELD   *
*                MODIFICATION WOULD BE LOST.                  *
*               ***********************************************
*                                                             *
*               VALIDATE JOBNAME PREFIX AGAINST LOADED TABLE  *
*               IEFUJITB.  COMPARE 5 CHARACTERS IF THE PREFIX *
*               IS A USERID ( FIRST CHARACTER OF JOBNAME IS   *
*               '$', '@', OR '#' ), ELSE COMPARE 4 CHARACTERS *
*               ( ASSUMED TO BE A MEMOREX PROJECT ID ).       *
*                                                             *
*               SYSTEMS PROGRAMMING REQUESTED A BYPASS -- THE *
*               CHARACTERS '$SYS' AS THE SECOND ACCOUNTING    *
*               SUB-FIELD.                                    *
*                                                             *
*  MACRO'S    : GETMAIN, FREEMAIN, WTO, LOAD, DELETE.         *
*                                                             *
*  INPUT      : R1 -> LIST OF 4-BYTE ADDRESSES :              *
*                                                             *
*               +0  A(SMF COMMON EXIT PARM AREA)              *
*               +4  A(20-BYTE PROGRAMMER NAME AREA)           *
*               +8  A(1-BYTE JOB SELECTION PRIORITY)          *
*               +C  A(ACCOUNTING INFORMATION)                 *
*                                                             *
*               THE ACCOUNTING INFORMATION HAS THE            *
*               FOLLOWING FORMAT :                            *
*               +0  NUMBER OF ACCOUNTING FIELDS ( XL1 )       *
*               +1  ACCOUNTING FIELDS                         *
*                                                             *
*               EACH ACCOUNTING FIELD HAS THE FOLLOWING       *
*               FORMAT :                                      *
*               +0  LENGTH OF THE FIELD  ( XL1 )              *
*                   A 0 => OMITTED FIELD                      *
*               +1  EBCDIC FIELD                              *
*                                                             *
*                                                             *
*  REGISTER USAGE:                                            *
*               R0       WORK                                 *
*               R1       WORK                                 *
*               R2       WORK                                 *
*               R3       A(JCT)                               *
*               R4       SEARCH PARM POINTER                  *
*               R5       SUBROUTINE LINKAGE                   *
*               R6       HOLDS SEARCH ARG LENGTH              *
*               R7       ID TABLE POINTER                     *
*               R8       INPARM 1  -- A(SMF PARM AREA)        *
*               R9       INPARM 2  -- A(PGMR NAME)            *
*               R10      INPARM 3  -- A(PRIO BYTE)            *
*               R11      INPARM 4  -- A(ACCTG AREA)           *
*               R12      BASEREG                              *
*               R13      SA POINTER                           *
*               R14      RETREG                               *
*               R15      RETCODE                              *
*                                                             *
*  OUTPUT     : IF PREFIX FOUND IN TABLE ( OR SYSTEM          *
*               BYPASS USED ) -- R15=0                        *
*                                                             *
*               IF PREFIX NOT IN TABLE :                      *
*                -- R15=4 TO FAIL JOB                         *
*                -- WTO, WTP REASON FOR JOBFAIL               *
*                                                             *
*                                                             *
*-------------------------------------------------------------*
         SPACE 1
         B     20(,R15)
         DC    CL8'IEFUJI'
         DC    CL8'&SYSDATE'
         STM   R14,R12,12(R13)
         LR    R12,R15
         USING IEFUJI,R12
         LR    R3,R9                   ->JCT (R7 FOR SE1, R9 FOR SP1)
         LM    R8,R11,0(R1)            LOAD PARM REGS
         SPACE 1
*-------------------------------------------------------------*
*     ENFORCE THE CLASS/TIME STANDARDS : B->7 MINS, C->3 MINS *
*     D-> 1 MIN.                                              *
*-------------------------------------------------------------*
         BAL   R5,CHKTIME              GO DO JOBCLASS/TIME CHECK
         SPACE 1
*-------------------------------------------------------------*
*  1. LOAD THE USERID/PROJECT NUMBER TABLE IEFUJITB           *
*     IF THE LOAD FAILS, ABEND.                               *
*-------------------------------------------------------------*
         SPACE 1
         LOAD  EP=IEFUJITB
         LR    R7,R0                   SET TABLE POINTER
         EJECT
*-------------------------------------------------------------*
*  1. SET THE COMPARE LENGTH TO 4 OR 5 BASED UPON FIRST       *
*     CHARACTER OF THE JOBNAME.                               *
*  2. CALL THE TABLE SEARCH ROUTINE                           *
*  3. BRANCH BASED UPON RETURN CODE                           *
*-------------------------------------------------------------*
         LA    R6,5                    ASSUME LENGTH IS 5
         CLI   0(R8),C'$'              IF NOT $
         BE    SRCHJOB                 THEN
         CLI   0(R8),C'#'              IF NOT #
         BE    SRCHJOB                 THEN
         CLI   0(R8),C'@'              IF NOT @
         BE    SRCHJOB                 THEN
         LA    R6,4                    ASSUME LENGTH IS 4
SRCHJOB  DS    0H
         LR    R4,R8                   SET SEARCH PARM POINTER
         SLR   R15,R15                 ASSUME ID WILL BE FOUND
         ST    R15,16(R13)             PLACE IN R15 SAVE AREA
         BAL   R5,SEARCH               GO SEARCH TABLE
         B     IDFOUND                 RETURN IF FOUND
SRCHACT  DS    0H                      RETURN IF NOT FOUND
         BAL   R5,ACT#2                SET R4 -> 2ND ACCT PARM
         B     IDBAD                   ERROR RETURN
         CLC   0(4,R4),=C'$SYS'        IF NOT BYPASS
         BE    IDFOUND                 THEN
         LA    R6,4                    LENGTH NOW IS 4
         BAL   R5,SEARCH               GO SEARCH TABLE
         B     IDFOUND                 RETURN IF FOUND
IDBAD    DS    0H                      RETURN IF NOT FOUND
         EJECT
*-------------------------------------------------------------*
*  COME HERE IF NEITHER JOBNAME PREFIX NOR ACCT. PARM FOUND   *
*  1. GET A WORK AREA FOR THE WTO                             *
*  2. SET UP THE WORK AREA AND DO THE WTO                     *
*  3. FREE THE WORK AREA                                      *
*  4. SET THE JOB CANCEL RETURN CODE                          *
*-------------------------------------------------------------*
         GETMAIN R,LV=WTOLEN,SP=255
         MVC   0(WTOLEN,R1),WTODATA    MOVE IN WTO MSG AND CNTL
         MVC   WTONAME(8,R1),0(R8)     MOVE IN JOBNAME
         LR    R2,R1                   SAVE AREA ADDRESS
         SVC   35                      WTO AND WTP
         LR    R1,R2                   RESTORE AREA ADDRESS
         FREEMAIN R,A=(1),LV=WTOLEN,SP=255
         LA    R15,4                   SET CANCEL RETURN CODE
         ST    R15,16(R13)             PLACE IN R15 SAVE AREA
*-------------------------------------------------------------*
*  COME HERE REGARDLESS OF WHETHER SEARCH GOT A HIT           *
*  1. DELETE THE ID TABLE                                     *
*  2. RETURN                                                  *
*-------------------------------------------------------------*
IDFOUND  DS    0H
         DELETE EP=IEFUJITB
RETURN   LM    R14,R12,12(R13)
         BR    14
         EJECT
ACT#2    DS    0H
*-------------------------------------------------------------*
*                                                             *
*  THIS ROUTINE LOCATES AND CHECKS THE SECOND ACCONTING FIELD *
*  FOR A REASONABLE 4 CHARACTER PROJECT ID.                   *
*                                                             *
*  INPUT   : R11 = A( ACCOUNTING INFO)                        *
*                                                             *
*  OUTPUT  : IF THERE IS A SECOND PARM AND IT'S LENGTH IS 4   *
*            THEN SET R4 = A(PARM) AND RETURN 4(R5)           *
*                                                             *
*            IF ANYTHING IS WRONG, RETURN 0(R5)               *
*                                                             *
*-------------------------------------------------------------*
         CLI   0(R11),X'2'            IF 2 OR MORE PARMS
         BL    NOACT                  THEN
         SLR   R1,R1                  CLEAR R1 FOR IC
         IC    R1,1(R11)              LEN OF FIRST FIELD
         LA    R11,2(R1,R11)          BUMP PAST IT
         CLI   0(R11),X'4'            IF FIELD 4 CHARS LONG
         BNE   NOACT                  THEN
         LA    R4,1(R11)              USE IT
         B     4(R5)                  GOOD RETURN
NOACT    BR    R5                     ERROR RETURN
         EJECT
SEARCH   DS    0H
*-------------------------------------------------------------*
*                                                             *
*  THE SEARCH ROUTINE ATTEMPTS TO FIND A GIVEN ID IN THE      *
*  LOADED IEFUJITB TABLE.                                     *
*                                                             *
*  INPUT   : R4 = A(SEARCH ARGUMENT)                          *
*            R6 = LENGTH OF SEARCH ARGUMENT                   *
*            R7 = A(TABLE)                                    *
*                                                             *
*  OUTPUT  : IF ID FOUND IN TABLE RETURN 0(R5)                *
*            IF ID NOT IN TABLE   RETURN 4(R5)                *
*                                                             *
*-------------------------------------------------------------*
         LA     R1,1500                LOOP CONTROL ... TO BE SURE
         BCTR   R6,0                   DECREMENT FOR EXECUTE
         LR     R2,R7                  USE WORK REG FOR TABLE SCAN
TESTEND  CLI    0(R2),C'0'             IF NOT AT END OF TABLE
         BE     MISS                   THEN
         EX     R6,COMPARE             COMPARE
         BE     HIT
         BH     MISS                   ASSUMES ASCENDING TABLE
         LA     R2,5(,R2)              BUMP TO NEXT ENTRY
         BCT    R1,TESTEND             JUST IN CASE
MISS     B      4(R5)                  NOT FOUND RETURN
HIT      BR     R5                     FOUND RETURN
COMPARE  CLC    0(*-*,R2),0(R4)        TABLE VS ARGUMENT
         EJECT
CHKTIME  DS    0H
*-------------------------------------------------------------*
*                                                             *
*  THIS ROUTINE ENFORCES THE JOBCLASS/TIME STANDARDS :        *
*    JOBCLASS B IS ALLOWED UP TO 7 MINUTES CPU TIME           *
*    JOBCLASS C IS ALLOWED UP TO 3 MINUTES CPU TIME           *
*    JOBCLASS D IS ALLOWED UP TO 1 MINUTE  CPU TIME           *
*                                                             *
*  INPUT   : R8  = A( SMF COMMON EXIT PARAMETER AREA)         *
*            R3  = A( JCT )                                   *
*                                                             *
*  FUNCTION: THE JCT CONTAINS THE JMR, WHICH HAS THE CPU TIME *
*            FOR THE JOB IN FIELD JCTJMRTL.  THE TIME IS IN   *
*            HUNDREDTHS OF A SECOND.  THE SMF COMMON EXIT     *
*            PARAMETER AREA HAS THE JOBCLASS.  JOBS ARE NOT   *
*            CANCELLED FOR VIOLATING THIS STANDARD;  THE      *
*            JCTJMRTL FIELD IS MODIFIED TO CONFORM TO THE     *
*            STANDARDS.  THE PROGRAMMER IS NOT INFORMED.      *
*                                                             *
*  OUTPUT  : POSSIBLY A MODIFIED JCTJMRTL FIELD.              *
*                                                             *
*-------------------------------------------------------------*
         SLR    R1,R1                  CLEAR FOR ICM
         ICM    R1,7,JCTJMRTL(R3)      R1 = JMR TIME LIMIT
*
CLB      CLI    JOBCLASS(R8),C'B'      IF JOBCLASS = B
         BNE    CLC                    THEN
         C      R1,SEVNMINS              IF TIME > 7 MINS
         BNH    CHKT                     THEN
         L      R1,SEVNMINS                USE 7 MINS
         STCM   R1,7,JCTJMRTL(R3)          FOR TIME
         B      CHKT                     END
*                                      ELSE
CLC      CLI    JOBCLASS(R8),C'C'      IF JOBCLASS = C
         BNE    CLD                    THEN
         C      R1,THREMINS              IF TIME > 3 MINS
         BNH    CHKT                     THEN
         L      R1,THREMINS                USE 3 MINS
         STCM   R1,7,JCTJMRTL(R3)          FOR TIME
         B      CHKT                     END
*                                      ELSE
CLD      CLI    JOBCLASS(R8),C'D'      IF JOBCLASS = D
         BNE    CHKT                   THEN
         C      R1,ONEMIN                IF TIME > 1 MIN
         BNH    CHKT                     THEN
         L      R1,ONEMIN                  USE 1 MIN
         STCM   R1,7,JCTJMRTL(R3)          FOR TIME
*                                        END
CHKT     BR     R5                     ELSE
         EJECT
*-------------------------------------------------------------*
*  THE FOLLOWING WTO IS MOVED INTO THE GOTTEN WORK AREA AND   *
*  MODIFIED WITH THE JOBNAME BEING FAILED .                   *
*-------------------------------------------------------------*
WTONAME  EQU    16                     OFFSET TO WTO JOBNAME
WTODATA  WTO    'MRX100I ==> XXXXXXXX FAILED MRX VALIDATION CHECKS     X
                ',ROUTCDE=(2),DESC=(3),MF=L
WTOLEN   EQU    *-WTODATA
*-------------------------------------------------------------*
*  CONSTANTS FOR THE CHKTIME ROUTINE                          *
*-------------------------------------------------------------*
JCTJMRTL EQU    140                    OFFSET TO TIME IN JCT
JOBCLASS EQU    31                     OFFSET TO JOBCLASS IN SMF AREA
         DS     0F                     TO BE SURE THE LOADS WORK
SEVNMINS DC     A(7*60*100)            7 MINS IN 100THS OF A SEC
THREMINS DC     A(3*60*100)            3 MINS IN 100THS OF A SEC
ONEMIN   DC     A(1*60*100)            1 MIN  IN 100THS OF A SEC
*-------------------------------------------------------------*
*  STANDARD REGISTER EQUATES                                  *
*-------------------------------------------------------------*
R0       EQU    0
R1       EQU    1
R2       EQU    2
R3       EQU    3
R4       EQU    4
R5       EQU    5
R6       EQU    6
R7       EQU    7
R8       EQU    8
R9       EQU    9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END
