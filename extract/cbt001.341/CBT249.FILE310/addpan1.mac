*          DATA SET PANAEXIT5  AT LEVEL 001 AS OF 01/24/77
         PRINT NOGEN
PANEXIT  CSECT
         ESTAB
         USING AIOAREA,R9
         USING AIOCODE,R10
         USING ADIRECT,R11
         LM    R9,R11,0(R1)
         CLI   IOCODE,C'3'
         BC    8,PRINT
         CLI   IOCODE,C'1'
         BC    7,EXIT
         CLI   FLAG,X'FF'
         BC    8,EXIT
FIRST    EQU   *
         TGET  BUFFER,80
         C     R15,=F'0'
         BC    7,ERROR
         C     R1,=F'0'
         BC    8,ERROR1
         LA    R1,12(R1)
         MVI   IOAREA,C' '
         MVC   IOAREA+1(79),IOAREA
         OC    BUFFER(40),MASK
         ST    R1,INLEN
         LA    R1,1(R1)
         LA    R2,BUFFER-1
         LA    R3,BUILDER
         MVC   BUILDER,BUILDER-1
         SR    R7,R7
         LA    R8,TABLE
         LA    R6,ERROR1
LOOP1    EQU   *
         LA    R2,1(R2)
         CLI   0(R2),C' '
         BC    7,NAME
         BCT   R1,LOOP1
         BC    15,ERROR1
NAME     EQU   *
         ST    R2,BGNAME
         BCT   R1,LOOP2
         BC    15,ERROR1
LOOP2    EQU   *
         LA    R2,1(R2)
         CLI   0(R2),C' '
         BC    8,ENDNAME
         BCT   R1,LOOP2
         BCR   15,R6
ENDNAME  EQU   *
         LR    R4,R2
         S     R4,BGNAME
         SR    R2,R4
         BCTR  R4,R0
         EX    R4,MOVE
         LA    R4,1(R4)
         AR    R3,R4
         AR    R2,R4
         BC    15,0(R7,R8)
COMMA    EQU   *
         MVI   0(R3),C','
         LA    R7,4(R7)
         LA    R3,1(R3)
         BC    15,LOOP1
TSO      EQU   *
         SR    R2,R4
         BCTR  R4,R0
         EX    R4,COMPARE
         BC    8,NOTSO
         EX    R4,COMPARO
         BC    8,NOTSO
         MVC   0(5,R3),=C',TSO,'
         LA    R3,5(R3)
         BC    15,TSOEND
NOTSO    EQU   *
         MVI   0(R3),C','
         LA    R3,1(R3)
TSOEND   EQU   *
         LA    R4,1(R4)
         AR    R2,R4
         LA    R7,4(R7)
         LA    R6,ENDNAME
         BC    15,LOOP1
END      EQU   *
         MVC   IOAREA(41),CARD
         MVI   IOCODE,C'A'
         MVI   FLAG,X'FF'
         BC    15,EXIT
TABLE    EQU   *
         BC    15,COMMA
         BC    15,TSO
         BC    15,END
EXIT     EXIT
PRINT    EQU   *
         MVC   MESS3,IOAREA
         TPUT  MESS3,50
         MVI   IOCODE,C'F'
         BC    15,EXIT
ERROR    EQU   *
         TPUT  MESS1,28
         MVI   IOCODE,C'G'
         BC    15,EXIT
ERROR1   EQU   *
         TPUT  MESS2,27
         MVI   IOCODE,C'G'
         BC    15,EXIT
         EJECT
         LTORG
INLEN    DS    F
BGNAME   DS    F
MOVE     MVC   0(0,R3),0(R2)
COMPARE  CLC   0(0,R2),DATA
COMPARO  CLC   0(0,R2),OBJ
MASK     DC    X'40404040404040404040'
         DC    X'40404040404040404040'
         DC    X'40404040404040404040'
         DC    X'40404040404040404040'
FLAG     DC    X'00'
CARD     DC    C'++ADD '
BUILDER  DS    CL35
BUFFER   DS    CL80
MESS1    DC    C'I/O ERROR IN SERVICE ROUTINE'
MESS2    DC    C'PANVALET PARAMETERS MISSING'
MESS3    DS    CL50
DATA     DC    CL10'DATA'
OBJ      DC    CL10'OBJECT'
         DEFINE  CODE=NO
AIOCODE  DSECT
IOCODE   DS    C
PROGRAM  DS    C
RETURN   DS    C
ADIRECT  DSECT
DIRENT   DS    CL80
AIOAREA  DSECT
IOAREA   DS    CL121
         END   PANEXIT
