        /* DATA SET JOVBPAN    AT LEVEL 001 AS OF 04/06/82    */
        /* DATA SET JOVBPAN    AT LEVEL 011 AS OF 03/11/81    */
        /* DATA SET VBPAN35    AT LEVEL 008 AS OF 12/31/79    */
 /*   PROGRAM TO CONVERT VSBASIC DATASETS TO 80 BYTES FIXED */
 /*   PROGRAMS SO CONVERTED WITH VBPAN MUST BE CONVERTED BACK WITH
      THE PROGRAM PANVB: CONVERSION TYPE IS IN THE INPUT PARM FIELD.
      METHOD:  READ THE VARIABLE LENGTH RECORD IN.
               WRITE IT OUT AS A FIXED LENGTH RECORD,
               80 BYTES AT A TIME.  IN THE FIRST 5 BYTES OF
               THE FIRST RECORD OF A GROUP, WRITE OUT THE LRECL.
               IN BYTES 70-71 OF EACH RECORD, WRITE OUT THE RECORD
               SEQUENCE NUMBER.    */
  VBPAN:  PROCEDURE(PARM)  OPTIONS(MAIN) ;
  DECLARE IN CHARACTER(1200) VARYING STATIC INIT('1'),
       INOUT CHARACTER(1200) VARYING STATIC ,
         SEQNO BIN FIXED(15,0) INIT(1),
         LISTREC CHARACTER(133),
       LIST1 CHARACTER(80) DEFINED LISTREC POSITION(1),
       LIST2 CHARACTER(53) DEFINED LISTREC POSITION(81),
       LIST1A CHARACTER(2) DEFINED LISTREC POSITION(1),
       LIST2A CHARACTER(2) DEFINED LISTREC POSITION(81),
       LIST3  CHARACTER(80),
      1 OUT BASED(PTR),
         2 LRECL BIN FIXED(15,0),    /* INPUT OR OUTPUT */
         2 RECNO CHAR(5),            /* VARIABLE LENGTH RECORDS */
         2 OUTA  CHAR(65),
         2 OUTB(15) CHAR(73),
      1 FIRST_RECORD,
         2 OUT70 CHARACTER(73) ,  /* FIXED LENGTH RECORD INPUT OR OUT*/
         2 SEQNOP CHAR(2)  INIT(' 1'),
         2 RECNO CHAR(5),
       1 SECOND_RECORD  DEFINED FIRST_RECORD.OUT70,
         2 OUTA  CHAR(65),
         2 LRECLP CHAR(5),
       1 THIRD_RECORD  DEFINED RECORD_IN,
         2 A72   CHAR(72),
         2 A08   CHAR(8),
       1 OUT2 BASED(PTR),
         2 LRECL2  BIN FIXED(15,0),
         2 A08     CHAR(8),
         2 A72     CHAR(72),
         PTR POINTER,
         ONSOURCE BUILTIN,
         ADDR BUILTIN,
         READIN FILE RECORD INPUT,
         RITEOUT FILE RECORD OUTPUT,
         RECORD_OUT CHARACTER(80),
         RECORD_IN  CHARACTER(80),
         (I,ICOUNT) BIN FIXED(31,0),
         LAST_SEQNOP CHAR(2) INIT('01'),
         PARM CHARACTER(100) VARYING
         ;
-   /* INITIALIZE AND START PROCESSING */
         PTR = ADDR(IN) ;
         OPEN FILE(READIN) , FILE(RITEOUT) ;
    ON ERROR BEGIN ;
         PUT SKIP LIST('*** YOUR PANVALET COMMAND FAILED IN ',
         'MODULE VBPAN ') ;
         PUT SKIP LIST(' CALL JIM OLEKSIW ON X5236 ')  ;
    END ;
    ON CONVERSION BEGIN ;
         ONSOURCE='00065' ;
    END ;
    RECORD_OUT =
    ' **** THIS IS A CONVERTED VARIABLE LENGTH DATASET *****';
-   ON ENDFILE(READIN) GO TO QUIT ;
    IF PARM = 'VB2PAN' THEN DO ;
    WRITE FILE(RITEOUT) FROM(RECORD_OUT) ;/*COMMENT RECORD IN NEW DATA*/
         READFILE: READ FILE(READIN) INTO(IN) ;
         SEQNOP= ' 1' ; SEQNO = 1 ;
         SECOND_RECORD.OUTA = ' ' ;
         PUT STRING(LRECLP)EDIT(LRECL) (F(5)) ;
         FIRST_RECORD = OUT,BY NAME ;
         SECOND_RECORD.OUTA = OUT.OUTA ;
         IF SUBSTR(OUT70,1,2) = '--' THEN
            SUBSTR(OUT70,1,2) = '$-' ;
         IF SUBSTR(OUT70,1,2) = '++' THEN
            SUBSTR(OUT70,1,2) = '$+' ;
         WRITE FILE(RITEOUT) FROM(FIRST_RECORD) ;
         OUT.OUTA=' ' ;
         ICOUNT = TRUNC(LRECL/65);
         IF MOD(LRECL,65) = 0  THEN ICOUNT = ICOUNT - 1 ;
         DO I = 1 TO ICOUNT WHILE(LRECL > 65) ;
              SEQNO = SEQNO + 1 ;
              PUT STRING(SEQNOP)EDIT(SEQNO) (F(2)) ;
              OUT70 = OUTB(I) ;
              OUTB(I) = (73)' ' ;
         IF SUBSTR(OUT70,1,2) = '--' THEN
            SUBSTR(OUT70,1,2) = '$-' ;
         IF SUBSTR(OUT70,1,2) = '++' THEN
            SUBSTR(OUT70,1,2) = '$+' ;
              WRITE FILE(RITEOUT) FROM(FIRST_RECORD) ;
         END ;
    GO TO READFILE ;
    END ;
    IF PARM='PAN2VB' THEN DO ;
    READ FILE(READIN) INTO(RECORD_IN) ;
       IF  SUBSTR(RECORD_IN,1,72)  ^= RECORD_OUT THEN DO ;
    LRECL2 = 80 ;  /*  JUST CONVERT TO VB RECORD */
     NZ: OUT2 = THIRD_RECORD, BY NAME ;
         WRITE FILE(RITEOUT) FROM(IN) ;
         READ FILE(READIN) INTO(RECORD_IN) ;
         GO TO NZ ;
         END ;
 READPAN:READ FILE(READIN) INTO(FIRST_RECORD) ;
 BYPASS: GET STRING(LRECLP) EDIT(LRECL) (F(5)) ; /* CONVERT TO BINARY*/
         OUT.OUTA = SECOND_RECORD.OUTA ;
         OUT.RECNO= FIRST_RECORD.RECNO;/*PUT IN LINE NUMBER AND DATA*/
         ICOUNT = TRUNC(LRECL/65) ;
         IF MOD(LRECL,65) = 0  THEN ICOUNT = ICOUNT - 1 ;
         LAST_SEQNOP =' 1' ;
         DO I = 1 TO ICOUNT WHILE(LRECL>65) ;
              READ FILE(READIN) INTO(FIRST_RECORD) ;
              OUT.OUTB(I) = FIRST_RECORD.OUT70 ;
              /* ADD IN OTHER DATA RECORDS */
              IF SEQNOP <= LAST_SEQNOP THEN DO ;
                   PUT SKIP EDIT(' RECORD MISSING AROUND ',
                   'LINE NUMBER ',OUT.RECNO,OUT.OUTA)
                   (A,A,SKIP,A,A)  ;
                   LRECL=65;
              END ;
              LAST_SEQNOP = SEQNOP ;
         END ;
         INOUT = IN ;
         WRITE FILE(RITEOUT) FROM(INOUT) ;
         GO TO READPAN ;
    END ;
    IF PARM = 'LIST2PAN' THEN DO ;
    RECORD_OUT =
    ' **** THIS IS A CONVERTED LISTING DATASET *****';
    WRITE FILE(RITEOUT) FROM(RECORD_OUT) ;/*COMMENT RECORD IN NEW DATA*/
         READLIST: READ FILE(READIN) INTO(LISTREC) ;
         IF LIST1A   = '--' THEN  LIST1A ='$-' ;
         IF LIST1A   = '++' THEN  LIST1A ='$+' ;
         IF LIST2A   = '--' THEN  LIST2A ='$-' ;
         IF LIST2A   = '++' THEN  LIST2A ='$+' ;
         WRITE FILE(RITEOUT) FROM(LIST1) ;
         LIST3 = LIST2 ;
         WRITE FILE(RITEOUT) FROM(LIST3) ;
    GO TO READLIST ;
    END ;
    IF PARM = 'PAN2LIST' THEN DO ;
    LISTREC    =
    ' **** THIS IS A CONVERTED LISTING DATASET *****';
    READ FILE(READIN) INTO(LIST1)    ;/*CHECK COMMENT RECORD       */
    IF LIST1  ^=LISTREC    THEN
       PUT SKIP EDIT('##### ERROR YOU DID NOT ADD THIS DATASET ',
       'TO PANVALET WITH THE CORRECT PANADD COMMAND #####',
       'AN ATTEMPT WILL BE MADE TO BUILD A LISTING DATASET ANYWAY.')
       (SKIP,A,A,SKIP,A) ;
         READLPAN: READ FILE(READIN) INTO(LIST1) ;
                   READ FILE(READIN) INTO(LIST3) ;
         LIST2 = LIST3 ;
         WRITE FILE(RITEOUT) FROM(LISTREC) ;
    GO TO READLPAN ;
    END ;
    ELSE PUT SKIP EDIT('INVALID PROGRAM OPTION - PROCESSING SKIPPED')
                   (SKIP,A) ;
    RETURN ;
-QUIT:  CLOSE FILE(RITEOUT), FILE(READIN) ;
    RETURN ;
    END VBPAN ;
