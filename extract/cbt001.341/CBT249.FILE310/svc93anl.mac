//O1003585 JOB (VSP,008),
//             'JIM OLEKSIW ',CLASS=F,TIME=1,
//  MSGCLASS=A,MSGLEVEL=1
//*
//*    **** PROGRAM TO READ TPUT SVC TRACE GTF RECORDS
//*     AND ANALYZE FOR VTAM BUFFER VTIOC SIZES
//*    USE OF PROGRAM:  DETERMINE A 90% CONFIDENCE INTERVAL
//*    FOR THE VALUES OF BUFFERS IN TSOKEY00 IN PARMLIB FOR VTAM
//*    I WAS SUPRISED AT THE RESULTS, SINCE WE HAVE MANY DIFFERENT
//*    SIZE TERMINALS AND USE MAY FULL SCREEN APPLICATIONS:  THE
//*    TYPICAL I/O SIZE WAS 132 BYTES.
//*          JIM OLEKSIW / NORTHEAST UTILITIES SERVICE COMPANY
//*          P.O. BOX 270   HARTFORD, CONNECTICUT   06101
//*
//*
//*    FIRST,SELECT THE SVC 93 RECORDS FROM THE GTF FILE
//*
//PRINT  EXEC PGM=AMDPRDMP
//TAPE   DD  DSN=USER.VTAMGTF,DISP=SHR
//SYSUT1 DD  UNIT=SCRATCH,DISP=NEW,SPACE=(CYL,(10,10))
//PRINTER  DD  DSN=&&PRINT,DISP=(,PASS),
//   UNIT=SCRATCHA,SPACE=(CYL,(30),RLSE)
//SYSPRINT DD  SYSOUT=A
 EDIT  DDNAME=TAPE,SVC=(93)
  END
//*
//*  SAS PROGRAM TO READ AND ANALYZE THE RECORDS
//*
//S1  EXEC SAS,COND=EVEN
//FT11F001  DD  SYSOUT=A
//FT12F001  DD  SYSOUT=A
//DD1  DD  DSN=&&PRINT,DISP=(OLD,DELETE)
//SYSIN DD  *
*
*    SAS PROGRAM TO ANALYZE SVC TRACE FOR SVC 93
*;
***OPTIONS OBS=700  LINESIZE=72 ;
  DATA SVC93 ;
   INFILE DD1 ;
   DROP TEST1 R15  REG15 REG1B  ASID ;
   INPUT @2 TEST1 $CHAR9. @ ;
   IF TEST1 = 'SVC   093' THEN DO ;
      INPUT @42 USER_ID $CHAR8. ;
      R15 = '   ' ;
      DO WHILE (R15 NE 'R15') ;
         INPUT @12 R15 $CHAR3. @ ;
         IF R15 EQ 'R15' THEN
             INPUT @17 REG15 HEX8.  @31 ASID HEX4. @35 LENGTH HEX4.
                   @44 FLAG  HEX2.  @46 REG1B HEX6. ;
          ELSE INPUT ;
      END;
   END ;
   ELSE DELETE ;

    *FLAG BYTES DESCRIBED IN GC28-0708-1 SEC. 5-39 ;
*;
     IF FLAG = '1.......'B THEN DO ;  *   T G E T   ;
        TYPE='TGET' ;
        IF FLAG='...0....'B THEN WAIT=1 ;
                            ELSE WAIT=0 ;
        IF FLAG='......01'B THEN KIND='ASIS' ;
                            ELSE KIND='EDIT' ;
        END ;
        ELSE DO ;   *  T P U T      ;
            TYPE = 'TPUT' ;
            IF FLAG='..1.....'B  THEN INTERCOM=1 ;
                                 ELSE INTERCOM=0 ;
            IF FLAG='...0....'B  THEN WAIT=1 ;
                                 ELSE WAIT=0 ;
            IF FLAG='....1...'B  THEN HOLD=1 ;
                                 ELSE HOLD=0 ;
            IF FLAG='......11'B  THEN KIND='FLSC' ;
            IF FLAG='......00'B  THEN KIND='EDIT' ;
            IF FLAG='......01'B  THEN KIND='ASIS' ;
            IF FLAG='......10'B  THEN KIND='CNTL' ;
            IF FLAG='.....1..'B  THEN BREAK=1 ;
                                 ELSE BREAK=0 ;
          END ;
*PROC PRINT ;
   TITLE1 NORTHEAST UTILITIES ENGINEERING COMPUTER SERVICES ;
   TITLE2  SVC 93 TRACE ;
PROC SORT ; BY TYPE ;
PROC MEANS  MAXDEC = 2 ;
   TITLE2 AVERAGE CONTENTS OF REGISTERS IN TPUT ;
PROC MEANS  MAXDEC = 2 ;
   BY TYPE ;
PROC CHART ;
     VBAR LENGTH / SYMBOL='A5'X MIDPOINTS=0 TO 4100 BY 100 ;
     TITLE2 VERTICAL BAR CHARTS OF TGET/TPUT LENGTH DISTRBUTION.
PROC CHART ;
     VBAR LENGTH / SYMBOL='A5'X MIDPOINTS=0 TO 4100 BY 100 ;
     BY TYPE ;
PROC UNIVARIATE  PLOT      NORMAL ;
     VAR LENGTH ;
     TITLE2 UNIVARIATE STATISTICS ON LENGTH OF TPUTS/TGETS ;
