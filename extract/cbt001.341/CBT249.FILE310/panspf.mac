*          DATA SET JOPANSPF   AT LEVEL 001 AS OF 10/23/81
         PRINT NOGEN
*
*   GENERAL PURPOSE SPF PANVALET EXIT
*   ORIGINALLY   PANAEXIT5 ON PANVALET
*
*   UPDATED BY J. OLEKSIW FOR SPF 09/81
*   UPDATED TO HANDLE SUPERSET/SUBSETS 6/82
*     "      " CHECK FOR -- AND +- DURING PANADD AND PANUPD  06/82
*     "      " DISPLAY SAME SCREEN FOR OUTPUT AS FOR INPUT   06/82
*
*   DOCUMENTATION:   PANVALET EXTENDED FEATURES AND ADVANCED TECHNIQUES
*                    PAGES 1-11
*
PANEXIT  CSECT
         ESTAB
*
*                 GET ADDRESSIBILITY TO PANVALET IO AREAS
*
         USING AIOAREA,R9        IOAREA
         USING AIOCODE,R10       IOCODE
         USING ADIRECT,R11
START1   LM    R9,R11,0(R1)   R1 -> ADDR(PARAMETER LIST)
*
         CLI   IOCODE,C'2'    IS THIS AFTER A READ REQUEST?
         BE    CHECKPP        IF SO, LOOK FOR -- OR ++ CHARACTERS
*
         CLI   IOCODE,C'3'    IS THIS A PRINT REQUEST?
         BE    PRINT          YES, PRINT IT 50 CHARACTERS WIDE
*
         CLI   IOCODE,C'5'    IS THIS  WRITE WORK?
         BE    EXIT           YES, DO NOTHING
*
         CLI   IOCODE,C'8'    IS THIS  END OF JOB REQUEST?
         BE    DISPLAY        YES, REDISPLAY THE SCREEN.
*
         CLI   IOCODE,C'7'    END OF PANVALET INPUT???
         BE    PUTCOMM        IF SO, TRY TO ADDA COMMENT
*
         CLI   IOCODE,C'1'    CODE 1: ISSUED BEFORE A READ
         BNE   EXIT           EXIT IF NOT EQUAL
*
*********************************************************************
*       IOCODE = 1  ... HE TRIES TO FIGURE OUT THE ACTION REQUIRED  *
*********************************************************************
*
         CLI   FLAG,X'FF'     HAVE WE BEEN THROUGH HERE BEFORE?
         BE    CHEKCOM        YES, GO BYE-BYE, NO ACTION
         MVI   FLAG,X'FF'     SET FLAG .. WE HAVE BEEN HERE
*
*        DETERMINE THE ACTION CODE
*
         CALL  ISPLINK,(VGET,PANWORK,SHARED),VL
         LTR   15,15        GET ACTION: ADD-UPDATE-RETRIEVE-ETC
         BNZ   ERROR0
         CALL  ISPLINK,(VCOPY,PANACT,F10,ACTION,MOVE),VL
         LTR   R15,R15    COPY IN CURRENT VALUES
         BNZ   ERROR0
         CLC   ACTION,=C'PANADD '      ++ADD?
         BE    PANADD
         CLC   ACTION,=C'PANFORM'      ++FORMAT?
         BE    PANFORMT
         CLC   ACTION,=C'PANRET '      ++WRITE WORK,??
         BE    PANRET
         CLC   ACTION,=C'PANUPD '      ++UPDATE   0,ALL?
         BE    PANUPD
         CLC   ACTION,=C'PANSTAT'      ++STATUS   N,N??
         BE    PANSTAT
         CLC   ACTION,=C'PANREN '      ++RENAME   N,N
         BE    PANREN
         CLC   ACTION,=C'PANOTHR'      GENERAL UNSPECIFIC INTERFACE
         BE    PANOTHER
         CLC   ACTION,=C'PANUSER'      ++USER   N,Y??
         BE    PANUSER
         CLC   ACTION,=C'PANPRNT'      ++WRITE PRINT?
         BE    PANPRINT
         CLC   ACTION,=C'PANCOM '      ++COMMENT  ?
         BE    PANCOM
         LA    R7,ACTERROR             GIVE ERROR MESSAGE
         BAL   R8,SPFPUT
         B     FIN
*     PANVALET NAME OBTAIN
*     REGISTER 8 = BAL REGISTER
*
GETNAME  EQU   *
*   OBTAIN THE PANVALET NAME FROM THE MENU
         CALL  ISPLINK,(VGET,PANNAMEL,SHARED),VL
         LTR   R15,R15       VGET SUCCESSFUL?
         BNZ   ERROR0        IF NOT, GIVE THE USER A MESSAGE
         CALL  ISPLINK,(VCOPY,NAMEPAN,LENNAME,PANNAME,MOVE),VL
         LTR   R15,R15       VCOPY SUCCESSFUL?
         BNZ   ERROR1        IF NOT, TELL USER
         BR    R8            RETURN ... MINI ROUTINE ENDED
*
*     PANVALET RETRIEVE ... GENERATE A ++WRITE WORK COMMAND
*
PANRET   EQU   *
         BAL   R8,GETNAME   GET THE PANVALET NAME
         MVC   BUFFER(13),=C'++WRITE WORK,'
         MVC   BUFFER+13(21),PANNAME    PUT IN NAME (SUB/SUPERSETS LEN)
         MVC   IOAREA(45),BUFFER        COPY TO PANWORK AREA
         MVI   COMMFLAG,X'FF'
         MVI   IOCODE,C'A'              SET FOR WRITE WORK
         B     EXIT                     ALL DONE
*
*        PANVALET UPDATE ... GENERATE A ++UPDATE  NAME,0,ALL LINE
*
PANUPD   EQU   *
         BAL    R8,GETNAME            OBTAIN THE PANVALET NAME
         MVC    BUFFER(9),=C'++UPDATE '
         MVC    BUFFER+9(21),PANNAME  INSERT NAME
         LA     R2,BUFFER+9           PUT IN ',0,ALL'
         A      R2,LENNAME
         MVC    0(12,R2),=C',0,ALL       '
         MVC    IOAREA(45),BUFFER     PUT INTO PANVALET WORK AREA
         MVI    COMMFLAG,X'0F'
         MVI    IOCODE,C'A'              SET FOR UPDATE
         B      EXIT                     ALL DONE
*
*        PANVALET STATUS     GENERATE A ++STATUS  NAME,ENABLE  LINE
*
PANSTAT  EQU   *
         BAL    R8,GETNAME            FIND OUT NAME
         CALL   ISPLINK,(VGET,PANSTATL,SHARED),VL
         LTR    R15,R15
         BNZ    ERROR0
         CALL   ISPLINK,(VCOPY,STATPAN,LENSTAT,PANSTATS,MOVE),VL
         LTR    R15,R15
         BNZ    ERROR1
         MVC    BUFFER(9),=C'++STATUS '
         MVC    BUFFER+9(21),PANNAME     SUB/SUPERSET LENGTH
         LA     R2,BUFFER+9
         A      R2,LENNAME
         MVI    0(R2),C','            INSERT A COMMA
         MVC    1(10,R2),PANSTATS     PUT IN STATUS CODE
         MVC    IOAREA(45),BUFFER     PUT INTO PANVALET WORK AREA
         MVI    COMMFLAG,X'FF'
         MVI    IOCODE,C'A'              SET FOR UPDATE
         B      EXIT                     ALL DONE
*
*        PANVALET RENAME     GENERATE A ++RENAME  OLDNAME,NEWNAME LINE
*
PANREN   EQU   *
         BAL    R8,GETNAME   GETNAME
         CALL   ISPLINK,(VGET,NEWNAMEL,SHARED),VL
         LTR    R15,R15
         BNZ    ERROR0
         CALL   ISPLINK,(VCOPY,NEWNAME,LENNEW,PANNEWN,MOVE),VL
         LTR    R15,R15
         BNZ    ERROR1
         MVC    BUFFER(9),=C'++RENAME '
         MVC    BUFFER+9(21),PANNAME
         LA     R2,BUFFER+9
         A      R2,LENNAME               FIND WHERE TO PUT NEW NAME
         MVI    0(R2),C','               COMMA BETWEEN NAMES
         MVC    1(10,R2),PANNEWN         NEW NAME
         MVC    IOAREA(60),BUFFER        COPY INTO BUFFER
         MVI    COMMFLAG,X'FF'
         MVI    IOCODE,C'A'              SET FOR UPDATE
         B      EXIT                     ALL DONE
*
*        PANVALET FORMAT     GENERATE A ++FORMAT  NAME,FORMAT
*
PANFORMT EQU   *
         BAL    R8,GETNAME   GETNAME
         CALL   ISPLINK,(VGET,FORMATL,SHARED),VL
         LTR    R15,R15
         BNZ    ERROR0
         CALL   ISPLINK,(VCOPY,PANFORM,F10,FORMAT,MOVE),VL
         LTR    R15,R15
         BNZ    ERROR1
         MVC    BUFFER(9),=C'++FORMAT '
         MVC    BUFFER+9(21),PANNAME
         LA     R2,BUFFER+9
         A      R2,LENNAME               FIND WHERE TO PUT NEW NAME
         MVI    0(R2),C','               COMMA BETWEEN NAMES
         MVC    1(10,R2),FORMAT          NEW NAME
         MVC    IOAREA(60),BUFFER        COPY INTO BUFFER
         MVI    COMMFLAG,X'FF'
         MVI    IOCODE,C'A'              SET FOR UPDATE
         B      EXIT                     ALL DONE
*
*        PANVALET COMMENT    GENERATE A ++COMMENT NAME,COMMENT
*
PANCOM   EQU   *
         BAL   R8,GETNAME    GET THE NAME
         CALL  ISPLINK,(VGET,PANCOML,SHARED),VL
         LTR   R15,R15
         BNZ   ERROR0
         B     PUTCOMM
*
*        PANVALET USER       GENERATE A ++USER OLDUSER,NEWUSER
*
PANUSER  EQU   *
         BAL   R8,GETNAME     GET THE NAME FOR ++USER
         CALL  ISPLINK,(VGET,PANUSERL,SHARED),VL
         LTR   R15,R15
         BNZ   ERROR0
         CALL  ISPLINK,(VCOPY,NUMBPAN,LENNUMB,PANNUMB,MOVE),VL
         LTR   R15,R15       ... GET THE OLD USER NUMBER ...
         BNZ   ERROR3
         CALL  ISPLINK,(VCOPY,NUMBNEW,F10,PANUSERN,MOVE),VL
         LTR   R15,R15       ... GET THE OLD USER NUMBER ...
         BNZ   ERROR3
         MVC   BUFFER(7),=C'++USER '
         MVC   BUFFER+7(21),PANNAME   INSERT NAME
         LA    R2,BUFFER+7     WHERE DO YOU PUT THE NEW USER?
         A     R2,LENNAME
         MVI   0(R2),C','      COMMA BETWEEN USER NOS.
         MVC   1(5,R2),PANNUMB PUT IN OLD NUMBER
         LA    R2,1(R2)        WHERE DO YOU PUT THE NEW USER?
         A     R2,LENNUMB
         MVI   0(R2),C','      COMMA BETWEEN USER NOS.
         MVC   1(5,R2),PANUSERN    INSERT NEW USER NO.
         MVI   COMMFLAG,X'FF'
         MVC   IOAREA(60),BUFFER
         MVI   IOCODE,C'A'
         B     EXIT
*
*        PANVALET PRINT      GENERATE A ++WRITE PRINT,DSNAME
*                            SET FLAGS TO ALLOW PRINT
*
PANPRINT EQU   *
         BAL   R8,GETNAME     GET THE NAME FOR ++USER
         MVC   BUFFER(14),=C'++WRITE PRINT,'
         MVC   BUFFER+14(21),PANNAME
         MVI   IOCODE,C'A'      SET FOR PRINT
         MVI   PRINTFLG,X'FF'   SET PRINT FLAG
         MVC   IOAREA(60),BUFFER
         MVI   COMMFLAG,X'FF'
         B     EXIT
*
*        PANVALET OTHER      JUST GIVE THE DATA LINE TO PANVALET
*
PANOTHER  EQU  *
         CALL   ISPLINK,(VGET,PANOTHRL,SHARED),VL
         LTR   R15,R15
         BNZ   ERROR0
         CALL  ISPLINK,(VCOPY,OTHRPAN,F80,BUFFER,MOVE),VL
         LTR   R15,R15
         BNZ   ERROR3
         MVC   IOAREA(65),BUFFER
         MVI   COMMFLAG,X'FF'
         MVI   IOCODE,C'A'
         B     EXIT
*
*        PANVALET ADD
*
PANADD   EQU   *
*
*    0.  CREATE ADDRESSIBILITY TO SPF VARIABLES
*
         CALL  ISPLINK,(VGET,NAMELIST,SHARED),VL
         LTR   R15,R15
         BNZ     ERROR0      NON-NORMAL RETURN CODE ... GO BYE-BYE
*
*    1.  GET PANVALET NAME
*
         CALL  ISPLINK,(VCOPY,NAMEPAN,LENNAME,PANNAME,MOVE),VL
         LTR   R15,R15
         BNZ     ERROR1      NON-NORMAL RETURN CODE ... GO BYE-BYE
*
*    2.  GET PANVALET TYPE
*
         CALL  ISPLINK,(VCOPY,TYPEPAN,LENTYPE,PANTYPE,MOVE),VL
         LTR   R15,R15
         BNZ   ERROR2      NON-NORMAL RETURN CODE ... GO BYE-BYE
*
*    3.  GET PANVALET NUMBER
*
         CALL  ISPLINK,(VCOPY,NUMBPAN,LENNUMB,PANNUMB,MOVE),VL
         LTR   R15,R15
         BNZ     ERROR3      NON-NORMAL RETURN CODE ... GO BYE-BYE
*
*  GENERATE THE '++ADD NAME,TYPE,NUMBER ' LINE
*
         MVI   IOAREA,C' '              BLANKOUT IO AREA
         MVC   IOAREA+1(79),IOAREA
*
         MVC   BUILDER(21),PANNAME   PUT IN PANVALET NAME
*
*  R2 WILL POINT TO THE PLACE WHERE THE DATA WILL BE PLACED
*
         LA    R2,BUILDER        FIND END OF NAME
         A     R2,LENNAME        NAME CAN BE UP TO 10 BYTES LONG
*
*        *   GET PANVALET TYPE
*
GETTYPE  EQU   *
*
*        ALLOW TYPES TEXT, CLIST, VSBASIC, AND APL ... CHANGE TO DATA
*
         CLC   PANTYPE,=C'TEXT    '   TEXT?
         BE    CHANGED                IF SO, CHANGE TO DATA
         CLC   PANTYPE,=C'CLIST   '   CLIST?
         BE    CHANGED                IF SO, CHANGE TO DATA
         CLC   PANTYPE,=C'VSBASIC '   VSBASIC?
         BE    CHANGED                IF SO, CHANGE TO DATA
         CLC   PANTYPE,=C'APL     '   APL?
         BNE   NOCHANGE               IF SO, CHANGE TO DATA
CHANGED  EQU   *
         MVC   PANTYPE,=C'DATA    '   CHANGE TO DATA
         MVC   LENTYPE,=F'4'          RESET LENGTH
NOCHANGE EQU   *
         MVI   0(R2),C','        PUT IN A COMMA
         LA    R2,1(R2)       NOW PUT IN THE DATA TYPE NEXT BYTE
         MVC   0(8,R2),PANTYPE    MOVE TYPE
         A     R2,LENTYPE     PAN TYPE CAN BE UP TO 8 BYTES LONG
ENDTYPE  EQU   *
         MVI   0(R2),C','         PUT IN COMMA AFTER NAME
         LA    R2,1(R2)       BUMP UP POINTER
         CLC   PANTYPE,=C'DATA    '   DO WE PUT ON 'TSO' PARAMETER
         BE    GETNUM
         CLC   PANTYPE,=C'OBJECT  '   DO WE PUT ON 'TSO' PARAMETER
         BE    GETNUM
         MVC   0(4,R2),=C'TSO,'
         LA    R2,4(R2)
GETNUM   EQU   *
         MVC   0(4,R2),PANNUMB      PUT IN PANVALET NUMBER
END      EQU   *
         MVC   IOAREA(61),CARD
         MVI   IOCODE,C'A'
*
EXIT     EXIT
*
CHEKCOM  CLI   COMMFLAG,X'FF'  EXIT IF COMMENT HAS BENN ENTERED
         BE    FIN
         B     EXIT
*
*
PUTCOMM  EQU   *     ENTER A COMMENT IF ONE EXISTS
*
*    4.  GET PANVALET COMMENT
*
         CLI   COMMFLAG,X'FF'    SET FLAG ... DON'T COME BACK
         BE    FIN
         CLC   ACTION,=C'PANUPD '
         BE    EXIT
         CALL  ISPLINK,(VCOPY,COMMPAN,LENCOMM,PANCOMM,MOVE),VL
         LTR   R15,R15
         BNZ   MESSCOMM      NON-NORMAL RETURN CODE ... GO BYE-BYE
         CLC   PANCOMM(2),=C'  '                               '
         BE    MESSCOMM   NO COMMENT,NO WORK
         MVC   BUFFER(10),=C'--COMMENT '   GENERATE ++COMMENT
         MVC   BUFFER+10(21),PANNAME
         LA    R2,BUFFER+10       FIND OUT WHERE TO PUT COMMENT
         A     R2,LENNAME         LENGTH OF PANVALET NAME
         MVI   0(R2),C','         --COMMENT NAME,BLABLB
         MVC   1(52,R2),PANCOMM   INSERT ACTUAL COMMENT
         MVC   IOAREA(65),BUFFER
         MVI   COMMFLAG,X'FF'    SET FLAG ... DON'T COME BACK
         MVI   IOCODE,C'A'
         B     EXIT
*
DISPLAY  EQU   *
         CALL  ISPLINK,(VPUT,TEXTLIST,SHARED),VL
         LTR   R15,R15
         BNZ   ERROR0
         B     DISQUIT
*        CLC   ACTION,=C'PANADD '
*        BE    PANAPR
*        CLC   ACTION,=C'PANPRNT'
*        BE    DISQUIT
*        CLC   ACTION,=C'PANRET '
*        BE    PANRPR
*        CLC   ACTION,=C'PANUPD '
*        BE    PANUPR
*        CALL  ISPLINK,(VDISPLAY),VL  SCREEN4
*        B     DISQUIT
PANRPR   EQU   *
*        CALL  ISPLINK,(VPUT,TEXTLIST,SHARED),VL
*        LTR   R15,R15
*        BNZ   ERROR0
*        CALL  ISPLINK,(VDISPLAY),VL
*        LTR   R15,R15
*        BNZ   ERROR0
*        B     DISQUIT
PANUPR   EQU   *
*        CALL  ISPLINK,(VDISPLAY,SCREEN2),VL
*        CALL  ISPLINK,(VPUT,TEXTLIST,SHARED),VL
*        LTR   R15,R15
*        BNZ   ERROR0
*        B     DISQUIT
PANAPR   EQU   *
*        CALL  ISPLINK,(VDISPLAY,SCREEN1),VL
*        CALL  ISPLINK,(VDISPLAY),VL
DISQUIT  CALL  ISPLINK,(VDELETE,TEXTLIST),VL
         LTR   R15,R15
         BNZ   ERROR0
         B     EXIT
*
*  LOOK FOR -- OR ++ AND CHANGE THEM TO $- OR $+ SO THAT PANVALET
*  WILL TRANSLATE THEM CORRECTLY.
*
CHECKPP  EQU   *
         CLC   IOAREA(2),=C'++'   SPECIAL CHARACTERS?
         BE    CHANGEP
         CLC   IOAREA(2),=C'--'   OTHER SPECIAL CHARS?
         BNE   EXIT
CHANGEP  MVI   IOAREA,C'$'        RESET FIRST CHARACTER
         BE EXIT                  NOW RETURN.
*
*
*
PRINT    EQU   *
         L     R7,=A(MESS5)
         MVC   0(L'MESS5,R7),IOAREA
         BAL   R8,SPFPUT
         MVI   IOCODE,C'F'
         B     EXIT
*
ERROR0   EQU   *
         C     R15,=F'16'
         BNE   ER0
         MVC   MESSVGET,=C' XLATION ERROR'
         B     ER9
ER0      C     R15,=F'20'
         BNE   ER1
         MVC   MESSVGET,=C'SEVERE ERROR'
         B     ER9
ER1      MVC   MESSVGET,=C'UNKNOWN ERROR'
ER9      LA    R7,MESS0      SET UP FOR PRINT ROUTINE
         TPUT  MESS0,50
FIN      CLI   IOCODE,C'8'     NO ACTION AT END OF JOB
         BE    EXIT
         MVI   IOCODE,C'G'
         B     EXIT
*
ERROR1   EQU   *
         LA    R1,ERRNUMBR
         AR    R1,R15
         MVC   MESS1A,0(R1)       MOVE IN ERROR FLAG
         MVC   MESSNAME,PANNAME
         TPUT  MESS1,50
*        LA    R7,MESS1      SET UP FOR PRINT ROUTINE
*        BAL   R8,SPFPUT
         MVI   IOCODE,C'G'
         B     EXIT
ERROR2   EQU   *
         LA    R1,ERRNUMBR
         AR    R1,R15
         MVC   MESS2A,0(R1)       MOVE IN ERROR FLAG
         MVC   MESSTYPE,PANTYPE
         LA    R7,MESS2      SET UP FOR PRINT ROUTINE
         BAL   R8,SPFPUT
         MVI   IOCODE,C'G'
         B     EXIT
ERROR3   EQU   *
         LA    R1,ERRNUMBR
         AR    R1,R15
         MVC   MESS3A,0(R1)       MOVE IN ERROR FLAG
         MVC   MESSNUMB,PANNUMB
         LA    R7,MESS3      SET UP FOR PRINT ROUTINE
         BAL   R8,SPFPUT
         MVI   IOCODE,C'G'
         B     EXIT
MESSCOMM EQU   *
         LA    R1,ERRNUMBR
         AR    R1,R15
         MVC   MESSCA,0(R1)       MOVE IN ERROR FLAG
         MVC   MESSCOMT,PANCOMM
         LA    R7,MESSC      SET UP FOR PRINT ROUTINE
         BAL   R8,SPFPUT
         MVI   IOCODE,C'G'
         B     EXIT
*
SPFPUT   EQU   *
         CLI   PRINTFLG,X'FF'     IS THIS A PRINT REQUEST?
         BE    EXIT               NO CHANGE IF IT IS.
         CLI   TEXT1F,C'1'        HAVE WE INITIALIZED?
         BE    PUT2
PUT1     EQU   *                  PRINT OUT FIRST LINE
         MVI   TEXT1F,C'1'        SET FLAG
*   ESTABLISH ADDRESSIBILITY
         MVC   TEXT1(50),0(R7)
*   COPY CURRENT VALUES
         CALL  ISPLINK,(VDEFINE,NAMETEX1,TEXT1,CHAR,F50),VL
         LTR   R15,R15
         BNZ   ERROR1
         CALL  ISPLINK,(VDEFINE,NAMETEX2,TEXT2,CHAR,F50),VL
         LTR   R15,R15
         BNZ   ERROR1
         CALL  ISPLINK,(VDEFINE,NAMETEX3,TEXT3,CHAR,F50),VL
         LTR   R15,R15
         BNZ   ERROR1
         CALL  ISPLINK,(VDEFINE,NAMETEX4,TEXT4,CHAR,F50),VL
         LTR   R15,R15
         BNZ   ERROR1
         CALL  ISPLINK,(VDEFINE,NAMETEX5,TEXT5,CHAR,F50),VL
         LTR   R15,R15
         BNZ   ERROR1
         BR    R8            RETURN
*   COPY OUTPUT TEXT INTO BUFFER
PUT2     CLI   TEXT2F,C'2'    HAVE WE BEEN HERE BEFORE?
         BE    PUT3
         MVI   TEXT2F,C'2'    SET FLAG FOR HERE
         MVC   TEXT2(50),0(R7)
         BR    R8            RETURN
*   COPY OUTPUT TEXT INTO BUFFER
PUT3     CLI   TEXT3F,C'3'    HAVE WE BEEN HERE BEFORE?
         BE    PUT4
         MVI   TEXT3F,C'3'    SET FLAG FOR HERE
         MVC   TEXT3(50),0(R7)
         BR    R8            RETURN
*   COPY OUTPUT TEXT INTO BUFFER
PUT4     CLI   TEXT4F,C'4'    HAVE WE BEEN HERE BEFORE?
         BE    PUT5
         MVI   TEXT4F,C'4'    SET FLAG FOR HERE
         MVC   TEXT4(50),0(R7)
         BR    R8            RETURN
         EJECT
PUT5     EQU   *
         MVC   TEXT5(50),0(R7)
         BR    R8            RETURN
         LTORG
TEXT1F   DC    X'0'
TEXT2F   DC    X'0'
TEXT3F   DC    X'0'
TEXT4F   DC    X'0'
*MASK    DC    X'40404040404040404040'
*        DC    X'40404040404040404040'
*        DC    X'40404040404040404040'
*        DC    X'40404040404040404040'
*
*======================================================================
*      SPF PARAMETERS : DOCUMENTED IN SPF DIALOG MANAGEMENT SVCS      ×
*      SC34-2036-1                                                    ×
VREPLACE DC    CL8'VREPLACE'    SPF SERVICE NAME                      ×
VDELETE  DC    CL8'VDELETE '    SPF SERVICE NAME                      ×
VDEFINE  DC    CL8'VDEFINE '    SPF SERVICE NAME                      ×
VDISPLAY DC    CL8'DISPLAY '    SPF SERVICE NAME                      ×
VCONTROL DC    CL8'CONTROL '    SPF SERVICE NAME                      ×
VREFRESH DC    CL8'REFRESH '    SPF SERVICE NAME                      ×
SCREEN1  DC    CL8'PANXXXA'     SPF SCREEN  NAME                      ×
SCREEN2  DC    CL8'PANXXXU'     SPF SCREEN  NAME                      ×
SCREEN3  DC    CL8'PANXXXR'     SPF SCREEN  NAME                      ×
SCREEN4  DC    CL8'PANOTHR'     SPF SCREEN  NAME                      ×
VCOPY    DC    CL8'VCOPY'       SPF SERVICE NAME                      ×
VGET     DC    CL8'VGET'        SPF SERVICE NAME                      ×
VPUT     DC    CL8'VPUT'        SPF SERVICE NAME                      ×
CHAR     DC    CL8'CHAR'        TYPE OF DATA FOR VDEFINE              ×
MOVE     DC    CL8'MOVE  '      MOVE MODE, NOT LOCATE                 ×
SHARED   DC    CL8'SHARED'      GET VARIABLE FROM SHARED VARIABLE POOL×
PANOTHRL DC    C'(OTHRPAN) '    LIST TYPE                             ×
PANUSERL DC    C'(NUMBPAN,NUMBNEW) '                                  ×
NEWNAMEL DC    C'(NEWNAME) '                                          ×
PANNAMEL DC    C'(NAMEPAN) '                                          ×
PANSTATL DC    C'(STATPAN) '                                          ×
PANCOML  DC    C'(COMMPAN) '                                          ×
STATPAN  DC    CL8'STATPAN'                                           ×
NEWNAME  DC    CL8'NEWNAME'                                           ×
NUMBNEW  DC    CL8'NUMBNEW'                                           ×
OTHRPAN  DC    CL8'OTHRPAN'                                           ×
LENSTAT  DC    F'10'                                                  ×
F80      DC    F'80'                                                  ×
PANUSERN DC    CL10'          '                                       ×
PANNEWN  DC    CL10'          '                                       ×
PANSTATS DC    CL10'          '                                       ×
NAMEPAN  DC    CL8'NAMEPAN'     SPF VARIABLE NAME OF PAN DATASET      ×
COMMPAN  DC    CL8'COMMPAN'     SPF VAR NAME OF COMMENT               ×
TYPEPAN  DC    CL8'TYPEPAN'     SPF VAR NAME OF TYPE                  ×
NUMBPAN  DC    CL8'NUMBPAN'     SPF VAR NAME OF USER NUMBER           ×
PANFORM  DC    CL8'PANFORM'     SPF VAR NAME OF USER NUMBER           ×
NAMELIST DC    CL35'(NAMEPAN,COMMPAN,TYPEPAN,NUMBPAN)'                ×
TEXTLIST DC    CL35'(TEXT1,TEXT2,TEXT3,TEXT4,TEXT5)  '                ×
NAMETEX1 DC    CL8'(TEXT1) '    OUTPUT VARIABLE NAME                  ×
NAMETEX2 DC    CL8'(TEXT2) '    OUTPUT VARIABLE NAME                  ×
NAMETEX3 DC    CL8'(TEXT3) '    OUTPUT VARIABLE NAME                  ×
NAMETEX4 DC    CL8'(TEXT4) '    OUTPUT VARIABLE NAME                  ×
NAMETEX5 DC    CL8'(TEXT5) '    OUTPUT VARIABLE NAME                  ×
PANWORK  DC    C'(PANWORK)'     VGET PARAMETER                        ×
FORMATL  DC    C'(PANFORM)'     VGET PARAMETER                        ×
PANACT   DC    CL8'PANWORK'     VCOPY PARAMETER                       ×
ACTION   DC    CL7'       '     PANVALET ACTION REQUESTED             ×
TEXT1    DC    50CL1' '         FIRST LINE OF OUTPUT TEXT             ×
TEXT2    DC    50CL1' '         2ND   LINE OF OUTPUT TEXT             ×
TEXT3    DC    50CL1' '         3RD   LINE OF OUTPUT TEXT             ×
TEXT4    DC    50CL1' '         4TH   LINE OF OUTPUT TEXT             ×
TEXT5    DC    50CL1' '         5TH   LINE OF OUTPUT TEXT             ×
F10      DC    F'10'            TEN                                   ×
F50      DC    F'50'            FIFTY                                 ×
LENNAME  DC    F'21'            LENGTH OF PANNAME  (SUPER/SUBSETS)    ×
LENNEW   DC    F'10'            LENGTH OF PANNAME                     ×
LENCOMM  DC    F'52'            LENGTH OF COMMENT                     ×
LENTYPE  DC    F'8'             LENGTH OF TYPE                        ×
LENNUMB  DC    F'4'             LENGTH OF USER NUMBER                 ×
FORMAT   DC    CL10'         '  NEW FORMAT CODE                       ×
PANNUMB  DS    CL4              PANVALET USER NUMBER                  ×
         DC    C' '             POSITIONAL                            ×
PANCOMM  DS    CL52             PANVALET COMMENT                      ×
         DC    C' '             POSITIONAL                            ×
PANTYPE  DC    CL8'        '    PANVALET TYPE                         ×
         DC    C' '             POSITIONAL                            ×
PANNAME  DS    CL21             PANVALET NAME   FOR SUPERSETS/SUBSETS ×
         DC    C' '             POSITIONAL                            ×
ERRNUMBR DC   C'0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZ'                 ×
*======================================================================
*
FLAG     DC    X'00'        ERROR FLAG
PRINTFLG DC    X'00'        ERROR FLAG
COMMFLAG DC    X'00'        ERROR FLAG
CARD     DC    C'++ADD '
BUILDER  DC    CL35'                                 '
BUFFER   DC    CL80'                                                  '
*
ACTERROR DC    C'ERROR - PAN ACTION UNKNOWN '
MESS0    DC    C'SPF VAR SVCS ERROR CODE:'
MESSVGET DS    CL20
         DC     5CL1' '
*
*
MESS1    DC    C'SPF DSN MISSING:CODE'
MESS1A   DC   2CL1' '
MESSNAME DS    CL10
         DC     5CL1' '
*
MESS2    DC    C'SPF DA TYPE MISSING:CODE'
MESS2A   DC   2CL1' '
MESSTYPE DS    CL10
         DC     5CL1' '
*
MESS3    DC    C'SPF PAN NO. MISSING:CODE'
MESS3A   DC   2CL1' '
MESSNUMB DS    CL4
         DC     5CL1' '
*
*
MESSC    DC    C'MISSING PAN COMMENT:CODE'
MESSCA   DC   2CL1' '
MESSCOMT DS    CL10
         DC     5CL1' '
*
MESS4    DC    C'UNKNOWN SPF-PANVALET ERROR  '
MESS5    DS    CL50
         DEFINE  CODE=NO
AIOCODE  DSECT
IOCODE   DS    C
PROGRAM  DS    C
RETURN   DS    C
ADIRECT  DSECT
DIRENT   DS    CL80
AIOAREA  DSECT
IOAREA   DS    CL121
         END   PANEXIT
