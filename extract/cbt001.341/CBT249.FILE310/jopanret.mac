*          DATA SET PANREXIT35 AT LEVEL 002 AS OF 10/29/79
*
*   PANRET  COMMAND
*  THIS COMMAND RETRIEVES A DATASET FROM PANVALET TO TSO
*  ORIGINALLY WRITTEN BY ANONYMOUS, UPDATED BY JIM OLEKSIW
*  10-15-79
*  ERROR CHECKING ADDED TO PANRET AND  SUPPORT OF SUPERSETS
*
*   TO RELINK THIS COMMAND:
*   ALLOC FILE(DD1) DA('SYS1.AUX.LINKLIB') SHR  (PAN#1 IS HERE)
*   LINK (PANRET *) LOAD(' *** LIBRARY NAME ')
*   INCLUDE DD1(FGPAN19B PAN#1)
*   ENTRY FGPAN#1
*   NAME  PANRET(R)
*
         PRINT NOGEN
PANEXIT  CSECT
         ESTAB
         USING AIOAREA,R9
         USING AIOCODE,R10
         USING ADIRECT,R11
         LM    R9,R11,0(R1)
         CLI   IOCODE,C'1'
         BC    8,ONE
         CLI   IOCODE,C'3'
         BC    8,PRINT
         CLI   IOCODE,C'5'
         BC    7,EXIT
         CLI   FLAG1,X'FF'
         BC    8,EXIT
         MVI   FLAG1,X'FF'
         CLC   TYPE,=C'FORT '
         BC    8,SKIP
         CLC   TYPE,=C'ASMB '
         BC    8,SKIP
         CLC   TYPE,=C'PL/1 '
         BC    8,SKIP
         BC    15,EXIT
SKIP     EQU   *
         MVI   IOCODE,C'F'
         BC    15,EXIT
ONE      EQU   *
         CLI   FLAG,X'FF'
         BC    8,SECOND
FIRST    EQU   *
         TGET  BUFFER,80
         C     R15,=F'0'
         BC    7,ERROR
         C     R1,=F'0'
         BC    8,ERROR1
         CLI   BUFFER,C'?'   A QUESTION MARK GETS YOU OUT
         BE    GETOUT
         C     R1,=F'19'
         BC    2,ERROR1
         LR    5,R1   COPY COUNT OF CHARACTERS
         LA    R1,12(R1)
         MVI   IOAREA,C' '
         MVC   IOAREA+1(79),IOAREA
         OC    BUFFER(19),MASK
         LR    6,R1   COPY COUNT OF CHARACTERS
         BCTR  5,0   SUBTRACT 1 FROM CHAR COUNT
         EX    5,TESTCHAR
         BNZ   BADCHAR
         EX    6,MOVE
         MVI   IOCODE,C'A'
         MVI   FLAG,X'FF'
EXIT     EXIT
SECOND   EQU   *
         MVI   IOCODE,C'G'
         BC    15,EXIT
PRINT    EQU   *
         MVC   MESS3,IOAREA
         TPUT  MESS3,50
         MVI   IOCODE,C'F'
         BC    15,EXIT
ERROR    EQU   *
         TPUT  MESS1,28
         MVI   IOCODE,C'G'
         BC    15,EXIT
BADCHAR TPUT MESS6,L'MESS6
        TPUT MESS7,L'MESS7
ERROR1   EQU   *
         TPUT  MESS2,L'MESS2
*        TPUT  MESS5,L'MESS5
*        TPUT  MESS4,L'MESS4
*        B     FIRST
GETOUT   MVI   IOCODE,C'G'
         B     EXIT
         EJECT
         LTORG
LEGALCHR DS    0CL256  TRANSLATE TABLE FOR LEGAL INPUT
         DC    64CL1'1'
         DC    XL1'1'         SPACE
         DC   10CL1'1'
         DC    1XL1'0'        CENT SIGN AND PERIOD
         DC    1XL1'1'        NO  LESS THAN SIGN
         DC    1XL1'1'         NO  LEFT PARNENTHSSIS
         DC    2XL1'1'        NO PLUS SIGN OR OR) SIGN
         DC    1XL1'1'             &
         DC    9CL1'1'
         DC    2XL1'1'             EXCLAM.  AND $
         DC    1XL1'1'        *
         DC    1XL1'1'            )
         DC    1XL1'1'        ;
         DC    1CL1'1'        NOT SIGN NOT ACCEPTED
         DC    2XL1'1'        MINUS SIGN AND SLASH
         DC     9CL1'1'
         DC    2XL1'1'        COMMA AND %
         DC    1XL1'1'        NO UNDERSCORE
         DC    1XL1'1'       > SIGN
         DC    12CL1'1'
         DC    XL1'1'         POUND SIGN
         DC    XL1'1'         AT SIGN
         DC   2XL1'1'         SINGLE QUOTE AND EQUAL SIGN
         DC    66XL1'1'
         DC    X'000000000000000000'  A-I
         DC    7XL1'1'
         DC    X'000000000000000000'     J-R
         DC    8XL1'1'
         DC    X'0000000000000000'       S-Z
         DC    6XL1'1'
         DC    X'00000000000000000000'      0-9
         DC    6XL1'1'
MOVE     MVC   IOAREA(0),CARD
TESTCHAR TRT   BUFFER(0),LEGALCHR
MASK     DC    X'40404040404040404040'
         DC    X'40404040404040404040'
FLAG     DC    X'00'
FLAG1    DC    X'00'
CARD     DC    C'++WRITE WORK,'
BUFFER   DS    CL80
MESS1    DC    C'I/O ERROR IN SERVICE ROUTINE'
MESS2    DC C'YOU ENTERED THE NAME OF THE PANVALET DATASET INCORRECTLY'
MESS4    DC    C'ENTER IN THE PANVALET DATASET NAME AGAIN ===>'
MESS5    DC    C'ENTER A  ?  TO GET OUT OF PANRET    - OR - '
MESS6    DC    C'YOU INPUT A BAD CHARACTER IN YOUR PANVALET NAME'
MESS7    DC    C'ONLY LETTERS AND NUMBERS ARE ACCEPTED.         '
MESS3    DS    CL50
         DEFINE  CODE=NO
AIOCODE  DSECT
IOCODE   DS    C
PROGRAM  DS    C
RETURN   DS    C
ADIRECT  DSECT
DIRENT   DS    0CL80
         DS    CL18
TYPE     DS    CL5
         DS    CL57
AIOAREA  DSECT
IOAREA   DS    CL121
         END   PANEXIT
