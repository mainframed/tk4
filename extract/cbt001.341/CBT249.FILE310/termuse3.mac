//BE008BBT JOB (TSO,008),
//   ' TERMINAL USE  ',MSGLEVEL=1,MSGCLASS=A,TIME=1,
//   CLASS=A
//*
//*     TERMINAL USE REPORT
//*
//SMF       EXEC SAS
//USER     DD  DSN=SYS1.SMF.WORKFILE,DISP=SHR,VOL=SER=SMFPK2,UNIT=SYSDA
//TEMP     DD UNIT=SCRATCH,SPACE=(TRK,(1))
//SYSIN DD  *
*  ;
OPTIONS NODATE OVP  ;
PROC FORMAT DDNAME=SASLIB ;

         VALUE  $ACTION 0=LOGOFF
                        9=LOGON     ;

         VALUE  SFT     1='OFF SHIFT  '
                        2='PRIME SHIFT';

         VALUE $VTAMNAME CYRBT02=Conn Yankee Health Physics
                         MRDAT05=South Broad Street T&D
                         HART00=Hartford Engineering Office
                         MPRET01=Millstone Health Physics Trailer
                         CYRAT04=Conn Yankee Cost & Scheduling Trailer;

         VALUE SUBT_ID  1=JOB START
                        2=INTERVAL
                        3=STEP TERMINATION
                        4=STEP TOTAL
                        5=JOB TERMINATION  ;

         VALUE INOUT    0='UPDATE/WRITE'
                        1='INPUT/READ  ' ;

         VALUE VSPCUP   1=UP 0=DOWN ;

         VALUE TYPEC    0=START OF SMF DUMP
                        1=END   OF SMF DUMP ;

     VALUE CONFIGFM     1='IPL'
                        2='VARY ONLINE'
                        3=VARY OFFLINE
                        4=MSS AT IPL
                        5='VARY ONLINE,S'
                        6='VARY OFFLINE,S'  ;

     VALUE  WKLOAD      1=SYSTEM LOAD
                        2=BATCH LOAD
                        3=TSO LOAD
                        4=VSPC
                        5='CICS/ADABAS' ;

     VALUE PERGRP       0=MASTER
                        1=NORMAL BATCH
                        2=NORMAL TSO
                        3=HOT BATCH
                        4=VTAM
                        5=SYSTEMS TSO
                        6=TCAM
                        7=JES2
                        8=STARTED TASKS
                        9=SYSTEM MONITORS
                       10=FREEZE
                       11=LOW BATCH
                       12=HP TSO
                       13=VSPC CONTROL
                       14=VSPC SECOND
                       15=APPLICATIONS TSO
                       16='CICS & ADABAS'
                       52=VSPC TRANSACTIONS;
                       RUN ;
*______________________________________________________________________*
*
*   Monthly Application Program Useage
*
*______________________________________________________________________*
;
*OPTIONS OBS=500 ;


DATA WORK.TYPE200 ; * Get today's records for appl use ;
    LENGTH  DEFAULT=4 ;

    SET USER._TYPE200;

 ***Eliminate the comments to get rid of weekends ;
 ***temp=WEEKDAY(DATEPART(DATETIME));
 ***IF temp = 1 or temp = 7 then delete ;
 ***DROP TEMP ;

    PROC SORT DATA=WORK.TYPE200 ;
          BY datetime ;

    data WORK.firstday ;  * Count number of days in study ;
         RETAIN FLAG 0 first 0 ;
         KEEP FIRST END NODAYS TERMINAL ;
         set work.type200 END=LAST  ;
         if flag = 0 then do ;
            if datetime eq . then return ;
            flag = 1 ;
            first = datepart(datetime) ;
            format first end WEEKDATE.;
         end ;
         if last then do ;
            end = datepart(datetime) ;
            nodays = end - first     ;
            end = end - 1 ;  /* Note: The day is finished at 1 a.m.*/
            TERMINAL = '÷÷÷DUMMY' ;
            output ;
            FILE TEMP ;
            PUT @58 'TITLE3 BEGINNING DAY:' /@1 FIRST WEEKDATE30.
            +4 'ENDING DAY: ' END WEEKDATE30. ';' ;
         end ;
         PROC PRINT DATA=WORK.FIRSTDAY ;

    PROC SORT DATA=WORK.TYPE200 ;
          BY TERMINAL DATETIME DESCENDING ACT_TYPE ;

    DATA WORK.DURATION ;
          SET WORK.TYPE200 ;
          KEEP TERMINAL APPLCATN DURATION ;
          BY TERMINAL      ;
          FORMAT DURATION TIME12. TIME_ON        DATETIME22. ;
          RETAIN TIME_ON 0 LASTACT '0' ;
          IF FIRST.TERMINAL THEN DO ;
              TIME_ON = . ;
              IF ACT_TYPE = '9' THEN       /* This is Logon */
                 TIME_ON = DATETIME ;
              IF LAST.TERMINAL THEN OUTPUT ; /* logon and no logoff*/
              IF ACT_TYPE = '0' THEN DO ;  /* logoff = error */
                 DURATION = . ;
                 OUTPUT ;
              END ;
              LASTACT = ACT_TYPE ;
              RETURN ;
          END ;
          /*   This is not a new terminal */
          IF ACT_TYPE = '9' THEN DO ; /* logon */
             IF LASTACT = '9' THEN DO ;
             /* We are missing a logoff record */
                OUTPUT ;
                END ;
             TIME_ON = DATETIME ;
             LASTACT= ACT_TYPE ;
             RETURN ;
          END ;
          IF ACT_TYPE = '0' THEN DO ; * logoff ;
             IF LASTACT = '9' THEN DO ; /* Logon followed by logoff*/
                DURATION = DATETIME - TIME_ON ;
                DURATION = TIMEPART(DURATION) ;
                OUTPUT ;
                TIME_ON = . ;
                LASTACT= ACT_TYPE ;
                RETURN ;
             END ;
             ELSE DO ;   /* logoff followed by logoff */
                OUTPUT ;
                LASTACT= ACT_TYPE ;
                RETURN ;
             END ;
          END ;

    DATA WORK.TERM_USE;
         SET WORK.FIRSTDAY (IN=a) WORK.DURATION END=EOF ;
         BY TERMINAL       ;

         DROP  APPLCATN DURATION TOTLCNT TOTLTOT HOURS24 NUMBER
               FIRST END NODAYS USED_HRS DAYCOUNT HOURCNT RATIO ;
         RETAIN TERMTOT 0 TERMCNT 0 TOTLCNT 0 TOTLTOT 0
                DAYCOUNT 0 HOURCNT 0  RATIO 0 ;

         IF A THEN DO ;
              RATIO = 40 / 168  ;  * Ratio of 100% Utilization
                                 (40 hours/week &7 days x 24 hours/day);
              HOURS24 =  24 ;
              DAYCOUNT = NODAYS ;
              HOURCNT = DAYCOUNT * HOURS24 ;* COUNT OF HOURS POSSIBLE ;
              RATIO = RATIO * HOURCNT ;
              DELETE ;
         END ;
         IF DURATION NE . THEN
            TERMTOT = TERMTOT + DURATION ;
         TERMCNT + 1 ;
         FILE PRINT PRINT HEADER=HEAD;
         IF LAST.TERMINAL THEN DO ;
            NUMBER  + 1 ;
            TOTLCNT + TERMCNT ;
            TOTLTOT = TOTLTOT + TERMTOT ;
            USED_HRS = TERMTOT/(60*60)  ;

            USE =(USED_HRS / RATIO)*100 ;
            PUT @31 TERMINAL $CHAR8.
                @53 TERMCNT  7.0
                @70 TERMTOT  TIME12.
                @95 USE      8.2 '%' ;
            OUTPUT ;
            TERMTOT = 0 ;
            TERMCNT = 0 ;
         END ;
         IF EOF THEN DO ;
            USED_HRS = TOTLTOT/(60*60)  ;
            USE =((USED_HRS/NUMBER) / RATIO  )*100 ;
            PUT // ;
            PUT @30 '== TOTALS =='
                @53 TOTLCNT  7.0
                @70 TOTLTOT  TIME14.
                @95 USE      8.2 '%' ;
            PUT @29 ' TERMINALS AVAILABLE ' NUMBER 4.0
                @54 ' TOTAL SESSIONS'
                @72 ' TOTAL HOURS '
                @90 ' OVERALL UTILIZATION' ;
         END ;
         RETURN ;
         HEAD: PUT // @55 '*** CUMULATIVE REPORT ***'
                   //@40 'No. of Days in interval:' DAYCOUNT 3.0
                     +4  'Hours in interval:' HOURCNT 4.0
       /@30 'If the terminal was logged on' RATIO 4.0 ' hours then the'
              ' %Utilization factor would be 100% during this interval'
                   //@31 'TERMINAL   '
                     @44 'NUMBER OF SESSIONS'
                     @64 'LOGON TIME(Hours,Min,Sec)'
                     @90 '%UTILIZATION FACTOR' ;
                   RETURN ;
         TITLE NORTHEAST UTILITIES SERVICE COMPANY ;
         TITLE2 Engineering Computer Services ;
         %INCLUDE TEMP/SOURCE2 ; * (Title3 ) ;
         * Documented in Sas Communications, Spring 81 ;
         TITLE4 Cumulative Terminal Connect Time Summary ;

    PROC SORT DATA=WORK.TERM_USE ;
         by TERMTOT ;
    PROC PRINT DATA=WORK.TERM_USE;
        TITLE5 Terminals listed in order of use ;
        Format TERMTOT TIME12. ;

    PROC SORT  DATA=WORK.DURATION ;
         BY APPLCATN  ;

    DATA _NULL_ ;
         SET WORK.DURATION END=EOF ;
         BY APPLCATN       ;
         RETAIN APPLTOT 0 SESSTOT 0 APPLCNT 0 SESSCNT 0 ;
         APPLTOT + DURATION ;
         APPLCNT + 1 ;
         FILE PRINT PRINT HEADER=HEAD;
         IF LAST.APPLCATN THEN DO ;
            SESSTOT + APPLTOT     ;
            SESSCNT + APPLCNT     ;
            PUT @31 APPLCATN $CHAR8.
                @53 APPLCNT  7.0
                @70 APPLTOT  TIME14. ;
            APPLTOT = 0 ;
            APPLCNT = 0 ;
         END ;
         IF EOF THEN DO ;
            PUT @30 '**TOTAL**'
                @53 SESSCNT  7.0
                @70 SESSTOT  TIME14. ;
         END ;
         RETURN ;
         HEAD: PUT //;
               PUT //@30 'APPLICATION'
                     @44 'NUMBER OF SESSIONS'
                     @64 'DURATION (HOURS,MIN,SEC)' ;
                   RETURN ;
         TITLE NORTHEAST UTILITIES SERVICE COMPANY ;
         TITLE2 ENGINEERING COMPUTER SERVICES ;
         TITLE4 MONTHLY INTERACTIVE NETWORK CONNECT TIME SUMMARY ;
         TITLE5;
*
*
*
*
