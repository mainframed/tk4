*          DATA SET VTAMEXIT35 AT LEVEL 002 AS OF 01/26/83
//BE008BBR JOB (TSO,008),
//       OLEKSIW,CLASS=Q,MSGCLASS=R,MSGLEVEL=1,
//       TIME=(,15),NOTIFY=BE008BB
//*
//*   INSTALL VTAM TERMINAL USE EXIT
//*
//ASMBL     EXEC ASMFC,OUTPUT='*',PARM='RENT,LOAD,NODECK'
//SYSIN     DD  *
         MACRO
&LABEL   ESTAB &BASE=12,&SECND=,&ID=*,&USING=15,&REFADR=*,&SP=0
         AIF   ('&REFADR' EQ '*').NOBALR
         BALR  15,0   ESTABLISH BASE ADDRESS FOR SAVE
.NOBALR  ANOP
         USING *,15
JETY&SYSNDX EQU   *
&LABEL   SAVE  (14,12),,&ID
         LR    &BASE,15
         GETMAIN R,LV=72,SP=&SP
         ST    1,8(0,13)
         ST    13,4(0,1)
         LM    0,1,20(13)
         L     13,8(0,13)
         XC    8(4,13),8(13)
         AIF   ('&SECND' EQ '').ONEBASE
         USING JETY&SYSNDX,&BASE,&SECND
         DROP  &USING
         LA    &SECND,4095(&BASE)
         LA    &SECND,1(&SECND)
         MEXIT
.ONEBASE ANOP
         USING JETY&SYSNDX,&BASE
         DROP  &USING
         MEND
         MACRO
&LABEL   EXIT  &RC=0
&LABEL   L     13,4(0,13)
         L     1,8(0,13)
         LA    0,72
         FREEMAIN R,LV=(0),A=(1)
         RETURN (14,12),RC=&RC
         MEND
            PRINT   GEN
*
*   ACF/VTAM EXIT AND ACCOUNTING ROUTINE
*
*  THIS EXIT WRITES A RECORD TO SMF WHEN A SESSION WITH A
*  LOGICAL UNIT IS ESTABLISHED OR TERMINATED
*
*  DOCUMENTATION: CHAPTER 5 ACF.VTAM SERVICES
*                 SC27-0468-0 ACF/VTAM INSTALLATION
*                 SC27-0610-0 ACF/VTAM VERSION 2 INSTALLATION
*                       PAGE 6-2 AND 19-1 (PLANNING/DESIGN)
*                       PAGE 19-11 LINKING,INSTALLING
*           SMF:  GC28-0706-1 P.51 USER EXIT ROUTINES
*
*  AUTHOR:  JIM OLEKSIW, NORTHEAST UTILITIES SERVICE COMPANY
*           JANUARY 1982
*
*  REGISTER CONTENTS ON CALL:
*  R0: POSITIVE IF A SESSION HAS BEEN ESTABLISHED
*      NEGATIVE ON TERMINATION
*
*  R7: POINTER TO THE DOUBLEWORD CONTAINING THE NAME OF
*      THE PRIMARY LOGICAL UNIT
*
*  R11: POINTER TO THE DOUBLEWORD CONTAINING THE NAME OF THE
*      SECONDARY LOGICAL UNIT
*  R13: SAVE AREA FOR USE BY THIS ROUTINE
*  R14: RETURN ADDRESS
*  R15: ADDRESS OF THE ENTRY POINT OF THIS ROUTINE
*
*  TO INSTALL: LINK INTO SYS1.LPALIB REPLACING ISTAUCAG
*  SEE 5-5 IN VTAM DOCUMENTATION ON HOW TO INSTALL
*
*  CREATE A RECORD FOR SMF:  RECORD TYPE 200
*  FORMAT OF RECORD:
*  RECLEN    RECORD LENGTH
*  RECTYPE   RECORD TYPE
*  DATETIME  PACKED DATE AND TIME
*  PLU       CHARACTER 8
*  SLU       CHARACTER 8
*  FLAG      1 CHAR BYTE :  C'9'= LOGOFF; C'1'=LOGON ; C'0' = ERROR
*
*
* OUTPUT
*    A 31 BYTE RECORD IS WRITTEN TO THE SMF DATA SET USING THE
*    SMFWTM MACRO. THE FORMAT OF THIS RECORD IS DESCRIBED BY THE
*    DSECT FOR THE SMF RECORD.
* EXTERNAL REFERENCES -  NONE
*
ISTAUCAG  CSECT
          ESTAB
*
* WHEN TSO USERS LOGON, THE VTAM APPLICATION 'TSO' PASSES CONTROL
* TO A SECONDARY 'TSO000X' ADDRESS SPACE VTAM APPLICATION.
* THUS, THESE RECORDS HAVE NO MEANING AND CAN BE IGNORED.
*
         CLC   TSO(8),0(R7)            IS THIS JUST 'TSO ' ?
         BE    QUIT                    IF SO, TERMINATE
*
         LR    R4,R0                   SAVE REG 0:LOGON-LOGOFF FLAG
         GETMAIN  RC,LV=40             GET CORE FOR SMF RECORD
         LTR   R15,R15                 SUCCESSFUL REQUEST?
         BNZ   QUIT                    END IF FAILURE
*
* GETMAIN WAS SUCCESSFUL ... RECORD IS OK WRITE
* NOTE: 8 EXTRA BYTES ARE RESERVED IN GETMAIN IN THE EVENT
* THAT YOU DECIDE TO ADD FIELDS TO THE RECORD.
*
         LR    R3,R1
         USING SMFDSECT,R3             BASE FOR SMF DSECT
         TIME  BIN                     GET TIME
         ST    R0,TIME                 TIME FOR SMF RECORD
         ST    R1,DATE                 DATE FOR SMF RECORD
         MVC   PLU(8),0(R7)         INSERT PRIMARY LOGICAL UNIT NAME
         MVC   SLU(8),0(R11)        INSERT SECONDARY LOGICAL UNIT NAME
         MVC   HEADER(6),SMFHEAD    GET HEADER
*
*  CHECK FOR POSITIVE/NEGATIVE REGISTER; SET A CHARACTER FLAG
*  FOR EASE OF TESTING IN SMF RECORD PRINTING.
*
         LTR   R4,R4                TEST REG 4
         BP    LOGON                REG 4 IS POSITIVE: LOGON
         BZ    LOGOFF               IF ZERO, LOGOFF
         MVC   FLAG,ZERO            CHARACTER 0 = ERROR.
         B     WRITE                WRITE RECORD
LOGON    MVC   FLAG,ONE             SET FLAG TO ONE: LOGON
         B     WRITE
LOGOFF   MVC   FLAG,NINE            SET FLAG TO NINE: LOGOFF
WRITE    LA    R1,SMFOUT               GET SMF BUFFER @
         SMFWTM   (R1)                 WRITE SMF RECORD TYPE 128
         LR    R1,R3
         FREEMAIN  R,LV=40,A=(R1)
QUIT     EXIT  RC=0
*
R0       EQU   0                       REG 0
R1       EQU   1                       REG 1
R3       EQU   3                       REG 3
R4       EQU   4                       REG 4
R5       EQU   5                       REG 5
R7       EQU   7                       REG 7
R11      EQU   11                      REG 11
R15      EQU   15                      REG 15
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
*   POSITIONAL DATA CONSTANTS                                         *
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
ZERO     DC    C'0'
ONE      DC    C'1'
NINE     DC    C'9'
TSO      DC    CL8'TSO'
*
SMFHEAD  DS   0XL6
RECLEN   DC    X'001F'    LENGTH OF THIS RECORD =31 BYTES
DUMMY    DC    X'0000'    RESERVED HALFWORD AFTER RECORD LENGTH
VS2      DC    X'02'      FLAG FOR VS2 SYSTEM (MVS)
RECTYPE  DC    X'C8'      RECORD NUMBER 200
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
*   DEFINE DSECT FOR VTAM LOGON/LOGOFF RECORD  TYPE H'200' X'C8'      *
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
SMFDSECT DSECT
         DS    1H                      ALIGN TIME/DATE ON FWD
SMFOUT   EQU   *
HEADER   DS    6C                      HEADER
TIME     DS    1F                      TIME
DATE     DS    1F                      DATE
PLU      DS    CL8                     PRIMARY LOGICAL UNIT
SLU      DS    CL8                   SECONDARY LOGICAL UNIT
FLAG     DS    CL1                   FLAG: 0=OFF 1=ON
         END
//RENAME EXEC SAS
//LPALIB DD  DSN=SYS1.NUSCO.LPALIB,DISP=SHR,UNIT=3350,VOL=SER=IPORES
//SYSIN  DD  *
      PROC PDS DDNAME=LPALIB NOLIST ;

 * USE THE FOLLOWING 2 STATEMENTS ONLY ON THE INITIAL INSTALL ;
 ******   DELETE ISTAUCAT ;
 ******   CHANGE ISTAUCAG=ISTAUCAT ;

//LINK     EXEC LINKS,CLASS='*',SVOL=IPORES,
//         PARM=(RENT,REUS,XREF,LET,LIST,NCAL),REGION=256K
//SYSPUNCH DD  DUMMY
//SYSLMOD  DD  DSN=SYS1.LPALIB,DISP=SHR,UNIT=3350,VOL=SER=IPORES
//SYSLIN   DD  DSN=&&LOADSET,DISP=(OLD,DELETE)
//         DD  DDNAME=SYSIN
//SYSIN    DD  *
    SETCODE AC(1)
    NAME ISTAUCAG(R)
