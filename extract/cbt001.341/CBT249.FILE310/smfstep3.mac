//SMFREPRT JOB (TSO,008),
//   'DAILY SMF',MSGLEVEL=1,MSGCLASS=A,TIME=5,
//   CLASS=F
//*
//*     SMF      STEP1  FOR THE NEW SMF SYSTEM
//*
//SMF       EXEC SASVERS,REGION=2048K
//FT12F001 DD  SYSOUT=A,COPIES=2
//FT20F001 DD  UNIT=SCRATCH,SPACE=(TRK,(1)),DCB=LRECL=80
//FT45F001 DD  UNIT=SCRATCH,SPACE=(TRK,(100)),
//          DCB=(RECFM=FBA,BLKSIZE=1330,LRECL=133)
//FT60F001 DD  SYSOUT=F,DCB=(RECFM=FBA,BLKSIZE=1330,LRECL=133)
//USER     DD  DSN=SYS1.SMF.WORKFILE,DISP=OLD,VOL=SER=SMFPK1,UNIT=SYSDA
//IN       DD  DSN=USER.TEMPO.BASE,DISP=SHR
//SAVE     DD  DSN=USER.SAS.GREPLAY,DISP=OLD
//SAS3287  DD  SYSOUT=(A,SASWTR),COPIES=2
//INDATE   DD   *
MM/DD/YY           <===== CURRENT DATE
//SYSIN DD  *
OPTIONS NODATE OVP
        DEVADDR=(SAS,.,PRINTER1) ;
PROC FORMAT DDNAME=SASLIB ;

         VALUE  $ACTION 0=LOGOFF
                        9=LOGON     ;

         VALUE  SFT     1='PRIME SHIFT'
                        2='OFF   SHIFT';

         VALUE $VTAMNAME CYRBT02=Conn Yankee Health Physics
                         MRDAT05=South Broad Street T&D
                         HART00=Hartford Engineering Office
                         MPRET01=Millstone Health Physics Trailer
                         CYRAT04=Conn Yankee Cost & Scheduling Trailer;

         VALUE SUBT_ID  1=JOB START
                        2=INTERVAL
                        3=STEP TERMINATION
                        4=STEP TOTAL
                        5=JOB TERMINATION  ;

         VALUE INOUT    0='UPDATE/WRITE'
                        1='INPUT/READ  ' ;

         VALUE VSPCUP   1=UP 0=DOWN ;

         VALUE TYPEC    0=START OF SMF DUMP
                        1=END   OF SMF DUMP ;

     VALUE CONFIGFM     1='IPL'
                        2='VARY ONLINE'
                        3=VARY OFFLINE
                        4=MSS AT IPL
                        5='VARY ONLINE,S'
                        6='VARY OFFLINE,S'  ;

     VALUE  WKLOAD      1=SYSTEM LOAD
                        2=BATCH LOAD
                        3=TSO LOAD
                        4=VSPC
                        5='CICS/ADABAS' ;

     VALUE PERGRP       0=MASTER
                        1=NORMAL BATCH
                        2=NORMAL TSO
                        3=HOT BATCH
                        4=VTAM
                        5=SYSTEMS TSO
                        6=TCAM
                        7=JES2
                        8=STARTED TASKS
                        9=SYSTEM MONITORS
                       10=FREEZE
                       11=LOW BATCH
                       12=HP TSO
                       13=VSPC CONTROL
                       14=VSPC SECOND
                       15=APPLICATIONS TSO
                       16='CICS & ADABAS'
                       17=VERY SLOW BATCH
                       52=VSPC TRANSACTIONS;
                       RUN ;
    DATA RPT_DATE  ;
    /*                                                         */
    /*   GET THE DATE FOR THE REPORT FROM A FILE CREATED AT    */
    /*   THE TIME THE JOB IS SUBMITTED                         */
    /*                                                         */
    LENGTH  DEFAULT=4 ;
    LENGTH DATE_FOR 4 DTE_TME 8 ;
    FORMAT DATE_FOR DATE7. ;
    INFILE INDATE  STOPOVER ;
    INPUT DATE_FOR  MMDDYY8. ;
    TIME = -1; /* DUMMY VARIABLE FOR USE IN MERGING DATASETS */
    IF DATE_FOR = . THEN ABORT 222 ;
    DTE_TME   =  DHMS(DATE_FOR, 0,0,0) ;   * CREATE A DATE-TIME VALUE ;
    RETURN ;
    RUN ;

    /*                                                         */
    /*    PRODUCE REPORTS FROM THE TYPE 26 AND 30 RECORDS      */
    /*                                                         */

 DATA WORK.TYPB2630 ;
    SET RPT_DATE (IN=DATEIN)
        USER._TYPE26
        USER._TYPE30 ;
    KEEP REPORTDT JOB_NAME JOB_TYPE JOB_NO   JOBCLASS MSGCLASS SYSTEMID
         RDR_STRT RDR_STOP EXECSTRT EXECSTOP OUT_STRT OUT_STOP TIME_RDR
         DATETIME DATTIM30          CARDSRED LINESOUT CARDSPUN
         PGR_NAME PROGRAM  CCC      STEPNAME STEP_NO           TIMEINIT
                  COMPCODE STEPCODE PAGE_TOT PAGECPU           SUBTYPE
         CPU_SERV IO_SERV  MSO_SERV SRB_SERV SERVUNIT EXCPDISK EXCPTAPE
         TCB_STEP SRB_STEP                   TGETS    TPUTS    TAPEMONT
         JBSTPCPU JOB_EXEC JOB_ELPS JOB_PRGE IN_WAIT  OUT_WAIT ;

    LENGTH JOB_PRGE EXECSTRT EXECSTOP 8 ;

    FORMAT JOB_PRGE DATETIME22. JOB_ELPS TIME8. ;

    RETAIN REPORTDT 0. ;
    IF DATEIN THEN REPORTDT = DATE_FOR ;
       ELSE IF DATEPART(DATETIME) = REPORTDT OR
               DATEPART(TIME_RDR) = REPORTDT OR
               DATEPART(TIME_RDR) = REPORTDT - 1 OR
               DATEPART(TIME_RDR) = REPORTDT - 2 OR
               DATEPART(TIME_RDR) = REPORTDT - 3 OR
               DATEPART(TIME_RDR) = REPORTDT - 4 OR
               DATEPART(TIME_RDR) = REPORTDT - 5 OR
               DATEPART(TIME_RDR) = REPORTDT - 6 THEN DO ;
         JBSTPCPU = TCB_STEP + SRB_STEP ;
         JBSTPCPU = JBSTPCPU / 100. ;
         JOB_EXEC = . ;
         IF EXECSTOP NE . AND EXECSTRT NE . THEN DO ;
           IF DATEPART(EXECSTOP) NE DATEPART(EXECSTRT) THEN DO ;
             HR_JX = 24 * (DATEPART(EXECSTOP)-DATEPART(EXECSTRT)-1) ;
             JOB_EXEC = HMS(HR_JX,0,0) + TIMEPART(EXECSTOP) ;
             JOB_EXEC = JOB_EXEC + (HMS(24,60,60)-TIMEPART(EXECSTRT));
           END ;
            ELSE JOB_EXEC = TIMEPART(EXECSTOP) - TIMEPART(EXECSTRT) ;
         END ;
         JOB_PRGE= DATETIME ;
          * USE RDR_STRT INSTEAD OF TIME_RDR SO THAT MISSING      ;
          * VARIABLES ARE MISSING WITHOUT A TYPE 26 RECORD        ;
          * USE TIME_RDR FOR REPORT PRINTOUT                      ;
         JOB_ELPS = . ;
         IF JOB_PRGE NE . AND RDR_STRT NE . THEN DO ;
           IF DATEPART(JOB_PRGE) NE DATEPART(RDR_STRT) THEN DO ;
             HR_JE = 24 * (DATEPART(JOB_PRGE)-DATEPART(RDR_STRT)-1) ;
             JOB_ELPS = HMS(HR_JE,0,0) + TIMEPART(JOB_PRGE) ;
             JOB_ELPS = JOB_ELPS + (HMS(24,60,60)-TIMEPART(RDR_STRT));
           END ;
            ELSE JOB_ELPS = TIMEPART(JOB_PRGE) - TIMEPART(RDR_STRT) ;
         END ;
         IN_WAIT = . ;
         IF EXECSTRT NE . AND RDR_STRT NE . THEN DO ;
           IF DATEPART(EXECSTRT) NE DATEPART(RDR_STRT) THEN DO ;
             HR_OW = 24 * (DATEPART(EXECSTRT)-DATEPART(RDR_STRT)-1) ;
             JOB_ELPS = HMS(HR_OW,0,0) + TIMEPART(EXECSTRT) ;
             JOB_ELPS = JOB_ELPS + (HMS(24,60,60)-TIMEPART(RDR_STRT));
           END ;
            ELSE IN_WAIT = TIMEPART(EXECSTRT) - TIMEPART(RDR_STRT) ;
         END ;
         OUT_WAIT = . ;
         IF JOB_PRGE NE . AND EXECSTOP NE . THEN DO ;
           IF DATEPART(JOB_PRGE) NE DATEPART(EXECSTOP) THEN DO ;
             HR_IW = 24 * (DATEPART(JOB_PRGE)-DATEPART(EXECSTOP)-1) ;
             JOB_ELPS = HMS(HR_IW,0,0) + TIMEPART(JOB_PRGE) ;
             JOB_ELPS = JOB_ELPS + (HMS(24,60,60)-TIMEPART(EXECSTOP));
           END ;
            ELSE OUT_WAIT = TIMEPART(JOB_PRGE) - TIMEPART(EXECSTOP) ;
         END ;
         IF SUBTYPE EQ 1 OR SUBTYPE EQ 5 THEN STEP_NO = 0 ;
         DATTIM30 = . ;
         IF SUBTYPE EQ 5 THEN DATTIM30 = DATETIME ;
         IF JOB_NAME EQ ' ' THEN JOB_NAME = JOB_NO ;

         OUTPUT WORK.TYPB2630 ;
       END ;
    RETURN ;

PROC SORT DATA=WORK.TYPB2630 ;
    BY JOB_NAME TIME_RDR JOB_NO STEP_NO ;

DATA
   WORK.TYPB2630                        /* DATASET FOR BATCH REPORTS */
   (KEEP=REPORTDT JOB_NAME JOB_TYPE JOB_NO   JOBCLASS MSGCLASS SYSTEMID
         RDR_STRT RDR_STOP EXECSTRT EXECSTOP OUT_STRT OUT_STOP TIME_RDR
         DATETIME DATTIM30          CARDSRED LINESOUT CARDSPUN
         PGR_NAME PROGRAM  CCC      STEPNAME STEP_NO           TIMEINIT
                  COMPCODE STEPCODE PAGE_TOT PAGECPU           SUBTYPE
         CPU_SERV IO_SERV  MSO_SERV SRB_SERV SERVUNIT EXCPDISK EXCPTAPE
         TCB_STEP SRB_STEP                   TGETS    TPUTS    TAPEMONT
         JBSTPCPU JOB_EXEC JOB_ELPS JOB_PRGE IN_WAIT  OUT_WAIT )

   WORK.TYPC2630                        /* DATASET FOR CCC REPORTS */
   (KEEP=REPORTDT CCC
         JBSTPCPU DATETIME DATTIM30
         CPU_SERV IO_SERV  MSO_SERV SRB_SERV SERVUNIT )

   WORK.TYPS2630                        /* DATASET FOR STC REPORT  */
   (KEEP=REPORTDT JOB_NAME JOB_TYPE JOB_NO   JOBCLASS MSGCLASS SYSTEMID
         RDR_STRT RDR_STOP EXECSTRT EXECSTOP OUT_STRT OUT_STOP TIME_RDR
         DATETIME DATTIM30          CARDSRED LINESOUT CARDSPUN
         PGR_NAME PROGRAM  CCC      STEPNAME STEP_NO           TIMEINIT
                  COMPCODE STEPCODE PAGE_TOT PAGECPU           SUBTYPE
         CPU_SERV IO_SERV  MSO_SERV SRB_SERV SERVUNIT EXCPDISK EXCPTAPE
         TCB_STEP SRB_STEP                   TGETS    TPUTS    TAPEMONT
         JBSTPCPU JOB_EXEC JOB_ELPS JOB_PRGE IN_WAIT  OUT_WAIT )

   WORK.TYPT2630                        /* DATASET FOR TSO REPORT  */
   (KEEP=REPORTDT JOB_NAME CCC      EXECSTRT EXECSTOP JOB_EXEC
         JBSTPCPU DATETIME DATTIM30 TIME_RDR SUBTYPE  TGETS    TPUTS
         CPU_SERV IO_SERV  MSO_SERV SRB_SERV SERVUNIT STEP_NO  JOB_NO);

    RETAIN JOB_NAM  JOB_N    JOBCLAS  PGR_NAM  PROGRA   CC
                    RDR_STR  RDR_STO  EXECSTR  EXECSTO  OUT_STR
                    TIME_RD  DATETIM  DATTIM3
           CARDSRE  LINESOU  CARDSPU  PAGE_TO
           CPU_SER  IO_SER   MSO_SER  SRB_SER  SERVUNI
           JBSTPCP  JOB_EXE  JOB_ELP  JOB_PRG  IN_WAI   OUT_WAI       ;

    LENGTH JOB_NAM JOB_N $8  PGR_NAM $20  PROGRA $8  CC $3
           EXECSTR EXECSTO 8 ;

    SET WORK.TYPB2630 ;
    BY JOB_NAME TIME_RDR JOB_NO STEP_NO ;

    IF JOB_N NE JOB_NO THEN DO ;     /* SET UP DATA SO THAT OUTPUT  */
       JOB_NAM = ' ' ;               /* RECORDS WILL CONTAIN ALL    */
       JOB_N   = ' ' ;               /* THE NECESSARY DATA (AFTER   */
       JOBCLAS = ' ' ;               /* TAKING DIFFERENT INPUT      */
       CARDSRE = . ;                 /* RECORDS, CARRYOVER THEIR    */
       LINESOU = . ;                 /* OWN PART, THEREBY COMBINING */
       CARDSPU = . ;                 /* THE NECESSARY DATA)         */
       RDR_STR = . ;
       TIME_RD = . ;
       RDR_STO = . ;
       EXECSTR = . ;
       EXECSTO = . ;
       OUT_STR = . ;
       DATETIM = . ;
       DATTIM3 = . ;
       PGR_NAM = ' ' ;
       PROGRA  = ' ' ;
       CC      = ' ' ;
       PAGE_TO = . ;
       CPU_SER = . ;
       IO_SER  = . ;
       MSO_SER = . ;
       SRB_SER = . ;
       SERVUNI = . ;
       JBSTPCP = . ;
       JOB_EXE = . ;
       JOB_ELP = . ;
       JOB_PRG = . ;
       IN_WAI  = . ;
       OUT_WAI = . ;
    END ;

    IF JOB_NAME NE ' ' THEN JOB_NAM = JOB_NAME ;
    IF JOB_NO NE ' ' THEN JOB_N     = JOB_NO   ;
    IF JOBCLASS NE ' ' THEN JOBCLAS = JOBCLASS  ;
    IF CARDSRED NE . THEN CARDSRE = CARDSRED ;
    IF LINESOUT NE . THEN LINESOU = LINESOUT ;
    IF CARDSPUN NE . THEN CARDSPU = CARDSPUN ;
    IF RDR_STRT NE . THEN RDR_STR = RDR_STRT ;
    IF TIME_RDR NE . THEN TIME_RD = TIME_RDR ;
    IF RDR_STOP NE . THEN RDR_STO = RDR_STOP ;
    IF EXECSTRT NE . THEN EXECSTR = EXECSTRT ;
    IF EXECSTOP NE . THEN EXECSTO = EXECSTOP ;
    IF OUT_STRT NE . THEN OUT_STR = OUT_STRT ;
    IF DATETIME NE . THEN DATETIM = DATETIME ;
    IF DATTIM30 NE . THEN DATTIM3 = DATTIM30 ;
    IF PGR_NAME NE ' ' THEN PGR_NAM = PGR_NAME ;
    IF PROGRAM NE ' ' THEN PROGRA = PROGRAM    ;
    IF CCC       NE ' ' THEN CC      = CCC     ;
    IF PAGE_TOT NE . THEN PAGE_TO = PAGE_TOT ;
    IF CPU_SERV NE . THEN CPU_SER = CPU_SERV ;
    IF IO_SERV NE .    THEN IO_SER  = IO_SERV  ;
    IF MSO_SERV NE . THEN MSO_SER = MSO_SERV ;
    IF SRB_SERV NE . THEN SRB_SER = SRB_SERV ;
    IF SERVUNIT NE . THEN SERVUNI = SERVUNIT ;
    IF JBSTPCPU NE . THEN JBSTPCP = JBSTPCPU ;
    IF JOB_EXEC NE . THEN JOB_EXE = JOB_EXEC ;
    IF JOB_ELPS NE . THEN JOB_ELP = JOB_ELPS ;
    IF JOB_PRGE NE . THEN JOB_PRG = JOB_PRGE ;
    IF IN_WAIT NE .    THEN IN_WAI  = IN_WAIT  ;
    IF OUT_WAIT NE . THEN OUT_WAI = OUT_WAIT ;

    IF SUBTYPE EQ 4 OR (LAST.STEP_NO AND STEP_NO EQ 0)
                    OR (LAST.JOB_NO AND STEP_NO EQ 0) THEN DO ;
       JOB_NAME = JOB_NAM ;
       JOB_NO = JOB_N     ;
       JOBCLASS = JOBCLAS ;
       CARDSRED = CARDSRE ;
       LINESOUT = LINESOU ;
       CARDSPUN = CARDSPU ;
       RDR_STRT = RDR_STR ;
       TIME_RDR = TIME_RD ;
       RDR_STOP = RDR_STO ;
       EXECSTRT = EXECSTR ;
       EXECSTOP = EXECSTO ;
       OUT_STRT = OUT_STR ;
       DATETIME = DATETIM ;
       DATTIM30 = DATTIM3 ;
       PGR_NAME = PGR_NAM ;
       PROGRAM    = PROGRA  ;
       CCC        = CC      ;
       PAGE_TOT = PAGE_TO ;
       CPU_SERV = CPU_SER ;
       IO_SERV    = IO_SER  ;
       MSO_SERV = MSO_SER ;
       SRB_SERV = SRB_SER ;
       SERVUNIT = SERVUNI ;
       JBSTPCPU = JBSTPCP ;
       JOB_EXEC = JOB_EXE ;
       JOB_ELPS = JOB_ELP ;
       JOB_PRGE = JOB_PRG ;
       IN_WAIT    = IN_WAI  ;
       OUT_WAIT = OUT_WAI ;
    END ;

    IF (SUBSTR(JOB_NO,1,3)) EQ 'STC' THEN DO ;
       PGR_NAME = ' ' ;
    END ;

    IF SUBTYPE EQ 4 THEN DO ;
       PGR_NAME = JOB_NAME ;
    END ;

    IF SUBTYPE EQ 4 OR (LAST.STEP_NO AND STEP_NO EQ 0)
                    OR (LAST.JOB_NO AND STEP_NO EQ 0) THEN DO ;
      IF DATEPART(DATTIM30) EQ REPORTDT THEN DO ;
        IF (SUBSTR(JOB_NO,1,3)) EQ 'JOB' THEN OUTPUT WORK.TYPB2630 ;
        IF (SUBSTR(JOB_NO,1,3)) EQ 'STC' THEN OUTPUT WORK.TYPS2630 ;
      END ;
    END ;
    IF (LAST.STEP_NO AND STEP_NO EQ 0) OR
                              (LAST.JOB_NO AND STEP_NO EQ 0) THEN DO ;
      IF DATEPART(DATTIM30) EQ REPORTDT THEN DO ;
        IF (SUBSTR(JOB_NO,1,3)) EQ 'JOB' THEN OUTPUT WORK.TYPC2630 ;
      END ;
    END ;

    IF (SUBSTR(JOB_NO,1,3)) EQ 'TSU' THEN DO ;
      IF DATEPART(DATTIM30) EQ REPORTDT THEN DO ;
        IF (LAST.STEP_NO AND STEP_NO EQ 0) OR
             (LAST.JOB_NO AND STEP_NO EQ 0) THEN OUTPUT WORK.TYPT2630 ;
      END ;
    END ;


 PROC SORT DATA = WORK.TYPC2630 ;
   BY CCC ;

*_____________________________________________________________________*;

*    CREATE DATA FOR BATCH REPORT BY CCC BY JOB NAME ;

 PROC SORT DATA = WORK.TYPB2630 OUT=WORK.CCCBTCH ;
   BY CCC JOB_NAME JOB_NO ;

 DATA WORK.CCCBTCH ;
   SET WORK.CCCBTCH ;
   BY CCC JOB_NAME JOB_NO ;
   KEEP REPORTDT DATTIM30 CCC JOB_NAME JOB_NO ;
   IF FIRST.JOB_NO ;

*_____________________________________________________________________*;

*    CREATE DATA FOR BATCH REPORT BY JOB NUMBER ;

 PROC SORT DATA = WORK.TYPB2630 OUT=WORK.JBNBTCH ;
   BY JOB_NO ;

 DATA WORK.JBNBTCH ;
   SET WORK.JBNBTCH ;
   KEEP REPORTDT DATTIM30 JOB_NO CCC JOB_NAME ;
   RETAIN JOB_N CC JOB_NAM ;
   IF JOB_NO NE JOB_N  OR  CCC NE CC  OR  JOB_NAME NE JOB_NAM THEN DO ;
     OUTPUT WORK.JBNBTCH ;
     JOB_N = JOB_NO ;
     CC = CCC ;
     JOB_NAM = JOB_NAME ;
   END ;

*_____________________________________________________________________*;

*    CREATE DATA STATS FOR BATCH CCC REPORT ;

 PROC MEANS N SUM DATA = WORK.TYPC2630 NOPRINT ;
   BY CCC ;
   VAR JBSTPCPU CPU_SERV IO_SERV MSO_SERV SRB_SERV SERVUNIT ;
   OUTPUT OUT = WORK.CCC_TOT            N = FREQ          SUM=
                 JBSTPCPU CPU_SERV IO_SERV MSO_SERV SRB_SERV SERVUNIT ;
   ID REPORTDT DATETIME DATTIM30 ;

*_____________________________________________________________________*;

*    SET UP FOR FICHE OUTPUT            ;

  OPTIONS NODATE ;
  PROC PRINTTO NEW UNIT=60 ;

  DATA _NULL_ ;
    SET WORK.TYPB2630 ;
    RETAIN FLAG 0 ;
    IF FLAG THEN STOP ;
    TDATE = DATEPART(DATTIM30) ;
    FILE FT20F001 LRECL=80 ;
    IF TDATE NE . THEN DO ;
      FLAG=1 ;
      PUT 'NUSCO   ECS   DAILY SMF REPORT      ' TDATE WEEKDATE30. ;
    END ;

  PROC FICHET UNIT=60 READ INITS='SMF' ;

*_____________________________________________________________________*;

*    MACRO FOR BATCH AND STC REPORTS    ;

  MACRO BTCH_STC ;
   HDRDATE = DATEPART(DATTIM30) ;

   IF (LAST.STEP_NO AND STEP_NO EQ 0)
     OR (LAST.JOB_NO AND STEP_NO EQ 0) THEN DO ;
*    PUT OUT SEPARATER LINE                                            ;
     PUT 66*'+ ' ;
     PUT ;
     IF LCTR > 43 THEN PUT _PAGE_ ;
*    PUT OUT TOTAL JOB RECORD                                          ;
     PUT @1   'JOB NAME                 '          JOB_NAME
         @47  'RDR START    '                      TIME_RDR
         @82  'CPU S.U.        '                   CPU_SERV  8.
         @110 'CARDS READ     '                    CARDSRED  8. ;
     PUT @1   'JOB NUMBER               '          JOB_NO
         @47  'RDR STOP     '                      RDR_STOP
         @82  'I/O S.U.        '                   IO_SERV   8.
         @110 'CARDS PUNCHED  '                    CARDSPUN  8. ;
     PUT @1   'JOB CLASS                '          JOBCLASS
         @47  'EXEC START   '                      EXECSTRT
         @82  'MAIN STOR S.U.  '                   MSO_SERV  8.
         @110 'LINES PRINTED  '                    LINESOUT  8. ;
     PUT @1   'COST CONTROL CENTER      '          CCC
         @47  'EXEC STOP    '                      EXECSTOP
         @82  'SRB S.U.        '                   SRB_SERV  8.
         @110 'PAGES PRINTED  '                                 ;
     PUT @1   'PROGRAMMER               '          PGR_NAME
         @47  'PRINT START  '                      OUT_STRT
         @82  'TOTAL JOB S.U. '                    SERVUNIT  9.
         @110 'PLOT UNITS     '                                 ;
     HR = INT(JBSTPCPU/3600) ;
     MN = INT((JBSTPCPU-3600*HR)/60) ;
     SC = MOD(JBSTPCPU,60) ;
     PUT @1   'JOB CPU TIME  (TCB+SRB) '
         @25  HR              2.
         @27  'H'
         @29  MN              2.
         @31  'M'
         @33  SC              5.2
         @47  'JOB PURGE    '                      JOB_PRGE
         @110 'FICHE         '                             ;
     HR = INT(JOB_EXEC/3600) ;
     MN = INT((JOB_EXEC-3600*HR)/60) ;
     SC = MOD(JOB_EXEC,60) ;
     HR1 = INT(IN_WAIT/3600) ;
     MN1 = INT((IN_WAIT-3600*HR1)/60) ;
     SC1 = MOD(IN_WAIT,60) ;
     PUT @1   'TOTAL EXEC TIME         '
         @25  HR              2.
         @27  'H'
         @29  MN              2.
         @31  'M'
         @33  SC              5.2
         @47  'INPUT WAIT  '
         @59  HR1             2.
         @61  'H'
         @63  MN1             2.
         @65  'M'
         @67  SC1             5.2 ;
     HR = INT(JOB_ELPS/3600) ;
     MN = INT((JOB_ELPS-3600*HR)/60) ;
     SC = MOD(JOB_ELPS,60) ;
     HR1 = INT(OUT_WAIT/3600) ;
     MN1 = INT((OUT_WAIT-3600*HR1)/60) ;
     SC1 = MOD(OUT_WAIT,60) ;
     PUT @1   'TOTAL ELAPSED TIME      '
         @24  HR              3.
         @27  'H'
         @29  MN              2.
         @31  'M'
         @33  SC              5.2
         @47  'OUTPUT WAIT '
         @58  HR1             3.
         @61  'H'
         @63  MN1             2.
         @65  'M'
         @67  SC1             5.2 ;
     PUT ;
   END ;
   IF SUBTYPE EQ 4 THEN DO ;
*    PUT OUT STEP TOTAL RECORD                                      ;
     IF LCTR > 47 THEN PUT _PAGE_ ;
     PUT @1   'STEP NUMBER              '          STEP_NO
         @47  'STEP INIT    '                      TIMEINIT
         @82  'CPU S.U.        '                   CPU_SERV  8.
         @110 'TAPE EXCP      '                    EXCPTAPE  8. ;
     PUT @1   'STEP NAME                '          STEPNAME
         @47  'STEP CODE     '                     STEPCODE  7.
         @82  'I/O S.U.        '                   IO_SERV   8.
         @110 'DASD EXCP      '                    EXCPDISK  8. ;
     PUT @1   'PROGRAM NAME             '          PROGRAM
         @47  'PAGING TOTAL'                       PAGE_TOT  9.
         @82  'MAIN STOR S.U.  '                   MSO_SERV  8.
         @110 'TAPE MOUNTS    '                    TAPEMONT  8. ;
     HR = INT(JBSTPCPU/3600) ;
     MN = INT((JBSTPCPU-3600*HR)/60) ;
     SC = MOD(JBSTPCPU,60) ;
     PUT @1   'STEP CPU TIME (TCB+SRB) '
         @25  HR              2.
         @27  'H'
         @29  MN              2.
         @31  'M'
         @33  SC              5.2
         @82  'SRB S.U.        '                   SRB_SERV  8. ;
     PUT @82  'TOTAL STEP S.U.'                    SERVUNIT  9. ;
     PUT ;
   END ;
   IF EOF THEN GO TO LAST_REC ;
   RETURN ;
   NEWPAGE:
      PUT @48  HDRDATE  WEEKDATE30. ;
      PUT ;
      RETURN ;
  %

*_____________________________________________________________________*;

*   BATCH REPORT         ;

 DATA _NULL_ ;
   SET WORK.TYPB2630 END=EOF ;
   BY JOB_NAME TIME_RDR JOB_NO STEP_NO ;
   FILE PRINT  LINE=LCTR  HEADER=NEWPAGE ;
   TITLE1  NORTHEAST UTILITIES;
   TITLE2  ENGINEERING COMPUTER SERVICES;
   TITLE3  SMF DAILY BATCH REPORT;

 * IF SUBSTR(JOB_NO,1,3) EQ 'JOB' THEN DO ;
      BTCH_STC ;                           /*CALL BATCH/STC MACRO*/
 * END ;

   LAST_REC:
      PUT 66*'+ ' ;
      PUT ;
      PUT // ;
      PUT @61  'END OF BATCH REPORT' ;

*_____________________________________________________________________*;

*   BATCH CCC REPORT         ;

 DATA _NULL_ ;
   SET WORK.CCC_TOT END=EOF ;
   FILE PRINT  LINE=LCTR  HEADER=NEWPAGE ;
   TITLE1  NORTHEAST UTILITIES;
   TITLE2  ENGINEERING COMPUTER SERVICES;
   TITLE3  SMF DAILY BATCH COST CONTROL CENTER REPORT;

   HDRDATE = DATEPART(DATTIM30) ;

   IF LCTR > 51 THEN PUT _PAGE_ ;
   HR = INT(JBSTPCPU/3600) ;
   MN = INT((JBSTPCPU-3600*HR)/60) ;
   SC = MOD(JBSTPCPU,60) ;
   PUT @1   CCC
       @12  FREQ
       @25  HR  2.  @27  'H'  @28  MN  2.  @30  'M'  @31  SC  4.1
       @42  CPU_SERV   8.
       @54  IO_SERV    8.
       @68  MSO_SERV   8.
       @80  SRB_SERV   8.
       @99  SERVUNIT   9. ;
   IF EOF THEN GO TO LAST_REC ;
   RETURN ;
   NEWPAGE:
      PUT @48  HDRDATE  WEEKDATE30. ;
      PUT ;
      PUT @ 1   'CCC'
          @ 9   '# OF JOBS'
          @ 23  'TOTAL CPU TIME'
          @ 42  'CPU S.U.'
          @ 55  'I/O S.U.'
          @ 68  'MSO S.U.'
          @ 81  'SRB S.U.'
          @ 94  'TOTAL SERVICE UNITS' ;
      PUT ;
      RETURN ;
   LAST_REC:
      PUT ;
      PUT // ;
      PUT @61  'END OF BATCH CCC REPORT' ;

*_____________________________________________________________________*;

*   BATCH REPORT BY CCC BY JOB NAME ;

 DATA _NULL_ ;
   SET WORK.CCCBTCH END=EOF ;
   BY CCC JOB_NAME JOB_NO ;
   FILE PRINT  LINE=LCTR  HEADER=NEWPAGE ;
   TITLE1  NORTHEAST UTILITIES;
   TITLE2  ENGINEERING COMPUTER SERVICES;
   TITLE3  SMF DAILY BATCH REPORT INDEXED BY CCC THEN BY JOB NAME;

   HDRDATE = DATEPART(DATTIM30) ;

   IF LCTR > 51 THEN PUT _PAGE_ ;
   PUT @1   CCC
       @9   JOB_NAME
       @22  JOB_NO ;
   IF LAST.CCC THEN PUT OVERPRINT @1 30*'_' / ;
   IF EOF THEN GO TO LAST_REC ;
   RETURN ;
   NEWPAGE:
      PUT @48  HDRDATE  WEEKDATE30. ;
      PUT ;
      PUT @ 1   'CCC'
          @ 9   'JOB NAME'
          @ 21  'JOB NUMBER' ;
      PUT ;
      RETURN ;
   LAST_REC:
      PUT ;
      PUT // ;
      PUT @61  'END OF BATCH CCC/JOB NAME REPORT' ;

*_____________________________________________________________________*;

*   BATCH REPORT BY JOB NUMBER ;

 DATA _NULL_ ;
   SET WORK.JBNBTCH END=EOF ;
   FILE PRINT  LINE=LCTR  HEADER=NEWPAGE ;
   TITLE1  NORTHEAST UTILITIES;
   TITLE2  ENGINEERING COMPUTER SERVICES;
   TITLE3  SMF DAILY BATCH REPORT BY JOB NUMBER;

   HDRDATE = DATEPART(DATTIM30) ;

   IF LCTR > 51 THEN PUT _PAGE_ ;
   PUT @1   JOB_NO
       @15  CCC
       @23  JOB_NAME ;
   IF EOF THEN GO TO LAST_REC ;
   RETURN ;
   NEWPAGE:
      PUT @48  HDRDATE  WEEKDATE30. ;
      PUT ;
      PUT @ 1   'JOB NUMBER'
          @ 15  'CCC'
          @ 23  'JOB NAME' ;
      PUT ;
      RETURN ;
   LAST_REC:
      PUT ;
      PUT // ;
      PUT @61  'END OF BATCH JOB NUMBER REPORT' ;

*_____________________________________________________________________*;

*   STC REPORT           ;

 DATA _NULL_ ;
   SET WORK.TYPS2630 END=EOF ;
   BY JOB_NAME TIME_RDR JOB_NO STEP_NO ;
   FILE PRINT  LINE=LCTR  HEADER=NEWPAGE ;
   TITLE1  NORTHEAST UTILITIES;
   TITLE2  ENGINEERING COMPUTER SERVICES;
   TITLE3  SMF DAILY STC REPORT;

 * IF SUBSTR(JOB_NO,1,3) EQ 'STC' THEN DO ;
      BTCH_STC ;                           /*CALL BATCH/STC MACRO*/
 * END ;

   LAST_REC:
      PUT 66*'+ ' ;
      PUT ;
      PUT // ;
      PUT @61  'END OF STC REPORT' ;

*_____________________________________________________________________*;

*   TSO USERS REPORT           ;

 DATA _NULL_ ;
   SET WORK.TYPT2630 END=EOF ;
   BY JOB_NAME TIME_RDR JOB_NO STEP_NO ;
   FILE PRINT  LINE=LCTR  HEADER=NEWPAGE ;
   TITLE1  NORTHEAST UTILITIES;
   TITLE2  ENGINEERING COMPUTER SERVICES;
   TITLE3  SMF DAILY TSO SESSIONS REPORT;

   LENGTH  EXECSTR EXECSTO 8 ;

   HDRDATE = DATEPART(DATTIM30) ;

   IF (LAST.STEP_NO AND STEP_NO EQ 0)
     OR (LAST.JOB_NO AND STEP_NO EQ 0) THEN DO ;
     IF LCTR > 51 THEN PUT _PAGE_ ;
*    PUT OUT TOTAL JOB RECORD                                          ;
      EXECSTR = TIMEPART(EXECSTRT) ;
      EXECSTO = TIMEPART(EXECSTOP) ;
      HR = INT(JOB_EXEC/3600) ;
      MN = INT((JOB_EXEC-3600*HR)/60) ;
      SC = MOD(JOB_EXEC,60) ;
      HR1 = INT(JBSTPCPU/3600) ;
      MN1 = INT((JBSTPCPU-3600*HR1)/60) ;
      SC1 = MOD(JBSTPCPU,60) ;
      PUT @1   JOB_NAME
          @12  CCC
          @18  EXECSTR    TIME8.
          @28  EXECSTO    TIME8.
          @39  HR  2.  @41  'H'  @42  MN  2.  @44  'M'  @45  SC  4.1
          @52  TGETS      6.
          @61  TPUTS      6.
          @71  HR1 2.  @73  'H'  @74  MN1 2.  @76  'M'  @77  SC1 4.1
          @83  CPU_SERV   8.
          @92  IO_SERV    8.
          @102 MSO_SERV   8.
          @112 SRB_SERV   8.
          @121 SERVUNIT   9. ;
   END ;
   IF EOF THEN GO TO LAST_REC ;
   RETURN ;
   NEWPAGE:
      PUT @48  HDRDATE  WEEKDATE30. ;
      PUT ;
      PUT @1   'USER ID '
          @11  'ACCT# '
          @20  'LOGON '
          @29  'LOGOFF '
          @40  'SESS TIME '
          @53  'TGETS '
          @62  'TPUTS '
          @71  'CPUTCB+SRB '
          @85  'CPU SU '
          @95  'IO SU '
          @104 'MSO SU '
          @114 'SRB SU'
          @124 'TOT SU' ;
      PUT ;
      RETURN ;
   LAST_REC:
      PUT ;
      PUT // ;
      PUT @61  'END OF TSO REPORT' ;

*_____________________________________________________________________*;
    RUN ;


    /*                                                         */
    /*    SEPARATE THE VSAM CLUSTER RECORDS FOR THIS DATE AND TIME  */
    /*                                                         */
DATA WORK.VSAMOPEN ;
    LENGTH  DEFAULT=4 ;
    KEEP REPORTDT DATETIME SYSTEMID JOB_NAME TIME_RDR DSNAME ;
    SET RPT_DATE (IN=DATEIN)   USER._TYPE62  ;
    RETAIN REPORTDT 0 ;
    IF DATEIN THEN REPORTDT = DATE_FOR ;
              ELSE IF DATEPART(DATETIME) = REPORTDT THEN OUTPUT ;
              RETURN ;
    RUN ;


    /*      PRODUCE FREQUENCY STATISTICS BASED ON DATASET USEAGE */
PROC SORT DATA=WORK.VSAMOPEN ;
     BY SYSTEMID REPORTDT  DSNAME ;


DATA _NULL_  ;
     SET WORK.VSAMOPEN    END=QUIT;
     FILE PRINT PRINT HEADER=HED ;
     BY SYSTEMID REPORTDT DSNAME ;

     IF FIRST.DSNAME THEN DSCOUNT = 0 ;

     DSCOUNT + 1 ;

     IF LAST.DSNAME  THEN DO ;
         PUT @40 DSCOUNT 7.  @50 DSNAME $44.  ;
         GTOTAL + DSCOUNT ;
         END ;

     IF QUIT THEN DO ;
         PUT  @38 '_________' @50 '__________________________________'@;
         PUT /@38 GTOTAL  9.  @50 '*********** GRAND TOTAL ********' ;
         END ;
     RETURN ;

     HED: PUT /@35 'SYSTEM: ' SYSTEMID $4. @45 REPORTDT WEEKDATE30.//
          @35 'ACCESS COUNT ' @55 ' VSAM CLUSTER NAME '  ;
     PUT  @35 '_____________' @50 '_________________________________'//;
          RETURN ;
          RUN ;


  /*     DAILY PLOT REPORT           */

       /* GET IPL RECORDS FROM TODAY */


DATA WORK.IPLTODAY;
    LENGTH  DEFAULT=4 ;
    KEEP REPORTDT TIME DATE ;
    FORMAT REPORTDT weekdate32. ;
    SET RPT_DATE (IN=DATEIN)   USER._TYPE0  ;

    RETAIN REPORTDT 0 ;

    IF DATEIN THEN REPORTDT = DATE_FOR ;
              ELSE DO ;
                   IF DATEPART(DATETIME) = REPORTDT THEN DO ;
                      DATE=DATEPART(DATETIME);
                      TIME=TIMEPART(DATETIME);
                      OUTPUT ;
                   END ;
              END ;

DATA WORK.PLOTCPU    /* FETCH TODAY'S OBSERVATIONS FROM MASTER FILE */
     WORK.TEMPCPU ;  /* FETCH TODAY'S OBSERVATIONS FROM MASTER FILE */
    LENGTH  DEFAULT=4 ;
    KEEP REPORTDT CPU_BUSY TSOMIN TSOMAX TIME CPU_ID  DATE CONFIG ;
    FORMAT REPORTDT WEEKDATE32. ;
    SET RPT_DATE (IN=DATEIN)   USER._TYPE70  ;

    RETAIN REPORTDT 0 ;

    IF DATEIN THEN REPORTDT = DATE_FOR ;
              ELSE DO ;
                   IF DATEPART(DATETIME) = REPORTDT THEN DO ;
                      DATE=DATEPART(DATETIME);
                      TIME=TIMEPART(DATETIME);
                      MIN=MINUTE(TIME) ;
                      HR =HOUR(TIME)   ;  DROP MIN HR ;
                      MIN=INT(MIN/15) * 15 ; *ONLY EVEN 15 MINUTE ;
                      TIME=HMS(HR,MIN,00) ;
                      IF CONFIG NE 1     THEN   CPU_BUSY = . ;
                      OUTPUT WORK.PLOTCPU ;
                      IF TIME > '07:45:00'T AND TIME < '17:00:00'T
                         THEN OUTPUT WORK.TEMPCPU ;
                   END ;
              END ;

 PROC PRINTTO NEW UNIT=45; /* CHANGE BCK TO HARDCPY AND FICH OUTPUT */

*______________________________________________________________________*
*
*   Daily Application Program Useage
*
*______________________________________________________________________*
;


DATA WORK.TYPE200 ; * Get today's records for appl use ;
    LENGTH  DEFAULT=4 ;
    DROP DATE_FOR ;
    FORMAT REPORTDT WEEKDATE32. ;

    SET USER.RPT_DATE (IN=DATEIN)   USER._TYPE200;

    RETAIN REPORTDT 0 ;

    IF DATEIN THEN REPORTDT = DATE_FOR ;
              ELSE DO ;
                   IF DATEPART(DATETIME) = REPORTDT THEN DO ;
                      OUTPUT ;
                   END ;
              END ;


    PROC SORT DATA=WORK.TYPE200 ;
          BY TERMINAL DATETIME DESCENDING ACT_TYPE ;

    DATA WORK.DURATION ;
          SET WORK.TYPE200 ;
          KEEP TERMINAL APPLCATN DURATION REPORTDT ;
          BY TERMINAL      ;
          FORMAT DURATION TIME12. TIME_ON        DATETIME22. ;
          RETAIN TIME_ON 0 LASTACT '0' ;
          IF FIRST.TERMINAL THEN DO ;
              TIME_ON = . ;
              IF ACT_TYPE = '9' THEN       /* This is Logon */
                 TIME_ON = DATETIME ;
              IF LAST.TERMINAL THEN OUTPUT ; /* logon and no logoff*/
              IF ACT_TYPE = '0' THEN DO ;  /* logoff = error */
                 DURATION = . ;
                 OUTPUT ;
              END ;
              LASTACT = ACT_TYPE ;
              RETURN ;
          END ;
          /*   This is not a new terminal */
          IF ACT_TYPE = '9' THEN DO ; /* logon */
             IF LASTACT = '9' THEN DO ;
             /* We are missing a logoff record */
                OUTPUT ;
                END ;
             TIME_ON = DATETIME ;
             LASTACT= ACT_TYPE ;
             RETURN ;
          END ;
          IF ACT_TYPE = '0' THEN DO ; * logoff ;
             IF LASTACT = '9' THEN DO ; /* Logon followed by logoff*/
                DURATION = DATETIME - TIME_ON ;
                DURATION = TIMEPART(DURATION) ;
                OUTPUT ;
                TIME_ON = . ;
                LASTACT= ACT_TYPE ;
                RETURN ;
             END ;
             ELSE DO ;   /* logoff followed by logoff */
                OUTPUT ;
                LASTACT= ACT_TYPE ;
                RETURN ;
             END ;
          END ;

    DATA _NULL_ ;
         SET WORK.DURATION END=EOF ;
         BY TERMINAL       ;
         RETAIN TERMTOT 0 TERMCNT 0 TOTLCNT 0 TOTLTOT 0
                HOURS24 '09:00:00'T ;
         IF DURATION NE . THEN
            TERMTOT = TERMTOT + DURATION ;
         TERMCNT + 1 ;
         FILE PRINT PRINT HEADER=HEAD;
         IF LAST.TERMINAL THEN DO ;
            NUMBER  + 1 ;
            TOTLCNT + TERMCNT ;
            TOTLTOT = TOTLTOT + TERMTOT ;
            USE =(TERMTOT / HOURS24)*100 ;
            PUT @31 TERMINAL $CHAR8.
                @53 TERMCNT  6.0
                @70 TERMTOT  TIME12.
                @95 USE      6.2 '%' ;
            TERMTOT = 0 ;
            TERMCNT = 0 ;
         END ;
         IF EOF THEN DO ;
            USE =((TOTLTOT/NUMBER) / HOURS24)*100 ;
            PUT // ;
            PUT @30 '**TOTAL**' NUMBER 4.0 ' TERMINALS'
                @53 TOTLCNT  6.0
                @70 TOTLTOT  TIME12.
                @95 USE      6.2 '%' ;
         END ;
         RETURN ;
         HEAD: PUT // @35 'Report for:' REPORTDT WORDDATE32.
                   //@30 'Terminal   '
                     @44 'Number of Sessions'
                     @64 'Logon Time(Hours,Min,Sec)'
                     @90 'Capacity Factor ' ;
                   RETURN ;
         TITLE NORTHEAST UTILITIES SERVICE COMPANY ;
         TITLE2 Engineering Computer Services ;
         TITLE4 Daily Terminal Connect Time Summary ;
         TITLE5 Capacity Factor is based on 9 hours = 100% ;

    PROC SORT  DATA=WORK.DURATION ;
         BY APPLCATN  ;

    DATA _NULL_ ;
         SET WORK.DURATION END=EOF ;
         BY APPLCATN       ;
         RETAIN APPLTOT 0 SESSTOT 0 APPLCNT 0 SESSCNT 0 ;
         APPLTOT + DURATION ;
         APPLCNT + 1 ;
         FILE PRINT PRINT HEADER=HEAD;
         IF LAST.APPLCATN THEN DO ;
            SESSTOT + APPLTOT     ;
            SESSCNT + APPLCNT     ;
            PUT @31 APPLCATN $CHAR8.
                @53 APPLCNT  6.0
                @70 APPLTOT  TIME12. ;
            APPLTOT = 0 ;
            APPLCNT = 0 ;
         END ;
         IF EOF THEN DO ;
            PUT @30 '**TOTAL**'
                @53 SESSCNT  6.0
                @70 SESSTOT  TIME12. ;
         END ;
         RETURN ;
         HEAD: PUT //;
               PUT //@30 'APPLICATION'
                     @44 'NUMBER OF SESSIONS'
                     @64 'DURATION (HOURS,MIN,SEC)' ;
                   RETURN ;
         TITLE NORTHEAST UTILITIES SERVICE COMPANY ;
         TITLE2 ENGINEERING COMPUTER SERVICES ;
         TITLE4 DAILY INTERACTIVE NETWORK CONNECT TIME SUMMARY ;
         TITLE5;
*
*
*
*
*;
*
*
*
*
*;
PROC MEANS  DATA=WORK.PLOTCPU MEAN MAXDEC=1 MAX ;
     by reportdt ;
     OUTPUT OUT=WORK.DALYAVG MEAN=DALYMEAN DLYTSMN ;
     VAR CPU_BUSY TSOMAX ;
     TITLE AVERAGE CPU BUSY/MAX TSO USERS FOR THE ENTIRE DAY ;
     TITLE3 NOTE THAT DOWNTIME IS NOT INCLUDED IN THE AVERAGE ;

*==> PUT THE VARIABLE TIME INTO THE SAS DATASET ;
DATA WORK.DALYAVG ;
    LENGTH  DEFAULT=4 ;
    SET USER.RPT_DATE (IN=DATEIN) WORK.DALYAVG ;

    RETAIN REPORTDT 0 ;

    IF DATEIN THEN REPORTDT = DATE_FOR ;
              ELSE DO ;
                      TIME = '23:59:59'T ; DATE=REPORTDT;
                      OUTPUT WORK.DALYAVG ;
              END ;

PROC MEANS  DATA=WORK.TEMPCPU MEAN MAXDEC=1 MAX ;
     by reportdt ;
     OUTPUT OUT=WORK.WORKAVG MEAN=WORKMEAN WRKTSMN ;
     VAR CPU_BUSY TSOMAX ;
     TITLE AVERAGE CPU BUSY/MAX TSO USERS FOR THE 8-5 WORKING DAY  ;
     TITLE3 NOTE THAT DOWNTIME IS NOT INCLUDED IN THE AVERAGE ;

*==> PUT THE VARIABLE TIME INTO THE SAS DATASET ;
DATA WORK.WORKAVG ;
    LENGTH  DEFAULT=4 ;
    SET USER.RPT_DATE (IN=DATEIN) WORK.WORKAVG ;

    RETAIN REPORTDT 0 ;

    IF DATEIN THEN REPORTDT = DATE_FOR ;
              ELSE DO ;
                      TIME = '23:59:59'T ; DATE=REPORTDT;
                      OUTPUT WORK.WORKAVG ;
              END ;

    /*  LOOK FOR MISSING 15 MINUTE VALUES ... PUT A ZERO IN THERE */
    DATA WORK.DUMMY ;
         LENGTH DEFAULT=4 ;
         TIME='00:00:00'T ;
         INCR='00:15:00'T ;
         KEEP  TIME CPU_BUSY TSOMIN TSOMAX ;
         CPU_BUSY = 0 ;
         TSOMIN   = 0 ;
         TSOMAX   = 0 ;
         DO I = 1 TO 96 ;
            OUTPUT ;
            TIME = TIME + INCR ;
         END ;
         /*  96 VALUES OF ZERO ARE THUS GENERATED FOR EACH
             15 MINUTE PERIOD OF THE DAY ...   */


DATA WORK.PLOTCPU ;
    LENGTH  DEFAULT=4 ;
    KEEP REPORTDT CPU_BUSY TSOMIN TSOMAX TIME CPU_ID  DATE CONFIG ;

    RETAIN REPORTDY 0 ;

    SET  USER.RPT_DATE(IN=DATEIN)
         WORK.PLOTCPU (IN=ACTUAL)
         WORK.DUMMY   (IN=DUMMY) ;
    BY TIME ;
    IF DATEIN THEN REPORTDY = DATE_FOR ;
     ELSE DO ;
         IF FIRST.TIME AND DUMMY THEN DO     ; *MISSING DATA FOR HOUR ;
              CPU_ID =0 ;
              DATE=REPORTDY ; *(REPORTDT IS IN WORK.PLOTCPU) ;
              REPORTDT=REPORTDY ;
              OUTPUT   ;
              CPU_ID = 1;
              OUTPUT    ;
         END ;
         IF ^FIRST.TIME AND DUMMY THEN DELETE; *DATA NOT MISSING ;
         IF  ACTUAL  THEN OUTPUT ;
      END ;

DATA WORK.PLOTCPU     /* GET BOTH PROCESSOR'S RECORDS */
     (KEEP=REPORTDT CPU_BUSY TSOUSERS TIME CPU_ID DATE CONFIG line)
     WORK.C_PLOT    /* GET BOTH PROCESSOR'S RECORDS FOR CPU PLOT */
     (KEEP=REPORTDT CPU_BUSY LINE TIME CPU_ID DATE CONFIG)
     WORK.T_PLOT    /* GET BOTH PROCESSOR'S RECORDS FOR TSO PLOT */
     (KEEP=REPORTDT TSOUSERS LINEX TIME CPU_ID DATE CONFIG) ;

    LENGTH  DEFAULT=4 ;
    SET WORK.PLOTCPU(IN=A)
        WORK.WORKAVG(IN=B)
        WORK.DALYAVG(IN=C)
        WORK.IPLTODAY(IN=D) ;
    BY DATE TIME ;

  RETAIN  LASTBUSY 0  WEEK_DAY  ;
  FORMAT CPU_BUSY 3.0 ;
  FORMAT TSOUSERS 3.0 ;

  IF A THEN DO ;      /* HANDLE ALL CPU BUSY RECORDS */

    IF REPORTDT NE . THEN WEEK_DAY = REPORTDT ;

    FORMAT DATE WEEK_DAY WEEKDATE35. CPU_BUSY 7.3 TIME TIME8.
                                     TSOUSERS 7.0 ;

    IF CPU_ID EQ 1 THEN DO ;
      LINE  = . ;
      LINEX = 1 ;   TSOUSERS = TSOMIN ;   OUTPUT WORK.T_PLOT ;
                                          OUTPUT WORK.PLOTCPU ;
      LINEX = 2 ;   TSOUSERS = TSOMAX ;   OUTPUT WORK.T_PLOT ;
                                          OUTPUT WORK.PLOTCPU ;
    END ;

    LINEX = . ;
    LINE  = CPU_ID ;
    IF CONFIG NE 1 THEN CPU_BUSY = 0 ;
                                          OUTPUT WORK.C_PLOT ;
                                          OUTPUT WORK.PLOTCPU ;
    LINE  =2 ; /*  FORM TOTAL OF THE TWO RECORDS */
    IF FIRST.TIME    THEN LASTBUSY = CPU_BUSY ;
    IF LAST.TIME AND ^FIRST.TIME    THEN CPU_BUSY= CPU_BUSY+LASTBUSY ;
    IF LAST.TIME    THEN  OUTPUT WORK.C_PLOT ;
    IF LAST.TIME    THEN  OUTPUT WORK.PLOTCPU ;
  END ;

  IF B THEN DO ;    /* WORKING HOUR AVERAGE */
      LINE =  3 ; LINEX = 3 ;
      REPORTDT= WEEK_DAY ;
      CPU_BUSY=WORKMEAN;
      TSOUSERS=WRKTSMN;
      TIME = '08:00:00'T ; OUTPUT WORK.C_PLOT ; OUTPUT WORK.T_PLOT ;
                                                OUTPUT WORK.PLOTCPU ;
      TIME = '12:00:00'T ; OUTPUT WORK.C_PLOT ; OUTPUT WORK.T_PLOT ;
                                                OUTPUT WORK.PLOTCPU ;
      TIME = '17:00:00'T ; OUTPUT WORK.C_PLOT ; OUTPUT WORK.T_PLOT ;
                                                OUTPUT WORK.PLOTCPU ;
  END ;

  IF C THEN DO ;  /* TOTAL DAY AVERAGE DATASET */
      LINE =  4 ; LINEX = 4 ;
      REPORTDT = WEEK_DAY ;
      CPU_BUSY=DALYMEAN;
      TSOUSERS=DLYTSMN;
      TIME = '00:00:00'T ; OUTPUT WORK.C_PLOT ; OUTPUT WORK.T_PLOT ;
                                                OUTPUT WORK.PLOTCPU ;
      TIME = '12:00:00'T ; OUTPUT WORK.C_PLOT ; OUTPUT WORK.T_PLOT ;
                                                OUTPUT WORK.PLOTCPU ;
      TIME = '24:00:00'T ; OUTPUT WORK.C_PLOT ; OUTPUT WORK.T_PLOT ;
                                                OUTPUT WORK.PLOTCPU ;
  END ;

  IF D THEN DO ;  /* YE OLDE IPL RECORDS  */
      LINE =  5 ; LINEX = 5 ;
      CPU_BUSY = 0   ; OUTPUT WORK.C_PLOT ; OUTPUT WORK.PLOTCPU ;
      CPU_BUSY = 100 ; OUTPUT WORK.C_PLOT ; OUTPUT WORK.PLOTCPU ;
      CPU_BUSY = 200 ; OUTPUT WORK.C_PLOT ; OUTPUT WORK.PLOTCPU ;
      CPU_BUSY = 0   ; OUTPUT WORK.C_PLOT ; OUTPUT WORK.PLOTCPU ;
                               /* BRINGS LINE DOWN TO ZERO WHEN */
                                   /*THERE ARE MULTIPLE IPLS    */
      TSOUSERS = 0   ; OUTPUT WORK.T_PLOT ; OUTPUT WORK.PLOTCPU ;
      TSOUSERS = 70  ; OUTPUT WORK.T_PLOT ; OUTPUT WORK.PLOTCPU ;
      TSOUSERS = 140 ; OUTPUT WORK.T_PLOT ; OUTPUT WORK.PLOTCPU ;
  END ;
    RUN ;

PROC FORMAT DDNAME=SASLIB ;
     VALUE CPNAME 0=CPU 0    1=CPU 1   2=COMPLEX TOTAL
                  3=WORKING AVERAGE    4=DAILY AVERAGE
                  5=IPL ;

     VALUE TSNAME 1=TSO  MIN  USERS    2=TSO  MAX  USERS
                  3=WORKING AVERAGE    4=DAILY   AVERAGE
                  5=IPL   OCCURANCE;


  /******   VERSATEC PLOT OF CPU BUSY *****************/

PROC GPLOT DATA=WORK.C_PLOT  GOUT=WORK.CPU_PLOT ;
  PLOT CPU_BUSY*TIME=LINE   /CAXIS=YELLOW
       VAXIS=0 TO 200 BY 20   VREF= 0 TO 200 BY 20 LVREF=2 CVREF=YELLOW
       CTEXT=red  HAXIS='00:00:00'T TO '24.00.00'T BY '02:00:00'T
       LHREF=2  VMINOR=1 HMINOR=3 VZERO HZERO
       CHREF=YELLOW  HREF='02:00:00'T TO '24.00.00'T BY '02:00:00'T  ;
  SYMBOL1 I=JOIN C=RED     L=5  V=NONE     ;
  SYMBOL2 I=JOIN C=RED     L=3  V=NONE     ;
  SYMBOL3 I=JOIN C=RED     L=1  V=NONE     ;
  SYMBOL4 I=JOIN C=BLUE    L=2  V=DIAMOND  ;
  SYMBOL5 I=JOIN C=BLUE    L=4  V=TRIANGLE ;
  SYMBOL6 I=JOIN C=BLACK   L=1  V=STAR     ;
  TITLE .H=2.2 .F=TRIPLEX .C=BLUE  NU Engineering Computer Services ;
 TITLE2 .F=TRIPLEX .C=BLUE .H=1.7 3033 Multiprocessor CPU Utilization;
                             FOOTNOTE1 .J=L.H=1.3 .F=SIMPLEX .C=red
 CPU busy from both processors is summed for complex total     ;
                            FOOTNOTE2 .J=L .H=1.3 .F=SIMPLEX .C=red
 Working Average: Average CPU Busy from 8 AM to 5 PM;
                            FOOTNOTE3 .J=L .H=1.3 .F=SIMPLEX .C=red
 Daily Average:   Average CPU Busy for the entire day ;
                            FOOTNOTE4 .J=L .H=1.3 .F=SIMPLEX .C=red
 Missing data is not included in the averages ;
   FORMAT LINE  CPNAME.  TIME TIME5. CPU_BUSY 4.0 ;
  LABEL  TIME   =TIME OF DAY  CPU_BUSY='%CPU UTILIZATION'
        REPORTDT=REPORT DATE ;
   BY   REPORTDT ;
 GOPTIONS DEVICE=VERSATEC  HSIZE=11 VSIZE=9  BORDER HBY=1.3
       FBY=ITALIC CBY=GREEN noterminal ;


  /******   VERSATEC PLOT OF TSO USERS *****************/

PROC GPLOT DATA=WORK.T_PLOT  GOUT=WORK.TSO_PLOT ;
  PLOT TSOUSERS*TIME=LINEX  /CAXIS=YELLOW
       VAXIS=0 TO 140 BY 20   VREF= 0 TO 140 BY 20 LVREF=2 CVREF=YELLOW
       CTEXT=red  HAXIS='00:00:00'T TO '24.00.00'T BY '02:00:00'T
       LHREF=2  VMINOR=1 HMINOR=3 VZERO HZERO
       CHREF=YELLOW  HREF='02:00:00'T TO '24.00.00'T BY '02:00:00'T  ;
  SYMBOL1 I=JOIN C=RED     L=4  V=NONE     ;
  SYMBOL2 I=JOIN C=RED     L=1  V=NONE     ;
  SYMBOL3 I=JOIN C=BLUE    L=2  V=DIAMOND  ;
  SYMBOL4 I=JOIN C=BLUE    L=4  V=TRIANGLE ;
  SYMBOL5 I=NEEDLE C=BLACK   L=1  V=STAR     ;
  TITLE .H=2.2 .F=TRIPLEX .C=BLUE  NU Engineering Computer Services ;
 TITLE2 .F=TRIPLEX .C=BLUE .H=1.7 3033 Multiprocessor TSO Usage;
                             FOOTNOTE1 .J=L.H=1.3 .F=SIMPLEX .C=red
 TSO MIN is the minimum number of TSO users over the 15 min. interval ;
                             FOOTNOTE2 .J=L.H=1.3 .F=SIMPLEX .C=red
 TSO MAX is the maximum number of TSO users over the 15 min. interval ;
                            FOOTNOTE3 .J=L .H=1.3 .F=SIMPLEX .C=red
 Working Average: Average TSO Max Users from 8 AM to 5 PM;
                            FOOTNOTE4 .J=L .H=1.3 .F=SIMPLEX .C=red
 Daily Average:   Average TSO Max Users for the entire day ;
                            FOOTNOTE5 .J=L .H=1.3 .F=SIMPLEX .C=red
 Missing data is not included in the averages ;
   FORMAT LINEX TSNAME.  TIME TIME5. TSOUSERS 4.0 ;
  LABEL  TIME   =TIME OF DAY  TSOUSERS=TSO  USERS
        REPORTDT=REPORT DATE ;
   BY   REPORTDT ;
 GOPTIONS DEVICE=VERSATEC  HSIZE=11 VSIZE=9  BORDER HBY=1.3
       FBY=ITALIC CBY=GREEN         ;

   /*** SET UP THE PLOT FOR DISPLAY ON THE IBM 3279 ****/

*PROC GPLOT DATA=WORK.T_PLOT  GOUT=SAVE.TSO_PLOT ;
* PLOT TSOUSERS*TIME=LINEX  /AREAS = 2    CAXIS=BLUE
*      VAXIS=0 TO 140 BY 20   VREF= 0 TO 140 BY 20 LVREF=2 CVREF=RED
*      CTEXT=BLUE HAXIS='00:00:00'T TO '24.00.00'T BY '02:00:00'T
*      LHREF=2  VMINOR=1 HMINOR=3 VZERO HZERO
*      CHREF=RED     HREF='02:00:00'T TO '24.00.00'T BY '02:00:00'T  ;
* SYMBOL1 I=JOIN C=CYAN    L=1  V=NONE     ;
* SYMBOL2 I=JOIN C=WHITE   L=1  V=NONE     ;
* SYMBOL3 I=JOIN C=BLUE    L=2  V=DIAMOND  ;
* SYMBOL4 I=JOIN C=BLUE    L=4  V=TRIANGLE ;
* SYMBOL5 I=NEEDLE C=BLUE    L=1  V=STAR     ;
* PATTERN1 C=CYAN   V=S ;
* PATTERN2 C=WHITE  V=S ;
* TITLE .H=1.3 .F=TRIPLEX .C=GREEN NU ENGINEERING COMPUTER SERVICES ;
* TITLE2 .F=DUPLEX .C=YELLOW .H=1.1 3033 MULTIPROCESSOR TSO USAGE;
* FOOTNOTE ;
*  FORMAT LINEX TSNAME.  TIME    TIME2. TSOUSERS 3.0 ;
* LABEL  TIME   =TIME OF DAY  TSOUSERS=TSO  USERS
*       REPORTDT=REPORT DATE ;
*  BY   REPORTDT NOTSORTED ;
*GOPTIONS DEVICE=IBM3279 BORDER HBY=1.0 FBY=ITALIC CBY=GREEN ;

 /* UPDATE THE MASTER DAILY AND WORKING AVERAGE FILE */
 DATA WORK.CPU_AVG ;
    LENGTH  DEFAULT=4 ;
      KEEP WORKMEAN DALYMEAN WRKTSMN DLYTSMN REPORTDT ;
      MERGE WORK.DALYAVG   WORK.WORKAVG ;
      BY REPORTDT ;

 DATA USER.CPU_AVG ;
    LENGTH  DEFAULT=4 ;
      UPDATE USER.CPU_AVG  WORK.CPU_AVG ;
      BY REPORTDT ;
      run ;
*******************************************************************;
*                                                                  ;
*                                                                  ;
*   + + + + + W O R K L O A D         A C T I V I T Y   + + + +    ;
*                                                                  ;
*                                                                  ;
*                                                                  ;
*   THIS SECTION OF THE REPORT USES THE DATA CAPTURED IN           ;
*                                                                  ;
*   THE PREVIOUS SMF STEPS TO CALCULATE 15 MINUTE                  ;
*                                                                  ;
*   AND HOURLY VALUES OF THE FIVE WORKLOAD ELEMENTS.               ;
*                                                                  ;
*   IT ALSO UPDATES  THE MASTER DATABASE OF WORKLOAD VALUES        ;
*                                                                  ;
*   AND PLOTS A GRAPH OF THE HOURLY VALUES FOR THE DAY.            :
*                                                                  ;
*                                                                  ;
*******************************************************************;
*******************************************************************;
*                                                                  ;
* ==> WORKLOAD ACTIVITY.STEP 1:                                    ;
*              EXTRACT THE RAW DATA FROM THE DATBASE(_TYPE72)      ;
*                                                                  ;
*******************************************************************;
DATA WORK.ALLWORK ;  /* FETCH TODAY'S OBSERVATIONS FROM MASTER FILE */
    LENGTH  DEFAULT=4 ;
    FORMAT REPORTDT DATE7. ;
    SET RPT_DATE (IN=DATEIN)   USER._TYPE72  ;
    RETAIN REPORTDT 0 ;
    IF DATEIN THEN REPORTDT = DATE_FOR ;
              ELSE DO ;
                   IF DATEPART(DATETIME) = REPORTDT THEN DO ;
                      DATE=DATEPART(DATETIME);
                      RPTDATE=DATE;
                      TIME=TIMEPART(DATETIME);
                      MIN=MINUTE(TIME) ;
                      HR =HOUR(TIME)   ;  DROP MIN HR ;
                      MIN=INT(MIN/15) * 15 ; *ONLY EVEN 15 MINUTE ;
                      TIME=HMS(HR,MIN,00) ;
                      OUTPUT WORK.ALLWORK ;
                      END;
              END ;
PROC SORT DATA=WORK.ALLWORK; BY TIME WORKLOAD PERIOD;
*******************************************************************;
*                                                                  ;
* ==> WORKLOAD ACTIVITY.STEP 2:                                    ;
*              REDUCE THE DATA INTO WORKLOAD COMPONENTS.           ;
*                                                                  ;
*******************************************************************;
DATA WORK.REDUCE  (DROP  =  DATE_FOR DTE_TME PGN PERIOD WORKLOAD
                            CPU_SERV IO_SERV MSO_SERV SRB_SERV
                            TRANTIME NO_TRANS DATE );
   RETAIN    CPU1     - CPU5
             MSO1     - MSO5
             SRB1     - SRB5
             IO1      - IO5
             TRANT1   - TRANT5
             NOTRANS1 - NOTRANS5;
   LENGTH DEFAULT=6 ;
   SET WORK.ALLWORK;
   BY TIME WORKLOAD PERIOD;
   ARRAY ACPU (K)    CPU1-CPU5 ;
   ARRAY AMSO (K)    MSO1-MSO5 ;
   ARRAY ASRB (K)    SRB1-SRB5 ;
   ARRAY AIO  (K)    IO1-IO5 ;
   ARRAY ATRAN (K)   TRANT1-TRANT5 ;
   ARRAY ANOTRAN (K) NOTRANS1-NOTRANS5;
   IF FIRST.TIME THEN DO;
                       DO K = 1 TO 5 ;
                       ACPU=0;AMSO=0;AIO=0;ASRB=0;ATRAN=0;ANOTRAN=0;
                       END ;
   END;


    K = WORKLOAD ;

  IF (K > 5 OR K < 1)  AND (PGN  LE 20) THEN ABORT 32 ;
       *×    IF K DOES NOT HAVE A VALUE, iT IS BECAUSE THE SYS1.PARMLIB
        ×  member (IEAIPSxx) changed WITHOUT GOING INTO SMFSTEP0 in the
        ×  record type 72 description (IF PGN=..) ;

* CHECK FOR REPORING PERFORMANCE GROUPS - THE STATISTICS ARE NOT PART;
* OF THE REPORTING;

    IF PGN = 52 THEN NOTRANS5 + NO_TRANS;

    IF WORKLOAD ^= . THEN DO;
      ACPU+CPU_SERV;AMSO+MSO_SERV;AIO+IO_SERV;ASRB+SRB_SERV;
      ATRAN+TRANTIME;ANOTRAN+NO_TRANS;
    END ;

     IF LAST.TIME THEN DO;
         CPU  = CPU1+CPU2+CPU3+CPU4+CPU5;
         IO   = IO1+IO2+IO3+IO4+IO5;
         MSO  = MSO1+MSO2+MSO3+MSO4+MSO5;
         SRB  = SRB1+SRB2+SRB3+SRB4+SRB5;
         OUTPUT;
     END;

*******************************************************************;
*                                                                  ;
* ==> WORKLOAD ACTIVITY.STEP 3:                                    ;
*              CREATE A DUMMY DATASET WITH ZEROES WHERE THERE      ;
*              ARE NO RECORDS.                                     ;
*******************************************************************;
DATA WORK.DUMMY1;
   ARRAY ACPU (K)    CPU1-CPU5 ;
   ARRAY AMSO (K)    MSO1-MSO5 ;
   ARRAY ASRB (K)    SRB1-SRB5 ;
   ARRAY AIO  (K)    IO1-IO5 ;
   ARRAY ATRAN (K)   TRANT1-TRANT5 ;
   ARRAY ANOTRAN (K) NOTRANS1-NOTRANS5;

   RETAIN    CPU1     - CPU5
             MSO1     - MSO5
             SRB1     - SRB5
             IO1      - IO5
             TRANT1   - TRANT5
             NOTRANS1 - NOTRANS5;
         LENGTH DEFAULT=4 ;
         TIME='00:00:00'T ;
         INCR='00:15:00'T ;

              DO K = 1 TO 5 ;
               ACPU=0;AMSO=0;AIO=0;ASRB=0;ATRAN=0;ANOTRAN=0;
              END ;

         CPU=0;MSO=0;IO=0;SRB=0;

         DO I = 1 TO 96 ;
            OUTPUT ;
            TIME = TIME + INCR ;
         END ;
         /*  96 VALUES OF ZERO ARE THUS GENERATED FOR EACH
             15 MINUTE PERIOD OF THE DAY ...   */

*******************************************************************;
*                                                                  ;
* ==> WORKLOAD ACTIVITY.STEP 4:                                    ;
*              MERGE TOGETHER THE TWO PREVIOUSLY CREATED DATSETS   ;
*              TO CREATE AN ENTIRE DAYS WORTH OF DATA.             ;
*******************************************************************;
DATA WORK.WORKLOAD;
    RETAIN REPORTDY 0 ;
    SET  USER.RPT_DATE(IN=DATEIN)
         WORK.REDUCE  (IN=ACTUAL)
         WORK.DUMMY1  (IN=DUMMY) ;
          BY TIME ;
    IF DATEIN THEN REPORTDY = DATE_FOR ;
     ELSE DO ;
         IF FIRST.TIME AND DUMMY THEN DO     ; *MISSING DATA FOR HOUR ;
              DATE=    REPORTDY ; *(REPORTDT IS IN WORK.PLOTCPU) ;
              REPORTDT=REPORTDY ;
              RPTDATE= REPORTDY ;
              OUTPUT   ;
         END ;
         IF ^FIRST.TIME AND DUMMY THEN DELETE; *DATA NOT MISSING ;
         IF  ACTUAL  THEN OUTPUT ;
      END ;

*******************************************************************;
*                                                                  ;
* ==> WORKLOAD ACTIVITY.STEP 5:                                    ;
*              USE THE DATA CREATED FOR THE CPU BUSY PLOT TO       ;
*              GET 15 MIN. CPU MINUTE DATA.                        ;
*******************************************************************;
DATA WORK.CPU (DROP =REPORTDY  DATE_FOR    DTE_TME
                     REPORTDT  CONFIG      CPU_ID
                     WORKMEAN  DALYMEAN   LASTBUSY
                     WEEK_DAY  LINE);
              SET WORK.PLOTCPU;IF LINE=2;
*******************************************************************;
*                                                                  ;
* ==> WORKLOAD ACTIVITY.STEP 6:                                    ;
*              MERGE TOGETHER THE TWO PREVIOUSLY CREATED DATASETS  ;
*              TO FORM ONE DATASET WITH WORKLOAD AND CPU DATA.     ;
*******************************************************************;
DATA WORK.DAY   (DROP=DATE I INCR
                    DATE_FOR DTE_TME);
   RETAIN    CPU1     - CPU5
             MSO1     - MSO5
             SRB1     - SRB5
             IO1      - IO5
             TRANT1   - TRANT5
             NOTRANS1 - NOTRANS5;
     MERGE WORK.CPU
           WORK.WORKLOAD;
    IF _N_ = 1 THEN NMISS = 0;
    CPUTOTAL=(CPU_BUSY/100.0)*900.0;
    IF CPUTOTAL = 0 THEN DO;
                           NMISS + 1;
                           SYSTEM_T=0;BATCH_T=0;TSO_TIME=0;
                           VSPC_TIM=0;CICS_ADA=0;END;
                     ELSE DO;
    SYSTEM_T = (   (CPU1+SRB1)  /   (CPU+SRB)    ) * CPUTOTAL ;
    BATCH_T  = (   (CPU2+SRB2)  /   (CPU+SRB)    ) * CPUTOTAL ;
    TSO_TIME = (   (CPU3+SRB3)  /   (CPU+SRB)    ) * CPUTOTAL ;
    VSPC_TIM = (   (CPU4+SRB4)  /   (CPU+SRB)    ) * CPUTOTAL ;
    CICS_ADA = (   (CPU5+SRB5)  /   (CPU+SRB)    ) * CPUTOTAL ;
               END;
*******************************************************************;
*                                                                  ;
* ==> WORKLOAD ACTIVITY.STEP 7:                                    ;
*              ADD ON THE PROPER DESIGNATION FOR PRIME AND OFF     ;
*              SHIFT DATA.                                         ;
*******************************************************************;
DATA WORK.REPORT;  SET WORK.DAY;
          IF  (HOUR(TIME)  GE 8) AND (HOUR(TIME) LE 16)THEN SHIFT=1;
                                                       ELSE SHIFT=2;
   IF WEEKDAY(RPTDATE)   = 1          THEN SHIFT = 2;* SUNDAY        ;
   IF WEEKDAY(RPTDATE)   = 7          THEN SHIFT = 2;* SATURDAY      ;
   IF RPTDATE            = '1JAN82'D  THEN SHIFT = 2;* NEW YEARS     ;
   IF RPTDATE            = '15FEB82'D THEN SHIFT = 2;* WASHINGTON    ;
   IF RPTDATE            = '9APR82'D  THEN SHIFT = 2;* GOOD FRIDAY   ;
   IF RPTDATE            = '31MAY82'D THEN SHIFT = 2;* MEMORIAL DAY  ;
   IF RPTDATE            = '5JUL82'D  THEN SHIFT = 2;* JULY 4        ;
   IF RPTDATE            = '6SEP82'D  THEN SHIFT = 2;* LABOR DAY     ;
   IF RPTDATE            = '11OCT82'D THEN SHIFT = 2;* COLUMBUS DAY  ;
   IF RPTDATE            = '11NOV82'D THEN SHIFT = 2;* VETERANS'S DAY;
   IF RPTDATE            = '25NOV82'D THEN SHIFT = 2;* THANKSGIVING  ;
   IF RPTDATE            = '26NOV82'D THEN SHIFT = 2;* DAY AFTER     ;
   IF RPTDATE            = '24DEC82'D THEN SHIFT = 2;* CHRISTMAS     ;
          PROC FORMAT DDNAME=SASLIB ;
                      VALUE  SFT    1='PRIME SHIFT'
                                    2='OFF   SHIFT';
          PROC SORT DATA=WORK.REPORT;  BY SHIFT TIME;

**********************************************************************;
*                                                                     ;
* ==> WORKLOAD ACTIVITY.STEP 8:                                       ;
*     this next section assembles all the data necessary              ;
*                                                                     ;
*     to produce the daily plot of cpu utilization by                 ;
*                                                                     ;
*     workload type. it also updates the SAS dataset                  ;
*                                                                     ;
*     containing the hourly load values.                              ;
*                                                                     ;
*                                                                     ;
*                                                                     ;
**********************************************************************;
*******************************************************************;
*                                                                  ;
* ==> WORKLOAD ACTIVITY.STEP 8A:                                   ;
*              CREATE A DATASET WITH HOURLY REDUCED SYSTEM         ;
*              CPU MINUTE LOADS                                    ;
*******************************************************************;
data work.hsystem  (  rename = ( system_t  =  ctime0
                                cpu1      =  cpu0
                                mso1      =  mso0
                                io1       =  io0
                                srb1      =  srb0
                                notrans1  =  notrans0));
                                workload  = 'SYSTEM';
     set work.report ;keep system_t cpu1 mso1 io1 srb1 notrans1 NMISS
                           RPTDATE  time datetime hour workload shift;
         hour=hour(  time  ) + 1;
PROC sort data=work.hsystem; by hour;
data work.system     ;set work.hsystem   ;by hour;
     if first.hour then do;
     cputime=0;  cpu=0;  mso=0;  io=0;  srb=0;  notrans=0;  end;
     cputime+ctime0;cpu+cpu0;mso+mso0;io+io0;srb+srb0;notrans+notrans0;
     if last.hour then output;
*******************************************************************;
*                                                                  ;
* ==> WORKLOAD ACTIVITY.STEP 8B:                                   ;
*              CREATE A DATASET WITH HOURLY REDUCED BATCH          ;
*              CPU MINUTE LOADS                                    ;
*******************************************************************;
data work.hbatch   (  rename = ( batch_t   =  ctime0
                                cpu2      =  cpu0
                                mso2      =  mso0
                                io2       =  io0
                                srb2      =  srb0
                                notrans2  =  notrans0));
                                workload  = 'BATCH ';
     set work.report ;keep batch_t  cpu2 mso2 io2 srb2 notrans2 NMISS
                           RPTDATE  time datetime hour workload shift;
         hour=hour(   time ) + 1;
PROC sort data=work.hbatch ; by hour;
data work.batch      ;set work.hbatch    ;by hour;
     if first.hour then do;
     cputime=0;  cpu=0;  mso=0;  io=0;  srb=0;  notrans=0;  end;
     cputime+ctime0;cpu+cpu0;mso+mso0;io+io0;srb+srb0;notrans+notrans0;
     if last.hour then output;
*******************************************************************;
*                                                                  ;
* ==> WORKLOAD ACTIVITY.STEP 8C:                                   ;
*              CREATE A DATASET WITH HOURLY REDUCED BATCH          ;
*              CPU MINUTE LOADS                                    ;
*******************************************************************;
data work.htso     (  rename = ( tso_time  =  ctime0
                                cpu3      =  cpu0
                                mso3      =  mso0
                                io3       =  io0
                                srb3      =  srb0
                                notrans3  =  notrans0));
                                workload  = 'TSO   ';
     set work.report ;keep tso_time cpu3 mso3 io3 srb3 notrans3 NMISS
                           RPTDATE time datetime hour workload shift;
         hour=hour(  time  ) + 1;
PROC sort data=work.htso   ; by hour;
data work.tso        ;set work.htso      ;by hour;
     if first.hour then do;
     cputime=0;  cpu=0;  mso=0;  io=0;  srb=0;  notrans=0;  end;
     cputime+ctime0;cpu+cpu0;mso+mso0;io+io0;srb+srb0;notrans+notrans0;
     if last.hour then output;
*******************************************************************;
*                                                                  ;
* ==> WORKLOAD ACTIVITY.STEP 8D:                                   ;
*              CREATE A DATASET WITH HOURLY REDUCED VSPC           ;
*              CPU MINUTE LOADS                                    ;
*******************************************************************;
data work.hvspc    (  rename = ( vspc_tim  =  ctime0
                                cpu4      =  cpu0
                                mso4      =  mso0
                                io4       =  io0
                                srb4      =  srb0
                                notrans4  =  notrans0));
                                workload  = 'VSPC  ';
     set work.report ;keep vspc_tim cpu4 mso4 io4 srb4 notrans4 NMISS
                           RPTDATE time datetime hour workload shift;
         hour=hour(  time  ) + 1;
PROC sort data=work.hvspc  ; by hour;

data work.vspc       ;set work.hvspc     ;by hour;
     if first.hour then do;
     cputime=0;  cpu=0;  mso=0;  io=0;  srb=0;  notrans=0;  end;
     cputime+ctime0;cpu+cpu0;mso+mso0;io+io0;srb+srb0;notrans+notrans0;
     if last.hour then output;
*******************************************************************;
*                                                                  ;
* ==> WORKLOAD ACTIVITY.STEP 8E:                                   ;
*              CREATE A DATASET WITH HOURLY REDUCED CICS           ;
*              CPU MINUTE LOADS                                    ;
*******************************************************************;
data work.hcics    (  rename = ( cics_ada  =  ctime0
                                cpu5      =  cpu0
                                mso5      =  mso0
                                io5       =  io0
                                srb5      =  srb0
                                notrans5  =  notrans0));
                                workload  = 'CICS  ';
     set work.report ;keep cics_ada cpu5 mso5 io5 srb5 notrans5 NMISS
                           RPTDATE time datetime hour workload shift;
         hour=hour(  time  ) + 1;

PROC sort data=work.hcics  ; by hour;

data work.cics       ;set work.hcics     ;by hour;
     if first.hour then do;
     cputime=0;  cpu=0;  mso=0;  io=0;  srb=0;  notrans=0;  end;
     cputime+ctime0;cpu+cpu0;mso+mso0;io+io0;srb+srb0;notrans+notrans0;
     if last.hour then output;
*******************************************************************;
*                                                                  ;
* ==> WORKLOAD ACTIVITY.STEP 9 :                                   ;
*              MERGE TOGETHER THE DATASETS FOR EACH OF THE         ;
*              WORKLOAD COMPONENTS TO FORM ONE DATASET(HOURLY).    ;
*******************************************************************;

data WORK.HOURLY;
          drop time datetime cpu0 mso0 io0 srb0 notrans0 ctime0;

    SET WORK.system WORK.batch WORK.tso WORK.vspc WORK.cics;
*******************************************************************;
*                                                                  ;
* ==> WORKLOAD ACTIVITY.STEP 10:                                   ;
*              UPDATE THE HISTORY FILE OF HOURLY WORKLOAD VALUES   ;
*              WITH THE VALUES FOR TODAY                           ;
*                                                                  ;
*******************************************************************;
     PROC SORT DATA=USER._HOURLY;BY RPTDATE HOUR WORKLOAD;
     PROC SORT DATA= WORK.HOURLY;BY RPTDATE HOUR WORKLOAD;
     DATA           USER._HOURLY;
             UPDATE USER._HOURLY WORK.HOURLY;
                                 BY RPTDATE HOUR WORKLOAD;
PROC FORMAT DDNAME=SASLIB;
     PICTURE THOUS 0-HIGH =   '000,009K'  (MULT=.001);
***********************************************************************;
***********************************************************************;
***********************************************************************;
***********************************************************************;
*                                                                      ;
*   SORT THE DATA BY SHIFT                                             ;
*                                                                      ;
***********************************************************************;
PROC SORT DATA= WORK.HOURLY;
          BY WORKLOAD SHIFT HOUR;
***********************************************************************;
*                                                                      ;
*   CREATE A DATASET OF TOTALS FOR EACH SHIFT                          ;
*                                                                      ;
***********************************************************************;
DATA WORK.DAYTOT;  SET WORK.HOURLY;
                   BY WORKLOAD SHIFT HOUR;
                   DROP HOUR CPUTIME CPU MSO IO SRB NOTRANS;
      IF FIRST.SHIFT THEN DO;
              CPU_TIM = 0.0;CPU_SERV = 0.0;MSO_SERV = 0.0;
              IO_SERV = 0.0;SRB_SERV = 0.0;NUMTRAN  = 0.0;
      END;

      CPU_TIM + CPUTIME;CPU_SERV + CPU;MSO_SERV + MSO   ;
      IO_SERV + IO     ;SRB_SERV + SRB;NUMTRAN  +NOTRANS;

      IF LAST.SHIFT THEN OUTPUT;
      RETURN;
***********************************************************************;
*                                                                      ;
*   PRODUCE THE DAILY REPORT                                           ;
*                                                                      ;
***********************************************************************;
PROC SORT DATA=WORK.DAYTOT;
          BY SHIFT WORKLOAD;
DATA _NULL_;SET WORK.DAYTOT END=EOF;
            BY SHIFT WORKLOAD;
            FILE PRINT PRINT HEADER=H NOTITLES;
      IF _N_ = 1 THEN DO;
             CPU1  = 0.0; CPU2  = 0.0; CPUT  = 0.0;
             CPUS1 = 0.0; CPUS2 = 0.0; CPUST = 0.0;
             MSO1  = 0.0; MSO2  = 0.0; MSOT  = 0.0;
             IO1   = 0.0; IO2   = 0.0; IOT   = 0.0;
             SRB1  = 0.0; SRB2  = 0.0; SRBT  = 0.0;
      END;

      IF SHIFT = 1 THEN DO;
             CPU1 + CPU_TIM; CPUS1 + CPU_SERV; MSO1 + MSO_SERV;
             IO1  + IO_SERV; SRB1  + SRB_SERV;
      END;

      IF SHIFT = 2 THEN DO;
             CPU2 + CPU_TIM; CPUS2 + CPU_SERV; MSO2 + MSO_SERV;
             IO2  + IO_SERV; SRB2  + SRB_SERV;
      END;


***********************************************************************;
      CPUT + CPU_TIM; CPUST + CPU_SERV; MSOT + MSO_SERV;
      IOT  + IO_SERV; SRBT  + SRB_SERV;
      IF CPU_TIM  ^= 0 THEN DO;
               PUT      ' ';
               PUT      @5      WORKLOAD
                        @29     CPU_TIM                 8.1
                        @46     CPU_SERV            THOUS.
                        @63     MSO_SERV            THOUS.
                        @80     IO_SERV             THOUS.
                        @97     SRB_SERV            THOUS.
                        @114    NUMTRAN             THOUS.;
               END;
***********************************************************************;

      IF LAST.SHIFT AND SHIFT = 1 THEN DO;
            PUT      ' ';
            PUT      @5      'PRIME SHIFT TOTALS'
                     @29     CPU1                    8.1
                     @46     CPUS1               THOUS.
                     @63     MSO1                THOUS.
                     @80     IO1                 THOUS.
                     @97     SRB1                THOUS.;
            PUT      ' ';
            END;
      IF LAST.SHIFT AND SHIFT = 2 THEN DO;
            PUT      ' ';
            PUT      @5      'OFF SHIFT TOTALS'
                     @29     CPU2                    8.1
                     @46     CPUS2               THOUS.
                     @63     MSO2                THOUS.
                     @80     IO2                 THOUS.
                     @97     SRB2                THOUS.;
            PUT      ' ';
            PUT      ' ';
            PUT      @5      'DAILY TOTALS    '
                     @29     CPUT                    8.1
                     @46     CPUST               THOUS.
                     @63     MSOT                THOUS.
                     @80     IOT                 THOUS.
                     @97     SRBT                THOUS.;
            END;
      IF EOF AND (NMISS GT 0 ) THEN DO;
            PUT ' ';
            PUT ' ';
            PUT ' ';
            PUT      @10    'NOTE: THERE WERE '
                     @27     NMISS               2.
                     @31    '15 MINUTE INTERVALS WITH NO REPORTED '
                     @68    'CPU TIME.'          ;
            END;
      RETURN;

   H: PUT      @5      '-----------------------------------------------'


               @52     '----------------------------------------------'


               @98     '------------------------';
      PUT      ' ';
      PUT      @18     'ENGINEERING COMPUTER SERVICES - WORKLOAD'
               @58     ' SUMMARY REPORT FOR - '
               @79     RPTDATE WEEKDATE32.;
      PUT      ' ';
      PUT      @5      ' WORKLOAD'
               @26     '   TOTAL   '
               @41     '     CPU     '
               @58     '     MSO     '
               @75     '     I/0     '
               @92     '     SRB     '
               @110    '  NUMBER OF  ';
      PUT      ' ';
      PUT      @5      'COMPONENT'
               @26     'CPU SECONDS'
               @41     'SERVICE UNITS'
               @58     'SERVICE UNITS'
               @75     'SERVICE UNITS'
               @92     'SERVICE UNITS'
               @110    'TRANSACTIONS ';
      PUT      ' ';
      PUT      @5      '-----------------------------------------------'


               @52     '----------------------------------------------'


               @98     '------------------------';
      RETURN;
*******************************************************************;
*                                                                  ;
* ==> WORKLOAD ACTIVITY.STEP 11:                                   ;
*              DRAW THE PLOT OF HOURLY CPU MINUTE LOADS FOR        ;
*              THIS DAY.                                           ;
*******************************************************************;

 PROC sort data=WORK.hourly; by rptdate workload hour;

goptions hsize=11 vsize=8 border device=IBM3287F terminal;
options DEVADDR=(SAS,.,PRINTER1) ;
PROC gchart data=work.hourly;by rptdate;format rptdate weekdate.;
     vbar hour/sumvar=cputime discrete subgroup=workload
           ctext=black  caxis=black axis=7200;
     pattern1  c=blue    v=x5;
     pattern2  c=red     v=r5;
     pattern3  c=black   v=s;
     pattern4  c=green   v=l5;
     pattern5  c=blue    v=s;
title1 .h=2 DAILY PROFILE OF UTILIZATION ON THE ECS MAINFRAME SYSTEM;
Footnote1 ;


**********************************************************************;
*                                                                     ;
*                                                                     ;
*                                                                     ;
*     END OF THE SECTION FOR WORKLOAD UTILIZATION PLOT                ;
*                                                                     ;
*                                                                     ;
*                                                                     ;
*                                                                     ;
**********************************************************************;
 *  ALLOC DA('USER.TEMPO.BASE') FILE(IN) SHR ;;
PROC FORMAT DDNAME=SASLIB ;
    VALUE RSPFMT 1='0- 1.9'
                 2='2- 3.9'
                 3='4- 5.9 '
                 4='6- 7.9'
                 5='8- 9.9'
                 6='10-11.9'
                 7='12-13.9'
                 8='14-15.9'
                 9='16-17.9'
                10='18-19.9'
                11='>20' ;
  * SAS PROGRAM TO ANALYZE THE OUTPUT OF THE TEMPO DATABASE ;
  DATA WORK.TEMPO ;
      ARRAY XTIME (J) XACTN1-XACTN11 ;
      INFILE IN MISSOVER ;
      INPUT  TIME TODSTAMP8.
             VTAMID    $CHAR7.
             LOCATION  $CHAR15.
             +9   /* UNUSED FIELD */
             USERID    $CHAR7.
             DATE      MMDDYY6.
             (H1 H2 H3) (3*2.)
             (E1 E2 E3) (3*2.)
             AVG_RESP  6.1
             TRANSACT   6.
             THRESHLD   6.1
             OVERTHRS   6.
             MIN_RESP   6.1
             MAX_RESP   6.1
             TOTLRESP   6.1
            (XACTN1-XACTN11)(11*4.) ;
      DROP H1-H3 E1-E3 XACTN1-XACTN11 ;
FORMAT DATE DATE7. TIME TIME8. TIME_END TIME8. ;
    FORMAT INTERVAL RSPFMT.;
      TIME=HMS(H1,H2,H3) ;
      TIME_END=HMS(E1,E2,E3) ;
     DO OVER XTIME;
        RSPCOUNT = XTIME ;
        INTERVAL = J ;
        OUTPUT ;
     END ;
     RUN ;

 *----------------------------------------------------------------;

 PROC PRINTTO UNIT=60 ;        /* CHANGE BACK TO FICHE OUTPUT */

 PROC MEANS MAXDEC=1 DATA=WORK.TEMPO ;

 PROC GCHART GOUT=WORK.RESPLOT1 ;
        HBAR INTERVAL / FREQ=RSPCOUNT CAXIS=YELLOW CTEXT=GREEN DISCRETE
        NOZEROS SUBGROUP=INTERVAL TYPE=PERCENT ;
      TITLE1 .C=CYAN .F=ITALIC  NU Engineering Computer Services ;
      TITLE2 .C=CYAN .F=ITALIC .h=1.8 Terminal Response Time;
      TITLE3 .C=green  .F=DUPLEX .H=1.5 Cumulative Data Report  ;
      Title4 .c=green .f=duplex .h=1.3 Data is from a TEMPO Response Tim
e Monitor collecting data at the terminal ;
      Label   Interval=RESPONSE TIME INTERVAL IN SECONDS
              Reportdt=Date
              Vtamid=Terminal Location ;
       PATTERN1 V=S C=GREEN ;
       PATTERN2 V=L2 C=WHITE ;
       PATTERN3 V=R2 C=RED ;
       PATTERN4 V=X2 C=BLUE ;
       PATTERN5 V=L3 C=GREEN ;
       PATTERN6 V=R3 C=CYAN ;
       PATTERN7 V=X3 C=RED ;
       PATTERN8 V=E C=BLUE ;
       PATTERN9 V=R5 C=YELLOW;
       PATTERN10 V=L5 C=M1X ;
       PATTERN11 V=L5 C=M4X45 ;
       Footnote1 .h=1.2 .f=simplex FREQ=Number of Transactions ;
       Footnote2 .h=1.2 .f=simplex CUM FREQ=Cumulative Transaction total
       ;

 GOPTIONS DEVICE=VERSATEC  HSIZE=11 VSIZE=9  BORDER HBY=1.3
       FBY=ITALIC CBY=GREEN NOTERMINAL;

DATA WORK.TEMPO    ; /* Collect only current data */
    LENGTH  DEFAULT=4 ;
    SET RPT_DATE (IN=DATEIN)   WORK.TEMPO    ;
    RETAIN REPORTDT 0 ;
    IF DATEIN THEN REPORTDT = DATE_FOR ;
              ELSE IF DATE = REPORTDT THEN OUTPUT ;
              RETURN ;
    Drop DATE_FOR DTE_TME J ;
    Format REPORTDT DATE7. ;
    RUN ;

PROC SORT DATA=work.TEMPO ;
   BY reportdt VTAMID ;

 /*    make sure that there is some data in the daily dataset */
DATA WORK.RESPLOT2 ;
      * Dummy Data Set in the Event that there is no data *;


 PROC PRINT DATA=WORK.TEMPO ;
 * Print out daily Response Data ;

 PROC GCHART GOUT=WORK.RESPLOT2 DATA=WORK.TEMPO ;
      BY reportdt  VTAMID ;
      HBAR INTERVAL / FREQ=RSPCOUNT CAXIS=YELLOW CTEXT=GREEN DISCRETE
      NOZEROS SUBGROUP=INTERVAL TYPE=PERCENT ;
      FORMAT REPORTDT WEEKDATE32. VTAMID VTAMNAME. ;
      TITLE1 .C=CYAN .F=ITALIC  NU Engineering Computer Services ;
      TITLE2 .C=CYAN .F=ITALIC .h=1.8 Terminal Response Time;
      TITLE3 .C=green  .F=DUPLEX .H=1.5 Current Data Report  ;
      Title4 .c=green .f=duplex .h=1.3 Data is from a TEMPO Response Tim
e Monitor collecting data at the terminal ;
      Label   Interval=RESPONSE TIME INTERVAL IN SECONDS
              Reportdt=Date
              Vtamid=Terminal Location ;
      PATTERN1 V=S C=GREEN ;
      PATTERN2 V=L2 C=WHITE ;
      PATTERN3 V=R2 C=RED ;
      PATTERN4 V=X2 C=BLUE ;
      PATTERN5 V=L3 C=GREEN ;
      PATTERN6 V=R3 C=CYAN ;
      PATTERN7 V=X3 C=RED ;
      PATTERN8 V=E C=BLUE ;
      PATTERN9 V=R5 C=YELLOW;
      PATTERN10 V=L5 C=M1X ;
      PATTERN11 V=L5 C=M4X45 ;
  *
  * ;

 /*  FINAL STEP .... REPLAY ALL STORED VERSATEC PLOTS ..... */
 DATA WORK.PLOTS ;
    LENGTH  DEFAULT=4 ;
      SET WORK.CPU_PLOT
          WORK.TSO_PLOT /*PLUS ANY OTHER WORK. PLOTS USED */
          WORK.RESPLOT1
          WORK.RESPLOT2 ;
     *(CONCATENATE ALL SAVED PLOTS TOGETHER ) ;


PROC GREPLAY DATA=WORK.PLOTS ;

 GOPTIONS DEVICE=VERSATEC  HSIZE=11 VSIZE=9  BORDER HBY=1.3
       FBY=ITALIC CBY=GREEN terminal;

     /********** v s p c session report ************/
PROC SORT DATA=USER._VSPC_ON;
    BY VSPC_ID DATETIME;

PROC SORT DATA=USER._VSPC_OF;
    BY VSPC_ID DATETIME;


    /*                                                         */
    /*    SEPARATE THE VSPC LOGON   RECORDS FOR THIS DATE AND TIME  */
    /*                                                         */
DATA WORK.ONVSPC  ;
 SET RPT_DATE(IN=DAY) _VSPC_ON;
    LENGTH DEFAULT=4;
    RETAIN REPORTDT 0 ;
    IF DAY    THEN REPORTDT = DATE_FOR ;
              ELSE IF DATEPART(DATETIME) ^= REPORTDT THEN DELETE ;

DATA WORK.OFFVSPC  ;
 SET RPT_DATE(IN=DAY) _VSPC_OF;
    LENGTH DEFAULT=4;
    RETAIN REPORTDT 0 ;
    IF DAY    THEN REPORTDT = DATE_FOR ;
              ELSE IF DATEPART(DATETIME) ^= REPORTDT THEN DELETE ;

DATA _null_       ;
 SET  WORK.ONVSPC(IN=LOGON) END=QUIT WORK.OFFVSPC(IN=LOGOFF) END=QUIT;
 BY VSPC_ID DATETIME;
 IF VSPC_ID= . THEN DELETE;
 FORMAT TERMINAL $8.;
 WEEKDATE = REPORTDT   ;
    /*                                                            */
    /*  GET THE DATE FOR THE REPORT FROM FILE CREATED AT THE TIME */
    /*  THE JOB IS SUBMITTED                                      */
    /*                                                            */
    LENGTH DEFAULT=4 TERM $8 ;
    FILE PRINT HEADER=H NOTITLES LINESLEFT=LLEFT ;
       RETAIN TERM ' ' START 0 CPUTOT 0 IOTOT 0 GCPUTOT 0 VSPCTOT 0
              DURTOT 0 TPTOT 0 GVSPCTOT 0 GDURTOT 0 GTPTOT 0
              GIOTOT 0 GUTOT 0 ;

    IF LLEFT < 6 THEN PUT _PAGE_ ;

    IF LOGON THEN DO;
       TERM=TERMINAL;
       START=DATETIME;
     END;

    IF LOGOFF THEN DO;
       OFF=TIMEPART(DATETIME);
       ON=TIMEPART(START);
       FORMAT ON OFF TIME12.;
       PUT @20 VSPC_ID @30 TERM  @40 ON TIME12. @60 OFF TIME12.
           @81 SESSTIME TIME12. @100 CPU_TIME 4.2 @110 DASDEXCP 4.
           @122 TP_EXCP 4. ;
       TERM=' ';
       START= . ;
       CPUTOT=CPU_TIME+CPUTOT;
       IOTOT=IOTOT+DASDEXCP;
       TPTOT=TPTOT+TP_EXCP;
       DURTOT=DURTOT+SESSTIME;
       VSPCTOT=VSPCTOT+1;
       GVSPCTOT=GVSPCTOT+1;
       GDURTOT=SESSTIME+GDURTOT;
       GCPUTOT=CPU_TIME+GCPUTOT;
       GIOTOT=DASDEXCP+GIOTOT;
       GTPTOT=TP_EXCP+GTPTOT;
     END ;


IF LAST.VSPC_ID  THEN DO ;
    GUTOT=GUTOT+1 ;   /* grand total of users */
    PUT @05 '
                     -----------------------------------------     '
       /@20 'TOTAL FOR USER #  '   VSPC_ID  5.0
       @50 'NO. OF SESSIONS= '     VSPCTOT  4.0
        @81 DURTOT TIME12. @100 CPUTOT 4.2 @110 IOTOT 4. @122 TPTOT 4.
       /@05 '               ____________________________________________
______________________________________________________________     '//;
    CPUTOT=0; IOTOT=0; TPTOT=0; DURTOT=0; VSPCTOT=0; TERM=' ';
    START=. ;
END ;


IF QUIT  THEN DO ;
    PUT @05 '===========================================================
==================================================================='
       /@03 'TOTAL NO. OF USERS=' GUTOT 5.0
        @30 'TOTAL SESSIONS='     GVSPCTOT  5.0
        @53 '***** GRAND TOTAL ******'
        @81 GDURTOT TIME12. @99 GCPUTOT 5.2 @110 GIOTOT 5.
        @122 GTPTOT 5.
       /@05 '===========================================================
===================================================================';
     CPUTOT=0; GIOTOT=0; GTPTOT=0; GDURTOT=0; GVSPCTOT=0;
END;

RETURN;


H:  PUT @57 'VSPC  SESSION  REPORT' /@50 WEEKDATE WEEKDATE32.
        ///@21 'VSPC' @30 'TERMINAL' @45 'SESSION' @65 'SESSION'
           @85 'SESSION' @100 'CPU' @110 'DASD' @120 'TERMINAL'
          /@21 'USER' @46 'START' @67 'STOP'
           @85 'DURATION' @100 'TIME' @110 'EXCP' @122 'I/O'
          /@20 'NUMBER' @87 '(HMS)' @100 '(SEC)' @ ;
    PUT    @05 '               _________________________________________
_________________________________________________________________     ';

     RETURN ;
     RUN ;
*______________________________________________________________________;


  PROC PRINTTO ;       /* CHANGE PRINT OPTION BACK TO PRINTER */

  DATA _NULL_ ;
     INFILE FT45F001 ;
     INPUT ;
     FILE FT60F001 NOPRINT ;
     PUT _INFILE_ ;
     FILE PRINT NOPRINT ;
     PUT _INFILE_ ;

/*
//*
//*
//STEP2.SYSVECTR   DD  SYSOUT=V,COPIES=1
/*
