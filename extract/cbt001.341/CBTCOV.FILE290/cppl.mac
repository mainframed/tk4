CPPL     TITLE      'RETURN ADDR OF CPPL TO CALLER'
         EQUREGS
CPPL     CSECT
         B         10(R15)             BRANCH AROUND ID
         DC        AL1(4)              LENGTH
         DC        CL5'CPPL'           ID
         STM       R14,R12,12(R13)     SAVE CALLERS REGS
         LR        R11,R15             SET UP BASE
         USING     CPPL,R11            REGISTER
*********
*   CPPL EXPECTS 4 ARGUMENTS AS INPUT/OUTPUT.
*        THEY CAN BE ANY 4 BYTE NON-DOPE VECTOR
*        PL/I DATA TYPES. MOST LIKELY TYPE WILL
*        BE POINTER VARIABLES. NO CHECK IS MADE
*        ON WHETHER THE FOUR ARGUMENTS ARE PRESENT,
*        IT IS ASSUMED THEY ARE. INCORRECT RESULTS
*        WILL OCCUR IF THERE ARE NOT FOUR ARGUMENTS.
*********
         LM        R2,R5,0(R1)         GET A(4 POINTERS INPUT)
         L         R10,PLIST           A(PLISTART)
         CLC       MYZAP(4),10(R10)    IS MY ZAP ON FOR COM PROCESSORS
         BNE       NULLS               NO, SET UP NULL POINTERS
         L         R10,124(R10)        GET A(CPPL) PASSED FROM TSO
         LTR       R10,R10             WAS IT INVOKED AS A CP
         BZ        NULLS               NO, SET UP NULL POINTERS
         LM        R6,R9,0(R10)        GET A(CPPL ECT PSCB UPT)
STORE    ST        R6,0(R2)            SAVE A(CPPL)
         ST        R7,0(R3)            SAVE A(ECT)
         ST        R8,0(R4)            SAVE A(PSCB)
         ST        R9,0(R5)            SAVE A(UPT)
         LM        R14,R12,12(R13)     RESTORE REGS
         BR        R14                 LEAVE
NULLS    LM        R6,R9,NULLP         GET 4 NULL POINTERS
         B         STORE               GO SAVE THE POINTERS
PLIST    DC        V(PLISTART)
MYZAP    DC        XL4'4560F04E'
NULLP    DC        XL4'FF000000'
         DC        XL4'FF000000'
         DC        XL4'FF000000'
         DC        XL4'FF000000'
         END       CPPL
