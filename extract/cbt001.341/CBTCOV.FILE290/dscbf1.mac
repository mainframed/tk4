         MACRO
         IDENT
         LCLC      &I,&B
&B       SETC      ' '
&I       SETC      '&SYSECT&B&SYSDATE&B&SYSTIME'
         SAVE      (14,12),,&I
         MEND
DSCBF1   START     0
*
*        CALL THIS ENTRY TO READ A FORMAT 1 DSCB FROM THE VTOC.
*        CALLING SEQUENCE:
*        DCL DSN CHAR(44);  /* DATA SET NAME */
*        DCL VOL CHAR(6);  /* VOLUME ON WHICH DATASET RESIDES */
*        DCL DSCB CHAR(96);  /* DATA PORTION OF FORMAT 1 DSCB */
*        DCL ORC FIXED BIN(31);  /* OBTAIN MACRO RETURN CODE */
*        CALL DSCBF1(DSN,VOL,DSCB,ORC);
*
         IDENT
         LR        R10,R15  BASE REGISTER
         USING     DSCBF1,R10  BASE ADDRESSABILITY
         LR        R2,R1  SAVE PARAMETER REGISTER
         GETMAIN   R,LV=WASIZE  GET WORK AREA
         XC        0(WASIZE,R1),0(R1)  CLEAR WORK AREA
         ST        R1,8(R13)  STORE FORWARD POINTER
         ST        R13,4(R1)  STORE BACKWARD POINTER
         LR        R13,R1  SET UP REG FOR SAVE AREA
         USING     WORK,R13  ESTABLISH DSECT ADDRESSABILITY
         LR        R1,R2  RESTORE PARAMETER REGISTER
         LR        R9,R1  R9->PARAMETER LIST
         USING     PARAM,R9  ADDRESSABILITY FOR DSECT PARAM
         MVC       CAMLST(LENCAM),PCAM  INITIALIZE CAMLST
         L         R2,UDSN  R2->DSN SDV
         L         R2,0(R2)  R2->DSN
         ST        R2,CAMLST+4  STORE DSN PTR
         L         R2,UVOL  R2->VOL SDV
         L         R2,0(R2)  R2->VOL
         ST        R2,CAMLST+8  STORE VOL PTR
         LA        R2,WAREA  R2->OBTAIN WORK AREA
         ST        R2,CAMLST+12  STORE WORK AREA PTR
         OBTAIN    CAMLST  FIND FORMAT 1 DSCB
         L         R2,UDSCB  R2->DSCB SDV
         L         R2,0(R2)  R2->STRING PROVIDED FOR DSCB
         MVC       0(96,R2),WAREA  RETURN DATA PORTION OF DSCB
         L         R2,UORC  R2->WORD PROVIDED FOR OBTAIN RETURN CODE
         ST        R15,0(R2)  SET RETURN CODE FROM OBTAIN
         LR        R1,R13  R1->WORK AREA
         L         R13,4(R13)  RESTORE R13
         FREEMAIN  R,LV=WASIZE,A=(1)  FREE WORK AREA
         RETURN    (14,12),T,RC=0
PCAM     CAMLST    SEARCH,0,0,0  PROTOTYPE CAMLST
LENCAM   EQU       *-PCAM
         EQUREGS
         EJECT
*
*        WORK AREA (ADDRESSED FROM R13)
*
WORK     DSECT
SAVE     DS        20F  SAVE AREA
CAMLST   DS        XL(LENCAM)  CAMLST FOR OBTAIN
         DS        0D  DOUBLE WORD ALIGN WORK AREA
WAREA    DS        148C  WORK AREA FOR OBTAIN
WASIZE   EQU       *-WORK  SIZE OF DSECT
PARAM    DSECT
UDSN     DS        A  PTR TO DSN SDV
UVOL     DS        A  PTR TO VOL SDV
UDSCB    DS        A  PTR TO DSCB SDV
UORC     DS        A  PTR TO OBTAIN RETURN CODE
         END
