BUAD     TITLE     'PL/I ROUTINE TO ENCODE/DECODE 3270 CURSOR ADDR'
         EQUREGS
         SPACE     3
BUFADD   CSECT
         B         12(R15)             BRANCH AROUND ID
         DC        AL1(6)              LENGTH
         DC        CL7'BUFADD'         ID
         STM       R14,R12,12(R13)     SAVE REGS
         LR        R11,R15             SET UP BASE
         USING     BUFADD,R11          REGISTER
         LM        R5,R7,0(R1)         A(ROW,COL,SDV)
         L         R8,0(R7)            A(STRING)
         TM        6(R7),X'80'         IS STRING VARYING
         BZ        START               NO, GO
         LA        R8,2(R8)            YES, POINT TO STRING
START    LA        R2,0                CLEAR WORK
         LR        R3,R2               REGISTERS
         LA        R4,X'3F'            MASK TO GET RID OF TOP 2 BITS
         IC        R2,0(R8)            GET 1ST ADDR BYTE
         IC        R3,1(R8)            GET 2ND ADDR BYTE
         NR        R2,R4               GET RID OF
         NR        R3,R4               BITS 0 AND 1
         SLL       R2,6                MOVE OVER 6 BITS
         OR        R3,R2               CONCATENATE 1ST AND 2ND ADDR
         LA        R4,80               DIVISOR
         LA        R2,0
         DR        R2,R4               GET ROW AND COL
         LA        R2,1(R2)            COL+1 FOR 1-INDEXING
         LA        R3,1(R3)            ROW+1 FOR 1-INDEXING
         STH       R3,0(R5)            SAVE ROW
         STH       R2,0(R6)            SAVE COL
         LM        R14,R12,12(R13)     RESTORE REGS
         LA        R15,0               SET ALL OKAY
         MVI       12(R13),X'FF'       SET WERE DONE
         BR        R14                 LEAVE
         EJECT
         ENTRY     BUFADE
BUFADE   B         12(R15)             BRANCH AROUND ID
         DC        AL1(6)              LENGTH
         DC        CL7'BUFADE'         ID
         STM       R14,R12,12(R13)     SAVE REGS
         LR        R11,R15             SET UP BASE
         USING     BUFADE,R11          REGISTERS
         LM        R5,R7,0(R1)         A(ROW,COL,SDV)
         L         R8,0(R7)            A(STRING)
         TM        6(R7),X'80'         IS STRING VARYING
         BZ        READY               NO, GO
         LA        R4,2                SET VARYING STRING
         STH       R4,0(R8)            LENGTH TO 2
         LA        R8,2(R8)            POINT TO STRING
READY    LH        R2,0(R5)            ROW
         LH        R3,0(R6)            COL
         BCTR      R2,R0               ROW-1  0-ORIGIN
         BCTR      R3,R0               COL-1  0-ORIGIN
         LA        R4,64               CODE CHARACTERS ARE MOD 64
         MH        R2,H80              (ROW-1)*80
         AR        R3,R2               +(COL-1)=BUFFER ADDR FROM 0.
         LA        R2,0
         DR        R2,R4               GET INDEX FOR CODE TABLE
         IC        R3,CODES(R3)        ENCODED 1ST BUFFER ADDR
         IC        R2,CODES(R2)        ENCODED 2ND BUFFER ADDR
         STC       R3,0(R8)            SAVE ADDR
         STC       R2,1(R8)            VALUES
         LM        R14,R12,12(R13)     RESTORE REGS
         LA        R15,0               SET ALL OKAY
         MVI       12(R13),X'FF'       SAY WERE DONE
         BR        R14                 LEAVE
H80      DC        H'80'
         ENTRY     CODES
CODES    DC        XL8'40C1C2C3C4C5C6C7'   CONTROL CHARACTERS
         DC        XL8'C8C94A4B4C4D4E4F'   I/O CODES FROM
         DC        XL8'50D1D2D3D4D5D6D7'   PAGE 2-11 OF
         DC        XL8'D8D95A5B5C5D5E5F'   GA27-2749-7
         DC        XL8'6061E2E3E4E5E6E7'   USED FOR ATTRIBUTE,AID
         DC        XL8'E8E96A6B6C6D6E6F'   ,WCC,CCC,CU AND
         DC        XL8'F0F1F2F3F4F5F6F7'   DEVICE ADDR AND BUFFER
         DC        XL8'F8F97A7B7C7D7E7F'   ADDRESS.
         END       BUFADD
