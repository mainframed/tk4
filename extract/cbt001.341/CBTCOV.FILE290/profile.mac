PROFILE  PRIME     BR=10,SA=SAVE
*
*        CALL THIS ENTRY TO DETERMINE OPERATING MODE
*        AND TERMINAL CHARACTERISTICS.
*        CALLING SEQUENCE:
*        DCL (MODE,DISPLAY,LNSZ,LNNO) FIXED BIN(15);
*        CALL PROFILE(MODE,DISPLAY,LNSZ,LNNO);
*        MODE=0          BATCH (ALL OTHER ARGS SET TO ZERO)
*            =1          TSO
*        DISPLAY=0       NOT A DISPLAY TERMINAL
*               =1       DISPLAY TERMINAL
*        LNSZ            TERMINAL LINESIZE
*        LNNO            TERMINAL SCREEN LENGTH (IF DISPLAY)
*
         LM        R2,R5,0(R1)  LOAD ARGUMENT ADDRESSES
         SR        R0,R0  R0<-0
         STH       R0,0(R2)  MODE=BATCH
         STH       R0,0(R3)  TERMINAL IS NOT DISPLAY
         STH       R0,0(R4)  LINESIZE IS ZERO
         STH       R0,0(R5)  SCREEN LENGTH IS ZERO
         L         R6,548  ASCB OLD POINTER
         CLC       60(4,R6),ZERO  IS THERE A TSB ADDRESS?
         BE        RET  NO, WE MUST BE RUNNING IN BATCH
         LA        R0,1  R0<-1
         STH       R0,0(R2)  MODE=TSO
         GTSIZE
*        REGS ON RETURN FROM GTSIZE MACRO:
*        R1        LINESIZE
*        R0        0 IF NOT DISPLAY / SCREEN LENGTH IF DISPLAY
         STH       R1,0(R4)  RETURN TERMINAL LINESIZE
         LTR       R0,R0  IS THIS A DISPLAY TERMINAL?
         BZ        RET  BRANCH IF NO
         STH       R0,0(R5)  RETURN SCREEN LENGTH
         LA        R0,1  R0<-1
         STH       R0,0(R3)  SET TERMINAL TYPE TO DISPLAY
RET      TERME
SAVE     DC        20F'0'
ZERO     DC        F'0'
         EQUREGS
         END
