HEXOUT   PRIME     BR=10,SA=SAVE
         LM        R2,R3,0(R1)  ADDR OF SDV'S
         TM        6(R2),X'80'
         BZ        FIXEDS1
VARS1    L         R7,0(R2)  ADDR S1 PREFIX
         LH        R4,0(R7)  CURRENT LENGTH OF S1
         ST        R4,I
         LA        R7,2(R7)  CHOB AND POINT TO STRING
         B         COMS1
FIXEDS1  L         R7,0(R2)  ADDR S1
         LA        R7,0(R7)  CHOB
         LH        R4,4(R2)  CURRENT LENGTH OF S1
         ST        R4,I
COMS1    DS        0H
         TM        6(R3),X'80'
         BZ        FIXEDS2
VARS2    L         R8,0(R3)  ADDR S2 PREFIX
         LH        R4,0(R8)  CURRENT LENGTH OF S2
         ST        R4,J
         LA        R8,2(R8)  CHOB AND POINT TO STRING
         B         COMS2
FIXEDS2  L         R8,0(R3)  ADDR S2
         LA        R8,0(R8)  CHOB
         LH        R4,4(R3)  CURRENT LENGTH OF S2
         ST        R4,J
COMS2    DS        0H
         LA        R6,1  JJ=1
         LR        R5,R6  II=1
         L         R2,I
         LTR       R2,R2  I=0?
         BZ        TESTOUT
         L         R2,J
         LTR       R2,R2  J=0?
         BZ        TERME
UNPACK   UNPK      S3(15),0(8,R7)
*
*        NOTE TO SYSTEM PROGRAMMER:
*        THERE IS THE REMOTE POSSIBILITY THAT THE ABOVE "UNPK"
*        INSTRUCTION COULD CAUSE DIFFICULTIES. THE LENGTH OF DATA TO
*        BE UNPACKED IS ALWAYS 8 BYTES AND THIS FIELD WILL EXTEND
*        BETWEEN 1 AND 7 BYTES BEYOND THE END OF THE INPUT STRING (S1)
*        DEPENDING UPON THE LENGTH OF THE INPUT STRING. THUS THIS
*        INSTRUCTION COULD ATTEMPT TO REFERENCE DATA IN FREE STORAGE
*        OR EVEN PERHAPS OUTSIDE THE USERS REGION. THE SOLUTION TO
*        THIS (AT THE COST OF EFFICIENCY) WOULD BE TO MOVE THE
*        APPROPRIATE NUMBER OF BYTES (MAX OF 7) FROM S1 TO AN
*        INTERNAL WORK STRING OF LENGTH 8 AND THEN UNPACK FROM
*        THIS NEW WORK STRING.
*
         TR        S3(14),TABLE-X'F0'
         L         R0,FOURTEEN
         L         R1,I
         SR        R1,R5  I-II
         LA        R1,1(R1)  I-II+1
         SLL       R1,1  2*(I-II+1)
         BAL       R9,MIN
         ST        R0,K  K=MIN(14,2*(I-II+1))
         L         R0,FOURTEEN
         L         R1,J
         SR        R1,R6  J-JJ
         LA        R1,1(R1)  J-JJ+1
         BAL       R9,MIN
         ST        R0,L  L=MIN(14,J-JJ+1)
         L         R0,K
         L         R1,L
         BAL       R9,MIN
         ST        R0,M  M=MIN(K,L)
         L         R4,M
         BCTR      R4,0  LENGTH FOR EXECUTE = M-1
         EX        R4,MVC1  SUBSTR(S2,JJ,M)=S3
         A         R5,SEVEN  II=II+7
         A         R7,SEVEN
         A         R6,M  JJ=JJ+M
         A         R8,M
         C         R5,I  II>I?
         BH        TESTOUT
         C         R6,J  JJ>J?
         BH        TERME
         B         UNPACK
TESTOUT  C         R6,J  JJ>J?
         BH        TERME
         MVI       0(R8),X'40'  SUBSTR(S2,JJ,1) = ' '
         LA        R6,1(R6)  JJ=JJ+1
         LA        R8,1(R8)
         B         TESTOUT
TERME    TERME
MIN      CR        R0,R1
         BL        RET
         LR        R0,R1
RET      BR        R9
         EJECT
         EQUREGS
SAVE     DC        20F'0'
SEVEN    DC        F'7'
FOURTEEN DC        F'14'
I        DS        F
J        DS        F
K        DS        F
L        DS        F
M        DS        F
MVC1     MVC       0(0,R8),S3
S3       DS        0F,CL15
TABLE    DS        0F
         DC        C'0123456789ABCDEF'
         END
