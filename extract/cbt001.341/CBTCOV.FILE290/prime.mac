           MACRO
&LABEL     PRIME &SA=,&BR=12,&START=0,&ID=*,&LV=72,&ENTRY=NO
           GBLA  &SIZE
           GBLB  &SASW
           LCLC  &NAME
&SIZE      SETA  &LV
&SASW      SETB  0
&NAME      SETC  '&LABEL'
           AIF   ('&ENTRY' EQ 'YES').CONT1
&NAME      SETC  ''
           AIF   (&SYSNDX EQ 1).START
&LABEL     CSECT
           AGO   .CONT1
.START     ANOP
&LABEL     START &START
.CONT1     ANOP
           AIF   ('&ID' EQ 'NO').SETNOID
&NAME      SAVE  (14,12),,&ID    STORE ALL REGISTERS & FORM IDENTIFIER
           AGO   .CONT2
.SETNOID   ANOP
&NAME      SAVE  (14,12)
.CONT2     ANOP
           BALR  &BR,0  CREATE BASE REGISTER
           USING *,&BR
           AIF   ('&SA' EQ '').DYNMC
           LR    2,1 SAVE PARAMETER REGISTER
           LA    1,&SA GET ADDRESS OF SAVE AREA
           ST    1,8(13)  STORE FORWARD POINTER
           ST    13,4(1)  STORE BACKWARD POINTER
           LR    13,1  SET UP REG FOR SAVE AREA
           LR    1,2   RESTORE PARAMETER REGISTER
           AGO   .END
.DYNMC     ANOP
&SASW      SETB  1
           LR    2,1 SAVE PARAMETER REGISTER
           GETMAIN R,LV=&SIZE
           ST    1,8(13) STORE FORWARD POINTER
           ST    13,4(1) STORE BACKWARD POINTER
           LR    13,1    SET UP REGISTER FOR SAVE AREA
           LR    1,2  RESTORE PARAMETER REGISTER
.END       ANOP
           MEND
