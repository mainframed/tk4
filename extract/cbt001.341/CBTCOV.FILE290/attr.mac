ATTR     TITLE     'PL/I ROUTINE TO SET 3270 FIELD ATTRIBUTE BYTE'
         EQUREGS
ATTR     CSECT
         B         10(R15)             BRANCH AROUND ID
         DC        AL1(4)              LENGTH
         DC        CL5'ATTR'           ID
         STM       R14,R12,12(R13)     SAVE REGS
         LR        R2,R15              SET UP BASE
         USING     ATTR,R2             REGISTER
         L         R3,CODE             A(CODE TABLE)
         LM        R4,R8,0(R1)         A(P,A,D,M,SDV)
         LH        R4,0(R4)            GET P FIELD
         LH        R5,0(R5)            GET A FIELD
         LH        R6,0(R6)            GET D FIELD
         LH        R7,0(R7)            GET M FIELD
         N         R4,M1               GET RID OF ALL EXCEPT LAST BIT
         N         R5,M1               GET RID OF ALL EXCEPT LAST BIT
         N         R6,M2               GET RID OF ALL EXCEPT LAST TWO
         N         R7,M1               GET RID OF ALL EXCEPT LAST BIT
         SLL       R4,5                MOVE BIT TO RIGHT PLACE
         SLL       R5,4                MOVE BIT TO RIGHT PLACE
         SLL       R6,2                MOVE BITS TO RIGHT PLACE
         OR        R7,R4               CONSTRUCT ATTRIBUTE BYTE
         OR        R7,R5               CONSTRUCT ATTRIBUTE BYTE
         OR        R7,R6               CONSTRUCT ATTRIBUTE BYTE
         IC        R7,0(R7,R3)         GET CODE BYTE
         L         R4,0(R8)            A(STRING)
         TM        6(R8),X'80'         IS STRING VARYING
         BZ        GO                  NO, GO DO IT
         LA        R4,2(R4)            POINT TO STRING
GO       STC       R7,0(R4)            SAVE CODE BYTE
         LM        R14,R12,12(R13)     RESTORE REGS
         LA        R15,0               SET ALL OKAY
         MVI       12(R13),X'FF'       SAY WERE DONE
         BR        R14                 LEAVE
CODE     DC        V(CODES)
M1       DC        XL4'00000001'
M2       DC        XL4'00000003'
         END       ATTR
