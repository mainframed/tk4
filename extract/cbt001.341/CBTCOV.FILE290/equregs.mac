         MACRO
&LAB EQUREGS &L=R,&DO=(0,15,1),&SYM=,&FROM=0,&TO=15,&SYMB=R,&LIMIT=15
          LCLA  &I,&J,&K      COUNTER,INCREMENT,SYM COUNTER FOR ASSIST
          LCLA  &COUNT,&LMT     LOCALS FOR UNB VERSION
          LCLB  &ASSW,&UNSW   SWITCHES TO DISTINGUISH TWO VERSIONS
.*                    MODIFIED BY BJ.CLAUS FEB 10/76 TO INCORPORATE
.*                            A U N B VERSION WITH DIFFERENT ARGUMENTS
.*                            WRITTEN AND DOCUMENTED BY U G GUJAR
.*                    AND THE ASSIST VERSION WHICH WAS DISTRIBUTED
.*                        FROM PENN STATE UNIVERSITY.....
          AIF ('&L' EQ 'R' AND &DO(1) EQ 0 AND &DO(2) EQ 15 AND &DO(3) *
               EQ 1 AND '&SYM' EQ '').QUNB IF ANY UNB PARAMETERS ARE
&ASSW     SETB  1    SPECIFIED, SET THE SWITCH ON....
.QUNB     AIF  (&FROM EQ 0 AND &TO EQ 15 AND '&SYMB' EQ 'R' AND &LIMIT *
               EQ 15).QTEST   IF ANY ASSIST PARAMETERS ARE
&UNSW     SETB  1   SPECIFIED, SET SWITCH ON.....
.QTEST    AIF  ( NOT (&ASSW AND &UNSW)).OK  BOTH CAN T BE ON...
          MNOTE 0,' CONFLICT BETWEEN ASSIST AND UNB ARGUMENTS'
          MEXIT
.OK       AIF  (&UNSW).UNBEXP   GO EXPAND USING UNB EQUATE
.*       MACRO FOR SETTING UP SETS OF REGISTER EQUATES.               *
.*----->  MACRO:  EQUREGS  GENERATE SYMBOLIC REGISTER EQUATES ......
.*                          JOHN B. MASHEY/JULY'69/PSU 360/67         *
.*       *** ARGUMENTS ***                                            *
.*       L=        SYMBOL USED TO BEGIN EQUATES, SUCH AS R, REG,ETC.  *
.*       DO=       (INITIAL,LIMIT,INCREMENT) WILL SET UP REGISTERS    *
.*             EQUATED TO THE VALUE AS CONTROLLED BY THE DO PARAMATER.*
.*             BEHAVES LIKE FORTRAN DO, INCLUDING ABILITY TO LEAVE OUT*
.*             INCREMENT.                                             *
.*       SYM=      LIST OF SYMBOLS TO BE CONCATENATED TO L PARM.      *
.*             LIST WILL SET UP EQUATES INCLUDING SYM VALUES, FOR     *
.*             FIRST SET OF EQUATES IN LIST, AND WILL THEN SET UP     *
.*             NUMERIC EQUATES IF DO VALUES EXCEED NUMBER OF ELEMENTS *
.*             IN SYM OPERAND.  MAY BE OMITTED ENTIRELY.              *
.*  * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         AIF   (N'&DO LT 2).XERROR           NOT ENOUGH ARGUMENTS-ERR
&K       SETA  1                   INIT
&I       SETA  &DO(1)              SET TO INITIAL VALUE
&J       SETA  1                   SET TO DEFAULT VALUE
         AIF   (N'&DO LT 3).XLOOP            DEFAULT VALUE IS OK
&J       SETA  &DO(3)              USE VALUE PROVIDED
.XLOOP   AIF   ('&SYM(&K)' EQ '').XLOOP1     USE NUMBER IF NO SYM VAL
&L&SYM(&K) EQU &I
&K       SETA  &K+1      INCREMENT TO GET NEXT SYM OPERAND
         AGO   .XLOOP2             SKIP OVER NORMAL GENRATION
.XLOOP1  ANOP
&L&I     EQU   &I
.XLOOP2  ANOP
&I       SETA  &I+&J               ADD INCREMENT TO COUNTER
         AIF   (&I LE &DO(2)).XLOOP          CONTINUE UNTIL DONE
         MEXIT
.XERROR  MNOTE 0,'** ERROR - EQUREGS REQUIRES AT LEAST 2 VALUES IN DO'
         MEXIT
.*-----   U N B VERSION OF EQUREGS,  WRITTEN BY U G GUJAR
.*
.UNBEXP  ANOP
&COUNT   SETA  &FROM
         AIF   (&TO GE &FROM).FINE
         AIF   (&FROM LE &LIMIT).LETGO
         MNOTE *,'TO<FROM AND FROM>LIMIT; 0 TO &TO EQUATES GENERATED'
         AGO   .ASSUME0
.LETGO   ANOP
.* GENERATE EQUATES FROM &FROM TO &LIMIT
&LMT     SETA  &LIMIT
.LOOP1   ANOP
&SYMB&COUNT EQU &COUNT
&COUNT   SETA  &COUNT+1
         AIF   (&LMT GE &COUNT).LOOP1
.ASSUME0 ANOP
&COUNT   SETA  0
.* GENERATE &COUNT (I.E. 0 OR &FROM) TO &TO EQUATES
.FINE    ANOP
&LMT     SETA  &TO
.LOOP2   ANOP
&SYMB&COUNT EQU &COUNT
&COUNT   SETA  &COUNT+1
         AIF   (&LMT GE &COUNT).LOOP2
         SPACE 4
         MEND
