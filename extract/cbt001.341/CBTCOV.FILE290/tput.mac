PTGT     TITLE     'PL/I SUBROUTINE FOR 3270 I/O'
         EQUREGS
TPUT     CSECT
         ENTRY     TGET
         B         10(R15)             BRANCH AROUND ID
         DC        AL1(4)              LENGTH
         DC        CL5'TPUT'           ID
         STM       R14,R12,12(R13)     SAVE REGS
         LR        R2,R15              SET UP BASE
         USING     TPUT,R2             REGISTER
         LM        R5,R6,0(R1)         A(SDV,RC)
         L         R3,0(R5)            A(STRING)
         LH        R4,4(R5)            MAX(CURRENT) LENGTH
         TM        6(R5),X'80'         IS STRING VARYING
         BZ        READY               NO, GO DO WRITE
         LH        R4,0(R3)            GET CURRENT LENGTH
         LA        R3,2(R3)            POINT TO STRING
READY    TPUT      (R3),(R4),FULLSCR   WRITE TO 3270
         STH       R15,0(R6)           SAVE RC
         LM        R14,R12,12(R13)     RESTORE REGS
         LA        R15,0               SET ALL OKAY
         MVI       12(R13),X'FF'       SAY WERE DONE
         BR        R14                 LEAVE
         EJECT
TGET     B         10(R15)             BRANCH AROUND ID
         DC        AL1(4)              LENGTH
         DC        CL5'TGET'           ID
         STM       R14,R12,12(R13)     SAVE REGS
         LR        R2,R15              SET UPO BASE
         USING     TGET,R2             REGISTER
         LM        R5,R6,0(R1)         A(SDV,RC)
         L         R3,0(R5)            A(STRING)
         LH        R4,4(R5)            MAX(CURRENT) LENGTH
         LA        R7,4(R5)            A(LENGTH)
         TM        6(R5),X'80'         IS STRING VARYIN
         BZ        GET                 NO, GO DO IT
         LR        R7,R3               A(LENGTH)
         LA        R3,2(R3)            POINT TO STRING
GET      TGET      (R3),(R4),ASIS,NOWAIT GET INPUT 3270
         CH        R15,FOUR            DID WE GET ANYTHING?
         BE        WAIT                NO, WAIT FOR A SEC AND RETRY
         STH       R15,0(R6)           SAVE RC
         STH       R1,0(R7)            SAVE LENGTH(READ)
         LM        R14,R12,12(R13)     RESTORE REGS
         LA        R15,0               SET ALL OKAY
         MVI       12(R13),X'FF'       SAY WERE DONE
         BR        R14                 LEAVE
WAIT     STIMER    WAIT,BINTVL=ONE
         B         GET                 TRY AGAIN?
FOUR     DC        H'4'
ONE      DC        F'100'
         END       TPUT
