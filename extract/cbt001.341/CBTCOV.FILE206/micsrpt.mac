OPTIONS PAGES=300 NOOVP BUFNO=2 GEN=0 NOSOURCE NODATE;
TITLE1 FEDERAL EXPRESS DATA SYSTEMS - DAILY MICS PERFORMANCE REPORT;
*---------------------------------------------------------------------*
*                                                                     *
*  THIS SAS PROGRAM PRODUCES A SIMPLE ONE PAGE REPORT FROM THE MICS   *
*  DATABASES SHOWING TSO AND IMS RESPONSE TIME AND TRANSACTION VOLUME *
*  BY HOUR.  BATCH SERVICE BY HOUR AND VOLUMES BY SELECTED CLASSES    *
*  ARE ALSO SHOWN.  A FEW VITAL STATISTICS ABOUT THE TWO SYSTEMS      *
*  (TEST AND PROD) ARE THEN SHOWN FOR AN ALL DAY AVERAGE AND PRIME    *
*  TIME (8-5).                                                        *
*                                                                     *
*  THIS CODE IS FAIRLY DEPENDENT ON OUR IMPLEMENTATION OF MICS AS     *
*  ESPECIALLY THE IMS COMPONENT.  IT IS NOT THE STANDARD MICS         *
*  COMPONENT, BUT ONE WE DEVELOPED OURSELVES.  IF WE EVER GET THE     *
*  MICS IMS COMPONENT, WE WILL CHANGE THIS CODE TO USE THAT COMPONENT *
*                                                                     *
*  THE SYSOUT FOR THIS PROGRAM REQUIRES THE ST12 CHARACTER SET ON A   *
*  3800 PLUS TWO ADDITIONAL CHARACTERS X'FB' IS A SOLID BLOCK AND     *
*  X'FC' IS A BLOCK WITH THE LEFT HALF SOLID SHADED                   *
*  I COULDN'T DISTRIBUTE THE CHARACTER SET BECAUSE THE ST12 SET       *
*  COMES FROM SCRIPT AND IS COPYRIGHTED.                              *
*                                                                     *
*---------------------------------------------------------------------*
*                                                                     *
*  PRODUCE THE DAILY PERFORMANCE REPORT FOR THE MICS SYSTEM           *
*                                                                     *
*---------------------------------------------------------------------*;
*---------------------------------------------------------------------*
*                                                                     *
*  IF 01 FILES IN DATABASES ARE OTHER THAN YESTERDAY CHANGE MACRO     *
*                                                                     *
*---------------------------------------------------------------------*;
MACRO _RPTDATE TODAY()-1 %

DATA TSOHOUR;
  SET DAYS.SCPPPA01;
  IF 20 <= PERFGRP < 40;
  RUN;
MACRO _BY DAY HOUR PERFPER %
MACRO _BREAK PERFPER %

PROC SORT DATA=TSOHOUR; BY _BY;

DATA TSOHOUR (KEEP=DAY HOUR PERFPER PPATTM PPATTX);
  SET TSOHOUR;
  _PPASUM

DATA TSOHOUR (KEEP=DAY HOUR TSORSPTM TSOTRNCT AVGTSOCT);
  SET TSOHOUR;
    BY _BY;
  FORMAT TSORSPTM TIME12.2;
  RETAIN HOURS 0 TSORSPTM TSORSPCT TOTRSPTM TOTRSPCT TOTTSOCT;

  IF FIRST.DAY THEN DO;
    HOURS = 0;
    TOTRSPTM = 0;
    TOTRSPCT = 0;
    TOTTSOCT = 0;
    END;
  IF FIRST.HOUR THEN DO;
    HOURS + 1;
    TSOTRNCT = 0;
    IF PERFPER = 1 THEN DO;
      TSORSPTM = PPATTM;
      TSORSPCT = PPATTX;
      END;
    END;
  TSOTRNCT + PPATTX;
  IF LAST.HOUR THEN DO;
    TOTRSPTM + TSORSPTM;
    TOTRSPCT + TSORSPCT;
    TOTTSOCT + TSOTRNCT;
    IF TSORSPCT ^= 0 THEN TSORSPTM = TSORSPTM / TSORSPCT;
    OUTPUT;
    END;
  IF LAST.DAY THEN DO;
    HOUR = 25;
    TSOTRNCT = TOTTSOCT;
    AVGTSOCT = TSOTRNCT / HOURS;
    TSORSPTM = TOTRSPTM / TOTRSPCT;
    OUTPUT;
    END;
RUN;PAGE;*------------------------------------------------------------*;

DATA TSOALL TSOPRIM;
  SET DAYS.SCPPGA01;
  IF 20 <= PERFGRP < 40;
  IF 08 <= HOUR <= 16 THEN OUTPUT TSOPRIM;
  OUTPUT TSOALL;
  RUN;
MACRO _BY DAY SYSID %
MACRO _BREAK SYSID %

PROC SORT DATA=TSOALL; BY _BY;
PROC SORT DATA=TSOPRIM; BY _BY;

DATA TSOALL (KEEP=DAY HOUR SYSID PGATTX);
  SET TSOALL;
  _PGASUM
DATA TSOALL (KEEP=DAY HOUR PRODATSO TESTATSO);
  SET TSOALL; BY _BY;
  HOUR = 25;
  RETAIN PRODATSO TESTATSO;
  IF FIRST.DAY THEN DO;
    PRODATSO = 0;
    TESTATSO = 0;
    END;
  IF SYSID = 'PROD' THEN PRODATSO = PGATTX;
    ELSE IF SYSID = 'TEST' THEN TESTATSO = PGATTX;
  IF LAST.DAY THEN OUTPUT;

DATA TSOPRIM (KEEP=DAY HOUR SYSID PGATTX);
  SET TSOPRIM;
  _PGASUM
SKIP 4;
DATA TSOPRIM (KEEP=DAY HOUR PRODPTSO TESTPTSO);
  SET TSOPRIM; BY _BY;
  HOUR = 25;
  RETAIN PRODPTSO TESTPTSO;
  IF FIRST.DAY THEN DO;
    PRODPTSO = 0;
    TESTPTSO = 0;
    END;
  IF SYSID = 'PROD' THEN PRODPTSO = PGATTX;
    ELSE IF SYSID = 'TEST' THEN TESTPTSO = PGATTX;
  IF LAST.DAY THEN OUTPUT;
RUN;PAGE;*------------------------------------------------------------*;

DATA CPUALL CPUPRIM;
  SET DAYS.HARCPU01;
  IF 08 <= HOUR <= 16 THEN OUTPUT CPUPRIM;
  OUTPUT CPUALL;
  RUN;
MACRO _BY DAY SYSID %
MACRO _BREAK SYSID %

PROC SORT DATA=CPUALL; BY _BY;
PROC SORT DATA=CPUPRIM; BY _BY;

DATA CPUALL (KEEP=DAY SYSID CPUPC0BS CPUAVT);
  SET CPUALL;
  _CPUSUM

DATA CPUALL (KEEP=DAY HOUR PRODABSY PRODAAVT TESTABSY TESTAAVT);
  SET CPUALL; BY _BY;
  RETAIN PRODABSY PRODAAVT TESTABSY TESTAAVT;
  HOUR = 25;
  IF FIRST.DAY THEN DO;
     PRODABSY = 0; PRODAAVT = 0; TESTABSY = 0; TESTAAVT = 0;
     END;
  IF SYSID = 'PROD' THEN DO;
    PRODABSY = CPUPC0BS;
    PRODAAVT = CPUAVT;
    END;
  ELSE IF SYSID = 'TEST' THEN DO;
    TESTABSY = CPUPC0BS;
    TESTAAVT = CPUAVT;
    END;
  IF LAST.DAY THEN OUTPUT;

DATA CPUPRIM (KEEP=DAY SYSID CPUPC0BS CPUAVT);
  SET CPUPRIM;
  _CPUSUM

DATA CPUPRIM (KEEP=DAY HOUR PRODPBSY PRODPAVT TESTPBSY TESTPAVT);
  SET CPUPRIM; BY _BY;
  RETAIN PRODPBSY PRODPAVT TESTPBSY TESTPAVT;
  HOUR = 25;
  IF FIRST.DAY THEN DO;
     PRODPBSY = 0; PRODPAVT = 0; TESTPBSY = 0; TESTPAVT = 0;
     END;
  IF SYSID = 'PROD' THEN DO;
    PRODPBSY = CPUPC0BS;
    PRODPAVT = CPUAVT;
    END;
  ELSE IF SYSID = 'TEST' THEN DO;
    TESTPBSY = CPUPC0BS;
    TESTPAVT = CPUAVT;
    END;
  IF LAST.DAY THEN OUTPUT;
RUN;PAGE;*------------------------------------------------------------*;

DATA PAGALL PAGPRIM;
  SET DAYS.SCPPAG01;
  IF 08 <= HOUR <= 16 THEN OUTPUT PAGPRIM;
  OUTPUT PAGALL;
  RUN;
MACRO _BY DAY SYSID %
MACRO _BREAK SYSID %

PROC SORT DATA=PAGALL; BY _BY;
PROC SORT DATA=PAGPRIM; BY _BY;

DATA PAGALL;
  SET PAGALL;
  _PAGSUM

DATA PAGALL (KEEP=DAY HOUR PRODADMD PRODAPIO PRODASWP PRODALOG
                           TESTADMD TESTAPIO TESTASWP TESTALOG );
  SET PAGALL; BY _BY;
  RETAIN PRODADMD PRODAPIO PRODASWP PRODALOG
         TESTADMD TESTAPIO TESTASWP TESTALOG;
  HOUR = 25;
  PAGEIO = (PAGPIN+PAGPOUT+PAGSIN+PAGSOT+PAGVIN+PAGVOT) / DURATION;
  TOSTIS = PAGTIS + PAGTOS;
  IF TOSTIS > 0 THEN PCTLOG = PAGLGS*100/TOSTIS;

  IF FIRST.DAY THEN DO;
    PRODADMD=0; PRODAPIO=0; PRODASWP=0; PRODALOG=0;
    TESTADMD=0; TESTAPIO=0; TESTASWP=0; TESTALOG=0;
    END;
  IF SYSID = 'PROD' THEN DO;
    PRODADMD = PAGPSSPG;
    PRODAPIO = PAGEIO;
    PRODASWP = PAGPSSWP;
    PRODALOG = PCTLOG;
    END;
  ELSE IF SYSID = 'TEST' THEN DO;
    TESTADMD = PAGPSSPG;
    TESTAPIO = PAGEIO;
    TESTASWP = PAGPSSWP;
    TESTALOG = PCTLOG;
    END;
  IF LAST.DAY THEN OUTPUT;

DATA PAGPRIM;
  SET PAGPRIM;
  _PAGSUM

DATA PAGPRIM (KEEP=DAY HOUR PRODPDMD PRODPPIO PRODPSWP PRODPLOG
                            TESTPDMD TESTPPIO TESTPSWP TESTPLOG );
  SET PAGPRIM; BY _BY;
  RETAIN PRODPDMD PRODPPIO PRODPSWP PRODPLOG
         TESTPDMD TESTPPIO TESTPSWP TESTPLOG;
  HOUR = 25;
  PAGEIO = (PAGPIN+PAGPOUT+PAGSIN+PAGSOT+PAGVIN+PAGVOT) / DURATION;
  PCTLOG = PAGLGS*100/(PAGTIS+PAGTOS);
  IF FIRST.DAY THEN DO;
    PRODPDMD=0; PRODPPIO=0; PRODPSWP=0; PRODPLOG=0;
    TESTPDMD=0; TESTPPIO=0; TESTPSWP=0; TESTPLOG=0;
    END;
  IF SYSID = 'PROD' THEN DO;
    PRODPDMD = PAGPSSPG;
    PRODPPIO = PAGEIO;
    PRODPSWP = PAGPSSWP;
    PRODPLOG = PCTLOG;
    END;
  ELSE IF SYSID = 'TEST' THEN DO;
    TESTPDMD = PAGPSSPG;
    TESTPPIO = PAGEIO;
    TESTPSWP = PAGPSSWP;
    TESTPLOG = PCTLOG;
    END;
  IF LAST.DAY THEN OUTPUT;
RUN;PAGE;*------------------------------------------------------------*;

DATA DTAALL DTAPRIM;
  SET DAYS.HARDTA01;
  IF DEVTYPE = '3350    ';
  IF 08 <= HOUR <= 16 THEN OUTPUT DTAPRIM;
  OUTPUT DTAALL;
  RUN;
MACRO _BY DAY SYSID %
MACRO _BREAK SYSID %

PROC SORT DATA=DTAALL; BY _BY;
PROC SORT DATA=DTAPRIM; BY _BY;

DATA DTAALL (KEEP=DAY SYSID DTAPSSIO);
  SET DTAALL;
  _DTASUM

DATA DTAALL (KEEP=DAY HOUR PRODASIO TESTASIO);
  SET DTAALL; BY _BY;
  RETAIN PRODASIO TESTASIO;
  HOUR = 25;
  IF FIRST.DAY THEN DO;
    PRODASIO = 0; TESTASIO = 0;
    END;
  IF SYSID = 'PROD' THEN PRODASIO = DTAPSSIO;
    ELSE IF SYSID = 'TEST' THEN TESTASIO = DTAPSSIO;
  IF LAST.DAY THEN OUTPUT;

DATA DTAPRIM (KEEP=SYSID DAY DTAPSSIO);
  SET DTAPRIM;
  _DTASUM

DATA DTAPRIM (KEEP=DAY HOUR PRODPSIO TESTPSIO);
  SET DTAPRIM; BY _BY;
  RETAIN PRODPSIO TESTPSIO;
  HOUR = 25;
  IF FIRST.DAY THEN DO;
    PRODPSIO = 0; TESTPSIO = 0;
    END;
  IF SYSID = 'PROD' THEN PRODPSIO = DTAPSSIO;
    ELSE IF SYSID = 'TEST' THEN TESTPSIO = DTAPSSIO;
  IF LAST.DAY THEN OUTPUT;
RUN;PAGE;*------------------------------------------------------------*;

DATA BATPGM (KEEP=DAY SYSID PGMCPUTM HOUR);
  SET DETAIL.BATPGM01 DETAIL.BAT_ST01 DETAIL.BAT_TS01 END=EOF;

PROC SORT DATA=BATPGM; BY DAY SYSID;

DATA BATPGM (KEEP=DAY HOUR PRODACPU PRODPCPU TESTACPU TESTPCPU);
  SET DETAIL.BATPGM01 DETAIL.BAT_ST01 DETAIL.BAT_TS01 END=EOF;
  BY DAY SYSID;
  RETAIN PRODACPU PRODPCPU TESTACPU TESTPCPU;
  FORMAT PRODACPU PRODPCPU TESTACPU TESTPCPU TIME12.2;
  IF FIRST.DAY THEN DO;
    PRODACPU = 0; PRODPCPU = 0; TESTACPU = 0; TESTPCPU = 0;
    END;
  IF SYSID = 'PROD' THEN DO;
    IF 8 <= HOUR <= 16 THEN PRODPCPU + PGMCPUTM;
    PRODACPU + PGMCPUTM;
    END;
  ELSE IF SYSID = 'TEST' THEN DO;
    IF 8 <= HOUR <= 16 THEN TESTPCPU + PGMCPUTM;
    TESTACPU + PGMCPUTM;
    END;
  IF LAST.DAY THEN DO;
    HOUR = 25;
    OUTPUT;
    END;
RUN;PAGE;*------------------------------------------------------------*;
DATA BATJOB (KEEP=DAY SYSID JOBCLASS JOBSRVMS HOUR);
  SET DETAIL.BATJOB01 DETAIL.BAT_JS01;

PROC SORT DATA=BATJOB; BY DAY HOUR;

DATA BATJOB (KEEP=DAY HOUR PRODATSJ PRODAPRJ PRODPTSJ PRODPPRJ TESTATSJ
                           TESTAPRJ TESTPTSJ TESTPPRJ SERVICE CLA CLB
                           CLF CLP CLO);
  SET BATJOB; BY DAY HOUR;
  RETAIN PRODATSJ PRODAPRJ PRODPTSJ PRODPPRJ TESTATSJ TESTAPRJ TESTPTSJ
         TESTPPRJ TOTCLA TOTCLB TOTCLF TOTCLP TOTCLO CLA CLB CLF
         CLP CLO BJOBT BJOBM TOTBJOBT TOTBJOBM;
  IF FIRST.DAY THEN DO;
    PRODATSJ=0; PRODAPRJ=0; PRODPTSJ=0; PRODPPRJ=0; TESTATSJ=0;
    TESTAPRJ=0; TESTPTSJ=0; TESTPPRJ=0; TOTCLA  =0; TOTCLB  =0;
    TOTCLF  =0; TOTCLP  =0; TOTCLO  =0; TOTBJOBT=0; TOTBJOBM=0;
    END;
  IF FIRST.HOUR THEN DO;
    CLA = 0; CLB = 0; CLF = 0; CLP = 0; CLO = 0;
    BJOBT=0; BJOBM=0;
    END;
  IF JOBCLASS = 'E' OR JOBCLASS = 'P' OR JOBCLASS = 'I'
    THEN JOBTYPE = 'P';
    ELSE JOBTYPE = 'T';
  IF JOBCLASS = 'A' THEN CLA + 1;
    ELSE IF JOBCLASS = 'B' THEN CLB + 1;
    ELSE IF JOBCLASS = 'F' THEN CLF + 1;
    ELSE IF JOBTYPE  = 'P' THEN CLP + 1;
    ELSE                        CLO + 1;
  BJOBT + 1;
  IF JOBSRVMS ^= 1 THEN BJOBM + 1;
  IF SYSID = 'PROD' THEN DO;
    IF 8 <= HOUR <= 16 THEN DO;
      IF JOBTYPE = 'P' THEN PRODPPRJ + 1;
                       ELSE PRODPTSJ + 1;
      END;
    IF JOBTYPE = 'P' THEN PRODAPRJ + 1;
                     ELSE PRODATSJ + 1;
    END;
  ELSE IF SYSID = 'TEST' THEN DO;
    IF 8 <= HOUR <= 16 THEN DO;
      IF JOBTYPE = 'P' THEN TESTPPRJ + 1;
                       ELSE TESTPTSJ + 1;
      END;
    IF JOBTYPE = 'P' THEN TESTAPRJ + 1;
                     ELSE TESTATSJ + 1;
    END;
  IF LAST.HOUR THEN DO;
    SERVICE = 100*BJOBM/BJOBT;
    TOTCLA + CLA;
    TOTCLB + CLB;
    TOTCLF + CLF;
    TOTCLP + CLP;
    TOTCLO + CLO;
    TOTBJOBM + BJOBM;
    TOTBJOBT + BJOBT;
    OUTPUT;
    END;
  IF LAST.DAY THEN DO;
    HOUR = 25;
    SERVICE = 100*TOTBJOBM/TOTBJOBT;
    CLA = TOTCLA;
    CLB = TOTCLB;
    CLF = TOTCLF;
    CLP = TOTCLP;
    CLO = TOTCLO;
    OUTPUT;
    END;
RUN;PAGE;*------------------------------------------------------------*;
PROC SORT DATA=PDB.IMSTRN01
           OUT=IMSHOUR (KEEP=DAY HOUR TRNTRNCT TRNRSPCT RESPTM);
  BY DAY HOUR;

DATA IMSHOUR (KEEP=DAY HOUR IMSRSPTM IMSTRNCT AVGIMSCT);
  SET IMSHOUR;
    BY DAY HOUR;
  RETAIN HOURS IMSRSPTM IMSRSPCT IMSTRNCT TOTRSPTM TOTRSPCT TOTIMSCT;

  IF FIRST.DAY THEN DO;
    HOURS = 0;
    TOTRSPTM = 0;
    TOTRSPCT = 0;
    TOTIMSCT = 0;
    END;
  IF FIRST.HOUR THEN DO;
    HOURS + 1;
    IMSTRNCT = 0;
    IMSRSPTM = 0;
    IMSRSPCT = 0;
    END;
  IMSTRNCT + TRNTRNCT;
  IF RESPTM ^= . THEN IMSRSPTM = IMSRSPTM + (RESPTM*TRNRSPCT);
  IMSRSPCT + TRNRSPCT;
  IF LAST.HOUR THEN DO;
    TOTRSPTM + IMSRSPTM;
    TOTRSPCT + IMSRSPCT;
    TOTIMSCT + IMSTRNCT;
    IF IMSRSPCT ^= 0 THEN IMSRSPTM = IMSRSPTM / IMSRSPCT;
    OUTPUT;
    END;
  IF LAST.DAY THEN DO;
    HOUR = 25;
    IMSTRNCT = TOTIMSCT;
    AVGIMSCT = IMSTRNCT / HOURS;
    IMSRSPTM = TOTRSPTM / TOTRSPCT;
    OUTPUT;
    END;
RUN;PAGE;*------------------------------------------------------------*;
DATA REPORT;
  MERGE TSOHOUR TSOALL TSOPRIM CPUALL CPUPRIM PAGALL PAGPRIM
        DTAALL DTAPRIM BATPGM BATJOB IMSHOUR;
  BY DAY HOUR;
RUN;PAGE;*------------------------------------------------------------*;
DATA _NULL_;

RETAIN RPTDATE RPTDAY FADMIHL 0;
FORMAT BLOCK SUBBLOCK $25.;
FILE PRINT N=PS;

SET REPORT;

IF _N_ = 1           THEN LINK LAYOUT;
  LINK REPORT;

RETURN;
SKIP 4;
LAYOUT:  /* SET UP ALL CONSTANT DATA ON THE PAGE */
TITLE1 F E D E R A L   E X P R E S S   D A T A   S Y S T E M S;

RPTDATE = _RPTDATE;
RPTDAY  = DAY(RPTDATE);

PUT #1 @100 'RUN DATE:';
PUT #2 @49 'DAILY PERFORMANCE REPORT' @97 'REPORT DATE:';
PUT #4 @1 'HOUR' @12 'TSO RESPONSE TIME' @38 'TSO'
       @48 'IMS RESPONSE TIME' @74 'IMS' @84 'BATCH SERVICE'
       @114 'JOBS COMPLETED';
PUT #5 @16 '(SECONDS)' @37 'TRANS' @52 '(SECONDS)' @73 'TRANS'
       @87 '(PERCENT)' @110 '-A-  -B-  -F- PROD OTHR';

DO L = 7 TO 30;
  J = L-7; K=L-6;
  IF J = 8 OR J = 16
    THEN VBAR = '8F'X;
    ELSE VBAR = 'FA'X;
  PUT #L @1 J Z2. '-' K Z2. @10 VBAR $1. @46 VBAR $1. @82 VBAR $1.;
  END;

UNDER = REPEAT('BF'X,25);
PUT #31 @10 'AB'X UNDER $25. @15 '8F'X
        @46 'AB'X UNDER $25. @51 '8F'X
        @82 'AB'X UNDER $25. @102 '8F'X;
PUT #32 @10 '0----1----2----3----4----5'
        @46 '0----1----2----3----4----5'
        @82 '0---20---40---60---80--100';
PUT #34 'AVERAGES' #36 'TOTALS';
PUT #39 @31 'PROD' @46 'TEST' @91 'PROD' @106 'TEST';
PUT #41 @27 '00-24' @34 '08-17' @42 '00-24' @49 '08-17'
        @87 '00-24' @94 '08-17' @102 '00-24' @109 '08-17';

PUT #43 'PERCENT CPU BUSY' @61 'AVG TSO USERS LOGGED ON';

PUT #45 'DASD I/O RATE' @61 'TSO TRANSACTIONS';

PUT #47 'TOTAL JOBS' @61 'DEMAND PAGE RATE';

PUT #49 'TEST JOBS' @61 'PAGE I/O RATE';

PUT #51 'PROD JOBS' @61 'SWAP RATE';

PUT #53 'PROBLEM CPU TIME (HH:MM)'
    @61 'PERCENT LOGICAL SWAPPING';
RETURN;
PAGE;
REPORT:
  IF RPTDAY ^= DAY THEN RETURN;
  IF HOUR = 25 THEN DO;
    RUNDATE = TODAY();
    PUT #1 @110 RUNDATE WEEKDATE23.
        #2 @110 RPTDATE WEEKDATE23.;

    /* PUT TOTAL JOBS FIRST, THEN AVERAGE */
    PUT #36
        @35 TSOTRNCT COMMA7.0 @71 IMSTRNCT COMMA7.0
        @109 CLA 4. @114 CLB 4. @119 CLF 4.
        @124 CLP 4. @129 CLO 4. ;

    CLA = CLA / 24;
    CLB = CLB / 24;
    CLF = CLF / 24;
    CLP = CLP / 24;
    CLO = CLO / 24;

    PUT #34
        @20 TSORSPTM 5.2  @37 AVGTSOCT 5.0
        @56 IMSRSPTM 5.2  @73 AVGIMSCT 5.0
        @94 SERVICE 5.2 @109 CLA 4. @114 CLB 4. @119 CLF 4.
        @124 CLP 4. @129 CLO 4. ;

    PUT #43 @27 PRODABSY 5.2  @34 PRODPBSY 5.2
            @42 TESTABSY 5.2  @49 TESTPBSY 5.2
            @87 PRODAAVT 5.0  @94 PRODPAVT 5.0
           @102 TESTAAVT 5.0 @109 TESTPAVT 5.0;

    PUT #45 @27 PRODASIO 5.1  @34 PRODPSIO 5.1
            @42 TESTASIO 5.1  @49 TESTPSIO 5.1
            @86 PRODATSO 6.0  @93 PRODPTSO 6.0
           @101 TESTATSO 6.0 @108 TESTPTSO 6.0;

    PRODATOT = PRODATSJ + PRODAPRJ;
    PRODPTOT = PRODPTSJ + PRODPPRJ;
    TESTATOT = TESTATSJ + TESTAPRJ;
    TESTPTOT = TESTPTSJ + TESTPPRJ;
    PUT #47 @27 PRODATOT 5.   @34 PRODPTOT 5.
            @42 TESTATOT 5.   @49 TESTPTOT 5.
            @87 PRODADMD 5.2  @94 PRODPDMD 5.2
           @102 TESTADMD 5.2 @109 TESTPDMD 5.2;
    PUT #49 @27 PRODATSJ 5.   @34 PRODPTSJ 5.
            @42 TESTATSJ 5.   @49 TESTPTSJ 5.
            @87 PRODAPIO 5.2  @94 PRODPPIO 5.2
           @102 TESTAPIO 5.2 @109 TESTPPIO 5.2;
    PUT #51 @27 PRODAPRJ 5.   @34 PRODPPRJ 5.
            @42 TESTAPRJ 5.   @49 TESTPPRJ 5.
            @87 PRODASWP 5.2  @94 PRODPSWP 5.2
           @102 TESTASWP 5.2 @109 TESTPSWP 5.2;
    PUT #53 @27 PRODACPU HHMM5. @34 PRODPCPU HHMM5.
            @42 TESTACPU HHMM5. @49 TESTPCPU HHMM5.
            @87 PRODALOG 5.2    @94 PRODPLOG 5.2
           @102 TESTALOG 5.2   @109 TESTPLOG 5.2;

    END;
  ELSE DO;  /* BY HOUR DETAIL */
    L = HOUR + 7;
    BLOCK = REPEAT('FB'X,24);

    PUT #L @37 TSOTRNCT 5.;
    IF TSORSPTM > 5.0 THEN PUT #L @11 BLOCK $24. '+';
    J = FLOOR(TSORSPTM/0.2);
    IF J => 1 THEN SUBBLOCK = SUBSTR(BLOCK,1,J);
              ELSE SUBBLOCK = ' ';
    K = MOD(TSORSPTM,0.2);
    IF K > .1 THEN SUBBLOCK = SUBBLOCK ×× 'FC'X;
    PUT #L @11 SUBBLOCK $24.;

    PUT #L @73 IMSTRNCT 5.;
    IF IMSRSPTM > 5.0 THEN PUT #L @47 BLOCK $24. '+';
    J = FLOOR(IMSRSPTM/0.2);
    IF J => 1 THEN SUBBLOCK = SUBSTR(BLOCK,1,J);
              ELSE SUBBLOCK = ' ';
    K = MOD(IMSRSPTM,0.2);
    IF K > .1 THEN SUBBLOCK = SUBBLOCK ×× 'FC'X;
    PUT #L @47 SUBBLOCK $25.;

    J = FLOOR(SERVICE/4);
    IF J => 1 THEN SUBBLOCK = SUBSTR(BLOCK,1,J);
              ELSE SUBBLOCK = ' ';
    K = MOD(SERVICE,4);
    IF K > 2 THEN SUBBLOCK = SUBBLOCK ×× 'FC'X;
    PUT #L @83 SUBBLOCK $25.;

    PUT #L @109 CLA 4. @114 CLB 4. @119 CLF 4. @124 CLP 4. @129 CLO 4.;
    END;
  RETURN;
