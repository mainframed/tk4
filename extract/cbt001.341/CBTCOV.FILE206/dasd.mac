DASD     TITLE 'DASD LIST PROGRAM'
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         SPACE 1
DASD     CENTRY
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         SPACE 1
         L     R2,PSATOLD-PSA          CURRENT TCB
         L     R2,TCBTIO-TCB(R2)       TIOT
         LA    R2,TIOENTRY-TIOT(R2)    DD ENTRIES
         USING TIOENTRY,R2
         XR    R3,R3                   CLEAR
INIT100  DS    0H
         CLI   TIOELNGH,X'00'          Q. TIOT DD ENTRIES END
         BE    INIT120                 ...YES
         CLC   TIOEDDNM,=CL8'DASDLIST' Q. DASDLIST DD PRESENT
         BE    INIT110                 ...YES
         IC    R3,TIOELNGH             TIOT ENTRY LENGTH
         LA    R2,0(R3,R2)             NEXT TIOT ENTRY
         B     INIT100                 CONTINUE
         DROP  R2
INIT110  DS    0H
         OPEN  (DASDLIST,(OUTPUT))
         B     UCB100                  CONTINUE
INIT120  DS    0H
         L     R1,PSAAOLD-PSA          CURRENT ASCB
         L     R1,ASCBTSB-ASCB(R1)     TSB
         LTR   R1,R1                   Q. TSO USER
         BZ    RETURN                  ...NO
         GTSIZE
         LTR   R0,R0                   Q. 3270 TERMINAL
         BZ    UCB100                  ...NO
         BCTR  R0,0                    ADJUST RELATIVE
         CVD   R0,MAXLINE              MAXIMUM LINES FOR TERMINAL
         CALL  CLRSCRN
         B     UCB100                  CONTINUE
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
UCB100   DS    0H                      UCB PROCESSING                 *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         SPACE 1
         L     R10,FLCCVT-PSA          CVT
         L     R10,CVTILK2-CVT(R10)    UCB LOOKUP TABLE ADDRESS LIST
         XR    R8,R8                   CLEAR
UCB110   DS    0H
         CLC   =H'0',0(R10)            Q. NULL UCB LOOKUP TABLE ENTRY
         BE    UCB119                  ...YES
         CLC   =X'FFFF',0(R10)         Q. UCB LOOKUP TABLE END
         BE    EXIT                    ...YES
         LH    R9,0(R10)               UCB ENTRY
         CR    R9,R8                   UCB ENTRY ADDRESS > PREVIOUS UCB
         BNH   UCB119                  ...NO
         LR    R8,R9
         CLC   18(1,R9),=X'20'         Q. DASD DEVICE
         BNE   UCB119                  ...NO
         TM    3(R9),X'80'             Q. DEVICE ONLINE
         BZ    UCB119                  ...NO
         TM    3(R9),X'10'             Q. UNLOAD COMMAND ISSUED
         BNZ   UCB119                  ...YES
         LA    0,0(R9)                 UCB
         LA    R1,MSGAREA              MESSAGE AREA
         SVC   78                      ISSUE DASD INFO SVC
         LTR   R15,R15                 Q. SUCCESSFUL
         BNZ   UCB119                  ...NO
         MVC   CUU,13(R9)              CHANNEL-UNIT
         MVI   STATUS,C'O'             ONLINE
         TM    3(R9),X'08'             Q. ALLOCATED
         BNO   UCB112                  ...NO
         MVI   STATUS,C'A'             ALLOCATED
UCB112   DS    0H
         MVC   VOLSER,28(R9)           VOLSER
         MVC   MOUNT,=CL8'PRIVATE'
         TM    34(R9),X'10'            Q. PRIVATE USE ATTRIBUTE
         BO    UCB114                  ...YES
         MVC   MOUNT,=CL8'STORAGE'
         TM    34(R9),X'04'            Q. STORAGE USE ATTRIBUTE
         BO    UCB114                  ...YES
         MVC   MOUNT,=CL8'PUBLIC'
UCB114   DS    0H
         MVC   CYLS,SVCCYL             TOTAL FREE CYLINDERS
         MVC   TRK,SVCTRK              TOTAL FREE TRACKS EXCLUDING CYLS
         MVC   EXTENTS,SVCXTNT         TOTAL FREE EXTENTS
         MVC   CONTCYL,SVCCCYL         LARGEST CONTIGUOUS FREE CYLS
         MVC   CONTTRK,SVCCTRK         LARGEST CONTIGUOUS FREE TRACKS
         BAL   R11,OUTPUT              PERFORM OUTPUT PROCESSING
UCB119   DS    0H
         LA    R10,2(R10)              NEXT UCB LOOKUP ENTRY
         B     UCB110                  CONTINUE
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
OUTPUT   DS    0H                      OUTPUT PROCESSING              *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         SPACE 1
         LA    R15,DASDLIST            DCB
         TM    DCBOFLGS-IHADCB(R15),DCBOFOPN Q. DCB OPEN
         BZ    OUT110                  ...NO
         CP    LINENUM,MAXLINE         Q. PAGE SIZE EXCEEDED
         BL    OUT102                  ...NO
         PUT   DASDLIST,HEADER
         PUT   DASDLIST,SPACES
         ZAP   LINENUM,=P'0'           SET LINE COUNT
OUT102   DS    0H
         PUT   DASDLIST,LINE
         BR    R11                     RETURN
OUT110   DS    0H
         CP    LINENUM,MAXLINE         Q. TERMINAL SIZE EXCEEDED
         BL    OUT112                  ...NO
         TPUT  HEADER,L'HEADER
         TPUT  SPACES,1
         ZAP   LINENUM,=P'2'           SET LINE COUNT
OUT112   DS    0H
         TPUT  LINE,L'LINE
         AP    LINENUM,=P'1'           INCREMENT LINE COUNT
         BR    R11                     RETURN
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
EXIT     DS    0H                      TERMINATION                    *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         SPACE 1
         LA    R1,DASDLIST             DCB
         TM    DCBOFLGS-IHADCB(R1),DCBOFOPN Q. DCB OPEN
         BZ    RETURN                  ...NO
         CLOSE DASDLIST
RETURN   DS    0H
         EXIT  RETCODE=0
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                      DATA AREA                      *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         SPACE 1
LINENUM  DC    PL2'199'
MAXLINE  DS    0D
         DC    PL8'50'                 MAXIMUM TERMINAL LINES
HEADER   DS    0CL078
         DC    C'UNIT STATUS VOLSER   MOUNT    CYLS     TRKS    EXTENTSX
                 LARGEST CONTIGUOUS                                   '
LINE     DS    0CL078
CUU      DS    CL3
         DC    CL4' '
STATUS   DS    CL1
         DC    CL4' '
VOLSER   DS    CL6
         DC    CL2' '
MOUNT    DS    CL7
         DC    CL3' '
CYLS     DS    CL4
         DC    CL5' '
TRK      DS    CL4
         DC    CL5' '
EXTENTS  DS    CL4
         DC    CL5' '
CONTCYL  DS    CL4
         DC    CL5' '
CONTTRK  DS    CL4
SPACES   DC    CL256' '
         DS    0F
MSGAREA  DS    0CL30                   SPACE AVAILABLE MESSAGE AREA
         DC    C'SPACE='
SVCCYL   DS    CL4                     TOTAL FREE CYLINDERS
         DC    C','
SVCTRK   DS    CL4                     TOTAL FREE TRACKS EXCLUDING CYLS
         DC    C','
SVCXTNT  DS    CL4                     TOTAL FREE EXTENTS
         DC    C'/'
SVCCCYL  DS    CL4                     LARGEST CONTIGUOUS FREE CYLS
         DC    C','
SVCCTRK  DS    CL4                     LARGEST CONTIGUOUS FREE TRACKS
         LTORG
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                      DCB AREA                       *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         SPACE 1
         PRINT NOGEN
DASDLIST DCB   DDNAME=DASDLIST,MACRF=PM,DSORG=PS,EXLST=OPENLIST
         PRINT GEN
OPENLIST DC    XL1'85'                 LIST END, OPEN EXIT INDICATORS
         DC    AL3(OPENEXIT)           OPEN EXIT ROUTINE
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         SPACE 3
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
OPENEXIT DS    0H                      OPEN EXIT ROUTINE              *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         SPACE 1
         USING OPENEXIT,R15
         USING IHADCB,R1
         OC    DCBBLKSI,DCBBLKSI       Q. DCB SPECIFIED
         BNZR  R14                     ...YES
         MVI   DCBRECFM,DCBRECF        DEFAULT DCB RECFM=F
         MVC   DCBLRECL,LENGTH         DEFAULT DCB LRECL
         MVC   DCBBLKSI,LENGTH         DEFAULT DCB BLKSIZE
         BR    R14                     RETURN
         SPACE 1
LENGTH   DC    AL2(L'LINE)             LINE LENGTH
         DROP  R1
         DROP  R15
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                      DSECTS                         *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         SPACE 1
         PRINT NOGEN
         IHAPSA
         CVT   DSECT=YES
         IHAASCB
         IKJTCB
         IKJPSCB
         IKJTSB
TIOT     DSECT
         IEFTIOT1
         DCBD  DSORG=PS
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                      REGISTER EQUATES               *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         SPACE 1
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         SPACE 3
         END
