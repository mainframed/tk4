         TITLE 'WHOAMI - TERMINAL ATTRIBUTE INQUIRY'
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                     *
* WHOAMI IS A TSO COMMAND PROCESSOR THAT DISPLAYS THE FOLLOWING       *
* INFORMATION ON THE TSO TERMINAL:                                    *
*                                                                     *
*   USERID   -  THE USERID FROM THE TIOT                              *
*   NAME     -  THE PROGRAMMER NAME FIELD FROM THE ACT                *
*   ACCOUNT  -  THE ACCOUNTING FIELDS FROM THE ACT                    *
*   TERMINAL -  THE NODE NAME FROM THE TSB EXTENSION (LINE FOR TCAM)  *
*   ROWS     -  THE NUMBER OF ROWS FROM A GTTERM MACRO                *
*   COLUMNS  -  THE NUMBER OF COLUMNS FROM A GTTERM MACRO             *
*   GRAPHICS -  WHETHER (YES/NO) THE TERMINAL HAS EXTENDED DATA STREAM*
*                       ALSO FROM THE GTTERM MACRO                    *
*                                                                     *
*   TO OBTAIN ACCESS TO THE TSB, A USER AUTHORIZATION SVC IS NEEDED   *
*   OR THE COMMAND CAN BE SET AUTHORIZED IN TSO.  WE USE THE FORMER   *
*   SO THAT IT CAN BE EXECUTED UNDER SPF                              *
*                                                                     *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         SPACE 1
WHOAMI   CENTRY SIZE=LWRKAREA
         USING WORKAREA,R13
         MVI   RETURNCD,8              RETURN CODE
* SET UP ESTAE
         MVC   ESTAEL(LESTAEL),ESTAELC ESTAE PARAMETER LIST
         ESTAE MF=(E,ESTAEL)
* SET AUTHORIZATION
SETAUTH  DS    0H
         TESTAUTH FCTN=1,RBLEVEL=1     TEST APF AUTHORIZATION
         STC   R15,TESTAUTH            PREVIOUS AUTHORIZATION FLAG
         LTR   R15,R15                 Q. ALREADY AUTHORIZED
         BZ    AUTHOK                  ...YES
         LA    R0,1                    AUTHORIZED
         SVC   242                     ISSUE AUTHORIZATION SVC
         SPACE 1
AUTHOK   DS    0H
         TITLE 'WHOAMI - USERID PROCESSING'
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         SPACE 1
USER001  DS    0H
         MVI   TPUTAREA,C' '            CLEAR AREA
         MVC   TPUTAREA+1(20),TPUTAREA
         MVC   TPUTAREA(10),LINE1
         L     R1,PSATNEW-PSA          CURRENT TCB
         L     R1,12(R1)               CURRENT TIOT
         MVC   TPUTAREA+11(8),0(R1)
         LA    R1,TPUTAREA
         LA    R0,20
         TPUT  (1),(0),R
         LA    R1,=C' '
         LA    R0,1
         TPUT  (1),(0),R
         TITLE 'WHOAMI - NAME PROCESSING'
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         SPACE 1
NAME001  L     R1,PSATNEW-PSA           -> TCB
         L     R1,TCBJSCB-TCB(,R1)      -> JSCB
         L     R1,JSCBJCT-IEZJSCB(,R1)  -> JCT
         LA    R1,16(,R1)
         L     R1,JCTACTAD-INJMJCT(,R1) -> ACT
         SRL   R1,8
         LA    R10,16(,R1)
         USING IEFAACTB,R10
         MVI   TPUTAREA,C' '            CLEAR TPUTAREA
         MVC   TPUTAREA+1(59),TPUTAREA
         MVC   TPUTAREA(10),LINE2
         MVC   TPUTAREA+11(20),ACTPRGNM
         LA    R1,TPUTAREA              DISPLAY NAME
         LA    R0,60
         TPUT  (1),(0),R
         LA    R1,=C' '
         LA    R0,1
         TPUT  (1),(0),R
         TITLE 'WHOAMI - DISPLAY ACCOUNT NUMBER'
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         SPACE 1
ACCT001  DS    0H
         SLR   R2,R2
         IC    R2,ACTJNFLD              NUMBER OF FIELDS
         LA    R3,ACTJNFLD+1            -> FIRST FIELD
         MVI   TPUTAREA,C' '            CLEAR TPUTAREA
         MVC   TPUTAREA+1(59),TPUTAREA
         MVC   TPUTAREA(10),LINE3
         LA    R4,TPUTAREA+11
         SLR   R5,R5
ACCT100  DS    0H
         IC    R5,0(,R3)                LENGTH THIS FIELD
         BCTR  R5,0                     ADJUST FOR MOVE
         EX    R5,MVCACCT
         LA    R4,1(R5,R4)              POINT PAST IT
         CH    R2,=H'1'                 LAST ONE
         BE    ACCT200
         MVI   0(R4),C','               SEPARATE BY COMMA
         LA    R4,1(,R4)
         LA    R3,2(R5,R3)              POINT TO NEXT FIELD
         BCT   R2,ACCT100
ACCT200  DS    0H
         LA    R1,TPUTAREA
         LA    R0,54
         TPUT  (1),(0),R
         LA    R1,=C' '
         LA    R0,1
         TPUT  (1),(0),R
         B     TERM001
MVCACCT  MVC   0(0,R4),1(R3)
         TITLE 'WHOAMI - DISPLAY TERMINAL NAME'
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         SPACE 1
TERM001  MVI   TPUTAREA,C' '            CLEAR TPUTAREA
         MVC   TPUTAREA+1(59),TPUTAREA
         MVC   TPUTAREA(10),LINE4
         L     R1,PSAANEW-PSA
         L     R8,ASCBTSB-ASCB(1)       -> TSB
         MODESET KEY=ZERO              MODESET KEY ZERO
         USING TSB,R8
         TM    TSBFLG5,TSBVTAM          Q. TSO/VTAM
         BO    GETTSBX                  ...YES, GET TSB EXTENSION
         LH    R1,TSBLINE               BINARY LINE ADDRESS
         STH   R1,WORK2
         UNPK  TPUTAREA+11(5),0(R1)     UNPACK
         TR    TPUTAREA+11(5),HEX-240   TRANSLATE TO EBCDIC
         B     PUTLINE
GETTSBX  DS    0H
         L     R9,TSBEXTNT              TSB EXTENSION
         USING TSBX,R9
         MVC   TPUTAREA+11(8),TSBTRMID  GET TERMINAL ID
PUTLINE  DS    0H
         LA    R1,TPUTAREA
         LA    R0,20
         TPUT  (1),(0),R
         LA    R1,=C' '
         LA    R0,1
         TPUT  (1),(0),R
         TITLE 'WHOAMI - TURN OFF AUTHORITY'
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         SPACE 1
AUTH500  DS    0H
         MODESET KEY=NZERO             MODESET USER KEY
         CLI   TESTAUTH,X'00'          Q. PREVIOUSLY AUTHORIZED
         BE    ATTR100                 ...YES
         LA    R0,0                    NOT AUTHORIZED
         SVC   242                     ISSUE AUTHORIZATION SVC
         TITLE 'WHOAMI - TERMINAL ATTRIBUTE PROCESSING'
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         SPACE 1
ATTR100  LA    R2,WORK2
         LA    R3,WORK2+2
         LA    R4,WORK2+4
         GTTERM PRMSZE=(2),ALTSZE=(3),ATTRIB=(4)
         LTR   R15,R15
         BNZ   EXIT
         CLC   WORK2+2(2),=X'0000'      USE ALTSIZE IF AVAILABLE
         BE    ALTSIZE
         MVC   WORK2(2),WORK2+2
ALTSIZE  DS    0H
         MVI   TPUTAREA,C' '            CLEAR TPUTAREA
         MVC   TPUTAREA+1(59),TPUTAREA
         MVC   TPUTAREA(10),LINE5
         SLR   R1,R1
         IC    R1,WORK2
         CVD   R1,WORK
         MVC   TPUTAREA+10(4),=X'40202120'
         ED    TPUTAREA+10(4),WORK+6
         LA    R1,TPUTAREA              DISPLAY ROWS
         LA    R0,15
         TPUT  (1),(0),R
         LA    R1,=C' '
         LA    R0,1
         TPUT  (1),(0),R
         MVI   TPUTAREA,C' '            CLEAR TPUTAREA
         MVC   TPUTAREA+1(59),TPUTAREA
         MVC   TPUTAREA(10),LINE6
         SLR   R1,R1
         IC    R1,WORK2+1
         CVD   R1,WORK
         MVC   TPUTAREA+10(4),=X'40202120'
         ED    TPUTAREA+10(4),WORK+6
         LA    R1,TPUTAREA              DISPLAY COLUMNS
         LA    R0,15
         TPUT  (1),(0),R
         LA    R1,=C' '
         LA    R0,1
         TPUT  (1),(0),R
         MVI   TPUTAREA,C' '            CLEAR TPUTAREA
         MVC   TPUTAREA+1(59),TPUTAREA
         MVC   TPUTAREA(10),LINE7
         MVC   TPUTAREA+11(2),=C'NO'
         TM    WORK2+7,X'01'
         BZ    NOGRAPH
         MVC   TPUTAREA+11(3),=C'YES'
NOGRAPH  DS    0H
         LA    R1,TPUTAREA              DISPLAY GRAPHICS
         LA    R0,15
         TPUT  (1),(0),R
         LA    R1,=C' '
         LA    R0,1
         TPUT  (1),(0),R
         TITLE 'WHOAMI - EXIT AND ESTAE'
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         SPACE 1
EXIT     DS    0H
         EXIT  RETCODE=RETURNCD
         SPACE 2
ESTAEXIT DS    0H
         USING ESTAEXIT,R15
         LA    R15,0                   NO RETRY
         BR    R14                     RETURN TO RTM
         DROP  R15
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         TITLE 'WHOAMI - DATA AREA'
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         SPACE 1
LINE1    DC    CL10'USERID   -'
LINE2    DC    CL10'NAME     -'
LINE3    DC    CL10'ACCT NO  -'
LINE4    DC    CL10'TERMINAL -'
LINE5    DC    CL10'ROWS     -'
LINE6    DC    CL10'COLUMNS  -'
LINE7    DC    CL10'GRAPHICS -'
HEX      DC    C'0123456789ABCDEF'
ESTAELC  ESTAE ESTAEXIT,TERM=YES,CT,MF=L
LESTAEL  EQU   *-ESTAELC
         LTORG
         PRINT GEN
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         TITLE 'WHOAMI - DSECTS'
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         SPACE 1
WORKAREA DSECT
SAVEAREA DS    18F
WORK     DS    D'0'
WORK2    DS    D'0'
RETURNCD DS    X                       RETURN CODE
TESTAUTH DS    X                       APF AUTHORIZATION TEST
TPUTAREA DS    CL60
ESTAEL   ESTAE ESTAEXIT,TERM=YES,CT,MF=L
         DS    0D
LWRKAREA EQU   *-WORKAREA
         PRINT NOGEN
         IHAPSA ,                      PSA
         IHAASCB                        ASCB
         IKJTSB  ,                      TSB
         IKTTSBX ,                      TSBX
         IEZJSCB ,                     JSCB
         IEFAJCTB ,                     JCT
         IKJTCB ,                      TCB
         PRINT GEN
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         TITLE 'WHOAMI - REGISTER EQUATES'
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         SPACE 1
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         SPACE 3
         END
