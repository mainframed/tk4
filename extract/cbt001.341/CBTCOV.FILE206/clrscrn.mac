         TITLE 'CLRSCRN'
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         SPACE 1
CLRSCRN  CSECT
         USING CLRSCRN,R15
         ST    R14,RETURN              SAVE
         LR    R14,R15                 BASE REGISTER
         DROP  R15
         USING CLRSCRN,R14
         GTSIZE
         LA    R15,20                  RETURN CODE 20 - NOT 3270 DEVICE
         LTR   R0,R0                   Q. 3270 TERMINAL
         BZ    EXIT                    ...NO
         MR    R0,R0                   SCREEN END = LINES * LINE LENGTH
         BCTR  R1,0                    ADJUST RELATIVE
         D     R0,=F'64'               MODULO 64
         N     R1,=X'0000003F'         INSURANCE
         STC   R0,SCREND+1             SCREEN OFFSET MODULO 64 COLUMN
         STC   R1,SCREND               SCREEN OFFSET MODULO 64 ROW
         TR    SCREND,SCRPOS           TRANSLATE TO 3270 ADDRESS
         TPUT  SCRCNTL,LSCRCNTL,FULLSCR,,HOLD
EXIT     DS    0H
         L     R14,RETURN
         BR    14                      RETURN
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         TITLE 'CLRSCRN - DATA AREA'
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         SPACE 1
RETURN   DS    F
SCRCNTL  DS    0CL11                   3270 FULL SCREEN CONTROL
         DC    AL1(WCC)                3270 WRITE CONTROL
         DC    AL1(SBA)                3270 SET BUFFER ADDRESS COMMAND
         DC    X'4040'                 3270 ADDRESS 0
         DC    AL1(IC)                 INSERT CURSOR
         DC    AL1(RA)                 3270 REPEAT TO ADDRESS COMMAND
SCREND   DC    X'5D7F'                 3270 ADDRESS - REPEAT END
         DC    X'00'                   REPEAT CHARACTER
LSCRCNTL EQU   *-SCRCNTL
SCRPOS   DS    0H                      MODULO 64 TRANSLATE TABLE
         DC    X'40C1C2C3C4C5C6C7C8C94A4B4C4D4E4F'
         DC    X'50D1D2D3D4D5D6D7D8D95A5B5C5D5E5F'
         DC    X'6061E2E3E4E5E6E7E8E96A6B6C6D6E6F'
         DC    X'F0F1F2F3F4F5F6F7F8F97A7B7C7D7E7F'
         LTORG
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
WCC      EQU   X'C3'                   KEYBOARD RESTORE,
*                                          RESET MODIFIED DATA TAG
SF       EQU   X'1D'                   START FIELD
SBA      EQU   X'11'                   SET BUFFER ADDRESS
IC       EQU   X'13'                   INSERT CURSOR
PT       EQU   X'05'                   PROGRAM TAB
RA       EQU   X'3C'                   REPEATE TO ADDRESS
EUA      EQU   X'12'                   ERASE UNPROTECTED TO ADDRESS
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         END
