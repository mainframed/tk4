//ASSEMBL  JOB (U00000),SOFTWARE,CLASS=A,MSGCLASS=A
//STEP1    EXEC ASMFC
//SYSIN   DD  *
 /* **************************************************************** */
 /*                                                                  */
 /*  SOURCE:   B. F. GOODRICH CORPORATION                            */
 /*            DEPT 0058  BLDG 17-F                                  */
 /*            500 S. MAIN STREET                                    */
 /*            AKRON, OHIO   44318                                   */
 /*            (216) 374-4560                                        */
 /*  CONTACT:  EMMETT C. GALLAGHER                                   */
 /*                                                                  */
 /* **************************************************************** */
 /*                                                                  */
 /*  VERSION 1.0                                                     */
 /*  -----------                                                     */
 /*                                                                  */
 /*     THIS PROGRAM CREATES THE DAILY FICHE.  BREAKING ON JOB NAME  */
 /*     PREFIX AND INSERTING EYEBALL READABLE FRAMES OF JOBNAME.     */
 /*     WITH THE USE OF THE EXTERNAL PREFIX TABLE.  PREFIX ENTRIES   */
 /*     CAN BE ADDED, CHANGED OR DELETED WITHOUT REASSEMBLING THIS   */
 /*     PROGRAM.                                                     */
 /*                                                                  */
 /*     ALSO ADDS DATA TO AN ACCOUNTING FILE FOR END OF MONTH        */
 /*     REPORTING.                                                   */
 /*                                                                  */
 /*                                                                  */
 /*          THIS IS A SAMPLE OF THE PREFIX TABLE                    */
 /*                                                                  */
 /*     CC  1-3  =  JOBNAME PREFIX                                   */
 /*                                                                  */
 /*     CC  4-11 =  CHARGE NUMBER                                    */
 /*                                                                  */
 /*     CC 12-13 =  NUMBER OF COPIES DESIRED (IN ADDITION TO ORIG.)  */
 /*                                                                  */
 /*     CC 14-16 =  FRAMES PER FICHE DESIRED (208 OR 360)            */
 /*                                                                  */
 /*     CC 17-46 =  TOP TITLE LINE ON FICHE                          */
 /*                                                                  */
 /*     CC 47-76 =  BOTTOM TITLE LINE ON FICHE                       */
 /*                                                                  */
 /*     CC 77    =  ' ' TO PRODUCE FICHE OR 'N' NOT TO PRODUCE FICHE */
 /*                                                                  */
 /*                                                                  */
 /*    A SAMPLE CAN BE FOUND IN MODULE 'PREFIXTB'                    */
 /*                                                                  */
 /*                                                                  */
 /*                                                                  */
 /* **************************************************************** */
SFT8MVAC CSECT
         ENTRY START
         PRINT NOGEN
START    EQU   *
         USING   START,R11,R12
         STM   R14,R12,12(R13)
         LR    R11,R15
         LA    R12,2048(R11)
         LA    R12,2048(R12)
         ST    R13,SAVEAREA+4
         LA    R13,SAVEAREA
         ST    R13,8(R13)
         B     SAVEAREA+72
SAVEAREA DS    18F
         REQUATE
START1   DS    0H
         OPEN  (FICH1,OUTPUT,INPUT,INPUT,FICH2,OUTPUT,FICH3,OUTPUT)
         OPEN  (COUNTER,OUTPUT,PREFIXTB,INPUT)
         LA    R4,PRETABLE
INIT1    GET   PREFIXTB,(R4)
         LA    R4,80(R4)
         B     INIT1
INIT2    DS    0H
         CLOSE (PREFIXTB)
*
READNEXT DS    0H
         BAL   R2,GETREC
         CLC   BUF+1(3),=C'JOB'
         BNE   WRITE1
         CLC   BUF+13(3),=C'JOB'
         BE    NEWJOB
WRITE1   DS    0H
         CLI   BYPASS,C'N'
         BE    READNEXT
         PUT   (R7),BUF
         XC    BUF,BUF
         B     READNEXT
NEWJOB   DS    0H
         BAL   R2,PAGCOUNT
         CLC   BUF+5(3),PREFIX
         BNE   NEWPREFX
NEWJOB1  MVC   JOBBUF+11(8),BUF+5
         CLI   BYPASS,C'N'
         BE    READNEXT
         PUT   (R7),JOBBUF
         B     WRITE1
NEWPREFX DS    0H
         LA    R5,PRETABLE
         MVC   PREFIX,BUF+5
NEWPRE01 CLC   PREFIX,0(R5)
         BE    NEWPRE02
         CLC   0(3,R5),=C'ZZZ'
         BE    UNKNOWN
         LA    R5,80(R5)
         B     NEWPRE01
NEWPRE02 DS    0H
         MVC   BYPASS,76(R5)
         MVC   COPY+11(2),11(R5)
         MVC   CHG+10(8),3(R5)
         MVC   DATA001+10(10),16(R5)
         MVC   DATA002+10(10),26(R5)
         MVC   DATA003+10(10),36(R5)
         MVC   DATA004+10(10),46(R5)
         MVC   DATA005+10(10),56(R5)
         MVC   DATA006+10(10),66(R5)
         MVC   PRE+10(3),PREFIX
         MVC   FRAMES(20),F208
         MVC   SEQ,SEQ208
         MVC   DATE,DATE208
         LA    R7,FICH1
         CLC   13(3,R5),=C'360'
         BNE   NEWPRE03
         MVC   FRAMES(20),F360
         MVC   SEQ,SEQ360
         MVC   DATE,DATE360
         LA    R7,FICH2
NEWPRE03 DS    0H
         CLI   BYPASS,C'N'
         BE    NEWJOB1
         PUT   (R7),PARM1
         PUT   (R7),PARM2
         PUT   (R7),PARM3
         B     NEWJOB1
*
UNKNOWN  DS    0H
         MVI   BYPASS,C' '
         MVC   PREFIX,=C'***'
         CLI   UNKFIRST,X'FF'
         BE    E2A
         MVI   UNKFIRST,X'FF'
         LA    R5,ZZZZZZZZ
         MVC   COPY+11(2),11(R5)
         MVC   CHG+10(8),3(R5)
         MVC   PRE+10(3),0(R5)
         MVC   DATA001+10(10),16(R5)
         MVC   DATA002+10(10),26(R5)
         MVC   DATA003+10(10),36(R5)
         MVC   DATA004+10(10),46(R5)
         MVC   DATA005+10(10),56(R5)
         MVC   DATA006+10(10),66(R5)
         MVC   FRAMES(20),F360
         MVC   SEQ,SEQ360
         MVC   DATE,DATE360
         PUT   FICH3,PARM1
         PUT   FICH3,PARM2
         PUT   FICH3,PARM3
         MVC   JOBBUF+11(8),BUF+5
         PUT   FICH3,JOBBUF
         PUT   FICH3,BUF
         XC    BUF,BUF
EREAD    BAL   R2,GETREC
EREAD1   CLC   BUF+1(3),=C'JOB'
         BNE   E1
         CLC   BUF+13(3),=C'JOB'
         BE    E2
E1       PUT   FICH3,BUF
         XC    BUF,BUF
         B     EREAD
E2       BAL   R2,PAGCOUNT
E2A      LA    R5,PRETABLE
         MVC   JOBBUF+11(8),BUF+5
E3       CLC   BUF+5(3),0(R5)
         BE    E4
         CLC   0(3,R5),=C'ZZZ'
         BE    E5
         LA    R5,80(R5)
         B     E3
E4       MVC   PREFIX,BUF+5
         B     NEWPRE02
E5       PUT   FICH3,JOBBUF
         B     E1
*
GETREC   DS    0H
         CLI   GETFIRST,X'FF'          DO WE HAVE REC IN BUFFER
         BE    GETREC01                YES  PROCESS IT
         GET   INPUT,BUFX              GET NEXT RECORD
         MVI   GETFIRST,X'FF'          SET FIRST PASS FLAG
GETREC01 MVC   BUF(80),BUFX            CONTINUATION - MOVE TO LAST LINE
         GET   INPUT,BUFX              GET NEXT RECORD
         CLI   BUFX,X'FE'              IS THIS A 'SPECIAL' RECORD?
         BNE   GETREC20                NO. PROCESS NORMALLY
         MVC   BUF+80(53),BUFX
         MVI   GETFIRST,X'00'
GETREC20 AP    LINES,=P'1'
         CLI   BUF+1,X'41'
         BNE   GETREC25
         MVI   BUF+1,X'40'
GETREC25 CLI   BUF,C'1'
         BNE   GETREC99
         AP    PAGES,=P'1'
GETREC99 BR    R2                      GIVE RECORD TO CALLER
*
PAGCOUNT DS    0H
         CLC   JOBBUF+11(8),=C'XXXXXXXX'
         BER   R2
         MVC   BUFY+3(8),JOBBUF+11                 SAVE JOB NAME
         MVC   BUFY+50(4),PREFIX                   SAVE PREFIX
         MVC   BUFY+60(1),BYPASS                   SAVE NO FICHE FLAG
         MVC   BUFY+16(8),LINES
         MVC   BUFY+28(8),PAGES
         ZAP   PAGES,=P'2'
         ZAP   LINES,=P'1'
         PUT   COUNTER,BUFY
         BR    R2
*
LOGEND   DS    0H
         BAL   R2,PAGCOUNT
         CLOSE (FICH1,,INPUT,,FICH2,,FICH3,,COUNTER)
         LA    R13,SAVEAREA
         EPILOGUE   RC=0
GETFIRST DC    X'00'
BYPASS   DC    C' '
BUFX     DC    CL133' '
BUFY     DC    CL133' '
BUF      DC    CL133' '
PREFIX   DC    CL3' '
UNKFIRST DC    X'00'
ZERO     DC    F'0'
ADZERO   DC    X'80'
         DC    AL3(ZERO)
LINES    DC    PL8'1'
PAGES    DC    PL8'2'
*
F208     DC    C'12013335162528141116'
F360     DC    C'12013028201821190912'
SEQ360   DC    CL20'B303203T  S$$$'
SEQ208   DC    CL20'B303163T  S$$$'
DATE360  DC    CL20'B408173B  D$$$'
DATE208  DC    CL20'B408133B  D$$$'
*
PARM1    DC    X'014163FF'
FRAMES   DC    C'12013335162528141116'
CHAN1    DC    CL20'20101'
H1       DC    CL20'H1      Y'
CHG      DC    CL20'B108013T'
COPY     DC    CL20'B203013B  CXX'
SEQ      DC    CL20'B303163T  S$$$'
         DC    X'FC10'
         DC    CL26' '
PARM2    DC    X'014163FF'
DATE     DC    CL20'B408133B  D$$$'
PRE      DC    CL20'B503043L  XXX'
DATA001  DC    CL20'B510063T            '
DATA002  DC    CL20'B610093T1           '
DATA003  DC    CL20'B710123T2           '
DATA004  DC    CL20'B810063B            '
         DC    X'FC10'
         DC    CL26' '
PARM3    DC    X'014163FF'
DATA005  DC    CL20'B910093B1           '
DATA006  DC    CL20'BA10123B2           '
H2       DC    CL20'H2       '
ENDPARM  DC    X'10'
         DC    CL20' '
         DC    CL20' '
         DC    CL20' '
         DC    CL27' '
*
JOBBUF   DC    CL133'1&&&&$$KY5 08XXXXXXXX'
*
ZZZZZZZZ DC    CL16'ZZZOPS8OP8000360'
         DC    CL30'THESE JOBS HAVE INVALID PREFIX'
         DC    CL30'SEND TO PROCESS MANAGEMENT    '
*
COUNTER  DCB   DDNAME=MONTH,DSORG=PS,MACRF=PM,RECFM=FB,LRECL=80,       C
               BLKSIZE=3120
FICH1    DCB   DDNAME=FICH1,DSORG=PS,MACRF=PM,RECFM=FBA,LRECL=133,     C
               BLKSIZE=2660
FICH2    DCB   DDNAME=FICH2,DSORG=PS,MACRF=PM,RECFM=FBA,LRECL=133,     C
               BLKSIZE=2660
FICH3    DCB   DDNAME=FICH3,DSORG=PS,MACRF=PM,RECFM=FBA,LRECL=133,     C
               BLKSIZE=2660
INPUT    DCB   DDNAME=INPUT,DSORG=PS,MACRF=GM,EODAD=LOGEND
PREFIXTB DCB   DDNAME=PREFIXTB,DSORG=PS,MACRF=GM,EODAD=INIT2
         LTORG
*
PRETABLE DC    101CL80' '
*
         END   SFT8MVAC
/*
//LINKEDIT EXEC PGM=IEWL,PARM='LIST,LET,XREF',COND=(0,LT)
//SYSPRINT  DD  SYSOUT=*
//SYSUT1    DD  UNIT=SYSDA,SPACE=(CYL,(3,1))
//SYSLMOD   DD  DISP=SHR,DSN=SYS1.SOFTJOB
//SYSLIN    DD  DISP=(OLD,DELETE),DSN=&&LOADSET
//          DD  *
 SETSSI   80000328
 IDENTIFY SFT8MVAC('SYSMSG SPOOLING/FICHE SYSTEM')
 ENTRY    SFT8MVAC
 NAME     SFT8MVAC(R)
/*
