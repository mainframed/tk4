         MACRO
&LBL     OACFSVC &DMY,&TYPE=,&GEN=,&PFX=OACF,&MF=,&DSECT=,             X
               &SRA=,&UID=,&PW=,&NPW=,&IRA=,&DSN=,&VOL=,               X
               &ACCESS=,&NFIELDS=,&FNAMES=,&IRAL=,&AREBUFL=,           X
               &AREBUF=,&SMFBUFL=,&SMFBUF=
         LCLC  &P,&NAME,&C
         LCLB  &SRAP,&UIDP,&PWP,&NPWP,&IRAP,&DSNP,&VOLP,&ACSP
         LCLB  &NFP,&NMP,&IRALP,&ABLP,&ABP,&SBLP,&SBP,&BGEN
         LCLB  &MFE,&MFI,&MFL,&MFB,&BMF
&NAME    SETC  '&LBL'
&P       SETC  '&PFX'
         AIF   ('&SRA' EQ '').A1
&SRAP    SETB  1
.A1      AIF   ('&UID' EQ '').A2
&UIDP    SETB  1
.A2      AIF   ('&PW' EQ '').A3
&PWP     SETB  1
.A3      AIF   ('&NPW' EQ '').A4
&NPWP    SETB  1
.A4      AIF   ('&IRA' EQ '').A5
&IRAP    SETB  1
.A5      AIF   ('&DSN' EQ '').A6
&DSNP    SETB  1
.A6      AIF   ('&VOL' EQ '').A7
&VOLP    SETB  1
.A7      AIF   ('&ACCESS' EQ '').A8
&ACSP    SETB  1
.A8      AIF   ('&NFIELDS' EQ '').A9
&NFP     SETB  1
.A9      AIF   ('&FNAMES' EQ '').A10
&NMP     SETB  1
.A10     AIF   ('&IRAL' EQ '').A10A
&IRALP   SETB  1
.A10A    AIF   ('&AREBUFL' EQ '').A11
&ABLP    SETB  1
.A11     AIF   ('&AREBUF' EQ '').A12
&ABP     SETB  1
.A12     AIF   ('&SMFBUFL' EQ '').A13
&SBLP    SETB  1
.A13     AIF   ('&SMFBUF' EQ '').A14
&SBP     SETB  1
.A14     AIF   ('&GEN' EQ '').A15
&BGEN    SETB  1
.A15     AIF   ('&MF' EQ '').B
&BMF     SETB  1
         AIF   ('&MF(1)' NE 'E').A16
&MFE     SETB  1
         AGO   .B
.A16     AIF   ('&MF(1)' NE 'I').A17
&MFI     SETB  1
         AGO   .B
.A17     AIF   ('&MF(1)' NE 'L').A18
&MFL     SETB  1
         AGO   .B
.A18     AIF   ('&MF(1)' NE 'B').MFERR
&MFB     SETB  1
         AGO   .B
.MFERR   MNOTE 12,'UNRECOGNIZEABLE MF='
         MEXIT
.CONF    MNOTE 12,'CONFLICTING PARMS'
         MEXIT
.B       AIF   (&BGEN AND &BMF).CONF
         AIF   (&BGEN OR &BMF).B1
&BMF     SETB  1
&MFI    SETB  1
.B1      AIF   (&BMF).B2
         AIF   ('&DSECT' EQ '').B2
&DSECT   DSECT
.B2      AIF   (NOT &MFI).B3
         AIF   ('&TYPE' EQ 'SLGF').B3
&NAME    B     B&SYSNDX
&NAME    SETC  ''
.B3      AIF   ('&GEN' EQ 'SRA').SRA
         AIF   ('&TYPE' EQ 'SLGN').SLGN
         AIF   ('&TYPE' EQ 'ULGN' ).ULGN
         AIF   ('&TYPE' EQ 'DS'   ).DS
         AIF   ('&TYPE' EQ 'INFO' ).INFO
         AIF   ('&TYPE' EQ 'CHG'  ).CHG
         AIF   ('&TYPE' EQ 'ULGF' ).ULGF
         AIF   ('&TYPE' EQ 'SMF'  ).SMF
         AIF   ('&TYPE' EQ 'SLGF' ).SLGF
         AIF   ('&TYPE' NE 'ALL'  ).TYPERR
         AIF   ('&GEN' EQ 'PARMS' OR NOT &BGEN).ALLP
.GENERR  MNOTE 12,'UNRECOGNIZABLE GEN='
         MEXIT
.TYPERR  MNOTE 12,'UNRECOGNIZABLE TYPE'
         MEXIT
.SRA     AIF   ('&NAME' EQ '').SRA1
&NAME    DS    0F
&NAME    SETC  ''
.SRA1    ANOP
&P.SRC   DS    F                  SECONDARY RETURN CODE
&P.MSG   DS    0H                 MESSAGE BUFFER
&P.MLEN  DS    H                  MSG LENGTH INCLUDING HEADER
         DS    H                  SET TO ZERO
&P.MTXT  DS    CL124              MESSAGE TEXT
&P.SRAL  EQU   132                LENGTH OF STANDARD RETURN AREA
         MEXIT
.ALLP    AIF   ('&NAME' EQ '').ALLP1
&NAME    DS    0F
&NAME    SETC  ''
.ALLP1   ANOP
&P.SRAP  DC    A(0)               PTR TO "STANDARD RETURN AREA"
&P.UIDP  DC    A(0)               PTR TO CL7 UID
&P.SBLP  EQU   &P.UIDP            PTR TO F LENGTH OF SMF INFO
&P.PWP   DC    A(0)               PTR TO CL8 CURRENT PASSWORD
&P.DSNP  EQU   &P.PWP             PTR TO CL44 DSNAME
&P.NFP   EQU   &P.PWP             PTR TO F NUMBER OF FIELD NAMES
&P.ABLP  EQU   &P.PWP             PTR TO F LENGTH OF ARE BUFFER
&P.SBP   EQU   &P.PWP             PTR TO  SMF BUFFER
&P.NPWP  DC    A(0)               PTR TO CL8 NEW PASSWORD
&P.VOLP  EQU   &P.NPWP            PTR TO CL6 VOLUME
&P.NMP   EQU   &P.NPWP            PTR TO LIST OF CL8 FIELD NAMES
&P.ABP   EQU   &P.NPWP            PTR TO  ARE BUFFER
&P.IRAP  DC    A(0)               PTR TO "INFO RETURN AREA"
&P.ACSP  EQU   &P.IRAP            PTR TO XL1 ACCESSES
&P.EXEC  EQU   X'80'              EXECUTE ONLY
&P.READ  EQU   X'40'              READ ONLY
&P.WRIT  EQU   X'20'              READ/WRITE
&P.ALOC  EQU   X'10'              ALLOC/SCRATCH/RENAME
&P.IRLP  DC    A(0)               PTR TO F IRA LENGTH
         MEXIT
.SLGN    AIF   (&UIDP OR &PWP OR &NPWP OR &IRAP OR &DSNP).CONF
         AIF   (&VOLP OR &ACSP OR &NFP OR &NMP OR &ABLP).CONF
         AIF   (&IRALP OR &ABP OR &SBLP OR &SBP).CONF
         AIF   (&BMF).SLGNMF
         AIF   ('&GEN' NE 'PARMS' AND &BGEN).GENERR
         AIF   ('&NAME' EQ '').SLGN1
&NAME    DS    F
.SLGN1   ANOP
&P.SRAP  DS    F                  POINTER TO "STANDARD RETURN AREA"
         MEXIT
.SLGNMF  AIF   (NOT &MFI).SLGN2
A&SYSNDX DC A(0)
B&SYSNDX LA    R1,A&SYSNDX
         OACINNER F=&SRA,T=0(R1)
         LA    R0,1               SERVICE LOGON CALL
         AGO   .SVC209
.SLGN2   AIF   (&MFL).SLGN3
&NAME    OACINNER F=&MF(2),T=(R1),COM='GET ADD OF PARM LIST'
         OACINNER F=&SRA,T=0(R1),COM='PTR TO "STANDARD RETURN AREA"'
         LA    R0,1               SERVICE LOGON CALL
         AGO   .SVC209
.SLGN3   ANOP
&NAME    OACDC &SRA,COM='PTR TO "STANDARD RETURN AREA"'
         MEXIT
.ULGN    AIF   (&DSN OR &VOLP OR &ACSP OR &NFP OR &NMP).CONF
         AIF   (&IRALP OR &ABLP OR &ABP OR &SBLP OR &SBP).CONF
         AIF   (&BMF).ULGNMF
         AIF   ('&NAME' EQ '').ULGN1
&NAME    DS    0F
.ULGN1   AIF   ('&GEN' EQ 'PARMS' AND &BGEN).ULGNP
         AIF   ('&GEN' NE 'IRA').GENERR
&P.NAME  DC    CL20' '            USERS NAME
&P.FONE  DC    CL12' '            PHONE NUMBER
&P.PFX   DC    CL7' '             DEFAULT DSN PREFIX
&P.UNIT  DC    CL8' '             GENERIC UNIT
&P.BIN   DC    CL4' '             DEFAULT BIN
&P.RMT   DC    CL8' '             DEFAULT REMOTE DEST
&P.SHC   DC    CL1' '             DEFAULT SUBMIT HOLD CLASS
&P.SJC   DC    CL1' '             DEFAULT SUBMIT JOB CLASS
&P.SMC   DC    CL1' '             DEFAULT SUBMIT MSGCLASS
&P.SSC   DC    CL1' '             DEFAULT SUBMIT SYSOUT CLASS
&P.LDC   DC    AL1(&P.ATTN)       LINE DELETE CHAR
&P.ATTN  EQU   X'37'              ATTN IS LINE DELETE
&P.CTLX  EQU   X'18'              CTLX IS LINE DELETE
&P.BSC   DC    AL1(&P.BS)         BACKSPACE CHARACTER
&P.BS    EQU   X'16'              BS IS C D CHARACTER
&P.MSGF  DC    AL1(0)             MESSAGE FLAGS
&P.MAIL  EQU   X'80'              MAIL/NOMAIL
&P.NTCS  EQU   X'40'              NOTICES/NONOTICES
&P.PRMT  EQU   X'20'              PROMPT/NOPROMPT
&P.INTR  EQU   X'10'              INTERCOM/NOINTERCOM
&P.PAUS  EQU   X'08'              PAUSE/NOPAUSE
&P.MID   EQU   X'04'              MSGID/NOMSGID
&P.MODE  EQU   X'02'              MODE/NOMODE
&P.WTP   EQU   X'01'              WTP/NOWTP
&P.FACS  DC    AL1(0)             FACILITIES
&P.BTCH  EQU   X'80'              BATCH WITH QUICKRUN
&P.QCK   EQU   X'40'              QUICKRUN ONLY
&P.BSIC  EQU   X'20'              BASIC ONLY
&P.APL   EQU   X'10'              APL ONLY
&P.TSO   EQU   X'08'              TSO WITH APL AND BASIC
&P.WBR   EQU   X'04'              WYLBUR
&P.ARPA  EQU   X'02'              ARPA
&P.OTHR  EQU   X'01'              OTHER
&P.RSVD  DC    AL1(0)             RESERVED
&P.PRML  EQU   *-&P.NAME          LENGTH
         MEXIT
.ULGNP   ANOP
&P.SRAP  DC    A(0)               PTR TO "STANDARD RETURN AREA"
&P.UIDP  DC    A(0)               PTR TO CL7 UID
&P.PWP   DC    A(0)               PTR TO CL8 "CURRENT" PASSWORD
&P.NPWP  DC    A(0)               PTR TO CL8 NEW PASSWORD
&P.IRAP  DC    A(0)               PTR TO XL68 "INFO RETURN AREA"
         MEXIT
.ULGNMF  AIF   (NOT &MFI).ULGN2
A&SYSNDX DC 5A(0)                 PARM AREA
B&SYSNDX LA     R1,A&SYSNDX       POINT TO PARM LIST
         OACINNER F=&SRA,T=0(R1),COM='PTR TO "STANDARD RETURN AREA"'
         OACINNER F=&UID,T=0(R1),TO=+4,COM='PTR TO CL7 UID'
         OACINNER F=&PW,T=0(R1),TO=+8,COM='PTR TO CL8 PASSWORD'
         OACINNER F=&NPW,T=0(R1),TO=+12,COM='PTR TO CL8 NEW PASSWORD'
         OACINNER F=&IRA,T=0(R1),TO=+16,COM='PTR TO XL68 RETURN AREA'
         LA    R0,2               USER LOGON CALL
         AGO   .SVC209
.ULGN2   AIF   (&MFL).ULGN3
&NAME    OACINNER F=&MF(2),T=(R1),COM='GET ADD OF PARM LIST'
         OACINNER F=&SRA,T=0(R1),COM='PTR TO "STANDARD RETURN AREA"'
         OACINNER F=&UID,T=0(R1),TO=+4,COM='PTR TO CL7 UID'
         OACINNER F=&PW,T=0(R1),TO=+8,COM='PTR TO CL8 PASSWORD'
         OACINNER F=&NPW,T=0(R1),TO=+12,COM='PTR TO CL8 NEW PASSWORD'
         OACINNER F=&IRA,T=0(R1),TO=+16,COM='PTR TO XL68 RETURN AREA'
         LA    R0,2               USER LOGON CALL
         AGO   .SVC209
.ULGN3   ANOP
&NAME    OACDC &SRA,COM='PTR TO "STANDARD RETURN AREA"'
         OACDC &UID,COM='PTR TO CL7 UID'
         OACDC &PW,COM='PTR TO CL8 PASSWORD'
         OACDC &NPW,COM='PTR TO CL8 NEW PASSWORD'
         OACDC &IRA,COM='PTR TO XL68 INFO RETURN AREA'
         AGO   .MEND
.DS      AIF   (&PWP OR &NPWP OR &IRAP OR &NFP OR &NMP).CONF
         AIF   (&IRALP OR &ABLP OR &ABP OR &SBLP OR &SBP).CONF
         AIF   (&BMF).DSMF
         AIF   ('&GEN' NE 'PARMS' AND &BGEN).GENERR
         AIF   ('&NAME' EQ '').DS1
&NAME    DS    0F
.DS1     ANOP
&P.SRAP  DC    A(0)               PTR TO "STANDARD RETURN AREA"
&P.UIDP  DC    A(0)               PTR TO CL7 UID
&P.DSNP  DC    A(0)               PTR TO CL44 DSNAME
&P.VOLP  DC    A(0)               PTR TO CL6 VOLUME
&P.ACSP  DC    A(0)               PTR TO XL1 ACCESSES
&P.EXEC  EQU   X'80'              EXEC ONLY
&P.READ  EQU   X'40'              READ ONLY
&P.WRIT  EQU   X'20'              READ/WRITE
&P.ALOC  EQU   X'10'              ALLOC/RENAME/SCRATCH
         MEXIT
.DSMF    AIF   (NOT &MFI).DS2
A&SYSNDX DC    5A(0)              PARM LIST
B&SYSNDX LA    R1,A&SYSNDX
         OACINNER F=&SRA,T=0(R1),COM='PTR TO "STANDARD RETURN AREA"'
         OACINNER F=&UID,T=0(R1),TO=+4,COM='PTR TO CL7 UID'
         OACINNER F=&DSN,T=0(R1),TO=+8,COM='PTR TO CL44 DSNAME'
         OACINNER F=&VOL,T=0(R1),TO=+12,COM='PTR TO CL6 VOLUME'
         OACINNER F=&ACCESS,T=0(R1),TO=+16,COM='PTR TO XL1 ACCESSES'
         LA    R0,3               DATASET ACCESS VERIFICATION
         AGO   .SVC209
.DS2     AIF   (&MFL).DS3
&NAME    OACINNER F=&MF(2),T=(R1),COM='GET ADD OF PARM LIST'
         OACINNER F=&SRA,T=0(R1),COM='PTR TO "STANDARD RETURN AREA"'
         OACINNER F=&UID,T=0(R1),TO=+4,COM='PTR TO CL7 UID'
         OACINNER F=&DSN,T=0(R1),TO=+8,COM='PTR TO CL44 DSNAME'
         OACINNER F=&VOL,T=0(R1),TO=+12,COM='PTR TO CL6 VOLUME'
         OACINNER F=&ACCESS,T=0(R1),TO=+16,COM='PTR TO XL1 ACCESSES'
         LA    R0,3               DATASET ACCESS VERIFICATION
         AGO   .SVC209
.DS3     ANOP
&NAME    OACDC &SRA,COM='PTR TO "STANDARD RETURN AREA"'
         OACDC &UID,COM='PTR TO CL7 UID'
         OACDC &DSN,COM='PTR TO CL44 DSNAME'
         OACDC &VOL,COM='PTR TO CL6 VOLUME'
         OACDC &ACCESS,COM='PTR TO XL1 ACCESSES'
         AGO   .MEND
.INFO    AIF   (&PWP OR &NPWP OR &DSNP OR &VOLP OR &ACSP).CONF
         AIF   (&ABP OR &ABLP OR &SBLP OR &SBP).CONF
         AIF   (&BMF).INFOMF
         AIF   ('&GEN' NE 'PARMS' AND &BGEN).GENERR
         AIF   ('&NAME' EQ '').INFO1
&NAME    DS    0F
.INFO1   ANOP
&P.SRAP  DC    A(0)               PTR TO "STANDARD RETURN AREA"
&P.UIDP  DC    A(0)               PTR TO CL7 UID
&P.NFP   DC    A(0)               PTR TO F NUMBER OF FIELD NAMES
&P.NMP   DC    A(0)               PTR TO LIST OF CL8 FIELD NAMES
&P.IRAP  DC    A(0)               PTR TO INFO RETURN AREA
&P.IRLP  DC    A(0)               PTR TO F LENGTH OF IRA
         MEXIT
.INFOMF  AIF   (NOT &MFI).INFO2
A&SYSNDX DC    6A(0)              PARM LIST
B&SYSNDX LA    R1,A&SYSNDX        PT TO PARM LIST
         OACINNER F=&SRA,T=0(R1),COM='PTR TO "STANDARD RETURN AREA"'
         OACINNER F=&UID,T=0(R1),TO=+4,COM='PTR TO CL7 UID'
         OACINNER F=&NFIELDS,T=0(R1),TO=+8,COM='PTR TO F NUM FIELDS'
         OACINNER F=&FNAMES,T=0(R1),TO=+12,COM='PTR TO LST OF CL8 NMS'
         OACINNER F=&IRA,T=0(R1),TO=+16,COM='PTR TO INFO RETURN AREA'
         OACINNER F=&IRAL,T=0(R1),TO=+20,COM='PTR TO F LENGTH OF IRA'
         LA    R0,4               INFO REQUEST
         AGO   .SVC209
.INFO2   AIF   (&MFL).INFO3
&NAME    OACINNER F=&MF(2),T=(R1),COM='POINT TO PARM AREA'
         OACINNER F=&SRA,T=0(R1),COM='PTR TO "STANDARD RETURN AREA"'
         OACINNER F=&UID,T=0(R1),TO=+4,COM='PTR TO CL7 UID'
         OACINNER F=&NFIELDS,T=0(R1),TO=+8,COM='PTR TO F NUM FIELDS'
         OACINNER F=&FNAMES,T=0(R1),TO=+12,COM='PTR TO LST OF CL8 NMS'
         OACINNER F=&IRA,T=0(R1),TO=+16,COM='PTR TO INFO RETURN AREA'
         OACINNER F=&IRAL,T=0(R1),TO=+20,COM='PTR TO F LENGTH OF IRA'
         LA    R0,4               INFO REQUEST
         AGO   .SVC209
.INFO3   ANOP
&NAME    OACDC &SRA,COM='PTR TO "STANDARD RETURN AREA"'
         OACDC &UID,COM='PTR TO CL7 UID'
         OACDC &NFIELDS,COM='PTR TO F NUMBER OF FIELDS'
         OACDC &FNAMES,COM='PTR TO LIST OF CL8 NAMES'
         OACDC &IRA,COM='PTR TO INFO RETURN AREA'
         OACDC &IRAL,COM='PTR TO F LENGTH OF IRA'
         AGO   .MEND
.CHG     AIF   (&PWP OR &NPWP OR  &IRAP OR &DSNP OR &VOLP).CONF
         AIF   (&ACSP OR &NFP OR  &NMP OR &IRALP OR &SBLP).CONF
         AIF   (&SBP).CONF
         AIF   (&BMF).CHGMF
         AIF   ('&GEN' NE 'PARMS' AND &BGEN).GENERR
         AIF   ('&NAME' EQ '').CHG1
&NAME    DS    0F
.CHG1    ANOP
&P.SRAP  DC    A(0)               PTR TO "STANDARD RETURN AREA"
&P.UIDP  DC    A(0)               PTR TO CL7 UID
&P.ABLP  DC    A(0)               PTR F LEN OF ARE BUFFER
&P.ABP   DC    A(0)               PTR TO ARE BUFFER
         MEXIT
.CHGMF   AIF   (NOT &MFI).CHG2
A&SYSNDX DC    4A(0)              PARM LIST
B&SYSNDX LA    R1,A&SYSNDX        PT TO PARM LIST
         OACINNER F=&SRA,T=0(R1),COM='PTR TO "STANDARD RETURN AREA"'
         OACINNER F=&UID,T=0(R1),TO=+4,COM='PTR TO CL7 UID'
         OACINNER F=&AREBUFL,T=0(R1),TO=+8,COM='PTR TO F BUF LEN'
         OACINNER F=&AREBUF,T=0(R1),TO=+12,COM='PTR TO ARE BUFFER'
         LA    R0,5               CHANGE REQUEST
         AGO   .SVC209
.CHG2    AIF   (&MFL).CHG3
         OACINNER F=&MF(2),T=(R1),COM='PT TO PARM LIST'
         OACINNER F=&SRA,T=0(R1),COM='PTR TO "STANDARD RETURN AREA"'
         OACINNER F=&UID,T=0(R1),TO=+4,COM='PTR TO CL7 UID'
         OACINNER F=&AREBUFL,T=0(R1),TO=+8,COM='PTR TO F BUF LEN'
         OACINNER F=&AREBUF,T=0(R1),TO=+12,COM='PTR TO ARE BUFFER'
         LA    R0,5               CHANGE REQUEST
         AGO   .SVC209
.CHG3    ANOP
&NAME    OACDC &SRA,COM='PTR TO "STANDARD RETURN AREA"'
         OACDC &UID,COM='PTR TO CL7 UID'
         OACDC &AREBUFL,COM='PTR TO F BUFFER LENGTH'
         OACDC &AREBUF,COM='PTR TO ARE BUFFER'
         MEXIT
.ULGF    AIF   (&PWP OR &NPWP OR &IRAP OR &DSNP OR &VOLP).CONF
         AIF   (&ACSP OR &NFP OR &NMP OR &IRALP OR &ABLP).CONF
         AIF   (&ABLP OR &ABP OR &SBLP OR &SBP).CONF
         AIF   (&BMF).ULGFMF
         AIF   ('&GEN' NE 'PARMS' AND &BGEN).GENERR
         AIF   ('&NAME' EQ '').ULGF1
&NAME    DS    0F
.ULGF1   ANOP
&P.SRAP  DC    A(0)               PTR TO "STANDARD RETURN AREA"
&P.UIDP  DC    A(0)               PTR TO CL7 UID
         MEXIT
.ULGFMF  AIF   (NOT &MFI).ULGF2
A&SYSNDX DC    2A(0)              PARM LIST
B&SYSNDX LA    R1,A&SYSNDX
         OACINNER F=&SRA,T=0(R1),COM='PTR TO "STANDARD RETURN AREA"'
         OACINNER F=&UID,T=0(R1),TO=+4,COM='PTR TO CL7 UID'
         LA    R0,6               USER LOGOFF REQUEST
         AGO   .SVC209
.ULGF2   AIF   (&MFL).ULGF3
&NAME    OACINNER F=&MF(2),T=(R1),COM='PT TO PARM LIST'
         OACINNER F=&SRA,T=0(R1),COM='PTR TO "STANDARD RETURN AREA"'
         OACINNER F=&UID,T=0(R1),TO=+4,COM='PTR TO CL7 UID'
         LA    R0,6               USER LOGOFF REQUEST
         AGO   .SVC209
.ULGF3   ANOP
&NAME    OACDC &SRA,COM='PTR TO "STANDARD RETURN AREA"'
         OACDC &UID,COM='PTR TO C L7 UID'
         MEXIT
.SMF     AIF   (&UID OR &PW OR &NPW OR &IRA OR &DSN OR &VOL).CONF
         AIF   (&ACSP OR &NFP OR &NMP OR &IRALP OR &ABP).CONF
         AIF   (&ABLP).CONF
         AIF   (&BMF).SMFMF
         AIF   ('&GEN' NE 'PARMS' AND &BGEN).GENERR
         AIF   ('&NAME' EQ '').SMF1
&NAME    DS    0F
.SMF1    ANOP
&P.SRAP  DC    A(0)               PTR TO "STANDARD RETURN AREA"
&P.SBLP  DC    A(0)               PTR TO F LENGTH OF SMF BUFFER
&P.SBP   DC    A(0)               PTR TO SMF BUFFER
         MEXIT
.SMFMF   AIF   (NOT &MFI).SMF2
A&SYSNDX DC    3A(0)              PARM LIST
B&SYSNDX LA    R1,A&SYSNDX        POINT TO PARM LIST
         OACINNER F=&SRA,T=0(R1),COM='PTR TO "STANDARD RETURN AREA"'
         OACINNER F=&SMFBUFL,T=0(R1),TO=+4,COM='PTR TO F BUFFER LEN'
         OACINNER F=&SMFBUF,T=0(R1),TO=+8,COM='PTR TO SMF BUFFER'
         LA    R0,7               SMF RECORD WRITE REQUEST
         AGO   .SVC209
.SMF2    AIF   (&MFL).SMF3
&NAME    OACINNER F=&MF(2),T=(R1),COM='PT TO PARM LIST'
         OACINNER F=&SRA,T=0(R1),COM='PTR TO "STANDARD RETURN AREA"'
         OACINNER F=&SMFBUFL,T=0(R1),TO=+4,COM='PTR TO F BUFFER LEN'
         OACINNER F=&SMFBUF,T=0(R1),TO=+8,COM='PTR TO SMF BUFFER'
         LA    R0,7               SMF RECORD WRITE REQUEST
         AGO   .SVC209
.SMF3    ANOP
&NAME    OACDC &SRA,COM='PTR TO "STANDARD RETURN AREA"'
         OACDC &SMFBUFL,COM='PTR  TO F BUFFER LEN'
         OACDC &SMFBUF,COM='PTR TO SMF BUFFER'
         MEXIT
.SLGF    AIF   (&UID OR &PW OR &NPW OR &IRA OR &DSN OR &VOL).CONF
         AIF   (&ACSP OR &NFP OR &NMP OR &IRALP OR &ABP).CONF
         AIF   (&ABLP OR &SBLP OR &SBP).CONF
         AIF   ('&GEN' NE 'PARMS' AND &BGEN).GENERR
         AIF   (&BMF AND NOT &MFI AND NOT &MFB).SLGFMF
&NAME    LA    R1,0               NO PARMS
         LA    R0,8               SERVICE LOGOFF CALL
.SVC209  AIF   (&MFB).MEND
         SVC   209
         MEXIT
.SLGFMF  MNOTE 8,'SLGF HAS NO PARMS, AND NO PARM LIST'
.MEND    ANOP
         MEND
