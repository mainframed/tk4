         MACRO
&LBL     OACACTWK &DSECT=YES,&PREFIX=ACT,&PREONLY=NO
         LCLC  &P,&L
.**********************************************************************
.*                                                                    *
.*  OACACTWK - OAC ACCOUNTING WORK AREA                               *
.*    - OBTAINED BY INITIATOR (IEFUJI)                                *
.*    - SUBPOOL 230 KEY ZERO                                          *
.*      (NOT FREED WHEN INITIATOR FREES REGION)                       *
.*                                                                    *
.*                                                                    *
.*  SERIALIZATION:   DURING INITIATOR PROCESSING THIS AREA            *
.*        IS ONLY MODIFIED FROM THE INITIATOR TASK.                   *
.*        (IEFUJI, IEFACTRT)                                          *
.*                                                                    *
.*                                                                    *
.*                   WHEN THE JOB IS RUNNING AND THIS AREA IS UPDATED *
.*        BY IEFU83 COMPARE AND SWAP IS USED.                         *
.*                                                                    *
.*      (THIS AREA IS FORMATED BY ........)                           *
.*                                                                    *
.**********************************************************************
.*
.*
.*  REQUIRE USAGE OF MACRO TO BE DOCUMENTED HERE....
.*
         AIF   ('&SYSECT' EQ 'IEFUJI').USEOK
         AIF   ('&SYSECT' EQ 'IEFU83').USEOK
         AIF   ('&SYSECT' EQ 'IEFACTRT').USEOK
         AIF   ('&SYSECT' EQ 'PREACTRT').USEOK IEFACTRT TEST FRONTEND
.*
         MNOTE 21,' &SYSECT NOT AUTHORIZED TO USE OACACTWK,'
         MNOTE 21,' UPDATE MACRO TO MAINTAIN LIST OF USERS,'
         MNOTE 21,' RE-ASSEMBLY ALL USERS IF REQUIRED BY CHANGES'
         MEXIT
.*
.USEOK   ANOP
&P       SETC  '&PREFIX'
&L       SETC  'OACACTWK'
         AIF   ('&LBL' EQ '').L0
&L       SETC  '&LBL'
.L0      AIF   ('&DSECT' NE 'YES').L1
&L       DSECT ,                  OACACTWK DSECT
         AGO   .L2
.L1      ANOP
&L       DC    0D'0'              OACACTWK AREA
.L2      ANOP
*  OACACTWK PREFIX (BLOCK ID, POINTERS/LENGTHS OF OTHER SECTIONS)
&P.PID   DC    CL8'OACACTWK'      CONTROL BLOCK ID
&P.PSP   DC    AL1(230)           CONTROL BLOCK SUB-POOL
&P.PLEN  DC    AL3(&P.L)          CONTROL BLOCK LENGTH
&P.PASAA DC    A(&P.WK)           @ ACTWK AREA (STEP ACCOUNT FIELDS)
&P.PASAL DC    A(&P.WKL)          LENGTH OF ACTWK AREA
&P.PMTA  DC    A(&P.MT)           @ ACTMT AREA (MOUNT FIELDS)
&P.PMTL  DC    A(&P.MTL)          LENGTH OF ACTMT AREA
         AIF   ('&PREONLY' EQ 'YES').HDR1
         SPACE 1
*   STEP ACCOUNTING FIELDS (IEFUJI, IEFACTRT)
&P.WK    DC    0D'0'
&P.WKMUR DC    H'0'               MUS RATE USED FOR THIS JOB
&P.WKRT  DC    H'0'               COST PER TAPE MOUNT
&P.WKRD  DC    H'0'               COST PER DISK MOUNT
&P.WKFLG DC    X'0'               FLAGS
&P.WK1ST EQU   X'80'              FIRST STEP FLAG (SET BY IEFUJI)
&P.WKRAN EQU   X'40'              AT LEAST ONE STEP WAS EXECUTED
&P.WKSUB EQU   X'20'              JOB IS USERID MONITORED
&P.WKACT EQU   X'10'              JOB IS USERID ACCOUNTED
&P.WKST# DC    X'0'               STEP COUNT
&P.WKDTE DC    F'0'               RESERVED
&P.WKSTA DC    F'0'               ADDRESS OF CURRENT ACCT ENTRY
*   THERE IS ONE ACTENT FOR EACH STEP IN JOB
&P.WKRST EQU   *                  ACTENT SECTION START
&P.WKENT EQU   *                  FIRST ACTENT START
&P.WKSNM DC    CL8' '             STEP NAME
&P.WKSRC DC    CL6' '             STEP RETURN CODE
         DC    XL2'0'             (RESERVED)
&P.WKCPU DC    F'0'               STEP CPU USED
&P.WKIOR DC    F'0'               STEP I/O USED
&P.WKEWS DC    F'0'               STEP ESTIMATED WORKING SET SIZE
&P.WKVMF DC    F'0'               STEP VIRTUAL MEMORY FACTOR USED
&P.WKMUS DC    F'0'               STEP MUS USED
&P.WKCST DC    F'0'               STEP COST
&P.WKAEL EQU   *-&P.WKRST         LENGTH OF EACH ACTENT
         ORG   &P.WKRST
         DC    (255)XL(&P.WKAEL)'00' ACTENT STEP TABLE
&P.WKSTL EQU   *-&P.WKRST         LENGTH OF ACTENT TABLE
&P.WKL   EQU   *-&P.WK            LENGTH OF ACTWK SECTION
         SPACE 1
*   MOUNT FIELDS (IEFUJI, IEFU83, IEFACTRT)
&P.MT    DC    0D'0'
&P.MTF   DC    F'0'      *CS*    MOUNT CONTROL FLAGS (USE CS)
&P.MTFO   EQU   X'80'             MOUNT TABLE OVERFLOWED
&P.MTSL# EQU   25                 # OF UNIT SLOTS GENERATED (EQU)
&P.MTSLN DC    Y(&P.MTSL#)        NUMBER OF UNIT SLOTS
         DC    H'0'               <RESERVED>
&P.MTCDA DC    F'0'      *CS*     COUNT OF DISK MOUNTS
&P.MTCTP DC    F'0'      *CS*     COUNT OF TAPE MOUNTS
&P.MTE   DC    0D'0'     *CSD*    FIRST MOUNT UNIT ENTRY (FOR CSD)
&P.MTEVS DC    XL6'00'   *CSD*    VOLUME SERIAL (LAST VOLUME OR ZERO)
&P.MTEUA DC    Y(0)      *CSD*    @ UCB (OR ZERO) FOR THIS ENTRY
&P.MTEL  EQU   *-&P.MTE  *CSD*    LENGTH OF MOUNT ENTRY
         ORG   &P.MTE
         DC    (&P.MTSL#)XL(&P.MTEL)'00' MOUNT UNIT TABLE ENTRIES
&P.MTL   EQU   *-&P.MT            LENGTH OF MOUNT SECTION
&P.L     EQU   *-&L               LENGTH OF OACACTWK
.HDR1    ANOP
         MEND
