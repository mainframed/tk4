//CSYSMASA  JOB ACCT,MAS,NOTIFY=CSYSMAS,MSGCLASS=A,COND=(0,NE)
/*ROUTE PRINT WYLBUR
//ASM EXEC ASMHCL,PARM.LKED='NORENT,REUS,REFER,XREF,LIST,LET'
//ASM.SYSLIB DD
//           DD
//           DD DISP=SHR,DSN=SYS5.MSSMAC
OACNSWAP CSECT ,
*---------------------------------------------------------------------*
*                                                                     *
*   OACNSWAP -                                                        *
*                                                                     *
*    THIS PROGRAM ALLOWS RUNNING NON-SWAPPING PROBLEM PROGRAMS.       *
*                                                                     *
*    THIS PROGRAM IS IN 'SYS1.OACLINK' (AN AUTHORIZED LIBRARY)        *
*    AND IN THE PPT AS NON-SWAPPABLE.  NOTE THAT THIS PROGRAM         *
*    IS NOT MARKED AUTHORIZED ANYWHERE.                               *
*                                                                     *
*    PARM FIELD FORMAT:  'OMMMMMMMM/PPPP'                             *
*      O - OPTION.  A - ATTACH WITH TASKLIB, X - XCTL                 *
*      M - MODULE NAME (UP TO 8 CHARACTERS, VARIABLE LENGTH)          *
*      / - SLASH (END OF OACNSWAP PARAMETERS)                         *
*      P - REST OF PARAMETER PASSED TO TARGET PROGRAM                 *
*                                                                     *
*   - OBTAINS CONTROL WITH NON-SWAP ALREADY SET                       *
*     WITH NO AUTHORIZATION.  (IS IN PPT AND AUTHORIZED LIBRARY)      *
*   - PARSE PARAMETER STRING FOR TARGET PROGRAM NAME.                 *
*   - CHECKS VALID CALLER (INTERNAL TABLE OF LOGON-IDS/PROGRAMS)      *
*     (OLE, ARPA, ??)                                                 *
*   - BUILDS DCB FOR DDNAME 'OACNSWAP'                                *
*   - LOAD'S SPECIFIED MODULE                                         *
*   - CLOSE DCB                                                       *
*   - XCTL'S TO SPECIFIED MODULE                                      *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 1
W#WORK   DSECT ,
W#SAVE   DS    9D                 SAVE AREA
W#PARMPT DS    A(0)               @ W#PARMLN
W#ECB    DS    F                  ECB FOR SUBTASK
W#TCBPT  DS    A(0)               @ SUBTASK TCB
W#OPT    DS    C' '               OPTION (A - ATTACH, X - XCTL)
W#MOD    DS    CL8' '             MODULE ID
W#PARMLN DS    H'0'               PARAMETER LENGTH
W#PARM   DS    CL256' '           PARAMETER STRING
W#LIST   DS    0F                 LIST FORMS
W#XCTL   XCTL  EPLOC=W#MOD,SF=L
W#ATT    ATTACH EPLOC=W#MOD,TASKLIB=W#DCB,ECB=W#ECB,SF=L
W#OPN    OPEN  (W#DCB),MF=L       DCB OPEN LIST
W#DCB    DCB   DDNAME=OACNSWAP,DSORG=PO,MACRF=(R)
         DS    0F
W#LISTL  EQU   *-W#LIST            LENGTH OF LIST FORMS
W#WORKL  EQU   *-W#WORK           LENGTH OF WORK AREA
         EJECT ,
OACNSWAP CSECT ,
         STM   R14,R12,12(R13)
         LR    R12,R15
         OACUSING OACNSWAP,R12
         LR    R11,R1             SAVE PARM POINTER
         L     R3,=A(W#WORKL)     LENGTH OF WORK AREA
         GETMAIN R,LV=(R3)        GET WORKAREA
         LA    R2,0(,R1)          @ WORK AREA
         SR    R15,R15            CLEAR FOR MVCL
         MVCL  R2,R14             ZERO WORK AREA
         ST    R13,4(,R1)         SAVE @ ORIG SAVE AREA
         LR    R13,R1             USE NEW SAVE AREA
         OACUSING W#WORK,R13
         SPACE 1
*---                FIND TARGET PROGRAM NAME                       ---*
         L     R4,0(,R11)         @ PARAMETER
         LH    R5,0(,R4)          LENGTH OF PARM FIELD
         LA    R4,2(,R4)          CLEAR SIGN AND SKIP LENGTH FIELD
         LTR   R5,R5              ANY PARM?
         BZ    ERRPARM0           BIF NO PARM
         LA    R1,1               CONSTANT
         LA    R2,W#OPT           OUTPUT OPTION, MODULE NAME
         LA    R0,W#MOD+L'W#MOD-1 @ FOR MODULE NAME LENGTH CHECK
         MVC   W#OPT(9),=CL9' '   INIT OPT, MODULE NAME TO BLANKS
         SPACE 1
SCAN1L   LTR   R5,R5              ANY INPUT LEFT?
         BZ    SCAN3
         CLI   0(R4),C'/'         THIS DELIMITER?
         BE    SCAN2
         CR    R2,R0              MODULE NAME TOO LONG?
         BH    ERRPARM1
         MVC   0(1,R2),0(R4)      COPY THIS CHARACTER
         AR    R2,R1              BUMP OUTPUT PTR
         AR    R4,R1              BUMP INPUT PTR
         SR    R5,R1              DCR INPUT LENGTH LEFT
         B     SCAN1L             AND CONTINUE
         SPACE 1
SCAN2    AR    R4,R1              SKIP OVER SLASH
         SR    R5,R1              DECR LENGTH LEFT
SCAN3    CLI   W#MOD,C' '         MODULE NAME BLANK?
         BE    ERRPARM2           BIF NO MODULE SPECIFIED
         LA    R2,W#PARM          OUTPUT AREA ADDR
         LR    R3,R5              OUTPUT AREA LENGTH
         STH   R3,W#PARMLN        SAVE PARM LENGTH FOR PGM
         MVCL  R2,R4              COPY PARM STRING
         LA    R1,W#PARMLN
         ST    R1,W#PARMPT        SET PARM POINTER
         MVI   W#PARMPT,X'80'     SET HIGH BYTE
         SPACE 1
*---              LOCATE JOBNAME                                   ---*
         OACUSING PSA,R0
         L     R3,PSATOLD         @ MY TCB
         OACDROP R0
         OACUSING TCB,R3
         L     R3,TCBTIO          @ MY TIOT
         OACDROP R3
         SPACE 1
*---             VALIDITY CHECK JOBNAME/PROGRAM NAME               ---*
         LM    R0,R2,=A(U#L,U#E,U#B-U#L) ENTRY LEN, @ END, @ BEGIN-LEN
CKU$LP1  BXH   R2,R0,CKU$FAIL     BIF INVALID CALLER....
         CLC   0(8,R2),0(R3)      THIS THE JOBNAME?
         BNE   CKU$LP1
         CLC   8(8,R2),=CL8'*'    ANY PROGRAM ALLOWED?
         BE    CKU$OK
         CLC   8(8,R2),W#MOD      CORRECT PROGRAM?
         BNE   CKU$LP1
         SPACE 1
*---               OPEN 'OACNSWAP' LIBRARY FOR PROGRAM             ---*
CKU$OK   MVC   W#LIST(W#LISTL),X#LIST LIST FORMS TO WORK AREA
         OPEN  (W#DCB),MF=(E,W#OPN) OPEN LIBRARY
         TM    DCBOFLG-IHADCB+W#DCB,DCBOFOPN OPEN OK?
         BZ    ERROPN             BIF OPEN FAILED
         SPACE 1
*---           CHECK OPTION (ATTACH / XCTL)                        ---*
         CLI   W#OPT,C'X'         XCTL?
         BE    GOXCTL
         CLI   W#OPT,C'A'         ATTACH? (WITH TASKLIB)
         BNE   ERROPT             BIF INVALID OPTION
         SPACE 1
         LA    R1,W#PARMPT        @ PARM FOR SUB-TASK
         ATTACH EPLOC=W#MOD,TASKLIB=W#DCB,ECB=W#ECB,SF=(E,W#ATT)
         LTR   R15,R15            ATTACH OK?
         BNZ   ERRATT             BIF ATTACH FAILED
         ST    R1,W#TCBPT         SAVE TCB ADDR FOR DETACH
         WAIT  ECB=W#ECB
         DETACH W#TCBPT           DETACH TASK
         CLOSE MF=(E,W#OPN)       CLOSE LIBRARY
         MVI   W#ECB,0            CLEAR HIGH BYTE
         L     R15,W#ECB          USE AS RETURN CODE
         B     RETURN
         SPACE 3
*---               LOAD TARGET MODULE                              ---*
GOXCTL   LOAD  EPLOC=W#MOD,DCB=W#DCB
         SPACE 1
*---              CLOSE LIBRARY, PREPARE FOR XCTL                  ---*
         CLOSE MF=(E,W#OPN)       CLOSE LIBRARY
         SPACE 1
         LA    R1,W#PARMPT        @ PARM FOR PROGRAM
         OACDROP R13
         OACUSING W#PARMPT,R1
         L     R13,4(,R13)        @ ORIG SAVE AREA
         L     R14,12(,R13)        AND ORIG R14
         LA    R15,W#XCTL         @ XCTL LIST
         LM    R2,R12,12+8+4*R2(R13) AND R2 - R12
         XCTL  (2,12),SF=(E,(15)),EPLOC=W#MOD
         SPACE 1
RETURN   L     R13,4(,R13)
         L     R14,12(,R13)
         LM    R0,R12,12+8(R13)   AND RETURN
         BR    R14
         SPACE 1
CKU$FAIL WTO   '---- OACNSWAP - INVALID JOBNAME',ROUTCDE=(11)
         B     ERRRET
ERROPN   WTO   '---- OACNSWAP - LIBRARY OPEN FAILED',ROUTCDE=(11)
         ABEND 99,DUMP
ERROPT   WTO   '---- OACNSWAP - INVALID OPTION, NOT X OR A',           X
               ROUTCDE=(11)
         B     ERRRET
ERRATT   WTO   '---- OACNSWAP - ATTACH FAILED',ROUTCDE=(11)
         ABEND 100,DUMP
ERRPARM0 WTO   '---- NO PARM FIELD SPECIFIED',ROUTCDE=(11)
         B     ERRRET
ERRPARM1 WTO   '---- PARM FIELD TOO LONG',ROUTCDE=(11)
         B     ERRRET
ERRPARM2 WTO   '---- NO MODULE NAME SPECIFIED',ROUTCDE=(11)
         B     ERRRET
ERRRET   LA    R15,99
         B     RETURN
         EJECT ,
         LTORG ,
         SPACE 1
*---    TABLE OF JOBNAME, PROGRAM NAME OF VALID USERS OF OACNSWAP  ---*
U#B      DC    CL8'ECN3OLE#',CL8'OLEMAIN '    OLE PRODUCTION
U#L      EQU   *-U#B              LENGTH OF ENTRY
         DC    CL8'CSDCLPR ',CL8'ACCONFG '    ARPA TESTING
         DC    CL8'CARPSYS#',CL8'ACCONFG '    ARPA PRODUCTION
         DC    CL8'CSMSCLC1',CL8'INASIRMI'    DLS
         DC    CL8'CSP3DDW#',CL8'CADMNVS2'    CADAM TEST
         DC    CL8'OCADCAM#',CL8'CADMNVS2'    CADAM PRODUCTION
         DC    CL8'CSDCKJH#',CL8'*       '    IMPOLT
         DC    CL8'CSYSMAS#',CL8'*       '    OLE/SYS TESTING
         DC    CL8'CSYSMAS ',CL8'*       '    OLE/SYS TESTING
         DC    CL8'CSDCLPR#',CL8'*       '    ARPA/IMP TESTING
         DC    CL8'CSYSJCJ#',CL8'*       '
         DC    CL8'CSYSCST#',CL8'*       '
         DC    CL8'DATALINK',CL8'INASIRMI'    DLS (AS STC)
         DC    CL8'ZAP ZAP ',CL8'ZAP ZAP '
         DC    CL8'ZAP ZAP ',CL8'ZAP ZAP '
         DC    CL8'ZAP ZAP ',CL8'ZAP ZAP '
U#E      EQU   *-U#L              @ LAST VALID ENTRY
         SPACE 1
X#LIST   DS    0F                 LIST FORMS
X#XCTL   XCTL  EPLOC=1,SF=L
X#ATT    ATTACH EPLOC=1,TASKLIB=1,ECB=1,SF=L
X#OPN    OPEN  (1),MF=L           DCB OPEN LIST
X#DCB    DCB   DDNAME=OACNSWAP,DSORG=PO,MACRF=(R)
         DS    0F
X#LISTL  EQU   *-X#LIST            LENGTH OF LIST FORMS
         SPACE 1
         OACMAP DSECT=(PSA,TCB,DCB),PRINT=NONE
         OACREGS PRINT=NONE
         END
//*KED.SYSLMOD DD DISP=OLD,DSN=SYS1.OACLINK,SPACE=,UNIT=
//LKED.SYSIN   DD *
  NAME OACN$WAP
//*
//C  EXEC PGM=COMPARE,PARM='OACN$WAP,OACNSWAP'
//STEPLIB  DD DISP=SHR,DSN=SYS5.MSSLOAD
//SYSPRINT DD SYSOUT=*
//I1       DD DISP=(SHR,PASS),DSN=&&GOSET
//I2       DD DISP=SHR,DSN=SYS1.OACLINK
//
