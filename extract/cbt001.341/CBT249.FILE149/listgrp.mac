//CSYSMASA JOB ACCT,MSGCLASS=A,COND=(0,NE)
/*ROUTE PRINT WYLBUR
//ASM EXEC ASMHCL
//ASM.SYSLIB DD
//           DD
//           DD DISP=SHR,DSN=SYS5.MSSMAC
         TITLE 'LISTGRP - GIVE DETAILS OF GENERIC GROUP COMPOSITION'
         MACRO
         OACWORK &DUMMY
TSOPL    DS    0D                 BASE FOR XC
         OACTSOPL PL=(IOPL,PPL)
TSOECB   DS    F                  TSO SERVICE ECB
PTPB     PUTLINE MF=L             PUTLINE PARM BLK
PTPBL    EQU   *-PTPB
PARANS   DS    F                  IKJPARS "ANSWER PLACE"
TSOPLEN  EQU   *-TSOPL            LENGTH FOR XC
BUFLEN   DS    H                  BUFFER LENGTH
BUFOFF   DS    H                  BUFFER OFFSET
BUF      DS    CL130              OUTPUT BUFFER
BUFDAD   EQU   BUF+1              DEVICE ADDRESS
BUFTYPE  EQU   BUFDAD+5           VOLID OR TYPE
BUFVOL   EQU   BUFTYPE+9          DEVICE STATUS
BUFSTAT  EQU   BUFVOL+8           VARIABLE STATUS PORTION
BUFUCBLN EQU   BUFSTAT+4-BUF
EGNPARM  DS    3F                 PARM LIST FOR OACEGNTU
EGNLIST  DS    300F               LIST OF UCB'S
         MEND
         MACRO
&LBL     CHK   &FLD,&MSK,&STR
         LCLA  &A,&B
&A       SETA  K'&STR
&B       SETA  &A-2
&LBL     TM    UCB&FLD,UCB&MSK
         BZ    CHK&SYSNDX
         MVC   0(&B,R3),=C&STR
         LA    R3,&A.(R3)
         LA    R4,&A.(R4)
CHK&SYSNDX DS  0H
         MEND
         COPY  OACGBLS
         SPACE 2
         OACMAP DSECT=(CVT,UCB),PRINT=NONE
         EJECT
LISTGRP  OACENTER DSECT=WDSECT,WORK=(,YES),STACK=(256,,STKERR,ABEND),  X
               EXIT=EXIT,PRINT=SHORT
*
*              INITIALIZE ALL THE SERVICE PARM LISTS
*
         XC    TSOPL(TSOPLEN),TSOPL ZERO PARM LSTS BEFOR BUILD
         OACBLDPL CPPL=0(R9),ECB=TSOECB,                               X
               IOPL=IOPL,                                              X
               PPL=(PPL,(=V(PCL),I),PARANS,(0(R9),I))
         XC    BUFOFF(2),BUFOFF   CLEAR BUFFER OFFSET
         LA    R1,300             GET LENGTH OF LIST IN FULLWORDS
         ST    R1,EGNPARM
         LA    R1,EGNLIST
         ST    R1,EGNPARM+4
         LA    R1,BUF             POINT TO BUFFER
         ST    R1,EGNPARM+8
*
*              CALL PARSE
*
         CALLTSSR EP=IKJPARS,MF=(E,PPL)
         LTR   R15,R15            CHECK FOR ERROR
         BZ    PROCESS
         CH    R15,=H'8'          CHECK FOR ATTENTION
         BE    RC0                RETURN QUIETLY
         MVC   BUFLEN(PARSERRL),PARSERR
         OACCALL PUTDATA          TELL THE USER
         B     RC12               ERROR RETURN
PARSERR  DC    H'23',H'0',C'LISTGRP - PARSE ERROR'
PARSERRL EQU   *-PARSERR
PROCESS  L     R9,PARANS          PT TO PDL
         LA    R9,GRPNAME-PDL(R9) POINT TO FIRST GROUPNAME
         TM    6(R9),X'80'        IS IT THERE?
         BO    UCB1               IF NOT EMPTY PROCESS
         LINK  EP=CLEAR           CLEAR THE SCREEN FOR HIM
         LOAD  EP=DEVNAMET        HE'S ASKING FOR ALL THE NAMES
         LR    R8,R0              SAVE PTR TO TABLE
         L     R7,0(R8)           GET NUMBER OF ENTRIES IN TABLE
         LA    R8,4(R8)           POINT TO FIRST ENTRY IN TABLE
         LA    R6,10              NUMBER OF ENTRIES PER LINE
         MVC   BUFLEN(2),=H'84'
         MVI   BUF,C' '           CLEAR BUFFER
         MVC   BUF+1(129),BUF
         LA    R5,BUF             PT TO FIRST POSITION
NAMELOOP MVC   0(6,R5),0(R8)      MOVE NAME TO BUFFER
         LA    R5,8(R5)           POINT TO NEXT BUFFER POSITION
         BCT   R6,NAME1           IF NOT OFF END OF NAME CONTINUE
         OACCALL PUTDATA          PUT OUT THE BUFFER
         MVI   BUF,C' '           CLEAR THE BUFFER
         MVC   BUF+1(129),BUF
         LA    R6,10              NUMBER OF ENTRIES PER LINE
         LA    R5,BUF             RESET POINTER
NAME1    LA    R8,12(R8)          POINT TO NEXT NAME ENTRY
         BCT   R7,NAMELOOP
         CH    R6,=H'10'          SEE IF WE ARE AT BEGINING OF LINE
         BE    NAME2              IF SO DONT REPUT
         OACCALL PUTDATA
NAME2    DELETE EP=DEVNAMET
         B     RC0
UCB1     LOAD  EP=OACEGNTU        GET ADDRESS OF SERVICE ROUTINE
         LR    R8,R0              SAVE FOR LATER USE
LOOP     TM    6(R9),X'80'        SEE IF THERE IS A GRPNAME THERE
         BNO   DONE               IF NOT THEN DONE
         MVI   BUF,C' '           CLEAR BUFFER
         MVC   BUF+1(129),BUF
         LH    R1,4(R9)           GET LENGTH
         BCTR  R1,0               DECREMENT FOR EXECUTE
         L     R2,0(R9)           GET PTR TO GROUPNAME
         MVC   BUF(1),0(R2)       <EXECUTED>
         EX    R1,*-6             MOVE IN THE GROUPNAME
         MVC   BUFLEN(2),=H'12'   MOVE IN LENGTH
         LA    R1,EGNPARM         POINT TO PARM LIST
         BALR  R14,R8             TRY TO FIND IT
         CH    R15,=H'8'          SEE IF NOT FOUND
         BNE   FOUND
         MVC   BUF+10(9),=C'NOT FOUND'
         MVC   BUFLEN(2),=H'24'
         OACCALL PUTDATA
         B     NEXT
FOUND    OACCALL PUTDATA
         LH    R7,EGNLIST         GET NUMBER OF UCB'S
         LA    R6,EGNLIST+4       POINT TO FIRST UCB ADD
NEXTUCB  L     R5,0(R6)           GET UCB ADDR
         OACUSING UCB,R5
         MVI   BUF,C' '           CLEAR BUFFER
         MVC   BUF+1(129),BUF
         MVC   BUFLEN(2),=AL2(BUFUCBLN)
         MVC   BUFDAD(3),UCBNAME  MOVE IN DEVICE ADDRESS
         TM    UCBJBNR,UCBVRDEV   SEE IF VIO
         BNO   FND1
         MVC   BUFTYPE(3),=C'VIO'
         B     FND9
FND1     CLI   UCBTBYT3,UCB3DACC
         BNE   FND2
         MVC   BUFTYPE(4),=C'DASD'
         B     FND8
FND2     CLI   UCBTBYT3,UCB3TAPE
         BNE   FND3
         MVC   BUFTYPE(4),=C'TAPE'
         B     FND8
FND3     CLI   UCBTBYT3,UCB3COMM
         BNE   FND4
         MVC   BUFTYPE(4),=C'COMM'
         B     FND9
FND4     CLI   UCBTBYT3,UCB3UREC
         BNE   FND5
         MVC   BUFTYPE(7),=C'UNT REC'
         B     FND9
FND5     CLI   UCBTBYT3,UCB3DISP
         BNE   FND6
         MVC   BUFTYPE(7),=C'DISPLAY'
         B     FND9
FND6     CLI   UCBTBYT3,UCB3CTC
         BNE   FND7
         MVC   BUFTYPE(7),=C'CH/CH'
         B     FND9
FND7     CLI   UCBTBYT3,UCB3CHAR
         BNE   FND7
         MVC   BUFTYPE(7),=C'CHR RDR'
         B     FND9
FND8     LA    R3,BUFSTAT         POINT TO VARIABLE PORTION OF BUF
         LA    R4,BUFUCBLN        GET CURRENT LENGTH OF BUF
         TM    UCBSTAT,UCBONLI    SEE IF ONLINE
         BNO   OFFLINE            IF NOT VOLUME NAME NOT THERE
         CLI   UCBVOLI,0          IS THERE A NAME?
         BE    ONLINE             IF NOT CONTINUE
         MVC   BUFVOL(6),UCBVOLI  MOVE IN VOL NAME
         B     ONLINE
FND9     LA    R3,BUFSTAT         POINT TO VARIABLE PORTION OF BUF
         LA    R4,BUFUCBLN        GET CURRENT LENGTH OF BUF
         TM    UCBSTAT,UCBONLI    SEE IF ONLINE
         BO    ONLINE
OFFLINE  MVC   0(7,R3),=C'OFFLINE'
         LA    R3,9(R3)           BUMP BUFFER PTR
         LA    R4,9(R4)           BUMP LENGTH
ONLINE   CHK   STAT,CHGS,'PENDING'
         CHK   STAT,RESV,'RESERVED'
         CHK   STAT,UNLD,'UNLOAD'
         CHK   STAT,ALOC,'ALLOC'
         CHK   STAT,PRES,'PERM RES'
         CHK   FLC,IVRS+UCBIVRR,'INT REQ'
         CHK   FLA,BSY,'BUSY'
         CHK   FLA,NRY,'NOT READY'
         CHK   FLA,CUB,'CU BUSY'
         CHK   FLA,ACTV,'ACTIVE'
         CHK   FLA,QISCE,'QUIESCED'
UCBPRT   STH   R4,BUFLEN          STORE LENGTH
         OACCALL PUTDATA
         LA    R6,4(R6)           POINT TO NEXT UCB ADDRESS
         BCT   R7,NEXTUCB
NEXT     MVC   BUFLEN(SPMSGL),SPMSG     SPACE
         OACCALL PUTDATA
         L     R9,8(R9)           PT TO NEXT LIST ELEMENT
         C     R9,=X'FF000000'    SEE IF END OF LIST
         BNE   LOOP
         OACDROP R5
DONE     DELETE EP=OACEGNTU
RC0      SR    R15,R15            RC 0
         B     EXIT
*
RC12     LA    R15,12             ERROR RC
         B     EXIT               RETURN TO CALLER
*
SPMSG    DC    H'5',H'0',C' '
SPMSGL   EQU   *-SPMSG
         LTORG
         OACDROP R10
         TITLE 'LISTGRP - SUBROUTINES'
PUTDATA  OACSUB
         XC    PTPB(PTPBL),PTPB   ZERO PARM BLK
         PUTLINE PARM=PTPB,OUTPUT=(BUFLEN,,DATA),MF=(E,IOPL)
         OACRET RC=(R15)
         OACDROP R10
         TITLE 'LISTGRP - PARSE CONTROLE LIST'
         PRINT ON,NOGEN
PCL      IKJPARM DSECT=PDL
GRPNAME  IKJIDENT 'GROUPNAME-LIST',LIST,FIRST=ANY,OTHER=ANY,MAXLNTH=8
         IKJENDP
         END
