VMCMD    TITLE 'VM/MVS COMMAND ISSUER'
*/*
*/*   "VMCMD" IS A PROGRAM RUN ON A MVS GUEST VIRTUAL MACHINE
*/*   THAT ISSUES VM/370 COMMANDS VIA A DIAGNOSE INSTRUCTION.
*/*
*/*   THE COMMAND(S) TO BE ISSUED ARE PASSED TO "VMCMD" AT PROGRAM
*/*   START TIME VIA THE PARM FIELD.  MULTIPLE COMMANDS (SEPARATED
*/*   BY A COMMA, ARE HANDLED.  "VMCMD" CAN BE STARTED VIA A COMMND..
*/*   MEMBER, THRU THE JES2 INIT PARMS, OR JUST INVOKED ANY OLD TIME.
*/*
*/*   EXAMPLE:  S VMCMD,PARM='CP SET STB 700K,CP SET MVS-C RES 1000'
*/*             WHERE VMCMD IS A PROC THAT MERELY EXECUTES VMCMD.
*/*
*/*   NOTES:
*/*     . THE MODESET MACRO IS USED TO GAIN/LOSE SUPERVISOR STATE.
*/*     . THE PROGRAM IS LINKED WITH AC=1.
*/*
*/*   AUTHOR - PETER TITTLER, 4/79
*/*
*/*   INSTALLED/COMMENTED/UPDATED AT ATARI --- FRANK PAJERSKI,07FEB82
*/*
         EJECT
VMCMD    CSECT
         SAVE  (14,12),,VMCMD_&SYSDATE_&SYSTIME
         LR    R12,R15             R12 IS THE BASE REG
         USING VMCMD,R12
*
*   LINKAGE
*
         LR    R11,R13             A(OLD SAVE AREA)
         CNOP  0,4                 ALIGNMENT
         BAL   R13,*+76            A(NEW SAVE AREA)
         DC    18F'0'
         ST    R13,8(,R11)         LINK SA'S ONE WAY
         ST    R11,4(,R13)         LINK SA'S ANOTHER WAY
*
*   CHECK FOR PROPER ENVIRONMENT & THE PRESENCE OF A PARM FIELD
*
         LR    R2,R1               SAVE R1 TO BE SAFE
         MODESET MODE=SUP          NEED SUPV STATE FOR STIDP/DIAGNOSE
         STIDP CPUID               LET'S SEE WHAT'S GOING ON
         CLI   CPUID,X'FF'         RUNNING UNDER VM/370?
         BNE   NOVM                BIF NOT - BETTER NOT ISSUE DIAGNOSE
         L     R3,0(,R2)           A(PARM FIELD) IN R3
         LH    R4,0(,R3)           LENGTH OF PARM FIELD IN R4
         LTR   R4,R4               IS THERE A PARM FIELD?
         BZ    NOPARM              BIF NOT - NOTHING TO DO
*
*   FIND A "COMPLETE" COMMAND IN THE PARAMETER FIELD
*
         LA    R7,2(,R3)           A(START OF A COMMAND)
         LR    R5,R7               SAVE IT WHILE WE SCAN FOR CMD END
SCANPARM EQU   *
         CLI   0(R7),C','          END OF A COMMAND?
         BE    COMMAND             BIF YES - GO PROCESS THE COMMAND
         LA    R7,1(,R7)           ELSE, POINT AT NEXT CHARACTER
         BCT   R4,SCANPARM         DECREMENT PARM LENGTH & GO LOOK
*
*   ISSUE THE COMMAND VIA A DIAGNOSE & WTO ITS RETURN CODE
*
COMMAND  EQU   *
         LR    R6,R7               A(LAST COMMAND CHARACTER + 1)
         SR    R6,R5               PRODUCE THE COMMAND LENGTH
         LRA   R5,0(R5)            NEED THE REAL ADDRESS OF THE CMD
         SPACE 1
         DC    X'83',X'56',X'0008'  ISSUE VM CMD VIA DIAGNOSE 8
         SPACE 1
         CVD   R6,RTNCODE          DECIPHER THE RETURN CODE
         UNPK  WTORC(2),RTNCODE    & SET IT UP FOR A WTO
         OI    WTORC+1,X'F0'
         LA    R1,WTOMSG1
         WTO   MF=(E,(1))          TELL THE WORLD
*
*   NOW SEE ABOUT ANOTHER COMMAND IN THE PARAMETER FIELD
*
         BCTR  R4,0                SUBTRACT 1 FOR A POSSIBLE COMMA
         LTR   R4,R4               ANY LENGTH LEFT TO THE PARM FIELD?
         BNP   EXIT                BIF NOT - WE'VE DONE OUR DUTY
         LA    R7,1(,R7)           POINT PAST THE COMMA
         LR    R5,R7               A(START OF A COMMAND)
         B     SCANPARM            GO SEE WHERE IT ALL ENDS
*
*   EXITING . . .
*
NOVM     WTO   'VMCMD -- NOT RUNNING UNDER VM, DIAGNOSE NOT ISSUED'
         B     EXIT
NOPARM   WTO   'VMCMD -- NO PARM FIELD SPECIFIED'
*
EXIT     EQU   *
         MODESET MODE=PROB         GET BACK TO WHAT WE REALLY ARE
         L     R13,4(,R13)         A(OLD SAVEAREA)
         RETURN (14,12),RC=0
         SPACE 3
*/*
*/*   CONSTANTS
*/*
CPUID    DC    D'0'
RTNCODE  DC    D'0'
         SPACE 2
WTOMSG1  WTO   'VMCMD -- DIAGNOSE RETURN CODE = XX',MF=L
WTORC    EQU   WTOMSG1+36
         SPACE 2
         REGS
         SPACE 2
         END
