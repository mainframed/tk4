RESET2VM TITLE 'ACF/VTAM APPLICATION TO RESET TERMINAL FROM VTAM TO VM'
RESET2VM CSECT ,
*
*
*   THIS PROGRAM IS AN ACF/VTAM APPLICATION PROGRAM DESIGNED TO
*   TRANSFER A LOCAL 3270 TERMINAL TO VM/370 VIA THE VM "DIAGNOSE"
*   COMMAND INTERFACE.  THE TERMINAL MUST BE DEFINED AS "SPECIAL"
*   TO THE MVS VIRTUAL MACHINE.  THE TRANSFER IS EFFECTED BY THE
*   TERMINAL USER ENTERING THE COMMAND "RESET" TO USS.
*
*   THE SOURCE WAS OBTAINED FROM JOHN BEVIS (UNIVERSITY OF FLORIDA).
*   THE COMMENTS WERE ALTERED/AUGMENTED IN A FEW PLACES, LOGIC WAS
*   CHANGED TO HANDLE OUR LOCAL TERMINAL NAMES OF "L3278CUA", AND CODE
*   WAS PRODUCED FOR HIS (UNAVAILABLE) MACROS.  MUCH THANX TO JOHN
*   FOR HIS HELP.
*
*
*   HISTORY ---
*      ... FRANK PAJERSKI / ATARI
*         04FEB82 - GOT THE SUCKER WORKING
*
*
*   ---------- OTHER RELATED CHANGES ----------
*
*     --- CATALOGED PROCEDURE ---
*          //RESET2VM EXEC PGM=RESET2VM
*          //SYSUDUMP DD  SYSOUT=A
*
*     --- ADDITION TO "USSTAB" ---
*          RESET2VM USSCMD  CMD=RESET,REP=LOGON,FORMAT=PL1
*                   USSPARM PARM=APPLID,DEFAULT=RESET2VM
*                   USSPARM PARM=LOGMODE
*                   USSPARM PARM=DATA
*
*     --- ADDITION TO "INTTAB" ---
*          LOGCHAR APPLID=(APPLICID,RESET),SEQNCE='RESET'
*          LOGCHAR APPLID=(APPLICID,RESET),SEQNCE='reset'
*
*
*     --- VTAMLST MEMBER "APPLRSET" ---
*                   VBUILD TYPE=APPL
*          RESET    APPL  EAS=5,AUTH=(ACQ)
*
*
*     --- ASM / LINK ---
*          //ASM     EXEC ASMFCL
*          //ASM.SYSIN     DD  DSN=......,DISP=SHR
*          //LKED.SYSLMOD  DD  DSN=SYS2.LINKLIB,DISP=SHR
*          //LKED.SYSIN    DD  *
*             IDENTIFY RESET2VM('FJP/04FEB82')
*             SETCODE AC(1)
*             NAME  RESET2VM(R)
*
*
         EJECT
R0       EQU   0
R1       EQU   1
R2       EQU   2                       WORK
R3       EQU   3                       WORK
R4       EQU   4                       WORK
R5       EQU   5                       WORK
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12                      BASE
R13      EQU   13
R14      EQU   14
R15      EQU   15
*
*   ESTABLISH BASE REG & LINKAGE
*
         SAVE  (14,12),,RESET2VM_&SYSDATE_&SYSTIME
         LR    R12,R15
         USING RESET2VM,R12
         ST    R13,SAVE0+4
         LA    R15,SAVE0
         ST    R15,SAVE0
         LR    R13,R15
*
*   OPEN THE ACB
*
         OPEN  VTAMACB                 SAY HI TO VTAM
         LTR   R15,R15                 DID HE/SHE/IT SAY HI BACK?
         BNZ   OPENERR                 BIF NOT
         MVC   WRKRPL(WRPLLEN),RPL1    SETUP THE RPL
         SETLOGON RPL=WRKRPL,OPTCD=START   ALLOW LOGON REQUESTS
         LTR   R15,R15                 DID THE SETLOGON DO ITS THING?
         BNZ   SETLOGER                BIF NOT
         EXTRACT COMMAREA,'S',FIELDS=(COMM)
         L     R5,COMMAREA             A(CIB POINTER)
         MVC   ECBLIST,0(R5)           COPY A(ECB)
         L     R2,4(,R5)               A(CIB)
         LTR   R2,R2                   IS THERE A CIB?
         BZ    GETBUFF                 BIF NOT - SKIP SOME CODE
         QEDIT ORIGIN=4(R5),BLOCK=(R2)     FREE THE START CIB
GETBUFF  QEDIT ORIGIN=4(R5),CIBCTR=2   OBTAIN BUFFERS
WAITLP   WAIT  1,ECBLIST=ECBLIST       WAIT FOR COMPLETION
         TM    TPENDECB,X'40'          IS THIS TPEND?
         BO    QUIT                    BIF YES - GET OUT
         L     R5,COMMAREA             A(CIB POINTER)
         L     R2,4(,R5)               A(CIB)
         CLI   4(R2),X'40'             IS THIS A "STOP" COMMAND?
         BE    QUIT                    BIF YES - GET OUT
         QEDIT ORIGIN=4(R5),BLOCK=(R2)     FREE THE CIB
         B     WAITLP                  AND WAIT
         SPACE 3
OPENERR  WTO   'RESET001 - VTAMACB OPEN ERROR - EXECUTION TERMINATED'
         B     QUIT
SETLOGER WTO   'RESET002 - SETLOGON ERROR - EXECUTION TERMINATED'
         B     QUIT
*
*   MAINLINE EXIT
*
QUIT     DS    0H
         CLOSE VTAMACB                 SAY BYE TO VTAM
         L     R13,SAVE0+4
         RETURN (14,12)
         EJECT
*
*   LOGON EXIT ROUTINE
*
LOGEXIT  DS    0H
         USING *,R15
         L     R12,BASEADDR
         DROP  R15
         USING RESET2VM,R12
         LA    R13,SAVEEXIT            A(EXIT SAVEAREA)
         LR    R5,R14                  SAVE THE RETURN ADDRESS
         LR    R2,R1                   SAVE THE PARM ADDRESS
         L     R3,4(,R2)               POINTER TO NAME
         USING LOGPARM,R3
         MVC   TERM,LOGNAME            TERMINAL NAME
         MODCB AM=VTAM,NIB=NIB,NAME=(*,LOGNAME)  SET IN THE NIB
         DROP  R3
         MVC   WRKRPL(WRPLLEN),RPL1    MOVE IN THE RPL
         CLC   =C'L3278',TERM          IS THIS A LOCAL TERMINAL?
         BNE   NOTALLOW                BIF NOT - DON'T TALK TO IT
         OPNDST RPL=WRKRPL,OPTCD=ACCEPT   ACCEPT THE TERMINAL
         LTR   R15,R15                 IS IT OK?
         BNZ   NOTALLOW                BIF NO - FORGET THIS TURKEY
         MVC   BUF4+6(3),TERM+5        MOVE IN TERMINAL ADDRESS
         MODESET KEY=ZERO,MODE=SUP,RELATED=(GET,SUP)
         LRA   R6,BUF4                 A(BUFFER)
         LA    R7,L'BUF4               L(BUFFER)
         DC    X'83670008'             WHAT HATH DIAGNOSE WROUGHT????
         MODESET KEY=NZERO,MODE=PROB,RELATED=(FREE,SUP)
NOTALLOW CLSDST RPL=WRKRPL
         BR    R5                      RETURN
BUF4     DC    C'RESET XXX'            MESSAGE BUFFER
         EJECT
*
*   "Z NET" EXIT
*
TPEND    DS    0H
         USING *,R15
         L     R12,BASEADDR
         DROP  R15
         USING RESET2VM,R12
         LR    R5,R14                  SAVE THE RETURN ADDRESS
         POST  TPENDECB                POST THE MAIN TASK
         BR    R5                      RETURN TO VTAM
         EJECT
*
*   VARIOUS WORKAREAS
*
BASEADDR DC    A(RESET2VM)             R12 ADDRESS FOR EXITS
SAVE0    DC    18F'0'                  MAINLINE CODE SAVEAREA
SAVEEXIT DC    18F'0'                  LOGON EXIT SAVEAREA
TPENDECB DC    A(0)                    TPEND ECB
CID      DC    A(0)                    CID
TERM     DC    CL8' '                  TERMINAL NAME
COMMAREA DC    A(0)                    A(CIB POINTER)
ECBLIST  DC    A(0)                    ECB LIST FOR TERMINATION
         DC    X'80',AL3(TPENDECB)
INQUIREA DS    128X
BUF2     DS    XL256
APPLNAME DC    X'05',C'RESET'
         LTORG
         EJECT
VTAMACB  ACB   AM=VTAM,APPLID=APPLNAME,EXLST=ACBEXIT
         EJECT
ACBEXIT  EXLST AM=VTAM,LOGON=LOGEXIT,TPEND=TPEND
         EJECT
RPL1     RPL   AM=VTAM,ACB=VTAMACB,AREA=INQUIREA,AREALEN=128,NIB=NIB
WRPLLEN  EQU   *-RPL1
         EJECT
RPL2     RPL   AM=VTAM,ACB=VTAMACB,AREA=BUF2,AREALEN=256,              X
               OPTCD=(ANY,CA,Q)
         EJECT
WRKRPL   RPL   AM=VTAM,ACB=VTAMACB,AREA=BUF2,AREALEN=256,              X
               OPTCD=(SPEC,CA,Q)
         EJECT
NIB      NIB   MODE=RECORD
         EJECT
LOGPARM  DSECT ,
LOGNAME  DS    CL8
         IFGRPL AM=VTAM
         SPACE 3
         END
