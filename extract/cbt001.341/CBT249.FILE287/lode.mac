         MACRO
&MINE    LODE  &BUCKET,&HONEY,&OP=LA,&OPR=LR,&REV=                *TSM*
.*             1ST PARAMETER IS A REGISTER #
.*             2ND PARAMETER IS AN RX EXPRESSION OR (REG)
.*             OP            IS LA, L, LH, OR N                   *TSM*
.*             OPR           IS LR OR NR                          *TSM*
.*             REV=YES       FOR &OPR, REVERSE REGISTERS          *GPP*
         GBLB  &SLANDER
         LCLC  &REG,&LIBEL
&REG     SETC  '&BUCKET'
         AIF   ('&REG'(1,1) NE 'R').NOSY
&REG     SETC  '&BUCKET'(2,K'&BUCKET-1)
.NOSY    AIF   (&SLANDER).NOLAB
&LIBEL   SETC  '&MINE'
.NOLAB   AIF   ('&HONEY'(1,1) NE '(').LA
         AIF   ('&HONEY'(2,1) EQ 'R').SYMBLR
         AIF   ('&HONEY' EQ '(&REG)').NO
         AGO   .LR
.SYMBLR  AIF   ('&HONEY' EQ '(R&REG)').NO
         AIF   ('&REV' NE 'YES').NOREV                      *TSM* *GPP*
&LIBEL   &OPR  &HONEY(1),R&REG                                    *GPP*
         AGO   .GO
.NOREV   ANOP  ,                                                  *GPP*
&LIBEL   &OPR  R&REG,&HONEY(1)                                    *TSM*
         AGO   .GO
.LR      ANOP
         AIF   ('&REV' NE 'YES').NOREV2                     *TSM* *GPP*
&LIBEL   &OPR  &HONEY(1),&BUCKET                                  *GPP*
         AGO   .GO
.NOREV2  ANOP  ,                                                  *GPP*
&LIBEL   &OPR  &BUCKET,&HONEY(1)                                  *TSM*
         AGO   .GO
.LA      AIF   ('&OP' EQ 'LA' AND '&HONEY' EQ '0').SR
&LIBEL   &OP   &BUCKET,&HONEY
         AGO   .GO
.SR      ANOP
&LIBEL   XR    &BUCKET,&BUCKET
.GO      ANOP
&SLANDER SETB  1
.NO      MEND
