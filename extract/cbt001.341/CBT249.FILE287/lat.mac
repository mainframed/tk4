         MACRO
&NM      LAT   &R,&ADDR,&BZ,&BNZ,&LA=
.*             THIS MACRO IS THE PROPERTY OF GERHARD POSTPISCHIL
.*             IT IS PART OF EXHIBIT, AND UNAUTHORIZED USERS
.*             WILL BE PERSECUTED BY MAD MAN METZ:
.*             YOU WUZ WARNED!
         GBLC  &MODEL
&NM      LODESTAR                                             ** TSM **
         AIF   ('&MODEL' EQ '370').A370
.DO360   ANOP
&NM      LODE  &R,&ADDR,OP=L LOAD ADDRESS
&NM      LODE  &R,0(&R),OP=LA .  CLEAR HIGH BYTE AND LABEL
         AIF   ('&LA' NE 'FW').LTR                                *TSM*
&NM      LODENAME                                                 *TSM*
         AGO   .LTR                                               *TSM*
         AIF   ('&BZ' EQ '' AND '&BNZ' EQ '').MEX
.LTR     LTR   &R,&R              TEST FOR ZERO                   *TSM*
         AGO   .BRT
.A370    AIF   ('&LA' EQ '').ICM370                           ** TSM **
         AIF   ('&LA' EQ 'RX').ICMRX                          ** TSM **
         AIF   ('&LA' EQ '0').ICMSR                           ** TSM **
         AIF   ('&LA' EQ 'FW').ICMFW                              *TSM*
         MNOTE 8,'&&LA=&LA INVALID - MUST BE RX, 0, OR FW'        *TSM*
         MNOTE 8,'&&LA=RX ASSUMED'                                *TSM*
.ICMRX   ANOP                                                 ** TSM **
&NM      LODE  &R,&ADDR,OP=LA        OFFSET(X,BASE)           ** TSM **
         ICM   &R,7,1(&R)                                     ** TSM **
         AGO   .BRT                                           ** TSM **
.ICMSR   ANOP                                                 ** TSM **
&NM      LODE  &R,0                                           ** TSM **
.ICM370  ANOP                                                 ** TSM **
&NM      LODENAME                                             ** TSM **
         ICM   &R,7,1+&ADDR .    LOAD ADDRESS
         AGO   .BRT                                               *TSM*
.ICMFW   ANOP                                                     *TSM*
&NM      LODENAME                                                 *TSM*
         ICM   &R,15,&ADDR         LOAD FULLWORD                  *TSM*
.BRT     AIF   ('&BZ' EQ '').BRNT
         AIF   ('&BZ'(1,1) EQ '(').BRZ
         BZ    &BZ .         BRANCH IF ZERO
         AGO   .BRNT
.BRZ     BZR   &BZ(1) .      BRANCH IF ZERO
.BRNT    AIF   ('&BNZ' EQ '').MEX
         AIF   ('&BNZ'(1,1) EQ '(').BRNZ
         BNZ   &BNZ .        BRANCH IF NOT ZERO
.MEX     MEXIT
.BRNZ    BNZR  &BNZ(1) .     BRANCH IF NOT ZERO
         MEND
