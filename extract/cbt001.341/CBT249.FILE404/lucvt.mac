          MACRO
&LABEL    LUCVT &P1,&P2
.*
.*   MACRO TO LOCATE AND TEST FOR PRESENCE OF THE UCVT POINTER IN
.*   SYSTEM CVT.
.*   TWO PARAMETERS ARE REQUIRED
.*       THE FIRST IS REQUIRED - A REGISTER DESIGNATION FOR THE
.*                               RETURN OF THE UCVT ADDRESS
.*       THE SECOND IS OPTIONAL - ROUTINE NAME TO TRANSFER TO IF
.*                                UCVT POINTER IS NOT FOUND,
.*                                MAY BE IN REGISTER FORMAT TO
.*                                DESIGNATE A BR TYPE BRANCH
.*                                E.G.  (R14) --> BZR R14
.*
.*   CHANGE ACTIVITY - WRITTEN 07/20/77 PWM
.*                   - ADD REG BRANCH FUNCTION 09/07/77 PWM
.*
          LCLC  &P3
          PUSH USING
&P3       SETC  '&P1'
          AIF   ('&P1' NE '').REALP1
&P3       SETC  'R1'
.REALP1   ANOP
          USING CVTMAP,&P3
&LABEL    L     &P3,16                  A (CVT)
          ICM   &P3,7,CVTUSER+1         A (USER CVT)
          DROP  &P3
          AIF   ('&P2' EQ '').NOTEST
          AIF   ('&P2'(1,1) EQ '(').REGNOTE
          BZ    &P2                      USER CVT NOT FOUND
          AGO   .NOTEST
.REGNOTE  ANOP
          BZR   &P2(1)                   USER CVT NOT FOUND
.NOTEST   ANOP
          POP   USING
          MEND
