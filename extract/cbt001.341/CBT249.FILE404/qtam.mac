         MACRO
&NAME    QTAM  &OPCODE,&TERM,&MESSAGE,&EOF
         LCLC  &OP
.*             AUTHOR.   DON HIGGINS.
.*             DATE.     11/08/70.
.*             REMARKS.  THIS MACRO GENERATES IN-LINE CODE TO CALL
.*                       QTAMFIP SUBROUTINE WHICH WILL HANDLE ALL
.*                       GET, PUT, AND CLOSE COMMANDS FOR QTAM
.*                       MESSAGE QUEUE
.*
.*                       &OPCODE = FUNCTION  (GET, PUT, OR CLOSE)
.*
.*                       &TERM   = RX LABEL OF 4 BYTE TERMINAL ID
.*
.*                       &MESSAGE= RX LABEL OF MESSAGE AREA
.*                                 (INCLUDES 2 BYTE LENGTH PREFIX)
.*
.*                       &EOF    = RX LABEL OF EOF ROUTE FOR GET ONLY
.*
.*             NOTES.
.*               1.  AN OPEN IS PERFORMED UPON FIRST GET OR PUT.
.*               2.  AN EXPLICIT CLOSE IS NOT REQUIRED.
.*
.*
         AIF   ('&OPCODE' EQ 'CLOSE').CLSCODE
         AIF   (T'&TERM   EQ 'O'    ).BADTERM
         AIF   (T'&MESSAGE EQ 'O'   ).BADMSG
         AIF   ('&OPCODE'  EQ 'GET' ).GETCODE
         AIF   ('&OPCODE'  EQ 'PUT' ).PUTCODE
.BADOP   MNOTE 12,'INVALID QTAM OPERATION CODE'
         MEXIT
.GETCODE ANOP
         AIF   (T'&EOF EQ 'O').PUTCODE
&NAME    LA    0,=A(&TERM,&MESSAGE,&EOF)
         AGO   .FINISH
.PUTCODE ANOP
&NAME    LA    0,=A(&TERM,&MESSAGE,1)
         AGO   .FINISH
.CLSCODE ANOP
&NAME    DS    0H
.FINISH  ANOP
&OP      SETC  '&OPCODE'(1,1)
         LA    1,=CL1'&OP'         LOAD OPERATION CODE
         L     15,=V(QTAMFIP)      LOAD QTAMFIP PGM ADDRESS
         BALR  14,15               CALL QTAMFIP
         MEXIT
.BADTERM MNOTE 12,'INVALID QTAM TERMINAL OPERAND'
         MEXIT
.BADMSG  MNOTE 12,'INVALID QTAM MESSAGE OPERAND'
         MEXIT
         MEND
