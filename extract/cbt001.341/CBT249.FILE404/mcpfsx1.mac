         MACRO
&NAME    MCPFSE1
         LCLA    &X
&X       SETA    &SYSNDX
***********************************************************************
*        INSERT THIS MACRO BETWEEN CARRIAGE AND SIMATTN MACROS        *
***********************************************************************
RD&X     EQU X'08'                     REDISPLAY FLAG BIT.
BC&X     EQU X'04'                     BROADCAST FLAG BIT.
FS&X     EQU X'02'                     FULLSCREEN FLAG BIT.
*
&NAME    L     R2,IEDADBUF             ADDR OF THE BUFFER.
         TM    20(R2),X'10'            TS MESSAGE?
         BNO   FSEZ&X                  NO, EXIT.
         TM    20(R2),X'01'            HEADER BUFFER?
         BO    FSEZ&X                  NO, EXIT.
         LH    R9,18(R2)               DATA LENGTH
         CH    R9,=H'30'               IS DATA PRESENT IN BUFFER?
         BNH   FSEZ&X                  NO, EXIT.
         L     R3,12(R2)               ADDR OF LCB.
         LH    R1,90(R3)               INDEX TO TERM NAME TABLE ENTRY.
         LTR   R1,R1                   IS TTCIN ZERO?
         BZ    FSEZ&X                  YES, EXIT.
         L     R15,IEDRNMPT            TERM NAME TABLE ROUTINE.
         BALR  R14,R15                 EXECUTE IT.
         L     R2,IEDADBUF             ADDR OF THE BUFFER.
         L     R3,12(R2)               ADDR OF LCB.
         LR    R4,R1                   ADDR OF TERM NAME TABLE ENTRY.
         L     R5,0(R4)                ADDR OF QCB.
         LH    R6,30(R5)               TJID/ASID
         LA    R6,TJIDMTRX(R6)         ADDR OF OPTION BYTE.
         LH    R9,18(R2)               DATA LENGTH
         SH    R9,=H'30'               MINUS 30 BYTES OF HEADER.
         SLR   R1,R1
         IC    R1,28(R3)               NUMBER OF RESERVE (PAD) BYTES.
         LA    R7,42(R1,R2)            POINT TO START OF DATA.
         CLI   0(R7),X'02'             STX (X'02') CHARACTER?
         BE    FSE3&X                  YES, SKIP STX, CU, AND DVC.
         CH    R9,=H'4'                MINIMUM POSSIBLE INPUT?
         BNH   FSE0&X                  YES, MUST BE AID,CSR,CSR,(NL)
         CLI   3(R7),X'11'             SBA (X'11') CHARACTER?
         BE    FSE0&X                  YES, AID MUST BE 1ST CHAR.
         B     FSE2&X                  NO, SKIP CU, AND DVC CHARS.
FSE3&X   LA    R7,1(R7)                SKIP 3 BYTES (STX,ESC,CMD).
FSE2&X   LA    R7,1(R7)                SKIP 2 BYTES (ESC,CMD).
FSE1&X   LA    R7,1(R7)                SKIP ONE BYTE (NOT REQUIRED).
FSE0&X   DS    0H
         CLI   0(R7),X'6C'             AID = PA1 ?
         BNE   FSEC&X                  NO, CHECK PREV OUTPUT.
         NI    0(R6),X'FF'-(FS&X+BC&X+RD&X)  TURN OFF FLAGS.
         B     FSEZ&X                  EXIT.
FSEC&X   DS    0H
         TM    0(R6),BC&X              BROADCAST AFTER FULLSCR?
         BZ    FSEE&X                  NO, CHECK FOR PREV FULLSCR.
         NI    0(R6),X'FF'-BC&X        TURN OFF BROADCAST FLAG.
         NI    23(R5),X'F7'            TURN OFF QCB SIM ATTN.
         XI    0(R6),RD&X              FLIP FULLSCR INTERCEPT FLAG.
         MVI   0(R7),X'6B'             ENSURE AID = PA3(SIM. RESHOW).
         B     FSEF&X                  GO RESET LINE/CARRAGE COUNTS.
FSEE&X   DS    0H
         TM    0(R6),FS&X              PREVIOUS OUTPUT FULL SCREEN?
         BZ    FSEZ&X                  NO, EXIT
FSEF&X   DS    0H
         MVI   21(R5),0                RESET LINE COUNT IN QCB.
         MVI   29(R5),0                RESET CARRIAGE COUNT IN QCB.
         B     FSEZ&X
*
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
*
TJIDMTRX DC    XL150'00'               OPTION BYTES FOR EACH TERMINAL
*                                      THIS TABLE MUST BE AT LEAST
*                                      MAX USERS + 1 FOR MVT AND SVS
*                                      AND SYSTEM USERS + 1 FOR MVS
FSEZ&X   DS    0H                      EXIT.
         MEND
