         MACRO
&NAME    QSAM  &OPCODE,&DDNAME,&RECORD,&EOF
         LCLC  &OP
.*
.*             AUTHOR.   DON HIGGINS
.*             DATE.     11/03/70
.*             REMARKS.
.*
.*                  THIS MACRO GENERATES IN-LINE CODE TO CALL QSAMFIP
.*                  SUBROUTINE WHICH WILL HANDLE ALL GET, PUT, AND
.*                  CLOSE COMMANDS FOR ANY QSAM DATA SET.
.*
.*                  &OPCODE = FUNCTION (GET, PUT, OR CLOSE)
.*
.*                  &DDNAME = DD CARD NAME DEFINING QSAM DATA SET
.*
.*                  &RECORD = RX LABEL OF RECORD FOR GET OR PUT
.*
.*                  &EOF    = RX LABEL OF EOF ROUTINE FOR GET ONLY
.*
.*             NOTES
.*               1. AN OPEN IS AUTOMATICALLY EXECUTED UPON FIRST
.*                  GET OR PUT REQUEST.
.*               2. AN EXPLICIT CLOSE IS NOT REQUIRED SINCE OS
.*                  WILL CLOSE FILES AT END OF JOB.
         AIF   (T'&DDNAME EQ 'O').BADDD
         AIF   ('&OPCODE' EQ 'CLOSE').CLSCODE
         AIF   (T'&RECORD EQ 'O').BADREC
         AIF   ('&OPCODE' EQ 'GET').GETCODE
         AIF   ('&OPCODE' EQ 'PUT').PUTCODE
.BADOP   MNOTE 12,'INVALID QSAM OPERATION CODE'
         MEXIT
.GETCODE ANOP
         AIF   (T'&EOF EQ 'O').BADEOF
&NAME    LA    0,=A(&RECORD,&EOF)  LOAD RECORD AND EOF ADDR LIST ADDR
         AGO   .FINISH
.PUTCODE ANOP
&NAME    LA    0,=A(&RECORD)       LOAD RECORD ADDR LIST ADDR
         AGO   .FINISH
.CLSCODE ANOP
&NAME    DS    0H
.FINISH  ANOP
&OP      SETC  '&OPCODE'(1,1)
         LA    1,=CL9'&OP&DDNAME'   LOAD OP CODE AND DDNAME
         L     15,=V(QSAMFIP)      LOAD QSAM FILE INTERFACE PGM ADDR
         BALR  14,15               CALL QSAMFIP
         MEXIT
.BADDD   MNOTE 12,'INVALID QSAM DD CARD NAME'
         MEXIT
.BADREC  MNOTE 12,'INVALID QSAM RECORD ADDRESS'
         MEXIT
.BADEOF  MNOTE 12,'INVALID QSAM EOF ADDRESS'
         MEXIT
         MEND
