         MACRO
&NAME    MCPFSE2
         LCLA   &X
&X       SETA   &SYSNDX
***********************************************************************
*        INSERT THIS MACRO BETWEEN THE OUTBUF AND CODE MACROS         *
***********************************************************************
LK&X     EQU   X'20'                   KEEP KEYBOARD LOCKED BIT.
RD&X     EQU   X'08'                   REDISPLAY FLAG BIT.
BC&X     EQU   X'04'                   BROADCAST FLAG BIT.
FS&X     EQU   X'02'                   FULLSCREEN FLAG BIT.
*
         L     R2,IEDADBUF             ADDR OF THE BUFFER.
         L     R3,12(R2)               ADDR OF LCB.
         L     R4,92(R3)               ADDR OF SCB
         L     R5,0(R4)                ADDR OF QCB.
         LH    R6,30(R5)               LOAD TJID/ASID
         LA    R6,TJIDMTRX(R6)         LOAD OPTION BYTE ADDRESS
         LH    R1,90(R3)               INDEX TO TERM NAME TABLE ENTRY.
         LTR   R1,R1                   IS TTCIN ZERO?
         BZ    FSEZ&X                  YES, EXIT FROM MACRO.
         TM    20(R2),X'01'            HEADER BUFFER?
         BO    FSEX&X                  NO, CHECK FOR TEXT BUFFER SCAN
         NI    0(R6),X'FF'-LK&X        TURN OFF KEYBOARD LOCK FLAG BIT
         TM    20(R2),X'10'            TS MESSAGE?
         BNO   FSEJ&X                  NO, NOT TIME SHARING MSG.
         LH    R9,18(R2)               DATA LENGTH.
         SH    R9,=H'30'               MINUS 30 BYTE PREFIX
         BNH   FSEZ&X                  EXIT IF NO DATA
         SLR   R1,1                    CLEAR REGISTER
         IC    R1,28(R3)               NUMBER OF RESERVE (PAD) BYTES.
         LA    R7,42(R1,R2)            POINT TO START OF DATA.
         CLI   0(R7),X'02'             STX (X'02') CHARACTER?
         BE    FSE3&X                  YES, SKIP STX, ESC, CMD.
         CLI   0(R7),X'27'             ESC (X'27') CHARACTER?
         BE    FSE2&X                  YES, SKIP ESC,CMD.
         B     FSE0&X                  NO, DONT SKIP ANY BYTES.
FSE3&X   LA    R7,1(R7)                SKIP 3 BYTES (STX,ESC,CMD).
         BCTR  R9,0                    DECREMENT DATA LENGTH
FSE2&X   LA    R7,1(R7)                SKIP 2 BYTES (ESC,CMD).
         BCTR  R9,0                    DECREMENT DATA LENGTH
FSE1&X   LA    R7,1(R7)                SKIP ONE BYTE (NOT REQUIRED).
         BCTR  R9,0                    DECREMENT DATA LENGTH
FSE0&X   DS    0H
         SH    R9,=H'6'                ADJUST LENGTH FOR WCC, SBA'S.
         LR    R8,R7                   ADDR OF WCC.
FSED&X   DS    0H
         LA    R8,1(R8)                STEP TO NEXT CHARACTER.
         CLI   0(R8),X'32'             IDLE (X'32') CHARACTER?
         BNE   FSEF&X                  NO, MUST BE FIRST SBA.
         BCT   R9,FSED&X               YES, LOOP TO SKIP IDLE CHARS.
         B     FSEJ&X                  NOT LONG ENOUGH FOR FULLSCR.
FSEF&X   DS    0H                      STEP TO NEXT CHARACTER.
         CLC   0(4,R8),=X'115D7F11'    FSE 5.0/SPF/IPCS/ETC HEADER?
         BE    FSEG&X
         CLC   0(4,R8),=X'11C14F3C'    FSE 4.0 HEADER?
         BE    FSEK&X
         CLC   0(4,R8),=X'1140403C'    FSE 4.0 HEADER?
         BNE   FSEH&X                  NO, SEE IF LAST FULLSCR.
         B     FSEK&X                  NO, SEE IF LAST FULLSCR.
FSEG&X   DS    0H
         OI    0(R6),FS&X              TURN ON FULL SCREEN FLAG.
FSEK&X   MVI   21(R5),0                RESET LINE COUNT IN QCB.
         MVI   29(R5),0                RESET CARRIAGE COUNT IN QCB.
         TM    0(R6),RD&X+BC&X         REDISPLAY OR BROADCAST?
         BZ    FSEZ&X                  NO, EXIT FROM MACRO
         XI    0(R6),RD&X              FLIP REDISPLAY FLAG.
         OI    0(R6),LK&X              TURN ON KEYBOARD LOCK BIT.
         TM    0(R7),X'02'             IS WCC INHIBIT KEYBOARD BIT ON
         BZ    FSEZ&X                  NO, EXIT FROM MACRO
         NI    22(R5),X'EF'            TURN OFF QCBSSSMI BIT
         NI    0(R7),X'FD'             SET WCC - INHIBIT KEYBOARD.
         CLI   0(R7),X'C0'             ARE ALL WCC CONTROL BITS OFF.
         BNE   FSEZ&X                  NO, EXIT FROM MACRO
         MVI   0(R7),X'40'             YES, CHANGE X'C0' TO X'40'.
         B     FSEZ&X                  EXIT FROM MACRO
FSEH&X   DS    0H
         CLC   0(4,R8),=X'115D7E11'    LAST FULLSCR'N MSG.?
         BNE   FSEJ&X                  NO, SEE IF BROADCAST.
         NI    0(R6),X'FF'-(FS&X+BC&X+RD&X) TURN OFF FLAGS.
         MVI   29(R5),0                RESET CARRIAGE COUNT IN QCB.
         LH    R1,4(R8)                COMPUTE VALUE TO WHICH LINE
         SLDL  R0,24                   COUNT IS TO BE RESET AS
         SLL   R1,2                    REQUESTED IN SECOND SBA
         SRDL  R0,6                    ADDRESS OF FSE FULL SCREEN
         SRL   R1,20                   MESSAGE.
         SLR   R0,0
         LA    R15,80
         DR    R0,R15
         STC   R1,21(R5)               RESET LINE COUNT IN QCB.
         B     FSEZ&X                  EXIT.
FSEJ&X   DS    0H
         TM    0(R6),FS&X              PREVIOUS MSG. FULL SCREEN?
         BNO   FSEZ&X                  NO, EXIT.
         NI    0(R6),X'FF'-FS&X        TURN OFF FULL SCREEN FLAG.
         OI    0(R6),BC&X              TURN ON BROADCAST FLAG.
         MVI   21(R5),23               SET LINE COUNT IN QCB.
         MVI   29(R5),0                SET CARRIAGE COUNT IN QCB.
         OI    23(R5),X'08'            TURN ON SIM ATTN READ REQUEST.
         OI    0(R7),X'C4'             SET 'SOUND ALARM' IN WCC.
         B     FSEZ&X
FSEX&X   DS    0H
         TM    0(R6),LK&X              IS KEYBOARD TO BE KEEP LOCKED
         BZ    FSEZ&X                  NO, EXIT FROM MACRO
         TM    20(R2),X'02'            IS THIS THE LAST BUFFER
         BO    FSEZ&X                  NO, EXIT FROM MACRO
         CLC   35(3,R2),=X'27F1C2'     UNLOCK SEQUENCE FOR NCP?
         BNE   FSEY&X NO,              CHECK FOR EP SEQUENCE
         NI    22(R5),X'EF'            TURN OFF QCBSSSMI BIT
         MVI   37(R2),X'40'            SET WCC TO LEAVE LOCKED
         B     FSEZ&X                  EXIT FROM MACRO
FSEY&X   DS    0H
         CLC   35(4,R2),=X'0227F1C2'   UNLOCK SEQUENCE FOR EP?
         BNE   FSEZ&X                  NO, EXIT FROM MACRO
         NI    22(R5),X'EF'            TURN OFF QCBSSSMI BIT
         MVI   38(R2),X'40'            YES, SET WCC TO LEAVE LOCKED
FSEZ&X   DS    0H
         MEND
