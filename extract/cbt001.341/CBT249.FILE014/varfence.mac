++USERMOD(MV80004)  /* VARIABLE STORAGE FENCE ZAP

PROBLEM DESCRIPTION:
    ON-LINE SYSTEMS SUCH AS IMS OR MAIS HAVE LARGE WORKING SETS.  WHEN
THE SRM (SYSTEM RESOURCES MANAGER) NEEDS TO REPLENISH THE AVAILABLE
FRAME QUEUE (STORAGE AVAILABLE FOR SATISFYING PAGE REQUESTS) IT IS
LIKELY TO STEAL MANY PAGES FROM THE ON-LINE SYSTEM.  FURTHERMORE, THE
ON-LINE WILL REFERENCE SOME OF ITS PAGES LESS FREQUENTLY THAN A TRUE
BATCH JOB WOULD, MAKING IT LIKELY THAT THESE PAGES WILL BE STOLEN.
WHEN A TRANSACTION REQUIRES THE USE OF THESE PAGES, SEVERAL PAGE FAULTS
MAY OCCUR BEFORE THE TRANSACTION CAN BE SERVICED.  THIS INCREASES THE
SYSTEM OVERHEAD AND IMPACTS IMS OR MAIS RESPONSE TIME.
    THE FIXED STORAGE FENCE (MOD MV79043) SETS A FIXED LOWER LIMIT
TO THE NUMBER OF PAGES THE IMS CONTROL REGION CAN HAVE BEFORE THE SRM
WILL STEAL PAGES.  THE ACTUAL NUMBER OF PAGES AN ON-LINE SYSTEM NEEDS
TO ASSURE GOOD RESPONSE VARIES WITH THE WORKLOAD.  HENCE THERE WILL BE
TIMES WHEN IMS IS STILL SHORT OF MEMORY AND ENCOUNTERS PERFORMANCE
PROBLEMS DUE TO EXCESSIVE PAGING.  AT OTHER TIMES IMS DOES NOT NEED THE
PAGES, YET THEY ARE MADE UNAVAILABLE TO OTHER ADDRESS SPACES. THIS
DEGRADES TOTAL SYSTEM PERFORMANCE.  FURTHERMORE, THE FIXED FENCE MOD
SUPPORTS ONLY IMS;  THERE IS CURRENTLY A NEED TO PROVIDE A STORAGE
FENCE FOR MAIS AS WELL.
    A FURTHER PROBLEM IS THAT PAGES BELONGING TO THE FAVORED ADDRESS
SPACE MAY HAVE A LONGER UNREFERENCED LIFE THAN PAGES ELSEWHERE IN THE
SYSTEM.  THIS DISTORTS THE VALUE OF THE SYSTEM-WIDE UNREFERENCED
INTERVAL COUNT,  WHICH IS USED BY THE SRM IN SWAPPING DECISIONS.
TO CORRECT THIS, THE FAVORED ADDRESS SPACE SHOULD BE OMITTED FROM THE
CALCULATION OF THE GLOBAL UIC.  HOWEVER, DOING THIS CAUSES THE SRM'S
STEAL CRITERION TO START AT THE LOWERED GLOBAL UIC VALUE.  PAGES OLDER
THAN THIS ARE STOLEN FROM THE FAVORED ADDRESS SPACE WITHOUT REGARD TO
THEIR RELATIVE AGE. THIS LOSES THE SOME OF THE BENEFITS OF THE LRU
ALGORITHM - IMPORTANT PAGES MAY BE STOLEN, INCREASING PAGING FOR THE
ON-LINE SYSTEM.

FIX DESCRIPTION:
    INSTEAD OF RESERVING A FIXED NUMBER OF PAGE FRAMES FOR THE IMS
CONTROL REGION,  TRY TO GUARANTEE IT A MINIMUM UNREFERENCED INTERVAL
COUNT (UIC).  THE NUMBER OF PAGES BELONGING TO THE ON-LINE SYSTEM WILL
FLUCTUATE WILDLY, BUT RESPONSE SHOULD BE MORE CONSISTENT.  THE AVERAGE
NUMBER OF PAGES AVAILABLE FOR OTHER WORK SHOULD BE GREATER THAN IF A
FIXED FENCE WERE SET HIGH ENOUGH TO PROVIDE COMPARABLE RESPONSE FOR THE
ON-LINE SYSTEM.  TO AVOID LETTING THE FAVORED ADDRESS SPACE HOLD AN
EXCESSIVE NUMBER OF PAGES, A MAXIMUM NUMBER OF FENCED PAGES IS SET.
    THE ORIGINAL VERSION OF THIS MODIFICATION WAS SUGGESTED AND USED BY
JACK MACLEAN OF THE HOBART CORPORATION.  HIS MOD USES THE OUXBUIC FIELD
IN THE OUXB TO TURN STEALING ON AND OFF FOR THE FAVORED ADDRESS SPACE.
HE NOTED THAT THE UIC FOR AN INDIVIDUAL ADDRESS SPACE, CONTAINED IN THE
OUXB, IS UPDATED INFREQUENTLY.  ONCE IT REACHES A HIGH ENOUGH VALUE TO
PERMIT STEALING, MANY PAGES MAY BE STOLEN BEFORE THE UIC IS RECOMPUTED
AND INHIBITS FURTHER STEALING. FOR THIS REASON THE TARGET UIC MUST BE
SET HIGHER THAN IS ACTUALLY NEEDED, SO THAT IT MAY REASONABLY BE HOPED
THAT BY THE TIME ENOUGH PAGES ARE STOLEN TO THREATEN RESPONSE, THE UIC
WILL BE RECOMPUTED TO INHIBIT STEALING.
    THIS VERSION OF THE MODIFICATION ATTEMPTS TO AVOID THE PROBLEM
DESCRIBED ABOVE BY COMPARING THE CURRENT PAGE STEAL CRITERION, FOUND IN
REGISTER 'MSX' (REGISTER 10) WITH THE TARGET UIC.  ASSUMING THAT THE
FAVORED ADDRESS SPACE HAS BEEN OMITTED FROM THE GLOBAL UIC, THIS
INTRODUCES A PROBLEM BECAUSE THE GLOBAL UIC MAY BE LOWER THAN THE
TARGET FOR THE FAVORED ADDRESS SPACE, IN WHICH CASE REGISTER 10 WILL
START BELOW THE TARGET AND NO PAGES WILL EVER GET STOLEN FROM THE
FAVORED ADDRESS SPACE.  FOR THIS REASON AN INITIAL PASS OVER ALL
ADDRESS SPACES IS PERFORMED TO INITIALIZE REGISTER 10 TO THE HIGHEST
UIC VALUE FOUND ANYWHERE IN THE SYSTEM.
    THIS IMPLEMENTATION OF THE VARIABLE STORAGE FENCE DEFINES A TABLE
GIVING A TARGET UIC VALUE AND MAXIMUM FENCE VALUE FOR EACH FAVORED
PERFORMANCE GROUP.  THIS PERMITS FENCES TO BE DEFINED FOR BOTH IMS AND
MAIS.  AS MACLEAN POINTS OUT, FENCING MORE THAN ONE ADDRESS SPACE IN A
SINGLE MACHINE WITH LIMITED STORAGE COULD CAUSE SERIOUS THRASHING OR
SYSTEM FAILURE.  IN OUR INSTALLATION THE MULTIPLE FENCE WILL PROBABLY
WORK PROPERLY ONLY ON THE 10M 3033, WHEN MAIS IS BACKED UP THERE.

MODULES/CSECTS MODIFIED:
    IEANUC01/IRARMSTM

SOURCE:
    IN W099.SMPPTFIN.DATA (CATALOGUED) AS MEMBER MV80004.

FIX APPLIED:
    TO THE BASE 3.8 SYSTEM
    WITH PTF UZ27173 FROM TAPE 7910

NOTES:
    THIS MODIFICATION SUPERSEDES MV79043, WHICH SHOULD BE BACKED OFF
BEFORE THIS MOD IS APPLIED.
    MODIFICATION MV79072 HITS THE SAME MODULE BUT IS NOT IN USE.
    THE FENCE FOR IMS IS DEFINED WITH A RIDICULOUSLY HIGH UIC TARGET.
THIS MEANS PAGES CANNOT BE STOLEN FROM IMS UNTIL ITS MAXIMUM NUMBER
OF PAGES IS REACHED.  THE RESULT IS THAT, IN THE CASE OF IMS, THIS
MOD WILL PRODUCE THE SAME EFFECT AS THE FIXED FENCE.
    IT IS INTENDED THAT THE CONSTANTS IN THE TABLE SHOULD BE
CORE-ZAPPED UNTIL DESIRABLE RESULTS ARE OBTAINED.
    THE END OF THE TABLE IS MARKED BY A HALF WORD OF ZEROES.  TO ADD
ADDITIONAL ENTRIES TO THE TABLE, EXPAND THE MODULE BY A FURTHER FOUR
BYTES PER ENTRY.  ZAP IN THE ADDITIONAL ENTRIES BEGINNING WITH THE
CURRENT END MARKER.
                                                                  */ .
++VER(Z038) FMID(EBB1102)  /* SCP BASE PROGRAM SCICX */
             PRE(UZ27173)  /* TAPE 7910 */
             SUP(MV79043)  /* GDS FIXED FENCE MOD */ .
++ZAP(IRARMSTM).
 EXPAND IRARMSTM(192)      /* OVER SIZE GENERATED BY IBM */
 NAME IEANUC01 IRARMSTM
*
* PART 1 - EXCLUDE FENCED JOB FROM SYSTEM-WIDE HIGH UIC CALCULATION.
*
 VER 00EC 58605094            L   OUXBPTR,ASCBOUXB(,OUXBPTR)
 REP 00EC 47F0CA46            B   PATCH1
*
*                      PATCH CODE CHECKS FOR FAVORED PERFORMANCE GROUP.
*                      IF SO, JUST ADVANCE TO NEXT ADDRESS SPACE.
*
 VER 0A48 000000000000        DC  24D'0'        PATCH SPACE FROM EXPAND
 REP 0A48 1BEE         PATCH1 SR  @14,@14       ZERO TABLE INDEX
 REP 0A4A 1B99                SR  @09,@09       CLEAR REG FOR INSERT
 REP 0A4C 49E0CAFA     TBSRC1 CH  @14,TBLSIZ    ALL TABLE ENTRIES CK'D?
 REP 0A50 47B0CA66            BNL CHKUIC        YES,ORDINARY ADDR SPACE
 REP 0A54 439ECAFC            IC  @09,TBLPGN(@14)  GET FAVORED PERF GRP
 REP 0A58 4490CAF6            EX  @09,CMPPGN     MATCH OUCBNPG
 REP 0A5C 4780C102            BE  @RF00107      BYPASS UIC CHECK IF SO
 REP 0A60 41EE0004            LA  @14,4(@14)    ADVANCE TABLE INDEX
 REP 0A64 47F0CA4A            B   TBSRC1        SEARCH FAVORED PGN'S
 REP 0A68 58605094     CHKUIC L   OUXBPTR,ASCBOUXB(,OUXBPTR)
 REP 0A6C 47F0C0EE            B   $0F0          RETURN TO NEXT INSTR
*
* PART 2 - THE VARIABLE FENCE ZAP
*
*                      INITIALIZE THE PAGE STEALING CRITERION IN
*                      REGISTER MSX (R10) TO THE SYSTEM HIGH UIC VALUE
*                      BY MEANS OF A LOOP THROUGH ALL ADDRESS SPACES.
*
 VER 01A2 48A021D0            LH  MSX,MCVSTCRI  LOAD SRM GLOBAL UIC
 VER 01A6 47F0C28E            B   @DE00133      BRANCH TO LOOP INIT
*
 REP 01A6 47F0CA6E            B   PATCH2        GO TO PATCH AREA
*
 REP 0A70 58402070     PATCH2 L   OUCBPTR,RMCTINQE HEAD OF OUCB IN Q
 REP 0A74 58404004            L   OUCBPTR,OUCBFWD  FIRST REAL OUCB
 REP 0A78 91E44010     LOOP2  TM  OUCBOUT,X'11100100' IF ADDR SPACE NOT
 REP 0A7C 4770CA92            BNZ NEXTAS        IN AND STAYING, IGNORE
 REP 0A80 58504028            L   ASCBPTR,OUCBASCB POINT TO ITS ASCB
 REP 0A84 58605094            L   OUXBPTR,ASCBOUXB AND ALSO TO OUXB
 REP 0A88 49A06086            CH  MSX,OUXBUIC   CHECK FOR A HIGHER UIC
 REP 0A8C 47B0CA92            BNL *+8           OBTAIN MAX OF CURRENT
 REP 0A90 48A06086            LH  MSX,OUXBUIC   MSX OR THIS OUXBUIC
 REP 0A94 58404004     NEXTAS L   OUCBPTR,OUCBFWD    GET ADDR NEXT OUCB
 REP 0A98 D5034000C9E6        CLC OUCBNAME,@CC02187  IS THIS AN OUCB?
 REP 0A9E 4780CA76            BE  LOOP2              CONTINUE IF SO
 REP 0AA2 58700010            L   @07,CVTPTR    USE ADDRESS OF CVT
 REP 0AA6 58707164            L   @07,PVTPTR    TO FIND THE PVT
 REP 0AAA 49A0772C            CH  MSX,PVTCHUIC  COMPARE SYS AREA UIC
 REP 0AAE 47B0CAB4            BNL *+8           SET MSX TO THE HIGHEST
 REP 0AB2 48A0772C            LH  MSX,PVTCHUIC  UIC IN ENTIRE SYSTEM
 REP 0AB6 47F0C1A8            B   @DL00133      BRANCH TO START OF LOOP
*
*                      MAIN PART OF VARIABLE FENCE ZAP
*
 VER 01FA 45E0C910            BAL @14,STEAL
 REP 01FA 47F0CAB8            B   PATCH3
*
 REP 0ABA 1BEE         PATCH3 SR  @14,@14       INIT TAB INDEX FOR SRCH
 REP 0ABC 1B99                SR  @09,@09       CLEAR REG FOR INSERT
 REP 0ACE 49E0CAFA     TBSRC3 CH  @14,TBLSIZ    STILL IN FAVORED PG TAB
 REP 0AC2 47B0CAEE            BNL $STEAL        IF NOT, NORMAL ADDRSPCE
 REP 0AC6 439ECAFC            IC  @09,TBLPGN(@14) LOAD PGN FROM TABLE
 REP 0ACA 4490CAF6            EX  @09,CMPPGN    MATCH IT VS OUCBNPG
 REP 0ACE 4780CAD8            BE  GOTPGN        BR IF FOUND FAVORED PGN
 REP 0AD2 41EE0004            LA  @14,4(@14)    ELSE BUMP TABLE INDEX
 REP 0AD6 47F0CACC            B   TBSRC3        AND CONTINUE SEARCH
 REP 0ADA 480ECAFE     GOTPGN LH  R0,TBLMAX(@14) LOAD MAX PAGES FOR PG
 REP 0ADE 49005098            CH  R0,ASCBFMCT(,ASCBPTR) HAVE THAT MANY?
 REP 0AE2 4720CAEE            BH  $STEAL          OK TO STEAL IF SO
 REP 0AE6 439ECAFD            IC  @09,TBLUIC(@14) LOAD UIC FENCE FOR PG
 REP 0AEA 19A9                CR  MSX,@09       IS STEAL CRIT ABOVE IT?
 REP 0AEC 4740C214            BL  @RF00144      NO, DONT STEAL FROM HIM
 REP 0AF0 45E0C910     $STEAL BAL @14,STEAL     CALL PAGE STEAL ROUTINE
 REP 0AF4 47F0C1FC            B   $1FE          RETURN TO VANILLA CODE
*
 REP 0AF8 95004018     CMPPGN CLI OUCBNPG,0     ** EXECUTED INSTR **
*
*                      TABLE OF FAVORED PERFORMANCE GROUPS
*                      ENTRIES MUST BE PLACED IN DESCENDING ORDER OF
*                      UIC VALUE (MIN UIC OF PAGE THAT CAN BE STOLEN)
*
 REP 0AFC 0008         TBLSIZ DC  H'8'          TABLE CONTAINS 2 ENTRYS
*
*                      TABLE ENTRY FOR IMS CONTROL REGION
*                      THIS IS EQUIVALENT TO A FIXED FENCE OF 320 PAGES
*
 REP 0AFE 20           TBLPGN DC  AL1(32)       PERF GROUP FOR IMS CNTL
 REP 0AFF F8           TBLUIC DC  AL1(248)      LOWEST UIC WE CAN STEAL
 REP 0B00 0140         TBLMAX DC  H'320'        MAX PAGES TO PROTECT
*
*                      TABLE ENTRY FOR MAIS GUARANTEES UNREFERENCED
*                      PAGE LIFE OF 5 FOR UP TO 350 PAGES (1400K)
*
 REP 0B02 36           PGN2   DC  AL1(54)       PERF GROUP FOR MAIS
 REP 0B03 06           UIC2   DC  AL1(6)        LOWEST UIC WE CAN STEAL
 REP 0B04 015E         MAX2   DC  AL2(350)      MAX PAGES TO PROTECT
*
 REP 0B06 0000         TBLEND DC  H'0'          MARK END OF TABLE
*
 IDRDATA MV80004
