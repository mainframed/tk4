*          DATA SET CBT544     AT LEVEL 001 AS OF 06/02/76
EWTR2    CSECT
***      INSTALLATION: ELI LILLY AND COMPANY
***      AUTHOR:       E. SIERP
***      DATE:         MARCH 12, 1976
         SYMGR
WK1      EQU   R3
WK2      EQU   R4
WK3      EQU   R5
WK4      EQU   R6
SSOBREG  EQU   R10
RETREG   EQU   R11
BASEREG  EQU   R12
BASEREG2 EQU   R9
         USING EWTR2,BASEREG,BASEREG2
         SAVE  (14,12)
         LR    BASEREG,R15
         LA    BASEREG2,4095(BASEREG)
         LA    BASEREG2,1(BASEREG2)
         ST    R13,SAVE+4
         LA    WK1,SAVE
         ST    WK1,8(R13)
         LR    R13,WK1
         OPEN  (SYSUT1,,SYSPRINT,(OUTPUT))
         TM    SYSPRINT+48,X'10'     SYSPRINT OPEN?
         BZ    NOPEN1         NO
         TM    SYSUT1+48,X'10'      SYSUT1 OPEN
         BZ    NOPEN2
         BAL   RETREG,GETUT1      GO GET 1ST RECORD
         CLC   0(4,R1),EWTRHDR     IT SYOULD BE EWTR
         BNE   ERR01
MOVHDR   MVC   EWTRHDR+4(17),4(R1)  GET TIME,DATE,JFCB INFO FROM EWTR1
         BAL   RETREG,GETUT1  GO GET SSOB REC
         CLC   0(4,R1),SSOBDS      SHOULD BE SSOB
         BNE   ERR02
         MVC   SSOBDS+4(129),4(R1) SAVE SSOB
         LA    SSOBREG,SSOBDS
         USING SSOB,SSOBREG
ALLOC    EQU   *
         MVI   ALLOCSW,X'00'  INIT CHANGE SWITCH
         CLC   SSSOCLAS,TUSOUTCL     SAME SYSOUT CLASS
         BE    CKFORM         YES, DONT MOVE IT
         MVC   TUSOUTCL(1),SSSOCLAS     SYSOUT CLASS TO TEXT UNIT
         OI    ALLOCSW,TUCHGCLS       INDICATE CLASS CHANGE
CKFORM   EQU   *
         CLC   SSSOFORM,=C'STD.'         STANDARD FORM
         BNE   NEWFORM        NO
         CLC   TUPFORM,=F'0'
         BE    CKCOPY
         XC    TUPFORM,TUPFORM
         B     FMCHSW
NEWFORM  EQU   *
         CLC   SSSOFORM,TUFORMNO        SAME FORM
         BNE   MVFORM         NO, GO MOVE NEW FORM
         CLC   TUPFORM,=F'0'    LAST ALLOC USE FORM NUM
         BNE   CKCOPY         YES, DONT NEED NEW ONE
MVFORM   MVC   TUFORMNO(4),SSSOFORM     PUT FORM NUM IN DYNALLOC TU
         LA    WK1,TUFORM     ADDR FORMNO TU
         ST    WK1,TUPFORM    ST IN TEXT POINTER LIST
FMCHSW   OI    ALLOCSW,TUCHGFRM       INDICATE FORM CHANGE
CKCOPY   CLC   SSSOCOPY+1(1),TUCPYS    SAME NUMBER COPIES
         BE    CKFCB          YES, DONT MOVE IT
         MVC   TUCPYS(1),SSSOCOPY+1     NUM OF COPIES IN DYNALLOC TU
         OI    ALLOCSW,TUCHGCPY       INDICATE COPIES CHANGE
CKFCB    EQU   *
         CLC  EHDRFCB(4),=C'****'    STD FCB?
         BNE  NEWFCB
         CLC  TUPFCB,=F'0'  USE FCB LAST ALLOC
         BE  CKDEST  NO
         XC  TUPFCB,TUPFCB      INDICATE DEFAULT FCB
         B  FCBCHSW
NEWFCB   EQU  *
         CLC   EHDRFCB(4),TUFCNAM      SAME FCB
         BNE   MVFCB          NO, GO MOVE NEW FCB
        CLC   TUPFCB,=F'0'    LAST ALLOC USE FCB
        BNE   CKDEST
MVFCB    MVC   TUFCNAM(4),EHDRFCB       FCB IN DYNALLOC TEXT UNIT
FCBCHSW  OI    ALLOCSW,TUCHGFCB      INDICATE FCB CHANGE
CKDEST   EQU   *
         CLC   SSSODEST,=CL8'LOCAL'     LOCAL DEST
         BNE   NEWDEST
         CLC   TUPROUTE(3),=F'0'
         BE    CKJOB
         XC    TUPROUTE(3),TUPROUTE
         B     DSTCHSW
NEWDEST  EQU   *
         CLC   SSSODEST(7),TUROUTNM    SAME DESTINATION
         BNE   MVDEST
         CLC   TUPROUTE,=F'0'     LAST ALLOC USE DEST
         BNE   CKJOB          YES, DONT DO ANOTHER
MVDEST   LA    WK1,SSSODEST        DESTINATION
         LA    WK2,8               MAX LNGTH+1 OF DEST NAME
CLIBLNK  CLI   0(WK1),C' '
         BE    DESTLNG
         LA    WK1,1(WK1)
         BCT   WK2,CLIBLNK
         B     BADEST
DESTLNG  LA    WK3,8
         SR    WK3,WK2             LENGTH OF DEST NAME
         STH   WK3,TUROUTLN        LENGTH IN TEXT UNIT
         MVC   TUROUTNM(7),SSSODEST  MV DEST TO TU
         MVC   TUPROUTE,=AL3(TUROUTE)
DSTCHSW  OI    ALLOCSW,TUCHGDST    INDICATE DEST CHANGE
CKJOB    CLC   SSSOJOBI,JCTJOBID      SAME JOB ID
         BE    CKANYCHG         YES, DONT FLAG IT
         OI    ALLOCSW,CHGJOBID     INDICATE NEW JOBID
CKANYCHG TM    ALLOCSW,X'FF'      DID ANYTHING CHANGE
         BZ    GET1          NO, SKIP DYNALLOC AND OPEN
         BAL   RETREG,CLOSUT2 GO CLOSE SYSUT2
         LA    R1,S99RBPTR
         DYNALLOC             ALLOCATE SYSOUT DATA SET
         LTR   R15,R15        SUCCESSFUL ALLOCATION
         BZ    ALLOCOK        YES
         DC    H'0'
ALLOCOK  EQU   *
         OPEN  (SYSUT2,(OUTPUT))
         MVC   JCTJNAME,SSSOJOBN   SAVE JOBNAME
         MVC   JCTJOBID,SSSOJOBI   SAVE JOBID
         MVC   PWKJOE,SSSOCLAS     SAVE SYSOUT CLASS
         MVC   $SID,SSSODSN        1ST 4 CHAR OF DSN = SID
         MVC   DEST,SSSODEST
         BAL   RETREG,SEPARATE
GET1     BAL   RETREG,GETUT1       GET SYSOUT REC
         CLC   0(4,R1),EWTRHDR     NEW DS?
         BE    MOVHDR
         TM    SYSUT1+36,X'06'      DO WE HAVE CC
         BM    CKMACHCC            YES, FIND OUT WHAT KIND
         TM    EHDRRECF,X'06'  ORIG DS HAVE CC?
         BM    CKEHDRMC        YES, FIND OUT WHAT KIND
         LH    WK1,SYSUT1+82       LRECL OF REC READ
         BCTR  WK1,R0              DECREMENT FOR MOVE
         EX    WK1,MV1             MOVE RECORD
         LA    WK1,2(WK1)     REC LNG +1
         STH   WK1,SYSUT2+82
         MVI   PRTLINE,X'09'  SPACE 1 LINE AFTER PRINT
         LA    R1,PRTLINE
         B     GOPUT1
MV1      MVC   BUFSTART(0),0(R1)
CKEHDRMC EQU   *
         TM    EHDRRECF,X'02'  MACH CC
         BO    MACHCC          YES
         B     ASA             MUST BE ASA
CKMACHCC EQU   *
         TM    SYSUT1+36,X'02'      MACH CC?
         BZ    ASA            NO, MUST BE ASA
MACHCC   EQU   *               PROCESS MACH CC
         LA    WK1,MCHCCTBL        ADDR MACH CC VERIFIACTION TBL
         LA    WK2,L'MCHCCTBL NUMBER OF TBL ENTRIES
MCHLOOP  EQU   *
         CLC   0(1,R1),0(WK1)      CC MATCH TBL ENTRY
         BE    GOPUT               YES, ITS VALID
         LA    WK1,1(WK1)          NEXT TBL ENTRY
         BCT   WK2,MCHLOOP
         MVI   0(R1),X'09'         ITS INVALID, SPACE 1 AFTER WRITE
         B     GOPUT
ASA      LA    WK1,PASAMCH         ASA TO MACH CC CONVERSION TBL
         LA    WK2,L'PASAMCH/2-1   SET NUMBER OF ENTRIES MINUS 1
ASALOOP  EQU   *
         CLC   0(1,R1),0(WK1)      DOES CC MATCH ASA ENTRY
         BE    MVMCH               YES
         LA    WK1,2(WK1)          NEXT TBL ENTRY
         BCT   WK2,ASALOOP
MVMCH    MVC   PRTLINE(1),1(WK1)  GET IMMEDIATE MACH CC IN WK AREA
         MVI   0(R1),X'01'         SET WRITE CC
         MVC   SYSUT2+82(2),=H'2'     MAKE OP REC LNG EQ 2
         LA    WK1,PRTLINE
         LR    WK2,R1              SAVE R1
         BAL   RETREG,PUTUT2       GO WRITE IMMED COMMAND
         LR    R1,WK2              RESTORE R1
GOPUT    EQU   *
         MVC   SYSUT2+82(2),SYSUT1+82      LENGTH OF RECORD
GOPUT1   LR    WK1,R1
         BAL   RETREG,PUTUT2       GO PUT SYSOUT RECORD
         B     GET1
GETUT1   EQU   *
         GET   SYSUT1
         BR    RETREG
PUTUT2   EQU   *
         PUT   SYSUT2,(WK1)
         BR    RETREG
PUTPRT   EQU   *
         PUT   SYSPRINT,PRTLIN
         BR    RETREG
ERRET    MVI   RETCODE+3,X'10'     SET ERROR RETURN CODE
CLOSEM   TM    SYSUT1+48,X'10'     SYSUT1 OPEN?
         BZ    CLSUT2
         CLOSE (SYSUT1)
CLSUT2   BAL   RETREG,CLOSUT2
         TM    SYSPRINT+48,X'10'   SYSPRINT OPEN
         BZ    RET
         CLOSE (SYSPRINT)
RET      L     R15,RETCODE
         L     R13,SAVE+4
         RETURN (14,12),RC=(15)
NOPEN1   EQU   *
         WTO   'UNABLE TO OPEN SYSPRINT',ROUTCDE=11
         B     ERRET
CLOSUT2  TM    SYSUT2+48,X'10'     SYSUT2 OPEN
         BZR   RETREG              NO
         ST    RETREG,SRETREG
         BAL   RETREG,SEPARATE
         CLOSE (SYSUT2)
         L     RETREG,SRETREG
         BR    RETREG
NOPEN2   EQU   *
         MVC   PRTMSG(30),OPNMSG01
         BAL   RETREG,PUTPRT
         B     ERRET
ERR01    EQU   *
         LR    WK1,R1              SAVE REC PTR
         MVC   PRTMSG(46),ERRMSG01
ERR01A   BAL   RETREG,PUTPRT
         MVI   PRTCC,C'0'          DOUBLE SPACE
         MVC   PRTMSG(132),0(WK1)  1ST 132 CHAR OF BAD REC
         BAL   RETREG,PUTPRT
         B     ERRET
ERR02    EQU   *
         LR    WK1,R1              SAVE BAD REC POINTER
         MVI   PRTCC,C'1'
         MVI   PRTMSG,C' '
         MVC   PRTMSG+1(131),PRTMSG
         MVC   PRTMSG(46),ERRMSG02
         B     ERR01A
SEPARATE NOPR  RETREG
         OI    SEPFLG,SEPOVRFL      INDICATE NEW PAGE
PRINTID  DS    0H
         ST    PL,PCESAVEA         SAVE RETURN REGISTER
*BEGIN   ******
         LA    PL,$TPIDCT          REMOTE SEPARATOR LINE COUNT
         CLC   DEST,=CL8'LOCAL'      TEST FOR LOCAL PRINTER
         BNE   BLKRMT              BRANCH IF NOT LOCAL
*END     ******
*
         LA    PL,$PRIDCT          LOCAL SEPARATOR LINE COUNT
BLKRMT   DS    0H
         CLM   PL,1,=AL1(30)       AT LEAST 30 LINES REQUESTED
         BL    BLKSKIP             BRANCH IF NO
         MVC   PCCWORK,JCTJNAME    JOB NAME FORM JOB CARD
         BAL   PL,BLKPRT           WRITE 12 LINES OF JOBNAME
         MVI   BUFSTART,C' '
         MVC   BUFSTART+1(131),BUFSTART
         MVI   PRTLINE,X'1B'       TRIPLE SPACE IMMED
         MVC   SYSUT2+82(2),=H'1'      LRECL = 1
         LA    WK1,PRTLINE
         BAL   RETREG,PUTUT2
         MVI   PRTLINE,X'0B'       SINGLE SPACE IMMED
         BAL   RETREG,PUTUT2
         XC    PCCWORK,PCCWORK     CLEAR WORK AREA
         MVC   PCCWORK(1),JCTJOBID GET JOB TYPE
         MVC   PCCWORK+1(4),JCTJOBID+4 AND JOB NUMBER
         OC    PCCWORK+1(4),=CL4'0000' BLANKS TO ZEROS
         MVC   PCCWORK+7(1),PWKJOE AND SYSOUT CLASS
         BAL   PL,BLKPRT           WRITE 12 LINES OF JOB#, CLASS
         MVI   PRTLINE,X'1B'       SPACE 3 IMMED
         MVC   SYSUT2+82(2),=H'1'     SYSUT2 LRECL
         LA    WK1,PRTLINE
         BAL   RETREG,PUTUT2
BLKSKIP  DS    0H
         MVI   BUFSTART,C' '       BLANK 1ST PRINT POSITION
         MVC   BUFSTART+1(130),BUFSTART AND ALL OTHERS TO 132
         MVC   BUFSTART(4),=CL4'****'       PP001 FRAME CHARS
         MVC   BUFSTART+128(4),=CL4'****'   PP129 FRAME CHARS
         MVC   BUFSTART+4(1),PWKJOE         PP005 SYSOUT CLASS
         MVC   BUFSTART+127(1),PWKJOE       PP128 SYSOUT CLASS
         NI    SEPFLG,FF-SEPOVRFL        INSURE OVERFLOW BIT NOT ON
         XI    SEPFLG,STARTSEP       START SEPARATOR?
         BZ    ENDSEPR
         MVC   BUFSTART+7(5),=C'START'
         B     MVJID
ENDSEPR  MVC   BUFSTART+7(5),=C' END '
MVJID    EQU   *
         MVC   BUFSTART+14(8),JCTJOBID      PP015 JOB NUMBER
         MVC   BUFSTART+110(8),JCTJOBID     PP111 JOB NUMBER
         MVC   BUFSTART+24(8),JCTJNAME      PP025 JOB NAME
         MVC   BUFSTART+100(3),=C'MVS'      PP101 SYS                QS
         MVC   BUFSTART+104(4),$SID         PP105 SYSTEM ID          QS
         TIME  DEC                 GET TIME AND DATE
         MVC   BUFSTART+67(21),PTIMASK      PP068 TIME DATE
         CL    R0,=X'12000000'     TEST TIME
         BL    PMORNING            BRANCH IF AM
         MVI   BUFSTART+76,C'P'    CHANGE FROM AM TO PM
         SL    R0,=X'12000000'     SUBTRACT TWELVE HOURS
PMORNING ST    R0,PCCWORK          STORE ADJUSTED TIME
         CLI   PCCWORK,X'00'       TEST FOR ZERO HOURS
         BNE   *+8                 BRANCH IF NOT
         MVI   PCCWORK,X'12'       CONVERT ZERO TO TWELVE
         TM    PCCWORK,X'08'       TEST FOR ADJUSTMENT ERROR
         BZ    *+8                 BRANCH IF NO ERROR
         NI    PCCWORK,X'09'       CORRECT FOR BINARY SUBTRACT ERROR
         ED    BUFSTART+66(9),PCCWORK       PP068 HH.MM.SS
         ST    R1,PCCWORK+4        STORE DATE
         MVI   PYEARTAB+4,28       ASSUME NON-LEAP YEAR
         TM    PCCWORK+5,X'01'     TEST
         BO    *+16                 FOR
         TM    PCCWORK+5,X'12'       LEAP
         BM    *+8                    YEAR
         MVI   PYEARTAB+4,29       SET UP TABLE FOR LEAP YEAR
         ED    BUFSTART+85(3),PCCWORK+5     PP087 YEAR
         XC    PCCWORK(6),PCCWORK  CLEAR ALL BUT JULIAN DATE
         SLR   R0,R0               CLEAR REGISTER
         CVB   R1,PCCWORK          CONVERT JULIAN DATE TO BINARY
         LA    WK1,PYEARTAB-4       PREPARE TO SCAN CONVERSION TABLE
PRIDATE  SR    R1,R0               CONVERT
         LA    WK1,4(,WK1)            FROM JULIAN
         IC    R0,0(,WK1)             DATE TO
         CLR   R0,R1                  MONTH
         BL    PRIDATE                 AND DAY
         CVD   R1,PCCWORK          CONVERT TO DECIMAL
         MVO   PCCWORK(2),PCCWORK+6(2)  SHIFT FOR EDIT
         ED    BUFSTART+78(3),PCCWORK       PP080 DAY
         MVC   BUFSTART+82(3),1(WK1)         PP083 MONTH
         MVC   BUFSTART+90(8),=C' EXT WTR'       PP091 DEVICE NAME
PRIDOUT  DS    0H                  END OF SETUP
*
*
*
*BEGIN   ******
         LA    WK4,$TPIDCT         REMOTE SEPARATOR LINE COUNT
         CLC   SSSODEST,=CL8'LOCAL'      TEST FOR LOCAL PRINT
         BNE   PRIEJECT            BRANCH IF NOT LOCAL
*END     ******
.PNRJE08 ANOP                      *
*
         LA    WK4,$PRIDCT         LOCAL  SEPARATOR LINE COUNT
PRIEJECT DS    0H
         CLM   WK4,1,=AL1(30)      WAS BLOCK LETTER SEGMENT USED
         BL    *+8                 BRANCH IF NO
         SH    WK4,=H'29'          ACCOUNT FOR BLOCK LETTER LINES
         MVI   PRTLINE,X'09'       SPACE 1 AFTER PRINT
         MVC   SYSUT2+82(2),=H'132'
         LA    WK1,PRTLINE
PRINT1   BAL   RETREG,PUTUT2
         BCT   WK4,PRINT1
         L     PL,PCESAVEA         RESTORE LINK REGISTER
         BR    PL                   AND EXIT
         TITLE 'HASP PRINT/PUNCH SERVICE -- BLOCK LETTER ROUTINE'
BLKPRT   DS    0H
         ST    PL,PCEFORM+4        SAVE RETURN REGISTER
         MVC   BUFSTART+140(8),PCCWORK SAVE TEXT
         OC    PCCWORK(8),=8X'C0'  SHIFT ALL TO 4TH QUADRANT
         TR    PCCWORK(8),BLOCKTR-192 TRANSLATE TO INDEX VALUE
         SLR   WK2,WK2               LINE 0 OF 12
BLKBLD   DS    0H
         STH   WK2,PCEFORM+8        SAVE LINE COUNTER
         TM    SEPFLG,SEPOVRFL     NEED OVERFLOW?
         BZ    CLRBUF              NO
         MVI   PRTLINE,X'8B'       SKIP TO CHAN 1
CLRBUF   EQU   *
         MVI   BUFSTART,C' '       INITIAL BLANK
         MVC   BUFSTART+1(131),BUFSTART BLANK BALANCE OF LINE
         SLR   WK2,WK2               SET FOR A LETTER INDEX OF 0
         LA    WK3,BUFSTART+10      1ST BLOCK LETTER IN PP11
BLKLUP   DS    0H
         IC    R7,BUFSTART+140(WK2) USE RELATIVE TEXT LETTER
         STC   R7,BLKMVI+1          TO FORM BLOCK TEXT
         LA    R7,PCCWORK(WK2)      CURRENT LETTER INDEX
         SLR   R15,R15             CLEAR REGISTER
         ICM   R15,1,0(R7)         GET TRANSLATED LETTER INDEX
         BZ    BLKSTOR             BRANCH IF INDEX ZERO
         BCTR  R15,0               DECREMENT BY ONE
         MH    R15,=H'24'          CONVERT TO DISPLACEMENT
         AH    R15,PCEFORM+8       SELECT FOR LINE WITHIN LETTER
         LA    R15,BLOCKA(R15)     LETTER MASK ADDRESS
         ICM   R15,12,0(R15)       LETTER MASK BITS
BLKSTOR  DS    0H
         LA    R7,12               BLOCK WIDTH OF 12
BLKLOOP  DS    0H
         ALR   R15,R15             SHIFT LEFT AND TEST HI BIT
         BC    12,*+8              BRANCH IF OFF
BLKMVI   MVI   0(WK3),*-*           OVERSTORE BLANK TO FORM BLOCK
         LA    WK3,1(,WK3)           INCREMENT COL NUMBER
         BCT   R7,BLKLOOP          BRANCH TO FILL 12 COL'S
         LA    WK3,2(,WK3)           2 BLANKS BETWEEN BLOCKS
         LA    WK2,1(,WK2)           STEP TO NEXT LETTER INDEX
         CL    WK2,=F'8'            HAVE WE DONE 8 BLOCKS
         BL    BLKLUP              BRANCH IF NO
         LA    WK1,PRTLINE
         MVC   SYSUT2+82(2),=H'1'
         BAL   RETREG,PUTUT2
         NI    SEPFLG,FF-SEPOVRFL    TURN OFF OVERFLOW
         MVI   PRTLINE,X'01'
         MVC   SYSUT2+82(2),=H'132'
         BAL   RETREG,PUTUT2
         MVI   PRTLINE,X'0B'
         LH    WK2,PCEFORM+8        GET LINE COUNTER
         LA    WK2,2(,WK2)           STEP TO NEXT LINE
         CH    WK2,=H'24'           LAST LINE FINISHED
         BL    BLKBLD              BRANCH IF NO
         L     PL,PCEFORM+4        LOAD RETURN REGISTER
         BR    PL                  RETURN TO CALLER
         TITLE 'HASP PRINT/PUNCH SERVICE -- VARIABLE STORAGE'
*
*                             MISCELLANEOUS CONSTANTS
*
PTIMASK  DC    X'21204B20204B202040C1D440212040404040402120' TIME MASK
PYEARTAB DC    AL1(31),C'JAN'      JULIAN
         DC    AL1(28),C'FEB'       DATE
         DC    AL1(31),C'MAR'        TO
         DC    AL1(30),C'APR'         DAY
         DC    AL1(31),C'MAY'          AND
         DC    AL1(30),C'JUN'           MONTH
         DC    AL1(31),C'JUL'            CONVERSION
         DC    AL1(31),C'AUG'             TABLE
         DC    AL1(30),C'SEP'              *
         DC    AL1(31),C'OCT'               *
         DC    AL1(30),C'NOV'                *
         DC    AL1(255),C'DEC'                *
PASAMCH  DC    X'400BF18BF013601BF293F39BF4A3F5ABF6B3F7BBF8C3F9CBC1D3C2C
               DBC3E34E03E541E681000B' ASA CARRIAGE TO MACHINE
*
*                             JOB STATISTICS MESSAGE
*
PJOBSTAT DC    CL39'------ JES2 JOB STATISTICS ------'
PRDRSTAT DC    X'40202020206B202120',CL30' CARDS READ'
PPRTSTAT DC    X'40202020206B202120',CL30' SYSOUT PRINT RECORDS'
PPUNSTAT DC    X'40202020206B202120',CL30' SYSOUT PUNCH RECORDS'
PXEQSTAT DC    X'4020202021204B2020',CL30' MINUTES EXECUTION TIME'
*
*
         DS    0D
         LTORG
         EJECT
*
*BEGIN   ******
*
*                             PRINT TRANSLATE MECHANISM
*
PTRTAB   DC    C'                                '  TRANSLATE
         DC    C'                                '  TABLE
         DC    C'           .<(+×&&          $*);^' USED TO
         DC    C'-/         ,%_>?          :#@''="' TRANSLATE
         DC    C' ABCDEFGHI       JKLMNOPQR      '  ALL
         DC    C'  STUVWXYZ      0123456789      '  ILLEGAL
         DC    C' ABCDEFGHI       JKLMNOPQR      '  CHARACTERS
         DC    C'  STUVWXYZ      0123456789      '  TO BLANKS
*END     ******
.PNTRAN3 ANOP                      *
*
         TITLE 'HASP PRINT/PUNCH SERVICE -- BLOCK LETTER TABLES'
BLOCKTR  DC    X'0001020304050607080900000000000000'
         DC    X'0A0B0C0D0E0F1011120013000000000000'
         DC    X'1415161718191A1B000000000000'
         DC    X'1C1D1E1F202122232425002627000000'
BLOCKA   DC    X'7FE0FFF0C030C030C030FFF0FFF0C030C030C030C030C030'
BLOCKB   DC    X'FFE0FFF0C030C030C060FFC0FFC0C060C030C030FFF0FFE0'
BLOCKC   DC    X'7FE0FFF0C030C000C000C000C000C000C000C030FFF07FE0'
BLOCKD   DC    X'FF80FFC0C060C030C030C030C030C030C030C060FFC0FF80'
BLOCKE   DC    X'FFF0FFF0C000C000C000FF00FF00C000C000C000FFF0FFF0'
BLOCKF   DC    X'FFF0FFF0C000C000C000FF00FF00C000C000C000C000C000'
BLOCKG   DC    X'7FE0FFF0C030C000C000C000C1F0C1F0C030C030FFF07FE0'
BLOCKH   DC    X'C030C030C030C030C030FFF0FFF0C030C030C030C030C030'
BKOCKI   DC    X'7FE07FE0060006000600060006000600060006007FE07FE0'
BLOCKJ   DC    X'3FF03FF0030003000300030003000300C300C300FF007E00'
BLOCKK   DC    X'C030C060C0C0C180C300FE00FE00C300C180C0C0C060C030'
BLOCKL   DC    X'C000C000C000C000C000C000C000C000C000C000FFF0FFF0'
BLOCKM   DC    X'C030E070F0F0D9B0CF30C630C030C030C030C030C030C030'
BLOCKN   DC    X'C030E030F030D830CC30C630C330C1B0C0F0C070C030C010'
BLOCKO   DC    X'FFF0FFF0C030C030C030C030C030C030C030C030FFF0FFF0'
BLOCKP   DC    X'FFE0FFF0C030C030C030FFF0FFE0C000C000C000C000C000'
BLOCKQ   DC    X'7FE0FFF0C030C030C030C030C030C330C1B0C0F0FFE07FB0'
BLOCKR   DC    X'FFE0FFF0C030C030C030FFF0FFE0C300C180C0C0C060C030'
BLOCK$   DC    X'06007FE0FFF0C630E6007FC03FE00670C630FFF07FE00600'
BLOCKS   DC    X'7FE0FFF0C030C000E0007FC03FE000700030C030FFF07FE0'
BLOCKT   DC    X'FFF0FFF00600060006000600060006000600060006000600'
BLOCKU   DC    X'C030C030C030C030C030C030C030C030C030C030FFF07FE0'
BLOCKV   DC    X'C030C030C030C030C030C030C030606030C019800F000600'
BLOCKW   DC    X'C030C030C030C030C030C030C630CF30D9B0F0F0E070C030'
BLOCKX   DC    X'C030C030606030C019800F000F00198030C06060C030C030'
BLOCKY   DC    X'C030C030606030C019800F00060006000600060006000600'
BLOCKZ   DC    X'FFF0FFF0006000C00180030006000C00180030007FF0FFF0'
BLOCK0   DC    X'3FC07FE0C030C030C030C030C030C030C030C0307FE03FC0'
BLOCK1   DC    X'06000E001E0006000600060006000600060006007FE07FE0'
BLOCK2   DC    X'7FE0FFF0C0300030003000600180060018006000FFF0FFF0'
BLOCK3   DC    X'7FE0FFF0C0300030003001E001E000300030C030FFF07FE0'
BLOCK4   DC    X'038007800D80198031807FF0FFF001800180018001800180'
BLOCK5   DC    X'FFF0FFF0C000C000C000FF80FFC0006000300030FFF0FFE0'
BLOCK6   DC    X'7FE0FFF0C030C000C000FFE0FFF0C030C030C030FFF07FE0'
BLOCK7   DC    X'FFF0FFE0C0C0018003000600060006000600060006000600'
BLOCK8   DC    X'7FE0FFF0C030C03060603FC03FC06060C030C030FFF07FE0'
BLOCK9   DC    X'7FE0FFF0C030C030C030FFF0FFF000300030C030FFF07FE0'
BLOCK#   DC    X'30C030C0FFF0FFF030C030C030C030C0FFF0FFF030C030C0'
BLOCK@   DC    X'3FC07FE0C030003000301E303F306330C330C3307FE03FC0'
PRTLINE  DC    X'00'
BUFSTART DC    164X'00'
$PRIDCT  EQU   35
$TPIDCT  EQU   4
PL       EQU   RETREG
PCCWORK  DC    D'0'
FF       EQU   X'FF'
JCTJNAME DC    CL8' '
JCTJOBID DC    CL8' '
PWKJOE   DC    C' '
$SID     DC    CL4' '
DEST     DC    CL8' '
MCHCCTBL DC    X'01090B1113191B83898B9193999BA1A3A9ABB1B3B9BBC1C3C9CBD1X
               D3D9DBE1E3'         MACH CC VERIFICATION TBL
PCESAVEA DC    F'0'
PCEFORM  DC    3F'0'
BADEST   DC    H'0'
SAVE     DC    18F'0'
SRETREG  DC    F'0'
RETCODE  DC    F'0'
SYSUT1   DCB   DDNAME=SYSUT1,DSORG=PS,MACRF=(GL),EODAD=CLOSEM
SYSUT2   DCB   DDNAME=SYSUT2,DSORG=PS,MACRF=(PM),RECFM=UM,BLKSIZE=165
SYSPRINT DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=(PM),RECFM=FB,           X
               LRECL=133,BLKSIZE=3990
PRTLIN   DS    0C
PRTCC    DC    C'1'
PRTMSG   DC    132C' '
OPNMSG01 DC    C'UNSUCCESSFUL OPEN FOR SYSUT1  '
ERRMSG01 DC    C'1ST SYSUT1 REC NOT EWTR - 1ST RECORD FOLLOWS'
EWTRHDR  DC    C'EWTR'
EHDRTIME DC    4C' '
EHDRDATE DC    4C' '
EHDRFCB  DC    4C' '
EHDRBLK  DC    2X'00'
EHDRLREC DC    2X'00'
EHDRRECF DC    X'00'
ERRMSG02 DC    C'EWTR NOT FOLLOWED BY SSOB -BAD RECORD FOLLOWS'
SSOBDS   DC    C'SSOB'
         DC    129X'00'
SEPFLG   DC    X'00'
         DS    0F
S99RBPTR DC    X'80'
         DC    AL3(S99RB)
***********************************************************************
***                                                                 ***
***              DYNAMIC ALLOCATION REQUEST BLOCK                   ***
***              SEE JOB MANAGEMENT SRL PAGE 31                     ***
***                                                                 ***
***********************************************************************
S99RB    EQU   *
S99RBLN  DC    AL1(20)             S99RB LENGTH
S99VERB  DC    X'01'               VERB CODE - ALLOC DSNAME
S99FLAG1 DC    B'0000000000000000' FLAGS 1, SYSOUT DATA SET
S99ERROR DC    AL2(0)              ERRORCODE
S99INFO  DC    AL2(0)              INFORMATION CODE
S99TSTPP DC    A(S99TUPL)          POINTER TO TEXT POINTERS
         DC    A(0)                RESERVED
S99FLAG2 DC    A(0)                FLAGS 2
***********************************************************************
***                                                                 ***
***            DYNAMIC ALLOCATION TEXT UNIT POINTERS                ***
***                                                                 ***
***********************************************************************
S99TUPL  DS    0F          TEXT UNIT POINTERS
TUPDDN   DC    A(TUDDN)
TUPSOUT  DC    A(TUSOUT)
TUPFORM  DC    A(0)
TUPNLLOC DC    A(TUNALLOC)
TUPCOPYS DC    A(TUCOPYS)
TUPFCB   DC    A(0)
         DC    X'80'
TUPROUTE DC    AL3(0)
***********************************************************************
***                                                                 ***
***                 DYNAMIC ALLOCATION TEXT UNITS                   ***
***                                                                 ***
***********************************************************************
TUDDN    EQU   *                   DDNAME TEXT UNIT
         DC    X'0001'             KEY
         DC    AL2(1)              NUMBER OF PARMS
         DC    AL2(6)              LENGTH
         DC    C'SYSUT2'           DDNAME
***********************************************************************
TUSOUT   EQU   *                   SYSOUT TEXT UNIT
         DC    X'0018'
         DC    AL2(1)
         DC    AL2(1)
TUSOUTCL DC    X'00'               SYSOUT CLASS
***********************************************************************
TUNALLOC EQU   *                   UNALLOCATE AT CLOSE TEXT UNIT
         DC    X'001C'
         DC    AL2(0)
***********************************************************************
TUFORM   EQU   *                   SYSOUT FORM TEXT UNIT
         DC    X'001A'
         DC    AL2(1)
         DC    AL2(4)
TUFORMNO DC    4C' '               SYSOUT FORM NUMBER
***********************************************************************
TUCOPYS  EQU   *                   NUMB OF COPIES TEXT UNIT
         DC    X'001D'
         DC    AL2(1)
         DC    AL2(1)
TUCPYS   DC    X'00'               NUMBER OF COPIES
***********************************************************************
TUROUTE  EQU   *                   ROUTING TEXT UNIT
         DC    X'0058'
         DC    AL2(1)
TUROUTLN DC    AL2(0)              LNGTH OF ROUTING NAME
TUROUTNM DC    7C' '               ROUTING NAME
***********************************************************************
TUFCB    EQU   *                   FCB TEXT UNIT
         DC    X'0025'
         DC    AL2(1)
         DC    AL2(4)
TUFCNAM  DC    4C' '               FCB IDENTIFIER
***********************************************************************
ALLOCSW  DC    X'00'
TUCHGCLS EQU   X'01'
TUCHGFRM EQU   X'02'
TUCHGCPY EQU   X'04'
TUCHGFCB EQU   X'08'
TUCHGDST EQU   X'10'
CHGJOBID EQU   X'20'
CHGRECL  EQU   X'40'
CHGBLKSZ EQU   X'40'
CHGRECFM EQU   X'80'
OLDJOB   DS    0CL16
OLDJOBNM DC    8X'00'
OLDJOBID DC    8X'00'
***********************************************************************
TUBLKSIZ DC    X'0030'        DCB BLKSIZE TEXT UNIT
         DC    AL2(1)
         DC    AL2(2)
TUBLKSZP DC    AL2(0)
**********************************************************************
TULRECL  DC    X'0042'        DCB LRECL TEXT UNIT
         DC    AL2(1)
         DC    AL2(2)
TULRECLP DC    AL2(0)
**********************************************************************
TURECFM  DC    X'0049'        DCB RECFM TEXT UNIT
         DC    AL2(1)
         DC    AL2(1)
TURECFMP DC    X'00'
**********************************************************************
STARTSEP EQU   X'01'
ENDSEP EQU     X'02'
SEPOVRFL EQU   X'04'
         EJECT
         IEFJSSOB (SO)
         END
