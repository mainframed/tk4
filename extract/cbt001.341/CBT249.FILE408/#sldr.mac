         MACRO
&N       #SLDR &R1,&R2
.*
.*
.*
.* LAST CHANGE DATE - OCTOBER 18, 1983
.*                  - MAILING ADDRESS CHANGE
.*
.* LAST CHANGE DATE - APRIL 21, 1981
.*                  - MACRO NAME CHANGED FROM $SLDR TO #SLDR.
.*
.* LAST CHANGE DATE - FEBRUARY 2, 1977
.*                  - MAILING ADDRESS CHANGE.
.*
.* LAST CHANGE DATE - FEBRUARY 3, 1976
.*
.* THIS MACRO WAS WRITTEN BY DAVID B. COLE. ANY QUESTIONS CONCERNING IT
.* MAY BE ADDRESSED TO:
.*       RR#2 BOX 712
.*       AFTON, VA. 22920
.*
.*
.*
.*   THIS MACRO SIMULATES A HYPOTHETICAL "SUBTRACT LOGICAL DOUBLE
.* REGISTER" MACHINE INSTRUCTION. ITS FUNCTION IS SIMILAR TO THE "SLR"
.* MACHINE INSTRUCTION EXCEPT THAT IT OPERATES ON 64-BIT NUMBERS.
.* WARNING, THE RESULTING CONDITION CODE IS NOT AN ANALOGOUS EXTENSION
.* FROM THE "SLR" INSTRUCTION.
.*
.* INNER MACROS USED - #TEST
.*
.*
.*
         GBLA  &#TESERR
         GBLC  &#TESRET(20)
         LCLA  &A1
         LCLC  &R1ODD,&R2ODD
&R1ODD   SETC  '&R1+1'
         #TEST REGS=&R1
         AIF   (&#TESERR NE 0).R1UNKN
&A1      SETA  &#TESRET(1)+1
         AIF   (&A1 LE 15).GOTA1A
&A1      SETA  0
.GOTA1A  #TEST PFIX=
&R1ODD   SETC  '&#TESRET(1)&A1'
.R1UNKN  ANOP
&R2ODD   SETC  '&R2+1'
         #TEST REGS=&R2
         AIF   (&#TESERR NE 0).R2UNKN
&A1      SETA  &#TESRET(1)+1
         AIF   (&A1 LE 15).GOTA1B
&A1      SETA  0
.GOTA1B  #TEST PFIX=
&R2ODD   SETC  '&#TESRET(1)&A1'
.R2UNKN  ANOP
&N       SLR   &R1ODD,&R2ODD LOW-ORDER DIFFERENCE; HI-ORDER AFFECTED?
         BC    3,SLDR&SYSNDX NO, SKIP
         BCTR  &R1,0 YES, ADJUST HI-ORDER
SLDR&SYSNDX SLR &R1,&R2 GET HI-ORDER DIFFERENCE
         MEND
