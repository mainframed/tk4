FORMAT4  TITLE 'ICSA DISK MAPPER - TIOT WALKER AND JFCB MODIFIER'
FORMAT4  SAVEX BANNER=YES
         EJECT
*        1. RUN DOWN THE TIOT AND DETERMINE THE NUMBER OF VOLUMEXX DDS
*        2. ALLOCATE THE DD, VOLSER AND UCB POINTER TABLE.
*        3. RDJFCB, OPENJ, AND CLOSE EACH DD.
*        4. RETURN WITH A POINTER TO THE TABLE.
*        CALLED FROM PL/I AS CALL FORMAT4 (POINTER);
         SPACE 5
         LA    R4,DCB             FIND THE GENERAL DCB
         USING IHADCB,R4
         LR    R11,R1             SAVE THE PARAMETER ADDRESS
         L     R5,16              POINT TO THE CVT
         L     R5,0(,R5)
         L     R5,0(,R5)          FIND THE TCB
         L     R5,12(,R5)         FIND THE TIOT
         LR    R8,R5              SAVE TIOT ADDRESS
         SR    R6,R6
         LR    R7,R6
TIOTLOOP ICM   R6,B'0001',24(R5)  GET LENGTH THIS ENTRY
         BZ    ENDLOOP1
         CLC   28(6,R5),=C'VOLUME'     IS THIS A VOLUMEXX DD ENTRY?
         BNE   LOOPEND1
         LA    R7,1(,R7)          INCREMENT THE COUNTER
LOOPEND1 AR    R5,R6              BUMP THE POINTER
         B     TIOTLOOP           AND TRY NEXT ENTRY
ENDLOOP1 LR    R9,R7              SAVE COUNT FOR LATER
         MH    R9,=H'18'
         LA    R9,2(,R9)          COMPUTE TABLE SIZE
         GETMAIN R,LV=(9)    GET THE TABLE SPACE
         LR    R10,R1             SAVE TABLE ADDRESS
         STH   R7,0(,R10)     STORE THE COUNTER
         LA    R2,2(,R10)         POINT TO DDNAME PORTION
         SR    R6,R6
TLOOP    ICM   R6,B'0001',24(R8)
         BZ    ENDLOOP2
         CLC   28(6,R8),=C'VOLUME'
         BNE   LOOPEND2
         MVC   0(8,R2),28(R8)     SAVE DDNAME
         L     R1,40(,R8)         FIND THE UCB
         MVC   8(6,R2),28(R1)     SAVE THE VOLSER
         ST    R1,14(,R2)         AND THE UCB ADDRESS
         LA    R2,18(,R2)
         MVC   DCBDDNAM,28(R8)    MOVE DDNAME INTO THE DCB
         RDJFCB (DCB)
         MVC   JFCB(44),=44X'04'  INSERT FORMAT 4 DSNAME
         OPEN  (DCB),TYPE=J
         CLOSE (DCB)              REWRITES THE JFCB WITH FORMAT 4 NAME
LOOPEND2 AR    R8,R6              BUMP POINTER
         B     TLOOP
ENDLOOP2 L     R1,0(,R11)
         ST    R10,0(,R1)
         RETURNX
         LTORG
         PRINT NOGEN
         EJECT
DCB      DCB   DDNAME=*,EXLST=EXLST,DSORG=PS,MACRF=RC
EXLST    DCBEXLST JFCB=JFCB
JFCB     DS    CL176              THE JFCB
         DCBD  DSORG=BS
         END
