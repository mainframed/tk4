         MACRO
&LABEL   XWTORD &L,&D,&TEXT
.**********************************************************************
.*       THIS MACRO IS CALLED AS AN INNER MACRO BY XWTO TO GENERATE   *
.*       ROUTE AND DESCRIPTOR CODE LISTS.                             *
.**********************************************************************
         LCLB  &B(16)             BITS FOR EACH CODE VALUE
         LCLA  &I,&J,&N
         AIF   ('&L' EQ '').DEF
&I       SETA  1
.LOOP    AIF   (K'&L(&I) LE 2).OK
.ERR     MNOTE 4,'XWTO01 &L(&I) IS AN INVALID ROUTE OR DESCRIPTOR CODE,*
                IGNORED.'
         AGO   .NEXT
.OK      AIF   (K'&L(&I) EQ 2).L2
         AIF   ('&L(&I)' LE '0' OR '&L(&I)' GT '9').ERR
         AGO   .GOOD
.L2      AIF   ('&L(&I)' LT '10' OR '&L(&I)' GT '16').ERR
.GOOD    ANOP
&N       SETA  &N+1
&J       SETA  &L(&I)
&B(&J)   SETB  (1)                TURN ON CORRESPONDING BIT
.NEXT    ANOP
&I       SETA  &I+1
         AIF   (&I LE N'&L).LOOP
         AIF   (&N EQ 0).DEF      IF NO GOOD PARMS
&LABEL   UHBGENV B'&B(1)&B(2)&B(3)&B(4)&B(5)&B(6)&B(7)&B(8)&B(9)&B(10)&*
               B(11)&B(12)&B(13)&B(14)&B(15)&B(16)',COM=&TEXT..CODES,  *
               MF=I
         MEXIT
.DEF     ANOP
&LABEL   UHBGENV ,COM=&TEXT..CODES,TYPE=X,DEF='&D',MF=I
         MEND
