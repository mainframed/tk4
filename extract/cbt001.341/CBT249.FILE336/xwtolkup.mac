         MACRO
         XWTOLKUP &L
.**********************************************************************
.*       THIS MACRO IS USED AS IN INNER MACRO BY XWTO TO DO A TABLE   *
.*       LOOKUP FOR A SYMBOL (THE LABEL OF A MESSAGE SEGMENT), AND    *
.*       TO RETURN THE INDEX NUMBER, AND THE CORRESPONDING ELEMENT OF *
.*       ANOTHER TABLE (THE TABLE OF PARAMETER LIST NAMES). THE       *
.*       CORRESPONDING ELEMENT MAY BE AN INDIRECT REFERNCE TO ANOTHER *
.*       TABLE ELEMENT. IN THIS CASE, THE CORRECT INDEX IS RETURNED,  *
.*       BUT THE LOOKUP RESULT IS THE MEMBER OF THE PARMLIST TABLE    *
.*       IDENTIFIED BY THE INDIRECT REFERENCE.                        *
.**********************************************************************
         GBLC  &XWTOFLD(200)      THE MESSAGE SEGMENT TABLE
         GBLC  &XWTOID(200)       THE PARM LIST TABLE
         GBLC  &XWTOAL            THE RETURNED RESULT
         GBLA  &XWTOIX            THE CURRENT TABLE SIZE
         GBLA  &XWTOLK            THE RETURNED INDEX (0 IF NOT FOUND)
         LCLA  &I
&I       SETA  1
.LOOP    AIF   (&I GT &XWTOIX).NO
         AIF   ('&XWTOFLD(&I)' EQ '&L').YES
&I       SETA  &I+1
         AGO   .LOOP
.NO      ANOP
&XWTOLK  SETA  0                  NEGATORY, GOOD BUDDY
         MEXIT
.YES     ANOP
&XWTOLK  SETA  &I                 RETURNED INDEX
&XWTOAL  SETC  '&XWTOID(&I)'      CORRESPONDING PARM LIST
         AIF   ('&XWTOAL'(1,1) NE '0').MEND   IF NOT INDIRECT
&I       SETA  &XWTOAL            CONVERT INDIRECT REF TO NUMERIC
&XWTOAL  SETC  '&XWTOID(&I)'      AND RETURN THAT ONE INSTEAD
.MEND    MEND
