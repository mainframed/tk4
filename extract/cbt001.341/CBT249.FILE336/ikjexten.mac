         MACRO
&LABEL   IKJEXTEN &TYPNAM,&TYPE,&OPTION,&OPTION2,&OPTION3,             *
               &PROMPT=,&DEFAULT=,&HELP=,&VALIDCK=,&PARM=
.*       THIS MACRO IS A MODIFIED VERSION OF THE SVS IKJIDENT
.* RELEASE 20 SUPPORT CODE -- 20035
.* A 007320,007340,007360,007380,005920,005940,005960,005980      R0301
.* NOTE: FLAGGING CODES: R=DCRR,A=APAR,F=PTF,M=PTM,S=SUPPORT CODE R0301
         GBLC  &IKJCSNM,&IKJDSNM
         GBLB  &CLUXTEN
         LCLA  &T,&PDESIZE,&END
         LCLB  &FLIST,&FRANGE,&FASIS,&FPRPT,&FDFLT
         LCLB  &FHELP,&FVALID,&FENDBIT
         LCLC  &BADFRST,&QUOTE
&FENDBIT SETB  0
         AIF   (N'&SYSLIST LT 6).GOOD
         MNOTE 8,'NO MORE THAN 5 POSITIONAL PARAMETERS ALLOWED'
.GOOD    AIF   ('&TYPNAM' EQ 'LIST' OR '&TYPNAM' EQ 'RANGE').BAD
         AIF   ('&TYPNAM' NE 'UPPERCASE' AND '&TYPNAM' NE 'ASIS'       *
               AND '&TYPNAM' NE '').GOOD1
.BAD     MNOTE 8,'PARAMETER TYPE NAME MISSING BUT REQUIRED'
&FENDBIT SETB  1
.GOOD1   ANOP
         AIF   ('&TYPE' NE 'LIST' AND '&TYPE' NE 'RANGE' AND '&TYPE' NE*
               'ASIS' AND '&TYPE' NE 'UPPERCASE' AND '&TYPE' NE '').BET*
               TER
         MNOTE 8,'RECOGNIZER NAME MISSING BUT REQUIRED'
.BETTER  ANOP
         AIF   ('&IKJCSNM' NE '').GOOD2
         MNOTE 8,'CSECT NAME (FROM IKJPARM MACRO LABEL) IS MISSING'
&FENDBIT SETB  1
.GOOD2   ANOP
         AIF   ('&LABEL' NE '').GOOD3
         MNOTE 8,'STATEMENT LABEL MISSING BUT REQUIRED'
&FENDBIT SETB  1
.GOOD3   ANOP
&FLIST   SETB  ('&OPTION' EQ 'LIST' OR '&OPTION2' EQ 'LIST' OR         *
               '&OPTION3' EQ 'LIST')
&FRANGE  SETB  ('&OPTION' EQ 'RANGE' OR '&OPTION2' EQ 'RANGE' OR       *
               '&OPTION3' EQ 'RANGE')
&FASIS   SETB  ('&OPTION' EQ 'ASIS' OR '&OPTION2' EQ 'ASIS' OR         *
               '&OPTION3' EQ 'ASIS')
         AIF   ('&OPTION' EQ '' OR '&OPTION' EQ 'LIST' OR '&OPTION'    *
               EQ 'RANGE' OR '&OPTION' EQ 'ASIS' OR '&OPTION'          *
               EQ 'UPPERCASE').OPT1
         MNOTE 8,'''&OPTION'' IS AN INVALID POSITIONAL PARAMETER'
&FENDBIT SETB  1
.OPT1    AIF   ('&OPTION2' EQ '' OR '&OPTION2' EQ 'LIST' OR '&OPTION2' *
               EQ 'RANGE' OR '&OPTION2' EQ 'ASIS' OR '&OPTION2' EQ     *
               'UPPERCASE').OPT2
         MNOTE 8,'''&OPTION2'' IS AN INVALID POSITIONAL PARAMETER'
&FENDBIT SETB  1
.OPT2    AIF   ('&OPTION3' EQ '' OR '&OPTION3' EQ 'LIST' OR '&OPTION3' *
               EQ 'RANGE' OR '&OPTION3' EQ 'ASIS' OR '&OPTION3' EQ     *
               'UPPERCASE').OPT3
         MNOTE 8,'''&OPTION3'' IS AN INVALID POSITIONAL PARAMETER'
&FENDBIT SETB  1
.OPT3    AIF   (('&OPTION' EQ 'UPPERCASE' OR '&OPTION2' EQ             *
               'UPPERCASE' OR '&OPTION3' EQ 'UPPERCASE') AND &FASIS).ER*
               ROR0
         AGO   .A0
.ERROR0  MNOTE 8,'BOTH ''UPPERCASE'' AND ''ASIS'' CANNOT BE SPECIFIED'
&FENDBIT SETB  1
.A0      ANOP
&FPRPT   SETB  ('&PROMPT' NE '')
&FDFLT   SETB  ('&DEFAULT' NE '')
&FHELP   SETB  ('&HELP' NE '')
&FVALID  SETB  ('&VALIDCK' NE '')
         AIF   (NOT(&FDFLT AND &FPRPT)).GOOD4
         MNOTE 8,'PARAMETER CANNOT BOTH DEFAULT AND REQUIRE PROMPTING'
&FENDBIT SETB  1
.GOOD4   ANOP
         AIF   (NOT(&FENDBIT)).GEN     IF ERRORS DO NOT GENERATE CODE
         MEXIT
.GEN     ANOP
         AIF   (&CLUXTEN).NONOTE
         MNOTE 0,'WARNING: THE IKJEXTEN MACRO IS AN EXTENSION TO IBM TS*
               O'
&CLUXTEN SETB  (1)
.NONOTE  ANOP
&IKJCSNM CSECT ,                       RESUME PCL CSECT
* FLAGS -    BIT3=PROMPT,  BIT4=DEFAULT,  BIT5=IKJEXTEN,  BIT6=HELP,
*            BIT7=VALIDCK, BIT8=LIST,     BIT9=ASIS,      BIT10=RANGE
DENT&SYSNDX EQU *
         DC    BL2'100&FPRPT&FDFLT.1&FHELP&FVALID&FLIST&FASIS&FRANGE.00*
               000'                    FLAGS
         DC    AL2(DEND&SYSNDX-DENT&SYSNDX) LENGTH OF PCE
         DC    AL2(&LABEL-&IKJDSNM)    OFFSET TO PDE
         DC    AL3(&TYPE)         USER RECOGNIZER ROUTINE ADDR
.*
.* THE FOLLOWING CODE SUPPLIES QUOTES TO &TYPNAM IF THEY ARE MISSING.
.*
&QUOTE   SETC  '&TYPNAM'(1,1)
         AIF   ('&QUOTE' EQ '''').QUOT
&QUOTE   SETC  ''''
         DC    AL2(IKJ@&SYSNDX-*),AL2(18),C&QUOTE&TYPNAM&QUOTE PARAMETE*
               R TYPE MESSAGE SEGMENT
         AGO   .EQUGEN1
.QUOT    ANOP
         DC    AL2(IKJ@&SYSNDX-*),AL2(18),C&TYPNAM PARAMETER TYPE MESSA*
               GE SEGMENT
.EQUGEN1 ANOP
IKJ@&SYSNDX EQU *
         AIF   (&FPRPT).PRPT           PROMPT SPECIFIED
         AIF   (NOT &FDFLT).NPRDF
         DC    AL1(IKJ$&SYSNDX-*-2),C&DEFAULT DEFAULT DATA
         AGO   .EQUGEN
.PRPT    DC    AL1(IKJ$&SYSNDX-*-2),C&PROMPT PROMPT DATA
.EQUGEN  ANOP
IKJ$&SYSNDX EQU *
.NPRDF   ANOP
         AIF   (NOT &FHELP).NOHELP     IS HELP SPECIFIED
         DC    AL2(IKJA&SYSNDX-*)      LENGTH OF HELP DATA
&T       SETA  1                       SET &T FOR LOOP CONTROL
.HELPLP  ANOP
&T       SETA  &T+1
         AIF   (&T EQ 10).HELPER
         AIF   (&T LE N'&HELP).HELPLP
         AGO   .HELPCT
.HELPER  MNOTE 4,'ONLY 9 LEVELS OF HELP AVAILABLE. OTHERS IGNORED'
&T       SETA  &T+1                    PROVIDE FOR DECREMENT
.HELPCT  ANOP
&T       SETA  &T-1
         DC    AL1(&T)                 NUMBER OF HELP MESSAGES
&END     SETA  &T
&T       SETA  1
.HELPLPA ANOP
         DC    AL2(IKJ&SYSNDX.&T-*),AL2(00),C&HELP(&T) HELP MSG SEGMENT
IKJ&SYSNDX.&T  EQU *
&T       SETA  &T+1
         AIF   (&T LE &END).HELPLPA
IKJA&SYSNDX EQU *
.NOHELP  ANOP
         DC    AL2(IKJ#&SYSNDX-*-2)    LENGTH OF RECOGNIZER PARM
         AIF   ('&PARM' EQ '').NOPARM
&QUOTE   SETC  '&PARM'(1,1)
         AIF   ('&QUOTE' EQ '''').RQUOT
         DC    C'&PARM'           RECOGNIZER PARAMETER STRING
         AGO   .NOPARM
.RQUOT   DC    C&PARM             RECOGNIZER PARAMETER STRING
.NOPARM        ANOP
IKJ#&SYSNDX EQU *
         AIF   (NOT &FVALID).NOVALID   IS VALIDCK SPECIFIED
         DC    AL3(&VALIDCK)           VALIDCK ROUTINE ADDRESS
.NOVALID ANOP
DEND&SYSNDX EQU *
&IKJDSNM DSECT
&PDESIZE SETA  5
         AIF   (&FLIST AND &FRANGE).DSECT IS LIST AND RANGE SPECIFIED
&PDESIZE SETA  4
         AIF   (&FRANGE).DSECT
&PDESIZE SETA  3
         AIF   (&FLIST).DSECT
&PDESIZE SETA  2
.DSECT   ANOP
&LABEL   DS    &PDESIZE.A              ALLOCATE PDE SPACE
&SYSECT  CSECT ,                       RESUME USERS CSECT
         MEXIT
         MEND
