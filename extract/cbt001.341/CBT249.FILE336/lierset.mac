         TITLE 'LIERSET -- ASSEMBLER ENVIRONMENT ERROR TRACE ROUTINE'
         BANNER LIERSET
         SPACE
***********************************************************************
*                                                                     *
* ROUTINE NAME -                                                      *
*        LIERSET                                                      *
*                                                                     *
* FUNCTION -                                                          *
*        TO ESTABLISH HELPFUL PROGRAM-CHECK HANDLING IN A PSEUDO-PL/I *
*        (LIE) ENVIRONMENT. THE HANDLING IS CANCELLED AUTOMATICALLY   *
*        BY LIEXIT ON EXIT FROM THE CALLING PROCEDURE.                *
*                                                                     *
* ENVIRONMENT -                                                       *
*        PSEUDO-PL/I                                                  *
*                                                                     *
* LINKAGE -                                                           *
*        CALL, GENERATED BY THE LIERRSET MACRO.                       *
*                                                                     *
* PARAMETERS -                                                        *
*        REGISTER 1 CONTAINS THE ADDRESS TO WHICH CONTROL IS TO BE    *
*        TRANSFERRED AFTER A PROGRAM CHECK. AS THE CONTENTS OF THE    *
*        REGISTERS AT THIS TIME (PARTICULARLY THE DSA REGISTER) ARE   *
*        UNPREDICTABLE, IT IS ASSUMED THIS WILL ADDRESS A LIGOTO      *
*        MACRO, WHICH WILL RESTORE A PREDICTABLE ENVIRONMENT.         *
*                                                                     *
* RETURN INFORMATION -                                                *
*        NONE                                                         *
***********************************************************************
         EJECT
LIERRASM LISECT
         SPACE
LIERSET  LIENTRY PARM=R2,DSALEN=88,DSA=*
         EJECT
***********************************************************************
*        THIS ROUTINE IS CALLED TO ESTABLISH AN ERROR EXIT POINT FOR  *
*        THE DURATION OF THE CALLING PROCEDURE. IT ISSUES A SPIE MACRO*
*        AND STORES INFORMATION IN THE DSA AND PSEUDO-TCA FOR USE BY  *
*        LIEXIT ON TERMINATION OF THE CALLING PROCEDURE.              *
***********************************************************************
         SPACE
         USING ATCA,R12
         USING ADSA,R3
         SPACE
         L     R3,ADSACHN-ADSA(,R13)   FIND CALLER'S SAVE AREA
         CLI   ATCAOPIC,0         HAS A SPIE BEEN ISSUED?
         BNE   ALREADY            YES.
         MVC   ATCAPICA(6),PICA   NO, DO IT NOW
         SPIE  MF=(E,ATCAPICA)    ALL BUT SIGNIF & FIXOFL
         ST    R1,ATCAOPIC        SAVE OLD PICA ADDR
         MVI   ATCAOPIC,X'FF'     NOTE SPIE ACTIVE
         MVI   ADSAOERX,X'FF'     NOTE SPIE ISSUED HERE
         B     SAVEOLD
         SPACE
ALREADY  OC    ADSAOERX,ADSAOERX  IS THIS A REPEAT CALL?
         BNZ   REPEAT             YES, DON'T CHANGE OLD
SAVEOLD  MVC   ADSAOERX+1(3),ATCAERRX  NO, SAVE PREVIOUS EXIT POINT
REPEAT   ST    R2,ATCAERRX        SAVE NEW EXIT POINT
         SPACE
         LIEXIT ,                 RETURN TO CALLER
         SPACE
PICA     SPIE  LIERROR,((1,7),(9,13),15),MF=L
         SPACE
         DROP  R3
         TITLE 'LIERROR -- PSEUDO-PL/I ENVIRONMENT PROGRAM CHECK EXIT'
         BANNER LIERRROR
         SPACE
***********************************************************************
*                                                                     *
* ROUTINE NAME -                                                      *
*        LIERROR                                                      *
*                                                                     *
* FUNCTION -                                                          *
*        TO SERVE AS A SPIE EXIT IN A PSEUDO-PL/I ENVIRONMENT. IT     *
*        ARRANGES FOR CONTROL TO PASS TO THE LIESNAP ROUTINE, AS IF   *
*        IT HAD BEEN CALLED NORMALLY, SO THAT PROGRAM CHECKS DURING   *
*        ITS EXECUTION CAN ALSO BE RECOVERED.                         *
*                                                                     *
* ENVIRONMENT -                                                       *
*        PSEUDO-PL/I. THIS CODE RUNS AS AN OS SPIE EXIT               *
*                                                                     *
* LINKAGE -                                                           *
*        CALL FROM THE OPERATING SYSTEM PROGRAM CHECK HANDLER (AS A   *
*        SPIE EXIT).                                                  *
*                                                                     *
* PARAMETERS -                                                        *
*        REGISTER 1 CONTAINS THE ADDRESS OF THE PROGRAM INTERRUPTION  *
*        ELEMENT (PIE).                                               *
*                                                                     *
* RETURN INFORMATION -                                                *
*        THE ADDRESS TO RESUME AT (THE LIESNAP ROUTINE) IS RETURNED   *
*        IN THE PIE.                                                  *
*                                                                     *
* NOTE -                                                              *
*        IF A PROGRAM CHECK OCCURS IN CODE WHICH DOES NOT RUN IN THE  *
*        PSEUDO-PL/I ENVIRONMENT (AN EXIT ROUTINE), OR AFTER THE      *
*        ENVIRONMENT HAS BEEN CORRUPTED, THIS ROUTINE WILL ALMOST     *
*        CERTAINLY PROGRAM CHECK AGAIN, DUE TO THE DEPENDENCE OF      *
*        THE LIENTER ROUTINE ON THE CONTENTS OF REGISTERS 12 AND 13.  *
***********************************************************************
         EJECT
***********************************************************************
*        THIS ROUTINE IS ENTERED AS A SPIE EXIT AFTER A PROGRAM CHECK.*
*        IT USES LIENTRY TO CREATE AND CHAIN A SAVE AREA (WHICH       *
*        ASSUMES, INCIDENTALLY, THAT REGISTER 13 PRESENTLY ADDRESSES  *
*        A LIE DSA). WHEN CONTROL IS PASSED (THROUGH THE MARVELS OF   *
*        THE PROGRAM CHECK HANDLER) TO LIESNAP, IT WILL HAVE THE USE  *
*        OF THAT SAVE AREA.                                           *
***********************************************************************
         SPACE
LIERROR  LIENTRY PARM=R9          OBTAIN & CHAIN NEW DSA
         L     R8,ADSACHN-ADSA(,R13)   FIND R13 AT TIME OF ERROR
         USING ADSA,R8
         L     R7,ADSASAVE        LOAD SYSTEM RETURN POINT
         MVC   ADSASAVE(20),12(R9)     COPY PIE REGS TO DSA
         MVC   DSAPIE(12),0(R9)   SAVE INTERRUPT INFO
         LR    R14,R7
         MVC   9(3,R9),=AL3(LIESNAP)   SET RETURN POINT FROM SYSTEM
         BR    R14
         TITLE 'LIESNAP -- PSEUDO-PL/I DIAGNOSTIC TRACE ROUTINE'
         BANNER LIESNAP
         SPACE
***********************************************************************
*                                                                     *
* ROUTINE NAME -                                                      *
*        LIESNAP                                                      *
*                                                                     *
* FUNCTION -                                                          *
*        TO USE WTP TO WRITE A TRACE OF ACTIVE ROUTINE NAMES AND      *
*        OFFSETS FOR DIAGNOSTIC PURPOSES, AND THEN RETURN CONTROL TO  *
*        THE EXIT POINT REQUESTED BY THE MOST RECENT USE OF LIERSET.  *
*                                                                     *
* ENVIRONMENT -                                                       *
*        PSEUDO-PL/I                                                  *
*                                                                     *
* LINKAGE -                                                           *
*        ENTERED FROM THE SUPERVISOR AS A SPIE "RETRY" ROUTINE.       *
*                                                                     *
* PARAMETERS -                                                        *
*        THE PIE FOR THE ORIGINAL PROGRAM CHECK HAS BEEN STORED IN    *
*        THE CURRENT DSA.                                             *
*                                                                     *
* RETURN INFORMATION -                                                *
*        NONE                                                         *
*                                                                     *
* NOTE -                                                              *
*        THIS ROUTINE RETURNS VIA INTENTIONAL PROGRAM CHECK, SO THAT  *
*        RETURN CAN BE EFFECTED WITH THE REGISTERS AT THE TIME OF     *
*        ERROR INTACT, A FEAT THAT CAN ONLY BE ACCOMPLISHED BY A      *
*        DELIBERATE PROGRAM CHECK AND A COOPERATIVE SPIE ROUTINE.     *
***********************************************************************
         EJECT
***********************************************************************
*        THIS ROUTINE TRAVERSES THE SAVE AREA CHAIN AND WRITES (USING *
*        WTP) A TRACEBACK FROM THE POINT OF ERROR. IF A PROGRAM CHECK *
*        OCCURS DURING THIS PROCESS, THE ATTEMPT IS ABANDONED.        *
***********************************************************************
         SPACE
         ENTRY LIESNAP
         SPACE
LIESNAP  MVC   ATCAPICA+1(3),=AL3(LIERCUR)  PROTECT FROM RECURSION
         LA    R3,15
         N     R3,DSAPIE+4        ISOLATE PGM CHECK NUMBER
         LA    R3,HEX(R3)
         XWTO  'LIE100L PROGRAM CHECK ',                               *
               (CODE,(R3),1),' AT',DESC=7,ROUTCDE=11,                  *
               MF=(G,DSAMSG,MSGLEN1)   HEADING MESSAGE
         SPACE
         XWTO  '  ',(NAME,8' '),'    ',(ADDR,7' '),'  ',               *
               (FROM,'FROM'),DESC=7,ROUTCDE=11,                        *
               MF=(L,DSAMSG,MSGLEN2)   BUILD BASIC TRACE MSG
         L     R5,DSAPIE+8        START WITH INTERRUPT POINT
         L     R4,ADSACHN         LOCATE 1 EARLIER SAVE AREA
         SPACE
***********************************************************************
*        THIS CODE ASSUMES THAT ALL ACTIVE ROUTINES OBEY PL/I         *
*        CONVENTIONS FOR LOCATION OF THE ROUTINE NAME (HONORED BY     *
*        THE LIENTRY MACRO).                                          *
***********************************************************************
         SPACE
TRACE    L     R7,ADSASAVE+4-ADSA(R4)       FIND ENTRY TO THIS PROC
         LA    R3,0(,R7)          SAVE IT
         BCTR  R7,0
         CLI   0(R7),0            LOOK AT NAME LENGTH
         BE    STARNAME           MAKE SURE 1-8 CHARS
         CLI   0(R7),8
         BH    STARNAME           IF NOT, NAME IS UNKNOWN
         SR    R6,R6
         IC    R6,0(,R7)
         SR    R7,R6              R7->PROC NAME
         MVC   DSANAME,=CL8' '
         BCTR  R6,0
         EX    R6,COPYNAME
         TM    ADSALEN,ADSAFINL   IS THIS THE MAIN PROC?
         BNO   NOTMAIN            NO.
         XWTO  (FROM,'MAIN'),MF=(M,DSAMSG)  YES, UPDATE MSG
         SPACE
***********************************************************************
*        ADDRESSES ARE PRINTED AS RELATIVE ADDRESSES IF REASONABLE,   *
*        OR AS ABSOLUTE ADDRESSES IF NOT.                             *
***********************************************************************
         SPACE
NOTMAIN  LA    R1,0(,R5)
         SR    R1,R3              DETERMINE OFFSET
         LPR   R0,R1              SEE IF MAGNITUDE REASONABLE
         C     R0,=F'16384'
         BH    ABSADDR            NO, TREAT AS ABSOLUTE ADDR
         STH   R0,DSAHWK          YES, CONVERT OFFSET TO HEX
         MVI   DSAHWK+2,X'04'
         UNPK  DSAABUF+1(5),DSAHWK(3)
         TR    DSAABUF+1(4),HEX-240
         MVI   DSAABUF+6,C' '
         MVI   DSAABUF,C'+'       ASSUME POSITIVE OFFSET
         LTR   R1,R1
         BNM   SENDTRAC
         MVI   DSAABUF,C'-'       NOPE, CHANGE TEXT
         B     SENDTRAC
         SPACE
STARNAME MVC   DSANAME,=C'*UNKNOWN'
ABSADDR  ST    R5,DSAHWK          CONVERT ADDR TO HEX
         MVI   DSAHWK+4,X'B4'     ->'.' ON UNPK
         UNPK  DSAABUF(7),DSAHWK+1(4)
         TR    DSAABUF(6),HEX-240
         SPACE
SENDTRAC XWTO  (NAME,DSANAME),(ADDR,DSAABUF),MF=(E,DSAMSG)             *
                                  SEND TRACE LINE
         SPACE
         TM    ADSALEN,ADSAFINL   REACHED THE END?
         BO    TRACED             YES, TRACE DONE
         LR    R8,R4              NO, CONTINUE
         L     R5,ADSASAVE        LOCATE POINT OF CALL
         L     R4,ADSACHN         LOCATE PREVIOUS AREA
         B     TRACE              AND CONTINUE
         SPACE
***********************************************************************
*        TO GET TO THE EXIT POINT, WE FORCE A PROGRAM CHECK SO WE     *
*        CAN RESUME AT THE EXIT POINT WITH THE ORIGINAL REGS.         *
***********************************************************************
         SPACE
TRACED   MVC   ATCAPICA+1(3),=AL3(LIERRET)  ESTABLISH RETURN POINT
         DC    H'0'               AND FALL INTO IT
HEX      DC    C'0123456789ABCDEF'
COPYNAME MVC   DSANAME(0),0(R7)   MOVE PROC NAME TO DSA
         TITLE 'LIERRET -- ROUTINE TO EXIT TO USER ERROR LABEL'
         BANNER LIERRET
         SPACE
***********************************************************************
*                                                                     *
* ROUTINE NAME -                                                      *
*        LIERRET                                                      *
*                                                                     *
* FUNCTION -                                                          *
*        TO ROUTE CONTROL TO A PREVIOUSLY SPECIFIED ADDRESS AFTER A   *
*        PROGRAM CHECK.                                               *
*                                                                     *
* ENVIRONMENT -                                                       *
*        PSEUDO-PL/I. THIS CODE EXECUTES AS AN OS SPIE EXIT.          *
*                                                                     *
* LINKAGE -                                                           *
*        CALL FROM THE OPERATING SYSTEM PROGRAM CHECK HANDLER (AS A   *
*        SPIE EXIT).                                                  *
*                                                                     *
* PARAMETERS -                                                        *
*        REGISTER 1 CONTAINS THE ADDRESS OF THE PROGRAM INTERRUPTION  *
*        ELEMENT (PIE).                                               *
*                                                                     *
* RETURN INFORMATION -                                                *
*        THE ADDRESS TO RESUME AT (THE USER'S LABEL) IS RETURNED      *
*        IN THE PIE.                                                  *
***********************************************************************
         EJECT
***********************************************************************
*        THIS ROUTINE MODIFIES ITS DSA (OBTAINED BY LIERROR) TO       *
*        RETURN TO THE SPIE EXIT RETURN ADDRESS PASSED IN REGISTER    *
*        14. IT THEN ESTABLISHES THE USER'S LABEL AS THE SPIE RETRY   *
*        POINT AND ISSUES LIEXIT. LIEXIT WILL FREE THE DSA AND RETURN *
*        TO THE SYSTEM, WHICH WILL ROUTE CONTROL BACK TO THE SPECIFIED*
*        ADDRESS.                                                     *
***********************************************************************
         SPACE
         ENTRY LIERRET
         SPACE
LIERRET  MVC   ATCAPICA+1(3),=AL3(LIERROR)  RESTORE NORMAL ERREXIT
         L     R8,ADSACHN-ADSA(,R13)   FIND USER'S DSA
         MVC   12(20,R1),ADSASAVE RESTORE ORIGINAL 14-2
         ST    R14,ADSASAVE       SAVE SYSTEM RETURN FOR LIEXIT
         MVC   9(3,R1),ATCAERRX+1 RETURN TO USER EXIT
         LIEXIT ,                 RETURN & RELEASE DSA
         TITLE 'LIERCUR -- LIE PROGRAM CHECK RECURSION ROUTINE'
         BANNER LIERCUR
         SPACE
***********************************************************************
*                                                                     *
* ROUTINE NAME -                                                      *
*       LIERCUR                                                       *
*                                                                     *
* FUNCTION -                                                          *
*        TO RECOVER FROM A PROGRAM CHECK DURING THE LIESNAP TRACE     *
*        OUTPUT ROUTINE.                                              *
*                                                                     *
* ENVIRONMENT -                                                       *
*        PSEUDO-PL/I. THIS ROUTINE IS ENTERED AS AN OS SPIE EXIT.     *
*                                                                     *
* LINKAGE -                                                           *
*        CALL FROM THE OPERATING SYSTEM PROGRAM CHECK HANDLER (AS A   *
*        SPIE EXIT).                                                  *
*                                                                     *
* PARAMETERS -                                                        *
*        REGISTER 1 CONTAINS THE ADDRESS OF THE PROGRAM INTERRUPTION  *
*        ELEMENT (PIE).                                               *
*                                                                     *
* RETURN INFORMATION -                                                *
*        THE ADDRESS TO RESUME AT (THE USER'S LABEL) IS RETURNED      *
*        IN THE PIE.                                                  *
***********************************************************************
         EJECT
         ENTRY LIERCUR
         SPACE
***********************************************************************
*        THIS ROUTINE IS ENTERED IF A PROGRAM CHECK OCCURS DURING THE *
*        WRITING OF THE TRACEBACK. IT WRITES A DESPARATE MESSAGE AND  *
*        EXITS TO THE USER'S ERROR EXIT LABEL.                        *
***********************************************************************
         SPACE
LIERCUR  LR    R2,R14             SAVE THE RETURN ADDRESS
         LR    R3,R1
         XWTO  'LIE101L TRACEBACK TERMINATED. ERROR IN SAVE AREA CHAIN'*
               ,DESC=7,ROUTCDE=11
         LR    R14,R2             RESTORE RETURN ADDRESS
         LR    R1,R3
         B     LIERRET            GO RETURN TO USER LABEL
         SPACE
         LTORG
         TITLE 'LIERSET -- DATA AREAS'
DSA      DSECT ,                  DSA FOR LIERROR & FRIENDS
         DS    22A
DSAPIE   DS    3A                 SAVE AREA FOR PIE INFO
DSAHWK   DS    2A                 HEX WORK AREA
DSAMSG   DS    CL(MSGLEN1)        WTO AREA
         ORG   DSAMSG
         DS    CL(MSGLEN2)
         ORG   ,                  MAKE ROOM FOR BOTH MESSAGES
DSANAME  DS    CL8                PROC NAME AREA
DSAABUF  DS    CL7                ADDRESS WORK AREA
         DS    0D
         SPACE
DSALEN   EQU   *-DSA              LENGTH OF OUR DSA
         EJECT
         ASMTCA
         SPACE 5
         ASMDSA
         SPACE
         END
