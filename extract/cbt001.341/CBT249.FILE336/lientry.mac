         MACRO
&LABEL   LIENTRY &PARM=R2,&DSALEN=DSALEN,&BASE=R11,&ALIAS=*,&ISASIZE=, *
               &DSA=DSA,&ENV=
.*       THIS MACRO GENERATES LANGUAGE INDEPENDENT CODE FOR SAVING
.*       REGISTERS, GETTING A BASE REGISTER, SAVING A PARAMETER
.*       POINTER, AND GETTING A NEW DYNAMIC SAVE AREA.
         GBLC  &LISECT            NAME OF CONTAINING CSECT
         GBLC  &LIBASE            DECLARED BASE REGISTER
         GBLC  &LIONSCT           NAME OF ONCB CSECT
         GBLC  &LIDSA             NAME OF DSA
         GBLB  &LIDSAU            USING ACTIVE FOR DSA
         LCLA  &K
         LCLC  &PRMR              TEMP. PARM REG
&PRMR    SETC  '&PARM'
         AIF   ('&PRMR' NE '1' AND '&PRMR' NE 'R1' AND '&PRMR' NE '10' *
               AND '&PRMR' NE 'R10' AND '&PRMR' NE '11' AND            *
               '&PRMR' NE 'R11').OK
         AIF   ('&BASE' EQ 'R3').PRMR2
&PRMR    SETC  'R3'
         AGO   .OK
.PRMR2   ANOP
&PRMR    SETC  'R2'
.OK      ANOP
&K       SETA  K'&LABEL
         CNOP  0,4                ALIGN ON FULLWORD
         AIF   (&K EQ 8).LEN8
         DC    (7-&K)C' '
         AGO   .JLEN
.LEN8    DC    3C' '
.JLEN    DC    C'&LABEL'          NAME OF ROUTINE
         DC    AL1(&K)            LENGTH OF NAME
         AIF   ('&ENV' NE '').NOENT
         ENTRY &LABEL
.NOENT   USING &LABEL,R15
&LABEL   STM   R14,R12,12(RDSA)   SAVE REGISTERS
         AIF   ('&ALIAS' EQ '*').NOALI
         ENTRY &ALIAS             MAKE ALTERNATE NAME AN ENTRY
&ALIAS   EQU   &LABEL             DEFINE ALIAS NAME
.NOALI   LR    &PRMR,R1           SAVE PARM POINTER
         LA    R0,&DSALEN         LENGTH OF SAVE AREA
         AIF   ('&ISASIZE' EQ '').NISA
         LA    R1,&ISASIZE        PASS ISASIZE IN R1
         L     R10,=V(LIEBEGN)    GET ISA CODE ADDRESS
         AGO   .CALL
.NISA    L     R10,=V(LIENTER)    GET DSA CODE ADDRESS
.CALL    BALR  R11,R10            GO GET A DSA
         DROP  R15
         USING *,R11
         AIF   ('&DSA' EQ '*').NODSA
         USING &DSA,RDSA          ESTABLISH SAVE AREA BASE
&LIDSAU  SETB  (1)
         AGO   .JDSA
.NODSA   AIF   (NOT &LIDSAU).JDSA
         DROP  RDSA
&LIDSAU  SETB  (0)
.JDSA    AIF   ('&LIDSA' NE '').DSAA
&LIDSA   SETC  '&DSA'
.DSAA    AIF   ('&LIONSCT' EQ '').NOON
         MVC   92(4,RDSA),=A(&LIONSCT) SAVE ONCB PTR IN DSA
         XC    LIONCELS(LIONCELN),LIONCELS  CLEAR ON CELLS
         MVI   LIONCELS+LIONCELN,X'01' STORE ON CELL END MARK
         LA    R10,LIONCELS
         ST    R10,112(,RDSA)     STORE ON CELL PTR IN DSA
         OI    0(RDSA),X'40'      SET ON-CELLS EXIST FLAG
.NOON    ANOP
&LIBASE  SETC  '&BASE'            SAVE BASE REGISTER NAME
         L     &BASE,=A(&LISECT)  SET UP BASE REGISTER
         AIF   ('&BASE' EQ 'R11' OR '&BASE' EQ '11').B11
         DROP  R11
.B11     USING &LISECT,&BASE      ESTABLISH REAL BASE
         AIF   ('&PARM' EQ '&PRMR').PARMOK
         LR    &PARM,&PRMR        SET UP PROPER PARM REGISTER
.PARMOK  AIF   ('&ENV' EQ '').NOENV
         AIF   ('&ENV' EQ 'R5' OR '&ENV' EQ '5').ENV5
         LR    &ENV,R5            SET UP ENVIRONMENT POINTER
.ENV5    AIF   ('&LIDSA' EQ '*').NOENV
         USING &LIDSA,&ENV        ADDRESS CONTAINING DSA
.NOENV   SPACE
         MEND
