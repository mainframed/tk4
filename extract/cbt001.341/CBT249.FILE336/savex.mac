         MACRO
&LABEL   SAVEX &BASE=12,&REGS=(14,12),&ID=*,&LV=72,&CSECT=YES,         X
               &SP=,                                           *MW73011X
               &PARM=NO,&TMS=YES,&XID=,                        *FG75189X
               &BANNER=,                                       *FG76267X
               &PARMR=1,                                       *AB76329X
               &CHAIN=YES,&REGSTRS=YES,&SA=
.* 1) ADDED SP= OPERAND TO ALLOW SUB-POOL TO BE SPECIFIED      *MW73011
.*    FOR GETMAIN                                              *MW73011
.* 2) ADDED SA=* TO ALLOW INTERNAL SAVE AREA                   *MW73011
.* 3) ADDED TMS (TIMESTAMP), PARM (SYSPARM), XID FOR           *FG75188
.*    IMPROVED MODULE DOCUMENTATION.                           *FG75188
.* 4) ADDED PARMR FOR SAVING PARM IN REGISTER OTHER THAN 1.    *AB76329
         GBLC  &SVXSIZE                                        *MW74138
         GBLC  &SVXSP                                          *MW74138
         GBLB  &SVXREG
         LCLA  &A,&B,&C,&D,&E,&F,&G,&H,&I,&J,&K,&Z             *FG75189
         LCLC  &TTIME,&TDATE,&ATIME                            *FG76267
         LCLC  &L(5)                 /* MOVED */               *FG76267
         LCLB  &AB                   /* MOVED */               *FG76267
         LCLC  &PR                                             *AB76329
         LCLC  &TB                                             *AB78004
&TDATE   SETC  '&SYSDATE'                                      *FG76267
&TTIME   SETC  '&SYSTIME'                                      *FG76267
&ATIME   SETC  '&TTIME'
         AIF   ('&BANNER' NE 'YES').SAVEWAZ    @               *FG76267
         AIF   ('&LABEL' EQ '').SAVEWAA                        *FG76267
         BANNER &LABEL                  ,                      *FG76267
.SAVEWAA AIF   ('&TMS' NE 'YES').SAVEWAB                       *FG76267
         BANNER &TDATE                  ,                      *FG76267
         BANNER &ATIME                                     ,   *FG76267
.SAVEWAB ANOP                                                  *FG76267
         EJECT                                                 *FG76267
.SAVEWAZ ANOP                                                  *FG76267
&AB      SETB  1
         AIF   ('&SA' EQ '').SAVEXAA
&SVXSIZE SETC  '0'                                             *MW74138
         AGO   .SAVEXBA
.SAVEXAA ANOP
&SVXSIZE SETC  '&LV'                                           *MW74138
.SAVEXBA ANOP
         AIF   (N'&REGS GT 2 OR '&REGS' EQ '').SAVEXCA
         AIF   (T'&REGS(1) EQ 'N').SAVEXDA
.SAVEXCA ANOP
&AB      SETB  0
         MNOTE 4,'*** INVALID REGS PARAMETER ***'
.SAVEXDA ANOP
         AIF   ('&LV' NE '' OR '&SA' NE '').SAVEXEA
&AB      SETB  0
         MNOTE 4,'*** EITHER LV OR SA PARAMETER MUST BE SPECIFIED ***'
.SAVEXEA ANOP
         AIF   ('&ID' NE '' OR '&LABEL' NE '' OR '&SYSECT' NE '').SAVEXX
               FA
&AB      SETB  0
         MNOTE 4,'*** INVALID ID PARAMETER ***'
.SAVEXFA ANOP
         AIF   ('&CSECT'(1,1) EQ 'N' OR '&LABEL' NE '').SAVEXGA
&AB      SETB  0
         MNOTE 4,'*** LABEL MUST BE SUPPLIED IF CSECT WANTED ***'
.SAVEXGA ANOP
         AIF   (&AB).SAVEXHA
&SVXSIZE SETC  '0'                                             *MW74138
         MEXIT
.SAVEXHA ANOP
&A       SETA  4*(N'&BASE-1)
         AIF   (&A GE 0).SAVEXIA
&A       SETA  0
.SAVEXIA ANOP
         AIF   ('&ID' EQ '').SAVEXMA
         AIF   ('&ID' EQ '*').SAVEXJA
&B       SETA  K'&ID
         AGO   .SAVEXNA
.SAVEXJA ANOP
         AIF   ('&LABEL' EQ '').SAVEXKA
&B       SETA  K'&LABEL
         AGO   .SAVEXNA
.SAVEXKA ANOP
&B       SETA  0
.SAVEXLA ANOP
&B       SETA  &B+1
         AIF   ('&SYSECT'(1,&B) NE '&SYSECT').SAVEXLA
         AGO   .SAVEXNA
.SAVEXMA ANOP
&B       SETA  0
.SAVEXNA ANOP
         AIF   (&B EQ 0).SAVEXND                               *FG75188
         AIF   ('&TMS'(1,1) NE 'Y').SAVEXNB                    *FG75188
&B       SETA  &B+2+K'&TDATE+K'&TTIME                          *FG75188
.SAVEXNB AIF   ('&XID' EQ '').SAVEXNC                          *FG75188
&B       SETA  &B+K'&XID+1                                     *FG75188
         AIF   ('&XID'(1,1) NE '''').SAVEXNC                   *FG75189
&B       SETA  &B-2                                            *FG75189
.SAVEXNC AIF   ('&PARM'(1,1) NE 'Y').SAVEXND                   *FG75189
&B       SETA  &B+K'&SYSPARM+1                                 *FG75189
.SAVEXND AIF   (&A EQ 0 AND &B EQ 0).SAVEXPA                   *FG75190
         AIF   (&A EQ 0).SAVEXOA
&C       SETA  (&B+4)/4*4-&B-1
         AGO   .SAVEXQA
.SAVEXOA ANOP
&C       SETA  (&B+2)/2*2-&B-1
         AGO   .SAVEXQA
.SAVEXPA ANOP
&C       SETA  0
&D       SETA  0
&E       SETA  0
         AGO   .SAVEXRA
.SAVEXQA ANOP
&D       SETA  &B                                              *AB78305
&E       SETA  &A+&D+&C+5                                      *AB78305
.SAVEXRA ANOP
         AIF   ('&CSECT'(1,1) NE 'N').SAVEXSA
         CNOP  0,4                SAVEX STARTS ON A FULLWORD BOUNDARY
         AIF   ('&LABEL' EQ '').SAVEXTA
&LABEL   DS    0F                 DEFINE LABEL
         AGO   .SAVEXTA
.SAVEXSA ANOP
&LABEL   CSECT                    ESTABLISH CONTROL SECTION
.SAVEXTA ANOP
         USING *,15               REGISTER 15 IS BASE AT ENTRY
         AIF   (&E EQ 0).SAVEXYA
         B     *+&E               BRANCH AROUND CONSTANTS
         DC    AL1(&D)            LENGTH OF CONSTANTS
         AIF   (&B EQ 0).SAVEXWA
         AIF   ('&ID' NE '*').SAVEXVA
         AIF   ('&LABEL' EQ '').SAVEXUA
         DC    C'&LABEL' IDENTIFIER
         AGO   .SAVEXWA
.SAVEXUA ANOP
         DC    C'&SYSECT' IDENTIFIER
         AGO   .SAVEXWA
.SAVEXVA ANOP
         DC    C'&ID' IDENTIFIER
.SAVEXWA AIF   ('&TMS'(1,1) NE 'Y').SAVEXWB                    *FG75189
         DC    C'-&TDATE.-&TTIME' TIME AND DATE OF ASSEMBLY
.SAVEXWB AIF   ('&XID' EQ '').SAVEXWC                          *FG75189
         DC    C'-'
         AIF   ('&XID'(1,1) EQ '''').SAVEXWZ                   *FG75189
         DC    C'&XID' EXTENDED IDENTIFIER
         AGO   .SAVEXWC                                        *FG75189
.SAVEXWZ DC    C&XID EXTENDED IDENTIFIER
.SAVEXWC AIF   ('&PARM'(1,1) NE 'Y').SAVEXWD                   *FG75189
         DC    C'-&SYSPARM' SYSPARM SUPPLIED TO ASSEMBLER
.SAVEXWD AIF   (&A EQ 0).SAVEXYA                               *FG75189
&F       SETA  0-(&B+&C+1)
&G       SETA  N'&BASE
&H       SETA  2
.SAVEXXA ANOP
&F       SETA  &F+4092
         DC    A(*+&F) BASE ADDRESS CONSTANT
&H       SETA  &H+1
         AIF   (&H LE &G).SAVEXXA
.SAVEXYA ANOP
&H       SETA  &REGS(1)*4+20
         AIF   (&H LE 75).SAVEXZA
&H       SETA  &H-64
.SAVEXZA ANOP
         AIF   (N'&REGS NE 2).SAVEX0A
         STM   &REGS(1),&REGS(2),&H.(13)       SAVE REGISTERS
         AGO   .SAVEX1A
.SAVEX0A ANOP
         ST    &REGS(1),&H.(,13)         SAVE REGISTER
.SAVEX1A ANOP
         AIF   ('&BASE' EQ '').SAVEX9A
         AIF   ('&BASE(1)' EQ '15' AND &A EQ 0).SAVEX9A        *AB78004
         DROP  15
&K       SETA  &E+4
         AIF   (&A NE 0).SAVEX2A
         USING *-&K,&BASE(1)            ESTABLISH BASE REGISTER
         LR    &BASE(1),15
         AGO   .SAVEX9A
.SAVEX2A ANOP
&H       SETA  2
&I       SETA  K'&BASE-2
&J       SETA  1
.SAVEX3A ANOP
         AIF   (&I LT 8).SAVEX4A
&L(&J)   SETC  '&BASE'(&H,8)
&H       SETA  &H+8
&I       SETA  &I-8
&J       SETA  &J+1
         AGO   .SAVEX3A
.SAVEX4A ANOP
         AIF   (&I EQ 0).SAVEX5A
&L(&J)   SETC  '&BASE'(&H,&I)
.SAVEX5A ANOP
         USING *-&K,&L(1).&L(2).&L(3).&L(4).&L(5) ESTABLISH BASE REGISTX
               ERS
         AIF   ('&BASE(1)' EQ '15').SAVEX5B                    *AB78004
         LR    &BASE(1),15
.SAVEX5B ANOP
&H       SETA  2
&I       SETA  N'&BASE
&H       SETA  &A+4                                            *AB78004
         AIF   ('&BASE(1)' EQ '15').SAVEX5C                    *AB78004
&H       SETA  &A+6                                            *AB78004
.SAVEX5C ANOP  ,                                               *AB78004
&J       SETA  2
         AIF   (&I EQ 2).SAVEX7A                               *AB78199
&TB      SETC  T'&BASE(2)                                      *AB78004
         AIF   ('&TB' NE 'N').SAVEX7A                          *AB78004
.SAVEX6A ANOP
&G       SETA  &J                                              *AB78004
.SAVEX6C ANOP  ,                                               *AB78004
         AIF   (&J GE &I).SAVEX8A                              *AB78004
&TB      SETC  T'&BASE(&J+1)                                   *AB78004
         AIF   ('&TB' NE 'N').SAVEX8A                          *AB78004
         AIF   (&BASE(&J+1) NE &BASE(&J)+1).SAVEX8A            *AB78004
&J       SETA  &J+1                                            *AB78004
         AGO   .SAVEX6C                                        *AB78004
.SAVEX7A ANOP
         L     &BASE(&J),*-&H                                  *AB78004
&J       SETA  &J+1                                            *AB78004
         AIF   (&J GT &I).SAVEX9A                              *AB78004
&TB      SETC  T'&BASE(&J)                                     *AB78004
         AIF   ('&TB' NE 'N').SAVEX7A                          *AB78004
         AGO   .SAVEX6A                                        *AB78004
.SAVEX8A ANOP
         AIF   (&G EQ &J).SAVEX7A                              *AB78004
         LM    &BASE(&G),&BASE(&J),*-&H                        *AB78004
&H       SETA  &H-(&J-&G)*4                                    *AB78004
&J       SETA  &J+1                                            *AB78004
         AIF   (&J LE &I).SAVEX6A                              *AB78004
.SAVEX9A ANOP
         AIF   ('&CHAIN'(1,1) NE 'N').SAVEXZZ
&SVXSIZE SETC  '-1'                                            *MW74138
         AIF   ('&PARMR' EQ '' OR  '&PARMR' EQ '1').SAVEXZY    *AB76329
.SAVEXZZ ANOP
         AIF   ('&PARMR' EQ '').SAVEXP0                        *AB76329
&PR      SETC  '2'                                             *AB76329
         AIF   ('&PARMR' EQ '1').SAVEXP1                       *AB76329
&PR      SETC  '&PARMR'                                        *AB76329
.SAVEXP1 LR    &PR,1              SAVE PARAMETER REGISTER      *AB76329
         AIF   ('&CHAIN'(1,1) EQ 'N').SAVEXZY                  *AB76329
.SAVEXP0 ANOP  ,                                               *AB76329
         AIF   ('&SA' EQ '').SAVEXYZ
.* CHECK FOR INTERNAL SAVE AREA                                *MW73011
         AIF   ('&SA' NE '*').SAVEXYY                          *MW73011
         CNOP  0,4 .                                           *MW73011
         BAL   1,*+76 .                                        *MW73011
         DS    18F .                                           *MW73011
         AGO   .SAVEXXZ                                        *MW73011
.SAVEXYY ANOP                                                  *MW73011
         LA    1,&SA          GET L(SAVE AREA)
         AGO   .SAVEXXZ
.SAVEXYZ ANOP
&SVXSP   SETC  '0'                                             *MW74138
         AIF   ('&SP' EQ '').SAVEXXX                           *MW73011
&SVXSP   SETC  '&SP'                                           *MW74138
         GETMAIN R,LV=&LV,SP=&SP .                             *MW73011
         AGO   .SAVEXXZ                                        *MW73011
.SAVEXXX ANOP                                                  *MW73011
         GETMAIN R,LV=&LV
.SAVEXXZ ANOP
         ST    1,8(,13)           SATISFY STANDARD LINKAGE CONVENTIONS
         ST    13,4(,1)             BY CHAINING SAVE AREAS BOTH
         LR    13,1                 BACKWARD AND FOREWARD
         AIF   ('&PARMR' NE '1').SAVEXZY                       *AB76329
         LR    1,&PR              RESTORE PARM ADDR TO REG 1   *AB76329
.SAVEXZY ANOP
         AIF   ('&REGSTRS'(1,1) EQ 'N').SAVEX9B
         AIF   (&SVXREG).SAVEX9B
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
RA       EQU   10
RB       EQU   11
RC       EQU   12
RD       EQU   13
RE       EQU   14
RF       EQU   15
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
FR0      EQU   0
FR2      EQU   2
FR4      EQU   4
FR6      EQU   6
.SAVEX9B ANOP
&SVXREG  SETB  1
         MEND
