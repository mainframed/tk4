         MACRO
&LABEL   UHBMOD &TO,&FROM,&LEN,&REG=14,&COM=OPERAND,&SUB=,&MF=
.*       THIS MACRO IS CALLED WITH A SOURCE ADDRESS, A DESTINATION
.*       ADDRESS, A LENGTH AND A MACRO FORM TO GENERATE CODE TO STORE
.*       A VALUE INTO A PARAMETER LIST.
         GBLB  &UHBM1,&UHBIM
         GBLB  &UHBSM(5)
         GBLC  &UHBML
         GBLC  &UHBOGL,&UHBSL(5)
         LCLC  &R,&L,&P,&S,&XCOM
&S       SETC  ' .      '
         AIF   (&UHBIM OR ('&MF(1)' EQ 'L' AND '&MF(2)' EQ '')).MEND
         AIF   (&LEN LE 2).NADDR
&XCOM    SETC  '.ADDRESS'
.NADDR ANOP
&L       SETC  '&LABEL'
         AIF   ('&FROM' EQ '').EXIT
         AIF   ('&FROM' EQ '*' AND '&SUB' EQ '').EXIT
         AIF   (NOT &UHBM1).L0
&UHBM1   SETB  (0)
         AIF   ('&UHBML' EQ '').L0
         AIF   ('&L' NE '').L2
&L       SETC  '&UHBML'
         AGO   .L0
.L2      ANOP
&UHBML   DS    0H
.L0      AIF   ('&FROM' EQ '*').STAR
         AIF   ('&FROM'(1,1) EQ '(').RFORM
         AIF   (K'&FROM LT 3).NORML
         AIF   ('&FROM'(2,1) NE ':').NORML
         AIF   ('&FROM'(1,1) EQ '*').STFORM
         AIF   ('&FROM'(1,1) EQ 'A' AND ('&MF(1)' NE 'M' AND '&MF(1)'  *
               NE 'E')).EXIT
&L       UHBADDR  &REG,&FROM,COM=LOAD.&COM&XCOM,MF=&MF
         AGO   .NRFORM
.NORML   AIF   ('&MF(1)' NE 'M' AND '&MF(1)' NE 'E').EXIT
         AIF   ('&LEN' EQ '1').MVI
&L       LA    &REG,&FROM&S.LOAD.&COM&XCOM
.NRFORM  ANOP
&L       SETC  ''
&R       SETC  '&REG'
         AGO   .STORE
.RFORM   ANOP
&R       SETC  '&FROM'(2,K'&FROM-2)
.STORE   AIF   ('&LEN' EQ '1').STC
         AIF   ('&LEN' EQ '2').STH
         AIF   ('&LEN' EQ '3').STCM
&L       ST    &R,&TO&S.STORE.&COM&XCOM
         MEXIT
.STCM    ANOP
&L       STCM  &R,B'0111',&TO&S.STORE.&COM&XCOM
         MEXIT
.STH     ANOP
&L       STH   &R,&TO&S.STORE.&COM
         MEXIT
.STC     ANOP
&L       STC   &R,&TO&S.STORE.&COM
         MEXIT
.MVI     ANOP
&L       MVI   &TO,&FROM&S.INSERT.&COM
         MEXIT
.STFORM  UHBCOLON &FROM,&LEN,S,MF=&MF
         AIF   (&LEN NE 3).OKLEN
&P       SETC  '1+'
.OKLEN   ANOP
&L       UHBMOVE &TO,&P,&LEN,COPY.&COM&XCOM
         MEXIT
.STAR    AIF   ('&MF(1)' EQ 'L' OR '&MF(1)' EQ 'G').SUBMV
         AIF   ('&MF(1)' NE 'I').SUBLD
         AIF   (NOT &UHBSM(&SUB)).EXIT
&L       UHBADDR &REG,&UHBSL(&SUB),COM=ADDRESS.&COM
         AGO   .MEND
.SUBLD   AIF   ('&LEN' EQ '3').ICM
&L       L     &REG,&TO&S.LOAD.&COM..ADDRESS
         MEXIT
.ICM     ANOP
&L       ICM   &REG,B'0111',&TO&S.LOAD.&COM..ADDRESS
         MEXIT
.SUBMV   ANOP
&L       UHBADDR &REG,&UHBSL(&SUB)-&UHBOGL.(1),COM=ADDRESS.&COM..COPY
         AGO   .NRFORM
.EXIT    AIF   ('&LABEL' NE '').GLAB
         AIF   ('&L' EQ '').MEND
&UHBM1   SETB  (1)
         MEXIT
.GLAB    ANOP
&LABEL   DS    0H
.MEND    MEND
