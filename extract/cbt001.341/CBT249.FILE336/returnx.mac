         MACRO
&LABEL   RETURNX &REGS=(14,12),&RC=0
         GBLC  &SVXSIZE
         GBLC  &SVXSP
         LCLA  &A,&I,&M,&N,&BEGIN,&END
         LCLB  &B,&RG(16)
&B       SETB  1
         AIF   (N'&REGS GE 3).RETURNA
         AIF   ('&REGS' EQ '').RETURNB
         AIF   (T'&REGS(1) EQ 'N').RETURNB
.RETURNA ANOP
&B       SETB  0
         MNOTE 4,'*** INVALID REGS PARAMETER ***'
.RETURNB ANOP
         AIF   (&B).RETURND
         MEXIT
.RETURND ANOP
         AIF   ('&RC'(1,1) NE '(').ENDL1
&N       SETA  N'&RC
&I       SETA  1
.LOOP1   AIF   (&I GT &N).ENDL1
         AIF   (T'&RC(&I) NE 'N').RCERROR
         AIF   ('&RC(&I)' EQ '13').RCERROR
&A       SETA  &RC(&I)
&RG(&A+1) SETB 1
.K4      ANOP
&I       SETA  &I+1
         AGO   .LOOP1
.RCERROR MNOTE 4,'*** INVALID RC ELEMENT - IGNORED ***'
         AGO   .K4
.ENDL1   ANOP
&LABEL   DS    0H
         AIF   ('&SVXSIZE' EQ '-1').K1
         AIF   ('&SVXSIZE' EQ '0').K2
         LR    14,13                                           *AB81125
.K2      L     13,4(,13)
.K1      ANOP
.* HANDLE RC STORES INTO OLD SAVE AREA *.
&I       SETA  15
&N       SETA  16
&BEGIN   SETA  0-1
&END     SETA  0-1
.LOOP2   AIF   (&I GT &N).ENDL2A
&M       SETA  &I-1
         AIF   (&RG(&I)).ADDR1
.GENR1   AIF   (&BEGIN EQ 0-1).NEXTL2
.K12     AIF   (&BEGIN EQ &END).GENR2
.GENR3   ANOP
         STM   &BEGIN,&END,&A.(13) .      STORE RETURN CODES INTO SA
         AGO   .GENR4
.GENR2   ANOP
         ST    &BEGIN,&A.(,13) .          STORE RETURN CODE INTO SA
.GENR4   ANOP
&BEGIN   SETA  0-1
&END     SETA  0-1
         AGO   .NEXTL2
.ADDR1   AIF   (&BEGIN NE 0-1).ADDR2
&BEGIN   SETA  &M
&END     SETA  &M
&A       SETA  &M*4+20
         AIF   (&A LE 75).NEXTL2
&A       SETA  &A-64
         AGO   .NEXTL2
.ADDR2   ANOP
&END     SETA  &M
.NEXTL2  ANOP
&I       SETA  &I+1
         AGO   .LOOP2
.ENDL2A  AIF   (&I LT 16).ENDL2B
&I       SETA  1
&N       SETA  12
         AGO   .LOOP2
.ENDL2B  AIF   (&BEGIN NE 0-1).K12
         AIF   ('&SVXSIZE' EQ '-1').K3
         AIF   ('&SVXSIZE' EQ '0').K3
         FREEMAIN R,LV=&SVXSIZE,A=(14),SP=&SVXSP               *AB81125
.K3      ANOP
         AIF   ('&REGS' EQ '').RETURNL
&A       SETA  &REGS(1)*4+20
         AIF   (&A LE 75).RETURNG
&A       SETA  &A-64
.RETURNG ANOP
         AIF   (N'&REGS EQ 1).RETURNK
         AIF   (&RG(16)).RETURNZ
         AGO   .RETURNJ
.RETURNZ AIF   (&REGS(1) NE 14).RETURNH
         AIF   (&REGS(2) NE 14).RETURNJ
         LM    14,15,12(13) .            RESTORE REGS AND RC
         AGO   .RETURNL
.RETURNH AIF   (&REGS(1) EQ 15).RETURNJ
         AIF   (&REGS(1) NE 0).RETURNI
         LM    15,&REGS(2),16(13) .      RESTORE REGS AND RC
         AGO   .RETURNL
.RETURNI ANOP
         L     15,16(,13) .              RESTORE RC
.RETURNJ ANOP
         LM    &REGS(1),&REGS(2),&A.(13) . RESTORE REGISTERS
         AGO   .RETURNL
.RETURNK ANOP
         L     &REGS(1),&A.(,13) .       RESTORE REGISTER
.RETURNL ANOP
         MVI   12(13),X'FF' .  SET RETURN INDICATOR
         AIF   ('&RC' EQ '').RETURNN
         AIF   ('&RC'(1,1) EQ '(').RETURNN
         AIF   (&RC EQ 0).RETURNM
         LA    15,&RC .                  GET RETURN CODE
         AGO   .RETURNN
.RETURNM ANOP
         SLR   15,15 .                   GET RETURN CODE
.RETURNN ANOP
         BR    14 .                      RETURN TO CALLER
         MEND
