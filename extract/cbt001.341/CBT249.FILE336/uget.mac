         MACRO
&NAME    UGET  &BFF,&SIZE,&EDIT,&WAIT,&CLIST,&CPPL=,&IOPL=,&MF=I,      *
               &PROMPT=                                        RTS02A1
         LCLA  &OPT,&GOPT                                      RTS02A1
         LCLA  &RET
         LCLB  &E,&W,&G
         LCLC  &PARM,&S,&L,&CPIOPL
&S       SETC  '&SYSNDX'                                       RTS02A1
&L       SETC  'L'''                                           RTS02A1
         AIF   ('&MF' EQ 'L').MFL                              RTS02A1
         AIF   ('&MF(1)' EQ 'E').MFE                           RTS02A1
         AIF   ('&MF(1)' EQ 'I' OR '&MF' EQ '').MFI            RTS02A1
         IHBERMAC 35,0,&MF                                     RTS02A1
         MEXIT                                                 RTS02A1
.MFI     ANOP                                                  RTS02A1
         AIF   ('&BFF' EQ '*' OR '&BFF' EQ '').ERROR1
         AIF   ('&BFF'(1,1) EQ '(' AND ('&SIZE' EQ '' OR '&SIZE' EQ '*'*
               )).ERROR1                                       RTS02A1
.MFE     AIF   ('&CPPL' EQ '' AND '&IOPL' EQ '').ERROR1        RTS02A1
         AIF   ('&CPPL' NE '' AND '&IOPL' NE '').ERROR2        RTS02A1
&CPIOPL  SETC  '&CPPL&IOPL'                                    RTS02A1
&PARM    SETC  '&EDIT'
.CKPARM  ANOP
&RET     SETA  &RET+1
         AIF   ('&PARM' EQ '').RET           NULL, TRY NEXT
         AIF   ('&PARM' EQ 'EDIT').EDI       SET EDIT
         AIF   ('&PARM' EQ 'ASIS').ASI       SET ASIS
         AIF   ('&PARM' EQ 'WAIT').WAI       SET WAIT
         AIF   ('&PARM' EQ 'NOWAIT').NOW     SET NOWAIT
         AIF   ('&PARM' EQ 'TERM').TERM                        RTS02A1
         AIF   ('&PARM' EQ 'CLIST').CLIST                      RTS02A1
         AGO   .ERROR3
.*
.*  CHECK REGISTER NOTATION IS USED
.*
.CHKREG  SPACE 1
         AIF   ('&MF' EQ 'L').GENLIST                          RTS02A1
         AIF   ('&MF' NE 'I' AND '&MF' NE '').GENE             RTS02A1
.EXPAND  CNOP  0,4
         AIF   ('&NAME' EQ '').NONAME
&NAME    EQU   *                                               RTS02A1
.NONAME  AIF   ('&SIZE' EQ '' OR '&SIZE' EQ '*').NOSIZER       RTS02A1
         AIF   ('&SIZE'(1,1) NE '(').NOSIZER                   RTS02A1
         STH   &SIZE(1),UGT&S+2   STORE MESSAGE LENGTH         RTS02A1
.NOSIZER AIF   ('&BFF'(1,1) NE '(').NOBFFR                     RTS02A1
         STCM  &BFF(1),7,UGT&S+5  STORE BUFFER ADDRESS         RTS02A1
.NOBFFR  ANOP                                                  RTS02A1
         AIF   ('&PROMPT' EQ '').NOPRMP                        RTS02A1
         AIF   ('&MF' EQ 'I' OR '&MF' EQ '').NOPRMP            RTS02A1
         AIF   ('&PROMPT'(1,1) EQ '(').NOPRMP                  RTS02A1
         LA    14,4&PROMPT   ADDR TRUE START OF PROMPT PARMS   RTS02A1
         ST    14,UGT&S+8         STORE PROMPT LIST ADDR       RTS02A1
.NOPRMP  ANOP                                                  RTS02A1
         BAL   1,UGE&S            BRANCH AROUND CONSTANTS      RTS02A1
UGT&S    EQU   *                                               RTS02A1
         AGO   .GENI                                           RTS02A1
.GENLIST ANOP                                                  RTS02A1
&NAME    DS    0A                                              RTS02A1
.GENI    DC    AL2(&GOPT)               GETLINE OPTIONS        RTS02A1
         AIF   ('&BFF' EQ '' OR '&BFF' EQ '*').DUMSIZE         RTS02A1
         AIF   ('&SIZE' NE '' AND '&SIZE' NE '*').GOTSIZE      RTS02A1
         DC    AL2(&L&BFF)        BUFFER LENGTH                RTS02A1
         AGO   .JSIZE                                          RTS02A1
.GOTSIZE AIF   ('&SIZE'(1,1) NE '(').OKSIZE                    RTS02A1
.DUMSIZE DC    AL2(0)             BUFFER SIZE                  RTS02A1
         AGO   .JSIZE                                          RTS02A1
.OKSIZE  ANOP                                                  RTS02A1
         DC    AL2(&SIZE)               BUFFER SIZE
.JSIZE   DC    AL1(&OPT+X'80')          OPTIONS                RTS02A1
         AIF   ('&BFF' EQ '' OR '&BFF' EQ '*').DUMBFF          RTS02A1
         AIF   ('&BFF'(1,1) NE '(').OKBFF                      RTS02A1
.DUMBFF  DC    AL3(0)             BUFFER ADDR                  RTS02A1
         AGO   .JBFF                                           RTS02A1
.OKBFF   ANOP                                                  RTS02A1
         DC    AL3(&BFF)                BUFFER ADDR
.JBFF    AIF   ('&PROMPT' EQ '').DUMPRMT                       RTS02A1
         AIF   ('&PROMPT'(1,1) EQ '(').DUMPRMT                 RTS02A1
         DC    AL4(4+&PROMPT)     ADDR OF PROMPT PARMS         RTS02A1
         AGO   .JPRMPT                                         RTS02A1
.DUMPRMT DC    AL4(0)             PROMPT PARM LIST             RTS02A1
.JPRMPT  ANOP                                                  RTS02A1
         AIF   ('&MF' EQ 'L').GEND                             RTS02A1
UGE&S    EQU   *                                               RTS02A1
.LDCPPL  AIF   ('&CPIOPL' EQ '(0)').CPPL0                      RTS02A1
         AIF   ('&CPIOPL'(1,1) EQ '(').CPPLR                   RTS02A1
         LA    0,&CPIOPL        LOAD CPPL/IOPL ADDRESS         RTS02A1
         AGO   .CPPL0                                          RTS02A1
.CPPLR   LA    0,0&CPIOPL     LOAD CPPL/IOPL ADDRESS           RTS02A1
         AIF   ('&IOPL' NE '').IOPLFF                          RTS02A1
         AGO   .CPPLO                                          RTS02A1
.CPPL0   AIF   ('&IOPL' NE '').IOPLFF                          RTS02A1
         ICM   0,8,=X'00'         INDICATE CPPL PASSED         RTS02A1
         AGO   .CPPLO                                          RTS02A1
.IOPLFF  ICM   0,8,=X'FF'         INDICATE IOPL PASSED         RTS02A1
.CPPLO   ANOP                                                  RTS02A1
         L     15,=V(UKJUGET)                                  RTS02A1
         BALR  14,15              CALL UGET ROUTINE            RTS02A1
.GEND    SPACE 1
         MEXIT
.EDI     ANOP
         AIF   (&E).ERROR2              DUP OPTION
&E       SETB  1                        EDIT OPTION SPECFIED
         AGO   .RET
.ASI     ANOP
         AIF   (&E).ERROR2              DUP OPTION
&E       SETB  1                        EDIT OPTION SPECFIED
         MNOTE 0,'UGET001 WARNING -- EFFECTS OF ASIS OPTION ARE DEVICE *
               DEPENDENT'                                      RTS02A1
&OPT     SETA  &OPT+1                   SET EDIT=ASIS
         AGO   .RET
.WAI     ANOP
         AIF   (&W).ERROR2              DUP OPTION
&W       SETB  1                        WAIT OPTION SPECIFIED
         AGO   .RET
.NOW     ANOP
         AIF   (&W).ERROR2              DUP OPTION
&W       SETB  1                        WAIT OPTION SPECIFIED
&OPT     SETA  &OPT+X'10'               SET WAIT=NOWAIT
         AGO   .RET
.CLIST   ANOP                                                  RTS02A1
         AIF   (&G).ERROR2              DUP OPTION             RTS02A1
&G       SETB  1                  GETLINE OPTION SPECIFIED     RTS02A1
&GOPT    SETA  X'0080'            SET CLIST INPUT              RTS02A1
         AGO   .RET
.TERM    ANOP                                                  RTS02A1
         AIF   (&G).ERROR2              DUP OPTION             RTS02A1
&G       SETB  1                  GETLINE OPTION SPECIFIED     RTS02A1
&GOPT    SETA  X'1000'            SET TERMINAL INPUT           RTS02A1
.RET     ANOP
&PARM    SETC  '&WAIT'
         AIF   ('&RET' EQ '1').CKPARM   CHECK WAIT
&PARM    SETC  '&CLIST'
         AIF   ('&RET' EQ '2').CKPARM   CHECK CLIST            RTS02A1
         AGO   .CHKREG                  DONE WITH OPTIONS
.MFL     ANOP                                                  RTS02A1
         AIF   ('&BFF' EQ '').BFFOK                            RTS02A1
         AIF   ('&BFF'(1,1) EQ '(').LREG                       RTS02A1
.BFFOK   AIF   ('&SIZE' EQ '').SIZEOK                          RTS02A1
         AIF   ('&SIZE'(1,1) EQ '(').LREG                      RTS02A1
.SIZEOK  AIF   ('&PROMPT' EQ '').PRMPTOK                       RTS02A1
         AIF   ('&PROMPT'(1,1) NE '(').PRMPTOK                 RTS02A1
.LREG    IHBERMAC 69                                           RTS02A1
         MEXIT                                                 RTS02A1
.PRMPTOK AIF   ('&CPPL' EQ '' AND '&IOPL' EQ '').NOCPPL        RTS02A1
         MNOTE 4,'UGET002 CPPL/IOPL OPERAND IGNORED WHEN MF=L' RTS02A1
.NOCPPL  ANOP                                                  RTS02A1
&PARM    SETC  '&EDIT'                                         RTS02A1
         AGO   .CKPARM                                         RTS02A1
.GENE    ANOP                                                  RTS02A1
&NAME    DS    0H                                              RTS02A1
         AIF   ('&BFF' NE '(1)').NER1                          RTS02A1
         LR    15,1               COPY MSG ADDR TO 15          RTS02A1
.NER1    IHBINNRA &MF(2)                                       RTS02A1
.JEX     AIF   (&GOPT EQ 0).ENOGOPT                            RTS02A1
         LH    14,=AL2(&GOPT)     LOAD GETLINE OPTIONS         RTS02A1
         STH   14,0(1)            STORE GETLINE OPTIONS        RTS02A1
.ENOGOPT AIF   ('&SIZE' EQ '').ENOSIZ                          RTS02A1
         AIF   ('&SIZE' EQ '*').STARSIZ                        RTS02A1
         AIF   ('&SIZE'(1,1) EQ '(').RSIZ                      RTS02A1
         LA    14,&SIZE                                        RTS02A1
         STH   14,2(1)            STORE MSG LENGTH IN PARM LIST RTS02A1
         AGO   .ENOSIZ                                         RTS02A1
.RSIZ    STH   &SIZE(1),2(1)      STORE MSG LENGTH IN PARM LIST RTS02A1
         AGO   .ENOSIZ                                         RTS02A1
.STARSIZ AIF   ('&BFF' EQ '*' OR '&BFF' EQ '').ENOSIZ          RTS02A1
         AIF   ('&BFF'(1,1) EQ '(').ENOSIZ                     RTS02A1
         LA    14,&L&BFF          GET LENGTH OF MESSAGE        RTS02A1
         STH   14,2(1)            STORE IN PARM LIST           RTS02A1
.ENOSIZ  AIF   (NOT (&E OR &W)).ENOPT                          RTS02A1
         MVI   4(1),&OPT+X'80'    STORE TGET OPTIONS           RTS02A1
.ENOPT   AIF   ('&BFF' EQ '' OR '&BFF' EQ '*').ENOBFF          RTS02A1
         AIF   ('&BFF'(1,1) EQ '(').ERBFF                      RTS02A1
         LA    14,&BFF                                         RTS02A1
         STCM  14,7,5(1)     STORE BUFFER ADDR IN PARM LIST    RTS02A1
         AGO   .ENOBFF                                         RTS02A1
.ERBFF   AIF   ('&BFF' NE '(1)').ERN1                          RTS02A1
         STCM  15,7,5(1)     STORE MSG ADDRESS IN PARM LIST    RTS02A1
         AGO   .ENOBFF                                         RTS02A1
.ERN1    STCM  &BFF(1),7,5(1)   STORE MSG ADDRESS IN PARM LIST RTS02A1
.ENOBFF  AIF   ('&PROMPT' EQ '').LDCPPL                        RTS02A1
         AIF   ('&PROMPT'(1,1) EQ '(').ERPRMT                  RTS02A1
         LA    14,4+&PROMPT       ADDRESS PROMPT PARMS         RTS02A1
         ST    14,8(1)            STORE IN GET PARMS           RTS02A1
         AGO   .LDCPPL                                         RTS02A1
.ERPRMT  LA    14,4&PROMPT        ADDRESS PROMPT PARM LIST     RTS02A1
         ST    14,8(1)            STORE IN GET PARMS           RTS02A1
.RERR    IHBERMAC 192
         MEXIT
.ERROR1  IHBERMAC 24
         MEXIT
.ERROR2  IHBERMAC 54,,,
         MEXIT
.ERROR3  IHBERMAC 49,,&PARM
         MEND
