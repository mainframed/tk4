         MACRO
&LABEL   UHBGENV &VAL,&LEN,&COM=VALUE,&TYPE=,&RQR=,&NAME=,&DEF=,&MF=
.*       THIS MACRO IS CALLED WITH A VALUE, A LENGTH, A MACRO FORM, AND
.*       AN OPTIONAL TYPE, TO GENERATE A DATA VALUE, OR THE SPACE FOR
.*       ONE, IN A PARAMETER LIST.
         GBLA  &UHBSF
         GBLB  &UHBM,&UHBG1,&UHBIM
         GBLB  &UHBSM(5)
         GBLC  &UHBGL
         LCLA  &W
         LCLC  &S,&T,&L,&P
         AIF   (&UHBIM OR '&MF(1)' EQ 'E' OR '&MF(1)' EQ 'M').MEND
&S       SETC  ' .      '
         AIF   ('&RQR' NE 'Y').NORQR
         UHBRQR &VAL,&NAME,MF=&MF
.NORQR   ANOP
&L       SETC  '&LABEL'
         AIF   (NOT &UHBG1).L0
&UHBG1   SETB  (0)
         AIF   ('&UHBGL' EQ '').L0
&L       SETC  '&UHBGL'
.L0      AIF   ('&VAL' EQ '').NVAL
         AIF   ('&VAL'(1,1) EQ '''').TLIT
         AIF   (K'&VAL LE 2).NLIT
         AIF   ('&TYPE' EQ '' AND '&VAL'(K'&VAL,1) EQ '''').YLIT
.NLIT    AIF   ('&MF(1)' NE 'L' OR '&MF(2)' NE '').YVAL
         MNOTE 8,'UHB009 NON-LITERAL DATA &VAL NOT ALLOWED WITH MF=L'
         AGO   .NVAL
.YVAL    AIF   (&UHBSF NE 0).MSUB
&UHBM    SETB  (1)
         AGO   .NVAL
.MSUB    ANOP
&UHBSM(&UHBSF) SETB (1)
.NVAL    AIF   ('&DEF' NE '').DEF
&T       SETC  '&TYPE'
         AIF   ('&T' NE '').TYPOK
&T       SETC  'C'
.TYPOK   AIF   ('&T' EQ 'C' OR '&T' EQ 'X' OR '&T' EQ 'B').NALIGN
         AIF   ('&T' EQ 'Z' OR '&T' EQ 'P').DALIGN
&W       SETA  2
         AIF   ('&T' EQ 'H').ALIGN
&W       SETA  4
         AIF   ('&T' EQ 'F' OR '&T' EQ 'E').ALIGN
&W       SETA  8
         AIF   ('&T' EQ 'D').ALIGN
&W       SETA  16
.ALIGN   ANOP
&L       DS    ((&LEN+&W-1)/&W)&T&S.SPACE.FOR.&COM
         AGO   .L2
.NALIGN  ANOP
&L       DS    &T.L&LEN&S.SPACE.FOR.&COM
         AGO   .L2
.DALIGN  ANOP
&L       DS    &LEN&T&S.SPACE.FOR.&COM
         AGO   .L2
.TLIT    ANOP
&T       SETC  '&TYPE'
         AIF   ('&T' NE '').YLIT
&T       SETC  'C'
.YLIT    AIF   ('&LEN' EQ '').NOLEN
         AIF   ('&L' NE '').YLAB
&L       SETC  'UHB&SYSNDX.V'
.YLAB    ANOP
&L       DC    &T&VAL&S&COM
         AIF   (T'&LEN EQ 'N').NLEN
&LEN     EQU   *-&L&S.LENGTH.OF.&COM
         AGO   .L2
.NOLEN   ANOP
&L       DC    &T&VAL&S&COM
         AGO   .L2
.NLEN    AIF   ('&T' EQ 'C' OR '&VAL'(1,1) EQ 'C').CPAD
         DC    (&LEN+&L-*)X'00'   PAD TO FULL LENGTH
         AGO   .L2
.CPAD    DC    (&LEN+&L-*)C' '    PAD TO FULL LENGTH
.L2      AIF   ('&LABEL' EQ '' OR '&LABEL' EQ '&L').MEND
&LABEL   EQU   &L
         MEXIT
.DEF     AIF   ('&DEF'(1,1) EQ '''').QDEF
&L       DC    &DEF&S.DEFAULT.&COM
         MEXIT
.QDEF    AIF   ('&TYPE' NE '').TDEF
&L       DC    C&DEF&S.DEFAULT.&COM
         MEXIT
.TDEF    ANOP
&L       DC    &TYPE&DEF&S.DEFAULT.&COM
.MEND    MEND
