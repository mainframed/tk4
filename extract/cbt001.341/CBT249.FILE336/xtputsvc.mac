         TITLE 'SVC 238 -- EXTENDED TPUT FOR XSEND COMMAND'    RTS03A1
***********************************************************************
*        THIS SVC IS ISSUED BY THE XSEND COMMAND TO SEND MESSAGES     *
*        PUNCTUATED BY BELL CHARACTERS (ON AN ASCII TERMINAL) OR THE  *
*        SOUNDING OF A 3270 ALARM. IT IS A CALLER SPECIFIABLE OPTION  *
*        WHETHER THE BELLS/ALARM SHOULD BE SENT AT HIGH-PRIORITY      *
*        (THOUGH THE MESSAGE TEXT PROPER WILL NEVER BE SO SENT).      *
*                                                                     *
*        INPUT: TPUT PARAMETERS IN REGISTERS 0 & 1. ONLY A TPUT TO    *
*               ASID IS ACCEPTABLE. FOR THE TPUT OF BELLS/ALARM, HOLD *
*               AND BREAKIN ARE IGNORED. FOR THE TEXT TPUT, HIGHP     *
*               AND HOLD ARE IGNORED, AND CONTROL IS CHANGED TO ASIS. *
*                                                                     *
*        OUTPUT: SAME AS TPUT RETURN CODE.                            *
***********************************************************************
         SPACE
IGC238   CSECT ,                                               RTS03A1
         SPACE
         REGISTER ,                                            RTS03A1
         USING IGC238,R6          STANDARD SVC BASE REGISTER   RTS03A1
         USING CVTMAP,R3                                       RTS03A1
         LR    R5,R14             SAVE THE RETURN ADDRESS      RTS03A1
         LR    R9,R0              COPY THE LENGTH & ASID       RTS03A1
         LTR   R11,R1             COPY BUFFER ADDR & OPTIONS   RTS03A1
         BM    ERROR              ERROR IF TGET BIT SET        RTS03A1
         SR    R8,R8              GET ASID TO R8               RTS03A1
         SLDL  R8,16                                           RTS03A1
         LR    R9,R0              RESTORE R9 FROM R0           RTS03A1
         SR    R10,R10                                         RTS03A1
         SLDL  R10,8              ISOLATE OPTION BITS          RTS03A1
         LR    R11,R1             RESTORE ORIGINAL R11         RTS03A1
         N     R11,=X'D7FFFFFF'   TURN OFF HIGHP & HOLD        RTS03A1
         N     R10,=X'000000F0'   OFF HOLD/BREAK/EDIT FOR BELL RTS03A1
         L     R7,CVTASVT         FIND THE ASVT                RTS03A1
         USING ASVT,R7                                         RTS03A1
         LTR   R1,R8              PRESERVE ASID                RTS03A1
         BZ    ERROR              ASID=0 NOT ALLOWED           RTS03A1
         CL    R1,ASVTMAXU        IS IT TOO LARGE?             RTS03A1
         BH    ERROR              YES, GO NO FURTHER           RTS03A1
         SLL   R1,2               GET INDEX INTO ASVT          RTS03A1
         LA    R2,ASVTFRST(R1)    FIND THE ASCB                RTS03A1
         TM    0(R2),X'80'        IS IT ACTIVE?                RTS03A1
         BNZ   ERROR              NO, ERROR                    RTS03A1
         L     R7,0(,R2)          YES, LOAD ASCB POINTER       RTS03A1
         USING ASCB,R7                                         RTS03A1
         ICM   R7,7,ASCBTSB+1     FIND THE TSB                 RTS03A1
         BZ    ERROR              ERROR IF NONE                RTS03A1
         USING TSB,R7                                          RTS03A1
         SPACE
***********************************************************************
*        IF THE CALLER IS NOT AUTHORIZED, VERIFY THAT THE TEXT ENDS   *
*        WITH THE REQUESTOR'S USERID.                                 *
***********************************************************************
         SPACE
         TESTAUTH FCTN=1,STATE=YES,KEY=YES,RBLEVEL=2           RTS03A1
         LTR   R15,R15            ALL-POWERFUL CALLER?         RTS03A1
         BZ    NOUIDCHK           YES, HE CAN SEND ANYTHING    RTS03A1
         ICM   R10,8,=X'80'       NO, SET UNAUTH FLAG          RTS03A1
         USING TCB,R4                                          RTS03A1
         L     R15,TCBJSCB        FIND THE PSCB FROM TCB       RTS03A1
         USING IEZJSCB,R15                                     RTS03A1
         ICM   R15,15,JSCBPSCB                                 RTS03A1
         BZ    ERROR              PSCB MUST EXIST              RTS03A1
         USING PSCB,R15                                        RTS03A1
         CLI   PSCBUSRL,0         CALLER MUST HAVE USERID      RTS03A1
         BE    ERROR                                           RTS03A1
         LR    R14,R9             GET LENGTH OF TEXT           RTS03A1
         N     R14,=X'0000FFFF'                                RTS03A1
         SR    R1,R1                                           RTS03A1
         ICM   R1,1,PSCBUSRL      GET USERID LENGTH            RTS03A1
         CR    R1,R14             IS THERE ROOM FOR USERID?    RTS03A1
         BNL   ERROR              NO, DON'T ALLOW              RTS03A1
         AR    R14,R11            POINT TO MSG END             RTS03A1
         SR    R14,R1             BACK UP BY ID LENGTH         RTS03A1
         MODESET EXTKEY=TCB,WORKREG=2  GET USER KEY FOR CLC    RTS03A1
         BCTR  R1,0                                            RTS03A1
         EX    R1,COMPUSID        IS THE ID AT THE END?        RTS03A1
         BE    AUTH               YES, PROCEED                 RTS03A1
         LR    R15,R9             INDICATE UNAUTH              RTS03A1
AUTH     MODESET EXTKEY=ZERO      GET BACK SUPER KEY           RTS03A1
         CR    R1,R9              CAN WE PROCEED?              RTS03A1
         BE    ERROR              NO, SAY PARM LIST ERROR      RTS03A1
         SPACE
NOUIDCHK LR    R0,R8              GET DESTINATION ASID         RTS03A1
         SLL   R0,16              MOVE TO RIGHT HALF OF REG    RTS03A1
         LR    R1,R10             GET OPTION FLAGS IN R1       RTS03A1
         SLL   R1,24              ALIGN IN REGISTER            RTS03A1
         TM    TSBTERM2,TSBCTWX   IS IT AN ASCII TERMINAL?     RTS03A1
         BZ    NOTASCII           NO, CHECK FOR 3270           RTS03A1
         O     R1,BEEPADDR        PUT BELL ADDR+CNTL IN R1     RTS03A1
         ICM   R0,3,=H'16'        LENGTH OF BELL TEXT          RTS03A1
         B     BELLTPUT           JOIN 3270 CASE               RTS03A1
NOTASCII TM    TSBSTAT,TSB3270    IS THIS A 3270?              RTS03A1
         BZ    NOBELLS            NO, FORGET BELLS             RTS03A1
         O     R1,ALARMADR        PUT ALARM ADDR+ASIS IN R1    RTS03A1
         ICM   R0,3,=H'5'         LENGTH OF ALARM TEXT         RTS03A1
BELLTPUT TPUT  (1),(0),R          SEND IT OUT                  RTS03A1
         LTR   R15,R15            ANY PROBLEMS?                RTS03A1
         BNZ   GOHOME             YES, RETURN                  RTS03A1
         SPACE
***********************************************************************
*        IF OUR CALLER WAS NOT AUTHORIZED, SYNCH TO DO THE TPUT OF THE*
*        MESSAGE TEXT, SO THAT NORMAL BUFFER VALIDATION, AND THE      *
*        PREFIXING OF THE TEXT WITH A + WILL OCCUR.                   *
***********************************************************************
         SPACE
NOBELLS  LTR   R10,R10            IS CALLER AUTHORIZED?        RTS03A1
         BM    SYNCHER            NO, SYNCH TO DO TPUT         RTS03A1
         LR    R1,R11             COPY TPUT PARMS              RTS03A1
         LR    R0,R9                                           RTS03A1
         TPUT  (1),(0),R          SEND MESSAGE TEXT            RTS03A1
         B     GOHOME             RETURN TO ISSUER             RTS03A1
         SPACE
SYNCHER  SYNCH NORMTPUT           SYNCH TO TPUT IF UNAUTH      RTS03A1
         LR    R15,R2             COPY TPUT RETURN CODE        RTS03A1
GOHOME   LR    R14,R5             COPY RETURN ADDRESS          RTS03A1
         BR    R14                EXIT FROM SVC                RTS03A1
         SPACE
NORMTPUT LR    R2,R14             SAVE SYNCH RETURN ADDR       RTS03A1
         LR    R0,R9              COPY ASID & LENGTH           RTS03A1
         L     R1,=X'03000000'    CHECK FOR NON-EDIT TPUT      RTS03A1
         NR    R1,R11                                          RTS03A1
         BZ    EDITPUT            BRANCH EDIT                  RTS03A1
         O     R11,=X'01000000'   ELSE FORCE TO ASIS           RTS03A1
         N     R11,=X'FDFFFFFF'                                RTS03A1
EDITPUT  LR    R1,R11             COPY BUFFER+OPTIONS TO R1    RTS03A1
         TPUT  (1),(0),R          SEND MSG IN USER KEY/STATE   RTS03A1
         LR    R14,R2             RESTORE SYNCH RETURN ADDR    RTS03A1
         LR    R2,R15             SAVE TPUT RET CODE           RTS03A1
         BR    R14                AND RETURN TO SVC            RTS03A1
         SPACE
ERROR    LA    R15,16             SET BAD PARMS CODE           RTS03A1
         B     GOHOME                                          RTS03A1
         SPACE
COMPUSID CLC   0(0,R14),PSCBUSER  CHECK FOR USERID IN MSG      RTS03A1
         DC    0A(0)                                           RTS03A1
BEEPADDR DC    X'02',AL3(BEEP)    TO SEND BELLS CONTROL        RTS03A1
ALARMADR DC    X'01',AL3(ALARM)   TO SEND ALARM ASIS           RTS03A1
BEEP     DC    16X'2F'            16 BELL RINGS                RTS03A1
ALARM    DC    X'27F1C41D40'      TO SOUND 3270 ALARM          RTS03A1
         SPACE
         LTORG ,                                               RTS03A1
         SPACE
         PRINT NOGEN                                           RTS03A1
         CVT   DSECT=YES                                       RTS03A1
         SPACE
         IHAASCB ,                                             RTS03A1
         SPACE
         IHAASVT ,                                             RTS03A1
         SPACE
         IKJTSB ,                                              RTS03A1
         SPACE
         IKJTCB ,                                              RTS03A1
         SPACE
         IEZJSCB ,                                             RTS03A1
         SPACE
         IKJPSCB ,                                             RTS03A1
         END   ,                                               RTS03A1
