         MACRO
&NAME    LIBEL &LAB,&ADDR=
.*       THIS MACRO GENERATES CODE TO DEFINE EITHER A LABEL CONSTANT OR
.*       A LABEL VARIABLE FOR A LANGUAGE INDEPENDENT ENVIRONMENT.
         GBLC  &LIBASE            THIS ROUTINE'S BASE
         AIF   ('&ADDR' NE '').STLAB
         AIF   ('&LIBASE' NE '2' AND '&LIBASE' NE 'R2').NOBASE2
&LAB     EQU   *                  DEFINE GOTO TARGET
         AIF   ('&NAME' NE '').ADDNAME
         MEXIT
.NOBASE2 B     *+6                BRANCH AROUND GOTO TARGET
&LAB     LR    &LIBASE,R2         COPY BASE REGISTER
         AIF   ('&NAME' EQ '').MEND
.ADDNAME ANOP
&NAME    EQU   *
         MEXIT
.STLAB   ANOP
&NAME    LA    R14,8+&ADDR        LOCATION OF LABEL VALUE
         ST    R14,&ADDR          STORE IN LABEL VARIABLE
         ST    RDSA,4+&ADDR       STORE DSA ADDRESS
         LA    R14,&LAB           ADDRESS OF TARGET
         ST    R14,8+&ADDR
         ST    &LIBASE,12+&ADDR   STORE ROUTINE'S BASE REG
.MEND    MEND
