         MACRO
&NAME    LISECT &ON=NO
.*       THIS MACRO ESTABLISHES THE CSECT NAME FOR THE OTHER
.*       LANGUAGE INDEPENDENT MACROS.
         GBLC  &LISECT
         GBLC  &LIONSCT           NAME FOR ONCB CSECT
         GBLA  &LION#             NUMBER OF ON-UNITS PRESENT
         LCLC  &FINAL
         LCLA  &I
&NAME    CSECT ,                  LANGUAGE INDEPENDENT CSECT
         AIF   ('&LISECT' NE '').NOREGS
         SPACE
.RLOOP   ANOP
R&I      EQU   &I
&I       SETA  &I+1
         AIF   (&I LE 15).RLOOP
RTCA     EQU   R12                PL/I TCA POINTER
RDSA     EQU   R13                SAVE AREA POINTER
.NOREGS  SPACE
&LISECT  SETC  '&NAME'
         AIF   ('&ON' EQ 'NO').NOON
         AIF   (K'&NAME EQ 8).CNTRACT
&LIONSCT SETC  '&NAME.######'(1,7)     GENERATE ON SECT NAME
         AGO   .APPEND2
.CNTRACT ANOP
&LIONSCT SETC  '&NAME'(1,6)
&FINAL   SETC  '&NAME'(8,8)
&LIONSCT SETC  '&LIONSCT&FINAL'
.APPEND2 ANOP
&LIONSCT SETC  '&LIONSCT.2'
&LION#   SETA  0                  REINIT ON UNIT COUNTER
.NOON    MEND
