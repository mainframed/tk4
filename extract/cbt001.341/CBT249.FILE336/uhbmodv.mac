         MACRO
&LABEL   UHBMODV &TO,&FROM,&LEN,&COM=VALUE,&MF=
.*       THIS MACRO IS CALLED TO STORE A DATA VALUE INTO A PARM LIST
         GBLB  &UHBM1,&UHBIM
         GBLC  &UHBML
         LCLC  &L,&S,&R,&RL,&T
&S       SETC  ' .      '
         AIF   (&UHBIM OR ('&MF(1)' EQ 'L' AND '&MF(2)' EQ '')).MEND
         AIF   ('&FROM' EQ '').EXIT
&L       SETC  '&LABEL'
         AIF   (NOT &UHBM1).L0
&UHBM1   SETB  (0)
         AIF   ('&UHBML' EQ '').L0
         AIF   ('&L' NE '').L2
&L       SETC  '&UHBML'
         AGO   .L0
.L2      ANOP
&UHBML   DS    0H
.L0      AIF   ('&FROM'(1,1) EQ '''').LIT
         AIF   (K'&FROM LE 2).NLIT
         AIF   ('&FROM'(K'&FROM,1) EQ '''').LIT
.NLIT    ANOP
&R       SETC  '14'
&RL      SETC  '15'
         AIF   (T'&LEN EQ 'N').SNVLEN
         AIF   ('&LEN' EQ '' OR '&LEN' EQ '*').SNVLEN
         AIF   (K'&LEN LE 2).SNVLEN
         AIF   ('&LEN'(2,1) NE ':').SNVLEN
         UHBCOLON &LEN,2,S,MF=E
&L       UHBILDUP LH,'15,',,LOAD.LENGTH.OF.&COM
&L       SETC  ''
.SNVLEN  ANOP
         AIF   ('&FROM'(1,1) EQ '(').SRFROM
         AIF   (K'&FROM LE 2).SNFROM
         AIF   ('&FROM'(1,2) EQ 'S:').SSFROM
         AIF   ('&FROM'(2,1) NE ':').SNFROM
.SCFROM  ANOP
&L       UHBADDR 14,&FROM,ADDRESS.&COM
&L       SETC  ''
         AGO   .SDOMOVE
.SRFROM  ANOP
&R       SETC  '&FROM'(2,K'&FROM-2)
         AGO   .SDOMOVE
.SSFROM  UHBCOLON &FROM,4,S,MF=E
         AGO   .SDOMOVE
.SNFROM  UHBRKDN &FROM
.SDOMOVE AIF   (T'&LEN NE 'N').SNCMOVE
         AIF   ('&LEN'(1,1) EQ '(').SVREG
.SNCMOVE AIF   ('&LEN' EQ '' OR '&LEN' EQ '*').SCMOVE
         AIF   (K'&LEN LE 2).SCMOVE
         AIF   ('&LEN'(1,1) EQ '(').SVREG
         AIF   ('&LEN'(2,1) EQ ':').SVNOR
.SCMOVE  AIF   ('&FROM'(1,1) EQ '(').SCRMOVE
         AIF   (K'&FROM LE 2).SCDMOVE
         AIF   ('&FROM'(2,1) NE ':' OR '&FROM'(1,2) EQ 'S:').SCDMOVE
.SCRMOVE UHBRKDN 0(&R)
.SCDMOVE ANOP
&L       UHBMOVE &TO,,&LEN,COPY.&COM
         MEXIT
.SVREG   ANOP
&RL      SETC  '&LEN'(2,K'&LEN-2)
.SVNOR   ANOP
&L       BCT   &RL,*+10            DECREMENT LENGTH FOR EXECUTE
&L       SETC  ''
         AIF   ('&FROM'(1,1) EQ '(').SVRMOVE
         AIF   (K'&FROM LE 2).SVDMOVE
         AIF   ('&FROM'(2,1) EQ 'S:').SVDMOVE
         AIF   ('&FROM'(2,1) NE ':').SVDMOVE
.SVRMOVE UHBRKDN 0(&R)
.SVDMOVE UHBMOVE &TO,,*-*,TO.BE.EXECUTED
         EX    &RL,*-6&S.COPY.&COM
         MEXIT
.LIT     AIF   ('&MF(1)' NE 'E' AND '&MF(1)' NE 'M').MEND
         AIF   ('&FROM'(1,1) NE '''').LTY
&T       SETC  'C'
.LTY     UHBRKDN =&T&FROM
         AGO   .SCDMOVE
.EXIT    AIF   ('&LABEL' EQ '').MEND
&LABEL   DS    0H
.MEND    MEND
