FINDJOB  QSTART 'QUEUE COMMAND - LOCATE JQE, JCT, AND IOT BY JOBNAME', X
               REGS=NO
         PRINT OFF
         COPY  $HASPGEN
         PRINT ON
BUFSTART EQU   0
BUFDSECT EQU   0
         $JCT
         $JQE
         $PCE
         $PDDB
         $TAB
         $JOE
         $JOT
         GBLB   &QACF2         ACF2 CHECKING FOR AUTH              FCI*
         AIF    (NOT &QACF2).NACF1                                 FCI*
*******************************************************************FCI*
* MOD 1 - K TRUE  - 22 OCT 79 -                                    FCI*
*   ADD ACF2 AUTH CHKING FOR USER AUTH TO LOOK AT STUFF.           FCI*
*    UPDATE FOR ACF2 RELEASE 3.1.3                                ANRP*
*    OPER CHKS ALL..                                               FCI*
*    USER CHKS OWN GOODIES (LOGONID = LOGONID IN JCT)              FCI*
*    OTHER: ISSUE ACFVLD CHK FOR AUTH                              FCI*
*******************************************************************FCI*
         GBLC  &ACF2JCT                                           ANRP*
         #ACFJES2 ,                                               ANRP*
.NACF1   ANOP                                                      FCI*
         USING QCKPT,R10      BASE REG FOR CHECKPOINT WORK AREA
         L     R10,QVCKPT     LOAD BASE REG
         USING WORK,R13       LOCAL WORK AREA
***********************************************************************
*                                                                     *
*   DETERMINE IF SEARCH IS BY JOBNUMBER OR JOBNAME                    *
*                                                                     *
***********************************************************************
         STC   R1,AFFBYTE1    ONLY VALID IF CALLER IS SYSLOG.
         LH    R1,QLNG1       LENGTH OF PARAMETER FIELD
         SH    R1,=H'1'       IS THE LENGTH ZERO?
         BM    TILT           YES. TILT.
         CLI   QPARM1,C'0'    IS THE FIRST CHARACTER NUMERIC?
         BL    JOBNAME        NO. SEARCH BY JOBNAME
         MVC   QFZONES,QFZONE INITIALIZE NUMERIC TEST
         EX    R1,MVZ         MOVE THE ZONES FOR VALIDITY CHECK
         CLC   QFZONES,QFZONE IS THE FIELD NUMERIC?
         BNE   TILT           NO. TILT.
         EX    R1,PACK        PACK THE FIELD
         CVB   R5,CONVERT     CONVERT TO BINARY
         SPACE
***********************************************************************
*                                                                     *
*   LOCATE JQE BY JOBNAME                                             *
*                                                                     *
***********************************************************************
JOBNAME  LA    R1,$JQTYPES
         L     R2,QCJQHEAD
         B     ENDJQE         SKIP FREE QUEUE
NEXTJQT  ICM   R3,7,1(R2)
         BZ    ENDJQE         END OF QUEUE
NEXTJQE  AL    R3,QCJQTA
         USING JQEDSECT,R3    BASE REG FOR JQE
         CLI   QPARM1,C'0'    IS SEARCH BY JOBNUMBER?
         BL    CLCNAME        NO. SEARCH BY JOBNAME.
         CH    R5,JQEJOBNO    IS THIS THE RIGHT JOBNUMBER?
         BNE   NOMATCH        NO. TRY NEXT JQE.
         B     FOUND          YES. PROCESS IT.
CLCNAME  CLC   QPARM1,JQEJNAME IS THIS THE RIGHT JOBNAME?
         BE    FOUND          YES. PROCESS IT.
NOMATCH  ICM   R3,7,JQENEXT+1
         BNZ   NEXTJQE
ENDJQE   LA    R2,$JQHEADL(,R2)
         BCT   R1,NEXTJQT
TILT     QTILT '*** JOBNAME NOT FOUND OR INVALID ***'
***********************************************************************
*                                                                     *
*   READ JCT AND IOT                                                  *
*                                                                     *
***********************************************************************
FOUND    CLC   =C'SL',QSUBNAME     ?/SYSLOG COMMAND
         BNE   FOUND1              NO. CONTINUE NORMAL PROCESSING
         CLC   QPARM1,=CL8'SYSLOG' ?/SPECIAL SYSLOG PROCESSING
         BNE   FOUND1              NO. CONTINUE NORMAL PROCESSING
         MVC   AFFBYTE2,JQEFLAGS   SAVE JQEFLAGS.
         NI    AFFBYTE2,X'07'      TURN OFF USELESS BITS
         CLC   AFFBYTE1,AFFBYTE2   ?/SYSLOG FOR THIS SYSTEM
         BNE   NOMATCH             NO. CONTINUE JQE SEARCH
FOUND1   ST    R3,QCJQEA           SAVE THE ADDRESS
         MVC   QCTRAK,JQETRAK      DISK ADDR OF JCT
         DROP  R3
         L     R3,QCJCTA           ADDR OF IOAREA FOR JCT
         LR    R1,R3               PARM FOR READSPC
         L     R15,=V(READSPC)     ROUTINE TO READ HASPACE
         BALR  R14,R15             GO TO IT
         USING JCTDSECT,R3         BASE REG FOR JCT
         L     R0,QCJQEA           LOAD  ADDRESS OF JQE
         SL    R0,QCJQTA           MINUS ADDRESS OF JQT
         CL    R0,JCTJQE           IS THE JQE STILL VALID?
         BNE   TILT                NO ABORT
         MVC   QPJOBID,JCTJBKEY    JOB IDENTIFICATION
         AIF    (NOT &QACF2).NACF2                                 FCI*
         EJECT
*******************************************************************FCI*
* MOD 1 - K TRUE  - 22 OCT 79 -                                    FCI*
* MOD 2 - G HARPER - 4 MARCH 83 -                                 ANRP*
*                                                                  FCI*
         TM    QXAUTH,1       IS USER QUEUE AUTHORIZED?           ANRP*
         BO    ACF2OK         YES..CONTINUE FORTHWITH              FCI*
*                                                                  FCI*
         ICM   R8,15,&ACF2JCT  TEST, GET ACF2 AREA OFFSET         ANRP*
         BZ    ACF2OK          BRANCH IF NONE FOUND               ANRP*
**********************************************                    ANRP*
****   THE FOLLOWING SUBTRACTION IS BECAUSE THE IOB IS NOT USED   ANRP*
****     IN QUEUE AND THEREFORE THE JCT ADDRESS NEEDS TO BE       ANRP*
****     REDUCED ACCORDINGLY.                                     ANRP*
         SH    R8,=Y(88)        REDUCE BY LENGTH OF IO BUFFER     ANRP*
**********************************************                    ANRP*
         LA    R8,0(R3,R8)      CALCULATE ADDRESS                 ANRP*
         USING #ACFJCTX,R8      ADDRESS ACF2 JCT                  ANRP*
         CLC   QLOGON,LIDLID                                      ANRP*
*                      SEE IF USER'S LOGONID=JOB'S ACF LOGON ID   ANRP*
         BE    ACF2OK         YES..CONTINUE FORTHWITH              FCI*
**** SHOW NO AUTHORITY UNTIL ACF2 IN ABORT MODE  ********         ANRP*
         B     NOTOK          NO  MUST SHOW CANCEL                ANRP*
*                                                                  FCI*
         MVC   DSNAME,=CL44'SYSOUT. '  INITIALIZE DSNAME TO USE    FCI*
         XC    ACFSPARM(ACFSPRML),ACFSPARM   CLEAR REQUEST BLOCK   FCI*
         MVI   ACFSPREQ,ACFSPRDS  DSNAME ACCESS ONLY               FCI*
         MVI   ACFSPID1,ACFSPIUR  DIS AM DE USER TALKING....       FCI*
         LA    R7,DSNAME          GET DSNAME ADDRESS               FCI*
         ST    R7,ACFSPDSN        AND GIVE IT TO ACF PARM LIST     FCI*
*                                                                  FCI*
*  GENERATE DSNAME OF FORMAT 'SYSOUT.LOGONID.JOBNAME' FOR CHKING   FCI*
*                                                                  FCI*
         MVC   DSNAME+7(8),LIDLID  MOVE LID TO DSNAME              FCI*
         CLI   DSNAME+7,C' '       IS THE LID BLANK?               FCI*
         BNE   ADSNCHK0            NO..NORMAL PROCESS              FCI*
         DROP  R8               DROP ADDRESSABILITY               ANRP*
*                                                                  FCI*
*  GOT HERE BECAUSE LID IS ' ' (BLANK)..SUBSTITUTE 'SYSTEM'        FCI*
*                                                                  FCI*
         MVC   DSNAME+7(8),=CL8'SYSTEM'                            FCI*
*                                                                  FCI*
*                                                                  FCI*
ADSNCHK0 LA    R1,DSNAME+7         GET ADDRESS                     FCI*
         LA    R7,8                LOAD COUNT                      FCI*
ADSNCHK  CLI   0(R1),C' '          LOOK FOR BLANK                  FCI*
         BE    ADSNCHK1            GOTIT..                         FCI*
         LA    R1,1(R1)            BUMP AND                        FCI*
         BCT   R7,ADSNCHK            GRIND                         FCI*
ADSNCHK1 MVI   0(R1),C'.'          MOVE IN PERIOD..                FCI*
         MVC   1(8,R1),JCTJNAME    MOVE IN JOBNAME                 FCI*
*                                                                  FCI*
         LA    R1,ACFSPARM        GET ADDRESS OF PARM FIELD        FCI*
         ACFSVC (1),TYPE=S,NONE=NOTOK   ISSUE DATASET VALIDATION  ANRP*
*                                                                  FCI*
         LTR   R15,R15            HOW DID YOU LIKE THEM APPLES?    FCI*
         BE    ACF2OK             ..OK BY YOU...CONTINUE..         FCI*
*                                                                  FCI*
NOTOK    QTILT '*** SORRY..NO ACF2 AUTHORITY TO LOOK AT THIS JOB'  FCI*
*******************************************************************FCI*
*******************************************************************FCI*
ACF2OK   DS    0H                                                  FCI*
.NACF2   ANOP                                                      FCI*
         MVC   QCTRAK,JCTIOT      DISK ADDR OF IOT
         DROP  R3
         L     R1,QCIOTA          ADDR OF IOAREA FOR IOT
         L     R15,=V(READSPC)    ROUTINE TO READ HASPACE
         BALR  R14,R15            GO TO IT
         USING IOTDSECT,R1
         CLC   QPJOBID,IOTJBKEY   IS THE IOT STILL VALID
         BNE   TILT               NO
         DROP  R1
STOP     QSTOP
         EJECT
***********************************************************************
*                                                                     *
*   MISCELLANY                                                        *
*                                                                     *
***********************************************************************
MVZ      MVZ   QFZONES(1),QPARM1     CHECK FOR NUMERIC
PACK     PACK  CONVERT,QPARM1(1)     CONVERT TO BINARY
         LTORG
         $QINDEX
         $CAT
         $HCT
         $IOT
         QCOMMON
WORK     DSECT
         DS    72C
CONVERT  DS    D
AFFBYTE1 DS    C
AFFBYTE2 DS    C
         AIF    (NOT &QACF2).NACF3                                 FCI*
         ACDSV DSECT=NO                                            FCI*
DSNAME   DS    CL44                                                FCI*
         ACCVT   ,                                                 FCI*
         ACUCB   ,                                                 FCI*
         #ACFJCTX ,                                               ANRP*
.NACF3   ANOP    ,                                                 FCI*
         END
