DUMP     QSTART 'QUEUE COMMAND - FORMAT JES2 CONTROL BLOCKS',SEC=YES
         GBLB  &QACF2         ACF2 FLAG                            FCI*
         AIF   (NOT &QACF2).$SKIP0                                ANRP*
         GBLC  &ACF2JCT                                           ANRP*
         #ACFJES2 ,                                               ANRP*
.$SKIP0  ANOP                                                     ANRP*
         USING QDISPLAY,R9        BASE REG FOR DISPLAY WORK AREA
         L     R9,QVDSPL          LOAD BASE REG
         USING WORK,R13
         CLC   =C'HCT ',QPARM1     DUMP THE HCT VARIABLES?
         BE    DHCT                YES. GO PROCESS
         CLC   =C'CKPT',QPARM1     DUMP THE CKPT?
         BE    DCKPT               YES. GO PROCESS
         CLC   =C'JOT ',QPARM1     DUMP THE JOT?
         BE    DJOT                YES. GO PROCESS
***********************************************************************
*                                                                     *
*   CALL FINDJOB TO LOCATE JQE,JCT, AND IOT                           *
*                                                                     *
***********************************************************************
         L     R15,=V(FINDJOB)    ROUTINE TO FIND JOB
         BALR  R14,R15            GO TO IT
         EJECT
***********************************************************************
*                                                                     *
*   DETERMINE THE CONTROL BLOCK TO BE FORMATTED                       *
*                                                                     *
***********************************************************************
         CLC   =C'ALL ',QPARM2       DUMP ALL CONTROL BLOCKS FOR JOB
         BE    DALL                  YES. GO PROCESS
         XC    DALLREG,DALLREG       INITIALIZE
         CLC   =C'JQE',QPARM2        DUMP THE JQE?
         BE    DJQE                  YES. GO PROCESS
         CLC   =C'JCT ',QPARM2       DUMP THE JCT?
         BE    DJCT                  YES. GO PROCESS
         CLC   =C'JOE',QPARM2        DUMP THE JOE?
         BE    DJOE                  YES. GO PROCESS
         CLC   =C'IOT',QPARM2        DUMP THE IOT?
         BE    DIOT                  YES. GO PROCESS
         CLC   =C'PDDB',QPARM2       DUMP THE PDDB?
         BE    DPDDB                 YES. GO PROCESS
         AIF   (NOT &QACF2).$SKIP1                                ANRP*
         CLC   =C'JCTA',QPARM2       DUMP THE ACF2 JCT EXTENSION?
         BE    DJCTA                 YES. GO PROCESS
.$SKIP1  ANOP
         B     STOP
***********************************************************************
*                                                                     *
*   DUMP ALL THE CONTROL BLOCKS FOR THE JOB                           *
*                                                                     *
***********************************************************************
DALL     SR    R2,R2          CLEAR REG
         LA    R2,FIRSTCB     ADDR OF FIRST CB TO BE FORMATTED
         ST    R2,DALLREG     SAVE ADDRESS
FIRSTCB  B     DJQE           DUMP JQE
         B     DJCT           DUMP JCT
         B     DJCTA          DUMP ACF2 JCT EXTENSTION
         B     DJOE           DUMP JOE
         B     DIOT           DUMP IOT
         B     DPDDB          DUMP PDDB
         B     STOP1          EXIT
***********************************************************************
*                                                                     *
*   FORMAT THE JQE                                                    *
*                                                                     *
***********************************************************************
DJQE     MVC   QDHLINE,JQEHDR MOVE IN THE HEADING
         LA    R2,JQEDESC     A(JQE DESCRIPTION)
         L     R4,QCJQEA      ADDR OF INPUT DATA
         BAL   R3,FORMAT      GO FORMAT THE JQE
         B     STOP           EXIT
***********************************************************************
*                                                                     *
*   FORMAT THE JOE                                                    *
*                                                                     *
***********************************************************************
         USING JQEDSECT,R1        BASE REG FOR JQE
DJOE     MVC   QDHLINE,JOEHDR     MOVE IN THE HEADING
         L     R1,QCJQEA          A(JQE)
         SR    R4,R4              CLEAR REG
         ICM   R4,7,JQEJOE+1      JOE OFFSET
         DROP  R1
JOELOOP  LTR   R4,R4              ?/JOE PRESENT
         BZ    STOP               NO. EXIT
         SLL   R4,2               YES. MULTIPLY OFFSET BY 4
         L     R1,QCJOTA          A(JOT)
         AR    R4,R1              A(JOE)
         ST    R4,JOEADDR         SAVE JOE ADDR
         LA    R2,JOEDESC         A(JOE DESCRIPTION)
         BAL   R3,FORMAT          BRANCH TO FORMAT ROUTINE
         MVC   QDMSG,QBLANK       BLANK OUT THE MESSAGE AREA
         BAL   R8,DISPLAY1        GO DISPLAY THE BLANK LINE
         USING JOEDSECT,R4        BASE REG FOR JOE
         L     R4,JOEADDR         RESTORE JOE ADDR
         SR    R5,R5              CLEAR REG
         ICM   R5,7,JOEJQNXT+1    GET NEXT JOE OFFSET
         LR    R4,R5              R4 - JOE OFFSET
         B     JOELOOP            GO THRU THE CYCLE AGAIN
         DROP  R4
***********************************************************************
*                                                                     *
*   FORMAT THE JCT                                                    *
*                                                                     *
***********************************************************************
DJCT     MVC   QDHLINE,JCTHDR MOVE IN THE HEADING
         LA    R2,JCTDESC     A(JCT DESCRIPTION)
         L     R4,QCJCTA      A(INPUT DATA)
         BAL   R3,FORMAT      GO FORMAT THE DATA
         B     STOP           EXIT
         EJECT
         AIF   (NOT &QACF2).$SKIP2
***********************************************************************
*                                                                     *
*   FORMAT THE ACF2 JCT EXTENSION                                     *
*                                                                     *
***********************************************************************
DJCTA    MVC   QDHLINE,JCTAHDR MOVE IN THE HEADING
         LA    R2,JCTADESC     A(JCTA DESCRIPTION)
         L     R4,QCJCTA       A(INPUT DATA)
         USING JCTDSECT,R4     TEMP ADDRESS TO JCT                ANRP*
         ICM   R8,15,&ACF2JCT       TEST, GET ACF2 AREA OFFSET    ANRP*
         BZ    NOACF2               NOT PRESENT, SKIP AROUND      ANRP*
         DROP  R4              DROP TEMP                          ANRP*
**********************************************                    ANRP*
****   THE FOLLOWING SUBTRACTION IS BECAUSE THE IOB IS NOT USED   ANRP*
****     IN QUEUE AND THEREFORE THE JCT ADDRESS NEEDS TO BE       ANRP*
****     REDUCED ACCORDINGLY.                                     ANRP*
         SH    R8,=Y(88)        REDUCE BY LENGTH OF IO BUFFER     ANRP*
**********************************************                    ANRP*
         LA    R8,0(R4,R8)     CALCULATE ADDRESS                  ANRP*
         LR    R4,R8           LOAD ADDRESS IN CORRECT REG        ANRP*
         BAL   R3,FORMAT       GO FORMAT THE DATA
NOACF2   DS    0H                                                 ANRP*
         B     STOP            EXIT
.$SKIP2  ANOP
***********************************************************************
*                                                                     *
*   FORMAT THE IOT                                                    *
*                                                                     *
***********************************************************************
DIOT     MVC   QDHLINE,IOTHDR MOVE IN THE HEADING
         MVI   SWITCH,0
         L     R4,QCIOTA      A(INPUT DATA)
         LTR   R4,R4          ?/IOT VALID
         BZ    STOP           NO. EXIT
IOTLOOP  ST    R4,IOTADDR     YES. SAVE FOR LATER USE
         LA    R2,IOTDESC     A(IOT DESCRIPTION)
         BAL   R3,FORMAT      GO FORMAT THE DATA
         MVC   QDMSG,QBLANK       BLANK OUT THE MESSAGE AREA
         BAL   R8,DISPLAY1        GO DISPLAY THE BLANK LINE
         L     R4,IOTADDR     RESTORE IOT ADDR
         L     R4,NEXTIOT(R4) GET ADDR OF NEXT IOT
IOT010   LTR   R4,R4          ?/NEXT IOT PRESENT
         BZ    SPINIOT        NO. CHECK SPIN IOT'S
         BAL   R8,READ        YES. READ THE IOT IN
         B     IOTLOOP
SPINIOT  TM    SWITCH,1       ?/SPIN IOT ALREADY SEARCHED
         BO    STOP           YES. EXIT
         OI    SWITCH,1       NO. INDICATE ITS NOW BEING SEARCHED
         L     R4,QCJCTA      A(JCT)
         L     R4,12(R4)      GET ADDR OF SPIN IOT
         B     IOT010         GO VERIFY THE IOT IS VALID
         EJECT
***********************************************************************
*                                                                     *
*   FORMAT THE PDDB                                                   *
*                                                                     *
***********************************************************************
DPDDB    MVC   QDHLINE,PDDBHDR MOVE IN THE HEADING
         MVI   SWITCH,0       INITIALIZE SWITCH
         USING PDBDSECT,R2    BASE REG FOR PDDB
         USING IOTDSECT,R3    BASE REG FOR IOT
         L     R3,QCIOTA      LOAD BASE REG
         LR    R4,R3          BASE OF IOT
PDDB010  A     R4,IOTPDDBP    OFFSET BEYOND LAST PDDB
         LR    R2,R3          BASE OF IOT
         A     R2,QCPDDB1     OFFSET TO FIRST PDDB IN IOT
PDDB020  ST    R2,PDDBADDR    SAVE ADDR OF PDDB
         ST    R3,IOTADDR     SAVE ADDR OF IOT
         ST    R4,LASTPDDB    SAVE ADDR OF LAST PDDB
         LR    R4,R2          ADDR OF INPUT DATAET?
         LA    R2,PDDBDESC    A(PDDB DESCRIPTION)
         BAL   R3,FORMAT      GO FORMAT THE PDDB
         MVC   QDMSG,QBLANK       BLANK OUT THE MESSAGE AREA
         BAL   R8,DISPLAY1        GO DISPLAY THE BLANK LINE
         L     R2,PDDBADDR    RESTORE ADDR OF PDDB
         L     R3,IOTADDR     RESTORE ADDR OF IOT
         L     R4,LASTPDDB    RESTORE ADDR OF LAST PDDB
         LA    R2,PDBLENG(R2) NO. LOOK AT NEXT PDDB.
         CR    R2,R4          HAVE WE GONE PAST THE LAST PDDB?
         BNL   PDDB025        YES. GET NEXT PDDB
         CLC   PDBDSKEY,=H'0' ?/VALID PDDB
         BNE   PDDB020        YES. GO PROCESS
PDDB025  L     R4,IOTIOTTR    DISK ADDR OF NEXT IOT
PDDB030  LTR   R4,R4          IS THERE ANOTHER IOT?
         BZ    PDDB040        NO. TRY THE SPIN IOT?
         BAL   R8,READ        READ THE IOT
         B     PDDB010        SEARCH THE NEXT IOT
         USING JCTDSECT,R1    BASE REG FOR JCT
PDDB040  TM    SWITCH,1       DID WE SEARCH THE SPINIOT ALREADY?
         BO    STOP           YES. EXIT.
         OI    SWITCH,1       SET SWITCH
         L     R1,QCJCTA      LOAD BASE REG
         L     R4,JCTSPIOT    DISK ADDR OF SPIN IOT
         B     PDDB030        SEARCH THE SPIN IOT CHAIN
         DROP  R1,R2,R3
         EJECT
***********************************************************************
*                                                                     *
*   FORMAT THE HCT                                                    *
*                                                                     *
***********************************************************************
DHCT     MVC   QDHLINE,HCTHDR MOVE IN THE HEADING
         LA    R2,HCTDESC     A(HCT DESCRIPTION)
         L     R4,QCJQTL      ADDR OF INPUT DATA
         BAL   R3,FORMAT      GO FORMAT THE HCT
         B     STOP           EXIT
***********************************************************************
*                                                                     *
*   FORMAT THE JOT                                                    *
*                                                                     *
***********************************************************************
DJOT     MVC   QDHLINE,JOTHDR MOVE IN THE HEADING
         LA    R2,JOTDESC     A(JOT DESCRIPTION)
         L     R4,QCJOTA      ADDR OF INPUT DATA
         BAL   R3,FORMAT      GO FORMAT THE JOT
         B     STOP           EXIT
***********************************************************************
*                                                                     *
*   FORMAT THE CHECKPOINT                                             *
*                                                                     *
***********************************************************************
DCKPT    MVC   QDHLINE,CKPTHDR MOVE IN THE HEADING
         LA    R2,4095        LENGTH OF CKPT RECORDS-1
         LA    R2,1(,R2)      LENGTH OF CKPT RECORDS
         L     R4,QCJQTL      A(FIRST CHECKPOINT RECORD)
         L     R6,QCJOTL      NO. OF CHECKPOINT RECORDS
         LA    R6,0(R6)       PURIFY THE REG
         B     CKPT000        GO FORMAT THE DATA
         EJECT
***********************************************************************
*                                                                     *
*   FORMAT THE DATA                                                   *
*                                                                     *
***********************************************************************
FORMAT   ST    R3,RETURN          SAVE LINK ADDRESS
         SR    R5,R5              OFFSET REGISTER
FMT000   LA    R3,QDMSG           A(DISPLAY LINE)
         LH    R6,QDLNELEN        LOAD LENGTH OF LINE              MOD5
         AR    R6,R3              A(END OF DISPLAY)                MOD5
         MVC   QDMSG,QBLANK       INIT THE CURRENT LINE
         MVI   0(R3),C'+'         INDICATE RELATIVE
         XC    PLAY,PLAY
         ST    R5,PLAY            TRANSLATE
         UNPK  PLAY(9),PLAY(5)        OFFSET TO
         TR    PLAY(8),TBL-240               DECIMAL
         MVC   1(3,R3),PLAY+5
         LA    R3,5(R3)           NEXT LOCATION OF DISPLAY LINE
FMT010   CLI   0(R2),X'FF'        ?/ALL FIELDS FORMATTED
         BNE   FMT012             NO. CONTINUE PROCESSING
         ICM   R7,15,NUMOFVOL     YES. GET NUM VOLSERS IN JRT LIST
         BZ    DISPLAY            NONE PRESENT, GO DISPLAY THE LINE
         BCTR  R7,R0              DECREMENT NO. OF VOLSERS BY 1
         ST    R7,NUMOFVOL        SAVE THE CURRENT COUNT
         SH    R2,=H'18'          GO DISPLAY THIS FIELD AGAIN
FMT012   SR    R7,R7              CLEAR REG FOR INSERT
         IC    R7,5(R2)           GET LENGTH OF FIELD
         CLC   =C'HCT',QPARM1     ?/FORMATTING THE HCT
         BNE   NOHCT0             NO. DON'T BUMP
         IC    R7,8(R2)           GET LENGTH OF FIELD
NOHCT0   LA    R8,6(R3)           CURRENT DISPLACEMENT IN DISPLAY LINE
         CLC   =C'HCT',QPARM1     ?/FORMATTING THE HCT
         BNE   NOHCT1             NO. DON'T BUMP
         LA    R8,3(R8)           ADD THE EXTRA FOR HCT
NOHCT1   CLM   R7,1,=X'80'        ?/CHARACTER FIELD
         BC    13,DOUBLE          ** NO. DOUBLE THE LENGTH
         N     R7,=X'0000007F'    ** TURN OFF INDICATOR IN CASE
         SRL   R7,1               ** YES. CUT LENGTH IN HALF
         B     NODOUBLE           ** DON'T DOUBLE
DOUBLE   AR    R8,R7              ADD LENGTH TO DISPLACEMENT
NODOUBLE AR    R8,R7              DOUBLE IT
         CR    R8,R6              ?/ENOUGH ROOM ON DISPLAY
         BH    FMT050             NO. GO DISPLAY THIS LINE
         CLC   =C'HCT',QPARM1     ?/FORMATTING THE HCT
         BNE   NOHCT2             NO. BYPASS PROCESSING FOR HCT
         LA    R1,18(R3)          DEFAULT FOR NEXT OFFSET
         CH    R7,=H'4'           ?/FIELD LENGTH <= 4
         BNH   FMT015H            YES. CONTINUE
         LA    R1,18(R1)          BUMP FOR NEXT OFFSET
         CH    R7,=H'13'          ?/FIELD LENGTH <= 13
         BNH   FMT015H            YES. CONTINUE
         LA    R1,18(R1)          BUMP FOR NEXT OFFSET
         CH    R7,=H'22'          ?/FIELD LENGTH <= 22
         BNH   FMT015H            YES. CONTINUE
         LA    R1,18(R1)          BUMP FOR NEXT OFFSET
         CH    R7,=H'31'          ?/FIELD LENGTH <= 31
         BNH   FMT015H            YES. CONTINUE
         QTILT '** UNABLE TO FORMAT DUE TO FIELD DESCRIPTION ERROR **'
FMT015H  ST    R1,NEXTOSET        SAVE NEXT OFFSET
         MVC   0(8,R3),0(R2)      YES. MOVE IN FIELD ID
         LA    R3,9(R3)           UPDATE DISPLACEMENT
         B     FMT020             BRANCH AROUND NON-HCT CODE
NOHCT2   LA    R1,15(R3)          DEFAULT FOR NEXT OFFSET
         CH    R7,=H'4'           ?/FIELD LENGTH <= 4
         BNH   FMT015             YES. CONTINUE
         LA    R1,15(R1)          BUMP FOR NEXT OFFSET
         CH    R7,=H'11'          ?/FIELD LENGTH <= 11
         BNH   FMT015             YES. CONTINUE
         LA    R1,15(R1)          BUMP FOR NEXT OFFSET
         CH    R7,=H'19'          ?/FIELD LENGTH <= 19
         BNH   FMT015             YES. CONTINUE
         LA    R1,15(R1)          BUMP FOR NEXT OFFSET
         CH    R7,=H'26'          ?/FIELD LENGTH <= 26
         BNH   FMT015             YES. CONTINUE
         QTILT '** UNABLE TO FORMAT DUE TO FIELD DESCRIPTION ERROR **'
MVECHAR  MVC   0(0,R3),0(R4)      **
FMT015   ST    R1,NEXTOSET        SAVE NEXT OFFSET
         LH    R15,QDLNELEN       LOAD LENGTH FOR COMPARE
         CH    R15,=H'80'         IS THIS A MOD 2 SCREEN
         BE    MVENAME            YES  MOVE FIELD NAME
         CLI   0(R2),C' '         IS THE FIELD NAME BLANK
         BE    FMT020             YES  DO NOT MOVE FIELD NAME
MVENAME  MVC   0(5,R3),0(R2)      YES. MOVE IN FIELD ID
         LA    R3,6(R3)           UPDATE DISPLACEMENT
FMT020   SR    R15,R15            CLEAR REG
         IC    R15,5(R2)          INSERT LENGTH AND FLAG
         CLC   =C'HCT',QPARM1     ?/DUMPING THE HCT
         BNE   NOHCT3             NO. BYPASS HCT PROCESSING
         IC    R15,8(R2)          YES. GET THE TRUE LENGTH AND FLAG
NOHCT3   ST    R15,SAVER15        SAVE CONTENTS OF REG 15
         N     R15,=X'00000080'   DESTROY LENGTH AND SAVE THE FLAG
         LTR   R15,R15            ?/CHARACTER FIELD
         BZ    FMT030             NO. GO TRANSLATE DATA TO EBCDIC
         L     R15,SAVER15        RESTORE CONTENTS OF REG 15
         N     R15,=X'0000007F'   TURN OFF THE FLAG AND SAVE LENGTH
         BCTR  R15,R0             DECREMENT LENGTH FOR EXECUTE
         EX    R15,MVECHAR        MOVE DATA TO DISPLAY LINE
         LA    R15,1(R15)         RESET LENGTH
         AR    R3,R15             ADD LENGTH TO DISPLAY LINE
         AR    R4,R15             ADD LENGTH TO INPUT DATA OFFSET
         AR    R5,R15             ADD LENGTH TO OFFSET
         B     FMT040             GO PREPARE FOR NEXT FIELD
FMT030   XC    PLAY,PLAY          CLEAR WORK AREA
         MVC   PLAY(1),0(R4)      MOVE DATA TO WORK AREA
         UNPK  PLAY+2(3),PLAY(2)  UNPACK THE DATA
         TR    PLAY+2(2),TBL-240  TRANSLATE TO PRINTABLE CHAR.
         MVC   0(2,R3),PLAY+2     MOVE INTO DISPLAY LINE
         LA    R3,2(R3)           UPDATE DISPLACEMENT
         LA    R4,1(R4)           POINT TO NEXT BYTE OF DATA
         LA    R5,1(R5)           RELATIVE OFSSET
         BCT   R7,FMT030          DO IT AGAIN.
FMT040   L     R3,NEXTOSET        GET NEXT OFFSET
         LA    R2,6(R2)           GET NEXT FIELD ID
         CLC   =C'HCT',QPARM1     ?/DUMPING THE HCT
         BNE   FMT010             NO. GO TO LOOP
         LA    R2,3(R2)           YES. POINT TO THE REAL NEXT FIELD
         B     FMT010
FMT050   BAL   R8,DISPLAY1        GO DISPLAY THIS LINE
         B     FMT000
DISPLAY  BAL   R8,DISPLAY1        GO DISPLAY THE LAST LINE
         L     R3,RETURN          RESTORE LINK ADDRESS
         BR    R3                 RETURN TO CALLER
DISPLAY1 DS    0H
         CLC   =C'CKPT',QPARM1    ?/CHECKPOINT BEING DUMPED
         BNE   DISPLAY2           NO. KEEP ON TRUCKING
         L     R1,STARTADR        STARTING ADDR OF DATA FOR THIS LINE
         LR    R15,R4             ENDING ADDR OF DATA FOR THIS LINE
         SR    R15,R1             LENGTH OF DATA
         BCTR  R15,R0             DECREMENT BY 1 FOR EXECUTE.
         EX    R15,MOVE0          MOVE THE DATA TO THE WORK AREA
         EX    R15,TRANSLTE       TRANSLATE THE DATA
         EX    R15,MOVE1          MOVE THE DATA TO THE DISPLAY LINE
         MVI   QDMSG+52,C'*'      FILL IN THE REST
         MVI   QDMSG+73,C'*'           OF THE DATA
DISPLAY2 LA    R1,QDMSG           SAVE ADDRESS
         ST    R1,QDMSGA              OF THE MESSAGE
***      MVC   QDMLNG,=H'80'      SET THE LENGTH
         MVC   QDMLNG,QDLNELEN    SET THE LENGTH
         L     R15,=V(DISPLAY)    BRANCH TO
         BALR  R14,R15              DISPLAY MODULE
         BR    R8
MOVE0    MVC   PLAY1(0),0(R1)     MOVE DATA TO WORK AREA
MOVE1    MVC   QDMSG+53(0),PLAY1  MOVE DATA TO DISPLAY LINE
TRANSLTE TR    PLAY1(0),TBL1      CHANGE THE DATA TO SPECIAL FORMAT
         EJECT
***********************************************************************
*                                                                     *
*   FORMAT THE CHECKPOINT DATA                                        *
*                                                                     *
***********************************************************************
CKPT000  SR    R5,R5                CLEAR OFFSET REGISTER
CKPT010  ST    R4,STARTADR          SAVE START ADDR OF DATA FOR LINE
         LA    R3,QDMSG             A(DISPLAY LINE)
         MVC   QDMSG,QBLANK         BLANK THE CURRENT LINE
         MVI   0(R3),C'+'           INDICATE RELATIVE
         XC    PLAY,PLAY            CLEAR THE WORK AREA
         ST    R5,PLAY              MOVE OFFSET TO WORK AREA
         UNPK  PLAY(9),PLAY(5)
         TR    PLAY(8),TBL-240      TRANSLATE TO EBCDIC
         MVC   1(4,R3),PLAY+4       MOVE OFFSET TO DISPLAY LINE
         LA    R3,6(R3)             POINT TO NEXT AREA IN DISPLAY LINE
         LA    R7,5                 NO. OF FIELDS PER DISPLAY LINE
CKPT020  XC    PLAY,PLAY            CLEAR THE WORK AREA
         MVC   PLAY(4),0(R4)        MOVE DATA TO WORK AREA
         UNPK  PLAY(9),PLAY(5)
         TR    PLAY(8),TBL-240      TRANSLATE TO EBCDIC
         MVC   0(8,R3),PLAY         AND MOVE TO DISPLAY LINE
         LA    R5,4(R5)             UPDATE THE OFFSET
         LA    R3,9(R3)             UPDATE DISPLAY LINE LOCATION
         LA    R4,4(R4)             POINT AT NEXT 4 BYTES OF DATA
         SH    R2,=H'4'             DECREMENT LENGTH BY 4
         C     R2,=F'0'             ?/ANY DATA LEFT
         BNH   CKPT030              NO. GO DISPLAY THE LINE
         BCT   R7,CKPT020           YES, DO ANOTHER FIELD IF ROOM
         BAL   R8,DISPLAY1          NO ROOM. DISPLAY THIS LINE
         B     CKPT010              GO GET ANOTHER LINE
CKPT030  BAL   R8,DISPLAY1          GO DISPLAY THE LINE
         LA    R2,4008              LENGTH OF CKPT RECORDS
***      LA    R2,4095              LENGTH OF CKPT RECORDS-1
***      LA    R2,1(,R2)            LENGTH OF CKPT RECORDS
         BCT   R6,CKPT010           GO FORMAT THIS BUFFER
         B     STOP                 NO MORE BUFFERS. GO HOME
         EJECT
***********************************************************************
*                                                                     *
*   PREPARE TO READ THE SPOOL PACK                                    *
*                                                                     *
***********************************************************************
READ     ST    R4,QCTRAK
         L     R1,QCIOTA
         L     R15,=V(READSPC)
         BALR  R14,R15
         L     R4,QCIOTA          A(IOT)
         BR    R8
         SPACE 4
***********************************************************************
*                                                                     *
*   RETURN TO THE CALLER                                              *
*                                                                     *
***********************************************************************
STOP     L     R2,DALLREG     RESTORE REG TO DETERMINE NEXT CB
         LTR   R2,R2          ?/IS IT VALID
         BZ    STOP1          NO. EXIT
         LA    R2,4(R2)       YES. UPDATE THE POINTER
         ST    R2,DALLREG     SAVE THE POINTER
         MVC   QDMLNG,=H'0'   INDICATE DISPLAY NOW
         L     R15,=V(DISPLAY) ADDR OF DISPLAY ROUTINE
         BALR  R14,R15        LINK TO IT
         BR    R2             GO TO IT
STOP1    QSTOP
         EJECT
***********************************************************************
*                                                                     *
*   FORMAT OF TABLES FOR JQE, JOE, IOT, PDDB, JCT, JOT, JCTA.         *
*                                                                     *
*        OFFSET    LENGTH         DESCRIPTION                         *
*          0         5              FIELD ID TAG.                     *
*          5         1              X... ....  IF ON INDICATES THE    *
*                                              FIELD IS ALREADY IN    *
*                                              EBCDIC FORMAT.         *
*                                   ...X XXXX  LENGTH OF FIELD.       *
*                                              MAXIMUM LENGTH IS X'1A'*
*                                                                     *
***********************************************************************
***********************************************************************
*                                                                     *
*   DESCRIPTION OF JQE FIELDS                                         *
*                                                                     *
***********************************************************************
JQEDESC  DS    0F
         DC    CL5'PRIO ',X'01'
         DC    CL5'TYPE ',X'01'
         DC    CL5'JOBNO',X'02'
         DC    CL5'FLAGS',X'01'
         DC    CL5'CHAIN',X'03'
         DC    CL5'FLAG2',X'01'
         DC    CL5'JOE  ',X'03'
         DC    CL5'FLAG3',X'01'
         DC    CL5'JCLAS',X'81'
         DC    CL5'IJBNO',X'02'
         DC    CL5'TRAK ',X'04'
         DC    CL5'SPOLS',X'04'
         DC    CL5'INNDE',X'02'
         DC    CL5'EXNDE',X'02'
         DC    CL5'PTNDE',X'02'
         DC    CL5'PRTRT',X'02'
         DC    CL5'PUNDE',X'02'
         DC    CL5'PUNRT',X'02'
         DC    CL5'HLDCT',X'01'
         DC    CL5'HQLOK',X'01'
         DC    CL5'JNAME',X'88'
         DC    CL5'JOEID',X'02'
         DC    CL5'WDSCT',X'02'
         DC    6X'FF'
         EJECT
***********************************************************************
*                                                                     *
*   DESCRIPTION OF JOE FIELDS                                         *
*                                                                     *
***********************************************************************
JOEDESC  DS    0F
         DC    CL5'TYPE ',X'01'
         DC    CL5'NEXT ',X'03'
         DC    CL5'RESV1',X'01'
         DC    CL5'PREV ',X'03'
         DC    CL5'FLAG ',X'01'
         DC    CL5'JQEB ',X'03'
         DC    CL5'FLAG2',X'01'
         DC    CL5'JQNXB',X'03'
         DC    CL5'SEC  ',X'01'
         DC    CL5'CHARB',X'03'
         DC    CL5'CPU  ',X'01'
         DC    CL5'CHNXB',X'03'
         DC    CL5'CURCL',X'81'
         DC    CL5'CKPT ',X'03'
         DC    CL5'PRIO ',X'01'
         DC    CL5'SXMIT',X'03'
         DC    CL5'RECCT',X'04'
         DC    CL5'RECNT',X'04'
         DC    CL5'IOTTR',X'04'
         DC    CL5'DEVID',X'03'
         DC    CL5'PDBCL',X'81'
         DC    CL5'TFLAG',X'01'
         DC    CL5'RNODE',X'02'
         DC    CL5'REMOT',X'02'
         DC    CL5'PDBND',X'02'
         DC    CL5'PDBRT',X'02'
         DC    CL5'JNAME',X'84'
         DC    CL5'JOEID',X'84'
         DC    6X'FF'
         EJECT
***********************************************************************
*                                                                     *
*   DESCRIPTION OF JCT FIELDS                                         *
*                                                                     *
***********************************************************************
JCTDESC  DS    0H
         DC    CL5'ID   ',X'84'
         DC    CL5'LENG ',X'02'
         DC    CL5'FLAG1',X'01'
         DC    CL5'JTFLG',X'01'
         DC    CL5'JBKEY',X'04'
         DC    CL5'SPIOT',X'04'
         DC    CL5'IOT  ',X'04'
         DC    CL5'OCTTR',X'04'
         DC    CL5'XTRK ',X'04'
         DC    CL5'XBUFO',X'04'
         DC    CL5'JQE  ',X'04'
         DC    CL5'SAMSK',X'04'
         DC    CL5'PDDBK',X'02'
         DC    CL5'PDDBO',X'02'
         DC    CL5'CNVRC',X'04'
         DC    CL5'JLOGC',X'04'
         DC    CL5'MSGSC',X'04'
         DC    CL5'USER1',X'04'
         DC    CL5'USER2',X'04'
         DC    CL5'USER3',X'04'
         DC    CL5'USER4',X'04'
         DC    CL5'USER5',X'04'
         DC    CL5'USER6',X'04'
         DC    CL5'USER7',X'04'
         DC    CL5'USER8',X'04'
         DC    CL5'USER9',X'04'
         DC    CL5'USER0',X'04'
         DC    CL5'USERA',X'04'
         DC    CL5'USERB',X'04'
         DC    CL5'USERC',X'04'
         DC    CL5'USERD',X'04'
         DC    CL5'USERE',X'04'
         DC    CL5'USERF',X'04'
         DC    CL5'PRTY ',X'82'
         DC    CL5'JSSTP',X'02'
         DC    CL5'HDSCT',X'02'
         DC    CL5'JSFLG',X'01'
         DC    CL5'SMFLG',X'01'
         DC    CL5'JOBFL',X'01'
         DC    CL5'JBOPT',X'01'
         DC    CL5'RESV1',X'02'
         DC    CL5'JOBID',X'88'
         DC    CL5'JNAME',X'88'
         DC    CL5'PNAME',X'94'
         DC    CL5'MCLAS',X'81'
         DC    CL5'JCLAS',X'81'
         DC    CL5'IPRIO',X'01'
         DC    CL5'PRIO ',X'01'
         DC    CL5'IOPRI',X'01'
         DC    CL5'OPRIO',X'01'
         DC    CL5'RESV2',X'02'
         DC    CL5'RNODE',X'02'
         DC    CL5'RRMT ',X'02'
         DC    CL5'INDEV',X'88'
         DC    CL5'ACCTN',X'84'
         DC    CL5'ROOMN',X'84'
         DC    CL5'ETIME',X'04'
         DC    CL5'ESTLN',X'04'
         DC    CL5'ESTPU',X'04'
         DC    CL5'FORMS',X'84'
         DC    CL5'RESV3',X'01'
         DC    CL5'CPYCT',X'01'
         DC    CL5'RESV4',X'01'
         DC    CL5'LINCT',X'01'
         DC    CL5'PRNOD',X'02'
         DC    CL5'PRRMT',X'02'
         DC    CL5'PUNOD',X'02'
         DC    CL5'PURMT',X'02'
         DC    CL5'PROCN',X'88'
         DC    CL5'PASS ',X'88'
         DC    CL5'NUPAS',X'88'
         DC    CL5'CNVON',X'04'
         DC    CL5'CDTON',X'04'
         DC    CL5'CNVOF',X'04'
         DC    CL5'CDTOF',X'04'
         DC    CL5'XEQON',X'04'
         DC    CL5'XDTON',X'04'
         DC    CL5'XEQOF',X'04'
         DC    CL5'XDTOF',X'04'
         DC    CL5'OUTON',X'04'
         DC    CL5'ODTON',X'04'
         DC    CL5'OUTOF',X'04'
         DC    CL5'ODTOF',X'04'
         DC    CL5'CARDS',X'04'
         DC    CL5'LINES',X'04'
         DC    CL5'PUNCH',X'04'
         DC    CL5'RDSID',X'84'
         DC    CL5'CVSID',X'84'
         DC    CL5'EXSID',X'84'
         DC    CL5'OTSID',X'84'
         DC    CL5'XEQND',X'02'
         DC    CL5'XNODE',X'02'
         DC    CL5'NJSID',X'84'
         DC    CL5'NJTON',X'04'
         DC    CL5'NDTON',X'04'
         DC    CL5'NJTOF',X'04'
         DC    CL5'NDTOF',X'04'
         DC    CL5'NACCT',X'88'
         DC    CL5'NOJID',X'88'
         DC    CL5'NNDEV',X'88'
         DC    CL5'NONDE',X'88'
         DC    CL5'NXNDE',X'88'
         DC    CL5'NNNDE',X'88'
         DC    CL5'NLNDE',X'88'
         DC    CL5'ESOUT',X'04'
         DC    CL5'XOUT ',X'04'
         DC    CL5'TSUID',X'87'
         DC    CL5'TSUAF',X'01'
         DC    CL5'PSN1 ',X'08'
         DC    CL5'PNS2 ',X'08'
         DC    CL5'WORK ',X'1A'
         DC    CL5'     ',X'1A'
         DC    CL5'     ',X'1A'
         DC    CL5'     ',X'1A'
         DC    CL5'     ',X'1A'
         DC    CL5'     ',X'0E'
         DC    CL5'XWRK ',X'1A'
         DC    CL5'     ',X'1A'
         DC    CL5'     ',X'1A'
         DC    CL5'     ',X'02'
         DC    CL5'JMRJN',X'88'
         DC    CL5'RDRON',X'04'
         DC    CL5'RDTON',X'04'
         DC    CL5'CPUID',X'84'
         DC    CL5'USEID',X'88'
         DC    CL5'STEP ',X'01'
         DC    CL5'INDC ',X'01'
         DC    CL5'JTCC ',X'01'
         DC    CL5'CLASS',X'81'
         DC    CL5'UCOM ',X'04'
         DC    CL5'UJVP ',X'04'
         DC    CL5'RDROF',X'04'
         DC    CL5'RDTOF',X'04'
         DC    CL5'JOBIN',X'04'
         DC    CL5'RDR  ',X'02'
         DC    CL5'JMOPT',X'01'
         DC    CL5'RESV3',X'01'
         DC    CL5'XMASK',X'1A'
         DC    CL5'     ',X'06'
         DC    6X'FF'
         EJECT
***********************************************************************
*                                                                     *
*   DESCRIPTION OF ACF2 JCT EXTENSION FIELDS                          *
*                                                                     *
***********************************************************************
JCTADESC DS    0H
         DC    CL5'ID   ',X'84'
         DC    CL5'SAVE ',X'04'
         DC    CL5'FCN  ',X'01'
         DC    CL5'SCN  ',X'01'
         DC    CL5'CNTL ',X'01'
         DC    CL5'RFLG ',X'01'
         DC    CL5'UCB  ',X'04'
         DC    CL5'MSG  ',X'04'
         DC    CL5'LID  ',X'88'
         DC    CL5'RES1 ',X'10'
         DC    CL5'SRCE ',X'88'
         DC    CL5'JOBN ',X'88'
         DC    CL5'SLID ',X'88'
         DC    CL5'PATH ',X'88'
         DC    CL5'PROG ',X'88'
         DC    CL5'RECB ',X'04'
         DC    CL5'RECL ',X'04'
         DC    CL5'UIDB ',X'04'
         DC    CL5'UIDL ',X'04'
         DC    CL5'QNAME',X'04'
         DC    6X'FF'
         EJECT
***********************************************************************
*                                                                     *
*   DESCRIPTION OF IOT FIELDS                                         *
*                                                                     *
***********************************************************************
IOTDESC  DS    0H
         DC    CL5'ID   ',X'84'
         DC    CL5'LENG ',X'02'
         DC    CL5'FLAG1',X'01'
         DC    CL5'FLAG2',X'01'
         DC    CL5'JBKEY',X'04'
         DC    CL5'TRACK',X'04'
         DC    CL5'IOTTR',X'04'
         DC    CL5'JCT  ',X'04'
         DC    CL5'IOT  ',X'04'
         DC    CL5'SJB  ',X'04'
         DC    CL5'SPIOT',X'04'
         DC    CL5'HDSCT',X'02'
         DC    CL5'RESV1',X'02'
         DC    CL5'PDDBP',X'04'
         DC    CL5'MSTAB',X'10'
         DC    6X'FF'
         EJECT
***********************************************************************
*                                                                     *
*   DESCRIPTION OF PDDB FIELDS                                        *
*                                                                     *
***********************************************************************
PDDBDESC DS    0H
         DC    CL5'FLAG1',XL1'01'
         DC    CL5'RECFM',XL1'01'
         DC    CL5'LRECL',XL1'02'
         DC    CL5'MTTR ',XL1'04'
         DC    CL5'DSKEY',XL1'02'
         DC    CL5'CLASS',XL1'81'
         DC    CL5'COPYS',XL1'01'
         DC    CL5'DNODE',XL1'02'
         DC    CL5'DRMT ',XL1'02'
         DC    CL5'USER ',XL1'88'
         DC    CL5'CPU  ',XL1'01'
         DC    CL5'SEC  ',XL1'01'
         DC    CL5'FLAG2',XL1'01'
         DC    CL5'CPTN ',XL1'01'
         DC    CL5'RECCT',XL1'04'
         DC    CL5'FORMS',XL1'84'
         DC    CL5'FCB  ',XL1'84'
         DC    CL5'UCS  ',XL1'84'
         DC    CL5'DSID ',XL1'88'
         DC    CL5'WTRID',XL1'88'
         DC    CL5'RBA  ',XL1'88'
         DC    CL5'CHAR1',XL1'84'
         DC    CL5'CHAR2',XL1'84'
         DC    CL5'CHAR3',XL1'84'
         DC    CL5'CHAR4',XL1'84'
         DC    CL5'FLASH',XL1'04'
         DC    CL5'MODF ',XL1'04'
         DC    CL5'FLSHC',XL1'01'
         DC    CL5'MODFT',XL1'01'
         DC    CL5'COPYG',XL1'08'
         DC    CL5'CKPTP',XL1'02'
         DC    CL5'CKPTL',XL1'02'
         DC    CL5'FLAG3',XL1'01'
         DC    CL5'STAT ',XL1'01'
         DC    CL5'DDNME',XL1'84'
         DC    CL5'CRTME',XL1'04'
         DC    CL5'PLIOT',XL1'04'
         DC    CL5'PLOFF',XL1'04'
         DC    CL5'SSOFM',XL1'84'
         DC    CL5'LINCT',XL1'01'
         DC    CL5'INDEX',XL1'01'
         DC    CL5'FUNC ',XL1'01'
         DC    CL5'RSVD1',XL1'01'
         DC    CL5'RESV1',XL1'10'
         DC    6X'FF'
         EJECT
***********************************************************************
*                                                                     *
*   DESCRIPTION OF JOT FIELDS                                         *
*                                                                     *
***********************************************************************
JOTDESC  DS    0H
         DC    CL5'ID   ',X'84'
         DC    CL5'FREC ',X'02'
         DC    CL5'RESV1',X'02'
         DC    CL5'FREQ ',X'04'
         DC    CL5'CHRQ ',X'04'
         DC    CL5'DUMPQ',X'04'
         DC    CL5'NTWKQ',X'04'
         DC    CL5'LOCAQ',X'04'
         DC    CL5'RMTAQ',X'04'
         DC    CL5'LOCBQ',X'04'
         DC    CL5'RMTBQ',X'04'
         DC    CL5'LOCCQ',X'04'
         DC    CL5'RMTCQ',X'04'
         DC    CL5'LOCDQ',X'04'
         DC    CL5'RMTDQ',X'04'
         DC    CL5'LOCEQ',X'04'
         DC    CL5'RMTEQ',X'04'
         DC    CL5'LOCFQ',X'04'
         DC    CL5'RMTFQ',X'04'
         DC    CL5'LOCGQ',X'04'
         DC    CL5'RMTGQ',X'04'
         DC    CL5'LOCHQ',X'04'
         DC    CL5'RMTHQ',X'04'
         DC    CL5'LOCIQ',X'04'
         DC    CL5'RMTIQ',X'04'
         DC    CL5'LOCJQ',X'04'
         DC    CL5'RMTJQ',X'04'
         DC    CL5'LOCKQ',X'04'
         DC    CL5'RMTKQ',X'04'
         DC    CL5'LOCLQ',X'04'
         DC    CL5'RMTLQ',X'04'
         DC    CL5'LOCMQ',X'04'
         DC    CL5'RMTMQ',X'04'
         DC    CL5'LOCNQ',X'04'
         DC    CL5'RMTNQ',X'04'
         DC    CL5'LOCOQ',X'04'
         DC    CL5'RMTOQ',X'04'
         DC    CL5'LOCPQ',X'04'
         DC    CL5'RMTPQ',X'04'
         DC    CL5'LOCQQ',X'04'
         DC    CL5'RMTQQ',X'04'
         DC    CL5'LOCRQ',X'04'
         DC    CL5'RMTRQ',X'04'
         DC    CL5'LOCSQ',X'04'
         DC    CL5'RMTSQ',X'04'
         DC    CL5'LOCTQ',X'04'
         DC    CL5'RMTTQ',X'04'
         DC    CL5'LOCUQ',X'04'
         DC    CL5'RMTUQ',X'04'
         DC    CL5'LOCVQ',X'04'
         DC    CL5'RMTVQ',X'04'
         DC    CL5'LOCWQ',X'04'
         DC    CL5'RMTWQ',X'04'
         DC    CL5'LOCXQ',X'04'
         DC    CL5'RMTXQ',X'04'
         DC    CL5'LOCYQ',X'04'
         DC    CL5'RMTYQ',X'04'
         DC    CL5'LOCZQ',X'04'
         DC    CL5'RMTZQ',X'04'
         DC    CL5'LOC0Q',X'04'
         DC    CL5'RMT0Q',X'04'
         DC    CL5'LOC1Q',X'04'
         DC    CL5'RMT1Q',X'04'
         DC    CL5'LOC2Q',X'04'
         DC    CL5'RMT2Q',X'04'
         DC    CL5'LOC3Q',X'04'
         DC    CL5'RMT3Q',X'04'
         DC    CL5'LOC4Q',X'04'
         DC    CL5'RMT4Q',X'04'
         DC    CL5'LOC5Q',X'04'
         DC    CL5'RMT5Q',X'04'
         DC    CL5'LOC6Q',X'04'
         DC    CL5'RMT6Q',X'04'
         DC    CL5'LOC7Q',X'04'
         DC    CL5'RMT7Q',X'04'
         DC    CL5'LOC8Q',X'04'
         DC    CL5'RMT8Q',X'04'
         DC    CL5'LOC9Q',X'04'
         DC    CL5'RMT9Q',X'04'
         DC    6X'FF'
         EJECT
***********************************************************************
*                                                                     *
*   FORMAT OF TABLE FOR HCT VARIABLES.                                *
*                                                                     *
*        OFFSET    LENGTH         DESCRIPTION                         *
*          0         8              FIELD ID TAG.                     *
*          8         1              X... ....  IF ON INDICATES THE    *
*                                              FIELD IS ALREADY IN    *
*                                              EBCDIC FORMAT.         *
*                                   ...X XXXX  LENGTH OF FIELD.       *
*                                              MAXIMUM LENGTH IS X'1E'*
*                                                                     *
***********************************************************************
***********************************************************************
*                                                                     *
*   DESCRIPTION OF HCT VARIABLES                                      *
*                                                                     *
***********************************************************************
HCTDESC  DS    0H
         DC    CL8'$MASTERL',XL1'04'
         DC    CL8'$CKRECN ',XL1'02'
         DC    CL8'$WCHECK ',XL1'01'
         DC    CL8'$CKPTFLG',XL1'01'
         DC    CL8'$CKPUSER',XL1'08'
         DC    CL8'$UNSPUNQ',XL1'04'
         DC    CL8'$NEWSIOT',XL1'04'
         DC    CL8'$NEWSTTR',XL1'04'
         DC    CL8'$JQEFREC',XL1'02'
         DC    CL8'$RESV1  ',XL1'02'
         DC    CL8'$JQFREE ',XL1'04'
         DC    CL8'$JQHEADS',XL1'1E'
         DC    CL8'        ',XL1'1E'
         DC    CL8'        ',XL1'1E'
         DC    CL8'        ',XL1'1E'
         DC    CL8'        ',XL1'1E'
         DC    CL8'        ',XL1'1E'
         DC    CL8'        ',XL1'0A'
         DC    CL8'$SPLONON',XL1'04'
         DC    CL8'$SPLEXST',XL1'04'
         DC    CL8'$TGALLOC',XL1'1E'
         DC    CL8'        ',XL1'1E'
         DC    CL8'        ',XL1'06'
         DC    CL8'$DATAKEY',XL1'04'
         DC    CL8'$JOBNO  ',XL1'02'
         DC    CL8'$JOBMIN ',XL1'02'
         DC    CL8'$JOBMAX ',XL1'02'
         DC    CL8'$HASPID ',XL1'84'
         DC    CL8'$CHKPT  ',XL1'86'
         DC    CL8'$CHKPT2 ',XL1'86'
         DC    CL8'$SPOOL  ',XL1'86'
         DC    CL8'$BUFSIZE',XL1'02'
         DC    CL8'$MAXJOBS',XL1'02'
         DC    CL8'$NUMJBNO',XL1'02'
         DC    CL8'$NUMJOES',XL1'02'
         DC    CL8'$MINJOES',XL1'02'
         DC    CL8'$NUMRJE ',XL1'02'
         DC    CL8'$NUMTG  ',XL1'02'
         DC    CL8'$TGSIZE ',XL1'02'
         DC    CL8'$QSENO  ',XL1'02'
         DC    CL8'$NUMNODE',XL1'02'
         DC    CL8'$NODEID ',XL1'01'
         DC    CL8'$SPOLMSG',XL1'01'
         DC    CL8'$RECINCR',XL1'01'
         DC    CL8'$TCELSIZ',XL1'01'
         DC    CL8'$RESV2  ',XL1'02'
         DC    CL8'$RESV3  ',XL1'02'
         DC    CL8'$RESV4  ',XL1'02'
         DC    6X'FF'
         EJECT
***********************************************************************
*                                                                     *
*   MISCELLANEOUS NUTS, BOLTS, ETC                                    *
*                                                                     *
***********************************************************************
TBL1     DC    64C'.',C' ',9C'.',C'Ö.<(+×',X'50',9C'.',C'!$*);^-/'
         DC    9C'.',C',%_>?',10C'.',C':#@',X'7D',C'="'
         DC    65C'.',C'ABCDEFGHI',7C'.',C'JKLMNOPQR'
         DC    8C'.',C'STUVWXYZ'
         DC    6C'.',C'0123456789',6C'.'
TBL      DC    C'0123456789ABCDEF'
JQEHDR   DC    CL80'***  J Q E  ***'
JOEHDR   DC    CL80'***  J O E  ***'
JCTHDR   DC    CL80'***  J C T  ***'
JCTAHDR  DC    CL80'***  ACF2 JCT EXTENSION ***'
IOTHDR   DC    CL80'***  I O T  ***'
PDDBHDR  DC    CL80'***  P D D B  ***'
JOTHDR   DC    CL80'***  JOB OUTPUT TABLE ****'
HCTHDR   DC    CL80'***  HCT CHECKPOINTED VARIABLES  ***'
CKPTHDR  DC    CL80'***  CHECKPOINT  RECORD  ***'
NEXTIOT  EQU   16
         LTORG
         EJECT
JCT      EQU   0
BUFSTART EQU   0
BUFDSECT EQU   0
         $JCT
         $JQE
         $PDDB
         $TAB
         $IOT
         $JOE
         QCOMMON
WORK     DSECT
         DS    CL72
         DS    CL500            BIG FILLER
SWITCH   DS    C
JOEADDR  DS    F
IOTADDR  DS    F
PDDBADDR DS    F
LASTPDDB DS    F
RETURN   DS    F
NEXTOSET DS    F
SAVER15  DS    F
NUMOFVOL DS    F
DALLREG  DS    F
STARTADR DS    F
PLAY     DS    CL16
PLAY1    DS    CL24
         END
