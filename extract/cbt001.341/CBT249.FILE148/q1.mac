QUEUE    QSTART 'QUEUE COMMAND - MAINLINE MODULE',MAIN=YES,REGS=NO
***********************************************************************
*                                                                     *
*   CALL - INITIALIZATION                                             *
*     BUT FIRST VERIFY A 3270 IS BEING USED                           *
*                                                                     *
***********************************************************************
         GTSIZE ,                      READ 3270 SCREEN SIZE
         LTR   R0,R0                   IS THIS A 3270??
         BNZ   DOINIT                  YES
         TPUT  NO3270,L'NO3270,EDIT    TELL USER NOT ALLOWED
         B     EXITNG
         SPACE  1
DOINIT   L     R15,=V(INIT)         ADDR OF INIT
         BALR  R14,R15              GO TO IT
         GTSIZE ,                   READ 3270 SCREEN SIZE
         STM   R0,R1,QDOSZRO        SAVE FOR LATER RESTORE
         SPACE  1
         STFSMODE  ON,INITIAL=YES   GO INTO FULLSCREEN MODE
         SPACE  1
         LM    R0,R1,QDOSZRO        RESTORE DESTROYED REGS         MOD5
         CH    R1,=H'80'            POSSIBLE MOD 2 OR 4 TERMINAL   MOD5
         BE    SETM2                YES SET MOD 2 DEFAULTS         MOD5
         CH    R1,=H'132'           POSSIBLE MOD 5 TERMINAL        MOD5
         BNE   SETM2                NO SET MOD 2 DEFAULTS          MOD5
         CH    R0,=H'27'            ACTUAL MOD 5?                  MOD5
         BNE   SETM2                NO SET MOD 2 DEFAULTS          MOD5
         SPACE  1
MODEL5   MVC   QDLNELEN,=H'132'     LINE LENGTH                    MOD5
         MVC   QDLINES,=PL2'24'     LINES PER SCREEN AREA          MOD5
         MVC   QDSCRLEN,=AL2(24*132)   SCR LENGTH                  MOD5
         MVC   QDSCRPLN,=AL2(24*132+QDLINE1-QDSCREEN)   TPUT LEN   MOD5
         MVI   QDSCRO1,X'7E'        ERASE/WRITE ALTERNATE          MOD5
         MVC   QDSCRO2,=X'C2C4'     (2,1)                          MOD5
         MVC   QDSCRO3,=X'F5E8'     (27,1)                         MOD5
         MVC   QDSCRO4,=X'F66F'     (27,72)                        MOD5
         MVC   QDSCRO5,=X'C4C8'     (3,1)                          MOD5
         B     RESUME               RESUME ORIG PROG               MOD5
         SPACE  1
SETM2    STSIZE  SIZE=80,LINE=24    FORCE MODEL 2 DEFAULT          MOD5
         EJECT
RESUME   LA    R10,LOOP       INTERRUPTED RETURN ADDRESS
         USING QDISPLAY,R9    BASE REG FOR DISPLAY WORK AREA
         L     R9,QVDSPL      LOAD BASE REG
         L     R3,8(R2)       LOAD ADDRESS OF CPPL: PSCB           FCI*
         USING PSCB,R3                                             FCI*
CHKOPER  TM    PSCBATR1,PSCBCTRL DOES USER HAVE OPER PRIVILEDGE    FCI*
         BNO   CKPT           NO. SKIP AUTH SETUP              HWC FCI*
         DROP  R3                                                  FCI*
         OI    QXAUTH,X'80'   INDICATE OPER PRIV USER              FCI*
         OI    QXAUTH,1       INDICATE PASSWORD ENTERED
         B     CKPT                                                HWC
***********************************************************************
*                                                                     *
*   LOOK THRU TABLE FOR COMMANDS WHICH DONT NEED CHECKPOINT READS HWC *
*                                                                 HWC *
***********************************************************************
LOOP     EQU   *              CHECK FOR COMMANDS WHICH DONT       HWC
         LA    R15,QUICKTBL   NEED CHECKPOINT PROCESSING          HWC
LOOP1    CLC   QDREPLY(2),0(R15)                                  HWC
         BE    PARSE                                              HWC
         LA    R15,2(,R15)                                        HWC
         CLI   0(R15),X'FF'                                       HWC
         BNE   LOOP1                                              HWC
***********************************************************************
*                                                                     *
*   CALL - READ JES2 CHECKPOINT FILE                                  *
*                                                                     *
***********************************************************************
CKPT     L     R15,=V(CKPT)   ADDR OF CKPT
         BALR  R14,R15        GO TO IT
***********************************************************************
*                                                                     *
*   CALL - COMMAND LINE PARSE                                         *
*                                                                     *
***********************************************************************
PARSE    L     R15,=V(PARSE)  ADDR OF PARSE
         BALR  R14,R15        GO TO IT
         CLC   =C'E ',QSUBNAME STOP?
         BE    EXIT           YES.
         CLC   =C'EXIT ',QSUBNAME STOP?
         BE    EXIT           YES.
         CLC   =C'END ',QSUBNAME STOP?
         BE    EXIT           YES.
         CLC   =C'STOP ',QSUBNAME STOP?
         BE    EXIT           YES.
         CLC   =C'Z ',QSUBNAME STOP?
         BE    EXIT           YES.
***********************************************************************
*                                                                     *
*   CALL - SUB-COMMAND MODULE SELECTED BY PARSE                       *
*                                                                     *
***********************************************************************
         MVC   QDHLINE,DUMMY  NO OUTPUT MESSAGE
         L     R15,QSUBCMD    ADDR OF SUBCMD FROM QCOMMON
         BALR  R14,R15        GO TO IT
         MVC   QDMLNG,=H'0'   ZERO OUT MESSAGE LENGTH
         L     R15,=V(DISPLAY) ADDR OF DISPLAY MODULE
         BALR  R14,R15        WRITE LAST SCREEN, GET NEXT INPUT
         B     LOOP           DO IT AGAIN
***********************************************************************
*                                                                     *
*   CLEAN UP AND GO HOME                                              *
*                                                                     *
***********************************************************************
EXIT     STSIZE  SIZELOC=QDOSZR1,LINELOC=QDOSZRO  RESTORE SCRSIZE  MOD5
         TPUT  CLEAR,L'CLEAR,FULLSCR,MF=(E,QTPUT) CLEAR SCREEN     PWF*
         USING QCKPT,R8                 BASE REG FOR CKPT WORK AREA
         L     R8,QVCKPT                LOAD BASE REG
         CLOSE MF=(E,HOCKPT)
         L     R1,QGETA3              CLOSE HASPACE
         CLOSE MF=(E,(1))               DATASETS
         TM    QPFLAG,HARDCPY         IS HARDCOPY INVOKED?         FCI*
         BNO   CHKAUTH                  NO..SPLIT THIS STUFF       FCI*
         L     R15,=V(PRINT)               INVOKE PRINT            FCI*
         MVC   QDREPLY,QBLANK                  TO                  FCI*
         MVC   QDREPLY(09),=C'PRINT OFF'          FREE UP          FCI*
         MVC   QDRLNG,=X'0009'                        HARDCOPY     FCI*
         BALR  R14,R15                                     OUTPUT  FCI*
         SPACE 2
***********************************************************************
*                                                                     *
*   TURN OFF AUTHORIZATION IF TURNED ON TO BE NEAT                    *
*                                                                     *
***********************************************************************
CHKAUTH  TM    QXAUTH,X'40'       WAS AUTHORIZATION TURNED ON
         BNO   FREEUP             NO  BYPASS SVC CALL
         SR    R0,R0              RELEASE AUTHORIZATION
         SVC   235                CALL SVC
         EJECT
***********************************************************************
*                                                                  FCI*
*   FREE HASPCKPT AND HASPACEN DDNAMES BEFORE LEAVING TO BE NEAT.. FCI*
*                                                                  FCI*
***********************************************************************
FREEUP   MVI   DAIRFLAG,X'18'    INDICATE FREE DDNAME(XXXXXXXX)    FCI*
         MVC   DA18DDN,HASPCKPT+40  GET DDNAME USED...             FCI*
         L     R15,=V(ALLOCATE)  GET ROUTINE NAME                  FCI*
         BALR  R14,R15           GO FREE IT..                      FCI*
*
         LA    R2,QCSPOOLS       GET ADDR OF LIST OF HASPACE DCBS  FCI*
         LA    R4,$MAXDA         MAX OF 32 PASSES THROUGH HERE..
FREEUP1  L     R3,0(R2)          GET ADDRESS OF DCB TO WORK ON     FCI*
         LTR   R3,R3             ANYONE THERE?                     FCI*
         BZ    EXITQCK                                             FCI*
         MVC   DA18DDN,40(R3)    MOVE IN DDNAME FROM DCB           FCI*
         L     R15,=V(ALLOCATE)  GET ROUTINE NAME                  FCI*
         BALR  R14,R15           AND GO INVOKE DAIR TO FREE IT..   FCI*
         LA    R2,4(R2)          BUMP                              FCI*
EXITQCK  BCT   R4,FREEUP1                                          FCI*
***********************************************************************
*                                                                     *
*          FREE THE AREAS ACQUIRED IN INIT (Q10)                      *
*                                                                     *
***********************************************************************
         OI    QGETL4,X'80'   PREPARE FOR FREEMAIN             PWF FCI*
         FREEMAIN MF=(E,QFREE)
         SPACE 2
***********************************************************************
*                                                                     *
*   DELETE QUEUE COMMON TO BE NEAT                                    *
*                                                                     *
***********************************************************************
*        DELETE  EP=QUEUECMN      DELETE QUEUE COMMON LOADED IN INIT RX
         FREEMAIN R,LV=QLENGTH,A=(11)                                RX
         STFSMODE  OFF            COME OUT OF FULLSCREEN MODE
EXITNG   QSTOP
***********************************************************************
*                                                                     *
*   CONSTANTS AND OTHER ODDITIES                                      *
*                                                                     *
***********************************************************************
         LTORG
**CLEAR  DC    X'C1115D7E1140403C4040001DC813'  *** SPF FULLSCRN
CLEAR1   EQU   *                     START OF CLEAR DATA           MOD5
         DC    X'115D7E'             SBA 24,80                     MOD5
         DC    X'114040'             SBA 1,1                       MOD5
         DC    X'3C404000'           RTA 1,1 WITH NULLS            MOD5
         DC    X'1DC8'               SF, INTENSIFIED               MOD5
         DC    X'13'                 INSERT CURSOR                 MOD5
CLEAR    EQU   CLEAR1,*-CLEAR1
DUMMY    DC    CL80'    NO DATA IS AVAILABLE FOR YOUR REQUEST'
NO3270   DC    CL80'  QUEUE ONLY FOR 3270 TYPE DEVICES  '
QUICKTBL DC    C'HEFAXFTOBOUPENSAXPXBCODCXIB C D E F U - @ + HBHFPFPB'
         DC    C'MD'                                               HWC
         DC    C'hefaxftoboupensaxpxbcodcxib c d e f u hbhfpfpb'   HWC
         DC    X'FFFFFFFFFFFFFF' LEAVE SOME ZAPPING ROOM           HWC
         PRINT OFF
         COPY  $HASPGEN
         PRINT ON
         QCOMMON
QLENGTH  EQU   *-QCOMMON                                           RX
         SPACE 2
         IKJPSCB                                                   FCI*
         END
