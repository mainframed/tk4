         MACRO
&NAME    LINKOUT &RC=,&NRBYTES=72     DOCUMENTATION IN MACRO SOURCE
.*
.*       LINKIN AND LINKOUT ARE COMPLEMENTARY MACROS WHICH ACCOMPLISH
.*         STANDARD LINKAGE CONVENTIONS INTO AND OUT OF ASSEMBLER
.*         PROGRAMS.
.*       LINKOUT WILL FREE THE REGISTER SAVE AREA ACQUIRED BY LINKIN,
.*         OPTIONALLY SET A RETURN CODE IN REGISTER 15, AND EXIT VIA
.*         A BRANCH TO REGISTER 14.
.*       LINKOUT PARAMETERS ARE ALL OPTIONAL KEYWORDS AS FOLLOWS:
.*         RC - RETURN CODE TO BE PLACED IN REGISTER 15. TO BE CODED
.*               EXACTLY AS IN IBM MACRO 'RETURN'.
.*               DEFAULT - REGISTER 15 WILL BE CLEARED TO ZERO.
.*               TO KEEP A PRE-LOADED REGISTER 15 INTACT, CODE RC=(15)
.*         NRBYTES - LENGTH OF AREA TO BE FREED.
.*               DEFAULT - 72 BYTES.
.*               YOU NEED CODE NRBYTES ONLY IF YOU ACQUIRED A LARGER
.*               AREA USING LINKIN FOR USE AS AN ATTACHED REENRANT
.*               WORKAREA.
.*
*              *** LINKOUT ***  DOCUMENTATION IN MACRO SOURCE LISTING.
.*
&NAME    LR    01,13                       REG 1 ADDRESS FOR DELETE
         L     13,4(13)                    REG 13 ADDRESS FOR RESTORE
         FREEMAIN R,LV=&NRBYTES,A=(1)
         AIF   (T'&RC EQ 'O').RCOMIT
         RETURN (14,12),RC=&RC
         AGO   .CONT
.RCOMIT  ANOP
         RETURN (14,12),RC=0
         MNOTE '*************** RC DEFAULT.  RETURN CODE WILL BE 0.'
.CONT    ANOP
         MEND
