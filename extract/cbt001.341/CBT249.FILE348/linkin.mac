         MACRO
&NAME    LINKIN &NRBYTES=72,&BASEREG= DOCUMENTATION IN MACRO SOURCE
.*
.*       LINKIN AND LINKOUT ARE COMPLEMENTARY MACROS WHICH ACCOMPLISH
.*         STANDARD LINKAGE CONVENTIONS INTO AND OUT OF ASSEMBLER
.*         PROGRAMS.
.*       LINKIN WILL ACQUIRE A REGISTER SAVE AREA AND OPTIONALLY
.*         ESTABLISH ADDRESSABILITY ASSUMING THE INITIAL CONTENTS OF
.*         REGISTER 15 TO BE THE ENTRY POINT TO THE PROGRAM, IN
.*         ACCORDANCE WITH IBM LINKAGE CONVENTIONS.
.*       ORIGINAL CONTENTS OF REGISTERS 12, 13 AND 15 MAY BE DESTROYED
.*         AND MUST BE RECOVERED FROM REGISTER SAVE AREA IF NEEDED.
.*       LINKIN PARAMETERS ARE ALL OPTIONAL KEYWORDS AS FOLLOWS:
.*         NRBYTES - LENGTH OF AREA TO BE OBTAINED FOR LINKAGE ETC.
.*               DEFAULT - 72 BYTES.
.*               YOU NEED CODE NRBYTES ONLY IF YOU WISH TO ACQUIRE MORE
.*               THAN THE REQUIRED 72 BYTE LINKAGE SAVE AREA. ANY EXTRA
.*               AREA SHOULD BE ASSUMED TO START AT R13 +72.
.*         BASEREG - BASE REGISTER TO BE USED IN ESTABLISHING
.*               ADDRESSABILITY. ONLY A SINGLE REGISTER IS SUPPORTED.
.*               DEFAULT - NO ADDRESSABILITY IS ESTABLISHED.
.*
*              *** LINKIN ***  DOCUMENTATION IN MACRO SOURCE LISTING.
.*
Z&SYSNDX DS    0H
         USING *,15
&NAME    B     28(15)                  BRANCH AROUND MODULE NAME
         DC    CL8'&SYSECT'            CSECT NAME
         DC    CL8'&SYSDATE'           DATE ASSEMBLED
         DC    CL8'&SYSTIME'           TIME ASSEMBLED
         STM   14,12,12(13)            STORE CALLERS REGS
         LR    12,01                   SAVE CONTENTS OF REGISTER 1
         GETMAIN R,LV=&NRBYTES         GET LINKAGE AREA FOR THIS MODULE
         ST    13,4(1)                 STORE CALLING AREA ADR IN CALLED
         ST    1,8(13)                 STORE CALLED AREA ADR IN CALLING
         LR    13,1                    R13 LINKS TO THIS MODULE'S AREA
         LR    01,12                   RESTORE CONTENTS OF REGISTER 1
         DROP  15
         AIF   (T'&BASEREG EQ 'O').MACEND
         AIF   ('&BASEREG' EQ '15').IFFISH
         AIF   ('&BASEREG' EQ '14').LOADREG
         AIF   ('&BASEREG' EQ '13').NOT13
         AIF   ('&BASEREG' EQ '12').LOADREG
         AIF   ('&BASEREG' EQ '11').LOADREG
         AIF   ('&BASEREG' EQ '10').LOADREG
         AIF   ('&BASEREG' EQ '09').LOADREG
         AIF   ('&BASEREG' EQ '08').LOADREG
         AIF   ('&BASEREG' EQ '07').LOADREG
         AIF   ('&BASEREG' EQ '06').LOADREG
         AIF   ('&BASEREG' EQ '05').LOADREG
         AIF   ('&BASEREG' EQ '04').LOADREG
         AIF   ('&BASEREG' EQ '03').LOADREG
         AIF   ('&BASEREG' EQ '02').LOADREG
         AIF   ('&BASEREG' EQ '01').IFFISH
         AIF   ('&BASEREG' EQ '9').LOADREG
         AIF   ('&BASEREG' EQ '8').LOADREG
         AIF   ('&BASEREG' EQ '7').LOADREG
         AIF   ('&BASEREG' EQ '6').LOADREG
         AIF   ('&BASEREG' EQ '5').LOADREG
         AIF   ('&BASEREG' EQ '4').LOADREG
         AIF   ('&BASEREG' EQ '3').LOADREG
         AIF   ('&BASEREG' EQ '2').LOADREG
         AIF   ('&BASEREG' EQ '1').IFFISH
.NOT13   MNOTE 12,'BASEREG MUST BE 1 (OR 01) THROUGH 15, EXCLUDING 13, X
               IF SPECIFIED.  MACRO EXPANSION TERMINATED'
         MEXIT
.IFFISH  MNOTE 04,'REGISTERS 01 AND 15 ARE NOT RECOMMENDED AS BASE REGIX
               STERS.'
.LOADREG L     &BASEREG,4(13)
         L     &BASEREG,16(&BASEREG)
.USEREG  USING Z&SYSNDX,&BASEREG
.MACEND  MEND
