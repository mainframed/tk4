         CVT   DSECT=YES
UCB      DSECT
         IEFUCBOB
VOLSER   CSECT
         STM   14,12,12(13)
CVTBASE  EQU   2
UCBPTR   EQU   3
UCBHTAB  EQU   4
         USING CVT,CVTBASE
         USING UCB,UCBPTR
         LR    12,15
         USING VOLSER,12
PREFLEN  EQU   7
PREFPTR  EQU   1
         LA    PREFLEN,5(PREFPTR)
BLOOP    DS    0H
         CLI   0(PREFLEN),C' '
         BNE   BEND
         BCT   PREFLEN,BLOOP
BEND     DS    0H
         SR    PREFLEN,PREFPTR
         BL    QUIT
         L     CVTBASE,CVTPTR
         L     UCBHTAB,CVTILK2
LOOP     DS    0H
         LH    UCBPTR,0(UCBHTAB)
         LTR   UCBPTR,UCBPTR
         BL    QUIT
         LA    UCBHTAB,2(UCBHTAB)
         TM    UCBSTAT,UCBONLI         IS VOLUME ONLINE
         BZ    LOOP                    NO, SKIP IT
         TM    UCBTBYT3,UCB3DACC       DASD ?
         BZ    LOOP                    NOT INTERESTED IN NON DASD
         CLC   0(1,PREFPTR),UCBVOLI    EXECUTED COMPARISON
         EX    PREFLEN,*-6
         BNE   LOOP
         MVC   0(L'UCBVOLI,PREFPTR),UCBVOLI
         B     BR14
QUIT     DS    0H
         MVC   0(L'UCBVOLI,PREFPTR),BLANK8
BR14     DS    0H
         LM    14,12,12(13)
         SLR   15,15
         BR    14
BLANK8   DC    CL8' '
         END
