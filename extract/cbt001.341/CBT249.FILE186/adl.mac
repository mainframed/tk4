         MACRO
&NAME    ADL   &P1,&P2,&P3,&P4,&P5,&P6,&P7,&P8,&P9,&P10,&P11,&P12,     *
               &ALIGN=3
         LCLA  &N,&SIZE
         LCLC  &SYMB
         AIF   (&ALIGN LE 3 AND &ALIGN GE 0).ALOK
         MNOTE 8,'IMPROPER BOUNDARY ALIGNMENT - &ALIGN'
         MEXIT
.ALOK    ANOP
&N       SETA  1
         DS    0D                  ALIGN ON DOUBLE WORD BOUNDARY
         DS    &ALIGN.H            SPACE OVER AS REQUIRED
&SIZE    SETA  N'&SYSLIST*8
&NAME    DC    AL2(&SIZE)          LENGTH OF ADL
         DS    0D                  ENSURE ALIGNMENT
.LOOP    ANOP
&SYMB    SETC  '&SYSLIST(&N)'
         AIF   ('&SYMB' EQ '').DONE
         AIF   ('&SYMB' NE '0').A1
         DC    D'0'                DUMMY ENTRY
         AGO   .BUMP
.A1      DC    CL8'&SYMB'          DDNAME
.BUMP    ANOP
&N       SETA  &N+1
         AGO   .LOOP
.DONE    MEND
