         MACRO
&N       COMMAND &NAME,&CDL=0,&ALIAS=,&LAST=NO
         GBLA  &NCMDS
         GBLB  &SW(20)
         GBLC  &CMDS(20)
         LCLA  &I
         AIF   (&SW(1)).A1         CHECK IF NOT FIRST COMMAND MACRO
GPPCMDS  CSECT
DA08SHR  EQU   X'08'    DATA SET IS SHR
DA08NEW  EQU   X'04'    DATA SET IS NEW
DA08MOD  EQU   X'02'    DATA SET IS MOD
DA08OLD  EQU   X'01'    DATA SET IS OLD
DA08KEEP EQU   X'08'    DS DISP IS KEEP
DA08DEL  EQU   X'04'    DS DISP IS DELETE
DA08CAT  EQU   X'02'    DS DISP IS CATLG
DA08UCAT EQU   X'01'    DS DISP IS UNCATALOG
DA08KEP  EQU   X'08'
DA08DELE EQU   X'04'
DA08CATL EQU   X'02'    DS DISP OF CATLG DESIRED
DA08UNCT EQU   X'01'    DS DISP OF UNCATLG DESIRED
DA08TRKS EQU   X'80'    UNITS ARE TRKS
DA08ABKL EQU   X'40'
DA08CYLS EQU   X'C0'    UNITS ARE CYLS
DA08UID  EQU   X'20'    USER ID IS TO BE PREFIXED
DA08RLSE EQU   X'10'    RELEASE (RLSE) REQUESTED
DA08PERM EQU   X'08'    DATA SET MUST BE SPECIFICALLY UNALLOCATED
DA08DMMY EQU   X'04'
PDS      EQU   X'02'               BIT ON FOR A PDS
OLD      EQU   X'01'          DS MUST BE OLD
MOD      EQU   X'02'
SHR      EQU   X'08'
&SW(1)   SETB  1                   INDICATE WE'VE BEEN HERE
.A1      AIF   (NOT &SW(2)).A2     CHECK IF END OF CMD MACROS SET
         MNOTE 8,'COMMAND MACRO INVOKED AFTER END OF COMMAND LIST'
         MEXIT
.A2      ANOP
&N       DS    0F
         AIF   ('&ALIAS' EQ '').A
         DC    CL8'&ALIAS'
         DC    A(&CDL)
&NCMDS   SETA  &NCMDS+1
&CMDS(&NCMDS)  SETC  '&ALIAS'
.A       AIF   ('&LAST' EQ 'YES').B
         DC    CL8'&NAME'
         DC    A(&CDL)
&NCMDS   SETA  &NCMDS+1
&CMDS(&NCMDS)  SETC  '&NAME'
         MEXIT
.B       ANOP
&SW(2)   SETB  1
         AIF   ('&NAME' EQ '').C
&NCMDS   SETA  &NCMDS+1
&CMDS(&NCMDS)  SETC  '&NAME'
.C       ANOP
         DC    CL8'&NAME '
         DC    AL1(X'FF'),AL3(&CDL)
         SPACE 3
         MNOTE *,'THE FOLLOWING &NCMDS COMMANDS MAY BE USED TO INVOKE'
         MNOTE *,' THIS PROMPTER - '
.X       ANOP
&I       SETA  &I+1
         MNOTE *,'       &CMDS(&I)'
         AIF   (&I LT &NCMDS).X
         MEND
