         MACRO
&LABEL   DYNABLD &ADDR,&PARMS
.*--------------------------------------------------------------------*
.* M.SCHERFLING   GTE LABORATORIES  1/83                              *
.*                                                                    *
.* MACRO BUILDS THE DYNAMIC ALLOCATION CONTROL BLOCK                  *
.* PARAMETER: ADDR  - LOCATION TO BUILD LIST                          *
.*                    * - HERE  (&LABEL THEN REQUIRED)                *
.*                    #.. OR $..  - DSECT ADDR                        *
.*                                                                    *
.*            PARMS - LIST (IN PARENTHESIS) OF NAMES OF CONTROL BLKS  *
.*   TO BUILD IN LIST                                                 *
.*                                                                    *
.* MNOTE IF ANY OPERANDS ARE MISSING                                  *
.*--------------------------------------------------------------------*
.*
         LCLA  &END,&CNT,&OFFSET
.*
         AIF   ('&ADDR'   EQ '').NOADDR
         AIF   ('&PARMS' EQ '').NOPARMS
&LABEL   DS    0H                 TEXT POINTER CHAIN
.*
         AIF   ('&ADDR'(1,1) NE '#' AND '&ADDR'(1,1) NE '$').GOLOOP
         LA    1,&ADDR            LOAD POINTER TO CHAIN AREA
.GOLOOP  ANOP
&END     SETA  N'&PARMS
&CNT     SETA  1
.LOOP    AIF   (&CNT   GE  &END).ENDLOOP
         AIF   ('&ADDR' NE '*').TRYDSCT
         DC    A(&PARMS(&CNT))    TEXT UNIT &CNT
         AGO   .INCRCNT
.TRYDSCT LA    0,&PARMS(&CNT)     LOAD PARM NUMBER &CNT
         ST    0,&OFFSET.(1)      STORE IN CHAIN
&OFFSET  SETA  &OFFSET+4
.INCRCNT ANOP
&CNT     SETA  &CNT+1
         AGO   .LOOP
.ENDLOOP AIF   ('&ADDR' NE '*').TRYDST2
         DC    A(X'80000000'+&PARMS(&CNT)) TEXT UNT &CNT
         MEXIT
.TRYDST2 LA    0,&PARMS(&CNT)     LOAD PARM NUMBER &CNT
         ST    0,&OFFSET.(1)      STORE IN CHAIN
         MVI   &OFFSET.(1),X'80'  SET HIGH BYTE
         MEXIT
.NOADDR  MNOTE 8,'SUPPLY ADDRESS OF TEXT UNIT POINTERS'
         MEXIT
.NOPARMS MNOTE 8,'SUPPLY TEXT UNITS TO CREATE POINTER CHAIN'
         MEND
