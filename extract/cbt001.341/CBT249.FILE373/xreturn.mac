         MACRO
&LABEL   XRETURN &RTN=,&REEN=
.*********************************************************************
.*  DATE     01/81                                                   *
.*  AUTHOR   MARK SCHERFLING                                         *
.*  MACRO    XRETURN                                                 *
.*                                                                   *
.*  FUNCTION: USING STANDARD LINKAGE, RETURN A CALLING ROUTINES      *
.*     REGISTERS AND RETURN CONTROL TO THAT ROUTINE                  *
.*                                                                   *
.*  PARAMETERS:                                                      *
.*     RTN=  RC    - SAVE REGISTER 15 THAT CONTAINS A RETURN CODE    *
.*           VALUE - SAVE REGISTER 0 THAT CONTAINS A COMPUTED VALUE  *
.*           BOTH  - SAVE BOTH REGISTER 15 AND 0                     *
.*                 - DEFAULT IS RETURN ALL SAVED REGISTERS           *
.*     REEN= VALUE - NUMBER OF BYTES USED FOR A LOCAL SAVE AREA      *
.*                   (INCLUDING THE 72 BYTES USED FOR REG SVAREA     *
.*********************************************************************
.*
         AIF   ('&REEN' EQ '').NOFREE
&LABEL   L     12,4(,13)          SAVE ADDR OF NEXT SAVE AREA
         LR    10,0               SAVE REG 0
         LR    11,15              SAVE REG 15 - MAY HAVE RC
         LA    0,&REEN            LOAD USER STORAGE AREA
         LR    1,13               MOVE ADDR OF AREA TO FREE
         FREEMAIN  R,LV=(0),A=(1) FREE THE SAVE AREA
         LR    0,10               RETURN THE REGISTER VALUE
         LR    15,11              RETURN RC REGISTER
         LR    13,12              RETURN ADDR OF SAVE AREA
.*
         AGO   .NEXT
.NOFREE  ANOP
&LABEL   L     13,4(,13)          LOAD CALLERS SAVE AREA
.*
.NEXT    AIF   ('&RTN' EQ '').SAVEALL
         AIF   ('&RTN' EQ 'RC').SVRC
         AIF   ('&RTN' EQ 'VALUE').SVVAL
         AIF   ('&RTN' EQ 'BOTH').SVBOTH
         MNOTE 8,'** INVALID RETURN SPECIFICATION'
         MEXIT
.SAVEALL LM    14,12,12(13)       RETURN ALL THE REGISTERS
.*
.RETURN  BR    14                 RETURN TO CALLER
         MEXIT
.SVRC    L     14,12(,13)         RETURN REG 14
         LM    0,12,20(13)        PASS OVER REG 15
         AGO   .RETURN
.SVVAL   LM    14,15,12(13)       RETURN 14 AND 15
         LM    1,12,24(13)        PASS OVER REG 0
         AGO   .RETURN
.SVBOTH  L     14,12(,13)         RETURN 14
         LM    1,12,24(13)        PASS OVER 15 AND 0
         AGO   .RETURN
         MEND
