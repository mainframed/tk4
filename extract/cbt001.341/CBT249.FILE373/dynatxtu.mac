         MACRO
&LABEL   DYNATXTU  &CODE,&LEN,&PARM
.*--------------------------------------------------------------------*
.* M.SCHERFLING  GTE LABORATORIES  1/83                               *
.*                                                                    *
.* MACRO CREATES DYNAMIC ALLOCATION TEXT UNIT                         *
.* ** WARNING ** MACRO NOT DESIGNED FOR MULTIBLE PARAMETER TEXT UNITS *
.*                                                                    *
.* PARMS: &LABEL - NAME ASSOCIATED WITH TEXT UNIT                     *
.*        &CODE  - TEXT UNIT IDENTIFIER, CAN BE THE DYNALLOC EQUATES  *
.*        &LEN   - LENGTH OF PARMETER. IF OMITED, IT WILL BE CALC'ED  *
.*        &PARM  - PARM VALUE, CAN BE QUOTED                          *
.*                 PREFIX: C - CHARACTER PARM VALUE                   *
.*                         X - HEX PARM VALUE                         *
.*                         F - FILL VALUE (SPECIFIED IN HEX)          *
.* NOTE : TEXT UNIT PARM COUNT DEFAULTS TO 1                          *
.*        IF LENGTH OMITTED, DEFAULTS TO ONE IF PARAMETER IS SPECIFIED*
.*                                                                    *
.* MNOTE IF &LABEL, OR &CODE MISSING                                  *
.*       IF PARM CODE NOT C OR X OR F                                 *
.*                                                                    *
.* CHANGES:                                                           *
.*         3/8/83  CHANGED &CODE FROM XL2' '  TO AL2( ) SO THAT       *
.*                 IEFZB4D2 SYMBOLS FOR KEYS CAN BE USED INSTEAD OF   *
.*                 STRAIGHT HEX CODES.  -- RIC FORD                   *
.*        3/17/83  CHANGED &LEN  FROM XL2' '  TO AL2( ) SO THAT       *
.*                 DECIMAL LENGTHS ARE USED INSTEAD OF HEX LENGTHS.   *
.*                                      -- RIC FORD                   *
.*                                                                    *
.*--------------------------------------------------------------------*
.*
         LCLC  &PARMX,&T
         LCLA  &LENX
         LCLB  &F
         AIF   ('&LABEL' EQ '').NOLABL
         AIF   ('&CODE' EQ '').NOCODE
.*
&LABEL   DS    0H                START OF TEXT UNIT
         DC    AL2(&CODE)        TEXT UNIT ID
         AIF   ('&LEN' NE '').GOCOUNT
         AIF   ('&PARM' NE '').GETLEN
         DC    XL2'00'           NO PARM DEFINITION
         MEXIT
.GOCOUNT DC    XL2'01'           PARM COUNT
         DC    AL2(&LEN)         PARM LENGTH * DEFAULT
&F       SETB  1                 FLAG SET TO INDICATE ALREADY DONE
         AGO   .PARM
.GETLEN  DC    XL2'01'           PARM COUNT
.*
.PARM    AIF   ('&PARM'(1,1) EQ 'C').GOCHAR
         AIF   ('&PARM'(1,1) EQ 'X').GOHEX
         AIF   ('&PARM'(1,1) EQ 'F').GOHEX
         AGO   .BADTYPE
.GOCHAR  ANOP
&T       SETC  'C'
         AIF   (&F).GOPARM
&LENX    SETA  (K'&PARM)-1
         AGO   .GOPARM
.GOHEX   ANOP
&T       SETC  'X'
         AIF   (&F).GOPARM
&LENX    SETA  ((K'&PARM)-1)/2
.GOPARM  ANOP
&PARMX   SETC  '&PARM'(2,(K'&PARM)-1)
         AIF   (NOT &F).DOLEN
         AIF   ('&PARM'(1,1) EQ 'F').DOFILL1
         DC    &T.L&LEN.'&PARMX' PARM VALUE
         MEXIT
.DOFILL1 DC    &LEN.&T.'&PARMX'  PARM VALUE
         MEXIT
.DOLEN   DC    AL2(&LENX)        PARM LENGTH
         AIF   ('&PARM'(1,1) EQ 'F').DOFILL2
         DC    &T.L&LENX.'&PARMX'      PARM VALUE
         MEXIT
.DOFILL2 DC    &LEN.&T.'&PARMX'  PARM VALUE
         MEXIT
.NOLABL  MNOTE 8,'LABEL MISSING FOR TEXT UNIT NAME'
         MEXIT
.NOCODE  MNOTE 8,'TEXT UNIT CODE MISSING'
         MEXIT
.BADTYPE MNOTE 8,'PREFIX OF PARM TYPE MUST BE "C" OR "X" OR "F"'
         MEND
