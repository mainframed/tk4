AMDMAC   TITLE 'MACLIB - MAP STORAGE LOCATIONS USING DSECTS'
***********************************************************************
         SPACE 3
         PRINT GEN
*-------------------------------------------------------------
*
*    AUTHOR: HEWITT WRIGHT
*            SENIOR SYSTEMS ENGINEER -XT
*            AMDAHL CORPORATION
*
* FUNCTION - MAPS AN AREA ACCORDING TO THE SPECIFIED DSECT AND
*            MACRO.
*
* SYNTAX     MACLIB  ADDRESS=XXXXXX,
*                    MACRO=<MACRO NAME>,
*                    DSECT=<DSECT NAME>,
*                    ASID=<ASID>,
*                    FROM=<FROM SYMBOL>,
*                    TO=<TO SYMBOL>,
*                    COMMENTS/NOCOMMENTS,  (DEFAULT=NOCOMMENTS)
*                    HEX/NOHEX,            (DEFAULT=NOHEX)
*                    SNAP                  (DEFAULT=NO STORAGE DUMPS)
*
*//MACLIB DD DSN=SYS1.MACLIB,DISP=SHR
*//       DD DSN=SYS1.AMODGEN,DISP=SHR
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
*        R0  = WORK REGISTER
*        R1  = PARAMETER LIST POINTER & WORK REGISTER
*        R2  = WORK REGISTER
*        R3  = WORK REGISTER AND I/O LOGICAL RECORD POINTER
*        R4  = WORK REGISTER AND DCB ADDR WHEN REQUIRED
*        R5  = WORK REGISTER
*        R6  = WORK REGISTER
*        R7  = WORK REGISTER
*        R8  = WORK REGISTER
*        R9  = WORK REGISTER
*        R10 = SECOND BASE REGISTER
*        R11 = ARDA'S DSECT BASE
*        R12 = FIRST BASE REGISTER
*        R13 = SAVE AREA ADDRESS & MACLIB'S WORK AREA ADDRESS
*        R14 = WORK REGISTER AND LINK REG
*        R15 = ENTRY POINT AND WORK REGISTER
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         SPACE 3
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         SPACE
BUF      EQU   10              OFFSET INTO PRINT BUFFER
         EJECT
MACLIB   CSECT
         SAVE  (14,12),,MACLIB_&SYSDATE_&SYSTIME_COPYRIGHT_1985_AMDAHL_*
               CORP._SE_TOOLS_-_SETT010_AMDIPCSE   SAVE REGISTERS DFG00
         LR    R12,R15              ESTABLISH BASE REGISTER
         LA    R10,4095(R12)        ESTABLISH SECOND
         LA    R10,1(R10)              BASE REG
         USING MACLIB,R12,R10       ASSIGN BASE REGISTERS
         SPACE 3                                                SH81040
         USING ABDPL,R1            PARM LIST ADDRESSABILITY
         LR    R14,R1              SAVE PARM REG
         L     R8,=A(DBUFFERL)     SIZE FOR GETMAIN
         SR    R4,R4               CLEAR REG
         IC    R4,ADPLSBPL         SUBPOOL
         GETMAIN R,LV=(R8),SP=(R4)
         USING MACDSECT,R1
         ST    R13,4(R1)           CHAIN REGISTER SAVE AREA
         ST    R1,8(R13)           BACK CHAIN SAVE AREA
         LA    R13,SAVEAREA        REGISTER SAVE AREA
         USING MACDSECT,R13        ASSING WORK AREA DSECT
         ST    R14,PARMADDR        SAVE PARM ADDRESS
         USING ABDPL,R1            PARM LIST ADDRESSABILITY
         LR    R1,R14              RESTORE PARM ADDRESS
         L     R15,ADPLPRNT        PRINT ROUTINE ADDRESS
         BALR  R14,R15             BLANK LINE
         L     R14,ADPLBUF         PRINT BUFFER ADDRESS
         MVC   0(26,R14),=C'*** MACLIB COMMAND OUTPUT:'           DFG00
         L     R8,ADPLEXT          GET EXTENSION ADDRESS.         DFG00
         ICM   R8,B'1111',0(R8)    GET POINTER TO OPERANDS.       DFG00
         BZ    NOOPERND            NONE TO BE HAD, SKIP MOVE.     DFG00
         LR    R15,R8              LOAD FLOATING POINTER.         DFG00
SCANEND  DS    0H                                                 DFG00
         LA    R15,1(,R15)         POINT TO NEXT BYTE.            DFG00
         CLI   0(R15),C' '         END OF OPERANDS?               DFG00
         BNE   SCANEND             NOPE, CONTINUE SCANNING.       DFG00
         SR    R15,R8              COMPUTE OPERAND LENGTH.        DFG00
         EX    R15,OPSMOVE         MOVE OPERANDS TO MESSAGE.      DFG00
NOOPERND DS    0H                                                 DFG00
         L     R15,ADPLPRNT        PRINT ROUTINE ADDRESS
         BALR  R14,R15             PRINT HEADER
         L     R15,ADPLPRNT        PRINT ROUTINE ADDRESS
         BALR  R14,R15             PRINT BLANK LINE
         L     R15,ADPLPRNT        PRINT ROUTINE ADDRESS
         BALR  R14,R15             PRINT BLANK LINE
         XC    BLDLLIST,BLDLLIST   CLEAR BLDL LIST
         MVC   DSECTNAM,=CL8' '    CLEAR DSECT NAME
         MVC   MACRONAM,=CL8' '    CLEAR MACRO NAME
         MVC   TO,=CL8' '          CLEAR TO SYMBOL
         MVC   FROM,=CL8' '        CLEAR FROM SYMBOL
         XC    OPTIONS,OPTIONS     CLEAR OPTION BYTE
         XC    ORGSAVE,ORGSAVE     CLEAR ORG STATEMENT SAVE ADDRESS
         XC    SWITCHES,SWITCHES   PROGRAM STATUS SWITCHES
         TITLE 'INPUT PARMLIST PARSE ROUTINE'
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
*        THIS IS THE PARSE ROUTINE
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
          L     R8,ADPLEXT          ADDRESS OF EXTENSION
          L     R8,0(R8)            ADDRESS OF MACLIB'S OPERANDS
* R8 POINTS TO MACLIB CONTROL STATEMENT
PARSE     DS   0H
PARSE30   DS   0H
          LR   R7,R8                ADDRESS OF BEGINNING DATA
          TRT  0(80,R8),SEPTBL      FIND A SEPARATOR CHAR
* R1 = ADDRESS OF SEP CHAR
* KEYWORD ENTERED
          LR   R8,R1                POINT TO SEP CHAR
          LR   R5,R1                SEP CHAR
          SR   R5,R7                LENGTH OF KEYWORD
          BCTR R5,0                  ADJUST FOR EXECUTE
          EX   R5,COMPADDR          Q. ADDRESS
          BE   PARADDR              A. YES
          EX   R5,COMPMACR          Q. MACRO
          BE   PARMACRO             A. YES
          EX   R5,COMPDSEC          Q. DSECT
          BE   PARDSECT             A. YES
          EX   R5,COMPFROM          Q. FROM
          BE   PARFROM              A. YES
          EX   R5,COMPTO            Q. TO
          BE   PARTO                A. YES
          EX   R5,COMPCOM           Q. COMMENTS
          BE   PARCOM               A. YES
          EX   R5,COMPNCOM          Q. NOCOMMENTS
          BE   PARNCOM              A. YES
          EX   R5,COMPHEX           Q. HEX
          BE   PARHEX               A. YES
          EX   R5,COMPNHEX          Q. NOHEX
          BE   PARNHEX              A. YES
          EX   R5,COMPASID          Q. ASID
          BE   PARASID              A. YES
          EX   R5,COMPSNAP          Q. SNAP DUMP
          BE   PARSNAP              A. YES
          L    R1,PARMADDR          PARAMETER ADDRESS
          L    R14,ADPLBUF          PRINT BUFFER ADDRESS
          MVC  BUF(24,R14),=C'BAD CONTROL CARD KEYWORD'
          L    R15,ADPLPRNT         PRINT ROUTINE ADDRESS
          BALR R14,R15             PRINT THE LINE
          B    BADPARSE
          SPACE
PARADDR   DS    0H
* ADDRESS=
          LA    R8,1(R8)           BUMP PAST SEP CHAR
          LR    R5,R8              SAVE START OF FIELD ADDRESS
PARADDR1  CLI   0(R8),C','         Q. IS IT A SEP
          BNH   PARADDR5           A. YES
          CLI   0(R8),C'A'         Q. LESS THAN 'A'
          BL    PARADDRE           A. YES, ERROR
          CLI   0(R8),C'0'         Q. <= ZERO
          BNL   PARADDR3           A. YES
          CLI   0(R8),C'F'         Q. < 'F'
          BH    PARADDRE           A. YES, ERROR
PARADDR3  LA    R8,1(R8)           BUMP TO NEXT CHAR
          B     PARADDR1           TRY AGAIN
PARADDR5  LR    R14,R8             ENDING ADDRESS
          SR    R14,R5             CALCULATE LENGTH
          CH    R14,=H'8'          Q. TOO LONG                    DFG00
          BH    PARADDRE           A. YES, ERROR
          BCTR  R14,0              ADJUST FOR EXECUTE
          LA    R1,FTEMP           TEMP WORK AREA
          LR    R15,R5             ADDRESS OF INPUT DATA
          EX    R14,MACMOVE        MOVE ADDRESS TO WORK AREA
          LR    R15,R1             WORK AREA ADDRESS
          EX    R14,MACTRANS       TRANSLATE TO HEX
          LA    R14,1(R14)         ADD 1 FOR PACK
          EX    R14,MACPACK        PACK ADDRESS INTO DTEMP
          MVC   DUMPADDR,DTEMP+3   ADDRESS TO SAVE AREA
PARSECHK  DS    0H
          CLI   0(R8),C' '         Q. END OF CONTROL STATEMENT
          BE    PARSEDN            A. YES
          LA    R8,1(R8)           BUMP TO NEXT CHAR
          B     PARSE30            TRY AGAIN
PARADDRE  L    R1,PARMADDR          PARAMETER ADDRESS
          L    R14,ADPLBUF          PRINT BUFFER ADDRESS
          MVC  BUF(23,R14),=C'INVALID ADDRESS ENTERED'
          L    R15,ADPLPRNT         PRINT ROUTINE ADDRESS
          BALR R14,R15             PRINT THE LINE
          B    BADPARSE
         SPACE
PARMACRO DS    0H
         LA    R4,MACRONAM         MACRO NAME
PARMOVE  DS    0H
         LA    R8,1(R8)            BUMP PAST '='
         MVC   0(8,R4),=CL8' '     BLANK FIELD NAME
         TRT   0(9,R8),SEPTBL      FIND SEPARATOR CHAR
         LR    R15,R8              BEGINNING OF FIELD
         LR    R8,R1               NEW BEGINNING OF DATA
         LR    R14,R1              END OF FIELD
         SR    R14,R15             CALCULATE FIELD LENGTH
         BCTR  R14,0               ADJUST FOR EXECUTE
         LR    R1,R4               TO FIELD ADDRESS
         EX    R14,MACMOVE         MOVE IN FIELD
         B     PARSECHK            CONTINUE
         SPACE
PARDSECT DS    0H
         LA    R4,DSECTNAM         DSECT NAME SAVE AREA
         B     PARMOVE             MOVE IT IN
         SPACE
PARFROM  DS    0H
         LA    R4,FROM             FROM SAVE AREA
         B     PARMOVE             MOVE IT IN
         SPACE
PARTO    DS    0H
         LA    R4,TO               TO SAVE AREA
         B     PARMOVE              MOVE IT IN
         SPACE
PARCOM   DS    0H
         OI   OPTIONS,COMMENTS      TURN ON COMMENTS
         B    PARSECHK              CONTINUE
         SPACE
PARNCOM  DS    0H
         NI   OPTIONS,X'FF'-COMMENTS TURN OFF COMMENTS
         B    PARSECHK              CONTINUE
         SPACE
PARHEX   DS    0H
         OI    OPTIONS,HEX          TURN ON FORCE HEX
         B     PARSECHK             CONTINUE
         SPACE
PARNHEX  DS    0H
         NI    OPTIONS,X'FF'-HEX    TURN OFF FORCE HEX
         B     PARSECHK             CONTINUE
         SPACE
PARSNAP  DS    0H
         OI    OPTIONS,SNAPDUMP      TURN ON FORCE DUMP
         B     PARSECHK             CONTINUE
         SPACE
PARASID   DS    0H
* ASID=
          LA    R8,1(R8)           BUMP PAST SEP CHAR
          LR    R5,R8              SAVE START OF FIELD ADDRESS
PARASID1  CLI   0(R8),C','         Q. IS IT A SEP
          BNH   PARASID5           A. YES
          CLI   0(R8),C'A'         Q. LESS THAN 'A'
          BL    PARASIDE           A. YES, ERROR
          CLI   0(R8),C'0'         Q. <= ZERO
          BNL   PARASID3           A. YES
          CLI   0(R8),C'F'         Q. < 'F'
          BH    PARASIDE           A. YES, ERROR
PARASID3  LA    R8,1(R8)           BUMP TO NEXT CHAR
          B     PARASID1           TRY AGAIN
PARASID5  LR    R14,R8             ENDING ADDRESS
          SR    R14,R5             CALCULATE LENGTH
          CH    R14,=H'4'          Q. TOO LONG
          BH    PARASIDE           A. YES, ERROR
          BCTR  R14,0              ADJUST FOR EXECUTE
          LA    R1,FTEMP           TEMP WORK AREA
          LR    R15,R5             ADDRESS OF INPUT DATA
          EX    R14,MACMOVE        MOVE ADDRESS TO WORK AREA
          LR    R15,R1             WORK AREA ADDRESS
          EX    R14,MACTRANS       TRANSLATE TO HEX
          LA    R14,1(R14)         ADD 1 FOR PACK
          EX    R14,MACPACK        PACK ADDRESS INTO DTEMP
          MVC   DUMPASID,DTEMP+5   ADDRESS TO SAVE AREA
          B     PARSECHK           CONTINUE
PARASIDE  L    R1,PARMADDR          PARAMETER ADDRESS
          L    R14,ADPLBUF          PRINT BUFFER ADDRESS
          MVC  BUF(20,R14),=C'INVALID ASID ENTERED'
          L    R15,ADPLPRNT         PRINT ROUTINE ADDRESS
          BALR R14,R15             PRINT THE LINE
          B    BADPARSE
COMPADDR  CLC  0(*-*,R7),=C'ADDRESS'
COMPMACR  CLC  0(*-*,R7),=C'MACRO'
COMPDSEC  CLC  0(*-*,R7),=C'DSECT'
COMPFROM  CLC  0(*-*,R7),=C'FROM'
COMPTO    CLC  0(*-*,R7),=C'TO'
COMPCOM   CLC  0(*-*,R7),=C'COMMENTS'
COMPNCOM  CLC  0(*-*,R7),=C'NOCOMMENTS'
COMPHEX   CLC  0(*-*,R7),=C'HEX'
COMPNHEX  CLC  0(*-*,R7),=C'NOHEX'
COMPASID  CLC  0(*-*,R7),=C'ASID'
COMPSNAP  CLC  0(*-*,R7),=C'SNAP'
PARSEDN   DS   0H
         TITLE 'MACLIB - INITIALIZATION'
         LH    R15,DUMPASID        REQUESTED DUMP ASID
         LTR   R15,R15             Q. WAS THERE ONE
         BNZ   MACINIT1            A. YES
         MVC   DUMPASID,=H'1'      DEFAULT ASID                SH81040
MACINIT1 DS    0H
         L     R1,PARMADDR         PICK UP PARM ADDRESS
         MVC   SAVEASID,ADPLASID   SAVE ENTRY ASID
         MVC   ADPLASID,DUMPASID   REQUESTED ASID'S STORAGE
         SPACE 3
*
*        R13  = MACLIB'S WORK AREA
*               NOW BUILD CONTROL BLOCKS IN WORK AREA
*
         MVC   MACDCB(MACDCBE-MACDCB),MMACDCB MODEL DCB
         MVC   READ(READE-READ),MREAD MODEL READ
         MVC   CLOSE(CLOSEE-CLOSE),MCLOSE  MODEL CLOSE
         MVC   OPEN(OPENE-OPEN),MOPEN      MODEL OPEN
         B     MACSTART            GO ALLOC LIBS                SH81040
         SPACE 2
*
MACPACK  PACK  DTEMP,0(0,R15) PACK THE DATA
MACTRANS TR    0(*-*,R15),HEXCONVT CONVERT DATA FOR PACK
MACMOVE  MVC   0(*-*,R1),0(R15)    MOVE THE DATA
OPSMOVE  MVC   0+28(0,R14),0(R8) MOVE OPERANDS TO TITLE MESSAGE.  DFG00
         TITLE ' MACLIB - OPEN MACRO LIBRARIES'
MACSTART DS    0H
         LA    R4,MACDCB           MACLIB DCB ADDRESS
         USING IHADCB,R4           ASSIGN DCB DSECT
         TM    DCBOFLGS,DCBOFOPN   Q. ALREADY OPEN
         BO    MACOPENA            A. YES
         OPEN  ((R4)),MF=(E,OPEN)  OPEN MACLIB
         LTR   R15,R15             Q. GOOD OPEN
         BZ    MACOPENA            A. YES
         L     R1,PARMADDR         PARMLIST ADDRESSABILITY
         L     R14,ADPLBUF         PRINT BUFFER ADDRESS.
         MVC   BUF(L'M01009,R14),M01009
         L     R15,ADPLPRNT         PRINT SUBROUTINE ADDRESS
         BALR  R14,R15              PRINT THE LINE
         LA    R15,13              FORCE COMMAND REENTER
         B     RETURN1             RETURN TO CALLER
MACOPENA DS    0H
         L     R14,BUFPTR          POINTER TO IO BUFFER
         LTR   R14,R14             Q. ALREADY HAVE A BUFFER
         BNZ   MACBLDL             A. YES, SKIP ALLOCATION OF BUFFER
         LH    R6,DCBBLKSI         IO BUFFER SIZE
         L     R1,PARMADDR         PARAMETER ADDRESS
         SR    R4,R4               CLEAR REG 4
         IC    R4,ADPLSBPL         PICK UP SUBPOOL
         GETMAIN R,LV=(R6),SP=(R4) GET A BUFFER
         ST    R1,BUFPTR           SAVE THE BUFFER ADDRESS
*
*        MACLIB IS NOW OPEN AND READY FOR PROCESSING
*
         TITLE 'MAXLIB - BLDL + FIND MEMBER'
MACBLDL  DS    0H
         CLI   MACRONAM,C' '       Q. MACRO NAME ENTERED
         BE    MACBLDLA            A. NO
         MVC   BLDLNO,=H'1'        NUMBER OF ENTRIES FOR BLDL
         MVC   BLDLLGTH,=H'12'     LENGTH OF ENTRY
         MVC   BLDLNAME(8),=CL8' ' BLANKS TO ENTRY NAME
         XC    BLDLTTRK,BLDLTTRK   CLEAR TTRK
         MVC   BLDLNAME,MACRONAM   MOVE IN THE DMACRO NAME
         LA    R4,MACDCB           MACLIB DCB ADDRESS
         BLDL  (R4),BLDLLIST       ISSUE BLDL
         LTR   R15,R15             Q. GOOD BLDL
         BZ    MACBLDLD            A. YES
         L     R1,PARMADDR         PARMLIST ADDRESSABILITY
         L     R14,ADPLBUF         PRINT BUFFER ADDRESS.
         MVC   BUF(L'M01005,R14),M01005
         MVC   BUF+24(8,R14),BLDLNAME MOVE IN BLDLNAME
         L     R15,ADPLPRNT         PRINT SUBROUTINE ADDRESS
         BALR  R14,R15              PRINT THE LINE
         LA    R15,13              BAD RETURN CODE
         B     RETURN1             RETURN TO CALLER
MACBLDLA DS    0H
         TM    BLDLNAME,X'FF'      Q. ENTRY ALREADY BUILD
         BNZ   BLDDONE             A. YES, SKIP MACRO PROCESSING
         L     R1,PARMADDR         PARMLIST ADDRESSABILITY
         L     R14,ADPLBUF         PRINT BUFFER ADDRESS.
         MVC   BUF(L'M01010,R14),M01010
         MVC   BUF+16(8,R14),BLDLNAME MOVE IN BLDLNAME
         L     R15,ADPLPRNT         PRINT SUBROUTINE ADDRESS
         BALR  R14,R15              PRINT THE LINE
         LA    R15,13              FORCE COMMAND REENTER
         B     RETURN1             RETURN TO CALLER
MACBLDLD DS    0H
         FIND  (R4),BLDLTTRK,C     POINT TO BEGINNING OF MACRO
MFINDDN  DS    0H
         CLI   DSECTNAM,C' '       Q. DSECT NAME ENTERED
         BNE   MDSECTDN            A. YES
         MVC   DSECTNAM,BLDLNAME   DSECT NAME = MACRO NAME
MDSECTDN DS    0H
         TITLE 'MAXLIB - FIND DSECT IN MACRO'
MDSECTRD BAL   R6,READRTN        READ RECORD FROM LIBRARY
         NI    SWITCHES,X'7F'    TURN OFF DSECT FOUND INDICATOR
MDSECTCK CLC   DSECTNAM,0(R3)      Q. THIS THE DSECT
         BE    MDSECTB             A. NO, TRY AGAIN
         BAL   R6,READDBLK         READ ANOTHER RECORD
         B     MDSECTCK            CHECK FOR DSECT NAME AGAIN
         TITLE 'MACLIB - BUILD DSECT NAME TABLE'
MDSECTB  DS    0H
         OI    SWITCHES,X'80'      TURN ON DSECT FOUND INDICATOR
         SPACE
* * * * * * * * ------- -------- -------- ------- *
* * * * * * * * BUILD MACLIB MAP
* * * * * * * * ------- -------- -------- ------- *
         USING BUFDSECT,R3    BUFFER ADDRESS DSECT
         USING SYMDSECT,R4    SYMBOL TABLE ENTRY DSECT
         LA    R14,SYMTABLE   SYMBOL TABLE ADDRESS
         LA    R7,SYMSIZE     SYMBOL TABLE SIZE
         ST    R7,SYMCNT      SAVE THE COUNT
         BCTR  R7,0           ADJUST TO POINT TO LAST ENTRY
         M     R6,=A(SYMELGTH) MULTIPLY BY LENGTH
         AR    R7,R14         POINT TO LAST ENTRY
         ST    R7,SYMLAST     SAVE THE POINTER
         S     R14,=A(SYMELGTH) ADJUST FOR FIRST BUMP
         ST    R14,SYMPTR     SAVE THE POINTER TO TABLE - 1 ENTRY
         XC    OFFSET,OFFSET  CLEAR OFFSET
         XC    NEWOFF,NEWOFF  CLEAR NEW OFFSET
         XC    FMTADDR,FMTADDR CLEAR DUMP STORAGE ADDR
BUILDMAP DS    0H                                               SH81040
         BAL   R6,READDBLK     READ RECORD FROM BLOCK
         CLI   BUFFER,C'*'     IS THIS A COMMENT
         BE    BUILDAST        YES - BUILD COMMENT RECORD
         L     R14,=A(SKPNBLK) GET TRT TABLE ADDRESS
         TRT   BUFFER(20),0(R14) STOP ON BLANK
         BZ    BUILDMAP        IF NO BLANK SKIP RECORD
         L     R14,=A(SKPBLK)  GET TRT TABLE ADDRESS
         TRT   0(20,R1),0(R14)  STOP ON AT OPERATION
         BZ    BUILDMAP        IF ALL BLANKS SKIP RECORD
         CLC   0(3,R1),=C'DS ' DEFINE STORAGE?
         BE    BLDDS          YES - OK, USE IT
         CLC   0(3,R1),=C'DC ' DEFINE CONSTANT?
         BE    BLDDC          YES - OK, USE IT
         CLC   0(6,R1),=C'DSECT ' DUMMY CONTROL SECTION?
         BE    MDSECTCK       YES - LEAVING CURRENT DSECT
         CLC   0(6,R1),=C'MEND  ' MACRO END
         BE    BLDDONE         YES - DONE WITH THE MAP
         CLC   0(4,R1),=C'ORG ' ORIGIN?
         BE    BLDORG         YES - OK, USE IT
         CLC   0(4,R1),=C'EQU ' EQUATE?
         BE    BLDEQU         YES - OK, USE IT
         B     BUILDMAP       FORGET IT
         SPACE 2
BUILDAST BAL   R14,REMEMBER   PICK UP SYMBOL TABLE ENTRY ADDR
         MVI   SYMTYPE,C'*'   INDICATE COMMENT CARD
         MVC   SYMCOM,BUFFER  SAVE COMMENT CARD
         B     BUILDMAP       CHECK NEXT CARD
         SPACE 2
BLDDS    BAL   R14,REMEMBER   REMEMBER OFFSET OF THIS SYMBOL
         MVC   SYMCODE,=CL4'DS' OP CODE
         L     R14,=A(SKPBLK)  GET TRT TABLE ADDRESS
         TRT   3(80,R1),0(R14) SKIP TO OPERAND
         BAL   R14,GETCTL     GET TYPE, COUNT AND LENGTH
         BAL   R14,DEFINE     GO DEFINE THIS FIELD
         B     BUILDMAP       ADVANCE TO NEXT RECORD
BLDDC    BAL   R14,REMEMBER   REMEMBER OFFSET OF THIS SYMBOL
         MVC   SYMCODE,=CL4'DC' SAVE OP CODE
         L     R14,=A(SKPBLK)  GET TRT TABLE ADDRESS
         TRT   3(80,R1),0(R14) SKIP TO OPERAND
         BAL   R14,GETCTL     GET TYPE, COUNT AND LENGTH
         BAL   R14,DEFINE     GO DEFINE THIS FIELD
         B     BUILDMAP       ADVANCE TO NEXT RECORD
BLDORG   DS    0H
         BAL   R14,REMEMBER   GET SYMBOL TABLE ENTRY
         MVC   SYMCODE,=CL4'ORG' CODE CONSTANT
         MVI   SYMTYPE,C'O'   TYPE ORG
         MVC   SYMCOM,BUFFER  SAVE THE CARD
         MVI   SYMCOML,X'47'  CARD LENGTH
*
*        ADJUST OFFSET POINTER AS PER THIS ORG STATEMENT
*
         LA    R1,4(R1)       SKIP OVER ORG
         L     R14,=A(SKPBLK)  GET TRT TABLE ADDRESS
         TRT   0(80,R1),0(R14) STOP AT OPERAND
         LA    R2,BUFFER+71   COL 72 OF CARD
         CR    R1,R2          Q. WAS IT ORG(BLANK OPERAND)
         BE    BLDORG20       A. YES
         CLI   0(R1),C'*'     Q. OFFSET FROM CURRENT LOC COUNTER
         BNE   BLDORG05       A. NO, CONTINUE
         LA    R1,1(R1)       BUMP TO DISPLACEMENT FROM OFFSET
         L     R14,OFFSET     PICK UP CURRENT LOC COUNTER
         B     BLDORG40       CONTINUE
BLDORG05 LR    R15,R1         SAVE START OF SYMBOL
         TRT   0(66,R1),TRTABLE1 FIND END OF SYMBOL
         LR    R14,R1         END OF SYMBOL
         SR    R14,R15        GET SYMBOL LENGTH
         BCTR  R14,0          ADJUST FOR EX INST
         MVC   DTEMP,=CL8' '  BLANK TEMP WORK AREA
         EX    R14,BLDORGM1   MOVE IN THE SYMBOL
         LA    R2,SYMTABLE    ADDRESS OF SYMBOL TABLE
BLDORG10 CLC   DTEMP,0(R2)    Q. THIS THE SYMBOL
         BE    BLDORG30       A. YES
         LA    R2,SYMELGTH(R2) BUMP TO NEXT ENTRY
         C     R2,SYMLAST      Q. END OF TABLE
         BH    BLDORG80        A. YES, FORCE CARD TO PRINT
         B     BLDORG10        TRY AGAIN
BLDORG20 CLC   ORGSAVE,=F'0'   Q. ANY SAVED ORIGINATION
         BE    BUILDMAP        A. NO, PROCESS NEXT CARD
*
*        THIS ORG CARD DOES NOT REFERENCE A VALID SYMBOL, TREAT IT
*        AS AN 'ORG    ' STATEMENT AND RESET THE ORIGIN ADDRESS
*
         CLC   ORGSAVE,OFFSET   Q. ORGSAVE GT OFFSET
         BL    BLDORG25         A. NO, DON'T RESTORE
         MVC   OFFSET,ORGSAVE   RESTORE OFFSET
BLDORG25 XC    ORGSAVE,ORGSAVE  CLEAR ORIGIN SAVE AREA
         B     BUILDMAP         PROCESS NEXT CARD
BLDORG30 LH    R14,SYMOFSET-SYMNAME(R2) OFFSET FOR SYMBOL
BLDORG40 CLI   0(R1),C' '       Q. SYMBOL WITH NO DISPLACEMENT
         BE    BLDORG90         A. YES, SKIP TO END
         CLI   1(R1),C'X'       Q. HEX DISPLACEMENT
         BE    BLDORG60         A. YES, GO HANDLE IT
         CLI   1(R1),C'0'       Q. DISPLACEMENT NUMERIC
         BL    BLDORG80         A. NO, DON'T CALCULATE, FORCE PRINT
         LR    R15,R1           SAVE REGISTER ONE
         TRT   0(65,R1),SKPNBLK FIND END OF FIELD
         LR    R7,R1            PICK UP END OF FIELD ADDRESS
         SR    R7,R15           SUBTRACT BEGINNING OF FIELD ADDR
         BCTR  R7,0             ADJUST FOR PACK
         BCTR  R7,0             ADJUST FOR PLUS OR MINUS SIGN
         LR    R1,R15           RESTORE R1
         LA    R15,1(R15)       SKIP OVER SIGN
         EX    R7,MACPACK       PACK THE DISPLACEMENT
         CVB   R7,DTEMP         CONVERT DISP TO BINARY
BLDORG45 CLI   0(R1),C'-'       Q. MINUS DISPLACEMENT
         BNE   BLDORG50         A. NO
         SR    R14,R7           ADJUST POINTER BACKWARDS
         B     BLDORG90         WRAP IT UP
BLDORG50 AR    R14,R7           ADJUST POINTER FORWARD
         B     BLDORG90         WRAP IT UP
BLDORG60 LR    R15,R1           SAVE REG 1
         LR    R0,R1            SAVE IT AGAIN
         TRT   0(65,R1),SKPNBLK FIND END OF FIELD
         LR    R7,R1            SET UP LENGTH REG
         SR    R7,R15           CALCULATE LENGTH
         SH    R7,=H'5'         ADJUST THE LENGTH FOR EX
         LA    R15,3(R15)       POINT TO HEX DATA
         MVC   FTEMP,0(R15)     MOVE DATA TO WORK AREA
         LA    R15,FTEMP        SET UP FOR TRANS
         EX    R7,MACTRANS      TRANSLATE FOR PACK
         LA    R7,1(R7)         ADD 1 (PACK EXRTA BYTE)
         EX    R7,MACPACK       PACK FIELD INTO DTEMP
         MVC   FTEMP(4),DTEMP+3 HEX DATA TO WORD BOUNDARY
         L     R7,FTEMP         PICK UP HEX DATA
         LR    R1,R0            RESTORE REG 1
         B     BLDORG45         COMPLETE DISPLACEMENT ADJUSTMENT
         SPACE
BLDORG80 MVI   SYMTYPE,C'P'     ALWAYS PRINT(COULDN'T FIGURE THIS ONE)
         B     BLDORG20         MAY NEED TO REORG
BLDORG90 C     R14,OFFSET       Q. NEW OFFSET GT OLD OFFSET
         BL    BLDORG95         A. NO
         C     R14,ORGSAVE      Q. NEW OFFSET GT SAVED OFFSET
         BL    BLDORG94         A. NO, JUST ADJUST NEW OFFSET
         ST    R14,ORGSAVE      SAVE THE ORIGIN
BLDORG94 ST    R14,OFFSET       MAKE IT THE CURRENT OFFSET
         B     BUILDMAP         GO TO NEXT CARD
BLDORG95 CLC   ORGSAVE,OFFSET   SAVED ORIGIN GT CURRENT
         BH    BLDORG94         A. YES, DON'T SAVE NEW ONE
         MVC   ORGSAVE,OFFSET   SAVE CURRENT OFFSET
         B     BLDORG94         ADJUST CURRENT OFFSET
         SPACE 2
BLDORGM1 MVC   DTEMP(*-*),0(R15) SAVE THE SYMBOL
         B     BUILDMAP       GO PRINT LINE
BLDEQU   BAL   R14,REMEMBER   GET SLOT IN SYMBOL TABLE
         MVC   SYMCODE,=CL4'EQU' SAVE OP CODE
         MVC   SYMCOM,BUFFER  SAVE THE CARD FOR PRINT
         B     BUILDMAP        PROCESS NEXT CARD
         SPACE
* * * * * * * * ------- -------- -------- ------- *
* * * * * * * * DEFINE FIELD IN DSECT
* * * * * * * * ------- -------- -------- ------- *
DEFINE   CLC   TOTLGTH,=H'0'  Q. DOES THIS REALLY DEFINE ANY STORAGE
         BNE   DEFINE1        A. YES, CONTINUE
         MVI   SYMTYPE,C'*'   MAKE IT A COMMENT CARD
         MVC   SYMCOM,BUFFER  SAVE THE CARD FOR PRINT
         B     DEFEXIT        GO TO COMMON EXIT
DEFINE1  L     R15,COUNT      COUNT OF ENTRIES
         STH   R15,SYMCOUNT   SAVE THE COUNT IN TABLE
         MVC   SYMTLGTH,TOTLGTH TOTAL LENGTH THIS SYMBOL
         L     R15,LENGTH     GET FIELD LENGTH
         STH   R15,SYMLGTH    SAVE THE FIELD LENGTH IN TABLE
         MVC   SYMTYPE,TYPE   SAVE THE FIELD TYPE
         L     R15,COMADDR    COMMENTS ADDRESS
         LA    R1,BUFFER+72   END OF COMMENTS AREA
         SR    R1,R15         CALCULATE COMMENTS LENGTH
         BCTR  R1,0           ADJUST LENGTH OF 'EX' INST
         STC   R1,SYMCOML     SAVE THE LENGTH FOR PRINT LATER
         EX    R1,DEFINEM1    MOVE IN THE COMMENTS
DEFEXIT  MVC   OFFSET(4),NEWOFF SET OFFSET TO OFFSET OF NEXT FIELD
         BR    R14            GET BACK
DEFINEM1 MVC   SYMCOM(*-*),0(R15) SAVE THE COMMENTS
         SPACE
* * * * * * * * ------- -------- -------- ------- *
* * * * * * * * GET COUNT TYPE AND LENGTH FROM REC
* * * * * * * * ------- -------- -------- ------- *
GETCTL   STM   R14,R4,SAVER14  SAVE CALLER'S REGS
         NI    SWITCHES,X'BF'  TURN OF BOUNDARY ALIGN
         MVI   NUMFLAG,X'00'  INDICATE NUMBER NOT SET
         MVI   LFLAG,X'00'    INDICATE LENGTH MODIFIER NOT PRESENT
         LA    R4,COUNT       NUMBER IS COUNT
GETCTL1  CLI   0(R1),C' '     END OF LINE
         BE    GETCTL91       YES - EXIT
         SR    R2,R2          CLEAR FOR INSERT BY TRT
         L     R14,=A(SKPBLK)  GET TRT TABLE ADDRESS
         TRT   0(80,R1),0(R14) STOP AT OPERAND
         B     *(R2)          JUMP INTO TABLE
         B     GETCTL9        FINISH UP         04
         B     GETCTL2        STOPPED ON NUMBER 08
         B     GETCTL3        STOPPED ON 'L'    0C   (L)
         B     GETCTL41       STOPPED ON TYPE   10   (C, X, B, P, Z)
         B     GETCTL42       STOPPED ON TYPE   14   (H, Y, S)
         B     GETCTL43       STOPPED ON TYPE   18   (F, E, A V Q)
         B     GETCTL44       STOPPED ON TYPE   1C   (D)
         B     GETCTL5        STOPPED FOR TIC   20
GETCTL2  CLI   NUMFLAG,X'FF'  1ST ENTRY THIS CODE FOR THIS NUMBER
         BE    GETCTL21       NO - SKIP CLEAR
         XC    0(4,R4),0(R4)  ZERO NUMBER
         MVI   NUMFLAG,X'FF'  INDICATE COUNT IS NOW VALID
GETCTL21 SR    R2,R2          CLEAR FOR MULTIPLY
         L     R3,0(,R4)      GET NUMBER
         M     R2,=F'10'      TIMES 10 FOR DECIMAL
         IC    R2,0(,R1)      GET DECIMAL DIGIT
         N     R2,=XL4'F'     CLEAR
         AR    R3,R2          NEW NUMBER
         ST    R3,0(,R4)      SET NUMBER
         LA    R1,1(,R1)      NEXT CHARACTER
         B     GETCTL1        LOOP
GETCTL3  LA    R4,LENGTH      NUMBER IS LENGTH
         MVI   NUMFLAG,X'00'  SET NUMBER CODE FOR 1ST ENTRY
         MVI   LFLAG,X'FF'    INDICATE LENGTH MODIFIER IS PRESENT
         LA    R1,1(,R1)      NEXT CHARACTER
         NI    SWITCHES,X'BF' DON'T ALLOW FORCED ALIGNMENT
         B     GETCTL1        LOOP
GETCTL4  CLI   NUMFLAG,X'FF'  IS THERE A COUNT
         BE    *+10           YES - SKIP SET
         MVC   COUNT(4),=F'1' SET COUNT TO 1
         ST    R3,LENGTH      STORE LENGTH
         MVC   TYPE(1),0(R1)  SAVE TYPE
         LA    R1,1(,R1)      NEXT BYTE
         B     GETCTL1        LOOP
GETCTL41 LA    R3,1           ASSUME ONE BYTE LENGTH
         B     GETCTL4        JOIN COMMON CODE
GETCTL42 LA    R3,2           ASSUME ONE BYTE LENGTH
         OI    SWITCHES,X'40' POSSIBLE BOUNDARY ALIGNMENT FORCE
         B     GETCTL4        JOIN COMMON CODE
GETCTL43 LA    R3,4           ASSUME FOUR BYTE LENGTH
         OI    SWITCHES,X'40' POSSIBLE BOUNDARY ALIGNMENT FORCE
         B     GETCTL4        JOIN COMMON CODE
GETCTL44 LA    R3,8           ASSUME EIGHT BYTE LENGTH
         OI    SWITCHES,X'40' POSSIBLE BOUNDARY ALIGNMENT FORCE
         B     GETCTL4        JOIN COMMON CODE
GETCTL45 LA    R3,16          ASSUME SIXTEEN BYTE LENGTH
         B     GETCTL4        JOIN COMMON CODE
GETCTL5  LA    R3,1(,R1)      SAVE ADDRESS OF 1ST ELEMENT
         L     R14,=A(SKPNTIC) GET TRT TABLE ADDRESS
         TRT   0(80,R3),0(R14) STOP ON NEXT TIC
         LR    R0,R1          MOVE FOR SUBTRACTION
         LA    R1,1(,R1)      POINT AT BLANK AFTER TIC
         CLI   LFLAG,X'FF'    IS LENGTH BEEN DEFINED?
         BE    GETCTL91       YES - GET OUT
         SR    R0,R3          GET FIELD LENGTH
         CLI   TYPE,C'C'      CHARACTER FIELD?
         BE    GETCTL59       YES - STORE LENGTH
         CLI   TYPE,C'Z'      ZONED FIELD?
         BE    GETCTL59       YES - STORE LENGTH
         LA    R2,1           ADDITION FACTOR FOR HEX FIELD
         LA    R3,1           SHIFT FACTOR FOR HEX AND PACKED FIELDS
         CLI   TYPE,C'X'      HEX FIELD?
         BE    GETCTL58       YES - ADD, SHIFT AND STORE
         LA    R2,2           ADDITION FACTOR FOR PACKED FIELD
         CLI   TYPE,C'P'      PACKED FIELD?
         BE    GETCTL58       YES - ADD, SHIFT AND STORE
         LA    R2,7           ADDITION FACTOR FOR BIT FIELD
         LA    R3,3           SHIFT FACTOR FOR BIT FIELD
         CLI   TYPE,C'B'      BIT FIELD?
         BE    GETCTL58       YES - ADD, SHIFT AND STORE
         B     GETCTL91       ALL ELSE LENGTH IS SET
GETCTL58 AR    R0,R2          ADD FACTOR IN REG 2
         SRA   R0,0(R3)       SHIFT FACTOR IN REG 3
GETCTL59 ST    R0,LENGTH      SAVE LENGTH
         B     GETCTL91       R1 POINTS AT BLANK, FIND COMMENT, LEAVE
GETCTL9  L     R14,=A(SKPNBLK) GET TRT TABLE ADDRESS
         TRT   0(80,R1),0(R14) FIND END OF OPERAND
GETCTL91 L     R14,=A(SKPBLK) GET TRT TABLE ADDRESS
         TRT   0(80,R1),0(R14) STOP AT BEGINNING OF COMMENT
         ST    R1,COMADDR     SAVE ADDRESS OF COMMENT
         L     R2,OFFSET      CURRENT OFFSET
*        CHECK FOR BOUNDARY ALIGNMENT ONLY
         TM    SWITCHES,X'40' Q. FORCE BOUNDARY ALIGNMENT
         BNO   GETCTL95       A. NO
         L     R2,LENGTH          LOAD THE LENGTH              AN002DMS
         BCTR  R2,0               SUBTRACT ONE                 AN002DMS
         LR    R14,R2             SAVE COPY OF LN-1 FOR SHIFTS AN002DMS
         A     R2,OFFSET          ADD THE OLD OFFSET           AN002DMS
         X     R14,=X'FFFFFFFF'   GET THE ONES COMPLEMENT      AN002DMS
         NR    R2,R14             AND OUT THE UNWANTED BITS    AN002DMS
         ST    R2,OFFSET          SAVE FOR USE BY LATER        AN002DMS
GETCTL95 SR    R14,R14        CLEAR FOR MULTIPLY
         L     R15,COUNT      GET COUNT
         M     R14,LENGTH     * LENGTH FOR TOTAL SIZE
         STH   R15,TOTLGTH    SAVE THE TOTAL LENGTH
         AR    R15,R2             + OLD OFFSET (AFT BNDRY ALG) AN002DMS
         ST    R15,NEWOFF     GIVES NEW OFFSET
         LM    R14,R4,SAVER14 RESTORE CALLER'S REGS
         BR    R14            RETURN
         TITLE 'MACLIB - FORMAT OUTPUT LINES'
BLDDONE  TM    SWITCHES,X'80'  Q. WAS A DSECT FOUND
         BO    BLDDONE1        A. YES, CONTINUE
         L     R1,PARMADDR         PARMLIST ADDRESSABILITY
         L     R14,ADPLBUF         PRINT BUFFER ADDRESS.
         MVC   BUF(L'M01001,R14),M01001
         MVC   BUF+23(8,R14),DSECTNAM  DSECT NAME
         MVC   BUF+41(8,R14),BLDLNAME  BUILDL NAME
         L     R15,ADPLPRNT         PRINT SUBROUTINE ADDRESS
         BALR  R14,R15              PRINT THE LINE
         LA    R15,13           RETURN CODE = REENTER
         B     RETURN1          RETURN TO CALLER
BLDDONE1 BAL   R14,REMEMBER   PICK UP ENTRY ADDRESS
         MVC   DUMPLGTH,OFFSET SAVE ENDING LENGTH
         MVI   SYMTYPE,X'FF'  END OF DSECT TABLE ENTRIES
GETDUMP  L     R2,DUMPLGTH    PICK UP LENGTH OF STORAGE REQ
         LA    R2,4(R2)       INCLUDE WORD FOR FREEMAIN INFO
         L     R1,PARMADDR    PARAMETER ADDRESS
         SR    R4,R4          CLEAR REG 4
         IC    R4,ADPLSBPL    PICK UP SUBPOOL
         GETMAIN R,LV=(R2),SP=(R4) GET STORAGE
         ST    R2,0(R1)       SAVE LENGTH
         LA    R1,4(R1)       BUMP TO STORAGE FOR DUMP
         ST    R1,FMTADDR     TO ADDRESS
         LR    R2,R1          SAVE REG FOR STORAGE ACCESS CALL
*
*    GET DUMPS STORAGE INTO OUR WORK AREA
*
         L     R11,DUMPLGTH   DUMP LENGTH
         L     R0,DUMPADDR    START DUMP ADDRESS
         LR    R5,R0          START DUMP ADDRESS
         L     R1,PARMADDR    PARAMETER ADDRESS
         L     R15,ADPLMEMA   DUMP STORAGE ACCESS ROUTINE
         BALR  R14,R15        ACCESS FIRST OF DUMP
         L     R14,DUMPADDR   DUMP START ADDRESS
         N     R14,=X'00000FFF' ISOLATE DISPLACEMENT
         L     R15,=F'4096'   BLOCK SIZE
         SR    R15,R14        CALCULATE LENGTH JUST READ
         AR    R5,R15         POINT TO BEGINNING OF NEXT BLOCK
*   R5 POINTS TO NEW DUMP ADDRESS
         L     R6,FMTADDR     OUR WORK AREA ADDRESS
* R14 = LENGTH
* R0 = ADDRESS OF DUMP STORAGE
* R6 = WORK AREA STORAGE
         CR    R11,R15        Q. READ IN MORE THAN WE NEED
         BNL   ACCLOOP1       A. NO
         LR    R15,R11        ADJUST TO EXACTLY WHAT WE NEED
ACCLOOP1 DS    0H
         LA    R1,0(0,R15)    LENGTH
         LR    R7,R1          LENGTH
         MVCL  R6,R0          MOVE DUMP STORAGE TO WORK AREA
ACCLOOP  DS    0H
* R6 HAS BEEN INCREASED TO NEXT POSITION BY THE MVCL
         SR    R11,R15        SUBTRACT AMOUNT READ
         BC    12,ACCDONE     ZERO OR NEGATIVE = DONE
         LR    R0,R5          DUMP START ADDRESS
         L     R1,PARMADDR    PARAMETER ADDRESS
         L     R15,ADPLMEMA   STORAGE ACCESS ROUTINE
         BALR  R14,R15        GET THE STORAGE
         L     R15,=F'4096'   AMOUNT JUST READ
* R0 = INPUT DUMP STORAGE ADDRESS
* R6 = OUR WORK AREA ADDRESS
* R14 = LENGTH
         CR    R11,R15        Q. READ IN MORE THAN WE NEED
         BNL   ACCLOOP2       A. NO
         LR    R15,R11        ADJUST TO EXACTLY WHAT WE NEED
ACCLOOP2 DS    0H
         LA    R1,0(0,R15)    LENGTH
         LR    R7,R1          LENGTH
         MVCL  R6,R0          MOVE DUMP STORAGE TO WORK AREA
         B     ACCLOOP        KEEP READING
ACCDONE  DS    0H
         TITLE 'MACLIB - FORMAT PRINT'
         LA    R4,SYMTABLE    SYMBOL TABLE ENTRY ADDRESS
         MVI   FMTLINE,C' '   BLANK TO FORMAT LINE
         MVC   FMTLINE+1(L'FMTLINE-1),FMTLINE ... FORMAT LINE
MACFMTLP CLI   SYMTYPE,X'FF'  LAST LINE IN MACRO
         BE    MACWRAP        ALL DONE
         CLI   FROM,C' '      Q. FROM SYM REQUESTED
         BE    MACFMTLA       A. NO
         CLC   FROM(8),SYMNAME Q. THIS THE SYMBOL
         BNE   NEXTREC         A. NO
         MVI   FROM,C' '      DELETE FROM REQ
MACFMTLA DS    0H
         CLI   SYMTYPE,C'P'   Q. ALWAYS PRINT THIS ONE
         BE    LISTIT0        A. YES
         CLI   SYMTYPE,C'*'   IS THIS A COMMENT
         BNE   NOCOM          NO - COMMENT
         B     LISTIT         TYPE COMMENT
NOCOM    DS    0H
         CLC   SYMCODE,=CL4'DS' DEFINE STORAGE?
         BE    DS             YES - OK, USE IT
         CLC   SYMCODE,=CL4'DC' DEFINE CONSTANT?
         BE    DC             YES - OK, USE IT
         CLC   SYMCODE,=C'ORG ' ORIGIN?
         BE    ORG            YES - OK, USE IT
         CLC   SYMCODE,=C'EQU ' EQUATE?
         BE    EQU            YES - OK, USE IT
         B     NEXTREC        FORGET IT
DS       DS    0H
         L     R0,DUMPADDR    GET REQUESTED STORAGE ADDRESS
         AH    R0,SYMOFSET    ADDRESS OF FIELD
         ST    R0,FTEMP4      SAVE FOR CONVERSION                 DFG00
         LA    R1,FTEMP4      FROM ADDRESS                        DFG00
         LA    R2,DTEMP       TO ADDRESS
         BAL   R14,UNHEX4     EXPAND ONE WORD
         MVC   FMTLINE(8),DTEMP MOVE ADDRESS                      DFG00
         LH    R14,SYMOFSET   GET CURRENT OFFSET
         ST    R14,FTEMP4     SAVE IT FOR CONVERSION TO HEX PRINT DFG00
         BAL   R14,UNHEX4     CONVERT, REGS ALREADY SET
         MVC   FMTLINE+9(3),DTEMP+5 MOVE OFFSET                   DFG00
         MVC   FMTLINE+13(8),SYMNAME MOVE FIELD NAME              DFG00
         TM    OPTIONS,COMMENTS Q. COMMENTS REQUESTED
         BNO   DSNOCOM       A. NO, SKIP THEM
         IC    R14,SYMCOML   PICK UP LENGTH
         EX    R14,PUTITM1   MOVE IN THE COMMENTS
DSNOCOM  BAL   R14,GETVAL     BUILD OUTPUT LINE
         B     PUTIT          GO PRINT LINE
DC       DS    0H
         L     R0,DUMPADDR    GET REQUESTED STORAGE ADDRESS
         AH    R0,SYMOFSET    ADDRESS OF FIELD
         ST    R0,FTEMP4      SAVE FOR CONVERSION                 DFG00
         LA    R1,FTEMP4      FROM ADDRESS                        DFG00
         LA    R2,DTEMP       TO ADDRESS
         BAL   R14,UNHEX4     EXPAND ONE WORD
         MVC   FMTLINE(8),DTEMP MOVE ADDRESS                      DFG00
         LH    R14,SYMOFSET   GET CURRENT OFFSET
         ST    R14,FTEMP4     SAVE IT FOR CONVERSION TO HEX PRINT DFG00
         BAL   R14,UNHEX4     CONVERT, REGS ALREADY SET
         MVC   FMTLINE+9(3),DTEMP+5 MOVE OFFSET                   DFG00
         MVC   FMTLINE+13(8),SYMNAME MOVE FIELD NAME              DFG00
         TM    OPTIONS,COMMENTS Q. COMMENTS REQUESTED
         BNO   DCNOCOM       A. NO, SKIP THEM
         IC    R14,SYMCOML   PICK UP LENGTH
         EX    R14,PUTITM1   MOVE IN THE COMMENTS
DCNOCOM  BAL   R14,GETVAL     BUILD OUTPUT LINE
         B     PUTIT          GO PRINT LINE
ORG      DS    0H
         B     LISTIT         GO PRINT LINE
EQU      DS    0H
LISTIT   DS    0H
         TM    OPTIONS,COMMENTS Q. COMMENTS REQUESTED
         BNO   NEXTREC        A. NO, SKIP IT
LISTIT0  L     R1,PARMADDR         PARMLIST ADDRESSABILITY
         L     R14,ADPLBUF         PRINT BUFFER ADDRESS.
         MVC   BUF+13(72,R14),SYMCOM INDENT SYMBOL COMMENTS.      DFG00
         L     R15,ADPLPRNT         PRINT SUBROUTINE ADDRESS
         BALR  R14,R15              PRINT THE LINE
         B     NEXTREC        ON TO NEXT RECORD
PUTIT    DS    0H
PUTIT0   DS    0H
         L     R1,PARMADDR         PARMLIST ADDRESSABILITY
         L     R14,ADPLBUF         PRINT BUFFER ADDRESS.
         MVC   BUF(L'FMTLINE,R14),FMTLINE ... FORMAT LINE
         L     R15,ADPLPRNT         PRINT SUBROUTINE ADDRESS
         BALR  R14,R15              PRINT THE LINE
         MVI   FMTLINE,C' '   CLEAR ...
         MVC   FMTLINE+1(L'FMTLINE-1),FMTLINE ... FORMAT LINE
         SPACE
NEXTREC  DS    0H
         CLI   TO,C' '        IS 'TO' OPTION IN USE
         BE    NEXTRECA       NO - NEXT RECORD
         CLC   TO(8),SYMNAME  SAME LABEL?
         BNE   NEXTRECA       CONTINUE
         B     MACWRAP        YES - DONE
NEXTRECA LA    R4,SYMELGTH(R4) NEXT ENTRY
         B     MACFMTLP       HANDLE NEXT ENTRY
         SPACE
PUTITM1  MVC   FMTLINE+41(*-*),SYMCOM MOVE IN THE COMMENTS        DFG00
         SPACE 3
* * * * * * * * ------- -------- -------- ------- *
* * * * * * * * GET VALUE CONVERT AND PUT IN RECORD
* * * * * * * * ------- -------- -------- ------- *
GETVAL   ST    R14,R14SAVE    SAVE RETURN ADDRESS
         ST    R4,R4SAVE      SAVE SYMBOL DSECT REG
         L     R1,FMTADDR     GET ADDRESS OF CONTROL BLOCK
         AH    R1,SYMOFSET    COMPUTE ADDRESS OF FIELD
         LA    R2,CBFIELD     LOCAL BUFFER FOR FIELD
         MVC   FMTLINE+22(1),SYMTYPE MOVE TYPE                    DFG00
         LH    R5,SYMLGTH     LENGTH OF EACH PIECE
         LH    R15,SYMTLGTH   PICK UP TOTAL LENGTH REQUIRED
         LH    R7,SYMCOUNT    MULTIPLICATION FACTOR
         XC    SAVESTRT,SAVESTRT CLEAR DUMP STORAGE ADDRESS
         XC    SAVELGTH,SAVELGTH CLEAR SAVE LENGTH
         XC    SAVEPLTH,SAVEPLTH PIECE LENGTH FOR LONG FIELDS
         XC    SAVEPCNT,SAVEPCNT TEMPORARY MULTIPLIER FOR LONG FIELDS
         CH    R15,=H'256'     Q. TOTAL LENGTH > 256
         BNH   GETVALA9        A. NO, LET NORMAL FLOW HANDLE IT
         ST    R15,SAVELGTH    TOTAL PIECE LENGTH
         ST    R1,SAVESTRT     SAVE START OF STORAGE FOR DUMP
         LA    R5,256          SET LENGTH TO 256
         MVC   SAVEPCNT,=F'1'  TEMP COUNT = 1
         ST    R5,SAVEPLTH     SAVE THE PIECE LENGTH
         L     R5,SAVEPLTH     PICK UP PIECE LENGTH
         L     R7,SAVEPCNT     PICK UP TEMP MULTIPLIER
         LR    R15,R5          PICK UP TOTAL LENGTH
GETVALA9 LR    R3,R15         SAVE THE TOTAL LENGTH
         BCTR  R15,0          ADJUST FOR EX INST
         EX    R15,GETVALM2   MOVE THE DATA TO CBFIELD
         TM    OPTIONS,HEX    Q. REQUESTOR FORCING HEX
         BO    GETVAL0        A. YES
         CLI   SYMTYPE,C'C'   IS IT CHARACTER DATA?
         BE    GETVAL2        YES - MOVE IT OUT
GETVAL0  LA    R1,3(,R3)      ROUND UP ...
         SRL   R1,2           ... TO FULL WORDS
         BCTR  R1,0           NUMBER OF WORDS -1
         LR    R2,R1          NUMBER OF DOUBLE WORDS -1
         LA    R3,1(,R1)      WORDS TO TRANSLATE
         SLL   R1,2           OFFSET TO LAST WORD
         SLL   R2,3           OFFSET TO LAST DOUBLE WORD
         LA    R1,CBFIELD(R1) ADDRESS OF LAST WORD
         LA    R2,CBFIELD(R2) ADDRESS OF LAST DOUBLE WORD
GETVAL1  BAL   R14,UNHEX4     CONVERT WORD INTO DOUBLE WORD
         S     R1,=F'4'       BACK UP ONE WORD
         S     R2,=F'8'       BACK UP ONE DOUBLE WORD
         BCT   R3,GETVAL1     REPEAT FOR ALL WORDS
         AR    R5,R5          DOUBLE LENGTH
         B     GETVAL21       JOIN COMMON CODE
GETVAL2  LA    R14,TRTABLE2   ADDRESS OF TR TABLE
         TR    CBFIELD(256),0(R14) REDUCE NOISE
GETVAL21 C     R5,=F'16'      IS THIS ONE OF THOSE LONG ONES
         BNH   GETVAL25       NO TROUBLE FITTING THIS ONE IN
         LA    R5,15(,R5)     ROUND TO ...
         SRL   R5,4           ... 16 BYTE UNITS
         SR    R6,R6          CLEAR FOR MULTIPLY
         MR    R6,R5          INCREASE COUNT
         LA    R5,16          SET NEW LENGTH
GETVAL25 BCTR  R5,0           REDUCE LENGTH BY ONE FOR MOVES
         LA    R2,CBFIELD     ADDRESS OF FROM VALUE
         LA    R3,15          SPACE IN LINE -1
         LA    R4,FMTLINE+24  TO LOCATION                         DFG00
GETVAL3  CR    R3,R5          IS THERE ROOM
         BL    GETVAL4        NO - BREAK OUTPUT
         EX    R5,GETVALM1    MOVE VALUE INTO LINE
         LA    R2,1(R5,R2)    ADVANCE POINTER IN VALUE FIELD
         LA    R4,2(R5,R4)    ADVANCE POINTER IN OUTPUT SPACE W/BLANK
         SR    R3,R5          REDUCE ...
         BCTR  R3,0           ... LENGTH
         BCTR  R3,0           AND BLANK
         BCT   R7,GETVAL3     REPEAT FOR EACH FIELD
         L     R15,SAVELGTH   PICK UP PIECE LENGTH
         LTR   R15,R15        Q. DONE
         BZ    GETVAL9        A. YES
         L     R1,SAVESTRT    STARTING ADDR OF STORAGE DUMP
         A     R1,SAVEPLTH    ADD LENGTH JUST FINISHED
         ST    R1,SAVESTRT    MAKE IT THE NEW START ADDR
         S     R15,SAVEPLTH   SUBTRACT AMOUNT JUST FINISHED
         ST    R15,SAVELGTH   SAVE THE NEW LENGTH
         CH    R15,=H'256'    Q. REMAINDER LESS THAN 256
         BNL   GETVAL35       A. NO
         ST    R15,SAVEPLTH   SET UP TO COMPLETE THIS TIME
GETVAL35 LA    R2,CBFIELD     SET UP FOR NEXT FIELD
         L     R7,SAVEPCNT    RESTORE TEMP MULT COUNT
         L     R5,SAVEPLTH    PICK UP PIECE LENGTH
         LTR   R15,R15        Q. DONE WITH THIS PIECE
         BZ    GETVAL9        A. YES, WE ARE DONE
         LR    R15,R5         PICK UP LENGTH
         L     R4,R4SAVE      RESTORE REG 4 FOR LOOP
         B     GETVALA9       DO IT AGAIN
GETVAL4  DS    0H
         L     R1,PARMADDR         PARMLIST ADDRESSABILITY
         L     R14,ADPLBUF         PRINT BUFFER ADDRESS.
         MVC   BUF(L'FMTLINE,R14),FMTLINE ... FORMAT LINE
         L     R15,ADPLPRNT         PRINT SUBROUTINE ADDRESS
         BALR  R14,R15              PRINT THE LINE
         MVI   FMTLINE,C' '   CLEAR ...
         MVC   FMTLINE+1(L'FMTLINE-1),FMTLINE ... FORMAT LINE
         LA    R3,48          NEW SPACE IN LINE -1
         LA    R4,FMTLINE+24  POSITION IN OUTPUT LINE             DFG00
         B     GETVAL3        LOOP
GETVAL9  L     R14,R14SAVE    RESTORE RETURN ADDRESS
         L     R4,R4SAVE      RESTORE SYMBOL TABLE DSECT REG
         BR    R14            DUMMY GETVAL ROUTINE
         SPACE
GETVALM  MVC   FMTLINE+22(0),0(R3) MOVE CONSTANT TO FORMAT LINE   DFG00
GETVALM1 MVC   0(0,R4),0(R2)   MOVE VALUE TO FORMAT LINE
GETVALM2 MVC   CBFIELD(*-*),0(R1) MOVE DATA TO WORK AREA
         SPACE 3
* * * * * * * * ------- -------- -------- ------- *
* * * * * * * * REMEMBER OFFSET OF SYMBOL ROUTINE
* * * * * * * * ------- -------- -------- ------- *
REMEMBER L     R15,SYMCNT     GET NUMBER OF FREE ENTRIES
         BCT   R15,REMEMB1    REDUCE AND JUMP
         L     R1,PARMADDR         PARMLIST ADDRESSABILITY
         L     R14,ADPLBUF         PRINT BUFFER ADDRESS.
         MVC   BUF(L'M01011,R14),M01011
         L     R15,ADPLPRNT         PRINT SUBROUTINE ADDRESS
         BALR  R14,R15              PRINT THE LINE
         LA    R15,13         BAD RETURN CODE
         B     RETURN1        RETURN TO CALLER
REMEMB1  ST    R15,SYMCNT     STORE NEW COUNT
         L     R4,SYMPTR      SYMBOL TABLE ENTRY POINTER
         LA    R4,SYMELGTH(R4) POINT TO NEXT ENTRY
         ST    R4,SYMPTR      SAVE POINTER
         MVC   SYMNAME,BUFFER MOVE SYMBOL
         MVC   SYMOFSET,OFFSET+2 MOVE OFFSET
         BR    R14            DUMMY REMEMBER ROUTINE
         SPACE
* * * * * * * * ------- -------- -------- ------- *
BADPARSE DS 0H
         LA    R15,13             REENTER COMMAND
         B     RETURN1            RETURN TO CALLER
         SPACE 2
MACWRAP  DS    0H
         SR    R15,R15             GOOD COMPLETION
RETURN1  DS    0H
         L     R1,PARMADDR         PARAMETER ADDRESS
         MVC   ADPLASID,SAVEASID   RESTORE CALLING ASID
         LR    R3,R15              SAVE REG 15(RETURN CODE)
         TM    OPTIONS,SNAPDUMP    Q. SNAP DUMP REQUESTED
         BNO   RETURN2             A. NO
* SNAP STORAGE DUMP
         OPEN  (SNAP,(OUTPUT))
         SNAP  ID=1,DCB=SNAP,PDATA=REGS
         LA    R4,SAVEAREA         BEGINNING OF WORK AREA
         L     R5,=A(DBUFFERL)     LENGTH OF WORKAREA
         AR    R5,R4               END OF WORKAREA
         SNAP  ID=2,DCB=SNAP,STORAGE=((R4),(R5))
         L     R4,FMTADDR          DUMP STORAGE ADDRESS
         LTR   R4,R4               Q. WAS THERE ANY
         BZ    RETURN1A            A. NO, SKIP DUMPING STORAGE
         SH    R4,=H'4'            BACK UP TO BEGINNING
         L     R5,0(R4)            PICK UP LENGTH
         AR    R5,R4               BUMP TO END OF STORAGE
         SNAP  ID=3,DCB=SNAP,STORAGE=((R4),(R5))
RETURN1A DS    0H
         L     R1,PARMADDR         PARAMETER LIST ADDRESS
         L     R4,ADPLEXT          ADDRESS OF EXTENSION
         L     R4,0(R4)            CONTROL CARD ADDRESS
         LA    R5,160(R4)          ALLOW FOR 2 CARDS
         SNAP  ID=4,DCB=SNAP,STORAGE=((R4),(R5))
         CLOSE (SNAP)
RETURN2  L     R5,BUFPTR          IO BUFFER ADDRESS
         LA    R5,0(R5)           CLEAR HIGH ORDER BYTE
         LTR   R5,R5              Q. WAS THERE ONE
         BZ    RETURN3            A. NO
         LA    R4,MACDCB          ESTABLISH ADDRESSABILITY FOR DSECT
         USING IHADCB,R4          ASSIGN ADDRESSABILITY FOR DSECT
         LH    R6,DCBBLKSI        BLOCK SIZE = BUFFER SIZE
         DROP  R4                 DROP DSECT ADDRESSABILITY
         L     R1,PARMADDR        PARAMETER ADDRESS
         SR    R4,R4              CLEAR REG 4
         IC    R4,ADPLSBPL        PICK UP SUBPOOL
         FREEMAIN R,LV=(R6),A=(R5),SP=(R4) FREE THE BUFFER
         XC    BUFPTR,BUFPTR      CLEAR BUFFER POINTER
RETURN3  DS    0H
         L     R1,PARMADDR        PARAMETER ADDRESS
         SR    R4,R4              CLEAR REG 4
         IC    R4,ADPLSBPL        PICK UP SUBPOOL
         L     R1,FMTADDR         STORAGE DUMP ADDRESS
         LA    R1,0(R1)           CLEAR HIGH ORDER BYTE
         LTR   R1,R1              Q. WAS THERE ONE
         BZ    RETURN4            A. NO
         S     R1,=F'4'            BACK UP TO SIZE AND SUBPOOL
         L     R0,0(R1)             PICK UP SIZE AND SUBPOOL
         FREEMAIN R,LV=(R0),A=(R1),SP=(R4) FREE DUMP STORAGE
         XC    FMTADDR,FMTADDR    CLEAR STORAGE AREA FOR DUMP ADDR
         LR    R15,R3            RESTORE RETURN CODE
RETURN4  DS    0H
         LA    R4,MACDCB           MACLIB DCB
         USING IHADCB,R4           ASSIGN DCB DSECT
         L     R5,BUFPTR           POINTER TO IO BUFFER
         LTR   R5,R5               Q. IO BUFFER IN USE
         BZ    MACCLOSE            A. NO, DON'T TRY FREE
         LH    R6,DCBBLKSI         SIZE OF IO BUFFER AREA
         L     R1,PARMADDR         PARAMETER ADDRESS
         SR    R7,R7               CLEAR REG 7
         IC    R7,ADPLSBPL         PICK UP SUBPOOL
         FREEMAIN R,LV=(R6),A=(R5),SP=(R7) FREE THE BUFFER
         XC    BUFPTR,BUFPTR       INDICATE NO BUFFER EXISTS
MACCLOSE CLOSE ((R4)),MF=(E,CLOSE) CLOSE & FREE THE FILE
         LTR   R15,R15             Q. GOOD CLOSE
         BZ    MACSTRTA            A. YES
         L     R1,PARMADDR         PARMLIST ADDRESSABILITY
         L     R14,ADPLBUF         PRINT BUFFER ADDRESS.
         MVC   BUF(L'M01006,R14),M01006
         L     R15,ADPLPRNT         PRINT SUBROUTINE ADDRESS
         BALR  R14,R15              PRINT THE LINE
MACSTRTA DS    0H
         L     R7,4(R13)         PICK UP CALLER'S REGS ADDRESS
         ST    R15,16(R7)        SAVE OUR RETURN CODE
         L     R1,PARMADDR       PARAMETER LIST
         SR    R4,R4             CLEAR REG 4
         IC    R4,ADPLSBPL       SUBPOOL ID
         L     R8,=A(DBUFFERL)   BUFFER LENGTH
         FREEMAIN R,A=(R13),LV=(R8),SP=(R4)
         LR    R13,R7            CALLER'S REG 13
         LM    R14,R12,12(R13)   RESTORE CALLER'S REGS
         BR    R14               RETURN TO CALLER
         TITLE 'MACLIB - I/O HANDLING SUBROUTINES'
READRTN  DS    0H
         STM   R4,R6,READSAV       SAVE REGISTERS OVER READ
         LA    R4,MACDCB           MACLIB DCB ADDRESS
         USING IHADCB,R4           ASSIGN DCB DSECT REG
         L     R3,BUFPTR           IO BUFFER ADDRESS
         READ  MACLIBED,SF,(R4),(R3),MF=E READ A RECORD
         CHECK MACLIBED,DSORG=ALL   CHECK I/O COMPLETION
         LH    R6,DCBBLKSI         PICK UP BLOCKSIZE
         L     R5,DCBIOBA          IOB ADDRESS
         SH    R6,22(,R5)          SUBTRACT RESIDUAL COUNT
         LR    R5,R3               IO BUFFER ADDRESS
         LA    R5,0(R6,R5)         CALCULATE END OF BUFFER ADDRESS
         ST    R5,BUFDONE          SAVE END OF BUFFER ADDRESS
         LM    R4,R6,READSAV       RESTORE USER REGISTERS
         BR    R6                  RETURN TO CALLER
         SPACE 3
READDBLK DS    0H
         LA    R3,80(R3)           BUMP TO NEXT LOGICAL RECORD
         C     R3,BUFDONE          Q. LAST RECORD COMPLETE
         BL    0(R6)               A. NO, BACK TO CALLER
         B     READRTN             READ NEXT PHYSICAL BLOCK
         SPACE 2
         SPACE 2
* * * * * * * * ------- -------- -------- ------- *
* * * * * * * * COMMON HEX TO EBCIDIC CONVERTOR
* * * * * * * * ------- -------- -------- ------- *
UNHEX4   UNPK  DTEMP(9),0(5,R1) ASSUMES FTEMP FOLLOWS DTEMP
         TR    DTEMP(8),HEX4TAB-X'F0' ONLY 16 BYTES OF TABLE USED
         MVC   0(8,R2),DTEMP  MOVE TO USER BUFFER
         BR    R14            FOUR BYTES CONVERTED, RETURN
         SPACE 2
* * * * * * * * ------- -------- -------- ------- *
* * * * * * * * SET VALUE OF EQUATED SYMBOL
* * * * * * * * ------- -------- -------- ------- *
EQUSET   STM   R14,R7,SAVER14 SAVE REGISTERS
         BAL   R14,SYMFIND     FIND THE EQUATED SYMBOL
         SR    R15,R15         CLEAR REG 15
         L     R14,DTEMP       Q. SYMBOL FOUND
         BNZ   EQUSET4         A. YES
         LA    R15,4           A. NO
         B     EQUSET4         CONTINUE
EQUSET4  LM    R14,R7,SAVER14 RESTORE REGS
         LTR   R15,R15       SET CONDITION CODE
         BR    R14            GO BACK
         SPACE 2
* * * * * * * * ------- -------- -------- ------- *
* * * * * * * * FIND SYMBOL IN TABLE
* * * * * * * * ------- -------- -------- ------- *
SYMFIND  XC    DTEMP,DTEMP    CLEAR ADDRESS RETURN AREA
         USING SYMDSECT,R4    ASSIGN DSECT ADDRESSABILITY
         LA    R2,SYMTABLE    ADDRESS OF SYMBOL TABLE
SYMFINDA CLC   SYMCOM(8),0(R2) Q. THIS THE SYMBOL
         BE    SYMFIND1        A. YES
         LA    R2,SYMELGTH(R2) BUMP TO NEXT ENTRY
         C     R2,SYMLAST      Q. END OF TABLE
         BH    0(R14)          A. YES, RETURN TO CALLER
         B     SYMFINDA        TRY AGAIN
SYMFIND1 ST    R2,DTEMP        SAVE THE ENTRY ADDRESS
         BR    R14            RETURN
         SPACE 2
         TITLE 'MACLIB - TABLES'
         LTORG
         SPACE 3
SEPTBL   DS    0H
         DC    256XL1'00'
         ORG   SEPTBL+C' '
         DC    C' '
         ORG   SEPTBL+C'='
         DC    C'='
         ORG   SEPTBL+C','
         DC    C','
         ORG
         SPACE 3
SKPBLK   DC    64X'04',X'00',60X'04'    00 - 7C  SKIP BLANK
         DC    X'20',67X'04'            7D - C0  TIC FOR LENGTH
         DC    X'18',2X'10',X'1C'       C1 - C4  TYPE A B C D
         DC    2X'18',X'04'             C5 - C7  TYPE E F
         DC    X'14',10X'04'            C8 - D2  TYPE H
         DC    X'0C',3X'04'             D3 - D6  LENGTH L  <TYPE L>
         DC    X'10',X'18',9X'04'       D7 - E1  TYPE P Q
         DC    X'14',2X'04'             E2 - E4  TYPE S
         DC    X'18',X'04'              E5 - E6  TYPE V
         DC    X'10',X'14',X'10',6X'04' E7 - EF  TYPE X Y Z
         DC    10X'08',6X'04'           F0 - FF  NUMBER 0 - 9
SKPNBLK  DC    64X'00',X'04',191X'00'   SKIP NON BLANK
SKPNTIC  DC    125X'00',X'04',130X'00'  SKIP NON TIC
         SPACE 3
TRTABLE1 DC    256X'00'
         ORG   TRTABLE1+C' '
         DC    C' '          BLANK IN TABLE
         ORG   TRTABLE1+C'-'
         DC    C'-'           MINUS SYMBOL TO TABLE
         ORG   TRTABLE1+C'+'
         DC    C'+'           PLUS SYMBOL TO TABLE
         ORG
         SPACE 3
TRTABLE2 DC    64C'.'         64 UNPRINTABLE CHARACTERS
         DC    C' '           DISPLAY BLANK AS BLANK
         DC    9C'.'          9 UNPRINTABLE CHARACTERS
         DC    X'4A4B4C4D4E4F50' 7 '.<(+&'
         DC    9C'.'          9 UNPRINTABLE CHARACTERS
         DC    X'5A5B5C5D5E5F6061' 8 '!$*);^-/'
         DC    9C'.'          9 UNPRINTABLE CHARACTERS
         DC    X'6B6C6D6E6F'  5 ',%_>?'
         DC    10C'.'         10 UNPRINTABLE CHARACTERS
         DC    X'7A7B7C7D7E7F' 6 ':#@'="'
         DC    C'.'           1 UNPRINTABLE CHARACTER
         DC    X'818283848586878889' 9 'ABCDEFGHI' LOWER CASE
         DC    7C'.'          7 UNPRINTABLE CHARACTERS
         DC    X'919293949596979899' 9 'JKLMNOPQR' LOWER CASE
         DC    8C'.'          8 UNPRINTABLE CHARACTERS
         DC    X'A2A3A4A5A6A7A8A9' 8 'STUVWXYZ' LOWER CASE
         DC    23C'.'         23 UNPRINTABLE CHARACTERS
         DC    X'C1C2C3C4C5C6C7C8C9' 9 'ABCDEFGHI' UPPER
         DC    7C'.'          7 UNPRINTABLE CHARACTERS
         DC    X'D1D2D3D4D5D6D7D8D9' 9 'JKLMNOPQR' UPPER
         DC    8C'.'          8 UNPRINTABLE CHARACTERS
         DC    X'E2E3E4E5E6E7E8E9' 8 'STUVWXYZ' UPPER
         DC    6C'.'          6 UNPRINTABLE CHARACTERS
         DC    X'F0F1F2F3F4F5F6F7F8F9' 10 '0123456789'
         DC    6C'.'          6 UNPRINTABLE CHARACTERS
HEX4TAB  DC    CL16'0123456789ABCDEF'
         SPACE 2
HEXCONVT EQU   *-193           WE DON'T USE FIRST PART OF TABLE
         DC    X'0A0B0C0D0E0F' A-F
         DC    42X'00'
         DC    X'010203040506070809' 1-9
         SPACE 2
         TITLE 'MACLIB - CONSTANTS'
         EJECT
         SPACE 2
HEXTAB1  DC    X'00FAFBFCFDFEFF000000000000000000F0F1F2F3F4F5F6F7F8F9'
         SPACE 2
SNAP     DCB   DDNAME=SNAP,DSORG=PS,RECFM=VBA,BLKSIZE=1632,LRECL=125,  -
               MACRF=(W)
MMACDCB  DCB   DDNAME=MACLIB,MACRF=(R),DSORG=PO,EODAD=BLDDONE MODEL DCB
MREAD    READ  MACLIBE,SF,MF=L       MODEL READ
MCLOSE   CLOSE (,),MF=L              MODEL CLOSE                  DFG00
MOPEN    OPEN  (,INPUT),MF=L         MODEL OPEN
         SPACE 3
BLDLMOVE MVC   BLDLNAME(*-*),0(R15) MOVE MACRO NAME TO BLDL LIST
DSECTMVC MVC   DSECTNAM(*-*),0(R15) MOVE DSECT NAME TO SAVE AREA
         LTORG
M01001   DC   C'UNABLE TO LOCATE DSECT          IN MACRO'
M01005   DC   C'UNABLE TO LOCATE MACRO          IN MACLIB'
M01006   DC  C'CLOSE FOR PREVIOUS MACLIB FAILED, COMMAND WILL CONTINUE'
M01009   DC   C'MACLIB LIBRARY OPEN FAILURE'
M01010   DC   C'BLDL FOR MACRO          FAILED'
M01011   DC   C'SYMBOL TABLE FULL - PROGRAM CANNOT CONTINUE'
         TITLE 'MACLIB - DSECTS'
         PRINT GEN
         IHAABDPL
*
*
*        DEFINE DSECT FOR ADDR. FILEDS
*
PDDSCT   DSECT
PDEXPR   DS    F                  ==> EXPRESSION
PDLEN3   DS    H                  LENGTH OF EXPRESSION
         DS    H                  RESERVED
PDFLG4   DS    X
PDSIGN   DS    X
PDINDR   DS    H                  INDIRECT COUNT
PDNEXT   DS    A                  POINTER TO NEXT
*
SYMDSECT DSECT
SYMNAME  DS    CL8                SYMBOL NAME
SYMOFSET DS    H                  OFFSET FROM BEGINNING OF DSECT
SYMLGTH  DS    H                  LENGTH OF EACH SUBFIELD
SYMCOUNT DS    H                  COUNT OF SUBFIELDS
SYMTLGTH DS    H                  TOTAL LENGTH OF THIS FIELD
SYMCOML  DS    XL1                LENGTH - 1 OF COMMENTS FIEDL
SYMTYPE  DS    XL1                SYMBOL TYPE ENTRY
SYMCODE  DS    CL4                OP CODE FOR THIS OPERATION
SYMCOM   DS    CL72               COMMENTS
SYMELGTH EQU   *-SYMNAME          LENGTH OF TABLE ENTRY
         SPACE 3
MACDSECT DSECT
SAVEAREA DS    18F                WORKING SAVEAREA
PARMADDR DS    F                  PARAMETER ADDRESS SAVE AREA
ZZ2MACCB DS    F
MACDCB   DCB   DDNAME=MACLIB,MACRF=(R),DSORG=PO,EODAD=BLDDONE
MACDCBE  EQU   *
         SPACE 3
READ     READ  MACLIBED,SF,MF=L DUMMY DECB NAME
READE    EQU   *
         SPACE 3
OPEN     OPEN  (,INPUT),MF=L    OPEN THE FILE FOR INPUT
OPENE    EQU   *
         SPACE 3
CLOSE    CLOSE (,),MF=L         CLOSE & FREE THE FILE             DFG00
CLOSEE   EQU   *
         SPACE 3
         DS    0D
BLDLLIST DS    0CL16
BLDLNO   DS    H                  NUMBER OF ENTRIES IN LIST
BLDLLGTH DS    H                  LENGTH OF AN ENTRY
BLDLNAME DS    CL8                NAME OF MEMBER
BLDLTTRK DS    CL4                TTR AND CONCATENATION NUMBER
         SPACE 3
DSECTNAM DS    CL8                DSECT NAME SAVE AREA
MACRONAM DS    CL8                MACRO NAME SAVE AREA
         SPACE 3
BUFPTR   DS    A                  MACRO LIBRARY DATA RECORD ADDRESS
BUFDONE  DS    A                  END OF DATA RECORD ADDRESS
READSAV  DS    3A                 R4 - R6 SAVE AREA DURING READ
         SPACE 3
DUMPLGTH DS    F              LENGTH OF STORAGE FOR DSECT MAP
DUMPADDR DS    F              VIRTUAL ADDRESS OF CONTROL BLOCK
DUMPASID DS    H              ASID ADDRESS
SAVEASID DS    H              SAVE AREA FOR CALLING ASID
FMTLINE  DC    CL100' '       DISPLAY LINE                        DFG00
FROM     DS    CL8            STARTING FIELD
TO       DS    CL8            ENDING FIELD
R4SAVE   DC    A(0)           TEMP SAVE FOR REG 4
R14SAVE  DC    A(0)           TEMP SAVE FOR REG 14
         SPACE 2
OPTIONS  DS    XL1            USER OPTION BYTE
COMMENTS EQU   X'80'          COMMENTS REQUESTED
HEX      EQU   X'40'          FORCE HEX CONVERSION OF CHARACTER DATA
SNAPDUMP EQU   X'20'
         SPACE
SWITCHES DS    XL1            PROGRAM STATUS SWITCHES
*              X'80'          DSECT NAME WAS FOUND IN MACRO
*              X'40'          POSSIBLE BOUNDARY ALIGNMENT IN PROGRESS
         SPACE 2
FMTADDR  DC    A(0)           CONTROL BLOCK ADDRESS
COMADDR  DC    A(0)           ADDRESS OF 1ST BLANK AFTER OPERAND
OPERADDR DC    A(0)           ADDRESS OF 1ST OPERAND
COUNT    DC    F'0'           FIELD COUNT ON DC OR DS RECORD
OFFSET   DC    F'0'           OFFSET OF CURRENT FIELD
NEWOFF   DC    F'0'           OFFSET OF NEXT FIELD
LENGTH   DC    F'0'           LENGTH OF EACH SUBFIELD
TOTLGTH  DC    H'0'           LENGTH OF CURRENT FIELD
SAVER14  DS    10F            REGISTER SAVE AREA
DTEMP    DC    D'0'           WORK AREA
FTEMP    DC    D'0'           WORK AREA
FTEMP4   EQU   FTEMP+4,4      WORK WORD.                          DFG00
ORGSAVE  DS    F              ORIGIN SAVE AREA FOR ORG STATEMENTS
FIRSTTIC DC    A(0)           ADDRESS OF 1ST TIC
FMTSIZE  DC    A(0)           LENGTH OF CURRENT LINE
SYMPTR   DC    A(0)           SYMBOL TABLE ENTRY POINTER
SYMCNT   DS    F              NUMBER OF AVAILABLE ENTRIES +1
SAVESTRT DS    A              STARTING ADDRESS OF STORAGE DUMP
SAVELGTH DS    A              LENGTH OF PIECE TO BE PRINTED
SAVEPLTH DS    A              LENGTH OF SMALL PIECE EACH FIELD
SAVEPCNT DS    A              TEMPORARY MULTIPLIER
CBFIELD  DS    XL512          LOCAL BUFFER FOR FIELD DATA
TYPE     DC    C' '           TYPE OF FIELD
NUMFLAG  DC    X'00'          INDICATE IF COUNT WAS PRESENT
LFLAG    DC    X'00'          INDICATE IF LENGTH WAS PRESENT
SYMLAST  DS    A             ADDRESS OF LAST SYMBOL ENTRY
SYMSIZE  EQU   1250           NUMBER OF ENTRIES IN SYMBOL TABLE
         DS    0D
SYMTABLE DS    (SYMSIZE)XL(SYMELGTH) SYMBOL TABLE ENTRIES
DBUFFERL EQU   *-SAVEAREA         DSECT LENGTH
         TITLE 'MACLIB - SVC99 CONTROL BLOCK DSECTS'
         PRINT NOGEN
         IEFZB4D0
         IEFZB4D2
         SPACE 3
BUFDSECT DSECT
BUFFER   DS    CL80             IO BUFFER
         SPACE 3
         TITLE 'MACLIB - DCB DSECT'
         IHADCB DSORG=PO
         END
