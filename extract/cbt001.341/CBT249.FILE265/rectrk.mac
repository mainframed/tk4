 TITLE '                             COMPUTE NUMBER OF RECORDS / TRACK'
         SPACE 1
* TITLE -- SUBROUTINE TO COMPUTE NUMBER OF RECORDS / TRACK
*
* CALLING SEQUENCE --
*
* NAME   L     R15,=V(RECTRK)      LOAD ADDRESS OF SUBROUTINE
*        LA    R1,PARMS            LOAD ADDRESS OF PARM LIST
*        BALR  R14,R15             CALL SUBROUTINE
*        ...
* PARMS  DC    A(DCB)              ADDRESS OF AN OPEN'ED DCB
*        DC    A(BLKSIZE)          ADDRESS OF WORD CONTAINING THE BLOCK
* *                                 SIZE
*        DC    A(SWITCH)           ADDRESS OF BYTE CONTAINING BIT
* *                                 SIGNIFYING IF RECORD IS KEYED OR
* *                                 UNKEYED
*        DC    A(RESULT)           RESULT PLACED HERE
* DCB    DCB   --- ANY DCB PARMS ---
* BLKSIZE DC   F'---'              BLKSIZE
* RESULT DC    F'**'               NUMBER OF RECORDS / TRACK
* SWITCH DC    X'80'               HI BIT SET IF KEYED RECORD
*
* REGISTER 15 IS USED AS A RECORD CODE.  IF R15 = 0, THEN THE NUMBER
*   RECORDS / TRACK IS PLACED IN RESULT.  IF R15 ^= 0, THEN THERE WAS
*   A SPECIFICATION ERROR.
         SPACE 5
         MACRO
         SETR
         LCLA  &A
.L       AIF   (&A GT 15).X
R&A      EQU   &A
&A       SETA  &A+1
         AGO   .L
.X       MEND
         SPACE 1
RECTRK   CSECT
         USING *,R12               ESTABLISH PROGRAM ADDRESSABILITY
         SAVE  (14,12),,COMPUTE-RECORDS-PER-TRACK--V0L0--SEP-15-1978
         LR    R12,R15             SETUP PROGRAM BASE REGISTER
         LM    R2,R5,0(R1)         LOAD PARM REGISTERS
         SPACE 1
         USING IHADCB,R2           SETUP DCB ADDRESSABILITY
         SPACE 1
         TM    DCBOFLGS,X'10'      TEST IF DCB OPEN'ED
         BZ    RECERR1             BR IF SO
         SPACE 1
         L     R2,DCBDEBAD         LOAD ADDRESS OF DEB FROM DCB
         SPACE 1
         DROP  R2                  TERMINATE DCB ADDRESSABILITY
         SPACE 1
         L     R2,32(,R2)          LOAD UCB ADDRESS FROM DEB
         SPACE 1
         CLI   UCBTBYT3(R2),X'20'  IS THIS A DIRECT ACCESS UCB
         BNE   RECERR2             BR IF NOT
         SPACE 1
         SLR   R14,R14             GET DEVICE TYPE
         IC    R14,UCBTBYT4(,R2)    FROM UCB
         L     R15,CVTPTR          GET CVT ADDRESS
         L     R15,CVTZDTAB(,R15)  POINT TO DEVICE
         IC    R14,0(R14,R15)       CHARACTERISTICS TABLE
         ALR   R14,R15               ENTRY FOR DEVICE
         SPACE 1
         USING SPZ,R14             PROVIDE TBL ENTRY ADDRESSABILITY
         SPACE 1
         L     R6,0(,R3)           LOAD BLKSIZE
         LR    R15,R6              SET NOT-LAST-RECORD-LENGTH
         SLR   R7,R7               CLEAR R7 AND R10
         LR    R10,R7               FOR INSERTS
         IC    R7,SPZU             GET SUBTRAHEND FOR UNKEYED BLOCKS
         IC    R10,SPZL            GET LAST-BLOCK OVERHEAD
         TM    SPZF,SPZF2305       TEST FOR 2305
         BZ    RECS10              BR IF NOT 2305
         LH    R10,SPZO             ELSE GET HALFWORD OVERHEAD
RECS10   ALR   R6,R10              ADD OVERHEAD AND SUBTRACT
         TM    0(R4),X'80'          UNKEYED OVERHEAD              KEYED
         BO    RECS12                ADJUSTMENT                   KEYED
         SLR   R6,R7
RECS12   TM    SPZF,SPZFT          SHOULD WE APPLY TOLERANCE      KEYED
         BZ    RECS20              BR IF NOT
         MH    R15,SPZT             ELSE MULTIPLY BY TOLERANCE
         SRL   R15,9                 AND DIVIDE BY 512
RECS20   TM    SPZF,SPZF2305       TEST FOR 2305
         BO    RECS30              BR IF SO
         IC    R10,SPZNL            ELSE GET NOT-LAST-BLOCK-OVERHEAD
RECS30   ALR   R15,R10             ADD OVERHEAD, SUBTRACT         KEYED
         TM    0(R4),X'80'          UNKEYED OVERHEAD              KEYED
         BO    RECS32                ADJUSTMENT TO GETKEYED
         SLR   R15,R7                 NOT-LAST-BLOCK LENGTH
RECS32   LH    R7,SPZR             GET MAXIMUM LENGTH             KEYED
         SLR   R7,R6               TAKE AWAY LAST-BLOCK LENGTH
         SLR   R6,R6               GET COUNT OF
         DR    R6,R15               NOT-LAST BLOCKS
         LA    R6,1(,R7)           ADD 1 FOR RECORDS / TRACK
         ST    R6,0(,R5)           SAVE RESULT
         SR    R15,R15             SETUP RETURN CODE
RECRTN   RETURN (14,12),T,RC=(15)  RETURN TO CALLER
RECERR1  LA    R15,4               SETUP DCB NOT OPEN ERROR
         B     RECRTN              GO RETURN
RECERR2  LA    R15,8               SETUP UCB NOT D.A.
         B     RECRTN              GO RETURN
SPZ      DSECT
SPZC     DS    H                   NUMBER OF CYLINDERS
SPZH     DS    H                   NUMBER OF TRACKS PER CYLINDER
SPZR     DS    H                   MAXIMUM TRACK LENGTH
SPZO     DS    0H                  BLOCK OVERHEAD -- 2305
SPZNL    DS    X                   BLOCK OVERHEAD -- KEYED
SPZL     DS    X                   BLOCK OVERHEAD -- KEYED LAST
SPZU     DS    X                   BLOCK OVERHEAD -- UNKEYED
*                                                     ADJUSTMENT
SPZF     DS    X                   FLAGS --
SPZFT    EQU   1                      APPLY TOLERANCE FACTOR
SPZF2305 EQU   8                      DEVICE IS 2305
SPZT     DS    H                   TOLERANCE FACTOR IF SPZFT
SPZA     DS    H                   NUMBER OF ALTERNATE TRACKS
         SPACE 1
         PRINT NOGEN               TURN OFF MACRO EXPANSION
         DCBD  ,                   DEFINE DCB SYMBOLIC SYMBOLS
         SPACE 1
RECTRK   CSECT                     RETURN TO BASE CSECT
         SPACE 1
         SETR                      SETUP SYMBOLIC REGISTER DEFINITIONS
         SPACE 1
CVTPTR   EQU   16                  ADDRESS OF CVT IN PSA
CVTZDTAB EQU   64                  ADDRESS OF DEVICE CHARACTERISTICS TB
UCBTBYT3 EQU   18                  OFFSET OF 3RD BYTE OF DEVICE TYPE
UCBTBYT4 EQU   19                  OFFSET OF 4TH BYTE OF DEVICE TYPE
         SPACE 1
         END  ,                    END OF PROGRAM
