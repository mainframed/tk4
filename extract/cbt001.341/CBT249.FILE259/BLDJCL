00000100PROC 0 DEBUG
00000200/* )STARTHELP
00000300/* )F FUNCTION -
00000400/*    THE BLDJCL COMMAND IS A CLIST WHOSE FUNCTION IS TO
00000500/*    TRANSCRIBE NEW MEMBERS INTO PRODUCTION JCL LIBRARIES.
00000600/*    BLDJCL SHOULD BE USED TO UPDATE "RDC" AND "SYS2" LIBRARIES
00000700/*    IN ORDER TO AVOID THE ENQUEUE AND RESERVE PROBLEMS NORMALLY
00000800/*    ENGENDERED BY FOREGROUND COMMANDS SUCH AS EDIT.
00000900/*
00001000/* )X SYNTAX -  'BLDJCL'
00001100/* )O OPERANDS - YOU WILL BE PROMPTED FOR THE FOLLOWING:
00001200/*
00001300/*      * ENTER SOURCE 'DATASET' OR 'DATASET(MEMBER)'
00001400/*                -ENTER THE NAME OF THE DATASET TO BE TRANSCRIBED.
00001500/*                 IF IT IS A PDS, INCLUDE (MEMBERNAME).
00001600/*      * ENTER TARGET 'DATASET(MEMBER)' OR NULL TO UPDATE
00001700/*               'SYS2.JCLLIB(MEMBER)'.
00001800/*                -ENTER THE NAME OF THE TARGET DATASET;
00001900/*                 INCLUDE (MEMBERNAME)  -OR-
00002000/*                 DEPRESS ENTER TO UPDATE THE DEFAULT LIBRARY.
00002100/*
00002200/*             MISCELLANEOUS MESSAGES -
00002300/*
00002400/*      *  DATASET NAMES WITHOUT QUOTES WILL BE PREFIXED WITH "LOGONID".
00002500/*                 -INFORMATIONAL MESSAGE.
00002600/*      *  SEQUENTIAL DATASET SPECIFIED OR ERROR SPECIFYING MEMBER.
00002700/*                 -INFORMATIONAL MESSAGE. IMPLIES MISSING OR UNBALANCED
00002800/*                   PARENTHESES.
00002900/*      *  PLEASE ENTER MEMBER NAME IN TARGET DATASET:
00003000/*                 -OCCURS IF SEQUENTIAL SOURCE DATASET SPECIFIED.
00003100/*                  ENTER MEMBER NAME.
00003200/*      *  COMMAND "BLDJCL" INTENDED FOR   L I B R A R Y  USE ONLY.
00003300/*         PLEASE SPECIFY TARGET IN THE FORM: 'DSNAME(MEMBER)'.
00003400/*                 -NO TARGET MEMBER NAME WAS SPECIFIED. RE-ENTER
00003500/*                  TARGET DATASET NAME AND MEMBER NAME.
00003600/*      *  SPECIFY VOLUME-SERIAL NUMBER OR NULL IF CATALOGUED:
00003700/*                 -ALLOWS USER TO BYPASS CATALOG  BY SPECIFYING
00003800/*                  VOLUME SERIAL NUMBER. ENTER EITHER VOLUME-SERIAL
00003900/*                  NUMBER OR NULL LINE TO ACCEPT CATALOG DEFAULT.
00004000/*      *  SOURCE DATASET IS: DSNAME - TARGET DATASET IS: DSNAME
00004100/*         IS THIS CORRECT? Y/N
00004200/*                 -FINAL VERIFICATION MESSAGE. REPLY "Y" TO ALLOW
00004300/*                  UPDATE OF TARGET DATASET. REPLY "N" TO RESPECIFY.
00004400/*      *  UPDATE PROCEEDING.
00004500/*                 -INFORMATIONAL MESSAGE. OCCURS AFTER RESPONDING "Y"
00004600/*                  TO PREVIOUS MESSAGE.
00004700/*
00004800/* )ENDHELP
00004900CONTROL NOFLUSH NOMSG
00005000ATTN EXIT
00005100IF &DEBUG NE &STR() THEN CONTROL CONLIST LIST SYMLIST
00005200SET &TARG EQ &SUBSTR(4:&LENGTH(&SYSICMD),&SYSICMD)
00005300REDO: +
00005400WRITE DATASET NAMES WITHOUT QUOTES WILL BE PREFIXED WITH &SYSUID..
00005500WRITENR ENTER SOURCE 'DATASET' OR 'DATASET(MEMBER)'
00005600READ
00005700SET &SOURCE EQ &STR(&SYSDVAL)
00005800SET &N EQ &LENGTH(&SOURCE)
00005900DO WHILE &N GT 1
00006000  IF &SUBSTR(&N,&SOURCE) EQ ) THEN +
00006100  DO
00006200    SET &M EQ &N
00006300    SET &N EQ &N-1
00006400    DO WHILE &SUBSTR(&N,&SOURCE) NE &STR((
00006500      SET &N EQ &N-1
00006600    END
00006700    SET &MEMBER EQ &SUBSTR(&N+1:&M-1,&SOURCE)
00006800    GOTO GETARG
00006900  END
00007000  SET &N EQ &N-1
00007100END
00007200WRITE SEQUENTIAL SOURCE DATASET SPECIFIED OR ERROR SPECIFYING MEMBER.
00007300WRITENR PLEASE ENTER MEMBER NAME IN TARGET DATASET:
00007400READ &MEMBER
00007500GETARG: WRITE ENTER TARGET 'DATASET(MEMBER)' OR NULL TO UPDATE 'SYS2.&TARG.LIB(&MEMBER)'
00007600RESPEC: READ
00007700SET &X EQ N
00007800IF &SYSDVAL EQ &STR() THEN +
00007900DO
00008000  SET &TARG EQ 'SYS2.&TARG.LIB(&MEMBER)'
00008100  SET &X EQ Y
00008200END
00008300/* ENSURE MEMBER OF TARGET DATASET IS SPECIFIED.*/
00008400ELSE +
00008500DO
00008600  DO
00008700    SET &TARG EQ &STR(&SYSDVAL)
00008800    SET &N EQ &LENGTH(&TARG)
00008900    DO WHILE &N GT 1
00009000      IF &SUBSTR(&N,&TARG) EQ ) THEN +
00009100      DO
00009200        /* RIGHT PAREN FOUND, LOOK FOR LEFT PAREN. */
00009300        SET &N EQ &N-1
00009400        DO WHILE &SUBSTR(&N,&TARG) NE &STR((
00009500          SET &N EQ &N-1
00009600        END
00009700        GOTO MEMFOUND
00009800      END
00009900      SET &N EQ &N-1
00010000    END
00010100  END
00010200WRITE COMMAND "&SYSICMD" INTENDED FOR   L I B R A R Y  UPDATE ONLY.
00010300WRITE PLEASE SPECIFY TARGET IN THE FORM: 'DSNAME(MEMBER)'.
00010400GOTO RESPEC
00010500END
00010600MEMFOUND: +
00010700SET &VOL EQ &STR()
00010800SET &UNIT EQ &STR()
00010900IF &X NE Y THEN +
00011000DO
00011100  WRITENR SPECIFY VOLUME-SERIAL NUMBER OR NULL IF CATALOGUED:
00011200  READ &VOL
00011300  IF &VOL EQ &STR() THEN GOTO FINAL
00011400  SET &VOL EQ &STR(VOLUME(&VOL))
00011500  SET &UNIT EQ UNIT(SYSALLDA)
00011600END
00011700FINAL: +
00011800WRITE SOURCE DATASET IS: &SOURCE
00011900WRITE TARGET DATASET IS: &TARG &VOL
00012000WRITENR IS THIS CORRECT? Y/N
00012100READ &YN
00012200IF &YN NE Y THEN GOTO REDO
00012300ATTN GOTO CLEANUP
00012400WRITE UPDATE PROCEEDING.
00012500%UMSG
00012600FREE F(SYSIN SYSPRINT SYSUT1 SYSUT2)
00012700ALLOC F(SYSUT2) DA(&TARG) SH &VOL &UNIT
00012800ALLOC F(SYSUT1) DA(&SOURCE) SH
00012900FREE F(SYSIN SYSPRINT)
00013000ALLOC F(SYSIN) DUMMY
00013100ALLOC F(SYSPRINT) DUMMY
00013200CALL 'SYS1.LINKLIB(IEBGENER)'
00013300CLEANUP: FREE F(SYSUT1 SYSUT2 SYSIN SYSPRINT)
00013400ALLOC F(SYSIN) DA(*)
00013500ALLOC F(SYSPRINT) DA(*)
