00000100PROC 3 MONTH DAY YEAR
00000200/*                                                                 */
00000300/* EXTERNAL FORM OF JULDATE (Q.V.).                                */
00000400/* CONVERTS GREGORIAN DATE TO JULIAN AND YIELDS OTHER GOODIES      */
00000500/* AS WELL. SEE JULDATE FOR SHOPDATE EXPLANATION.                  */
00000600/* SORRY, NO GOOD AFTER 1999.                                      */
00000700/*                                                                 */
00000800CONTROL LIST
00000900SET &JULIAN = &SUBSTR(&EVAL((&MONTH*3)-2):&EVAL(&MONTH*3),000031059090120151181212243273304334)
00001000SET &JULIAN = &JULIAN + &DAY
00001100IF &YEAR//4 = 0 AND &MONTH GT 2 THEN SET &JULIAN = &JULIAN + 1
00001200SET &SHOP = &JULIAN
00001300SET &L = &LENGTH(&JULIAN)
00001400SET &JULIAN = &STR(X00&JULIAN)
00001500SET &JULIAN = &STR(&YEAR)&STR(&SUBSTR(&EVAL(&L+1):&EVAL(&LENGTH(&JULIAN)),&JULIAN))
00001600WRITE THE JULIAN DATE FOR &MONTH-&DAY-&YEAR IS &JULIAN
00001700SET &SHOP = &EVAL(&YEAR*365) + &EVAL((&YEAR-1)/4) + &SHOP - 1
00001800SET &WEEKDAY = &SUBSTR(&EVAL(((&SHOP//7)*6)+1):&EVAL(((&SHOP//7)*6)+6),   MON  TUESWEDNES THURS   FRI SATUR   SUN)
00001900SET &WEEKDAY = &SUBSTR(&EVAL(6-&LENGTH(&WEEKDAY)+1):6,&WEEKDAY)
00002000WRITE THE SHOP DATE IS &SHOP
00002100WRITE THE DAY OF THE WEEK IS &WEEKDAY.DAY
