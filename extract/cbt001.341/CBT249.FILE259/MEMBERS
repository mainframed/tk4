00000100/*                                                                 */
00000200/* THIS HANDY CLIST IS USED TO GENERATE A LIST OF MEMBER NAMES     */
00000300/* OF ANY CATALOGUED PARTITIONED DATASET IN THE SEQUENTIAL         */
00000400/* DATASET OF YOUR CHOICE. THESE MAY THEN BE EDITED AND USED AS    */
00000500/* IEBCOPY CONTROL CARDS, INPUT FOR ANOTHER CLIST, ETC.            */
00000600/* NOTE: THE RECEIVING DATASET MUST EXIST BEFORE EXECUTION         */
00000700/*                                                                 */
00000800CONTROL NOFLUSH NOMSG
00000900FREE F(SYSPRINT SYSIN DD TARGET)
00001000FREE ATTR(A B)
00001100ATTR A RECFM(F B) LRECL(121) BL(1210)
00001200ATTR B RECFM(F B) LRECL(80) BL(1600)
00001300ALLOC F(SYSPRINT) NEW SP(1 1) TRACKS  USING(A) UNIT(SYSVIO)
00001400ALLOC F(SYSIN) NEW SP(1 1) TRACKS  USING(B) UNIT(SYSVIO)
00001500WRITENR ENTER NAME OF PDS:
00001600READ
00001700SET &PDSNAME EQ &SYSDVAL
00001800IF &SUBSTR(1,&PDSNAME) NE &STR(') THEN SET &PDSNAME EQ &SYSPREF..&PDSNAME
00001900ELSE SET &PDSNAME EQ &SUBSTR(2:&LENGTH(&PDSNAME)-1,&PDSNAME)
00002000WRITENR ENTER VOLUME THAT &SYSDVAL RESIDES ON:
00002100READ &PDSVOL
00002200WRITENR ENTER DEVICE TYPE OF &PDSVOL - (3330-1 OR 3350):
00002300READ &DEVTYPE
00002400SET &S EQ &STR( LISTPDS DSNAME=&PDSNAME,VOL=&DEVTYPE=&PDSVOL)
00002500ALLOC F(DD) DA('&PDSNAME') SH UNIT(SYSALLDA) VOL(&PDSVOL)
00002600WRITENR ENTER TARGET DATASET NAME:
00002700READ
00002800SET &FRED EQ &SYSDVAL
00002900ALLOC F(TARGET) DA(&SYSDVAL) SH
00003000OPENFILE SYSIN OUTPUT
00003100SET &SYSIN EQ &STR(&S)
00003200PUTFILE SYSIN
00003300CLOSFILE SYSIN
00003400CALL 'SYS1.LINKLIB(IEHLIST)'
00003500ERROR +
00003600DO
00003700  CLOSFILE SYSPRINT
00003800  CLOSFILE TARGET
00003900  FREE F(SYSPRINT SYSIN DD TARGET)
00004000  FREE ATTR(A B)
00004100  ALLOC F(SYSPRINT) DA(*)
00004200  ALLOC F(SYSIN) DA(*)
00004300  WRITE
00004400  WRITE YOU MAY NOW EDIT '&FRED'.
00004500  EXIT
00004600END
00004700OPENFILE SYSPRINT
00004800OPENFILE TARGET OUTPUT
00004900GET: GETFILE SYSPRINT
00005000IF &SUBSTR(2:11,&SYSPRINT) NE &STR(          ) THEN GOTO GET
00005100IF &SUBSTR(12,&SYSPRINT) EQ &STR( ) THEN GOTO GET
00005200SET &SYSPRINT EQ &SUBSTR(12:21,&SYSPRINT)
00005300SET &TARGET EQ &SYSPRINT
00005400PUTFILE TARGET
00005500GOTO GET
