00000100PROC 0 DATE()
00000200GLOBAL MONTH DAY YEAR JULIAN SHOP WEEKDAY
00000300/*                                                                   */
00000400/* THE FOLLOWING CLIST PERFORMS THESE FUNCTIONS:                     */
00000500/*                                                                   */
00000600/* TAKES ANY VALID GREGORIAN DATE IN THIS CENTURY AND                */
00000700/* CALCULATES THE JULIAN DATE, THE DAY OF THE WEEK, AND              */
00000800/* A 'SHOP DATE' IE- THE NUMBER OF DAYS ELAPSED BETWEEN              */
00000900/* THE INPUT DATE AND JAN. 1, 1900. THUS IF YOU FEED                 */
00001000/* THIS CLIST 2 DATES, THE DIFFERENCE BETWEEN THE TWO                */
00001100/* SHOP DATES EQUALS THE NUMBER OF ELAPSED DAYS BETWEEN              */
00001200/* THE TWO.                                                          */
00001300/*                                                                   */
00001400/* THE FORMATS OF THE GLOBAL VARIABLES ARE AS FOLLOWS:               */
00001500/*                                                                   */
00001600/* INPUT - MONTH=MM, DAY=DD, YEAR=YY                                 */
00001700/*         FOR EXAMPLE: 01 01 76                                     */
00001800/*         NOTE - 1 1 76 WILL NOT WORK.                              */
00001900/*                                                                   */
00002000/* OUTPUT - JULIAN=YYDDD, IE 99365 FOR 12/31/99                      */
00002100/* SHOP   - DD..DD, IE, 36523 FOR THE SAME DATE                      */
00002200/* WEEKDAY - MONDAY, TUESDAY, WEDNESDAY, ETC.                        */
00002300/*                                                                   */
00002400ERROR +
00002500DO
00002600  SET &LASTCC EQ 4
00002700  EXIT
00002800END
00002900IF &DATE EQ SYSDATE THEN -
00003000DO
00003100  SET &MONTH EQ &SUBSTR(1:2,&SYSDATE)
00003200  SET &DAY   EQ &SUBSTR(4:5,&SYSDATE)
00003300  SET &YEAR  EQ &SUBSTR(7:8,&SYSDATE)
00003400END
00003500SET &JULIAN = &SUBSTR(&EVAL((&MONTH*3)-2):&EVAL(&MONTH*3),000031059090120151181212243273304334)
00003600SET &JULIAN = &JULIAN + &DAY
00003700IF &YEAR//4 = 0 AND &MONTH GT 2 THEN SET &JULIAN = &JULIAN + 1
00003800SET &SHOP = &JULIAN
00003900SET &L = &LENGTH(&JULIAN)
00004000SET &JULIAN = &STR(X00&JULIAN)
00004100SET &JULIAN = &STR(&YEAR)&STR(&SUBSTR(&EVAL(&L+1):&EVAL(&LENGTH(&JULIAN)),&JULIAN))
00004200SET &SHOP = &EVAL(&YEAR*365) + &EVAL((&YEAR-1)/4) + &SHOP - 1
00004300SET &WEEKDAY = &SUBSTR(&EVAL(((&SHOP//7)*6)+1):&EVAL(((&SHOP//7)*6)+6),   MON  TUESWEDNES THURS   FRI SATUR   SUN)
00004400SET &WEEKDAY = &SUBSTR(&EVAL(6-&LENGTH(&WEEKDAY)+1):6,&WEEKDAY)DAY
