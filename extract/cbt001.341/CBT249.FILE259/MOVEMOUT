00000100/*                                                                 */
00000200/* THIS HAS BEEN INCLUDED MAINLY AS AN EXAMPLE OF A JCL GENERATOR. */
00000300/*                                                                 */
00000400/* IT WAS USED TO BUILD JCL FOR A 3330-1 TO 3350 DATASET MIGRATION.*/
00000500/*                                                                 */
00000600/* PLEASE NOTE THE FOLLOWING:                                      */
00000700/*                                                                 */
00000800/* 1) IEHMAP FROM THE CBT MODS TAPE IS THE DRIVING PROGRAM.        */
00000900/*    (IEHLIST COULD BE USED AS WELL, BUT DOES NOT YIELD           */
00001000/*    SPACE ALLOCATION INFORMATION.)                               */
00001100/* 2) NOTE THE TECHNIQUE USED FOR READING SYSPRINT AS AN INPUT     */
00001200/*    DATASET.                                                     */
00001300/* 3) NOTE IEHMAP CONTROL CARD GENERATION TECHNIQUE.               */
00001400/* 4) NOTE "DSECT" OF SYSPRINT INPUT RECORD. GENERATED BY          */
00001500/*    MAPREC CLIST. (SUBSEQUENTLY MODIFIED.)                       */
00001600/* 5) NOTE NODELIST SELECTION.                                     */
00001700/*                                                                 */
00001800CONTROL NOMSG
00001900SET &VOL EQ TSO001
00002000SET &TARGET EQ TSO000
00002100CONTROL NOFLUSH
00002200/*
00002300/*       GENERATE VIO IEHMAP LISTING OF &VOL
00002400/*
00002500FREE F(DSNAME1 SYSIN SYSPRINT) ATTR(MAPIN MAPOUT)
00002600ATTR MAPIN LRECL(80) BLKSIZE(3120) RECFM(F B)
00002700ATTR MAPOUT LRECL(137) BLKSIZE(3509) RECFM(V B A)
00002800ALLOC F(SYSIN) UNIT(SYSVIO) USING(MAPIN) SPACE(1) TRACKS
00002900ALLOC F(SYSPRINT) UNIT(SYSVIO) USING(MAPOUT) SPACE(1) CYLINDERS
00003000ALLOC F(DSNAME1) NEW SPACE(0) TRACKS UNIT(SYSALLDA) VOL(&VOL)
00003100OPENFILE SYSIN OUTPUT
00003200SET &SYSIN EQ &STR( MAP VOL=&VOL)
00003300PUTFILE SYSIN
00003400CLOSFILE SYSIN
00003500CALL 'SYS2.LINKLIB(IEHMAP)' 'TSO'
00003600/*
00003700/* BUILD "DSECT" OF IEHMAP OUTPUT  -  TO BE USED AS SUBSEQUENT INPUT.
00003800/*
00003900SET DSNAME EQ &&SUBSTR(2:45,&&SYSPRINT)
00004000SET SERIAL EQ &&SUBSTR(47:52,&&SYSPRINT)
00004100SET EXPDT EQ &&SUBSTR(69:76,&&SYSPRINT)
00004200SET DSO EQ &&SUBSTR(79:80,&&SYSPRINT)
00004300SET RECFM EQ &&SUBSTR(83:85,&&SYSPRINT)
00004400SET BLKSIZE EQ &&SUBSTR(89:93,&&SYSPRINT)
00004500SET LRECL EQ &&SUBSTR(95:99,&&SYSPRINT)
00004600SET TRKAL EQ &&SUBSTR(109:113,&&SYSPRINT)
00004700SET TYP EQ &&SUBSTR(131:131,&&SYSPRINT)
00004800/*
00004900/* OPEN INPUT FILE
00005000/*
00005100OPENFILE SYSPRINT
00005200/*
00005300/* FIRE UP OUTPUT FILES
00005400/*
00005500FREE F(OUT1 OUT2 OUT3)
00005600ALLOC F(OUT1) DA(EXEC.CNTL) SH
00005700ALLOC F(OUT2) DA(CATLG.CNTL) SH
00005800OPENFILE OUT1 OUTPUT
00005900OPENFILE OUT2 OUTPUT
00006000SET &OUT1 EQ &STR(//MOVEMOUT JOB 780780,SYS,NOTIFY=NORTHRO,CLASS=1
00006100PUTFILE OUT1
00006200SET &OUT1 EQ &STR(/*JOBPARM PROCLIB=PROCTEST)
00006300PUTFILE OUT1
00006400SET &OUT2 EQ &STR(//CATLG    JOB 780780,SYS,NOTIFY=NORTHRO,CLASS=1
00006500PUTFILE OUT2
00006600SET &OUT2 EQ &STR(//         EXEC PGM=IEHPROGM)
00006700PUTFILE OUT2
00006800SET &OUT2 EQ &STR(//SYSPRINT DD SYSOUT=A)
00006900PUTFILE OUT2
00007000SET &OUT2 EQ &STR(//&VOL   DD  UNIT=3330-1,VOL=SER=&VOL,DISP=OLD)
00007100PUTFILE OUT2
00007200SET &OUT2 EQ &STR(//&TARGET   DD  UNIT=3350,VOL=SER=&TARGET,DISP=OLD)
00007300PUTFILE OUT2
00007400SET &OUT2 EQ &STR(//SYSIN    DD  *)
00007500PUTFILE OUT2
00007600/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
00007700/*
00007800/* BUILD LIST OF NODES TO BE INCLUDED.
00007900/*
00008000/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
00008100SET &IP EQ 1
00008200WRITE NODES TO BE INCLUDED MUST BE ENTERED IN ALPHABETICAL ORDER.
00008300INC: WRITE ENTER NODE TO BE INCLUDED OR NULL LINE TO TERMINATE.
00008400  READ
00008500  IF &LENGTH(&SYSDVAL) EQ 0 THEN GOTO INCDONE
00008600  SET I&IP EQ &SYSDVAL
00008700  SET &IP EQ &IP+1
00008800  SET &SYSDVAL EQ &STR()
00008900GOTO INC
00009000INCDONE: +
00009100SET &IC EQ 1
00009200SET &NODE EQ &&I&IC
00009300/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
00009400/*                      MAIN LOOP
00009500/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
00009600ERROR +
00009700DO
00009800  WRITE &LASTCC
00009900  EXIT
00010000END
00010100GET: GETFILE SYSPRINT  /*  I.E. VIO IEHMAP LISTING.  */
00010200IF &LENGTH(&SYSPRINT) LT 52 THEN GOTO GET /* STRIP GARBAGE LINES. */
00010300/*
00010400/*  DISCOVER DEVICE TYPE AND BUILD LATENCY VALUES. */
00010500/*
00010600IF &SUBSTR(1:18,&DSNAME) EQ &STR(DEVICE DESCRIPTION) THEN +
00010700DO
00010800  IF &SUBSTR(27:32,&DSNAME) EQ &STR(3330-1) THEN +
00010900  DO
00011000    SET &TRKLENGTH EQ 13165
00011100    SET &CN EQ 35
00011200  END
00011300  ELSE +
00011400  DO
00011500    SET &TRKLENGTH EQ 19254
00011600    SET &CN EQ 185
00011700  END
00011800END
00011900IF &SERIAL NE &VOL THEN GOTO GET  /* USE VOLUME CONSTANT AS SENTINEL */
00012000IF &DSO EQ PO THEN SET &PROC EQ COPY /* PROC IN USER.PROCLIB. */
00012100ELSE SET &PROC EQ GENER              /* DITTO. */
00012200SET &DS EQ &DSNAME                   /* TO STRIP RIGHT BLANKS. */
00012300SET &DS EQ &DS                       /* RIGHT BLANKS NOW STRIPPED */
00012400/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
00012500/*
00012600/* ENSURE JCL IS CREATED ONLY FOR MEMBERS IN NODE LIST
00012700/*
00012800/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
00012900IF &NODE GT &SUBSTR(1:&LENGTH(&NODE),&DS) THEN GOTO GET
00013000IF &NODE LT &SUBSTR(1:&LENGTH(&NODE),&DS) THEN +
00013100DO
00013200  SET &IC EQ &IC+1
00013300  IF &IC GE &IP THEN EXIT /* NORMAL EXIT */
00013400  SET &NODE EQ &&I&IC
00013500  IF &NODE GT &SUBSTR(1:&LENGTH(&NODE),&DS) THEN GOTO GET
00013600  IF &NODE LT &SUBSTR(1:&LENGTH(&NODE),&DS) THEN +
00013700  DO
00013800    WRITE NODE LIST OUT OF SYNCH WITH IEHMAP
00013900    WRITE CURRENT NODE: &NODE
00014000    WRITE CURRENT DSNAME: &DS
00014100    EXIT
00014200  END
00014300END
00014400/*
00014500/*  FOLLOWING TWO DO GROUPS STRIP LEFT BLANKS FROM &TRKAL & &BLKSIZE.
00014600/*
00014700SET &N EQ 1
00014800DO WHILE &SUBSTR(&N,&TRKAL) EQ &STR( )
00014900  SET &N EQ &N+1
00015000END
00015100SET &ALLOC EQ &STR()
00015200SET &TRK EQ &SUBSTR(&N:5,&TRKAL)
00015300SET &P EQ 1
00015400DO WHILE &SUBSTR(&P,&BLKSIZE) EQ &STR( )
00015500  SET &P EQ &P+1
00015600END
00015700SET &BLK EQ &SUBSTR(&P:5,&BLKSIZE)
00015800/*
00015900/*  NEXT STATEMENT BUILDS ALLOCATION VALUE IN BLOCKS.
00016000/*  NOTE: NO SECONDARIES ALLOCATED.
00016100/*
00016200SET &PRIMARY EQ &EVAL((&TRKLENGTH/(&BLK+&CN))*&TRK)
00016300SET &DIR EQ &STR()                   /* ASSUME SEQUENTIAL */
00016400IF &DSO EQ PO THEN SET &DIR EQ 69    /* ARBITRARY VALUE   */
00016500/*
00016600/*  EXPIRATION DATE WILL BE EITHER 00000 OR THE FIRST DAY OF THE YEAR.
00016700/*
00016800IF &SUBSTR(4,&EXPDT) EQ &STR(.) THEN SET &EXP EQ &STR(00000)
00016900ELSE SET &EXP EQ &SUBSTR(7:8,&EXPDT)001
00017000/*
00017100/* BUILD IEBCOPY/IEBGENER EXEC CARDS.
00017200/*
00017300SET &OUT1 EQ &STR(//         EXEC &PROC,DSNAME='&DS',)
00017400PUTFILE OUT1
00017500SET &OUT1 EQ &STR(//             SPACE='(&BLK,(&PRIMARY,,&DIR))',)
00017600PUTFILE OUT1
00017700SET &OUT1 EQ &STR(//             EXPDT=&STR(&EXP),TARGET=&TARGET)
00017800PUTFILE OUT1
00017900/*
00018000/* BUILD IEHPROGM CONTROL CARDS FOR UNCATCAT JOB
00018100/*
00018200SET &OUT2 EQ &STR( CATLG   DSNAME=&DS,VOL=3350=&TARGET)
00018300PUTFILE OUT2
00018400GOTO GET  /* END OF LOOP. EXIT IS VIA ERROR RETURN CODE 400 (NORMAL).
