00000100PROC 1 NAME GROUP
00000200/*                                                                 */
00000300/* THIS CLIST IS SIMILAR TO "ADDRESS" IN FUNCTION.                 */
00000400/* SINCE IT IS SELF-MODIFYING, IT WOULD BEHOOVE THE                */
00000500/* USER TO KEEP IT IN A SEQUENTIAL DATASET IF THE UPDATE           */
00000600/* FUNCTION IS TO BE USED OFTEN. (A WAY TO UTILIZE IT AS A PRIMARY */
00000700/* COMMAND AND STILL MAINTAIN IT AS A SEQUENTIAL DATASET IS TO     */
00000800/* INSTALL ANOTHER CLIST IN SYS2.CMDPROC OR EQUIVALENT WHICH       */
00000900/* IN TURN INVOKES THIS ONE. EXAMPLE:                              */
00001000/*                                                                 */
00001100/* PROC 1 NAME GROUP                                               */
00001200/* EXEC '&SYSUID.PHONE.CLIST' '&NAME &GROUP'                       */
00001300/*                                                                 */
00001400/* NOTE: THE "GROUP" FUNCTION ENABLES YOU TO HANDLE THE PROBLEM OF */
00001500/* MULTIPLE NAMES.                                                 */
00001600/*                                                                 */
00001700CONTROL END(ENDC)
00001800ERROR +
00001900      DO
00002000        CONTROL NOMSG
00002100        WRITE &NAME HAS AN UNLISTED NUMBER
00002200        WRITE DO YOU WISH TO ENTER &NAME.'S NUMBER? Y/N
00002300        READ &ANS
00002400        IF &ANS EQ Y THEN GOTO NEWPHONE
00002500        EXIT QUIT
00002600      ENDC
00002700CONTROL MAIN
00002800IF &LENGTH(&NAME) GT 8 THEN SET &NAME EQ &SUBSTR(1:8,&NAME)
00002900IF &GROUP NE &STR() THEN GOTO NEWPHONE
00003000GOTO &NAME
00003100/*
00003200/* THIS ROUTINE IS ENTERED FROM THE ERROR ROUTINE
00003300/* IN RESPONSE TO A "Y" ANSWER.
00003400/*
00003500NEWPHONE: WRITE ENTER TEXT/NUMBER
00003600READ
00003700CONTROL NOFLUSH
00003800E '&SYSUID.SHARE.CLIST(PHONE)' CLIST  /* <== MODIFY THIS LINE  */
00003900V OFF
00004000RENUM 100 100
00004100IF &GROUP NE &STR() THEN +
00004200DO
00004300  FIND /END OF NEWPHONE ROUTINE/
00004400  FIND /&NAME/
00004500  INSERT WRITE &SYSDVAL
00004600  GOTO QUIT
00004700ENDC
00004800B
00004900INSERT &NAME: WRITE &SYSDVAL
00005000INSERT EXIT
00005100QUIT: END S
00005200EXIT QUIT
00005300/*
00005400/* END OF NEWPHONE ROUTINE. THAT WHICH FOLLOWS IS DATA.
00005500/*
