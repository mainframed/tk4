*          DATA SET DIDOCS     AT LEVEL 002 AS OF 04/05/82
DIDOCS   TITLE 'D I D O C S - SIMULATE OS CONSOLE FOR TSO USER '
*        REAL CODE STARTS ABOUT 694 LINES DOWN FROM HERE
         MACRO
&NAME    PGM   &BASE=R12,&SAVE=$$SAVE,&START=$$START,&EOJ=$$EOJ,&RC=16
&NAME    CSECT
         B     14(0,R15)           BRANCH AROUND PROGRAM ID.
         DC    AL1(8)              CSECT NAME LENGTH.
         DC    CL8'&NAME'          CSECT IDENTIFIER.
         DC    X'FF'               FILLER.
         STM   R14,R12,12(R13)     STORE THE REGISTERS.
         LR    &BASE,R15           ESTABLISH ADDRESSABILITY.
         USING &NAME,&BASE         TELL ASSEMBLER ABOUT BASE REGISTER.
R0       EQU   0                   REGISTER 0.
R1       EQU   1                   REGISTER 1.
R2       EQU   2                   REGISTER 2.
R3       EQU   3                   REGISTER 3.
R4       EQU   4                   REGISTER 4.
R5       EQU   5                   REGISTER 5.
R6       EQU   6                   REGISTER 6.
R7       EQU   7                   REGISTER 7.
R8       EQU   8                   REGISTER 8.
R9       EQU   9                   REGISTER 9.
R10      EQU   10                  REGISTER 10.
R11      EQU   11                  REGISTER 11.
R12      EQU   12                  REGISTER 12.
R13      EQU   13                  REGISTER 13.
R14      EQU   14                  REGISTER 14.
R15      EQU   15                  REGISTER 15.
         LA    R15,&SAVE           ADDRESS OF OUR SAVE AREA.
         ST    R13,4(R15)          BACKWARD SAVE AREA CHAIN.
         ST    R15,8(R13)          FORWARD SAVE AREA CHAIN.
         LR    R13,R15             POINT R13 TO CURRENT SAVE AREA.
         B     &START              BRANCH TO ENTRY CODE.
&EOJ     CH    R15,$$RC            IS RETURN CODE HIGHER THAN &RC?
         BNH   *+6                 YES - LETS ZERO R15.
         SR    R15,R15             ZERO OUT REGISTER 15.
         L     R13,&SAVE+4         POINT R13 TO PREVIOUS SAVE AREA.
         L     R14,12(R13)         RESTORE REGISTER 14.
         LM    R0,R12,20(R13)      RESTORE THE REGISTERS.
         BR    R14                 RETURN TO OS.
&SAVE    DS    18F                 OUR SAVE AREA.
$$RC     DC    H'&RC'              RETURN CODE.
$$START  DS    0H                  DEFAULT ENTRY POINT.
         MEND
         MACRO
         IEERDCM
*     01/15/73          LEVEL=05                              MB Y02958
DCMTSRT  DSECT DCMTSPTR
DCMADTRN DS    A                   POINTER TO PAGABLE DCM
         DS    CL1                 RESERVED
DCMRFLGS DS    BL1                 FLAGS
DCMDOM   EQU   X'10'               DOM MUST BE TRIED
DCMNIPP  EQU   X'08'               DCM WAS USED BY NIP
DCMLEN   DS    H                   LENGTH OF PAGABLE DCM
DCMADKP  DS    A                   ADDRESS OF ROUTED K COMMAND
*                                   PARAMETER LIST
DCMTOPAR DS    CL1                 TOP DISPLAY AREA DEFINED
DCMTOPDS DS    CL1                 TOP DISPLAY ON SCREEN
         DS    BL1                 RESERVED
DCMDEVTY DS    BL1                 DEVICE TYPE FLAGS
DCMTY60  EQU   X'80'               USABLE FOR SD
DCMTY50  EQU   X'40'               NOT USABLE FOR SD
DCMADSDS DS    A                   POINTER TO FIRST SDS SUPPORT AREA
DCMRMS   DS    CL1                 NUMBER OF CCWS TO WRITE
DCMADRMS DS    AL3                 POINTER TO RMS CCWS
DCMWLAST DS    A                   PT CON Q ENTRY LAST OUT (O-O-L)
DCMRMSAL DS    H                   NUMBER LINES IN MSG AREA
DCMDOMKY DS    H                   CONSOLE DOM ELEMENT        MC XM5812
DCMSUBAD DS    A                   POINTER TO SUB CONTROL BLOCK
DCMADPFK DS    A                   POINTER TO RESIDENT PFK AREA
DCMINTVL DS    H                   INTERVAL FOR THIS DCM
DCMTMCTR DS    H                   TIME COUNTER FOR THIS DCM
DCMR2FLG DS    BL1                 TIMER FLAGS
DCMRXSFL EQU   X'80'               FULL SCREEN FLAG
DCMRXUNV EQU   X'40'               UNVIEWABLE MESSAGE DISPLAYED
DCMRXTMR EQU   X'20'               TIMER FLAG
DCMRXRLL EQU   X'10'               READY TO ROLL
DCMRXDEL EQU   X'08'               PENDING DELETE REQUEST
DCMRXTIM EQU   X'02'               TIMER ELAPSED FOR THIS DISPLAY
DCMR3FLG DS    BL1                 MISC FLAGS
DCMSTSWT EQU   X'80'               CHANGING STATUS OF OUTPUT ONLY CON
DCMKVIP  EQU   X'40'               ENTRY FOR K VARY COMMAND
DCMCLPR  EQU   X'20'               CLOSE IN PROCESS
DCMRXSCN EQU   X'10'               ASY ERROR MESSAGE ON SCREEN
DCMR3PKA EQU   X'08'               DA I/O COMPLETE
DCMRXHMT EQU   X'04'               FULL SCREEN SIMULATED      MC YM4102
         DS    H                   RESERVED
DCMRQDEL DS    A                   DELETE REQUEST BUFFER
         DS    A                   DELETE REQUEST BUFFER
DCMMSGSV DS    A                   POINTER TO SAVED NIP MESSAGES
DCMPACB  EQU   *                   SACB PREFIX
DCMPLN   DS    FL2                 SYSGEN LENGTH OF AREA
DCMPLNPR DS    H                   LENGTH OF SACB PREFIX-IN BYTES
DCMPRSZ  EQU   *-DCMPACB           SIZE OF PREFIX
DCMACB   EQU   *                   SACB
DCMACBNX DS    A                   POINTER TO NEXT SACB
DCMAID   DS    CL1                 AREA ID
DCMASACB DS    BL1                 SACB FLAGS
DCMAUSE  EQU   X'80'               AREA PRESENTLY DEFINED     MB Y02958
*    IF DCMAUSE IS OFF, A SYSGEND AREA HAS BEEN FREED E.G. K A,NONE
DCMAGM   EQU   X'40'               GETMAINED SACB
DCMALN   DS    FL2                 LENGTH OF AREA
DCMATOP  DS    FL1                 TOP ROW OF AREA
DCMACLR  EQU   *                   REINITIALIZED PORTION      MB Y02958
DCMAROW  DS    FL1                 ROW TO BE WRITTEN NEXT
DCMAFR   DS    H                   FRAME ON SCREEN
DCMAMJWQ DS    A                   POINTER TO CON Q ENTRY FOR MAJOR
DCMAMIN  DS    A                   POINTER TO MINOR WQE
DCMATIME DS    F                   TIME CONTROL LINE WAS WRITTEN
DCMAMT   DS    XL2                 RESERVED                   MB Y02958
DCMAFLG1 DS    XL1                 AREA FLAGS1
DCMADISP EQU   X'40'               DISPLAY IN AREA
DCMADEND EQU   X'20'               END OF DISPLAY ON SCREEN
DCMAFRPR EQU   X'10'               FRAMING IN PROGRESS
DCMAFULL EQU   X'08'               FRAME FULL
DCMABL   EQU   X'04'               BLANKING TO BE DONE
DCMAFLG2 DS    XL1                 AREA FLAGS 2
DCMALMIN EQU   X'80'               SAVED POINTER TO LAST MINOR OUTPUT
DCMAWCON EQU   X'40'               WRITE CONTROL LINE
DCMARCON EQU   X'20'               REWRITE CONTROL LINE
DCMAMJFR EQU   X'10'               MAJOR WQE HAS BEEN FOUND
DCMADFLG DS    XL1                 DYNAMIC DISPLAY FLAGS      MB Y02958
DCMADD   EQU   X'80'               TRACK IN AREA              MB Y02958
DCMAHOLD EQU   X'40'               TRACK IN HOLD MODE         MB Y02958
DCMACSIB EQU   X'20'               TRACK WITH CONTINUATION LINE IN     *
                                   SCREEN IMAGE BUFFER        MB Y02958
DCMATRCK DS    X                   TRACK REQUEST INDICATORS   MB Y02958
DCMATJOB EQU   X'80'               TRACK JOBS                 MB Y02958
DCMATJBL EQU   X'40'               TRACK JOBS,LIST            MB Y02958
DCMATTS  EQU   X'08'               TRACK TS                   MB Y02958
DCMATTSL EQU   X'04'               TRACK TS,LIST              MB Y02958
DCMATA   EQU   X'88'               TRACK A                    MB Y02958
DCMATAL  EQU   X'44'               TRACK A,LIST               MB Y02958
DCMAUTME DS    XL2                 UTME=NNN VALUE             MB Y02958
DCMATECB DS    F                   TRACK CANCEL ECB ADDRESS   MB Y02958
DCMAPAD  DS    F                   RESERVED                   MB Y02958
DCMACBND EQU   *                   SACB END
DCMCLRLN EQU   *-DCMACLR           LENGTH FOR REINITIALIZING  MB Y02958
DCMACBSZ EQU   *-DCMACB            SACB SIZE
DCMREND  DS    0C                  END OF DCM
         MEND  , */
         MACRO
         IEETDCM
*     12/15/72          LEVEL=04
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                     *
*                         WORK AND SAVE AREAS                         *
*                                                                     *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
DCMSTRT  DSECT DCMSTPTR
         DS    0F                  FULL WORD ALIGNMENT
         DS    H                   DCM LENGTH
         DS    H                   PADDING
         SPACE 4
DCMFLG1  DS    BL1                 DCM AREA INDICATORS
         SPACE 2
DCMOUTPT EQU   X'02'               DCM UPDATED FOR OUTPUT ONLY
         SPACE 4
DCMATI   DC    X'00'               SAVED UCB ATTN INDEX      MC SA55058
         DC    AL2(0)              RESERVED                  MC SA55058
         SPACE 2
DCMWTINT DS    A                   DCMWTINT INITIAL VALUE
DCMLNCNT DS    H                   NUMBER OF LINES TO BLANK  MC SA53462
DCMLNNUM DC    X'00'               FIRST LINE TO BLANK       MC SA53462
         DC    X'00'               RESERVED                  MC SA53462
DCMPACK  DS    F                   AREA TO PLACE NUMBER FOR PACKING
DCMCVBIN DS    F                   AREA FOR CONVERSION TO BINARY
         SPACE 2
DCMTIMES DS    BL1                 TIME RTNS INDICATOR BYTE
         SPACE 2
DCMTIMER EQU   X'80'               TIME ELAPSED FOR THIS DISPLAY
DCMOPTTI EQU   X'40'               OPTIONS TO TI RTN
DCMOTTMM EQU   X'10'               OPTIONS OR TI RTNS TO MSG MODULE
DCMTASYN EQU   X'04'               TIMER SET FOR ASYNC ERROR MSG
DCMOCTTI EQU   X'02'               OPEN-CLOSE TO TI RTN
DCMRMTTI EQU   X'01'               ROLL MODE TO TIMER ROUTINE
         EJECT
         DS    XL1                 UNUSED
         SPACE 2
DCMELGN  DS    H                   ENTRY AREA LAST CHARACTER POINTER
         SPACE 2
DCMBUFAD DS    A                   POINTER TO BUFFER ADDRESS TABLE
DCMDOMPK DS    A                   ADDRESS OF FIRST DOM NUMBER
DCMAMTAB DS    A                   ADDRESS OF FIRST SCT ENTRY
DCMADSEC DS    A                   ADDRESS OF FIRST SSCT ENTRY
DCMADDRL DS    A                   ADDRESS OF LAST SCT ENTRY
DCMASCRN DS    A                   POINTER TO SCREEN IMAGE BUFFER
DCMLSCRN DS    A                   POINTER TO LAST BUFFER LINE
DCMWTBUF DS    A                   SCREEN LENGTH POINTER
DCMAINS  DS    A                   POINTER TO INSTRUCTION LINE
DCMAENTR DS    A                   POINTER TO ENTRY AREA
DCMAWARN DS    A                   POINTER TO WARNING LINE
DCMADCHP DS    A                   ADDRESS OF CHANNEL PROGRAM AREA
DCMPFKLN DS    A                   POINTER TO PFK LINE
DCMCXSVE DS    F                   CXSA SAVE AREA
DCMADOPN DS    A                   ADDRESS OF COMMAND OPERAND
DCMDSAV  DS    5F                  SAVE AND WORK AREA
         SPACE 2
*           INPUT BUFFER FOR ISSUING SVC 34 AND SVC 35                *
*           FIELDS DCMWQEXP THROUGH DCMINPUT MUST REMAIN IN ORDER     *
DCMWQEXP DS    0F                  ENSURE FULL WORD BOUNDARY
DCMINLGN DS    H                   FIELD FOR INPUT LENGTH
DCMMCSFL DS    H                   MCS FLAGS FIELD
DCMINPUT DS    32F                 INPUT MESSAGE TEXT
         SPACE 2
DCMLGNTH DS    H                   LENGTH OF A LINE
DCMBAINC DS    H                   ADDRESS TO INSERT CURSOR
DCMIRCTR DS    H                   INTERVENTION REQ'D MSG COUNTER
DCMBADLN DS    H                   BUFFER ADDR TO BEGIN MSG WRITE
DCMBYTCT DS    H                   NUMBER OF BYTES TO WRITE
DCMADNUM DS    H                   NEXT LINE NUMBER
DCMAXLGN DS    H                   MAXIMUM LINE LENGTH
DCMMSGAL DS    H                   NUMBER OF LINES IN MESSAGE AREA
DCMRMINC DS    H                   INCREMENT INTO RMI
DCMSCTCN DS    H                   LENGTH OF ONE SCT ENTRY
DCMCORLN DS    H                   LENGTH OF DCM LINE IN CORE
         DS    H                   TIME COUNTER
DCMPFKNM DS    XL1                 NUMBER OF KEY BEING PROCESSED
DCMPFKKN DS    XL1                 LIST KEY NUMBER
         EJECT
DCMDEL   DS    CL2                 DEL VALUE
DCMCON   DS    CL1                 CON VALUE
DCMSEG   DS    FL1                 SEG VALUE
DCMDL    DS    FL1                 DISPLAY AREA OPTION
DCMRNUM  DS    FL1                 ROLL NUMBER VALUE
DCMRTME  DS    H                   ROLL TIME VALUE
         SPACE 2
*  DEFAULT VALUES FOR OPTIONS
DCMSEGDF DS    FL1                 SEG DEFAULT
DCMRNUMD DS    FL1                 RNUM DEFAULT
DCMRTMED DS    H                   RTME DEFAULT
         SPACE 2
DCMASKEN DS    XL1                 ENTER MASK
DCMASKCN DS    XL1                 CANCEL MASK
DCMASKCR DS    XL1                 CURSOR MASK
DCMASKLP DS    XL1                 LIGHT PEN MASK
DCMASKPF DS    XL1                 PFK MASK
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                     *
*                         COMMUNICATIONS AREA                         *
*                                                                     *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         SPACE 2
DCMOPTST DS    BL1                 STATUS OF SCREEN CONTROL OPTIONS
         SPACE 2
DCMOPTVR EQU   X'80'               DELETE VERIFICATION  CON=(Y=1,N=0)
DCMOPTAD EQU   X'40'               AUTOMATIC DELETION   DEL=(Y=1,N=0)
DCMOPTSG EQU   X'20'               DEFAULT SEGMENT SPECIFIED  SEG=(0=0)
DCMOPRLL EQU   X'10'               ROLL MODE  (Y=1,N=0)
         SPACE 4
DCMCS    DS    BL1                 OPEN/CLOSE REQUEST
         SPACE 2
DCMCSC   EQU   X'80'               CLOSE REQUEST
DCMCSO   EQU   X'40'               OPEN REQUEST
         SPACE 4
DCMUTILT DS    BL1                 RESERVED
         SPACE 2
DCMUTILA EQU   X'80'               THESE BITS ARE
DCMUTILB EQU   X'40'                 INITIALIZED AND USED
DCMUTILC EQU   X'20'                 SOLELY WITHIN
DCMUTILD EQU   X'10'                 EACH MODULE
DCMUTILE EQU   X'08'               THEY ARE NEVER
DCMUTILF EQU   X'04'               USED FOR INTERFACE
DCMTEST1 EQU   X'02'               FOR TESTING
DCMTEST2 EQU   X'01'               FOR TESTING
         SPACE 4
DCMDSTAT DS    BL1                 CURRENT DISPLAY STATUS
         SPACE 2
DCMDSTNM EQU   X'20'               MESSAGES ARE NUMBERED
DCMDSTNH EQU   X'10'               MSGS NUMBERED - HOLD OPTION
DCMDSINR EQU   X'08'               INTERVENTION REQ'D DELETION TRIED
DCMDSAUT EQU   X'04'               AUTOMATIC DELETION TRIED
         SPACE 4
DCMMCSST DS    BL1                 MCS INTERFACE BYTE
         SPACE 2
DCMDUSE  EQU   X'80'               OUR SUPPORT IN CONTROL
DCMOOMSS EQU   X'04'               MESSAGE STREAM ENTRY
DCMOOSDS EQU   X'01'               STATUS DISPLAY ENTRY
         SPACE 4
DCMIOUNQ DS    BL1                 UNIQUE IO BYTE
         SPACE 2
*  UNIQUE INTERFACE BITS FOR 2260
DCMIO226 EQU   X'80'               RMI PERFORMED
DCMRPCUR EQU   X'40'               ADVANCE CURSOR TO BLANKS
DCMFRSCN EQU   X'20'               PUT OUTPUT IN HOLD MODE
*  UNIQUE INTERFACE BITS FOR 2250
DCMRDARM EQU   X'10'               PERFORM READ AFTER RMI
DCMW2250 EQU   X'08'               DEVICE IS 2250
DCMINNOR EQU   X'04'               NORMAL INSTRUCTION LINE
DCMINERR EQU   X'02'               ERROR INSTRUCTION LINE
         SPACE 4
DCMIOCM1 DS    BL1                 IO COMMUNICATIONS BYTE 1
         SPACE 2
DCMDORMI EQU   X'80'               ISSUE RMI
DCMSOUND EQU   X'40'               SOUND ALARM
DCMWRWRN EQU   X'20'               WRITE WARNING LINE
DCMWRMSG EQU   X'10'               WRITE FULL MESSAGE AREA
DCMWRPAR EQU   X'08'               WRITE PARTIAL MESSAGE AREA
DCMWRINS EQU   X'04'               WRITE INSTRUCTION LINE
DCMWRENT EQU   X'02'               WRITE ENTRY AREA
DCMINSC  EQU   X'01'               INSERT CURSOR
         EJECT
DCMIOCM2 DS    BL1                 IO COMMUNICATIONS BYTE 2
         SPACE 2
DCMBLENT EQU   X'80'               BLANK ENTRY AREA
DCMBLWRL EQU   X'40'               BLANK LEFT HALF WARNING LINE
DCMBLWRR EQU   X'20'               BLANK RIGHT HALF WARNING LINE
DCMINSSH EQU   X'10'               INIT AND SHIFT INSTRUCTION LINE
DCMWINFD EQU   X'08'               WRITE INFORMATIONAL DISPLAY
DCMERASE EQU   X'04'               PERFORM ERASE
DCMIOCRD EQU   X'02'               PERFORM READ (2250,22DOC)
DCMWRASY EQU   X'01'               WRITE ASYNC ERROR MSG TO MID-SCREEN
         SPACE 4
DCMIOCM3 DS    BL1                 IO COMMUNICATIONS BYTE 3
         SPACE 2
DCMOPRMI EQU   X'80'               RMI AFTER OPEN TO UNLOCK KEYBOARD
DCMSSRG  EQU   X'40'               SUPPRESS START REGENERATION
DCMWRPFK EQU   X'10'               DCM WRITE PFK AREA
DCMPFKAT EQU   X'08'               PFK ATTENTION
DCMRDPFK EQU   X'04'               PFK AREA READ
DCMACPFK EQU   X'02'               TURN ACTIVE PFK LIGHTS ON
DCMLTPFK EQU   X'01'               TURN ALL PFK LIGHTS ON
         SPACE 4
DCMLINEN DS    XL1                 LINE NUMBER TO BEGIN WRITE
         SPACE 2
DCMCULNO DS    XL1                 LINE IN ENTRY AREA TO INSERT CURSOR
         SPACE 2
DCMPOSCU DS    XL1                 POSITION TO INSERT CURSOR
         SPACE 4
DCMASYNC DS    BL1                 ASYN ERROR COMMUNICATIONS/RETRY BYTE
         SPACE 2
DCMASDA  EQU   X'40'               RETRY BIT
DCMASIN  EQU   X'20'               RETRY BIT
DCMASBA  EQU   X'10'               RETRY BIT
DCMASLOG EQU   X'08'               LOG ASYNCHRONOUS ERROR
         EJECT
DCMCOM1  DS    BL1                 COMMUNICATIONS BYTE
         SPACE 2
DCMLPENT EQU   X'80'               ENTER BY LP OR CURSOR
DCMIOPRD EQU   X'40'               READ PERFORMED
DCMCOMRM EQU   X'20'               RMI PERFORMED
DCMCOMAU EQU   X'10'               PERFORM AUTO DELETE
DCMCOMRD EQU   X'08'               PERFORM REGULAR DELETE
DCMCOMNM EQU   X'04'               NUMBER MESSAGES
DCMCANCL EQU   X'01'               INDICATE CANCEL TO COMMAND ROUTINE
         SPACE 3
DCMCOM2  DS    BL1                 COMMUNICATIONS BYTE
         SPACE 2
DCMCM2I  EQU   X'80'               INPUT TO BE PROCESSED
DCMSPLIT EQU   X'40'               MSG TO BE SPLIT
DCMCOMAR EQU   X'20'               ACCEPTED REPLY
DCMERPF  EQU   X'08'               ERASE PERF-PROC CAN NOW CLOSE DEVICE
DCMCMIN5 EQU   X'04'               RETURN TO INTER. 5 FOR BLNK
DCMCBLNK EQU   X'02'               BLANKING REQUIRED
DCMAE    EQU   X'01'               CLEANUP FOR ASY ERROR
         SPACE 3
DCMCOM3  DS    BL1                 COMMUNICATIONS BYTE
         SPACE 2
DCMCDSP3 EQU   X'80'               DISPLAY 3 COMPLETED WORK
DCMRTPFK EQU   X'40'               RETURN TO PFK ROUTINE
DCMVLPFK EQU   X'20'               VERIFYING LAST COMMAND
DCMXINT1 EQU   X'10'               ENTRY FOR INTERFACE 1 ROUTINE
DCMOLUNV EQU   X'08'               O-O-L MSG CAUSED UNVIEW. MSG.
DCMPFKWR EQU   X'04'               WRITE PFK UPDATES TO LIB
DCMOLHLD EQU   X'02'               OUT OF LINE MESSAGES HELD  MB Y02958
DCMCMIN7 EQU   X'01'               RETURN TO INTER. 7 FOR BLANKING
         SPACE 3
DCMCMSG1 DS    BL1                 MSG MODULE COMMUNICATIONS BYTE 1
         SPACE 2
DCMMSGWT EQU   X'80'               MOVE IN MESSAGE WAITING
DCMUNMSG EQU   X'40'               MOVE IN UNVIEWABLE MESSAGE
DCMSTEX  EQU   X'20'               MOVE IN STATUS EXISTS
DCMCHOPT EQU   X'10'               MOVE IN CHANGE OPTIONS
DCMELONG EQU   X'08'               MOVE IN ENTRY TOO LONG
DCMWRCDL EQU   X'04'               MOVE IN CON=N,DEL=Y
DCMDELNT EQU   X'02'               MOVE IN DEL UNCHANGED, NO TIMER
         EJECT
DCMCMSG2 DS    BL1                 MSG MODULE COMMUNICATIONS BYTE 2
         SPACE 2
DCMDLREQ EQU   X'80'               MOVE IN DELETION REQUESTED
DCMRQINC EQU   X'40'               MOVE IN REQUEST INCONSISTENT
DCMMSGCR EQU   X'20'               MOVE IN INVALID CURSOR OPERATION
DCMINVOP EQU   X'10'               MOVE IN INVALID OPERAND
DCMCILLP EQU   X'08'               MOVE IN ILLEGAL LP OPERATION
DCMDELRI EQU   X'04'               MOVE IN DELETE REQUEST INCONSISTANT
DCMASYRT EQU   X'02'               MOVE IN ASYN ERROR RETRYABLE
DCMASYCD EQU   X'01'               MOVE IN ASYN ERROR MAYBE RETRYABLE
         SPACE 4
DCMCMSG3 DS    BL1                 MSG MODULE COMMUNICATIONS BYTE 3
         SPACE 2
DCMCMRLL EQU   X'80'               MOVE IN ROLL MODE MESSAGE
DCMCDLR1 EQU   X'40'               NO DELETABLE MESSAGES
DCMCDLR2 EQU   X'20'               INVALID RANGE
DCMCDLR3 EQU   X'10'               SEG EQU TO ZERO
DCMCDLR4 EQU   X'08'               DISPLAY NOT ON SCREEN
DCMCDLR5 EQU   X'04'               INVALID OPERAND
DCMDTBSY EQU   X'01'               COMMAND REJECTED - TASK BUSY
         SPACE 4
DCMCMSG4 DS    BL1                 MSG MODULE COMMUNICATIONS BYTE 4
         SPACE 2
DCMPFKNA EQU   X'80'               MOVE IN PFK NOT ALLOCATED FOR
DCMPFKND EQU   X'40'               MOVE IN PFK NOT DEFINED
DCMPFKNO EQU   X'20'               MOVE IN NO PFK ALLOCATION
DCMPFKIP EQU   X'10'               MOVE IN PFK IN PROCESS
         SPACE 4
DCMSVC34 DS    BL1                 SVC 34 COMMUNICATION BYTE
         SPACE 2
DCMMYCMD EQU   X'80'               COMMAND TO BE HANDLED BY THIS CONS
DCMINVLD EQU   X'40'               INVALID K COMMAND
DCMTYPE1 EQU   X'20'               K COMMAND IS NOT ROUTABLE
         SPACE 2
DCMPAD   DS    X                   RESERVED COMMUNICATION BYTE   Y02958
DCMIONDX DS    X                   INDEX FOR SELECTING THE    MB Y02958
*                                  APPROPRIATE I/O ROUTINE    MB Y02958
*                                  X'04' - M/165 CONSOLE      MB Y02958
*                                  X'08' - 2250               MB Y02958
*                                  X'0C' - 2260               MB Y02958
*                                  X'10' - 3277               MB Y02958
DCMTEST  DS    H                   RESERVED FOR TESTING       MB Y02958
         EJECT
**                                                                   **
*                        MODULE ADDRESSES                             *
**                                                                   **
         SPACE 1                                              MB Y02958
DCMIORTN DS    F                   APPROIATE I/O ROUTINE      MB Y02958
*                  NAME   TRACE ID   DESCRIPTION              MB Y02958
*                 IEECVETH    EH   3066(M/165) IO ROUTINE     MB Y02958
*                 IEECVETP    EP   2250 IO ROUTINE            MB Y02958
*                 IEECVETR    ER   2260 IO ROUTINE            MB Y02958
*                 IEECVETU    EU   3277 IO ROUTINE            MB Y02958
         SPACE 1                                              MB Y02958
*                  NAME   TRACE ID   DESCRIPTION              MB Y02958
DCMNPRZ  DS    F  IEECVFT1    F1   PROCESSOR 0 LOAD ONE       MB Y02958
DCMNPROC DS    F  IEECVET1    E1   PROCESSOR ROUTINE LOAD ONE MB Y02958
DCMNDSP1 DS    F  IEECVET2    E2   DISPLAY ROUTINE 1          MB Y02958
DCMNDSP2 DS    F  IEECVET3    E3   DISPLAY ROUTINE 2          MB Y02958
DCMNDSP3 DS    F  IEECVFT2    F2   DISPLAY ROUTINE 3          MB Y02958
DCMNCMD1 DS    F  IEECVET4    E4   COMMAND ROUTINE 1          MB Y02958
DCMNDEL1 DS    F  IEECVET6    E6   DELETE ROUTINE 1           MB Y02958
DCMNDEL2 DS    F  IEECVET7    E7   DELETE ROUTINE 2           MB Y02958
DCMNDEL3 DS    F  IEECVET8    E8   DELETE ROUTINE 3           MB Y02958
DCMNDEL4 DS    F  IEECVET9    E9   DELETE ROUTINE 4           MB Y02958
DCMNOPT1 DS    F  IEECVETA    EA   OPTIONS ROUTINE 1          MB Y02958
DCMNPFK1 DS    F  IEECVFTA    FA   PFK ROUTINE 1              MB Y02958
DCMNPFK2 DS    F  IEECVFTB    FB   PFK ROUTINE 2              MB Y02958
DCMNERRO DS    F  IEECVETC    EC   ASYNCHRONOUS ERROR ROUTINE MB Y02958
DCMNMSG1 DS    F  IEECVETD    ED   MESSAGE ROUTINE 1          MB Y02958
DCMNMSG2 DS    F  IEECVETE    EE   MESSAGE ROUTINE 2          MB Y02958
DCMNMSG3 DS    F  IEECVFTD    FD   MESSAGE ROUTINE 3          MB Y02958
DCMNLPCR DS    F  IEECVETF    EF   LIGHT PEN/CURSOR SERVICE   MB Y02958
DCMNOPCL DS    F  IEECVETG    EG   OPEN-CLOSE ROUTINE         MB Y02958
DCMNCLN  DS    F  IEECVFTG    FG   CLEANUP MODULE             MB Y02958
DCMNROLL DS    F  IEECVETJ    EJ   ROLL MODE ROUTINE          MB Y02958
DCMNTIMR DS    F  IEECVETK    EK   TIMER INTERPRETER ROUTINE  MB Y02958
DCMNINT1 DS    F  IEECVFTL    FL   INTERFACE 1 ROUTINE        MB Y02958
DCMNINT2 DS    F  IEECVFTM    FM   INTERFACE 2 ROUTINE        MB Y02958
DCMNINT3 DS    F  IEECVFTN    FN   INTERFACE 3 ROUTINE        MB Y02958
DCMNINT4 DS    F  IEECVFTO    FO   INTERFACE 4 ROUTINE        MB Y02958
DCMNINT5 DS    F  IEECVFTP    FP   INTERFACE 5 ROUTINE        MB Y02958
DCMNINT6 DS    F  IEECVFTQ    FQ   INTERFACE 6 ROUTINE        MB Y02958
DCMNINT7 DS    F  IEECVFTT    FT   INTERFACE 7 ROUTINE        MB Y02958
         SPACE 2
DCMTRACE DS    CL30                DIDOCS MODULE TRACE AREA   MB Y02958
DCMTRAC2 EQU   DCMTRACE+2          TRACE MOVE FROM-ADDRESS    MB Y02958
DCMTRLEN EQU   *-DCMTRACE          LENGTH FOR TRACE MOVE      MB Y02958
DCMTREN1 DS    C                   1ST BYTE OF TRACE ENTRY    MB Y02958
DCMTREN2 DS    C                   2ND BYTE OF TRACE ENTRY    MB Y02958
         EJECT
*  FIRST BYTE OF SCT ENTRIES
DCMMSGWR EQU   X'80'               WTOR MESSAGE DISPLAYED IN LINE
DCMMSGIN EQU   X'40'               MESSAGE DISPLAYED IN LINE
DCMMSGCN EQU   X'20'               MESSAGE CONTINUED ON NEXT LINE
DCMMSGJK EQU   X'10'               TO WRITE OUT-OF-LINE DISPLAY FROM
*                                  MAY CONTAIN JUNK (SDS INTERFACE 2)
DCMMSGAD EQU   X'08'               MESSAGE CAN BE DELETED AUTOMATICALLY
DCMMSGRD EQU   X'04'               REQUEST HAS SPECIFIED MSG BE REMOVED
DCMMSGIF EQU   X'02'               INFORMATIONAL MESSAGE IN LINE
DCMMSGST EQU   X'01'               END OF TABLE INDICATOR
         SPACE 2
*  SECOND BYTE OF SCT ENTRIES
DCMMSGAC EQU   X'80'               ACTION MESSAGE
DCMMSGC7 EQU   X'40'               DESCRIPTOR CODE 7 MESSAGE
DCMMSGDM EQU   X'20'               MESSAGE HAS BEEN DOMMED
DCMMSGAR EQU   X'10'               MESSAGE IS AN ACCEPTED REPLY
DCMMSGIR EQU   X'08'               INTERVENTION REQUIRED MESSAGE
DCMMSGCT EQU   X'04'               CONTINUATION LINE
DCMMSGPP EQU   X'02'               ISSUED BY PROBLEM PROGRAM
DCMMSGCL EQU   X'01'               CONTROL LINE OF IN LINE MLWTO
         SPACE 2
*  SECONDARY SCT ENTRIES
DCMSECCL EQU   X'80'               CONTROL LINE OF OUT OF LINE DISPLAY
DCMSECLL EQU   X'40'               LABEL LINE OF OUT OF LINE DISPLAY
DCMSECDL EQU   X'20'               DATA LINE OF OUT OF LINE DISPLAY
DCMSECBL EQU   X'10'               THIS LINE IS BLANKED
DCMSECDD EQU   X'02'               LINE RESERVED FOR DYNAMIC DISPLAY
DCMSECST EQU   X'01'               END OF TABLE INDICATOR
DCMEND   DS    0C                  END OF DCM
         MEND  , */
*********************D C M S   M A C R O S*****************************
*
         MACRO
&CSECT   XSTART &STAE=NO,&XCMD=YES,&LV=(18*4)
*        SPACE 4
         DCMSREGS
*        SPACE 4
&CSECT   CSECT
         SAVE  (14,12),,*
         USING &CSECT,R15
         LR    R5,R1               SAVE PARM INPUT REGISTER HERE
         LR    R12,R15             ESTABLISH A BASE
         USING &CSECT,R12
         DROP  R15
         CNOP  0,4
         BAL   R1,*+8              LENGTH FOR A STANDARD SAVE AREA
         DC    A(&LV.)              AND USER SPECIFIED LENGTH (LV)
         L     R0,0(,R1)
         SVC   10
*
*
*        EXPLICIT GETMAIN EXPANSION DONE TO FIX BUGS
*
*        GETMAIN R,LV=(0)          GET A SAVE AREA
*                                   ------
*                                  × S  L ×
         ST    R13,4(R1)           × T  I ×  SAVE ADDRESS OF PREVIOUS
         ST    R1,8(R13)           × D  N ×  CREATE FORWARD POINTER
         LR    R13,R1              ×    K ×  AND USE R13 JUST FOR FUN
*                                   ------
         AIF   ('&STAE' NE 'YES').X1
         L     R10,=V(XCMDSTAE)
         ESTAE (R10),CT            SET ESTAE ENVIRONMENT
.X1      ANOP
         LR    R1,R5               RESTORE PARM REGISTER
         EJECT
         MEND
         MACRO
         DCMSREGS &R
*        COPY  DCMSGBLS
.*                                                                    *
.*       G B L A    D C M S  G R O U P                                *
.*                                                                    *
         GBLA  &DP(33) .
         GBLA  &GCT .
         GBLA  &GCOUNT .
.*       GBLA  &LVNUM .                                          BFG001
         GBLA  &REG .
.*                                                                    *
.*       G B L B    D C M S  G R O U P                                *
.*                                                                    *
         GBLB  &DBLE .
         GBLB  &LIST .
         GBLB  &DBUG .
         GBLB  &EQU .
         GBLB  &WDSW .
         GBLB  &RENT .
         GBLB  &PLG .
         GBLB  &ONETM .
         GBLB  &DASHS .
         GBLB  &PWRK .
         GBLB  &RCD .
         GBLB  &DCMSCLR .
         GBLB  &DCMSHEX .
         GBLB  &DCMSDEC .
         GBLB  &DCMSWRK .
         GBLB  &DCMSCRN .
         GBLB  &DCMSCST .
.*                                                                    *
.*       G B L C    D C M S  G R O U P                                *
.*                                                                    *
         GBLC  &WD(33) .
         GBLC  &NAM .
         GBLC  &DNAM .
         GBLC  &ONAM .
         GBLC  &INAM .
         GBLC  &RNAM .
         GBLC  &NDX .
         GBLC  &CSECT .
         GBLC  &DSECT .
         GBLC  &DCMSHLP
         GBLC  &LVNUM                                            BFG001
         AIF   (&EQU).END
&EQU     SETB  1
          AIF   (&LIST).LIST
          AGO   .LISTX
.LIST    ANOP
         SPACE
.END     MEXIT
.LISTX    ANOP
         SPACE 8
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                     *
*                                                                     *
*                                                                     *
*        G E N E R A L   R E G I S T E R   E Q U A T E S              *
*                                                                     *
*                                                                     *
*                                                                     *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         SPACE 4
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         EJECT
         SPACE 8
*                                                                     *
*                                                                     *
*              R E G I S T E R    U S A G E                           *
*                                                                     *
*                                                                     *
         SPACE 6
*                                                                     *
*                                                                     *
*                                                                     *
*        R0         PARAMETER REGISTER                                *
*        R1         PARAMETER LIST REGISTER                           *
*        R2         SUBROUTINE LINKAGE REGISTER                       *
*        R3         WORK REGISTER                                     *
*        R4         WORK REGISTER                                     *
*        R5         WORK REGISTER                                     *
*        R6         WORK REGISTER                                     *
*        R7         WORK REGISTER                                     *
*        R8         WORK REGISTER                                     *
*        R9         WORK REGISTER                                     *
*        R10        IOCB/DSECT REGISTER                               *
*        R11        PARM/WORK  REGISTER                               *
*        R12        BASE REGISTER                                     *
*        R13        SAVE AREA REGISTER                                *
*        R14        RETURN LINKAGE REGISTER                           *
*        R15        ENTRY POINT LINKAGE REGISTER                      *
*                                                                     *
*                                                                     *
*                                                                     *
         MEND
         PRINT ON
*.
***********************************************************************
*                                                                     *
* TSO COMMAND -  D I D O C S -                                        *
*                                                                     *
* PURPOSE: FOR TSO OPER PRIVLEDGE USERS, SIMULATE AN OS 3277 CONSOLE  *
*    AT THEIR TSO TUBE BY PEEKING AT THE DIDOCS CONSOLE MESSAGE       *
*    BUFFERS IN CSA.  DIDOCS ACCEPTS A REPLY FROM THE USER.           *
*    NON-BLANK REPLIES ARE FORMATTED INTO A CMD AND ISSUED VIA        *
*    SVC34,  BLANK REPLIES JUST CAUSE THE SCREEN TO BE UPDATED.       *
*    ANY COMMANDS ISSUED VIA SVC34 ARE ALSO LOGGED VIA A WTO SO THAT  *
*    YOUR AUDITORS AND OPERATIONS KNOW WHAT IS GOING ON.              *
*                                                                     *
* THIS CODE IS BASED ON THE DIDOCS COMMAND FROM THE SHARE DCMS SYSTEM.*
* ORIGINAL CODE GENERATED BY FRED LUDDY OF AMDAHL CORP.               *
*                                                                     *
***********************************************************************
*                                                                     *
* TO INVOKE:   TYPE  DIDOCS                                           *
*                                                                     *
* ERROR MESSAGE:                                                      *
*   NO ACCEPTABLE (3277) CONSOLES FOUND                               *
*                                                                     *
* EXIT FROM DIDOCS IS VIA  PFK3.                                      *
*                                                                     *
***********************************************************************
* MOD 2 - ARNOLD CASINGHINO - CONNECTICUT BANK AND TRUST    16 SEP 82 *
*         GET THE MASTER CONSOLE ONLY AND BYPASS THE REST   18 SEP 82 *
* MOD 1 - KEN TRUE - FAIRCHILD CAMERA AND INSTRUMENT CORP   18 MAR 80 *
*    A) ADDED MODESETS TO FIX FETCH PROT. ON MCS BUFFERS W MVS3.8B.   *
*       INTERNALIZED ALL MACROS.                                      *
*    B) REMOVED DCMS SCREEN HANDLING AND CONVERTED TO SINGLE CSECT    *
*       USING DIRECT SCREEN FORMATTING.                               *
*                                                                     *
*  NOTE: TO USE 'REAL' MODESET, CHANGE MACRO NAME IN 'PRINT OFF'      *
*      SECTION TO 'FMODESET' AND REASSEMBLE.                          *
*      LINK WITH 'AC(1)', AND AUTHORIZE COMMAND VIA ENTRY IN IKJEFTE8.*
*                                                                     *
*                                                                     *
***********************************************************************
         EJECT
DIDOCS   PGM    ,                NON REENT CHEAP PGM
         EJECT
         EXTRACT MF=(E,EXTRPSCB)      YES..CHECK FOR OPER PRIVLEDGE
         L     R1,ANSWER
         USING PSCB,R1
                                  EJECT
*********************************************************************
*                                                                   *
*  CHECK USER AUTHORITY                                             *
*                                                                   *
*     THREE POSSIBLE AUTHORITITES                                   *
*                                                                   *
*     $     - DO ANYTHING WITH NO LOGGING                           *
*     L     - DO ANYTHING BUT LOG IT ALL                            *
*     BLANK - DO ALL BUT $ COMMANDS AND LOG IT ALL                  *
*                                                                   *
*********************************************************************
CHKOPER  CLC   PSCBUSER(4),=C'S666'
         BNE   END1
         CLC   PSCBUSER+4(2),=C'73'
         BE    SYSINIT
         CLC   PSCBUSER+4(2),=C'93'
         BE    SYSINIT
         CLC   PSCBUSER+4(2),=C'72'
         BE    SYSINIT
         CLC   PSCBUSER+4(2),=C'64'
         BE    SYSINIT
         CLC   PSCBUSER+4(2),=C'73'         SPARE ZAPPABLE
         BE    SYSINIT
*    LOGGED DIDOCS USER
         CLC   PSCBUSER+4(2),=C'A6'
         BE    LOGINIT
         CLC   PSCBUSER+4(2),=C'A3'
         BE    LOGINIT
*    COMMON DIDOCS USER
         CLC   PSCBUSER+4(2),=C'03'
         BE    INITIAL
         CLC   PSCBUSER+4(2),=C'34'
         BE    INITIAL
         CLC   PSCBUSER+4(2),=C'29'
         BE    INITIAL
         CLC   PSCBUSER+4(2),=C'70'  SPARE AND ZAPABLE
         BE    INITIAL
         CLC   PSCBUSER+4(2),=C'70'  SPARE AND ZAPABLE
         BE    INITIAL
         B     END1
LOGINIT  MVI   CMDALLOW,C'L'
         B     INITIAL
SYSINIT  MVI   CMDALLOW,C'$'
INITIAL  MVC   LOGUSER,PSCBUSER    SET UP TO LOG THE COMMAND
         DROP  R1
         LA    R0,2               SET UP FOR AUTHORIZATION
         SVC   248                AUTHORIZE
         MODESET MODE=SUP,KEY=ZERO
         ESAR  R5                  GET SEC ASID
         ST    R5,SASID            KEEP IT
         AXSET AX=ONE              SET AX TO ALLOW SSAR
RESTART  L     R1,CVTPTR           AND IN THE BEGINNING...
         L     R1,CVTCUCB-CVT(R1)  ...
         USING UCM,R1
         LM    R3,R5,UCMVEA        R3 -> TO THE FIRST UCME
*                                   R4 CONTAINS UCME LENGTH
*                                    R5 -> TO THE LAST UCME
DID0     EQU   *                   SEARCH FOR SOMETHING USEFUL
         USING UCMLIST,R3
***
         L     R1,UCMUCB           DEBUGGING
         MVC   UNITXXX(3),13(R1)   ...
***
         TM    UCMATR,UCMUF        IS IT ACTIVE ?
         BZ    DID1                NO.. UNUSEABLE
         L     R10,UCMXB           IS IT GRAPHICS ?
         LTR   R10,R10             ...
         BZ    DID1                NO.. USELESS
         TM    UCMDISP1,UCMDISPA   IS IT THE MASTER    CBT-AXC
         BZ    DID1                NO.. USELESS        CBT-AXC
         USING DCMTSRT,R10         POINT TO THE DCM
         L     R6,DCMADTRN         FIND  THE PAGEABLE DCM (TDCM)
         LTR   R6,R6               DOES IT EXIST ?
         BZ    DID1                NO...
         LA    R7,CONASID          GET CONSOLE ASID
         SSAR  R7                  SET CONSOLE AS SECONDARY
         LA    R7,LOCDCM           THE 'TO' ADDRESS
         LA    R3,L'LOCDCM         LENGTH OF THE 'TO'
         SR    R15,R15             ZERO R15 FOR 'FROM' KEY
MOVEEM   MVCP  0(R3,R7),0(R6),R15  MOVE IT
         BZ    DCMMOVED
         LA    R6,256(R6)          BUMP 'FROM' ADDRESS BY 256
         LA    R7,256(R7)          BUMP 'TO' ADDRESS BY 256
         SH    R3,=H'256'          DECREMENT LENGTH OF MOVE LEFT
         B     MOVEEM              GO BACK AGAIN
         SPACE 1
DCMMOVED L     R5,SASID            RESTORE PREVIOUS SECONDARY
         SSAR  R5                  SHOULD BE PRIMARY
         LA    R6,LOCDCM           ADDRESS OF COPY OF DCM BUFFER
         USING DCMSTRT,R6          A LITTLE
         CLI   DCMIONDX,X'10'      IS IT A 3270 ?
         MVC   XUCMID,UCMID        SAVE THIS
         BE    GOTONEA             YES BY DINGLES !
*                                      DINGLES ?
DID1     EQU   *
         BXLE  R3,R4,DID0          GET THE NEXT ENTRY
         MVC   SCREEN(L'ERMSG),ERMSG NO ACCEPTABLE CONSOLES ACTIVE
         BAL   R9,WRITE            WRITE THE SCREEN
         B     ENDIT               FINI
         EJECT
*
* A CONSOLE HAS BEEN FOUND COPY THE SCREEN AND DISPLAY IT FER THE USER
*        R3 ->  UCME
*        R10 -> RDCM
*        R6 ->  TDCM
*
GOTONE   EQU   *
GOTONEA  LA    R2,21               NUMBER  OF LINES IN MSG AREA
         MH    R2,=H'80'           LENGTH OF STUFF TO MOVE
         L     R7,DCMASCRN         POINT TO  THE  FIRST INPUT LINE
         LA    R8,SCREEN           OUTPUT LINE
         XR    R9,R9              SET SCREEN LENGTH TO ZERO (SO PADDING
*                                 WILL OCCUR)
GOT1     STM   R4,R9,MOVEBSAV      SAVE THE REGISTERS
         L     R5,=X'40000000'     SET UP FOR BLANKS
         LA    R4,*
         MVCL  R8,R4               PAD WITH SPACES
         SPACE 1
         LA    R9,SCRLEN           LENGTH OF SCREEN
         LA    R4,CONASID          GET CONSOLE ASID
         SSAR  R4                  SET CONSOLE AS SECONDARY
         LM    R4,R9,MOVEBSAV      GET BACK ADDRESSES
         SR    R15,R15             'FROM' KEY
MOVEBUF  MVCP  0(R2,R8),0(R7),R15  MOVE IT
         BZ    ENDMOVE
         LA    R7,256(R7)          BUMP 'FROM' ADDRESS
         LA    R8,256(R8)          BUMP 'TO' ADDRESS
         SH    R2,=H'256'          DECREMENT THE LENGTH FIELD
         B     MOVEBUF             GO BACK AND GET MORE
ENDMOVE  L     R4,SASID
         SSAR  R4                  SHOULD BE SAME AS PRIMARY
         MVC   MSGLINE(UNITLEN),UNITMSG  MOVE IN TITLES
         MVC   TIMELINE(L'FAKEWARN),FAKEWARN
         BAL   R9,WRITE            DOIT JACK !!!
         MVC   SVCRCM,LOTSBLNK
         MVC   NOTR15,LOTSBLNK
         OC    REPLY,LOTSBLNK      CONVERT TO UPPER CASE
         CLI   REPLY,C' '          NULL INPUT ?
         BE    GOTONE              YES..NO COMMAND..GO PEEK AND SHOW
*                                  SIGH..GOT TO ISSUE THE COMMAND
         MVC   CMNDBUF,LOTSBLNK
         MVC   CMNDBUF(79),REPLY
         MVC   REPLY,LOTSBLNK
         MVC   LOGCMD,CMNDBUF        SAVE COMMAND AS EXECUTED
*        IF AUTHORITY LEVEL IS $ THEN DO NOT LOG
         CLI   CMDALLOW,C'$'
         BE    NOLOG
         WTO   MF=(E,WTOLOG)         DO THE WTO FOR POSTERITY
NOLOG    LA    R1,CMND             WELL ?
         XR    R0,R0
         IC    R0,XUCMID           OUR MAN IN THE COMM TASK
         SR    R15,R15
*        IF AUTHORITY IS NOT BLANK THEN DO IT
         CLI   CMDALLOW,C' '
         BNE   DOIT
*        IF AUTHORITY IS BLANK AND COMMAND IS $ THEN DONT DO IT
         CLI   CMNDBUF,C'$'
         BE    DONTDOIT
DOIT     SVC   34                  FIRE HIM THRU
DONTDOIT LR    R2,R15              SAVE RETURN CODE
         MODESET MODE=PROB
         LTR   R15,R2
         BZ    GOTONE              FIND OUT WUTS HAPPENIN
         SPACE  1
***
* SEE IF  WE ARE HERE BY A PREVIOUS MARRIAGE ... (WITH A NZERO RC)
*
         SPACE 1
         MVC   SVCRCM(L'SVCRCM),=C' CMD RC='
         BAL   R9,CVTPRT           DO IT
         MVC   NOTR15,CVTA         MOVE IN RC
         MVC   MSGLINE(UNITLEN),UNITMSG  MOVE IN ERROR LINE
         BAL   R9,WRITE            ...
         MVC   REPLY,LOTSBLNK
         MVC   SVCRCM,LOTSBLNK
         MVC   NOTR15,LOTSBLNK
         B     GOTONE
END1     EQU   *
         LA   R1,CLEAR
         LA   R0,L'CLEAR
         ICM  R1,B'1000',TPUTFLG        INDICATE FULL SCREEN
         TPUT (1),(0),R
ENDIT    B    $$EOJ
         EJECT
*
* LOCAL SUBROUTINE TO CONVERT R15 TO PRINTABLE HEX (EBCDIC)
*
CVTPRT   DS    0H
         ST    R15,CVTB
         UNPK  CVTA(9),CVTB(5)
         NC    CVTA(8),HEXMASK
         TR    CVTA(8),HEXTAB
         BR    R9
         SPACE 2
*
* LOCAL SUBROUTINE WRITE SCREEN VIA TPUT, AND GET REPLY VIA TGET
*
WRITE    EQU   *
         LA    R1,FULLSCRN
         LA    R0,SCRLEN
         ICM   R1,B'1000',TPUTFLG        INDICATE FULL SCREEN
PUTIT    TPUT  (1),(0),R
         SPACE 2
GETIT    TGET  PFREPLY,L'PFREPLY,ASIS
         SPACE 2
GOTIT    CH    R15,=H'14'                IS INPUT LONGER THAN BUFFER
         BNE   SKIPCLRQ
         TCLEARQ INPUT                  FLUSH THE TRASH
SKIPCLRQ CLI   PFREPLY,X'F3'      PF 3 ENTERED?
         BE    END1                  YES...SPLIT
         CLI   PFREPLY,X'7C'      PF 12 ENTERED?
         BE    END1                  YES...SPLIT
         CLI   PFREPLY,X'6E'      PA 2 ENTERED?
         BE    RESTART              YES...START OVER
         BR    R9
         EJECT
***********************************************************************
*        MESSAGES                                                     *
***********************************************************************
         SPACE
CMDALLOW DC    C' '
ERMSG    DC    C'>>> NO 32XX CONSOLES FOUND <<<'
         SPACE 1
WTOLOG   WTO   'XXXXXXX CMD:                                           *
                                                     ',ROUTCDE=(1),MF=L
         ORG   WTOLOG+4
LOGUSER  DS    CL7
         ORG   WTOLOG+17
LOGCMD   DS    CL80
         ORG
         SPACE 1
UNITMSG  DC    X'11D7F01D40'        SET BUFFER ADDRESS
         DC    C'SIMULATED OPERATOR CONSOLE USING DIDOCS CONSOLE AT -'
UNITXXX  DC    CL4' '
SVCRCM   DC    CL9' '
NOTR15   DC    CL8' '
         DC    C' '
UNITLEN  EQU   *-UNITMSG
         SPACE 1
*                01234567890123
FAKEWARN DC    C'PRESS PF3 OR PF12 TO EXIT'
         SPACE 2
*
* TURN OFF FULL SCREEN (SPF) , CLEAR AND RESET CURSOR TO ROW1,COL1
*
CLEAR    DC   X'C7115D7E1140403C40400011404013'
*
TPUTFLG  DC   X'03'                   ITS FULL SCREEN TIME
         EJECT
***********************************************************************
*        CONSTANTS                                                    *
***********************************************************************
         SPACE
LOTSBLNK DC    CL90' '
HEXMASK  DC    XL8'0F0F0F0F0F0F0F0F'
HEXTAB   DC    C'0123456789ABCDEF'
         SPACE 2
BLANKTAB DC    CL256' '
         ORG   BLANKTAB+C'Ö'
         DC    C'Ö.<(+×',X'50'
         ORG   BLANKTAB+C'!'
         DC    C'!$*);^-/'
         ORG   BLANKTAB+C','
         DC    C',%_>?'
         ORG   BLANKTAB+C':'
         DC    C':#@''="'
         ORG   BLANKTAB+X'81'      TRANSLATE LOWER CASE TO UPPER
         DC    C'ABCDEFGHI'
         ORG   BLANKTAB+X'91'      ...
         DC    C'JKLMNOPQR'
         ORG   BLANKTAB+X'A2'      ...
         DC    C'STUVWXYZ '
         ORG   BLANKTAB+C'A'
         DC    C'ABCDEFGHI'
         ORG   BLANKTAB+C'J'
         DC    C'JKLMNOPQR'
         ORG   BLANKTAB+C'S'
         DC    C'STUVWXYZ '
         ORG   BLANKTAB+C'0'
         DC    C'0123456789'
         ORG
         EJECT
***********************************************************************
*        VARIABLES                                                    *
***********************************************************************
         SPACE
CVTA     DC    D'0',X'0'               PLUS A GARBAGE BYTE
CVTB     DC    F'0'
         SPACE 1
CMND     DC    H'79',H'0'
CMNDBUF  DC    CL80' '
CMNDLEN  EQU   *-CMND
         SPACE 1
XUCMID   DC    X'0'
ANSWER   DC    F'0'                ADDRESS POINTER FOR EXTRACT
         SPACE 2
PFREPLY  DS    0CL86               AREA TO HOLD TGET REPLY
PFCODE   DC    CL6' '              AID+JUNK
REPLY    DC    CL80' '             REAL REPLY
         SPACE 1
SASID    DS    F                   STORE FOR SECONDARY ASID
ONE      DC    H'1'                TO SET AX FOR 1
ZERO     DC    H'0'                TO SET AX FOR 0
CONASID  EQU   5                   'CONSOLE' ASID
         EJECT
***********************************************************************
*        LIST FORMS OF MACROS                                         *
***********************************************************************
         SPACE 2
EXTRACT  EXTRACT ANSWER,'S',FIELDS=(TSO),MF=L
         SPACE 2
EXTRPSCB EXTRACT ANSWER,'S',FIELDS=(PSB),MF=L
         SPACE 2
         SPACE 2
         EJECT
***********************************************************************
*        LITERAL POOL                                                 *
***********************************************************************
         SPACE
         LTORG
         SPACE 1
MOVEBSAV DS    6F                REGISTER SAVE AREA
         SPACE 1
LOCDCM   DS    CL(DCMEND-DCMSTRT)  COPY OF DCM AREA
         EJECT
***********************************************************************
*        SCREEN FORMAT                                                *
***********************************************************************
         SPACE 2 1 2 3 4 5 6 7 8 9 0 1 2 3 4 5 6 7
FULLSCRN DC    X'C1115D7F1140403C4040001140401D6040'  SET BUFFER/CLEAR
         SPACE 2
SCREEN   DC    20CL80' '   DATA LINES
MSGLINE  DC    CL80' '
         DC    X'11'              WRITE STRUCTURED FIELD
         DC    X'5A50'            ROW 22,COL 1
         DC    X'1D40'            ATTRIBUTE BYTES
         DC    X'11'              WRITE STRUCTURED FIELD
         DC    X'5B60'            ROW 23,COL 1
         DC    X'1D40'            ATTRIBUTE BYTES (END OF INPUT LINE)
         SPACE 2
         DC    X'115CF01D40'     LAST LINE       ROW 24,COL 1
TIMELINE DC    CL79' '           FOR TIME STAMP
         DC    X'115AD113'       SET CURSOR TO OPERATOR 22 COL 1
SCRLEN   EQU   *-FULLSCRN
*
         TITLE 'MVS UCM DEFINITION'
         IEECUCM FORMAT=NEW
         TITLE 'MVS RESIDENT DCM DEFINITION (RDCM)'
         IEERDCM
         TITLE 'MVS TRANSIENT DCM DEFINITION (TDCM)'
         IEETDCM
         TITLE 'MVS CVT DEFINITION'
         CVT   DSECT=YES
         TITLE 'TSO PROTECTED STEP CONTROL BLOCK'
         IKJPSCB
         END
