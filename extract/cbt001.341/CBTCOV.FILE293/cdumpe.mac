&TRACE OFF
*TRACE ALL
&IF &INDEX = 1 &IF &1 = ? &GOTO -EXP
GLOBAL TXTLIB CSSLIB
&FN = PROFILE
&FT = EXEC
&FM = A
&IF &INDEX GT 0 &FN = &1
&IF &INDEX GT 1 &FT = &2
&IF &INDEX GT 2 &FM = &3
-FILEDF
FI INDCB DISK &FN &FT &FM (XTENT 99999
&IF &0 = CDUMP  FI OUTDCB DISK CDUMP LISTING A3
&IF &0 = CDUMPT FI OUTDCB TERM
&IF &0 = CDUMPP FI OUTDCB PRINTER
SET CMSTYPE HT
STATE &FN &FT &FM
&RCS = &RC
SET CMSTYPE RT
&IF &RCS EQ 0 &SKIP 2
&TYPE FILE &FN &FT &FM NOT FOUND
&GOTO -EXIT
&TYPE ENTER SKIP PRINT INPUT
&READ VARS &SKIPP &PRINT &INPUT
&IF .&SKIPP = . &GOTO -EXIT
&IF .&PRINT NE . &SKIP 2
&PRINT = &SKIPP
&SKIPP = 0
&IF &PRINT = 0 &GOTO -EXIT
&IF .&INPUT EQ . &INPUT = EBCDIC
&SKIPP = &RIGHT OF 00000000&SKIPP 7
&PRINT = &RIGHT OF 00000000&PRINT 7
-DOIT
&S1 = &MULT OF &SKIPP 1
&P1 = &MULT OF &PRINT 1
&SKIPP = &CONCAT OF C &SKIPP
-DOIT
&TYPE CDUMP ON FILE &FN &FT &FM -- SKIP= &S1 , PRINT= &P1
&IF &0 ^= CDUMPT SET CMSTYPE HT
&TRACE ALL
FDUMP &SKIPP ,&PRINT ,&INPUT
&TRACE OFF
SET CMSTYPE RT
&IF &0 = CDUMP EXEC E CDUMP LISTING
-EXIT
&EXIT
-EXP
&BEGTYPE -END
CDUMP IS THE CMS VERSION OF THE PROGRAM FILEDUMP.
THE PROPER FORM IS :
          CDUMP FN FT FM

WHERE FN ETC. IS THE FILEID OF THE CMS FILE TO BE DUMPED. A PROMPT WILL
BE GIVEN FOR # OF BLOCKS TO BE PRINTED AND # OF BLOCKS TO BE SKIPPED.
AN EXAMPLE FOLLOWS.

* CDUMP PROFILE EXEC
  ENTER PRINT SKIP
* 20 150
  CDUMP ON PROFILE EXEC A -- SKIP= 150 , PRINT= 20

(AT THIS POINT A FILE CALLED CDUMP LISTING A3 WILL BE CREATED AND EDITED
 IF CDUMPT IS DEFINED AS A SYNONYM AND IS USED IN PLACE OF CDUMP THE OUT
 WILL BE TO THE TERMINAL )
 IF CDUMPP IS DEFINED AS A SYNONYM AND IS USED IN PLACE OF CDUMP THE OUT
 WILL BE TO THE PRINTER )
(* DENOTES USER ENTRY )

-END
