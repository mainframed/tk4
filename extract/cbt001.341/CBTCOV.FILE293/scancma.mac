SCANCMA  SMBEGIN
         LR    R2,R1
         L     R3,0(,R2)
         L     R4,4(,R2)
         L     R5,8(,R2)
         L     R6,12(,R2)
         XC    0(80,R6),0(R6)
                                                           SPACE 1
         LA    R7,FNFTFM
         USING FSCBD,R7
         LA    R8,INLINE
         USING BUFF,R8
                                                           SPACE 1
         IF (TM,WKFLAG,X'80'),OFF,THEN CMSEXEC NOT OPEN
           MVI   INLINE,X'40'
           MVC   INLINE+1(88),INLINE
           FSOPEN  FSCB=CMSEXEC        OPEN
           ERRIF (LTR,R15,R15),NZERO,EOF
           OI    WKFLAG,X'80'          IND CMSEXEC OPEN
         ENDIF
                                                           SPACE 1
         IF (TM,WKFLAG,X'20'),OFF,THEN CMSEXEC NEEDS READING
           MVI   INLINE,X'40'
           MVC   INLINE+1(88),INLINE
           FSREAD FSCB=CMSEXEC         READ IT
           ERRIF (LTR,R0,R0),ZERO,EOF  END OF FILE
           ERRIF (LTR,R15,R15),NZERO,EOF  END OF FILE
           MVC    0(8,R3),FN           MOVE IN FILE ID
           MVC    0(8,R4),FT
           MVC    0(2,R5),FM
                                                           SPACE 1
           ERRIF (CLC,RL,C256),GT,LONG IF LRECL > 256 ERROR
           OI    WKFLAG,X'20'          SHOW IT WAS READ
         ENDIF
                                                           SPACE 1
         IF (TM,WKFLAG,X'40'),OFF,THEN CMS FILE NEEDS OPENING
           MVC    FSCBFN,FN
           MVC    FSCBFT,FT
           MVC    FSCBFM,FM
           FSOPEN FSCB=FNFTFM
           ERRIF (LTR,R15,R15),NZERO,BAD
           OI    WKFLAG,X'40'
         ENDIF
                                                           SPACE 1
         MVI   LINE255,X'40'
         MVC   LINE255+1(254),LINE255
         FSREAD FSCB=FNFTFM            READ CMS FILE
                                                           SPACE 1
         IF (LTR,R0,R0),ZERO,OR        EOF ON CMS FILE
         IF (LTR,R15,R15),NZERO,THEN   EOF ON CMS FILE
           XC     0(80,R6),0(R6)
           FSCLOSE FSCB=FNFTFM         YES
           MVI   INLINE,X'40'
           MVC   INLINE+1(88),INLINE
           NI    WKFLAG,X'9F'          TURN OFF OPEN IND
         ELSE
           MVC    0(80,R6),LINE255
         ENDIF
                                                           SPACE 1
         SMBEXIT
         ERENT EOF,MAIN
           MVC   0(8,R3),EOF
         ERENT LONG,MAIN
           MVC   0(8,R6),TOOLONG
           NI    WKFLAG,X'BF'
         ERENT BAD,MAIN
           FSCLOSE FSCB=FNFTFM
           MVC   0(8,R6),BAD
           NI    WKFLAG,X'9F'
         ERENT DUM,MAIN
         LTORG
                                                           SPACE 1
CMSEXEC  FSCB 'CMS EXEC A',RECFM=F,BSIZE=88,BUFFER=INLINE
                                                           SPACE 1
FNFTFM   FSCB 'FN  FT  FM',RECFM=F,BUFFER=LINE255
                                                           SPACE 1
INLINE   DS    0CL88
         DS    CL7
INLINE2  DS    CL80
         DS    CL1
                                                           SPACE 1
WKAREA   DS    0D
LINE255  DS    CL255
WKFLAG   DC    X'00'
EOF      DC    CL8'99999EOF'
BAD      DC    CL8'99999BAD'
TOOLONG  DC    CL8'>256   '
C255     DC    CL9'      255'
C256     DC    CL9'      256'
WKLENGTH EQU   *-WKAREA
                                                           SPACE 1
BUFF     DSECT
         DS    CL7
FN       DS    CL8
         DS    CL1
FT       DS    CL8
         DS    CL1
FM       DS    CL2
         DS    CL1
RF       DS    CL2
         DS    CL1
RL       DS    CL9
         DS    CL1
NR       DS    CL10
         DS    CL1
NB       DS    CL10
         DS    CL1
                                                           SPACE 1
DT       DS    0CL8
MO       DS    CL2
         DS    CL1
DA       DS    CL2
         DS    CL1
YR       DS    CL2
                                                           SPACE 1
         DS    CL1
TM       DS    CL5
         DS    CL10
BUFFL    EQU   *-BUFF
                                                           SPACE 1
         FSCBD
                                                           SPACE 1
         END
