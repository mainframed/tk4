       IDENTIFICATION DIVISION.
         PROGRAM-ID.    OPS4320G.
         INSTALLATION.  HOOKER CHEMICAL AND PLASTICS COMPANY.
       DATE-COMPILED.   DATE.
       REMARKS. WRITTEN BY :
                                   ROBERT P DZIALAK
                                   OCCIDENTAL CHEMICAL CORPORATION
                                   360 RAINBOW BLVD. SOUTH
                                   NIAGARA FALLS , NEW YORK 14303
                                   (716) 286-3251
       ENVIRONMENT DIVISION.
        CONFIGURATION SECTION.
          SOURCE-COMPUTER. IBM-370.
          OBJECT-COMPUTER. IBM-370.
        INPUT-OUTPUT SECTION.
          FILE-CONTROL.
             SELECT DLMCARD               ASSIGN TO UT-S-DLMCARD.
             SELECT COPYCARD              ASSIGN TO UT-S-COPYCARD.
             SELECT MCDCODE               ASSIGN TO UT-S-MCDCODE.
       DATA DIVISION.
        FILE SECTION.
         FD  DLMCARD
             BLOCK CONTAINS 0 RECORDS
             LABEL RECORDS ARE STANDARD.
         01  DLMCARD-REC                PIC X(80).
         FD  COPYCARD
             BLOCK CONTAINS 0 RECORDS
             LABEL RECORDS ARE STANDARD.
         01  COPYCARD-REC               PIC X(80).
         FD  MCDCODE
             BLOCK CONTAINS 0 RECORDS
             LABEL RECORDS ARE STANDARD.
         01  MCDCODE-REC.
             02 MCD-BASE                PIC 9999.
             02 MCD-BASE-X REDEFINES MCD-BASE PIC XXXX.
             02 FILLER                  PIC X(76).
        WORKING-STORAGE SECTION.
         77  MO-CTR                     PIC S9(9) COMP VALUE +0.
         77  MOD-COUNT                  PIC S9(9) COMP VALUE +0.
         77  DATE-CONVERTED             PIC S9(9) COMP VALUE +0.
         77  IDLE-DAYS                  PIC S9(9) COMP VALUE +0.
         77  LEAPTEST                   PIC S9(9) COMP VALUE +0.
         77  LEAP-RMNDR                 PIC S9(9) COMP VALUE +0.
         77  HOURS-ONLINE               PIC S9(9) COMP VALUE +0.
         77  JOB-DAYS                   PIC S9(9) COMP VALUE +0.
         77  JOB-HOUR                   PIC S9(9) COMP VALUE +0.
         77  CURRENT-DAYS               PIC S9(9) COMP VALUE +0.
         77  CURRENT-HOUR               PIC S9(9) COMP VALUE +0.
         77  RUN-HOUR                   PIC S9(9) COMP VALUE +0.
         77  BASE-MCD-CODE              PIC S9(9) COMP VALUE +0.
         01  OPN-RETURN-CODE            PIC  X       VALUE SPACE.
         01  OPN-AREA                   PIC  X(16)   VALUE SPACE.
         01  MOD-NAME                   PIC  X(8)    VALUE SPACE.
         01  MOD-RETURN-CODE            PIC  X       VALUE SPACE.
         01  MOD-AREA.
             02 MOD-PASSWORD            PIC X(04).
             02 DATE-ADDED              PIC X(06).
             02 LAST-UP-DATE.
                03 LST-UP-MM            PIC 99.
                03 LST-UP-DD            PIC 99.
                03 LST-UP-YY            PIC 99.
             02 LAST-UP-HH              PIC 99.
             02 LAST-UP-MM              PIC 99.
             02 FILLER                  PIC XXX.
             02 JOBNAME                 PIC X(8).
             02 USERID                  PIC X(7).
             02 FILLER                  PIC X(8).
             02 MODLNAME                PIC X(8).
             02 JUL-RUN-DATE            PIC X(5).
             02 RUN-DATE REDEFINES JUL-RUN-DATE.
                03 JUL-RUN-YY           PIC 99.
                03 JUL-RUN-DDD          PIC 999.
             02 RUN-STR-HH              PIC 99.
             02 RUN-STR-MM              PIC 99.
             02 RUN-END-HH              PIC 99.
             02 RUN-END-MM              PIC 99.
             02 FILLER                  PIC X(21).
             02 LAST-ACCESS             PIC X(06).
             02 FILLER                  PIC X(06).
         01  WS-DATE.
             02 MM                      PIC 99.
             02 DD                      PIC 99.
             02 YY                      PIC 99.
         01  WS-GREGORIAN-DATE.
             02 GREG-MM                 PIC 99.
             02 FILLER                  PIC X.
             02 GREG-DD                 PIC 99.
             02 FILLER                  PIC X.
             02 GREG-YY                 PIC 99.
         01  CUR-GREG-MM-DD.
             02 CUR-GREG-MM                 PIC 99.
             02 CUR-GREG-DD                 PIC 99.
         01  MCD-CODE-FACTOR REDEFINES CUR-GREG-MM-DD PIC 9999.
         01  WS-CUR-JULIAN-DATE.
             02 CUR-JUL-YY                  PIC 99.
             02 CUR-JUL-DDD                 PIC 999.
         01  WS-CUR-TIME.
             02 CUR-HH                      PIC 99.
             02 CUR-MM                      PIC 99.
             02 FILLER                      PIC X(4).
         01  CALENDAR-TABLE.
             02 FILLER                  PIC S9999  COMP VALUE +31.
             02 FEB                     PIC S9999  COMP VALUE +28.
             02 FILLER                  PIC S9999  COMP VALUE +31.
             02 FILLER                  PIC S9999  COMP VALUE +30.
             02 FILLER                  PIC S9999  COMP VALUE +31.
             02 FILLER                  PIC S9999  COMP VALUE +30.
             02 FILLER                  PIC S9999  COMP VALUE +31.

             02 FILLER                  PIC S9999  COMP VALUE +31.
             02 FILLER                  PIC S9999  COMP VALUE +30.
             02 FILLER                  PIC S9999  COMP VALUE +31.
             02 FILLER                  PIC S9999  COMP VALUE +30.
             02 FILLER                  PIC S9999  COMP VALUE +31.
         01  CALENDAR-DAYS REDEFINES CALENDAR-TABLE.
             02 MTH-DAY                 PIC S9999  COMP OCCURS 12 TIMES.
         01  OPT-CARD.
             02 FILLER                  PIC X(04)      VALUE '-OPT'.
             02 FILLER                  PIC X(76)      VALUE SPACES.
         01  UTL-CARD.
             02 FILLER                  PIC X(12) VALUE '-OPT UTILITY'.
             02 FILLER                  PIC X(68)      VALUE SPACES.
         01  MCD-CARD.
             02 FILLER                  PIC X(05)      VALUE '-MCD '.
             02 MCD-CD                  PIC 9999.
             02 FILLER                  PIC X(71)      VALUE SPACES.
         01  NOPC-CARD.
             02 FILLER                  PIC X(09)    VALUE '-OPT NOPC'.
             02 FILLER                  PIC X(71)      VALUE SPACES.
         01  DLM-CARD.
             02 FILLER                  PIC X(05)      VALUE '-DLM '.
             02 DLM-NAME                PIC X(8).
             02 FILLER                  PIC X          VALUE ','.
             02 DLM-PASSWORD            PIC X(8).
             02 FILLER                  PIC X(58)      VALUE SPACES.
         01  COPY-CARD.
             02 FILLER                  PIC X(06)      VALUE '-COPY '.
             02 COPY-NAME               PIC X(8).
             02 FILLER                  PIC X(12) VALUE ',STATUS=TEST'.
             02 FILLER                  PIC X(54)      VALUE SPACES.
         01  END-CARD.
             02 FILLER                  PIC X(04)      VALUE '-END'.
             02 FILLER                  PIC X(76)      VALUE SPACES.
       LINKAGE SECTION.
         01  CUT-OFF.
             02 PARM-LENGTH             PIC S9(4) COMP.
             02 PARMS.
                 03 HOUR-WORD               PIC X(6).
                 03 CUT-OFF-HOURS           PIC 999.
                 03 MOD-WORD                PIC X(9).
                 03 CUT-OFF-MODULES         PIC 999.
       PROCEDURE DIVISION USING CUT-OFF.
           IF PARM-LENGTH = 21
              IF HOUR-WORD = 'HOURS='
                 IF MOD-WORD = ',MODULES='
                    IF CUT-OFF-HOURS NUMERIC
                        IF CUT-OFF-MODULES NUMERIC
                            GO TO PARMS-OK.
           DISPLAY 'INPUT PARAMETERS TO PROGRAM OPSXXXXG INVALID'
           DISPLAY 'PARM LENGTH =' PARM-LENGTH.
           DISPLAY 'PARAMETERS =' PARMS.
           MOVE 1 TO CUT-OFF-MODULES.
           GO TO STOP-RUN.
       PARMS-OK.
           DISPLAY 'CUT OFF HOURS =' CUT-OFF-HOURS.
           DISPLAY 'CUT OFF MODULES =' CUT-OFF-MODULES.
           ACCEPT WS-CUR-JULIAN-DATE FROM DAY.
           DISPLAY 'CURRENT JULIAN DATE = ' WS-CUR-JULIAN-DATE.
           ACCEPT WS-CUR-TIME FROM TIME.
           DISPLAY 'CURRENT TIME (HHMMSSTT) = ' WS-CUR-TIME.
      *
           OPEN INPUT MCDCODE.
           READ MCDCODE AT END
               MOVE 0 TO MCD-CD
               GO TO END-MCD.
           IF MCD-BASE-X = 'NONE'
               MOVE 0 TO MCD-CD
               GO TO END-MCD.
           IF MCD-BASE NOT NUMERIC
               DISPLAY 'MCD BASE NUMBER INVALID=' MCD-BASE
               DISPLAY 'MUST BE NUMERIC (4 NUMBERS) OR THE WORD NONE'
               CLOSE MCDCODE
               GO TO STOP-RUN.
           MOVE MCD-BASE TO BASE-MCD-CODE.
           MOVE CURRENT-DATE TO WS-GREGORIAN-DATE.
           MOVE GREG-MM TO CUR-GREG-MM.
           MOVE GREG-DD TO CUR-GREG-DD.
           COMPUTE MCD-CD = BASE-MCD-CODE + MCD-CODE-FACTOR.
       END-MCD.
           CLOSE MCDCODE.
      *
           DIVIDE CUR-JUL-YY BY 4 GIVING LEAPTEST REMAINDER LEAP-RMNDR.
           IF LEAP-RMNDR = 0
               MOVE 29 TO FEB.
      *
           IF CUR-MM GREATER 29
               ADD 1 TO CUR-HH.
           COMPUTE CURRENT-DAYS = (CUR-JUL-YY * 365) +
                                  (CUR-JUL-YY / 4) + CUR-JUL-DDD - 1.
           COMPUTE CURRENT-HOUR = CURRENT-DAYS * 24 + CUR-HH
           DISPLAY 'CURRENT-HOUR = ' CURRENT-HOUR.
      *
           OPEN OUTPUT COPYCARD.
           OPEN OUTPUT DLMCARD.
      *
           WRITE COPYCARD-REC FROM UTL-CARD.
           IF MCD-CD NOT = 0
               WRITE COPYCARD-REC FROM MCD-CARD.
           WRITE COPYCARD-REC FROM NOPC-CARD.
      *
           WRITE DLMCARD-REC FROM OPT-CARD.
           IF MCD-CD NOT = 0
               WRITE DLMCARD-REC FROM MCD-CARD.
           WRITE DLMCARD-REC FROM NOPC-CARD.
      *
           CALL 'MASTOPN' USING OPN-AREA, OPN-RETURN-CODE.
           IF OPN-RETURN-CODE NOT = '0'
                DISPLAY 'LIBRARIAN INPUT FILE COULD NOT BE OPENED'
                DISPLAY 'OPEN RETURN CODE =' OPN-RETURN-CODE
                GO TO END-RUN.
      *
       GET-NEXT-MODULE.
           MOVE SPACES TO MOD-NAME.
      *
           CALL 'MASTMOD' USING MOD-NAME, MOD-RETURN-CODE, MOD-AREA.
           IF MOD-RETURN-CODE = '9' OR '2'
                DISPLAY 'LIBRARIAN INPUT FILE MODULE LOCATE PROBLEM'
                DISPLAY 'READ RETURN CODE =' MOD-RETURN-CODE
                MOVE 0 TO MOD-COUNT
                GO TO END-RUN.
           IF MOD-RETURN-CODE = '1'
                GO TO END-RUN.
      *
           IF JUL-RUN-DATE = 'YYDDD'
               GO TO USE-ADD-DATE-TIME.
      *
           IF RUN-STR-MM GREATER 29
               ADD 1 TO RUN-STR-HH.
           COMPUTE JOB-DAYS = (JUL-RUN-YY * 365) +
                                  (JUL-RUN-YY / 4) + JUL-RUN-DDD - 1.
           COMPUTE JOB-HOUR = JOB-DAYS * 24 + RUN-STR-HH.
           COMPUTE HOURS-ONLINE = CURRENT-HOUR - JOB-HOUR.
      *
           DISPLAY 'JOB-HOUR = ' JOB-HOUR ' HOURS-ONLINE = '
                              HOURS-ONLINE   ' MOD = ' MODLNAME.
      *
           IF HOURS-ONLINE GREATER CUT-OFF-HOURS
               GO TO RITE-DLM-COPY
           ELSE
               GO TO GET-NEXT-MODULE.
      *
       USE-ADD-DATE-TIME.
           MOVE LAST-UP-DATE TO WS-DATE.
           PERFORM CONVERT-DATE THRU CONVERT-EXIT.
           MOVE DATE-CONVERTED TO JOB-DAYS.
      *
           IF LAST-UP-MM GREATER 29
               ADD 1 TO LAST-UP-HH.
           COMPUTE JOB-HOUR = JOB-DAYS * 24 + LAST-UP-HH.
           COMPUTE HOURS-ONLINE = CURRENT-HOUR - JOB-HOUR.
           DISPLAY 'NOT SCHEDULED'
           DISPLAY 'JOB-HOUR = ' JOB-HOUR ' HOURS-ONLINE = '
                              HOURS-ONLINE   ' MOD = ' MODLNAME.
      *
           IF HOURS-ONLINE GREATER CUT-OFF-HOURS
               GO TO RITE-DLM-COPY
           ELSE
               GO TO GET-NEXT-MODULE.
      *
       RITE-DLM-COPY.
           ADD 1 TO MOD-COUNT.
      *
           MOVE MOD-NAME TO COPY-NAME.
           WRITE COPYCARD-REC FROM COPY-CARD.
      *
           MOVE MOD-NAME TO DLM-NAME.
           MOVE MOD-PASSWORD TO DLM-PASSWORD.
           WRITE DLMCARD-REC FROM DLM-CARD.
      *
           GO TO GET-NEXT-MODULE.
      *
       CONVERT-DATE.
           COMPUTE DATE-CONVERTED = (365 * YY) + (YY / 4) + DD - 1.
           MOVE 0 TO MO-CTR.
       INCR-MO-CTR.
           ADD 1 TO MO-CTR.
           IF MO-CTR LESS MM
               ADD MTH-DAY (MO-CTR) TO DATE-CONVERTED
               GO TO INCR-MO-CTR.
       CONVERT-EXIT. EXIT.
      *
       END-RUN.
           WRITE DLMCARD-REC FROM END-CARD.
           WRITE COPYCARD-REC FROM END-CARD.
           CLOSE DLMCARD.
           CLOSE COPYCARD.
       STOP-RUN.
           IF MOD-COUNT GREATER CUT-OFF-MODULES
               MOVE +0 TO RETURN-CODE
           ELSE
               MOVE +999 TO RETURN-CODE.
           DISPLAY 'MODULES TO BE TAKEN OFFLINE = ' MOD-COUNT.
      *
           STOP RUN.
