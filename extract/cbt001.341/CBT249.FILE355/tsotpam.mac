         MACRO
&NAME    TSOTPAM
.**********************************************************************
.*                                                                    *
.*  MACRO: TSOTPAM                                                    *
.*                                                                    *
.*  PURPOSE: TO PROVIDE INFORMATION ABOUT THE INTERFACE BETWEEN TSO   *
.*    AND THE TP TELE-PROCESSING ACCESS METHOD (VTAM OR TCAM) AND     *
.*    TO PROVIDE EXTENDED FUNCTION TO THIS INTERFACE.                 *
.*                                                                    *
.*  FORMAT:                                                           *
.*    TSOTPAM INPUT  - CHANGE THE TERMINAL I/O STATUS TO INPUT.  THIS *
.*                     ALLOWS FULL SCREEN TPUTS TO TO PROCEED AFTER   *
.*                     LINE MODE TPUTS WITHOUT VTAM HOLDING THE       *
.*                     SCREEN WITH '***'.  (3270'S UNDER VTAM ONLY,   *
.*                     NOT NECESSARY WITH TCAM)                       *
.*                                                                    *
.*    TSOTPAM OUTPUT - CHANGE THE TERMINAL I/O STATUS TO OUTPUT.      *
.*                     THIS DISALLOWS FULL SCREEN TPUTS TO PROCEED    *
.*                     AFTER LINE MODE TPUTS WITHOUT VTAM HOLDING THE *
.*                     SCREEN WITH '***'.  (3270'S UNDER VTAM ONLY)   *
.*                                                                    *
.*    TSOTPAM TSO,POINTER - RETURNS AN INDICATION OF THE TSO          *
.*                     ENVIORNMENT IN WHICH THE CALLER IS RUNNING.    *
.*                     POINTER POINTS TO A TWO FULL WORD FIELD        *
.*                     CONTAINING:                                    *
.*                     "TSOTPAM " => TSO RUNNING UNDER A TP ACCESS    *
.*                                   METHOD, I.E. UNDER CONTROL OF A  *
.*                                   TERMINAL.                        *
.*                     "TSOBATCH" => TSO RUNNING AS A BATCH JOB, I.E. *
.*                                   NOT CONNECTED TO A TERMINAL.     *
.*                     "BATCH   " => NOT A TSO SESSION, A BATCH JOB.  *
.*                                                                    *
.*    TSOTPAM TRMID,POINTER - RETURNS THE TERMINAL ID OF THE CALLER.  *
.*                     POINTER POINTS TO A TWO FULL WORD FIELD        *
.*                     CONTAINING THE TERMINAL ID.                    *
.*                                                                    *
.*    TSOTPAM APPLID,POINTER - RETURNS THE APPLICATION ID FOR THE     *
.*                     TSO SESSION.  POINTER POINTS TO A TWO FULL     *
.*                     FIELD CONTAINING THE APPLICATION ID.           *
.*                     (VTAM ONLY)                                    *
.*                                                                    *
.*    TSOTPAM TRMMODE,POINTER - RETURNS AN INDICATOR TELLING IF THE   *
.*                     TERMINAL IS IN FULL SCREEN MODE OR LINE MODE.  *
.*                     (3270 AND VTAM ONLY) POINTER POINTS TO A TWO   *
.*                     FULL WORD FIELD CONTAINING:                    *
.*                     "FULLSCRN" => THE 3270 IS IN FULL SCREEN MODE. *
.*                     "LINEMODE" => THE 3270 IS IN LINE MODE.        *
.*                                                                    *
.*    TSOTPAM SPF,POINTER - RETURNS AN INDICATOR TELLING IF THE       *
.*                     TERMINAL IS IN SPF MODE OR NOT. (3270 AND TSO  *
.*                     SESSION MANAGER ONLY) POINTER POINTS TO A TWO  *
.*                     FULL WORD FIELD CONTAINING:                    *
.*                     "SPFON   " => THE 3270 IS IN SPF MODE.         *
.*                     "SPFOFF  " => THE 3270 IS NOT IN SPF MODE.     *
.*                                                                    *
.*    TSOTPAM TPAM,POINTER - RETURNS AN INDICATION OF THE TP ACCESS   *
.*                     METHOD THE TSO SESSION IS USING.  POINTER      *
.*                     POINTS TO A FULL WORD FIELD CONTAINING:        *
.*                     "VTAM" => THE TSO SESSION IS USING VTAM.       *
.*                     "TCAM" => THE TSO SESSION IS USING TCAM.       *
.*                                                                    *
.*    TSOTPAM TRMTYPE,POINTER - RETURNS AN INDICATION OF THE TERMINAL *
.*                     TYPE.  POINTER POINTS TO A FULL WORD           *
.*                     CONTAINING:                                    *
.*                     "3270" => FOR A 3270 TERMINAL.                 *
.*                     "TWX " => FOR A TWX TERMINAL.                  *
.*                     "2741" => FOR A 2741 TERMINAL.                 *
.*                     "1050" => FOR A 1050 TERMINAL.                 *
.*                     "3767" => FOR A 3767 OR 3770 TERMINAL.         *
.*                     "USER" => FOR A USER DEFINED TERMINAL.         *
.*                                                                    *
.*    TSOTPAM TRMSTAT,POINTER - RETURNS AN INDICATION OF THE TYPE OF  *
.*                     I/O LAST PERFORMED AT THIS TERMINAL.           *
.*                     (VTAM ONLY) POINTER POINTS TO A FULL WORD      *
.*                     CONTAINING:                                    *
.*                     "IN  " => IF THE LAST I/O WAS A TGET.          *
.*                     "OUT " => IF THE LAST I/O WAS A TPUT.          *
.*                                                                    *
.*  REGISTER USAGE                                                    *
.*    REGISTER 0  - USED TO PASS THE REQUEST CODE TO THE SVC          *
.*      0 => RETURN THE TSO SESSION TYPE (TSO OR BATCH)               *
.*      1 => RETURN THE TELE-PROCESSING ACCESS METHOD                 *
.*      2 => RETURN THE TERMINAL TYPE                                 *
.*      3 => RETURN THE TERMINAL ID                                   *
.*      4 => RETURN THE TERMINAL MODE (FULL SCREEN OR LINE MODE)      *
.*      5 => RETURN THE SPF INDICATOR                                 *
.*      6 => RETURN THE APPLICATION ID                                *
.*      7 => RETURN THE TERMINAL I/O STATUS (INPUT OR OUTPUT)         *
.*      8 => SET THE TERMINAL I/O STATUS TO OUTPUT                    *
.*      9 => SET THE TERMINAL I/O STATUS OT INPUT                     *
.*    REGISTER 1  - PIONTS TO A USER DEFINED AREA (ON A FULL WORD     *
.*                  BOUNDARY) WHERE THE INDICATOR REQUESTED BY THE    *
.*                  CALLER IS TO BE PLACED.                           *
.*    REGISTER 15 - RETURN CODE                                       *
.*       0 => SUCESSFULL COMPLETION                                   *
.*       4 => TERMINAL I/O STATUS WAS TO BE SET TO INPUT OR OUTPUT    *
.*            HOWEVER THIS ALREADY WAS THE TERMINAL I/O STATUS.       *
.*       8 => AN INVALID REQUEST CODE WAS RECEIVED.                   *
.*      12 => THE CALLER WAS NOT RUNNING AS A TSO SESSION CONNECTED   *
.*            TO A TERMINAL THUS THE REQUEST COULD NOT BE HONORED.    *
.*      16 => THE CALLER WAS NOT RUNNING AS A TSO / TCAM SESSION AND  *
.*            THE REQUEST WAS VALID ONLY FOR TSO / VTAM SESSIONS.     *
.*            THE REQUEST WAS NOT HONORED.                            *
.*      20 => THE CALLER WAS NOT RUNNING AS A TSO / VTAM SESSION AND  *
.*            THE REQUEST WAS VALID ONLY FOR TSO / TCAM SESSIONS.     *
.*            THE REQUEST WAS NOT HONORED.                            *
.*      24 => THE CALLER WAS NOT CONNECTED TO A 3270.  THE REQUEST IS *
.*            ONLY VALID FOR A 3270, THUS IT WAS NOT BE HONORED.      *
.*                                                                    *
.**********************************************************************
         LCLC   &SVCNO   TSO / TPAM EXTENDED INTERFACE SVC NUMBER
&SVCNO   SETC  '###'
         AIF   (N'&SYSLIST GT 0).START
         MNOTE 8,'FIRST POSITIONAL PARAMETER IS REQUIRED'
         MEXIT
.START   ANOP
         AIF   ('&SYSLIST(1)' EQ 'TSO').DOUBLE
         AIF   ('&SYSLIST(1)' EQ 'TPAM').DOUBLE
         AIF   ('&SYSLIST(1)' EQ 'TRMTYPE').DOUBLE
         AIF   ('&SYSLIST(1)' EQ 'TRMID').DOUBLE
         AIF   ('&SYSLIST(1)' EQ 'TRMMODE').DOUBLE
         AIF   ('&SYSLIST(1)' EQ 'SPF').DOUBLE
         AIF   ('&SYSLIST(1)' EQ 'APPLID').DOUBLE
         AIF   ('&SYSLIST(1)' EQ 'TRMSTAT').DOUBLE
         AIF   ('&SYSLIST(1)' EQ 'OUTPUT').OUTPUT
         AIF   ('&SYSLIST(1)' EQ 'INPUT').INPUT
         MNOTE 8,'FIRST PARAMETER IS AN INVALID REQUEST'
         MEXIT
.DOUBLE  ANOP
         AIF   (N'&SYSLIST GT 1).CASE
         MNOTE 8,'SECOND POSITIONAL PARAMETER IS REQUIRED'
         MEXIT
.CASE    ANOP
         AIF   ('&SYSLIST(1)' EQ 'TSO').TSO
         AIF   ('&SYSLIST(1)' EQ 'TPAM').TPAM
         AIF   ('&SYSLIST(1)' EQ 'TRMTYPE').TRMTYPE
         AIF   ('&SYSLIST(1)' EQ 'TRMID').TRMID
         AIF   ('&SYSLIST(1)' EQ 'TRMMODE').TRMMODE
         AIF   ('&SYSLIST(1)' EQ 'SPF').SPF
         AIF   ('&SYSLIST(1)' EQ 'APPLID').APPLID
         AIF   ('&SYSLIST(1)' EQ 'TRMSTAT').TRMSTAT
         MNOTE 8,'FIRST PARAMETER IS AN INVALID REQUEST'
         MEXIT
.TSO     ANOP
         SR    0,0                     REQUEST FOR TSO INDICATION
         LA    1,&SYSLIST(2)
         SVC   &SVCNO                  ISSUE LOCAL SVC
         MEXIT
.TPAM    ANOP
         LA    0,1                     REQUEST FOR TPAM INDICATION
         LA    1,&SYSLIST(2)
         SVC   &SVCNO                  ISSUE LOCAL SVC
         MEXIT
.TRMTYPE ANOP
         LA    0,2                     REQUEST FOR TERMINAL TYPE
         LA    1,&SYSLIST(2)
         SVC   &SVCNO                  ISSUE LOCAL SVC
         MEXIT
.TRMID   ANOP
         LA    0,3                     REQUEST FOR TERMINAL ID
         LA    1,&SYSLIST(2)
         SVC   &SVCNO                  ISSUE LOCAL SVC
         MEXIT
.TRMMODE ANOP
         LA    0,4                     REQUEST FOR TERMINAL MODE
         LA    1,&SYSLIST(2)
         SVC   &SVCNO                  ISSUE LOCAL SVC
         MEXIT
.SPF     ANOP
         LA    0,5                     REQUEST FOR SPF INDICATION
         LA    1,&SYSLIST(2)
         SVC   &SVCNO                  ISSUE LOCAL SVC
         MEXIT
.APPLID  ANOP
         LA    0,6                     REQUEST FOR APPLICATION ID
         LA    1,&SYSLIST(2)
         SVC   &SVCNO                  ISSUE LOCAL SVC
         MEXIT
.TRMSTAT ANOP
         LA    0,7                     REQUEST FOR TERMINAL I/O STATUS
         LA    1,&SYSLIST(2)
         SVC   &SCVNO                  ISSUE LOCAL SVC
         MEXIT
.OUTPUT  ANOP
         LA    0,8                     SET OUTPUT TERMINAL STATUS
         SVC   &SVCNO                  ISSUE LOCAL SVC
         MEXIT
.INPUT   ANOP
         LA    0,9                     SET INPUT TERMINAL STATUSN
         SVC   &SVCNO                  ISSUE LOCAL SVC
         MEXIT
         MEND
