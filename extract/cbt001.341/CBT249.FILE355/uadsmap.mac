         MACRO
         UADSMAP &DSPL,&ALL,&HDR,&POB,&AOB,&ROB,&PDB,&ADB,&RDB,        X
               &FSQ,&GNL
         LCLC  &FULL
***********************************************************************
*                                                                     *
*                       U A D S M A P                                 *
*                                                                     *
*                UADS STRUCTURE MAPPING MACR0                         *
*                                                                     *
*                                                                     *
***********************************************************************
         SPACE 2
         AIF   ('&ALL' EQ 'ALL').FULL   CHK TO DO ALL
         AIF   ('&HDR' EQ 'HDR').HDR1   CHK TO DO HEADER BLOCK
         AIF   ('&GNL' EQ 'GNL').GOB1   CHK TO DO GENERAL OFFSET
         AIF   ('&POB' EQ 'POB').POB1   CHK TO DO PASSWD OFFSET
         AIF   ('&AOB' EQ 'AOB').AOB1   CHK TO DO ACCTNM OFFSET
         AIF   ('&ROB' EQ 'ROB').ROB1   CHK TO DO PROCNAME OFFSET
         AIF   ('&PDB' EQ 'PDB').PDB1   CHK TO DO PASSWD DATA
         AIF   ('&ADB' EQ 'ADB').ADB1   CHK TO DO ACCTNM DATA
         AIF   ('&RDB' EQ 'RDB').RDB1   CHK TO DO PROCNAME DATA
         AIF   ('&FSQ' EQ 'FSQ').FSQ1   CHK TO DO FREE SPACE ELEMENT
.FULL    ANOP
&FULL    SETC  'ALL'
.HDR1    ANOP
         AIF   ('&DSPL' NE 'DSPL').HDRA CHK TO DISPLAY FORMAT
         EJECT
***********************************************************************
*                                                                     *
*                     H E A D E R   B L O C K                         *
*                                                                     *
***********************************************************************
*                                                                     *
*                                                                     *
*       UADSHDR +---------------------+---------------------+         *
*        0  (0) × UADSBLKL            × UADSFSQP            ×         *
*               +---------------------+---------------------+         *
*        4  (4) × UADSUSER (ACTUALLY UADS MEMBER NAME)      ×         *
*               ×      (USERID SUFIXED WITH A DIGIT)        ×         *
*               +----------+----------+---------------------+         *
*       12  (C) × RESERVED × UADSBLKF × UADSMAXC            ×         *
*               +----------+----------+---------------------+         *
*       16 (10) × UADSUATT × RESERVED × UADSINST            ×         *
*               +----------+----------+---------------------+         *
*       20 (14) × UADSUPTP                                  ×         *
*               +-------------------------------------------+         *
*       24 (18) × UADSPWOP                                  ×         *
*               +-------------------------------------------+         *
*       28 (1C) × UADSPERP                                  +         *
*               +-------------------------------------------+         *
*       32 (20) × UADSDEST                                  ×         *
*               ×                                           ×         *
*               +----------+----------+----------+----------+         *
*       40 (28) × UADSHLDC × UADSJOBC × UADSMSGC × UADSOUTC I         *
*               +----------+----------+----------+----------+         *
*       44 (2C) × RESERVED                                  ×         *
*               ×                                           ×         *
*               ×                                           ×         *
*               ×                                           ×         *
*               ×                                           ×         *
*               +-------------------------------------------+         *
*                                                                     *
*                                                                     *
***********************************************************************
         SPACE 2
.HDRA    ANOP
UADSHDR  DSECT
         SPACE 1
UADSBLKL DS    AL2                     UADS MEMBER BLOCK LENGTH
UADSFSQP DS    AL2                     OFFSET TO INITIAL FSQE
UADSUSER DS    CL8                     USERID (UADS MEMBER NAME)
         DS    XL1                     RESERVED
UADSBLKF DS    XL1                     BLOCK DISCRIPTION FLAG
UADSNUSP EQU   X'80'                   0... .... NO NON-USABLE SPACE
*                                      1... .... ONLY NON-USABLE SPACE
*                                                EXISTS IN THIS BLOCK
UADSEXPF EQU   X'40'                   .0.. .... OLD FORMAT UADS
*                                      .1.. .... NEW FORMAT UADS
*                                                EXISTS IN THIS BLOCK
UADSXBLK EQU   X'20'                   ..0. .... BLOCKS NOT AVAILABLE
*                                      ..1. .... BLOCKS AVAILABLE
UADSXUSD EQU   X'10'                   ...0 .... EXTRA BLOCK NOT USED
*                                      ...1 .... EXTRA BLOCK IS USED
UADSMAXC DS    AL2                     USER MAXIMUM CORE SIZE
UADSUATT DS    XL1                     SYSTEM ATTRIBUTES OF USERID
UADSOPER EQU   X'80'                   1... .... OPERATOR CAPABILITY
UADSACCT EQU   X'40'                   .1.. .... ACCOUNT CAPABILITY
UADSJCL  EQU   X'20'                   ..1. .... JCL CAPABILITY
UADSMNT  EQU   X'10'                   ...1 .... MOUNT CAPABILITY
UADERCVR EQU   X'04'                   .... .1.. EDIT RECOVER CAPABILTY
         DS    XL1                     RESERVED
UADSINST DS    XL2                     FOR INSTALLATION USE
UADSUPTP DS    AL4                     OFFSET TO CURRENT UPT
UADSPWOP DS    AL4                     OFFSET TO FIRST PASSWORD OFFSET
UADSPERP DS    AL4                     OFFSET TO PERFORMANCE GROUP
UADSDEST DS    CL8                     DESTINATION
UADSHLDC DS    CL1                     HOLD CLASS
UADSJOBC DS    CL1                     JOB CLASS
UADSMSGC DS    CL1                     MESSAGE CLASS
UADSOUTC DS    CL1                     SYSOUT CLASS
         DS    CL20                    RESERVED
UADSHDRL EQU   *-UADSHDR               HEADER BLOCK LENGTH
         SPACE 2
         AIF   ('&FULL' EQ 'ALL').GOB1  CHK TO DO ALL
         AIF   ('&GNL' NE 'GNL').DOPOB  CHK TO DO GENERAL OFFSET
.GOB1    ANOP
         AIF   ('&DSPL' NE 'DSPL').GOBA CHK TO DISPLAY FORMAT
         EJECT
***********************************************************************
*                                                                     *
*             G E N E R A L   O F F S E T   B L O C K                 *
*                                                                     *
***********************************************************************
*                                                                     *
*                                                                     *
*       UADSGOB +----------+--------------------------------+         *
*        0  (0) × UADSGFLG × UADSGNOP                       ×         *
*               +----------+--------------------------------+         *
*        4  (4) × UADSGAOP                                  ×         *
*               +-------------------------------------------+         *
*        8  (8) × UADSGDBP                                  ×         *
*               +-------------------------------------------+         *
*                                                                     *
*                                                                     *
***********************************************************************
         SPACE 2
.GOBA    ANOP
UADSGOB  DSECT
         SPACE 1
UADSGFLG DS    XL1                    GENERAL OFFSET BLOCK
UADSGEND EQU   X'80'                  0... ... MORE OFFSET BLOCKS
*                                     1... ... LAST OFFSET BLOCK
         ORG   UADSGFLG
UADSGNOP DS    AL4                    NEXT OFFSET BLOCK
UADSGAOP DS    AL4                    ASSOCIATED OFFSET BLOCK
UADSGDBP DS    AL4                    DATA BLOCK
UADSGOBL EQU   *-UADSGOB              GENERAL OFFSET BLOCK LENGTH
         SPACE 2
.DOPOB   ANOP
         AIF   ('&FULL' EQ 'ALL').POB1  CHK TO DO ALL
         AIF   ('&POB' NE 'POB').DOAOB  CHK TO DO PASSWORD OFFSET
.POB1    ANOP
         AIF   ('&DSPL' NE 'DSPL').POBA CHK TO DISPLAY FORMAT
         EJECT
***********************************************************************
*                                                                     *
*            P A S S W O R D   O F F S E T   B L O C K                *
*                                                                     *
***********************************************************************
*                                                                     *
*                                                                     *
*      UADSPWOB +----------+--------------------------------+         *
*        0  (0) × UADSPFLG I UADSPNOP                       ×         *
*               +----------I--------------------------------+         *
*        4  (4) × UADSPAOP                                  ×         *
*               +-------------------------------------------+         *
*        8  (8) × UADSPPWP                                  ×         *
*               +-------------------------------------------+         *
*                                                                     *
*                                                                     *
***********************************************************************
         SPACE 2
.POBA    ANOP
UADSPWOB DSECT
         SPACE 1
UADSPFLG DS    XL1                     PASSWORD OFFSET BLOCK FLAGS
UADSPEND EQU   X'80'                   0... .... MORE PASSWORD BLOCKS
*                                      1... .... LAST PASSWORD BLOCK
         ORG   UADSPFLG
UADSPNOP DS    AL4                     NEXT PASSWORD OFFSET BLOCK
UADSPAOP DS    AL4                     ASSOCIATED ACCOUNT NO OFFSET BLK
UADSPPWP DS    AL4                     PASSWORD DATA BLOCK
UADSPOBL EQU   *-UADSPWOB              PASSWORD OFFSET BLOCK LENGTH
         SPACE 2
.DOAOB   ANOP
         AIF   ('&FULL' EQ 'ALL').AOB1  CHK TO DO ALL
         AIF   ('&AOB' NE 'AOB').DOROB  CHK TO DO ACCT NO. OFFSET
.AOB1    ANOP
         AIF   ('&DSPL' NE 'DSPL').AOBA CHK TO DISPLAY FORMAT
         EJECT
***********************************************************************
*                                                                     *
*                   A C C O U N T   N U M B E R                       *
*                     O F F S E T   B L O C K                         *
*                                                                     *
***********************************************************************
*                                                                     *
*                                                                     *
*      UADSANOB +----------+--------------------------------+         *
*        0  (0) × UADSAFLG × UADSANOP                       ×         *
*               +----------+--------------------------------+         *
*        4  (4) × UADSAPOP                                  ×         *
*               +-------------------------------------------+         *
*        8  (8) × UADSAANP                                  ×         *
*               +-------------------------------------------+         *
*                                                                     *
*                                                                     *
***********************************************************************
         SPACE 2
.AOBA    ANOP
UADSANOB DSECT
         SPACE 1
UADSAFLG DS    XL1                    ACCOUNT NUMBER OFFSET BLOCK FLAGS
UADSAEND EQU   X'80'                  0... .... MORE ACCOUNT NO. BLOCKS
*                                     1... .... LAST ACCOUNT NO. BLOCK
         ORG   UADSAFLG
UADSANOP DS    AL4                    NEXT ACCOUNT NO. OFFSET BLOCK
UADSAPOP DS    AL4                    ASSOCIATED PROCEDURE OFFSET BLOCK
UADSAANP DS    AL4                    ACCOUNT NO. DATA BLOCK
UADSAOBL EQU   *-UADSANOB             ACCOUNT NO. OFFSET BLOCK LENGTH
         SPACE 2
.DOROB   ANOP
         AIF   ('&FULL' EQ 'ALL').ROB1  CHK TO DO ALL
         AIF   ('&ROB' NE 'ROB').DOPDB  CHK TO DO PROCNAME OFFSET
.ROB1    ANOP
         AIF   ('&DSPL' NE 'DSPL').ROBA CHK TO DISPLAY FORMAT
         EJECT
***********************************************************************
*                                                                     *
*                   P R O C E D U R E   N A M E                       *
*                     O F F S E T   B L O C K                         *
*                                                                     *
***********************************************************************
*                                                                     *
*                                                                     *
*      UADSPROB +----------+--------------------------------+         *
*        0  (0) × UADSRFLG × UADSRNOP                       ×         *
*               +----------+--------------------------------+         *
*        4  (4) × RESERVED                                  ×         *
*               +-------------------------------------------+         *
*        8  (8) × UADSRPNP                                  ×         *
*               +-------------------------------------------+         *
*                                                                     *
*                                                                     *
***********************************************************************
         SPACE 2
.ROBA    ANOP
UADSPROB DSECT
         SPACE 1
UADSRFLG DS    XL1                     PROCNAME OFFSET BLOCK FLAG
UADSREND EQU   X'80'                   0... .... MORE PROCNAME BLOCKS
*                                      1... .... LAST PROCNAME BLOCK
         ORG   UADSRFLG
UADSRNOP DS    AL4                     NEXT PROCNAME OFFSET BLOCK
         DS    AL4                     RESERVED
UADSRPNP DS    AL4                     PROCNAME DATA BLOCK
UADSROBL EQU   *-UADSPROB              PROCNAME OFFSET BLOCK LENGTH
         SPACE 2
.DOPDB   ANOP
         AIF   ('&FULL' EQ 'ALL').PDB1  CHK TO DO ALL
         AIF   ('&PDB' NE 'PDB').DOADB  CHK TO DO PASSWORD DATA
.PDB1    ANOP
         AIF   ('&DSPL' NE 'DSPL').PDBA CHK TO DISPLAY FORMAT
         EJECT
***********************************************************************
*                                                                     *
*              P A S S W O R D   D A T A   B L O C K                  *
*                                                                     *
***********************************************************************
*                                                                     *
*                                                                     *
*      UADSPWDB +----------+--------------------------------+         *
*        4  (0) × UADSPCNT × RESERVED                       ×         *
*               +----------+--------------------------------+         *
*        8  (4) × UADSPPWD                                  ×         *
*               ×                                           ×         *
*               +-------------------------------------------+         *
*                                                                     *
*                                                                     *
***********************************************************************
         SPACE 2
.PDBA    ANOP
UADSPWDB DSECT
         SPACE 1
UADSPCNT DS    AL1                     PASSWORD DATA BLOCK USE COUNT
         DS    XL3                     RESERVED
UADSPPWD DS    CL8                     PASSWORD
UADSPDBL EQU   *-UADSPWDB              PASSWORD DATA BLOCK LENGTH
         SPACE 2
.DOADB   ANOP
         AIF   ('&FULL' EQ 'ALL').ADB1  CHK TO DO ALL
         AIF   ('&ADB' NE 'ADB').DORDB  CHK TO DO ACCT NO. DATA
.ADB1    ANOP
         AIF   ('&DSPL' NE 'DSPL').ADBA CHK TO DISPLAY FORMAT
         EJECT
***********************************************************************
*                                                                     *
*                   A C C O U N T   N U M B E R                       *
*                      D A T A     B L O C K                          *
*                                                                     *
***********************************************************************
*                                                                     *
*                                                                     *
*      UADSANDB +----------+--------------------------------+         *
*        0  (0) × UADSACNT × RESERVED                       ×         *
*               +----------+--------------------------------+         *
*        4  (4) × UADSADRF (40 BYTES)                       ×         *
*               ×                                           ×         *
*               ×                                           ×         *
*               ×                                           ×         *
*               ×                                           ×         *
*               ×                                           ×         *
*               ×                                           ×         *
*               ×                                           ×         *
*               ×                                           ×         *
*               ×                                           ×         *
*               +----------+--------------------------------+         *
*       44 (2C) × UADSALEN × UADSANUM (MAX OF 40 BYTES)     ×         *
*               +----------+                                ×         *
*               ×                                           ×         *
*               ×                                           ×         *
*               ×                                           ×         *
*               ×                                           ×         *
*               ×                                           ×         *
*               ×                                           ×         *
*               ×                                           ×         *
*               ×                                           ×         *
*               ×                                           ×         *
*               ×          +--------------------------------+         *
*               ×          ×                                          *
*               +----------+                                          *
*                                                                     *
*                                                                     *
***********************************************************************
         SPACE 2
.ADBA    ANOP
UADSANDB DSECT
         SPACE 1
UADSACNT DS    AL1                     ACCOUNT NO. DATA BLOCK USE COUNT
         DS    XL3                     RESERVED
UADSADRF DS    CL40                    DRIVER DATA FIELD
UADSALEN DS    AL1                     LENGTH OF ACCOUNT NO.
UADSANUM DS    CL40                    VARIABLE LENGTH ACCOUNT NO.
UADSADBL EQU   *-UADSANDB              ACCOUNT NO. DATA BLOCK LENGTH
         SPACE 2
.DORDB   ANOP
         AIF   ('&FULL' EQ 'ALL').RDB1  CHK TO DO ALL
         AIF   ('&RDB' NE 'RDB').DOFSQ  CHK TO DO PROCNAME DATA
.RDB1    ANOP
         AIF   ('&DSPL' NE 'DSPL').RDBA CHK TO DISPLAY FORMAT
         EJECT
***********************************************************************
*                                                                     *
*                   P R O C E D U R E   N A M E                       *
*                      D A T A     B L O C K                          *
*                                                                     *
***********************************************************************
*                                                                     *
*                                                                     *
*      UADSPRDB +----------+--------------------------------+         *
*        0  (0) × UADSRCNT × RESERVED                       ×         *
*               +----------+--------------------------------+         *
*        4  (4) × UADSRPRC                                  ×         *
*               ×                                           ×         *
*               +---------------------+---------------------+         *
*       12  (C) × RESERVED            × UADSRSIZ            ×         *
*               +---------------------+---------------------+         *
*       16 (10) × UADSRUNT                                  ×         *
*               ×                                           ×         *
*               +-------------------------------------------+         *
*                                                                     *
*                                                                     *
***********************************************************************
         SPACE 2
.RDBA    ANOP
UADSPRDB DSECT
         SPACE 1
UADSRCTR DS    AL1                     PROCNAME DATA BLOCK USE COUNT
         DS    XL3                     RESERVED
UADSRPRC DS    CL8                     LOGON PROCEDURE NAME
         DS    XL2                     RESERVED
UADSRSIZ DS    AL2                     REGION SIZE
UADSRUNT DS    CL8                     ESOTERIC GROUP UNIT NAME
UADSRDBL EQU   *-UADSPRDB              PROCNAME DATA BLOCK LENGTH
         SPACE 2
.DOFSQ   ANOP
         AIF   ('&FULL' EQ 'ALL').FSQ1  CHK TO DO ALL
         AIF   ('&FSQ' NE 'FSQ').DONE   CHK TO DO FSQE DATA
.FSQ1    ANOP
         AIF   ('&DSPL' NE 'DSPL').FSQEA CHK TO DISPLAY FORMAT
         EJECT
***********************************************************************
*                                                                     *
*                F R E E   S P A C E   Q U E U E                      *
*                         E L E M E N T                               *
*                                                                     *
***********************************************************************
*                                                                     *
*                                                                     *
*      UADSFSQE +---------------------+---------------------+         *
*        0  (0) × FSQELNTH            × FSQENEXT            ×         *
*               +---------------------+---------------------+         *
*                                                                     *
*                                                                     *
***********************************************************************
         SPACE 2
.FSQEA   ANOP
UADSFSQE DSECT
         SPACE 1
FSQELNTH DS    AL2                     LENGTH OF FREE SPACE AREA
FSQENEXT DS    AL2                     OFFSET TO NEXT FSQE
UADSFSQL EQU   *-UADSFSQE              FREE SPACE QUEUE ELEMENT LENGTH
.DONE    ANOP
         MEND
