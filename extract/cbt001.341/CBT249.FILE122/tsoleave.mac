         MACRO
&NAME    TSOLEAVE &LENGTH=TSOLEN,&ISN=,&RETCODE=0
         GBLC  &R
.*       TSOLEAVE MACRO                  BRUCE M. DANE
.*       LAST UPDATED:                  01 DEC 1972
.*       MACRO GENERATES RE-ENTRANT CODE
.*
&NAME    LR    1,&R.13 .                LOAD RETURN ADDRESS
         L     13,4(0,13) .             LOAD REGISTER POINTER
*        GIVE BACK MY CORE
         FREEMAIN R,A=(1),SP=1,LV=&LENGTH
.RETTEST AIF   ('&RETCODE' EQ '').RETEQ0 * MESSAGE, ASSUME ZERO
         AIF   ('&RETCODE'(1,1) EQ '(').REGTYPE * REGISTER NOT.
         AIF   (T'&RETCODE NE 'N').REGLD1 * RETURN NUMBER?
         AIF   (&RETCODE EQ 0).R0       * RETCODE=0
         AGO   .REGLA                   * LOAD ADDRESS ON RETCODE
.RETEQ0  MNOTE *,'*** WARNING *** RETCODE ASSUMED ZERO (0)'
         AGO   .R0
.REGLD1  L     15,&RETCODE .            LOAD SAVED RETURN CODE
         AGO   .BR14
.REGLA   LA    15,&RETCODE .            LOAD REQUESTED RETURN CODE
         AGO   .BR14
.REGTYPE AIF (('&RETCODE(1)' EQ '15')OR('&RETCODE(1)' EQ 'R15')).BR14
         LR    15,&RETCODE(1) .         LOAD RETURN CODE
         AGO   .BR14
.R0      LM    14,12,12(13) .           RELOAD CALLERS' REGS
         XR    15,15 .                  CLEAR RETURN CODE
         BR    14 .                     RETURN TO CALLER
         AGO   .ISNCHEK
.BR14    LM    0,12,20(13) .            LOAD SAVED REGS
         L     14,12(0,13) .            AND RETURN
         BR    14 .                     RETURN TO CALLER
.ISNCHEK AIF   ('&ISN' EQ '').MEND
         &ISN
.MEND    MEND
