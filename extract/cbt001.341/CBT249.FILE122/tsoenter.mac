         MACRO
&NAME    TSOENTER &FAKE,&BASE=12,&REGS=YES,&PL=,&WORK=0,&DSECT=NO
         GBLC  &R
         LCLA  &A
         LCLB  &OPTS(8),&BIT
         LCLC  &BASEREG
TSODSECT DSECT ,                        DEFINE NAME OF DSECT
TSOSAVE  DS    18F .                    FIRST SAVE AREA
         CPPL  DSECT=NO                 PARAMETER LIST
&A       SETA  1
.A1      AIF   (&A GT N'&PL).L1
&OPTS(1) SETB  ((&OPTS(1))OR('&PL(&A)' EQ 'MESSAGE'))
&OPTS(2) SETB  ((&OPTS(2))OR('&PL(&A)' EQ 'PARSE'))
&OPTS(3) SETB  ((&OPTS(3))OR('&PL(&A)' EQ 'IO'))
&OPTS(4) SETB  ((&OPTS(4))OR('&PL(&A)' EQ 'CSCAN'))
&OPTS(5) SETB  ((&OPTS(5))OR('&PL(&A)' EQ 'DAIR'))
&A       SETA  &A+1
         AGO   .A1
.L1      ANOP
TSOECB   DS    F .                      DEFINE COMMAND ECB
.LL1     AIF   (NOT &OPTS(1)).L2
         MNOTE 0,'MESSAGE PARM LIST UNDEFINED'
.L2      AIF   (NOT &OPTS(2)).L3
         PPL   DSECT=NO                 PARSE PARM LIST
.L3      AIF   (NOT &OPTS(3)).L4
         IOPL  DSECT=NO                 I/O PARAMETER LIST
.L4      AIF   (NOT &OPTS(4)).L5
         CSPL  DSECT=NO                 CMD SCAN PARM LIST
.L5      AIF   (NOT &OPTS(5)).L6
         DAPL  DSECT=NO                 DAIR PARM LIST
.L6      AIF   (('&WORK' EQ '0')OR('&WORK' EQ '')).L7
TSOWORK  DS    0D .                     ALIGN TO DOUBLE-WORD
TSOUSER  EQU   * .                      GIVE ALTERNATE NAME
         DS    (&WORK)X .               INCREMENT BY SIZE
.L7      ANOP
TSOLEN   EQU   *-TSODSECT .             LENGTH OF DSECT
         AIF   ('&REGS' NE 'YES').NOREGS
         REGS
&R       SETC  'R'
.NOREGS  AIF   ('&DSECT' EQ 'NO').GEN
         MEXIT
.GEN     EJECT
&NAME    CSECT
         SAVE  (14,12),T,*
         AIF   (T'&BASE NE 'N').LUSE1
&BASEREG SETC  '&R&BASE'
         AGO   .LUSE2
.LUSE1   ANOP
&BASEREG SETC  '&BASE'
.LUSE2   LUSE  &NAME,&BASEREG .         TELL ASSEMBLER
         LR    &BASEREG,15 .            GET ENTRY POINT
         LR    &R.2,1 .                 SAVE CPPL POINTER
         GETMAIN R,LV=TSOLEN,SP=1
         ST    13,4(,1) .               CHAIN
         ST    1,8(,13) .               SAVE
         LR    &R.13,1 .                AREAS
         LUSE  TSODSECT,&R.13 .         ESTABLISH ADDRESSABILITY
         MVC   CPPL(CPPLLEN),0(&R.2) .  COPY PARM LIST
         AIF   (NOT &OPTS(2)).NPARSE
         MVC   PPLUPT(4),CPPLUPT .      USER PROFILE TAB
         MVC   PPLECT(4),CPPLECT
         LA    &R.0,PPLRANS .              LOAD ANSWER PLACE
         ST    &R.0,PPLANS
         LA    &R.0,TSOECB
         ST    &R.0,PPLECB
&BIT     SETB  1
.NPARSE  AIF   (NOT &OPTS(3)).NIO
         MVC   IOPLECT(4),CPPLECT
         MVC   IOPLUPT(4),CPPLUPT
         AIF   (&BIT).Q1
         LA    &R.0,TSOECB
.Q1      ST    &R.0,IOPLECB
&BIT     SETB  1
.NIO     AIF   (NOT &OPTS(5)).NDAIR
         MVC   DAPLUPT(4),CPPLUPT
         MVC   DAPLECT(4),CPPLECT
         MVC   DAPLPSCB(4),CPPLPSCB
         AIF   (&BIT).Q2
&BIT     SETB  1
         LA    &R.0,TSOECB
.Q2      ST    &R.0,DAPLECB
.NDAIR   AIF   (NOT &OPTS(4)).NCSCAN
         MVC   CSPLUPT(4),CPPLUPT
         MVC   CSPLECT(4),CPPLECT
         AIF   (&BIT).Q3
&BIT     SETB  1
         LA    &R.0,TSOECB
.Q3      ST    &R.0,CSPLECB
         LA    &R.0,CSFLG
         ST    &R.0,CSPLFLG
         LA    &R.0,CSOA
         ST    &R.0,CSPLOA
.NCSCAN  INUSE
.MEND    MEND
