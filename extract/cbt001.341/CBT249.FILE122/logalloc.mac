UVLA     TITLE 'U V L A  ---   TSO LOGALLOC COMMAND'
***********************************************************************
*                                                                     *
*        L O G A L L O C                                              *
*                                                                     *
***********************************************************************
         SPACE 4
***********************************************************************
*                                                                     *
*        ALIASES: LOGALLOC, LA                                        *
*                                                                     *
*        ATTRIBUTES: RENT                                             *
*                                                                     *
*        DESCRIPTION:                                                 *
*                                                                     *
*        THE 'LOGALLOC' COMMAND IS USED IN CONJUNCTION WITH           *
*        THE CMDLOGON PROCEDURE.                                      *
*                                                                     *
*        LOGALLOC MARKS SOME OR ALL DATA SETS WHICH ARE ALLOCATED     *
*        AT THE TIME IT IS INVOKED AS BEING PERMANENTLY ALLOCA-       *
*        TED. THIS MEANS THAT THE 'FREEALL' COMMAND WILL NOT          *
*        FREE THEM, AND ALLOWS A USER TO CONCATENATE HIS OWN          *
*        HELP, CLIST, ETC., DATA SETS TO THE NORMAL ONES, EF-         *
*        FECTIVELY CREATING A CUSTOMIZED LOGON PROCEDURE.             *
*                                                                     *
*        NOTE THAT THE STEPLIB IS NOT PERMITTED TO BE CHANGED         *
*        BY THIS TECHNIQUE, SINCE IT IS OPENED BY THE INTIATOR.       *
*                                                                     *
*        IF A  FILE(FILE-NAME-LIST)                                   *
*           OR DDNAME(FILE-NAME-LIST)                                 *
*        IS ENTERED, ONLY THE DDNAMES IN THE LIST ARE MARKED          *
*        PERMANENTLY ALLOCATED; OTHERWISE, ALL DYNAMICALLY            *
*        ALLOCATED DATA SETS ARE MARKED.                              *
*                                                                     *
***********************************************************************
           EJECT
***********************************************************************
*                                                                     *
*        CONSTANTS, DSECTS, ETC.                                      *
*                                                                     *
***********************************************************************
TSODSECT DSECT
SAVEAREA DS    18F
ECB      DS     F
SAVER4   DS     F
FLAG     DS    X
FMMSG    DS    0CL78           MESSAGE TO USER
FMDDN    DS    CL8             DDNAME
FMB1     DS    CL1
FMDSN    DS    CL44            DSNAME
FMB2     DS    CL1
FMVOL    DS    CL6             VOL SER (IF DASD)
FMB3     DS    CL1
FMTEXT   DS    CL17            ACTION CODE
@MSGS    DC    XL4'00'                  ADDRESS OF DAIRMSGS
PDLSTART DC    XL4'00'                  START OF PDL LIST
LINE     DC    CL40' '                 AREA TO CLEAR
         DS    0D
DAIRWORK DC    XL100'00'
IKJ18PRM EQU   DAIRWORK+X'30'
MSGPARM  EQU   DAIRWORK+X'50'
         DS    0D
LDYNAMIC EQU   *-TSODSECT
         EJECT
LOGALLOC TSOENTER WORK=LDYNAMIC,PL=(PARSE,DAIR)
         EJECT
***********************************************************************
*                                                                     *
*        PARSE THE COMMAND.                                           *
*                                                                     *
***********************************************************************
*                                                                     *
         MVI   TSOECB,0
         MVI   FLAG,0                    CLEAR FLAG
         L     R15,CPPLECT
         XR    R4,R4                    CLEAR PARM REG
         TM    28(R15),X'80'            ANY OPERANDS?
         BNO   PARSER                   YES
         XC    PPLRANS(4),PPLRANS       CLEAR SO IKJRLSA DON'T DIE
         B     FREEALL                  GO
PARSER   TSOPARSE PCL==V(PCL),CBUF=CPPLCBUF
         LTR   R15,R15                  TEST RETURN CODE
         L     R4,PPLRANS               GET ANSWER PLACE
         BZ    PARSEOK                  OK
         LR    R2,R15                   GET IT
         TPT   BADPARSE
         B     LEAVE1                   DO IT
         SPACE 4
***********************************************************************
*                                                                     *
*        CHECK FOR A FILE-NAME LIST.                                  *
*                                                                     *
***********************************************************************
*                                                                     *
PARSEOK  CLI   FILE-PDL+1(R4),0         SPECIFIED?
         BE    PARSE0                   NO
         LA    R4,DDNAMES-PDL(,R4)      GET PLACE
         TM    6(R4),X'80'              EXIST?
         BO    FREEALL                  YES
PARSE0   XR    R4,R4                    ZERO IT
         B     FREEALL
         EJECT
***********************************************************************
*                                                                     *
*        CLEANUP AND EXIT.                                            *
*                                                                     *
***********************************************************************
*                                                                     *
LEAVE    LR    R2,R15                   SAVE RC
LEAVE1   IKJRLSA PPLRANS                FREE UP CORE
         TSOLEAVE RETCODE=(R2)          DIE
LEAVE0   XR    R15,R15                  CLEAR RC
         B     LEAVE                    END PROGRAM
         EJECT
***********************************************************************
*                                                                     *
*        FIND THE DSAB CHAIN                                          *
*                                                                     *
***********************************************************************
*                                                                     *
FREEALL  ST    R4,SAVER4
         L     R2,CVTPTR      CVT ADDR
         USING CVTMAP,R2
         L     R2,CVTTCBP     TCB-TCB ADDR   (TCBHEAD)
         L     R2,4(,R2)      TCB ADDR       (CURRENT TCB)
         USING TCB,R2
         L     R2,TCBJSCB     JOB STEP CONTROL BLOCK ADDR
         USING IEZJSCB,R2
         L     R2,JSCDSABQ    LOAD ADDRESS OF QDB FOR DSAB CHAIN
         CLC   8(4,R2),=F'0'  ZERO DATA SETS FOR THIS USER
         BE    LEAVE          IF HE DONT HAVE ANY WE CANT PERM EM
         L     R2,12(R2)      LOAD ADDRESS OF FIRST DSAB
         USING DSAB,R2
         EJECT
***********************************************************************
*                                                                     *
*        LOOP THROUGH THE DSABS. IF DYNAMICALLY ALLOCATED,            *
*             MARK THEM PERMANENTLY ALLOCATED.                        *
*                                                                     *
*        IF A FILE-NAME LIST WAS SPECIFIED, ONLY MARK THE             *
*             ONES IN THE LIST.                                       *
*                                                                     *
***********************************************************************
*                                                                     *
INLOOP   EQU   *
         TM    DSABFLG1,DSABDALC   DATA SET DYNAMICALLY ALLOCATED
         BZ    NOPERM         NO, GO LOOK AT NEXT ONE
         ICM   R4,B'1111',SAVER4
         BZ    PERM           NO PARM, MARK THEM ALL
         L     R15,DSABTIOT   POINT TO TIOT
         USING TIOELNGH,R15
         CLI   TIOEDDNM,C' '  PART OF A CONCAT GROUP?
         BNE   RESETFLG       NO, CHECK THE LIST
         TM    FLAG,X'80'     IS THE CONCAT GROUP FLAG ON?
         BO    PERM           YES, GO MARK THIS ONE
RESETFLG NI    FLAG,X'FF'-X'80' TURN OFF THE FLAG
DDLOOP   L     R14,0(R4)      PARSED DDNAME
         LH    R1,4(R4)       PARSED LENGTH
         BCTR  R1,0           DECREMENT LENGTH
DDCLC    CLC   0(0,R14),TIOEDDNM IS THIS DDNAME REQUESTED ?
         EX    R1,*-6         **EXECUTE**
         BE    PERM           YES, GO MARK IT PERM
         CLI   8(R4),X'FF'    END OF LIST?
         BE    NOPERM         YES, GET NEXT DSAB
         L     R4,8(R4)       NO, GET NEXT PARSE PDE
         B     DDLOOP
         DROP  R15
PERM     BAL   R8,PERMDS      GO PERM THIS DDNAME
         OI    FLAG,X'80'     TURN ON FLAG FOR CONCAT GROUP
NOPERM   L     R2,DSABFCHN    LOAD POINTER TO NEXT DSAB ENTRY
         LTR   R2,R2          END OF CHAIN ?
         BNE   INLOOP         NO, KEEP LOOKING
         B     LEAVE0
         EJECT
***********************************************************************
*                                                                     *
*        PERMDS - MARKS THE DATA SET PERMANENTLY ALLOCATED            *
*                 (SUBROUTINE)                                        *
*                                                                     *
***********************************************************************
         SPACE 1
         PRINT NOGEN
PERMDS   EQU   *
         STAX  DEFER=YES                NO ATTN WHILE WE ARE AUTHORIZED
*        AUTH  ON (IF NEEDED UNDER SPF)
         MODESET KEY=ZERO
         NI    DSABFLG1,X'FF'-DSABDALC  INDICATE NO DYNAMICALLY ALLOCED
         MODESET KEY=NZERO
*        AUTH  OFF
         STAX  DEFER=NO
         BR    R8                       RETURN TO CALLER
BADPARSE DC    C' UNABLE TO PARSE, LOGALLOC NO EXECUTED'
         EJECT
***********************************************************************
*                                                                     *
*        CONSTANTS, DSECTS, ETC.
*                                                                     *
***********************************************************************
         LTORG *
         CVT   DSECT=YES
         IKJTCB
         IEZJSCB
DSAB     IHADSAB
TIOT     DSECT
         IEFTIOT1
LOGALLOC CSECT
         SPACE 4
PCL      IKJPARM DSECT=PDL
FILE     IKJKEYWD
         IKJNAME 'FILE',SUBFLD=DDNAME$
         IKJNAME 'DDNAME',SUBFLD=DDNAME$
DDNAME$  IKJSUBF
DDNAMES  IKJIDENT 'DDNAME(S) TO BE MARKED PERMALLOC',                  +
               LIST,                                                   +
               MAXLNTH=8,                                              +
               PROMPT='FILE NAME(S) TO MARK PERMALLOC',                +
               HELP='DDNAMES WHICH SHOULD BE PERMANENTLY ALLOCATED',   +
               OTHER=ALPHANUM
         IKJENDP
         END
