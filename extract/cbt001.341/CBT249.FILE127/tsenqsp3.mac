TSENQSP3 TITLE 'LIST DATA FROM GRS BY RNAME JOBNAME QNAME'
*********************************************************************
* ALIAS  $DENQ                                                      *
* FUNCTION - DISPLAY 1) USERS                                       *
*                    2) DISPOSITION                                 *
*                    3) WAITING FOR DATASETS                        *
*                    4) SYSTEM                                      *
*                    5) MUST COMPLETE FLAG                          *
*                    6) RESERVE FLAG                                *
*                    7) GLOBAL FLAG                                 *
*                    8) OWNERSHIP FLAG                              *
*                    FOR THE SPECIFIED DATASET.                     *
*                                                                   *
* COMMAND SYNTAX - $DENQ DSNAME JOBNAME QNAME                       *
* DEFAULTS NO OPERANDS: LOOK FOR ENQS  (SEE LIST OF QNAMES)         *
*          IF DSNAME = "RES" THEN SHOW ALL RESERVES (" ")           *
*          QNAME DEFAULTS TO SYSDSN IF DSNAME SPECIFIED             *
*          QNAME DEFAULTS TO QNAME LIST IF LOOKING FOR ENQS/RES     *
*                                                                   *
* 1) DO NOT USE QUOTES AROUND THE DATASET NAME                      *
* 2) WORKS FOR A FULL OR PARTIAL QUALIFIED DATASET NAME             *
*                                                                   *
* NOTE - THIS CODE HAS NOT BEEN TESTED WITH A GRS RING.             *
*        SCOPE=GLOBAL WILL HAVE TO BE SPECIFIED ON THE GQSCAN MACRO *
*        AND THE GQSCAN HAS TO RUN AUTHORIZED KEY ZERO.             *
*                                                                   *
* NOTE - THE 'SYSNAME' IN IEASYS00 SHOULD SPECIFY YOUR SMFID TO     *
*        GET THE CORRECT SYSTEM NAME IN THE 'SYSTEM' FIELD,         *
*        OTHERWISE (NONA) NONAME WILL BE DISPLAYED.                 *
*                                                                   *
*********************************************************************
*
TSENQSP3 @ENT  R12,(0,DSALEN,C),DSA=YES CSECT @ENT BASE,(SP,L',CLEAR)
         LR    R3,R1                   LOAD CPPL PTR
         USING CPPL,R3
*                                                              15OCT84
*------- ----- INITIALIZE CONSTANTS                            15OCT84
*                                                              15OCT84
         MVI   JOBNAME,C' '            CLEAR AREA              15OCT84
         MVC   JOBNAME+1(JOBLINEL-1),JOBNAME                   15OCT84
         MVI   ULINE,C'-'              DASHES                  15OCT84
         MVC   ULINE+1(L'ULINE-1),ULINE                        15OCT84
         MVC   SCAND(SCANDLEN),SCANC                           15OCT84
*------- ----- SET SYSTEM ID                                   15OCT84
         L     R9,16                   CVTPTR
         L     R4,196(R9)              CVTSMCA
         MVC   TITLE,=CL32'SHARE - DATASET USAGE DISPLAY -'
         MVC   TITLVER,=C' - V1.1'     RELEASE #
         MVC   TITLF1,=C' - '          FILLER                  15OCT84
         MVC   TITLSID,SMCASID-SMCABASE(R4)   SYSTEM ID
*
         L     R14,CPPLCBUF            GET COMMAND BUFFER
         LH    R15,2(R14)              GET OPERAND OFFSET
*
         LA    R15,4(R15,R14)          POINT TO OPERAND
         MVC   REQQNM,QNAMES           DEFAULT QNAME           15OCT84
         LA    R1,QNAMES               CURRENT QNAME           15OCT84
         ST    R1,REQQNMC              "                       15OCT84
         MVC   REQJOB,BLANKS           OUTPUT AREA             15OCT84
         MVC   REQJOBL,=H'1'           DEFAULT=ANY JOB         15OCT84
         MVI   REQJOB,C'*'                                     15OCT84
         MVC   REQDSN,BLANKS           OUTPUT AREA             15OCT84
         MVC   REQDSNL,=H'1'           DEFAULT=ANY DSN         15OCT84
         MVI   REQDSN,C'*'                                     15OCT84
         L     R1,CPPLECT              GET ECT
         TM    ECTSWS-ECT(R1),ECTNOPD  NO OPERANDS?            15OCT84
         BNO   GETPARMS                NO-SCAN                 15OCT84
         MVI   DSAENQWT,C'Y'           ONLY LOOK FOR ENQS      15OCT84
         B     GETEND                  SKIP OPR SCAN           15OCT84
GETPARMS DS    0H                                              15OCT84
         CLI   0(R15),C' '             ANY OPERANDS            15OCT84
         BE    INVPARM                 NO-ERROR                15OCT84
         LR    R0,R15                  REMEMBER                15OCT84
         AH    R14,0(,R14)             POINT TO END OF BUFFER
         LA    R1,REQDSN               OUTPUT AREA             15OCT84
GETDSN   DS    0H                                              15OCT84
         CR    R15,R14                 END OF OF 1ST OP        15OCT84
         BNL   GETDSNXX                OUT OF PARMS            15OCT84
         MVC   0(1,R1),0(R15)          MOVE 1 BYTE OF DSN      15OCT84
         LA    R1,1(R1)                                        15OCT84
         LA    R15,1(R15)                                      15OCT84
         CLI   0(R15),C' '             END OF DSN              15OCT84
         BE    GETDSNXX                YES-DO CALCS            15OCT84
         CLI   0(R1),C' '              DONT OVERRUN OUTPUT     15OCT84
         BE    GETDSN                  SPACE AVAIL-CONTINUE    15OCT84
         B     INVPARM                 DSNAME OVER 44 CHAR     15OCT84
GETDSNXX DS    0H                                              15OCT84
         LR    R1,R15                  L'DSN                   15OCT84
         SR    R1,R0                                           15OCT84
         STH   R1,REQDSNL                                      15OCT84
*                                                              15OCT84
*------- GET JOBNAME SEARCH IF ANY                             15OCT84
*                                                              15OCT84
         LA    R1,REQJOB               START OF OUTPUT         15OCT84
GETSKP1  DS    0H                                              15OCT84
         LR    R0,R15                  SHOW ZERO L' IF EXIT    15OCT84
         CR    R15,R14                 END OF ALL PARMS        15OCT84
         BNL   GETJOBXX                YES-END                 15OCT84
         CLI   0(R15),C' '             JOBNAME?                15OCT84
         BNE   GETJOB                  YES-DOIT                15OCT84
         LA    R15,1(R15)                                      15OCT84
         B     GETSKP1                 FIND START OF JOBNAME   15OCT84
GETJOB   DS    0H                                              15OCT84
         CR    R15,R14                 END OF 2ND PARM?        15OCT84
         BNL   GETJOBXX                                        15OCT84
         MVC   0(1,R1),0(R15)          MOVE 1 BYTE OF JOBNAME  15OCT84
         LA    R1,1(,R1)                                       15OCT84
         LA    R15,1(,R15)                                     15OCT84
         CLI   0(R15),C' '             END OF JOBNAME          15OCT84
         BE    GETJOBXX                                        15OCT84
         CLI   0(R1),C' '              AVAIL BYTE?             15OCT84
         BE    GETJOB                  YES-CONTINUE            15OCT84
         B     INVPARM                 JOBNAME OVER 8 CHAR     15OCT84
GETJOBXX DS    0H                                              15OCT84
         LR    R1,R15                  L'JOBNAME               15OCT84
         SR    R1,R0                                           15OCT84
         STH   R1,REQJOBL                                      15OCT84
*                                                              15OCT84
*------- GET QNAME OVERRIDE IF ANY                             15OCT84
*                                                              15OCT84
GETSKP2  DS    0H                                              15OCT84
         LR    R0,R15                  SHOW ZERO L' IF EXIT    15OCT84
         CR    R15,R14                 END OF ALL PARMS        15OCT84
         BNL   GETQNMXX                YES-END                 15OCT84
         CLI   0(R15),C' '             QNAME?                  15OCT84
         BNE   GETQNMS                 YES-DOIT                15OCT84
         LA    R15,1(R15)                                      15OCT84
         B     GETSKP2                 FIND START OF JOBNAME   15OCT84
GETQNMS  DS    0H                                              15OCT84
         LA    R1,REQQNM               START OF OUTPUT         15OCT84
         MVC   REQQNM,=CL8'*'          KILL SYSDSN DEF.        15OCT84
GETQNM   DS    0H                                              15OCT84
         CR    R15,R14                 END OF 3RD PARM?        15OCT84
         BNL   GETQNMXX                                        15OCT84
         MVC   0(1,R1),0(R15)          MOVE 1 BYTE OF QNAME    15OCT84
         LA    R1,1(,R1)                                       15OCT84
         LA    R15,1(,R15)                                     15OCT84
         CLI   0(R15),C' '             MORE                    15OCT84
         BE    GETQNMXX                NO-DO CALCS             15OCT84
         CLI   0(R1),C' '              AVAIL BYTE?             15OCT84
         BE    GETQNM                  YES-CONTINUE            15OCT84
         B     GETQNMXX                ?$?TEMP-STOP AT 8       15OCT84
         B     INVPARM                 QNAME OVER 8 CHAR       15OCT84
GETQNMXX DS    0H                                              15OCT84
         LR    R1,R15                  L'QNAME                 15OCT84
         SR    R1,R0                                           15OCT84
         STH   R1,REQQNML                                      15OCT84
GETFINI  DS    0H                                              15OCT84
         OC    REQQNM,BLANKS           UPPER CASE              15OCT84
         OC    REQJOB,BLANKS                                   15OCT84
         OC    REQDSN,BLANKS                                   15OCT84
*                                                              15OCT84
         CLC   =C'ENQ ',REQDSN         SPECIAL DSN             15OCT84
         BNE   NODSNSP1                NO                      15OCT84
         MVI   DSAENQWT,C'Y'           YES-FIXUP DATA          15OCT84
         MVC   REQDSN(3),=C'*  '       PUT BACK DEFAULT        15OCT84
         MVC   REQDSNL,=H'1'           DEFAULT=ANY DSN         15OCT84
NODSNSP1 DS    0H                                              15OCT84
*                                                              15OCT84
         CLC   =C'RES ',REQDSN         SPECIAL DSN             15OCT84
         BNE   NODSNSP2                NO                      15OCT84
         MVI   DSARESSW,C'Y'           YES-FIXUP DATA          15OCT84
         MVC   REQDSN(3),=C'*  '       PUT BACK DEFAULT        15OCT84
         MVC   REQDSNL,=H'1'           DEFAULT=ANY DSN         15OCT84
NODSNSP2 DS    0H                                              15OCT84
GETEND   DS    0H                                              15OCT84
*                                                              15OCT84
*------- FORMAT TIME                                           15OCT84
*                                                              15OCT84
         STCK  STCKSTCK
         @STCK CODE                    FORMAT TOD CLOCK        15OCT84
         MVC   TITLTOD,STCK                                    15OCT84
         TPUT  CLEAR,L'CLEAR,FULLSCR   CLEAR SCREEN
         TPUT  TITLE,TITLEL            ADR PRINT TITLE AND TIME
         TPUT  BLANKS,L'BLANKS         PRINT BLANK
*                                                              15OCT84
*------- ----- DO  G Q S C A N                                 15OCT84
*                                                              15OCT84
RESCAN   DS    0H                      LOOP THRU SELECTED QNMS 15OCT84
         LA    R7,REQDSN               ADR OF DSN MINOR NAME
         LA    R8,SCANAREA             ADR OF RETURNED DATA
         L     R10,=A(SCANLEN)         L'  OF RETURNED DATA
         LA    R9,REQQNM               ADR OF QNAME MAJOR NAME
SCAN     GQSCAN AREA=((R8),(R10)),     ADR AND LENGTH OF INFO  RETURNEDX
               REQLIM=MAX,             MAX NUM OF OWNERS TO BE RETURNEDX
               SCOPE=ALL,              ALL SYSTEMS IN GRS RING         X
               RESNAME=((R9)),                                 15OCT84 X
               MF=(E,SCAND)                                    15OCT84
*------- ----- ----------------------- SYSDSN/DSN/L'DSN        15OCT84
*------- ----- IF YOU ARE AT THE NEW GRS (VSAM SUPPORT)        15OCT84
*------- ----- TRY THE FOLLOWING:                              15OCT84
*?$?           RESNAME=((R9),(R7),(R4),GENERIC)                15OCT84
*------- ----- -----------------------                         15OCT84
         B     BRTABLE(R15)            BRANCH TO APPROPRIATE ROUTINE
BRTABLE  B     CODE00                  ALLS OK
         B     CODE04                  NO RESOURCES MATCHED REQUEST
         B     CODE08                  GQSCAN AREA TOO SMALL
         B     CODE0C                  ENCOUNTERED ERROR DATA AREA NG
         B     CODE10                  INVALID SYSNAME
***
CODE04   DS    0H                      DSNAME NOT FOUND
         CLI   DSAENQWT,C'Y'           EXCEPTIONS ONLY         15OCT84
         BE    CKRESCAN                YES-TRY MORE QNAMES     15OCT84
         CLI   DSARESSW,C'Y'           RESERVES ONLY           15OCT84
         BE    CKRESCAN                YES-TRY MORE QNAMES     15OCT84
         CLC   =C'$ ',REQDSN           SPECIAL DEBUG DSN       15OCT84
         BE    CKRESCAN                YES-TRY MORE QNAMES     15OCT84
         TPUT  CODE04M,L'CODE04M
         B     RETURN
CODE08   TPUT  CODE08M,L'CODE08M       GQSCAN AREA TOO SMALL
         B     RETURN
CODE0C   TPUT  CODE0CM,L'CODE0CM       ERROR IN GQSCAN
         B     RETURN
CODE10   TPUT  CODE10M,L'CODE10M       INVALID SYSNAME
         B     RETURN
INVPARM  TPUT  PMSG,L'PMSG             INVALID DSN SPECIFIED
         B     RETURN
*                                                              15OCT84
*------- GQSCAN ANSWER: R0=AL2(RIBLNGTH,RIBELENG)              15OCT84
*                       R1=AL4(#RIBS RETURNED)                 15OCT84
*                                                              15OCT84
CODE00   DS    0H                                              15OCT84
         LA    R10,SCANAREA            ADR OF GQSCAN AREA
         ST    R0,RIBLNGTH             STORE RIB AND RIBE LENGTH
         ST    R1,RIBNUMB              NUMBER OF RIBS RETURNED 15OCT84
RIBLOOP  DS    0H                      LOOP POINT FOR GENERIC  15OCT84
         USING RIB,R10                 RESOURCE INFORMATION BLOCK
         LR    R11,R10                 SAVE RIB POINTER
         AH    R11,RIBLNGTH            POINT TO RIB VARIABLE SECTION
         AH    R11,RIBVLEN             POINT TO FIRST RIBE
         USING RIBE,R11                RESOURCE INFORMATION BLOCK EXT
         L     R9,RIBNRIBE             LOAD THE # OF RIBE'S
         CLI   DSAENQWT,C'Y'           ONLY ENQ LOCKOUTS?      15OCT84
         BNE   ENQEND                  NO-NORMAL PATH          15OCT84
         L     R0,RIBNTWE              GET # WAITING EXC       15OCT84
         A     R0,RIBNTWS              ADD # WAITING SHR       15OCT84
         BZ    BUMPRIB                 ZERO-NO WAITS-SKIP      15OCT84
ENQEND   DS    0H                                              15OCT84
*                                                              15OCT84
*------- SEE IF DSNAME MATCH                                   15OCT84
*                                                              15OCT84
         CLI   REQDSN,C'*'             ANY?                    15OCT84
         BE    MOVEDSN                                         15OCT84
         CLC   =C'$ ',REQDSN           SPECIAL DEBUG -ALLDSNS  15OCT84
         BE    MOVEDSN                                         15OCT84
         LA    R0,REQDSN               GET DSN (OR PARTIAL)    15OCT84
         LH    R1,REQDSNL                                      15OCT84
         LR    R14,R10                 CALC ADR OF RIBRNAME    15OCT84
         AH    R14,RIBLNGTH            GET TO VARIABLE SECT.   15OCT84
         LA    R14,RIBRNAME-RIBVAR(R14) SENDING AREA           15OCT84
         LR    R15,R1                  USE CALLER L'           15OCT84
         CLCL  R0,R14                  SEE IF HIT              15OCT84
         BNE   BUMPRIB                 NO-TRY NEXT SYSDSN      15OCT84
*                                                              15OCT84
*------- MOVE RNAME (DSNAME)                                   15OCT84
*                                                              15OCT84
MOVEDSN  DS    0H                                              15OCT84
         LA    R0,DSN                  MOVE DSNAME             15OCT84
         LA    R1,L'DSN                                        15OCT84
         LR    R14,R10                 CALC ADR OF RIBRNAME    15OCT84
         AH    R14,RIBLNGTH            GET TO VARIABLE SECT.   15OCT84
         LA    R14,RIBRNAME-RIBVAR(R14) SENDING AREA           15OCT84
         XR    R15,R15                                         15OCT84
         IC    R15,RIBRNMLN                                    15OCT84
         ICM   R15,8,=C' '             FILL CHAR               15OCT84
         MVCL  R0,R14                  SHOW DSNAME FOUND       15OCT84
         OC    DSN,BLANKS              MAKE SURE PRINTABLE     15OCT84
*                                                              15OCT84
*------- SHOW USERS OF THIS RNAME (SYSDSN)                     15OCT84
*                                                              15OCT84
SEARCH   DS    0H                                              15OCT84
         CLI   REQJOB,C'*'             ONLY SOME JOBS          15OCT84
         BE    SHOWDSN                 NO-FORMAT LINE&DISPLAY  15OCT84
         LA    R0,REQJOB               GET DSN (OR PARTIAL)    15OCT84
         LH    R1,REQJOBL                                      15OCT84
         LA    R14,RIBEJBNM            JOBNAME                 15OCT84
         LR    R15,R1                  USE CALLER L'           15OCT84
         CLCL  R0,R14                  SEE IF HIT              15OCT84
         BNE   BUMPRIBE                  N0-SKIP EM            15OCT84
SHOWDSN  DS    0H                      FORMAT LINE DISPLAY     15OCT84
         MVC   JOBNAME(8),RIBEJBNM     JOBNAME OF REQUESTOR
         MVC   SYSTEM,RIBESYSN         SYSTEM NAME OF REQUESTOR
         CLC   =C'NONAME',SYSTEM       DEFAULT                 15OCT84
         BNE   SKIPSYSN                NO-USE SPECIFIED DATA   15OCT84
         MVC   SYSTEM,REQQNM           DEFAULT-SHOW QNAME      15OCT84
SKIPSYSN DS    0H                                              15OCT84
*
         MVC   EXCLFLAG,=C'SHR'
         TM    RIBERFLG,RIBETYPE       IS THIS EXCL OR SHARED?
         BO    SHARED
         MVC   EXCLFLAG,=C'EXC'
SHARED   DS    0H
*
         MVC   MCFLAG,=C'VIP!'
         TM    RIBERFLG,RIBEMC         IS MUST COMPLETE?
         BO    MUSTC
         MVC   MCFLAG,=C'    '
MUSTC    DS    0H
*
         MVC   RESVFLAG,=C'RESV'
         TM    RIBERFLG,RIBERESV       IS THIS RESERVE REQUEST?
         BO    RESERVE                 Y
         MVC   RESVFLAG,=C'NRES'
         CLI   DSARESSW,C'Y'           ONLY SELECT RESERVES?   15OCT84
         BE    BUMPRIBE                YES-SKIP THIS NRES ITEM 15OCT84
RESERVE  DS    0H
*
         MVC   GLOBFLAG,=C'GLB'
         TM    RIBERFLG,RIBERESC       IS THIS RESERVE REQUEST GLOBAL?
         BO    GLOBAL
         MVC   GLOBFLAG,=C'LCL'
GLOBAL   DS    0H
*
         MVC   OWNFLAG,=C'OWN'
         TM    RIBESFLG,RIBESTAT       IS THIS GUY THE OWNER?
         BO    OWNER
         MVC   OWNFLAG,=C'WTE'
OWNER    DS    0H
*
TPUTIT   DS    0H                      PRINT IT
         TPUT  JOBLINE,JOBLINEL                                15OCT84
         MVI   DSAHIT,C'Y'             SHOW AT LEAST 1 HIT     15OCT84
***
BUMPRIBE AH    R11,RIBELENG            POINT TO NEXT RIBE
         BCT   R9,SEARCH               AND DO IT FOR NEXT USER
BUMPRIB  DS    0H                                              15OCT84
*                                                              15OCT84
*------- SKIP  TO NEXT RIB SECTION                             15OCT84
*                                                              15OCT84
         L     R14,RIBNRIBE            # OF RIBE IN THIS RIB   15OCT84
         MH    R14,RIBELENG            * L' = L'ALL RIBES      15OCT84
         AH    R14,RIBLNGTH            + L'RIB                 15OCT84
         AH    R14,RIBVLEN             + L'RIB VAR.            15OCT84
         AR    R10,R14                 NEXT RIB+VRIB+RIBE(S)   15OCT84
         L     R14,RIBNUMB             DECREMENT TOTALS        15OCT84
         SH    R14,=H'1'                                       15OCT84
         ST    R14,RIBNUMB             DECREMENT TOTALS        15OCT84
         BNZ   RIBLOOP                 NEXT RIB                15OCT84
RIBFINI  DS    0H                                              15OCT84
*                                                              15OCT84
*------- SEE IF ANY HITS                                       15OCT84
*                                                              15OCT84
CKRESCAN DS    0H                                              15OCT84
         CLC   =C'$ ',REQDSN           SPECIAL DEBUG DSNAME    15OCT84
         BE    FORCSCAN                                        15OCT84
         CLI   DSAENQWT,C'Y'           LOOKING FOR ENQS?       15OCT84
         BE    FORCSCAN                YES-RESCAN              15OCT84
         CLI   DSARESSW,C'Y'           LOOKING FOR RESERVES    15OCT84
         BE    FORCSCAN                YES-RESCAN              15OCT84
         B     NORESCAN                NO                      15OCT84
*                                                              15OCT84
*------- SCAN  NEXT QNAME IN MY TABLE                          15OCT84
*                                                              15OCT84
FORCSCAN DS    0H                                              15OCT84
         L     R1,REQQNMC              GET CURRENT             15OCT84
         LA    R1,8(R1)                NEXT QNAME              15OCT84
         CLI   0(R1),C'9'              9+=END                  15OCT84
         BE    NORESCAN                YES-END                 15OCT84
         ST    R1,REQQNMC              SAVE NEW CURRENT        15OCT84
         MVC   REQQNM,0(R1)            COPY "                  15OCT84
         B     RESCAN                                          15OCT84
NORESCAN DS    0H                                              15OCT84
         CLI   DSAHIT,C'Y'             FILLED IN?              15OCT84
         BE    RETURN                  OK                      15OCT84
         TPUT  NFOUND,L'NFOUND         NEXT RIB                15OCT84
RETURN   @RET  EQ,RC=0
*                                                              15OCT84
******** ***** S T A T I C  S T O R A G E (READ-ONLY)          15OCT84
*                                                              15OCT84
MVPARM   MVC   REQDSN(*-*),0(R5)
*
CODE04M  DC    CL50'NOT IN USE OR NOT A FULLY QUALIFIED DATA SET NAME'
CODE08M  DC    CL50'GQSCAN AREA TOO SMALL - RETRY COMMAND'
CODE0CM  DC    CL50'ERROR ENCOUNTERED - RETRY COMMAND'
CODE10M  DC    CL50'INVALID SYSNAME - RETRY COMMAND'
PMSG     DC    CL50'INVALID PARAMETER SPECIFIED'
NFOUND   DC    CL50'NO MATCHES FOR REQUEST FOUND'
*
CLEAR    DC    X'C1115D7E1140403C4040001DC813'  CLEAR SCREEN
BLANKS   DC    CL72' '
QNAMES   DC    CL8'SYSDSN'                                     15OCT84
         DC    CL8'SPFEDIT '                                   10JAN85
         DC    CL8'SPFDSN  '                                   10JAN85
         DC    CL8'SYSIEA01'                                   15OCT84
         DC    CL8'SYSIEFSD'                                   15OCT84
         DC    CL8'SYSIEWLP'                                   15OCT84
         DC    CL8'SYSIGGV1'                                   15OCT84
         DC    CL8'SYSIGGV2'                                   15OCT84
         DC    CL8'SYSIKJBC'                                   15OCT84
         DC    CL8'SYSIKJUA'                                   15OCT84
         DC    CL8'SYSSMF01'                                   15OCT84
         DC    CL8'SYSVSAM'                                    15OCT84
         DC    CL8'SYSVTOC'                                    15OCT84
         DC    CL8'SYSZCAXW'                                   15OCT84
         DC    CL8'SYSZCOMM'                                   15OCT84
         DC    CL8'SYSZJES2'                                   15OCT84
         DC    CL8'SYSZJWTP'                                   15OCT84
         DC    CL8'SYSZOPEN'                                   15OCT84
         DC    CL8'SYSZPCCB'                                   15OCT84
         DC    CL8'SYSZRPLW'                                   15OCT84
         DC    CL8'SYSZSIPS'                                   15OCT84
         DC    CL8'SYSZSMF1'                                   15OCT84
         DC    CL8'SYSZTIOT'                                   15OCT84
         DC    CL8'SYSZVARY'                                   15OCT84
         DC    CL8'SYSZVOLS'                                   15OCT84
         DC    CL8'SYSZWTOR'                                   15OCT84
         DC    CL8'99999999'           SPARES                  15OCT84
         DC    CL8'99999999'                                   15OCT84
         DC    CL8'99999999'                                   15OCT84
         DC    CL8'99999999'                                   15OCT84
         DC    CL8'99999999'                                   15OCT84
         DC    CL8'99999999'                                   15OCT84
*------- ----- ----------------------- -----------------------
SCANC    GQSCAN MF=L                   SKELETON
*------- ----- ----------------------- -----------------------
         LTORG ,                                               15OCT84
         PRINT NOGEN
*                                                              15OCT84
******** ***** D Y N A M I C  S T O R A G E (MODIFIABLE)       15OCT84
*                                                              15OCT84
DSA      DSECT                         CONTINUE DYNAMIC STOR   15OCT84
         DS    0F
RIBLNGTH DC    H'0'                    L' FIXED PORTION OF RIB
RIBELENG DC    H'0'                    L' RIBE
RIBNUMB  DC    F'0'                    # RIBS RETURNED
         DS    0F
REQDSNL  DC    H'0'
REQDSN   DS    CL44' '
REQJOBL  DC    H'0'
REQJOB   DS    CL8' '
REQQNML  DC    H'0'
REQQNM   DS    XL8                     ENQ NAME
REQQNMC  DS    F                       POINTER TO CURR "QNAME"
SCAND    GQSCAN MF=L                   SKELETON
SCANDLEN EQU   *-SCAND                                         15OCT84
*------- ----- ----------------------- -----------------------
TITLE    DC    CL32'SHARE - DATASET USAGE DISPLAY -'
TITLSID  DC    CL04'XSID'              SYSTEM ID
TITLF1   DC    C' - '                  FILLER
TITLTOD  DC    CL28' '                 TIME OF DAY
TITLVER  DC    C' - VX.Y'              RELEASE
TITLEL   EQU   *-TITLE                                         15OCT84
*------- ----- ----------------------- -----------------------
         @STCK DATA                    @STCK WORK DATA         15OCT84
*------- ----- ----------------------- -----------------------
JOBLINE  DS    0CL72
MCFLAG   DS    CL4                     SYSTEM MUST COMP = VIPJ
         DC    C' '
JOBNAME  DS    CL8                     JOBNAME
         DC    C' '
EXCLFLAG DS    CL3                     SHR/EXC
         DC    C' '
OWNFLAG  DS    CL3                     OWM/WTE
         DC    C' '
RESVFLAG DS    CL4                     RES/BLANKS
         DC    C' '
GLOBFLAG DS    CL3                     GBL/LCL
         DC    C' '
SYSTEM   DS    CL8                     SYSTEM NAME
         DC    C' '
DSN      DS    CL44                    DSNAME
JOBLINEL EQU   *-JOBLINE                                       -
*------- ----- ----------------------- -----------------------
ULINE    DS   0CL72
         DC    72C'-'
ULINEL   EQU   *-ULINE
*------- ----- ----------------------- -----------------------
DSAENQWT DS    X'0'                    'Y'=ONLY WTE TASKS
DSARESSW DS    X'0'                    'Y'=ONLY RESERVES
DSAHIT   DS    X'0'                    'Y'=AT LEAST 1 ANSWER
         DS    0F
SCANSAVE DS    18F                     18 WORD SAVEAREA REQ FOR GQSCAN
         DS    0F
SCANAREA DS    400000XL1               SCAN INFO AREA
SCANLEN  EQU   *-SCANAREA              SCAN INFO AREA
DSALEN   EQU   *-DSA                                           15OCT84
         IKJCPPL
         IKJECT
         IEESMCA
         ISGRIB
         END
