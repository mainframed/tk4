         TITLE ' TSCVDAT GIVE DATE/DAY/TIME/LINE CTRS. TO PGMR.'
*              RENT,REUS,REFR
*              ALIAS TSCVDATE
TSCVDAT  @ENT  R12,(0,DSASIZE),DSA=YES
         LR    R2,R1                        SAVE PARM REGS
         ST    R1,GETSAVE
         L     R2,0(R2)
         ST    R2,PARMSAVE
         MVC   DSADATA(DSADLEN),MOVEDATA  MOVE CONSTANTS
         TIME  DEC                     GET THE TIME
         ST    R0,XTIME
         ST    R1,XDATE
*----------------------------------------------------------------------
*        SUBROUTINES TO FORMAT THE DATE AND TIME
*----------------------------------------------------------------------
FTIME    DS    0H
         MVC   XTIME1(8),=X'21204B20204B2020'
         ED    XTIME1-1(9),XTIME       EDIT THE TIME
*
*        FORMAT TODAY'S DATE
*
FDATE    DS    0H
         UNPK  DBLWORK(5),XDATE+1(3)   UNPACK DATE AND MOVE TO
         MVC   XDATE1(2),DBLWORK       THE OUTPUT AREAS
         MVC   XDATE1+3(3),DBLWORK+2
         MVC   XDATE1+21(2),DBLWORK
         XC    DBLWORK,DBLWORK
         MVC   DBLWORK+5(3),XDATE+1
         MVO   DBLWORK+5(3),DBLWORK+4(2) MAKE 00YYDDDS INTO 00000YYS
         CVB   R0,DBLWORK
         ST    R0,BINYEAR
         MVI   MONMSK+10,28
         TM    BINYEAR+3,X'03'    IS IT A LEAP YEAR?
         BNZ   FDATE1             NO.
         MVI   MONMSK+10,29       YES, CORRECT TABLE
FDATE1   DS    0H
         XC    DBLWORK,DBLWORK
         MVC   DBLWORK+6(2),XDATE+2
         CVB   R2,DBLWORK
         ST    R2,BINDAYS         SAVE FOR LATER
         XR    R0,R0
         LA    R1,11
FDATE2   DS    0H
         IC    R0,MONMSK(R1)
         SR    R2,R0
         BNP   FDATE3
         BCT   R1,FDATE2
         B     FDATE4
*
MOVEDATA DC    YL1(31,30,31,30,31,31,30,31,30,31,28,31)
         DC    C'0'        USED FOR EDIT
         DC    C'HH.MM.SS'
         DC    C' '
         DC    C'YY.DDD/WWW/MMM DD, 19YY'
*
DAYTAB   DC    C'TUEWEDTHUFRISATSUNMON'
MONTAB   DC    C'DECNOVOCTSEPAUGJULJUNMAYAPRMARFEBJAN'
LINECTR1 DS    0CL18
         DC    X'072C',X'080C',X'066C',X'056C'    LINE CTRS FOR PRINTER
         DC    10C' '
*
FDATE3   DS    0H
         AR    R2,R0       CORRECT OVERDRAW
FDATE4   DS    0H
         CVD   R2,DBLWORK
         OI    DBLWORK+7,X'F'
         UNPK  XDATE1+15(2),DBLWORK
         MH    R1,=H'3'     CALC. MONTH
         LA    R1,MONTAB(R1)
         MVC   XDATE1+11(3),0(R1)
*
*        DAY OF WEEK
*
         L     R1,BINYEAR
         SH    R1,=H'69'
         BNP   THEEND
         XR    R0,R0
         D     R0,=F'4'
         MH    R1,=H'5'
         AR    R0,R1
         A     R0,BINDAYS
         SRDA  R0,32
         D     R0,=F'7'
         MH    R0,=H'3'
         LR    R1,R0
         LA    R1,DAYTAB(R1)
         MVC   XDATE1+7(3),0(R1)
         MVC   LINECTR2,LINECTR1       MOVE DEFAULT COUNTERS
         L     R2,PARMSAVE
         CLI   8(R2),C'1'           IS IT NEW VERSION FOR LINE CTRS?
         BE    MOVECTRS
         MVC   0(32,R2),XTIME1         MOVE DATE DATA TO PGMR
         B     THEEND
MOVECTRS DS    0H
         MVC   0(50,R2),XTIME1         MOVE DATE/LINE CTRS TO PGMR
THEEND   DS    0H
         @RET  EQ,RC=0
         LTORG
         DS    0D
DSA      DSECT
DSADATA  EQU   *                       START OF COPY
MONMSK   DC    YL1(31,30,31,30,31,31,30,31,30,31,28,31)
         DC    C'0'                    USED FOR EDIT
XTIME1   DC    C'HH.MM.SS'
         DC    C' '
XDATE1   DC    C'YY.DDD/WWW/MMM DD, 19YY'
DSADLEN  EQU   *-DSADATA               END OF COPY
LINECTR2 DS    0CL18
         DC    8C' '                          DUMMY AREA FOR LINE CTRS
         DC    10C' '                          FILLER AREA
* WARNING DON''T PUT ANYTHING BETWEEN HERE AND XTIME1 '
         DC    0D'0'
XDATE    DC    F'0'
XTIME    DC    F'0'
BINYEAR  DC    F'0'
BINDAYS  DC    F'0'
MSAVE    DS    18F'0'
GETSAVE  DS    F'0'
PARMSAVE DS    F'0'
DBLWORK  DS    D'0'
         DS    D'0'
DSASIZE  EQU   *-DSA
         END
