         MACRO
&NAME    SRTNE &RTRN=R14,         REG CONTAINING RETURN ADDR           X
               &TYPE=NORENT       'TYPE' OF EXPANSION TO EXECUTE
         GBLB  &RENT              1 = RE-ENTRANT CODING REQUIRED
         GBLC  &RTRNNME(50)       ARRAY TO STORE RTRN FLD NME IN
         GBLA  &RTRNCNT           CNTR OF USED ENTRIES IN &RTRNNME
         GBLB  &START             1 = SUBRTNE EXIT LOGIC IS NEEDED
         GBLC  &RTRNREG           RTRN REG NAME STORED HERE
         GBLC  &NMES              1ST 5 DIGITS OF SUBRTNE TAG NME
         GBLC  &XITNME            SUBRTNE RTRN TAG NME KEPT HERE
         LCLA  &WORK              TEMP WORK FIELD
         LCLC  &TAG               TEMP WORK
         AIF   ('&TYPE' EQ 'NORENT').BYRENT
         AIF   ('&TYPE' EQ 'DSECT').RTRNFLD
         AIF   ('&TYPE' EQ 'RENT').SETB      HE WANTS SWITCH ON
         MNOTE 1,'TYPE=&TYPE INVALID - TYPE=RENT ASSUMED'
.SETB    ANOP
&RENT    SETB  1                  TURN ON RE-ENTRANT SWITCH
.BYRENT  ANOP
         AIF   (&START).XIT       Q-SUBRTNE INIT? - A.NO-ASSUME TERMIN.
&RTRNCNT SETA  &RTRNCNT+1         INCREMENT ARRAY COUNTER
         AIF   (&RTRNCNT LT 51).CNTOK       Q-WITHIN ARRAY SIZE?
         MNOTE 16,'GLOBAL ARRAY SIZE EXCEEDED - PROCESSING ENDED'
         MEXIT
.CNTOK   ANOP
&START   SETB  1                  FORCE TERMINATION LOGIC NEXT TIME
&RTRNREG SETC  '&RTRN'            SAVE RTRN REG NBR IN GLOBAL
&NMES    SETC  '&NAME'(1,5)       SAVE 1ST 5 DIGITS OF TAG NAME
&RTRNNME(&RTRNCNT) SETC '&NMES'.'RTN'       BUILD RTRN ADDR FLD NAME
&XITNME  SETC  '&NMES'.'XIT'       BUILD RTRN RTNE TAG NAME
&NAME    DS    0H                 FORCE HALFWORD ALIGNMENT
         ST    &RTRNREG,&RTRNNME(&RTRNCNT)   SAVE CALLER'S RTRN ADDR
         SPACE
         AGO   .MEND
.XIT     ANOP
&XITNME  EQU   *                  EXIT POINT FOR THIS SUBROUTINE
         L     &RTRNREG,&RTRNNME(&RTRNCNT)   RESTORE RETURN ADDR REG
         BR    &RTRNREG           RETURN TO CALLER
         SPACE
&START   SETB  0                  NEXT TIME THRU DO INIT. LOGIC
         AIF   (&RENT).MEND       IF RE-ENTRANT, THEN EXIT
&RTRNNME(&RTRNCNT)   DC    F'0'             RETURN ADDRESS SAVE AREA
         AGO   .MEND
.RTRNFLD ANOP
&WORK    SETA  &WORK+1            INCREMENT INDEX COUNTER
&TAG     SETC  '&RTRNNME(&WORK)'  PULL CURR NAME OUT OF TABLE
&TAG     DC    F'0'               SUBRTNE RTRN ADDR SAVE AREA
         AIF   (&WORK GE &RTRNCNT).MEND
         AGO   .RTRNFLD           LOOP TILL ARRAY EXHAUSTED
.MEND    ANOP
         MEND
