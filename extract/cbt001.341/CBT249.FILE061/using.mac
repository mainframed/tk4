         MACRO
         USING
         GBLB  &ANYUSE
         GBLC &USETABR(20),&USETABA(20)
         LCLA  &N,&I,&X
         LCLC  &ADDR
         AIF   (N'&SYSLIST LT 2).ERROR  MUST HAVE 2 OPERANDS
&N       SETA  2                   SECOND OPERAND IS FIRST REG
&ANYUSE  SETB  1                   FLAG FOR INUSE MACRO
&ADDR    SETC  '&SYSLIST(1)'       IN CASE NOT *
         AIF   ('&ADDR' NE '*').NOTSTAR   SEE IF USING *,X
&ADDR    SETC  'IHB'.'&SYSNDX'     BUILD A SYMBOL
&ADDR    EQU   *                   SYMBOL FOR INUSE REFERENCE
.NOTSTAR AIF   ('&SYSLIST(&N)' EQ '').ERROR MUST NOT OMIT ANY OPERANDS
&I       SETA  1                   SET TO SEARCH TABLE
.SEARCH  AIF   ('&SYSLIST(&N)' NE '&USETABR(&I)').NEXTI SEE IF SAME SYM
         DROP  &SYSLIST(&N)            IMPLICIT DROP
.NEXTI   ANOP
&I       SETA  &I+1                SCAN ALL OF TABLE
         AIF   (&I LT 20).SEARCH
&I       SETA  1                   PREPARE UNUSED ENTRY SEARCH
.SEARCH2 AIF   ('&USETABR(&I)' EQ '').GOTONE UNUSED IF NULL
&I       SETA  &I+1
         AIF   (&I LE 20).SEARCH2
         MNOTE 12,'USING TABLE FULL'
         MEXIT
.GOTONE  ANOP
&USETABR(&I) SETC '&SYSLIST(&N)'   PUT REGISTER IN TABLE
         AIF   (&N EQ 2).NOINCR    IF SECOND OERAND, NO INCR NEEDED
&X       SETA  (&N-2)*4096         ADJUSTMENT FOR ADDRESS
&ADDR    SETC  '&ADDR'.'+'.'&X'
.NOINCR  US1NG &ADDR,&SYSLIST(&N)
&USETABA(&I) SETC '&ADDR'
&N       SETA  &N+1 CHECK FOR MORE OPERANDS
         AIF   (&N LE N'&SYSLIST).NOTSTAR
         MEXIT
.ERROR   MNOTE 12,'INVALID USING STATEMENT'
         MEND
