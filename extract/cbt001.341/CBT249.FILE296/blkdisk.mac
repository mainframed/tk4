//*  STEP  ×  COMMENTS
//*  ------×------------------------------------------------------
//*  ASM   × ASSEMBLES BLKDISK (IEV90 OR IFOX00 MAY BE USED)
//*  LINK  × LINKS BLKDISK INTO 'SYS1.CMDLIB(BLKDISK)'
//*        × WITH ALIASES:  BLK23051, BLK23052, BLK2314,
//*        ×                BLK3330,  BLK33301, BLK3340,
//*        ×                BLK3350,  BLK3375,  BLK3380.
//*  COPY  × COPIES BLKDISK HELP TO 'SYS2.HELP(BLKDISK)'
//*        × USING IEBGENER AND SHARED ALLOCATION
//*        × NOTE: SEE HELP INSTRUCTIONS BEFORE  //COPY.SYSUT1
//*  XEQ   × EXECUTES THE ALIASES OF BLKDISK IN THE BACKGROUND
//*
//*
//*   BLKDISK VERSION 2.0:
//*               THE NAME BY WHICH THIS COMMAND PROCESSOR IS
//*               INVOKED DETERMINES THE DEVICE TYPE TO BE USED.
//*
//*               THIS COMMAND PROCESSOR USES PUTLINE OUTPUT --
//*               IT CAN BE USED AS A TSO COMMAND PROCESSOR OR
//*               IN BATCH MODE (SEE STEP XEQ).
//*
//*               THIS COMMAND PROCESSOR WILL USUALLY RECOMMEND A
//*               BLOCKSIZE NEAR A HALF-TRACK FIGURE FOR A GIVEN
//*               DEVICE.  IF THIS VIOLATES LOCAL GUIDELINES OR
//*               STANDARDS, THE PROGRAM (AND THE HELP TEXT) COULD
//*               BE CHANGED TO RECOMMEND THE LARGEST BLOCKSIZE
//*               FOR A LRECL BELOW A CERTAIN FIGURE (FOR EXAMPLE,
//*               6233 BYTES IS A COMMONLY USED MAXIMUM BLOCKSIZE).
//*
//INSTALL PROC MEMBER=BLKDISK
//ASM     EXEC PGM=IEV90,REGION=2048K,
//             PARM=(DECK,NOOBJECT,NORLD,TERM,'XREF(SHORT)')
//SYSLIB   DD  DSN=SYS1.AMODGEN,DISP=SHR
//         DD  DSN=SYS1.MACLIB,DISP=SHR
//SYSUT1   DD  UNIT=SYSDA,SPACE=(CYL,(10,5))
//SYSUT2   DD  UNIT=SYSDA,SPACE=(CYL,(10,5))
//SYSUT3   DD  UNIT=SYSDA,SPACE=(CYL,(10,5))
//SYSPUNCH DD  UNIT=SYSDA,SPACE=(TRK,(5,5)),DISP=(,PASS),DSN=&&X,
//         DCB=BLKSIZE=3120
//SYSPRINT DD  SYSOUT=*
//SYSTERM  DD  SYSOUT=*
//LINK    EXEC PGM=IEWL,PARM='MAP',COND=(5,LT)
//SYSPRINT DD  SYSOUT=*
//SYSLMOD  DD  DISP=SHR,DSN=SYS1.CMDLIB(&MEMBER)    <=== MODIFY
//SYSLIN   DD  DSN=&&X,DISP=(OLD,DELETE)
//SYSUT1   DD  UNIT=SYSDA,SPACE=(TRK,(5,5))
//COPY    EXEC PGM=IEBGENER
//SYSPRINT DD  SYSOUT=*
//SYSUT2   DD  DISP=SHR,DSN=SYS2.HELP(&MEMBER)      <=== MODIFY ?
//SYSIN    DD  DUMMY
//XEQ    EXEC  PGM=IKJEFT01,DYNAMNBR=30
//SYSTSPRT DD  SYSOUT=*
//        PEND
//*
//*
//A       EXEC INSTALL
//ASM.SYSIN DD *
BLKDISK TITLE ' DISK BLOCKSIZE PROGRAM VERSION 2.0   --   BRUCE LELAND'
*  DATA SET BLKDISK AT LEVEL 2.0 AS OF 8/18/82 (ASSEMBLER SOURCE)     *
*  DATA SET BLK3330 AT LEVEL 1.0 AS OF 1/03/78 (PL/I SOURCE)          *
*                                                                     *
*                                                                     *
*                                                                     *
*  PROGRAMMER:  A. BRUCE LELAND                                       *
*                                                                     *
*                                                                     *
*                                                                     *
*  DESCRIPTION:  THIS PROGRAM COMPUTES AN "OPTIMAL" BLOCKSIZE FOR     *
*      A DISK OR DRUM DATA SET GIVEN THE LOGICAL RECORD LENGTH.       *
*      INPUTS INCLUDE THE LRECL AND OPTIONALLY ANY OF THE             *
*      FOLLOWING:                                                     *
*      A.  A KEY LENGTH (ZERO, FOR NO KEY, IS THE DEFAULT)            *
*      B.  THE NUMBER OF RECORDS IN THE DATA SET (USED FOR AN         *
*          ALLOCATION COMPUTATION -- 100,000 IS THE DEFAULT)          *
*      C.  THE BLOCKSIZE TO USE FOR THE ALLOCATION COMPUTATION        *
*          (THE RECOMMENDED BLOCKSIZE VALUE IS THE DEFAULT)           *
*      D.  WHETHER OR NOT TO PROVIDE A TRACK CAPACITY REPORT          *
*      E.  WHETHER OR NOT TO VERIFY RESULTS AGAINST "TRKCALC"         *
*                                                                     *
*                                                                     *
*                                                                     *
*  SUPPORTED DEVICES:  THE NAME BY WHICH THIS COMMAND PROCESSOR       *
*      IS INVOKED DETERMINES THE DEVICE TYPE TO BE USED.              *
*                                                                     *
*      THE FIRST THREE CHARACTERS OF THE COMMAND NAME (USUALLY        *
*      "BLK") ARE IGNORED; THE REMAINING FOUR OR FIVE CHARACTERS      *
*      ARE COMPARED AGAINST A TABLE OF SUPPORTED DEVICES IN THE       *
*      PROGRAM.  THE VALID ALIAS NAMES FOR THE PROGRAM INCLUDE        *
*      THE FOLLOWING:                                                 *
*      A.  BLK23051  (FOR 2305-1 DRUMS)                               *
*      B.  BLK23052  (FOR 2305-2 DRUMS)                               *
*      C.  BLK2314   (FOR 2314 DISKS)                                 *
*      D.  BLK3330   (FOR 3330 DISKS)                                 *
*      E.  BLK33301  (FOR 3330 MODEL 11 DISKS)                        *
*      F.  BLK3340   (FOR 3340 DISKS)                                 *
*      G.  BLK3350   (FOR 3350 DISKS)                                 *
*      H.  BLK3375   (FOR 3375 DISKS) -- THE MVS SP 1.1 TRKCALC       *
*                                        DOES NOT SUPPORT 3375'S.     *
*      I.  BLK3380   (FOR 3380 DISKS)                                 *
*                                                                     *
*                                                                     *
*** *** *** *** *** *** *** *** *** *** *** *** *** *** *** *** *** ***
*                                                                     *
* PLEASE REPORT ANY PROBLEMS, ENHANCEMENTS, SUGGESTIONS OR COMMENTS   *
* CONCERNING THE BLKDISK COMMAND TO:                                  *
*                                                                     *
*     A. BRUCE LELAND           OR         A. BRUCE LELAND            *
*     HITACHI AMERICA, LTD.                6084 CRIMSON DRIVE         *
*     2210 O'TOOLE AVENUE                  SAN JOSE, CALIF 95120      *
*     SAN JOSE, CALIF. 95131                                          *
*     (408) 435-2143                       (408) 997-2366             *
*                                                                     *
*** *** *** *** *** *** *** *** *** *** *** *** *** *** *** *** *** ***
         EJECT
         MACRO
&NAME    MSG   &TEXT
&NAME    LA    R1,&TEXT
         LA    R15,L'&TEXT+4
         BAL   R14,$PUTLINE
         MEND
         SPACE 3
         MACRO
&NAME    EDIT  &REG,&LENGTH,&OUT,&DIGITS,&EQUAL=NO
&NAME    CVD   &REG,DOUBLE
         MVC   &OUT.(&LENGTH),EDITPIC+15-&LENGTH
         ED    &OUT-1(&LENGTH+1),DOUBLE+8-(&DIGITS+1)/2
         AIF   ('&EQUAL' EQ 'NO').MEND
         MVI   &OUT-1,C'='
         SPACE 1
         CLI   &OUT,X'40'              BLANK?
         BNE   *+14                    NO, DONE
         MVC   &OUT.(&LENGTH),&OUT+1   YES, SLIDE CHARACTERS DOWN 1
         B     *-14                    LOOP UNTIL FIRST NON-BLANK
.MEND    MEND
         SPACE 3
         MACRO
&NAME    PCENT &OUT
&NAME    M     R0,=F'1000'
         A     R1,MAXROUND
         D     R0,MAXBLOCK
         CVD   R1,DOUBLE
         MVC   &OUT+0(7),=X'202021204B206C'
         ED    &OUT-1(7),DOUBLE+5
         MEND
         EJECT
BLKDISK  CSECT
         PRINT NOGEN
         SAVE  (14,12),,*
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         LR    R11,R15
         LA    R12,2048
         LA    R12,2048(R11,R12)
         USING BLKDISK,R11,R12
         ST    R13,SAVE+4
         LR    R9,R13
         LA    R13,SAVE
         ST    R13,8(,R9)
         LR    R10,R1
         USING CPPL,R10
         SPACE 2
         L     R1,CPPLECT            ECT ADDRESS
         USING ECT,R1
         SPACE 2
***
***      FIND THE DEVICE TYPE FOR THIS INVOCATION
***
         SPACE 1
         LA    R15,UNITTBL-22
NEXTUNIT LA    R15,22(,R15)
         CLI   0(R15),X'FF'          END OF TABLE?
         BE    NOUNIT                YES, BRANCH
         CLC   0(5,R15),ECTPCMD+3    THIS UNIT NAME?
         BNE   NEXTUNIT              NO, BRANCH
         B     FOUNDUNI              YES, BRANCH
         SPACE 1
NOUNIT   MSG   ERRUNIT               UNIT NOT SUPPORTED
         B     RC12                  TERMINATE
         DROP  R1
         SPACE 1
FOUNDUNI MVC   DEVNAME(22),0(R15)
         EJECT
***
***      INVOKE PARSE
***
         SPACE 1
         LA    R1,MYPPL
         USING PPL,R1
         MVC   PPLUPT(4),CPPLUPT
         MVC   PPLECT(4),CPPLECT
         LA    R0,MYECB
         ST    R0,PPLECB
         L     R0,=A(BLKPCL)
         ST    R0,PPLPCL
         LA    R0,MYANS
         ST    R0,PPLANS
         MVC   PPLCBUF(4),CPPLCBUF
         DROP  R1
         SPACE 2
         L     R15,16              CVTPTR
         L     R15,X'20C'(,R15)    CVTPARS
         BALR  R14,R15             CALL IKJPARS
         LTR   R15,R15
         BNZ   RC12
         SPACE 2
         L     R9,MYANS
         USING IKJPARMD,R9
         EJECT
***
***      PROCESS PARSE RESULTS
***
         SPACE 1
         LH    R15,LRE+4
         S     R15,=F'1'
         L     R1,LRE
PACKNUM  PACK  DOUBLE(8),0(*-*,R1)  <<EXECUTED>>
         EX    R15,PACKNUM
         CVB   R1,DOUBLE
         ST    R1,LRECL
         LTR   R1,R1               ANY BYTES IN THE RECORD?
         BP    NOLRE               YES, BRANCH
         CLI   TRACKKW+1,1         TRACK CAPACITY REPORT REQUESTED?
         BE    NOLRE               YES, BRANCH
         MSG   ERRNULL             NO, ERROR MESSAGE
         B     RC12                    AND QUIT
         SPACE 1
NOLRE    LH    R15,KEY+4
         S     R15,=F'1'
         BM    NOKEY
         L     R1,KEY
         EX    R15,PACKNUM
         CVB   R1,DOUBLE
         ST    R1,KEYLEN
         C     R1,=F'255'          KEY LENGTH<256
         BNH   NOKEY               YES, BRANCH
         MSG   ERRKEY              NO, ERROR
         B     RC12
         SPACE 1
NOKEY    LH    R15,REC+4
         S     R15,=F'1'
         BM    NOREC
         L     R1,REC
         EX    R15,PACKNUM
         CVB   R1,DOUBLE
         ST    R1,RECNUM
         SPACE 1
NOREC    LH    R15,BLK+4
         S     R15,=F'1'
         BM    NOBLK
         L     R1,BLK
         EX    R15,PACKNUM
         CVB   R1,DOUBLE
         ST    R1,USEBLK
         EJECT
***
***      CLEAR THE SCREEN (IF IT IS 24X80 OR 27X132)
***
         SPACE 1
NOBLK    GTSIZE ,
         ST    R0,LINES
         C     R0,=F'24'        24 LINES PER SCREEN?
         BNE   CLEAR2           NO, BRANCH
         C     R1,=F'80'        80 CHARACTERS PER LINE?
         BE    CLEAR4           YES, BRANCH
         SPACE 1
CLEAR2   C     R0,=F'27'        27 LINES PER SCREEN?
         BNE   MAXTRACK         NO, BRANCH
         C     R1,=F'132'       132 CHARACTERS PER LINE?
         BNE   MAXTRACK         NO, BRANCH
         SPACE 2
CLEAR4   LA    R0,3             GET TPUT OPTION BYTE
         SLL   R0,24            SHIFT TO HIGH ORDER
         LA    R1,=X'F1115D7E114040133C404000'
         OR    R1,R0            COPY TPUT OPTION
         LA    R0,12            SET SIZE FOR TPUT
         SVC   93               ISSUE TPUT SVC
         EJECT
***
***      CALCULATE MAXIMUM TRACK CAPACITY FOR THIS KEYLENGTH
***
         SPACE 1
***   NOTE: THE TRACK CAPACITY TABLE IS COMPUTED BY GUESSING AT THE
***         MAXIMUM BLOCKSIZE FOR EACH OF THE 17 BLOCKING FACTORS
***         (IN TURN) AND CALLING DEVCALC UNTIL THE BLOCKSIZE IS
***         DECREMENTED TO THE CORRECT VALUE FOR THE BLOCKING FACTOR.
         SPACE 1
MAXTRACK LA    R3,18                  CALCULATE TRACK CAPACITY
         LA    R6,SAVEMAX-4           MAXIMUM BLOCKSIZE ARRAY -4
MAXLOOP  S     R3,=F'1'               BLOCKS/TRACK -- ZERO REACHED?
         BNP   MAXDONE                YES, BRANCH
         A     R6,=F'4'               NEXT MAX CELL
         LH    R1,DEVOKEY             ASSUME KEYED FILE
         CLI   KEYLEN+3,0             CORRECT?
         BH    *+8                    YES, BRANCH
         SH    R1,DEVNOKEY            NO, LESS KEY OVERHEAD AMOUNT
         A     R1,KEYLEN              PLUS THE KEY LENGTH
         MR    R0,R3                  ACCUMULATED OVERHEAD
         SR    R5,R5
         ICM   R5,3,DEVTRACK          MAXIMUM TRACK SIZE
         SR    R5,R1                  ESTIMATED BYTES LEFT FOR RECORDS
         SR    R4,R4
         DR    R4,R3                  ESTIMATED MAXIMUM BLOCKSIZE
         CLI   VERIFYKW+1,1           VERIFY?
         BNE   MAXTRIAL               NO, BRANCH
         A     R5,=F'1'               YES, USE ONE HIGHER
         SPACE 2
MAXTRIAL LR    R0,R5
         BAL   R14,DEVCALC            CALCULATE TRACK CAPACITY
         L     R1,TRKCALCN
         A     R1,=F'1'               COUNT CALLS TO TRKCALC
         ST    R1,TRKCALCN
         S     R5,=F'1'               ASSUME IT FAILED
         LTR   R15,R15                CORRECT?
         BNZ   MAXTRIAL               YES, BRANCH
         SPACE 1
         CR    R0,R3                  CORRECT BLOCKING FACTOR?
         BL    MAXTRIAL               NO, NOT YET
         BH    CAPERROR               NO, BLOCKED HIGHER THAN ESTIMATED
         A     R5,=F'1'
         ST    R5,0(,R6)              SAVE MAXIMUM BLOCKSIZE
         B     MAXLOOP
         SPACE 3
CAPERROR LR    R2,R0
         MSG   ERRPROG
         ABEND 10
         EJECT
***
***      CALCULATE OPTIMAL BLOCKSIZES USING THE PROVIDED LRECL
***
         SPACE 1
MAXDONE  ST    R5,MAXBLOCK      MAXIMUM BLOCKSIZE
         SRL   R5,1             1/2 MAXBLOCK FOR ROUNDING TO 1/10
         ST    R5,MAXROUND      SAVE FOR LATER
         ICM   R0,15,LRECL      ANY BYTES IN THE RECORD?
         BP    UNBLOCK          YES, BRANCH
         MSG   ERRNULL          NO, ERROR MESSAGE
         B     TRKCHECK
         SPACE 2
UNBLOCK  SR    R1,R1
         ICM   R1,3,DEVTRACK    MAXIMUM TRACK SIZE
         CR    R0,R1            LRECL>DEVICE TRACK CAPACITY?
         BH    TRKLRECL         YES, BRANCH
         C     R0,=F'32760'     LRECL>32760?
         BH    TRKERR           YES, BRANCH
         BAL   R14,DEVCALC      COMPUTE NUMBER PER TRACK
         LTR   R15,R15          DO ANY FIT ON A TRACK?
         BNZ   TRKLRECL         NO, BRANCH
         LR    R2,R0            YES, SAVE THE UNBLOCKED NUMBER
         SPACE 3
         L     R3,=F'1'         BLOCKING FACTOR
         L     R4,LRECL         TRIAL BLOCKSIZE
         LA    R5,17*4          BLOCKS PER TRACK *4
         LA    R6,SAVEMAX-4     BLOCKSIZE RANGE ARRAY START -4
         SPACE 2
NEWBLK   S     R5,=F'4'         DONE ALL ELEMENTS?
         BNP   DONEBLK          YES, BRANCH
         A     R6,=F'4'         CURRENT BLOCKSIZE RANGE
         B     OLDLRECL
         SPACE 1
NEWLRECL A     R3,=F'1'         BLOCKING FACTOR
         A     R4,LRECL         TRIAL BLOCKSIZE
         SPACE 1
OLDLRECL C     R4,=F'32760'     ABOVE MAXIMUM BLOCKSIZE?
         BH    DONEBLK          YES, BRANCH
         C     R4,0(,R6)        IN THE LOWER RANGE?
         BNH   NEWLRECL         NO, BRANCH -- NEXT LRECL
         C     R4,4(,R6)        INSIDE THE UPPER RANGE?
         BH    NEWBLK           NO, BRANCH -- NEXT RANGE
         ST    R3,SAVEBLOK(R5)  SAVE THIS BLOCKING FACTOR
         B     NEWLRECL         GET THE NEXT LRECL
         EJECT
***
***      OUTPUT THE SUMMARY BLOCKSIZE HEADERS AND FIRST OUTPUT LINE
***
         SPACE 1
DONEBLK  MVC   BLOCKHED(6),DEVCHAR   DEVICE TYPE
         L     R1,LRECL              LRECL
         EDIT  R1,6,BLOCKHED+35,5,EQUAL=YES
         L     R1,KEYLEN             KEY LENGTH
         EDIT  R1,3,BLOCKHED+56,3,EQUAL=YES
         MSG   BLOCKHED
         MSG   BLOCKLIN
         MSG   BLOCKUNL
         SPACE 2
         C     R2,=F'16'             MORE THAN 16 BLOCKS?
         BNH   OPTSKIP               NO, BRANCH
         MVC   OUTLINE+1(L'OUTLINE-1),OUTLINE
         L     R1,LRECL              BLOCKSIZE
         EDIT  R1,6,OUTLINE+15,5     BLKSIZE
         EDIT  R2,6,OUTLINE+31,5     BLOCKS/TRACK
         EDIT  R2,6,OUTLINE+47,5     RECORDS/TRACK
         LR    R1,R2                 RECORDS/TRACK
         MH    R1,LRECL+2            BYTES/TRACK
         PCENT OUTLINE+61            CONVERT TO % UTILIZED
         MSG   OUTLINE
***
***
***  NOTE: THE FOLLOWING STATEMENTS BIAS THE ORDER OF CHOICE FOR
***        "OPTIMAL" BLOCKING.  CURRENTLY, THESE STATEMENTS ARE
***        BIASED FOR A 1/2 TRACK BLOCK, FOLLOWED BY 1/3, 1/4,
***        1/5, ... 1/16 (WITH NO FULL-TRACKING RECOMMENDED).
***
         SPACE 1
OPTSKIP  L     R4,=F'1'
         LR    R5,R4
         SR    R1,R1
OPT      LH    R2,BLKPREF(R1)        NEXT INDEX
         A     R1,=F'2'              ADVANCE POINTER
         LTR   R2,R2                 POSITIVE?
         BNP   OPTDONE               NO, BRANCH
         LR    R3,R2
         SLL   R3,2                  TIMES FOUR
         L     R3,SAVEBLOK(R3)       SAVEBLOK ELEMENT
         C     R3,=F'1'              ANY BLOCKED QUANITITY?
         BNH   *+6                   NO, BRANCH
         LR    R4,R2                 RECOMMENDS BLOCKED RECORDS
         C     R3,=F'0'              ANY UNBLOCKED QUANITITY?
         BNH   *+6                   NO, BRANCH
         LR    R5,R2                 RECOMMENDS UNBLOCKED RECORDS
         B     OPT
         EJECT
***
***      OUTPUT THE REMAINDER OF THE SUMMARY BLOCKSIZE TABLE
***
         SPACE 1
OPTDONE  C     R4,=F'1'              ANY BLOCKED QUANTITY?
         BNE   *+6                   YES, BRANCH
         LR    R4,R5                 NO, USE THE UNBLOCKED QUANTITY
         LR    R1,R4
         SLL   R1,2                  INDEX TO SAVEBLOK ENTRY
         L     R1,SAVEBLOK(R1)       BLOCKING FACTOR
         MH    R1,LRECL+2            RECOMMENDED BLOCKSIZE
         ST    R1,RECOMEND           SAVE FOR LATER
         SPACE 2
         LA    R2,17                 LOOP CONTROL
OUTNEXT  S     R2,=F'1'
         BNP   ALLOC
         LR    R1,R2
         SLL   R1,2
         L     R3,SAVEBLOK(R1)
         LTR   R3,R3
         BNP   OUTNEXT
         MVC   OUTLINE+1(L'OUTLINE-1),OUTLINE
         LR    R1,R3
         MH    R1,LRECL+2            BLOCKSIZE
         EDIT  R1,6,OUTLINE+15,5
         CR    R2,R4                 RECOMMENDED VALUE?
         BNE   *+10                  NO, BRANCH
         MVC   OUTLINE(15),=C' RECOMMENDED-->'
         EDIT  R2,6,OUTLINE+31,5
         LR    R1,R3
         MR    R0,R2                 LRECLS/TRACK
         EDIT  R1,6,OUTLINE+47,5
         MH    R1,LRECL+2            BYTES/TRACK
         PCENT OUTLINE+61            CONVERT TO % UTILIZED
         MSG   OUTLINE
         B     OUTNEXT
         EJECT
***
***      CALCULATE ALLOCATION QUANTITIES
***
         SPACE 1
ALLOC    ICM   R5,15,RECNUM            ANY RECORDS?
         BZ    TRKCHECK                NO, BRANCH
         MSG   BLANKLNE
         ICM   R4,15,USEBLK            ANY BLOCKSIZE?
         BP    *+8                     YES, BRANCH
         L     R4,RECOMEND             NO, USE RECOMMENDED VALUE
         SR    R1,R1
         ICM   R1,3,DEVTRACK
         CR    R4,R1                   BLOCKSIZE>TRACK CAPACITY?
         BH    TRKCAPTY                YES, BRANCH
         C     R4,=F'32760'            BLOCKSIZE>32760?
         BH    TRKERR                  YES, BRANCH
         LR    R3,R4
         SR    R2,R2
         D     R2,LRECL
         LTR   R2,R2                   BLOCKSIZE EVENLY DIVISIBLE?
         BZ    ALLOC2                  YES, BRANCH
         MSG   ERRBLOCK                BLOCKSIZE NOT EVENLY DIVISIBLE
         B     TRKCHECK
         SPACE 1
ALLOC2   LR    R0,R4                   DD OF RKDD
         BAL   R14,DEVCALC             COMPUTE THE NUMBER PER TRACK
         LTR   R15,R15                 DID IT FIT?
         BNZ   TRKCAPTY                NO, BRANCH
         LR    R2,R0                   BLOCKS/TRACK
         EDIT  R4,6,ALLOCH+12,5
         EDIT  R5,9,ALLOCH+24,7
         MSG   ALLOCH
         SR    R4,R4
         DR    R4,R3
         LTR   R4,R4
         BZ    *+8
         A     R5,=F'1'                BLOCKS
         EDIT  R5,9,ALLOCL+1,7
         SR    R4,R4
         DR    R4,R2
         LTR   R4,R4
         BZ    *+8
         A     R5,=F'1'                TRACKS
         EDIT  R5,9,ALLOCL+19,7
         LH    R2,DEVTRKCL             TRACKS PER CYLINDER
         SR    R4,R4
         DR    R4,R2
         LTR   R4,R4
         BZ    *+8
         A     R5,=F'1'
         EDIT  R5,9,ALLOCL+40,7
         MSG   ALLOCL
         EJECT
***
***      OUTPUT THE TRACK CAPACITY TABLE IF NEEDED
***
         SPACE 1
TRKCHECK CLI   TRACKKW+1,1            TRACK CAPACITY REPORT REQUESTED?
         BNE   TRKCALLS               NO, BRANCH
         B     TRKCAP
         SPACE 1
TRKERR   MSG   ERR32760               BLOCKSIZE EXCEEDS 32,760 BYTES
         B     TRKNOCAP
         SPACE 1
TRKLRECL MVC   USEBLK,LRECL           LRECL EXCEEDS DEVICE MAXIMUM
TRKCAPTY L     R1,USEBLK              BLKSIZE EXCEEDS DEVICE MAXIMUM
         EDIT  R1,6,ERRSIZE+16,5
         MSG   ERRSIZE
         SPACE 1
TRKNOCAP CLI   TRACKKW+1,2            TRACK CAPACITY REPORT DISALLOWED?
         BE    TRKCALLS               YES, BRANCH
         SPACE 2
TRKCAP   CLC   PUTLCALL,=F'1'         JUST ONE LINE SO FAR?
         BE    TRKCAP4                YES, BRANCH
TRKCAP2  MSG   BLANKLNE
         CLC   PUTLCALL,LINES         FILLED THE SCREEN?
         BL    TRKCAP2                NO, BRANCH
         SPACE 1
TRKCAP4  MVC   TRKCAPH(6),DEVCHAR     DEVICE TYPE
         L     R1,KEYLEN              KEY LENGTH
         EDIT  R1,3,TRKCAPH+37,3,EQUAL=YES
         MSG   TRKCAPH
         MSG   TRKCAPM
         MSG   TRKCAPU
         MVC   OUTLINE+1(L'OUTLINE-1),OUTLINE
         SPACE 1
         SR    R3,R3
         LA    R6,SAVEMAX+16*4        MAXIMUM BLOCKSIZE POSITITION
NEXTMAX  A     R3,=F'1'               NEXT BLOCK/TRACK MULTIPLE
         EDIT  R3,6,OUTLINE+13,5      BLOCKS/TRACK
         L     R5,0(,R6)              BLOCKSIZE
         EDIT  R5,6,OUTLINE+25,5
         MR    R4,R3                  BYTES/TRACK
         EDIT  R5,6,OUTLINE+41,5
         LR    R1,R5                  BYTES/TRACK
         PCENT OUTLINE+56             CONVERT TO % UTILIZED
         MSG   OUTLINE
         S     R6,=F'4'               NEXT MAXIMUM BLOCKSIZE POSTITION
         C     R3,=F'16'
         BL    NEXTMAX
         EJECT
***
***      OUTPUT DEVICE SUMMARY LINE ONE
***
         SPACE 1
         MSG   BLANKLNE
         SR    R0,R0
         ICM   R0,3,DEVTRACK          MAXIMUM TRACK LENGTH
         SH    R0,DEVOKEY             LESS RECORD AND KEY OVERHEAD
         AH    R0,DEVNOKEY            PLUS KEY OVERHEAD AMOUNT
         CLI   DEVTYPE,X'0E'          3380 DEVICE?
         BE    SUM3380                YES, BRANCH
         CLI   DEVTYPE,X'0C'          3375 DEVICE?
         BNE   SUMDONE                NO, BRANCH
         SPACE 1
SUM3375  L     R0,=F'35616'           3375 DISK
         B     SUMDONE
         SPACE 1
SUM3380  L     R0,=F'47476'           3380 DISK
         SPACE 2
SUMDONE  EDIT  R0,6,TRKDEVS+30,5,EQUAL=YES
         SPACE 1
         LH    R1,DEVCYL              TOTAL CYLINDERS
         MH    R1,DEVTRKCL            TOTAL TRACKS
         EDIT  R1,6,TRKDEVS+46,5,EQUAL=YES
         SPACE 1
         MR    R0,R0                  TOTAL BYTES
         EDIT  R1,11,TRKDEVS+61,9,EQUAL=YES
         SPACE 1
         MSG   TRKDEVS
         EJECT
***
***      OUTPUT DEVICE SUMMARY LINE TWO
***
         SPACE 1
         LH    R1,DEVCYL
         EDIT  R1,3,TRKSUMM+07,3,EQUAL=YES
         LH    R1,DEVTRKCL
         EDIT  R1,3,OUTLINE+1,3
         MVC   TRKSUMM+22(1),OUTLINE+3
         CLI   OUTLINE+2,X'40'
         BE    *+10
         MVC   TRKSUMM+22(2),OUTLINE+2
         ICM   R1,3,DEVTRACK
         EDIT  R1,6,TRKSUMM+35,5,EQUAL=YES
         SPACE 1
         LA    R0,96                  DD OF RKDD
         MVI   KEYLEN+3,44            K  OF RKDD
         BAL   R14,DEVCALC            COMPUTE THE NUMBER THAT FIT
         EDIT  R0,3,OUTLINE+1,3
         MVC   TRKSUMM+53(1),OUTLINE+3
         CLI   OUTLINE+2,X'40'
         BE    *+10
         MVC   TRKSUMM+53(2),OUTLINE+2
         SPACE 1
         LA    R0,256                 DD OF RKDD
         MVI   KEYLEN+3,8             K  OF RKDD
         BAL   R14,DEVCALC            COMPUTE THE NUMBER THAT FIT
         EDIT  R0,3,OUTLINE+1,3
         MVC   TRKSUMM+66(1),OUTLINE+3
         CLI   OUTLINE+2,X'40'
         BE    *+10
         MVC   TRKSUMM+66(2),OUTLINE+2
         MSG   TRKSUMM
         SPACE 3
TRKCALLS CLI   VERIFYKW+1,1           VERIFY?
         BNE   RC0                    NO, BRANCH
         L     R3,TRKCALCN            TOTAL NUMBER OF TRKCALC CALLS
         EDIT  R3,9,TOTCALLS+1,7
         SPACE 1
         MSG   BLANKLNE
         MSG   TOTCALLS
         B     RC0
         EJECT
***
***      DEVCALC COMPUTATIONS
***
         SPACE 1
DEVCALC  STM   R14,R0,DEVREGS         SAVE INITIAL REGISTERS
         SR    R15,R15
         IC    R15,DEVTYPE            DEVTYPE CHARACTER
         SH    R15,=H'6'              ADJUST (6:14) TO (0:8)
         MH    R15,=H'4'              ADJUST (0:8) TO (0:32)
         SPACE 1
         B     *+4(R15)               BRANCH TO PROCESSING ROUTINE
         B     DEV23051               2305-1
         B     DEV23052               2305-2
         B     DEV2314                2314
         B     DEV3330                3330
         B     DEV3340                3340
         B     DEV3350                3350
         B     DEV3375                3375
         B     DEV33301               3330-1
         B     DEV3380                3380
         SPACE 1
DEV23051 DS    0H
DEV23052 DS    0H
DEV3330  DS    0H
DEV3340  DS    0H
DEV3350  DS    0H
DEV33301 LH    R15,DEVOKEY            NORMAL DEVICE CALCULATIONS
         CLI   KEYLEN+3,0             ANY KEY?
         BH    *+8                    YES, BRANCH
         SH    R15,DEVNOKEY           NO, LESS KEYED PORTION
         A     R15,KEYLEN             ADD KEY LENGTH
         AR    R15,R0                 KEY AND RECORD OVERHEAD
         B     DEVDIVID
         SPACE 1
DEV2314  L     R15,KEYLEN             KEY LENGTH
         AR    R15,R0                 KEY AND RECORD LENGTH
         LH    R1,DEVTRACK            TRACK CAPACITY
         SR    R1,R15                 BYTES REMAINING LESS KEY OVERHEAD
         CLI   KEYLEN+3,0             ANY KEY?
         BNH   *+8                    NO, BRANCH
         S     R1,=F'45'              BYTES REMAINING ON TRACK
         LTR   R1,R1                  ANY RECORD FIT ON THE TRACK?
         BM    DEVDONE2               NO, BRANCH
         M     R14,=F'534'
         D     R14,=F'512'
         A     R15,=F'146'            NORMAL KEY AND RECORD OVERHEAD
         CLI   KEYLEN+3,0             ANY KEY?
         BH    *+8                    YES, BRANCH
         S     R15,=F'45'             NO, LESS KEYED PORTION
         SR    R0,R0
         DR    R0,R15                 NUMBER OF RECORDS THAT FIT -1
         A     R1,=F'1'               NUMBER OF RECORDS THAT FIT
         B     DEVDONE
         EJECT
DEV3375  ICM   R15,15,KEYLEN          ANY KEY?
         BZ    DEV3375R               NO, BRANCH
         A     R15,=F'191'
         SR    R14,R14
         D     R14,=F'32'
         M     R14,=F'32'
DEV3375R LR    R1,R15                 SAVE KEY AND OVERHEAD LENGTH
         LR    R15,R0                 RECORD LENGTH
         A     R15,=F'191'
         SR    R14,R14
         D     R14,=F'32'
         M     R14,=F'32'
         AR    R15,R1                 RECORD AND KEY LENGTH
         A     R15,=F'224'            BASIC OVERHEAD AMOUNT
         B     DEVDIVID
         SPACE 2
DEV3380  ICM   R15,15,KEYLEN          ANY KEY?
         BZ    DEV3380R               NO, BRANCH
         A     R15,=F'267'
         SR    R14,R14
         D     R14,=F'32'
         M     R14,=F'32'
DEV3380R LR    R1,R15                 SAVE KEY AND OVERHEAD LENGTH
         LR    R15,R0                 RECORD LENGTH
         A     R15,=F'267'
         SR    R14,R14
         D     R14,=F'32'
         M     R14,=F'32'
         AR    R15,R1                 RECORD AND KEY LENGTH
         A     R15,=F'256'            BASIC OVERHEAD AMOUNT
         EJECT
DEVDIVID SR    R0,R0
         SR    R1,R1
         ICM   R1,3,DEVTRACK          MAXIMUM TRACK SIZE
         DR    R0,R15                 NUMBER OF RECORDS THAT FIT
         SPACE 2
DEVDONE  SR    R15,R15                RETURN CODE ZERO
         LTR   R0,R1                  ANY RECORDS FIT?
         BNZ   *+8                    YES, BRANCH
DEVDONE2 LA    R15,4                  NO, RETURN CODE FOUR
         SPACE 1
         L     R14,DEVREGS            RELOAD REGISTER 14
         CLI   VERIFYKW+1,1           VERIFY WITH TRKCALC?
         BNER  R14                    NO, RETURN
         SPACE 2
         L     R1,DEVREGS+8           RESTORE RECORD LENGTH
         STM   R14,R0,DEVREGS         SAVE RESULTS FOR NOW
         SPACE 3
         LR    R0,R1                  DD OF RKDD
         ICM   R0,B'0100',KEYLEN+3    K  OF RKDD
         ICM   R0,B'1000',=X'01'      R  OF RKDD
         TRKCALC FUNCTN=TRKCAP,TYPE=DEVTYPE,REGSAVE=YES,RKDD=(0)
         L     R14,DEVREGS            RELOAD REGISTER 14
         LTR   R15,R15                FIT?
         BNZ   DEVNOFIT               NO, BRANCH
         C     R15,DEVREGS+4          YES -- OTHER RETURN CODE ZERO?
         BNE   DEVERROR               NO, BRANCH
         C     R0,DEVREGS+8           SAME NUMBER OF RECORDS?
         BNE   DEVERROR               NO, ERROR
         BR    R14
         SPACE 1
DEVNOFIT ICM   R1,15,DEVREGS+4        OTHER RETURN CODE ZERO?
         BNZR  R14                    NO, RETURN
         SPACE 2
DEVERROR LM    R2,R3,DEVREGS+4        FIRST RETURN CODE; # OF RECORDS
         LR    R4,R15                 SECOND RETURN CODE
         LR    R5,R0                  SECOND NUMBER OF RECORDS
         MSG   ERRVERFY               VERIFY ERROR
         ABEND 20
         EJECT
***
***      PUTLINE OUTPUT ROUTINE
***
         SPACE 1
$PUTLINE ST    R14,R14SAVE
         SPACE
         MVC   PUTLINE+4(80),0(R1)    INPUT DATA
         SLL   R15,16
         STCM  R15,15,PUTLINE          SAVE LENGTH
         L     R1,PUTLCALL
         A     R1,=F'1'
         ST    R1,PUTLCALL
         LA    R1,PUTLPARM
         USING IOPL,R1
         L     R14,CPPLUPT
         L     R15,CPPLECT
         LA    R0,MYECB
         STM   R14,R0,IOPLUPT         INITIALIZE IOPLUPT, IOPLECT
*                                                AND IOPLECB
         SPACE
         LA    R0,PUTLINE             DATA LINE POINTER
         L     R15,16                 CVT ADDRESS
         L     R15,X'1BC'(,R15)       PUTLINE ROUTINE
         SPACE
         PUTLINE PARM=PUTLPARM+16,ENTRY=(15),MF=(E,(1)),               X
               OUTPUT=((0),TERM,SINGLE,DATA),                          X
               TERMPUT=(EDIT,WAIT,NOHOLD,NOBREAK)
         SPACE
         DROP  R1
         TM    MYECB,X'40'           ATTN OCCUR?
         BO    RC12                  YES, BRANCH
         SPACE
         L     R14,R14SAVE
         LTR   R15,R15               SUCCESSFUL?
         BZR   R14                   YES, DONE
         SPACE 1
         EDIT  R15,3,ERRPUTL+23,3,EQUAL=YES
         TPUT  ERRPUTL,L'ERRPUTL
         SPACE 5
RC12     LA    R15,12
         B     *+6
         SPACE 1
RC0      SR    R15,R15
         L     R13,4(,R13)
         RETURN (14,12),RC=(15)
         EJECT
***
***      CURRENT DEVICE CHARACTERISTICS (COPIED FROM UNITTBL):
***
         SPACE 1
         DS    0H
DEVNAME  DC    CL5'ABCDE'          ALIAS NAME OF THIS ROUTINE(XXXABCDE)
DEVCHAR  DC    CL6'123456'         RIGHT-ADJUSTED NAME FOR THIS DEVICE
DEVTYPE  DC    X'12'               DEVTYPE CHARACTER
DEVTRKCL DC    H'12'               TRACKS TO A CYLINDER
DEVCYL   DC    H'123'              NUMBER OF CYLINDERS FOR THE DEVICE
DEVOKEY  DC    H'123'              MINIMUM BLOCK OVERHEAD WITH A KEY
DEVNOKEY DC    H'123'              KEYED AMOUNT OF BLOCK OVERHEAD
DEVTRACK DC    H'12345'            MAXIMUM TRACK LENGTH
         SPACE 5
***
***      DEVICES CURRENTLY SUPPORTED:
***
         SPACE 1
UNITTBL DC  C'23051',C'2305-1',X'06',H'08',H'048',X'27A',X'0CA',X'38E8'
        DC  C'23052',C'2305-2',X'07',H'08',H'096',X'121',X'05B',X'3A0A'
        DC  C'2314 ',C'  2314',X'08',H'20',H'200',X'000',X'000',X'1C7E'
        DC  C'3330 ',C'  3330',X'09',H'19',H'404',X'0BF',X'038',X'336D'
        DC  C'33301',C'3330-1',X'0D',H'19',H'808',X'0BF',X'038',X'336D'
        DC  C'3340 ',C'  3340',X'0A',H'12',H'696',X'0F2',X'04B',X'2157'
        DC  C'3350 ',C'  3350',X'0B',H'30',H'555',X'10B',X'052',X'4B36'
        DC  C'3375 ',C'  3375',X'0C',H'12',H'959',X'220',X'0A0',X'8CA0'
        DC  C'3380 ',C'  3380',X'0E',H'15',H'885',X'2D8',X'0EC',X'BB60'
        DC  X'FF'
         EJECT
***
***      PROGRAM VARIABLES
***
         SPACE 1
DOUBLE   DC    D'0'             WORKAREA
SAVE     DC    18F'0'           PROGRAM SAVE AREA
TRKCALCN DC    F'0'             CALLS TO TRKCALC FOR TRACK CAPACITY
DEVREGS  DC    3F'0'            DEVCALC HOLD AREA FOR R14, R15 AND R0
KEYLEN   DC    F'0'             KEY LENGTH
LRECL    DC    F'0'             LRECL
RECNUM   DC    F'100000'        NUMBER OF ALLOCATED RECORDS
USEBLK   DC    F'0'             BLOCKSIZE TO USE
RECOMEND DC    F'0'             RECOMMENDED BLOCKSIZE
SAVEBLOK DC    18F'0'           SAVED BLOCKSIZE ARRAY
SAVEMAX  DC    18F'0'           TRACK CAPACITY ARRAY
LINES    DC    F'0'             LINES TO A SCREEN
MAXBLOCK DC    F'0'             MAXIMUM BLOCKSIZE FOR THIS KEY & TRACK
MAXROUND DC    F'0'             1/2 OF MAXBLOCK FOR ROUNDING TO 1/10
MYPPL    DC    7A(0)            PPL FOR PARSE
PUTLPARM DC    8A(0)            PARMS FOR PUTLINE
PUTLCALL DC    A(0)             PARMS FOR PUTLINE
MYANS    DC    A(0)             PARSE ANSWER AREA
MYECB    DC    F'0'             ECB FOR PARSE AND PUTLINE
R14SAVE  DC    F'0'             SAVE AREA FOR PARSE
         SPACE 3
* BLOCKING FACTOR PREFERENCE TABLE -- ENTRIES TO THE RIGHT ARE FAVORED:
         SPACE 1
BLKPREF  DC    AL2(16,15,14,13,12,11,10,9,8,7,6,5,4,3,2,0)
         EJECT
***
***      PROGRAM HEADERS
***
         SPACE 1
EDITPIC  DC    X'2020206B2020206B2020206B202120'
BLOCKHED DC    C'XXXXXX BLOCKSIZE SUMMARY;    LRECL 123456    KEY LENGTX
               H 123 '
BLOCKLIN DC C'            BLOCKSIZE    BLOCKS/TRACK    LRECLS/TRACK    X
               UTILIZATION'
BLOCKUNL DC C'            ---------    ------------    ------------    X
               -----------'
BLANKLNE DC    C' '
ALLOCH   DC    C'FOR BLKSIZE 12,345  AND 1,234,567 RECORDS, ALLOCATE:'
ALLOCL   DC    C' 1,234,567 BLOCKS, 1,234,567 TRACKS, OR 1,234,567 CYLIX
               NDERS'
PUTLINE  DC    CL84' '
TRKCAPH  DC    C'XXXXXX TRACK CAPACITY;    KEY LENGTH 123 '
TRKCAPM  DC    C'       BLOCKS/TRACK     BLKSIZE     BYTES/TRACK     UTX
               ILIZATION'
TRKCAPU  DC    C'       ------------     -------     -----------     --X
               ---------'
OUTLINE  DC    CL80' '
TRKDEVS  DC   C'DEVICE SUMMARY: MAX BLOCKSIZE 12,345   TRACKS 12,345   X
               BYTES 123,456,789 '
TRKSUMM  DC     C'NOCYLS 123   TRKS/CYL=2    TRKSIZE 12,345   DSCB/TRK=X
               2    PDS/TRK=2  '
TOTCALLS DC    C' 1,234,567 CALLS WERE MADE TO COMPUTE TRACK CAPACITY; X
               34 IS THE BASELINE NUMBER'
ERRVERFY DC    C'*** VERIFICATION ERROR, COMPARE R2 && R3 WITH R4 && R5X
               ; CORRESPONDS TO R15 && R0'
ERRPUTL  DC    C'*** PUTLINE ERROR, R15 123 '
ERRSIZE  DC    C'*** RECORD SIZE 12,345 DOES NOT FIT ON A TRACK'
ERR32760 DC    C'*** 32,760 IS THE MAXIMUM ALLOWED RECORD SIZE'
ERRBLOCK DC    C'*** YOUR BLOCKSIZE IS NOT A MULTIPLE OF YOUR LOGICAL RX
               ECORD LENGTH'
ERRNULL  DC    C'*** NULL RECORDS ARE NOT SUPPORTED'
ERRKEY   DC    C'*** THE MAXIMUM LEGAL KEY LENGTH IS 255 CHARACTERS'
ERRUNIT  DC    C'*** THIS DEVICE NAME IS NOT SUPPORTED'
ERRPROG  DC    C'*** PROGRAM ERROR, LOOK AT R2 AND R3'
         EJECT
***
***      PROGRAM LITERALS
***
         SPACE 1
         LTORG
         EJECT
***
***      PARSE CONTROL DATA
***
         SPACE 1
BLKPCL   IKJPARM
LRE      IKJIDENT 'LRECL',                                             +
               FIRST=NUMERIC,OTHER=NUMERIC,MAXLNTH=5,                  +
               PROMPT='LOGICAL RECORD LENGTH'
KEYKW    IKJKEYWD
         IKJNAME 'KEYLENGTH',SUBFLD=KEYSF
RECKW    IKJKEYWD
         IKJNAME 'RECORDS',SUBFLD=RECSF
NUMKW    IKJKEYWD
         IKJNAME 'NUMBER',SUBFLD=RECSF
BLKKW    IKJKEYWD
         IKJNAME 'BLKSIZE',SUBFLD=BLKSF
TRACKKW  IKJKEYWD
         IKJNAME 'TRACKCAP'
         IKJNAME 'NOTRACKCAP'
VERIFYKW IKJKEYWD
         IKJNAME 'VERIFY'
KEYSF    IKJSUBF
KEY      IKJIDENT 'KEY LENGTH',                                        +
               FIRST=NUMERIC,OTHER=NUMERIC,MAXLNTH=3,                  +
               PROMPT='THE RECORD KEY LENGTH'
RECSF    IKJSUBF
REC      IKJIDENT 'NUMBER OF RECORDS',                                 +
               FIRST=NUMERIC,OTHER=NUMERIC,MAXLNTH=7,PROMPT='THE NUMBER+
                OF RECORDS FOR AN ALLOCATION COMPUTATION'
BLKSF    IKJSUBF
BLK      IKJIDENT 'BLOCKSIZE TO USE',                                  +
               FIRST=NUMERIC,OTHER=NUMERIC,MAXLNTH=5,PROMPT='THE BLOCKS+
               IZE TO USE FOR AN ALLOCATION COMPUTATION'
         IKJENDP
         PRINT GEN
         EJECT
         IKJCPPL
         SPACE 3
         IKJPPL
         SPACE 3
         IKJIOPL
         EJECT
         IKJECT
         SPACE 3
         END   BLKDISK
//LINK.SYSLIN  DD
//             DD     *
 ALIAS BLK23051
 ALIAS BLK23052
 ALIAS BLK2314
 ALIAS BLK3330
 ALIAS BLK33301
 ALIAS BLK3340
 ALIAS BLK3350
 ALIAS BLK3375
 ALIAS BLK3380
 NAME BLKDISK(R)
//*
//*  NOTE: MAKE A VERSION OF THE FOLLOWING HELP DATA FOR EACH
//*        DEVICE TYPE TO BE SUPPORTED AT YOUR INSTALLATION.
//*
//*        FOR EACH DISK TYPE, CREATE A HELP MEMBER CALLED BLKXXXX
//*        WHERE XXXX IS ONE OF THE FOLLOWING: 23051, 23052,
//*        2314, 3330, 33301, 3340, 3350, 3375 OR 3380;
//*        THESE NAMES CORRESPOND TO DASD TYPES: 2305-1, 2305-2,
//*        2314, 3330, 3330-11, 3340, 3350, 3375 OR 3380.
//*
//*        COPY MEMBER BLKDISK, REPLACING YYYY IN THE TEXT WITH THE
//*        NAME OF ONE OF THE SUPPORTED DEVICE TYPES: 2305-1, 2305-2,
//*        2314, 3330, 3330-11, 3340, 3350, 3375 OR 3380.
//*
//*        ALSO, REPLACE BLKXXXX IN THE HELP TEXT WITH THE NAME OF
//*        THIS MEMBER (BLKXXXX OCCURS TWICE IN THE TEXT).
//*
//COPY.SYSUT1 DD *
)F FUNCTION -
  THE BLKXXXX COMMAND COMPUTES AN OPTIMAL BLOCKSIZE FOR A DATA SET
  TO BE PLACED ON A YYYY DISK PACK.  THE PROGRAM OUTPUT INCLUDES
  THE FOLLOWING REPORTS:
     1.  A SUMMARY BLOCKSIZE REPORT FOR THE GIVEN LRECL AND KEY
         LENGTH WHICH INCLUDES THE RECOMMENDED BLOCKSIZE TO USE.
     2.  A RECOMMENDED DATA SET SPACE ALLOCATION.
     3.  A OPTIONAL TRACK CAPACITY REPORT FOR THE PROVIDED KEY LENGTH.

  THE RECOMMENDED BLOCKSIZE VALUE IS FOR DATA SETS IN WHICH THE
  PREDOMINANT ACCESS IS SEQUENTIAL; FOR DATA SETS WHERE RANDOM
  ACCESS TIME IS CRITICAL OR THE USUAL ACCESS IS RANDOM, A SMALL
  BLOCKSIZE (500-2000 BYTES) SHOULD PROBABLY BE USED.

  THE RECOMMENDED BLOCKSIZE WILL USUALLY TEND TO BE NEAR A HALF-TRACK
  FIGURE AS THIS IS CONSIDERED TO BE THE MOST EFFICIENT IN TERMS OF
  THE TRADE-OFFS AMONG BUFFER SIZE, SECONDARY STORAGE REQUIREMENTS,
  CHANNEL USE, NUMBER OF INPUT/OUTPUTS AND OVERALL PROCESSING TIME.
  THIS FIGURE IS ONLY A GENERAL GUIDE; FOR MAXIMAL EFFICIENCY
  CONSIDERING OTHER FACTORS, STUDY THE GENERATED BLOCKSIZE SUMMARY
  REPORT OR A TRACK CAPACITY REPORT.

  THE PROGRAM'S RECOMMENDATIONS ASSUME A FAIRLY LARGE AMOUNT OF DATA
  IS TO BE STORED; DATA SETS WHICH OCCUPY ONLY A FEW TRACKS SHOULD
  PROBABLY BE PLACED IN PARTITIONED DATA SETS.  IN CASES WHERE THIS
  IS NOT FEASIBLE, THE USE OF A SMALL BLOCKSIZE (2400 - 4000 BYTES)
  MAY BE A GOOD ALTERNATIVE PRACTICE.
)X SYNTAX  -
         BLKXXXX  'LRECL'  KEYLENGTH('INTEGER')
                           TRACKCAP / NOTRACKCAP
                           BLKSIZE('INTEGER')
                           NUMBER('INTEGER') / RECORDS('INTEGER')
                           VERIFY
  REQUIRED - LRECL
  DEFAULTS - KEYLENGTH(0),
             NOTRACKCAP,
             BLKSIZE(RECOMMENDED VALUE),
             NUMBER(100000)
)O OPERANDS -
 'LRECL'  - THE LOGICAL RECORD LENGTH OF THE DATA WHICH IS TO BE
            PLACED IN THE DATA SET.
))KEYLENGTH('INTEGER')
          - THE KEY LENGTH, IN BYTES, OF THE KEYS TO BE USED IN THE
            DATA SET.  THE MAXIMUM LEGAL KEY LENGTH IS 255.
))TRACKCAP
          - SPECIFIES THAT A TRACK CAPACITY REPORT IS TO BE PROVIDED
            FOR THE DEVICE USING THE SPECIFIED (OR DEFAULT) KEY LENGTH.

            NOTE THAT A TRACK CAPACITY REPORT IS ALSO PROVIDED IF
            NOTRACKCAP IS NOT SPECIFIED AND LRECL EXCEEDS THE MAXIMUM
            BLOCKSIZE FOR A TRACK OR BLKSIZE EXCEEDS THE MAXIMUM
            BLOCKSIZE FOR A TRACK.
))NOTRACKCAP
          - SPECIFIES THAT A TRACK CAPACITY REPORT IS NOT DESIRED.
))BLKSIZE('INTEGER')
          - THE BLOCKSIZE TO USE FOR THE ALLOCATION COMPUTATION; IF
            BLKSIZE IS NOT ENTERED (OR ZERO IS ENTERED), THE PROGRAM'S
            RECOMMENDED BLOCKSIZE WILL BE USED.
))NUMBER('INTEGER')
          - THE NUMBER OF LOGICAL RECORDS THAT WILL BE IN THE DATA SET.
))RECORDS('INTEGER')
          - THE NUMBER OF LOGICAL RECORDS THAT WILL BE IN THE DATA SET.
))VERIFY  - SPECIFIES THAT THE MVS "TRKCALC" ROUTINE IS TO BE USED TO
            VERIFY TRACK CAPACITY CALCULATIONS.  IF VERIFY IS USED,
            THE NUMBER OF CALLS TO "TRKCALC" TO DETERMINE A TRACK
            CAPACITY TABLE IS OUTPUT AT THE END OF THE OUTPUT.

            NOTE: WITH VERIFY ON, A MINIMUM OF 34 CALLS IS REQUIRED TO
            DETERMINE A TRACK CAPACITY TABLE; OTHERWISE A MINIMUM OF
            17 CALLS IS REQUIRED TO DETERMINE THE TRACK CAPACITY TABLE.
//XEQ.SYSTSIN DD *
 BLK23051  80 TRACKCAP
 BLK23052  80 TRACKCAP
 BLK2314  140 KEY(44) TRA
 BLK3330 4096 TRACKCAP
 BLK33301  80 KEY(20) TRACKCAP
 BLK3340   80 TRACKCAP
 BLK3350  133 TR VERIFY
 BLK3375   80 TR VERIFY
 BLK3380   80 TR VERIFY
