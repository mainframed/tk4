         MACRO
&LABEL   FTCLOSE &P1,                                                  *
               &P2,                                                    *
               &P3,                                                    *
               &NAME=,                  MEMBER                         *
               &LIBRARY=,               LIBRARY                        *
               &NOREPL=,                ISPF KEYWORD                   *
               &LOAD=ISPLINK,           ISPLINK ADDRESS                *
               &MF=I                    MACRO FORMAT
.*
.**********************************************************************
.*       GLOBALS
.**********************************************************************
.*
         GBLB  &SPFFTCLOSE,&SPFNULLC,&SPFDEBUG,&SPFVAR
         GBLA  &SPFVARS,&SPFVARC
         GBLC  &SPFLVL
         LCLC  &NDX,&TEMP
         AIF   ('&SPFLVL' NE '1').NL1
&LABEL   @FTCLOSE &P1,&P2,&P3,LOAD=&LOAD,MF=&MF
         MEXIT
.NL1     ANOP
         AIF   ('&P1' NE '').LVLERR
         AIF   ('&P2' NE '').LVLERR
         AIF   ('&P3' NE '').LVLERR
         AGO   .NL2
.LVLERR  ANOP
         MNOTE 12,'POSITIONAL PARAMETER FOUND - NOT ALLOWED'
         MEXIT
.NL2     ANOP
&NDX     SETC  '&SYSNDX'
.*
.**********************************************************************
.*       GENERAL EDITS
.**********************************************************************
.*
         AIF   ('&MF(1)' NE 'I' AND                                    *
               '&MF(1)' NE 'E').ERR2
.*
         AIF   ('&LABEL' EQ '').BYP10
&LABEL   DS    0H
.BYP10   ANOP
         AIF   ('&MF(2)' NE '').BYP15
&TEMP    SETC  'SPFPARMS'
         AGO   .BYP17
.BYP15   ANOP
&TEMP    SETC  '&MF(2)'
.BYP17   ANOP
         AIF   ('&NOREPL' EQ '').K1
         AIF   ('&NOREPL'(1,1) NE '''' OR '&NOREPL' EQ '''NOREPL''').K1
         MNOTE 4,'WARNING - NOREPL KEYWORD EXPECTED IN QUOTES'
.K1      ANOP
.*
.**********************************************************************
.*       CHECK TO SEE IF A BRANCH AROUND LITERALS IS REQUIRED
.**********************************************************************
.*
         AIF   (NOT &SPFFTCLOSE).GENLIT
         AIF   (NOT &SPFNULLC).GENLIT
         AIF   ('&NAME' EQ '').CNT1
         AIF   ('&NAME'(1,1) EQ '''').GENLIT
.CNT1    AIF   ('&LIBRARY' EQ '').CNT2
         AIF   ('&LIBRARY'(1,1) EQ '''').GENLIT
.CNT2    AIF   ('&NOREPL' EQ '').CNT3
         AIF   ('&NOREPL'(1,1) EQ '''').GENLIT
.CNT3    AGO   .BRAREA
.GENLIT  ANOP
.*
.*********************************************************************
.*       GENERATE THE BRANCH AROUND LITERALS AND THE LITERALS
.*********************************************************************
.*
         B     SPF&NDX
         AIF   (&SPFFTCLOSE).BYP20
FTCLOSE  DC    CL8'FTCLOSE'             ISPF KEYWORD
&SPFFTCLOSE SETB 1
.BYP20   ANOP
         AIF   ('&NAME' EQ '').BYP40
         AIF   ('&NAME'(1,1) NE '''').BYP40
SPFM&NDX DC    CL8&NAME                 GENERATED MEMBER
.BYP40   ANOP
         AIF   ('&LIBRARY' EQ '').BYP50
         AIF   ('&LIBRARY'(1,1) NE '''').BYP50
SPFL&NDX DC    CL8&LIBRARY              GENERATED LIBRARY
.BYP50   ANOP
         AIF   ('&NOREPL' EQ '').BYP60
         AIF   ('&NOREPL'(1,1) NE '''').BYP60
SPFX&NDX DC    CL8&NOREPL               ISPF KEYWORD
.BYP60   ANOP
         AIF   (&SPFNULLC).BYP70
SPFNULLC DC    CL8'        '            DUMMY ISPF FIELD
&SPFNULLC SETB  1
.BYP70   ANOP
SPF&NDX  DS    0H
.BRAREA  ANOP
.*
.*********************************************************************
.*       GENERATE THE CALL
.*********************************************************************
.*
         AIF   ('&MF(1)' EQ 'I').MFI
         LA    14,FTCLOSE               GET ADDRESS OF FTCLOSE KEYWORD
         AIF   ('&NAME' EQ '').BYP100
         AIF   ('&NAME'(1,1) EQ '''').BYP110
         LA    15,&NAME                 GET ADDRESS OF MEMBER
         AGO   .BYP120
.BYP110  ANOP
         LA    15,SPFM&NDX              GET ADDRESS OF GENERATED MEMBER
         AGO   .BYP120
.BYP100  ANOP
         LA    15,SPFNULLC              GET DUMMY PARM FOR MEMBER
.BYP120  ANOP
         AIF   ('&LIBRARY' EQ '').BYP140
         AIF   ('&LIBRARY'(1,1) NE '''').BYP130
         LA    0,SPFL&NDX               GET ADDRESS OF GENERATED LIB
         AGO   .BYP150
.BYP130  ANOP
         LA    0,&LIBRARY               GET ADDRESS OF LIBRARY
         AGO   .BYP150
.BYP140  ANOP
         LA    0,SPFNULLC               GET DUMMY PARM FOR LIBRARY
.BYP150  ANOP
         AIF   ('&NOREPL' EQ '').BYP170
         AIF   ('&NOREPL'(1,1) EQ '''').BYP175
         LA    1,&NOREPL                GET ADDRESS OF KEYWORD
         AGO   .BYP180
.BYP170  ANOP
         LA    1,SPFNULLC               GET DUMMY PARM FOR KEYWORD
         AGO   .BYP180
.BYP175  ANOP
         LA    1,SPFX&NDX               GET GENNED NOREPL
.BYP180  ANOP
         STM   14,1,&TEMP               STORE PARAMETERS
         OI    &TEMP+12,X'80'           MARK LAST PARM
         LA    1,&TEMP                  GET PARAMETER LIST ADDRESS
         L     15,&LOAD                 LOAD ADDRESS OF ISPF
         BALR  14,15                    CALL ISPF
         AIF   (NOT &SPFVAR).BYVAR
         AIF   (&SPFVARC LT 4).ERRVAR
         AGO   .BYPVARS
.BYVAR   AIF   (&SPFVARS GE 4).BYPVARS
&SPFVARS SETA  4
.BYPVARS ANOP
         MEXIT
.*
.**********************************************************************
.*       MACRO FORMAT I
.**********************************************************************
.*
.MFI     ANOP
         CNOP  0,4
         BAL   1,SPFE&NDX               BRANCH AROUND LITERALS
         DC    A(FTCLOSE)               BROWSE LITERAL
         AIF   ('&NAME' EQ '').BYP200
         AIF   ('&NAME'(1,1) EQ '''').BYP210
         DC    A(&NAME)                 ADDRESS OF MEMBER
         AGO   .BYP220
.BYP210  ANOP
         DC    A(SPFM&NDX)              ADDRESS OF GENERATED MEMBER
         AGO   .BYP220
.BYP200  ANOP
         DC    A(SPFNULLC)              DUMMY PARM FOR MEMBER
.BYP220  ANOP
         AIF   ('&LIBRARY' EQ '').BYP240
         AIF   ('&LIBRARY'(1,1) NE '''').BYP230
         DC    A(SPFL&NDX)              ADDRESS OF GENERATED LIBRARY
         AGO   .BYP250
.BYP230  ANOP
         DC    A(&LIBRARY)              ADDRESS OF LIBRARY
         AGO   .BYP250
.BYP240  ANOP
         DC    A(SPFNULLC)              DUMMY PARM FOR LIBRARY
.BYP250  ANOP
         AIF   ('&NOREPL' EQ '').BYP270
         AIF   ('&NOREPL'(1,1) EQ '''').BYP275
         DC    A(&NOREPL+X'80000000')   ADDRESS OF KEYWORD
         AGO   .BYP300
.BYP270  ANOP
         DC    A(SPFNULLC+X'80000000')  DUMMY PARM FOR KEYWORD
         AGO   .BYP300
.BYP275  ANOP
         DC    A(SPFX&NDX+X'80000000')  GENNED NOREPL KEYWORD
.BYP300  ANOP
SPFE&NDX DS    0H                       MARK LAST PARM
         L     15,&LOAD                 LOAD ADDRESS OF ISPF
         BALR  14,15                    CALL ISPF
         MEXIT
.*
.**********************************************************************
.*       MACRO ERRORS
.**********************************************************************
.*
.ERR2    ANOP
         MNOTE 16,'INVALID MACRO FORMAT SPECIFIED - FOUND ''&MF(1)'''
         MEXIT
.ERRVAR  ANOP
         MNOTE 16,'ISPF PARMS MACRO WILL NOT HOLD FULL PARMLIST'
         MNOTE *,' ISPF PARMS MACRO GENERATE PARMLIST TOO SMALL'
         MEXIT
         MEND
