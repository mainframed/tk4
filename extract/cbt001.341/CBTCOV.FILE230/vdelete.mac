         MACRO
&NAME    VDELETE &VAR,                  NAMELIST                       *
               &LOAD=ISPLINK,           ISPLINK ADDRESS                *
               &MF=I                    MACRO FORMAT
.*
.**********************************************************************
.*       GLOBALS
.**********************************************************************
.*
         GBLB  &SPFVDELETE,&SPFDEBUG,&SPFVAR
         GBLA  &SPFVARS,&SPFVARC
         LCLC  &NDX,&TEMP
&NDX     SETC  '&SYSNDX'
         AIF   ('&MF(2)' NE '').BYP0
&TEMP    SETC  'SPFPARMS'
         AGO   .BYP00
.BYP0    ANOP
&TEMP    SETC  '&MF(2)'
.BYP00   ANOP
         AIF   ('&NAME' EQ '').BYP10
&NAME    DS    0H
.BYP10   ANOP
.*
.**********************************************************************
.*       EDITS
.**********************************************************************
.*
         AIF   ('&MF(1)' NE 'I' AND                                    *
               '&MF(1)' NE 'E').ERRMF
         AIF   ('&VAR' EQ '').ERR1
.*
.**********************************************************************
.*       CHECK TO SEE IF A BRANCH AROUND LITERALS IS REQUIRED
.**********************************************************************
.*
         AIF   (NOT &SPFVDELETE).GENLIT
         AIF   ('&VAR' EQ '').CNT1
         AIF   ('&VAR'(1,1) EQ '''').GENLIT
.CNT1    ANOP
         AGO   .BRAREA
.GENLIT  ANOP
.*
.*********************************************************************
.*       GENERATE THE BRANCH AROUND LITERALS AND THE LITERALS
.*********************************************************************
.*
         B     SPF&NDX
         AIF   (&SPFVDELETE).BYP20
VDELETE  DC    CL8'VDELETE'             ISPF KEYWORD
&SPFVDELETE SETB 1
.BYP20   ANOP
         AIF   ('&VAR' EQ '').BYP30
         AIF   ('&VAR'(1,1) NE '''').BYP30
SPFV&NDX DC    C&VAR                    GENERATED VAR LIST
.BYP30   ANOP
SPF&NDX  DS    0H
.BRAREA  ANOP
.*
.*********************************************************************
.*       GENERATE THE CALL
.*********************************************************************
.*
         AIF   ('&MF(1)' EQ 'I').MFI
         LA    14,VDELETE               GET ADDRESS OF VDELETE KEYWORD
.*
         AIF   ('&VAR' EQ '').BYP1000
         AIF   ('&VAR'(1,1) EQ '''').BYP1010
         LA    15,&VAR                  GET ADDRESS OF VAR NAME
         AGO   .BYP1050
.BYP1000 ANOP
         LA    15,SPFNULLC              GET DUMMY PARM FOR VAR
         AGO   .BYP1050
.BYP1010 ANOP
         LA    15,SPFV&NDX              GET ADDRESS OF GENERATED VAR
.BYP1050 ANOP
         STM   14,15,&TEMP              STORE PARAMETERS INTO LIST
         OI    &TEMP+4,X'80'            MARK LAST PARM
         LA    1,&TEMP                  GET PARAMETER LIST ADDRESS
         L     15,&LOAD                 LOAD ADDRESS OF ISPF
         BALR  14,15                    CALL ISPF
         AIF   (NOT &SPFVAR).BYVAR
         AIF   (&SPFVARC LT 2).ERRVAR
         AGO   .BYPVARS
.BYVAR   AIF   (&SPFVARS GE 2).BYPVARS
&SPFVARS SETA  3
.BYPVARS ANOP
         MEXIT
.*
.**********************************************************************
.*       MACRO FORMAT I
.**********************************************************************
.*
.MFI     ANOP
         CNOP  0,4
         BAL   1,SPFE&NDX               BRANCH AROUND LITERALS
         DC    A(VDELETE)               VDELETE LITERAL
.*
         AIF   ('&VAR' EQ '').BYP2000
         AIF   ('&VAR'(1,1) EQ '''').BYP2010
         DC    A(&VAR+X'80000000')      ADDRESS OF VAR NAME
         AGO   .BYP2050
.BYP2000 ANOP
         DC    A(SPFNULLC+X'80000000')  DUMMY PARM FOR VAR NAME
         AGO   .BYP2050
.BYP2010 ANOP
         DC    A(SPFV&NDX+X'80000000')  ADDRESS OF GENERATED VAR
.BYP2050 ANOP
.*
.*
SPFE&NDX DS    0H                       MARK LAST PARM
         L     15,&LOAD                 LOAD ADDRESS OF ISPF
         BALR  14,15                    CALL ISPF
         MEXIT
.*
.**********************************************************************
.*       MACRO ERRORS
.**********************************************************************
.*
.ERRMF   ANOP
         MNOTE 16,'INVALID MACRO FORMAT SPECIFIED - FOUND ''&MF(1)'''
         MEXIT
.ERR1    ANOP
         MNOTE 16,'THE NAME LIST PARAMETER IS REQUIRED'
         MEXIT
.ERRVAR  ANOP
         MNOTE 16,'ISPF PARMS MACRO WILL NOT HOLD FULL PARMLIST'
         MNOTE *,' ISPF PARMS MACRO GENERATE PARMLIST TOO SMALL'
         MEXIT
         MEND
