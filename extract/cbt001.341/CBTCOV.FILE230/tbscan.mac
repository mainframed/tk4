         MACRO
&NAME    TBSCAN &TABLE,                 TABLE NAME                     *
               &P2,                                                    *
               &P3,                                                    *
               &P4,                                                    *
               &P5,                                                    *
               &P6,                                                    *
               &P7,                                                    *
               &P8,                                                    *
               &ARGLIST=,               ARGLIST                        *
               &SAVENAME=,              EXTENSION VAR LIST             *
               &ROWID=,                 SAVE THE ROWID                 *
               &DIRECTION=,             NEXT OR PREVIOUS               *
               &NOREAD=,                NOREAD KEYWORD                 *
               &POSITION=,              POSITION OF CRP                *
               &CONDLIST=,              CONDITIONS FOR SEARCH          *
               &LOAD=ISPLINK,           ISPLINK ADDRESS                *
               &MF=I                    MACRO FORMAT
.*
.**********************************************************************
.*       GLOBALS
.**********************************************************************
.*
         GBLB  &SPFTBSCAN,&SPFNULLC,&SPFDEBUG,&SPFVAR
         GBLA  &SPFVARS,&SPFVARC
         LCLC  &NDX,&TEMP
         GBLC  &SPFLVL
         AIF   ('&SPFLVL' NE '1').NL1
&NAME    @TBSCAN &TABLE,&P2,&P3,&P4,&P5,&P6,&P7,&P8,LOAD=&LOAD,MF=&MF
         MEXIT
.NL1     ANOP
         AIF   ('&P2' NE '').LVLERR
         AIF   ('&P3' NE '').LVLERR
         AIF   ('&P4' NE '').LVLERR
         AIF   ('&P5' NE '').LVLERR
         AIF   ('&P6' NE '').LVLERR
         AIF   ('&P7' NE '').LVLERR
         AIF   ('&P8' NE '').LVLERR
         AGO   .NL2
.LVLERR  ANOP
         MNOTE 12,'POSITIONAL PARAMETER FOUND - NOT ALLOWED'
         MEXIT
.NL2     ANOP
&NDX     SETC  '&SYSNDX'
         AIF   ('&MF(2)' NE '').BYP0
&TEMP    SETC  'SPFPARMS'
         AGO   .BYP00
.BYP0    ANOP
&TEMP    SETC  '&MF(2)'
.BYP00   ANOP
         AIF   ('&NAME' EQ '').BYP10
&NAME    DS    0H
.BYP10   ANOP
.*
.**********************************************************************
.*       EDITS
.**********************************************************************
.*
         AIF   ('&MF(1)' NE 'I' AND                                    *
               '&MF(1)' NE 'E').ERRMF
         AIF   ('&TABLE' EQ '').ERR1
         AIF   ('&DIRECTION' EQ '').K3
         AIF   ('&DIRECTION'(1,1) NE '''').K3
         AIF   ('&DIRECTION' EQ '''NEXT''').K3
         AIF   ('&DIRECTION' EQ '''PREVIOUS''').K3
         MNOTE 4,'WARNING - DIRECTION KEYWORD EXPECTED NEXT OR PREVIOUS*
                ENCLOSED IN QUOTES'
.K3      ANOP
         AIF   ('&NOREAD' EQ '').K2
         AIF   ('&NOREAD'(1,1) NE '''').K2
         AIF   ('&NOREAD' EQ '''NOREAD''').K2
         MNOTE 4,'WARNING - NOREAD KEYWORD EXPECTED NOREAD IN QUOTES'
.K2      ANOP
.*
.**********************************************************************
.*       CHECK TO SEE IF A BRANCH AROUND LITERALS IS REQUIRED
.**********************************************************************
.*
         AIF   (NOT &SPFTBSCAN).GENLIT
         AIF   (NOT &SPFNULLC).GENLIT
         AIF   ('&TABLE' EQ '').CNT1
         AIF   ('&TABLE'(1,1) EQ '''').GENLIT
.CNT1    ANOP
         AIF   ('&ARGLIST' EQ '').CNT2
         AIF   ('&ARGLIST'(1,1) EQ '''').GENLIT
.CNT2    ANOP
         AIF   ('&SAVENAME' EQ '').CNT3
         AIF   ('&SAVENAME'(1,1) EQ '''').GENLIT
.CNT3    ANOP
         AIF   ('&ROWID' EQ '').CNT4
         AIF   ('&ROWID'(1,1) EQ '''').GENLIT
.CNT4    ANOP
         AIF   ('&DIRECTION' EQ '').CNT5
         AIF   ('&DIRECTION'(1,1) EQ '''').GENLIT
.CNT5    AIF   ('&NOREAD' EQ '').CNT6
         AIF   ('&NOREAD'(1,1) EQ '''').GENLIT
.CNT6    AIF   ('&POSITION' EQ '').CNT8
         AIF   ('&POSITION'(1,1) EQ '''').GENLIT
.CNT8    ANOP
         AIF   ('&CONDLIST' EQ '').CNT9
         AIF   ('&CONDLIST'(1,1) EQ '''').GENLIT
.CNT9    ANOP
         AGO   .BRAREA
.GENLIT  ANOP
.*
.*********************************************************************
.*       GENERATE THE BRANCH AROUND LITERALS AND THE LITERALS
.*********************************************************************
.*
         B     SPF&NDX
         AIF   (&SPFTBSCAN).BYP20
TBSCAN   DC    CL8'TBSCAN'              ISPF KEYWORD
&SPFTBSCAN SETB 1
.BYP20   ANOP
         AIF   ('&TABLE' EQ '').BYP30
         AIF   ('&TABLE'(1,1) NE '''').BYP30
SPFT&NDX DC    CL8&TABLE                GENERATED TABLE NAME
.BYP30   ANOP
         AIF   ('&ARGLIST' EQ '').BYP40
         AIF   ('&ARGLIST'(1,1) NE '''').BYP40
SPFA&NDX DC    C&ARGLIST                GENERATED ARGLIST-LIST
.BYP40   ANOP
         AIF   ('&SAVENAME' EQ '').BYP50
         AIF   ('&SAVENAME'(1,1) NE '''').BYP50
SPFV&NDX DC    CL8&SAVENAME             GENERATED VARIABLE NAME
.BYP50   ANOP
         AIF   ('&ROWID' EQ '').BYP60
         AIF   ('&ROWID'(1,1) NE '''').BYP60
SPFR&NDX DC    CL8&ROWID                GENERATED ROWID VAR NAME
.BYP60   ANOP
         AIF   ('&DIRECTION' EQ '').BYP70
         AIF   ('&DIRECTION'(1,1) NE '''').BYP70
SPFD&NDX  DC   CL8&DIRECTION            ISPF KEYWORD
.BYP70   ANOP
         AIF   ('&NOREAD' EQ '').BYP80
         AIF   ('&NOREAD'(1,1) NE '''').BYP80
SPFN&NDX DC    CL8&NOREAD               ISPF KEYWORD
.BYP80   ANOP
         AIF   ('&POSITION' EQ '').BYP100
         AIF   ('&POSITION'(1,1) NE '''').BYP100
SPFP&NDX DC    CL8&POSITION             GENERATED POSITION VAR
.BYP100  ANOP
         AIF   ('&CONDLIST' EQ '').BYP110
         AIF   ('&CONDLIST'(1,1) NE '''').BYP110
SPFC&NDX DC    C&CONDLIST               GENERATED COND LIST
.BYP110  ANOP
         AIF   (&SPFNULLC).BYP120
SPFNULLC DC    CL8'        '            DUMMY ISPF FIELD
&SPFNULLC SETB  1
.BYP120  ANOP
SPF&NDX  DS    0H
.BRAREA  ANOP
.*
.*********************************************************************
.*       GENERATE THE CALL
.*********************************************************************
.*
         AIF   ('&MF(1)' EQ 'I').MFI
         LA    14,TBSCAN                GET ADDRESS OF TBSCAN KEYWORD
.*
         AIF   ('&TABLE' EQ '').BYP1000
         AIF   ('&TABLE'(1,1) EQ '''').BYP1010
         LA    15,&TABLE                GET ADDRESS OF TABLE NAME
         AGO   .BYP1050
.BYP1000 ANOP
         LA    15,SPFNULLC              GET DUMMY PARM FOR TABLE
         AGO   .BYP1050
.BYP1010 ANOP
         LA    15,SPFT&NDX              GET ADDRESS OF GENERATED TABLE
.BYP1050 ANOP
.*
         AIF   ('&ARGLIST' EQ '').BYP1100
         AIF   ('&ARGLIST'(1,1) EQ '''').BYP1110
         LA    0,&ARGLIST               GET ADDRESS OF ARGLIST LIST
         AGO   .BYP1150
.BYP1100 ANOP
         LA    0,SPFNULLC               GET DUMMY PARM FOR ARGLIST
         AGO   .BYP1150
.BYP1110 ANOP
         LA    0,SPFA&NDX               GET ADDRESS OF GENERATED ARG
.BYP1150 ANOP
.*
         AIF   ('&SAVENAME' EQ '').BYP1200
         AIF   ('&SAVENAME'(1,1) EQ '''').BYP1210
         LA    1,&SAVENAME              GET ADDRESS OF VAR NAME
         AGO   .BYP1250
.BYP1200 ANOP
         LA    1,SPFNULLC               GET DUMMY PARM FOR VAR
         AGO   .BYP1250
.BYP1210 ANOP
         LA    1,SPFV&NDX               GET ADDRESS OF GENERATED VAR
.BYP1250 ANOP
.*
         STM   14,1,&TEMP               STORE PARAMETERS INTO LIST
         AIF   ('&ROWID' EQ '').BYP1300
         AIF   ('&ROWID'(1,1) EQ '''').BYP1310
         LA    14,&ROWID                GET ADDRESS OF ROWID NAME
         AGO   .BYP1350
.BYP1300 ANOP
         LA    14,SPFNULLC              GET DUMMY PARM FOR ROWID
         AGO   .BYP1350
.BYP1310 ANOP
         LA    14,SPFR&NDX              GET ADDRESS OF GENERATED ROWID
.BYP1350 ANOP
.*
         AIF   ('&DIRECTION' EQ '').BYP1400
         AIF   ('&DIRECTION'(1,1) EQ '''').BYP1405
         LA    15,&DIRECTION            GET ADDRESS OF KEYWORD
         AGO   .BYP1450
.BYP1400 ANOP
         LA    15,SPFNULLC              GET DUMMY PARM FOR ROW
         AGO   .BYP1450
.BYP1405 ANOP
         LA    15,SPFD&NDX              GET GENNED NEXT/PREVIOUS
.BYP1450 ANOP
.*
         AIF   ('&NOREAD' EQ '').BYP1500
         AIF   ('&NOREAD'(1,1) EQ '''').BYP1505
         LA    0,&NOREAD                GET ADDRESS OF KEYWORD
         AGO   .BYP1550
.BYP1500 ANOP
         LA    0,SPFNULLC               GET DUMMY PARM FOR CSRPOS
         AGO   .BYP1550
.BYP1505 ANOP
         LA    0,SPFN&NDX               GET GENNED NOREAD KEYWORD
.BYP1550 ANOP
.*
         AIF   ('&POSITION' EQ '').BYP1600
         AIF   ('&POSITION'(1,1) EQ '''').BYP1610
         LA    1,&POSITION              GET ADDRESS OF POS VAR NAME
         AGO   .BYP1650
.BYP1600 ANOP
         LA    1,SPFNULLC               GET DUMMY PARM FOR POS VAR
         AGO   .BYP1650
.BYP1610 ANOP
         LA    1,SPFP&NDX               GET GENERATED POS VAR NAME
.BYP1650 ANOP
.*
         STM   14,1,&TEMP+16            STORE PARAMETERS INTO LIST
         AIF   ('&CONDLIST' EQ '').BYP1700
         AIF   ('&CONDLIST'(1,1) EQ '''').BYP1710
         LA    14,&CONDLIST             GET ADDRESS OF COND NAME
         AGO   .BYP1750
.BYP1700 ANOP
         LA    14,SPFNULLC              GET DUMMY PARM FOR COND
         AGO   .BYP1750
.BYP1710 ANOP
         LA    14,SPFC&NDX              GET ADDRESS OF GENERATED COND
.BYP1750 ANOP
.*
         ST    14,&TEMP+32              STORE PARAMETERS INTO LIST
         OI    &TEMP+32,X'80'           MARK LAST PARM
         LA    1,&TEMP                  GET PARAMETER LIST ADDRESS
         L     15,&LOAD                 LOAD ADDRESS OF ISPF
         BALR  14,15                    CALL ISPF
         AIF   (NOT &SPFVAR).BYVAR
         AIF   (&SPFVARC LT 9).ERRVAR
         AGO   .BYPVARS
.BYVAR   AIF   (&SPFVARS GE 9).BYPVARS
&SPFVARS SETA  9
.BYPVARS ANOP
         MEXIT
.*
.**********************************************************************
.*       MACRO FORMAT I
.**********************************************************************
.*
.MFI     ANOP
         CNOP  0,4
         BAL   1,SPFE&NDX               BRANCH AROUND LITERALS
         DC    A(TBSCAN)                TBSCAN LITERAL
.*
         AIF   ('&TABLE' EQ '').BYP2000
         AIF   ('&TABLE'(1,1) EQ '''').BYP2010
         DC    A(&TABLE)                ADDRESS OF TABLE NAME
         AGO   .BYP2050
.BYP2000 ANOP
         DC    A(SPFNULLC)              DUMMY PARM FOR TABLE NAME
         AGO   .BYP2050
.BYP2010 ANOP
         DC    A(SPFT&NDX)              ADDRESS OF GENERATED TABLE
.BYP2050 ANOP
.*
         AIF   ('&ARGLIST' EQ '').BYP2100
         AIF   ('&ARGLIST'(1,1) EQ '''').BYP2110
         DC    A(&ARGLIST)              ADDRESS OF ARGLIST NAME
         AGO   .BYP2150
.BYP2100 ANOP
         DC    A(SPFNULLC)              DUMMY PARM FOR ARGLIST NAME
         AGO   .BYP2150
.BYP2110 ANOP
         DC    A(SPFA&NDX)              ADDRESS OF GENERATED ARGLIST
.BYP2150 ANOP
.*
         AIF   ('&SAVENAME' EQ '').BYP2200
         AIF   ('&SAVENAME'(1,1) EQ '''').BYP2210
         DC    A(&SAVENAME)             ADDRESS OF VAR NAME
         AGO   .BYP2250
.BYP2200 ANOP
         DC    A(SPFNULLC)              DUMMY PARM FOR VAR
         AGO   .BYP2250
.BYP2210 ANOP
         DC    A(SPFV&NDX)              ADDRESS OF GENERATED VAR
.BYP2250 ANOP
.*
         AIF   ('&ROWID' EQ '').BYP2300
         AIF   ('&ROWID'(1,1) EQ '''').BYP2310
         DC    A(&ROWID)                ADDRESS OF ROWID NAME
         AGO   .BYP2350
.BYP2300 ANOP
         DC    A(SPFNULLC)              DUMMY PARM FOR ROWID
         AGO   .BYP2350
.BYP2310 ANOP
         DC    A(SPFR&NDX)              ADDRESS OF GENERATED ROWID
.BYP2350 ANOP
.*
         AIF   ('&DIRECTION' EQ '').BYP2400
         AIF   ('&DIRECTION'(1,1) EQ '''').BYP2405
         DC    A(&DIRECTION)            ADDRESS OF ISPF KEYWORD
         AGO   .BYP2450
.BYP2400 ANOP
         DC    A(SPFNULLC)              DUMMY PARM FOR ROW
         AGO   .BYP2450
.BYP2405 ANOP
         DC    A(SPFD&NDX)              GENNED NEXT/PREVIOUS
.BYP2450 ANOP
.*
         AIF   ('&NOREAD' EQ '').BYP2500
         AIF   ('&NOREAD'(1,1) EQ '''').BYP2505
         DC    A(&NOREAD)               ADDRESS OF ISPF KEYWORD
         AGO   .BYP2550
.BYP2500 ANOP
         DC    A(SPFNULLC)              DUMMY PARM FOR CSRPOS
         AGO   .BYP2550
.BYP2505 ANOP
         DC    A(SPFN&NDX)              GENNED NOREAD KEYWORD
.BYP2550 ANOP
.*
         AIF   ('&POSITION' EQ '').BYP2600
         AIF   ('&POSITION'(1,1) EQ '''').BYP2610
         DC    A(&POSITION)             ADDRESS OF POS NAME
         AGO   .BYP2650
.BYP2600 ANOP
         DC    A(SPFNULLC)              ADDRESS OF DUMMY PARM
         AGO   .BYP2650
.BYP2610 ANOP
         DC    A(SPFP&NDX)              GENERATED POS VAR NAME
.BYP2650 ANOP
.*
         AIF   ('&CONDLIST' EQ '').BYP2800
         AIF   ('&CONDLIST'(1,1) EQ '''').BYP2810
         DC    A(&CONDLIST+X'80000000') ADDRESS OF COND NAME
         AGO   .BYP2850
.BYP2800 ANOP
         DC    A(SPFNULLC+X'80000000')  DUMMY PARM FOR COND
         AGO   .BYP2850
.BYP2810 ANOP
         DC    A(SPFC&NDX+X'80000000')  ADDRESS OF GENERATED COND
.BYP2850 ANOP
SPFE&NDX DS    0H                       MARK LAST PARM
         L     15,&LOAD                 LOAD ADDRESS OF ISPF
         BALR  14,15                    CALL ISPF
         MEXIT
.*
.**********************************************************************
.*       MACRO ERRORS
.**********************************************************************
.*
.ERRMF   ANOP
         MNOTE 16,'INVALID MACRO FORMAT SPECIFIED - FOUND ''&MF(1)'''
         MEXIT
.ERR1    ANOP
         MNOTE 16,'TABLE NAME IS REQUIRED'
         MEXIT
.ERR3    ANOP
         MNOTE 16,'EXPECTED NOREAD KEYWORD - FOUND ''&NOREAD'''
         MEXIT
.ERRVAR  ANOP
         MNOTE 16,'ISPF PARMS MACRO WILL NOT HOLD FULL PARMLIST'
         MNOTE *,' ISPF PARMS MACRO GENERATE PARMLIST TOO SMALL'
         MEXIT
         MEND
