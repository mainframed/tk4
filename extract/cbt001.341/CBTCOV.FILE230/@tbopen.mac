         MACRO
&NAME    @TBOPEN &TABLE,                TABLE NAME                     *
               &WRITE,                  WRITE/NOWRITE PARM             *
               &LIB,                    LIBRARY                        *
               &SHARE,                  SHARE OPERAND                  *
               &LOAD=ISPLINK,           ISPLINK ADDRESS                *
               &MF=I                    MACRO FORMAT
.*
.**********************************************************************
.*       GLOBALS
.**********************************************************************
.*
         GBLB  &SPFTBOPEN,&SPFNULLC,&SPFDEBUG,&SPFVAR
         GBLB  &SPFSHARE,&SPFWRITE,&SPFNOWRITE
         GBLA  &SPFVARS,&SPFVARC
         LCLC  &NDX,&TEMP
&NDX     SETC  '&SYSNDX'
         AIF   ('&MF(2)' NE '').BYP0
&TEMP    SETC  'SPFPARMS'
         AGO   .BYP00
.BYP0    ANOP
&TEMP    SETC  '&MF(2)'
.BYP00   ANOP
         AIF   ('&NAME' EQ '').BYP10
&NAME    DS    0H
.BYP10   ANOP
.*
.**********************************************************************
.*       EDITS
.**********************************************************************
.*
         AIF   ('&MF(1)' NE 'I' AND                                    *
               '&MF(1)' NE 'E').ERRMF
         AIF   ('&TABLE' EQ '').ERR1
         AIF   ('&SHARE' NE '' AND '&SHARE' NE 'SHARE').ERR2
         AIF   ('&WRITE' NE '' AND '&WRITE' NE 'WRITE' AND             *
               '&WRITE' NE 'NOWRITE').ERR3
.*
.**********************************************************************
.*       CHECK TO SEE IF A BRANCH AROUND LITERALS IS REQUIRED
.**********************************************************************
.*
         AIF   (NOT &SPFTBOPEN).GENLIT
         AIF   (NOT &SPFNULLC).GENLIT
         AIF   ('&TABLE' EQ '').CNT1
         AIF   ('&TABLE'(1,1) EQ '''').GENLIT
.CNT1    ANOP
         AIF   ('&WRITE' EQ 'WRITE' AND NOT &SPFWRITE).GENLIT
         AIF   ('&WRITE' EQ 'NOWRITE' AND NOT &SPFNOWRITE).GENLIT
         AIF   ('&LIB' EQ '').CNT3
         AIF   ('&LIB'(1,1) EQ '''').GENLIT
.CNT3    ANOP
         AIF   ('&SHARE' EQ '').CNT4
         AIF   ('&SHARE'(1,1) EQ '''').GENLIT
.CNT4    ANOP
         AGO   .BRAREA
.GENLIT  ANOP
.*
.*********************************************************************
.*       GENERATE THE BRANCH AROUND LITERALS AND THE LITERALS
.*********************************************************************
.*
         B     SPF&NDX
         AIF   (&SPFTBOPEN).BYP20
TBOPEN   DC    CL8'TBOPEN'              ISPF KEYWORD
&SPFTBOPEN SETB 1
.BYP20   ANOP
         AIF   ('&TABLE' EQ '').BYP30
         AIF   ('&TABLE'(1,1) NE '''').BYP30
SPFT&NDX DC    CL8&TABLE                GENERATED TABLE NAME
.BYP30   ANOP
         AIF   ('&WRITE' EQ '').BYP40
         AIF   ('&WRITE' NE 'WRITE' OR &SPFWRITE).BYP35
WRITE    DC    CL8'WRITE'               ISPF KEYWORD
&SPFWRITE SETB 1
.BYP35   ANOP
         AIF   ('&WRITE' NE 'NOWRITE' OR &SPFNOWRITE).BYP40
NOWRITE  DC    CL8'NOWRITE'             ISPF KEYWORD
&SPFNOWRITE SETB 1
.BYP40   ANOP
         AIF   ('&LIB' EQ '').BYP50
         AIF   ('&LIB'(1,1) NE '''').BYP50
SPFL&NDX DC    CL8&LIB                  GENERATED LIB NAME
.BYP50   ANOP
         AIF   ('&SHARE' EQ '' OR &SPFSHARE).BYP60
SHARE    DC    CL8'SHARE'               ISPF KEYWORD
&SPFSHARE SETB 1
.BYP60   ANOP
         AIF   (&SPFNULLC).BYP120
SPFNULLC DC    CL8'        '            DUMMY ISPF FIELD
&SPFNULLC SETB  1
.BYP120  ANOP
SPF&NDX  DS    0H
.BRAREA  ANOP
.*
.*********************************************************************
.*       GENERATE THE CALL
.*********************************************************************
.*
         AIF   ('&MF(1)' EQ 'I').MFI
         LA    14,TBOPEN                GET ADDRESS OF TBOPEN KEYWORD
.*
         AIF   ('&TABLE' EQ '').BYP1000
         AIF   ('&TABLE'(1,1) EQ '''').BYP1010
         LA    15,&TABLE                GET ADDRESS OF TABLE NAME
         AGO   .BYP1050
.BYP1000 ANOP
         LA    15,SPFNULLC              GET DUMMY PARM FOR TABLE
         AGO   .BYP1050
.BYP1010 ANOP
         LA    15,SPFT&NDX              GET ADDRESS OF GENERATED TABLE
.BYP1050 ANOP
.*
         AIF   ('&WRITE' EQ '').BYP1100
         LA    0,&WRITE                 GET ADDRESS OF WRITE NAME
         AGO   .BYP1150
.BYP1100 ANOP
         LA    0,SPFNULLC               GET DUMMY PARM FOR WRITE
.BYP1150 ANOP
.*
         AIF   ('&LIB' EQ '').BYP1200
         AIF   ('&LIB'(1,1) EQ '''').BYP1210
         LA    1,&LIB                   GET ADDRESS OF LIB NAME
         AGO   .BYP1250
.BYP1200 ANOP
         LA    1,SPFNULLC               GET DUMMY PARM FOR LIB
         AGO   .BYP1250
.BYP1210 ANOP
         LA    1,SPFL&NDX               GET ADDRESS OF GENERATED LIB
.BYP1250 ANOP
.*
         STM   14,1,&TEMP               STORE PARAMETERS INTO LIST
         AIF   ('&SHARE' EQ '').BYP1300
         LA    14,SHARE                 GET ADDRESS OF FIELD NAME
         AGO   .BYP1350
.BYP1300 ANOP
         LA    14,SPFNULLC              GET DUMMY PARM FOR FIELD
.BYP1350 ANOP
.*
         ST    14,&TEMP+16              STORE PARAMETERS INTO LIST
         OI    &TEMP+16,X'80'           MARK LAST PARM
         LA    1,&TEMP                  GET PARAMETER LIST ADDRESS
         L     15,&LOAD                 LOAD ADDRESS OF ISPF
         BALR  14,15                    CALL ISPF
         AIF   (NOT &SPFVAR).BYVAR
         AIF   (&SPFVARC LT 5).ERRVAR
         AGO   .BYPVARS
.BYVAR   AIF   (&SPFVARS GE 5).BYPVARS
&SPFVARS SETA  5
.BYPVARS ANOP
         MEXIT
.*
.**********************************************************************
.*       MACRO FORMAT I
.**********************************************************************
.*
.MFI     ANOP
         CNOP  0,4
         BAL   1,SPFE&NDX               BRANCH AROUND LITERALS
         DC    A(TBOPEN)                TBOPEN LITERAL
.*
         AIF   ('&TABLE' EQ '').BYP2000
         AIF   ('&TABLE'(1,1) EQ '''').BYP2010
         DC    A(&TABLE)                ADDRESS OF TABLE NAME
         AGO   .BYP2050
.BYP2000 ANOP
         DC    A(SPFNULLC)              DUMMY PARM FOR TABLE NAME
         AGO   .BYP2050
.BYP2010 ANOP
         DC    A(SPFT&NDX)              ADDRESS OF GENERATED TABLE
.BYP2050 ANOP
.*
         AIF   ('&WRITE' EQ '').BYP2100
         DC    A(&WRITE)                ADDRESS OF WRITE NAME
         AGO   .BYP2150
.BYP2100 ANOP
         DC    A(SPFNULLC)              DUMMY PARM FOR WRITE NAME
.BYP2150 ANOP
.*
         AIF   ('&LIB' EQ '').BYP2200
         AIF   ('&LIB'(1,1) EQ '''').BYP2210
         DC    A(&LIB)                  ADDRESS OF LIB NAME
         AGO   .BYP2250
.BYP2200 ANOP
         DC    A(SPFNULLC)              DUMMY PARM FOR LIB
         AGO   .BYP2250
.BYP2210 ANOP
         DC    A(SPFL&NDX)              ADDRESS OF GENERATED LIB
.BYP2250 ANOP
.*
         AIF   ('&SHARE' EQ '').BYP2300
         DC    A(SHARE+X'80000000')     ADDRESS OF SHARE NAME
         AGO   .BYP2350
.BYP2300 ANOP
         DC    A(SPFNULLC+X'80000000')  DUMMY PARM FOR SHARE
.BYP2350 ANOP
SPFE&NDX DS    0H                       MARK LAST PARM
         L     15,&LOAD                 LOAD ADDRESS OF ISPF
         BALR  14,15                    CALL ISPF
         MEXIT
.*
.**********************************************************************
.*       MACRO ERRORS
.**********************************************************************
.*
.ERRMF   ANOP
         MNOTE 16,'INVALID MACRO FORMAT SPECIFIED - FOUND ''&MF(1)'''
         MEXIT
.ERR1    ANOP
         MNOTE 16,'TABLE NAME IS REQUIRED'
         MEXIT
.ERR2    ANOP
         MNOTE 16,'SHARE KEYWORD EXPECTED - FOUND ''&SHARE'''
         MEXIT
.ERR3    ANOP
         MNOTE 16,'WRITE NOWRITE KEYWORD EXPECTED - FOUND ''&WRITE'''
         MEXIT
.ERRVAR  ANOP
         MNOTE 16,'ISPF PARMS MACRO WILL NOT HOLD FULL PARMLIST'
         MNOTE *,' ISPF PARMS MACRO GENERATE PARMLIST TOO SMALL'
         MEXIT
         MEND
