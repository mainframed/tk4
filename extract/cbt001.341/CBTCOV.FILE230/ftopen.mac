         MACRO
&NAME    FTOPEN &P1,&TEMP=,             TEMP KEYWORD                   *
               &LOAD=ISPLINK,           ISPLINK ADDRESS                *
               &MF=I                    MACRO FORMAT
.*
.**********************************************************************
.*       GLOBALS
.**********************************************************************
.*
         GBLB  &SPFFTOPEN,&SPFDEBUG,&SPFVAR
         GBLA  &SPFVARS,&SPFVARC
         LCLC  &NDX,&TEM
         GBLC  &SPFLVL
         AIF   ('&SPFLVL' NE '1').NL1
&NAME    @FTOPEN &P1,LOAD=&LOAD,MF=&MF
         MEXIT
.NL1     ANOP
         AIF   ('&P1' NE '').LVLERR
         AGO   .NL2
.LVLERR  ANOP
         MNOTE 12,'POSITIONAL PARAMETER FOUND - NOT ALLOWED'
         MEXIT
.NL2     ANOP
&NDX     SETC  '&SYSNDX'
.*
.**********************************************************************
.*       GENERAL EDITS
.**********************************************************************
.*
.*
         AIF   ('&NAME' EQ '').BYP10
&NAME    DS    0H
.BYP10   ANOP
         AIF   ('&MF(2)' NE '').BYP15
&TEM     SETC  'SPFPARMS'
         AGO   .BYP17
.BYP15   ANOP
&TEM     SETC  '&MF(2)'
.BYP17   ANOP
         AIF   ('&TEMP' EQ '').K1
         AIF   ('&TEMP'(1,1) NE '''' OR '&TEMP' EQ '''TEMP''').K1
         MNOTE 4,'WARNING - TEMP KEYWORD EXPECTED IN QUOTES'
.K1      ANOP
.*
.**********************************************************************
.*       CHECK TO SEE IF A BRANCH AROUND LITERALS IS REQUIRED
.**********************************************************************
.*
         AIF   (NOT &SPFFTOPEN).GENLIT
         AIF   ('&TEMP' EQ '').CNT1
         AIF   ('&TEMP'(1,1) EQ '''').GENLIT
.CNT1    AGO   .BRAREA
.GENLIT  ANOP
.*
.*********************************************************************
.*       GENERATE THE BRANCH AROUND LITERALS AND THE LITERALS
.*********************************************************************
.*
         B     SPF&NDX
         AIF   (&SPFFTOPEN).BYP20
FTOPEN   DC    CL8'FTOPEN'              ISPF KEYWORD
&SPFFTOPEN SETB 1
.BYP20   ANOP
         AIF   ('&TEMP' EQ '').BYP60
         AIF   ('&TEMP'(1,1) NE '''').BYP60
SPFX&NDX DC    CL8&TEMP                 ISPF TEMP KEYWORD
.BYP60   ANOP
SPF&NDX  DS    0H
.BRAREA  ANOP
.*
.*********************************************************************
.*       GENERATE THE CALL
.*********************************************************************
.*
         AIF   ('&MF(1)' EQ 'I').MFI
         LA    14,FTOPEN                GET ADDRESS OF FTOPEN KEYWORD
         AIF   ('&TEMP' EQ '').BYP170
         AIF   ('&TEMP'(1,1) EQ '''').BYP175
         LA    15,&TEMP                 GET ADDRESS OF TEMP
         AGO   .BYP180
.BYP170  ANOP
         LA    15,SPFNULLC              GET DUMMY PARM FOR TEMP
         AGO   .BYP180
.BYP175  ANOP
         LA    15,SPFX&NDX              GET GENNED TEMP KEYWORD
.BYP180  ANOP
         STM   14,15,&TEM               STORE PARAMETERS
         OI    &TEM+4,X'80'             MARK LAST PARM
         LA    1,&TEM                   GET PARAMETER LIST ADDRESS
         L     15,&LOAD                 LOAD ADDRESS OF ISPF
         BALR  14,15                    CALL ISPF
         AIF   (NOT &SPFVAR).BYVAR
         AIF   (&SPFVARC LT 2).ERRVAR
         AGO   .BYPVARS
.BYVAR   AIF   (&SPFVARS GE 2).BYPVARS
&SPFVARS SETA  2
.BYPVARS ANOP
         MEXIT
.*
.**********************************************************************
.*       MACRO FORMAT I
.**********************************************************************
.*
.MFI     ANOP
         CNOP  0,4
         BAL   1,SPFE&NDX               BRANCH AROUND LITERALS
         DC    A(FTOPEN)                BROWSE LITERAL
         AIF   ('&TEMP' EQ '').BYP270
         AIF   ('&TEMP'(1,1) EQ '''').BYP275
         DC    A(&TEMP+X'80000000')     ADDRESS OF TEMP KEYWORD
         AGO   .BYP300
.BYP270  ANOP
         DC    A(SPFNULLC+X'80000000')  DUMMY PARM FOR TEMP
         AGO   .BYP300
.BYP275  ANOP
         DC    A(SPFX&NDX+X'80000000')  GENNED TEMP KEYWORD
.BYP300  ANOP
SPFE&NDX DS    0H                       MARK LAST PARM
         L     15,&LOAD                 LOAD ADDRESS OF ISPF
         BALR  14,15                    CALL ISPF
         MEXIT
.*
.**********************************************************************
.*       MACRO ERRORS
.**********************************************************************
.*
.ERR1    ANOP
         MNOTE 16,'INVALID VALUE FOR TEMP PARM - FOUND ''&TEMP'''
         MEXIT
.ERR2    ANOP
         MNOTE 16,'INVALID MACRO FORMAT SPECIFIED - FOUND ''&MF(1)'''
         MEXIT
.ERRVAR  ANOP
         MNOTE 16,'ISPF PARMS MACRO WILL NOT HOLD FULL PARMLIST'
         MNOTE *,' ISPF PARMS MACRO GENERATE PARMLIST TOO SMALL'
         MEXIT
         MEND
