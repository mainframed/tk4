         MACRO
&NAME    SELECT &BUFLEN,                BUFFER LENGTH (POS)            *
               &BUF,                    BUFFER ADDRESS (POS)           *
               &LOAD=ISPLINK,           ISPLINK ADDRESS                *
               &MF=I                    MACRO FORMAT
.*
.**********************************************************************
.*       GLOBALS
.**********************************************************************
.*
         GBLB  &SPFSELECT               WAS A SELECT GENERATED
         GBLB  &SPFDEBUG                DEBUG MODE SWITCH
         GBLB  &SPFVAR                  WAS ISPF PARMS ISSUED
         GBLA  &SPFVARS                 NUMBER OF FULLWORDS REQUIRED
         GBLA  &SPFVARC                 NUMBER OF FULLWORDS USED
         LCLC  &NDX                     REDEFINE MACRO INDEX SYSNDX
         LCLC  &P1                      INTERNAL- FIRST CALL PARM
         LCLC  &P2                      INTERNAL- SECOND CALL PARM
         LCLA  &LEN                     INTERNAL- LENGTH OF BUFFER GEN
.*
.**********************************************************************
.*       SET INTERNAL GLOBALS
.**********************************************************************
.*
&NDX     SETC  '&SYSNDX'
.*
.**********************************************************************
.*       GENERAL EDITS
.**********************************************************************
.*
         AIF   ('&BUF' EQ '').ERR1
         AIF   ('&BUF'(1,1) EQ '''' AND '&BUFLEN' NE '').WARN
.CONT    AIF   ('&MF(1)' NE 'I' AND '&MF(1)' NE 'E').ERR2
.*
         AIF   ('&NAME' EQ '').BYP10
&NAME    DS    0H
.BYP10   ANOP
.*
.**********************************************************************
.*       CHECK TO SEE IF A BRANCH AROUND LITERALS IS REQUIRED
.**********************************************************************
.*
         AIF   (NOT &SPFSELECT).GENLIT
         AIF   ('&BUFLEN' EQ '').CNT1
         AIF   (T'&BUFLEN EQ 'N' AND '&BUFLEN'(1,1) NE '(').GENLIT
.CNT1    AIF   ('&BUF' EQ '').CNT2
         AIF   ('&BUF'(1,1) EQ '''').GENLIT
.CNT2    AGO   .BRAREA
.GENLIT  ANOP
.*
.*********************************************************************
.*       GENERATE THE BRANCH AROUND LITERALS AND THE LITERALS
.*********************************************************************
.*
         B     SPF&NDX
         AIF   (&SPFSELECT).BYP20
SELECT   DC    CL8'SELECT'              ISPF KEYWORD
&SPFSELECT SETB 1
.BYP20   ANOP
         AIF   ('&BUFLEN' EQ '').BYP40
         AIF   (T'&BUFLEN NE 'N' OR '&BUFLEN'(1,1) EQ '(').BYP40
         AIF   ('&BUF'(1,1) EQ '''').BYP40
SPFL&NDX DC    F'&BUFLEN'               BUFFER LENGTH GENERATED
.BYP40   ANOP
         AIF   ('&BUF'(1,1) NE '''').BYP50
         CNOP  0,4
&LEN     SETA  K'&BUF+3
&LEN     SETA  &LEN/4*4
SPFB&NDX DC    CL&LEN&BUF               GENERATED SELECT BUFFER
SPFL&NDX DC    F'&LEN'                  GENERATED SELECT LENGTH
.BYP50   ANOP
SPF&NDX  DS    0H
.BRAREA  ANOP
.*
.*********************************************************************
.*       GENERATE THE CALL
.*********************************************************************
.*
         AIF   ('&BUFLEN' EQ '').BYP100
         AIF   ('&BUF'(1,1) EQ '''').BYP100
         AIF   (T'&BUFLEN EQ 'N' AND '&BUFLEN'(1,1) NE '(').BYP100
&P1      SETC  '&BUFLEN'
         AGO   .BYP120
.BYP100  ANOP
&P1      SETC  'SPFL&NDX'
.BYP120  ANOP
         AIF   ('&BUF'(1,1) EQ '(').BYP130
         AIF   ('&BUF'(1,1) NE '''').BYP130
&P2      SETC  'SPFB&NDX'
         AGO   .BYP150
.BYP130  ANOP
&P2      SETC  '&BUF'
.BYP150  ANOP
         L     15,&LOAD                 LOAD ADDRESS OF ISPLINK
         AIF   ('&MF(1)' EQ 'E' AND '&MF(2)' EQ '').CALL2
         CALL  (15),(SELECT,&P1,&P2),VL,MF=&MF
         AGO   .VARIT
.CALL2   ANOP
         CALL  (15),(SELECT,&P1,&P2),VL,MF=(E,SPFPARMS)
.VARIT   AIF   (NOT &SPFVAR).BYVAR      WAS ISPF PARMS ISSUED
         AIF   (&SPFVARC LT 3).ERRVAR   WAS PARMLIST TOO LITTLE
         AGO   .BYPVARS
.BYVAR   AIF   (&SPFVARS GE 3).BYPVARS  SET PARMLIST SIZE TO AT LEAST
&SPFVARS SETA  3                        3 FULLWORDS
.BYPVARS ANOP
         MEXIT
.*
.**********************************************************************
.*       MACRO ERRORS
.**********************************************************************
.*
.ERR1    ANOP
         MNOTE 16,'BUFFER FOR SELECT COMMAND IS REQUIRED'
         MEXIT
.ERR2    ANOP
         MNOTE 16,'INVALID MACRO FORMAT SPECIFIED - FOUND ''&MF(1)'''
         MEXIT
.ERR5    ANOP
         MNOTE 16,'SELECT BUFFER MAY BE REGISTER ONLY WHEN MF=E'
         MEXIT
.WARN    ANOP
         MNOTE 4,'BUFFER LENGTH IS IGNORED FOR LITERAL BUFFER'
         AGO   .CONT
.ERRVAR  ANOP
         MNOTE 16,'ISPF PARMS MACRO WILL NOT HOLD FULL PARMLIST'
         MNOTE *,' ISPF PARMS MACRO GENERATE PARMLIST TOO SMALL'
         MEXIT
         MEND
