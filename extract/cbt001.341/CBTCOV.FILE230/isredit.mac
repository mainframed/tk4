         MACRO
&NAME    ISREDIT &BUFLEN,               MEMBER NAME                    *
               &BUF,                    LIBRARY NAME                   *
               &LOAD=ISPLINK,           ISPLINK ADDRESS                *
               &MF=I                    MACRO FORMAT
.*
.**********************************************************************
.*       GLOBALS
.**********************************************************************
.*
         GBLB  &SPFISREDIT,&SPFDEBUG,&SPFVAR
         GBLA  &SPFVARS,&SPFVARC
         LCLC  &NDX,&TEMP
         LCLA  &LEN
&NDX     SETC  '&SYSNDX'
.*
.**********************************************************************
.*       GENERAL EDITS
.**********************************************************************
.*
         AIF   ('&BUF' EQ '').ERR1
         AIF   ('&BUF'(1,1) EQ '''' AND '&BUFLEN' NE '').WARN
.CONT    ANOP
         AIF   ('&MF(1)' NE 'I' AND                                    *
               '&MF(1)' NE 'E').ERR2
.*
         AIF   ('&NAME' EQ '').BYP10
&NAME    DS    0H
.BYP10   ANOP
         AIF   ('&MF(2)' NE '').BYP15
&TEMP    SETC  'SPFPARMS'
         AGO   .BYP17
.BYP15   ANOP
&TEMP    SETC  '&MF(2)'
.BYP17   ANOP
.*
.**********************************************************************
.*       CHECK TO SEE IF A BRANCH AROUND LITERALS IS REQUIRED
.**********************************************************************
.*
         AIF   (NOT &SPFISREDIT).GENLIT
         AIF   (T'&BUFLEN EQ 'N').GENLIT
.CNT1    AIF   ('&BUF' EQ '').CNT2
         AIF   ('&BUF'(1,1) EQ '''').GENLIT
.CNT2    AGO   .BRAREA
.GENLIT  ANOP
.*
.*********************************************************************
.*       GENERATE THE BRANCH AROUND LITERALS AND THE LITERALS
.*********************************************************************
.*
         B     SPF&NDX
         AIF   (&SPFISREDIT).BYP20
ISREDIT  DC    CL8'ISREDIT'             ISPF KEYWORD
&SPFISREDIT SETB 1
.BYP20   ANOP
         AIF   ('&BUFLEN' EQ '').BYP40
         AIF   (T'&BUFLEN NE 'N').BYP40
         AIF   ('&BUF'(1,1) EQ '''').BYP40
SPFL&NDX DC    F'&BUFLEN'               BUFFER LENGTH GENERATED
.BYP40   ANOP
         AIF   ('&BUF'(1,1) NE '''').BYP50
         CNOP  0,4
&LEN     SETA  K'&BUF+3
&LEN     SETA  &LEN/4*4
SPFB&NDX DC    CL&LEN&BUF               GENERATED ISREDIT BUFFER
SPFL&NDX DC    F'&LEN'                  GENERATED ISREDIT LENGTH
.BYP50   ANOP
SPF&NDX  DS    0H
.BRAREA  ANOP
.*
.*********************************************************************
.*       GENERATE THE CALL
.*********************************************************************
.*
         AIF   ('&MF(1)' EQ 'I').MFI
         LA    14,ISREDIT               GET ADDRESS OF ISREDIT KEYWORD
         AIF   ('&BUFLEN' EQ '').BYP100
         AIF   ('&BUF'(1,1) EQ '''').BYP100
         AIF   (T'&BUFLEN EQ 'N').BYP100
         LA    15,&BUFLEN               GET ADDRESS OF BUFFER LENGTH
         AGO   .BYP120
.BYP100  ANOP
         LA    15,SPFL&NDX              GET ADDRESS OF GENNED LENGTH
.BYP120  ANOP
         AIF   ('&BUF'(1,1) NE '''').BYP130
         LA    0,SPFB&NDX               GET ADDRESS OF GENERATED BUF
         AGO   .BYP150
.BYP130  ANOP
         LA    0,&BUF                   GET ADDRESS OF BUFFER
         AGO   .BYP150
.BYP150  ANOP
         STM   14,0,&TEMP               STORE PARAMETERS
         OI    &TEMP+8,X'80'            MARK LAST PARM
         LA    1,&TEMP                  GET PARAMETER LIST ADDRESS
         L     15,&LOAD                 LOAD ADDRESS OF ISPF
         BALR  14,15                    CALL ISPF
         AIF   (NOT &SPFVAR).BYVAR
         AIF   (&SPFVARC LT 3).ERRVAR
         AGO   .BYPVARS
.BYVAR   AIF   (&SPFVARS GE 3).BYPVARS
&SPFVARS SETA  3
.BYPVARS ANOP
         MEXIT
.*
.**********************************************************************
.*       MACRO FORMAT I
.**********************************************************************
.*
.MFI     ANOP
         CNOP  0,4
         BAL   1,SPFE&NDX               BRANCH AROUND LITERALS
         DC    A(ISREDIT)               BROWSE LITERAL
         AIF   ('&BUFLEN' EQ '').BYP200
         AIF   ('&BUF'(1,1) EQ '''').BYP200
         AIF   (T'&BUFLEN EQ 'N').BYP200
         DC    A(&BUFLEN)               ADDRESS OF BUFFER LENGTH
         AGO   .BYP220
.BYP200  ANOP
         DC    A(SPFL&NDX)              ADDRESS OF GENNED LENGTH
.BYP220  ANOP
         AIF   ('&BUF'(1,1) NE '''').BYP230
         DC    A(SPFB&NDX+X'80000000')  ADDRESS OF GENERATED BUFFER
         AGO   .BYP250
.BYP230  ANOP
         DC    A(&BUF+X'80000000')      ADDRESS OF BUFFER
.BYP250  ANOP
SPFE&NDX DS    0H                       MARK LAST PARM
         L     15,&LOAD                 LOAD ADDRESS OF ISPF
         BALR  14,15                    CALL ISPF
         MEXIT
.*
.**********************************************************************
.*       MACRO ERRORS
.**********************************************************************
.*
.ERR1    ANOP
         MNOTE 16,'BUFFER FOR ISREDIT COMMAND IS REQUIRED'
         MEXIT
.ERR2    ANOP
         MNOTE 16,'INVALID MACRO FORMAT SPECIFIED - FOUND ''&MF(1)'''
         MEXIT
.WARN    ANOP
         MNOTE 4,'BUFFER LENGTH IS IGNORED FOR LITERAL BUFFER'
         AGO   .CONT
.ERRVAR  ANOP
         MNOTE 16,'ISPF PARMS MACRO WILL NOT HOLD FULL PARMLIST'
         MNOTE *,' ISPF PARMS MACRO GENERATE PARMLIST TOO SMALL'
         MEXIT
         MEND
