         MACRO
&NAME    TBSORT &TABLE,                 TABLE NAME                     *
               &FIELDS=,                FIELDS NAME-LIST               *
               &LOAD=ISPLINK,           ISPLINK ADDRESS                *
               &MF=I                    MACRO FORMAT
.*
.**********************************************************************
.*       GLOBALS
.**********************************************************************
.*
         GBLB  &SPFTBSORT,&SPFDEBUG,&SPFVAR
         GBLA  &SPFVARS,&SPFVARC
         LCLC  &NDX,&TEMP
&NDX     SETC  '&SYSNDX'
.*
.**********************************************************************
.*       GENERAL EDITS
.**********************************************************************
.*
         AIF   ('&TABLE' EQ '').ERR3
         AIF   ('&FIELDS' EQ '').ERR4
         AIF   ('&MF(1)' NE 'I' AND                                    *
               '&MF(1)' NE 'E').ERR2
.*
         AIF   ('&NAME' EQ '').BYP10
&NAME    DS    0H
.BYP10   ANOP
         AIF   ('&MF(2)' NE '').BYP15
&TEMP    SETC  'SPFPARMS'
         AGO   .BYP17
.BYP15   ANOP
&TEMP    SETC  '&MF(2)'
.BYP17   ANOP
.*
.**********************************************************************
.*       CHECK TO SEE IF A BRANCH AROUND LITERALS IS REQUIRED
.**********************************************************************
.*
         AIF   (NOT &SPFTBSORT).GENLIT
         AIF   ('&TABLE' EQ '').CNT1
         AIF   ('&TABLE'(1,1) EQ '''').GENLIT
.CNT1    ANOP
         AIF   ('&FIELDS' EQ '').CNT2
         AIF   ('&FIELDS'(1,1) EQ '''').GENLIT
.CNT2    AGO   .BRAREA
.GENLIT  ANOP
.*
.*********************************************************************
.*       GENERATE THE BRANCH AROUND LITERALS AND THE LITERALS
.*********************************************************************
.*
         B     SPF&NDX
         AIF   (&SPFTBSORT).BYP20
TBSORT   DC    CL8'TBSORT'              ISPF KEYWORD
&SPFTBSORT SETB 1
.BYP20   ANOP
         AIF   ('&TABLE' EQ '').BYP40
         AIF   ('&TABLE'(1,1) NE '''').BYP40
SPFT&NDX DC    CL8&TABLE                GENERATED TABLE NAME
.BYP40   ANOP
         AIF   ('&FIELDS' EQ '').BYP50
         AIF   ('&FIELDS'(1,1) NE '''').BYP50
SPFS&NDX DC    C&FIELDS                 GENERATED SORT KEYS
.BYP50   ANOP
SPF&NDX  DS    0H
.BRAREA  ANOP
.*
.*********************************************************************
.*       GENERATE THE CALL
.*********************************************************************
.*
         AIF   ('&MF(1)' EQ 'I').MFI
         LA    14,TBSORT                GET ADDRESS OF TBSORT KEYWORD
         AIF   ('&TABLE' EQ '').BYP100
         AIF   ('&TABLE'(1,1) EQ '''').BYP110
         LA    15,&TABLE                GET ADDRESS OF TABLE
         AGO   .BYP120
.BYP110  ANOP
         LA    15,SPFT&NDX              GET ADDRESS OF GENERATED TABLE
         AGO   .BYP120
.BYP100  ANOP
         LA    15,SPFNULLC              GET DUMMY PARM FOR TABLE
.BYP120  ANOP
         AIF   ('&FIELDS' EQ '').BYP140
         AIF   ('&FIELDS'(1,1) EQ '''').BYP130
         LA    0,&FIELDS                GET ADDRESS OF SORT KEYS
         AGO   .BYP150
.BYP130  ANOP
         LA    0,SPFS&NDX               GET ADDRESS OF GENNED FIELDS
         AGO   .BYP150
.BYP140  ANOP
         LA    0,SPFNULLC               GET DUMMY PARM FOR SORT KEY
.BYP150  ANOP
         STM   14,0,&TEMP               STORE PARAMETERS
         OI    &TEMP+8,X'80'            MARK LAST PARM
         LA    1,&TEMP                  GET PARAMETER LIST ADDRESS
         L     15,&LOAD                 LOAD ADDRESS OF ISPF
         BALR  14,15                    CALL ISPF
         AIF   (NOT &SPFVAR).BYVAR
         AIF   (&SPFVARC LT 3).ERRVAR
         AGO   .BYPVARS
.BYVAR   AIF   (&SPFVARS GE 3).BYPVARS
&SPFVARS SETA  3
.BYPVARS ANOP
         MEXIT
.*
.**********************************************************************
.*       MACRO FORMAT I
.**********************************************************************
.*
.MFI     ANOP
         CNOP  0,4
         BAL   1,SPFE&NDX               BRANCH AROUND LITERALS
         DC    A(TBSORT)                BROWSE LITERAL
         AIF   ('&TABLE' EQ '').BYP200
         AIF   ('&TABLE'(1,1) EQ '''').BYP210
         DC    A(&TABLE)                ADDRESS OF TABLE
         AGO   .BYP220
.BYP210  ANOP
         DC    A(SPFT&NDX)              ADDRESS OF GENERATED TABLE
         AGO   .BYP220
.BYP200  ANOP
         DC    A(SPFNULLC)              DUMMY PARM FOR TABLE
.BYP220  ANOP
         AIF   ('&FIELDS' EQ '').BYP240
         AIF   ('&FIELDS'(1,1) EQ '''').BYP230
         DC    A(&FIELDS+X'80000000')   ADDRESS OF FIELDS
         AGO   .BYP250
.BYP230  ANOP
         DC    A(SPFS&NDX+X'80000000')  ADDRESS OF GENERATED SORT KEYS
         AGO   .BYP250
.BYP240  ANOP
         DC    A(SPFNULLC+X'80000000')  DUMMY PARM FOR SORT KEYS
.BYP250  ANOP
SPFE&NDX DS    0H                       MARK LAST PARM
         L     15,&LOAD                 LOAD ADDRESS OF ISPF
         BALR  14,15                    CALL ISPF
         MEXIT
.*
.**********************************************************************
.*       MACRO ERRORS
.**********************************************************************
.*
.ERR2    ANOP
         MNOTE 16,'INVALID MACRO FORMAT SPECIFIED - FOUND ''&MF(1)'''
         MEXIT
.ERR3    ANOP
         MNOTE 16,'TABLE NAME IS REQUIRED'
         MEXIT
.ERR4    ANOP
         MNOTE 16,'SORT KEYS ARE REQUIRED'
         MEXIT
.ERRVAR  ANOP
         MNOTE 16,'ISPF PARMS MACRO WILL NOT HOLD FULL PARMLIST'
         MNOTE *,' ISPF PARMS MACRO GENERATE PARMLIST TOO SMALL'
         MEXIT
         MEND
