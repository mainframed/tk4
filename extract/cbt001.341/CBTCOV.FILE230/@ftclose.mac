         MACRO
&NAME    @FTCLOSE &MEM,                 MEMBER NAME                    *
               &LIB,                    LIBRARY NAME                   *
               &NOREPLL,                NOREPL KEYWORD                 *
               &LOAD=ISPLINK,           ISPLINK ADDRESS                *
               &MF=I                    MACRO FORMAT
.*
.**********************************************************************
.*       GLOBALS
.**********************************************************************
.*
         GBLB  &SPFFTCLOSE,&SPFNULLC,&SPFDEBUG,&SPFVAR,&SPFNOREPL
         GBLA  &SPFVARS,&SPFVARC
         LCLC  &NDX,&TEMP
&NDX     SETC  '&SYSNDX'
.*
.**********************************************************************
.*       GENERAL EDITS
.**********************************************************************
.*
         AIF   ('&NOREPLL' NE '' AND                                   *
               '&NOREPLL' NE 'NOREPL').ERR1
         AIF   ('&MF(1)' NE 'I' AND                                    *
               '&MF(1)' NE 'E').ERR2
.*
         AIF   ('&NAME' EQ '').BYP10
&NAME    DS    0H
.BYP10   ANOP
         AIF   ('&MF(2)' NE '').BYP15
&TEMP    SETC  'SPFPARMS'
         AGO   .BYP17
.BYP15   ANOP
&TEMP    SETC  '&MF(2)'
.BYP17   ANOP
.*
.**********************************************************************
.*       CHECK TO SEE IF A BRANCH AROUND LITERALS IS REQUIRED
.**********************************************************************
.*
         AIF   (NOT &SPFFTCLOSE).GENLIT
         AIF   (NOT &SPFNULLC).GENLIT
         AIF   ('&MEM' EQ '').CNT1
         AIF   ('&MEM'(1,1) EQ '''').GENLIT
.CNT1    AIF   ('&LIB' EQ '').CNT2
         AIF   ('&LIB'(1,1) EQ '''').GENLIT
.CNT2    AIF   ('&NOREPLL' NE '' AND NOT &SPFNOREPL).GENLIT
         AGO   .BRAREA
.GENLIT  ANOP
.*
.*********************************************************************
.*       GENERATE THE BRANCH AROUND LITERALS AND THE LITERALS
.*********************************************************************
.*
         B     SPF&NDX
         AIF   (&SPFFTCLOSE).BYP20
FTCLOSE  DC    CL8'FTCLOSE'             ISPF KEYWORD
&SPFFTCLOSE SETB 1
.BYP20   ANOP
         AIF   ('&MEM' EQ '').BYP40
         AIF   ('&MEM'(1,1) NE '''').BYP40
SPFM&NDX DC    CL8&MEM                  GENERATED MEMBER NAME
.BYP40   ANOP
         AIF   ('&LIB' EQ '').BYP50
         AIF   ('&LIB'(1,1) NE '''').BYP50
SPFL&NDX DC    CL8&LIB                  GENERATED LIBRARY
.BYP50   ANOP
         AIF   ('&NOREPLL' EQ '').BYP60
         AIF   (&SPFNOREPL).BYP60
NOREPL   DC    CL8'NOREPL'              ISPF KEYWORD
&SPFNOREPL SETB 1
.BYP60   ANOP
         AIF   (&SPFNULLC).BYP70
SPFNULLC DC    CL8'        '            DUMMY ISPF FIELD
&SPFNULLC SETB  1
.BYP70   ANOP
SPF&NDX  DS    0H
.BRAREA  ANOP
.*
.*********************************************************************
.*       GENERATE THE CALL
.*********************************************************************
.*
         AIF   ('&MF(1)' EQ 'I').MFI
         LA    14,FTCLOSE               GET ADDRESS OF FTCLOSE KEYWORD
         AIF   ('&MEM' EQ '').BYP100
         AIF   ('&MEM'(1,1) EQ '''').BYP110
         LA    15,&MEM                  GET ADDRESS OF MEMBER
         AGO   .BYP120
.BYP110  ANOP
         LA    15,SPFM&NDX              GET ADDRESS OF GENERATED MEMBER
         AGO   .BYP120
.BYP100  ANOP
         LA    15,SPFNULLC              GET DUMMY PARM FOR MEMBER
.BYP120  ANOP
         AIF   ('&LIB' EQ '').BYP140
         AIF   ('&LIB'(1,1) NE '''').BYP130
         LA    0,SPFL&NDX               GET ADDRESS OF GENERATED LIB
         AGO   .BYP150
.BYP130  ANOP
         LA    0,&LIB                   GET ADDRESS OF LIBRARY
         AGO   .BYP150
.BYP140  ANOP
         LA    0,SPFNULLC               GET DUMMY PARM FOR LIBRARY
.BYP150  ANOP
         AIF   ('&NOREPLL' EQ '').BYP170
         LA    1,NOREPL                 GET ADDRESS OF NOREPL
         AGO   .BYP180
.BYP170  ANOP
         LA    1,SPFNULLC               GET DUMMY PARM FOR NOREPL
.BYP180  ANOP
         STM   14,1,&TEMP               STORE PARAMETERS
         OI    &TEMP+12,X'80'           MARK LAST PARM
         LA    1,&TEMP                  GET PARAMETER LIST ADDRESS
         L     15,&LOAD                 LOAD ADDRESS OF ISPF
         BALR  14,15                    CALL ISPF
         AIF   (NOT &SPFVAR).BYVAR
         AIF   (&SPFVARC LT 4).ERRVAR
         AGO   .BYPVARS
.BYVAR   AIF   (&SPFVARS GE 4).BYPVARS
&SPFVARS SETA  4
.BYPVARS ANOP
         MEXIT
.*
.**********************************************************************
.*       MACRO FORMAT I
.**********************************************************************
.*
.MFI     ANOP
         CNOP  0,4
         BAL   1,SPFE&NDX               BRANCH AROUND LITERALS
         DC    A(FTCLOSE)               BROWSE LITERAL
         AIF   ('&MEM' EQ '').BYP200
         AIF   ('&MEM'(1,1) EQ '''').BYP210
         DC    A(&MEM)                  ADDRESS OF MEMBER
         AGO   .BYP220
.BYP210  ANOP
         DC    A(SPFM&NDX)              ADDRESS OF GENERATED MEMBER
         AGO   .BYP220
.BYP200  ANOP
         DC    A(SPFNULLC)              DUMMY PARM FOR MEMBER
.BYP220  ANOP
         AIF   ('&LIB' EQ '').BYP240
         AIF   ('&LIB'(1,1) NE '''').BYP230
         DC    A(SPFL&NDX)              ADDRESS OF GENERATED LIBRARY
         AGO   .BYP250
.BYP230  ANOP
         DC    A(&LIB)                  ADDRESS OF LIBRARY
         AGO   .BYP250
.BYP240  ANOP
         DC    A(SPFNULLC)              DUMMY PARM FOR LIBRARY
.BYP250  ANOP
         AIF   ('&NOREPLL' EQ '').BYP270
         DC    A(NOREPL+X'80000000')    ADDRESS OF NOREPL KEYWORD
         AGO   .BYP300
.BYP270  ANOP
         DC    A(SPFNULLC+X'80000000')  DUMMY PARM FOR NOREPL
.BYP300  ANOP
SPFE&NDX DS    0H                       MARK LAST PARM
         L     15,&LOAD                 LOAD ADDRESS OF ISPF
         BALR  14,15                    CALL ISPF
         MEXIT
.*
.**********************************************************************
.*       MACRO ERRORS
.**********************************************************************
.*
.ERR1    ANOP
         MNOTE 16,'INVALID VALUE FOR NOREPL - FOUND ''&NOREPLL'''
         MEXIT
.ERR2    ANOP
         MNOTE 16,'INVALID MACRO FORMAT SPECIFIED - FOUND ''&MF(1)'''
         MEXIT
.ERRVAR  ANOP
         MNOTE 16,'ISPF PARMS MACRO WILL NOT HOLD FULL PARMLIST'
         MNOTE *,' ISPF PARMS MACRO GENERATE PARMLIST TOO SMALL'
         MEXIT
         MEND
