         MACRO
&NAME    @TBSKIP &TABLE,                TABLE NAME                     *
               &NUM,                    NUMBER                         *
               &VAR,                    VARIABLE FOR EXT VARS          *
               &ROWID,                  VARIABLE NAME FOR ROWID        *
               &ROW,                    ROWID TO GET                   *
               &NOREAD,                 NOREAD KEYWORD                 *
               &CRP,                    CRP VARIABLE                   *
               &LOAD=ISPLINK,           ISPLINK ADDRESS                *
               &MF=I                    MACRO FORMAT
.*
.**********************************************************************
.*       GLOBALS
.**********************************************************************
.*
         GBLB  &SPFTBSKIP,&SPFNULLC,&SPFDEBUG,&SPFVAR
         GBLB  &SPFNOREAD,&SPFF1
         GBLA  &SPFVARS,&SPFVARC
         LCLC  &NDX,&TEMP
&NDX     SETC  '&SYSNDX'
         AIF   ('&MF(2)' NE '').BYP0
&TEMP    SETC  'SPFPARMS'
         AGO   .BYP00
.BYP0    ANOP
&TEMP    SETC  '&MF(2)'
.BYP00   ANOP
         AIF   ('&NAME' EQ '').BYP10
&NAME    DS    0H
.BYP10   ANOP
.*
.**********************************************************************
.*       EDITS
.**********************************************************************
.*
         AIF   ('&MF(1)' NE 'I' AND                                    *
               '&MF(1)' NE 'E').ERRMF
         AIF   ('&TABLE' EQ '').ERR1
         AIF   ('&NOREAD' NE '' AND '&NOREAD' NE 'NOREAD').ERR2
.*
.**********************************************************************
.*       CHECK TO SEE IF A BRANCH AROUND LITERALS IS REQUIRED
.**********************************************************************
.*
         AIF   (NOT &SPFTBSKIP).GENLIT
         AIF   (NOT &SPFNULLC).GENLIT
         AIF   ('&TABLE' EQ '').CNT1
         AIF   ('&TABLE'(1,1) EQ '''').GENLIT
.CNT1    ANOP
         AIF   (T'&NUM EQ 'N').GENLIT
         AIF   ('&NUM' EQ '' AND NOT &SPFF1).GENLIT
         AIF   ('&VAR' EQ '').CNT3
         AIF   ('&VAR'(1,1) EQ '''').GENLIT
.CNT3    ANOP
         AIF   ('&ROWID' EQ '').CNT4
         AIF   ('&ROWID'(1,1) EQ '''').GENLIT
.CNT4    ANOP
         AIF   (T'&ROW EQ 'N').CNT5
         AIF   ('&NOREAD' EQ 'NOREAD' AND NOT &SPFNOREAD).GENLIT
         AIF   ('&CRP' EQ '').CNT8
         AIF   ('&CRP'(1,1) EQ '''').GENLIT
.CNT8    ANOP
         AGO   .BRAREA
.GENLIT  ANOP
.*
.*********************************************************************
.*       GENERATE THE BRANCH AROUND LITERALS AND THE LITERALS
.*********************************************************************
.*
         B     SPF&NDX
         AIF   (&SPFTBSKIP).BYP20
TBSKIP   DC    CL8'TBSKIP'              ISPF KEYWORD
&SPFTBSKIP SETB 1
.BYP20   ANOP
         AIF   ('&TABLE' EQ '').BYP30
         AIF   ('&TABLE'(1,1) NE '''').BYP30
SPFT&NDX DC    CL8&TABLE                GENERATED TABLE NAME
.BYP30   ANOP
         AIF   (T'&NUM NE 'N').BYP40
SPFN&NDX DC    F'&NUM'                  GENERATED NUMBER
.BYP40   ANOP
         AIF   ('&NUM' NE '' OR &SPFF1).BYP45
SPFF1    DC    F'1'
&SPFF1   SETB  1
.BYP45   ANOP
         AIF   ('&VAR' EQ '').BYP50
         AIF   ('&VAR'(1,1) NE '''').BYP50
SPFV&NDX DC    CL8&VAR                  GENERATED EXT VAR NAME
.BYP50   ANOP
         AIF   ('&ROWID' EQ '').BYP60
         AIF   ('&ROWID'(1,1) NE '''').BYP60
SPFI&NDX DC    CL8&ROWID                GENERATED ROWID VAR NAME
.BYP60   ANOP
         AIF   (T'&ROW NE 'N').BYP70
SPFR&NDX DC    F'&ROW'                  GENERATED ROWID
.BYP70   ANOP
         AIF   ('&NOREAD' NE 'NOREAD' OR &SPFNOREAD).BYP80
NOREAD   DC    CL8'NOREAD'              ISPF KEYWORD
&SPFNOREAD SETB 1
.BYP80   ANOP
         AIF   ('&CRP' EQ '').BYP100
         AIF   ('&CRP'(1,1) NE '''').BYP100
SPFC&NDX DC    CL8&CRP                  GENERATED CRP VARIABLE NAME
.BYP100  ANOP
         AIF   (&SPFNULLC).BYP120
SPFNULLC DC    CL8'        '            DUMMY ISPF FIELD
&SPFNULLC SETB  1
.BYP120  ANOP
SPF&NDX  DS    0H
.BRAREA  ANOP
.*
.*********************************************************************
.*       GENERATE THE CALL
.*********************************************************************
.*
         AIF   ('&MF(1)' EQ 'I').MFI
         LA    14,TBSKIP                GET ADDRESS OF TBSKIP KEYWORD
.*
         AIF   ('&TABLE' EQ '').BYP1000
         AIF   ('&TABLE'(1,1) EQ '''').BYP1010
         LA    15,&TABLE                GET ADDRESS OF TABLE NAME
         AGO   .BYP1050
.BYP1000 ANOP
         LA    15,SPFNULLC              GET DUMMY PARM FOR TABLE
         AGO   .BYP1050
.BYP1010 ANOP
         LA    15,SPFT&NDX              GET ADDRESS OF GENERATED TABLE
.BYP1050 ANOP
.*
         AIF   ('&NUM' EQ '').BYP1100
         AIF   (T'&NUM EQ 'N').BYP1110
         LA    0,&NUM                   GET ADDRESS OF NUM FIELD
         AGO   .BYP1150
.BYP1100 ANOP
         LA    0,SPFF1                  GET DUMMY PARM FOR NUM
         AGO   .BYP1150
.BYP1110 ANOP
         LA    0,SPFN&NDX               GET ADDRESS OF GENERATED NUM
.BYP1150 ANOP
.*
         AIF   ('&VAR' EQ '').BYP1200
         AIF   ('&VAR'(1,1) EQ '''').BYP1210
         LA    1,&VAR                   GET ADDRESS OF VAR NAME
         AGO   .BYP1250
.BYP1200 ANOP
         LA    1,SPFNULLC               GET DUMMY PARM FOR VAR
         AGO   .BYP1250
.BYP1210 ANOP
         LA    1,SPFV&NDX               GET ADDRESS OF GENERATED VAR
.BYP1250 ANOP
.*
         STM   14,1,&TEMP               STORE PARAMETERS INTO LIST
         AIF   ('&ROWID' EQ '').BYP1300
         AIF   ('&ROWID'(1,1) EQ '''').BYP1310
         LA    14,&ROWID                GET ADDRESS OF ROWID NAME
         AGO   .BYP1350
.BYP1300 ANOP
         LA    14,SPFNULLC              GET DUMMY PARM FOR ROWID
         AGO   .BYP1350
.BYP1310 ANOP
         LA    14,SPFI&NDX              GET ADDRESS OF GENERATED ROWID
.BYP1350 ANOP
.*
         AIF   ('&ROW' EQ '').BYP1400
         AIF   (T'&ROW EQ 'N').BYP1410
         LA    15,&ROW                  GET ADDRESS OF ROW NAME
         AGO   .BYP1450
.BYP1400 ANOP
         LA    15,SPFNULLC              GET DUMMY PARM FOR ROW
         AGO   .BYP1450
.BYP1410 ANOP
         LA    15,SPFR&NDX              GET ADDRESS OF GENERATED ROW
.BYP1450 ANOP
.*
         AIF   ('&NOREAD' EQ '').BYP1500
         LA    0,&NOREAD                GET ADDRESS OF NOREAD
         AGO   .BYP1550
.BYP1500 ANOP
         LA    0,SPFNULLC               GET DUMMY PARM FOR NOREAD
.BYP1550 ANOP
.*
         AIF   ('&CRP' EQ '').BYP1600
         AIF   ('&CRP'(1,1) EQ '''').BYP1610
         LA    1,&AUTOSEL               GET ADDRESS OF CRP NAME
         AGO   .BYP1650
.BYP1600 ANOP
         LA    1,SPFNULLC               GET DUMMY PARM FOR AUTOSEL
         AGO   .BYP1650
.BYP1610 ANOP
         LA    1,SPFC&NDX               GET DUMMY PARM FOR AUTOSEL
.BYP1650 ANOP
.*
         STM   14,1,&TEMP+16            STORE PARAMETERS INTO LIST
         OI    &TEMP+28,X'80'           MARK LAST PARM
         LA    1,&TEMP                  GET PARAMETER LIST ADDRESS
         L     15,&LOAD                 LOAD ADDRESS OF ISPF
         BALR  14,15                    CALL ISPF
         AIF   (NOT &SPFVAR).BYVAR
         AIF   (&SPFVARC LT 8).ERRVAR
         AGO   .BYPVARS
.BYVAR   AIF   (&SPFVARS GE 8).BYPVARS
&SPFVARS SETA  8
.BYPVARS ANOP
         MEXIT
.*
.**********************************************************************
.*       MACRO FORMAT I
.**********************************************************************
.*
.MFI     ANOP
         CNOP  0,4
         BAL   1,SPFE&NDX               BRANCH AROUND LITERALS
         DC    A(TBSKIP)                TBSKIP LITERAL
.*
         AIF   ('&TABLE' EQ '').BYP2000
         AIF   ('&TABLE'(1,1) EQ '''').BYP2010
         DC    A(&TABLE)                ADDRESS OF TABLE NAME
         AGO   .BYP2050
.BYP2000 ANOP
         DC    A(SPFNULLC)              DUMMY PARM FOR TABLE NAME
         AGO   .BYP2050
.BYP2010 ANOP
         DC    A(SPFT&NDX)              ADDRESS OF GENERATED TABLE
.BYP2050 ANOP
.*
         AIF   ('&NUM' EQ '').BYP2100
         AIF   ('&NUM'(1,1) EQ '''').BYP2110
         DC    A(&NUM)                  ADDRESS OF NUM NAME
         AGO   .BYP2150
.BYP2100 ANOP
         DC    A(SPFF1)                 DUMMY PARM FOR NUM NAME
         AGO   .BYP2150
.BYP2110 ANOP
         DC    A(SPFN&NDX)              ADDRESS OF GENERATED NUM
.BYP2150 ANOP
.*
         AIF   ('&VAR' EQ '').BYP2200
         AIF   ('&VAR'(1,1) EQ '''').BYP2210
         DC    A(&VAR)                  ADDRESS OF VAR NAME
         AGO   .BYP2250
.BYP2200 ANOP
         DC    A(SPFNULLC)              DUMMY PARM FOR VAR
         AGO   .BYP2250
.BYP2210 ANOP
         DC    A(SPFV&NDX)              ADDRESS OF GENERATED VAR
.BYP2250 ANOP
.*
         AIF   ('&ROWID' EQ '').BYP2300
         AIF   ('&ROWID'(1,1) EQ '''').BYP2310
         DC    A(&ROWID)                ADDRESS OF ROWID NAME
         AGO   .BYP2350
.BYP2300 ANOP
         DC    A(SPFNULLC)              DUMMY PARM FOR ROWID
         AGO   .BYP2350
.BYP2310 ANOP
         DC    A(SPFI&NDX)              ADDRESS OF GENERATED ROWID
.BYP2350 ANOP
.*
         AIF   ('&ROW' EQ '').BYP2400
         AIF   ('&ROW'(1,1) EQ '''').BYP2410
         DC    A(&ROW)                  ADDRESS OF ROW NAME
         AGO   .BYP2450
.BYP2400 ANOP
         DC    A(SPFNULLC)              DUMMY PARM FOR ROW
         AGO   .BYP2450
.BYP2410 ANOP
         DC    A(SPFR&NDX)              ADDRESS OF GENERATED ROW
.BYP2450 ANOP
.*
         AIF   ('&NOREAD' EQ '').BYP2500
         DC    A(&NOREAD)               ADDRESS OF NOREAD
         AGO   .BYP2550
.BYP2500 ANOP
         DC    A(SPFNULLC)              DUMMY PARM FOR CSRPOS
.BYP2550 ANOP
.*
         AIF   ('&CRP' EQ '').BYP2800
         AIF   ('&CRP'(1,1) EQ '''').BYP2810
         DC    A(&CRP+X'80000000')      ADDRESS OF CRP NAME
         AGO   .BYP2850
.BYP2800 ANOP
         DC    A(SPFNULLC+X'80000000')  DUMMY PARM FOR CRP
         AGO   .BYP2850
.BYP2810 ANOP
         DC    A(SPFI&NDX+X'80000000')  ADDRESS OF GENERATED CRP
.BYP2850 ANOP
SPFE&NDX DS    0H                       MARK LAST PARM
         L     15,&LOAD                 LOAD ADDRESS OF ISPF
         BALR  14,15                    CALL ISPF
         MEXIT
.*
.**********************************************************************
.*       MACRO ERRORS
.**********************************************************************
.*
.ERRMF   ANOP
         MNOTE 16,'INVALID MACRO FORMAT SPECIFIED - FOUND ''&MF(1)'''
         MEXIT
.ERR1    ANOP
         MNOTE 16,'TABLE NAME IS REQUIRED'
         MEXIT
.ERR2    ANOP
         MNOTE 16,'EXPECTED NOREAD KEYWORD - FOUND ''&NOREAD'''
         MEXIT
.ERRVAR  ANOP
         MNOTE 16,'ISPF PARMS MACRO WILL NOT HOLD FULL PARMLIST'
         MNOTE *,' ISPF PARMS MACRO GENERATE PARMLIST TOO SMALL'
         MEXIT
         MEND
