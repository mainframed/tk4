         MACRO
&NAME    @BROWSE &DSN,                  TSO DSNAME                     *
               &VOL,                    VOLUME                         *
               &PASS,                   PASSWORD                       *
               &PANEL,                  PANEL ID                       *
               &DATAID,                 DATA ID                        *
               &MEMBER,                 MEMBER NAME                    *
               &LOAD=ISPLINK,           ISPLINK ADDRESS                *
               &MF=I                    MACRO FORMAT
.*
.**********************************************************************
.*       GLOBALS
.**********************************************************************
.*
         GBLB  &SPFBROWSE,&SPFNULLC,&SPFDEBUG,&SPFVAR
         GBLA  &SPFVARS,&SPFVARC
         LCLC  &NDX,&TEMP
&NDX     SETC  '&SYSNDX'
.*
.**********************************************************************
.*       GENERAL EDITS
.**********************************************************************
.*
         AIF   ('&DSN' EQ '').ERR1
         AIF   ('&MF(1)' NE 'I' AND                                    *
               '&MF(1)' NE 'E').ERR2
.*
         AIF   ('&NAME' EQ '').BYP10
&NAME    DS    0H
.BYP10   ANOP
         AIF   ('&MF(2)' NE '').BYP15
&TEMP    SETC  'SPFPARMS'
         AGO   .BYP17
.BYP15   ANOP
&TEMP    SETC  '&MF(2)'
.BYP17   ANOP
.*
.**********************************************************************
.*       CHECK TO SEE IF A BRANCH AROUND LITERALS IS REQUIRED
.**********************************************************************
.*
         AIF   (NOT &SPFBROWSE).GENLIT
         AIF   (NOT &SPFNULLC).GENLIT
         AIF   ('&DSN'(1,1) EQ '''').GENLIT
         AIF   ('&VOL' EQ '').CNT1
         AIF   ('&VOL'(1,1) EQ '''').GENLIT
.CNT1    AIF   ('&PASS' EQ '').CNT2
         AIF   ('&PASS'(1,1) EQ '''').GENLIT
.CNT2    AIF   ('&PANEL' EQ '').CNT3
         AIF   ('&PANEL'(1,1) EQ '''').GENLIT
.CNT3    AIF   ('&DATAID' EQ '').CNT4
         AIF   ('&DATAID'(1,1) EQ '''').GENLIT
.CNT4    AIF   ('&MEMBER' EQ '').CNT5
         AIF   ('&MEMBER'(1,1) EQ '''').GENLIT
.CNT5    AGO   .BRAREA
.GENLIT  ANOP
.*
.*********************************************************************
.*       GENERATE THE BRANCH AROUND LITERALS AND THE LITERALS
.*********************************************************************
.*
         B     SPF&NDX
         AIF   (&SPFBROWSE).BYP20
BROWSE   DC    CL8'BROWSE'              BROWSE LITERAL FOR ISPF
&SPFBROWSE SETB 1
.BYP20   ANOP
         AIF   ('&DSN'(1,1) NE '''').BYP30
SPFD&NDX DC    CL56&DSN                 BROWSE THIS DATASET NAME
.BYP30   ANOP
         AIF   ('&VOL' EQ '').BYP40
         AIF   ('&VOL'(1,1) NE '''').BYP40
SPFV&NDX DC    CL6&VOL                  GENERATED VOLSER
.BYP40   ANOP
         AIF   ('&PASS' EQ '').BYP50
         AIF   ('&PASS'(1,1) NE '''').BYP50
SPFP&NDX DC    CL8&PASS                 GENERATED PASSWORD
.BYP50   ANOP
         AIF   ('&PANEL' EQ '').BYP60
         AIF   ('&PANEL'(1,1) NE '''').BYP60
SPFI&NDX DC    CL8&PANEL                GENERATED PANEL ID
.BYP60   ANOP
         AIF   ('&DATAID' EQ '').BYP62
         AIF   ('&DATAID'(1,1) NE '''').BYP62
SPFA&NDX DC    CL8&DATAID               GENERATED DATAID
.BYP62   ANOP
         AIF   ('&MEMBER' EQ '').BYP64
         AIF   ('&MEMBER'(1,1) NE '''').BYP64
SPFM&NDX DC    CL8&MEMBER               GENERATED MEMBER
.BYP64   ANOP
         AIF   (&SPFNULLC).BYP70
SPFNULLC DC    CL8'        '            DUMMY ISPF FIELD
&SPFNULLC SETB  1
.BYP70   ANOP
SPF&NDX  DS    0H
.BRAREA  ANOP
.*
.*********************************************************************
.*       GENERATE THE CALL
.*********************************************************************
.*
         AIF   ('&MF(1)' EQ 'I').MFI
         LA    14,BROWSE                GET ADDRESS OF BROWSE KEYWORD
         AIF   ('&DSN'(1,1) NE '''').BYP80
         LA    15,SPFD&NDX              GET ADDRESS OF GENERATED DSN
         AGO   .BYP90
.BYP80   ANOP
         LA    15,&DSN                  GET ADDRESS OF DATASET NAME
.BYP90   ANOP
         AIF   ('&VOL' EQ '').BYP100
         AIF   ('&VOL'(1,1) EQ '''').BYP110
         LA    0,&VOL                   GET ADDRESS OF VOLSER
         AGO   .BYP120
.BYP110  ANOP
         LA    0,SPFV&NDX               GET ADDRESS OF GENERATED VOLSER
         AGO   .BYP120
.BYP100  ANOP
         LA    0,SPFNULLC               GET DUMMY PARM FOR VOLSER
.BYP120  ANOP
         AIF   ('&PASS' EQ '').BYP140
         AIF   ('&PASS'(1,1) NE '''').BYP130
         LA    1,SPFP&NDX               GET ADDRESS OF GENERATED PASS
         AGO   .BYP150
.BYP130  ANOP
         LA    1,&PASS                  GET ADDRESS OF PASSWORD
         AGO   .BYP150
.BYP140  ANOP
         LA    1,SPFNULLC               GET DUMMY PARM FOR PASSWORD
.BYP150  ANOP
         STM   14,1,&TEMP               STORE PARAMETERS INTO LIST
         AIF   ('&PANEL' EQ '').BYP170
         AIF   ('&PANEL'(1,1) EQ '''').BYP160
         LA    14,&PANEL                GET ADDRESS OF PANEL ID
         AGO   .BYP180
.BYP160  ANOP
         LA    14,SPFI&NDX              GET ADDRESS OF GENERATED PANEL
         AGO   .BYP180
.BYP170  ANOP
         LA    14,SPFNULLC              GET DUMMY PARM FOR PANEL ID
.BYP180  ANOP
         AIF   ('&DATAID' EQ '').BYP172
         AIF   ('&DATAID'(1,1) EQ '''').BYP162
         LA    15,&DATAID               GET ADDRESS OF DATA ID
         AGO   .BYP182
.BYP162  ANOP
         LA    15,SPFA&NDX              GET ADDRESS OF GENNED DATAID
         AGO   .BYP182
.BYP172  ANOP
         LA    15,SPFNULLC              GET DUMMY PARM FOR DATA ID
.BYP182  ANOP
         AIF   ('&MEMBER' EQ '').BYP174
         AIF   ('&MEMBER'(1,1) EQ '''').BYP164
         LA    0,&MEMBER                GET ADDRESS OF MEMBER
         AGO   .BYP184
.BYP164  ANOP
         LA    0,SPFM&NDX               GET ADDRESS OF GENNED MEMBER
         AGO   .BYP184
.BYP174  ANOP
         LA    0,SPFNULLC               GET DUMMY PARM FOR MEMBER
.BYP184  ANOP
         STM   14,0,&TEMP+16            STORE AS FIFTH PARM
         OI    &TEMP+24,X'80'           MARK LAST PARM
         LA    1,&TEMP                  GET PARAMETER LIST ADDRESS
         L     15,&LOAD                 LOAD ADDRESS OF ISPF
         BALR  14,15                    CALL ISPF
         AIF   (NOT &SPFVAR).BYVAR
         AIF   (&SPFVARC LT 7).ERRVAR
         AGO   .BYPVARS
.BYVAR   AIF   (&SPFVARS GE 7).BYPVARS
&SPFVARS SETA  7
.BYPVARS ANOP
         MEXIT
.*
.**********************************************************************
.*       MACRO FORMAT I
.**********************************************************************
.*
.MFI     ANOP
         CNOP  0,4
         BAL   1,SPFE&NDX               BRANCH AROUND LITERALS
         DC    A(BROWSE)                BROWSE LITERAL
         AIF   ('&DSN'(1,1) NE '''').BYP280
         DC    A(SPFD&NDX)              ADDRESS OF GENERATED DSN
         AGO   .BYP290
.BYP280  ANOP
         DC    A(&DSN)                  ADDRESS OF DATASET NAME
.BYP290  ANOP
         AIF   ('&VOL' EQ '').BYP200
         AIF   ('&VOL'(1,1) EQ '''').BYP210
         DC    A(&VOL)                  ADDRESS OF VOLSER
         AGO   .BYP220
.BYP210  ANOP
         DC    A(SPFV&NDX)              ADDRESS OF GENERATED VOLSER
         AGO   .BYP220
.BYP200  ANOP
         DC    A(SPFNULLC)              DUMMY PARM FOR VOLSER
.BYP220  ANOP
         AIF   ('&PASS' EQ '').BYP240
         AIF   ('&PASS'(1,1) NE '''').BYP230
         DC    A(SPFP&NDX)              ADDRESS OF GENERATED PASS
         AGO   .BYP250
.BYP230  ANOP
         DC    A(&PASS)                 ADDRESS OF PASSWORD
         AGO   .BYP250
.BYP240  ANOP
         DC    A(SPFNULLC)              DUMMY PARM FOR PASSWORD
.BYP250  ANOP
         AIF   ('&PANEL' EQ '').BYP270
         AIF   ('&PANEL'(1,1) EQ '''').BYP260
         DC    A(&PANEL)                ADDRESS OF PANEL ID
         AGO   .BYP300
.BYP260  ANOP
         DC    A(SPFI&NDX)              ADDRESS OF GENERATED PANEL
         AGO   .BYP300
.BYP270  ANOP
         DC    A(SPFNULLC)              DUMMY PARM FOR PANEL ID
.BYP300  ANOP
         AIF   ('&DATAID' EQ '').BYP320
         AIF   ('&DATAID'(1,1) EQ '''').BYP310
         DC    A(&DATAID)               ADDRESS OF DATA ID
         AGO   .BYP330
.BYP310  ANOP
         DC    A(SPFA&NDX)              ADDRESS OF GENNED DATAID
         AGO   .BYP330
.BYP320  ANOP
         DC    A(SPFNULLC)              DUMMY PARM FOR DATA ID
.BYP330  ANOP
         AIF   ('&MEMBER' EQ '').BYP374
         AIF   ('&MEMBER'(1,1) EQ '''').BYP364
         DC    A(&MEMBER.+X'80000000')  ADDRESS OF MEMBER
         AGO   .BYP384
.BYP364  ANOP
         DC    A(SPFM&NDX+X'80000000')  ADDRESS OF GENNED MEMBER
         AGO   .BYP184
.BYP374  ANOP
         DC    A(SPFNULLC+X'80000000')  DUMMY PARM FOR MEMBER
.BYP384  ANOP
SPFE&NDX DS    0H                       MARK LAST PARM
         L     15,&LOAD                 LOAD ADDRESS OF ISPF
         BALR  14,15                    CALL ISPF
         MEXIT
.*
.**********************************************************************
.*       MACRO ERRORS
.**********************************************************************
.*
.ERR1    ANOP
         MNOTE 16,'BROWSE REQUIRES A VALID DSNAME PARAMETER - MISSING'
         MEXIT
.ERR2    ANOP
         MNOTE 16,'INVALID MACRO FORMAT SPECIFIED - FOUND ''&MF(1)'''
         MEXIT
.ERRVAR  ANOP
         MNOTE 16,'ISPF PARMS MACRO WILL NOT HOLD FULL PARMLIST'
         MNOTE *,' ISPF PARMS MACRO GENERATE PARMLIST TOO SMALL'
         MEXIT
         MEND
