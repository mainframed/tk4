         MACRO
&NAME    TBGET &TABLE,                  TABLE NAME                     *
               &P2,                                                    *
               &P3,                                                    *
               &P4,                                                    *
               &P5,                                                    *
               &SAVENAME=,              SAVE NAME-LIST                 *
               &ROWID=,                 ROW ID NAME                    *
               &NOREAD=,                NOREAD KEYWORD                 *
               &POSITION=,              POSITION NUMBER                *
               &LOAD=ISPLINK,           ISPLINK ADDRESS                *
               &MF=I                    MACRO FORMAT
.*
.**********************************************************************
.*       GLOBALS
.**********************************************************************
.*
         GBLB  &SPFTBGET,&SPFNULLC,&SPFDEBUG,&SPFVAR,&SPFNOREAD
         GBLA  &SPFVARS,&SPFVARC
         LCLC  &NDX,&TEMP
         GBLC  &SPFLVL
         AIF   ('&SPFLVL' NE '1').NL1
&NAME    @TBGET &TABLE,&P2,&P3,&P4,&P5,LOAD=&LOAD,MF=&MF
         MEXIT
.NL1     ANOP
         AIF   ('&P2' NE '').LVLERR
         AIF   ('&P3' NE '').LVLERR
         AIF   ('&P4' NE '').LVLERR
         AIF   ('&P5' NE '').LVLERR
         AGO   .NL2
.LVLERR  ANOP
         MNOTE 12,'POSITIONAL PARAMETER FOUND - NOT ALLOWED'
         MEXIT
.NL2     ANOP
&NDX     SETC  '&SYSNDX'
         AIF   ('&MF(2)' NE '').BYP0
&TEMP    SETC  'SPFPARMS'
         AGO   .BYP00
.BYP0    ANOP
&TEMP    SETC  '&MF(2)'
.BYP00   ANOP
         AIF   ('&NAME' EQ '').BYP10
&NAME    DS    0H
.BYP10   ANOP
.*
.**********************************************************************
.*       EDITS
.**********************************************************************
.*
         AIF   ('&MF(1)' NE 'I' AND                                    *
               '&MF(1)' NE 'E').ERRMF
         AIF   ('&TABLE' EQ '').ERR1
         AIF   ('&NOREAD' EQ '').K3
         AIF   ('&NOREAD'(1,1) NE '''').K3
         AIF   ('&NOREAD' EQ '''NOREAD''').K3
         MNOTE 4,'WARNING - NOREAD KEYWORD EXPECTED NOREAD IN QUOTES'
.K3      ANOP
.*
.**********************************************************************
.*       CHECK TO SEE IF A BRANCH AROUND LITERALS IS REQUIRED
.**********************************************************************
.*
         AIF   (NOT &SPFTBGET).GENLIT
         AIF   (NOT &SPFNULLC).GENLIT
         AIF   ('&TABLE' EQ '').CNT1
         AIF   ('&TABLE'(1,1) EQ '''').GENLIT
.CNT1    ANOP
         AIF   ('&SAVENAME' EQ '').CNT2
         AIF   ('&SAVENAME'(1,1) EQ '''').GENLIT
.CNT2    ANOP
         AIF   ('&ROWID' EQ '').CNT3
         AIF   ('&ROWID'(1,1) EQ '''').GENLIT
.CNT3    ANOP
         AIF   ('&NOREAD' EQ '').CNT3A
         AIF   ('&NOREAD'(1,1) EQ '''').GENLIT
.CNT3A   ANOP
         AIF   ('&POSITION' EQ '').CNT4
         AIF   ('&POSITION'(1,1) EQ '''').GENLIT
.CNT4    ANOP
         AIF   ('&POSITION' EQ '').CNT5
         AIF   ('&POSITION'(1,1) EQ '''').GENLIT
.CNT5    ANOP
         AGO   .BRAREA
.GENLIT  ANOP
.*
.*********************************************************************
.*       GENERATE THE BRANCH AROUND LITERALS AND THE LITERALS
.*********************************************************************
.*
         B     SPF&NDX
         AIF   (&SPFTBGET).BYP20
TBGET DC       CL8'TBGET'               ISPF KEYWORD
&SPFTBGET SETB 1
.BYP20   ANOP
         AIF   ('&TABLE' EQ '').BYP30
         AIF   ('&TABLE'(1,1) NE '''').BYP30
SPFT&NDX DC    CL8&TABLE                GENERATED TABLE NAME
.BYP30   ANOP
         AIF   ('&SAVENAME' EQ '').BYP40
         AIF   ('&SAVENAME'(1,1) NE '''').BYP40
SPFS&NDX DC    CL8&SAVENAME             GENERATED SAVE
.BYP40   ANOP
         AIF   ('&ROWID' EQ '').BYP50
         AIF   ('&ROWID'(1,1) NE '''').BYP50
SPFR&NDX DC    CL8&ROWID                GENERATED ROWID VAR NAME
.BYP50   ANOP
         AIF   ('&NOREAD' EQ '').BYP60
         AIF   ('&NOREAD'(1,1) NE '''').BYP60
SPFX&NDX DC    CL8&NOREAD               GENERATED NOREAD KEYWORD
.BYP60   ANOP
         AIF   ('&POSITION' EQ '').BYP70
         AIF   ('&POSITION'(1,1) NE '''').BYP70
SPFP&NDX DC    CL8&POSITION             GENERATED POSITION VAR
.BYP70   ANOP
         AIF   (&SPFNULLC).BYP120
SPFNULLC DC    CL8'        '            DUMMY ISPF FIELD
&SPFNULLC SETB  1
.BYP120  ANOP
SPF&NDX  DS    0H
.BRAREA  ANOP
.*
.*********************************************************************
.*       GENERATE THE CALL
.*********************************************************************
.*
         AIF   ('&MF(1)' EQ 'I').MFI
         LA    14,TBGET                 GET ADDRESS OF TBGET KEYWORD
.*
         AIF   ('&TABLE' EQ '').BYP1000
         AIF   ('&TABLE'(1,1) EQ '''').BYP1010
         LA    15,&TABLE                GET ADDRESS OF TABLE NAME
         AGO   .BYP1050
.BYP1000 ANOP
         LA    15,SPFNULLC              GET DUMMY PARM FOR TABLE
         AGO   .BYP1050
.BYP1010 ANOP
         LA    15,SPFT&NDX              GET ADDRESS OF GENERATED TABLE
.BYP1050 ANOP
.*
         AIF   ('&SAVENAME' EQ '').BYP1100
         AIF   ('&SAVENAME'(1,1) EQ '''').BYP1110
         LA    0,&SAVENAME              GET ADDRESS OF SAVE
         AGO   .BYP1150
.BYP1100 ANOP
         LA    0,SPFNULLC               GET DUMMY PARM FOR SAVE
         AGO   .BYP1150
.BYP1110 ANOP
         LA    0,SPFS&NDX               GET ADDRESS OF GENERATED SAVE
.BYP1150 ANOP
.*
         AIF   ('&ROWID' EQ '').BYP1200
         AIF   ('&ROWID'(1,1) EQ '''').BYP1210
         LA    1,&ROWID                 GET ADDRESS OF ROWID NAME
         AGO   .BYP1250
.BYP1200 ANOP
         LA    1,SPFNULLC               GET DUMMY PARM FOR ROWID
         AGO   .BYP1250
.BYP1210 ANOP
         LA    1,SPFR&NDX               GET ADDRESS OF GENERATED ROWID
.BYP1250 ANOP
.*
         STM   14,1,&TEMP               STORE PARAMETERS INTO LIST
         AIF   ('&NOREAD' EQ '').BYP1300
         AIF   ('&NOREAD'(1,1) EQ '''').BYP1305
         LA    14,&NOREAD               GET ADDRESS OF NOREAD KEYWORD
         AGO   .BYP1350
.BYP1300 ANOP
         LA    14,SPFNULLC              GET DUMMY PARM FOR NOREAD
         AGO   .BYP1350
.BYP1305 ANOP
         LA    14,SPFX&NDX              GENNED NOREAD KEYWORD
.BYP1350 ANOP
.*
         AIF   ('&POSITION' EQ '').BYP1400
         AIF   ('&POSITION'(1,1) EQ '''').BYP1410
         LA    15,&POSITION             GET ADDRESS OF POSITION NAME
         AGO   .BYP1450
.BYP1400 ANOP
         LA    15,SPFNULLC              GET DUMMY PARM FOR POSITION
         AGO   .BYP1450
.BYP1410 ANOP
         LA    15,SPFP&NDX              GET ADDRESS OF GENERATED POS
.BYP1450 ANOP
.*
         STM   14,15,&TEMP+16           STORE PARAMETERS INTO LIST
         OI    &TEMP+20,X'80'           MARK LAST PARM
         LA    1,&TEMP                  GET PARAMETER LIST ADDRESS
         L     15,&LOAD                 LOAD ADDRESS OF ISPF
         BALR  14,15                    CALL ISPF
         AIF   (NOT &SPFVAR).BYVAR
         AIF   (&SPFVARC LT 6).ERRVAR
         AGO   .BYPVARS
.BYVAR   AIF   (&SPFVARS GE 6).BYPVARS
&SPFVARS SETA  6
.BYPVARS ANOP
         MEXIT
.*
.**********************************************************************
.*       MACRO FORMAT I
.**********************************************************************
.*
.MFI     ANOP
         CNOP  0,4
         BAL   1,SPFE&NDX               BRANCH AROUND LITERALS
         DC    A(TBGET)                 TBGET LITERAL
.*
         AIF   ('&TABLE' EQ '').BYP2000
         AIF   ('&TABLE'(1,1) EQ '''').BYP2010
         DC    A(&TABLE)                ADDRESS OF TABLE NAME
         AGO   .BYP2050
.BYP2000 ANOP
         DC    A(SPFNULLC)              DUMMY PARM FOR TABLE NAME
         AGO   .BYP2050
.BYP2010 ANOP
         DC    A(SPFT&NDX)              ADDRESS OF GENERATED TABLE
.BYP2050 ANOP
.*
         AIF   ('&SAVENAME' EQ '').BYP2100
         AIF   ('&SAVENAME'(1,1) EQ '''').BYP2110
         DC    A(&SAVENAME)             ADDRESS OF SAVE NAME
         AGO   .BYP2150
.BYP2100 ANOP
         DC    A(SPFNULLC)              DUMMY PARM FOR SAVE
         AGO   .BYP2150
.BYP2110 ANOP
         DC    A(SPFS&NDX)              ADDRESS OF GENERATED SAVE
.BYP2150 ANOP
.*
         AIF   ('&ROWID' EQ '').BYP2200
         AIF   ('&ROWID'(1,1) EQ '''').BYP2210
         DC    A(&ROWID)                ADDRESS OF ROWID NAME
         AGO   .BYP2250
.BYP2200 ANOP
         DC    A(SPFNULLC)              DUMMY PARM FOR ROWID
         AGO   .BYP2250
.BYP2210 ANOP
         DC    A(SPFR&NDX)              ADDRESS OF GENERATED ROWID
.BYP2250 ANOP
.*
         AIF   ('&NOREAD' EQ '').BYP2300
         AIF   ('&NOREAD'(1,1) EQ '''').BYP2305
         DC    A(&NOREAD)               ADDRESS OF NOREAD KEYWORD
         AGO   .BYP2350
.BYP2300 ANOP
         DC    A(SPFNULLC)              DUMMY PARM FOR NOREAD
         AGO   .BYP2350
.BYP2305 ANOP
         DC    A(SPFX&NDX)              GENNED NOREAD KEYWORD
.BYP2350 ANOP
.*
         AIF   ('&POSITION' EQ '').BYP2400
         AIF   ('&POSITION'(1,1) EQ '''').BYP2410
         DC    A(&POSITION+X'80000000') ADDRESS OF POSITION NAME
         AGO   .BYP2450
.BYP2400 ANOP
         DC    A(SPFNULLC+X'80000000')  DUMMY PARM FOR POSITION
         AGO   .BYP2450
.BYP2410 ANOP
         DC    A(SPFP&NDX+X'80000000')  ADDRESS OF GENERATED POSITION
.BYP2450 ANOP
.*
SPFE&NDX DS    0H                       MARK LAST PARM
         L     15,&LOAD                 LOAD ADDRESS OF ISPF
         BALR  14,15                    CALL ISPF
         MEXIT
.*
.**********************************************************************
.*       MACRO ERRORS
.**********************************************************************
.*
.ERRMF   ANOP
         MNOTE 16,'INVALID MACRO FORMAT SPECIFIED - FOUND ''&MF(1)'''
         MEXIT
.ERR1    ANOP
         MNOTE 16,'TABLE NAME IS REQUIRED'
         MEXIT
.ERR2    ANOP
         MNOTE 16,'NOREAD KEYWORD EXPECTED - FOUND ''&NOREAD'''
         MEXIT
.ERRVAR  ANOP
         MNOTE 16,'ISPF PARMS MACRO WILL NOT HOLD FULL PARMLIST'
         MNOTE *,' ISPF PARMS MACRO GENERATE PARMLIST TOO SMALL'
         MEXIT
         MEND
