*%;  /*
         MACRO
&NAME    IRARMEP   &DSECT=YES,     USE OTHER OPRNDS IF DSECT=NO SPECD  *
               &EPN=,              ROUTINE (ENTRY POINT) IDENTIFIER    *
               &ERR=,              ROUTINE RECOVERY POINT IDENTIFIER   *
               &NXT=,              NEXT CHAINED RMEP BLOCK IDENTIFIER  *
               &SCH=,              INTERVAL IF ROUTINE IS TIME DRIVEN  *
               &RCR=               RECURSIVE CALL NEEDED TO GET LOCK
         GBLC  &IRAEPAD            ROUTINE ENTRY POINT ADDRESS NAME
         GBLC  &IRAEPER            ROUTINE RECOVERY POINT ADDR NAME
         GBLC  &IRAEPFL            ROUTINE INVOCATION FLAG MASK
         GBLC  &IRAEPPV            PREV RMEP BLOCK ADDRESS NAME
         GBLC  &IRAEPTM            PREV SCHED RMEP ADDRESS NAME
         GBLB  &IRAEPCM            IRARMEP COMMENTARY PRINT CONTROL
         LCLC  &RMEPFLG            ROUTINE INVOCATION, TYPE FLAGS
         LCLC  &RMEPINT            SCHEDULED ROUTINE INVOCN INTERVAL
         LCLA  &RMEPTYP            ROUTINE TYPE HEX ACCUMULATOR
         AIF   (&IRAEPCM).RMEPAL1  BYPASS DUPLICATE IRARMEP INTRO
         IEZBITS
         AGO   .RMEPAL0
*                                                                    */
*@       LIST  PUSH;                /* SAVE LISTING STATUS FOR RMEP  */
*        %IF RMEPSFX = ''           /* IS NAMING CHARACTER OMITTED   */
*            %THEN %RMEPSFX = ' ';  /* YES, INSERT PLACEHOLDER BLANK */
*        %IF RMEPSFX ^= ' '         /* IS THIS THE DUPLICATE INCLUDE */
*            %THEN %RMEPDUM = '@       LIST  OFF '; /* BYP INTRO CMT */
*        %ELSE     %RMEPDUM = '@       LIST  PUSH'; /* DMY CNTRL CMT */
*RMEPDUM;
*        %IF RMEPSFX = ' '          /* IS THIS  THE FIRST INCLUDE    */
*            %THEN %RMEPDUM = '@       LIST  POP '; /* PRT INTRO CMT */
*RMEPDUM;
*%;  /*
.RMEPAL0 ANOP                                                        */
* /* MACCOMP Z-4 SC1CX/PKD16                                         */
* /* MACSTAT Z-4 75007/Z40BPSS                                       */
* /* *************************************************************** */
* /*                                                                 */
* /*   MACRO NAME - IRARMEP                                          */
* /*                                                                 */
* /*   DESCRIPTIVE NAME - SYSTEM RESOURCES MANAGER ENTRY POINT BLOCK */
* /*                                                                 */
* /*   COPYRIGHT - NONE                                              */
* /*                                                                 */
* /*   STATUS - OS/VS2 RELEASE 4, 08/01/75, LEVEL=1                  */
* /*                                                                 */
* /*   FUNCTION - THE RMEP DESIGNATES A SYSTEM RESOURCES MANAGER     */
* /*         PROCESSING ROUTINE WHICH MAY BE INVOKED THROUGH THE     */
* /*         CONTROL ALGORITHM.   THE RMEP CONTAINS THE ROUTINE      */
* /*         ENTRY POINT ADDRESS, DEFINES A BIT MASK TO BE USED      */
* /*         TO REQUEST THE ROUTINE, AND OPTIONALLY PROVIDES FOR     */
* /*         PERIODIC EXECUTION OF THE ROUTINE.  THE RMEP BLOCK      */
* /*         CONTAINS FLAGS INDICATING HOW THE DESCRIBED ROUTINE     */
* /*         MAY BE INVOKED.                                         */
* /*                                                                 */
* /*   METHOD OF ACCESS - THE ADDRESS OF THE CURRENT RMEP IS IN      */
* /*         THE -RRPAEPA- FIELD OF THE SYSTEM RESOURCES MANAGER     */
* /*         RECOVERY ROUTINE PARAMETER AREA.                        */
* /*                                                                 */
* /* *************************************************************** */
*%;  /*
&IRAEPCM SETB  1                   IRARMEP COMMENTARY WAS PRINTED
         AGO   .RMEPAL1                                              */
*@       LIST  OFF;                 /* BYPASS REMAINING COMMENTARY   */
* /* *************************************************************** */
* /*                                                                 */
* /*   CONSTRUCTION METHOD - THE RMEP LOCATIONS FOR ALGORITHMS -     */
* /*         ROUTINES WITH SYSTEM-WIDE PROCESS EFFECTS - ARE SHOWN   */
* /*         IN THE IRAEPAT MAPPING MACRO.  THE RMEP LOCATIONS FOR   */
* /*         ACTION ROUTINES, WHICH PERFORM PROCESSING FOR A GIVEN   */
* /*         USER, ARE SHOWN IN THE IRAEPDT MAPPING MACRO.           */
* /*                                                                 */
* /*   INVOCATION PARAMETERS AND/OR KEYWORDS =                       */
* /*                                                                 */
* /*     BAL - DSECT=NO IF DSECT IS NOT TO BE PRODUCED.              */
* /*         - OTHERWISE, SEE MACRO INSTRUCTION PROTOTYPE OPERANDS.  */
* /*                                                                 */
* /*           IF DSECT=NO IS NOT CODED, USING ON RMEP WILL GIVE     */
* /*           ADDRESSABILITY TO ALL SYMBOLS.  NO OTHER OPERANDS     */
* /*           ARE REQUIRED IN SUCH CASE BY THE IRARMEP MACRO.       */
* /*                                                                 */
* /*           IF DSECT=NO IS CODED, THE IRARMEP MACRO WILL GEN      */
* /*           'DC' INSTRUCTIONS CONTAINING APPROPRIATE VALUES,      */
* /*           IN ACCORDANCE WITH THE SPECIFIED MACRO OPERANDS.      */
* /*                                                                 */
* /*     PLS - %RMEPSFX='USERCHAR' IF THE INDICATED CHARACTER IS     */
* /*           TO BE APPENDED TO ALL NAMES IN THE MAPPING, THEREBY   */
* /*           PERMITTING MULTIPLE INCLUDES WHICH CREATE DUPLICATE   */
* /*           DEFINITIONS BASED ON DIFFERENT POINTERS.              */
* /*                                                                 */
* /*           NOTE - DUE TO THE MULTIPLE INCLUDE FACILITY FOR THE   */
* /*           IRARMEP MACRO, IT IS NOT POSSIBLE TO CONCATENATE THE  */
* /*           RMEP DECLARATION WITH ANOTHER DECLARATION IN SUCH A   */
* /*           FASHION THAT BOTH ARE BASED ON THE SAME POINTER.      */
* /*                                                                 */
* /*         TO ACCESS AS A STANDALONE STRUCTURE, DCL RMEPPTR PTR.   */
* /*                                                                 */
* /*   CHANGE ACTIVITY =                                             */
* /*                                                                 */
* /*   A000000-999999                                          Y02133*/
* /*                                                                 */
* /* *************************************************************** */
*%;  /*
.RMEPAL1 ANOP
         SPACE 1
         AIF   ('&DSECT' EQ 'NO').RMEPAL2
RMEP     DSECT
RMEPEPB  DS    CL16 -              ENTRY POINT DESCRIPTOR BLOCK
RMEPEPA  DS    A -                 ROUTINE NORMAL ENTRY ADDRESS
RMEPERA  DS    A -                 ROUTINE RECOVERY ENTRY ADDRESS
RMEPFLG  DS    0XL4 -              ROUTINE INVOCATION FLAG FIELD
         DS    XL3 -               ROUTINE INVOCATION FLAG IDENTIFIER
         DS    XL1 -               ROUTINE INVOCATION TYPE IDENTIFIER
RMEPRCR  EQU   BIT5 -              RECURSIVE CALL NEEDED TO GET LOCK
RMEPTMD  EQU   BIT6 -              ROUTINE INVOCATION TIME-DEPENDENT
RMEPACN  EQU   BIT7 -              ROUTINE PROCESS USER LEVEL ACTION
RMEPPRV  DS    A -                 ADDRESS OF PREV BLOCK IN RMEP CHAIN
RMEPEND  DS    0D -                END OF RMEP BASIC ENTRY BLOCK
RMEPSCH  DS    CL16 -              ENTRY POINT SCHEDULING EXTENSION
RMEPFWD  DS    A -                 TIME DRIVEN CHAIN FORWARD POINTER
RMEPBCK  DS    A -                 TIME DRIVEN CHAIN BACKWRD POINTER
RMEPTME  DS    F -                 TIME WHEN ROUTINE TO BE INVOKED
RMEPINT  DS    F -                 ROUTINE INVOCATION INTERVAL
RMEPSND  DS    0D -                END OF RMEP SCHEDULING EXTENSION
RMEPLEN  EQU   RMEPEND-RMEP -      LENGTH OF RMEP
         AGO   .RMEPML4
.RMEPAL2 ANOP
&IRAEPAD SETC  'IRARMNOP'          REINIT TO DUMMY ENTRY POINT NAME
&IRAEPER SETC  'IRARMNOP'          REINIT TO DUMMY RETRY POINT NAME
&IRAEPFL SETC  '00000000'          REINIT TO DUMMY RMEP INVOCN FLAG
         IRAEPNMP  EPN=&EPN,ERR=&ERR  GET ROUTINE ENTRY DESCRIPTION
         AIF   ('&IRAEPAD' EQ 'IRARMNOP').RMEPML4
&RMEPTYP SETA  1                   INIT TO NON-ZERO INDEX
         AIF   ('&NXT' EQ '').RMEPYL1  TEST DEFAULT CHAINING USED
&IRAEPPV SETC  'RMEPB&NXT'         SETUP PREVIOUS CHAINED RMEP NAME
         AIF   ('&NXT'(1,3) NE '000').RMEPYL1  BYP CLR PRV IF NOT END
&IRAEPPV SETC  '00000000'          INIT TO NO PREVIOUS ASSEMBLED RMEP
.RMEPYL1 AIF   ('&RCR' NE 'YES').RMEPYL2  TEST RTNE CRITICAL CALL
&RMEPTYP SETA  &RMEPTYP+4          SETON HEX BIT 1
.RMEPYL2 AIF   ('&SCH' EQ '').RMEPYL3  TEST ROUTINE TIME-DRIVEN
&RMEPTYP SETA  &RMEPTYP+2          SETON HEX BIT 2
.RMEPYL3 AIF   ('&IRAEPFL'(8,1) NE '1').RMEPYL4  TEST USER RTNE
&RMEPTYP SETA  &RMEPTYP+1          SETON HEX BIT 3
.RMEPYL4 ANOP
&RMEPFLG SETC  '&IRAEPFL'(1,7).'0123456789ABCDEF'(&RMEPTYP,1)
         DS    0D
RMEPB&EPN EQU  *
         AIF   ('&NAME' EQ '').RMEPAL3
&NAME    EQU   *
.RMEPAL3 ANOP
*        DS    CL16 -              ENTRY POINT DESCRIPTOR BLOCK
         DC   A(0) A(&IRAEPAD) -  ROUTINE NORMAL ENTRY ADDRESS
         DC   A(0) A(&IRAEPER) -  ROUTINE RECOVERY ENTRY ADDRESS
         DC   X'00' X'&RMEPFLG' -  ROUTINE INVOCATION FLAG IDENTIFIER
         DC   A(0) A(&IRAEPPV) -  ADDRESS OF PREV BLOCK IN RMEP CHAIN
&IRAEPPV SETC  'RMEPB&EPN'         SAVE AS PREVIOUS ASSEMBLED RMEP
         AIF   ('&SCH' EQ '').RMEPML4  BYPASS SCHEDULE IF NOT WANT
&RMEPINT SETC  '0000000'(1,8-K'&SCH).'&SCH'  PICK SCHEDULING INTERVAL
*        DS    CL16 -              ENTRY POINT SCHEDULING EXTENSION
         DC    A(0) -              TIME DRIVEN CHAIN FORWARD POINTER
         DC   A(0) A(&IRAEPTM) -  TIME DRIVEN CHAIN BACKWRD POINTER
         DC    F'0' -              TIME WHEN ROUTINE TO BE INVOKED
         DC    F'&RMEPINT' -       ROUTINE INVOCATION INTERVAL
&IRAEPTM SETC  'RMEPB&EPN'         SAVE AS PREVIOUS SCHEDULED RMEP
.RMEPML4 ANOP
         MEND
*                                                                    */
*@       LIST  POP;                 /* RESUME LIST STATUS FOR RMEP   */
*        %RMEPLABL = 'RMEP';        /* INITIALIZE THE LABEL VARIABLE */
*        %IF RMEPSFX ^= ' '         /* IS THIS THE DUPLICATE INCLUDE */
*            %THEN %RMEPLABL = 'RMEP'××RMEPSFX; /* RENAME STRUCTURE  */
*        %IF RMEP9999 ^= ','        /* DOES STRUCTURE CONTINUE       */
*            %THEN %RMEP9999 = ';'; /* NO, END DECLARE STATEMENT     */
*        %RMEPDUM = RMEPLABL××'PTR' ;  /* QUALIFY BASED POINTER NAME */
*   DCL
*     1 RMEPLABL BASED(RMEPDUM) BDY(DWORD),
*         %RMEPLABL = 'RMEPEPB'××RMEPSFX       ;
*       10 RMEPLABL CHAR(16) BDY(DWORD), /* ENTRY POINT BLOCK        */
*         %RMEPLABL = 'RMEPEPA'××RMEPSFX       ;
*         12 RMEPLABL PTR(31),      /* ENTRY POINT ADDRESS           */
*         %RMEPLABL = 'RMEPERA'××RMEPSFX       ;
*         12 RMEPLABL PTR(31),      /* ERROR RETRY POINT ADDRESS     */
*         %RMEPLABL = 'RMEPFLG'××RMEPSFX       ;
*         12 RMEPLABL BIT(32) BDY(WORD), /* INVOCATION FLAG MASK     */
*         %RMEPLABL = 'RMEPVFL'××RMEPSFX       ;
*           14 RMEPLABL BIT(29),    /* RTNE INVOC FLAG FIELD @Z40BPCH*/
*         %RMEPLABL = 'RMEPRCR'××RMEPSFX       ;
*           14 RMEPLABL BIT(01),    /* CRITICAL ALGORITHM INDICATOR  */
*         %RMEPLABL = 'RMEPTMD'××RMEPSFX       ;
*           14 RMEPLABL BIT(01),    /* RTNE INVOKE TIME-DEPENDENT    */
*         %RMEPLABL = 'RMEPACN'××RMEPSFX       ;
*           14 RMEPLABL BIT(01),    /* RTNE PERFORMS USER LEVL ACTN  */
*         %RMEPLABL = 'RMEPPRV'××RMEPSFX       ;
*         12 RMEPLABL PTR(31),      /* ADDRESS OF PREV RMEP BLOCK    */
*         %RMEPLABL = 'RMEPEND'××RMEPSFX       ;
*       10 RMEPLABL CHAR(0) BDY(DWORD),  /* END OF BASE RMEP         */
*         %RMEPLABL = 'RMEPSCH'××RMEPSFX       ;
*       10 RMEPLABL CHAR(16) BDY(DWORD), /* SCHEDULING EXTENSN       */
*         %RMEPLABL = 'RMEPFWD'××RMEPSFX       ;
*         12 RMEPLABL PTR(31),      /* TIME DRIVEN CHAIN FORWRD PTR  */
*         %RMEPLABL = 'RMEPBCK'××RMEPSFX       ;
*         12 RMEPLABL PTR(31),      /* TIME DRIVEN CHAIN BCKWRD PTR  */
*         %RMEPLABL = 'RMEPTME'××RMEPSFX       ;
*         12 RMEPLABL FIXED(32) UNSIGNED, /* TIME WHEN ENTRY SCHED   */
*         %RMEPLABL = 'RMEPINT'××RMEPSFX       ;
*         12 RMEPLABL FIXED(32) UNSIGNED, /* INVOCATION INTERVAL     */
*         %RMEPLABL = 'RMEPSND'××RMEPSFX       ;
*       10 RMEPLABL CHAR(0) BDY(DWORD)   /* END OF SCHD RMEP         */
*   RMEP9999
*   %RMEPLEN = 'LENGTH(RMEPEPB)' ;
