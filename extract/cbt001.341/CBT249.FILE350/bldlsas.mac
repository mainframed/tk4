*+--------------------------------------------------------------------+
 ×           GTFBLDL:  RESIDENT BLDL LIST GENERATOR                   ×
 ×   FUNCTION:                                                        ×
 ×       TO ANALYZE GTF TRACE RECORDS OF SVC 18 (BLDL/FIND)           ×
 × ACTIVITY, AND FROM THE RESULTS, GENERATE A LIST IN THE CORRECT     ×
 × SYNTAX TO BE PLACED IN A 'IEABLDXX' MEMBER OF SYS1.PARMLIB.        ×
 ×                                                                    ×
 ×   USAGE:                                                           ×
 ×      THIS SAS PROGRAM MUST BE EXECUTED USING YOUR INSTALLATION     ×
 × STANDARD SAS PROCEDURE WITH THE FOLLOWING ADDITIONAL DD            ×
 × STATEMENTS INCLUDED:                                               ×
 ×                                                                    ×
 × //GTF DD DSN=...                                                   ×
 ×                                                                    ×
 × SPECIFIES THE INPUT GTF DATA, WHICH SHOULD HAVE BEEN CAPTURED BY   ×
 × RUNNING GTF WITH THE EXEC PARMS OF 'MODE=EXT,TIME=YES,DEBUG=NO'.   ×
 × THE TRACE OPTIONS REQUIRED ARE:                                    ×
 ×         TRACE=SVCP                                                 ×
 ×         SVC=(18),END                                               ×
 × THIS GTF SETUP CAUSES LOW OVERHEAD, AND THE TRACE DATA MAY BE      ×
 × DIRECTED TO DISK WITHOUT MUCH PROBLEM.  (8 HOURS OF TRACE DATA     ×
 × FROM AN ACTIVE AMDAHL V8 SYSTEM WAS ACCUMULATED IN A DATASET OF    ×
 × LESS THAN 15 3350 CYLINDERS.)                                      ×
 ×                                                                    ×
 × //SASLIB DD DISP=SHR,DSN=...                                       ×
 ×                                                                    ×
 × SPECIFIES THE LOAD LIBRARY CONTAINING THE 'BLDLBLDR' LOAD MODULE.  ×
 ×                                                                    ×
 ×    THE FOLLOWING DD STATEMENTS ARE REQUIRED BY THE 'BLDLBLDR'      ×
 × PROGRAM, WHICH IS INVOKED AS A PROCEDURE FROM THE GTFBLDL SAS      ×
 × PROGRAM:                                                           ×
 ×                                                                    ×
 × //LPALIB DD DISP=SHR,DSN=SYS1.LPALIB                               ×
 ×                                                                    ×
 × //LINKLIB DD DISP=SHR,DSN=SYS1.LINKLIB                             ×
 × //        DD DISP=SHR,DSN=LNKLSTXX.LIBRARY1                        ×
 ×           .                                                        ×
 ×           .                                                        ×
 ×           .                                                        ×
 × //        DD DISP=SHR,DSN=LNKLSTXX.LIBRARYN                        ×
 ×                                                                    ×
 × SPECIFIES THE LINKLIST CONCATENATION ACTIVE WHEN THE GTF TRACE     ×
 × WAS RUN.                                                           ×
 ×                                                                    ×
 × //BLDLMEMS DD DSN=BLDL.CNTL,DISP=OLD                               ×
 ×                                                                    ×
 × SPECIFIES A CARD IMAGE FILE PRODUCED BY THE FIRST PHASE OF THE     ×
 × GTFBLDL SAS PROGRAM AND READ BY THE BLDLBLDR PROGRAM.  IT IS THIS  ×
 × FILE WHICH CONTAINS THE NAMES OF PROGRAMS, SORTED IN DESCENDING    ×
 × ORDER BY USE COUNT, WHICH WERE SELECTED FROM THE GTF TRACE DATA.   ×
 ×                                                                    ×
 × //MEMLIST DD DSN=&&MEMLIST,UNIT=VIO,SPACE=(CYL,(1,1)),             ×
 × //      DCB=(RECFM=FB,LRECL=12,BLKSIZE=...)                        ×
 ×                                                                    ×
 × SPECIFIES THE FILE CONTAINING THE PROGRAMS SELECTED BY BLDLBLDR    ×
 × TO BE PLACED IN THE RESIDENT BLDL LIST, ALONG WITH THEIR USE       ×
 × COUNT AND LINKLIB CONCATENATION NUMBER.  THIS FILE IS READ BY THE  ×
 × SECOND PHASE OF THE GTFBLDL SAS PROGRAM.                           ×
 ×                                                                    ×
 × //SYSPRINT DD SYSOUT=A                                             ×
 ×                                                                    ×
 × THIS FILE RECEIVES THE REPORT FROM BLDLBLDR, LISTING THE PROGRAMS  ×
 × SELECTED FOR THE RESIDENT BLDL LIST, THEIR USE COUNT AND           ×
 × CONCATENATION NUMBER, AND THE NUMBER OF PAGES OF MEMORY REQUIRED   ×
 × TO CONTAIN THE LIST.                                               ×
 ×                                                                    ×
 × //SYSPUNCH DD DSN=XXX.CNTL(IEABLDXX),DISP=OLD                      ×
 ×                                                                    ×
 × SPECIFIES THE FILE INTO WHICH BLDLBLDR WILL PLACE THE GENERATED    ×
 × BLDL LIST.  THIS LIST MAY THEN BE COPIED INTO SYS1.PARMLIB.        ×
 ×                                                                    ×
 ×                           PHILIP A. FOSTER                         ×
 ×                           I.S.T. INC.                              ×
 ×                           2 COMPLEXE DESJARDINS                    ×
 ×                           MONTREAL, QUEBEC                         ×
 ×                           H5B 1B3                                  ×
 ×                           (514) 284-1111                           ×
 +--------------------------------------------------------------------+
;
PAGE;
*
   GTFBLDL PHASE 1:  GTF DATA SELECTION AND ANALYSIS
                                                      ;
DATA BLDLPGM (KEEP=PGMNAME);
INFILE GTF; INPUT @1  ID       $2.
      @11 EID      $2.@;
IF ID='FF01'X AND EID='1000'X; /* SELECT SVC TRACE RECORDS */
INPUT @29 SVCNO    PIB2.
      @55 DCBPTR   IB4.@;
IF SVCNO=18 AND DCBPTR GE 0; /* SELECT SVC 18 BLDL RECORDS */

LINKSEL;  /*  INVOKE MACRO TO SELECT BLDLS TO LINKLIB DCB  */

INPUT @62 SS       $2.@;
IF SS='SS' THEN DELETE;      /* SCRAP 'SECURE' RECORDS     */
INPUT @62 LENGTH   PIB2.@;
IF LENGTH < 58 THEN DELETE;  /* IGNORE SHORT ENTRIES       */
INPUT @64 PGMNAME  $8.;
PROC FREQ;                   /* DETERMINE USAGE FREQUENCY  */
 TABLES PGMNAME/OUT=PGMFREQ NOPRINT;
PROC SORT DATA=PGMFREQ OUT=PGMFREQ;
 BY DESCENDING COUNT;
*  PRODUCE BLDLMEMS FILE ;
DATA _NULL_;
SET PGMFREQ;
FILE BLDLMEMS;
PUT PGMNAME $8.
 @10 COUNT 6.;
*
   GTFBLDL PHASE 2:  RESIDENT BLDL BUILDER
                                                      ;
PROC BLDLBLDR;  /*   INVOKE BLDL LIST BUILDER AS SAS PROC   */
*
   GTFBLDL PHASE 3:  BREAKDOWN BY LINKLIST LIBRARY
                                                      ;
DATA HITLIST;
INFILE MEMLIST;
INPUT @1 PGMNAME  $8.
         CONCT_NO PIB1.
         USE_CNT  PIB3.;
PROC SORT DATA=HITLIST;
  BY CONCT_NO DESCENDING USE_CNT;
PROC PRINT N;
BY CONCT_NO;
SUM USE_CNT;
SUMBY CONCT_NO;
ID PGMNAME;
TITLE1 'GTF BLDL ANALYSIS';
TITLE2 'MODULE USAGE BY DIMINISHING FREQUENCY';
TITLE3 'AND LINKLIB CONCATENATION NUMBER';
TITLE4 'CONCT_NO IS CONCATENATION NUMBER (0-15) AS';
TITLE5 'AS SPECIFIED IN LINKLIB DD STATEMENT';
PROC FREQ DATA=HITLIST;
 TABLES CONCT_NO/NOPRINT OUT=LNKLST;
 WEIGHT USE_CNT;
PROC SORT DATA=LNKLST; BY DESCENDING COUNT;
PROC PRINT;
ID CONCT_NO;
TITLE1 RECOMMENDED CONCATENATION ORDER FOR LINKLIST LIBRARIES;
TITLE2 BASED ON MODULE USAGE BY LINKLIB DD CONCATENATION NUMBER;
TITLE3 'CONCT_NO IS CONCATENATION NUMBER (0-15) AS';
TITLE4 AS SPECIFIED IN LINKLIB DD STATEMENT;
