         TITLE 'EDTDSP0  F O R M A T  D M S D S P 0  R E C O R D'
         PRINT NOGEN
         DMSMACRO
         PRINT ON,GEN
         SPACE
EDTDSP0  PGMSTART
         USING DMSCONST,DMSREG
         USING BLKBUFF,R7
         LR    R7,R1                    RECORD POINTER
         MVI   LTEXT,64
         MVC   LTEXT+1(122),LTEXT       CLEAR LINE
         HEADER L=DSP
         LA    R6,EMSGTABC
         SR    R5,R5                    CLEAR FOR
         IC    R5,BLKPSWCD+1            INSERT EVENT TYPE OF DISP,
         LTR   R5,R5                    IS IT VALID TYPE
         BZ    RETURN                   NO, GO OUT
         CLI   BLKPSWCD+1,6             IS IT VALID TYPE
         BH    RETURN                   NO, GO OUT
         BCTR  R5,R0                    SUBTRACT ONE FOR INDEXING
         SLL   R5,5                     MULTIPL0 WITH 32
         LA    R6,0(R5,R6)              POINT TO ACCURATE MSG
         MVC   LTEXT+59(32),0(R6)       MOVE MSG TO LINE
         LA    R9,LTEXT                 POINT TO START OF TEXT
         MVC   0(15,R9),=CL15'GPRS 0,1,14,15:'
         LA    R9,18(,R9)               NEW DATA FIELD
         LA    R6,BLKDATA               DATA FIELD
         LA    R4,2                     COUNT 1
LOOP0    LA    R3,2                     COUNT 2
LOOP1    UNPK  0(9,R9),0(5,R6)          CONVERT
         TR    0(8,R9),TRTAB            TO EBCDIC
         MVI   8(R9),64                 BLANK TO BAD FIELD
         LA    R9,9(,R9)                NEW ADDRESS
         LA    R6,4(,R6)                TO BOTH REGS
         BCT   R3,LOOP1                 PROCESS FIRST GROUP
       LA      R9,2(,R9)                START NEW GROUP
         BCT   R4,LOOP0                 PROCESS AL.
         PRINTER LINE
RETURN   L     R13,4(R13)
         LM    R14,R12,12(R13)
         SR    R15,R15
         BR    R14
         EJECT
         DS    0D
EMSGTABC DC    CL32'GLOBAL SRB DISPATCH'
         DC    CL32'WAIT TASK DISPATCH'
         DC    CL32'LOCAL SRB DISPATCH'
         DC    CL32'LOCAL SUPERVISOR DISPATCH'
         DC    CL32'NORMAL TASK DISPATCH'
         DC    CL32'TASK DISPATCH FROM EXIT'
         DC    CL32' '
LINE     DC    AL2(137,0),CL10' '
LTEXT    DC    123C' '
         DS    0F
TRTAB    DC    256X'40'
         ORG   TRTAB+C'A'
         DC    X'0A0B0C0D0E0F'
         ORG   TRTAB+C'0'
         DC    C'0123456789ABCDEF'
         LTORG
         END
