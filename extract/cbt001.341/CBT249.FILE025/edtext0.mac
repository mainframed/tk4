         TITLE 'EDTEXT0  F O R M A T  D M S E X T 0  R E C O R D'
         PRINT NOGEN
         DMSMACRO
         PRINT ON,GEN
****************************************<
*                                       *
*                                       *
*        E D T E X T 0                  *
*                                       *
*                                       *
*****************************************
         SPACE
EDTEXT0  PGMSTART
         USING DMSCONST,DMSREG
         USING BLKBUFF,R7
         LR    R7,R1                    RECORD
         MVI   LTEXT,64
         MVC   LTEXT+1(122),LTEXT       CLEAR LINE
         HEADER L=EXT
         LA    R9,LTEXT+9               POINT TO TEXT
         CLC   BLKPSWCD(2),=XL2'0080'   INTERVAL TIMER
         BNE   *+10
         MVC   9(20,R9),=CL20'INTERVAL TIMER'
         CLC   BLKPSWCD(2),=XL2'0040'   INTERRUPT KEY
         BNE   *+10
         MVC   9(20,R9),=CL20'INTERRUPT KEY'
         TM    BLKPSWCD,X'FF'           IS
         BNZ   NOSIG                    IT
         TM    BLKPSW+1,X'3F'           EXTERNAL SIGNAL
         BZ    NOSIG
         MVC   9(20,R9),=CL20'EXTERNAL SIGNAL'
NOSIG    CLC   BLKPSWCD(2),=XL2'1200'   MALFUNCTION ALERT
         BNE   *+10
         MVC   9(20,R9),=CL20'MALFUNCTION ALERT'
         CLC   BLKPSWCD(2),=XL2'1201'   EMERGENCY SIGNAL
         BNE   *+10
         MVC   9(20,R9),=CL20'EMERGENCY SIGNAL'
         CLC   BLKPSWCD(2),=XL2'1202'   EXTERNAL CALL
         BNE   *+10
         MVC   9(20,R9),=CL20'EXTERNAL CALL'
         CLC   BLKPSWCD(2),=XL2'1003'   TOD SYNC CHECK
         BNE   *+10
         MVC   9(20,R9),=CL20'TOD CLOCK SYNC CHECK'
         CLC   BLKPSWCD(2),=XL2'1004'   CLOCK COMPARATOR
         BNE   *+10
         MVC   9(20,R9),=CL20'CLOCK COMPARATOR'
         CLC   BLKPSWCD(2),=XL2'1005'   CPU TIMER
         BNE   *+10
         MVC   9(20,R9),=CL20'CPU TIMER'
         TM    BLKPSWCD,X'02'           IS CPU ID PRESENT
         BZ    EDTEXT00                 NO
         BCTR  R9,R0                    SUBTRACT FOR GOOD LOCATION
         MVC   0(4,R9),=CL4'CPU='
         UNPK  5(5,R9),BLKDATA(3)
         TR    5(4,R9),TRTAB
         MVI   9(R9),64
EDTEXT00 CLC   0(20,R9),TRTAB           IS ANYTHING IN LINE
         BE    RETURN                   NO, DO NOT PRINT
         PRINTER LINE
RETURN   L     R13,4(R13)
         LM    R14,R12,12(R13)
         SR    R15,R15
         BR    R14
         EJECT
LINE     DC    AL2(137,0),CL10' '
LTEXT    DC    123C' '
         DS    0F
TRTAB    DC    256X'40'
         ORG   TRTAB+C'A'
         DC    X'0A0B0C0D0E0F'
         ORG   TRTAB+C'0'
         DC    C'0123456789ABCDEF'
         LTORG
         END
