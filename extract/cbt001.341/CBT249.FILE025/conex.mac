         MACRO
&NAME    CONEX &BLEN=1,&L=DMSID,&COMP=255,&TEXT='TEXT',&RTN=NO,        X
               &RTNID=DMSID,&HEXA=NO,&SKIP=NO,&CHAR=NO,&DEC=NO,        X
               &CHKSEL=NO,&CHKALL=NO
         LCLA  &A1
&A1      SETA  K'&TEXT
         CNOP  0,4                      ALIGN TO WORD BOUNDARY
&NAME    BAL   R1,CON&SYSNDX            GO AROUND CONST
         DC    AL1(&BLEN)               LENGTH OF STRING
         DC    AL3(&L-DMSID)            OFFSET OF STRING WITHIN CONST
         DC    AL1(0)                   FLAG BYTE
         DC    AL3(&RTNID-DMSID)        OFFSET OF RTN BYTE
         DC    AL1(&COMP)               VALUE FOR =ALL/=OFF COMPARISON
         DC    AL1(&A1-3)               LENGTH OF TEXT IN EXECUTE FORM
         DC    C&TEXT                   TEXT
CON&SYSNDX DS 0H
         AIF   ('&RTN' EQ 'NO').NOID
         OI    4(R1),X'80'              CHECK FOR RTN
.NOID    AIF   ('&HEXA' EQ 'NO').NOHEXA
         OI    4(R1),X'40'              PRINT STRING AS HEX AFTER TEXT
.NOHEXA  AIF   ('&SKIP' EQ 'NO').NOSKIP
         OI    4(R1),X'20'              SKIP MAP PRINTING
.NOSKIP  AIF   ('&CHAR' EQ 'NO').NOCHAR
         OI    4(R1),X'10'              STRING IS CHARACTER FIELD
.NOCHAR  AIF   ('&CHKSEL' EQ 'NO').NOSEL
         OI    4(R1),X'08'              EQUAL COMPARE MEANS =OFF
.NOSEL   AIF   ('&CHKALL' EQ 'NO').NOALL
         OI    4(R1),X'04'              EQUAL COMPARE MEANS =ALL
.NOALL   AIF   ('&DEC' EQ 'NO').NODEC
         OI    4(R1),X'02'              PRINT STRING AS DECIMAL VALUE
.NODEC   ANOP
         BAL   R14,STRINGRT             GO TO STRING CONVERTER
         MVI   CONLTEXT,64              BLANK OUT
         MVC   CONLTEXT+1(119),CONLTEXT LINE
         SPACE
         MEND
