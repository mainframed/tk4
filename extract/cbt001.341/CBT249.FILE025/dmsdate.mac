DATE     TITLE 'DMSDATE / DATE EDIT ROUTINE '
* * * * * * * * * * * * * * * * * * * * *
*                                       *
*              D M S D A T E            *
*                                       *
* * * * * * * * * * * * * * * * * * * * *
         SPACE
* FUNCTION - OPERATION
*        THIS ROUTINE FORMATS THE CURRENT DATE & TIME IN A
*        PRINTABLE FORM
         SPACE
* INPUT
*        R1 POINTS TO A VARIABLE LENGTH PARAMETER LIST
*        THE FIRST ADDRESS POINTS TO AN 8-BYTE FIELD,
*        WHICH WILL RECEIVE THE EDITED DATE IN THE FORM MM/DD/JJ
*        THE SECOND ADDRESS (IF SPECIFIED) POINTS TO AN 8-BYTE FIELD,
*        WHICH WILL RECEIVE THE EDITED TIME IN THE FORM HH.MM.SS
         SPACE
* OUTPUT
*        LOADED DATE, AND IF REQUESTED, LOADED TIME FIELD
         SPACE
* EXIT
*        RETURN TO CALLER VIA BR 14
         EJECT
         PRINT ON,NOGEN
         DMSMACRO
DMSDATE  PGMSTART
         SPACE
         LM    R2,R3,0(R1)              -> TO PARMS
         TIME  DEC                      GET ACTUAL TIME
         LA    R4,56(R13)               POS. R9 ON SAVE
         SRL   R4,3                     ROUND TO
         SLL   R4,3                     DW WORK AREA
         SPACE
         LTR   R2,R2                    IS TIME REQUESTED
         BNP   DATDATE                  NO, GO
         SPACE
         ST    R0,0(R4)                 TO SAVE
         UNPK  0(3,R3),2(2,R4)          CONVERT SS
         MVC   6(2,R3),0(R3)            SHIFT TO PLACE
         UNPK  3(3,R3),1(2,R4)          CONVERT MM
         UNPK  0(3,R3),0(2,R4)          CONVERT SS
         MVI   2(R3),C'.'               SET
         MVI   5(R3),C'.'               SEPARATORS
         SPACE 2
DATDATE  ST    R1,4(R4)
         UNPK  0(3,R2),5(2,R4)          CONVERT
         MVC   6(2,R2),0(R2)            JJ
         XC    0(6,R4),0(R4)            CLEAR
         CVB   R5,0(R4)                 CONVERT DDD TO BIN
         SLDL  R0,28                    SHIFT UP TO ZONE
         SRL   R0,12                    SHIFT OUT DDD
         SRDL  R0,28                    SHIFT BACK JJ
         ST    R1,4(R4)                 CONVERT JJ
         CVB   R6,0(R4)                 TO BINARY
         SRDA  R6,32                    DIVIDE
         LA    R0,4                     JJ
         DR    R6,0                     BY FOUR
         LA    R1,1                     SET MONTH TO 1
         CH    R5,JANUARY               CHECK DAYS
         BNH   DATEDIT
         SH    R5,JANUARY
         LA    R1,2
         LTR   R6,R6                    IS IT LEAP
         BZ    DAT001                   YES, GO
         CH    R5,FEBRUARY
         BNH   DATEDIT
         SH    R5,FEBRUARY
         B     DAT002
DAT001   CH    R5,FEBRLEAP
         BNH   DATEDIT
         SH    R5,FEBRLEAP
DAT002   LA    R3,MARCH                 BASE DAY TABLE
DAT003   LA    R1,1(,R1)                INCR MONTH
         CH    R5,0(R3)                 IS IT THIS MONTH
         BNH   DATEDIT                  YES, GO
         SH    R5,0(R3)                 DECR DDD
         LA    R3,2(,R3)                INCR DAY TABLE
         B     DAT003                   RETRY
         SPACE 2
DATEDIT  CVD   R1,0(R4)                 CONVERT MONTH
         UNPK  0(2,R2),0(8,R4)          TO ZONED
         OI    1(R2),240                SET ZONE
         MVI   2(R2),C'/'               SET SEPARATOR
         CVD   R5,0(R4)                 CONVERT DAY
         UNPK  3(2,R2),0(8,R4)          TO ZONED
         OI    4(R2),240                SET ZONE
         MVI   5(R2),C'/'               SET SEPARATOR
         SR    R15,R15        SET RETURN CODE 0
         SPACE 2
         L     R13,4(R13)               RELOAD
         RETURN     (14,12),T,RC=(15)
         SPACE 3
***
*        CONSTANTS
***
         SPACE
JANUARY  DC    H'31'
FEBRUARY DC    H'28'
FEBRLEAP DC    H'29'
MARCH    DC    H'31'
         DC    H'30'
         DC    H'31'
         DC    H'30'
         DC    H'31'
         DC    H'31'
         DC    H'30'
         DC    H'31'
         DC    H'30'
         DC    H'31'
         SPACE 2
         END
