         TITLE 'DMSTFMT  C O N V E R T  T E X T  T O   O B J E C T'
         PRINT NOGEN
         DMSMACRO
         PRINT ON,GEN
*****************************************
*                                       *
*                                       *
*        D M S T F M T                  *
*                                       *
*                                       *
*****************************************
         SPACE 3
*        THIS PROGRAM USES CARD FORMAT TYPE RECORDS (NORMAL TEXT) AS
*        INPUT FROM SYSIN. IT WILL CONVERT THESE TEXT RECORDS TO AN
*        OBJECT MODULE THAT CAN BE PROCESSED AS INPUT BYLINKAGE
*        EDITOR.
*
*        EXAMPLE FOR JCL:
*
*        //S1 EXEC PGM=DMSTFMT
*        //SYSUT2 DD DSN=&&A,UNIT=SYSDA,SPACE=(CYL,(1,1)),
*        //  DCB=BLKSIZE=400,DISP=(,PASS)
*        //SYSIN DD DSN=DMS.SOURCE(DESCRIPT),DISP=SHR
*        //SYSUDUMP DD SYSOUT=A
*        //S2 EXEC PGM=IEWL,PARM='XREF,LIST,NCAL'
*        //SYSPRINT DD SYSOUT=A
*        //SYSUT1 DD UNIT=SYSDA,SPACE=(1024,(200,50))
*        //SYSLIN DD DSN=&&A,DISP=(OLD,DELETE),UNIT=SYSDA,
*        //  DCB=(RECFM=FB,BLKSIZE=400)
*        //SYSLMOD DD DSN=DMS.LOAD(DMSCRIPT),DISP=SHR
*        /*
         EJECT
DMSTFMT  PGMSTART
         USING DMSCONST,DMSREG
         OPEN (SYSIN1,(INPUT))
         L     R5,RECOUNT               LOAD RECORD COUNT
LOOP1    GET   SYSIN1,INPUT             READ CARD
         LA    R5,1(,R5)                INCREASE COUNTER
         B     LOOP1                    COUNT ALL CARDS
ENDRUN1  LTR   R5,R5                    AT LEAST ONE RECORD
         BNP   RETURN                   NO,GO OUT
         ST    R5,RECOUNT               SAVE RECORD COUNT
         MH    R5,=H'80'                MULTIPLY WITH RECORD SIZE
         ST    R5,BSIZE                 SAVE BUFFER SIZE
         GETMAIN R,LV=(R5)              GET BUFFER
         ST    R1,BADDR                 SAVE POINTER OF GOTTEN AREA
         CLOSE SYSIN1                   CLOSE FILE
         OPEN  (SYSIN2,(INPUT),SYSUT2,(OUTPUT))
         L     R6,BADDR                 LOAD BUFFER ADDRESS
LOOP2    GET   SYSIN2,INPUT             READ INFILE
         MVC   0(80,R6),INPUT           MOVE TO GOTTEN AREA
         LA    R6,80(,R6)               INCREMENT POINTER
         B     LOOP2                    READ ALL
ENDRUN2  CLOSE SYSIN2                   CLOSE INPUT FILE
         MVC   OUTPUT(80),TRTAB         CLEAR LINE
         MVC   CSTATMNT(4),ESDTEXT      MOVE ESD TO RECORD
         MVC   CSECTLEN(2),=AL2(16)     LENGTH OF ESD DATA
         MVC   CESDID(2),=AL2(1)        MOVE SD ID
         MVC   CARDTEXT(8),MODNAME1     MODULE NAME
         MVC   CARDTEXT+8(4),=AL4(0)    CSECT ID AND OFFSET IS ZERO
         MVC   CARDTEXT+13(3),BSIZE+1   LENGTH OF SECTION
         BAL   R14,SEQNUMB              FILLIN SEQUENCE NUMBER
         PUT   SYSUT2,OUTPUT            WRITE ESD CARD
         SR    R4,R4                    CLEAR REGISTER
         L     R5,BADDR                 ADDRESS OF BUFFER
         L     R7,BSIZE                 LENGTH OF BUFFER
LOOP3    MVC   OUTPUT(80),TRTAB         CLEAR LINE
         MVC   CSTATMNT(4),TXTTEXT      MOVE TEXT INDICATOR
         STCM  R4,7,CADDRESS            STORE OFFSET INTO RECORD
         MVC   CSECTLEN(2),=AL2(56)     STORE LENGTH OF DATA
         MVC   CESDID(2),=AL2(1)
         MVC   CARDTEXT(56),0(R5)       MOVE TEXT TO OUTPUT
         BAL   R14,SEQNUMB              INSERT CARD NUMBER
         PUT   SYSUT2,OUTPUT
         LA    R5,56(,R5)               POINT TO NEW INPUT
         LA    R4,56(,R4)               INCREASE DATA POINTER
         S     R7,=F'56'                DECREASE LENGTH
         C     R7,=F'56'                IS LENGTH MORE THAN ONE CARD
         BNL   LOOP3                    YES, MOVE ALL
         MVC   OUTPUT(80),TRTAB         CLEAR LINE
         MVC   CSTATMNT(4),TXTTEXT      MOVE TEXT INDICATOR
         STCM  R4,7,CADDRESS            STORE OFFSET INTO RECORD
         STCM  R7,3,CSECTLEN            STORE LENGTH INTO
         MVC   CESDID(2),=AL2(1)        SAME ID
         BCTR  R7,R0                    SUBTRACT ONE FOR EXECUTE
         EX    R7,MOVETEXT              MOVE INTO RECORD
         BAL   R14,SEQNUMB              INSERT CARD NUMBER
         PUT   SYSUT2,OUTPUT
         MVC   OUTPUT(80),TRTAB         CLEAR LINE
         MVC   CSTATMNT(4),ENDEND       END INDICATOR
         MVC   OUTPUT+32(20),=CL20'CONVERTED BY DMSTFMT'
         BAL   R14,SEQNUMB              INSERT CARD NUMBER
         PUT   SYSUT2,OUTPUT            WRITE LAST RECORD
         CLOSE SYSUT2
RETURN   L     R13,4(R13)               OLD SAVE AREA
         LM    R14,R12,12(R13)          CALLERS REGISTER
         SR    R15,R15                  CLEAR RETURN CODE
         BR    R14
MOVETEXT MVC   CARDTEXT(1),0(R5)        MOVE TEXT
         EJECT
SEQNUMB  L     R15,CCOUNT               GET CARD COUNT
         LA    R15,1(,R15)              INCREASE BY ONE
         ST    R15,CCOUNT               AND SAVE AGAIN
         CVD   R15,DWD1                 CONVERT
         UNPK  CARDSEQN(8),DWD1+4(4)    INTO
         OI    CARDSEQN+7,240           CARD TEXT
         BR    R14
         SPACE 3
DWD1     DC    1D'0'                    DOUBLE WORD
ENDEND   DC    X'02',CL3'END'           END CONTROL FIELD
TXTTEXT  DC    X'02',CL3'TXT'           TEXT CONTROL FIELD
ESDTEXT  DC    X'02',CL3'ESD'           ESD CONTROL FIELD
MODNAME1 DC    CL8'DMSCRIPT'            OUPUT MODULE NAME
CCOUNT   DC    1F'0'                    CARD COUNT
BADDR    DC    1F'0'                    ADDRESS OF BUFFER
BSIZE    DC    1F'0'                    SIZE FOR TEXT
RECOUNT  DC    1F'0'                    NUMBER OF INPUT RECORDS
INPUT    DC    CL80' '                  INPUT WORK SPACE
SYSIN1   DCB   DDNAME=SYSIN,DSORG=PS,MACRF=(GM),RECFM=FB,              X
               LRECL=80,EODAD=ENDRUN1
SYSIN2   DCB   DDNAME=SYSIN,DSORG=PS,MACRF=(GM),RECFM=FB,              X
               LRECL=80,EODAD=ENDRUN2
SYSUT2   DCB   DDNAME=SYSUT2,DSORG=PS,MACRF=(PM),                      X
               RECFM=FB,LRECL=80
TRTAB    DC    256X'40'
         ORG   TRTAB+C'A'
         DC    X'0A0B0C0D0E0F'
         ORG   TRTAB+C'0'
         DC    C'0123456789ABCDEF'
OUTPUT   DC    CL80' '
CSTATMNT EQU   OUTPUT+0
CADDRESS EQU   OUTPUT+5
CSECTLEN EQU   OUTPUT+10
CESDID   EQU   OUTPUT+14
CARDTEXT EQU   OUTPUT+16
CARDSEQN EQU   OUTPUT+72
         DC    C' '
         LTORG
         END
