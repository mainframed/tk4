         TITLE 'EDTCON0  F O R M A T  D M S M A C R O  R E C O R D'
         PRINT NOGEN
         DMSMACRO
         PRINT ON,GEN
*****************************************
*                                       *
*                                       *
*        E D T C O N 0                  *
*                                       *
*                                       *
*                                       *
*****************************************
         SPACE
EDTCON0  PGMSTART
         LA    R12,2048(R11)            ASSIGN
         LA    R12,2048(R12)            SECOND
         USING EDTCON0+4096,R12         BASE REGISTER
         USING DMSCONST,DMSREG
         LA    R9,16(R1)                POINT TO ACTUAL RECORD
         TM    PRTFLAG,PRTCONFO         IS ENTRY FROM LIST COMMAND
         BO    CONLST00                 YES, PRODUCE LIST OUTPUT
         TM    DMSEDTB1,DMSFORMA        IS EDIT =ALL SPECIFIED
         BZ    CONTST01                 NO,CHECK FOR FORMAT
         NI    DMSEDTB1,ALLBIT-DMSFORMA SET =ALL OFF
         OI    DMSEDTB1,DMSFORMC        SET FORMAT
         B     CONFOALL                 FORMAT SETUP
CONTST01 DS    0H
         TM    DMSFUNCT,DMSTEST
         BO    CONDMSCL
         TM    DMSEDTB1,DMSFORMC        IS FORMAT SELECTED
         BZ    RETURN                   NO, DO NOT FORMAT
         TM    DMSFUNCT-DMSID(R9),DMSSTOP+DMSEOJ
         BZ    RETURN
CONDMSCL CLC   DMSID(8),0(R9)           ARE WE ON RIGHT PLACE
         BE    CONOK                    YES, CONTINUE
CONABEND TM    DMSFUNCT,DMSTEST         IS ABEND WANTED
         BZ    RETURN                   NO, RETURN
         ABEND 1,DUMP                   FORCE ABEND
         SPACE
RETURN   MVI   CONLASAR,C'1'
         L     R13,4(R13)
         L     R14,12(R13)              TO
         L     R15,RETCODE
         LM    R0,R12,20(R13)           CALLER
         BR    R14
         EJECT
CONLST00 DS    0H
         MVI   CONLASAR,64
         TM    DMSFUNCT,DMSEDT+DMSMONIT IS MONITOR/EDIT FUNCTION ON
         BNZ   CONLST01                 YES, CONTINUE
         MVC   CONLTEXT(60),=CL60'NO MONITOR/EDIT FUNCTION SELECTED. LIX
               ST FUNCTION SUPPRESSED.' WRITE MESSAGE
         PRINTER CONLINE
         BLINE
         B     RETURN                   AND RETURN
CONLST01 TM    DMSFUNCT,DMSEDT          IS IT EDIT
         BZ    CONLST02                 NO, ITS MONITOR
         MVC   CONLTEXT(4),=CL4'EDIT'
         LA    R6,CONLTEXT+5
         B     CONLST03
CONLST02 MVC   CONLTEXT(7),=CL7'MONITOR'
         LA    R6,CONLTEXT+8
CONLST03 MVC   0(21,R6),=CL21'OPTIONS SELECTED ARE:'
         PRINTER CONLINE
         BLINE
         TM    DMSFUNCT,DMSEDT
         BZ    CONLST04
         TM    DMSEDTB1,DMSFORMA
         BZ    CONLST04
         MVC   CONLTEXT(8),=CL8'EDIT,ALL'
         TM    PRTFLAG,PRTCONGO
         BZ    *+10
         MVC   CONLTEXT+8(3),=CL3',GO'
         PRINTER CONLINE
         BLINE
         B     RETURN
CONOK    MVC   CONLTEXT+5(35),=CL35'DMS WAS CALLED UNDER MVS REL XX.X'
         MVC   CONLTEXT+34(02),DMSRELS1+4-DMSID(R9)
         MVC   CONLTEXT+37(2),DMSRELS1+6-DMSID(R9)
         MVC   CONLTEXT+40(17),=CL17'ON A CPU ???? MP'
         MVI   CONLASAR,C'1'
         UNPK  CONLTEXT+49(5),DMSRELS1+2-DMSID(3,R9)
         OI    CONLTEXT+52,240
         MVC   CONLTEXT+53(4),=CL4' MP'
         TM    CPUFLG-DMSID(R9),CPUMP
         BO    *+8
         MVI   CONLTEXT+54,C'U'
         L     R6,HIMACH-DMSID(R9)
         SRL   R6,16
         LA    R7,1(R6)
*        LR    R6,R7            THIS LINE IS DELETED BY              RY
         SRL   R6,4
         LA    R6,240(R6)
         MVC   CONLTEXT+58(15),=CL15'(00.0MEGABYTES)'                RY
         SLL   R7,28
         LTR   R7,R7
         BZ    *+10
         BCTR  R6,0
         MVI   CONLTEXT+62,C'5'                                      RY
         CLM   R6,1,=X'F9'                                           RY
         BL    NOMOTH9                                               RY
         MVI   CONLTEXT+59,C'1'                                      RY
         SL    R6,=F'10'                                             RY
NOMOTH9  LA    R6,1(R6)                                              RY
         STC   R6,CONLTEXT+60                                        RY
         MVC   CONLTEXT+74(23),=CL23'WITH NO SU''S INSTALLED.'
         OC    DMSSULEV-DMSID(16,R9),DMSSULEV-DMSID(R9)
         BZ    *+10
         MVC   CONLTEXT+79(21),=CL21'FOLLOWING SU BIT MAP:'
         PRINTER CONLINE
         MVI   CONLASAR,64
         BLINE
         OC    DMSSULEV-DMSID(16,R9),DMSSULEV-DMSID(R9)
         BZ    CONSUOK
         CONEX BLEN=14,L=DMSSULEV,TEXT='  ',HEXA=YES
       CONEX   TEXT='  ',SKIP=YES
CONSUOK MVC CONLTEXT+5(30),=CL30'MONITOR OPTIONS SELECTED WERE:'
         PRINTER CONLINE
         BLINE
         CONEX TEXT='  ',SKIP=YES
         MVI   CONLASAR,64
         MVC   CONLTEXT+5(4),=CL4'TEST'
         TM    DMSFUNCT-DMSID(R9),DMSTEST
         BO    CONOTST
CONLST04 TM    PRTFLAG,PRTCONFO         IS IT LIST
         BO    CONLST05                 YES, SKIP PRINT
         MVC   CONLTEXT+9(17),=CL17' WAS NOT SELECTED'
CONOTST  PRINTER CONLINE
         BLINE
         MVC   CONLTEXT+5(7),=CL7'DEFAULT'
         TM    DMSFUNCT-DMSID(R9),DMSDEFLT
         BO    CONODEFL
CONLST05 TM    PRTFLAG,PRTCONFO         IS IT LIST
         BO    CONLST06                 YES, SKIP PRINT
         MVC   CONLTEXT+12(17),=CL17' WAS NOT SELECTED'
CONODEFL PRINTER CONLINE
CONLST06 BLINE
         TM    DMSFUNCT,DMSEDT          IS IT EDIT
         BZ    CONLST07                 NO, PRINT
         TM    PRTFLAG,PRTCONFO         YES. IS IT LIST
         BO    CONLST08                 YES, SKIP PRINT
CONLST07 DS    0H
         CONEX BLEN=4,L=DMSBDFLT,TEXT='BP=',DEC=YES
CONLST08 DS    0H
         CONEX BLEN=2,L=CPUID,TEXT='CPUID=',CHKALL=YES
         CONEX BLEN=2,L=DMSASID,TEXT='ASID=',COMP=0,CHKALL=YES,HEXA=YES
         CONEX BLEN=2,L=EXTINT,TEXT='EXT=',CHKALL=YES,RTN=YES,         X
               RTNID=DMEXT+6,HEXA=YES
         CONEX BLEN=32,L=SVCINT,TEXT='SVC=',RTN=YES,CHKALL=YES,        X
               RTNID=DMSVC+6
         CONEX L=PGMINT,BLEN=32,TEXT='PGM=',RTN=YES,CHKALL=YES,        X
               RTNID=DMPGM+6
         CONEX TEXT='PER=',RTNID=DMPER+6,RTN=YES,SKIP=YES
         CLI   DMPER+6-DMSID(R9),64     IS PER SELECTED
         BE    CONOPER                  NO, SKIP EMASK
         CONEX TEXT='EMASK=',BLEN=1,HEXA=YES,L=PEREMASK
         CONEX TEXT='GPMASK=',BLEN=2,HEXA=YES,L=PERGPMSK
         CONEX TEXT='PERSTART=',BLEN=3,HEXA=YES,L=PERSTART+1
         CONEX TEXT='PEREND=',BLEN=3,HEXA=YES,L=PEREND+1
         CONEX TEXT='PERMODE=',BLEN=2,L=PERMODES,HEXA=YES,CHKALL=YES
CONOPER  DS    0H
         CONEX TEXT='DAT=',RTNID=DMDAT+6,RTN=YES,SKIP=YES
         CONEX TEXT='MON=',RTNID=DMMON+6,RTN=YES,SKIP=YES
         CONEX L=MCHINT,TEXT='MCH=',BLEN=1,RTN=YES,RTNID=DMMCH+6,      X
               CHKALL=YES
         CLI   DMUNT+6-DMSID(R9),64     WAS UNIT SELECTED
         BNE   CONOSIO1                 YES DO NOT PRINT
         CONEX L=IOSINT,TEXT='I/O=',BLEN=255,RTN=YES,RTNID=DMIOI+6,    X
               CHKALL=YES
         CLI   DMUNT+6-DMSID(R9),64     WAS UNIT SELECTED
         BNE   CONOSIO1
         CONEX L=SIOINT,TEXT='SIO=',BLEN=255,RTN=YES,RTNID=DMSIO+6,    X
               CHKALL=YES
CONOSIO1 DS    0H
         TM    PCIINT-DMSID(R9),255     IS PCI ALL
         BO    CONPCI1                  YES, SO PRINT
         TM    PRTFLAG,PRTCONFO
         BO    CONPCI2
         CONEX TEXT='PCI=OFF',SKIP=YES
         B     CONPCI2
CONPCI1  DS    0H
         CONEX L=PCIINT,TEXT='PCI=',BLEN=1,CHKALL=YES,DEC=YES
CONPCI2  DS    0H
         CONEX RTN=YES,RTNID=DMDSP+6,L=DMSPAGMK+1,BLEN=1,CHKALL=YES,   X
               TEXT='DSP='
         CONEX L=SIOINT,BLEN=255,CHKALL=YES,RTN=YES,RTNID=DMUNT+6,     X
               TEXT='UNIT='
         CLI   DMUNT+6-DMSID(R9),64
         BE    CONLST10
CONLST09 DS    0H
         TM    DMSUTYPE-DMSID(R9),DMSUTYPS+DMSUTYPI
         BNO   CONUT01
         CONEX TEXT='UNITTYPE=ALL',SKIP=YES
         B     CONUT03
CONUT01  TM    DMSUTYPE-DMSID(R9),DMSUTYPS
         BZ    CONUT02
         CONEX TEXT='UNITTYPE=SIO',SKIP=YES
         B     CONUT03
CONUT02  CONEX 'UNITTYPE=I/O',SKIP=YES
CONUT03  DS    0H
         TM    DATASFLG-DMSID(R9),192   IS IT ALL
         BNO   CONDAS01                 NO,SO DO NOT PRINT HERE
         CONEX L=DATASFLG,BLEN=1,TEXT='DATASIO=',COMP=192,CHKALL=YES,  X
               HEXA=YES
         B     CONDAI00                 CHECK INPUT FLAG
CONDAS01 TM    DATASFLG-DMSID(R9),32    IS IT OFF
         BZ    CONDAS02                 NO, SO DO NOT PRINT HERE
CONDAS03 CONEX TEXT='DATASIO=OFF',SKIP=YES
         B     CONDAI00
CONDAS02 TM    DATASFLG-DMSID(R9),128   IS IT OUTPUT
         BZ    CONDAS04                 NO, DO NOT PRINT HERE
         CONEX TEXT='DATASIO=OUTPUT',SKIP=YES
         B     CONDAI00
CONDAS04 TM    DATASFLG-DMSID(R9),64    IS IT INPUT
         BZ    CONDAS03                 NO, ASSUME OFF
         CONEX TEXT='DATASIO=INPUT',SKIP=YES
CONDAI00 TM    DATAIFLG-DMSID(R9),192   IS IT ALL
         BNO   CONDAI01                 NO, SO DO NOT PRINT HERE
         CONEX L=DATAIFLG,BLEN=1,TEXT='DATAI/O=',COMP=192,CHKALL=YES,  X
               HEXA=YES
         B     CONDLI00                 CONTINUE
CONDAI01 TM    DATAIFLG-DMSID(R9),32    IS IT OFF
         BZ    CONDAI02                 NO, CHECK FOR OUTPUT
CONDAI03 CONEX TEXT='DATAI/O=OFF',SKIP=YES
         B     CONDLI00                 CONTINUE
CONDAI02 TM    DATAIFLG-DMSID(R9),128   IS IT OUTPUT
         BZ    CONDAI04                 NO, CHECK FOR INPUT
         CONEX TEXT='DATAI/O=OUTPUT',SKIP=YES
         B     CONDLI00                 CONTINUE
CONDAI04 TM    DATAIFLG-DMSID(R9),64    IS IT INPUT
         BZ    CONDAI03                 NO, ASSUME OFF
         CONEX TEXT='DATAI/O=INPUT',SKIP=YES
CONDLI00 DS    0H
         CONEX L=DATASLNG,BLEN=2,TEXT='LENGTH=',DEC=YES
         CONEX L=CCWNUM,BLEN=1,TEXT='CCW=',DEC=YES,CHKALL=YES
CONLST10 DS    0H
         CONEX L=DMSJOBN,BLEN=8,COMP=64,CHKSEL=YES,TEXT='JOBNAME=',    X
               CHAR=YES
         CONEX L=DMSSTEPN,BLEN=16,COMP=64,CHKSEL=YES,                  X
               TEXT='PROCEDURE/STEPNAME=',CHAR=YES
         CONEX L=DMUSR,BLEN=8,COMP=64,                               SBX
               CHAR=YES,TEXT='USERRTN=',CHKSEL=YES
         TM    PRTFLAG,PRTCONFO+PRTCONGO
         BO    CONLST11
         TM    PRTFLAG,PRTCONFO
         BO    RETURN
CONLST11 DS    0H
         CONEX TEXT='GO',SKIP=YES
         TM    PRTFLAG,PRTCONFO
         BO    RETURN
         CONEX TEXT='  ',SKIP=YES
         SPACE 3
         CLC   DMSEDTPC-DMSID(8,R9),=XL8'00'    IS CLOCK VALUE IN COM
         BE    RETURN                   DO NOT FORMAT SECOND PART
         CONEX BLEN=4,L=RETCODE,HEXA=YES,                              X
               TEXT='DMS RETURN CODE WAS '
         CONEX BLEN=4,L=DMSLSTEV,HEXA=YES,                             X
               TEXT='NUMBER OF LOST EVENTS WAS '
         CONEX BLEN=4,L=DMSBALC,DEC=YES,                               X
               TEXT='NUMBER OF BUFFERS ALLOCATED: '
         CONEX BLEN=4,L=DMSSEQCT,HEXA=YES,                             X
               TEXT='HIGHEST RECORD NUMBER WAS '
         CONEX BLEN=4,L=DMSXMPST,DEC=YES,                              X
               TEXT='COUNT OF ENTERING XMEM-POST WAS '
         CONEX BLEN=8,L=DMSEDTSC,TEXT='START TOD CLOCK: ',HEXA=YES
         CONEX BLEN=8,L=DMSEDTPC,TEXT='STOP  TOD CLOCK: ',HEXA=YES
         MVC   CONLTEXT+5(30),=CL30'START OF SESSION: DATE YY.DDD '
         MVC   CONLTEXT+35(14),=CL14'TIME HH.MM.SS '
         LA    R1,DMSEDTSC-DMSID(R9)
         BAL   R8,DATECON
         MVC   CONLTEXT+5(5),=CL5'STOP '
         LA    R1,DMSEDTPC-DMSID(R9)
         BAL   R8,DATECON
         BLINE
         NI    DMSEDTB1,ALLBIT-DMSFORMC SWITCH FLAG OFF
         B     RETURN
         EJECT
CONFOALL CLC   DMSID(8),0(R9)           IS ID EQUAL
         BNE   CONABEND                 NO, FORCE ABEND
         SETUP DMUNT+6,1
         SETUP DMIOI+6,1
         SETUP DMSIO+6,1
         SETUP DMPGM+6,1
         SETUP DMPER+6,1
         SETUP PEREMASK,12
         SETUP DMDAT+6,1
         SETUP DMMON+6,1
         SETUP DMEXT+6,1
         SETUP DMSVC+6,1
         SETUP DMDSP+6,1
         SETUP DMMCH+6,1
*        SETUP DMSASID,2         THIS LINE IS DELETED BY             SF
*        SETUP DMSJOBN,24        THIS LINE IS DELETED BY             SF
         SETUP EXTINT,2
         SETUP SVCINT,32
         SETUP PGMINT,32
         SETUP MCHINT,1
         SETUP IOSINT,256
         SETUP IOSINT+256,256
         SETUP SIOINT,256
         SETUP SIOINT+256,256
         SETUP PCIINT,1
         SETUP CCWNUM,1
         SETUP DATASFLG,1
         SETUP DATASCOD,32
         SETUP DATASLNG,2
         SETUP DATAIFLG,1
         SETUP DATAICOD,32
*        SETUP CPUID,2           THIS LINE IS DELETED BY             SF
         B     RETURN
         EJECT
CONPRT   DS    0H
         ST    R14,CONPRTSV             SAVE RETURN REGISTER
         STM   R1,R9,CONPRTSV+4         SAVE DESTROYED REGISTERS
         LR    R8,R1                    PARMLIST REGISTER
         LM    R2,R5,0(R8)              GET PARMLIST
         SRL   R5,16                    TWO BYTE VALUE
         STCM  R2,8,4(R3)
         LA    R5,0(R5,R2)              FIRST BYTE NOT IN STRING
         SR    R7,R7                    CURRENT UNIT IS ZERO
         LA    R2,0(R2)                 CLEAR HIGH BYTE
         MVC   CONCNT(1),4(R8)          MOVE UNIT COUNT
         LA    R9,200                   SET MAX. NUMBER OF LINES
CONRETL0 L     R3,4(R8)                 LINE POINTER
         BLINE
CONPRTL1 LA    R6,128                   BIT 0 MASK
         CR    R2,R5                    END OF MAP
         BNL   CONPOUT                  YES,GO OUT
CONPRTL2 EX    R6,CONTSTMK              IS THIS UNIT ON
         BO    CONUNIT                  YES, STORE INTO LINE
CONRETL1 SRA   R6,1                     TAKE NEXT UNIT
         LA    R7,1(R7)                 INCREASE UNIT
         BNZ   CONPRTL2                 STILL SAME BYTE
         LA    R2,1(R2)                 POINT TO NEXT BYTE
         B     CONPRTL1
CONUNIT  SLL   R7,4                     SHIFT TO GOOD POSITION
         LA    R7,12(,R7)               ADD 'SIGN'
         ST    R7,CONWRKUN              SAVE TO UNPACK FIELD
         SRL   R7,4                     MOVE BACK TO ORIGIN POSITION
         LA    R15,X'21'                ASSUME THREE BYTE FOR EXECUTE
         CLI   8(R8),3                  IS IT THREE DIGIT
         BE    *+8                      YES, CONTINUE
         LA    R15,X'11'                NO, TWO BYTE EXECUTE
         SLR   R14,R14                  CLEAR FOR INSERT
         IC    R14,8(R8)                LENGTH OF UNIT
         BCTR  R14,0                    SUBTRACT ONE FOR MACHINE CODE
         EX    R15,CONUNPK              DO UNPACK INTO LINE
         LA    R15,0(R4,R14)            POINT TO LAST BYTE
         OI    0(R15),240               SET IT TO 'FX'
         EX    R14,CONTRLAT             ACTUAL EBCDIC
         LA    R4,2(R4,R14)             POINT TO NEW FIELD
         LA    R15,120(R3)              END OF TEXT WITHIN LINE
         CR    R4,R15                   CHECK OFFSET
         BNL   CONABEND                 OF LINE
         SR    R15,R15                  CLEAR FOR
         IC    R15,CONCNT               INSERT UNITS PER LINE
         BCTR  R15,0                    SUBTRACT ONE
         STC   R15,CONCNT               SAVE NEW VALUE
         LTR   R15,R15                  STILL UNITS TO DO
         BP    CONRETL1                 YES, PROCESS NEXT ONES
*
*        THE LINE HAS BEEN FILLED WITH UNITS
*
         PRINTER 0(R3)                  WRITE TEXT
         BLINE
         MVC   CONCNT(1),4(R8)          NEW VALUE
         L     R4,8(R8)
         BCT   R9,CONRETL1              PROCESS ALL
         SPACE
CONPOUT  CLC   5(120,R3),CONTRTAB       IS LINE BLANK
         BE    CONRETL2                 YES, DO NOT PRINT
         PRINTER 0(R3)                  PRINT LINE
CONRETL2 L     R14,CONPRTSV             LOAD CALLERS RETURN ADDRESS
         LM    R1,R9,CONPRTSV+4         RELOAD DESTROYED REGISTERS
         BR    R14
         SPACE
CONTSTMK TM    0(R2),X'00'              TEST BIT IN STRING
         SPACE
CONUNPK  UNPK  0(1,R4),CONWRKUN+2(1)
         SPACE
CONTRLAT TR    0(1,R4),CONTRTAB
         SPACE
CONTMOVE MVC   CONLTEXT+5(1),10(R8)
         SPACE
STCOMCLI CLI   0(R5),X'00'
         SPACE
CHARMOVE MVC   0(1,R7),0(R5)
         EJECT
STRINGRT DS    0H
         ST    R14,CONSTRSV             SAVE REGISTER
         STM   R1,R8,CONSTRSV+4
         LR    R8,R1                    PARMLIST
         SR    R7,R7                    CLEAR FOR
         IC    R7,9(R8)                 INSERT LENGTH OF TEXT
         EX    R7,CONTMOVE              MOVE TEXT TO LINE
         LA    R7,CONLTEXT+6(R7)        POINT TO NEXT FREE BYTE
         TM    4(R8),X'80'              RTN ID SELECTED
         BZ    STRSKIP                  NO,TEST FOR SKIP
         L     R6,4(R8)                 YES,GET OFFSET TO RTN ID
         LA    R6,0(R9,R6)              POINT TO BYTE
         CLI   0(R6),64                 IS IT BLANK
         BE    WANOSEL
         MVC   3(5,R7),=CL5',RTN='
         MVC   8(1,R7),0(R6)
         B     STRSKIP                  AND CONTINUE
         SPACE
WANOSEL  MVC   0(17,R7),=CL17' WAS NOT SELECTED'
         TM    PRTFLAG,PRTCONFO         IS IT LIST
         BO    STRETURN                 YES, DO NOT PRINT
STRPRTER PRINTER CONLINE
         SPACE
STRETURN LM    R1,R8,CONSTRSV+4
         BLINE
         L     R14,CONSTRSV
         BR    R14                      RETURN
         SPACE 3
STRSKIP  TM    4(R8),X'20'              SKIP MAP
         BO    STRPRTER                 YES, GO OUT
         SPACE 3
STRCOMP  TM    4(R8),X'0C'              IS COMPARE WANTED
         BZ    STRHEXA                  NO, GO OUT
         SR    R6,R6                    CLEAR FOR
         IC    R6,0(R8)                 INSERT LENGTH OF MAP
         CLI   0(R8),X'FF'              SPECIAL
         BNE   *+8                      NO, CONTINUE
         LA    R6,512                   YES SET LENGTH TO 512
         L     R5,0(R8)                 POINT TO MAP OFFSET
         LA    R5,0(R9,R5)              POINT TO MAP ADDRESS
         SR    R4,R4
         IC    R4,8(R8)                 INSERT COMPARE CHARACTER
STCOMLOP EX    R4,STCOMCLI              CHECK CHARACTER
         BNE   STRHEXA                  IS NOT EQUAL
         LA    R5,1(R5)                 POINT TO NEXT
         BCT   R6,STCOMLOP
         MVC   0(3,R7),=CL4'ALL'        ASSUME =ALL
         TM    4(R8),X'04'              IS IT ALL
         BO STRPRTER                    YES, PRINT IT
         B     WANOSEL
         SPACE 3
STRHEXA  TM    4(R8),X'40'              IS IT HEXA
         BZ    STRCHAR                  NO,CHECK FOR CHARACTER
         SR    R2,R2                    CLEAR FOR
         IC    R2,0(R8)                 INSERT LENGTH OF MAP
         CLI   0(R8),X'FF'              IS IT 512
         BNE   *+8                      NO,CONTINUE
         LA    R2,512                   SET LENH TO 512
         CLI   0(R8),17                 IS IT MORE THAN 4 BLKS
         BL    STRHEXA1                 NO, SO WRITE ON SAME LINE
         PRINTER CONLINE
         BLINE
         LA    R7,CONLTEXT+5
STRHEXA1 L     R3,0(R8)                 OFFSET OF MAP
         LA    R3,0(R3,R9)              ADDRESS OF MAO
STRHEXA2 LA    R5,4                     BLOCK COUNT
STRHEXA3 LTR   R2,R2                    ALL DONE
         BNP   STRHEXA4                 YES, GO OUT
         UNPK  0(9,R7),0(5,R3)          CONVERT
         TR    0(8,R7),CONTRTAB
         MVI   8(R7),64
         LA    R3,4(R3)                 POINT TO NEXT FIELD
         LA    R7,9(R7)
         SH    R2,=H'4'                 DECR. LENGTH
         BCT   R5,STRHEXA3              4 BLOCKS
         LTR   R2,R2
         BNP   STRHEXA4
         PRINTER CONLINE
         B     STRHEXA2
STRHEXA4 LTR   R2,R2
         BZ    STRPRTER
         LPR   R3,R2
         SLL   R3,1
         LA    R3,1(R3)
STRHEXA5 BCTR  R7,0
         BCT   R3,STRHEXA5
         MVC   0(9,R7),=CL9' '          BLANK OUT BAD FIELD
         B     STRPRTER
         SPACE 3
STRCHAR  TM    4(R8),X'10'              TEST FOR CHARACTER
         BZ    STRDEC                   NO, TEST FOR DEC
         SR    R6,R6                    CLEAR FOR
         IC    R6,0(R8)                 INSERT LENGTH OF STRING
         BCTR  R6,R0
         L     R5,0(R8)                 POINT TO
         LA    R5,0(R5,R9)              STRING
         EX    R6,CHARMOVE              MOVE
         B     STRPRTER                 GO OUT
         SPACE 3
STRDEC   TM    4(R8),X'02'              IS DECIMAL VALUE
         BZ    STRSTR                   NO IT IS A STRING
         L     R5,0(R8)                 POINT
         LA    R5,0(R9,R5)              TO STRING
         SR    R6,R6
         IC    R6,0(R5)
         CLI   0(R8),X'01'              IS IT ONE
         BE    STRDEC01
         ICM   R6,3,0(R5)               ASSUME TWO
         CLI   0(R8),X'02'              IS IT TWO
         BE STRDEC01                    YES, GO OUT
         ICM   R6,7,0(R5)               ASSUME THREE BYTES
         CLI   0(R8),X'03'              IS IT THREE
         BE    STRDEC01                 YES, GO OUT
         ICM   R6,15,0(R5)              LOAD FOR BYTES
STRDEC01 DS 0H
         BAL   R14,CONPACK
         LA    R6,6
         LA    R5,DWD2
STRDECLP CLI   0(R5),C'0'
         BNE   STRDEC02
         LA    R5,1(R5)
         BCT   R6,STRDECLP
STRDEC02 EX    R6,CHARMOVE
         LA    R6,1(R7,R6)
         MVC   0(3,R6),CONTRTAB
         B     STRPRTER
         SPACE 3
STRSTR   DS    0H
         PRINTER CONLINE
         SR    R6,R6
         IC    R6,0(R8)                 LENGTH OF MAP
         CLI   0(R8),X'FF'              IS IT 512
         BNE   *+8
         LA    R6,512                   YES, LOAD 512
         STCM  R6,3,SBLEN               INTO
         LA    R5,CONLTEXT+5            PARM
         STCM  R5,7,STO                 LIST
         MVI   SASAC,64
         L     R5,0(R8)                 OFFSET
         LA    R5,0(R9,R5)              ADDRESS OF MAP
         STCM  R5,7,SMAP                INTO PARM
         MVI   SCHAR,3                  SET FOR XXX
         MVI   SNUM,16                  SET UNITS PER LINE
         CLI   0(R8),X'FF'              IS IT 512
         BE    *+12
         MVI   SCHAR,2
         MVI   SNUM,20
         LA    R5,CONLINE
         STCM  R5,7,SLINE
         CNOP  0,4
         BAL   R1,SARND
SASAC    DC    AL1(0)
SMAP     DC    AL3(0)
SNUM     DC    AL1(0)
SLINE    DC    AL3(0)
SCHAR    DC    AL1(0)
STO      DC    AL3(0)
SBLEN    DC    AL2(0)
SARND    BAL   R14,CONPRT
         B     STRETURN
         EJECT
DATECON  STM   R0,R14,CONSAVE0          SAVE REGS
         LM    R2,R3,0(R1)              PREPARE CLOCK
         SRDL  R2,12                    ACTUAL CLOCK
         D     R2,=F'60000000'
         LR    R4,R3
         SRDL  R2,32
         D     R2,=F'1000000'
         CVD   R3,DWD1
         UNPK  CONLTEXT+46(2),DWD1+6(2) SS
         OI    CONLTEXT+47,240
         SRDL  R4,32
         D     R4,=F'60'
         CVD   R4,DWD1
         UNPK  CONLTEXT+43(2),DWD1+6(2) MM
         OI    CONLTEXT+44,240
         SLR   R4,R4
         D     R4,=F'24'
         CVD   R4,DWD1
         UNPK  CONLTEXT+40(2),DWD1+6(2) HH
         OI    CONLTEXT+41,240
         S     R5,=F'365'
         SLR   R4,R4
         D     R4,=F'1461'
         MH    R5,=H'4'
         LR    R6,R5
         SRDL  R4,32
         D     R4,=F'365'
         LA    R5,1(R5,R6)
         CVD   R5,DWD1
         UNPK  CONLTEXT+28(2),DWD1+6(2)
         OI    CONLTEXT+29,240
         LA    R4,1(,R4)
         CVD   R4,DWD1
         UNPK  CONLTEXT+31(3),DWD1+6(2)
         OI    CONLTEXT+33,240
         PRINTER CONLINE
         LM    R0,R14,CONSAVE0
         BR    R8
CONPACK  CVD   R6,DWD1
         UNPK  DWD2(7),DWD1+4(4)
         OI    DWD2+6,X'F0'
         MVC   0(4,R7),DWD2+3
         BR    R14
         SPACE 3
DWD1     DC    1D'0'
DWD2     DC    1D'0'
CONSAVE0 DC    15F'0'
CONPRTSV DC    10F'0'
CONSTRSV DC    10F'0'
CONCNT   DC    F'0'
CONWRKUN DC    F'0'
CONLINE  DC    AL2(CONLEND-CONLINE),AL2(0)
CONLASAR DC    C'1'
CONLTEXT DC    120C' '
CONLEND  EQU   *
CONTRTAB DS    0D
         DC    256C' '
         ORG   CONTRTAB+C'A'
         DC    X'0A0B0C0D0E0F'
         ORG   CONTRTAB+C'0'
         DC    C'0123456789ABCDEF'
         LTORG
         END
