*          DATA SET LSPACE     AT LEVEL 002 AS OF 08/04/83
*          DATA SET LSPACE     AT LEVEL 001 AS OF 08/03/83
*          DATA SET LSPACE     AT LEVEL 020 AS OF 08/03/83 *~*MVS/XA*~*
*          DATA SET LSPACE     AT LEVEL 013 AS OF 09/26/79
*          DATA SET LSPACE     AT LEVEL 012 AS OF 08/14/79
*---------------------------------------------------------------------*
* ROUTINES TO BE ADDED: MYVOLS (DEFAULT): THOSE VOLUMES USER IS PCF   *
*                          AUTHORIZED TO ALLOCATE ON.                 *
*                       VOLKYWD: SPECIFIC VOLSER OR STRING OF VOLSER  *
*                       UCBKYWD: SPECIFIC UCB ADDRESS REQUEST         *
*                       DEVKYWD: SPECIFIC DEV NAME REQUEST (SYSDA)    *
*                       UCBFLAG: DISPLAY UCB FLAG DATA FOR VOLUMES    *
*                                REQUESTED (MUST SPECIFY FLAGS KEYWD) *
*                       SVC78:   ISSUE LSPACE SVC TO FIND SPACE ATTR  *
*                       ERRORS:  ISSUE ALL PERTINENT ERROR MSGS       *
*---------------------------------------------------------------------*
LSPACE   CSECT
         SAVE  (14,12),,LSPACE-P1-&SYSDATE-&SYSTIME   SAVE REGISTERS
         LR    R12,R15                 LOAD REG 12 WITH ENTRY ADDRESS
         USING LSPACE,R12              ESTABLISH ADDRESSABILITY TO
*                                      PROGRAM CSECT
         LA    R11,4095(R12)           LOAD REG 11 WITH ENTRY ADDR 4095
         USING LSPACE+4095,R11         ESTABLISH ADDRESSABILITY TO THE
*                                      REST OF THE PROGRAM
         LR    R8,R1                   PRESERVE PTR TO CPPL CNTL BLK
         GETMAIN R,LV=LDYNAMIC         GETMAIN DYNAMIC WORKAREA
         LR    R10,R1                  PRESERVE PTR TO WORKAREA
         ST    R13,4(R10)              STORE ADDRESS PREVIOUS SAVEAREA
         ST    R10,8(,R13)             STORE ADDRESS OF CURRENT
*                                      SAVEAREA IN PREVIOUS SAVEAREA
         LR    R13,R10                 LOAD REG 13 WITH ADDRESS OF
*                                      CURRENT SAVEAREA
         USING WORKAREA,R13            ESTABLISH ADDRESSABILITY TO
*                                      DYNAMIC WORKAREA
         EJECT
*                                                                     *
***********************************************************************
* BUILD PARSE PARAMETER LISTS USING COMMAND PROCESSOR PARAMETER LIST  *
* AND INITIALIZE INPUT/OUTPUT CONTROL BLOCKS IN WORKAREA.             *
***********************************************************************
*                                                                     *
         USING CPPL,R8                 ESTABLISH ADDRESSABILITY TO CPPL
         LA    R4,PPLSECT              LOAD ADDRESS OF PARSE PARM LIST
         USING PPL,R4                  ESTABLISH ADDRESSABILITY TO PPL
         LA    R5,IOPLSECT             LOAD ADDR OF PUTLINE PARM LIST
         USING IOPL,R5                 ESTABLISH ADDRESSABILITY TO IOPL
         L     R1,CPPLUPT              LOAD PTR TO USER PROFILE TABLE
*                                      FROM CPPL
         L     R2,CPPLECT              LOAD PTR TO ENVIRONMENTAL
*                                      CONTROL TABLE FROM CPPL
         SLR   R3,R3                   ZERO REGISTER 3
         ST    R3,ECB                  STORE REG AS ECB FOR PPL
         LA    R3,ECB                  LOAD ADDRESS OF ECB
         STM   R1,R3,PPLSECT           STORE ADDRESS OF UPT,ECT, AND
*                                      ECB IN PPL
         STM   R1,R3,IOPLSECT          STORE ADDRESS OT UPT, ECT, AND
*                                      ECB IN PPL
         LA    R3,IOPB                 LOAD ADDRESS OF PUTLINE
*                                      PARAMETER BLOCK
         ST    R3,IOPLIOPB             STORE IOPB ADDRESS IN IOPL
         MVC   PPLPCL,PCLADDR          MOVE PTR TO PARAMETER CONTROL
*                                      LIST INTO PPL
         LA    R2,PARSBACK             LOAD ADDRESS OF FULLWORD TO
*                                      CONTAIN ADDRESS OF PDE RETURNED
*                                      BY PARSE
         ST    R2,PPLANS               STORE PTR TO RETURN ADDRESS
         MVC   PPLCBUF,CPPLCBUF        MOVE PTR TO CMD BUFFER INTO PPL
         LA    R2,PPLUWA               LOAD ADDRESS OF USER WORKAREA
         ST    R2,PPLUWA               STORE ADDRESS IN PPL
         DROP  R4,R5,R8                DROP ADDRESSABILITY TO CPPL,
*                                      IOPL, AND PPL
         EJECT
*                                                                     *
***********************************************************************
* CALLTSSR TO PARSE (ENTRY POINT IKJPARS); ON RETURN PARSBACK -> PDL  *
* HANDLE ALL ERROR CODES RETURNED BY PARS AND IF NECESSARY CALL       *
* GENERAL FAIL SERVICE ROUTINE.                                       *
***********************************************************************
*                                                                     *
         CALLTSSR EP=IKJPARS,MF=(E,PPLSECT)
         LTR   R15,R15                 SEE IF RETURN CODE ZERO
         BZ    GOODPARS                IF YES GO TO GOOD PARS ROUTINE
         LR    R8,R15                  LOAD RETURN CODE FROM REG 15
SETCODE  DS    0H
         CVD   R8,DEC                  CONVERT RETURN CODE
         UNPK  CONV(8),DEC(8)                AND PREPARE
         MVZ   CONV+1(7),CONV                  FOR MESSAGE
         MVC   RETNCD(2),HALF12                   INSERT
         MVC   RETNCD+2(2),HALF0
*                                                                     *
         PUTLINE OUTPUT=(GNRLERR,SINGLE,DATA),MF=(E,IOPLSECT)
         PUTLINE OUTPUT=(RETNCD,SINGLE,DATA),MF=(E,IOPLSECT)
         EJECT
***********************************************************************
* SET UP ADDRESSABILITY TO PDL & LOAD REG 6 WITH ADDRESS OF DSNAME PDE*
***********************************************************************
*                                                                   *~*
*---------------------------------------------------------------------*
*        MODIFY TO SUPPORT MVS/XA CHANGES IN UCB STRUCTURE          *~*
*---------------------------------------------------------------------*
*                                                                   *~*
GOODPARS DS    0H
         L     R2,16                   LOAD ADDRESS OF CVT          *~*
         USING CVT,R2                    & ESTABLISH ADDRESSABILITY *~*
         L     R3,CVTDCQA              LOAD ADDR OF DEV CLASS Q HEAD*~*
         USING DCQ,R3                    & ESTABLISH ADDRESSABILITY *~*
         L     R3,DCQFIRST             LOAD ADDR OF FIRST DCQ ELEMNT*~*
         DROP  R3                      DROP DCQ HEADER DSECT        *~*
         USING DCQELMNT,R3               AND USE DCQ ELEMENT DSECT  *~*
DCQCHECK DS    0H                                                   *~*
         CLC   =C'DASD',DCQDEVNM       IF THIS IS THE DASD DCQ ELEMT*~*
         BE    DASDADDR                  THEN BRANCH AND CONTINUE   *~*
         CLC   =X'00000000',DCQCHAIN   IF THIS IS THE LAST DCQ ELEMT*~*
         BE    DCQERROR                  THEN ERROR (DASD NOT FOUND)*~*
         L     R3,DCQCHAIN             GET NEXT DCQ ELEMENT ADDRESS *~*
         B     DCQCHECK                  & BR BACK TO FIND 'DASD'   *~*
DCQERROR DS    0H                                                   *~*
         TPUT  DCQERR,L'DCQERR         ISSUE 'ERROR IN DCQ CHAIN' MSG~*
         B     FINISH                                               *~*
UCBERROR DS    0H                                                   *~*
         TPUT  UCBERR,L'UCBERR         ISSUE 'ERROR -- NO DASD UCBS'*~*
         B     FINISH                                               *~*
*-------------------------------------------------------------------*~*
DCQERR   DC    C'*** ERROR IN DCQ CHAIN -- FUNCTION CANNOT BE PERFORMEDX
               *** '
UCBERR   DC    C'*** ERROR IN DASD UCB CHAIN -- FUNCTION CANNOT BE PERFX
               ORMED *** '
$DASDUCB DS    F                       FIRST 'DASD' UCB ADDRESS     *~*
$DASDNUM DS    H                       COUNT OF 'DASD' UCB'S        *~*
*-------------------------------------------------------------------*~*
DASDADDR DS    0H                                                   *~*
         CLC   =X'00000000',DCQUCBAD   IF NO UCB'S FOR 'DASD' CLASS *~*
         BE    UCBERROR                  THEN ERROR                 *~*
         MVC   $DASDUCB,DCQUCBAD       SAVE FIRST DASD UCB ADDRESS  *~*
         MVC   $DASDNUM,DCQUCBCT       SAVE COUNT OF DASD UCB'S     *~*
         DROP  R2,R3                   DROP DCQ AND CVT DSECTS      *~*
*                                                                   *~*
*-------------------------------------------------------------------*~*
*                                                                     *
         L     R9,PARSBACK             LOAD PTR TO PARS ANSWER
         USING IKJPARMD,R9             ESTABLISH ADDRESSABILITY TO PDL
         LA    R6,VOLUMES              LOAD PTR TO DSNAME LIST
         LA    R5,KWORD                LOAD PTR TO FLAG LIST
         CLI   1(R5),X'00'             Q. ANY KEYWORDS
         DROP  R9                      DROP ADDRESSABILITY TO PDL
         BE    LDPTR                   A. NO -- BRANCH AROUND
SETSW    MVI   FLGSW,X'FF'           INDICATE KEYWORD IS PRESENT
LDPTR    L     R5,0(R6)                SET UP PTR TO VOLUME LIST
CHKL     LH    R4,4(R6)                SET UP LENGTH OF VOL LIST
         ST    R4,SAVELGTH
         LTR   R4,R4                   IS LENGTH ZERO ?
         BZ    DEFAULT                 YES   ASSUME MYVOLS
         BCTR  4,0
         EX    R4,MOVEVOL
         B     TESTPARM
*                                                                     *
MOVEVOL  MVC   PARMNM(0),0(R5)         GET PARAMETER FROM COMMAND
*                                                                     *
         EJECT
***********************************************************************
*    STEP THROUGH SEQUENCE CHECKING FOR COMMAND PARAMETER ENTRY TYPE  *
***********************************************************************
*                                                                     *
TESTPARM DS    0H
         ST    R6,PARSAVE
         CLC   PARMNM(6),=C'PUBLIC'    IF REQUEST IS FOR PUBLIC VOLUMES
         BE    PUBLWD                    THEN BRANCH AND PERFORM
         CLC   PARMNM(7),=C'PRIVATE'   IF REQUEST IS FOR PRIVATE VOLUME
         BE    PRIVWD                    THEN BRANCH AND PERFORM
         CLC   PARMNM(7),=C'STORAGE'   IF REQUEST IS FOR STORAGE VOLUME
         BE    STORWD                    THEN BRANCH AND PERFORM
         BAL   R5,MYVOLS               IS REQUEST FOR MYVOLS ?
         BAL   R5,VOLKYWD              IS REQUEST FOR VOLUME ID ?
         CLC   REQFLG,VOLFLG
         BE    CHKPDE
         BAL   R5,UCBKYWD              IS REQUEST FOR UCB ADDRESS ?
         BAL   R5,DEVKYWD              IS REQUEST FOR DEVICE TYPE ?
*                                                                     *
         EJECT
*---------------------------------------------------------------------*
*        FIND 'PUBLIC' VOLUMES                                      *~*
*---------------------------------------------------------------------*
PUBLWD   DS    0H                                                   *~*
         USING UCBCMSEG,R3            ESTABLISH UCB ADDRESSABILITY  *~*
         L     R3,$DASDUCB              AND LOAD FIRST DASD UCB     *~*
         LH    R2,$DASDNUM            LOAD DASD UCB'S COUNT         *~*
         LA    R2,1(,R2)              INCREMENT COUNT BY 1 FOR BCT  *~*
EOTPUB   DS    0H                                                   *~*
         TM    UCBSTAB,UCBBPUB        IF THIS IS NOT PUBLIC VOLUME  *~*
         BZ    NEXTPUB                  THEN BR AND GET NEXT UCB    *~*
         TM    UCBSTAT,UCBONLI        IF THIS VOLUME IS NOT ONLINE  *~*
         BZ    NEXTPUB                  THEN BR AND GET NEXT UCB    *~*
         BAL   R14,RUCBCHEK           BRANCH AND CHECK UCB          *~*
         LTR   R15,R15                IF RETURN CODE IS NOT ZERO    *~*
         BNZ   NEXTPUB                  BR AND GET NEXT UCB         *~*
         MVI   REQFLG,X'11'           TURN ON PUBLIC FLAG
         B     SVC78                  BRANCH AND PERFORM LSPACE     *~*
NEXTPUB  DS    0H                                                   *~*
         L     R3,UCBNXUCB            LOAD ADDR OF NEXT UCB IN CHAIN*~*
         BCT   R2,EOTPUB              DECR UCB COUNT; IF COUNT>1, BR*~*
         B     NOPUBL                 BRANCH AND CONTINUE           *~*
         EJECT
*---------------------------------------------------------------------*
*        FIND 'PRIVATE' VOLUMES                                     *~*
*---------------------------------------------------------------------*
PRIVWD   DS    0H                                                   *~*
         USING UCBCMSEG,R3            ESTABLISH UCB ADDRESSABILITY  *~*
         L     R3,$DASDUCB              AND LOAD FIRST DASD UCB     *~*
         LH    R2,$DASDNUM            LOAD DASD UCB'S COUNT         *~*
         LA    R2,1(,R2)              INCREMENT COUNT BY 1 FOR BCT  *~*
EOTPRIV  DS    0H                                                   *~*
         TM    UCBSTAB,UCBBPRV        IF THIS IS NOT PRIVATE VOLUME *~*
         BZ    NEXTPRIV                 THEN BR AND GET NEXT UCB    *~*
         TM    UCBSTAT,UCBONLI        IF THIS VOLUME IS NOT ONLINE  *~*
         BZ    NEXTPRIV                 THEN BR AND GET NEXT UCB    *~*
         BAL   R14,RUCBCHEK           BRANCH AND CHECK UCB          *~*
         LTR   R15,R15                IF RETURN CODE IS NOT ZERO    *~*
         BNZ   NEXTPRIV                 BR AND GET NEXT UCB         *~*
         MVI   REQFLG,X'22'           TURN ON PRIVATE FLAG
         B     SVC78                  BRANCH AND PERFORM LSPACE     *~*
NEXTPRIV DS    0H                                                   *~*
         L     R3,UCBNXUCB            LOAD ADDR OF NEXT UCB IN CHAIN*~*
         BCT   R2,EOTPRIV             DECR UCB COUNT; IF COUNT>1, BR*~*
         B     CHKFLG                 BRANCH AND CONTINUE           *~*
         EJECT
*---------------------------------------------------------------------*
*        FIND 'STORAGE' VOLUMES                                     *~*
*---------------------------------------------------------------------*
STORWD   DS    0H
         USING UCBCMSEG,R3            ESTABLISH UCB ADDRESSABILITY  *~*
         L     R3,$DASDUCB              AND LOAD FIRST DASD UCB     *~*
         LH    R4,$DASDNUM            LOAD DASD UCB'S COUNT         *~*
         LA    R2,1(,R2)              INCREMENT COUNT BY 1 FOR BCT  *~*
EOTSTOR  DS    0H                                                   *~*
         TM    UCBSTAB,UCBBSTR        IF THIS IS NOT STORAGE VOLUME *~*
         BZ    NEXTSTOR                 THEN BR AND GET NEXT UCB    *~*
         TM    UCBSTAT,UCBONLI        IF THIS VOLUME IS NOT ONLINE  *~*
         BZ    NEXTSTOR                 THEN BR AND GET NEXT UCB    *~*
         BAL   R14,RUCBCHEK           BRANCH AND CHECK UCB          *~*
         LTR   R15,R15                IF RETURN CODE IS NOT ZERO    *~*
         BNZ   NEXTSTOR                 BR AND GET NEXT UCB         *~*
         MVI   REQFLG,X'33'           TURN ON STORAGE FLAG
         B     SVC78                  BRANCH AND PERFORM LSPACE     *~*
NEXTSTOR DS    0H                                                   *~*
         L     R3,UCBNXUCB            LOAD ADDR OF NEXT UCB IN CHAIN*~*
         BCT   R2,EOTSTOR             DECR UCB COUNT; IF COUNT>1, BR*~*
         B     CHKFLG                 BRANCH AND CONTINUE           *~*
         EJECT
DEFAULT  MVC   PARMNM,=C'MYVOLS'
******** B     MYVOLS
*                                                                     *
***********************************************************************
*         IF REQUEST IS MYVOLS, THEN PROCESS; ELSE, BYPASS            *
***********************************************************************
*                                                                     *
MYVOLS   CLC   =C'MYVOLS',PARMNM    MYVOLS REQUEST ?
         BNER  R5                        NO - DONT NEED THIS CODE
*                                                                     *
         ST    R6,SAVEPDE        SAVE REG 6 - STEPPED ON BY IEAVVMSR
*                                                                     *
         MVI   REQFLG,X'FF'         TURN ON MYVOLS FLAG
*                                                                     *
         L     R3,CVTPTR                 YES - GET ADDRESS OF CVT
         L     R15,CVTLPDSR-CVT(,R3) GET ADDR OF LPDE SEARCH ROUTINE
         LM    R0,R1,PCFTBLS       GET 'PCFTBLS' IN R0 & R1
         BALR  R14,R15             CALL IEAVVMSR - DESTROYS R6, R8 & R9
         B     GOTTBLS             +0 - PCFTBLS FOUND
         B     NOTTSO              +4 - PCFTBLS NOT FOUND - NOTTSO
GOTTBLS  DS    0H                  PCFTBLS LPDE FOUND
         LR    R1,R0               GET ADDRESS OF PCFTBLS LPDE
         L     R1,LPDENTP-LPDE(,R1) GET ADDRESS OF PCFTBLS
         ICM   R1,B'1111',4(R1)    GET ADDRESS OF PCFVOLS
         BZ    NOTTSO              IF ZERO - NOTTSO
         L     R15,PSATNEW-PSA     ELSE - GET ADDRESS OF MY TCB
         L     R15,TCBJSCB-TCB(,R15) GET ADDRESS OF MY JSCB
         L     R15,JSCBPSCB-IEZJSCB(,R15) GET ADDRESS OF MY PSCB
         IC    R15,PSCBATR2+1-PSCB(,R15) GET VOL INDEX BYTE
         SLL   R15,26              CLEAR SMF BITS
         SRL   R15,26              REPOSITION
         LTR   R15,R15             IS INDEX PRESENT
         BZ    NOTTSO              NO - NOTTSO
         BCTR  R15,0               YES - REDUCE FOR RELATIVE TO ZERO
         SLL   R15,1               DOUBLE THE VALUE
         LH    R2,4(R1,R15)        GET OFFSET TO NEXT INDEX VALUE
         LH    R14,2(R1,R15)       AND ALSO THIS INDEX VALUE
         SR    R2,R14              CALCULATE SIZE OF THIS INDEX ENTRY
         BNP   NOTTSO              IF NONE - NOTTSO
         LA    R3,(64*2)+4(R1,R14) ELSE - GET BEGINNING OF THIS ENTRY
         LR    R9,R3               SAVE PCFTBL ENTRY
NEXTVOL  DS    0H
         LA    R15,0
         MVC   VOLSER,0(R9)        GET SERIAL
*
         BAL   R4,SRCHUCB          GO GET CORRECT UCB ADDRESS
         LTR   R15,R15             WAS REUTRN ZERO FROM UCB SEARCH
         BNZ   INCR                NO - POINT TO NEXT ENTRY
         BAL   R4,SVC78            CALCULATE SPACE USAGE FOR THIS VOLID
INCR     LA    R9,8(,R9)           POINT TO NEXT ENTRY
         SH    R2,=H'8'            SUBTRACT OFF THIS ENTRY
         BP    NEXTVOL             IF MORE - LOOP
         L     R6,SAVEPDE             RESTORE PTR TO VOL LIST
         SLR   R15,R15             ELSE - SET RETURN CODE
         B     CHKPDE
         EJECT
*-------------------------------------------------------------------*~*
SRCHUCB  DS    0H
         USING UCBCMSEG,R3            ESTABLISH UCB ADDRESSABILITY  *~*
         L     R3,$DASDUCB              AND LOAD FIRST DASD UCB     *~*
         LH    R2,$DASDNUM            LOAD DASD UCB'S COUNT         *~*
         LA    R2,1(,R2)              INCREMENT COUNT BY 1 FOR BCT  *~*
UCBTBL   DS    0H
         TM    UCBSTAT,UCBONLI        IF THIS VOLUME IS NOT ONLINE  *~*
         BZ    NEXTUCB                  THEN BRANCH AND GET NEXT UCB*~*
         CLC   VOLSER(6),28(R3)
         BER   R4
NEXTUCB  DS    0H                                                   *~*
         L     R3,UCBNXUCB            LOAD ADDR OF NEXT UCB IN CHAIN*~*
         BCT   R2,UCBTBL              DECR UCB COUNT; IF COUNT>1, BR*~*
NOFIND   DS    0H
         LA    R15,16                  SET RETURN CODE
         BR    R4                        AND RETURN TO CALLER
*-------------------------------------------------------------------*~*
NOTTSO   DS    0H
         MVC   VOLSER,KNOTTSO      INDICATE 'NOTTSO'
         TPUT  VOLSER,6            TELL TSO USER
         LA    R15,16              SET RETURN CODE
         B     CHKPDE              AND EXIT
*-------------------------------------------------------------------*~*
         EJECT
***********************************************************************
*        IF REQUEST IS FOR VOL(S), THEN PROCESS; ELSE BYPASS          *
***********************************************************************
VOLKYWD  DS    0H
*-------------------------------------------------------------------*~*
         USING UCBCMSEG,R3            ESTABLISH UCB ADDRESSABILITY  *~*
         L     R3,$DASDUCB              AND LOAD FIRST DASD UCB     *~*
         LH    R2,$DASDNUM            LOAD DASD UCB'S COUNT         *~*
         LA    R2,1(,R2)              INCREMENT COUNT BY 1 FOR BCT  *~*
         MVC   REG3SAV,CLEAR       CLEAR REG 3 SAVE AREA
RDUCB1   DS    0H
         BAL   R14,RUCBCHEK           BRANCH AND CHECK UCB          *~*
         LTR   R15,R15                IF RETURN CODE IS NOT ZERO    *~*
         BNZ   REQNEXT                  BR AND GET NEXT UCB         *~*
         L     R4,SAVELGTH         RESTORE REG 4
         BCTR  4,0
         EX    R4,COMPARE
         BE    CHKVOL              YES - CHECK FOR RIGHT TYPES
REQNEXT  DS    0H                                                   *~*
         L     R3,UCBNXUCB            LOAD ADDR OF NEXT UCB IN CHAIN*~*
         BCT   R2,RDUCB1              DECR UCB COUNT; IF COUNT>1, BR*~*
         BR    R5                     BRANCH AND CONTINUE           *~*
CHKVOL   DS    0H
         TM    UCBSTAT,UCBONLI        IF THIS VOLUME IS NOT ONLINE  *~*
         BZ    MSG08                    THEN ISSUE MESSAGE & RETURN *~*
         MVI   REQFLG,X'04'        TURN ON VOLSER FLAG
         B     SVC78               IS LOW - FIND SPACE ATTRIBUTES
*
*---------------------------------------------------------------------*
*
COMPARE  CLC   PARMNM(0),28(R3)    IS THIS THE VOL REQUESTED ?
*
*---------------------------------------------------------------------*
         EJECT
***********************************************************************
*        IF REQUEST IS FOR UCB(S), THEN PROCESS; ELSE BYPASS          *
***********************************************************************
UCBKYWD  DS    0H
*-------------------------------------------------------------------*~*
         USING UCBCMSEG,R3            ESTABLISH UCB ADDRESSABILITY  *~*
         L     R3,$DASDUCB              AND LOAD FIRST DASD UCB     *~*
         LH    R2,$DASDNUM            LOAD DASD UCB'S COUNT         *~*
         LA    R2,1(,R2)              INCREMENT COUNT BY 1 FOR BCT  *~*
RDUCB2   DS    0H
         MVC   VOLSER(6),28(R3)    KEEP VOLSER FOR MESSAGE TEXT
         L     R4,SAVELGTH
         BCTR  4,0
         EX    R4,COMPUCB
         BE    CHKUCB              YES - CHECK IF RIGHT TYPE ?
REQNEXT1 DS    0H                                                   *~*
         L     R3,UCBNXUCB            LOAD ADDR OF NEXT UCB IN CHAIN*~*
         BCT   R2,RDUCB2              DECR UCB COUNT; IF COUNT>1, BR*~*
         BR    R5                     BRANCH AND CONTINUE           *~*
CHKUCB   DS    0H
         TM    UCBSTAT,UCBONLI        IF THIS VOLUME IS NOT ONLINE  *~*
         BNO   MSG0C               NO - ISSUE MSG AND RETURN
         MVI   REQFLG,X'08'        TURN ON UCB REQUEST FLAG
         B     SVC78               RIGHT TYPE - GO TO LSPACE SVC
*-------------------------------------------------------------------*~*
         EJECT
***********************************************************************
*        IF REQUEST IS DEVNAME(S), THEN PROCESS; ELSE BYPASS          *
***********************************************************************
DEVKYWD  DS    0H
*-------------------------------------------------------------------*~*
         MVC   $UNIT,PARMNM           MOVE UNIT NAME TO PARM LIST   *~*
         MVC   $UCBLIST,=X'00000000'  SET UCB LIST ADDR TO NULLS    *~*
         LA    R1,$DEVPARM            POINT TO PARAMETER LIST AND   *~*
         LINK  EP=IEFEB4UV             CALL RTN TO GET UCB ADDR LIST*~*
         LTR   R15,R15                IF UCB LIST WAS NOT OBTAINED, *~*
         BNZ   DEVERROR                 BRANCH AND FIND OUT WHY     *~*
         L     R4,$UCBLIST            UCB LIST ADDR (FROM IEFEB4UV) *~*
         L     R2,4(,R4)              GET # OF UCB ENTRIES IN LIST  *~*
         LA    R2,1(,R2)                AND INCREMENT BY 1 FOR BCT  *~*
NEXTDEV  DS    0H                                                   *~*
         L     R3,8(,R4)              GET FIRST UCB ADDRESS         *~*
         LA    R4,8(,R4)              GET FIRST UCB ADDRESS         *~*
         B     DASDCOM2               BRANCH AND CONTINUE           *~*
         USING UCBCMSEG,R3            ESTABLISH UCB ADDRESSABILITY  *~*
DASDCOMP DS    0H                                                   *~*
         LA    R4,4(,R4)              PT TO NEXT ENTRY IN UCB LIST  *~*
         L     R3,0(,R4)              GET ADDR OF NEXT UCB IN LIST  *~*
DASDCOM2 DS    0H                                                   *~*
         TM    UCBSTAT,UCBONLI        IF THIS DEVICE IS NOT ONLINE, *~*
         BZ    NOTONL                   BRANCH AND PRINT MESSAGE    *~*
         TM    UCBTBYT3,UCB3DACC      IF THIS DEVICE IS NOT DASD,   *~*
         BZ    DEVMSG                   BRANCH AND PRINT MESSAGE    *~*
         MVI   REQFLG,X'88'           INDICATE DEVICE REQUEST       *~*
         BAL   R5,SVC78               BRANCH AND PERFORM LSPACE     *~*
NEXTDASD DS    0H                                                   *~*
         BCT   R2,DASDCOMP            DECR UCB COUNTER; BR TILL R2=1*~*
         L     R2,$UCBLIST            GET ADDR OF UCB LIST          *~*
         L     R0,0(,R2)              GET SUBPOOL # AND LENGTH      *~*
         FREEMAIN R,LV=(0),A=(2)      FREE UCB LIST AREA            *~*
         B     CHKPDE                 BRANCH AND CONTINUE PROCESSING*~*
NOTONL   DS    0H                                                   *~*
         MVC   OFFL(3),UCBNAME        MOVE UCB NAME TO MESSAGE AREA *~*
         TPUT  OFFL,L'OFFL              & PRINT 'OFFLINE' MESSAGE   *~*
         B     NEXTDASD               BRANCH AND CONTINUE PROCESSING*~*
DEVERROR DS    0H                                                   *~*
         CL    R15,=X'00000004'       IF RC^=4 (UNSUPP. DEVICE),THEN*~*
         BNE   NODEVSTR                 BR & ASSUME STORAGE UNAVAIL.*~*
         TPUT  NONSUPP,L'NONSUPP      ISSUE UNSUPPORTED DEV TYPE MSG*~*
         B     CHKPDE                 CHECK FOR MORE REQUESTS       *~*
NODEVSTR DS    0H                                                   *~*
         TPUT  NOSTRG,L'NOSTRG        ISSUE STORAGE UNAVAILABLE MSG *~*
         B     CHKPDE                 CHECK FOR MORE REQUESTS       *~*
*                                                                   *~*
*---------------------------------------------------------------------*
$DEVPARM DS    0D                     IEFEB4UV PARM LIST            *~*
         DC    A($UNITTAB)            UNIT TABLE ADDRESS            *~*
         DC    A($UNITFLG)            FUNCTION CODE ADDRESS         *~*
*                                                                   *~*
$UNITFLG DC    B'00010000'            INDICATE 'RETURN UCB ADDRESSES'~*
*                                                                   *~*
         DS    0F                                                   *~*
$UNITTAB DS    0CL16                  UNIT TABLE                    *~*
$UNIT    DC    CL8' '                 UNITNAME (INPUT TO IEFEB4UV)  *~*
$UCBLIST DS    F                      UNITNAME LIST ADDR (OUTPUT)   *~*
$UCBATTR DC    X'00000000'            DON'T PROVIDE ATTRIBUTE LIST  *~*
*---------------------------------------------------------------------*
*---------------------------------------------------------------------*
         EJECT
***********************************************************************
*        CALL LSPACE SVC (SVC 78) AND ISSUE SPACE MESSAGE             *
***********************************************************************
SVC78    ST    R3,STARTUCB             SAVE UCB ADDR FOR TABLE SEARCH
         LR    R0,R3                   SAVE ADDRESS OF UCB
         LA    R1,VOLSPC               LOAD REG 1 WITH ADDR OF MSG AREA
         SVC   78                      ISSUE LSPACE SVC
         LTR   R15,R15                 CHECK FOR RETURN CODE ZERO
         BZ    PUTMSG                  IF ZERO, GO TO PUTMSG
         LR    R8,R15                  LOAD RETURN CODE INTO REG 15
         BAL   R14,ERRORS              GO TO CONVERT ROUTINE
         PUTLINE OUTPUT=(SVC78ER,SINGLE,DATA),MF=(E,IOPLSECT)
         PUTLINE OUTPUT=(RETNCD,SINGLE,DATA),MF=(E,IOPLSECT)
         B     CHKPDE                  EXIT - ALL DONE
*                                                                     *
PUTMSG   MVC   SPCMSG+5(6),28(R3)    VOLSER
         MVC   SPCMSG(3),13(R3)      UNIT NAME
         MVC   SPCMSG+L'SPCMSG-7(7),=C'       '
         TM    UCBSTAT,UCBALOC
         BO    NOTALLOC
         MVC   SPCMSG+L'SPCMSG-7(7),=C'*ALLOC*'
NOTALLOC DS    0H
         TPUT  SPCMSG,L'SPCMSG
         MVC   VOLSPC1+5(30),VOLSPC
         TPUT  VOLSPC1,L'VOLSPC1
*                                                                     *
         CLI   FLGSW,X'FF'
         BE    UCBFLAG
CHKFLGS  CLC   REQFLG,PUBFLG
         BE    NEXTPUB
         CLC   REQFLG,PRIVFLG
         BE    NEXTPRIV
         CLC   REQFLG,STORFLG
         BE    NEXTSTOR
         CLC   REQFLG,MYFLG
         BER   R4
         CLC   REQFLG,VOLFLG           WAS REQUEST FOR VOLSER
         BE    REQNEXT                 YES - ALL DONE
         CLC   REQFLG,UCBFLG           WAS THIS A UCB REQUEST
         BE    CHKPDE                  YES - ALL DONE
         CLC   REQFLG,DEVFLG           WAS THIS A DEV REQUEST
         BER   R5                      YES - PROCESS NEXT VOLSER
*
         ABEND 123,DUMP                NONE OF THE ABOVE - ABEND
*
ERRORS   DS    0H
         C     R8,RC4
         BE    MSG04
         C     R8,RC8
         BE    MSG08
         C     R8,RC12
         BE    MSG0C
         CVD   R8,DEC                  CONVERT RETURN CODE
         UNPK  CONV(8),DEC(8)                AND PREPARE
         MVZ   CONV+1(7),CONV                  FOR MESSAGE
         MVC   RETNCD(2),HALF12                   INSERT
         MVC   RETNCD+2(2),HALF0
         BR    R14                     RETURN TO CALLER
*
MSG04    MVC   RC04(3),13(R3)
         MVC   RC04+4(6),28(R3)
         TPUT  RC04,L'RC04
         B     CHKPDE
MSG08    TPUT  RC08,L'RC08
         B     CHKPDE
MSG0C    TPUT  RC0C,L'RC0C
         B     CHKPDE
NODASD   TPUT  DMSG,L'DMSG
         B     CHKPDE
*
NOSUP    TPUT  UNKNWN,L'UNKNWN
*
         B     CHKPDE
         EJECT
***********************************************************************
* CHECK FOR ADDITIONAL PARAMETERS, IF MORE; PROCESS, ELSE CLEANUP YOUR*
*           MESS AND EXIT (ALL DONE)                                  *
***********************************************************************
*
NOPUBL   TPUT  PUBLC,L'PUBLC
         B     CHKPDE
*
CHKFLG   CLI   REQFLG,X'00'            REQUEST NOT FOUND
         BE    NOSUP
*
CHKPDE   L     R6,PARSAVE
         L     R5,8(,R6)              LOAD CHAIN FOR VOL LIST
         C     R5,ENDCHAIN             IS IT END OF VOL LIST?
         BE    FINISH                  IF YES CLEANUP & EXIT
         L     R6,8(,R6)               LOAD PTR TO NEXT VOLUME
         L     R5,0(R6)
         MVC   PARMNM,ZEROS
         MVC   REQFLG,ESOCODE          CLEAR REQFLG AREA
         MVC   LASTUCB(2),CLEAR
         MVC   WORKUCB,CLEAR
         MVC   WORKUCB+4,CLEAR
         B     CHKL                          AND PROCESS
***********************************************************************
*        EXECUTED INSTRUCTIONS                                        *
***********************************************************************
COMPUCB  CLC   PARMNM(0),13(R3) COMPARE PARM TO UCB
*                                                                     *
TEST4FLG TM    0(R15),*-*       TEST FOR FLAG
*                                                                     *
         EJECT
***********************************************************************
* CLEANUP AREAS GETMAINED BY PARSE, SET RETURN CODE, RESTORE REGISTERS*
* AND EXIT
***********************************************************************
*                                                                     *
FINISH   DS    0H
         LR    R2,R15
         IKJRLSA PARSBACK
*                                                                     *
         LA    R0,LDYNAMIC
         LR    R1,R13
         L     R13,4(R13)
         FREEMAIN R,LV=(0),A=(1)
         LR    R15,R2
         RETURN (14,12),RC=(15)
*
         TITLE 'REAL UCB CHECK ROUTINE'
RUCBCHEK DS    0H
         ST    R14,RUCBSAVE        SAVE RETURN ADDRESS
         MVC   RUCBNAME,13(R3)     COPY EBCDIC UCB NAME
         LA    R1,RUCBNAME         GET START OF UCB NAME
         LA    R15,3               GET COUNT OF BYTES TO PROCESS
RUCBLOOP DS    0H                  CONVERT EBCDIC TO HEX - STEP 1
         TM    0(R1),X'F0'         IS THIS BYTE NUMERIC
         BO    RUCBNEXT            YES - GO TO NEXT BYTE
         IC    R14,0(,R1)          NO - GET BYTE IN R14
         LA    R14,9(,R14)         ADD NINE (X'C1' -> X'CA')
         STC   R14,0(,R1)          AND UPDATE UCB NAME
RUCBNEXT DS    0H                  CHECK NEXT BYTE
         LA    R1,1(,R1)           INCREMENT TO NEXT BYTE
         BCT   R15,RUCBLOOP        AND LOOP UNTIL END
         PACK  WORKUCB(3),RUCBNAME(4) PACK - STEP 2
         CLC   WORKUCB(2),LASTUCB IS THIS THE REAL UCB
         BH    RUCBGOOD            YES - SAVE UCB NUMBER AND EXIT
         LA    R15,4               NO - SET RETURN CODE
RUCBEXIT DS    0H                  EXIT
         L     R14,RUCBSAVE        RESTORE RETURN ADDRESS
         BR    R14                 RETURN TO CALLER
         SPACE 3
RUCBGOOD DS    0H                  THIS UCB IS GREATER THAN LAST UCB
         MVC   LASTUCB,WORKUCB     SAVE LAST UCB
         SLR   R15,R15             SET RETURN CODE
         B     RUCBEXIT            AND EXIT
         SPACE 3
DEVMSG   TPUT  DMSG,L'DMSG
         B     CHKPDE
         EJECT
*
UCBFLAG  DS    0H
         STM   R2,R3,CHAINSAV       SAVE REGISTERS SO NEXT CODE
         STM   R14,R15,CHAINSAV+8   DOESNT DESTROY THEM
         STM   R4,R5,BUFRSAV
         LA    R4,FLGMSG            POINT TO MSG AREA
         LA    R5,ENDMSG            POINT TO END OF AREA
         TPUT  HDR,L'HDR
*
         L     R2,STARTUCB
NXTUCB   LA    R3,TABLE
FLAGLOOP CLI   0(R3),X'FF'          END OF TABLE
         BE    DONEWFLG             YES - ALL DONE
         LR    R15,R2               GET COPY OF UCB ADDRESS
         SLR   R14,R14              ZERO R14 FOR INSERT
         IC    R14,0(R3)            GET OFFSET OF FLAG IN UCB
         AR    R15,R14              GET ADDRESS OF FLAG
         IC    R14,1(R3)            GET FLAG
         EX    R14,TEST4FLG         TEST FOR CORRECT FLAG
         BZ    NEXTFLAG             NOT ON - LOOP
         BAL   R14,MSGBUF
*                                                                     *
NEXTFLAG DS    0H
         LA    R3,10(R3)             BUMP FLAG POINTER
         B     FLAGLOOP
*                                                                     *
DONEWFLG DS    0H
         TPUT  FLGMSG,L'FLGMSG
         MVI   FLGMSG,X'40'
         MVC   FLGMSG+1(64),FLGMSG
         LM    R4,R5,BUFRSAV
         LM    R2,R3,CHAINSAV
         LM    R14,R15,CHAINSAV+8
         B     CHKFLGS
*                                                                     *
MSGBUF   EQU   *
         MVC   5(8,R4),2(R3)         MOVE IN STATUS DESCRIPTOR
         LA    R4,10(R4)             BUMP AREA POINTER
         CR    R4,R5                 COMPARE FOR MORE ROOM
         BLR   R14                   LOW - RETURN
*        TPUT  FLGMSG,L'FLGMSG       NOT LOW - WRITE IT OUT
*        LA    R4,FLGMSG
         B     NEXTFLAG
*                                                                     *
***********************************************************************
* ERROR AND INFORMATIONAL MESSAGES                                    *
***********************************************************************
*                                                                     *
GNRLERR  DC    AL2(GNRLERRL),AL2(0)
         DC    C'AN ERROR WAS ENCOUNTERED IN THE GENERAL FAIL SERVICE RX
               OUTINE. THE RETURN CODE FROM GNRFAIL IS: '
GNRLERRL EQU   *-GNRLERR
*
SVC78ER  DC    AL2(SVC78ERL),AL2(0)
         DC    C'AN ERROR WAS ENCOUNTERED IN THE LSPACE SVC ROUTINE. THX
               E RETURN CODE IS: '
SVC78ERL EQU   *-SVC78ER
***********************************************************************
*        UCB FLAGS TABLE                                              *
***********************************************************************
TABLE    DS    0H
         DC    X'0104',CL8'NONALLOC'     JES2,ETC
         DC    X'0340',CL8'CHANGING'     CHANGING STATUS
         DC    X'0320',CL8'RESERVED'     MOUNT STATUS - RESERVED
         DC    X'0310',CL8'UNLOAD  '     UNLOAD PENDING
         DC    X'0308',CL8'ALLOCATD'     ALLOCATED
         DC    X'0304',CL8'RESIDENT'     PERM. RESIDENT
         DC    X'0302',CL8'SYSRES  '     SYSRES, MSTRCONS, CONS
         DC    X'0301',CL8'SECONS  '     LABELS VERIFD/SECONS
         DC    X'0680',CL8'BUSY    '     DEVICE BUSY
         DC    X'0640',CL8'NOT-RDY '     NOT READY
         DC    X'0620',CL8'POSTED  '     POST FLAG
         DC    X'0610',CL8'SENSE OP'     PENDING SENSE OP
         DC    X'0608',CL8'CU BUSY '     CONTROL UNIT BUSY
         DC    X'0604',CL8'S/A PROC'     STANDALONE PROCESS
         DC    X'0602',CL8'ACTIVE  '     CHANNEL PGN ACTIVE
         DC    X'0601',CL8'QUIESCED'     QUIESCED
         DC    X'0740',CL8'SNS ACTV'     SENSE ACTIVE
         DC    X'0720',CL8'SNS POST'     SENSE POST
         DC    X'0710',CL8'DEV-RESV'     DEVICE RESERVED
         DC    X'0708',CL8'CRH CONN'     CRH CONNECTION
         DC    X'0C10',CL8'REWIND  '     REWIND IN PROGRESS
         DC    X'1480',CL8'ATT PEND'     ATTENTION PENDING
         DC    X'1420',CL8'U DEV ND'     UNSOLICITED DEVICE END
         DC    X'1410',CL8'INTRCEPT'     INTERCEPT CONDITION
         DC    X'1408',CL8'INT MSG '     INTERVENTION MESSAGE ISSUED
         DC    X'1404',CL8'INT REQD'     INTERVENTION MSG REQUIRED
         DC    X'1402',CL8'- MTP - '     MOUNT PENDING/CE/DE
         DC    X'1401',CL8'DDR PEND'     DDR SWITCH PENDING
         DC    X'2210',CL8'PRIVATE '     USE STATUS - PRIVATE
         DC    X'2208',CL8'PUBLIC  '     USE STATUS - PUBLIC
         DC    X'2204',CL8'STORAGE '     USE STATUS - STORAGE
         DC    X'FFFF',CL8'        '     END OF TABLE
*
***********************************************************************
* GENERAL DECLARE AND EQUATE STATEMENTS                               *
***********************************************************************
*                                                                     *
PCLADDR  DC    A(PARMTAB)
         EJECT
*
********************** LIST MACRO INSTRUCTIONS ************************
*
IOPB     PUTLINE MF=L
         EJECT
*
***********************************************************************
*
RUCBSAVE DC    F'0'                REAL UCB CHECK ROUTINE SAVE AREA
WORKUCB  DC    2D'0'               WORK AREA
CHAINSAV DC    2D'0'               SAVE AREA FOR FOUR REGS
BUFRSAV  DS    D                   SAVE AREA FOR TWO REGS
RUCBNAME DC    CL3' '              EBCDIC UCB NAME
LASTUCB  DC    XL2'00'             LAST UCB
PARMNM   DC    CL8' '
ZEROS    DC    CL8'        '
CLEAR    DC    X'00000000'
LC       DC    CL1' '
DEVCODE  DS    CL4
UCBADDR  DS    F
STARTUCB DS    F
DEVSERL  DS    CL6
GFPARMP  DS    F
         DS    0D
DEC      DS    2F
RETNCD   DS    F
CONV     DS    2F
RC4      DC    F'4'
RC8      DC    F'8'
RC12     DC    X'0000000C'
VOLSPC   DC    CL30' '
VOLSPC1  DC    CL38' '
SPCMSG   DC    C'            CYL TRK  XTNT CYL-C TRK-C          '
OFFL     DC    C'     OFFLINE'
MCNT     DC    C'TOO MANY BITS IN MASK COUNT'
NONSUPP  DC    C'UNSUPPORTED DEVICE NAME SPECIFIED'
NOSTRG   DC    C'STORAGE UNAVAILABLE TO BUILD UCB LIST'
         DS    0F
ENDCHAIN DC    X'FF000000'
SAVEPDE  DS    F
SAVELGTH DS    F
SAVEUCB  DS    F
CNTSAVE  DS    F
BITSAVE  DS    F
PARSAVE  DS    F
REG3SAV  DS    F
EXAMED   DS    D
FLGSW    DC    X'00'
ESOCODE  DC    X'00'
REQFLG   DC    X'00'
MYFLG    DC    X'FF'
VOLFLG   DC    X'04'
UCBFLG   DC    X'08'
DEVFLG   DC    X'88'
PUBFLG   DC    X'11'
PRIVFLG  DC    X'22'
STORFLG  DC    X'33'
HALF12   DC    H'12'
HALF0    DC    H'0'
STOR     DC    CL240' '
STOR1    DC    CL6' '
*
RC04     DC    C'            PERMANENT I/O ERROR'
RC08     DC    C'VOLUME NOT MOUNTED  '
RC0C     DC    C'UCB NOT READY OR INVALID UCB SPECIFIED '
DMSG     DC    C'NOT A DIRECT ACCESS DEVICE'
PUBLC    DC    C'NO PUBLIC DEVICES MOUNTED'
UNKNWN   DC    C'ERROR..UNKNOWN OR INVALID DEVICE SPECIFIED'
FLGMSG   DC    CL65' '
ENDMSG   DC    C'END OF MSG'
HDR      DC    C'     STATUS FLAGS ARE:'
*
DFID     DS    CL2
RETCODE  DS    F
         EJECT
         TITLE 'LTORG'
         LTORG ,
         TITLE 'SAVE AREA AND CONSTANTS'
SAVE1    DS    9D                  SAVE AREA
PCFTBLS  DC    CL8'PCFTBLS ' MUST BE DOUBLE WORD ALIGNED
VOLSER   DC    CL6' '
KNOTTSO  DC    C'NOTTSO'
         TITLE 'PATCH AREA'
PATCH    DC    50S(*)              GENERATE SCON'S FOR PATCH AREA
         TITLE 'REGISTER EQUATES'
R0       EQU   0
R1       EQU   1                   PARAMETER & WORK REGISTER
R2       EQU   2                   WORK REGISTER
R3       EQU   3                   ADDRESS OF CVT REQUIRED BY IEAVVMSR
R4       EQU   4                   ADDRESS OF CALLER'S PARM
R5       EQU   5
R6       EQU   6                   DESTROYED BY IEAVVMSR
R7       EQU   7
R8       EQU   8                   DESTROYED BY IEAVVMSR
R9       EQU   9                   DESTROYED BY IEAVVMSR
R10      EQU   10
R11      EQU   11
R12      EQU   12                  BASE - EP + 0
R13      EQU   13                  SAVE AREA ADDRESS
R14      EQU   14                  OS LINKAGE REGISTER
R15      EQU   15                  OS LINKAGE REGISTER
         SPACE 5
         PRINT NOGEN
         SPACE 5
         TITLE 'CVT DSECT'
         CVT   DSECT=YES
         SPACE 5
LSPACE   CSECT
         TITLE 'PSA DSECT'
         IHAPSA
         SPACE 5
LSPACE   CSECT
         TITLE 'DCQ DSECT'
         IHADCQ
         SPACE 5
LSPACE   CSECT
         TITLE 'UCB DSECT'
         IEFUCBOB
         SPACE 5
LSPACE   CSECT
         TITLE 'ASCB DSECT'
         IHAASCB
         SPACE 5
LSPACE   CSECT
         TITLE 'LPDE DSECT'
         IHALPDE
         SPACE 5
LSPACE   CSECT
         TITLE 'JSCB DSECT'
         IEZJSCB
         SPACE 5
LSPACE   CSECT
         TITLE 'PSCB DSECT'
         IKJPSCB
         SPACE 5
LSPACE   CSECT
         TITLE 'TCB DSECT'
         IKJTCB
         SPACE 5
LSPACE   CSECT
***********************************************************************
* DSECTS FOR CONTROL BLOCKS                                           *
***********************************************************************
*                                                                     *
PARMTAB  IKJPARM DSECT=IKJPARMD
VOLUMES  IKJIDENT  VOLUME,LIST,FIRST=ALPHANUM,OTHER=ALPHANUM,          X
               DEFAULT='MYVOLS',CHAR
KWORD    IKJKEYWD
         IKJNAME   'FLAGS'
         IKJENDP
*****************************CONTROL BLOCKS****************************
*
         IKJCPPL
CPPLLEN  EQU   *-CPPL
         SPACE 5
         IKJPPL
PPLLEN   EQU   *-PPL
         SPACE 5
         IKJIOPL
IOPLLEN  EQU   *-IOPL
         SPACE 5
*                                                                     *
***********************************************************************
* DSECT FOR GETMAINED WORKAREA                                        *
***********************************************************************
*                                                                     *
WORKAREA DSECT
SAVEAREA DS    18F
ECB      DS    F
*                                                                     *
PPLSECT  DS    0F
         ORG   PPLSECT+PPLLEN
*
IOPLSECT DS    0F
         ORG   IOPLSECT+IOPLLEN
         EJECT
*
PARSBACK DS    F
*
         IKJEFFGF
         DS    0H
VOLMSG   DC    AL2(VOLMSGL),AL2(0)
         DC    CL72' '
VOLMSGL  EQU   *-VOLMSG
LDYNAMIC EQU   *-WORKAREA
         END
