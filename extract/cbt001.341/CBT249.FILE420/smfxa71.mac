//STEP#1 EXEC SAS,SO=O,LOG=O
//SYSIN  DD *
 /*------------------------------------------------------------------+
  × THIS ROUTINE ANALYZES SMF/XA TYPE 71 RECORDS.                    ×
  × NAMING CONVENTIONS GENERALLY FOLLOW THE SMF RECORD LAYOUTS,      ×
  × EXCEPT FOR TRIPLETS, COMMON SECTIONS, AND COMPUTED VARIABLES.    ×
  +------------------------------------------------------------------*/
  DATA DMNDPG
      (KEEP = SYSID DATE SHIFT SMFTIME INTVL DURATION SAMPLES SAMPCYCL
       SMF71PIN SMF71POT SMF71SSQ SMF71SIN SMF71SOT SMF71VIN SMF71VOT
       SMF71SNI SMF71SNO SMF71TIS SMF71TOS SMF71LGS SMF71LFS);

     LINK SMFCOM;

     IF ID=71 THEN GOTO TYPE_71;
              ELSE DELETE;

 /*-------------------------------------------------------------------+
  ×  TYPE_71 (RMF PAGING ACTIVITY) HEADER...                          ×
  +-------------------------------------------------------------------*/
  TYPE_71:
     INPUT
        @18+OFFSET SUBSYSID $4.   /*'RMF '                            */
        @24+OFFSET NTRIPLTS PIB2. /* # OFF/LEN/NUM                    */
        @28+OFFSET OFFRMFP  PIB4. /* OFFSET TO RMF PRODUCT SECTION    */
        @32+OFFSET LENRMFP  PIB2. /* LENGTH    RMF PRODUCT SECTION    */
        @34+OFFSET NUMRMFP  PIB2. /* NUMBER    RMF PRODUCT SECTIONS   */
        @36+OFFSET OFFPAGD  PIB4. /* OFFSET TO PAGING DATA SECTION    */
        @40+OFFSET LENPAGD  PIB2. /* LENGTH    PAGING DATA SECTION    */
        @42+OFFSET NUMPAGD  PIB2. /* NUMBER    PAGING DATA SECTIONS   */
        @;

        OFFRMFP+OFFSET;
        OFFPAGD+OFFSET;

        LINK RMFCOM;

 /*------------------------------------------------------------------+
  × TYPE_71 PAGING DATA SECTION...                                   ×
  +------------------------------------------------------------------*/
        INPUT
           @OFFPAGD     SMF71PIN PIB4. /* NONVIO, NON-SWAP PAGE-INS  */
           @OFFPAGD+004 SMF71POT PIB4. /* NONVIO, NON-SWAP PAGE-OUTS */
           @OFFPAGD+012 SMF71SSQ PIB4. /* ADDR SPACE SWAP SEQUENCES  */
           @OFFPAGD+016 SMF71SIN PIB4. /* NUMBER PAGES SWAPPED-IN    */
           @OFFPAGD+020 SMF71SOT PIB4. /* NUMBER PAGES SWAPPED-OUT   */
           @OFFPAGD+024 SMF71VIN PIB4. /* NUMBER VIO PAGE-INS        */
           @OFFPAGD+028 SMF71VOT PIB4. /* NUMBER VIO PAGE-OUTS       */
           @OFFPAGD+036 SMF71SNI PIB4. /* NUMBER LPA/CSA PAGEINS     */
           @OFFPAGD+040 SMF71SNO PIB4. /* NUMBER LPA/CSA PAGEOUTS    */
           @OFFPAGD+240 SMF71TIS PIB4. /* #LOGICAL TI CANDIDATES     */
           @OFFPAGD+276 SMF71TOS PIB4. /* #LOGICAL TO CANDIDATES     */
           @OFFPAGD+284 SMF71LGS PIB4. /* #LOGICAL SWAPS             */
           @OFFPAGD+288 SMF71LFS PIB4. /* #LOG, HAD TO BE PHYS SWAP  */
           @;

     OUTPUT DMNDPG;

     RETURN; /*------------------------------------------------------*/

  %INCLUDE PDS(SMFXASMF);

  %INCLUDE PDS(SMFXARMF);

 PROC PRINT;

//SMF DD DISP=SHR,DSN=TEC.BCHSMF.DATA(+0)
//PDS DD DISP=SHR,DSN=THIS LIBRARY
