//STEP#1 EXEC SAS,SO=O,LOG=O
//SYSIN  DD *
 /*------------------------------------------------------------------+
  × THIS ROUTINE ANALYZES SMF/XA TYPE 73 RECORDS.                    ×
  × NAMING CONVENTIONS GENERALLY FOLLOW THE SMF RECORD LAYOUTS,      ×
  × EXCEPT FOR TRIPLETS, COMMON SECTIONS, AND COMPUTED VARIABLES.    ×
  +------------------------------------------------------------------*/
  DATA CHAND
      (KEEP = SYSID DATE SHIFT SMFTIME INTVL DURATION SAMPLES SAMPCYCL
       SMF73SMP SMF73PID SMF73FG2 SMF73BSY);

  LINK SMFCOM;

     IF ID=73 THEN GOTO TYPE_73;
              ELSE DELETE;

 /*-------------------------------------------------------------------+
  ×  TYPE_73 (RMF CHANNEL PATH ACTIVITY HEADER...)                    ×
  +-------------------------------------------------------------------*/
  TYPE_73:
     INPUT
        @18+OFFSET SUBSYSID $4.   /*'RMF '                            */
        @24+OFFSET NTRIPLTS PIB2. /* # OFF/LEN/NUM                    */
        @28+OFFSET OFFRMFP  PIB4. /* OFFSET TO RMF PRODUCT SECTION    */
        @32+OFFSET LENRMFP  PIB2. /* LENGTH    RMF PRODUCT SECTION    */
        @34+OFFSET NUMRMFP  PIB2. /* NUMBER    RMF PRODUCT SECTIONS   */
        @36+OFFSET OFFCHPC  PIB4. /* OFFSET TO CHANPATH CTL SECTION   */
        @40+OFFSET LENCHPC  PIB2. /* LENGTH    CHANPATH CTL SECTION   */
        @42+OFFSET NUMCHPC  PIB2. /* NUMBER    CHANPATH CTL SECTIONS  */
        @44+OFFSET OFFCHPD  PIB4. /* OFFSET TO CHANPATH DAT SECTION   */
        @48+OFFSET LENCHPD  PIB2. /* LENGTH    CHANPATH DAT SECTION   */
        @50+OFFSET NUMCHPD  PIB2. /* NUMBER    CHANPATH DAT SECTIONS  */
        @;

        OFFRMFP+OFFSET;
        OFFCHPC+OFFSET;
        OFFCHPD+OFFSET;

  LINK RMFCOM;

 /*------------------------------------------------------------------+
  × TYPE_73 CHANNEL PATH CONTROL SECTION (ONE PER RECORD)...         ×
  +------------------------------------------------------------------*/
        INPUT
           @OFFCHPC    SMF73SMP PIB4.  /* #TIMES SRM ISSUED STCPS    */
           @;

 /*------------------------------------------------------------------+
  × TYPE_73 CHANNEL PATH DATA SECTION (ONE PER CHANNEL PATH)...      ×
  +------------------------------------------------------------------*/
     DO WHILE(NUMCHPD>0);
        INPUT
           @OFFCHPD    SMF73PID PIB1.  /* CHANNEL PATH ID            */
           @OFFCHPD+01 SMF73FG2 PIB1.  /* CHANNEL FLAGS              */
           @OFFCHPD+04 SMF73BSY PIB4.  /* #SAMPLES CHANNEL PATH BUSY */
           @;

        *IF SMF73FG2='.....001'B THEN OUTPUT;
        OUTPUT CHAND;

        NUMCHPD=NUMCHPD-1;
        OFFCHPD+LENCHPD;
     END;

 RETURN; /*----------------------------------------------------------*/

 %INCLUDE PDS(SMFXASMF);

 %INCLUDE PDS(SMFXARMF);

 PROC PRINT;

//SMF DD DISP=SHR,DSN=TEC.BCHSMF.DATA(+0)
//PDS DD DISP=SHR,DSN=THIS LIBRARY
